* /* START OF SPECIFICATIONS****                             @Y30LB35
*01*  PROCESSOR = ASSEM
**** END OF SPECIFICATIONS **                                @Y30LB35*/
         LCLC  &LIB
&LIB     SETC  'LIB2'
         LCLC  &SS1                                            @Y30LB35
&SS1     SETC  'SS1'                                           @Y30LB35
         TITLE 'IGC0408B --- SVC 82 FOR IDCAMS UTILITIES'      @Y30LB35
         PRINT ON,DATA,GEN                                     @Y30LB35
IGC0408B CSECT                                                 @Y30LB35
*STATUS- CHANGE LEVEL 000                                      @Y30LB35
*                                                              @Y30LB35
*FUNCTION/OPERATION-  THIS LOAD OF SVC 82 PERFORMS OPERATIONS  @Y30LB35
*   FOR THE IDCAMS UTILITES.  THIS LOAD IS A COMBINATION OF    @Y30LB35
*   IGC0008B AND IGC0208B.  THE FUNCTIONS ARE AS FOLLOWS:      @Y30LB35
*                                                              @Y30LB35
*         1) PERFORM PSEUDO OPEN BY BUILDING DEB IN PROTECTED, @Y30LB35
*            PLACING THE DEB IN SYSTEM DEB TABLE, AND CHAINING @Y30LB35
*            IT TO TCB DEB CHAIN.                              @Y30LB35
*                                                              @Y30LB35
*         2) PERFORM PSEUDO CLOSE BY REVERSING STEPS ABOVE IN  @Y30LB35
*            PSEUDO OPEN                                       @Y30LB35
*                                                              @Y30LB35
*         3) CLEAR UCB VOLUME SERIAL NUMBER AND VTOC TTR       @Y30LB35
*                                                              @Y30LB35
*         4) POST UCB WITH VOLUME SERIAL NUMBER AND VTOC TTR   @Y30LB35
*                                                              @Y30LB35
*ENTRY POINTS- THE ONLY ENTRY POINT IS -IGC0408B-.             @Y30LB35
*                                                              @Y30LB35
*INPUT-  THIS MODULE IS XCTL TO FROM IGC0008B.  A PARAMETER    @Y30LB35
*   LIST POINTED TO BY REGISTER 1 CONTAINS THREE WORDS.        @Y30LB35
*                                                              @Y30LB35
*      WORD1 PTR TO COPY OF CALLER'S PARM LIST IN SUBPOOL 229  @Y30LB35
*      WORD2 PTR TO SVRB OF THIS SVC                           @Y30LB35
*      WORD3 PTR TO INSTRUCTION AFTER SVC 82 IN CALLER'S PGM   @Y30LB35
*                                                              @Y30LB35
*   CALLER'S PARM LIST FOR FUNCTIONS                           @Y30LB35
*                                                              @Y30LB35
*     1) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=20.           @Y30LB35
*        -2ND WORD-   PTR TO DCB                               @Y30LB35
*                                                              @Y30LB35
*     2) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=21.           @Y30LB35
*        -2ND WORD-   RESERVED.                                @Y30LB35
*        -3RD WORD-   RESERVED.                                @Y30LB35
*        -4TH WORD-   ADDRESS OF OPEN DEB                      @Y30LB35
*                                                              @Y30LB35
*     3) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=22.           @Y30LB35
*        -2ND WORD-   RESERVED.                                @Y30LB35
*        -3RD WORD-   RESERVED.                                @Y30LB35
*        -4TH WORD-   PTR AREA TO RTN OLD LABEL AND VTOC TTR0  @Y30LB35
*                                                              @Y30LB35
*     4) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=23.           @Y30LB35
*        -2ND WORD-   PTR TO 6-BYTE SERIAL NO, IF OVERWRITTEN. @Y30LB35
*        -3RD WORD-   PTR TO TTR0 FOR VTOC.                    @Y30LB35
*                                                              @Y30LB35
*EXITS-NORMAL- VIA A SVC 3 TO THE CALLING ROUTINE.             @Y30LB35
*                                                              @Y30LB35
*EXITS-ERROR-   RETURN TO CALLER VIA REG 14. RETURN CODE=4.    @Y30LB35
*                                                              @Y30LB35
*SUPERVISOR MACROS-  GETMAIN,FREEMAIN,DEBCHK,MODESET           @Y30LB35
*                                                              @Y30LB35
*OUTPUT-  IF PSEUDO OPEN WAS REQUESTED THEN A DEB WILL BE      @Y30LB35
*         BUILT IN PROTECTED CORE, CHAINED TO TCB, AND INSERTED@Y30LB35
*         INTO SYSTEM DEB TABLE.                               @Y30LB35
*                                                              @Y30LB35
*         IF PSEUDO CLOSE WAS REQUESTED THEN THE DEB WILL BE   @Y30LB35
*         REMOVED AND FREED.                                   @Y30LB35
*                                                              @Y30LB35
*         IF POST UCB WAS REQUESTED THEN THE UCB WILL BE       @Y30LB35
*         POSTED WITH THE VOLUME SERIAL NUMBER, VTOC TTR0 AND  @Y30LB35
*         SET NOTREADY BIT OFF.                                @Y30LB35
*                                                              @Y30LB35
*         IF CLEAR UCB WAS REQUESTED THEN THE UCB WILL BE      @Y30LB35
*         CLEARED OF THE VOLUME SERIAL NUMBER, VTOC TTR0 AND   @Y30LB35
*         SET NOTREADY BIT ON.                                 @Y30LB35
*                                                              @Y30LB35
*ATTRIBUTES-  REENTRANT,RELOCATABLE,PRIVILEGED,ENABLED.        @Y30LB35
*                                                              @Y30LB35
***********************************************************************
         AIF   ('&SS1' EQ 'SS1').SS001  OMIT DASDR CODE        @Y30LB35
*098765,098432                                                VS06562
*                                                               YM04695
*                                                               VS02889
*                                                               YM03870
*                                                               YM03804
*STATUS CHANGE LEVEL 004    VS2/2 (MVM)                         YL02912
*                                                                     *
*FUNCTION/OPERATION-  SVC 82 PERFORMS THE FOLLOWING FUNCTIONS FOR     *
*   THE -IEHDASDR- UTILITY PROGRAM//TYPE 4 SVC WITH EXTENDED SVRB.    *
*                                                                     *
*     1) IN PREPARATION FOR SURFACE ANALYSIS FOR OFF-LINE VOLUMES.    *
*        -ASK FOR OPERATORS PERMISSION TO INITIALIZE A SPECIFIC VOL.  *
*        -IF OPERATOR REPLIES WITH A 'T', RETURN TO CALLER.RC=8.      *
*        -IF OPERATOR REPLIES WITH A 'U', BUILDS A DEB, LOADS IN      *
*         THE ABNORMAL-END APPENDAGE(IGG019P9), AND RETURNS TO        *
*         CALLER.RC=0.                                                *
*                                                                     *
*     2) ALTERNATE TRACK ASSIGNMENT REQUEST FOR GETALT FUNCTION.      *
*        -GETS CORE FOR A WORKAREA, BUILDS A DEB WITHIN THIS CORE,    *
*         AND PASSES CONTROL VIA A XCTL TO -IGC0108B- FOR THE         *
*         ACTUAL ALTERNATE TRACK ASSIGNMENT.                          *
*                                                                     *
*     3) ALTERNATE TRACK REQUEST BY FORMAT/ANALYZE FUNCTION.          *
*        -SAME AS OPERATION 2 ABOVE.                                  *
*                                                                     *
*     4) POST UCB REQUEST.                                            *
*        -PASSES CONTROL VIA A XCTL TO -IGC0208B-.                    *
*                                                                     *
*                                                                     *
*     5) SPECIAL POST UCB REQUEST.                                    *
*        -DELETES THE ABNORMAL-END APPENDAGE.                         *
*        -FREES THE CORE FOR THE DEB.                                 *
*        -PASSES CONTROL TO -IGC0208B-.                               *
*                                                                     *
         AIF   ('&LIB' EQ 'LIB1').X227900                        YM3475
*     6) DELETE DEB REQUEST.                                          *
*        -DELETES THE ABNORMAL-END APPENDAGE.                         *
*        -FREES THE CORE FOR THE DEB.                                 *
*                                                                     *
*     7) ISSUE SENSE FOR WINCHESTER DEVICES.                    XL03130
*        -ISSUE SENSE AND RETURN WINCHESTER MODEL NUMBER IN     XL03130
*         USERS' PARAMETER LIST + 4 :                           XL03130
*              MODEL 1 = F'01'                                  XL03130
*              MODEL 2 = F'02'                                  XL03130
*                                                                     *
*     8) ONLINE ENQ REQUEST.                                          *
*        -ENQ'S ON VOLID OF 'TODD' VOLUME.                      YL02912
*                                                                     *
.X227900 ANOP                                                    YM3475
*ENTRY POINT-  THE ONLY ENTRY POINT IS -IGC0008B-.                    *
*                                                                     *
*INPUT-  A PARAMETER LIST POINTED TO BY REGISTER 1 FOR THE ABOVE      *
*   DESCRIBED FUNCTIONS.                                              *
*                                                                     *
*     1) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=8F.                  *
*        -2ND WORD-   PTR TO DCB,HIGH ORDER BYTE=80.                  *
*                                                                     *
*     2) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=1F.                  *
*        -2ND WORD-   CCHH OF DEFECTIVE TRACK.                        *
*                                                                     *
*     3) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=00.                  *
*        -2ND WORD-   CCHH OF DEFECTIVE TRACK.                        *
*        -3RD WORD-   PTR TO 6-BYTES OF ALT. TRACK INFO.              *
*                                                                     *
*     4) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=08.                  *
*        -2ND WORD-   PTR TO 6-BYTE SERIAL NO, IF OVERWRITTEN.        *
*        -3RD WORD-   PTR TO MBBCCHHR FOR NEW VTOC.                   *
*                                                                     *
*     5) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=88.                  *
*        -2ND WORD-   PTR TO 6-BYTE SERIAL NO, IF OVERWRITTEN.        *
*        -3RD WORD-   PTR TO MBBCCHHR FOR NEW VTOC.                   *
*        -4TH WORD-   PTR TO DEB.                                     *
*                                                                     *
         AIF   ('&LIB' EQ 'LIB1').X227901                        YM3475
*     6) -1ST WORD-   RESERVED, HIGH ORDER BYTE=F8.                   *
*        -2ND WORD-   RESERVED.                                       *
*        -3RD WORD-   RESERVED.                                       *
*        -4TH WORD-   PTR TO DEB.                                     *
*                                                                     *
*     7) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=F1.            XL03130
*        -2ND WORD-   HIGH ORDER BYTE=80.                       XL03130
*                                                                     *
*     8) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=04.            YL02912
*                                                                     *
*     9) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=44.            YL02912
*                                                                     *
.X227901 ANOP                                                    YM3475
*EXITS-NORMAL-  RETURN TO CALLER VIA REG 14. RETURN CODE=0.           *
*                                                                     *
*EXITS-ERROR-   RETURN TO CALLER VIA REG 14. RETURN CODE=8.           *
*                                                                     *
*EXTERNAL ROUTINES- SVC IS ISSUED FOR THE FOLLOWING FUNCTIONS FROM    *
*                                                                     *
*   THE FOLLOWING MODULES.                                            *
*                                                                     *
*     1) IHEDANAL.                                                    *
*                                                                     *
*     2) IEHDGETA.                                                    *
*                                                                     *
*     3) IEHDANAL,IEHDCELL.                                           *
*                                                                     *
*     4) IEHDLABL,IEHDANAL,IEHDREST,IEHDDUMP                          *
*                                                                     *
*     5) IEHDANAL                                                     *
*                                                                     *
         AIF   ('&LIB' EQ 'LIB1').X227902                        YM3475
*     6) IEHDANAL,IEHDLABL.                                           *
*                                                                     *
*     7) IEHDASDS                                               XL03130
*                                                                     *
*     8) IEHDASDS, FOR ENQING ON THE TODD UCBS' VOLID.          YL02912
*                                                                     *
.X227902 ANOP                                                    YM3475
*SUPERVISOR MACROS-  GETMAIN,FREEMAIN,WTOR,WTO,LOAD,XCTL.             *
*                                                                     *
*TABLES/WORKAREAS-  OBTAINS A WORKAREA FOR -IGC0108B- WHICH IS        *
*   DESCRIBED BY THE DSECT -IOBLOCKS-.                                *
*                                                                     *
*OUTPUT-                                                              *
*     1) FOR FUNCTION NO. 1,BUILDS A PROPER DEB AND LOADS IN THE      *
*        ABNORMAL-END APPENDAGE.                                      *
*                                                                     *
*     -FOR ALL OTHER FUNCTIONS, SEE  -IGC0108B- OR -IBC0208B.         *
*                                                                     *
*ATTRIBUTES-  REENTRANT,RELOCATABLE,PRIVILEGED,ENABLED.               *
*                                                                     *
***********************************************************************
.SS001   ANOP
         EJECT
*   THE FOLLOWING ARE REGISTER ASSIGNMENTS.
PARMPTR  EQU   1                       POINTER TO INPUT PARAMETERS.
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5                                               @Y30LB35
R6       EQU   6
R7       EQU   7                                                 Y01021
R8       EQU   8                       TCB ADDRESS.
R9       EQU   9
R11      EQU   11                      POINTER TO INPUT PARAMETERS.
R14      EQU   14                      LINK REGISTER.
R15      EQU   15
GR0      EQU   0
GR1      EQU   1
GR2      EQU   2                       INDEX TO UCB TABLE ENTRIES.
GR3      EQU   3                       POINTER TO TABLE UCB.
GR4      EQU   4                       POINTER TO NEW SERIAL.
GR5      EQU   5                       POINTER TO TABLE UCB  SERIAL.
GR6      EQU   6                       POINTER TO INPUT UCB.
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10
GR11     EQU   11
GR12     EQU   12
GR14     EQU   14                      LINK REGISTER.
BASEREG  EQU   12                      BASE REGISTER.
GR15     EQU   15
         SPACE
DYNALT   EQU   X'1F'                   GETALT ASSIGNMENT.
SENS     EQU   X'F1'                    SENSE REQUEST FOR WINCH XL03130
*
NEWVOL   EQU   X'8F'                   NEW VOLUME INPUT.
COMMA    EQU   C','                    EBCDIC COMMA.
SLASH    EQU   C'/'                    2321 BIN SEPARATOR.
WSIZE    EQU   52                      CONSOLE MESSAGE SIZE.
WSIZE1   EQU   WSIZE-12                MESSAGE LENGTH.         @Y30LB35
DA2321   EQU   X'05'                   UCB DEVICE TYPE CODE FOR 2321.
LASTBIN  EQU   X'09'                   LAST BIN NO. FOR SUB-UCBS.
SUBLNG   EQU   16                      LENGTH OF EACH SUB-UCB.
MAINUCB  EQU   X'FF'                   PRIMARY UCB IDENTIFIER.
NOTREADY EQU   X'40'                   UCB NOT READY BIT.
PGFIXBIT EQU   X'80'                   PAGE FIX APPENDAGE PRESENT
         SPACE
INIT     EQU   X'00'                   ANALYZE/FORMAT REQUEST.
POSTSPEC EQU   X'88'                   POST REQUEST FOR NEW VOLUMES.
POST     EQU   X'08'                   POST INDICATOR.
RETURN   EQU   3                       RETURN SVC CODE.
X06      EQU   X'06'                    HEX CONSTANT             S20201
X07      EQU   X'07'                    HEX CONSTANT             S20201
E0       EQU   0                        CONSTANT OF 0            Y01021
E3       EQU   3                        CONSTANT OF 3            Y01021
E4       EQU   4                        CONSTANT OF 4            Y01021
E5       EQU   5                        CONSTANT OF 5            Y01021
E8       EQU   8                        CONSTANT OF 8            Y01021
E12      EQU   12                       CONSTANT OF 12           Y01021
E44      EQU   44                       CONSTANT OF 44           Y01021
E45      EQU   45                       CONSTANT OF 45           Y01021
FOXES    EQU   X'FF'                    END OF P.L. FLAG FOR ENQ SJ0054
D0       EQU   0                        ZERO DISPLACEMENT.      YL02912
D1       EQU   1                        DISPL. OF ONE.          YL02912
D3       EQU   3                        DISPL. OF THREE         YM04695
HEX0     EQU   X'00'                    WILL ZERO ENQ R.C.      YM04695
L3       EQU   3                        LENGTH OF THREE.        YL02912
L6       EQU   6                        LENGTH OF SIX
K36      EQU   36                       CONSTANT OF 36.         YL02912
K24      EQU   24                       CONSTANT OF 24.        @Y30LB35
PTRCVT   EQU   16                      CVT PTR ADDR              Y01021
         AIF   ('&LIB' EQ 'LIB1').X227903                        YM3475
DELDEB   EQU   X'F8'                    DEB DELETE REQUEST.      YM3475
NALOCOFF EQU   X'FB'                    MASK TO RESET UCBNALOC  YL02912
EIGHT    EQU   8                        RETURN CODE FROM ENQ    YL02912
ENQ      EQU   X'04'                    ONLINE ENQ FUNC BYTE    YL02912
DEQ      EQU   X'44'                    ONLINE DEQ FUNC BYTE    YL02912
ONE      EQU   GR1                      CONSTANT                YL02912
OFFLINE  EQU   X'88'                    OFFLINE LABEL IND.      YL02912
RBEXSAV  EQU   96                      ADDRESSING OFFSET - RB  @Y30LB35
SS1OPEN  EQU   X'20'                   ENTRY CODE FOR OPEN     @Y30LB35
SS1CLOSE EQU   X'21'                   ENTRY CODE FOR CLOSE    @Y30LB35
SS1CLEAR EQU   X'22'                   ENTRY CODE CLEAR UCB    @Y30LB35
SS1POST  EQU   X'23'                   ENTRY CODE FOR POST UCB @Y30LB35
FOUR     EQU   4                       CODE INDICATE DUP VOL   @Y30LB35
.X227903 ANOP                                                    YM3475
         EJECT
*   THE FOLLOWING ARE REGISTER ASSIGNMENTS.                    @Y30LB35
         LR    BASEREG,GR15            SET UP ADDRESSING.      @Y30LB35
         USING IGC0408B,BASEREG                                @Y30LB35
         USING RBBASIC,GR5                                     @Y30LB35
         L     GR5,FOUR(GR1)           PICK RB EXTENT AREA     @Y30LB35
         LA    GR5,0(GR5)              CLEAR HIGH ORDER BYTE   @Y30LB35
         SPACE 2                                               @Y30LB35
         L     GR11,0(GR1)             PARM LIST POINTER.      @Y30LB35
         USING PARMLIST,GR11                                    YL02912
         USING TCB,R8                                           YL02912
         LR    GR9,GR5                 SVRB ADDRESS.           @Y30LB35
         USING CVT,GR2                                         @Y30LB35
         L     GR2,CVTPTR              ADDRESS OF CVT.         @Y30LB35
         L     GR8,CVTTCBP             PICK UP TCB PTR         @Y30LB35
         L     GR8,FOUR(GR8)           PICK UP THIS TCB ADDR   @Y30LB35
         L     GR2,CVTILK2             UCB ADDRESS TABLE.      @Y30LB35
         DROP  GR2                                             @Y30LB35
         AIF   ('&SS1' EQ 'SS1').SS01  OMIT DASDR CODE         @Y30LB35
         BALR  BASEREG,0               SET UP ADDRESSING.
         USING *,BASEREG
         SPACE
         LR    GR10,PARMPTR             GET PARAMETER POINTER.  YL02912
         SPACE
***********************************************************************
*                                                                     *
*  THE CALLERS PARAMETER LIST WILL BE PLACED INTO A FIVE WORD BLOCK   *
*   OF GOTTEN CORE OF THE SVC'S KEY. THE FIRST FOUR WORDS WILL BE A   *
*   COPY OF THE CALLER'S LIST AND THE LAST WORD WILL CONTAIN THE KEY  *
*   OF THE CALLER IN THE FIRST BYTE AND THE ADDRESS OF THE CALLER'S   *
*   LIST IN THE LAST THREE BYTES.                                     *
*                                                                     *
***********************************************************************
         LR    GR8,GR4                 LOAD TCB PTR             XL03130
         SPACE
         USING PARMLIST,R11                                     YL02912
         USING RBBASIC,GR5                                      YL02912
         MODESET EXTKEY=SUPR                                    YL02912
         ST    GR14,RBEXSAVE+K36        SAVE RETURN ADDRESS.    YL02912
         MODESET EXTKEY=DATAMGT                                 YL02912
         LA    R2,ENDLIST-PARMLIST      CORE FOR PARM LIST.     YL02912
         GETMAIN R,LV=(2),SP=229        FOR CALLER'S PARAMETER. YL02912
         LR    R11,R1                   SAVE NEW PARM POINTER.  YL02912
         ST    GR10,PRMPTR              SAVE OLD PARM POINTER.  YL02912
         LR    R8,R4                    GET ADDR OF CURRENT TCB.YL02912
         SPACE
         MODESET EXTKEY=RBT234,WORKREG=3  GET CALLER'S KEY.     YL02912
         LM    R1,R4,E0(GR10)           GET CALLER'S PARM LIST. YL02912
         MODESET EXTKEY=DATAMGT,SAVEKEY=KEY,WORKREG=15          YL02912
         STM   R1,R4,PARMLIST           CALLER'S LIST TO MINE.  YL02912
.SS01    ANOP                                                  @Y30LB35
***********************************************************************
*                                                                     *
*   DETERMINE THE FUNCTION TO BE PERFORMED.                           *
*                                                                     *
***********************************************************************
         AIF   ('&SS1' EQ 'SS1').SS02  OMIT DASDR CODE         @Y30LB35
         SPACE
         AIF   ('&LIB' EQ 'LIB1').NOWIN BRCH IF OS ASSEM        XL03130
.NOWIN   ANOP                                                   XL03130
         CLI   FUNCTION,SENS           IS THIS A SENSE REQUEST. XL03130
         BE    SENSIT                  YES, GO ISSUE SENSE CMD  XL03130
.SS02    ANOP                                                  @Y30LB35
         MODESET EXTKEY=DATAMGT                                @Y30LB35
         SPACE
         CLI   FUNCTION,SS1OPEN         NEW VOLUME REQUEST?    @Y30LB35
         BE    NEWPACK                  YES, GO BUILD DEB.     @Y30LB35
         SPACE
         CLI   FUNCTION,SS1CLOSE        DYNAMIC ALT TRK REQT   @Y30LB35
         BE    CLOSENEW                 YES, ASSIGN ALTERNATE. @Y30LB35
         SPACE
         CLI   FUNCTION,SS1CLEAR        ANALYZE/FORMAT REQUEST @Y30LB35
         BE    CLEARUCB                 YES, PROCESS TRACK.    @Y30LB35
         SPACE
         CLI   FUNCTION,SS1POST         POST UCB REQUEST?      @Y30LB35
         BE    POSTUCB                  YES, UPDATE UCBS.       YL02912
         AIF   ('&SS1' EQ 'SS1').SS03  OMIT DASDR CODE         @Y30LB35
         SPACE
         CLI   FUNCTION,POSTSPEC        OFFLINE POST REQUEST?   YL02912
         BE    SPECPOST                 YES, UPDATE UCB.        YL02912
         SPACE
         CLI   FUNCTION,DELDEB          DEB DELETE REQUEST?     YL02912
         BE    SPECPOST                 YES DELETE DEB.         YL02912
         SPACE
         CLI   FUNCTION,ENQ             ENQUEUE REQUEST?        YL02912
         BE    ENQUE                    YES, UPDATE UCB.        YL02912
.SS03    ANOP                                                  @Y30LB35
         SPACE
         B     EXIT                     UNSUPPORTED FUNCTION.   YL02912
         EJECT
         AIF   ('&SS1' EQ 'SS1').SS04  OMIT DASDR CODE         @Y30LB35
         AIF   ('&LIB' EQ 'LIB1').NOWIN1 BRCH IF OS ASSEM       XL03130
**********************************************************************
*        THE FOLLOWING ROUTINE WILL ISSUE A 'SENSE' CMD TO THE
*        WINCHESTER DEVICE BEING PROCESSED BY IEHDASDS. THE SENSE WILL
*        DETERMINE WHICH MODEL WINCHESTER IS BEING USED.
**********************************************************************
         SPACE
SENSIT   EQU   *                                                XL03130
         GETMAIN R,LV=88,SP=229         CORE FOR EXCP CTL BLKS  YL02130
         USING CTLBLKS,GR2                                      XL03130
         LR    GR2,GR1                  SAVE AREA ADDR          XL03130
         L     GR1,CVTPTR               GET CVT ADDR            XL03130
         USING CVT,GR1
         LA    GR4,BUMP                 DCBORG                  XL03130
         XC    DCBORG(L88),DCBORG       CLEAR CTL BLKS          XL03130
         SR    GR2,GR4                  RESET DSECT BASE        XL03130
         L     GR3,UCBPTR               GET UCB ADDR            XL03130
         USING UCB,GR3                                          XL03130
         MODESET EXTKEY=SUPR                                     YM5769
         NI    UCBFL1,MASK-UCBNOTRD    TURN OFF NOT-READY BIT   XL03130
         MODESET EXTKEY=DATAMGT                                  YM5769
         ST    GR3,DEBUCB               STORE UCB IN DEB        XL03130
         MVI   DEBUCB,INIT              ZERO FILE MASK          XL03130
         LA    GR3,ECB1                 GET ECB ADDR            XL03130
         ST    GR3,IOBECB               STORE IT IN IOB         XL03130
         MVC   CCW,SENSCCW              MOVE SKELTON CCW        XL03130
         LA    GR4,CCW                  GET CCW ADDR            XL03130
         ST    GR4,IOBCCW               STORE IN IOB            XL03130
         LA    GR4,SENSE                GET AREA ADDR           XL03130
         ST    GR4,CCW                  SETUP SENSE CCW         XL03130
         MVI   CCW,SENSEOP              RESET OP CODE           XL03130
         ST    GR2,DEBDCB               PUT DCB ADDR IN DEB     XL03130
         MVC   DEBAPP(L4),CVTXAPG       APPDGE VECTOR TABL ADDR XL03130
         ST    GR2,IOBDCB               DCB ADDR IN IOB         XL03130
         LA    GR4,DEBORG               LOAD DEB ADDR           XL03130
         ST    GR4,DCBDEB               STORE IT IN DCB         XL03130
         MVC   DEBDCB(L1),TCBPKF        MOVE TCB KEY TO DEB     XL03130
         OI    DEBDCB,DEBID             SET DEB ID              XL03130
         EXCP  IOB1                     ISSUE SENSE CMD         XL03130
         WAIT  ECB=ECB1                 WAIT FOR SENSE          XL03130
         CLI   ECB1,GOODRT              SUCCESSFUL SENSE ?      XL03130
         BNE   FREEIT                   NO, DONT SET MODEL NO.  XL03130
         TM    SENSE+L2,MOD1            36MEG WINCH. ?          XL03130
         BO    STORE1                   YES, GO STORE X'01'     XL03130
         TM    SENSE+L2,MOD2            72 MEG WINCH            XL03130
         BNO   FREEIT                   NO, EXIT                XL03130
         MVI   WINMOD,MOD2              MOVE MODEL NO TO PL     XL03130
         B     FREEIT                   EXIT                    XL03130
STORE1   EQU   *                                                XL03130
         MVI   WINMOD,MOD1              MOVE MODEL NO TO PL     XL03130
FREEIT   EQU   *                                                XL03130
         LA    GR2,BUMP(GR2)            RESET CORE PTR          XL03130
         FREEMAIN R,LV=88,A=(GR2),SP=229 FREE CTL BLK CORE      YL02130
         L     GR4,PRMPTR               CALLER'S LIST ADDR.     YL02912
         IC    GR9,WINMOD              GET WINCH MODEL NO.      VS02889
         MODESET KEYADDR=KEY,WORKREG=15 GET CALLER'S KEY.       YL02912
         STC   GR9,L7(GR4)             STORE MODEL NO. IN P.L.  VS02889
         MODESET EXTKEY=DATAMGT                                 YL02912
         CLI   WINMOD,INIT              WAS SENSE GOOD ?        XL03130
         BNE   EXIT                     YES, EXIT               XL03130
         BAL   R9,FREEPARM              GO FREE PARM LIST.
         LA    GR15,GR15                NO, SET NON-ZERO R.C.   XL03130
         B     EXIT2                    RETURN TO 'DASDS        XL03130
         DROP GR1
         EJECT
.NOWIN1  ANOP                                                   XL03130
.SS04    ANOP                                                  @Y30LB35
CLOSENEW DS    0H                                              @Y30LB35
         AIF   ('&SS1' EQ 'SS1').SS05  OMIT DASDR CODE         @Y30LB35
         DELETE EP=IGG019P9            FREE UP CORE USED BY APPENDAGE.
         AIF   ('&LIB' EQ 'LIB1').WM004
         DELETE EP=IGG019P7            FREE UP CORE USED BY      YM3011
*                                          APPENDAGE.
.WM004   ANOP
.SS05    ANOP                                                  @Y30LB35
         L     GR6,DEBPTR               ADDRESS OF DEB.         YL02912
         LA    GR0,DEB-MYDEB           OFFSET FORM WORKAREA START.
         SR    GR6,GR0                 START OF WORK AREA.      YL02912
         USING MYDEB,GR6                                        YL02912
         AIF   ('&LIB' EQ 'LIB1').WM008
         AIF   ('&SS1' EQ 'SS1').SS051 OMIT DASDR CODE         @Y30LB35
         L     GR4,DEBUCBAD-ONE         GET UCB ADDR            YL02912
         USING UCB,GR4                                          YL02912
         MODESET EXTKEY=SUPR                                    YL02912
         NI    UCBFL5,NALOCOFF          SET BIT OFF             YL02912
.SS051   ANOP                                                  @Y30LB35
         MODESET EXTKEY=DATAMGT                                 YL02912
         L     GR4,DEBDEBID             GET DCB ADDR             Y01021
         LA    GR4,E0(GR4)              CLEAR HIGH ORDER BYTE    Y01021
         DEBCHK (GR4),TYPE=DELETE,AM=EXCP                       YL02912
         EJECT
***********************************************************************
*                                                                     *
*    TAKE DEB OFF TCB DEB CHAIN.                                      *
*                                                                     *
***********************************************************************
         SPACE
         LA    GR9,DEBDEBAD             GET ADDRESS OF NEXT DEB.YL02912
         CLC   TCBDEB+D1(L3),DEBPTR+D1  TCB POINT TO MY DEB?    YL02912
         BNE   GETDEB                   NO,SEARCH DEB CHAIN.    YL02912
         MODESET EXTKEY=SUPR                                    YL02912
         MVC   TCBDEB+D1(L3),D0(GR9)    YES, UNCHAIN DEB.       YL02912
         MODESET EXTKEY=DATAMGT                                 YL02912
         B     FREEDEB                  GO FREE DEB AREA.       YL02912
         SPACE
GETDEB   EQU   *                                                YL02912
         L     GR4,TCBDEB               GET DEB CHAIN POINTER.  YL02912
         USING DEB,GR4                                          YL02912
CHKDEB   EQU   *                                                YL02912
         CLC   DEBDEBAD(L3),DEBPTR+D1   DEB POINT TO MY DEB?    YL02912
         BE    OURDEB                   YES, GO UNCHAIN.        YL02912
         L     GR4,DEBDEBAD-D1          NO, GET NEXT DEB.       YL02912
         B     CHKDEB                   CHECK NEXT DEB.         YL02912
         SPACE
OURDEB   EQU   *                                                YL02912
         MVC   DEBDEBAD(L3),D0(GR9)     UNCHAIN MY DEB.         YL02912
         B     FREEDEB                  GO FREE DEB AREA.       YL02912
         DROP  GR4                                              YL02912
         DROP  GR6                                             @Y30LB35
         EJECT
         AIF   ('&SS1' EQ 'SS1').SS06  OMIT DASDR CODE         @Y30LB35
***********************************************************************
*        THE FOLLOWING ROUTINE WILL CAUSE THE DASDI DEVICE TO BE ENQ'ED
*        UPON, VIA ITS' VOLID. THE INITIATORS' TCB WILL BE USED IN
*        THE EVENT IEHDASDR SHOULD ABEND BEFORE DEQ'ING.
***********************************************************************
         SPACE
ENQUE    EQU   *                                                YL02912
         L     GR6,UCBPTR               GET UCB ADDR YL02912
         USING UCB,GR6                                          YL02912
         LA    GR7,UCBVOLI              GET ADDR OF VOLID  YL02912
         L     GR9,TCBOTC               LOAD MOTHER TCB ADDR    YM03804
         MVC   ENQLIST(SYSQ-ENQLISTF),ENQLISTF MOVE LIST TO W/A YM03804
*
         ENQ   (,(GR7),E,6,SYSTEM),RET=USE,TCB=(GR9),           YM03804*
               MF=(E,ENQLIST)                                   YM03804
*
         LTR   GR15,GR15                HAVE WE GOT THE RESR ?  YL02912
         BZ    EXIT                     YES, BRCH               YL02912
         LA    GR15,D3(GR15)            INCRE TO R.C.           YL02912
         CLI   0(GR15),EIGHT            RC = 8 ?                YL02912
         BE    ENQAGN                   YES, RE-ENQ             YL02912
         BAL   R9,FREEPARM              FREE PARAMETER LIST.    YL02912
         LA    GR15,NOGO                SOMEONE ELSE HAS RESRC  YL02912
         B     EXIT2                    RETURN TO 'DASDS        YL02912
ENQAGN   EQU   *                                                YL02912
*
         MVI   ENQLIST+D3,HEX0          ZERO R.C.               YM04695
         ENQ   (,(GR7),E,6,SYSTEM),RET=CHNG,TCB=(GR9),          YM03804*
               MF=(E,ENQLIST)                                   YL02912
*
         LTR   GR15,GR15                HAVE WE GOT THE RESRC?  YL02912
         BZ    EXIT                     YES, RETURN TO 'DASDS   YL02912
         LA    GR15,D3(GR15)            INCRE TO R.C.           YL02912
         CLI   0(GR15),EIGHT            RC = 8 ?                YL02912
         BE    ENQUE                    ENQ WITH RET=USE        YL02912
         BAL   R9,FREEPARM              FREE PARAMETER LIST.    YL02912
         LA    GR15,NOGO                SOMEONE ELSE HAS RESRC  YL02912
         B     EXIT2                                            YL02912
.SS06    ANOP                                                  @Y30LB35
FREEDEB  EQU   *                        CONTINUE                 YM2646
         LR    GR1,GR6                  GET DEB ADDRESS.        YL02912
         USING MYDEB,GR1                                        YL02912
         L     GR4,DEBDEBID             GET DCB ADDRESS.         YM3475
         L     GR6,BLANKS               GET BLANK DDNAME.        YM3475
         MODESET KEYADDR=KEY,WORKREG=2  GET CALLER'S KEY.       YL02912
         ST    GR6,E44(GR4)             SET BLANKS IN DCB DDNAME.YM3475
         MODESET EXTKEY=DATAMGT                                 YL02912
.WM008   ANOP
         LA    R3,DEBEND-MYDEB          SIZE OF DEB WORKAREA.   YL02912
         DROP  GR1                                             @Y30LB35
         FREEMAIN R,LV=(3),A=(1),SP=230                          YM4604
         LR    GR1,GR11                RESTORE PARM POINTER.
         AIF   ('&LIB' EQ 'LIB1').X227102                        YM3475
         AIF   ('&SS1' EQ 'SS1').SS07  OMIT DASDR CODE         @Y30LB35
         CLI   FUNCTION,DELDEB          DEB DELETE REQUEST?     YL02912
         BNE   CKUCBS                   NO, GO POST UCBS.        YM3475
.SS07    ANOP                                                  @Y30LB35
EXIT     EQU   *                                                YL02912
         BAL   R9,FREEPARM              GO FREE PARM LIST.      YL02912
         SR    R15,R15                  SET RETURN CODE ZERO.   YL02912
         L     R14,RBEXSAVE+K24         GET RETURN ADDRESS.     YL02912
         MODESET EXTKEY=SUPR                                    SJ0054
         BR    R14                      RETURN.                 YL02912
.X227102 ANOP                                                    YM3475
         AIF   ('&SS1' EQ 'SS1').SS071 OMIT DASDR CODE         @Y30LB35
         SPACE
CKUCBS   EQU   *
         MODESET EXTKEY=SUPR                                    YL02912
         ST    GR11,RBEXSAVE+K16       SAVE PARM POINTER.       YL02912
         LR    GR1,GR5                 ADDRESS OF EXTENTED SAVE AREA.
         LA    GR4,XCTL1               ADDRESS OF XCTL LIST.
         B     NEXTLOAD                GO BRING IN NEXT LOAD.
.SS071   ANOP                                                  @Y30LB35
         SPACE 2
         USING IOBLOCKS,R2
FREEPARM EQU   *                                                YL02912
         LA    R3,ENDLST-PARMLIST       SIZE OF PARM LIST.     @Y30LB35
         LR    R1,R11                   ADDRESS OF PARM LIST.   YL02912
         FREEMAIN R,LV=(3),A=(1),SP=229 FREE THE WORK AREA.     YL02912
         BR    R9                      RETURN.
         EJECT
*************************************************************  @Y30LB35
*                                                              @Y30LB35
*  PREPARE TO INITIALIZE HERE.                                 @Y30LB35
*                                                              @Y30LB35
*************************************************************  @Y30LB35
NEWPACK  DS    0H                      BUILD A DEB             @Y30LB35
         AIF   ('&SS1' EQ 'SS1').SS08  OMIT DASDR CODE         @Y30LB35
         LA    R2,WTORP1-MSG           SIZE OF GETMAIN AREA.
         GETMAIN R,LV=(2),SP=229       CORE FOR MESSAGE AREA.   YL02912
         LR    R2,R1                   SAVE ADDRESS OF WORKAREA.
         USING MSG,R2
TRYAGAIN EQU   *
         XC    MSG(WTORP1-MSG),MSG     CLEAR AREA TO ZERO.
         MVC   MSG(WTOREND-WTORP),WTORP PLACE MESSAGE IN WORKAREA.
.SS08    ANOP                                                  @Y30LB35
         SPACE
         USING UCB,R6
         L     R6,UCBPTR                UCB ADDRESS.            YL02912
         AIF  ('&LIB' EQ 'LIB2').X301001                        XL03912
         CLI   UCBID,MAINUCB           THIS A 2321.
         BNE   W2321                   YES-GO PROCESS.
         SPACE
FIXMSG   EQU   *
.X301001 ANOP                                                   XL03912
         AIF   ('&SS1' EQ 'SS1').SS09  OMIT DASDR CODE         @Y30LB35
         MODESET EXTKEY=SUPR                                    YL02912
         NI    UCBFL2,X'FF'-X'40'  *** TURN-OFF NOT READY BUT.  *******
.SS09    ANOP                                                  @Y30LB35
         MODESET EXTKEY=DATAMGT                                 YL02912
         AIF   ('&SS1' EQ 'SS1').SS10  OMIT DASDR CODE         @Y30LB35
         MVC   MSG6(3),UCBNAME         EBCDIC NAME TO MESSAGE.
         LA    GR4,INITIAL                                         O122
         CLI   DCBPTR,OFFLINE          OFFLINE LABEL REQUEST?   YL02912
         BNE   FIXMSGA                 NO-ANALYZE-ALL SET.         O122
         LA    GR4,LABEL               YES-POINT TO LABEL WORD.    O122
FIXMSGA  MVC   MSG5+34(10),0(GR4)      FINISH SETTING UP MESSAGE.  O122
         SPACE
         LA    R4,REPLY                REPLY ADDRESS.
         LA    R3,WECB                 ECB ADDRESS FOR WTOR.
         LR    R1,R2                   INSURE ADDRESS IS SET FOR LIST.
         WTOR  ,(4),1,(3),MF=(E,(1))   ASK FOR PERMISSION TO INITIALIZE
*                                                                     *
         WAIT  ECB=WECB                AWAIT FOR REPLY.
         CLI   REPLY,C'U'              DOES REPLY INDICATE 'USE'.
         BE    USE                     YES-GO BUILD A DEB.
         CLI   REPLY,C'T'              DOES REPLY INDICATE TERMINATE.
         BE    NOUSE                   YES--EXIT NOW.
         MVC   MSG(WTOEND-WTO),WTO     MESSAGE TO BUFFER.
         LR    R1,R2                   ISURURE LIST ADDRESS IS SET.
         WTO   ,MF=(E,(1))             TELL OPERATOR ABUOT ERROR.
         B     TRYAGAIN                REPEAT THE REQUEST.
*                                                                     *
NOUSE    EQU   *                       DEB BUILDING TERMINATED.
         LA    R3,WTORP1-MSG           LENGTH OF WORK AREA.
         LR    R1,R2                   ADDRESS OF WORKAREA.
         BAL   GR9,FREECORE            GO FREE THE WORKAREA.
         BAL   R9,FREEPARM              GO FREE PARM LIST.      YL02912
         LA    R15,8                   SET RETURN CODE.
         B     EXIT2                    RETURN TO CALLER.       YL02912
         EJECT
OFFLENQ  EQU   *                                                YL02912
         LA    GR4,SYSQ                 ADDR OF QNAME HDR       YL02912
         LA    GR6,Q4                   ADDR OF RNAME ID        YL02912
         L     GR7,TCBOTC               GET MOTHER' TCB ADDR    YM03804
         MVC   ENQLIST(SYSQ-ENQLISTF),ENQLISTF MOVE LIST TO W/A YM03804
*
         ENQ   ((GR4),(GR6),E,2,SYSTEM),RET=USE,TCB=(GR7),             *
               MF=(E,ENQLIST)
*
         L     GR1,UCBPTR               DEQ'ED UCB ADDR         YL02912
         USING UCB,GR1
         TM    UCBSTAT,UCBONLI          THIS UCB STILL OFFLINE? YL02912
         BO    ERR                      NO, DEQ AND LEAVE.      YL02912
         MODESET EXTKEY=SUPR                                    YL02912
         OI    UCBFL5,UCBNALOC          SET FLAG FOR DEV ALLOC  YL02912
*                                       AND VARY PROCESSOR.     YL02912
         MODESET EXTKEY=DATAMGT                                 YL02912
DEQU     EQU   *                                                YL02912
         DEQ   ((GR4),(GR6),2,SYSTEM),RET=HAVE,TCB=(GR7),       YL02912*
               MF=(E,ENQLIST)
*
         BR    GR9                      EXIT                    YL02912
ERR      EQU   *                                                YL02912
         LA    GR9,ERREXIT              SVC RETURN CODE SET     YL02912
         B     DEQU                     DEQUEUE FROM SYSQ HDR   YL02912
.SS10    ANOP                                                  @Y30LB35
         B     USE                     GO BUILD A DEB.         @Y30LB35
         AIF   ('&SS1' EQ 'SS1').SS101 OMIT DASDR CODE         @Y30LB35
ERREXIT  EQU   *                                                YL02912
         BAL   R9,FREEPARM              GO FREE PARM LIST.      YL02912
         LA    GR15,NOGO                SETUP SVC RETURN CODE   YL02912
         B     EXIT2                    EXIT TO DASDS           YL02912
         EJECT                                                  YL02912
         AIF  ('&LIB' EQ 'LIB2').X301002                        XL03912
*         USING DATACELL,GR6
*W2321    MVC   MSG6+4(1),DCELBBNR+1    BIN NO. TO MESSAGE.
         OI    MSG6+4,X'F0'            SET ZONE BITS.
         MVI   MSG6+3,SLASH            BIN SEPARATOR.
         BAL   GR14,CONV2321           FIND MAIN UCB ADDRESS.
         B     FIXMSG                  GO COMPLETE THE MESSAGE.
.X301002 ANOP                                                   XL03912
.SS101   ANOP                                                  @Y30LB35
         EJECT                                                 @Y30LB35
***********************************************************************
*                                                                     *
*   BUILD A DEB FOR A NEW VOLUME HERE.                                *
*                                                                     *
***********************************************************************
USE      EQU   *                       OKAY TO BUILD A DEB.
         AIF   ('&SS1' EQ 'SS1').SS11  OMIT DASDR CODE         @Y30LB35
         LA    R3,WTORP1-MSG           LENGTH OF WORK AREA.
         LR    R1,R2                   ADDRESS OF WORKAREA.
         BAL   GR9,FREECORE            GO FREE THE WORKAREA.
.SS11    ANOP                                                  @Y30LB35
         L     R3,DCBPTR                DCB ADDRESS.            YL02912
         LA    R2,DEBEND-MYDEB         SIZE OF DEB.
*        USING MYDEB,GR1                                         YM4604
         GETMAIN R,LV=(2),SP=230                                 YM4604
         LR    R2,R1                   SAVE AREA ADDRESS.        YM4604
         XC    MYDEB(DEBEND-MYDEB),MYDEB  CLEAR DEB TO ZER.      YM4604
         AIF   ('&SS1' EQ 'SS1').SS111 OMIT DASDR CODE         @Y30LB35
         BAL   GR9,OFFLENQ              ENQ FOR OFFLINE UCB     YL02912
.SS111   ANOP                                                  @Y30LB35
         BAL   R9,BUILDDEB             GO BUILD A DEB.
         AIF   ('&LIB' EQ 'LIB2').WM007
         ST    R4,44(R3)                DEB ADDRESS TO DCB
.WM007   ANOP
         AIF   ('&SS1' EQ 'SS1').SS12  OMIT DASDR CODE         @Y30LB35
         L     R4,DEBAPPAD-1           ADDRESS OF APPENDAGE TABL5#
         MVC   DEBEOEA(20),0(R4)       PLACE IN DEB.
         LA    R4,DEBEOEA              ADDRESS OF NEW TABLE.
         ST    R4,DEBAPPAD-1           SAVE ADDRESS IN DEB.
.SS12    ANOP                                                  @Y30LB35
         MVZ   DEBDEBID(L1),TCBPKF      PROTECT KEY TO DEB.     YL02912
         AIF   ('&SS1' EQ 'SS1').SS121 OMIT DASDR CODE         @Y30LB35
         CLI   FUNCTION,POSTSPEC        OFFLINE LABEL REQUEST?  YL02912
         BE    EXIT                     YES, NOT NEED APPENDAGE.YL02912
.SS121   ANOP                                                  @Y30LB35
         AIF   ('&SS1' EQ 'SS1').SS13  OMIT DASDR CODE         @Y30LB35
         EJECT
***********************************************************************
*                                                                     *
*   LOAD IN THE ABNORMAL END APPENDAGE HERE.                          *
*                                                                     *
***********************************************************************
         USING CVT,1
         L     GR1,CVTPTR              CVT ADDRESS.
         L     GR4,CVTSVDCB            DCB ADDRESS.
         LOAD  EP=IGG019P9,DCB=(4)
         ST    R0,DEBXCEA              PLACE ADDRESS IN DEB.
         AIF   ('&LIB' EQ 'LIB1').WM001
         SRL   GR1,7(0)                SAVE SIZE OF MODULE       M4873
         LA    GR1,1(GR1)              IN 2K BYTES IN            M4873
         STC   GR1,DEBXCEA             AVT TABLE                 M4873
         LOAD  EP=IGG019P7,DCB=(4)     LOAD PAGE FIX APPENDAGE   M5431
         ST    R0,DEBSIOA              PLACE ADDRESS IN DEB       M5431
         SRL   GR1,7(0)                SAVE SIZE OF MODULE        M5431
         LA    GR1,1(GR1)              IN 2K BYTES IN             M5431
         STC   GR1,DEBSIOA             AVT TABLE                  M5431
         OI    DEBSIOA,PGFIXBIT        SHOW APPENDAGE IS PAGE FIX M5431
.WM001   ANOP
.SS13    ANOP                                                  @Y30LB35
         B     EXIT                     RETURN TO CALLER.       YL02912
         AIF   ('&SS1' EQ 'SS1').SS14  OMIT DASDR CODE         @Y30LB35
***********************************************************************
*                                                                     *
*   ASSIGN ALTERNATE TRACKS HERE.                                     *
*        1)    GETALT REQUEST(MUST READ IN FORMAT4 DSCB)              *
*        2)    ANALYZE/FORMAT REQUEST.                                *
*                                                                     *
***********************************************************************
         SPACE 3
ASSGALT  EQU   *                       GETALT/DYNAMIC TRACK ASSIGN.
         LA    R2,IOEND-MYDEB          SIZE OF GETMAIN AREA.
         LR    R4,R2                   SIZE OF AREA.             YM6524
         BAL   R9,GETMAIN              OBTAIN A WORK AREA.       YM6524
         USING IOBLOCKS,R2                                       YM6524
         XC    IOEND1(IOEND-IOEND1),IOEND1    CLEAR EXTENSION.   YM6524
         B     CLEAR                   GO TO CLEAR REST.         YM6524
         SPACE 3
         USING MYDEB,GR1                                         YM6524
GETMAIN  EQU   *                                                 YM6524
         GETMAIN R,LV=(2),SP=229                                 YM6524
         LR    R2,R1                   SAVE AREA ADDRESS.        YM6524
         XC    MYDEB(DEBEND-MYDEB),MYDEB  CLEAR DEB TO ZER.      YM6524
         BR    R9                      RETURN.                   YM6524
         SPACE 3                                                 YM6524
ASGALT1  EQU   *
         LA    R2,IOEND1-MYDEB         SIZE OF GETMAIN AREA.
         LR    R4,R2                   SIZE OF AREA.
         BAL   R9,GETMAIN              OBTAIN A WORK AREA.       YM6524
CLEAR    STH   R4,SIZE                 REMEMBER THE SIZE.        YM6524
         XC    ECB(IOEND1-ECB),ECB     CLEAR PART OF WORK AREA.  YM6524
         LA    GR3,DCB-44              DCB ADDRESS.
         BAL   R9,BUILDDEB             GO BUILD A DEB.
         ST    R4,DCB                  DEB ADDRESS TO DCB.
         LR    GR3,GR11                PARM LIST POINTER.
         LR    GR4,GR2                 WORKAREA POINTER.
         L     GR7,UCBPTR               ADDRESS OF UCB.         YL02912
         USING UCB,GR7                                           S20201
         MODESET EXTKEY=SUPR                                    YL02912
         CLI   UCBTBYT4,X06             IS THIS A 2305 MOD 1.    S20201
         BE    ASGALT3                  YES                      S20201
         CLI   UCBTBYT4,X07             IS THIS A 2305 MOD 2.    S20201
         BE    ASGALT3                                           S20201
         STM   GR3,GR5,RBEXSAVE+K16     STORE 2ND LOAD LIST.    YL02912
         LA    GR1,RBEXSAVE+K16         LIST ADDR FOR 2ND LOAD. YL02912
         LA    GR4,XCTL                XCTL LIST FOR NEXT LOAD.
         B     NEXTLOAD                                          S20201
ASGALT3  EQU   *                                                 S20201
         DROP  GR7                                               S20201
         STM   GR3,GR5,RBEXSAVE+K16     STORE 4TH LOAD LIST.    YL02912
         LA    GR1,RBEXSAVE+K16         LIST ADDR FOR 4TH LOAD. YL02912
         LA    GR4,XCTL2                                         S20201
         SPACE 2
NEXTLOAD EQU   *                       MUST BRING IN THE NEXT LOAD.
         MVC   RBEXSAVE(XCTLEND-XCTL),E0(GR4)  GET XCTL LIST.   YL02912
         LA    GR15,RBEXSAVE            ADDR OF EXTENDED AREA.  YL02912
         LA    GR6,8(GR15)             ADDRESS OF ENTRY POINT.
         ST    GR6,0(GR15)             STORE IN LIST LIST.
         MODESET EXTKEY=DATAMGT                                  YM1315
         XCTL  ,MF=(E,(1)),SF=(E,(15))                          YM03870
.SS14    ANOP                                                  @Y30LB35
         EJECT
***********************************************************************
*                                                                     *
*   BUILD A DEB HERE.                                                 *
*        R3 CONTAINS THE DCB ADDRESS UPON INPUT.
*        R4 CONTAINS THE DEB ADDRESS UPON EXIT.
*        R9 IS THE RETURN REGISTER.
*                                                                     *
***********************************************************************
BUILDDEB EQU   *
         MVI   DEBNMEXT,1              SET NO. OF EXTENTS.
         MVI   DEBENDCC,X'7F'          SET UPPER EXTENT LIMIT.
         MVI   DEBNMTRK,X'7F'          SET NO. OF TRACKS.
         ST    R3,DEBDCBAD-1           DCB ADDRESS TO DEB.
         MVI   DEBDEBID,X'0F'          PROTECT KEY//ID.
         AIF  ('&LIB' EQ 'LIB2').X301003                        XL03912
         L     GR4,UCBPTR               ADDRESS OF UCB.         YL02912
         USING UCB,GR4
         CLI   UCBTBYT4,X06             IS THIS A 2305-1.        S20201
         BE    SW2305                   SET 2305 SWITCH.         S20201
         CLI   UCBTBYT4,X07             IS THIS A 2305-2.        S20201
         BNE   CHK2321                  NO-CHECK FOR 2321        S20201
SW2305   EQU   *                                                 S20201
         OI    DEVTYP,MAINUCB           YES-SET DEVICE BYTE.     S20201
         B     BUILD                    CONTINUE PROCESS         S20201
CHK2321  EQU   *                                                 S20201
         CLI   UCBID,MAINUCB           THIS A 2321.
         DROP  GR4
         BNE   DEB2321                 YES-GO PROCESS
BUILD    EQU   *                        *                        S20201
.X301003 ANOP                                                   XL03912
         MVC   DEBUCBAD(L3),UCBPTR+D1  UCB ADDRESS TO DEB.      YL02912
         MVI   DEBDVMOD,X'C0'          SET THE FILE MASK.      @Y30LB35
         L     R4,CVTPTR               ADDRESS OF CVT.
         USING CVT,R4
         L     R4,CVTXAPG              APPENDAGE TABLE ADDRESS.
         DROP  R4
         ST    R4,DEBAPPAD-1           STORE IN DEB.
         LA    R4,DEB                  DEB ADDRESS.
         AIF   ('&LIB' EQ 'LIB1').WM006
         AIF   ('&SS1' EQ 'SS1').SS141 OMIT DASDR CODE         @Y30LB35
         CLI   FUNCTION,NEWVOL          NEW VOLUME REQUEST?     YL02912
         BNE   NOCHK4                   NO, DEB WILL NOT BE      Y01021
*                                       VALIDATED                Y01021
.SS141   ANOP                                                  @Y30LB35
         MODESET KEYADDR=KEY,WORKREG=15 GET CALLERS KEY.        YL02912
         ST    R4,E44(R3)               DEB ADDR TO DCB          Y01021
         MODESET EXTKEY=DATAMGT                                 YL02912
         DROP  R2                                              @Y30LB35
         EJECT
**********************************************************************
*        PLACE DEB ON TCB DEB CHAIN                                  *
**********************************************************************
         SPACE
         USING DEB,GR4                                          YL02912
         ST    R8,DEBTCBAD-D1           PUT TCB PTR IN DEB      YL02912
         L     R7,TCBDEB                GET DEB PTR FROM TCB    YL02912
         ST    R7,DEBDEBAD-D1           DEB CHAIN PTR TO MY DEB.YL02912
         MODESET EXTKEY=SUPR                                    YL02912
         ST    R4,TCBDEB                CHAIN MY DEB.           YL02912
         MODESET EXTKEY=DATAMGT                                 YL02912
         DEBCHK (R3),TYPE=ADD,AM=EXCP                            YM0960
         LR    GR1,GR11                 RESTORE PARM POINTER     YM2646
.WM006   ANOP
         BR    R9                      RETURN.
         AIF  ('&LIB' EQ 'LIB2').X301004                        XL03912
         SPACE
         USING DATACELL,GR6                                    @Y30LB35
DEB2321  LR    GR6,GR4                 SUB-UCB ADDRESS.
         MVC   DEBBINNO(2),DCELBBNR    BIN NO. TO DEB.         @Y30LB35
         BAL   GR14,CONV2321           CONVERT SUB TO MAIN UCB ADDRESS.
         ST    GR6,DEBUCBAD-1          MAIN UCB ADDRESS TO DEB.
         B     BUILD1                  GO FINISH THE DEB.
         DROP  6
         SPACE 3
CONV2321 LH    GR10,0(GR6)             PICK UP BIN NO.
         SLA   GR10,4                  TIMES SIXTEEN.
*         LA    GR10,DATACELL-UCBOB(GR10)  ADD LENGTH OF MAIN.
         LCR   GR10,GR10               NEGATE.
         AR    GR6,GR10                ADDRESS OF MAIN.
         BR    GR14                    RETURN.
.X301004 ANOP                                                   XL03912
         AIF   ('&SS1' EQ 'SS1').SS15  OMIT DASDR CODE         @Y30LB35
         AIF   ('&LIB' EQ 'LIB1').WM005
.WM005   ANOP
         DROP  R2
XCTL     XCTL  EP=IGC0108B,SF=L        LIST FORM .
XCTLEND  DS    0C                      END OF XCTL LIST.
XCTL1    XCTL  EP=IGC0208B,SF=L        LIST FORM.  2ND LOAD.
XCTL2    XCTL  EP=IGC0308B,SF=L         LIST FORM. 3RD LOAD.     S20201
.SS15    ANOP                                                  @Y30LB35
         DROP  GR5                                             @Y30LB35
         DROP  GR4                                             @Y30LB35
         DROP  GR6                                             @Y30LB35
         EJECT                                                 @Y30LB35
DEVTYP   DC    X'00'                    DEVICE TYPE SWITCH.      S20201
K16      EQU   16                       DISPLACEMENT CONSTANT    S20201
         SPACE 1
         AIF   ('&SS1' EQ 'SS1').SS16  OMIT DASDR CODE         @Y30LB35
WTORP    WTOR  'IEH841D        CONFIRM REQUEST TO           ',0,1, O122X
               0,ROUTCDE=(4),DESC=(2),MF=L                         MC0I
WTOREND  DS    0C                      END OF WTOR.
         SPACE
WTO      WTO   'IEH808I REPLY IN ERROR. REPLY WITH U OR T',        MC0IX
               ROUTCDE=(4),DESC=(5),MF=L                           MC0I
WTOEND   DS    0C
.SS16    ANOP                                                  @Y30LB35
DEBLEN   DS    0F                                                A34646
         DC    X'E5'                    SUB POOL NUMBER.        YL02912
         DC    AL3(DEBEND-MYDEB)        LENGTH OF DEB FOR        Y01021
*                                       VALIDATION               Y01021
MAINT    DS    10F                      MAINTENANCE AREA        XL02912
         AIF   ('&LIB' EQ 'LIB1').X227904                        YM3475
BLANKS   DC    X'40404040'              OFFLINE DDNAME END.      YM3475
.X227904 ANOP                                                    YM3475
         AIF   ('&SS1' EQ 'SS1').SS17  OMIT DASDR CODE         @Y30LB35
LABEL    DC    C'LABEL     '           USED FRO LABEL OFFLINE.     O122
INITIAL  DC    C'INITIALIZE'           USED FOR OFFLINE DASDI.     O122
ENQLISTF ENQ   (QHDR,,E,6,SYSTEM),TCB=,RET=USE,MF=L             YM03804
SYSQ     DC    C'SYSIEFSD'              ENQ/DEQ QNAME HEADER    YL02912
QHDR     DC    C'SYSZVOLS'             ONLINE ENQ HEADER        YM03804
Q4       DC    C'Q4'                    ENQ/DEQ RNAME HEADER    YL02912
.SS17    ANOP                                                  @Y30LB35
NOGO     EQU   8                        ENQ FAILED RETURN CODE. YL02912
         SPACE
SENSCCW  CCW   4,SENSCCW,X'20',6        SENSE CMD               XL03130
MAINTNC  DS    50D                      MAINTENANCE AREA        XL03130
GOODRT   EQU   X'7F'                    GOOD ECB RETURN CODE    XL03130
MASK     EQU   X'FF'                    'AND' MASK              XL03130
TCBKOFST EQU   28                       OFFSET TO TCB KEY       XL03130
MOD1     EQU   X'01'                    MODEL ONE SENSE VAL     XL03130
MOD2     EQU   X'02'                    MODEL TWO SENSE VAL     XL03130
L1       EQU   1                                                XL03130
L4       EQU   4                                                XL03130
L2       EQU   2                                                XL03130
DEBID    EQU   X'0F'                    DEB ID                  XL03130
L7       EQU   7                        OFFSET TO USERS' R.C.   XL03130
SENSEOP  EQU   X'04'                    SENSE OP CODE           XL03130
L88      EQU   88                       LENGTH OF EXCP CTL BLKS XL03130
BUMP     EQU   X'28'                    ADDR ADJUSTMENT         XL03130
CTLBLKS  DSECT                                                  XL03130
DCBORG   DS    7F                       DCB                     XL03130
DEBORG   DS    3F                       DEB                     XL03130
ECB1     DS    1F                       ECB                     XL03130
DCBDEB   DS    A                        DCB+2C                  XL03130
DEBPURG  DS    F                        DEB+14                  XL03130
DEBDCB   DS    A                        DEB+18                  XL03130
DEBAPP   DS    F                        DEB+1C                  XL03130
DEBUCB   DS    A                        DEB+20                  XL03130
DEBSKA   DS    2F                       DEB                     XL03130
IOB1     DS    F                        IOB                     XL03130
IOBECB   DS    A                        IOB+4                   XL03130
         DS    2F                       FILL                    XL03130
IOBCCW   DS    A                        IOB+10                  XL03130
IOBDCB   DS    A                        IOB+14                  XL03130
SENSE    DS    2F                       INPUT BUFFER            XL03130
IOBSKA   DS    2F                       IOB+20                  XL03130
CCW      DS    D                        SENSE CMD               XL03130
         AIF   ('&SS1' EQ 'SS1').SS18  OMIT DASDR CODE         @Y30LB35
MSG      DSECT
         DS    0D
MSG1     DS    CL1                     REPLY LENGTH.
MSG2     DS    CL3                     REPLY ADDRESS.
MSG3     DS    CL4                     ECD ADDRESS.
MSG4     DS    CL2                     MESSAGE LENGTH.
         DS    CL2
MSG5     DS    CL44                    MESSAGE BODY                MC0I
MSG55    DS    CL4                     MCS FLAGS/CODES.            MC0I
MSG6     EQU   MSG5+8                  CUU OR CUU/B                MC0I
WECB     DS    CL4                     ECB FOR WTOR.
REPLY    DS    CL1                     REPLY AREA.
WTORP1   DS    0C
.SS18    ANOP                                                  @Y30LB35
         EJECT
IOBLOCKS DSECT
MYDEB    EQU   *                       DEB DEFINITION.
DEBEOEA  DS    1F                      END-OF-EXTENT.
DEBSIOA  DS    1F                      START I/O.
DEBPCIA  DS    1F                      PCI.
DEBCEA   DS    1F                      CHANNEL END.
DEBXCEA  DS    1F                      ABNORMAL END.
         DS    CL12
DEBLNGTH DS    CL1                     DEB LENGTH(DOUBLE WORDS)
         DS    CL3
DEB      EQU   *                       DEB PROPER
DEBNMSUB DS    CL1                     OPEN SUBROUTINES.
DEBTCBAD DS    CL3                     TCB ADDRESS.
DEBAMLNG DS    CL1                     LENGTH ACCESS METHOD.
DEBDEBAD DS    CL3                     NEXT DEB ADDRESS.
DEBOFLGS DS    CL1                     DATA SET FLAGS.
DEBIRBAD DS    CL3                     IRB ADDRESS.
DEBOPATB DS    CL1                     TYPE OF I/O.
DEBSYSPG DS    CL3                     IOB PURGE ADDRESS.(SYSTEM)
DEBNMEXT DS    CL1                     NO. OF EXTENTS.
DEBUSRPG DS    CL3                     IOB USER PURGE ADDRESS.
DEBPRIOR DS    CL1                     ZERO
DEBECBAD DS    CL3                     PURGE ECB.
DEBDEBID DS    CL1                     PROTECT KEY//ID.
DEBDCBAD DS    CL3                     DCB ADDRESS.
DEBEXSCL DS    CL1                     EXTENT SCALE.
DEBAPPAD DS    CL3                     APPENDAGE VECTOR TABLE.
DEBDVMOD DS    CL1                     FILE MASK.
DEBUCBAD DS    CL3                     UCB ADDRESS.
DEBBINNO DS    CL2                     BIN NO. FOR 2321.
DEBSTRCC DS    CL2                     CYLINDER START.
DEBSTRHH DS    CL2                     HEAD START.
DEBENDCC DS    CL2                     CYLINDER END.
DEBENDHH DS    CL2                     HEAD END.
DEBNMTRK DS    CL2                     NUMBER OF TRACKS.
         DS    CL20
DEBEND   DS    0C                      END OF DEB.
         EJECT
ECB      DS    1F                      ECB HERE.
IOB      EQU   *                       IOB HERE.
IOBFLAG1 DS    CL1                     FLAG1
IOBFLAG2 DS    CL1                     FLAG2.
IOBSENS0 DS    CL1                     SENSE BYTE ZERO.
IOBSENS1 DS    CL1                     SENSE BYTE ONE.
IOBECBAD DS    CL4                     ADDRESS OF ECB.
IOBCSW   DS    CL8                     STATUS WORDS.
IOBSTART DS    CL4                     CHANNEL PROGRAM ADDRESS.
IOBDCBPT DS    CL4                     ADDRESS OF DCB.
IOBRESTR DS    CL4                     RESTART ADDRESS.
IOBINCAM DS    CL2                     BLOCK COUNT.
IOBERRCT DS    CL2                     ERROR COUNT.
IOBSEEK  DS    CL8                     SEEK ADDRESS.
CCHHR    EQU   IOBSEEK+3               SEARCH ADDRESS.
DCB      DS    1F                      DEB ADDRESS.
ALTINFO  DS    1D                      KEEP ALTERNATE TRACK INFO HERE.
         AIF  ('&LIB' EQ 'LIB1').X302300                        XL03130
SCHHA    DS    1D                      SEARCH HOME ADDR.        XL03130
TICSRCH  DS    1D                      TIC ON SEARCH.           XL03130
.X302300 ANOP                                                   XL03130
WRTHA    DS    1D                      WRITE HOME ADDRESS.
WRTR0    DS    1D                      WRITE RECORD ZERO.
RDHA     DS    1D                      READ HOME ADDRESS.
READR0   DS    1D                      READ RECORD ZERO.
         AIF   ('&LIB' EQ 'LIB1').X227800                       XL03130
         DS    CL6                      RESERVED.               XL03130
WINHA    DS    CL2                      3340 HOME ADDRESS.      XL03130
.X227800 ANOP                                                   XL03130
HA       DS    CL5                     HOME ADDRESS.
SW       DS    CL1                     SWITCH.
D2314    EQU   X'80'                   2314 DEVICE.
D2321    EQU   X'40'                   2321 DEVICE.
SIZE     DS    CL2                     SIZE OF GETMAIN AREA.
R0COUNT  DS    1D                      RECORD ZERO COUNT FIELD.
R0DATA   DS    1D                      RECORD ZERO DATA  FIELD.
         AIF   ('&LIB' EQ 'LIB1').X322203                        XM4405
SNSBYTES DS    CL24                    3340 SENSE BYTE AREA.     XM4405
.X322203 ANOP                                                    XM4405
IOEND1   DS    0C                      END OF BASIC I/O BLOCKS.
*   EXTENSION FOR GETALT//DYNAMIC TRACK REQUEST.
SEARCH   DS    1D                      SEARCH CCW.
TIC      DS    1D                      REPEAT UNTIL FOUND.
WRTRD    DS    1D                      WRITE OR READ DATA.
FORMAT4  DS    CL96                    FORMAT4 DSCB READ IN AREA.
ALTDATA  EQU   FORMAT4+8               ALTERNATE TRACK INFORMATION.
IOEND    DS    0C
         EJECT
         AIF   ('&SS1' EQ 'SS1').SS19  OMIT DASDR CODE         @Y30LB35
***********************************************************************
*                                                                     *
*   PARAMETER LIST DSECT                                              *
*                                                                     *
***********************************************************************
         SPACE 2
PARMLIST DSECT                                                  YL02912
WINMOD   EQU   DCBPTR+3                 3340 MODEL NO.          YL02912
EDQLIST  DS    4F                       ENQ/DEQ PARAM LIST      SJ0054
ENQLIST  EQU   EDQLIST+4                                        SJ0054
ENDLIST  EQU   *                        PARAMETER LIST END.     YL02912
         EJECT
CVT      DSECT
         CVT   SYS=MIN
         IKJTCB LIST=YES
         EJECT
         IHARB DSECT=YES
         EJECT
UCB      DSECT
         IEFUCBOB
.SS19    ANOP                                                  @Y30LB35
IGC0408B CSECT                                                 @Y30LB35
         AIF   ('&SS1' EQ 'SS1').SS20  OMIT DASDR CODE         @Y30LB35
*STATUS- CHANGE LEVEL 000                                             *
*                                                                     *
*FUNCTION/OPERATION-  THIS LOAD OF SVC 82 -POSTS- THE UCBS FOR        *
*   DIRECT ACCESS VOLUMES THAT ARE WRITTEN ON BY THE -IEHDASDR-       *
*   UTILITY PROGRAM. IT DOES SO AS FOLLOWS.                           *
*                                                                     *
*        -IF DUPLICATE SERIAL NUMBERS ARE ENCOUNTERED ;         SA55451
*         1) SVC 91 IS ISSUED,                                  SA55451
*         2) VOLUME SERIAL NO. IN THE UCB IS CLEARED,           SA55451
*         3) NOT-READY BIT IN UCB IS SET,                       SA55451
*         4) A DEMOUNT MESSAGE IS SENT TO THE OPERATOR.         SA55451
*                                                                     *
*        -IF A NEW/UNIQUE SERIAL NO. RESULTS,ISSUE A -NEW- MESSAGE    *
*         TO THE OPERATOR. PLACE THE SERIAL NO. IN THE UCB.           *
*                                                                     *
*        -IF A NEW VTOC HAS BEEN PLACED ON THIS VOLUME, PUT ITS       *
*         TTR IN THE UCB.                                             *
*                                                                     *
*ENTRY POINTS- THE ONLY ENTRY POINT IS -IGC0208B-.                    *
*                                                                     *
*INPUT-  REGISTER 1 POINTS TO THE SVRB.                               *
*   THE EXTENDED AREA OF THE SVRB CONTAINS THE INPUT PARM POINTER     *
*   THAT WAS PASSED TO -IGC0008B-.                                    *
*                                                                     *
*EXITS-NORMAL- VIA A SVC 3 TO THE CALLING ROUTINE.                    *
*                                                                     *
*EXITS-ERROR-  NONE POSSIBLE.                                         *
*                                                                     *
*SUPERVISOR MACROS-  GETMAIN,FREEMAIN,WTO.                            *
*                                                                     *
*OUTPUT-  IF NEW VTOC, ITS TTR IS PLACED IN THE UCB.                  *
*         IF DUPLICATE SERIAL NO., A RETAIN MESSAGE IS ISSUED.        *
*         IF NEW SERIAL NO., A NEW MESSAGE IS ISSUED.                 *
*                                                                     *
*ATTRIBUTES-  REENTRANT,RELOCATABLE,PRIVILEGED,ENABLED.               *
*                                                                     *
***********************************************************************
         EJECT
*   THE FOLLOWING ARE REGISTER ASSIGNMENTS.
         LR    BASEREG,GR15            SET UP ADDRESSING.
         USING IGC0408B,BASEREG                                @Y30LB35
         SPACE
         SPACE 2
.SS20    ANOP                                                  @Y30LB35
         EJECT                                                 @Y30LB35
*************************************************************  @Y30LB35
*                                                              @Y30LB35
*  POST UCB WITH VOLUME SERIAL NUMBER, VTOC TTR0 AND TEST FOR  @Y30LB35
*  DUPLICATE VOLUMES.                                          @Y30LB35
*                                                              @Y30LB35
*************************************************************  @Y30LB35
POSTUCB  DS    0H                      IF REQT TO POST UCB     @Y30LB35
         AIF   ('&SS1' EQ 'SS1').SS201 OMIT DASDR CODE         @Y30LB35
         L     GR11,RBEXSAV+16(GR1)    PARM LIST POINTER.      @Y30LB35
         LR    GR9,GR1                 SVRB ADDRESS.
.SS201   ANOP                                                  @Y30LB35
         USING PARMLIST,GR11                                    YL02912
         USING RBBASIC,GR9                                     @Y30LB35
STARTEST EQU   *                                                   O122
         L     GR4,SERPTR              POINTER TO NEW SERIAL.   YL02912
         L     GR6,UCBPTR              POINTER TO UCB.          YL02912
         SLL   GR6,16                  MOVE HIGH ORDER BYTES   @Y30LB35
         SRL   GR6,16                  MOVE BACK WITH ZEROS    @Y30LB35
         LH    GR3,0(GR2)              PICK UP ENTRY.              O122
         SLL   GR3,16                  MOVE HIGH ORDER BYTES   @Y30LB35
         SRL   GR3,16                  MOVE BACK WITH ZEROS    @Y30LB35
         LTR   GR3,GR3                 VALID ENTRY.
         BZ    UPUCBPTR                NO--GO SEE IF MORE ENTRIES.
         USING UCB,GR3
         TM    UCBTBYT3,UCB3DACC       THIS A DIRECT ACCESS DEVICE.
         BZ    UPUCBPTR                NO--GO SEE IF MORE ENTRIES.
         AIF   ('&LIB' NE 'LIB1').X301201                       XL03912
         CLI   UCBTBYT4,DA2321         THIS A 2321.
         BE    PROC2321                YES-SPECIAL CASE.
.X301201 ANOP                                                   XL03912
         LA    GR5,SRTEVOLI            ADDRESS OF SERIAL NO.
         C     GR5,RBEXSAVE+16         THIS UCB TESTED          SA53836
         BE    UPUCBPTR                GO TEST NEXT UCB         SA53836
         BAL   GR7,TESTUCB             GO TEST FOR IDENTICAL/NEW.
         B     UPUCBPTR                GO TEST NEXT UCB.
         AIF   ('&LIB' NE 'LIB1').X301202                       XL03912
         SPACE
PROC2321 LA    GR3,DATACELL            ADDRESS OF 1ST SUB-UCB. @Y30LB35
CK2321   LA    GR5,DCELVOLI-DATACELL(GR3) SERIAL NO. ADDRESS.  @Y30LB35
         BAL   GR7,TESTUCB             GO TEST IDENTICAL//NEW.
         CLI   1(GR3),LASTBIN          THIS THE LAST SUB-UCB.
         BE    UPUCBPTR                YES--GO TEST NEXT UCB.
         LA    GR3,SUBLNG(GR3)         STEP TO NEXT SUB-UCB.
         B     CK2321                  GO CHECK ALL SUB-UCBS.
.X301202 ANOP                                                   XL03912
         DROP  3
         SPACE
UPUCBPTR LA    GR2,2(GR2)              STEP TO NEXT TABLE ENTRY.
         CLC   0(2,GR2),UCBLAST        ANY MORE ENTRIES.
         BNE   STARTEST                YES--KEEP LOOKING.
PRETURN  EQU  *                                                 YL02912
         LA    R2,ENDLST-PARMLIST       PARM LIST SIZE.        @Y30LB35
         FREEMAIN R,LV=(2),A=(11),SP=229 FREE PARM LIST         YL02912
         SR    R15,R15                  SET RETURN CODE.        YL02912
         L     R14,RBEXSAVE+K24         GET RETURN ADDRESS.    @Y30LB35
         MODESET EXTKEY=SUPR                                    YM1315
         BR    R14                      RETURN TO CALLER.       YL02912
ERRORRTN EQU  *                                                 YL02912
         LA    R2,ENDLST-PARMLIST       PARM LIST SIZE.        @Y30LB35
         FREEMAIN R,LV=(2),A=(11),SP=229 FREE PARM LIST         YL02912
         LA    R15,FOUR                 SET ERROR CODE         @Y30LB35
         L     R14,RBEXSAVE+K24         GET RETURN ADDRESS.    @Y30LB35
         MODESET EXTKEY=SUPR                                    YM1315
         BR    R14                      RETURN TO CALLER.       YL02912
         EJECT
*************************************************************  @Y30LB35
*                                                              @Y30LB35
*  TEST FOR  DUPLICATE VOLUMES.                                @Y30LB35
*                                                              @Y30LB35
*************************************************************  @Y30LB35
TESTUCB  EQU   *
         MODESET KEYADDR=KEY,WORKREG=15 GET CALLER'S KEY.       YL02912
         CLC   D0(L6,GR4),D0(GR5)       SERIAL NUMBERS EQUAL?   YL02912
         MODESET EXTKEY=DATAMGT                                 UL02912
         BE    DUP                     YES-HANDLE ERROR        @Y30LB35
TESTUCB1 CLR   GR6,GR3                 THIS THE SELECTED UCB/SUB UCB.
         BE    POSTIT                  YES-POST NEW SERIAL.    @Y30LB35
         BR    GR7                     NO--RETURN.
         SPACE 2
POSTIT   EQU   *                       UPDATE UCB HERE         @Y30LB35
         LTR   GR4,GR4                 IS THERE A NEW SERIAL NO.
         BZ    TESTTR                  NO--GO SEE IF TTR NEEDS UPDATE.
         MODESET KEYADDR=KEY,WORKREG=15 GET CALLER'S KEY.       YL02912
         CLC   0(6,GR4),0(GR5)         ARE THE SERIAL NOS. THE SAME.
         MODESET EXTKEY=DATAMGT                                 YL02912
         BE    TESTTR                  YES-NO NEED TO TELL THE USER.
         AIF   ('&SS1' EQ 'SS1').SS21  OMIT DASDR CODE         @Y30LB35
         BAL   GR8,SETUPMSG            PLACE MESSAGE IN BUFFER.
         MVI   12(GR10),C'N'           'NEW' INDICATOR TO MESSAGE.
         BAL   GR8,WRITE               WRITE TO OPERATOR//FREECORE.
.SS21    ANOP                                                  @Y30LB35
         MODESET   EXTKEY=SUPR                                  YL02912
         ST    GR5,RBEXSAVE+16         INDICATE UCB TESTED     @Y30LB35
         MODESET   EXTKEY=DATAMGT                               YL02912
         AIF   ('&SS1' EQ 'SS1').SS211 OMIT DASDR CODE         @Y30LB35
         TM    SRTESTAT-UCB(GR6),SRTEONLI  DEVICE ONLINE.          X726
         BZ    TESTTR                  NO-GO PROCESS VTOC.         X726
.SS211   ANOP                                                  @Y30LB35
         USING UCB,GR3                                         @Y30LB35
         MODESET EXTKEY=SUPR                                    YL02912
         MVC   0(6,GR5),0(GR4)         YES-NEW SERIAL NO TO UCB.   X726
         NI    UCBFL2,X'FF'-NOTREADY   TURN OFF NOT READY BIT  @Y30LB35
         NI    UCBFLC,X'FF'-UCBUDE     TURN OFF DEVICE END BIT @Y30LB35
         MODESET EXTKEY=DATAMGT                                 YL02912
         DROP  GR3                                             @Y30LB35
*    UPDATE TTR OF VTOC HERE.
TESTTR   EQU   *                                                YL02912
         AIF   ('&SS1' EQ 'SS1').SS22  OMIT DASDR CODE         @Y30LB35
         OC    NEWCCHH+D1(L3),NEWCCHH+D1 NEED TTR UPDATE?       YL02912
         BCR   8,GR7                   NO--GO CHECK MORE UCBS.     O122
*     BUILD A DUMMY DEB HERE.
         LA    R3,RBEXSAVE             SAVE AREA ADDRESS.      @Y30LB35
         L     GR8,K36(R3)             SAVE RETURN ADDRESS.     YL02912
         MODESET   EXTKEY=SUPR                                  YL02912
         STM   1,12,0(R3)              SAVE THE REGISTERS.
         MODESET   EXTKEY=DATAMGT                               YL02912
         GETMAIN R,LV=0+DEBEND-DEB,SP=229                       YL02912
         USING DEB,1
         XC    DEB(DEBEND-DEB),DEB     CLEAR CORE TO ZERO.
         MVI   DEBNMEXT,1              SET FOR 1 EXTENT.
         MVI   DEBENDCC,X'7F'          SET UPPER EXTENT LIMIT.
         MVI   DEBNMTRK,X'7F'          SET NO. OF TRACKS.
         MVI   DEBDEBID,X'0F'          PROTECT KEY//DEB ID.
         AIF   ('&LIB' NE 'LIB1').X301203                       XL03912
         L     GR4,0(GR11)             UCB ADDRESS.
         USING UCB,4
         CLI   UCBID,MAINUCB           THIS A 2321.
         BNE   DEB2321                 YES--GO HANDLE.
.X301203 ANOP                                                   XL03912
         MVC   DEBUCBAD(L3),UCBPTR+D1   UCB ADDRESS TO DEB.     YL02912
         AIF   ('&LIB' NE 'LIB1').X301204                       XL03912
         DROP  4
.X301204 ANOP                                                   XL03912
         SPACE
BUILD2   EQU   *
         USING CVT,R15
         L     R15,CVTPTR              ADDRESS OF CVT.
         L     R15,CVTPRLTV            CONVERT ADDRESS//MBBCCHHR TO TTR
         DROP  15
         SPACE
         L     R2,NEWCCHH               VTOC PTR FOR UCB.       YL02912
         MODESET KEYADDR=KEY,WORKREG=14  GET CALLERS KEY         YM1384
         LM    R4,R5,D0(R2)            GET VTOC CCHH.            YM1180
         MODESET EXTKEY=DATAMGT                                  YM1384
         STM   R4,R5,VTOCCCHH          PUT CCHH IN SVC AREA.     YM1180
         LA    R2,VTOCCCHH             TTR PTR FOR CONVERT.      YM1129
         SPACE
         BALR  R14,R15                 CONVERT MBBCCHHR TO TTR.
         SPACE
         LR    R15,R1                  SAVE DEB ADDRESS.
         LM    1,12,0(R3)              RESTORE THE REGISTERS.
         MODESET   EXTKEY=SUPR                                  YL02912
         ST    GR8,K36(R3)             RESTORE RETURN ADDRESS.  YL02912
         MODESET   EXTKEY=DATAMGT                               YL02912
         SPACE
.SS22    ANOP                                                  @Y30LB35
         USING UCB,R3
         L     R3,UCBPTR               GET UCB ADDRESS.         YL02912
         SLL   R3,16                   MOVE HIGH ORDER BYTES   @Y30LB35
         SRL   R3,16                   MOVE BACK WITH ZEROS    @Y30LB35
         MODESET EXTKEY=SUPR                                    YL02912
         AIF   ('&LIB' NE 'LIB1').X301205                       XL03912
         CLI   UCBID,MAINUCB           THIS A 2321.
         BNE   PR2321                  YES--GO HANDLE.
.X301205 ANOP                                                   XL03912
         L     R4,NEWTTR               PICK TTR ADDR           @Y30LB35
         MVC   SRTEFSCT(4),0(R4)       MOVE TTR TO UCB         @Y30LB35
         MODESET EXTKEY=DATAMGT                                 YL02912
         DROP  GR3                                             @Y30LB35
         AIF   ('&SS1' EQ 'SS1').SS23  OMIT DASDR CODE         @Y30LB35
         LR    R1,R15                  RESTORE DEB ADDRESS.
         FREEMAIN R,LV=0+DEBEND-DEB,A=(1),SP=229                YL02912
.SS23    ANOP                                                  @Y30LB35
         BR    GR7                     GO CHECK MORE UCBS.         O122
         AIF   ('&LIB' NE 'LIB1').X301206                       XL03912
         SPACE
*         USING DATACELL,R3
*PR2321   ST    R0,DCELVTOC             TTR OF VTOC TO UCB---2321.
         B     TTREND                  GO EXIT.
         DROP  3
         SPACE 2
DEB2321  LR    GR6,GR4                 UCB ADDRESS.
         BAL   GR14,CONV2321           FIND ADDRESS OF MAIN UCB.
         ST    GR6,DEBUCBAD-1          2321 UCB ADDRESS TO DEB.
         B     BUILD2                  EXIT.
.X301206 ANOP                                                   XL03912
         SPACE
*   IDENTICAL SERIAL NUMBERS                                   @Y30LB35
DUP      EQU   *                                               @Y30LB35
         LTR   GR4,GR4                 WAS THERE A NEW SERIAL NO.
         BZ    TESTUCB1                NO--GO MAKE MORE TESTS.
         AIF   ('&SS1' EQ 'SS1').SS24  OMIT DASDR CODE         @Y30LB35
         USING UCB,3
         AIF   ('&LIB' NE 'LIB1').X301207                       XL03912
         CLI   UCBID,MAINUCB           THIS A SUB-UCB.
         BNE   TESTSUB3                YES--GO CHECK STATUS.
.X301207 ANOP                                                   XL03912
         TM    SRTESTAT,SRTEONLI       IS THE TABLE UCB ON-LINE.
         BCR   8,GR7                   NO--RETURN.
         SPACE
         USING UCB,6
         TM    SRTESTAT,SRTEONLI       THE SUBJECT UCB ON-LINE.
         BC    8,TESTUCB1              NO--GO MAKE MORE TESTS.
         DROP  3,6
.SS24    ANOP                                                  @Y30LB35
         CLR   GR6,GR3                 THIS THE SUBJECT UCB.   @Y30LB35
         BE    TESTUCB1                YES-GO MAKE MORE TESTS. @Y30LB35
         AIF   ('&SS1' EQ 'SS1').SS241 OMIT DASDR CODE         @Y30LB35
         BAL   GR8,SETUPMSG            COMPLETE THE MESSAGE.   @Y30LB35
         MVI   12(GR10),C'R'           'REMOVE' INDICATOR TO MESSAGE.
.SS241   ANOP                                                  @Y30LB35
         USING UCB,GR6
         AIF   ('&LIB' NE 'LIB1').X301208                       XL03912
         CLI   UCBTBYT4,DA2321         THIS A 2321.
         BE    YES2321                 YES-GO CLEAR SERIAL NO.
.X301208 ANOP                                                   XL03912
         AIF   ('&SS1' EQ 'SS1').SS25  OMIT DASDR CODE         @Y30LB35
         TM    SRTESTAT,SRTEPRES       THIS A NON-DEMOUNTABLE DEVICE.
         BO    SETNR2                  YES-BETTER MARK IT OFF-LINE.
         LNR   R0,R6                    COMPLIMENTED UCB ADDR   SA55451
         SVC   91                       ISSUE VOLSTAT SVC       SA55451
.SS25    ANOP                                                  @Y30LB35
         MODESET EXTKEY=SUPR                                    YL02912
         XC    SRTEVOLI(6),SRTEVOLI    CLEAR SERIAL NO.         SA55451
         XC    SRTEFSCT(4),SRTEFSCT    CLEAR TTR0 TO VTOC      @Y30LB35
*   TURN ON NOT-READY BIT HERE.
         OI    UCBFL2,NOTREADY         MAKE UCB NOT-READY.
         MODESET EXTKEY=DATAMGT                                 YL02912
SETNR    EQU   *
         AIF   ('&LIB' NE 'LIB1').X301211                       XL03912
         DROP  GR6
.X301211 ANOP                                                   XL03912
         AIF   ('&SS1' EQ 'SS1').SS251 OMIT DASDR CODE         @Y30LB35
         BAL   GR8,WRITE               TELL OPERATOR TO REMOVE VOLUME.
.SS251   ANOP                                                  @Y30LB35
         B     ERRORRTN                POSTING COMPLETE.       @Y30LB35
         AIF   ('&SS1' EQ 'SS1').SS26  OMIT DASDR CODE         @Y30LB35
         AIF   ('&LIB' NE 'LIB1').X301209                       XL03912
YES2321  L     GR11,0(GR11)            INPUT SUB-UCB ADDRESS.
*         USING DATACELL,GR11
*         NI    DCELSTAT,X'FF'-SRTEONLI  PUT THIS SUB-UCB OFF-LINE.
*         XC    DCELVOLI-DATACELL(6,GR11),DCELVOLI-DATACELL(GR11)
*                                      CLEAR OUT NO. IN SUB-UCB.
.X301209 ANOP                                                   XL03912
SETNR1   MVC   42(12,GR1),OFFMSG       TELL ABOUT OFFLINE.
         MVI   1(GR1),WSIZE            SET SIZE IN MESSAGE.
         B     SETNR                   GO MAKE UCB NOT READY.
         AIF   ('&LIB' NE 'LIB1').X301210                       XL03912
         DROP  11
         SPACE
         USING UCB,3
*TESTSUB3 TM    DCELSTAT,SRTEONLI       THIS SUB-UCB ONLINE.
         BCR   8,GR7                   NO--GO MAKE MORE TESTS.
         USING UCB,6
*TESTSUB6 TM    DCELSTAT,SRTEONLI       THIS SUB-UCB ONLINE.
         BCR   8,GR7                   NO--GO MAKE MORE TESTS.
         B     TESTIDEN                YES-CONTINUE RETAIN TEST.
.X301210 ANOP                                                   XL03912
SETNR2   EQU  *                                                 YL02912
         MODESET EXTKEY=SUPR                                    YL02912
         NI    SRTESTAT,X'FF'-SRTEONLI NON-DEMOUNTABLE DEVICE OFF-LINE.
         MODESET EXTKEY=DATAMGT
         B     SETNR1                  GO TELL THE USER.
         AIF   ('&LIB' NE 'LIB1').X301212                       XL03912
         DROP  3,6
.X301212 ANOP                                                   XL03912
         EJECT
*   COMPLETE MESSAGE IEH809 HERE.
SETUPMSG EQU   *
         GETMAIN R,LV=0+WSIZE,SP=229   GET WORK AREA.           XL02912
         XC    0(WSIZE,GR1),0(GR1)     CLEAR MESSAGE AREA.
         MVI   1(GR1),WSIZE1           SET SIZE IN MESSAGE.
         MVC   4(10,GR1),WTOLIST       MESSAGE IDENTIFIER.
         LR    GR15,GR6                SAVE UCB REG.               O122
         USING UCB,GR6
         AIF   ('&LIB' NE 'LIB1').X301213                       XL03912
         CLI   UCBID,MAINUCB           THIS A MAIN UCB.
         BNE   FINDMAIN                NO--MUST BE 2321.
.X301213 ANOP                                                   XL03912
         LR    GR10,GR1                MESSAGE AREA ADDRESS.
SETNAME  MVC   14(3,GR10),UCBNAME      DEVICE ADDRESS TO MESSAGE.
         DROP  GR6
         LR    GR6,GR15                RESTORE UCB REG.            O122
         MVI   17(GR1),COMMA           INSERT COMMA IN MESSAGE.
         MODESET EXTKEY=SUPR           ALLOW FETCH OF VOLSER     YM4604
         MVC   18(6,GR1),0(GR4)        SERIAL NO. TO MESSAGE.
         MODESET EXTKEY=DATAMGT        RESET MODE FOR DATAMGT    YM4604
         MVI   24(GR1),COMMA           INSERT COMMA IN MESSAGE.
         LR    GR15,GR7                SAVE BR AND LINK REG.       O122
         L     GR7,CVTPTR              CVT ADDRESS.
         USING CVT,GR7
         L     GR7,CVTTCBP             CVT DOUBLE WORD ADDRESS.
         DROP  7
         L     GR7,4(GR7)              TCB ADDRESS.
         L     GR7,12(GR7)             TIOT ADDRESS.
         USING TIOT,GR7
         MVC   25(8,GR1),TIOCNJOB      JOB NAME TO MESSAGE.
         MVI   33(GR1),COMMA           INSERT COMMA IN MESSAGE.
         MVC   34(16,GR1),TIOCSTEP     STEP OR PROCEDURE NAME TO MSG.
         LR    GR7,GR15                RESTORE BR AND LINK REG.    O122
         BR    GR8                     RETURN
         DROP  7
         AIF   ('&LIB' NE 'LIB1').X301214                       XL03912
         EJECT
         SPACE
FINDMAIN MVI   17(GR1),SLASH           BIN SEPARATOR.
         MVC   18(1,GR1),1(GR3)        BIN NO. TO MESSAGE.
         OI    18(GR1),X'F0'           SET ZONE BITS.
         BAL   GR14,CONV2321           CONVERT SUB TO MAIN UCB ADDRESS.
         LR    GR10,GR1                MESSAGE AREA ADDRESS.
         LA    GR1,2(GR1)              RESET 'MOVE TO' POINTER.
         B     SETNAME                 COMPLETE THE MESSAGE.
         SPACE
CONV2321 LH    GR10,0(GR6)             PICK UP BIN NO.
         SLA   GR10,4                  TIMES SIXTEEN.
         LA    GR10,DATACELL-UCBOB(GR10) ADD LENGTH OF MAIN.
         LCR   GR10,GR10               NEGATE.
         AR    GR6,GR10                ADDRESS OF MAIN.
         BR    GR14                    RETURN.
.X301214 ANOP                                                   XL03912
         EJECT
WRITE    EQU   *
         LR    GR1,GR10                MESSAGE ADDRESS.
         WTO   ,MF=(E,(1)),ROUTCDE=(4),DESC=(4)                    MC0I
         LR    GR1,GR10                RESTORE ADDRESS OF GETMAIN AREA.
         FREEMAIN R,LV=0+WSIZE,A=(1),SP=229                     YL02912
         BR    GR8                     RETURN.
.SS26    ANOP                                                  @Y30LB35
         EJECT                                                 @Y30LB35
*************************************************************  @Y30LB35
*                                                              @Y30LB35
*  REMOVE UCB VOLUME SER NO, VTOC TTR0 AND SET NOT-READY BIT   @Y30LB35
*                                                              @Y30LB35
*************************************************************  @Y30LB35
CLEARUCB DS    0H                                              @Y30LB35
         L     GR4,OLDVOL              PICK UP PTR FOR OLD SER @Y30LB35
         L     GR6,UCBPTR              PICK UP PTR TO UCB      @Y30LB35
         SLL   GR6,16                  MOVE HIGH ORDER BYTES   @Y30LB35
         SRL   GR6,16                  MOVE BACK WITH ZEROS    @Y30LB35
         MODESET EXTKEY=SUPR                                   @Y30LB35
         MVC   0(L6,GR4),SRTEVOLI      SAVE OLD SER FOR CALLER @Y30LB35
         MVC   OLDTTR(L4,GR4),SRTEFSCT SAVE TTR PTR            @Y30LB35
         XC    SRTEFSCT(L4),SRTEFSCT   CLEAR TTR PTR           @Y30LB35
         XC    SRTEVOLI(L6),SRTEVOLI   CLEAR VOL SER           @Y30LB35
         OI    UCBFL2,NOTREADY         TURN ON NOT READY BIT   @Y30LB35
         MODESET EXTKEY=DATAMGT                                @Y30LB35
         B     PRETURN                 BRANCH TO THE EXIT      @Y30LB35
         SPACE 2
UCBLAST  DC    X'FFFF'                 LAST ENTRY IN UCB POINTER TABLE.
MAINT2   DC    10F'0'                  MAINTENANCE AREA        @Y30LB35
         AIF   ('&SS1' EQ 'SS1').SS261 OMIT DASDR CODE         @Y30LB35
OFFMSG   DC    C',NOW OFFLINE'         2321 MESSAGE COMPLETION.
         SPACE 2
WTOLIST  DC    C'IEH809I   '           MESSAGE IDENTIFIER.
         EJECT
MAINT2   DS    10F                     MAINTENANCE AREA        @Y30LB35
         SPACE
TIOT     DSECT
TIOCNJOB DS    CL8                     JOB NAME
TIOCSTEP DS    CL16                    STEP OR PROCEDURE NAME.
.SS261   ANOP                                                  @Y30LB35
         EJECT
***********************************************************************
*                                                                     *
*   PARAMETER LIST DSECT                                              *
*                                                                     *
***********************************************************************
         SPACE 2
PARMLIST DSECT                                                  YL02912
FUNCTION DS    0F                       CALLER'S FUNCTION.      YL02912
UCBPTR   DS    F                        UCB POINTER.            YL02912
DCBPTR   DS    F                        DCB POINTER OR          YL02912
CCHHPARM EQU   DCBPTR                   ADDR OF TRACK OR        YL02912
SERPTR   EQU   DCBPTR                   ADDR OF VOL SER.        YL02912
ALTPTR   DS    F                        ADDR OF ALTINFO OR      YL02912
NEWTTR   EQU   ALTPTR                   ADDR OF NEW TTR0       @Y30LB35
NEWCCHH  EQU   ALTPTR                   NEW TRACK ADDRESS.      YL02912
DEBPTR   DS    F                        DEB POINTER OR          YL02912
DEVMODP  EQU   DEBPTR                   ADDR OF 3340 MODEL NO.  YL02912
OLDVOL   EQU   DEBPTR                   ADDR AREA STORE OLD VOL@Y30LB35
OLDTTR   EQU   6                        PLACMENT OF OLD TTR VAL@Y30LB35
PRMPTR   DS    F                        ADDR OF CALLER'S LIST.  YL02912
KEY      EQU   PRMPTR                   KEY OF CALLER.          YL02912
VTOCCCHH DS    2F                      VTOC PTR.                 YM1129
IGCRESV  DS    2F                      RESERVED.                 YM1129
ENDLST   EQU   *                        PARAMETER LIST END.    @Y30LB35
         SPACE 2
         EJECT
UCB      DSECT                                                 @Y30LB35
         IEFUCBOB                                              @Y30LB35
         EJECT
CVT      DSECT                                                 @Y30LB35
         CVT   SYS=MIN                                         @Y30LB35
         EJECT                                                 @Y30LB35
         IKJTCB LIST=YES                                       @Y30LB35
         EJECT                                                 @Y30LB35
         IHARB  DSECT=YES                                      @Y30LB35
         END
