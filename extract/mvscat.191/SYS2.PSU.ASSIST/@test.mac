//...JOBCARD...
//* TEST THE ASSIST INTERPRETER
//*
//* GIVE A VALID JOBCARD
//* CHANGE ALL "THISPDS" TO THIS PDS NAME
//* CHANGE ALL "MYLOADLIB" TO THE LOADLIBRARY WHERE ASSIST LIVES
//ASACG PROC
//DATA EXEC PGM=ASSIST,PARM='BATCH,MACRO=H'
//STEPLIB  DD DISP=SHR,DSN=MYLOADLIB               <== CHANGE
//SYSPRINT DD  SYSOUT=*
//FT05F001 DD DDNAME=INPUT    (ONLY NEEDED IF &$DATARD=1: 2 READERS)
//FT06F001 DD SYSOUT=*,DCB=(RECFM=FA,LRECL=133,BLKSIZE=133)  PRINTER
//FT07F001 DD SYSOUT=B,DCB=(RECFM=F,LRECL=80,BLKSIZE=80)      PUNCH
//FT08F001 DD UNIT=SYSDA,DISP=(,DELETE),SPACE=(3520,(100,10)),
//             DCB=(RECFM=F,BLKSIZE=3520)      DISK INTERMEDIATE
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR             ** MVS MACROS
//         DD DSN=THISPDS,DISP=SHR                 <== CHANGE
//  PEND
//*
//TESTPROG EXEC ASACG
//INPUT DD *
//SYSIN DD *
$JOB   ASSIST  TESTCVB
         MACRO
         REGS  &SYM
         LCLA  &N
         LCLC  &R
&R       SETC  'R'
         AIF   ('&SYM' EQ '').NEXT
&R       SETC  '&SYM'
.NEXT    ANOP
&N       SETA  0
****
*
*     MACRO TO ESTABLISH REGISTER EQUATES
*
****
         SPACE 2
.LOOP    ANOP
&R&N     EQU   &N
&N       SETA  &N+1
         AIF   (&N LT 16).LOOP
         MEND
         REGS
TESTCVB  CSECT
         USING *,15
READLOOP XREAD CARD
         BNZ   EOF
*
* DERIVE TERMID FROM NETNAME
*
         LA    R8,NETNAME          ADDRESS NETNAME FIELD
         LH    R6,NETNAMLN         PICK UP NETNAME LENGTH
         LA    R7,4                SET LENGTH FOR COMPARE
         CR    R6,R7               NETNAME LONGER THAN 4 CHARS?
         BNH   NETNAMSL            ...NO, TAKE FIRST N CHARS
NETSCAN1 DS    0H
* SCAN TO FIND LAST 4 NON-BLANK CHARS
         BCTR  R6,R0               DECREMENT FOR NEXT CHAR
         LA    R7,0(R6,R8)         ADDRESS NEXT CHAR
         CLI   0(R7),C' '          IS IT BLANK?
         BE    NETSCAN1            ...YES, TRY NEXT CHARACTER
         LA    R7,3                SET LENGTH FOR SUBTRACT
         AR    R8,R6               ADDRESS END OF NETNAME
         SR    R8,R7               ADDRESS LAST 4 NON-BLANK CHARS
         LA    R6,4                SET LENGTH OF 4 FOR MOVE
NETNAMSL DS    0H
* MOVE DERIVED TERMID TO RETURN FIELD. R8==>START ADDR, R6=LENGTH
         BCTR  R6,R0               SET LENGTH FOR EXECUTE
         EX    R6,TERMIDMV         SELECT TERMID
*
* HANDLE OTHER CASES
*
         MVC   PORTNUM(2),TRMIDSEL+2 GET LAST 2 CHARS OF NET NAME
         CLC   =C'ASC',NETNAME     ASCII (7171) TERMINAL?
         BE    ASCTERM
         CLC   =C'T',NETNAME       REMOTE (SNA) TERMINAL?
         BE    GMURMT
         CLC   =C'WRLC',NETNAME    EXTERNAL (WRLC) TERMINAL?
         BE    WRLCTERM
         B     TERMDONE
*
TERMIDMV MVC   TRMIDSEL(0),0(R8)   EXECUTED MOVE FOR TERMID SELECTION
*
*    ASCNNN
ASCTERM  DS    0H
         MVC   TRMIDSEL(1),=C'A'   MAKE 1ST CHAR OF ASCII TERMS "A"
         B     TERMDONE
*    WRLCNNNN
WRLCTERM DS    0H
         MVC   TRMIDSEL(1),=C'W'   MAKE 1ST CHAR OF WRLC TERMS "W"
         B     TERMDONE
*    GMU REMOTE SNA TERMINALS
GMURMT   DS    0H
         MVC   LINENUM,NETNAME+1
         MVC   CTRLNUM,NETNAME+3
         CLC   PORT0,LINENUM       2 DIGIT LINE NUMBER?
         BH    TERMDONE
         CLC   PORT0,CTRLNUM       2 DIGIT CNTRL NUMBER?
         BH    TERMDONE
         CLC   PORT0,PORTNUM       2 DIGIT PORT NUMBER?
         BH    TERMDONE
         MVC   TRMIDSEL(1),=C'T'
         MVC   DECNNUM,LINENUM
         BAL   R12,CNVRT
         MVC   TRMIDSEL+1(1),0(R7) EXTRACT BASE36 CHARACTER
         MVC   DECNNUM,CTRLNUM
         BAL   R12,CNVRT
         MVC   TRMIDSEL+2(1),0(R7) EXTRACT BASE36 CHARACTER
         MVC   DECNNUM,PORTNUM
         BAL   R12,CNVRT
         MVC   TRMIDSEL+3(1),0(R7) EXTRACT BASE36 CHARACTER
         B     TERMDONE
*    SUBROUTINE TO CONVERT 2 DIGIT VALUE TO BINARY VALUE
CNVRT    DS    0H
         PACK  PACKPORT,DECNNUM  UNZONE (PACK) PORT NUMBER
         CVB   R8,PACKPORT       PUT BINARY VALUE IN R6
         LA    R7,BASE36         POINT TO BASE36 TABLE
         AR    R7,R8
         BR    R12
*
BASE36M  DS    0H
         DC    CL1'$'
BASE36   DC    CL10'0123456789'
         DC    CL10'ABCDEFGHIJ'
         DC    CL10'KLMNOPQRST'
         DC    CL6'UVWXYZ'
PORT0    DC    CL2'00'
*
*    TERMINAL ID CREATION COMPLETED  ...
*
TERMDONE DS    0H
         XPRNT CARD-1,81
         B     READLOOP
EOF      BR    14
         DC    CL1' '
CARD     DS    0CL80
NETNAME  DS    CL8                 START OF NETNAME
         DS    CL4
LINENUM  DS    CL2'00'           HOLDS LAST 2 CHARS OF NETNAME
         DS    CL4
CTRLNUM  DS    CL2'00'           HOLDS LAST 2 CHARS OF NETNAME
         DS    CL4
PORTNUM  DS    CL2'00'           HOLDS LAST 2 CHARS OF NETNAME
         DS    CL4
DECNNUM  DS    CL2'00'           HOLDS LAST 2 CHARS OF NETNAME
         DS    CL4
TRMIDSEL DS    CL4                 TERMINAL ID SELECTED
         DC    CL80' '
PACKPORT DS    D                 HOLD'S PACKED VERSION OF PORTNUM
NETNAMLN DC    XL2'07'             LENGTH OF NETNAME
         END
$ENTRY
T030101
T000000
T010100
T040122
T020207
T040115
T050505
T252423
ASC858
L745
WRLCGOR
COSMICX
TABCDEF
