 TITLE   'IGG019V4 - 1419 AND 1275 CONTROL ROUTINE'
IGG019V4 CSECT
*
*
*STATUS  CHANGE LVL 000,  VERSION LVL 000,  MODIFICATION LVL 000
*
*        RELEASE 04.0 CHANGES                                  @ZA07621
*C933000                                                       @ZA07621
*                                                              @ZA07621
*FUNCTION - DISENGAGE AND POCKET LITE ROUTINES FOR 1419 AND 1275.
*        1- IF DISENGAGE IS REQUESTED THE ROUTINE SETS FLAGS IN THE
*           DCB SO THE PCI APPENDAGE CAN DISENGAGE THE DEVICE.
*        2- IF LITE IS REQUESTED THE ROUTINE:
*           A- DETERMINES IF THE DEVICE IS DISENGAGED
*           B- CHECKS THE TWO BYTE POCKET LITE BUCKET AREA TO
*              DETERMINE WHICH LIGHTS TO TURN ON
*           C- STARTS THE CHANNEL PROGAM TO TURN THE LIGHTS ON.
*           D- CHECKS TO SEE THE CCW STRING EXECUTED SUCCESSFULLY.
*           E- IF AN ERROR OCCURRED BRANCH TO THE USER'S SYNAD
*              ROUTINE.  (IF NO SYNAD IS AVAILABLE AN ABEND IS ISSUED)
*ENTRY POINT - IGG019V4 FROM THE MACRO EXPANSION.
*              AN EXAMPLE OF THE EXPANSION IS:
*                BAL 1,*+12          LOAD ADDRESS OF PARAMETER LIST
*                DC  A(DCB)          DCB ADDRESS
*                DC  A(LITE-BUCKET)  LITE-BUCKET ADDRESS
*                L  14,0(1,0)        LOAD DCB ADDRESS
*                L  15,84(14,0)      LOAD ROUTINE ADDRESS
*                BALR 14,15 FOR LITE (BAL 14,4(15) FOR DESENGAGE)
*
*INPUT - DCB
*         DCBMRIND - X'08' CONTROL ROUTINE ENTERED BIT SET ON
*         DCBIOBAD - PCU IOB ADDRESS
*         DCBSYNAD - SYNAD ROUTINE ADDRESS
*        SCU IOB
*         16(SCUIOBR) - SCU CCW STRING ADDRESS
*         4(SCUIOBR) - SCU ECB ADDRESS
*        POCKET LITE BUCKET-
*         TWO BYTES SUPPLIED BY USER.  (CONTAINS BITS INDICATING
*         WHICH LITES ARE TO BE TURNED ON)
*
*OUTPUT -DCB
*         DCBMRFLG-X'30' - DISENGAGE REQUESTED BY USER
*        POCKET LITE BUCKET-
*         BYTE 2, BIT 7 IS SET ON IF THE DEVICE IS NOT DISENGAGED
*         WHEN THE USER REQUESTS POCKET LIGHTS TO BE TURNED OR
*         IF AN ERROR OCCURRED IN THE CCW STRING TO TURN ON THE
*         LIGHTS.
*        RETURN CODES
*         X'00' - NORMAL COMPLETION-NO I/O ERRORS
*         X'04' - I/O ERROR-SYNAD RETURNED TO CHECK
*
*        LITE CCW STRING
*         THE COMMAND CODE CORRESPONDING TO THE POCKET LITE TO
*         BE TURNED ON IS OUTPUT FROM THE ROUTINE
*
*EXTERNAL ROUTINES - EXCP (SVC 00)
*                    WAIT (SVC 01)
*                    EOV (SVC 55)
*                    USER'S SYNAD ROUTINE
*
*EXITS NORMAL - RETURN TO USER VIA REGISTER 14
*
*EXITS ERROR -  ABEND DUMP IF NO SYNAD ROUTINE IS SPECIFIED
*
*TABLES/WORK AREAS-- CALLERS SAVE AREA
*                  - POCKET LITE BUCKET (TWO BYTES)
*
*ATTRIBUTES - REENTERABLE
*
*NOTES -  NONE
*
*
*
*
*              SYMBOLIC DEFINITIONS OF REGISTERS
*
ZEROR    EQU   0
REG1     EQU   1                       WORK REGISTER
PARMR    EQU   1
DCBR     EQU   2                       DCB ADDRESS
BCKETR   EQU   3                       POCKET LITE BUCKET ADDRESS
BASER    EQU   4                       BASE REGISTER
PCKTR    EQU   5                       CONTAINS POCKET COMMAND CODE
EVENR    EQU   6                       REGS 6 & 7 ARE USED IN DETER-
ODDR     EQU   7                       MINING WHICH POCKET LITE
SAVER    EQU   13                      ADDRESS OF SAVE AREA
RETURNR  EQU   14                      RETURN ADDRESS
SCUIOBR  EQU   14                      SCU IOB ADDRESS
REG15    EQU   15                      WORK REGISTER
ENTRYR   EQU   15                      ENTRY POINT ADDR AND RETRUN CODE
*
*              SYMBOLIC DEFINITIONS FOR DISPLACEMENTS
*
BCKTADDR EQU   4                       DISP TO PCKT LITE BCKT ADDR
BCKTBYT1 EQU   1                       DISP TO PCKT LITE BYTE 1
CCWADDR  EQU   24                      DISP TO CCW ADDR IN IOB
DISP12   EQU   12                      DISP TO REG 14 SAVE AREA
DISP20   EQU   20
DISP28   EQU   28
DISP56   EQU   56
IOBDISP  EQU   8                       DISP TO IOB FROM IOB PREFIX
IOBECB   EQU   4                       DISP TO ECB IN IOB
IOBECBAD EQU   12                      DISP TO ECB ADDR IN IOB
LITECC   EQU   8                       DISP TO CCW FOR LITE COMMAND CDE
LITECCW  EQU   136                     DISP FROM PCU CCW TO LITE CCW
RTNCDSP  EQU   16                      DISP TO STORE RETURN CODE
SCUIOB   EQU   48                      DISP FROM PCU IOB TO SCU IOB
UNITEXCP EQU   20                      DISP TO CSW IN IOB
ZEROD    EQU   0                       ZERO DISPLACEMENT
*
*              MISC DEFINITIONS
*
BITSAB   EQU   2                       SHIFT TO PROC BITS A & B LAST
DCBALTER EQU   X'10'                   DCB ALTERED--SET ON FOR CLOSE
ERBITOFF EQU   X'FE'                   SET ERROR BIT OFF
ERBITON  EQU   X'01'                   SET ERROR BIT ON
EXCPISUD EQU   X'20'
FOUR     EQU   4
FRST6BTS EQU   X'04'                   FIRST 6 LITES PROCESSED
INITEXCP EQU   X'CF'
LASTEXCP EQU   X'10'                   LAST EXCP INDICATOR
LFTJSTFY EQU   24                      SHIFT TO LEFT JUSTIFY
LST6BTS  EQU   6                       SHIFT FOR LAST SIX BITS
NOSYNAD  EQU   X'01'                   SYNAD ROUTINE PRESENT
SETLITE  EQU   X'F7'                   SET CC TO LITE COMMAND CODE
SETOFF   EQU   X'FE'
SETON    EQU   X'0C'                   CONTROL RTN-LITES PRCSD BITS
SHFTOFF  EQU   4                       SHIFT OFF LOW-ORDER BITS
SHIFT12  EQU   12                      SHIFT ABEND COMPLETION CODE
TESTBIT  EQU   1                       TEST THIS BIT
THREE    EQU   3
USERDISN EQU   X'20'                   USER DISENGAGE BIT
X7FPSTD  EQU   X'7F'                   EVENT POSTED-SUCCESSFUL COMPLETE
*
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
* THIS BRANCH IS TAKEN WHEN LITE IS SPECIFIED IN THE MACRO EXPANSION  *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         USING *,ENTRYR                ESTABLISH BASE REGISTER
         B     CNTL010                 BRANCH TO LITE ROUTINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
* THE DISENGAGE ROUTINE SETS BITS IN THE DCBMRFLG SO THE PCI APPENDAGE*
* CAN ENGAGE THE DEVICE                                               *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         USING IHADCB,PARMR            ESTABLISH BASE REGISTER FOR DCB
         OI    DCBMRFLG,USERDISN       SET ON DISENGAGE BIT (USER)
         BR    RETURNR                 RETURN CONTROL TO USER
*
CNTL010  EQU   *
*
         DROP  ENTRYR
         DROP  PARMR
         USING IHADCB,DCBR
         USING IGG019V4,BASER
         ST    RETURNR,DISP12(SAVER)   SAVE RETURN ADDR
         STM   DCBR,ODDR,DISP28(SAVER)   SAVE USER REGS
         LR    BASER,ENTRYR            ESTABLISH BASE REGISTER
         L     DCBR,ZEROD(PARMR)       ESTABLISH BASE REGISTER FOR DCB
         L     BCKETR,BCKTADDR(PARMR)  PICK UP POCKET LITE BUCKET ADDR
         NI    BCKTBYT1(BCKETR),ERBITOFF   SET POCKET ERROR BIT TO ZERO
         NI    DCBMRFG,INITEXCP        INITIALIZE EXCP INDICATOR BITS
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
* IF THE DEVICE HAS BEEN DISENGAGED EACH BIT CORRESPONDING TO A POCKET*
* IS TESTED TO DETERMINE IF THE POCKET LIGHT IS TO BE TURNED ON       *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         OI    DCBMRIND,SETON          SET CNTRL RTN-LITE PRCSD BTS ON
         LH    PCKTR,PCKTCC            LOAD REG WITH PCKT COMMAND CODE
         SR    EVENR,EVENR             ZERO REGISTER
         SR    ODDR,ODDR               ZERO REGISTER
         IC    ODDR,ZEROD(BCKETR)      PICK UP 1ST BYTE OF LITE BUCKET
         IC    EVENR,BCKTBYT1(BCKETR)  PICK UP 2ND BYTE OF LITE BUCKET
         SRL   EVENR,SHFTOFF           SHIFT OFF LOW ORDER BITS-BYTE 2
         SLL   ODDR,LFTJSTFY           LEFT-JUSTIFY BYTE 1
         SLDL  EVENR,BITSAB            PUT POCKET LITE BITS A&B IN EVEN
*
CNTL020  EQU   *
*
         LTR   ODDR,ODDR               IS THIS BIT ONE (ON)
         BM    CNTL060                 YES, BRANCH TO TURN ON LIGHT
*
CNTL030  EQU   *
*
         TM    DCBMRIND,FRST6BTS       FIRST 6 LITE BITS PROCESSED
         BO    CNTL040                 NO, BRANCH
         L     REG1,SCNDTST            LOAD TEST REG FOR LAST 6 BITS
         B     CNTL050                 BRANCH TO SHIFT FOR NEXT BIT
*
CNTL040  EQU   *
*
         LH    REG1,FIRSTST            LOAD TEST REG FOR FIRST 6 BITS
*
CNTL050  EQU   *
*
         TM    DCBMRFG,EXCPISUD        EXCP BEEN ISSUED
         BZ    CNTL051                 NO, BRANCH TO SHIFT NEXT BIT
         XI    DCBMRFG,EXCPISUD        YES, SET OFF INDICATOR
         B     CNTL052                 SKIP THE SHIFT
*
CNTL051  EQU   *
*
         SLL   ODDR,TESTBIT            SHIFT TO TEST THE NEXT BIT
*
CNTL052  EQU   *
*
         AH    PCKTR,NXTPCKT           UPDATE COMMAND CODE REGISTER
         CR    PCKTR,REG1              FIRST 6 BITS TESTED
         BNE   CNTL020                 NO, BRANCH TO TEST ANOTHER ONE
         CL    REG1,SCNDTST            HAVE ALL LIGHT BITS BEEN PROCD
         BE    CNTL080                 YES, BRANCH TO EXIT THIS ROUTINE
         XI    DCBMRIND,FRST6BTS       SET OFF FIRST 6 BITS INDICATOR
         SRDL  EVENR,LST6BTS           LAST 6 BITS IN ODD REG TO PROC
         B     CNTL020                 BRANCH TO TEST LAST 6 BITS
*
CNTL060  EQU   *
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
* THE FOLLOWING INSTRUCTIONS CAUSE THE LIGHTS TO BE TURNED ON         *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         XI    DCBMRFG,EXCPISUD        SET ON EXCP ISSUED BIT
         SLL   ODDR,TESTBIT            SHIFT TO TEST THE NEXT BIT
         LTR   ODDR,ODDR               ANY MORE POCKET LITES TO LITE
         BC    7,CNTL061               YES, BRANCH
         LTR   EVENR,EVENR             ANY MORE POCKET LITES TO LITE
         BC    7,CNTL061               YES, BRANCH
         XI    DCBMRFG,LASTEXCP        NO, SET LAST EXCP INDICATOR
*
CNTL061  EQU   *
*
         L     SCUIOBR,DCBIOBAD        PICK UP PCU IOB PREFIX ADDRESS
         L     REG15,CCWADDR(SCUIOBR)  PICK UP PCU CCW STRING ADDR
         LA    REG15,LITECCW(REG15)    COMPUTE LITE CCW STRING ADDR
         LA    SCUIOBR,SCUIOB(SCUIOBR) COMPUTE SCU IOB PREFIX ADDRESS
         ST    REG15,CCWADDR(SCUIOBR)  PUT CCW ADDR IN SCU IOB
         STC   PCKTR,LITECC(REG15)     PUT COMMAND CODE IN CCW STRING
         NI    LITECC(REG15),SETLITE   SET OFF BATCH NUMBER UPDATE BIT
         XC    IOBECB(FOUR,SCUIOBR),IOBECB(SCUIOBR)  ZERO SCU IOB ECB
         LA    PARMR,IOBDISP(SCUIOBR)  COMPUTE SCU IOB ADDRESS
         EXCP  (1)
         L     SCUIOBR,DCBIOBAD        PICK UP PCU IOB ADDRESS
         LA    SCUIOBR,SCUIOB(SCUIOBR) COMPUTE SCU IOB ADDRESS
         L     PARMR,IOBECBAD(SCUIOBR) LOAD ECB ADDR
         WAIT  1,ECB=(1)
         L     SCUIOBR,DCBIOBAD        LOAD SCUIOB PREFIX ADDR
         LA    SCUIOBR,SCUIOB(SCUIOBR)   COMPUTE SCU IOB ADDR
         CLI   IOBECB(SCUIOBR),X7FPSTD EVENT SUCCESSFULLY COMPLETED
         BE    CNTL030                 YES, TEST THE NEXT BIT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
* IF AN ERROR OCCURS IN THE I/O OPERATION TO CAUSE THE LIGHT TO BE    *
* TURNED ON THE USER'S SYNAD ROUTINE IS BRANCHED TO.  IF THERE IS     *
* NO SYNAD ROUTINE THE JOB IS TERMINATED VIA EOV (SVC 55).            *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         OI    BCKTBYT1(BCKETR),ERBITON    NO, SET ERROR BIT ON
         TM    DCBSYNAD+THREE,NOSYNAD  SYNAD ROUTINE PRESENT
         BZ    CNTL070                 YES, BRANCH
         LA    ZEROR,NOSYNAD           SET UP REG FOR ABEND - NO SYNAD
         SLL   ZEROR,SHIFT12           SHIFT ABEND CODE 12 BITS FOR EOV
         NI    UNITEXCP(SCUIOBR),SETOFF   SET OFF UNIT EXCEPTION BIT
         EOV   (2)
*
CNTL070  EQU   *
*
         OI    DCBMRIND,DCBALTER       SET BIT ON TO SHOW DCB ALTERED
         ST    SCUIOBR,DCBIOBAD        STORE SCUIOB PREFIX ADDR IN DCB
         LR    PARMR,DCBR              SET UP PARAMETERS
         SR    ZEROR,ZEROR             FOR SYNAD
         L     REG15,CNTRLBIT          LOAD CONTROL BIT FOR REG 1
         LA    PARMR,ZEROD(PARMR)      CLEAR HIGH-ORDER BYTE
         AR    PARMR,REG15             SET ON CONTROL BIT FOR SYNAD
         L     ENTRYR,DCBSYNAD         PICK UP SYNAD ADDRESS
         STM   DCBR,BCKETR,DISP20(SAVER)   SAVE REGS 2-3
         STM   BASER,ODDR,DISP56(SAVER)    SAVE REGS 4-7
         LM    DCBR,ODDR,DISP28(SAVER)     LOAD USER REGS
         BALR  RETURNR,ENTRYR          BRANC TO SYNAD ROUTINE
         STM   DCBR,ODDR,DISP28(SAVER)     SAVE USER REGS
         LM    DCBR,BCKETR,DISP20(SAVER)   LOAD REGS 2-3
         LM    BASER,ODDR,DISP56(SAVER)    LOAD REGS 4-7
         NI    BCKTBYT1(BCKETR),ERBITOFF   SET POCKET ERROR BIT TO ZERO
         LA    REG1,SCUIOB             LOAD DISP FROM PCU TO SCU IOB
         L     SCUIOBR,DCBIOBAD        LOAD SCU IOB PREFIX ADDR
         SR    SCUIOBR,REG1            COMPUTE PCU IOB PREFIX ADDR
         ST    SCUIOBR,DCBIOBAD        RESTORE DCB PCU IOB PREFIX ADDR
         XI    DCBMRIND,DCBALTER       SET BIT OFF TO SHOW DCB RESTORED
         LA    REG15,FOUR              LOAD RETURN CODE
         ST    REG15,RTNCDSP(SAVER)    SET RETURN CODE
         B     CNTL030                 BRANCH TO TEST THE NEXT BIT
*
CNTL080  EQU   *
*
         LA    ENTRYR,FOUR             LOAD I/O ERROR RETURN CODE
         CL    ENTRYR,RTNCDSP(SAVER)   RETURN CODE POSTED
         BE    CNTL090                 YES, BRANCH
         SR    ENTRYR,ENTRYR
         ST    ENTRYR,RTNCDSP(SAVER)   SET RETURN CODE
*
CNTL090  EQU   *
*
         LM    RETURNR,ODDR,DISP12(SAVER)   LOAD USER REGS
         BR    RETURNR                 RETURN TO USER
         DS    0F
CNTRLBIT DC    X'20000000'             SET THIS BIT IN REG 1 FOR SYNAD
SCNDTST  DC    XL4'000000CF'           USED TO FIND LAST BIT TO PROC
FIRSTST  DC    XL2'006F'               FIRST 6 BITS PROCESSED
PCKTCC   DC    XL2'000F'               INITIAL POCKET COMMAND CODE
NXTPCKT  DC    XL2'0010'               NEXT POCKET LITE COMMAND CODE
         DC    C'IGG019V4 ZA07621 02/20/76/'  SIGHT INDICATOR  @ZA07621
         DC    CL8'&SYSDATE'                  SIGHT INDICATOR  @ZA07621
         DC    C'Z07621'                      SIGHT INDICATOR  @ZA07621
PATCH    DC    XL30'00',D'0'                  PATCH AREA       @ZA07621
         DCBD  DSORG=BS,DEVD=MR
         END
