         TITLE 'IGG0201D - 1419 CLOSE EXECUTOR'
IGG0201D CSECT
         SPACE
***********************************************************************
*                                                                     *
* MODULE NAME - IGG0201D                                              *
*                                                                     *
* DESCRIPTIVE NAME - 1419 CLOSE EXECUTOR                              *
*                                                                     *
* COPYRIGHT - NONE                                                    *
*                                                                     *
* CHANGE ACTIVITY - AS FOLLOWS:                                       *
*                                                                     *
*          RELEASE 20 DELETIONS                                       *
*1022245000,246000,424000,427000-429000                          A48612
*A245000-246000,427000-429000                                    A33928
*D246000,428000                                                  A33928
*                                                                     *
*          VS1 RELEASE 2.6 DELETIONS                                  *
*D316000-324000                                                 SA60237
*D312000                                                        SA60756
*A366100-367700                        SA76228,YA09416,XA09359 @ZA07021
*A416600-417200                        SA76228,YA09416,XA09359 @ZA07021
*                                                              @ZA07021
*                                                               SA62501
*                                                                     *
*          VS2 RELEASE 02 DELETIONS                                   *
*D261320-261440,570000,574000,652000,658000-666000,678000-680000,Y02072
*D728000                                                         Y02072
*          VS2 RELEASE 03.6 CHANGES XA09359,YA09416,SA7228     @ZA07021
*A366100-367700                                                @ZA07021
*A416600-417200                                                @ZA07021
*          VS2 RELEASE 04.0 CHANGES
*C656040                                                       @ZA07621
*
*                                                                     *
*                                                                     *
* STATUS - VS2 RELEASE 2 LEVEL 0                                      *
*                                                                     *
*                                                                     *
* FUNCTION/OPERATION - THIS ROUTINE IS THE CLOSE EXECUTOR FOR 1419    *
*                   DATA SETS.  CLOSE FUNCTIONS WILL BE PERFORMED  ON *
*                   THOSE DCBS WHOSE DCBDSORG FIELDS SPECIFY PS AND   *
*                   WHOSE DCBDEVT FIELDS SPECIFIES MR.  FOR THOSE     *
*                   DCB'S THE FOLLOWING FUNCTIONS WILL BE PERFORMED:  *
*                                                                     *
*                   1. THE AEQ, RB, AND LOGICAL CHANNEL QUEUES ARE    *
*                      PURGED FOR BOTH THE PCU AND SCU.               *
*                                                                     *
*                   2. STORAGE FOR THE FOLLOWING ITEMS IS FREED FOR   *
*                      EACH DCB:                                      *
*                        A. CHANNEL PROGRAMS FOR THE PCU AND SCU      *
*                        B. BUFFERS FOR THE WRITE DELAY COMMAND       *
*                        C. IOB FOR THE PCU                           *
*                        D. IOB FOR THE SCU                           *
*                        E. MICB TABLE                                *
*                        F. REGISTER SAVE AREA                        *
*                        G. TRACE TABLE                               *
*                        H. ECB LIST                                  *
*                        I. BUFFER POOL IF OPEN ACQUIRED BUFFERS      *
*                                                                     *
*                   3. THE FOLLOWING VECTORS IN THE DCB ARE RESTORED  *
*                      TO THEIR PRE-OPEN VALUES:                      *
*                        A. DCBSSID  - USER STACKER SELECT NAME       *
*                        B. DCBIOBAD - ADDRESS OF IOB                 *
*                        C. DCBCHECK - ADDRESS OF CHECK MODULE        *
*                        D. DCBIOBA  - MICB READ ADDRESS              *
*                        E. DCBNCP   - NUMBER OF CHANNEL PROGRAMS     *
*                        F. DCBEOBR  - END-OF-BLOCK MODULE ADDRESS    *
*                        G. DCBCNTRL - CONTROL MODULE ADDRESS         *
*                        H. DCBMRIND - INDICATOR BYTE                 *
*                        I. DCBECBLT - ECB LIST ADDRESS               *
*                        J. DCBIOBL  - IOB LENGTH                     *
*                        K. DCBMRFLG,DCBAPPIN - INDICATOR BYTES       *
*                        L. DCBBUFCB,DCBBUFL  - IF OPEN ACQUIRED BFRS *
*                                                                     *
*                   4. THE USER'S DCB IS REFRESHED.                   *
*                                                                     *
*                   5. THE USER'S STACKER SELECT ROUTINE, LOADED BY   *
*                      OPEN, IS DELETED.                              *
*                                                                     *
*                                                                     *
* ENTRY POINT - ENTRY TO MODULE IS AT IGG0201D.  CONTROL IS RECEIVED  *
*               FROM IGG0200A OR IGG0200Z VIA AN XCTL.                *
*                                                                     *
*                                                                     *
* INPUT - REGISTER 5 - ADDRESS OF THE DCB PARAMETER LIST              *
*         REGISTER 6 - ADDRESS OF THE WHERE-TO-GO TABLE               *
*         REGISTER 7 - ADDR OF CURRENT ENTRY IN DCB PARAMETER LIST    *
*         REGISTER 8 - ADDR OF CURRENT ENTRY IN WHERE-TO-GO TABLE     *
*                                                                     *
*                                                                     *
* OUTPUT - THE USER'S DCB IS REFRESHED.                               *
*                                                                     *
*                                                                     *
* EXTERNAL ROUTINES - NONE                                            *
*                                                                     *
*                                                                     *
* EXIT-NORMAL - XCTL TO IGG0200B OR IGG0200G                          *
*                                                                     *
* EXIT-ERROR - NONE                                                   *
*                                                                     *
*                                                                     *
* TABLES/WORK AREAS - WHERE-TO-GO TABLE AND THE CLOSE WORK AREA       *
*                                                                     *
*                                                                     *
* MACROS-ACTION - MODESET, FREEMAIN, DELETE, PURGE, WAIT, IECRES, XCTL*
*                                                                     *
* MACROS-MAPPING - CVT, DCBD, IECDSECS                                *
*                                                                     *
*                                                                     *
* ATTRIBUTES - REENTRANT, REUSEABLE, NORMALLY RUNS IN DATA MANAGEMENT *
*              KEY.                                                   *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*
*********                GENERAL REGISTER USAGE               *********
*
R0       EQU   0                   WORK REGISTER                 Y02072
RPARM0   EQU   R0                  PARM LIST POINTER/WORK REGISTER
RPARM1   EQU   1                   PARM LIST POINTER/WORK REGISTER
RDCB     EQU   2                   ADDR OF DCB
RBASE    EQU   3                   BASE REGISTER FOR CSECT
RCORE    EQU   4                   ADDR OF CLOSE WORK AREA FOR DCB
RPARM    EQU   5                   POINTER TO START OF PARM LIST
RWTG     EQU   6                   POINTER TO START OF WTG TABLE
RPARMC   EQU   7                   POINTER TO CURRENT ENTRY IN PARMLIST
RWTGC    EQU   8                   POINTER TO CURRENT ENTRY IN WTG TAB
RIOB     EQU   9                   POINTER TO PCU IOB
RUCB     EQU   10                  UCB ADDRESS POINTER
RDEB     EQU   11                  POINTER TO DEB
RWRK1    EQU   12                  WORK REGISTER
RWRK2    EQU   13                  WORK REGISTER
R14      EQU   14                  WORK REGISTER                 Y02072
RPARME   EQU   R14                 PARM LIST POINTER/WORK REGISTER
RPARMF   EQU   15                  PARM LIST POINTER/WORK REGISTER
*
***********************************************************************
         SPACE 2
***********************************************************************
*
*********                     CONSTANT EQUATES                *********
*
LENTRACE EQU   136                 TRACE TABLE LENGTH            A48612
FULWRD   EQU   X'01'               BFALN MASK FOR FULL WORD      A48612
BUFCBD   EQU   6                   DISP. TO ADJUST BUFR LENGTH   A48612
SCUIOBIN EQU   X'10'               MASK TO TEST DCBMRIND FOR SCU IOB
PIOBLNGH EQU   48                  LENGTH OF PCU IOB
SIOBLNGH EQU   48                  LENGTH OF SCU IOB
PCHANPGM EQU   80                  LENGTH OF PCU CHANNEL PROGRAM
SCHANPGM EQU   72                  LENGTH OF SCU CHANNEL PROGRAM
MICBLNGH EQU   28                  LENGTH OF MICB TABLE ENTRY
WDBUFL   EQU   128                 LENGTH OF WRITE DELAY BUFFERS
ECBLSTLG EQU   64                  LENGTH OF ECB LIST
EXTONE   EQU   X'01'               NUMBER OF EXTENTS FOR DEB
IOBSTRT  EQU   24                  DISP TO ADDR OF PCU CP IN IOB
SAVEAR   EQU   72                  LENGTH OF REGISTER SAVE AREA
BUFACQIN EQU   X'08'               MASK TO TEST BUFFER ACQUISTION TECH.
BUFCBL   EQU   8                   NORMAL CONTROL BLOCK LENGTH   A48612
RSTRVAL  EQU   X'01'               RESTORE VALUE FOR DCB VECTORS
RSTRBAB  EQU   X'F7'               MASK TO SET OFF BUF ACQ. BIT
NULL     EQU   X'00'               NULL MASK
WGOFF    EQU   8                   LENGTH OF WTG TABLE ENTRY
PARMOFF  EQU   4                   LENGTH OF DCB ENTRY IN PARM LIST
WAOFF    EQU   32                  DISP TO IDTTR ENTRIES IN WTG TAB
PURGMSK  EQU   X'A0'               MASK TO SET PURGE OPTIONS
SPNUMBER EQU   250                 SUBPOOL NUMBER
SHIFTNO  EQU   24                  NUMBER BITS TO SHIFT SP NUMBER
UNCOND   EQU   15                  MASK FOR UNCONDITIONAL BRANCH
KZERO    EQU   0                   CONSTANT OF ZERO
DZERO    EQU   0                   DISP OF ZERO
KWTGWK   EQU   4                   DISP TO DCB WORK AREA IN WTG ADDR
DEBUCBL  EQU   4                   LENGTH OF UCB ADDR IN DEB
DEBSUCBA EQU   36                  DISP TO SECOND UCB ADDR IN DEB
DBBUFCBL EQU   3                   LENGTH OF DCBBUFCB FIELD IN DCB
KONE     EQU   1                   CONSTANT OF ONE
KTHREE   EQU   3                   CONSTANT OF THREE
DBBUFLL  EQU   2                   LENGTH OF DCBBUFL FIELD IN DCB
DBSSIDL  EQU   8                   LENGTH OF DCBSSID FIELD IN DCB
DBCHECKL EQU   3                   LENGTH OF DCBCHECK FIELD IN DCB
DBECBLTL EQU   4                   LENGTH OF DCBECBLT FIELD IN DCB
ROUTIDL  EQU   2                   LENGTH OF ROUTINE ID
EQUAL    EQU   8                   MASK FOR BRANCH EQUAL
LOW      EQU   4                   MASK FOR BRANCH LOW
HIGH     EQU   2                   MASK FOR BRANCH HIGH
NE       EQU   7                   MASK FOR BRANCH NOT EQUAL
DID      EQU   6                   DISP IN MOD NAME TO ID
PURGLST  EQU   12                  LENGTH OF PURGE PARM LIST
DLKFLD   EQU   8                   DISP TO LINK FIELD PURGE LIST
DECBPURG EQU   4                   DISP TO ECB PURGE LIST
ORGTTR   EQU   1000                DISP TO TTR TABLE FROM START OF MOD
         EJECT
         USING IHADCB,RDCB
         USING IGGD0100,RBASE
         USING DEBD0100,RDEB
         USING FORCORE,RCORE
         BALR  RBASE,KZERO         SET UP CSECT BASE
IGGD0100 EQU   *
         L     RDCB,DZERO(RPARMC)  LOAD ADDR OF DCB TO BE CLOSED
         L     RDEB,DCBDEBAD       LOAD ADDR OF RELATED DEB
         L     RCORE,KWTGWK(RWTGC) LOAD ADDR OF DCB WORK AREA
         SPACE 2
*
* THE FOLLOWING WILL TEST A BIT IN DCBMRIND TO DETERMINE IF THE ADDR
* IN DCBIOBAD POINTS TO THE PCU IOB OR TO THE SCU IOB.  IF IT POINTS
* TO THE SCU IOB, THE FIELD IS UPDATED TO POINT TO THE PCU IOB.
*
         L     RIOB,DCBIOBAD       LOAD IOB ADDRESS
         TM    DCBMRIND,SCUIOBIN   TEST IF ADDR FOR PCU OR SCU
         BZ    IGGD0200            PCU/GO PURGE QUEUES
         LA    RWRK1,PIOBLNGH      LOAD LENGHT OF PCU IOB
         LA    RIOB,KZERO(RIOB)    CLEAR HIGH ORDER BITS
         SR    RIOB,RWRK1          CALCULATE PCU IOB ADDR
         ST    RIOB,DCBIOBAD       UPDATE IOB ADDR IN DCB
         SPACE 2
*
* THE FOLLOWING WILL PURGE THE QUEUES FOR THE PCU AND SCU VIA
* THE INTERNAL SUBROUTINE PURG0000.
*
IGGD0200 EQU   *
         LA    RWRK2,PURG0000      LOAD ADDR OF PURGE SUBROUTINE
         BALR  RWRK1,RWRK2         GO PURGE PCU QUEUES
         EJECT
************************ FREE STORAGE ROUTINES ************************
*
* THE FOLLOWING ROUTINE WILL FREE STORAGE FOR THE FOLLOWING:
*        1. CHANNEL PROGRAMS FOR THE PCU AND SCU
*        2. BUFFERS FOR THE WRITE DELAY COMMAND
* FREEMAIN IS ISSUED IN THE INTERNAL SUBROUTINE FREE0000
*
         MODESET KEYADDR=DXUKEY,WORKREG=1  CHANGE TO USER'S KEY  Y02072
         LA    RWRK2,FREE0000      LOAD ADDR OF FREE0000 SUBROUTINE
         L     RPARM1,IOBSTRT(RIOB) LOAD ADDR OF START OF PCU CP
         LA    RPARME,PCHANPGM     LOAD LENGTH OF PCU CP
         LA    RPARME,SCHANPGM(RPARME)  ADD LENGTH OF SCU CP
         LA    RPARM0,WDBUFL(RPARME)    ADD LENGTH OF WD BUFFERS
         BALR  RWRK1,RWRK2         GO FREE CP'S AND WD BUFFERS
         SPACE 2
*
* THE FOLLOWING ROUTINES WILL FREE STORAGE FOR THE FOLLOWING:
*        1. IOB'S FOR PCU AND SCU
*        2. MICB TABLE
*        3. REGISTER SAVE AREA
*        4. TRACE TABLE
* FREEMAIN IS ISSUED IN THE INTERNAL SUBROUTINE FREE0000
*
         TM    DCBCIND2,DCBCNBFP  DID OPEN GET BUFFER POOL     @ZA07021
         BO    SKPFREE             DONT FREE BUFFS/POOL WILL BE@ZA07021
         TM    DCBBUFCB+3,DCBBIT7  HAS POOL BEEN FREED         @ZA07021
         BO    SKPFREE             YES DONT FREE BUFFERS       @ZA07021
*                                                              @ZA07021
         LR    RPARM1,RDCB         LOAD ADDR OF DCB            @ZA07021
         SR    RWRK1,RWRK1         ZERO WORK REGISTER          @ZA07021
         IC    RWRK1,DCBBUFNO      INSERT NUMBER OF BUFFERS    @ZA07021
         LTR   RWRK1,RWRK1         ALL BUFFERS FREE            @ZA07021
         BZ    SKPFREE             YES DONT FREE ANY MORE      @ZA07021
         LA    RUCB,PIOBLNGH+SIOBLNGH+LENTRACE+SAVEAR(RIOB)    @ZA09994
FRBUF    L     RPARMF,24(RUCB)    POINT TO BUFFER              @ZA09994
         FREEBUF  (1),(RPARMF)     PUT BUFFER BACK IN POOL     @ZA07021
         L     RUCB,0(,RUCB)     POINT TO NEXT BUFFER          @ZA09994
         BCT   RWRK1,FRBUF         GO FREE NEXT BUFFER         @ZA07021
SKPFREE  EQU   *                                               @ZA07021
         LR    RPARM1,RIOB         LOAD ADDR OF PCU IOB
         SR    RPARMF,RPARMF       ZERO WORK REGISTER
         IC    RPARMF,DCBBUFNO     INSERT NUMBER OF BUFFERS
         LA    RWRK1,MICBLNGH      LOAD LENGTH OF MICB TABLE ENTRY
         MR    RPARME,RWRK1        CALCULATE LENGTH MICB TABLE
         LA    RPARMF,PIOBLNGH(RPARMF)  ADD LENGTH OF PCU IOB
         LA    RPARMF,SIOBLNGH(RPARMF)  ADD LENGTH OF SCU IOB
         LA    RPARMF,LENTRACE(RPARMF)  ADD LEN. OF TRACE TABLE  A48612
         LA    RPARM0,SAVEAR(RPARMF)    ADD LENGTH OF REG SAVE AREA
         BALR  RWRK1,RWRK2         FREE IOBS,MICB TABLE,SAVE AREA
         SPACE 2
*
* THE FOLLOWING ROUTINE WILL FREE THE ECB LIST.  FREEMAIN IS ISSUED
* IN THE INTERNAL SUBROUTINE FREE0000.
*
         L     RPARM1,DCBECBLT     LOAD ADDR OF ECB LIST
         LA    RPARM0,ECBLSTLG     LOAD LENGTH
         BALR  RWRK1,RWRK2         GO FREE ECB LIST
         EJECT
************************ FREE BUFFER POOL ROUTINE *********************
*
* THE FOLLOWING WILL FREE THE BUFFER POOL IF OPEN ACQUIRED THE BUFFERS.
* IN ADDITION, SEVERAL VECTORS IN THE DCB, DCBBUFCB AND DCBBUFL, ARE
* RESTORED TO THEIR PRE-OPEN VALUES IF OPEN ACQUIRED THE BUFFERS.
*
         TM    DCBBUFCB+3,DCBBIT7  HAS BUFF POOL BEEN FREED    @ZA07021
         BO    IGGD0300            YES  GO CLEAR VECTOR FIELDS @ZA07021
         TM    DCBCIND2,BUFACQIN   DID OPEN ACQUIRE BUFFERS
         BZ    IGGD0300            NO/GO CLEAR VECTOR FIELDS
         TM    DCBBUFCB+3,DCBBIT7  HAS BUFFER POOL BEEN FREED  @ZA07021
         BO    IGGD0300            YES/GO CLEAR VECTOR FIELDS  @ZA07021
         L     RPARM1,DCBBUFCB     LOAD ADDRESS OF BUFFER CNTRL BLK
         SR    RPARMF,RPARMF       ZERO WORK REGISTER
         IC    RPARMF,DCBBUFNO     INSERT NUMBER OF BUFFERS
         LH    RWRK1,BUFCBD(RPARM1) LOAD ADJ. BUF. LEN.          A48612
         MR    RPARME,RWRK1        CALCULATE LENGTH BUFFER POOL
         TM    DCBBFALN,FULWRD     TEST FULL WORD NOT DOUBLE     A48612
         LA    RPARM0,BUFCBL(RPARMF) ASSUME USUAL ALIGNMENT      A48612
         BZ    BUFPOOL1            FULL WORD BIT IS OFF          A48612
         LA    RPARM0,BUFCBL+8(RPARMF)  EXTRA 8 FOR FULL WORD    A48612
BUFPOOL1 EQU   *                                                 A48612
         BALR  RWRK1,RWRK2         GO FREE BUFFER POOL
         MODESET EXTKEY=DATAMGT    RETURN TO DATA MANAGEMENT KEY Y02072
         XC    DCBBUFCB+KONE(DBBUFCBL),DCBBUFCB+KONE  CLEAR BUFCB ADDR
         OI    DCBBUFCB+KTHREE,RSTRVAL  RESTORE TO PRE-OPEN VALUE
         XC    DCBBUFL(DBBUFLL),DCBBUFL RESTORE BUFFER LENGTH
         NI    DCBCIND2,RSTRBAB    SET BUFFER ACQUISITION BIT OFF
         EJECT
************************ RESTORE DCB VECTOR ROUTINE *******************
*
* THE FOLLOWING ROUTINE WILL RESTORE THE FOLLOWING VECTORS TO THEIR
* PRE-OPEN VALUES:
*        1. DCBSSID
*        2. DCBIOBAD
*        3. DCBCHECK
*        4. DCBIOBA
*        5. DCBNCP
*        6. DCBEOBR
*        7. DCBCNTRL
*        8. DCBMRIND
*        9. DCBECBLT
*       10. DCBIOBL
*       11. DCBMRFLG
*       12. DCBAPPIN
*
IGGD0300 EQU   *
         MODESET EXTKEY=DATAMGT    RETURN TO DATA MANAGEMENT KEY Y02072
         MVC   DCBSSID(DBSSIDL),DEBSSID MOVE SS ID BACK INTO DCB
         LA    RWRK1,RSTRVAL       LOAD RESTORE VALUE INTO REG
         ST    RWRK1,DCBIOBAD      RESTORE IOB ADDRESS
         XC    DCBCHECK+KONE(DBCHECKL),DCBCHECK+KONE   CLEAR CHECK
*                                  MODULE ADDRESS
         OI    DCBCHECK+KTHREE,RSTRVAL  RESTORE TO PRE-OPEN VALUE
         ST    RWRK1,DCBIOBA       RESTORE MICB READ ADDR
         ST    RWRK1,DCBNCP        RESTORE NCP TO ZERO
*                                  AND EOB MODULE ADDR TO 1
         ST    RWRK1,DCBCNTRL      RESTORE CONTROL MODULE ADDR
         XC    DCBECBLT(DBECBLTL),DCBECBLT  RESTORE ECB LIST ADDR
*                                  AND DCBMRIND INDICATORS
         NI    DCBIOBL,NULL        ZERO IOB LENGTH
         NI    DCBMRFLG,NULL       ZERO INDICATOR
         NI    DCBAPPIN,NULL       ZERO INDICATOR
         SPACE 2
****
****     REFRESH THE USER'S DCB
****
         USING WTG,RWTG            EST WHERE-TO-GO TBL BASE REG  Y02072
         IECRES INIT,DCBCOPY=FRWKAR,STM=(R0,R14,WTGPREFX)        Y02072
         DROP  RWTG                DROP WHERE-TO-GO TBL BASE REG Y02072
         SPACE 2
************************ DELETE USER SS ROUTINE ***********************
*
* THE FOLLOWING WILL DELETE THE USER'S STACKER SELECT ROUTINE
*
         LA    RPARM0,DCBSSID      LOAD ADDR OF USER'S SS ID IN DCB
         DELETE EPLOC=(0)          DELETE USER'S SS ROUTINE
*
         MVI   DZERO(RWTGC),NULL   DEACTIVATE THIS ENTRY
         SPACE 2
************************ WTG TABLE SEARCH *****************************
*
* THE FOLLOWING ROUTINE WILL SEARCH THE WTG TABLE FOR THE NEXT ENTRY
* WHICH REQUIRES THIS EXECUTOR OR FOR END OF TABLE THEN EXIT VIA XCTL
*
IGGD0400 EQU   *
         LA    RWTGC,WGOFF(RWTGC)  INCREMENT TO NEXT WTG TABLE ENTRY
         LA    RPARMC,PARMOFF(RPARMC) INCREMENT TO NEXT PARM ENTRY
         CLC   DZERO(ROUTIDL,RWTGC),ROUTNID  NEXT DCB REQUIRE THIS MOD
         BCR   EQUAL,RBASE         YES/RECURSE THROUGH MODULE
         CLC   DZERO(ROUTIDL,RWTGC),CLLDB    IS THIS END OF TABLE
         BC    LOW,IGGD0400        NO-GO INCREMENT TO NEXT ENTRY
         CLC   DZERO(ROUTIDL,RWTGC),CLLDG    IS THIS END OF TABLE
         BC    HIGH,IGGD0400       NO-GO INCREMENT TO NEXT ENTRY
         LR    RPARMC,RPARM        RESTORE TO START OF PARM LIST
         LA    RWTGC,WAOFF(RWTG)   POINT TO FIRST IDTTR IN WTG TABLE
IGGD0500 EQU   *
         CLI   DZERO(RWTGC),NULL   IS THIS ENTRY ACTIVE
         BC    NE,IGGD0600         YES/GO TRANSFER CONTROL
         LA    RWTGC,WGOFF(RWTGC)  INCREMENT TO NEXT ENTRY
         LA    RPARMC,PARMOFF(RPARMC) INCREMENT TO NEXT ENTRY
         B     IGGD0500            GO TEST NEXT ENTRY
IGGD0600 EQU   *
         MVC   DID(ROUTIDL,RWTG),DZERO(RWTGC)  MOVE ID INTO NAME
         XCTL  EPLOC=(RWTG),SF=(E,DXCCW12),DCB=0  TRANSFER CONTROL
         EJECT
************************ PURGE ROUTINE ********************************
*
* THE FOLLOWING ROUTINE WILL BUILD THE PURGE LIST IN THE DCB WORK AREA
* AND THEN ISSUE A PURGE MACRO TO PURGE THE QUEUES FOR THE PCU OR SCU.
* THE PURGE OPTIONS WHICH ARE SET ARE
*        1. PURGE ONLY THE REQUEST ELEMENTS ASSOCIATED WITH DEB
*        2. DO NOT POST THE PURGE REQUEST
*        3. HALT THE I/O OPERATION
*        4. PURGE ALL REQUESTS
*        5. PURGE AEQ, RB, AND LOGICAL CHANNEL QUEUES
*        6. PURGE BY DEB
*
PURG0000 EQU   *
         XC    DXCCW6(PURGLST),DXCCW6   CLEAR AREA FOR PURGE LIST
         ST    RDEB,DXCCW6         STORE DEB ADDR IN LIST
         MVI   DXCCW6,PURGMSK      SET PURGE OPTIONS
         LA    RPARM1,DEBUSRPG     LOAD ADDR OF USER'S LINK FIELD
         ST    RPARM1,DXCCW6+DLKFLD     STORE LINK FIELD ADDR INTO LIST
         LA    RPARM1,DXCCW6       LOAD ADDR OF PURGE LIST
         PURGE (1)                 PURGE
         LA    RPARM1,DXCCW6+DECBPURG   LOAD ADDR ECB FOR PURGE
         WAIT  ECB=(1)             WAIT FOR PURGE
         BCR   UNCOND,RWRK1        RETURN TO MAINLINE
         SPACE 2
************************ FREE STORAGE ROUTINE *************************
*
* THE FOLLOWING IS AN INTERNAL SUBROUTINE WHICH WILL ISSUE A FREEMAIN
* TO FREE STORAGE.  BEFORE THIS ROUTINE IS ENTERED, REGISTER 0 MUST BE
* LOADED WITH THE LENGTH OF THE AREA TO BE FREED AND REGISTER 1 WITH
* THE STARTING ADDRESS OF THE AREA TO BE FREED.
*
FREE0000 EQU   *
         LA    RPARMF,SPNUMBER     LOAD SUBPOOL NUMBER
         SLL   RPARMF,SHIFTNO      SHIFT SP NO TO HI ORDER BITS
         OR    RPARM0,RPARMF       PUT SUBPOOL NUMBER IN REG
         FREEMAIN R,LV=(0),A=(1)   FREE STORAGE
         BCR   UNCOND,RWRK1        RETURN TO MAINLINE
         EJECT
***********************************************************************
*
************************ CONSTANTS ************************************
*
CLLDB    DC    C'0B'               ID OF MODULE IGG0200B         Y02072
CLLDG    DC    C'0G'               ID OF MODULE IGG0200G         Y02072
ROUTNID  DC    C'1D'               ID OF THIS ROUTINE
         DC    C'IGG0201D'                    SIGHT INDICATOR  @ZA07621
         DC    CL8'&SYSDATE'                  SIGHT INDICATOR  @ZA07621
         DC    C'Z07621'                      SIGHT INDICATOR  @ZA07621
PATCH    DC    42X'0',D'0'             PATCH AREA              @ZA07621
MODLEN   EQU   *                   LENGTH OF THIS MODULE         Y02072
         SPACE 2
************************ DSECT DEFINITION *****************************
*
* THE FOLLOWING ARE DSECTS REQUIRED BY THIS EXECUTOR
*
         CVT   DSECT=YES                                         Y02072
         SPACE 3
         DCBD  DSORG=BS,DEVD=MR
         EJECT
DEBD0100 DSECT
DEBNMSUB DS    0CL1
DEBTCBAD DS    CL4
DEBAMLNG DS    0CL1
DEBDEBAD DS    CL4
DEBOFLGS DS    0CL1
DEBIRBAD DS    CL4
DEBOPATB DS    0CL1
DEBSYSPG DS    CL4
DEBNMEXT DS    0CL1
DEBUSRPG DS    CL4
DEBPRIOR DS    0CL1
DEBECBAD DS    CL4
DEBPROTG DS    0CL1
DEBDEBID DS    0CL1
DEBDCBAD DS    CL4
DEBEXSCL DS    0CL1
DEBAPPAD DS    CL4
DEBDVMOD DS    0CL1
DEBUCBAD DS    CL4
DEBUCBA2 DS    CL4
DEBSUBID DS    CL8                                              SA62501
DEBSSID  DS    CL8
         SPACE 3
         IECDSECS (MAIN,(IOB,NO)),PREFX,WTG,EXPAND=YES           Y02072
         END
