         TITLE '1419 ERROR RECOVERY ROUTINE'
IGE0011E CSECT
         SPACE
***********************************************************************
*                                                                     *
* MODULE NAME - IGE0011E                                              *
*                                                                     *
* DESCRIPTIVE NAME - 1419 ERROR RECOVERY ROUTINE                      *
*                                                                     *
* COPYRIGHT - NONE                                                    *
*                                                                     *
* CHANGE LEVEL - AS FOLLOWS:                                          *
*                                                                     *
*          VS2 RELEASE 04 CHANGES
*C712000                                                       @ZA07621
*A298100,454100-454200    *                                    @ZA05597
*
*          VS2 RELEASE 02 DELETIONS                                   *
*D096000-166000,174000-180000,192000-194000,202000-266000,       Y02072
*D272000-278000,286000-290000,294000-296000,304000-306000,       Y02072
*D384000-398000,412000,424000-444000,706000,710000               Y02072
*                                                                     *
*                                                                     *
* STATUS - VS2 RELEASE 2 LEVEL 0                                      *
*                                                                     *
*                                                                     *
* FUNCTION - ANALYZE STATUS AND SENSE DATA FROM BOTH THE PCU AND THE  *
*            SCU.  MAINTAIN CURRENT ERROR STATISTIC AND SET A FLAG    *
*            FOR RECORDING OF ERRORS THAT OCCUR IN THE 1419 AND IN    *
*            THE CONTROL UNITS.  ALSO SET A FLAG FOR THE PROPER       *
*            MESSAGE TO THE OPERATOR.                                 *
*                                                                     *
* OPERATION - THIS ROUTINE CHECKS THE STATUS AND SENSE BYTES STORED   *
*             IN THE IOSB AND TAKES THE NECESSARY ACTION.  STATISTICS *
*             ARE KEPT AS TO THE KIND AND FREQUENCY OF ERRORS.  THESE *
*             STATISTICS ARE USED BY THE CE TO RECOGNIZE, ANALYZE,    *
*             AND ISOLATE ERRORS.                                     *
*             A COMMON INTERPRETER ROUTINE IS USED TO CHECK BOTH      *
*             STATUS AND SENSE.  ON RETURN FROM THE INTERPRETER, THE  *
*             PROPER FLAGS ARE SET AND A MESSAGE IS WRITTEN TO THE    *
*             OPERATOR.                                               *
*                                                                     *
*                                                                     *
* ENTRY POINT - ER1419 FROM THE SCHEDULER                             *
*                                                                     *
*                                                                     *
* INPUT - REGISTER 1 - ADDRESS OF THE IOSB                            *
*                                                                     *
*                                                                     *
* OUTPUT - ERROR LOGOUTS AND MESSAGES TO THE OPERATOR.  CONSOLE       *
*          MESSAGES TO INDICATE ERROR CONDITIONS ARE:                 *
*             1. I/O PERMANENT ERROR                                  *
*             2. INTERVENTION REQUIRED                                *
*                                                                     *
*                                                                     *
* EXTERNAL ROUTINES - NONE                                            *
*                                                                     *
*                                                                     *
* EXITS-NORMAL - XCTL TO THE WRITE TO OPERATOR ROUTINE.  RETURN WILL  *
*                BE MADE FROM THIS ROUTINES TO THE SUPERVISOR VIA AN  *
*                SVC 3.                                               *
*                                                                     *
* EXITS-ERROR - NONE                                                  *
*                                                                     *
*                                                                     *
* TABLES/WORK AREAS - ERP WORK AREA                                   *
*                                                                     *
*                                                                     *
* MACROS-ACTION - NONE                                                *
*                                                                     *
* MACROS-MAPPING - IECDERWA, IECDIOSB, IEFUCBOB                       *
*                                                                     *
*                                                                     *
* ATTRIBUTES - REENTRANT, ENABLED                                     *
*                                                                     *
*                                                                     *
* NOTES - IOSB EXCEPTION AND IOSB ERROR FLAGS ARE INTERPRETED AS      *
*         FOLLOWS:                                                    *
*            IOSEX ON AND IOSERR ON  - ERROR ROUTINE IN CONTROL       *
*            IOSEX ON AND IOSERR OFF - PERMANENT ERROR                *
*                                                                     *
***********************************************************************
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*     REGISTER DEFINITION
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
RPARM    EQU   1                        ADDR OF IOSB AT ENTRY    Y02072
REWA     EQU   4                        ERP WORK AREA ADDRESS    Y02072
UCBREG   EQU   7                        UCB ADDRESS REG
COMREG   EQU   8                        COMMUNICATION TABLE REG
SAVREG   EQU   9                        SAVE REG
RIOSB    EQU   10                       IOSB ADDRESS REG         Y02072
ERREG1   EQU   13                       WORK REG 1
ERRETR   EQU   14                       RETURN REG
BASREG   EQU   15                       BASE REG
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*      MISC.  DEFINITIONS
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ONE      EQU   1
LOC016   EQU   16                       COMMUNICATION VECTOR TABLE
VECTXL   EQU   44                       VECTOR TO XCTL ROUTINE
VECT68   EQU   68                       INTERPRETER ROUTINE POINTER
WTORTN   EQU   253                      NAME OF WTO ROUTINE
UCBPCU   EQU   X'1D'                    PCU TEST BYTE            Y02072
UCB1275  EQU   X'1F'                    PCU 1275               @ZA05597
UCBSCU   EQU   X'1E'                    SCU TEST BYTE            Y02072
WKBIT1   EQU   X'01'
PCUMSK   EQU   X'2E'                    MASK FOR PCU SNS BITS   SA44107
SCUMSK   EQU   X'A6'                    MASK FOR SCU SNS BITS   SA44107
SWITCH   EQU   X'FF'
         EJECT
         USING ER1419,15
ER1419   LR    RIOSB,RPARM              GET IOSB ADDRESS         Y02072
         USING IOSB,RIOSB               EST IOSB BASE REGISTER   Y02072
         L     REWA,IOSERP              GET ERP WORK AREA ADDR   Y02072
         USING EWA,REWA                 EST ERP WORK AREA BASE   Y02072
         L     UCBREG,IOSUCB            GET UCB ADDRESS          Y02072
         USING UCB,UCBREG               EST UCB BASE REGISTER    Y02072
         L     COMREG,LOC016            LOAD ADD OF COMMUNICATION TABLE
         OI    IOSFLA,IOSERR+IOSEX      SET EX AND ERROR FLAG    Y02072
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        LINK TO THE COMMON INTERPRETER ROUTINE. THE BYTES FOLLOWING
*        THE BALR INDICATE IN WHAT ORDER THE BITS ARE TO BE TEST (BIT 3
*        ON OF CODE BYTE INDICATES STATUS) FOLLOWED BY AN INDEX VALUE
*        THE INTERPRETER USES TO RETURN TO THIS ROUTINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ERR010   LR    SAVREG,BASREG            SAVE BASE REGISTER
         L     BASREG,VECT68(COMREG)    GET ADDR.TO INTERP.ROUTINE
ERR020   BALR  ERRETR,BASREG            LINK TO INTERPRETER RTN  Y02072
         DC    X'1D'                    CHANNEL CONTROL CHECK
         DC    AL1(ERR025-ERR020-2)     BRANCH TO CHECK ERPIB TABLE
         DC    X'1E'                    INTERFACE CONTROL CHECK
         DC    AL1(ERR025-ERR020-4)     BRANCH TO CHECK ERPIB TABLE
         DC    X'1C'                    CHANNEL DATA CHECK
         DC    AL1(ERR028-ERR020-6)     BRANCH TO SET CDC SWITCH
         DC    X'10'                    SHOULD NOT OCCUR-INVALID STATUS
         DC    AL1(ERR070-ERR020-8)     BRANCH TO SET LOGOUT FLAG
         DC    X'11'                    SHOULD NOT OCCUR INVALID STATUS
         DC    AL1(ERR070-ERR020-10)    BRANCH TO SET LOGOUT FLAG
         DC    X'12'                    SHOULD NOT OCCUR INVALID STATUS
         DC    AL1(ERR070-ERR020-12)    BRANCH TO SET LOGOUT FLAG
         DC    X'16'                    UNIT CHECK
         DC    AL1(ERR029-ERR020-14)    BRANCH TO STAT TABLE UPDATE
         DC    X'2F'                    END OF LIST
         DC    AL1(ERR068-ERR020-16)    CHECK ADDITIONAL STATUS BITS
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*              FRONT END SUPPORT FOR CHANNEL CONTROL CHECK
*              AND FOR INTERFACE CONTROL CHECK
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ERR025   EQU   *                                                 Y02072
         XC    EWAERPIB,EWAERPIB        CLEAR ERPIB TABLE        Y02072
         B     ERR070                   BRANCH TO SET LOGOUT FLAG
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        CHANNEL DATA CHECK
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ERR028   MVI   EWAFLG3,SWITCH           SET CDC SWITCH           Y02072
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        STATISTIC UPDATE ROUTINE TO LOAD SENSE DATA INTO ERP WORK AREA
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ERR029   EQU   *                                                 Y02072
         TM    EWAFLG3,SWITCH           TEST FOR CHAN DATA CHK   Y02072
         BO    ER029C                   YES-OR BIT REP CTR NO 16
         OC    EWASTUP(ONE),IOSSNS      LD SENSE BYTE 0 INTO EWA Y02072
         CLI   UCBTBYT4,UCBPCU          TEST FOR PCU             Y02072
         BE    ER029D                   YES, BRANCH
         CLI   UCBTBYT4,UCB1275         TEST FOR 1275 PCU      @ZA05597
         BE    ER029D                   YES BRANCH             @ZA05597
R        CLI   UCBTBYT4,UCBSCU          TEST FOR SCU             Y02072
         BE    ER029E                   YES, CLEAR SCU BITS      Y02072
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*             CHANNEL DATA  CHECK
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ER029C   XC    EWAFLG3,EWAFLG3          CLEAR CDC SWITCH         Y02072
         OI    EWASTUP+1,WKBIT1         LOAD BIT FOR CTR NO 16   Y02072
*                                       INTO EWA                 Y02072
         B     ERR070                   BRANCH TO SET LOGOUT FLAG
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                MASK UNWANTED BITS FOR PCU
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ER029D   NI    EWASTUP,PCUMSK           MASK BITS EXCEPT DATA    Y02072
*                                       CHK, CDC, BUS OUT, AUTO  Y02072
*                                       SELECT, AND DATA OVERRUN Y02072
         B     ERR050                   BRANCH TO CK PCU SENSE
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                 MASK UNWANTED BITS FOR SCU
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ER029E   NI    EWASTUP,SCUMSK           MASK BITS EXCEPT AUTO    Y02072
*                                       SELECT, BUS OUT, LSS,    Y02072
*                                       CR, AND CDC              Y02072
         B     ERR060                   BRANCH TO CK SCU SENSE
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        LINK TO THE COMMON INTERPRETER ROUTINE. THE BYTES FOLLOWING
*        THE BALR INDICATE IN WHAT ORDER THE BITS ARE TO BE TEST (BIT 3
*        ON OF CODE BYTE INDICATES STATUS) FOLLOWED BY AN INDEX VALUE
*        THE INTERPRETER USES TO RETURN TO THIS ROUTINE
*             CHECK PCU SENSE BITS
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ERR050   LR    SAVREG,BASREG            SAVE BASE REGISTER
         L     BASREG,VECT68(COMREG)    GET ADDR.TO INTERPRETER ROUTINE
ERR051   BALR  ERRETR,BASREG            LINK TO INTERPRETER RTN  Y02072
         DC    X'02'                    BUS OUT CHECK
         DC    AL1(ERR070-ERR051-2)     BRANCH TO SET LOGOUT FLAG
         DC    X'03'                    SHOULD NOT OCCUR
         DC    AL1(ERR070-ERR051-4)     BRANCH TO SET LOGOUT FLAG
         DC    X'07'                    SHOULD NOT OCCUR
         DC    AL1(ERR070-ERR051-6)     BRANCH TO SET LOGOUT FLAG
         DC    X'01'                    INTERVENTION REQUIRED
         DC    AL1(ERR077-ERR051-8)     BRANCH TO SET MESSAGE FLAG
         DC    X'00'                    COMMAND REJECT
         DC    AL1(ERR075-ERR051-10)    BRANCH TO SET PERM ERR FLAG
         DC    X'04'                    DATA CHECK
         DC    AL1(ERR070-ERR051-12)    BRANCH TO SET LOGOUT FLAG
         DC    X'05'                    DATA OVERRUN
         DC    AL1(ERR070-ERR051-14)    BRANCH TO SET LOGOUT FLAG
         DC    X'06'                    AUTO SELECT
         DC    AL1(ERR070-ERR051-16)    BRANCH TO SET LOGOUT FLAG
         DC    X'2F'                    END OF LIST
         DC    AL1(ERR068-ERR051-18)    BRANCH TO CK REMAINING STATUS
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        LINK TO THE COMMON INTERPRETER ROUTINE. THE BYTES FOLLOWING
*        THE BALR INDICATE IN WHAT ORDER THE BITS ARE TO BE TEST (BIT 3
*        ON OF CODE BYTE INDICATES STATUS) FOLLOWED BY AN INDEX VALUE
*        THE INTERPRETER USES TO RETURN TO THIS ROUTINE
*                CHECK SENSE BITS FOR SCU
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ERR060   LR    SAVREG,BASREG            SAVE BASE REGISTER
         L     BASREG,VECT68(COMREG)    GET ADDR. OF INTERPRETER ROUT.
ERR061   BALR  ERRETR,BASREG            LINK TO INTERPRETER RTN  Y02072
         DC    X'02'                    BUS OUT CHECK
         DC    AL1(ERR070-ERR061-2)     BRANCH TO SET LOGOUT FLAG
         DC    X'04'                    SHOULD NOT OCCUR
         DC    AL1(ERR070-ERR061-4)     BRANCH TO SET LOGOUT FLAG
         DC    X'03'                    SHOULD NOT OCCUR
         DC    AL1(ERR070-ERR061-6)     BRANCH TO SET LOGOUT FLAG
         DC    X'01'                    INTERVENTION REQUIRED
         DC    AL1(ERR077-ERR061-8)     BRANCH TO SET MESSAGE FLAG
         DC    X'07'                    OPERATOR ATTENTION
         DC    AL1(ERR077-ERR061-10)    BRANCH TO SET MESSAGE FLAG
         DC    X'00'                    COMMAND REJECT SCU
         DC    AL1(ERR070-ERR061-12)    BR TO SET LOGOUT FLAG   SA44107
         DC    X'05'                    LATE STACKER SELECT
         DC    AL1(ERR070-ERR061-14)    BRANCH TO SET LOGOUT FLAG
         DC    X'06'                    AUTO SELECT
         DC    AL1(ERR070-ERR061-16)    BRANCH TO SET LOGOUT FLAG
         DC    X'2F'                    END OF LIST
         DC    AL1(ERR068-ERR061-18)    BRANCH TO CK REMAINING STATUS
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        LINK TO THE COMMON INTERPRETER ROUTINE. THE BYTES FOLLOWING
*        THE BALR INDICATE IN WHAT ORDER THE BITS ARE TO BE TEST (BIT 3
*        ON OF CODE BYTE INDICATES STATUS) FOLLOWED BY AN INDEX VALUE
*        THE INTERPRETER USES TO RETURN TO THIS ROUTINE
*               CHECK REMAINING STATUS BITS
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ERR068   LR    SAVREG,BASREG            SAVE BASE REGISTER
         L     BASREG,VECT68(COMREG)    GET ADDR. OF INTERPRETER ROUT.
ERR069   BALR  ERRETR,BASREG            LINK TO INTERPRETER RTN  Y02072
         DC    X'1F'                    CHAINING CHECK
         DC    AL1(ERR070-ERR069-2)     BRANCH TO SET LOGOUT FLAG
         DC    X'1A'                    PROGRAM CHECK
         DC    AL1(ER070A-ERR069-4)     BRANCH TO ECB CODE ROUTINE
         DC    X'1B'                    PROTECTION CHECK
         DC    AL1(ER070A-ERR069-6)     BRANCH TO ECB CODE ROUTINE
         DC    X'19'                    INCORRECT LENGTH
         DC    AL1(ER070A-ERR069-8)     BRANCH TO ECB CODE ROUTINE
         DC    X'2F'                    END OF LIST
         DC    AL1(ERR075-ERR069-10)    BRANCH TO SET MESSAGE FLAG P ER
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*      SET LOGOUT OUT FLAG BIT 7 OF IOBFLG3
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ERR070   OI    IOSFLB,IOSLOG            SET LOGOUT FLAG          Y02072
*
ER070A   OI    IOSCOD,IOSERRC           SET PERMANENT ERROR FLAG Y02072
*
ERR075   OI    IOSFLB,IOSMSG            SET PERM ERROR MSG FLG   Y02072
         B     ERR078                   BR TO RESET PERM ERROR FLAG
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*      SET MESSAGE FLAG FOR OPERATOR ATTN. OR INTERVENTION REQUIRED
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ERR077   NI    IOSFLB,X'FF'-IOSMSG      IND INTERVENTION REQ'D   Y02072
*                                       OR OP. ATTN FLAG         Y02072
ERR078   NI    IOSFLA,X'FF'-IOSERR      RESET PERM ERROR FLAG    Y02072
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                   WTO ROUTINE                                       *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         L     ERRETR,LOC016            GET ADDRESS OF CVT
         L     ERRETR,VECTXL(ERRETR)    GET ADDRESS OF XCTL
         LA    ERREG1,WTORTN            LOAD NAME OF WTO
         BR    ERRETR                   GO TO XCTL
*
         DC    C'IGE0011E'                   SIGHT IDENTIFIER  @ZA07621
         DC    CL8'&SYSDATE'                 SIGHT IDENTIFIER  @ZA07621
         DC    C'Z07621'                     SIGHT IDENTIFIER  @ZA07621
PATCH    DC    XL20'0',D'0'              PATCH AREA            @ZA07621
MODLEN   EQU   *                        LENGTH OF THIS MODULE    Y02072
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        CONTROL BLOCK DEFINITIONS                                    *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         IECDERWA NONE                                           Y02072
         EJECT
         IECDIOSB                                                Y02072
         EJECT
UCB      DSECT                                                   Y02072
         IEFUCBOB                                                Y02072
         END
