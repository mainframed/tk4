***********************************************************************
*                                                                     *
* MODULE NAME = BNGIRMOT                                              *
*                                                                     *
* DESCRIPTIVE NAME =  IMS DEMF REMOTE TERMINAL ERROR TEXT DISPLAY     *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 1, LEVEL 1                                         *
*                                                                     *
* FUNCTION = 1) DISPLAYS CANNED STATUS/SENSE TEXT OF ALL POSSIBLE     *
*               REMOTE TERMINAL ERROR COMBINATIONS;                   *
*            2) PROVIDES THE SELECTION DISPLAY OF EXPLANATION AND     *
*               POSSIBLE CAUSES ON THE REMOTE TERMINAL STATUS/SENSE   *
*               WHICH IS SELECTED BY DEMF USER.                       *
*                                                                     *
* NOTES = NONE                                                        *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS :                                           *
*    --------------------                                             *
*                                                                     *
*    ALL DEMF RECORDING DISPLAY COMPONENTS USE STANDARD REGISTER      *
*    CONVENTIONS WITH THE EXCEPTION OF THE IMS DISPLAY COMPONENTS.    *
*    CERTAIN IMS MODULES USE ADDITIONAL REGISTERS AND THE LINK MACRO  *
*    FOR PASSING OR RETRIEVING DATA PARAMETER INFORMATION AND ARE     *
*    DETAILED BELOW:                                                  *
*                                                                     *
*     MODULE    REGISTER    CONTENTS.                                 *
*    --------   --------    ------------------------------------      *
*    BNGIDISP     R5        PASSES ADDRESS OF IMS PROGRAM             *
*                           COMMUNICATION BLOCK (PCB) TO              *
*                           BNGI3270, BNGIMENU, BNGILOCL, AND         *
*                           BNGIRMOT.                                 *
*                                                                     *
*                 R11       PASSES TO BNGIREAD THE ADDRESS OF A       *
*                           PARAMETER LIST CONTAINING:                *
*                                                                     *
*                          1) 4-BYTES - ADDRESS OF I/O AREA FROM      *
*                                       BNGIREAD                      *
*                                                                     *
*                          2) 3-BYTES - RELATIVE BLOCK ADDRESS TO     *
*                                       BE READ BY BNGIREAD           *
*                                                                     *
*                          3) 2-BYTES - STATUS OF READ FROM           *
*                                       BNGIREAD                      *
*                                                                     *
*                 R12      FOR BNGI3270, PASSES THE ADDRESS OF A      *
*                          COMMUNICATION AREA CONTAINING              *
*                          UP TO SIX EDITTED PARM FIELDS, SAVE        *
*                          AND WORK AREAS, AND THE INPUT MESSAGE.     *
*                                                                     *
*                          FOR BNGILOCL, BNGIRMOT, AND BNGIMENU;      *
*                          PASSES THE ADDRESS OF THE INPUT MESSAGE.   *
*                                                                     *
*    BNGIREAD    LINK TO BNGIDCBA       RETRIEVES THE ADDRESS OF THE  *
*                (ALIAS FOR BNGISIMS)   ADDRESS OF THE SYS1.DEMFLOG   *
*                                       DCB.                          *
*                                                                     *
*                 R11      RECEIVES ADDRESS OF PARAMETER LIST         *
*                          (SEE BNGIDISP R11 FOR LIST).               *
*                                                                     *
*    BNGI3270     R5      RECEIVES ADDRESS OF IMS PCB FROM BNGIDISP   *
*                                                                     *
*                 R11     PASSES TO BNGIREAD THE ADDRESS OF A         *
*                         PARAMETER LIST (SEE BNGIDISP R11 FOR LIST)  *
*                                                                     *
*                 R12     RECEIVES FROM BNGIDISP THE ADDRESS OF THE   *
*                         COMMUNICATION AREA (SEE BNGIDISP R12 FOR    *
*                         CONTENTS OF AREA).                          *
*                                                                     *
*    BNGIMENU     R5      RECEIVES ADDRESS OF IMS PCB FROM BNGIDISP   *
*                                                                     *
*                 R12     RECEIVES ADDRESS OF INPUT MESSAGE FROM      *
*                         BNGIDISP.                                   *
*                                                                     *
*    BNGILOCL     R5      RECEIVES ADDRESS OF IMS PCB FROM BNGIDISP   *
*                                                                     *
*                 R12     RECEIVES ADDRESS OF INPUT MESSAGE FROM      *
*                         BNGIDISP.                                   *
*                                                                     *
*    BNGIRMOT     R5      RECEIVES ADDRESS OF IMS PCB FROM BNGIDISP   *
*                                                                     *
*                 R12     RECEIVES ADDRESS OF INPUT MESSAGE FROM      *
*                         BNGIDISP.                                   *
*                                                                     *
*    PATCH LABEL = NONE                                               *
*                                                                     *
* MODULE TYPE =  IMS DISPLAY                                          *
*                                                                     *
*    PROCESSOR = ASSEMBLER H                                          *
*                                                                     *
*    MODULE SIZE = 12365 DECIMAL BYTES                                *
*                                                                     *
*    ATTRIBUTES = SERIALLY REUSABLE                                   *
*                                                                     *
* ENTRY POINT = BNGIRMOT                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = R14 LOADED WITH THE RETURN ADDR (USED BY SUBROUTINE)   *
*                                                                     *
* INPUT = PARAM LIST ITEMS USED ARE : INPUT MESSAGE                   *
*                                                                     *
* OUTPUT = PARAM LIST ITEMS USED ARE : REMOTE INDEX AND TEXT DISPLAYS *
*                                                                     *
* EXIT-NORMAL = REGISTER 15 RETURN CODE RETURNED TO CALLER :          *
*               00 - OPERATION PERFORMED AS REQUESTED                 *
*                                                                     *
* EXIT-ERROR  = NONE                                                  *
*                                                                     *
* EXTERNAL REFERENCE = NONE                                           *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA AREAS = NONE                                                *
*                                                                     *
*    CONTROL BLOCK = DCB                                              *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = SAVE, RETURN                                               *
*                                                                     *
* CHANGE ACTIVITIES AS FOLLOWS:                                       *
* CHANGE 01   11/15/77  PROLOGUE ADDED                                *
*                                                                     *
***********************************************************************
BNGIRMOT TITLE 'DEMF REMOTE SRL TEXT MODULE'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
PCBREG   EQU   R5
MSGINREG EQU   R12
BASEREG  EQU   R3
TPIOPCB  DSECT
PCBTERMN DS    CL8                  REQUESTING TERMINAL SYMBOLIC NAME
         DS    CL2                  RESERVED
PCBSTAT  DS    CL2                  DL/I STATUS CODE
PCBDATE  DS    PL4                  TRANSACTION DATE STAMP
PCBTIME  DS    PL4                  TRANSACTION TIME STAMP
PCBSEQ   DS    F                    TRANSACTION SEQUENCE NUMBER
PCBMODNM DS    CL8                  MFS MSG OUTPUT DESCRIPTOR NAME
MSGINMAP DSECT
MSGIN    DS    0D                   MAP OF TRANSACTION MSG SEGMENT
TIOATDL  DS    H                    DATA LENGTH
MSGZ1    DS    X
MSGZ2    DS    X
MSGTXCD  DS    CL11                 DEMF LOCAL  FOR THIS MODULE
MSGEYED  DS    0CL8                 XSS=CODE  IF TEXT PAGE
X        DS    CL1                  X         IF TEXT PAGE
SSEQUAL  DS    CL3                  SS=       IF TEXT PAGE
SSCODE   DS    CL4                  CODE FOR CSW-SS IF TEXT
BNGIRMOT CSECT
XNGIRMOT SAVE  (14,12),T,*
         LR    BASEREG,R15          ESTABLISH BASE REGISTER
         USING BNGIRMOT,BASEREG     MODULE BASE REGISTER
         USING TPIOPCB,PCBREG       IMS TP IOPCB BASE
         USING MSGINMAP,MSGINREG    DSECT MAP OF TRAN MSG SEG
         LR    R6,R13               GET ADDRESS OF SAVE AREA
         ST    R13,SAVEAREA+4       SAVE R13 MY SAVE AREA
         LA    R13,SAVEAREA         ADDRESS OF MY SAVE AREA
         ST    R13,8(R6)            STORE MY SAVE
TESTCALL EQU   *                    TEST INDEX OR TEXT CALL
         CLC   SSEQUAL,QSSEQUAL+1   REQUEST FO TEXT PAGE?
         BE    TEXTPAGE             YES, GO FIND CSW-SS MSG
*                                   NO.  BUILD INDEX PAGE MSG
INDXPAGE EQU   *                    REQUEST FOR INDEX PAGE
*                                   OR UNDETERMINED CODE ENTERED
         LA    R6,CSWSSNBR          ADDR OF CSW-SS STATUS CODES
         LA    R7,MSGOINDX          ADDR OF MESSAGE OUTPUT AREA
         LA    R8,CSWSSEND+4       *UPPER LIMIT OF CSW-SS CODES
NEXTCODE EQU   *
         CR    R6,R8                TEST FOR UPPER LIMIT OF CODES
         BE    INDXOUT              END OF CSW-SS CODES-GET OUT
         MVC   0(4,R7),QSSEQUAL     MOVE ?SS= TO OUTPUT AREA
         MVC   4(4,R7),0(R6)        MOVE CSW-SS CODE TO OUTPUT AREA
         LA    R7,8(R7)             BUMP ADDR FOR NEXT ?SS=XXXX
         LA    R6,8(R6)            *BUMP ADDR FOR NEXT CSW-SS CODE
         B     NEXTCODE             GO GET NEXT ?SS=XXXX
INDXOUT  EQU   *                    INDEX PAGE OUTPUT PREPARATION
         MVI   0(R7),X'3F'          INSERT 3F LAST CHARACTER OF INDEX
         LA    R6,MSGOHDR           ADDR OF MSG HEADER OUT AREA
         MVC   0(IXHDRL,R6),IXHDR   MOVE INDEX HEADER TO OUT AREA
         SR    R6,R6                CLEAR REGISTER
         LA    R6,CSWSSLTH          LENGTH OF CSW-SS CODES
         AR    R6,R6                ADD LENGTH FOR EACH ?SS=
         LA    R6,4(R6)             ADD LENGTH OF LLZZ FIELD
         LA    R6,IXHDRL(R6)        ADD LENGTH OF HEADER
         LA    R6,1(R6)             ADD ONE FOR LENGTH OF 3F CHARACTER
         STH   R6,MSGOUTLL          STORE LENGTH IN LL FIELD
         MVC   MODNAME,REMOTEI      MFS MOD NAME INDEX PAGE
         B     CALLDLI              BRANCH TO IMS DL/I CALL
TEXTPAGE EQU   *
         LA    R6,CSWSSNBR          ADDR OF BEGIN OF CSW-SS CODES
         LA    R7,MSGADDR           ADDR OF FIRST MESSAGE
         LA    R8,CSWSSEND+4       *UPPER LIMIT CHECK FOR CODES
CODETEST EQU   *
         CR    R6,R8                TEST FOR LIMIT I.E. CODE NOT FOUND
         BE    INDXPAGE             BRANCH TO DISPLAY INDEX PAGE
         CLC   0(4,R6),SSCODE       COMPARE INPUT CSW-SS CODE TO TABLE
         BE    BUILDTXT             YES, BUILD TEXT PAGE MESSAGE
         LA    R6,8(R6)            *NO, LOOK FOR ANOTHER CSW-SS CODE
         LA    R7,8(R7)            *AND BUMP TO CORRESPONDING MESSAGE
         B     CODETEST
BUILDTXT EQU   *                    TEXT PAGE OUTPUT PREPARATION
         MVI   TEXTLG+150,C' '      CLEAR BOTTOM HALF OF LG SCREEN
         MVC   TEXTLG+151(240),TEXTLG+150
         LA    R6,MSGOHDR           ADDR MSG HEADER OUT AREA
         MVC   0(TXHDRL,R6),TXHDR   MOVE TEXT HEADER TO OUT AREA
         L     R7,0(R7)             ADDR OF MSG LENGTH
         LA    R8,2(R7)             ADDR OF MSG TEXT TO MOVE
         LH    R7,0(R7)             GET MSG LENGTH THIS MESSAGE
         LR    R9,R7                MSG LENGTH FOR MOVE LONG
         LA    R6,MSGOTEXT          ADDR MESSAGE OUTPUT AREA
         MVCL  R6,R8                MOVE MESSAGE
         MVI   1(R6),X'3F'         *INSERT 3F CHARACTER AT END OF TEXT
         MVC   MODNAME,REMOTET      MFS MOD NAME TEXT PAGE
         MVC   TEXTSM(200),TEXTLG      MOVE LARGE SCREEN TEXT TO
         MVC   TEXTSM+200(191),TEXTLG+200    SMALL SCREEN AREA
CALLDLI  EQU   *
         CALL  ASMTDLI,(ISRT,(PCBREG),MSGOUT,MODNAME),VL
*                                   TEST IMS DL/I STATUS CODE
         L     R13,SAVEAREA+4       RESTORE SAVE AREA ADDRESS
         RETURN (14,12),RC=0
         LTORG
SAVEAREA DC    18F'0'               SAVE AREA
ISRT     DC    CL4'ISRT'            IMS DL/I INSERT FUNCTION
REMOTEI  DC    CL8'BNGIOLC1'        NAME OF MFS REMOTE INDEX PAGE MOD
REMOTET  DC    CL8'BNGIOLC2'        NAME OF MFS REMOTE TEXT  PAGE MOD
QSSEQUAL DC    CL4'?SS='            CHARACTERS FOR INDEX PAGE
MODNAME  DC    CL8' '               MFS MOD NAME FOR IMS CALL
IXHDR    DC    CL11'DEMF REMOTE'
         DC    CL27'STATUS INDEX,X OVER ?,ENTER'
         DC    CL39'HIT CLEAR KEY TO EXIT DEMF'
IXHDRL   EQU   *-IXHDR
TXHDR    DC    CL11'DEMF REMOTE'
         DC    CL27'HIT ENTER TO REFRESH INDEX'
TXHDRL   EQU   *-TXHDR
MSGOUT   DS    0D                   MESSAGE OUTPUT AREA
MSGOUTLL DC    AL2(MSGOLTH)         MESSAGE LENGTH LL FIELD
MSGOUTZ1 DC    X'00'                               Z1 FIELD
MSGOUTZ2 DC    X'00'                               Z2 FIELD
MSGODATA DS    0F                   BEGINNING OF MSG DATA
MSGOHDR  DS    CL38                 MESSAGE HEADER-TEXT PAGE
MSGOTEXT DS    CL7                  TEXT PAGE
TEXTLG   DS    CL391                TEXT PAGE FOR LARGE SCREEN
TEXTSM   DS    CL391                TEXT PAGE FOR SMALL SCREEN
MSGOLTH  EQU   *-MSGOUT
SETORG   DS    C
         ORG   MSGODATA
         DS    CL77                 MESSAGE HEADER-INDEX PAGE
MSGOINDX DS    40CL8                INDEX PAGE
         ORG   SETORG
MSGADDR  DS    0F
M1       DC    AL4(MSG1)
CSWSSNBR DC    CL4'404C'
M2       DC    AL4(MSG2),CL4'4050'
M3       DC    AL4(MSG3),CL4'4060'
M4       DC    AL4(MSG4),CL4'40C1'
M5       DC    AL4(MSG5),CL4'40C2'
M6       DC    AL4(MSG6),CL4'40C3'
M7       DC    AL4(MSG7),CL4'40C4'
M8       DC    AL4(MSG8),CL4'40C5'
M9       DC    AL4(MSG9),CL4'40C8'
M10      DC    AL4(MSG10),CL4'40D1'
M11      DC    AL4(MSG11),CL4'4A40'
M12      DC    AL4(MSG12),CL4'4C40'
M13      DC    AL4(MSG13),CL4'4CC1'
M14      DC    AL4(MSG14),CL4'4E40'
M15      DC    AL4(MSG15),CL4'C140'
M16      DC    AL4(MSG16),CL4'C240'
M17      DC    AL4(MSG17),CL4'C250'
M18      DC    AL4(MSG18),CL4'C2C4'
M19      DC    AL4(MSG19),CL4'C2C8'
M20      DC    AL4(MSG20),CL4'C2D8'
M21      DC    AL4(MSG21),CL4'C4C1'
M22      DC    AL4(MSG22),CL4'C4C4'
M23      DC    AL4(MSG23),CL4'C4C5'
M24      DC    AL4(MSG24),CL4'C4D8'
M25      DC    AL4(MSG25),CL4'C6C4'
M26      DC    AL4(MSG26),CL4'C6C8'
M27      DC    AL4(MSG27),CL4'C6D8'
M28      DC    AL4(MSG28),CL4'C840'
M29      DC    AL4(MSG29),CL4'C8C1'
M30      DC    AL4(MSG30),CL4'HELP'
CSWSSEND EQU   *
CSWSSLTH EQU   ((CSWSSEND-CSWSSNBR+4)/8)*4
MSG1     DC    AL2(MSG2-*-2)
         DC    C'SS=404C',C'  '
         DC    CL29'EQUIPMENT CHECK,DATA CHECK'
         DC    C'THE 3271 DETECTED BAD PARITY ON DATA    '
         DC    C'RECEIVED FROM THE ADDRESSED DEVICE ON   '
         DC    C'A SPECIFIC OR GENERAL POLL OR A SELECT  '
         DC    C'COMMAND.                                '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= RVI-TO A SELECT        '
         DC    C'                 EOT-TO SPEC OR GEN POLL'
MSG2     DC    AL2(MSG3-*-2)
         DC    C'SS=4050',C'  '
         DC    CL29'INTERVENTION REQUIRED'
         DC    C'                                        '
         DC    C'THE ADDRESSED DEVICE IS NOT AVAILABLE   '
         DC    C'OR ADDRESSED PRINTER IS NOT READY ON A  '
         DC    C'SELECT,READ,WRITE,ERASE/WRITE OR COPY-  '
         DC    C'COMMAND.                                '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= RVI-TO A SELECT.       '
         DC    C'                 EOT-TO A READ,WRITE,-  '
         DC    C'                     ERASE/WRITE OR COPY'
         DC    C'                     COMMAND.'
MSG3     DC    AL2(MSG4-*-2)
         DC    C'                                        '
         DC    C'SS=4060',C'  '
         DC    CL29'COMMAND REJECT'
         DC    C'AN INVALID OR ILLEGAL COMMAND WAS       '
         DC    C'RECEIVED BY THE 3271 OR 3275.           '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= EOT-TO A READ OR WRITE '
         DC    C'                     COMMAND.'
MSG4     DC    AL2(MSG5-*-2)
         DC    C'SS=40C1',C'  '
         DC    CL29'OPERATION CHECK'
         DC    C'AN INVALID COMMAND SEQUENCE,(NON RECOVER'
         DC    C'ABLE PROGRAM ERROR)                     '
         DC    C'(1)ESCAPE COMMAND SEQUENCE NOT IN SECOND'
         DC    C'DATA CHARACTER POSITION (HEX 27)        '
         DC    C'(2)DATA FOLLOWS THE COMMAND IN THE DATA-'
         DC    C'STREAM RECEIVED BY THE DEVICE.          '
         DC    C'(3)DATA-STREAM CONTAINS OTHER THAN TWO  '
         DC    C'BYTES,(COPY CONTROL CHARACTER AND FROM  '
         DC    C'ADDRESS) COMMAND IS ABORTED.            '
         DC    C'DEVICE RESPONSE= EOT-TO READ,WRT OR EAU.'
MSG5     DC    AL2(MSG6-*-2)
         DC    C'SS=40C2',C'  '
         DC    CL29'CONTROL CHECK'
         DC    C'                                        '
         DC    C'THE DEVICE FAILS TO RESPOND TO THE 3271 '
         DC    C'IN TIME AND CAUSES A TIMEOUT OR THE POLL'
         DC    C'FINDS A DEVICE THAT WAS PREVIOUSLY      '
         DC    C'AVAILABLE AND READY,NOW UNAVAILABLE AND '
         DC    C'TIMES OUT.                              '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= RVI-TO A SELECT COMMAND'
         DC    C'                 EOT-TO A WRITE,SPECIFIC'
         DC    C'                     OR A GENERAL POLL.'
MSG6     DC    AL2(MSG7-*-2)
         DC    C'SS=40C3',C'  '
         DC    CL29'CONTROL CHECK,OPERATION CHECK'
         DC    C'                                        '
         DC    C'THE FROM DEVICE FAILS TO COMPLETE AN    '
         DC    C'OPERATION OR RESPOND TO THE 3271 IN A   '
         DC    C'PREDETERMINED TIME PERIOD AND TIMES OUT.'
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= EOT TO A COPY COMMAND.'
MSG7     DC    AL2(MSG8-*-2)
         DC    C'SS=40C4',C'  '
         DC    CL29'DATA CHECK'
         DC    C'(1)CURSOR CHECK IS DETECTED AT THE 3271 '
         DC    C'OR BAD PARITY ON DATA FROM THE ADDRESSED'
         DC    C'DEVICE,(NO DATA SENT TO TCU)(2) A PARITY'
         DC    C'CHECK DETECTED BY 3271,A SUB CHARACTER  '
         DC    C'IS SUBSTITUTED FOR THE ERROR CHARACTER  '
         DC    C'DURING TRANSMISSION TO TCU,WHEN COMPLETE'
         DC    C'THE 3271 SENDS ENQ TO INDICATE AN ERROR.'
         DC    C'THE TCU RESPONDS NAK,THE 3271 ANSWERS   '
         DC    C'EOT.ABOVE IS IN RESPONSE TO A READ,WRITE'
         DC    C'COMMAND OR A SPECIFIC OR GENERAL POLL.'
MSG8     DC    AL2(MSG9-*-2)
         DC    C'SS=40C5',C'  '
         DC    CL29'DATA CHECK,OPERATION CHECK'
         DC    C'                                        '
         DC    C'THE 3271 DETECTS A PARITY CHECK ON THE  '
         DC    C'DATA TRANSFERRED FROM THE FROM DEVICE.  '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= EOT-TO THE COPY COMMAND'
MSG9     DC    AL2(MSG10-*-2)
         DC    C'SS=40C8',C'  '
         DC    CL29'EQUIPMENT CHECK'
         DC    C'                                        '
         DC    C'THE 3271 DETECTED A PARITY CHECK ON DATA'
         DC    C'RECEIVED FROM THE DEVICE.               '
         DC    C'(EQUIPMENT CHECK MAY ALSO BE COMBINED   '
         DC    C'WITH DATA CHECK,REF 40C4)               '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= EOT-TO A SELECT,READ,  '
         DC    C'                     WRITE,SPECIFIC OR  '
         DC    C'                     GENERAL POLL.'
MSG10    DC    AL2(MSG11-*-2)
         DC    C'SS=40D1',C'  '
         DC    CL29'INTERVENTION REQUIRED'
         DC    C'                   OPERATION CHECK      '
         DC    C'                                        '
         DC    C'THE DEVICE IS UNAVAILABLE.              '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= EOT-TO A COPY COMMAND.'
MSG11    DC    AL2(MSG12-*-2)
         DC    C'SS=4A40',C'  '
         DC    CL29'DEVICE BUSY,DEVICE END'
         DC    C'                                        '
         DC    C'DEVICE BUSY AND DEVICE END CAN OCCUR    '
         DC    C'TOGETHER IN RESPONSE TO A SPECIFIC POLL '
         DC    C'TO A FORMATTED 3277 IF THE OPERATOR HAS '
         DC    C'PERFORMED BACKTAB OR ERASE INPUT IN     '
         DC    C'RAPID SUCCESSION.                       '
         DC    C'IGNORE DEVICE END AND TREAT AS BUSY ONLY'
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= EOT-TO A SPECIFIC POLL'
MSG12    DC    AL2(MSG13-*-2)
         DC    C'SS=4C40',C'  '
         DC    CL29'DEVICE BUSY,UNIT SPECIFY'
         DC    C'                                        '
         DC    C'ON A READ COMMAND,THE DEVICE WAS BUSY.  '
         DC    C'THE COMMAND WAS CHAINED TO A SPECIFIC   '
         DC    C'POLL,WRITE,ERASE/WRITE,OR COPY COMMAND  '
         DC    C'WHICH STARTED A PRINTER.                '
         DC    C'ON A COPY COMMAND, THE ADDRESSED "TO"   '
         DC    C'DEVICE WAS BUSY.                        '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= EOT-TO A READ,WRITE,OR '
         DC    C'                     A COPY COMMAND.'
MSG13    DC    AL2(MSG14-*-2)
         DC    C'SS=4CC1',C'  '
         DC    CL29'DEVICE BUSY,UNIT SPECIFY'
         DC    C'                OPERATION CHECK         '
         DC    C'THE DB,US AND OP CHECK STATUS AND SENSE '
         DC    C'WILL BE COMBINED IF A COPY COMMAND IS   '
         DC    C'ADDRESSED TO A BUSY "TO" DEVICE AND THE '
         DC    C'COMMAND ALSO SPECIFIES THE "FROM" DEVICE'
         DC    C'THE SAME AS THE "TO" DEVICE.            '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= EOT-TO A COPY COMMAND.'
MSG14    DC    AL2(MSG15-*-2)
         DC    C'SS=4E40',C'  '
         DC    CL29'DEVICE BUSY,UNIT SPECIFY'
         DC    C'                  DEVICE END            '
         DC    C'                                        '
         DC    C'THE ADDRESSED DEVICE BECOMES NOT BUSY   '
         DC    C'BEFORE A SPECIFIC POLL IS ISSUED TO     '
         DC    C'RETRIEVE THE STATUS AND SENSE DATA.     '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= EOT-TO A READ OR WRITE '
         DC    C'                     COMMAND.'
MSG15    DC    AL2(MSG16-*-2)
         DC    C'SS=C140',C'  '
         DC    CL29'TRANSMISSION CHECK'
         DC    C'                                        '
         DC    C'3275 DETECTED A BCC ERROR ON THE TCU    '
         DC    C'TRANSMISSION.                           '
         DC    C'(TRANSMISSION CHECK NOT USED BY 3271)   '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= EOT-TO A READ OR WRITE '
         DC    C'                     COMMAND.'
MSG16    DC    AL2(MSG17-*-2)
         DC    C'SS=C240',C'  '
         DC    CL29'DEVICE END'
         DC    C'THE POLL FINDS A DEVICE (1) PREVIOUSLY- '
         DC    C'BUSY,NOW NOT BUSY OR,(2) PREVIOUSLY-    '
         DC    C'UNAVAILABLE OR NOT READY, NOW AVAILABLE '
         DC    C'AND READY. (THE 3271 RECORD IS UPDATED) '
         DC    C'NOTE: WHEN 3271 POWER IS TURNED ON, THE '
         DC    C'"DEVICE END" IS SET FOR EACH AVAILABLE  '
         DC    C'AND READY DEVICE THAT IS ATTACHED.      '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= EOT-TO A SPECIFIC OR   '
         DC    C'                     GENERAL POLL.'
MSG17    DC    AL2(MSG18-*-2)
         DC    C'SS=C250',C'  '
         DC    CL29'DEVICE END'
         DC    C'                   INTERVENTION REQUIRED'
         DC    C'(1)UPON SELECTION THE ADDRESSED PRINTER '
         DC    C'IS OUT OF PAPER, ITS POWER HAS BEEN TURN'
         DC    C'ED OFF, OR ITS COVER IS OPEN.           '
         DC    C'DEVICE RESPONSE= RVI-TO A SELECT COMMAND'
         DC    C'                                        '
         DC    C'(2) THE POLL FINDS THE PRINTER NOT READY'
         DC    C'OR NOT AVAILABLE.                       '
         DC    C'DEVICE RESPONSE= EOT-TO A SPECIFIC OR   '
         DC    C'                     GENERAL POLL.'
MSG18    DC    AL2(MSG19-*-2)
         DC    C'SS=C2C4',C'  '
         DC    CL29'DEVICE END,DATA CHECK'
         DC    C'                                        '
         DC    C'3275 (ONLY) DETECTS AN INTERNAL PARITY  '
         DC    C'OR CURSOR CHECK ON ITS BUFFER WHEN THE  '
         DC    C'ATTACHED PRINTER GOES "NOT BUSY".       '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= EOT-TO A SPECIFIC OR   '
         DC    C'                     GENERAL POLL.'
MSG19    DC    AL2(MSG20-*-2)
         DC    C'SS=C2C8',C'  '
         DC    CL29'DEVICE END,EQUIPMENT CHECK'
         DC    C'                                        '
         DC    C'THE 3275 (ONLY) DETECTED A CHARACTER    '
         DC    C'GENERATOR READOUT ERROR.                '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= EOT-TO A SPECIFIC OR   '
         DC    C'                     GENERAL POLL.'
MSG20    DC    AL2(MSG21-*-2)
         DC    C'SS=C2D8',C'  '
         DC    CL29'DEVICE END'
         DC    C'  INTERVENTION REQUIRED,EQUIPMENT CHECK '
         DC    C'                                        '
         DC    C'(3275 ONLY) THE ATTACHED PRINTER, "AC-  '
         DC    C'POWER" IS TURNED OFF,OR A MECHANICAL    '
         DC    C'"HANG" CONDITION WAS DETECTED           '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= EOT-TO A SPECIFIC OR   '
         DC    C'                     GENERAL POLL.      '
MSG21    DC    AL2(MSG22-*-2)
         DC    C'SS=C4C1',C'  '
         DC    CL29'UNIT SPECIFY,OPERATION CHECK'
         DC    C'                                        '
         DC    C'THE "FROM DEVICE" HAS A "LOCKED BUFFER" '
         DC    C'AND CANNOT BE COPIED. (PROGRAM ERROR)   '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= EOT-TO THE COPY COMMAND'
MSG22    DC    AL2(MSG23-*-2)
         DC    C'SS=C4C4',C'  '
         DC    CL29'UNIT SPECIFY,DATA CHECK'
         DC    C'DURING POLLING,SELECT OR READ COMMANDS  '
         DC    C'THE ADDRESSED DEVICE DETECTS A "CURSOR" '
         DC    C'OR "PARITY" CHECK ON DATA THAT IS BEING '
         DC    C'TRANSFERRED TO THE CONTROL UNIT OR      '
         DC    C'DURING THE COMMAND OPERATION ON A WRITE '
         DC    C'COMMAND                                 '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= RVI-TO A SELECT.       '
         DC    C'                 EOT-TO READ,WRITE OR   '
         DC    C'                     POLLING COMMANDS.'
MSG23    DC    AL2(MSG24-*-2)
         DC    C'SS=C4C5',C'  '
         DC    CL29'UNIT SPECIFY,DATA CHECK'
         DC    C'              OPERATION CHECK           '
         DC    C'                                        '
         DC    C'THE "FROM DEVICE" DETECTED AN INTERNAL  '
         DC    C'CURSOR OR PARITY CHECK DURING A COPY-   '
         DC    C'COMMAND.                                '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= EOT-TO THE COPY COMMAND'
MSG24    DC    AL2(MSG25-*-2)
         DC    C'SS=C4D8',C'  '
         DC    CL29'UNIT SPECIFY'
         DC    C'   INTERVENTION REQUIRED,EQUIPMENT CHECK'
         DC    C'                                        '
         DC    C'A COMMAND ATTEMPTED TO START A PRINTER  '
         DC    C'OPERATION BUT THE PRINTER CARRIAGE MOTOR'
         DC    C'POWER SWITCH WAS TURNED OFF.            '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= EOT-TO A WRITE,ERASE/- '
         DC    C'                   WRITE OR COPY COMMAND'
MSG25    DC    AL2(MSG26-*-2)
         DC    C'SS=C6C4',C'  '
         DC    CL29'UNIT SPECIFY,DEVICE END'
         DC    C'                 DATA CHECK             '
         DC    C'                                        '
         DC    C'A PARITY CHECK IS DETECTED AT THE       '
         DC    C'PRINTER                                 '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= RVI-TO A SELECT COMMAND'
         DC    C'                 EOT-TO A SPECIFIC OR   '
         DC    C'                     GENERAL POLL.'
MSG26    DC    AL2(MSG27-*-2)
         DC    C'SS=C6C8',C'  '
         DC    CL29'UNIT SPECIFY,DEVICE END'
         DC    C'            EQUIPMENT CHECK             '
         DC    C'A CHARACTER GENERATOR ERROR IS DETECTED '
         DC    C'BUT THERE IS NO DATA ERROR,(BUFFER DATA-'
         DC    C'IS VALID AND REUSEABLE) OR A MECHANICAL '
         DC    C'FAILURE OCCURED.(COVER OPEN,PAPER OUTAGE'
         DC    C'OR DISABLED PRINT MECHANISM)            '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= RVI-TO A SELECT.       '
         DC    C'                 EOT-TO A SPECIFIC OR A '
         DC    C'                     GENERAL POLL.'
MSG27    DC    AL2(MSG28-*-2)
         DC    C'SS=C6D8',C'  '
         DC    CL29'UNIT SPECIFY,DEVICE END'
         DC    C'   INTERVENTION REQUIRED,EQUIPMENT CHECK'
         DC    C'                                        '
         DC    C'AN UNRECOVERABLE MECHANICAL FAILURE IS  '
         DC    C'DETECTED AT THE ADDRESSED PRINTER.      '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= EOT-TO A SPECIFIC OR A '
         DC    C'                     GENERAL POLL.      '
MSG28    DC    AL2(MSG29-*-2)
         DC    C'SS=C840',C'  '
         DC    CL29'DEVICE BUSY'
         DC    C'                                        '
         DC    C'INDICATES NORMAL STATUS FOR A DEVICE    '
         DC    C'EXECUTING A PREVIOUSLY ISSUED COMMAND,- '
         DC    C'PRINTER OPERATION,BUFFER TRANSFER,ID-   '
         DC    C'CARD READER OPERATION OR KEYBOARD-      '
         DC    C'FUNCTIONS.                              '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= RVI-TO A SELECT COMMAND'
         DC    C'                 EOT-TO A SPECIFIC POLL.'
MSG29    DC    AL2(MSG30-*-2)
         DC    C'SS=C8C1',C'  '
         DC    CL29'DEVICE BUSY,OPERATION CHECK'
         DC    C'                                        '
         DC    C'ON A COPY COMMAND,THE "FROM" DEVICE IS  '
         DC    C'BUSY EXECUTING A PRINTOUT,READING FROM  '
         DC    C'THE ID CARD READER OR PERFORMING KEY-   '
         DC    C'BOARD FUNCTIONS. THE COPY COMMAND IS    '
         DC    C'ABORTED.                                '
         DC    C'                                        '
         DC    C'DEVICE RESPONSE= EOT-TO THE COPY COMMAND'
MSG30    DC    AL2(LASTMSG-*-2)
         DC    C'SS=HELP',C'  '
         DC    CL29'REMOTE STATUS MSG HELPER.'
         DC    CL40'FIELDS "SS=XXXX" REFER TO REMOTE STATUS'
         DC    CL40'MESSAGES.  ALL COMBINATIONS ARE NOT'
         DC    CL40'DOCUMENTED.  IF YOU DON''T SEE THE ONE'
         DC    CL40'YOU NEED, SUCH AS "C2C2" DROP ONE OF THE'
         DC    CL40'INSIGNIFICANT BITS SUCH AS "DEVICE END".'
         DC    CL40'THIS WILL PRODUCE "40C2" (YOU NEED A'
         DC    CL40'6-BIT CODE CHART TO FIGURE THE HIGH-'
         DC    CL40'ORDER BITS).  NOW LOOK UP MSG FOR "40C2"'
         DC    CL40'TO SEE IF IT APPLIES TO YOUR SITUATION.'
LASTMSG  EQU   *
         END   BNGIRMOT
