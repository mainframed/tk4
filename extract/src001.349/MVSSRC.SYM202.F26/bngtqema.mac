         PRINT OFF
         MACRO
&NAME    BNGHJN &LABEL,&HJN
         LCLC  &HJA,&HJB
&HJA     SETC  '&SYSPARM'(1,4)
&HJB     SETC  '&SYSPARM'(5,4)
         AIF   ('&LABEL' EQ '').NOBRNCH
         B     &LABEL .                 BR AROUND CONSTANTS
         SPACE 2
.NOBRNCH ANOP
         AIF   ('&NAME' EQ '').NOLABEL
         DC    C'&NAME' .              MODULE IDENTIFIER
.NOLABEL ANOP
         DC    X'&HJA' .                DATE OF MODIFICATION
         AIF   ('&HJN' NE 'HJN').DATE
         DC    X'&HJB' .                HJN OF MODIFICATION
.DATE    ANOP
         AIF   ('&LABEL' EQ '').NOLBL
         SPACE 2
&LABEL   DS    0H
.NOLBL   ANOP
         MEND
         PRINT ON
***********************************************************************
*                                                                     *
* MODULE NAME = BNGTQEMA                                              *
*                                                                     *
* DESCRIPTIVE NAME = TCAM DEMF QUEUE CONTROL MODULE                   *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 1, LEVEL 1                                         *
*                                                                     *
* FUNCTION = 1) DETERMINES WHETHER THE REQUEST PASSED OVER FROM MCP   *
*               IS 'DEMF' REQUEST, IF NOT, PASS CONTROL BACK TO MCP   *
*               WITH ERROR INDICATION;                                *
*            2) IF THE REQUEST IS 'DEMF', FORWARD DEMF MESSAGE QUEUE  *
*               TO MCP WHEN PASSING CONTROL BACK TO IT;               *
*            3) INITIATES SOME MESSAGE REQUESTS TO BNGTQEMF MODULE    *
*               FOR BEING DISPLAYED TO THE 3270 DISPLAY TERMINAL.     *
*                                                                     *
* NOTES = NONE                                                        *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS = SEE REGISTERS EQU'S BELOW                 *
*                                                                     *
*    PATCH LABEL = PATCH                                       @OY20942
*                                                                     *
* MODULE TYPE =  TCAM DISPLAY                                         *
*                                                                     *
*    PROCESSOR = ASSEMBLER H                                          *
*                                                                     *
*    MODULE SIZE =  972 DECIMAL BYTES                                 *
*                                                                     *
*    ATTRIBUTES =                                                     *
*                                                                     *
* ENTRY POINT = BNGTQEMA                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = R1  LOADED WITH THE PARAM LIST POINTER                 *
*              R14 LOADED WITH THE RETURN ADDR (USED BY SUBROUTINE)   *
*              R15 LOADED WITH ENTRY POINT                            *
*                                                                     *
* INPUT = PARAM LIST ITEMS USED ARE :                                 *
*                                                                     *
* OUTPUT = PARAM LIST ITEMS USED ARE :                                *
*                                                                     *
* EXIT-NORMAL = REGISTER 15 RETURN CODE RETURNED TO CALLER :          *
*               00 - OPERATION PERFORMED AS REQUESTED                 *
*                                                                     *
* EXIT-ERROR  = NONE                                                  *
*                                                                     *
* EXTERNAL REFERENCE = NONE                                           *
*                                                                     *
*    ROUTINES =                                                       *
*                                                                     *
*    DATA AREAS =                                                     *
*                                                                     *
*    CONTROL BLOCK = DCB                                              *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = RETURN                                                     *
*                                                                     *
* CHANGE ACTIVITIES AS FOLLOWS:                                       *
* CHANGE 01  11/15/77  PROLOGUE ADDED                                 *
* CHANGE 02  02/22/78  BNGHJN MACRO ADDED                             *
*C061000-062500,197000                                         @OZ29913
*A226500                                                       @OZ29913
*D198500                                                       @OZ29913
*C081000-085000,172500                                         @OZ29899
*C173500-174000                                                @OZ29898
*C013500                                                       @OY20942
*A088000,226820                                                @OY20942
*                                                                     *
***********************************************************************
BNGTQEMA CSECT
**********************************************************************
* THIS MODULE IS LINKED WITH THE USER MCP BY ISSUING THE MACRO
* DEMF IN A MESSAGE HANDLER.  IT IS PASSED A PARM FIELD IN THE
* DEMF MACRO,WHICH IT USES TO PASS TO THE DEMF DISPLAY PROGRAMS
* THE LINE ADDRESS THE MODEL TYPE AND WHETHER THE TERMINAL IS A
* LOCAL OR REMOTE DEVICE.
*  INPUT   REGISTER ONE ADDRESS OF PARM FIELD IN MCP
*          ADDRESS OF BUFFER AT AVTADBUF IN AVT
*  OUTPUT  HARDWARE ADDRESS, MODEL AND TYPE IN PARM FIELD IF DEMF
*  RETURN CODES
*     NOT A DEMF REQUEST   RC8 BR TO NSI
*     DEMF REQUEST         RC0 BR TO NSI +4
**********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
RWK      EQU   2
RSCB     EQU   3                   TCAM SCB REG
RLCB     EQU   4                   TCAM LCB REG
RWK2     EQU   5
RBFR     EQU   6                   TCAM BUFFER REG
R7       EQU   7
R8       EQU   8                   EMF PARM REG
RAVT     EQU   9                   TCAM AVT REG
R10      EQU   10
R11      EQU   11
RBASE    EQU   12                  BASE REGISTER
R13      EQU   13
R14      EQU   14
R15      EQU   15
M3270    EQU   X'04'
EMF327L  EQU   X'02'
ENTKY    EQU   X'7D'
SBA      EQU   X'11'
RC8      EQU   8                   RETURN CODE WHEN DENO DEMF
R3270    EQU   X'91'
L3270    EQU   X'99'
MODONE   EQU   X'F1'
UCBMONE  EQU   X'11'
CVTOFF   EQU   16
SOH      EQU   X'02'
FOX0     EQU   240
FOXONE   EQU   241
FOXFOX   EQU   255
MODTWO   EQU   242
TERMSK   EQU   224                 MASK FOR TERMINAL IN TRM ENTRY
TRMLGB   EQU   128                 MASK FOR LGB INSTEAD OF TRM
TRMVTAM  EQU   16                  MASK FOR VTAM OR NCP TERMINAL ENTRY
         EJECT
****  FOLLOWING IS INITILAZATION CODE FOR PROGRAM
         USING BNGTQEMA,R15
BNGTQEMA BNGHJN GO                                             @OZ29913
         STM   R14,RBASE,12(R13)       SAVE CALLERS REGISTERS
         L     RAVT,CVTOFF             LOAD CVT ADDRESS
         L     RAVT,AVTCVTPT(,RAVT)    LOAD TCX ADDRESS
         L     RAVT,AVTZERO(,RAVT)     LOAD AVT ADDRESS
         USING IEDQAVTD,RAVT
         LR    RBASE,R13              LOAD CALLERS ADDRESS
         LA    R13,72(R13)            LOAD MY SAVE ADDRESS
         ST    RBASE,4(R13)           CONTENTS OF RAVT STORED
         ST    R13,8(RBASE)           STORE SAVE ADDRESS
         L     RBFR,AVTADBUF          INIT BUFFER REG
         USING IEDQPRF,RBFR
         L     RLCB,PRFLCB-1          INIT LCB REG
         USING IEDQLCB,RLCB
         L     RSCB,LCBSCBA-1         SET UP SCB REG
         USING IEDQSCB,RSCB
         LR    RBASE,R15              SWITCH BASE REGS
         USING BNGTQEMA,RBASE
         DROP  R15
         LR    R8,R1                  LOAD PARM REG
         USING EMF,R8
         EJECT
****  FOLLOWING DETERMINES IF THIS IS A DEMF REQUEST AND IF SO, IS
****  IT FROM A 3270 TERMINAL.
         XC    0(10,R8),0(R8)         CLEAR PARM AREA
         TM    PRFSTAT1,PRFTSMSG+PRFNHDRN   TSO OR TXT BFR?
         BNZ   NODEMF                 RETURN IF EITHER
         TM    SCBSTATE,SCBLCK1N       IS TERMINAL LOCKED ?
         BO    NODEMF                  RETURN IF IT IS
         LH    R1,PRFSRCE              GET SOURCE INDEX
         N     R1,AVTCLRHI           IS IT ZERO
         BZ    NODEMF                  RETURN IF SO
         L     R15,AVTRNMPT            LOAD TNT LOOK-UP ADDRESS
         BALR  R14,R15                 DO TNT LOOK-UP
         SR    R15,R15                 ZERO WORK REG
         IC    R15,16(R1)              GET CH TABLE INDES
         BCTR  R15,0                   SUBTRACT ONE
         SR    RWK2,RWK2               CLEAR REG               @OZ29899
         LA    RWK2,4                  LOAD REG WITH A FOUR (4)@OZ29899
         SR    RAVT,RWK2               BACK AVT UP FOUR (4)    @OZ29899
         CLI   2(RAVT),X'C1'           IS IT C'A' = TCAM10 ?   @OZ29899
         BE    TCAM10                  YES, BRANCH             @OZ29899
         SLL   R15,2                   NO, TCAM9, MULT BY FOUR @OZ29899
         B     CONT                                            @OZ29899
TCAM10   MH    R15,=H'6'               TCAM10, MULT BY SIX     @OZ29899
CONT     AR    RAVT,RWK2               RESTORE RAVT BACK       @OZ29899
         A     R15,AVTCSTCS            ADD TO START OF TABLE
         TM    1(R15),M3270            IS IT A 3270 ?
         BZ    NODEMF                  NO, RETURN
         TM    11(R1),TRMVTAM      IS THIS A NCP TERMINAL?
         BO    NODEMF              YES,DO NOT HONOR REQUEST
         LA    RWK,14                  LOAD MAX DISL TO SCAN
         SR    RWK2,RWK2                CLEAR REG FOR WORK     @OY20942
         LH    RWK2,PRFSIZE             GET LENGTH OF BUFFER   @OY20942
         CH    RWK2,MAXSIZE             MORE DATA THAN WE NEED @OY20942
         BH    DEFLEN                   YES, TAKE DEFAULT      @OY20942
         SH    RWK2,X1E                 REDUCE BY BUFFER HDR   @OY20942
         BNP   DEFLEN                     LENGTH FOR DATA LEN  @OY20942
         LR    RWK,RWK2                 SET UP SCAN REG        @OY20942
DEFLEN   EQU   *                                               @OY20942
         SR    RWK2,RWK2               CLEAR WORK REG
         IC    RWK2,LCBISZE            GET IDLES
         AR    RWK2,RBFR               ADD BUFFER ADDRESS
         LA    RWK2,42(0,RWK2)         ADD SIZE OF TCAM HDR
SECCMP   CLI   0(RWK2),ENTKY     IS THIS ENTER KEY
         BE    FIRSTCMP            YES,BR
         LA    RWK2,1(0,RWK2)      BUMP UP SCAN ADDRESS
         BCT   RWK,SECCMP          DEC AND CONTINUE SCAN FOR ENTER KEY
         B     NODEMF              DID NOT HIT ENTER KEY
FIRSTCMP CLC   0(4,RWK2),DEMFCON       IS THIS EMF REQUEST
         BE    FNDEMF                  YES BR CONTINUE
         CLC   0(4,RWK2),DEMFLOW       MAYBE IN LOWER CASE
         BE    FNDEMF                  YES,BR
         LA    RWK2,1(0,RWK2)
         BCT   RWK,FIRSTCMP
         B     NODEMF
         EJECT
FNDEMF   EQU   *
* AT THIS POINT WE HAVE DETERMINED THAT THIS MESSAGE IS A REQUEST
* FOR DEMF.  THIS MODULE WILL NOW INSERT CERTAIN INFORMATION IN THE
* PARM FIELD OF THE DEMF MACRO FOR THIS REQUEST
*         IF THE REQUEST IS FROM A LOCAL 3270
*             1. THE LINE ADDRESS
*             2. MODEL TYPE
*          IF THE REQUEST IS FROM A REMOTE 3270
*              1. THE LINE ADDRESS
*              2. THE CONTROL UNIT POLL CHAR
*              3. THE TERMINAL POLL CHAR
* IF THE REQUEST IS FOR TERMINAL INFORMATION BY SYMBOLIC NAME, CHECKS
* ARE MADE IF THE REQUESTED INFORMATION IS FOR A TCAM TERMINAL WHOSE
* LINE IS OPENED AND NOT NCP.  IF SO, THE PARM FIELD OF THE DEMF MACRO
* IS OVERLAYED WITH THE HARDWARE ADDRESS AND MODEL TYPE OF THE REQUEST
**********************************************************************
         OC    0(4,RWK2),FORTY         MAKE IT UPPERCASE
         MVI   EMFSTAT,R3270           TURM ON REMOTE FLAG
         TM    2(R15),EMF327L          IS THIS A LOCAL
         BZ    RMT3270                 NO BR ITS A REMOTE
         MVI   EMFSTAT,L3270           TURN ON LOCAL FLAG
*********    DETERMINE LINE ADDRESS AND STORE IT     *********
*********    IN PARM FIELD OF IEDEMFCB                          *****
RMT3270  SR    R7,R7                   CLEAR REGISTER
         IC    R7,LCBUCBX              RELATIVE LINE NUMBER
         L     RWK,LCBDCBPT            GET DCB ADR
         L     RWK,44(RWK)             LOAD ADR OF DEB
         LA    RWK,32(RWK)             POINT TO LIST OF UCB
         LA    R7,1(R7)                BUMP BY ONE FOR BCT
DO574    BCT   R7,DO576
         B     DO578
DO576    LA    RWK,4(RWK)              BCT TO RLN UCB
         B     DO574
DO578    L     RWK,0(RWK)              POINT TO UCB
         MVC   EMFLADR(3),13(RWK)      MOVE LINE ADR
*********   FOLLOWING CODE FINDS MODEL TYPE OF 3270 FROM UCB   ****
         MVI   EMFMODEL,MODTWO         ASSUME 3277 MODEL TWO
         CLI   EMFSTAT,R3270           IS INPUT FROM A REMOTE 3270
         BE    MODEL2                  YES,BR MODEL NOT IN UCB
         TM    16(RWK),UCBMONE         IS IT A 3277 MODEL ONE
         BNO   MODEL2                  BR, IT IS A MODEL TWO
         MVI   EMFMODEL,MODONE         MAKE IT A MODEL ONE
MODEL2   EQU   *
         EJECT
*********  DETERMINE IF USER ENTERED TERMINAL SYMBOLIC NAME   ***
*********  AND IF SO CONVERT IT TO HARDWARE ADDRESS           ***
         LA    R7,12(0,RBFR)       BUFFER ADR PLUS PREFIX
         AH    R7,PRFSIZE          ADD DATA SIZE
         SR    R7,RWK2             LGTH OF DATA ENTERED IN R7
         BNP   DO579               BR,IF R7 NOT PLUS NO DATA ENTERED
         STH   R7,LTHSAVE          SAVE LTH OF DATA ENTERED
         CLI   LTHSAVE+1,10        WAS MORE THAN 10 CHAR ENTERED
         BNH   DO579               BR,LESS THAN 11 CHAR ENTERED
         CLI   4(RWK2),SBA         IS IT FROM FORMATTED SCREEN?
         BNE   DO5781              BR,NO
         LA    RWK2,2(0,RWK2)      ADD TWO FOR THE CU TERM POLL CHARS
         BCTR  R7,R0               SUBTRACT
         BCTR  R7,R0               TWO FROM LTH ENTERED
         STH   R7,LTHSAVE          SAVE IT
         CLI   LTHSAVE+1,10        IS LTH GREATER THAN 10
         BNH   DO579               BR,NO NOT SYMBOLIC NAME CONVERT
DO5781   EQU   *
         CLC   5(4,RWK2),=C'3270'  IS 3270 2ND PARM
         BNE   DO579               BR,NO NEED TO CONVERT NAME
         CLI   10(RWK2),240        WAS A NUMERICAL LINE ENTERED
         BNL   DO579               BR,YES DO NOT CONVERT
         CLI   9(RWK2),SBA         IS IT SBA
         BE    DO579               YES,BR DID NOT ENTER SYMBOLIC NAME
         LA    R11,10(0,RWK2)      POINT R11 TO SYMBOLIC NAME
         LA    RBFR,0(0,RBFR)      CLEAR HIGH ORDER BYTE
         SR    R11,RBFR            R11 HAS AMOUNT OF DATE TO SYM NAME
         LA    R10,12(0)           BUFFER PREFIX LGTH IN R10
         AH    R10,PRFSIZE         PLUS LGTH OF DATA ENTERED
         SR    R10,R11             MINUS LENGTH UP TO SYMBOLIC NAME
*                                 EQUALS LENGTH OF SYMBOLIC NAME IN R10
         BNP   DO579               BR, IF NOT PLUS DID NOT ENTER
*                                  SYMBOLIC NAME.  IF R10 IS PLUS IT
*                                  CONTAINS LENGTH OF NAME PLUS ANY
*                                  SPACES PLUS TRAILING LINE CONTROL
***   GETTING TO THIS POINT MEANS USER ENTERED A DEMF REQUEST BY
***   SYMBOLIC NAME. SO CHECK IT TO SEE IF WE CAN CONVERT IT TO A
***   HARDWARE ADDRESS.
         SR    R7,R7               CLEAR FOR LATER USE
         LA    R10,1(0,R10)        SET R10 UP ONE FOR BCT
         LA    RWK,10(0,RWK2)      POINT RWK TO SYMBOLIC NAME
RFOUND   L     R11,AVTRNMPT        TNT ADDRESS
         USING IEDQTNT,R11         R11 WILL HAVE TNT PREFIX
         SR    R15,R15             CLEAR REG
         IC    R15,TNTENLEN        MAXIMUM TERMNAME LTH IN R15
         CLR   R15,R10             IS MAX LTH GREATER THAN DATA ENTERED
         BNL   AGAIN               YES,BR LEAVE R10 AS IS
         LR    R10,R15             LIMIT SEARCH TO MAX TERMNAME LENGTH
AGAIN    LA    R7,1(0,R7)          ADD ONE TO LENGTH OF NAME ENTERED
         CLI   0(RWK),90           IS IT A VALID CHAR
         BNL   NOTFOUND            BR,HAVE NOT FOUND LAST CHAR
         B     FOUND
NOTFOUND LA    RWK,1(0,RWK)        LOOK AT NEXT CHAR
         BCT   R10,AGAIN           BR,STILL GOT DATA TO LOOK AT
FOUND    BCTR  R7,R0               SUBTRACT ONE FOR EXEC MOVE
         BCTR  R7,R0               ONE MORE
         CLR   R15,R7              MAKE SURE NAME ENTERED IS NOT
         BNL   NOREV               LONGER THAN MAXIMUM LENGTH
         LR    R7,R15              OF TERMINAL NAMES IN TCAMS TNT
         BCTR  R7,R0
NOREV    EQU   *
         MVC   SYMNAME,FORTY       CLEAR TO BLANKS
         EX    R7,MVNAME           OR NAME INTO FIELD INTO UPPER CASE
         STC   R7,R7SAVE           SAVE LENGTH OF NAME FOR LATER USE
         ST    RWK2,RWK2SAVE       SAVE ADR OF DATA ENTERED FOR LATER
         SR    RWK,RWK             CLEAR
         LH    RWK2,TNTLEN         GET NUMBER OF TERMINAL ENTRIES
         N     RWK2,AVTCLRHI       CLEAR LEFT HALF OF REGISTER
         SR    R10,R10             CLEAR
         IC    R10,TNTENLEN        MAXIMUM TERMNAME LENGTH
         BCTR  R10,R0              SUB ONE FOR EXEC
         LA    R7,TNTFIRST         GET FIRST ENTRY ADDRESS
         DROP  R11
TESTTRM  EQU   *
         LA    RWK,1(RWK)          UPDATE OFFSET CNTR
         CR    RWK,RWK2            END OF TNT
         BH    DO590               NO SYMBOLIC NAME IN TCAM TNT
         EX    R10,COMPNAME        COMPARE NAMES
         BE    NAMEMAT             NAME
         LA    R7,4(R10,R7)        POINT TO NEXT ENTRY
         B     TESTTRM             CHECK NEXT ENTRY
NAMEMAT  EQU   *
***   GETTING TO HERE MEANS USER ENTERED A SYMBOLIC NAME THAT IS IN
***   TCAM TERMINAL NAME TABLE.  SO CHECK TO SEE IF IT IS A TCAM
***   TERMINAL THAT WE CAN CONVERT TO A HARDWARE ADDRESS.
         LA    R7,0(R7,R10)        ADD LENGTH TO GET ADR OF TRM
         MVC   TERMADR,0(R7)       MOVE ADR TO WORD FOR LOAD
         L     R10,TERMADR         TERMINAL TRM IN REG10
*              CHECK TO ENSURE THIS IS A TERMINAL ENTRY
         CLI   5(R10),TRMLGB       IS THIS A NCP LGB
         BE    FO2                 BR,YES A NCP TERMINAL DONOT CONVERT
         TM    11(R10),TRMVTAM     IS THIS A VTAM TERMINAL ENTRY
         BO    FO3                 BR,YES DO NOT CONVERT
         TM    0(R10),TERMSK       IS THIS A TCAM TERMINAL ENTRY
         BNE   DO590               BR,NO NOT A TERMINAL ENTRYSO IT
*                                  WILL NOT HAVE ADDRESSING CHARACTERS
         L     R7,0(R10)           QCB ADR IN R7
         L     RWK2,32(R7)         DCB OR LGB ADR IN RWK2
         CLI   27(RWK2),64         IS THIS A DCB, IS IT NON NCP
         BNE   FO3                 BR IT IS A NCP TERMINAL,NO CONVERT
         TM    48(RWK2),16       IS THE DCB OPENED?
         BNO   FO5                 BR,NO CAN NOT FIND LINE ADR
***  GETTING TO HERE MEANS WE HAVE A TCAM TERMINAL ENTRY THAT WE
***  WILL CONVERT TO A REAL HARDWARE ADDRESS.
         SR    R15,R15
         IC    R15,16(R10)         GET CH TABLE INDEX
         BCTR  R15,0               SUB ONE
         SR    RWK2,RWK2               CLEAR REG               @OZ29899
         LA    RWK2,4                  LOAD REG WITH A FOUR (4)@OZ29899
         SR    RAVT,RWK2               BACK AVT UP FOUR (4)    @OZ29899
         CLI   2(RAVT),X'C1'           IS IT C'A' = TCAM10 ?   @OZ29899
         BE    TCAM10A                 YES, BRANCH             @OZ29899
         SLL   R15,2                   NO, TCAM9, MULT BY FOUR @OZ29899
         B     CONTA                                           @OZ29899
TCAM10A  MH    R15,=H'6'               TCAM10, MULT BY SIX     @OZ29899
CONTA    AR    RAVT,RWK2               RESTORE RAVT BACK       @OZ29899
         A     R15,AVTCSTCS        ADD TO START OF TABLE
         TM    1(R15),M3270        IS IT A 3270 ?              @OZ29898
         BZ    FO6                 NO, DO NOT CONVERT          @OZ29898
         MVI   STATSW,R3270        TURN ON REMOTE
         TM    2(R15),EMF327L      IS IT A LOCAL
         BZ    UPFOUR              NO,BR IT IS A REMOTE
         MVI   STATSW,L3270
UPFOUR   EQU   *
         SR    RWK2,RWK2           CLEAR
         IC    RWK2,32(R7)         RELATIVE LINE NUM IN R7
         L     RWK,32(0,R7)        LOAD ADR OF DCB IN RWK
         L     RWK,44(0,RWK)       LOAD ADR OF DEB IN RWK
*                                  FOLLOWING CHECKS TO SEE IF THE LINE
*                                  IS OPEN. AT X'10' IN THE DEB IS THE
*                                  NUMBER OF EXTENTS, SO A COMPARISION
*                                  IS MADE BETWEEN THE NO. OF EXTENTS
*                                  AND THE REL. LINE NO.  THE REL LINE
*                                  NO. BEING HIGHER MEANS THE LINE IS
*                                  NOT OPEN AND WE CAN NOT GET LINE ADR
         SR    R11,R11             CLEAR
         IC    R11,16(0,RWK)       INSERT NO. EXTENT IN R11 FROM DEB
         CR    R11,RWK2            COMPARE NO. EXTENTS TO REL LINE NO.
         BL    FO7                 BR, CANNOT CONVERT TO HARDWARE ADR
*                                  BECAUSE LINE IS NOT OPENED.
*                                  ***       END OF ROUTINE      ***
         LA    RWK,32(RWK)         POINT TO LIST OF UCBS
CO574    BCT   RWK2,CO576
         B     CO578
CO576    LA    RWK,4(RWK)          BCT TO RLN
         B     CO574
CO578    SR    R7,R7               CLEAR
         IC    R7,R7SAVE           RESTORE SYMBOLIC NAME LTH IN R7
         LA    R7,FOX0(0,R7)       TURN ON F0 BITS TO INDICATE SWITCH
         STC   R7,EMFSIZE          STORE IT IN SIZE FIELD FOR QEMF
         L     RWK,0(RWK)          POINT TO UCB
         MVC   EMFLADR(3),13(RWK)  MOVE LINE ADR
         MVC   EMFSTAT,STATSW      MOVE LOCAL-REMOTE INDICATOR
         CLI   STATSW,L3270       IS IT A LOCAL 3270
         BE    DO590               YES,BR ALL FINISHED CONVERTING
         TM    0(R10),2            ARE OPTIONS FIELDS USED
         BZ    NOOPT               BR,NO OPTIONS FIELDS
         SR    R7,R7               CLEAR
         IC    R7,17(0,R10)        GET NO. OF OPTION FIELDS
         LA    R7,3(R10,R7)        ADD OFFSET
NOOPT    EQU   *                   REG7 POINTS INTO TRM AT-17 BEFORE
*                                  ADDRESSING CHARACTERS
         SR    RWK,RWK             CLEAR
         IC    RWK,18(0,R7)        GET CTRL UNIT ADR
         LA    RWK,CUTBL(RWK)           POINT TO CU ADDR CHAR  @OZ29913
         MVC   EMFCTADR(1),0(RWK)       STORE IT IN EMF PARM   @OZ29913
         IC    RWK,20(0,R7)        GET TERMINAL ADR
         STC   RWK,EMFCUADR        STORE IT IN EMF PARM
         B     DO590               FINISHED CONVERT RETURN
DO579    CLI   EMFSTAT,L3270           IS IT A 3270 LOCAL
         BE    DO590                   YES, BR
*  IT IS A REMOTE 3270 SO GET TERMINAL  ADDRESSING CHARACTERS INTO PAR
         SR    RWK2,RWK2               CLEAR
         IC    RWK2,LCBISZE            GET IDLES
         AR    RWK2,RBFR               ADD BUFFER ADDRESS
         LA    RWK2,42(0,RWK2)         ADD SIZE OF HEADER
*        RWK2 POINTS TO FIRST DATA BYT E
         CLI   0(RWK2),X'02'           IS IT STX
         BNE   DO580                   NO MUST BE ADDRESS
         LA    RWK2,1(0,RWK2)          POINT TO ADDRESS
DO580    MVC   EMFCTADR(2),0(RWK2)     MOVE ADR TO PARM
DO590    EQU   *
         L     RWK,4(R13)          LOAD WORK REG TO BUMP RETURN
         L     RWK2,12(RWK)        PLUS FOUR FOR GOOD RETURN
         LA    RWK2,4(0,RWK2)
         ST    RWK2,12(RWK)
RETURN   EQU   *
         L     R13,4(R13)           ADR OF PREVIOUS SACE AREA
         L     R14,12(R13)          LOAD RET ADR
         LM    R2,RBASE,28(R13)     RESTORE REST OF REGS
         MVI   12(R13),X'FF'        SER BYTE
         BR    R14                     RETURN
NODEMF   EQU   *
         MVI   EMFRTCD,RC8         MOVE RET CODE ON NO DEMF
         B     RETURN
FOO      STC   RWK,EMFLADR+1       STORE IT IN PARM FIELD
         MVI   EMFLADR,FOXFOX      INDICATE EMFLADR IS A SWITCH
         MVC   EMFLADR+2(1),R7SAVE MOVE TRM LNGTH TO PARM
         B     DO590               GET OUT,FINISHED
FO2      LA    RWK,2(0)            INSERT ERROR CODE  NCP LGB
         B     FOO
FO3      LA    RWK,3(0)                               VTAM TERMINAL
         B     FOO
FO5      LA    RWK,5(0)                               DCB NOT OPENED
         B     FOO
FO6      LA    RWK,6(0)                               NOT A 3270 TRM
         B     FOO
FO7      LA    RWK,7(0)                               LINE NOT OPENED
         B     FOO
MVNAME   OC    SYMNAME(0),10(RWK2)    EX INST MOVE TERM NAME
COMPNAME CLC   SYMNAME(0),0(R7)  EX INST
R7SAVE   DC    CL1' '
STATSW   DC    CL1' '
         DC    CL2' '
DEMFCON  DC    C'DEMF'
DEMFLOW  DC    X'84859486'             LOWER CASE DEMF
FORTY    DC    CL8' '                  USED TO MAKE UPPER CASE
SYMNAME  DC    CL8' '              WORK SPACE FOR CONVERT ROUTINE
         DC    CL4'0000'
         CNOP  0,4
TERMADR  DC    CL4' '              THESE TWO WORDS MUST BE ON WORD
RWK2SAVE DC    CL4' '              BOUNDRY FOR LOAD AND STORE INSTS
LTHSAVE  DC    XL2'00'
         CNOP   0,4
CUTBL    DC    X'00000000000000000000000000000000'             @OZ29913
         DC    X'00000000000000000000000000000000'             @OZ29913
         DC    X'20215E24243C262928292A2B2E202E41'             @OZ29913
         DC    X'264A4B4C4D4E4F5051525D3B3C3B2B21'             @OZ29913
         DC    X'2A4142434445464748494A4B4C4D4E4F'             @OZ29913
         DC    X'50515242434445464748495B5C5D5E28'             @OZ29913
         DC    X'40C100000000000000004A4B4C4D4E4F'             @OZ29913
         DC    X'000000000000000000005A5B5C5D5E5F'             @OZ29913
         DC    X'00000000000000000000000000000000'             @OZ29913
         DC    X'00000000000000000000000000000000'             @OZ29913
         DC    X'00000000000000000000000000000000'             @OZ29913
         DC    X'00000000000000000000000000000000'             @OZ29913
         DC    X'00C1C2C3C4C5C6C7C8C9000000000000'             @OZ29913
         DC    X'00D1D2D3D4D5D6D7D8D9000000000000'             @OZ29913
         DC    X'0000C2C3C4C5C6C7C8C9000000000000'             @OZ29913
         DC    X'50D1D2D3D4D5D6D7D8D9000000000000'             @OZ29913
         DS    0F                                              @OY20942
MAXSIZE  DC    X'002C'                                         @OY20942
X1E      DC    X'001E'                                         @OY20942
PATCH    DS    20F                                             @OY20942
EMF      DSECT
EMFSIZE  DS    CL2
EMFSTAT  DS    CL1
EMFMODEL DS    CL1
EMFLADR  DS    CL3
EMFCTADR DS    CL1
EMFCUADR DS    CL1
EMFRTCD  DS    CL1
*****    FOLLOWING DSECTS ARE TCAM RELEASE DEPENDENT. WHENEVER A
*****    NEW LEVEL OF TCAM IS INSTALLED THESE DSECTS SHOULD BE
*****    CHECKED TO SEE IF THEY HAVE CHANGED.
IEDQAVTD DSECT
AVTDUM   DS    82F                 NOT USED FILLER ONLY
AVTCSTCS DS    F
AVTDUM1  DS    23F                 NOT USED FILLER ONLY
AVTRNMPT DS    A
AVTDUM2  DS    72F
AVTCLRHI DS    2X
AVTDUM3  DS    XL2
AVTADBUF DS    A
AVTZERO  EQU   00
AVTCVTPT EQU   240
IEDQLCB  DSECT
LCBDUM1 DS 7F
LCBISZE  DS  XL1
LCBDUM2  DS XL3
LCBDUM3  DS 5F
LCBDCBPT DS A
LCBDUM5  DS 2F
LCBUCBX  DS    XL1
LCBDUM6  DS    XL3
LCBDUM7  DS    6F
LCBDUM4  DS XL1
LCBSCBA  DS AL3
IEDQPRF DSECT
PRFDUM DS 3F
PRFDUM1 DS XL1
PRFLCB DS  AL3
PRFSRCE DS H
PRFSIZE DS H
PRFSTAT1 DS XL1
PRFTSMSG EQU X'10'
PRFNHDRN EQU X'01'
IEDQTNT  DSECT
TNTDUM1  DS    CL40
TNTENLEN DS    CL1
TNTDUM2  DS    CL3
TNTLEN   DS    H
TNTDUM3  DS    CL36
TNTFIRST DS    C
IEDQSCB DSECT
SCBSTATE DS XL1
SCBLCK1N EQU X'20'
         END
