         PRINT OFF
         MACRO
&NAME    BNGHJN &LABEL,&HJN
         LCLC  &HJA,&HJB
&HJA     SETC  '&SYSPARM'(1,4)
&HJB     SETC  '&SYSPARM'(5,4)
         AIF   ('&LABEL' EQ '').NOBRNCH
         B     &LABEL .                 BR AROUND CONSTANTS
         SPACE 2
.NOBRNCH ANOP
         AIF   ('&NAME' EQ '').NOLABEL
         DC    C'&NAME' .              MODULE IDENTIFIER
.NOLABEL ANOP
         DC    X'&HJA' .                DATE OF MODIFICATION
         AIF   ('&HJN' NE 'HJN').DATE
         DC    X'&HJB' .                HJN OF MODIFICATION
.DATE    ANOP
         AIF   ('&LABEL' EQ '').NOLBL
         SPACE 2
&LABEL   DS    0H
.NOLBL   ANOP
         MEND
         PRINT ON
***********************************************************************
*                                                                     *
* MODULE NAME = BNGTQEMF                                              *
*                                                                     *
* DESCRIPTIVE NAME = TCAM DEMF MESSAGE QUEUE CONTROL MODULE           *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 1, LEVEL 1                                         *
*                                                                     *
* FUNCTION = 1) GETS INPUT MESSAGES FROM TCAM MESSAGE QUEUE(BNGTIN);  *
*            2) ACQUIRES STORAGE FOR BOTH INPUT AND OUTPUT MESSAGES   *
*               (2117 BYTES);                                         *
*            3) MOVES INPUT MESSAGE TO NEWLY ACQUIRED STORAGE AREA;   *
*            4) LINKS TO BNGIDISP WITH ADDRESS OF NEW STORAGE AREA;   *
*            5) WHEN CONTROL RETURNS TO BNGTQEMF, PUTS MESSAGE TO     *
*               OUTPUT MESSAGE QUEUE(BNGTQ) TO 3277 DISPLAY TERMINAL. *
*                                                                     *
* NOTES = NONE                                                        *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS = R10 CONTAINS ADDRESS OF I/O MESSAGE       *
*                                                                     *
*    PATCH LABEL = PATCH                                       @OY20942
*                                                                     *
* MODULE TYPE = TCAM MESSAGE QUEUE CONTROL                            *
*                                                                     *
*    PROCESSOR = ASSEMBLER H                                          *
*                                                                     *
*    MODULE SIZE = 1775 DECIMAL BYTES                                 *
*                                                                     *
*    ATTRIBUTES =                                                     *
*                                                                     *
* ENTRY POINT = BNGTQEMF                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = R14 LOADED WITH THE RETURN ADDR (USED BY SUBROUTINE)   *
*                                                                     *
* INPUT = PARAM LIST ITEMS USED ARE :                                 *
*         INPUT MESSAGE (MAX 117 BYTES)                               *
*                                                                     *
* OUTPUT = PARAM LIST ITEMS USED ARE :                                *
*         OUTPUT MESSAGE (MAX = 2008 BYTES)                           *
*                                                                     *
* EXIT-NORMAL = REGISTER 15 RETURN CODE RETURNED TO CALLER :          *
*               00 - OPERATION PERFORMED AS REQUESTED                 *
*                                                                     *
* EXIT-ERROR  = SYNAD ROUTINE AT LABEL - SNNAD                        *
*                                                                     *
* EXTERNAL REFERENCE = NONE                                           *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA AREAS = NONE                                                *
*                                                                     *
*    CONTROL BLOCK = DCB                                              *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = DCBD, OPEN, CLOSE, WTO, FREEMAIN, ABEND, LINK, GETMAIN     *
*                                                                     *
* CHANGE ACTIVITIES AS FOLLOWS:                                       *
* CHANGE 01  11/15/77  PROLOGUE ADDED                                 *
* CHANGE 02  02/22/78  BNGHJN MACRO ADDED                             *
*C061500-063000,082000                                         @OX21274
*C013500,068500,123000,124000,155500                           @OY20942
*D125500                                                       @OY20942
*A126000,126500,158500                                         @OY20942
*                                                                     *
***********************************************************************
BNGTQEMF CSECT
R0       EQU   0
R1       EQU   1
RWK      EQU   2
RWK3     EQU   3
RWK4     EQU   4
R4       EQU   4
RIN      EQU   5
R5       EQU   5
ROUT     EQU  10
RINDCB   EQU   7
ROUTDCB  EQU   8
RLNK     EQU   9
RBASE    EQU   12
RSAVE    EQU   13
RRET     EQU   14
RFOX     EQU   15
GOODOPEN EQU   16
SEVETEEN EQU   0017
SBA      EQU   X'11'
L1P1     EQU   X'4040'
SF       EQU   X'1D'
UNA      EQU   X'40'
UDM      EQU   X'C1'
PI       EQU   X'E8'
IC       EQU   X'13'
L2P1     EQU   X'C150'             3270 LINE2 COLUMN1
L3270    EQU   X'99'               LOCAL 3270 MASK
ENTKY    EQU   X'7D'               3270 ENTER KEY MASK
FOX0     EQU   240
FOXONE   EQU   241                 MASK TO INDICATE DEMF 3270 ENTERED
FOXFOX   EQU   255                 MASK TO INDICATE 2ND BYTE IS SW.
STX      EQU   X'02'               MASK FOR STX
RESKYBD  EQU   X'C3'               MASK FOR WCC CHAR TO RESET KEYBOARD
         EJECT
*
*        THIS MODULE RUNS AS A APPLICATION PROGRAM OF TCAM.  IT CAN BE
*        ATTACHED IN THE MCP AT INITILAZATION TIME OR RUN AS A
*        SEPARATE TASK.
*********  FOLLOWING MUST BE CODED IN USER MCP                    ****
*        BNGTPCB    PCB    BUFSIZE=502,MH=BNGTMH,BUFIN=4
*        BNGTQ    TPROCESS   PCB=BNGTPCB              DEMF PCB FOR PUT
*        BNGTIN   TPROCESS   PCB=BNGTPCB,QUEUES=MO    DEMF FOR GET
*********  FOLLOWING DD CARDS MUST BE IN JCL THAT STARTS THIS TASK ****
*        //BNGTINE  DD QNAME=BNGTIN
*        //BNGTOTE  DD QNAME=BNGTQ
***********************************************************************
*
         EJECT
***    INITILAZATION CODE
         STM   RRET,RBASE,12(RSAVE)    SAVE CALLERS REGS
         LR    RBASE,RFOX              SET MY BASE
         USING BNGTQEMF,RBASE
BNGTQEMF BNGHJN SKIPID                                         @OX21274
         LA    RFOX,SAVE               POINT TO MY SAVE AREA
         ST    RSAVE,4(RFOX)           STORE HIS SAVE ADDR IN MINE
         ST    RFOX,8(RSAVE)           STORE MY SAVE ADDR IN HIS
         LR    RSAVE,RFOX              SET UP MY SAVE REG
         OPEN  (OUTDCB,(OUTPUT))     OPENOUTPUT DCB
         LA    ROUTDCB,OUTDCB          LOAD DCB ADDRESS
         USING IHADCB,ROUTDCB
         TM    DCBOFLGS,GOODOPEN    WAS OPEN GOOD
         BO    NEXT                    YES,BRANCH
OPENERR  WTO   'BNG402I T OPEN FAILED FOR DEMF'
         B     CLOSDWN                  SHUTDOWN               @OY20942
NEXT     OPEN (INDCB,(INPUT))
         DROP  ROUTDCB
         LA    RINDCB,INDCB        SET UP INPUT DCB REG
         USING IHADCB,RINDCB
         TM    DCBOFLGS,GOODOPEN
         BNO   OPENERR
OPENOK   EQU   *
         WTO  'BNG400I  T DEMF SUBTASK ACTIVE'
         EJECT
***   WAIT HERE IN THE TCAM GET ROUTINE UNTIL TCAM PUTS A DEMF
***   REQUEST MESSAGE ON OUR QUEUE.
GET      GET   INDCB,INPUT             GET MESSAGE
         B     READTAB(RFOX)       CHECK GOOD RETURN
READTAB  B     GETMAIN             R/C 0 GOOD READ CONTINUE
         B     READER              R/C 4 SHOULD NOR OCCUR
         B     READER              R/C 8 SHOULD NOT OCCUR
         B     READER              R/C C SHOULD NOT OCCUR
         B     READER              R/C 10 BLKSIZE =0 OR LRECL=0
         B     REOPEN              R/C 14 TCAM CLOSED QUE
         DC    3F'0'               IN CASE MORE RETURN CODES PC
REOPEN   CLOSE INDCB               TCAM CLOSED QUE SO WE HAVE TO
         OPEN  (INDCB,(INPUT))     CLOSE DCB AND REOPEN
         B     GET                 CONTINUE
         EJECT
***   WE HAVE A DEMF REQUEST. GET STORAGE NEEDED BY THE DISPLAY
***   PROGRAMS TO WRITE TO THE TERMINAL.
GETMAIN  EQU   *                                               @OX21274
         GETMAIN  R,LV=2117        GET STORAGE FOR OUTPUT
         LR    ROUT,R1           SET UP OUTPUT REG OF GOTTEN STORAGE
**              FOLLOWING FIVE INSTRUCTIONS CLEAR INPUT AREA
         LR    R0,R1               ADDR OF FIELD TO BE CLEARED
         LA    R1,117              LENGTH OF FIELD TO CLEAR
         L     R5,=X'00000001'     LENGHT AND PADDING CHAR
         LA    R4,ZERO             FILL CHAR
         MVCL  0,R4                CLEAR FIELD
         LA    RIN,INPUT           GET INPUT I/O AREA
         USING INDSECT,RIN
         USING OUTDSECT,ROUT
         MVC   EMFDATA,INAREA        MOVE PARM FIELDS TO FORMAT SECT
         MVC   EMFNAME,INNAME      MOVE NAME OF TERML TO PARM
         MVI   EMFOTDAD,RESKYBD    WCC OF C3 TO RESET KEYBOARD
         CLI   INLADR,FOXFOX       IS 2ND BYTE A SWITCH
         BE    MSGOUT              YES,BR PUT OUT ERROR MESSAGE
         TM    TSWITCH,FOX0        IS THIS BYTE A SWITCH TO INDICATE
*                                  USER ENTERED SYMBOLIC NAME AND WE
*                                  MUST BLANK IT OUT OF THE DATA STREAM
*                                  AND SAVE IT.
         BO    SYMBOLIC            BR,YES
FO9      SR    RWK,RWK             CLEAR WORK REG
         LH    RWK,DCBLRECL        GET RECORD LENGTH IN REG
         LA    RWK3,SEVETEEN       ADD HEADER+8 FOR NAME+9 FOR PARM
         CLI   INDATA,SBA          IS FIRST BYTE SBA
         BE    BYPASS              YES,BR SET CORRECTLY NOW
         CLI   INDATA,ENTKY        IS FIRST BYTE ENTER KEY
         BE    ADDTHREE            YES,BR
         CLI   INDATA,STX          IS FIRST BYTE OF DATA STX
         BE    ADDSIX              YES,BR
         B     ADDFIVE             MUST BE CUA,TA,ENTKY,BA,BA
ADDSIX   LA    RWK3,1(RWK3)        STX,CUA,TA,ENTKY,BA,BA
ADDFIVE  LA    RWK3,2(RWK3)            CUA,TA,ENTKY,BA,BA
ADDTHREE LA    RWK3,3(RWK3)                   ENTKY,BA,BA
BYPASS   SR    RWK,RWK3            GET REAL INPUT LENGTH
         STH   RWK,EMFSIZE         MOVE IN SIZE FOR SCREEN FORMAT
         AR    RWK3,RIN            POINT RWK3 TO FIRST BYTE TO BE MOVED
         BCTR  RWK,0               SUB ONE FOR EXEC'D MOVE
         EX    RWK,EXECMVE         MOVE DATA
         EJECT
         LINK  EP=BNGTDISP         LINK TO DEMF SCREEN FORMAT PROGRAM
         TM    TSWITCH,FOX0        IS THE SYMBOLIC NAME SWITCH SET
         BO    GO1                 BR,YES
         CLI   EMFLADR,X'FF'       DO WE NEED TO INSERT TCAMID=?
         BE    F10                 YES,BR - SET BY BNGT3270
GO9      SR    RWK,RWK             CLEAR
         LH    RWK,EMFSIZE         GET SIZE
         LA    RWK,9(0,RWK)        ADD NINE FOR WCC CHAR AND 8 BYTE
         DROP  RINDCB                  TERMINAL NAME
         USING IHADCB,ROUTDCB
WRITE1   STH   RWK,DCBLRECL           STORE SIZE TO WRITE IN DCB
WRITE    PUT   OUTDCB,EMFWRITE         WRITE DATA TO TERMINAL
         B     TABLE(RFOX)             TABLE OF RETURN CODES
TABLE    B     RELCORE                 WRITE WAS SUCCESSFUL
         B     CLOSDWN                 MCP HAS CLOSED DOWN
         B WRITER                      SHOULD NOT OCCUR
         B INVTERM                     TERMINAL NAME INVALID
         B     WRITE                   WRITE WAS BUSY RETRY
         DC    3F'0'                   JUST IN CASE MORE RETURN CODES
RELCORE  LR    R1,ROUT                 INIT R1 FOR FREEMAIN
         FREEMAIN  R,LV=2117,A=(1)
         B     GET
SYMBOLIC SR    RWK,RWK             CLEAR
         MVC   SYMLTH,TSIZE        MOVE SIZE TO AREA
         NC    SYMLTH,ZEROFOX      REMOVE SWITCH INDICATOR BYTES
         IC    RWK,SYMLTH          LTH OF NAME ENTERED MINUS ONE
         SR    RWK4,RWK4           CLEAR
         LA    RWK4,TDATA          POINT RWK4 TO INPUT AREA
FO1      CLC   0(4,RWK4),=C'3270'  IS THIS 3270
         BE    FO2                 BR,YES
         LA    RWK4,1(0,RWK4)      LOOK AT NEXT BYTE
         B     FO1
FO2      MVC   FO6,FORTYS
         EX    RWK,FO3
         EX    RWK,FO4
         B     FO9
FO3      MVC   FO6(0),5(RWK4)
FO4      MVC   5(0,RWK4),FORTYS
EXECMVE  MVC   EMFINDAT(0),0(RWK3)     MOVE DATA TO FORMAT INPUT AREA
         EJECT
READER   WTO   'BNG403I T READ ERROR FILE INDCB PGM=BNGTQEMF'
         B     CLOSDWN                  SHUTDOWN               @OY20942
WRITER   WTO   'BNG404I T WRITE ERROR FILE OUTDCB PGM=BNGTQEMF'
         B     CLOSDWN                  SHUTDOWN               @OY20942
INVTERM  EQU   *
         WTO   'BNG405I T INVALID TCAM TERMINAL NAME'
CLOSDWN  EQU   *
         DROP  ROUTDCB                                         @OY20942
         LA    RINDCB,INDCB             GET DCB ADDRESS        @OY20942
         USING IHADCB,RINDCB                                   @OY20942
         TM    DCBOFLGS,GOODOPEN        IS DATA SET OPEN       @OY20942
         BNO   CKOUT                    NO, DON'T CLOSE THEN   @OY20942
         CLOSE INDCB
CKOUT    EQU   *                                               @OY20942
         DROP  RINDCB                                          @OY20942
         LA    ROUTDCB,OUTDCB           GET DCB ADDRESS        @OY20942
         USING IHADCB,ROUTDCB                                  @OY20942
         TM    DCBOFLGS,GOODOPEN        IS DATA SET OPEN       @OY20942
         BNO   EXIT                     NO, DON'T CLOSE THEN   @OY20942
         CLOSE OUTDCB
EXIT     EQU   *
         WTO   'BNG401I T DEMF SUBTASK CLOSED DOWN'
         L     RSAVE,4(RSAVE)          GET CALLERS SAVE
         LM    RRET,RBASE,12(RSAVE)    RESTORE CALLERS REGS
         SR    RFOX,RFOX               DO RETURN CODE THING
         BR    RRET                    RETURN TO CALLER
SNNAD    EQU   *
         CLOSE INDCB
         OPEN  (INDCB,(INPUT))
         B     GETMAIN
         EJECT
F10      MVC   FO6,INNAME          MOVE INPUT TERMINAL NAME
GO1      LA    RWK,131(ROUT)       POINT TO SYMBOLIC NAME IN OUTPUT
         CLC   135(7,ROUT),=C'BNG011I' IS IT MSG11 ERROR MSG?
         BE    GO3                 YES.
         CLC   135(7,ROUT),=C'BNG012I' IS IT MSG12 ERROR MSG?
         BE    GO2                 YES.
         MVC   38(16,RWK),TCAMID   MOVE IN SYMBOLIC NAME FOR 3270 SCR
         B     GO9
GO2      MVC   70(16,RWK),TCAMID   MOVE IN SYMBOLIC NAME MSG12
         B     GO9
GO3      MVC   107(16,RWK),TCAMID  MOVE IN SYMBOLIC NAME MSG11
         B     GO9
         EJECT
MSGOUT   EQU   *         THIS ROUTINE IS ENTERED WHEN THE SWITCH
***                      INDICATE A INVALID SYMBOLIC NAME HAS BEEN
***                      ENTERED AND WE WILL SEND A MESSAGE BACK TO
***                     THE USER TERMINAL.
         NI    MSGNUM+2,FOX0       TURN OFF LOW ORDER BITS
         OC    MSGNUM+2(1),EMFLADR+1    MOVE MSG NO. TO OUTPUT
         SR    RWK,RWK             CLEAR
         IC    RWK,EMFLADR+1
         BCTR  RWK,R0              SUB ONE
         MH    RWK,THIRTY8         MULT BY MSG LENGTH
         LA    RWK,MSG1(RWK)       GET ADR OF MSG
         MVC   EMFOTDAE+MSGLTH(MSG1LTH),0(RWK)  MOVE MSG TO OUTPUT
         MVC   MSGTERM,FORTYS      CLEAR NAME AREA TO BLANKS
         LA    RWK,INPUT+17        POINT REG TO INPUT AREA
         LA    R1,25               SET UP R1 TO LIMIT SCAN
RETRY    CLC   0(4,RWK),=C'3270'   IS IT 3270
         BE    DO1                 YES,BR FOUND INPUT
         LA    RWK,1(0,RWK)        ADD ONE TO INPUT POINTER
         BCT   R1,RETRY            LOOK AT NEXT POSITIONS OF INPUT
         B     DO3                 DID NOT FIND VALID NAME EXIT
DO1      CLI   5(RWK),X'4F'        IS A VALID SYMBOLIC
         BL    DO3                 NO,BR EXIT
         SR    RWK3,RWK3           CLEAR
         IC    RWK3,EMFLADR+2      INSERT TRM NAME LENGTH MINUS ONE
         EX    RWK3,DO2            MOVE TRM NAME TO OUTPUT AREA
DO3      MVC   EMFOTDAE(MSGLTH),MSG  MOVE MSG HDR TO OUTPUT AREA
         LA    RWK,79              PUT MSG LENGTH IN DCB
         B     WRITE1
DO2      MVC   MSGTERM(0),5(RWK) EXED INST TO MOVE TRM NAME
         EJECT
INDCB    DCB   BLKSIZE=117,DDNAME=BNGTINE,DSORG=PS,LRECL=117,          X
               MACRF=GM,OPTCD=WU,RECFM=U,                              X
               EODAD=CLOSDWN,SYNAD=SNNAD,BUFL=117              @OY20942
OUTDCB   DCB   RECFM=U,                MESSAGE PROCESSING              X
               DDNAME=BNGTOTE,         DD CARD MUST BE THIS NAME       X
               DSORG=PS,               TCAM DCB                        X
               LRECL=2008,             MESSAGE LENGTH ,WILL GET CHAMGE X
               MACRF=PM,         PUT USED                              X
               OPTCD=WU                MUST HAVE DESTINATION NAME
PATCH    DS    20F                                             @OY20942
         EJECT
SAVE     DC    18F'0'
ZERO     DC    X'00000000'
FORTYS   DC    CL9'         '
INPUT    DC    CL117'0'
         ORG   INPUT
TNAME    DS    CL8
TSWITCH  DS    CL1
         ORG   TSWITCH
TSIZE    DS    CL2
TSTAT    DS    CL1
TMODEL   DS    CL1
TLADR    DS    CL3
TTADR    DS    CL1
TCUADR   DS    CL1
TDATA    DS    CL100
SYMLTH   DC    CL1' '
ZEROFOX  DC    X'0F'
TCAMID   DC    C'TCAM ID='
FO6      DC    CL8' '              SAVE AREA FOR SYMBOLIC NAME
         CNOP  0,4
THIRTY8  DC    XL2'0026'
MSG      DS    0C'0'
         ORG   MSG
MSGHDR   DC    AL1(SBA),AL2(L1P1),AL1(SF,UDM),C'DEMF ',AL1(IC)
         DC    AL1(SBA),AL2(L2P1),AL1(SF,PI)
MSGID    DC    C'BNG'
MSGNUM   DC    C'450I T'
         DC    C' '
MSGTERM  DC    CL9' '
MSGLTH   EQU   *-MSG
MSG1     DC   C'IS NOT A TCAM TERM                    '
MSG1LTH  EQU  *-MSG1
MSG2     DC   C'IS A NCP LGB                          '
MSG3     DC   C'IS A VTAM TERMINAL                    '
MSG4     DC   C'IS EITHER A TPROCESS ,LOG,TLIST ENTRY '
MSG5     DC   C'DCB FOR THIS TERMINL NOT OPENED       '
MSG6     DC   C'IS NOT A 3270 TYPE   TERMINAL         '
MSG7     DC   C'LINE NOT OPENED FOR  THIS TERMINAL    '
         EJECT
INDSECT  DSECT
INNAME   DS    CL8
INSIZE   DS    CL2
INAREA   DS    CL7
         ORG   INAREA
INSTAT   DS    CL1
INMODEL  DS    CL1
INLADR   DS    CL3    NORMALLY INPUT TERMINAL LINE ADDRESS UNLESS
*                     BYTE ONE IS A X'FF' THEN BYTE TWO IS A ERROR
*                     CODE EXPLAINED BELOW, AND BYTE THREE IS THE
*                     LENGTH MINUS ONE OF THE SYMBOLIC NAME ENTERED.
*                     BYTE TWO
*                     01  NAME IS NOT A TCAM TERMINAL
*                     02  NAME IS A NCP LGB
*                     03  NAME IS A VTAM OR NCP TERMINAL
*                     04  NAME IS EITHER A TPROCESS,LOG,TLIST ENTRY
*                     05  DCB FOR THIS TERMINAL NOT OPENED
*                     06  NAME IS NOT A 3270 TERMINAL
*                     07  LINE NOT OPENED FOR THIS ENTRY
INCTADR  DS    CL1
INCUADR  DS    CL1
INDATA   DS    CL100
         EJECT
OUTDSECT DSECT
EMFSIZE  DS    CL2
EMFDATA  DS    CL7
         ORG   EMFDATA
EMFSTAT  DS    CL1
EMFMODEL DS    CL1
EMFLADR  DS    CL3
EMFCTADR DS    CL1
EMFCUADR DS    CL1
EMFINDAT DS    CL100
EMFWRITE DS    0XL2008
EMFNAME  DS    CL8
EMFOTDAD DS    CL1
EMFOTDAE DS    1999XL1
         EJECT
         PRINT   NOGEN
         DCBD  DSORG=(PS)
         END   BNGTQEMF
