***********************************************************************
*                                                                     *
* MODULE NAME = BNGIREAD                                              *
*                                                                     *
* DESCRIPTIVE NAME =  IMS DEMF BDAM MODULE                            *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 1, LEVEL 1                                         *
*                                                                     *
* FUNCTION = 1) LINKS TO BNGIDCBA (ALIAS FOR BNGISIMS) FOR ADDRESS OF *
*               THE ADDRESS OF THE SYS1.DEMFLOG DCB;                  *
*            2) DETERMINES IF THE FILE IS OPENED;                     *
*            3) READS THE REQUESTED RECORD INTO HIS I/O AREA;         *
*            4) PASSES THE ADDRESS OF I/O AREA AND STATUS OF READ TO  *
*               CALLER.                                               *
*                                                                     *
* NOTES = NONE                                                        *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS :                                           *
*    --------------------                                             *
*                                                                     *
*    ALL DEMF RECORDING DISPLAY COMPONENTS USE STANDARD REGISTER      *
*    CONVENTIONS WITH THE EXCEPTION OF THE IMS DISPLAY COMPONENTS.    *
*    CERTAIN IMS MODULES USE ADDITIONAL REGISTERS AND THE LINK MACRO  *
*    FOR PASSING OR RETRIEVING DATA PARAMETER INFORMATION AND ARE     *
*    DETAILED BELOW:                                                  *
*                                                                     *
*     MODULE    REGISTER    CONTENTS.                                 *
*    --------   --------    ------------------------------------      *
*    BNGIDISP     R5        PASSES ADDRESS OF IMS PROGRAM             *
*                           COMMUNICATION BLOCK (PCB) TO              *
*                           BNGI3270, BNGIMENU, BNGILOCL, AND         *
*                           BNGIRMOT.                                 *
*                                                                     *
*                 R11       PASSES TO BNGIREAD THE ADDRESS OF A       *
*                           PARAMETER LIST CONTAINING:                *
*                                                                     *
*                          1) 4-BYTES - ADDRESS OF I/O AREA FROM      *
*                                       BNGIREAD                      *
*                                                                     *
*                          2) 3-BYTES - RELATIVE BLOCK ADDRESS TO     *
*                                       BE READ BY BNGIREAD           *
*                                                                     *
*                          3) 2-BYTES - STATUS OF READ FROM           *
*                                       BNGIREAD                      *
*                                                                     *
*                 R12      FOR BNGI3270, PASSES THE ADDRESS OF A      *
*                          COMMUNICATION AREA CONTAINING              *
*                          UP TO SIX EDITTED PARM FIELDS, SAVE        *
*                          AND WORK AREAS, AND THE INPUT MESSAGE.     *
*                                                                     *
*                          FOR BNGILOCL, BNGIRMOT, AND BNGIMENU;      *
*                          PASSES THE ADDRESS OF THE INPUT MESSAGE.   *
*                                                                     *
*    BNGIREAD    LINK TO BNGIDCBA       RETRIEVES THE ADDRESS OF THE  *
*                (ALIAS FOR BNGISIMS)   ADDRESS OF THE SYS1.DEMFLOG   *
*                                       DCB.                          *
*                                                                     *
*                 R11      RECEIVES ADDRESS OF PARAMETER LIST         *
*                          (SEE BNGIDISP R11 FOR LIST).               *
*                                                                     *
*    BNGI3270     R5      RECEIVES ADDRESS OF IMS PCB FROM BNGIDISP   *
*                                                                     *
*                 R11     PASSES TO BNGIREAD THE ADDRESS OF A         *
*                         PARAMETER LIST (SEE BNGIDISP R11 FOR LIST)  *
*                                                                     *
*                 R12     RECEIVES FROM BNGIDISP THE ADDRESS OF THE   *
*                         COMMUNICATION AREA (SEE BNGIDISP R12 FOR    *
*                         CONTENTS OF AREA).                          *
*                                                                     *
*    BNGIMENU     R5      RECEIVES ADDRESS OF IMS PCB FROM BNGIDISP   *
*                                                                     *
*                 R12     RECEIVES ADDRESS OF INPUT MESSAGE FROM      *
*                         BNGIDISP.                                   *
*                                                                     *
*    BNGILOCL     R5      RECEIVES ADDRESS OF IMS PCB FROM BNGIDISP   *
*                                                                     *
*                 R12     RECEIVES ADDRESS OF INPUT MESSAGE FROM      *
*                         BNGIDISP.                                   *
*                                                                     *
*    BNGIRMOT     R5      RECEIVES ADDRESS OF IMS PCB FROM BNGIDISP   *
*                                                                     *
*                 R12     RECEIVES ADDRESS OF INPUT MESSAGE FROM      *
*                         BNGIDISP.                                   *
*                                                                     *
*    PATCH LABEL = NONE                                               *
*                                                                     *
* MODULE TYPE =  IMS BDAM READ                                        *
*                                                                     *
*    PROCESSOR = ASSEMBLER H                                          *
*                                                                     *
*    MODULE SIZE = 4414 DECIMAL BYTES                                 *
*                                                                     *
*    ATTRIBUTES = SERIALLY REUSABLE                                   *
*                                                                     *
* ENTRY POINT = BNGIREAD                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = R1  LOADED WITH THE PARAM LIST POINTER                 *
*              R14 LOADED WITH THE RETURN ADDR (USED BY SUBROUTINE)   *
*              R15 LOADED WITH ENTRY POINT                            *
*                                                                     *
* INPUT = PARAM LIST ITEMS USED ARE :                                 *
*                                                                     *
*                      PARMADDR DS    0F                              *
*                      IOAREA   DC    A(0)                            *
*                      RELREC   DC    XL3'0'                          *
*                      STATUS   DC    CL2'  '                         *
*                                                                     *
* OUTPUT = PARAM LIST ITEMS USED ARE :                                *
*                                                                     *
*                      PARMADDR DS    0F                              *
*                      IOAREA   DC    A(0)                            *
*                      RELREC   DC    XL3'0'                          *
*                      STATUS   DC    CL2'  '                         *
*                                                                     *
* EXIT-NORMAL = REGISTER 15 RETURN CODE RETURNED TO CALLER :          *
*               00 - OPERATION PERFORMED AS REQUESTED                 *
*                                                                     *
* EXIT-ERROR  = NONE                                                  *
*                                                                     *
* EXTERNAL REFERENCE = NONE                                           *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA AREAS = NONE                                                *
*                                                                     *
*    CONTROL BLOCK = DCB                                              *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = SAVE, RETURN, LOAD, DELETE, READ, WAIT                     *
*                                                                     *
* CHANGE ACTIVITIES AS FOLLOWS:                                       *
* CHANGE 01   11/15/77  PROLOGUE ADDED                                *
*                                                                     *
***********************************************************************
BNGIREAD TITLE '- DISPLAY EXCEPTION MONITORING FACILITY (DEMF) IMS BDAM,
                READ PROGRAM'
***********************************************************************
* THIS SECTION DOES PROGRAM INITIALIZATION, THEN GETS A RECORD        *
***********************************************************************
         PRINT NOGEN
PARMLIST DSECT
         USING *,PARMREG
IOAREA   DS    F               ADDRESS OF I/O AREA FOR CALLER
RELREC   DS    CL3             RELATIVE BDAM BLOCK TO READ
STATUS   DS    CL2             STATUS OF READ FOR CALLER
BNGIREAD CSECT ,                   RE-DO OF CICS DEMF (ORIGINALLY FERS)
         SAVE  (14,12)             SAVE CALLER'S REGISTERS
         BALR  BASE1,0
         USING *,BASE1,BASE2
         LA    BASE2,2048(BASE1)    SET SECOND BASE
         LA    BASE2,2048(BASE2)       FROM THE FIRST
         LA    4,SAVE              POINT TO OUR SAVE AREA
         ST    4,8(R13)            FORWARD CHAIN FROM OLD
         ST    R13,4(4)            BACKWARD CHAIN TO OLD
         LR    R13,4               FIRST BASE AND S/A FOR OUR REGS
         B     GO
SAVEID   DC    C'BNGIREAD - DEMF READ PROGRAM'
SAVE     DS    18F                 SAVE AREA
GO       DS    0H
A100     L     DCBREG,SAVEDCB      LOAD BDAM DCB ADDRESS
         LTR   DCBREG,DCBREG       TEST IF WE KNOW WHERE IT IS
         BNZ   READ                BYPASS LOAD IF WE ALREADY HAVE ADDR
         LOAD  EP=BNGIDCBA         ISSUE OS LOAD FOR DCB MODULE
         LR    DCBREG,0            SAVE ENTRY POINT ADDR
         DELETE EP=BNGIDCBA        DROP THE USE COUNT FOR THE MODULE
         L     DCBREG,0(DCBREG)    GET ADDR OF ADDR OF BDAM DCB
         L     DCBREG,0(DCBREG)    GET ADDR OF BDAM DCB
         LTR   DCBREG,DCBREG       IF EQ 0, DCB WAS NOT OPENED OK
         BZ    NOTOPEN             GO HERE IF FILE NOT AVAILABLE
         ST    DCBREG,SAVEDCB      NOW WE SAVE THE DCB ADDR
READ     DS    0H
         L     4,=A(INPUTA)        GET IOAREA INTO GPR 4
         ST    4,IOAREA            PASS I/O AREA ADDR TO CALLER
         MVC   READREL,RELREC      PRIME RELATIVE BLOCK ADDRESS
         XC    RDECB,RDECB         CLEAR THE DECB FOR BDAM READ
         READ  RDECB,DI,(DCBREG),(4),4096,0,READREL
         WAIT  ECB=RDECB           WAIT FOR COMPLETION OF READ
         MVC   STATUS,RDECB+1
RETURN   L     13,SAVE+4
         RETURN (14,12)
NOTOPEN  DS    0H                        OPEN FAILURE AND GO ON
         MVC   STATUS,=C'FF'
         B     RETURN
* PROGRAM CONSTANTS
         SPACE
SAVEDCB  DC    F'0'
READREL  DC    XL3'0'
         EJECT
* REGISTER EQUATES
         SPACE
R0       EQU   0
R1       EQU   1
BASE1    EQU   2
BASE2    EQU   3
PARMREG  EQU   11  PARAMETER REGISTER
DCBREG   EQU   5   DCB REGISTER POINTER
LINKREG  EQU   6   SUB-ROUTINE LINKAGE
OUTLOOP  EQU   7   OUTPUT LOOP CONTROL
OUTREG   EQU   8   OUTPUT DATA SCANNER
PCBREG   EQU   9   IMS PROGRAM COMMUNICATION BLOCK BASE
TABLEREG EQU   10  HEADING-DATA TABLE BASE
R11      EQU   11  PARMS PASSED IN THIS REGISTER FROM USER
R12      EQU   12  BASE REGISTER TWO - 4096-8191
R13      EQU   13  BASE REGISTER ONE - 0000-4095
R14      EQU   14
R15      EQU   15
         SPACE
         LTORG
INPUTA   DS    4096C
         END   BNGIREAD
