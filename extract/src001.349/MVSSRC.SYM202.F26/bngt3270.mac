         PRINT OFF
         MACRO
&NAME    BNGHJN &LABEL,&HJN
         LCLC  &HJA,&HJB
&HJA     SETC  '&SYSPARM'(1,4)
&HJB     SETC  '&SYSPARM'(5,4)
         AIF   ('&LABEL' EQ '').NOBRNCH
         B     &LABEL .                 BR AROUND CONSTANTS
         SPACE 2
.NOBRNCH ANOP
         AIF   ('&NAME' EQ '').NOLABEL
         DC    C'&NAME' .              MODULE IDENTIFIER
.NOLABEL ANOP
         DC    X'&HJA' .                DATE OF MODIFICATION
         AIF   ('&HJN' NE 'HJN').DATE
         DC    X'&HJB' .                HJN OF MODIFICATION
.DATE    ANOP
         AIF   ('&LABEL' EQ '').NOLBL
         SPACE 2
&LABEL   DS    0H
.NOLBL   ANOP
         MEND
         PRINT ON
***********************************************************************
*                                                                     *
* MODULE NAME = BNGT3270                                              *
*                                                                     *
* DESCRIPTIVE NAME = TCAM DEMF 3270 DISPLAY MODULE                    *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 1, LEVEL 1                                         *
*                                                                     *
* FUNCTION = 1) BNGT3270 IS LINKED TO BY BNGTDISP;                    *
*            2) HANDLES 3270 DISPLAY TERMINAL ERROR INFORMATION       *
*               REQUEST AND BITS PROMPTER REQUEST;                    *
*            3) ACCESSES SYS1.DEMFLOG DATA FILE AND COLLECTS THE      *
*               REQUESTED 3270 ERROR INFORMATION FROM IT;             *
*            4) FORMS OUTPUT MESSAGE AND WRITES IT TO THE 3270 DISPLAY*
*               TERMINAL; (ERR INFO AND SOME OF DISPLAY MESSAGES)     *
*            5) RETURNS CONTROL TO BNGTDISP.                          *
*                                                                     *
* NOTES = NONE                                                        *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS = R10 RECEIVES ADDRESS OF I/O MESSAGE AREA  *
*                           FROM BNGTDISP                             *
*                                                                     *
*    PATCH LABEL = NONE                                               *
*                                                                     *
* MODULE TYPE = TCAM DISPLAY                                          *
*                                                                     *
*    PROCESSOR = ASSEMBLER H                                          *
*                                                                     *
*    MODULE SIZE = 7623 DECIMAL BYTES                                 *
*                                                                     *
*    ATTRIBUTES = SERIALLY REUSABLE                                   *
*                                                                     *
* ENTRY POINT = BNGT3270                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = R1  LOADED WITH THE PARAM LIST POINTER                 *
*              R14 LOADED WITH THE RETURN ADDR (USED BY SUBROUTINE)   *
*              R15 LOADED WITH ENTRY POINT                            *
*                                                                     *
* INPUT = PARAM LIST ITEMS USED ARE :                                 *
*         IN MESSAGE ADDRESS  = TCAMFLDS (R10)                        *
*         IN MESSAGE COUNT    = VARIABLE (MAXIMUM=117 BYTES)          *
*                                                                     *
* OUTPUT = PARAM LIST ITEMS USED ARE :                                *
*         OUT MESSAGE ADDRESS = TIOADBAO (R10)                        *
*         OUT MESSAGE COUNT   = VARIABLE (MAXIMUM=2008 BYTES)         *
*                                                                     *
* EXIT-NORMAL = REGISTER 15 RETURN CODE RETURNED TO CALLER :          *
*               00 - OPERATION PERFORMED AS REQUESTED                 *
*                                                                     *
* EXIT-ERROR  = SYNAD ROUTINE AT LABEL - F1900                        *
*                                                                     *
* EXTERNAL REFERENCE = NONE                                           *
*                                                                     *
*    ROUTINES = LOCATE ROUTINE                                        *
*               GETTIOA ROUTINE (GET TERMINAL OUTPUT AREA)            *
*               BUOBR ROUTINE (BLOW-UP OBR ROUTINE)                   *
*               BUMDR ROUTINE (BLOW-UP MDR ROUTINE)                   *
*                                                                     *
*    DATA AREAS = SEE INPUT AND OUTPUT ABOVE                          *
*                                                                     *
*    CONTROL BLOCK = DCB (AT LABEL EMFILE)                            *
*                                                                     *
* TABLES = CALENDAR - CONVERT JULIAN DATE TO GREGORIAN DATE TABLE     *
*          LEAPTBL  - LEAP YEAR CALCULATION TABLE                     *
*          CCWTABL  - CONVERT 270X COMMAND CODE INTO ENGLISH          *
*          CSWTABL  - CONVERT LOCAL 327X STATUS OR 270X LINE STATUS   *
*                     INTO ENGLISH                                    *
*          SENSTABL - CONVERT 270X LINE SENSE CODE INTO ENGLISH       *
*          STATSTBL - CONVERT REMOTE 327X STATUS CODE INTO ENGLISH    *
*          SENS3272 - CONVERT LOCAL 327X SENSE CODE INTO ENGLISH      *
*          CCW3272  - CONVERT LOCAL 327X COMMAND CODE INTO ENGLISH    *
*          TPOPTBL  - CONVERT BTAM TP OP CODE INTO ENGLISH            *
*          TCAMPTOP - CONVERT TCAM TP OP CODE INTO ENGLISH            *
*          HXTOEB   - HEX CHARS TO EBCDIC CHARS TABLE                 *
*          EBTOHX   - EBCDIC CHARS TO HEX CHARS TABLE                 *
*                                                                     *
* MACROS = LINK, SAVE, RETURN, OPEN, READ, CHECK, CLOSE, TIME         *
*                                                                     *
******************CHANGE ACTIVITIES AS FOLLOWS:************************
* CHANGE 01  11/15/77  PROLOGUE ADDED                                 *
* CHANGE 02  02/27/78  BNGHJN MACRO ADDED                             *
*A143000,287000                                                @OZ30478
*D144500-145000                                                @OZ30478
*C081500-089000                                                @OZ30675
*A207500,485000,543500                                         @OZ31884
*D636000-639000                                                @OZ31884
*                                                                     *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1              REGISTER ONE
R8       EQU   8
REG9     EQU   9
BASE1    EQU   2                   1ST PROGRAM BASE REG.
BASE2    EQU   4                   2ND PROGRAM BASE REG.
TIOABAR  EQU   5                   TIOA POINTER.
FIOABAR  EQU   6                   FIOA POINTER.
DATAREG  EQU   7                   POINTER TO DATA IN EMF FILE BLOCK.
DATALOOP EQU   8                   LOOP REG FOR PROCESSING WITH DATAREG
OUTREG   EQU   9                   POINTER TO VARIABLE AREA IN TIOA.
LINKREG  EQU   11                  LOOP REG FOR PROCESSING WITH OUTREG.
OUTLOOP  EQU   12                  LINKAGE TO COMMON SUBROUTINES.
R14      EQU   14
R15      EQU   15
SBA      EQU   X'11'
SF       EQU   X'1D'
IC       EQU   X'13'
UMN      EQU   X'41'               UNPROT,NORMAL,MODIFIED
UNA      EQU   X'40'
PNA      EQU   X'60'
UHA      EQU   X'C8'
PDA      EQU   X'E8'
PIA      EQU   X'6D'
MNP      EQU   X'61'
SML2P1   EQU   X'40E8'
SML3P1   EQU   X'C150'
SML4P1   EQU   X'C1F8'
L1P1     EQU   X'4040'
L2P1     EQU   X'C150'
L3P1     EQU   X'C260'
L4P1     EQU   X'C3F0'
SML5P1   EQU   X'C260'   LINE 5 POSITION 1 FOR MOD-1 SCREEN
L1P36    EQU   X'40E3'
L1P7     EQU   X'40C6'
L3P39    EQU   X'C1F6'
L2P79    EQU   X'C25E'
L6P26    EQU   X'C6E9'
TCAMOBR  EQU   X'34'
BTAMOBR  EQU   X'30'
TCAMFLDS DSECT
         USING *,10
TIOATDL  DC    X'0000'    TERMINAL DATA LENGTH
TCTTETT  DS    CL1   91=REMOTE  99=LOCAL 3270
TCTTETM  DS    CL1    F1=MODEL 1  F2=MODEL 2 3270
TCTTEGU  DS    0H     EUATE LOCAL LINE ADDRESES
TCTLERLN DS    CL3     LINE ADDRESS RELATIVE LINE #
TCTTETA  DS    CL1   CONTROL UNIT ADDRESS
         DS    CL1     TERNINAL ADDRESS REMOTE
TIOADBA  DS    CL100        TERMINAL INPUT AREA
TCTTETI  DS    CL8          TCAM  SYMBOLIC ADDRESS
TIOWCC   DC    X'C3'       WRITE ERASE
TIOADBAO DS    CL2000     TERMINAL OUTPUT AREA
         ORG   TIOADBA             POINT TO INPUT AREA
SBA1     DS    CL3                 IST SBA POSITION
CODE     DS    CL4                 TRANS CODE.
SBA2     DS    CL3                 2ND SBA
PGRM     DS    CL4                 PROGRAM NAME.
SBA3     DS    CL3                 3RD SBA.
FLP      DS    CL15                FIRST/LAST/PREVIOUS IF BLOWUP RQST.
SBA4     DS    CL3                 4TH SBA.
BUX      DS    CL4                 X FOR BLOWUP REQUEST.
TIOITEM  DS    CL5                 ITEM ADDRESS TO BLOWUP.
         ORG   FLP
         DS    C                   FOR FORWARD AND BACKWARD PAGE REQ XH
         DS    CL3                 ALTERNATE POSN FOR 4TH SBA
TIOFIRST DS    CL5                 BLK & SLT 1ST ITEM ON SCREN.
TIOLAST  DS    CL5                 BLK & SLT FOR LAST
TIOPREV  DS    CL5                 BLK & SLT 1ST ITEM PREVIOUS SCREEN.
         DS    CL3                 5TH SBA
RTNX     DS    C                   X FOR RETURN FROM BLOWUP
BNGTDISP DSECT
         DS    4F                                              @OZ30675
         USING EMFILE,3                                        @OZ30675
EMFILE DCB DDNAME=EMFILE,DSORG=DA,MACRF=(RIC),OPTCD=R,                 X
               RECFM=F
SAVREG   DS    A                   RETURN SAVE AREA ADDRESS
SAVAREA  DS    18F                 DEMFDISP SAVE AREA
TCAFCAAA DS    A
TCTTEDA  DS    A                   ADDRESS OF PASSED FIELD
TCAPCPI  DC    CL8'00'    SYMBOLIC PROGRAM NAME TO LINK TO
TCAFCAA  DS    A                   FILE I/O AREA ADDRESS
         DS    0H
TWAAREA  EQU   *        START OF TRANSACTION WORK AREA PAASE PARMS
PARM1    DS    D                   1ST INPUT FIELD.
PARM2    DS    D                   2ND INPUT FIELD.
PARM3    DS    D                   3RD INPUT FIELD.
PARM4    DS    D                   4TH INPUT FIELD.
PARM5    DS    D                   5TH INPUT FIELD.
PARM6    DS    D                   6TH INPUT FIELD.
TWASBAS  DS    PL1                 COUNT OF SBA'S IN INPUT MSG.
TWAREL   DS    CL3                 FILE CONTROL RELATIVE BLK ADDR.
         DS    CL3            SAVE AREA FOR LINE/CTRLREL BLOCK NO.
DUBLWD   DS    D                   WORK AREA
TWATIOA  DS    F                   SAVE INPUT ADDRESS.
TWATCTID DS    F                   SAVE TERML ID.
TWASLOT  DS    F                   SLOT COUNTER.
SAVE14   DS    F                   SAVE AREA.
TWAOUT   DS    F                   SAVE OUTPUT HDR ADDR
LINESZ   DS    H                   LINE SIZE (3277 MOD 1 OR MOD 2)
LINELOOP DS    H                   NBR LINE ITEMS PER LINESZ.
OUTSWCH  DS    C
HXLINLIB DS    0CL6                LINE ADDRESS INCLUDING
         DS    C
HEXLINE  DS    CL2
HEXLIB   DS    CL3
HEXCTRL  DS    C                   CTRL UNIT POLL CHAR.
HEXTERM  DS    C                   TERML POLL CHAR.
NEWFIRST DS    CL5                 SAVE AREA FOR OLD BLK & SLOT NBR.
NEWPREV  DS    CL5                 SAVE AREA FOR OLD BLK & SLOT NBR.
BITFIRST DS    CL5                 SAVE AREA FOR FIRST BLK & SLOT NBR.
BITLAST  DS    CL5                 SAVE AREA FOR LAST  BLK & SLOT NBR.
BITPREV  DS    CL5                 SAVE AREA FOR PREV  BLK & SLOT NBR.
        CNOP   0,4
*                             IF 2ND SBA = L1P7
         ORG   PARM6+X'70'         JUMP PAST DEMFDISP WORK AREA
WORK15   DS    CL5
WORK16   DS    CL5
         DS    CL6
         DC    C'FILE I/O AREA'
         CNOP  0,4
         USING *,FIOABAR
DFHFIOA  DS    0H      FILE I/O AREA ADDRESS
FIOADBA  DC    4096X'00'        FILE INPUT AREA
ENDDATA  DC    X'FFFFFFFF'     MARK END OF DATA
         ORG   ENDDATA+8
PROGSTRT EQU   *
         ORG   FIOADBA
CTRLLAST DS    F                   LAST USED ADDR IN DATASET
CTRLTOTL DS    F                   TOTAL BLOCKS IN DATASET
CTRLBACK DS    F                   BACKWARD PTR IN CTRL BLK CHAIN
CTRLFWD  DS    F                   FORWARD PTR IN CTRL BLK CHAIN
CTRLENTS DS    C                   NBR ENTRIES IN THIS CTRL BLK
         DS    CL15                FOR FUTURE USE
CTRLSLT1 DS    0C                  START OF CTRL SLOT NBR ONE.
         ORG  FIOADBA
DATAPORT DS    CL6                 PORT & LIB ADDR
DATATIME DS    CL4                 TIME BLOCK CREATED
DATADATE DS    CL4                 DATE BLOCK CREATED
DATAFWD  DS    CL3                 FORWARD CHAIN ADDR
DATABACK DS    CL3                 BACKWAD CHAIN ADDR
BLKLTH   DS    CL2                 NBR BYTES USED IN THIS BLOCK
         DS    CL10
DATASLT1 DS    0C                  START OF 1ST DATA SLOT
DATASLOT DSECT
         USING *,DATAREG
DATALTH  DS    C                   LTH OF THIS SLOT
LINEID   DS    CL6                 LINE AND LIB ADDR
ENTRYTYP DS    C                   ENTRY TYPE - MDR OR OBR
         DS    CL3
MDRTYPE  DS    C                   TYPE HDWE - 03 EQUALS 3270
         DS    CL3
DATE     DS    CL4                 DATE OF ERROR - 00YY0DDF
TIME     DS    CL4                 TIME OF ERROR - HHMMSSHH
         DS    CL8
MDRCUA   DS    CL2                 MDR CHAN & UNIT ADDR
MDRCTRL  DS    C                   MDR CTRL UNIT POLL ADDR
MDRTERM  DS    C                   MDR TERMIL POLL ADDR
MDRSTATS DS    CL2                 REMOTE 3270 STATUS
         DS    CL2
OBRCCW   DS    CL8                 LAST CCW EXECUTED
OBRCSW   DS    CL8                 CHANNEL STATUS WORD
OBRDVDEP DS    CL1
         DS    CL5
OBRUCB   DS    C
         DS    CL1
OBRSDR   DS    CL1                 NUMBER BYTES OF STATICAL DATA
         DS    CL1
OBRCUA   DS    CL2                 OBR CHAN & UNIT ADDR
OBRPOLL  DS    CL2                 POLL CHAR
         DS    CL12
OBRSENSE DS    CL2                 OBR SENSE BYTES
         ORG   DATALTH
CTRLPORT DS    CL6                 PORT & LIB ADDRESS
         DS    CL4
CTRLFRST DS    CL3                 REL BLK ADDR 1ST BLK IN CHAIN.
         DS    CL3
SLOTLTH  EQU   *-CTRLPORT
BNGT3270 CSECT
         SAVE  (14,12)       SAVE CALLERS REGISTER
         BALR  BASE1,0
         USING *,BASE1,BASE2
BNGC3270 BNGHJN SKIPID                                         @OZ30478
         LA    BASE2,2048(BASE1)   SETUP PROGRAM BASE REGS.
         LA    BASE2,2048(BASE2)
A0000    ST    13,SAV3270R
         LA    13,SAV3270A
         L     TIOABAR,TCTTEDA     POINT TO TIOA INPUT AREA.
         L     FIOABAR,TCAFCAA  INITIALIZE FILE REG TO POINT FILE AREA
         OPEN  (EMFIL1,(INPUT))
         CP    TWASBAS,=P'2'       WERE LESS THAN 2 FIELDS INPUT?
         BNL   J010                NO. GO EXAMINE INPUT FURTHER.
A010     XC    TWAREL,TWAREL       YES. THIS IS A NEW RECAP REQUEST.
         CLC   PARM3(3),=C'ALL'    IF PARM 3 IS ONE OF THE DEMF
         BE    A140                DISPLAY CODES, THIS INPUT WAS FROM
         CLC   PARM3(4),=C'CTRL'          FORMATTED SCREEN.
         BE    A140                ZERO IT OUT AND TRY AGAIN.
         CLC   PARM3(4),=C'LINE'
         BE    A140
         CLC   PARM3(4),=C'3270'
         BE    A140
         CLC   PARM4(3),=C'ALL'
         BE    A130
         CLC   PARM4(4),=C'CTRL'
         BE    A130
         CLC   PARM4(4),=C'LINE'
         BE    A130
         CLC   PARM4(4),=C'3270'
         BE    A130
         CLC   PARM5(3),=C'ALL'
         BE    A120
         CLC   PARM5(4),=C'CTRL'
         BE    A120
         CLC   PARM5(4),=C'LINE'
         BE    A120
         CLC   PARM5(5),=C'3270'
         BE    A120
         OC    PARM3,PARM3         WAS LINE ID INPUT?
         BNZ   B140                YES. GO SEARCH CTRL BLK FOR IT.
****************************************************************
*  THIS CODE INSERTS LINE, CTRL AND TERMINAL ADDRESS OF THE    *
*  TERMINAL OF THE USER IF HE ENTERS ONLY DEMF 3270
****************************************************************
         MVC   DUBLWD+5(3),TCTLERLN     MOVE LINE ADDRESS TO WORK
         TR    DUBLWD,EBTOHX                 TR LINE ADDRESS
         PACK  DUBLWD(3),DUBLWD+5(4)        PACK IT
         MVC   HEXLINE,DUBLWD        MOVE IT TO BE USED LATER
         MVC   HEXCTRL,TCTTETA                 MOVE IN CTRL UNIT
         MVC   HEXTERM,TCTTETA+1         MOVE IN TERMINAL ADDRESS
         MVC   PARM3(3),TCTLERLN             MOVE IN LINE NUMBER
         MVI   TCTLERLN,X'FF'      IND TO BNGTQEMF TCAMID SHOULD BE INS
         CLI   TCTTETT,X'99'            IS THIS A LOCAL 3270
         BE    D079                     YES,BR NOT CTRL AOR TERM ADR
         UNPK  DUBLWD,TCTTETA(2)
         TR    DUBLWD,HXTOEB
         MVC   PARM4(2),DUBLWD+5    MOVE IN CTRL ADDRESS
         UNPK  DUBLWD,TCTTETA+1(2)
         TR    DUBLWD,HXTOEB
         MVC   PARM5(2),DUBLWD+5       MOVE IN TERMINAL ADDRESS
         B     D079
******************
* THIS SECTION OF CODE SHOULD NOT BE ENTERED AS  SYMBOLIC
*    ADDRESS FOR TCAM DEMF ARE NOT SUPPORTED
****************
A120     XC    PARM5,PARM5
         B     A010
A130     XC    PARM4(16),PARM4
         B     A010
A140     XC    PARM3(24),PARM3     ZERO OUT PARMS 3, 4, AND 5.
         B     A010
*
* AT THIS POINT, IF NO INPUT LINE/CTRL/TERM WAS SPECIFIED, THE
* ADDRESS PERTAINING TO THE REQUESTOR'S TERMINAL HAS BEEN PLACED
* IN THE INPUT PARMS AS IF THEY HAD BEEN KEYED IN.
*
B140     MVC   DUBLWD+5(3),PARM3
         TR    DUBLWD,EBTOHX            GET LINE,
         PACK  DUBLWD(3),DUBLWD+5(4)    CONTROL UNIT,
         MVC   HEXLINE,DUBLWD           AND
         MVC   DUBLWD+5(3),PARM4        TERMINAL
         TR    DUBLWD,EBTOHX            ADDRESS
         PACK  DUBLWD(2),DUBLWD+5(3)    INTO HEX
         MVC   HEXCTRL,DUBLWD           FORMAT
         MVC   DUBLWD+5(3),PARM5        FOR COMPARING
         TR    DUBLWD,EBTOHX            TO DATA IN
         PACK  DUBLWD(2),DUBLWD+5(3)    DISK FILE.
         MVC   HEXTERM,DUBLWD
D079     ST    TIOABAR,TWATIOA     SAVE ADDR OF ORIGINAL INPUT AREA.
         BAL   LINKREG,GETTIOA     SETUP NEW OUTPUT AREA WITH HDRS.
D080     MVC   FILREC,TWAREL
         READ  ECB1,DI,EMFIL1,(6),4096,0,FILREC
         CHECK ECB1
         B     D190    BRANCH AROUND I/O ERRRO RTNE
F1900    MVC   TIOADBAO(IOERRLTH),IOERRMSG
         MVC   TIOATDL,=AL2(IOERRLTH)
         UNPK  DUBLWD,TWAREL(4)         SETUP I/O ERROR MSG WITH
         TR    DUBLWD,HXTOEB            BLOCK NBR THAT
         MVC TIOADBAO+(IOBLOCK-IOERRMSG)(6),DUBLWD+1  WAS BEING READ,
         LR    R8,R0               DECB ADR IN R8
         USING DECB,R8
         UNPK  DUBLWD,EXCPBITS(3)       AND THE BDAM FILE
         DROP  R8
         TR    DUBLWD,HXTOEB            EXCEPTION RETURN CODE.
         MVC   TIOADBAO+(IOCODE-IOERRMSG)(4),DUBLWD+3
D170     CLOSE EMFIL1
         L     13,SAV3270R
         RETURN  (14,12)
D190     L     FIOABAR,TCAFCAA     POINT TO FILE I/O AREA.
         SR    DATALOOP,DATALOOP   SETUP TO LOOP ON NBR OF
         IC    DATALOOP,CTRLENTS   ENTRIES IN THIS CNTRL BLK.
         LTR   DATALOOP,DATALOOP   VERIFY THAT THE DATA SET
         BNZ   E030                IS NOT EMPTY.
         B     Z50
E030     LA    DATAREG,CTRLSLT1    POINT TO FIRST CTRL SLOT.
E040     OC    PARM4,PARM4         WAS A CTRL UNIT ADDR INPUT?
         BNZ   E110                YES. LINE ID IS CORRECT.
         XC    DUBLWD,DUBLWD       NO. THIS MUST BE RQST FOR LOCAL.
         MVC   DUBLWD(3),HXLINLIB  ROUND CUA ADDR DOWN
         NI    DUBLWD+2,B'11100000'     TO EVEN CHAN ADDR.
         CLC   DUBLWD(6),CTRLPORT       LOOK FOR EQUAL LINE ADDRESS.
         B     E120
E110     CLC   HXLINLIB,CTRLPORT   IS THIS SLOT = RQSTD LINE ADDR?
E120     BE    E200                YES. GO READ ITS DATA BLOCK.
         LA    DATAREG,SLOTLTH(DATAREG)  NO. BUMP TO NEXT CTRL SLOT.
         BCT   DATALOOP,E040            MORE SLOTS TO LOOK AT?
         OC    CTRLBACK,CTRLBACK        NO. IS THERE ANOTHER CTRL BLK?
         BZ    Z50                 NO. SEND 'NO ERRS' MSG.
         MVC   TWAREL,CTRLBACK+1        YES. SAVE NEXT BLOCK ADDR.
         B     D080
E200     MVC   TWAREL,CTRLFRST     MOVE DATA BLOCK ADDR.
         OC    TWAREL,TWAREL       IS THERE ANY DATA BLOCKS ?  @OZ31884
         BZ    T190                NO, WRITE 'NO DATA BLKS MSG'@OZ31884
F020     MVC   FILREC,TWAREL        ALIGN AND MKE RELOCATABLE
         READ  ECB2,DI,EMFIL1,(6),4096,0,FILREC
         CHECK ECB2
F050     L     FIOABAR,TCAFCAA     POINT TO DATA BLOCK.
         XC    TWASLOT,TWASLOT     INITIALIZE SLOT COUNT TO 1.
         LH    DATALOOP,BLKLTH     SETUP TO LOOP ON NBR BYTES IN REC.
         LA    DATAREG,DATASLT1    POINT TO FIRST DATA ITEM.
F080     DS    0H
         L     R15,TWASLOT         ADD 1 TO SLOT COUNT.
         LA    R15,1(R15)      ADD ONE TO REG
         ST    R15,TWASLOT
         CLI   ENTRYTYP,X'91'      IS THIS ENTRY AN MDR RECORD?
         BNE   H060                NO. TEST FURTHER.
         CLI   MDRTYPE,X'03'       YES. IS IT A 3270?
         BE    G060                YES. TEST FURTHER.
F120     SR    R1,R1               NO. BUMP
         IC    R1,0(DATAREG)     TO NEXT DATA ITEM.
         LTR   R1,R1               PRECENT ENDLESS LOOP
         BZ    F240                IN CASE GARBAGE IN RECORD.
         AR    DATAREG,R1
         SR    DATALOOP,R1         ALL DONE WITH THIS BLOCK?
         LTR   DATALOOP,DATALOOP
         BP    F080                NO. PROCESS NEXT ITEM.
         OC    DATAFWD,DATAFWD     YES. IS THERE ANOTHER BLOCK?
         BZ    F240                NO. GO WIND UP OUTPUT.
         MVC   TWAREL,DATAFWD      YES. GET CHAIN ADDR AND
         B     F020                GO READ NEXT BLOCK.
F240     CLI   OUTSWCH,X'FF'       WAS ANY OUTPUT CREATED?
         BNE   Z50                 NO. SEND 'NO ERRS' MSG.
G020     LA    1,TIOADBAO           YES. COMPUTE
         SR    OUTREG,1            TOTAL DATA LTH.
         STH   OUTREG,TIOATDL
         B     D170                GO TRANSMIT DISPLAY.
G060     CLC   MDRCTRL,HEXCTRL     IS THIS MDR FOR RQSTED CTRL UNIT?
         BNE   F120                NO. SKIP IT.
         CLC   MDRTERM,HEXTERM     YES. IS IT FOR REQUESTED TERML?
         BNE   F120                NO. SKIP IT.
         MVC   0(MDRLTH,OUTREG),MDRHDR   YES. SETUP BUILD 1 MDR LINE.
         UNPK  DUBLWD,MDRSTATS(3)
         TR    DUBLWD,HXTOEB       FILL IN REMOTE STATUS BYTES.
         MVC   (MDRSTAT-MDRHDR)(4,OUTREG),DUBLWD+3
G100     UNPK  DUBLWD,TWAREL(4)
         TR    DUBLWD,HXTOEB       FILL IN THIS BLK NBR.
         MVC   (MDRBBBSS-MDRHDR)(3,OUTREG),DUBLWD+4
         UNPK  DUBLWD,TWASLOT(5)
         TR    DUBLWD,HXTOEB       FILL IN THIS SLOT NBR.
         MVC   (MDRBBBSS+3-MDRHDR)(2,OUTREG),DUBLWD+5
         L     R14,TWAOUT          POINT TO OUTPUT BBBSS FIELDS
         CLC   (BBBSS-HDR3)(5,R14),=C'BBBSS' IS 'FIRST' FILLED IN?
         BNE   G200                YES,SKIP IT
         MVC   (BBBSS-HDR3)(5,R14),(MDRBBBSS-MDRHDR)(OUTREG) NO,DO IT.
         CLC   (BBBSS+10-HDR3)(5,R14),=C'BBBSS'  HOW ABOUT 'PREV'?
         BNE   G200                ALREADY THERE SKIP IT.
         MVC   (BBBSS+10-HDR3)(5,R14),(BBBSS-HDR3)(R14)  NO USE 'FIRST'
G200     MVC   (BBBSS+5-HDR3)(5,R14),(MDRBBBSS-MDRHDR)(OUTREG) FILL  XH
*                                                  IN 'LAST' BBBSS
         UNPK  DUBLWD,TIME
         MVC   (MDRHRS-MDRHDR)(2,OUTREG),DUBLWD+1   FILL IN HOURS
         MVC   (MDRMINS-MDRHDR)(2,OUTREG),DUBLWD+3     AND MINUTES.
G230     MVI   OUTSWCH,X'FF'       INDICATE SOME OUTPUT WAS CREATED.
         TIME    DEC          GET SYSTEM TIME
         STCM  0,15,DATTIME        STORE TIME
         STCM  1,15,DATDATE        STORE DATE
         CP    DATE,DATDATE        DID ERROR HAPPEN TODAY?
         BE    G240                YES,BR HIGH INTENSITY
         SP    DATDATE,=P'1'       MAKE DATE YESTERDAY
         CP    DATE,DATDATE        DID IT HAPPEN YESTERDAY
         BNE   G230A               NO,BR LEAVE INTENSITY NORMAL
         CLC   TIME,DATTIME        IS IT OLDER THAN 24 HRS?
         BNL   G240                NO,BR MAKE IT HIGH INTENSITY
G230A    LA    R1,LEAPTBL
G231     CLC   DATE+1(1),0(1)      SEE IF
         BE    G232                ERROR HAPPENED
         LA    R1,1(R1)            IN LEAP YEAR
         CLI   0(R1),X'FF'         IS IT END OF TABLE
         BNE   G231                NO,BR TRY NEXT YEAR
         ZAP   CALENDAR+8(2),=P'28' NOT LEAP YEAR
         B     G233                CONTINUE
G232     ZAP   CALENDAR+8(2),=P'29' YES,LEAP YEAR
G233     LA    R15,CALENDAR
G234     MVN   DATE+3(1),=X'0C'    MAKE SURE SIGN IS CORRECT
         CLC   DATE+2(2),3(R15)    COMPUTE MONTH
         BNH   G235                AND DAY FROM
         SP    DATE+2(2),3(2,R15)  DAY NUMBER
         LA    R15,5(R15)
         B     G234
G235     MVC   (MDRHRS-MDRHDR)(3,OUTREG),0(R15)  MOVE MONTH AND
         UNPK  (MDRMINS-MDRHDR)(2,OUTREG),DATE+2(2)   DAY TO OUTPUT
         OI    (MDRMINS+1-MDRHDR)(OUTREG),X'F0'
         B     H030
G240     MVI   1(OUTREG),PDA       YES.
         MVI   4(OUTREG),UHA
         MVI   15(OUTREG),PDA
H030     LA    OUTREG,MDRLTH(OUTREG)    BUMP TO NEXT OUTPUT SLOT.
         BCT   OUTLOOP,F120        MORE TIOA ROOM LEFT?
         B     G020                NO. GO WIND UP OUTPUT.
H060     CLI   ENTRYTYP,X'91'      IS THIS ENTRY AN OBR?
         BE    F120                NO. SKIP IT.
         CLC   OBRCUA,HEXLINE      YES. IS IT REQUESTED CUA ADDR?
         BNE   F120                NO. SKIP IT.
         CLI   OBRUCB,X'10'        IS THIS A LOCAL
         BE    H070                YES,BR
         CLC   OBRPOLL(1),HEXCTRL  IS THIS OBR FOR RQSTED CTRL UNIT?
         BNE   F120                NO,SKIP IT
         CLC   OBRPOLL+1(1),HEXTERM IS THIS OBR FOR RQSTED TERMINAL?
         BNE   F120                NO,SKIP IT
H070     MVC   0(MDRLTH,OUTREG),MDRHDR  YES. SETUP COMMON MDR/OBR HDR.
         UNPK  DUBLWD,OBRCSW+4(3)
         TR    DUBLWD,HXTOEB       FILL IN CSW STATUS
         MVC   (OBRSNS-MDRHDR)(4,OUTREG),DUBLWD+3
         UNPK  DUBLWD,OBRSENSE(3)                                    XH
         TR    DUBLWD,HXTOEB       FILL IN SENSE BYTES.
         MVC   (MDRSTAT-MDRHDR)(4,OUTREG),DUBLWD+3                   XH
         NI    OBRCCW+5,X'3F'      CLEAN UP TP OP CODE.
         UNPK  DUBLWD,OBRCCW+5(2)
         TR    DUBLWD,HXTOEB       FILL IN TP OP CODE.
         MVC   (OBRTPOP+1-MDRHDR)(2,OUTREG),DUBLWD+5
         MVI   (OBRTPOP-1-MDRHDR)(OUTREG),C'B'
         CLI   ENTRYTYP,TCAMOBR    IS THIS A TCAM OBR?
         BNE   J005                NO, LEAVE 'B' FOR BTAM
         MVI   (OBRTPOP-1-MDRHDR)(OUTREG),C'T'    T=TCAM
J005     UNPK  DUBLWD,OBRCCW(2)
         TR    DUBLWD,HXTOEB       FILL IN CHANL COMMAND.
         MVC   (OBRCMD-MDRHDR)(2,OUTREG),DUBLWD+5
         B     G100                GO FILL IN DATA COMMON TO MDR/OBR.
J010     CLC   TIOADBA+8(2),=AL2(L1P7)  IS SECOND FIELD LINE 1 POSN 7?
         BE    A010                YES. THIS IS NEW INPUT.
         CLC   TIOADBA+8(2),=AL2(L6P26) IS SECOND FIELD L2P26 FROM MENU
         BE    A010                YES,BR THIS IS NEW INPUT
         LA    R14,TIOADBA         SCAN THE INPUT TO SEE
J020     CLI   0(R14),C'%'         IF THE SPECIAL CHAR
         BE    J140                '%" IS PRESENT WHICH
         CLI   0(R14),X'00'        WAS PUT OUT BY THE BLOWUP
         BE    J050                ROUTINE TO INDICATE A
         LA    R14,1(R14)          ROUTINE FROM BLOWUP.
         B     J020
J050     CLC   PARM3(5),=C'BBBSS'  IF NO FIELDS WERE SELECTED, GO TO
         BE    S190                    INVALID INPUT REQUEST.
         CLC   PARM4(4),=C'PAGE'   IS THIS A 'PAGE' REQUEST?
         BNE   L160                NO. MUST BE 'BLOWUP' REQUEST.
         CLC   TIOFIRST,=C'BBBSS'  WAS A GOOD DISPLAY ON SCREEN?
         BE    S190                    NO. TRIED TO PAGE FROM NO-ERRS
         CP    TWASBAS,=P'4'       YES. IS IT RETURN FROM BLOWUP?
         CLC   19(2,R14),=X'C1D3'       PAGE REQUEST           @OZ30478
         BE    ALTBBB                   YES, POINT TO NEXT FLD @OZ30478
         CLC   19(2,R14),=X'C17B'       PAGE BKWRD LARGE SCREEN@OZ30478
         BE    ALTBBB                   YES, POINT TO NEXT FLD @OZ30478
         CLC   19(2,R14),=X'406B'       PAGE FRWRD SMALL SCREEN@OZ30478
         BE    ALTBBB                   YES, POINT TO NEXT FLD @OZ30478
         B     PTOK                     NO, POINTER IS OK      @OZ30478
ALTBBB   CLC   PARM5(7),=C'FORWARD'     IS IT PAGE FORWARD     @OZ30478
         BNE   J191                     NO, MUST BE BACKWARD   @OZ30478
         MVC   NEWFIRST,TIOLAST+4       OLD LAST TO NEW FIRST  @OZ30478
         MVC   NEWPREV,TIOFIRST+4       OLD FIRST TO NEW PREV  @OZ30478
         TR    TIOLAST+4,EBTOHX         TRANSLATE OLD LAST     @OZ30478
         PACK  DUBLWD,TIOLAST+4(6)      INTO HEX               @OZ30478
         B     J110                     CONTINUE               @OZ30478
J191     MVC   NEWFIRST,TIOPREV+4       OLD PREV TO NEW FIRST  @OZ30478
         MVC   NEWPREV,TIOPREV+4        OLD PREV TO NEW PREV   @OZ30478
         TR    TIOPREV+4,EBTOHX         TRANSLATE OLD PREV     @OZ30478
         PACK  DUBLWD,TIOPREV+4(6)      INTO HEX               @OZ30478
         B     J110                     CONTINUE               @OZ30478
PTOK     EQU   *                                               @OZ30478
         CLC   PARM5(7),=C'FORWARD'  NO. IS IT PAGE FORWARD?
         BNE   J190                NO. MUST BE PAGE BACKWARD.
         MVC   NEWFIRST,TIOLAST    YES. PROMOTE LAST & FIRST FROM OLD
         MVC   NEWPREV,TIOFIRST    SCREEN TO NEW FIRST & PREVIOUS.
         TR    TIOLAST,EBTOHX
         PACK  DUBLWD,TIOLAST(6)   GET OLD 'LAST' BLK & SLOT IN HEX.
J110     MVC   TWAREL,DUBLWD+3     SETUP TO READ APPROPRIATE BLK.
         MVC   TWASLOT(1),DUBLWD+6  SETUP TO LOCATE APPROPRIATE SLOT.
         B     K010       GO READ BLK WHERE DISPLAY WILL RESUME.
J140     SH    R14,=H'18'          BACKUP TO BBBSS FIELD
         MVC   BITFIRST(15),0(R14) ALIGN IT IN CASE NO PAGE ENTERED  XH
         MVC   NEWFIRST,BITFIRST   RESTORE OLD 'FIRST' & 'PREVIOUS'  XH
         MVC   NEWPREV,BITPREV     AS NEW 'FIRST' & 'PREVIOUS'       XH
         TR    BITFIRST,EBTOHX                                       XH
         PACK  DUBLWD,BITFIRST(6)  GET OLD 'FIRST' ADDR & SLOT IN HEXXH
         B     J110
J190     MVC   NEWFIRST,TIOPREV    OLD 'PREVIOUS' NOW BECOMES NEW
         MVC   NEWPREV,TIOPREV     'PREVIOUS' AND 'FIRST'
         TR    TIOPREV,EBTOHX
         PACK  DUBLWD,TIOPREV(6)   GET OLD 'PREVIOUS' IN HEX.
         B     J110
K010     BAL   LINKREG,LOCATE      LOCATE SLOT TO RESUME AT.
         XC    PARM4(16),PARM4     ZERO OUT INPUT PARMS.
         CLI   ENTRYTYP,X'91'      IS THIS AN MDR?
         BE    K020                YES,LINE ADDRESS IS OK
         XC    HXLINLIB,HXLINLIB   NO,DON'T USE ROUNDED DOWN ADDR
         MVC   HXLINLIB+1(2),OBRCUA   GET ACTUAL CUA INSTEAD
         B     K030
K020     MVC   HXLINLIB,LINEID
K030     UNPK  DUBLWD,HEXLINE(3)   RESTORE LINE NBR FROM DATA
         TR    DUBLWD,HXTOEB       BLOCK AS IF IT HAD BEEN KEYED IN.
         MVC   PARM3(3),DUBLWD+4
         CLI   ENTRYTYP,X'91'      IS THIS AN MDR RECORD?
         BE    K040                YES,PUT IN POLL CHARRS.
         CLI   OBRUCB,X'10'        IS THIS A LOCAL
         BE    L110                YES,BR NO POLL CHARS
         MVC   HEXCTRL,OBRPOLL     MOVE CNTRL UNIT ADDR
         MVC   HEXTERM,OBRPOLL+1   MOVE TERMINAL ADDR
         B     K050
K040     MVC   HEXCTRL,MDRCTRL     YES.
         MVC   HEXTERM,MDRTERM     RESTORE CTRL UNIT ADDR
K050     UNPK  DUBLWD,HEXCTRL(3)   AND TERMINAL ADDR
         TR    DUBLWD,HXTOEB       AS IF THEY HAD
         MVC   PARM4(2),DUBLWD+3   BEEN KEYED IN.
         MVC   PARM5(2),DUBLWD+5
L110     BAL   LINKREG,GETTIOA     SETUP OUTPUT AREA WITH HEADERS.
         L     R15,TWAOUT
         MVC   (BBBSS-HDR3)(5,R15),NEWFIRST FINISH HEADER WITH ADDR
         MVC   (BBBSS+10-HDR3)(5,R15),NEWPREV  OF FIRST & PREVIOUS BLK.
         B     F080                GO BUILD ANOTHER PAGE.
L160     CLC   PARM2(4),=C'TERM'     IS THIS COMPANION INPUT?
         BE    L200                YES. GO HUNT FOR CTRL & LINE ADDR.
**       FOLLOWING TEN  INSTRUCTIONS CHECK IF A FIELD WAS X'ED   33333
**       ON A BLOWUP REQUEST                                     33333
         MVC   WORK15,TIOITEM      MOVE XED FIELD TO WORK AREA   33333
         TR    WORK15,EBTOHX       TRANS TO HEX                  33333
         MVC   WORK16,FLP          MOVE FIRST BBBSS TO WORK AREA 33333
         TR    WORK16,EBTOHX       TRANSLATE TO HEX              33333
         MVC   WORK16,FLP+5        MOVE LAST BBBSS TO WORK ARE   33333
         TR    WORK16,EBTOHX       TRANSLATE TO HEX              33333
         TR    TIOITEM,EBTOHX      OK. GET REQUESTED ITEM ADDR.
         PACK  DUBLWD,TIOITEM(6)        AND SLOT NUMBER.
         MVC   TWAREL,DUBLWD+3          INTO HEX.
         MVC   TWASLOT(1),DUBLWD+6
         BAL   LINKREG,LOCATE      LOCATE APPROPRIATE SLOT IN DATA BLK.
** INITIALIZE I/O AREA
** TO ALL 40'S SO THAT BLOW UP WILL FORMAT PROPERLY
         ST    DATALOOP,R8SAVE     SAVE REGISTER EIGHT
         LA    0,TIOADBAO          FIELD ADDRESS TO BE CLEARED
         LA    1,2000              LENGTH OF FIELD TO BE CLEARED
         L     REG9,=X'40000001'  LENGTH AND PADDING CHARACTER
         LA    DATALOOP,FORTY      PLACE 40'S IN INITIALIZED AREA
         MVCL  0,DATALOOP          MOVE CHARACTER TO FIELD
         L     DATALOOP,R8SAVE     RESTORE REGISTER EIGHT
**    END OF STORAGE INITAILIZATION CODE
         LA    TIOABAR,TIOADBAO    RELOAD OUTPUT AREA
         ST    TIOABAR,TCTTEDA     FOR THE BLOWUP DISPLAY.
         XC    P,P                 CLEAR AREA
         CLI   ENTRYTYP,BTAMOBR    IS THIS A OBR?
         BE    L161                YES,BR HANDLE OBR
         CLI   ENTRYTYP,TCAMOBR    IS THIS A TCAM OBR
         BE    L161                YES,BR WILL ONLY HAVE LINE ADR
         UNPK  P3(4),DATAPORT+1(3) MOVE LINE ADR FROM RECORD
         MVI   P3+3,X'00'
         TR    P3,HXTOEB           PUT IT IN DISPLAY FORMAT
         UNPK  P4(3),MDRCTRL(2)    NO,GET CTRL UNIT POLL CHAR
         MVI   P4+2,X'00'
         TR    P4,HXTOEB           PUT IT IN DISPLAY FORMAT
         UNPK  P5(3),MDRTERM(2)    GET TERMINAL POLL CHAR
         MVI   P5+2,X'00'
         TR    P5,HXTOEB           PUT IT IN DISPLAY FORMAT
         B     L162
L161     UNPK  P3(4),OBRCUA(3)     GET  LINE ADR FROM MDR RECORD
         MVI   P3+3,X'00'
         TR    P3,HXTOEB           PUT IT IN DISPLAY FORMAT
         CLI   OBRUCB,X'10'        IS THIS A LOCAL
         BE    L162                YES,BR NO POLLING CHARS
         UNPK  P4(3),OBRPOLL(2)    GET CNTRL UNIT POLL CHARS
         MVI   P4+2,X'00'
         TR    P4,HXTOEB           PUT IT IN DISPLAY FORMAT
         UNPK  P5(3),OBRPOLL+1(2)  GET TERMINAL POLL CHARS
         MVI   P5+2,X'00'
         TR    P5,HXTOEB
L162     EQU   *
         MVC   TIOADBAO(HDR1LTH),HDR1         SET UP
         MVC   TIOADBAO+HDR1LTH(4),=C'3270'   FIRST LINE HEADER
         LA    OUTREG,TIOADBAO+HDR1LTH+4      BUMP OUTPUT ADDRESS
         CLI   TCTTETM,C'2'        IS THIS A MODEL TWO SCREEN
         BNE   L163
         MVC   0(3,OUTREG),HDR2    YES,ADD ANOTHER SBA
         LA    OUTREG,3(OUTREG)    THIS 80 CHAR SCREEN
L163     MVC   0(HDR3LTH,OUTREG),HDR3     MOVE PAGE REQUEST LINE
         MVC   (BBBSS-HDR3)(15,OUTREG),TIOADBA+17 MOVE BBBSS
         LA    OUTREG,HDR3LTH(OUTREG)     BUMP OUTREG ADDRESS
         MVC   0(HDR4LTH,OUTREG),HDR4     MOVE THE RECAP LINE HDR
         OC    P5,P5               IS THERE A TERMINAL ADDRESS
         BZ    L165                NO,BR TO SHOW ONLY LINE ADR
         MVC   (HDR4TERM-HDR4)(2,OUTREG),P5    FILL IN THE
         MVC   (HDR4CTRL-HDR4)(2,OUTREG),P4    ID OF THIS TERMINAL
         MVC   (HDR4LINE-HDR4)(3,OUTREG),P3    CTRL UNIT AND LINE
L164     CLI   TCTTETM,C'2'        IS THIS SCREEN A MODEL 2?
         BE    M060                YES. MOVE APPROPRIATE HEADER.
         MVC   TIOADBAO+152(BUHDR1LT),BUHDR1   NO. MOVE MODEL 1 BLOWUP
         LA    OUTREG,TIOADBAO+BUHDR1LT+152  POINT TO NEXT BYTE OF OUTA
         B     M080
L165     MVC   8(6,OUTREG),=C'LOCAL '   SO CHANGE HEADER TO
         MVC   14(3,OUTREG),P3          LOCAL FORMAT
         MVC   17(24,OUTREG),=24C' '    BLANK OUT REST OF LINE
         B     L164
L200     TR    PARM3,EBTOHX        PARM3 IS BBBSS OF CTRL UNIT DISPLAY.
         PACK  DUBLWD,PARM3(6)     GET ITS BLK & SLT IN HEX.
         MVC   TWAREL,DUBLWD+3
         MVC   TWASLOT(1),DUBLWD+6
         BAL   LINKREG,LOCATE      LOCATE BLK & SLOT OF PREVIOUS DISPLY
         XC    PARM3,PARM3         ERASE BBBSS GARBAGE.
         UNPK  PARM3(4),DATAPORT+1(3)   MOVE LINE ADDR FROM RECORD.
         MVI   PARM3+3,X'00'
         TR    PARM3,HXTOEB        MAKE IT LOOK LIKE IT WAS KEYED IN.
         XC    PARM4,PARM4
         CLI   ENTRYTYP,X'91'      IS THIS AN OBR?
         BNE   L270                YES,BR HANDLE OBR RECORD    .
         UNPK  PARM4(3),MDRCTRL(2) NO. GET CTRL UNIT POLL CHAR.
         MVI   PARM4+2,X'00'
         TR    PARM4,HXTOEB        MAKE IT LOOK LIKE IT WAS KEYED IN.
         B     A010
L270     CLI   OBRUCB,X'10'        IS THIS A LOCAL
         BE    L280                YES,BR NO POLL CHARS
         UNPK  PARM4(3),OBRPOLL(2) GET CNTRL UNIT POLL CHARS
         MVI   PARM4+2,X'00'
         TR    PARM4,HXTOEB        MAKE IT LOOK LIKE IT WAS KEYED IN
         B     A010
L280     MVC   PARM3,PARM5         UNROUND TERML ADDRESS
         XC    PARM5,PARM5         ERASE GARBAGE IF LOCAL CU.
L300     B     A010
M060     MVC   TIOADBAO+152(BUHDR2LT),BUHDR2  MOVE MODEL 2 BLOWUP HDR
         LA    OUTREG,TIOADBAO+BUHDR2LT+152 POINT TO NEXT BYTE OF OUTAR
M080     UNPK  DUBLWD,TIME         MOVE TIME OF
         MVC   0(2,OUTREG),DUBLWD+1     ERROR TO OUTPUT.
         MVI   2(OUTREG),C':'
         MVC   3(2,OUTREG),DUBLWD+3
         LA    1,LEAPTBL
M090     CLC   DATE+1(1),0(1)           FIGURE OUT IF
         BE    M100                     ERROR HAPPENED
         LA    1,1(1)                 DURING LEAP YEAR.
         CLI   0(1),X'FF'
         BNE   M090
         ZAP   CALENDAR+8(2),=P'28'
         B     M110
M100     ZAP   CALENDAR+8(2),=P'29'
M110     LA    R15,CALENDAR
M120     MVN   DATE+3(1),=X'0C'    PROTECT AGAINST GARBAGE IN PACKED
         CLC   DATE+2(2),3(R15)    FIELD.  COMPUTE MONTH AND
         BNH   M130                     DAY FROM DAY NUMBER.
         SP    DATE+2(2),3(2,R15)
         LA    R15,5(R15)
         B     M120
M130     MVC   6(3,OUTREG),0(R15)       MOVE MONTH TO OUTPUT.
         UNPK  9(2,OUTREG),DATE+2(2)    MOVE DAY TO OUTPUT.
         OI    10(OUTREG),X'F0'
         MVC   LINESZ,=H'40'
         MVC   LINELOOP,=H'4'
         CLI   TCTTETM,C'2'        SETUP CORRECT LINE SIZE & NBR
         BNE   M140                     ITEMS FOR SCREEN MODEL.
         MVC   LINESZ,=H'80'
         MVC   LINELOOP,=H'12'
M140     EQU   *
         CLI   ENTRYTYP,X'91'      IS THIS ENTRY AN OBR?
         BNE   M160                YES.
         BAL   LINKREG,BUMDR       NO. BLOW UP AN MDR ENTRY.
M150     LA    R1,TIOADBAO
         SR    OUTREG,R1
         STH   OUTREG,TIOATDL      FINISH UP THE OUTPUT.
         CLOSE EMFIL1
         L     13,SAV3270R
         RETURN  (14,12)
M160     BAL   LINKREG,BUOBR       BLOWUP AN OBR ENTRY.
         B     M150
BUOBR    MVC   13(5,OUTREG),=C'TP-OP'   MOVE THE
         UNPK  DUBLWD,OBRCCW+5(2)       TP OP CODE
         TR    DUBLWD,HXTOEB            TO OUTPUT AREA.
         MVC   19(2,OUTREG),DUBLWD+5
         LA    R15,TPOPTBL
         CLI   ENTRYTYP,TCAMOBR    IS THIS A TCAM OBR?
         BNE   O060                NO,REG15 POINTS AT BTAM
         L     R15,=A(TCAMTPOP)    YES,POINT REG15 TO TCAM TPOP TABLE
O060     CLC   0(2,R15),DUBLWD+5
         BE    O120
         CLC   0(2,R15),=X'FFFF'
         BE    O120
         LA    R15,20(R15)
         B     O060
O120     MVC   22(18,OUTREG),2(R15) MOVE EXPLANATION OF TP OP CODE.
         AH    OUTREG,LINESZ       BUMP TO NEXT LINE OF OUTPUT.
O170     MVC   0(11,OUTREG),=C'DATA LENGTH'
         UNPK  DUBLWD,OBRCCW+6(3)
         TR    DUBLWD,HXTOEB       MOVE DATA LENGTH.
         MVC   12(4,OUTREG),DUBLWD+3
         MVC   21(14,OUTREG),=C'RESIDUAL COUNT'
         UNPK  DUBLWD,OBRCSW+6(3)
         TR    DUBLWD,HXTOEB
         MVC   36(4,OUTREG),DUBLWD+3
         AH    OUTREG,LINESZ       BUMP TO NEXT OUTPUT LINE.
         MVC   0(15,OUTREG),=C'CHANNEL COMMAND'
         UNPK  DUBLWD,OBRCCW(2)
         TR    DUBLWD,HXTOEB       MOVE THE CHANNEL COMMAND.
         MVC   16(2,OUTREG),DUBLWD+5
         LA    R15,CCWTABL
         CLI   OBRUCB,X'40'        IS THIS A REMOTE?
         BE    P070                YES,USE 270X COMMANDS
         L     R15,=A(CCW3272)     NO,USE THE 3272 COMMANDS
P070     CLC   0(2,R15),DUBLWD+5
         BE    P130
         CLC   0(2,R15),=X'FFFF'
         BE    P130
         LA    R15,20(R15)
         B     P070
P130     MVC   19(18,OUTREG),2(R15) MOVE CCW EXPLANATION.
         AH    OUTREG,LINESZ       BUMP TO NEXT OUTPUT LINE.
         MVC   4(5,OUTREG),=C'SENSE'    MOVE SENSE & STATUS HEADERS.
         MVC   24(6,OUTREG),=C'STATUS'
         UNPK  DUBLWD,OBRSENSE(3)  MOVE SENSE                        XH
         TR    DUBLWD,HXTOEB       BYTES TO
         MVC   10(4,OUTREG),DUBLWD+3    OUTPUT AREA                  XH
         UNPK  DUBLWD,OBRCSW+4(3)  MOVE OBR STATUS
         TR    DUBLWD,HXTOEB       BYTES TO
         MVC   31(4,OUTREG),DUBLWD+3    OUTPUT AREA
         AH    OUTREG,LINESZ       BUMP TO NEXT OUTPUT LINE
         LR    R14,OUTREG          TEMPORARY POINTER TO SENSE OUTPUT.
         LH    R1,LINELOOP         LIMIT DISPLAY IF MOD 1 SCREEN.
         LA    R15,SENSTABL
         CLI   OBRUCB,X'40'        IS THIS A REMOTE?
         BE    P200                YES USE 270X SENSE
         L     R15,=A(SENS3272)    NO,USE 3272 SENSE
P200     MVC   DUBLWD(1),OBRSENSE  SETUP TO TEST SENSE BITS.         XH
         NC    DUBLWD(1),0(R15)
         CLC   DUBLWD(1),0(R15)
         BE    Q050
Q010     LA    R15,19(R15)         BUMP TO NEXT SLOT IN SENSE TABL.
         CLI   0(R15),X'FF'
         BNE   P200
         B     Q080
Q050     MVC   0(18,R14),1(R15)    MOVE EXPLANATION OF 1 SENSE BIT.
         AH    R14,LINESZ          BUMP TO NEXT SENSE OUTPUT AREA.
         BCT   R1,Q010             ANY TIOA ROOM LEFT?
Q080     ST    R14,SAVE14
         LR    R14,OUTREG          NO. SETUP TO LOOK AT STATUS BITS.
         LH    R1,LINELOOP
         LA    R15,CSWTABL
Q110     MVC   DUBLWD(2),OBRCSW+4
         NC    DUBLWD(2),0(R15)
         CLC   DUBLWD(2),0(R15)
         BE    Q180
Q150     LA    R15,20(R15)
         CLI   0(R15),X'FF'
         BNE   Q110
         B     Q210
Q180     MVC   20(18,R14),2(R15)   MOVE EXPLANATION OF 1 CSW STATUS BIT
         AH    R14,LINESZ         BUMP TO NEXT OUTPUT SLOT
         BCT   R1,Q150             ANY TIOA ROOM LEFT?
Q210     C     R14,SAVE14          NO.
         BH    R010
         L     R14,SAVE14          SET OUTPUT REG TO HIGHER
R010     LR    OUTREG,R14          OUTPUT ADDRESS (SENSE OR STATUS).
         BR    LINKREG
BUMDR    MVC   24(6,OUTREG),=C'SENSE '  MOVE SENSE HDR.
         UNPK  DUBLWD,MDRSTATS(3)  MOVE MDR
         TR    DUBLWD,HXTOEB       STATUS BYTES
         MVC   31(4,OUTREG),DUBLWD+3    TO OUTPUT AREA
         AH    OUTREG,LINESZ       BUMP TO NEXT OUTPUT LINE.
         LR    R14,OUTREG
         LH    R1,LINELOOP
         AR    R1,R1               LIMIT MOD 1 TO 7 BITS MAX.
         BCTR  R1,R0
         LA    R15,STATSTBL
R080     MVC   DUBLWD(2),MDRSTATS  SETUP TO LOOK AT REMOTE STATUS TBL.
         NC    DUBLWD(2),0(R15)
         CLC   DUBLWD(2),0(R15)
         BE    R170
R120     LA    R15,20(R15)
         CLI   0(R15),X'FF'
         BNE   R080
         B     R200
R170     MVC   20(18,R14),2(R15)   MOVE EXPLANATION OF 1 REMOTE STATUS
         AH    R14,LINESZ          BUMP TO NEXT OUTPUT AREA
         BCT   R1,R120             ANY TIOA ROOM LEFT?
R200     LR    OUTREG,R14          NO. SETUP TO COMPUTE DATA LTH.
         BR    LINKREG             RETURN TO MAIN LINE.
LOCATE   MVC   FILREC,TWAREL
         READ  ECB3,DI,EMFIL1,(6),4096,0,FILREC
         CHECK ECB3
S030     L     FIOABAR,TCAFCAA     POINT TO DATA BLK.
         LH    DATALOOP,BLKLTH     SETUP LOOP NBR BYTES THIS BLK.
         LA    DATAREG,DATASLT1    POINT TO FIRST DATA ITEM
         SR    R1,R1               PREPARE SLOT NBR FOR
         IC    R1,TWASLOT          PUTTING OUT IN DISPLAY
         BCTR  R1,R0
         STC   R1,TWASLOT+3
S060     CLI   TWASLOT,2           IF RQSTED SLOT = 1, THIS IS IT.
         BL    S220                GO RESUME DISPLAY BUILDING.
         SR    R1,R1
         IC    R1,TWASLOT          DECREMENT SLOT COUNT BY 1.
         BCTR  R1,R0
         STC   R1,TWASLOT
         IC    1,0(DATAREG)      BUMP TO NEXT SLOT.
         LTR   1,1
         BZ    S190                PREVENT LOOP IF GARBAGE INPUT.
         AR    DATAREG,1
         SR    DATALOOP,1
         LTR   DATALOOP,DATALOOP   VERIFY SLOT NBR VALID FOR THIS BLK.
         BP    S060
S190     DS    0H
         MVC   TIOADBAO(INVLTH),INVMSG  IF RQSTED SLOT CAN'T
         MVC   TIOATDL,=AL2(INVLTH)     BE FOUND, SEND 'INVALID INPUT'
         B     D170
S220     BR    LINKREG             RETURN TO MAIN LINE.
GETTIOA  LA     TIOABAR,TIOADBAO     POINT  TO TCAM OUTPUT AREA
* NEXT 5 LINES INITIALIZES TIOA TO 40'S
         ST    DATALOOP,R8SAVE     SAVE REGIDTER EIGHT
         LA    0,TIOADBAO          FIELD TO BE CLEARED
         LA    1,2000              LENGTH OF FIELD TO BE CLEARED
         L     REG9,=X'40000001'   LENGTH AND PADDING CHARACTER
         LA    DATALOOP,FORTY      FILL  CHARACTER
         MVCL  0,DATALOOP          MOVE FILL CHARACTER TO CLEAR FIELD
         L     DATALOOP,R8SAVE     RESTORE REGISTER EIGHT
* END OF INITIALIZE CODE
         ST    TIOABAR,TCTTEDA     ENOUGH FOR A MODEL-2 DISPLAY.
         MVC   TIOADBAO(HDR1LTH),HDR1
         MVC   TIOADBAO+HDR1LTH(4),=C'3270'    SET UP 1ST LINE OF HDR
         LA    OUTREG,TIOADBAO+HDR1LTH+4   BUMP OUTPUT AREA ADDRESS
         CLI   TCTTETM,C'2'        IS THIS SCREEN A MODEL 2?
         BNE   T120                NO. 1ST LINE IS OK.
         MVC   0(3,OUTREG),HDR2    YES. ADD ANOTHER SBA TO FINISH
         LA    OUTREG,3(OUTREG)    THIS 80-CHAR LINE.
T120     MVC   0(HDR3LTH,OUTREG),HDR3   MOVE THE 'PAGE' REQUEST LINES.
T130     ST    OUTREG,TWAOUT       SAVE THIS ADDR TO FILL IN BLK NBRS.
         LA    OUTREG,HDR3LTH(OUTREG)
         MVC   0(HDR4LTH,OUTREG),HDR4   MOVE THE RECAP LINE HDR.
         OC    PARM5,PARM5         IF NO TERM ADDR INPUT, SHOW ONLY LIN
         BZ    T150
         MVC   (HDR4TERM-HDR4)(2,OUTREG),PARM5   FILL IN THE
         MVC   (HDR4CTRL-HDR4)(2,OUTREG),PARM4    ID OF THIS TERML
T150     DS    0H
         MVC   (HDR4LINE-HDR4)(3,OUTREG),PARM3    CTRL UNIT, AND LINE.
         CLC   (HDR4TERM-HDR4)(2,OUTREG),=C'  '   IS THIS TERML BLANK?
         BNE   T160                     NO,LET IT GO
         MVC   8(6,OUTREG),=C'LOCAL '   YES,CHANGE TO
         MVC   14(3,OUTREG),PARM3       LOCAL FORMAT
         MVC   17(24,OUTREG),=24C' '
T160     DS    0H
         LA    OUTREG,HDR4LTH(OUTREG)
         CLI   TCTTETM,C'2'        IS THIS A BIG SCREEN?
         BNE   T170                NO. THIS LINE IS OK.
         MVC   0(3,OUTREG),HDR5    YES. ADD ANOTHER SBA TO
         LA    OUTREG,3(OUTREG)  GET US PAST 40 MORE CHARS.
T170     MVC   0(HDR6LTH,OUTREG),HDR6   MOVE COLUMN HEADERS.
         LA    OUTREG,HDR6LTH(OUTREG)
         LA    OUTLOOP,7           SETUP TO SHOW 7 ITEMS ON MOD 1.
         CLI   TCTTETM,C'2'        IS THIS A BIG SCREEN?
         BNE   T180                NO. RETURN TO MAIN LINE.
         MVC   0(HDR6LTH,OUTREG),HDR6   YES. MOVE ANOTHER COLUMN HDR.
         LA    OUTREG,HDR6LTH(OUTREG)
         LA    OUTLOOP,40          SETUP TO SHOW 40 ITEMS ON MOD 2.
T180     BR    LINKREG             RETURN TO MAIN LINE.
T190     MVC   TIOADBA(MSG13LTH),MSG13  MOVE IN THE MSG        @OZ31884
         MVC   TIOATDL,=AL2(MSG13LTH)   MOVE IN THE LENGTH     @OZ31884
         OC    PARM3(3),=CL3' '         INSURE NO X'00'        @OZ31884
         MVC   TIOADBA+(MSG13LN-MSG13)(3),PARM3 PUT IN LINE NO.@OZ31884
         LA    R8,MSG13LTH              SET UP MESSAGE LENGTH  @OZ31884
         B     Z50D                     GO WRITE MESSAGE       @OZ31884
Z50      CLI   PARM4,X'00'          IS FULL ADDRESS AVAILABLE ?
         BE    Z50A                 NO. JUST SHOW TERMINAL ADDR
         CLC   PARM4(2),=C'00'
         BE    Z50A                 NO. JUST SHOW TERMINAL ADDR
         MVC   TIOADBAO(MSG11LTH),MSG11  MOVE 'NO ERRS' MSG TO OUTPUT
         OC    PARM3(24),=CL24' '   CHANGE  X'00'  TO  X'40'
         LA    15,TIOADBAO          POINT TO OUTPUT AREA
         MVC   (MSG11TRM-MSG11)(2,15),PARM5  MOVE TERML TO OUTPUT
         MVC   (MSG11CU-MSG11)(2,15),PARM4   MOVE CTRL UNIT TO OUTPUT
         MVC   (MSG11LIN-MSG11)(3,15),PARM3  MOVE LINE      TO OUTPUT
         B     Z50C
Z50A     OC    PARM3,=CL8' '          CHANGE  X'00'  TO  X'40'
         MVC   MSG12TRM,PARM3         MOVE TERML TO OUTPUT
Z50B     MVC   TIOADBAO(MSG12LTH),MSG12 MOVE MSG12 TO OUTPUT AREA
         LA    R8,MSG12LTH
         B     Z50D
Z50C     LA    R8,MSG11LTH
Z50D     STH   R8,TIOATDL
         L     13,SAV3270R
         RETURN (14,12)
*
EMFIL1 DCB DDNAME=EMFILE,DSORG=DA,MACRF=(RIC),OPTCD=R,                 X
               RECFM=F,SYNAD=F1900
SAV3270R DS    A         CALLERS SAV AREA REGISTER ADDRESS
SAV3270A DS    18F   3270 PROGRAM SAVAREA
P        DS    XL24'00'            PARM AREA FOR BLOWUP
         ORG   P
P3       DC    XL8'00'
P4       DC    XL8'00'
P5       DC    XL8'00'
FORTY    DC    X'40404040'
HDR1     DC    AL1(SBA),AL2(L1P1),AL1(SF,MNP),C'DEMF'
         DC    AL1(SF,UNA,IC),AL1(SBA),AL2(L1P36),AL1(SF,MNP)
HDR1LTH  EQU   *-HDR1
HDR2     DC    AL1(SBA),AL2(L2P1)
HDR3     DC    AL1(SF,PNA),C'(',AL1(SF,UNA),C'?'
         DC    AL1(SF,PNA),C') PAGE FORWARD'
         DC    AL1(SF,PNA),C' '
HDR3ID   DC    C'                   '
         DC    AL1(SF,PNA),C'(',AL1(SF,UNA),C'?'
         DC    AL1(SF,PNA),C') PAGE BACKWRD'
         DC    AL1(SF,PIA)
BBBSS    DC    C'BBBSSBBBSSBBBSS',AL1(SF,PNA),C'    '
HDR3LTH  EQU   *-HDR3
HDR4     DC    AL1(SF,PNA),C'RECAP REMOTE '
HDR4TERM DC    CL2'  ',C' ON CU '
HDR4CTRL DC    CL2'  ',C' ON LINE '
HDR4LINE DC    CL3'999',C'  '
HDR4LTH  EQU   *-HDR4
HDR5     DC    AL1(SBA),AL2(L4P1)
HDR6     DC    AL1(SF,PNA),C'      FILE#  TIME SENSE STATUS TPOP CMD'
HDR6LTH  EQU   *-HDR6
IOERRMSG DC    AL1(SBA),AL2(L1P1),AL1(SF,MNP),C'DEMF',AL1(SF,UNA,IC)
         DC    XL34'00',AL1(SF,PNA),CL39' '
         DC    CL39'BNG001A T I/O ERR READING SYS1.DEMFLOG '
         DC    CL28'    THE BDAM RETURN CODE IS '
IOCODE   DC    CL4'0000'
         DC    CL8' '
         DC    CL34'THE BDAM RELATIVE BLOCK NUMBER IS '
IOBLOCK  DC    CL3'000'
IOERRLTH EQU   *-IOERRMSG
MSG11    DC    AL1(SBA),AL2(L1P1),AL1(SF,MNP),C'DEMF',AL1(SF,UNA,IC)
         DC    AL1(SBA),AL2(L2P1),AL1(SF,PNA)
         DC    C'BNG011I T NO ERRORS RECORDED FOR REMOTE'
         DC    AL1(SBA),AL2(L3P1),AL1(SF,PNA)
         DC    CL9'TERMINAL '
MSG11TRM DC    CL2' '
         DC    CL20'       ON CTRL UNIT '
MSG11CU  DC    CL2' '
         DC    AL1(SBA),AL2(L4P1),AL1(SF,PNA)
         DC    CL8'ON LINE '
MSG11LIN DC    CL3' '
         DC    CL8' '
MSG11ID  DC    CL20' '          IMS SYMBOLIC NAME
MSG11LTH EQU   *-MSG11
MSG12    DC    AL1(SBA),AL2(L1P1),AL1(SF,MNP),C'DEMF',AL1(SF,UNA,IC)
         DC    AL1(SBA),AL2(L2P1),AL1(SF,PNA)
         DC    C'BNG012I T NO ERRORS RECORDED FOR LOCAL '
         DC    AL1(SBA),AL2(L3P1),AL1(SF,PNA)
         DC    CL9'TERMINAL '
MSG12TRM DC    CL3' '
         DC    CL7' '
MSG12ID  DC    CL20' '          IMS SYMBOLIC NAME
MSG12LTH EQU   *-MSG12
MDRHDR   DC    AL1(SF,PNA),C'(',AL1(SF,UNA),C'? ) '
MDRBBBSS DC    CL5'BBBSS',AL1(SF,PNA)
MDRHRS   DC    CL2'24',C':'
MDRMINS  DC    CL2'60',C'  '
MDRSTAT  DC    CL4'XXXX',C'   '
OBRSNS   DC    CL4'    ',C'  '
OBRTPOP  DC    CL2'  ',C'  '
OBRCMD   DC    CL2'  ',C' '
MDRLTH   EQU   *-MDRHDR
BUHDR1   DC    AL1(SBA),AL2(L1P7),AL1(IC)
         DC   AL1(SBA),AL2(L3P39),AL1(SF,PIA),C'%',AL1(SBA),AL2(SML5P1)
         DC    X'3CC75F40',AL1(SBA),AL2(SML5P1)
BUHDR1LT EQU   *-BUHDR1
BUHDR2   DC    AL1(SBA),AL2(L1P7),AL1(IC)
         DC    AL1(SBA),AL2(L2P79),AL1(SF,PIA),C'%',AL1(SBA),AL2(L4P1)
         DC    X'3C5D7F40',AL1(SBA),AL2(L4P1)
BUHDR2LT EQU   *-BUHDR2
INVMSG   DC    AL1(SBA),AL2(L1P1),AL1(SF,MNP),C'DEMF',AL1(SF,UNA,IC)
         DC    XL34'00',AL1(SBA),AL2(L2P1),AL1(SF,PNA)
         DC    CL39'BNG005I T ERROR NOT FOUND ON FILE FOR '
         DC    AL1(SBA),AL2(L3P1),AL1(SF,PNA)
         DC    C'THE SELECTED ITEM '
INVLTH   EQU   *-INVMSG
MSG13    DC    AL1(SBA),AL2(L1P1),AL1(SF,MNP),C'DEMF',AL1(SF,UNA,IC)
         DC    AL1(SBA),AL2(L2P1),AL1(SF,PNA)                  @OZ31884
         DC    CL39'BNG013A T NO DATA BLOCKS AVAILABLE FOR '   @OZ31884
         DC    CL5'LINE '                                      @OZ31884
MSG13LN  DC    CL3' '                                          @OZ31884
         DC    CL31':  DATA BASE IN DEGRADED MODE. '           @OZ31884
         DC    CL39' MUST ALLOCATE MORE BLOCKS THAN LINES'     @OZ31884
MSG13LTH EQU   *-MSG13                                         @OZ31884
         DS    0F                       ALIGN ON FULL WORD     @OZ31884
R8SAVE   DC    X'00000000'              SAVE AREA REG 8        @OZ31884
DATTIME  DC    X'00000000'              SAVE AREA FOR TIME     @OZ31884
DATDATE  DC    PL4'0'                   SAVE AREA FOR DATE     @OZ31884
FILREC   DC    X'000000'                WORK AREA FOR FILE REC @OZ31884
SENSAV   DC    XL1'00'                  OBR SENSE WORK AREA    @OZ31884
         DS    XL3                      SPARE                  @OZ31884
         LTORG
CALENDAR DC    C'JAN',PL2'31'
         DC    C'FEB',PL2'28'
         DC    C'MAR',PL2'31'
         DC    C'APR',PL2'30'
         DC    C'MAY',PL2'31'
         DC    C'JUN',PL2'30'
         DC    C'JUL',PL2'31'
         DC    C'AUG',PL2'31'
         DC    C'SEP',PL2'30'
         DC    C'OCT',PL2'31'
         DC    C'NOV',PL2'30'
         DC    C'DEC',PL2'31'
LEAPTBL  DC    X'768084889296FF'
EBTOHX   DC    X'00000000000000000000000000000000'     00-0F  THIS
         DC    X'00000000000000000000000000000000'     10-1F  TABLE
         DC    X'00000000000000000000000000000000'     20-2F  CONVERTS
         DC    X'00000000000000000000000000000000'     30-3F  INPUT
         DC    X'00000000000000000000000000000000'     40-4F  A THRU F
         DC    X'000000000000000000000000FF000000'     50-5F    TO
         DC    X'00000000000000000000000000000000'     60-6F  0A - 0F
         DC    X'00000000000000000000000000000000'     70-7F   AND
         DC    X'00000000000000000000000000000000'     80-8F  F0 - F9
         DC    X'00000000000000000000000000000000'     90-9F    TO
         DC    X'00000000000000000000000000000000'     A0-AF  00 - 09
         DC    X'00000000000000000000000000000000'     B0-BF    AND
         DC    X'000A0B0C0D0E0F000000000000000000'     C0-CF     *
         DC    X'00000000000000000000000000000000'     D0-DF   BECOME
         DC    X'00000000000000000000000000000000'     E0-EF    FF.
         DC    X'00010203040506070809000000000000'     F0-FF
HXTOEB   DC    4XL16'00',C' ',XL15'00',7XL16'00'  THIS TABLE CONVERTS
         DC    X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'  HEX CHARS
         DC    2XL16'00'                CHARS FA-THRU-FF BACK
         DC    X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'  A THRU F
CCWTABL  DC    CL20'00TEST IO'
         DC    CL20'01WRITE'
         DC    CL20'02READ'
         DC    CL20'03NO OP'
         DC    CL20'04SENSE'
         DC    CL20'05TEST WRITE'
         DC    CL20'06READ PREPARE'
         DC    CL20'07STEP COUNT'
         DC    CL20'09WRITE POLL'
         DC    CL20'0AREAD INHIBIT'
         DC    CL20'0DWRITE BREAK'
         DC    CL20'0EREAD SEARCH'
         DC    CL20'11WRITE W/TIME OUT'
         DC    CL20'12TEST READ'
         DC    CL20'13SAD ZERO'
         DC    CL20'15WRAP'
         DC    CL20'17SAD ONE'
         DC    CL20'19POLL SOH'
         DC    CL20'1BSAD TWO'
         DC    CL20'1D DIAGNOSTIC POLL'
         DC    CL20'1EADDRESS PREPARE'
         DC    CL20'1FSAD THREE'
         DC    CL20'23SET MODE'
         DC    CL20'27ENABLE'
         DC    CL20'29WRITE DIAL'
         DC    CL20'2FDISABLE'
         DC    CL20'33TEST SYNC'
         DC    CL20'37SENFEOT'
         DC    CL20'3BSEND INQUIRY'
         DC    CL20'41WRITE BREAK'
         DC    CL20'42READ CLEAR'
         DC    X'FFFF',CL18'NO KNOWN MEANING'
TPOPTBL  DC    CL20'00ON LINE TEST'
         DC    CL20'01DISABLE'
         DC    CL20'02WRITE EOA EOT EOT'
         DC    CL20'03WRT POLL/ADDR CHRS'
         DC    CL20'04WRITE SPACE'
         DC    CL20'05RD RSP TO POLLING'
         DC    CL20'06RD RSP TO ADDRESSING'
         DC    CL20'07READ ID RESPONSE'
         DC    CL20'08WRITE EOA'
         DC    CL20'09NOP OR TIC'
         DC    CL20'0AREAD INDEX'
         DC    CL20'0BREAD INQUIRY'
         DC    CL20'0CRD RSP TO INQUIRY'
         DC    CL20'10WRT LINE ADDRESS'
         DC    CL20'11RD OR WRT TEXT'
         DC    CL20'12RD SKIP OR TIC'
         DC    CL20'13WRITE DLE ETX'
         DC    CL20'20RD RSP TO TEXT'
         DC    CL20'21ALL RESET COMMANDS'
         DC    CL20'22READ SKIP'
         DC    CL20'23WRITE BREAK'
         DC    CL20'24OPEN LOPEN CLOSE'
         DC    CL20'25RD RSP TO TEXT'
         DC    X'FFFF',CL18'NO KNOWN MEANING'
CSWTABL  DC    B'1000000000000000',CL18'ATTENTION'
         DC    B'0100000000000000',CL18'STATUS MODIFIER'
         DC    B'0010000000000000',CL18'CTRL UNIT END'
         DC    B'0001000000000000',CL18'BUSY'
         DC    B'0000100000000000',CL18'CHANNEL END'
         DC    B'0000010000000000',CL18'DEVICE END'
         DC    B'0000001000000000',CL18'UNIT CHECK'
         DC    B'0000000100000000',CL18'UNIT EXCEPTION'
         DC    B'0000000010000000',CL18'PGRM CTRLD INTRPT'
         DC    B'0000000001000000',CL18'INCORRECT LENGTH'
         DC    B'0000000000100000',CL18'PROGRAM CHECK'
         DC    B'0000000000010000',CL18'PROTECTION CHECK'
         DC    B'0000000000001000',CL18'CHANL DATA CHECK'
         DC    B'0000000000000100',CL18'CHANL CTRL CHECK'
         DC    B'0000000000000010',CL18'INTFACE CTRL CK'
         DC    B'0000000000000001',CL18'CHAINING CHECK'
         DC    X'FF'
SENSTABL DC    B'10000000',CL18'COMMAND REJECT'
         DC    B'01000000',CL18'INTERVENTION REQD'
         DC    B'00100000',CL18'BUS OUT CHECK'
         DC    B'00010000',CL18'EQUIPMENT CHECK'
         DC    B'00001000',CL18'DATA CHECK'
         DC    B'00000100',CL18'OVERRUN'
         DC    B'00000010',CL18'LOST DATA'
         DC    B'00000001',CL18'TIMEOUT'
         DC    X'FF'
STATSTBL DC    B'0010000000000000',CL18'RESERVED'
         DC    B'0001000000000000',CL18'RESERVED'
         DC    B'0000100000000000',CL18'DEVICE BUSY'
         DC    B'0000010000000000',CL18'UNIT SPECIFY'
         DC    B'0000001000000000',CL18'DEVICE END'
         DC    B'0000000100000000',CL18'TRANSMISSION CHECK'
         DC    B'0000000000100000',CL18'COMMAND REJECT'
         DC    B'0000000000010000',CL18'INTERVENTION REQD'
         DC    B'0000000000001000',CL18'EQUIPMENT CHECK'
         DC    B'0000000000000100',CL18'DATA CHECK'
         DC    B'0000000000000010',CL18'CONTROL CHECK'
         DC    B'0000000000000001',CL18'OPERATION CHECK'
         DC    X'FF'
SENS3272 DC    B'10000000',CL18'COMMAND REJECT'
         DC    B'01000000',CL18'INTERVENTION REQD'
         DC    B'00100000',CL18'BUS OUT CHECK'
         DC    B'00010000',CL18'EQUIPMENT CHECK'
         DC    B'00001000',CL18'DATA CHECK'
         DC    B'00000100',CL18'UNIT SPECIFY'
         DC    B'00000010',CL18'CONTROL CHECK'
         DC    B'00000001',CL18'OPERATION CHECK'
         DC    X'FF'
CCW3272  DC    CL20'01WRITE'
         DC    CL20'05ERASE WRITE'
         DC    CL20'02READ BUFFER'
         DC    CL20'06READ MODIFIED'
         DC    CL20'0BSELECT'
         DC    CL20'0FERASE ALL PROTECTD'
         DC    CL20'03NO OP'
         DC    CL20'04SENSE'
         DC    X'FFFF',CL18'NO KNOWN MEANING'
TCAMTPOP DC    CL20'01WRT EOT FOR SELECT'
         DC    CL20'02OPEN TP OP CODE'
         DC    CL20'03WRITE POLL CHARS'
         DC    CL20'04RD RSPNS TO POLL'
         DC    CL20'05WRITE PAD CHARS'
         DC    CL20'06ENABLE DIAL LINE'
         DC    CL20'07WRT ADDR SEQUENCE'
         DC    CL20'08READ RSPNS TO ADDR'
         DC    CL20'09WRITE EOA SEQUENCE'
         DC    CL20'0ARD RSPNS EOB/ETB'
         DC    CL20'0BWRITE CPU ID'
         DC    CL20'0CREAD ENQ'
         DC    CL20'0DWRITE ENQ'
         DC    CL20'0ERD RSPNS TO ENQ'
         DC    CL20'0FWRITE DLE EOT'
         DC    CL20'10READ ID (TSO)'
         DC    CL20'11NON-R/W CCW'
         DC    CL20'12WRITE BREAK (TSO)'
         DC    CL20'13WRT ENQ AFTER SEL'
         DC    CL20'14READ LCOUT'
         DC    CL20'15WRT RSP BEFOR TXT'
         DC    CL20'16WRITE RESPONSE'
         DC    CL20'17WRITE TONE'
         DC    CL20'18BSC READ ID ENQ'
         DC    CL20'1ABSC READ ID ACK'
         DC    CL20'1CABORT SEND/RECIEV'
         DC    CL20'1EREAD TWX ID'
         DC    CL20'20BUF TRM RESET BLK'
         DC    CL20'22 CLOSE SDR RECRDNG'
         DC    CL20'24WRT RESET AFT SEL'
         DC    CL20'51READ SKIP LOOP'
         DC    CL20'53WRT IDLES LOOP'
         DC    CL20'57WRT DLE STX'
         DC    CL20'59WRT DLE ETB(ETX)'
         DC    CL20'5BWRT ENQ RSP TO TXT'
         DC    CL20'FFTEXT CCW'
         DC    X'FFFF',CL18'NO KNOWN MEANING'
DECB     DSECT
         DS    CL1
EXCPBITS DS    CL2                 EXCEPTIONS BITS IN DECB
         DS    CL1
T3270ID  DC    C'BNGT3270 END'
         END
