***********************************************************************
*                                                                     *
* MODULE NAME = BNGISIMS                                              *
*                                                                     *
* DESCRIPTIVE NAME =  IMS DEMF OPEN/CLOSE SYS1.DEMFLOG FILE MODULE    *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 1, LEVEL 1                                         *
*                                                                     *
* FUNCTION = 1) OPENS/CLOSES SYS1.DEMFLOG DATA FILE FOR IMS DEMF      *
*               FUNCTION MODULES TO ACCESS TO IT;              *
*            2) LINKS TO IMS REGION CONTROLLER, DFSRRC00.             *
*                                                                     *
* NOTES = NONE                                                        *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS = SEE REGISTERS EQU'S BELOW                 *
*                                                                     *
*    PATCH LABEL = NONE                                               *
*                                                                     *
* MODULE TYPE =  IMS CONTROL                                          *
*                                                                     *
*    PROCESSOR = ASSEMBLER H                                          *
*                                                                     *
*    MODULE SIZE =  612 DECIMAL BYTES                                 *
*                                                                     *
*    ATTRIBUTES = REENTRANT                                           *
*                                                                     *
* ENTRY POINT = BNGIDCBA (USED BY IMS DEMF APPLICATION PGM BNGICTRL)  *
*               BNGINTRY (USED BY OS JOB SCHEDULER)                   *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = R1  LOADED WITH THE PARAM LIST POINTER                 *
*              R14 LOADED WITH THE RETURN ADDR (USED BY SUBROUTINE)   *
*              R15 LOADED WITH ENTRY POINT                            *
*                                                                     *
* INPUT  = N/A                                                        *
*                                                                     *
* OUTPUT = N/A                                                        *
*                                                                     *
* EXIT-NORMAL = REGISTER 15 RETURN CODE RETURNED TO CALLER :          *
*               00 - OPERATION PERFORMED AS REQUESTED                 *
*                                                                     *
* EXIT-ERROR  = NONE                                                  *
*                                                                     *
* EXTERNAL REFERENCE = NONE                                           *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA AREAS = NONE                                                *
*                                                                     *
*    CONTROL BLOCK = DCB                                              *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = OPEN, WTO, LINK, CLOSE, RETURN                             *
*                                                                     *
* CHANGE ACTIVITIES AS FOLLOWS:                                       *
* CHANGE 01   11/15/77  PROLOGUE ADDED                                *
*                                                                     *
***********************************************************************
         PRINT NOGEN
         TITLE 'BNGISIMS - OPEN/CLOSE EMFILE-START IMS MPR REGION'
         SPACE 2
***********************************************************************
*                                                                     *
*     THIS PROGRAM IS ENTERED EITHER BY THE OPERATING SYSTEM JOB      *
*     SCHEDULER USING ENTRY POINT BNGINTRY OR BY THE IMS APPLICATION  *
*     PROGRAM BNGICTRL USING ENTRY POINT BNGIDCBA.                    *
*                                                                     *
*     THIS PROGRAM MUST BE LINKEDIT AS 'REENTRANT'                    *
*                                                                     *
***********************************************************************
         DCBD  DSORG=DA
BNGIDCBA CSECT
         DC    A(EMFDCBA)            ADDR OF ADDR OF BDAM DCB
         ENTRY BNGINTRY
BNGINTRY   SAVE  (14,12)
         BALR  2,0
         USING *,2,3
         LA    3,2048(2)             SET SECOND BASE
         LA    3,2048(3)                 FROM THE FIRST
         LA    4,SAVE                POINT TO OUR SAVE AREA
         ST    4,8(13)               FORWARD CHAIN FROM OLD
         ST    13,SAVE+4             BACKWARD CHAIN TO OLD
         LR    13,4                  FIRST BASE AND S/A FOR OUR REGS
         B     GO
         DC    C'BNGISIMS - DEMF MODULE TO OPEN/CLOSE EMFILE AND'
         DC    C'           START IMS MESSAGE PROCESSING REGION'
SAVE     DS    18F
SAVEX    DS    CL72
GO       DS    0H
         OPEN  (EMFDCB)
         WTO   'SYS1.DEMFLOG OPENED',ROUTCDE=11
         L     4,=A(EMFDCB)
         ST    4,EMFDCBA            PRIME DCB ADDRESS POINTER
         USING IHADCB,4
         TM    DCBOFLGS,X'10'       OPEN SUCCESSFUL?
         BNZ   OPENOK               YES - CONTINUE
         WTO   'SYS1.DEMFLOG DID NOT OPEN',ROUTCDE=11
         XC    EMFDCBA,EMFDCBA      ZERO DCB POINTER TO SHOW
OPENOK   DS    0H                        OPEN FAILURE AND GO ON
         L     13,SAVE+4
         L     1,24(13)
         MVC   SAVEX,0(13)
         LINK  EP=DFSRRC00
         MVC   0(72,13),SAVEX
         L     13,8(13)
         OC    EMFDCBA,EMFDCBA
         BZ    CLOSEBYP
         XC    EMFDCB+28(4),EMFDCB+28        ZERO OUT OLD IOB ADDR
         CLOSE (EMFDCB)
         WTO   'SYS1.DEMFLOG CLOSED',ROUTCDE=11
CLOSEBYP DS    0H
         L     13,SAVE+4
         RETURN (14,12),RC=0
EMFDCBA  DC    A(0)
         DS    0F
         DS    F
EMFDCB   DCB   DDNAME=EMFILE,DSORG=DA,MACRF=(RI),OPTCD=R,RECFM=F
         LTORG
         END   BNGIDCBA
