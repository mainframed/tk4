SCAN     TITLE 'IEBUPDTE CONTROL CARD SCAN AND ANALYSER'
*TITLE- IEBUPDTE CONTROL CARD SCAN AND ANALYSIS CSECT- IEBASCAN       *
*                                                                     *
*STATUS- CHANGE LEVEL 001                                             *
*                                                                     *
*FUNCTION/OPERATION- THIS MODULE IS ABLE TO CREATE AND UPDATE         *
*   SYMBOLIC LIBRARIES WHICH ARE EITHER PHYSICAL SEQUENTIAL OR        *
*   PARTITIONED DATA SETS CONTAINED ON DIRECT ACCESS OR TAPE VOLUMES. *
*                                                                     *
*   THIS MODULE HANDLES FIXED OR FIXED-BLOCKED RECORDS. IT HAS THE    *
*   ABITLITY TO HANDLE RECORD OF LOGICAL RECORD LENGTHS UP TO EIGHTY  *
*   BYTES. IF BLOCKSIZE AND/OR LOGICAL RECORD LENGTH ARE NOT GIVEN    *
*   BY OPEN TIME, UNBLOCKED RECORDS OF 80 BYTES IS ASSUMED AND A      *
*   WARNING MESSAGE IS GIVEN ON THE LOG OUT DEVICE.                   *
*                                                                     *
*   THIS MODULE CAN-                                                  *
*                                                                     *
*   1. CREATE A NEW PARTITIONED OR PHYSICAL SEQUENTIAL DATA SET       *
*                                                                     *
*   2. SEQUENCE THE NEW DATA SET OR RESEQUENCE AN OLD ONE             *
*                                                                     *
*   3. REPRODUCE OR REPLACE A MEMBER OR A DATA SET                    *
*                                                                     *
*   4. PRODUCE A PARTITIONED DATA SET FROM A PHYSICAL SEQUENTIAL      *
*   MASTER OR THE CONVERSE.                                           *
*                                                                     *
*   4. ADD A MEMBER TO A PARTITIONED DATA SET.                        *
*                                                                     *
*   5. ASSIGN AN ALIAS NAME TO ANY MEMBER UPDATED                     *
*                                                                     *
*   6. CHANGE RECORDS IN A DATA SET                                   *
*                                                                     *
*   7. HANDLE RECORDS WITH NON-STANDARD SEQUENCE NUMBER FIELDS AND    *
*   LENGTHS OF SEQUENCE NUMBERS FROM 1 TO 8 CHARACTERS.               *
*                                                                     *
*   8. UPDATE ONLY SPECIFIED PORTION OF DATA SET RECORDS.             *
*                                                                     *
*   9. UPDATE INPLACE DATA SETS OR MEMBERS ON DIRECT ACCESS VOLUMES.  *
*                                                                     *
*ENTRY POINTS- ENTERED FROM THE OPERATING SYSTEM AT 'IEBUPDTE'.       *
*                                                                     *
*INPUT- SYSIN WHICH CONSISTS OF CONTROL CARDS AND LOGICAL DATA        *
*   RECORDS FOR INSERTION OR REPLACEMENT, THE INPUT DATA SET.         *
*                                                                     *
*       SYSUT1 WHICH DEFINES THE 'OLD MASTER' DATA SET WHICH MAY BE   *
*   PARTITIONED OR PHYSICAL SEQUENTIAL IN ORGANIZATION.               *
*                                                                     *
*OUTPUT-SYSPRINT WHICH CONTAINS DIAGNOSTIC MESSAGES, THE CONTENTS OF  *
*   SYSIN AND-IF THE 'LIST=ALL' CONDITION IS PRIMED- THE ENTIRE       *
*   'NEW MASTER' IS LISTED ON THE DEVICE DESIGNATED.                  *
*                                                                     *
*       SYSUT2 WHICH DEFINES THE 'NEW MASTER' DATA SET. IT WILL BE A  *
*   COMPOSITE OF THE SYSIN AND SYSUT1 DATA SETS. IT CAN BE EITHER     *
*   PARTITIONED OR PHYSICAL SEQUENTIAL IN ORGANIZATION.               *
*                                                                     *
*EXITS- NORMAL- A RETURN IS GIVEN TO THE SCHEDULAR MODULE OF THE      *
*   OPERATING SYSTEM. CONDITION CODE RETURNED ON A SUCCESSFUL RUN     *
*   WILL BE ZERO.                                                     *
*                                                                     *
*EXITS- ERROR- IF AN ERROR OCCURS, A CODE IS STORED, A MESSAGE        *
*   GIVEN TO THE USER EXPLAINING THE ERROR CONDITION AND A            *
*   RETURN TO THE OPERATING SYSTEM SCHEDULAR MODULE IS GIVEN.         *
*   RETURN CODE GIVEN ARE-                                            *
*   00 NORMAL TERMINATION OF A SUCCESSFUL RUN                         *
*   04 ERROR WITHIN A MEMBER- WILL TRY TO PROCESS NEXT MEMBER IF PODS *
*   12 UNRECOVERABLE ERROR- TERMINATION OF JOB.                       *
*   16 ERROR IN LABEL HANDLING IN USERS ROUTINE- TERMINATION OF JOB   *
*                                                                     *
*EXTERNAL ROUTINES- I/O HANDLED AT READ/WRITE LEVEL, GETMAIN, LINK,   *
*   BLDL, FIND, STOW USED.                                            *
*                                                                     *
*TABLES/WORK AREAS-                                                   *
*   -KEYTAB- LIST OF VALID KEY WORDS USED ON CONTROL CARDS            *
*   -COMDTABL- LIST OF VALID COMMAND WORDS USED ON CONTROL CARDS      *
*   -SWITCHRD- BUFFER CONTAINING CONTROL CARD AND SWITCHES USED       *
*   BY CARD SCAN ROUTINE.                                             *
*   -LOGOUTAR- BUFFER WHICH IS USED BY LOG OUT ROUTINE FOR MESSAGES.  *
*   -STARTAB- TABLE OF SWITCHES WHICH ARE RESET TO ZERO UPON ENTRY    *
*   TO MODULE. ALL SWITCHES NEEDING REINITIALIZATION SHOULD BE PUT    *
*   INTO THIS TABLE.                                                  *
*                                                                     *
*ATTRIBUTES- SERIAL REUSABLE, BLOCK LOADS
*                                                                     *
*   THIS CSECT- 1. SCANS CONTROL STATEMENTS AND RETURNS POINTERS TO   *
*        THE 'TOSCAN' ROUTINE WHICH ANALYZES COMMANDS, KEYWORDS, AND  *
*        PARAMETERS.                                                  *
*               2. SWITCHRD BUFFER CONTAINS THE CONTROL STATEMENT AS  *
*        WELL AS THE SWITCHES AND BUFFERS NEEDED.                     *
*               3. 'LENGTH' REGISTER IS PASSED BACK FROM 'READCC'     *
*        CONTAINING THE NUMBER OF BYTES IN THE COMMAND, KEYWORD, OR   *
*        PARAMETER ENCOUNTERED DURING THE SCAN.                       *
*               4. 'SCANADR' REGISTER POINTS TO THE START OF THE      *
*        COMMAND/KEYWORD/PARAMETER.                                   *
*               5. 'KEYTAB' IS A TABLE CONTAINING THE VALID KEYWORDS. *
*               6. 'COMDTABL' IS A TABLE OF VALID COMMAND WORDS.      *
*               7. EACH COMMAND WORD AND KEYWORD HAS A ROUTINE TO     *
*        ANALYZE THE PARAMETERS ASSOCIATED WITH IT.                   *
*                                                                     *
         EJECT
*     CHECKING OF VALID PARAMETER COMBINATIONS IS DONE IN 'SEQCKOUT'. *
*  'PARMSW' IS SET AS FOLLOWS-                                        *
*        KEYWORD             SETTING OF PARMSW                        *
*         SEQ1                    '01'                                *
*         SEQ2                    '02'                                *
*         INCR                    '04'                                *
*         NEW1                    '08'                                *
*         NAME                    '10'                                *
*         NEW                     '20'                                *
*         MEMBER                  '40'                                *
*                                                                     *
*     SYMBOLIC REGISTER NOTATION   GR0 - GR15
GR0      EQU   0
GR1      EQU   1
GR2      EQU   2
GR3      EQU   3
GR4      EQU   4                       REGISTER RESERVED FOR CSECT1
GR5      EQU   5
GR6      EQU   6
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10
GR11     EQU   11
GR12     EQU   12
GR13     EQU   13
GR14     EQU   14
GR15     EQU   15
SWF0     EQU   X'F0'                   DOUBLE PARM FLAGS        YM07522
SWF8     EQU   X'F8'                                            YM07522
SWFC     EQU   X'FC'                                            YM07522
SWFE     EQU   X'FE'                                            YM07522
D0       EQU   0                       DISPLACEMENT OF 0        YM07522
D1       EQU   1                       DISPLACEMENT OF 1        YM07522
D2       EQU   2                       DISPLACEMENT OF 2        YM07522
D7       EQU   7                       DISPLACEMENT OF 7        YM07522
L1       EQU   1                       LENGTH OF 1              YM07522
L2       EQU   2                       LENGTH OF 2              YM07522
L3       EQU   3                       LENGTH OF 3              YM07522
L7       EQU   7                       LENGTH OF 7              YM07522
L8       EQU   8                       LENGTH OF 8              YM07522
LODIG    EQU   C'0'                    LOWEST VALID DIGIT       YM07522
HIDIG    EQU   C'9'                    HIGHEST VALID DIGIT      YM07522
IEBASCAN CSECT
*1101027400,047400-047800,145200,147600-148200,148800-151400,      UL0H
*1101152400-159800,166200-213400,219000                            UL0H
*1132                                                              MA0H
*1132                                                              4929
*1132                                                              6701
*1132                                                              6728
*1132                                                              7198
*1101132200                                                       20497
*1132                                                             20194
*1132                                                             19013
*1132                                                            PTM901
* 077800-078150                                                  A30501
* 125200-125400,125600                                           A25432
*                                                                A24189
*                                                                A25924
* 037680-036810,037600-037800,042400-042600,069400-069600        A30480
* 118600-118800,145400-145600                                    A30480
* 070920,130600,141000                                           A33286
* 050600-050800,055400-058000                                    A33314
* 084600-084800                                                  A33318
* 160150                                                         A31449
*                                                                A36128
*A144300,144320                                                  A38748
*A078100                                                         A41839
*A158150                                                         A45121
*A135900,135920,136700-136740                                   OX00189
*D135800                                                        OX00189
*D061400-061600,108200-109800                                    A54081
*A037150,037250,037350,042100                                   YA01670
*D037200,042200                                                 YA01670
*A029100,029120,029500,029520,132700,132720,132900,133300       YA01677
*A133320,156820,156840                                          YA01677
*D133000                                                        YA01677
*A25420-25590,89060,89420-89480,95820-95840,96010-96110         YM07522
*A96610-96780,140410-140470,140820,143210,143610-143740,216410  YM07522
*D136750                                                        YM07522
*C89200,89600,134800,135200,135400,135600,135800,136000,136400  YM07522
*C140800,141000,143600                                          YM07522
*A161100-161160,161500-161540                                  @YA02570
*D161200                                                       @YA02570
*A160884-160924                                                @YA02571
*C160932                                                       @YA02571
*A076100-076540,076410-076500                                  @ZA01682
*D076200,076500                                                @ZA01682
*C076600                                                       @ZA01690
*A074220-074320                                                @ZA01691
*C073800,074200                                                @ZA01691
*C 070940                                                      @ZA01692
*D 074284,074288                                               @ZA01692
*A 078500-078560                                               @ZA01693
*C 078400                                                      @ZA01693
*A 027100,040700-040784                                        @ZA04442
*A 078544-078548                                               @ZA07321
*A 040900,041100-041468,042020,042040   @XA16051=(ORG)@YA14019=@ZA16462
*A 156880,214100,219550-219590          @XA16051=(ORG)@YA14019=@ZA16462
*C 041000,042000                        @XA16051=(ORG)@YA14019=@ZA16462
*A130500,130700-131000                  @AY19768=(ORG)@AX16973=@AZ27974
*C131050,158000                         @AY19768=(ORG)@AX16973=@AZ27974
         SAVE  (14,12)
         BALR  GR2,0                   BASE ADDRESS ALL CSECTS EXCEPT 1
         USING NEXT,2                  CSECT1 USES GR4
         USING IEBUPCON,12
NEXT     ST    GR13,SAVE13X            SAVE REGISTER 13
         LA    GR13,REGSAVE            REGISTER SAVE AREA
         USING REGSAVE,GR13                                    @ZA04442
TOSCAN   CLI   CCSCANSW,BIT01          IS THIS RETURN FROM CONTINUATION
         BE    SCANLINK                                            UL0H
         TM    SWITCHRD,BIT40          ARE ALL PARAMETERS COMPLETE
         BC    14,SCANLINK             NO- KEEP SCANNING
SEQCKOUT CLI   COMDCD,X'0E'            JUST ANALYSED A COMMAND CARD
         BE    CHEKPARM                YES- CHECK TH PARAMETERS
         BAL   GR14,CARDPRNT           PRINT CARD
         MVI   PRINTED,X'0F'           FLAG AS CARD PRINTED
         CLC   DELTSEQ1(8),DELTSEQ2    COMPARE IF SEQ1 MORE THAN SEQ2
         BH    SCANERR                 YES- ERROR
         CLI   NUMBR2SW,X'0F'     SECOND NUMBER STMT ANALYSED   YA01677
         BE    NUMBR2             YES,BRANCH TO TEST IF INSERT  YA01677
         CLI   NUMBRSW,X'0E'           JUST ANALYSED NUMBER STATEMENT
         BNE   SKIPCOMP                NO- CONTINUE
NUMBR2   CLI   OMEND,X'0F'        IS OLD MASTER FINISHED?       YA01677
         BE    SEQERR             YES,GO TO GIVE ERROR MSG      YA01677
         MVI   NUMBRSW,X'0F'           FLAG AS NUMBER
         TM    INSERTSW,X'0F'          TEST FOR BLOCK INSERT
         BC    1,TESTPARS              FIRST INSERT
         BC    4,PLAINUMB              INSERT AFTER INSERT IN EFFECT
         CLI   PARMSW,X'0C'            NEW1 AND INCR PARAMETERS GIVEN
         BE    CHK4CHNG                YES- NO SEQ FIELDS CHECK CHANGE
         TM    ADDSW,X'0E'             ADD OPERATION
         BC    1,SKIPCOMP              YES- IGNORE SEQ1 OR 2 FIELDS
         TM    REPLSW,X'0E'            REPLACE OPERATION
         BC    1,SKIPCOMP              YES- IGNORE SEQ1 OR 2 FIELDS
         CLI   PARMSW,X'0F'            ALL FIELDS ON NUMBER STATEMENT
         BE    COMPNMBR                YES- CHECK IF SEQ2 GREATER SEQ1
         CLI   PARMSW,X'0D'            SEQ1 MISSING IF NOT '0D' OR '0F'
         BNE   SCANERR                 SEQ1 MISSING- INVALID
         CLI   INPLCSW,X'0F'           UPDATE IN PLACE
         BE    SKIPCOMP                SEQ2 MISSING
         CLI   RENUMALL,X'0F'          RENUMBERING ALL DATA SET
         BNE   SCANERR                 NO- SEQ2 FIELD MISSING
         B     SKIPCOMP                CONTINUE
CHK4CHNG TM    CHNGESW,X'0E'           CHANGE OPERATION
         BC    12,SKIPCOMP             NO- CONTINUE NO SEQ1 OR 2
         MVI   RENUMALL,X'0F'          FORCE RENUMBERING OF ENTIRE DATA
         B     SKIPCOMP                FORCE RENUMBERING OF DATA SET
PLAINUMB CLI   INSRTRD,X'01'           PLAIN NUMBER WHILE INSERT EFFECT
         BNE   SCANERR                 YES- ILLEGAL MIXED NUMBER/INSERT
         MVI   INSERTSW,X'1F'          SET AS NEXT BLOCK INSERT + FORCE
         MVI   INSRTRD,RESET0          RESET SWITCH
         TM    PARMSW,X'01'            SEQ1 GIVEN FOR INSERT
         BC    8,SCANERR               NO- ERROR
         B     SKIPCOMP                OK- CONTINUE
COMPNMBR CLC   NUMRSEQ1(8),NUMRSEQ2    COMPARE SEQ1 AND SEQ2
         BH    SCANERR                 IF SEQUENCE 1 MORE THAN 2 -ERROR
SKIPCOMP CLI   FIRSENTR,BIT10          FIRST ENTRY
         BE    RESET                   YES- CHECK DCB SETTING
RESETPAR MVI   PARMSW,RESET0           RESET PARAMETER SWITCH
         MVI   PRINTED,RESET0          RESET CARD PRINTED SWITCH
         MVI   FIRSTCON,X'00'          RESET CONT. CARD PRINT SWITCUL0H
         CLI   ENDONLY,X'0F'           ACCEPT ENDUP ONLY           4929
         BNE   READNOW                 NO NOT FLUSHING             4929
         CLI   INPLCSW,X'0F'           UPDATE INPLACE              4929
         LA    GR11,UPDATMSG           UPDATE ERROR CODE           4929
         BE    STOWIT3                 SHOULD NOT HAVE ANOTHER     4929
         TM    CTONLYSW,X'11'     ALL INPUT FROM SYSIN           A30480
         BZ    OLDTO              NO.. OLD TO                    A30480
         CLI   PSSWNM,X'0F'            NM SEQUENTIAL               4929
         BE    REDNW                   NO- PARTITIONED             4929
         B     STOWIT2                 SEQUENTIAL                  4929
OLDTO    CLI   PSSWOM,X'0F'            OLD MASTER SEQUENTIAL       4929
         BE    REDNW                   NO- PARTITIONED             4929
         B     STOWIT2                 SEQUENTIAL                  4929
REDNW    MVI   ENDONLY,X'00'           CLEAR SWITCH                4929
         B     READNOW                 RETURN TO PROCESS           4929
RESET    CLI   FIRSTCON,X'0F'     CONTINUATION SWITCH SET?      YA01670
         BE    TESTORG            YES BRANCH TO TEST DSORG      YA01670
         MVI   FIRSENTR,RESET0    RESET FIRST ENTRY SWITCH      YA01670
         L     GR11,DCBNM              NEW MASTER DCB ADDRESS
         TM    CTONLYSW,X'11'     ALL INPUT FROM SYSIN           A30480
         BM    CHKIFDIF           YES.. SET UP DCBNM             A30480
         CLI   PARMSW,X'00'            BOTH PS- NO NAME,NEW OR MEMBER
         BNE   RESETPAR                NEW, NAME OR MEMBER ESTABLISHED
         L     GR10,DCBOM              OLD MASTER DCB
         MVI   BDSORG(GR10),PSDSET     SET OLD MASTER AS PS        3628
SETNM2PS MVI   BDSORG(GR11),PSDSET     SET NEW MASTER AS PS        3628
         B     RESETPAR                CONTINUE
CHKIFDIF TM    PARMSW,X'10'            'NAME' KEYWORD PRESENT
         BC    14,SETNM2PS             NO- SET NEW MASTER AS PS
         B     RESETPAR                CONTINUE- NM SET TO PO- FLAGDCBP
CHEKPARM MVI   COMDCD,X'0F'            FLAG AS COMMAND PROCESSED
         CLI   FIRSTCON,X'0F'          LAST PART OF COMMAND CONTINUED
         BE    JUSTPRNT                YES- JUST PRINT THIS PART
         CLI   FIRSENTR,BIT10          FIRST ENTRY
         BNE   NOTFIRST                NO- PRINT HEADER
         CLI   PSSWNM,X'0F'            NEW MASTER PARTITIONED  @ZA04442
         BNE   JUSTPRNT                NO,BRANCH               @ZA04442
         CLI   PSSWOM,X'00'            OLD MASTER SEQUENTIAL   @ZA04442
         BNE   JUSTPRNT                NO,BRANCH               @ZA04442
         CLI   PARMSW,X'60'   NEW AND MEMBER PARM IN CNTL STMT @ZA04442
         BNE   SCANERR               NO,BRANCH TO GIVE ERR MSG @ZA04442
JUSTPRNT LA    GR14,RESET              RETURN FROM PRINT
CARDPRNT ST    GR14,COMEBACK           STORE RETURN REGISTER   @ZA16462
PRNTMORE MVI   ASISMSW,X'0F'           FORCE PRINTING OF CARD  @ZA16462
PRINTHD  TM    COL72LG,BIT80           COL72 NOT BLANK?        @ZA16462
         BNO   PRINTY                  BLANK, GO PRINT         @ZA16462
         BAL   GR14,PRINTY             NOT BLANK, GO PRINT     @ZA16462
         CLI   EODCTSW,X'0F'           EOF ON SYSIN?           @ZA16462
         BE    COMMERR                 YES, SCANERR MSG        @ZA16462
         L     GR1,DCBCT               SYSIN DCB               @ZA16462
         GET   (1)                                             @ZA16462
         MVC   SWITCHRD+1(80),0(GR1)   MOVE CARD TO BUFFER     @ZA16462
         CLC   SWITCHRD+1(15),COMMCARD VALID COMMENT CARD?     @ZA16462
         BNE   COMMERR                 NO, GIVE SCANNERR MSG   @ZA16462
         CLI   SWITCHRD+72,X'40'       COL72 BLANK?            @ZA16462
         BNE   GOPRINT                NO, BRANCH TO PRINT MORE @ZA16462
         NI    COL72LG,X'7F'           RESET COL72 SWITCH      @ZA16462
         L     GR14,COMEBACK           RESTOR RETURN REG       @ZA16462
GOPRINT  B     PRNTMORE                GO PRINT CARD           @ZA16462
PRINTY   MVC   LOGOUTAR+7(80),SWITCHRD+1         MOVE CARD TO BUFFER
         L     GR15,PRIMEWRT           ADDRESS OF MESSAGE ROUTINE
         BR    GR15                    RETURN PRESET IN GR14
NOTFIRST MVI   PRIMESW,X'0E'           FORCE PRINTING OF HEADER
         LA    GR14,TESTORG            PRINT HEADER AND CARD   @ZA16462
         ST    GR14,COMEBACK                                   @ZA16462
         B     PRINTHD                                         @ZA16462
TESTORG  MVI   PRINTED,X'0F'      FLAG AS CARD ALREADY PRINTED  YA01670
         TM    CTONLYSW,X'11'     ALL INPUT FROM SYSIN           A30480
         BM    SAMETYPE           YES.. SPECIAL CASE             A30480
         CLC   PSSWOM(1),PSSWNM        ORGANIZATION SAME OM/NM
         BE    SAMETYPE                YES- CHECK SOME MORE
         CLI   NEWNAME,X'0F'           PS TO PO ORGANIZATION
         BE    PS2PO                   YES
         CLI   PARMSW,X'30'            PO TO PS- 'NAME, NEW' ONLY
         B     VALIDITY                TEST IF VALID
TESTPARS CLI   RENUMALL,X'0F'          RENUMBER ALL OF NEW MASTER
         BE    SCANERR                 YES- INVALID SEQ1=ALL
         CLI   PARMSW,X'0D'            SEQ1,INCR,NEW1, ON STATEMENT
         BL    SCANERR                 NO- SOMETHING MISSING
         CLC   NUMRSEQ1(8),NEW1NUMB    SEQ1 LOWER THAN NEW1 FOR INSERT
         BNL   SCANERR                 NO- CONTROL STATEMENT ERROR
         B     SKIPCOMP                CONTINUE
SAMETYPE CLI   PSSWNM,X'00'            NEW MASTER PS OR BOTH PS
         BE    CHKPRMSW                YES CHECK PARAMETERS
         TM    PARMSW,X'10'            BOTH PO OR ONLY HAVE NM 'NAME'
         BC    8,SCANERR               NO 'NAME' CONTROL STATEMENT ERR.
         B     SKIPCOMP                PARAMETER ARE OK CONTINUE
CHKPRMSW CLI   PARMSW,X'00'            SHOULD HAVE NO 'NAME,NEW,MEMBER'
         B     VALIDITY                TEST IF VALID
PS2PO    CLI   PARMSW,X'60'            ALLOW ONLY 'NEW' AND 'MEMBER'
VALIDITY BNE   SCANERR                 NO- CONTROL STATEMENT ERROR
         B     SKIPCOMP                PARAMETERS OK CONTINUE
SCANLINK L     GR15,RDCARD                                         UL0H
         BALR  GR14,GR15               SCAN FOR NEXT PARAMETER     UL0H
         LM    LENGTH,SCANADR,STOREG   RESTORE SCAN REGISTERS (0,1)UL0H
         CLI   LABEL2SW,X'F0'          HAS TRALER PROC BEEN REQUESTUL0H
         BE    CHEKCOMD                                            UL0H
SCANNED  TM    SWITCHRD,BIT80          IS ERROR BIT ON             UL0H
         BC    1,SCANERR               YES- CONTROL STATEMENT ERROR
         TM    SWITCHRD,BIT40          LAST PARAMETER IN CARD ENCOUNTER
         BC    1,COMMTEST              TEST FOR COMMAND OR KEY WORD
         LTR   LENGTH,LENGTH           IS SCAN LENGTH ZERO
         BZ    SCANERR                 YES- CONTROL STATEMENT ERROR
         LTR   SCANADR,SCANADR         SCAN ADDRESS POINTER ZERO
         BZ    SCANERR                 YES- CONTROL STATEMENT ERROR
COMMTEST TM    SWITCHRD,BIT08          IS THIS A COMMAND WORD
         BC    1,OPRLUP                YES- CHECK COMMAND
         TM    SWITCHRD,BIT04          IS THIS A KEYWORD
         BC    14,SEQCKOUT             CARD SCAN MUST BE COMPLETED
         SPACE 1
*   KEYWORD LOOKUP ROUTINE
         L     GR7,KEYCHECK            GET DISPL. IN STARTTAB    A33314
         L     GR5,KEYSTART(GR7)       GET START ADDR. OF KEYTAB A33314
         L     GR7,KEYSTART+4(GR7)     GET END ADDR. OF KEYTAB   A33314
         B     SCANTBL                 SEARCH FOR KEY WORD
*   COMMAND WORD LOOKUP ROUTINE
OPRLUP   LA    GR5,COMDTABL            START OF COMMAND TABLE
         LA    GR7,1                   PREPARE TO INCREMENT LENGTH20194
         AR    LENGTH,GR7              INSURE BLANK FOLLOWS COMAND20194
         LA    GR7,COMDEND             END OF COMMAND TABLE
         MVI   KEYCHECK+3,X'00'   SET DISPL. FOR KEY START TAB   A33314
SCANTBL  STC   LENGTH,SAVLGTH          SAVE LENGTH REGISTER (0)
         CLI   SAVLGTH,8               TEST IF LENGTH LESS THAN 8
         BH    SCANERR                 NO- LENGTH EXCEEDS 8 ERROR
         LA    GR6,12                  INCREMENT VALUE
         MVC   SARG(8),BLANKS8         CLEAR SEARCH ARGUMENT TO BLANKS
         LR    GR9,LENGTH              BE SURE REGISTER IS EXECUTABLE
         BCTR  GR9,0                   DECREMENT LENGTH FOR EXECUTE
         EX    GR9,OPRLUP3             MOVE PARAMETER TO STORAGE AREA
OPRCMP   CLC   SARG(8),0(GR5)          TEST IF SEARCH ARGUMENT IN TABLE
         BNE   OPRLUP2                 NO- CONTINUE SCAN OF TABLE
         LA    GR5,8(GR5)              ENTRY ADDRESS OF ROUTINE
         LM    LENGTH,SCANADR,STOREG   LOAD SCAN REGISTERS ( 0 AND 1)
         BR    GR5                     ENTER PROPER ROUTINE
OPRLUP2  BXLE  GR5,GR6,OPRCMP          LOOP TO CONTINUE SEARCH
         B     SCANERR                 COMMAND INVALID- NOT IN TABLE
* START AND END ADDRESSES OF VALID KEYWORDS IN KEYTAB            A33314
KEYCHECK DC    F'0'               DISPLACEMENT IN THIS TABLE     A33314
KEYSTART DC    A(OTHKEYS)         FIRST KEY FOR FUNCTION STMT    A33314
         DC    A(KEYEND)          LAST KEY FOR FUNCTION STMT     A33314
         DC    A(NMRKEYS)         FIRST KEY FOR NUMBER STMT      A33314
         DC    A(KEYEND1)         LAST KEY FOR NUMBER STMT       A33314
         DC    A(DELKEYS)         FIRST KEY FOR DELETE STMT      A33314
         DC    A(KEYEND2)         LAST KEY FOR DELETE STMT       A33314
         EJECT
*    KEYWORD LOOKUP TABLE
         DS    0F
* TABLE OF VALID KEYWORDS AND ADDRESSES OF THE ANALYSIS ROUTINES A33314
DELKEYS  EQU   *                  FIRST VALID KEY FOR DELETE     A33314
NMRKEYS  EQU   *                  FIRST VALID KEY FOR NUMBER     A33314
         DC    C'SEQ1    '                                       A33314
         B     CHKSEQ1                                           A33314
KEYEND2  EQU   *                  LAST VALID KEY FOR DELETE      A33314
         DC    C'SEQ2    '                                       A33314
         B     CHKSEQ2                                           A33314
         DC    C'INCR    '                                       A33314
         B     CHKINCR                                           A33314
         DC    C'NEW1    '                                       A33314
         B     CHKNEW1                                           A33314
KEYEND1  EQU   *                  LAST VALID KEY FOR NUMBER      A33314
         DC    C'INSERT  '                                       A33314
         B     CHKINSRT                                          A33314
OTHKEYS  EQU   *                  FIRST VALID KEY FOR FUNCTION   A33314
         DC    C'LIST    '                                       A33314
         B     CHKLIST                                           A33314
         DC    C'NAME    '                                       A33314
         B     CHKNAME                                           A33314
         DC    C'DATA    '              DATA FOR NO LABEL        A45205
         B     CHKDATA                  CHECK IF VALID           A45205
         DC    C'COLUMN  '
         B     CHKCOLMN
         DC    C'SEQFLD  '
         B     CHKIDENT
         DC    C'MEMBER  '
         B     CHKMEMBR
         DC    C'NEW     '
         B     CHKNEW
         DC    C'UPDATE  '
         B     UPDATANL
         DC    C'LEVEL   '
         B     CHKLEVEL
         DC    C'SSI     '
         B     CHKSSI
         DC    C'SOURCE  '
         B     CHKSORCE
         DC    C'INHDR   '
         B     CHKINHDR
         DC    C'INTLR   '
         B     CHKINTRL
         DC    C'OUTHDR  '
         B     CHKOUTHD
         DC    C'TOTAL   '
         B     CHKTOTAL                                            UL0H
KEYEND   DC    C'OUTTLR  '             END OF TABLE OF KEY WORDS
         B     CKOUTTLR
*    COMMAND LOOKUP TABLE
         DS    0F
COMDTABL DC    C'ADD     '             START OF COMMAND WORD TABLE
         B     ADDANAL                 ADDRESS OF COMMAND ANALYSIS
LABEL    DC    C'LABEL   '                                         UL0H
         B     LABELANL                                            UL0H
         DC    C'CHANGE  '
         B     CHNGEANL
         DC    C'NUMBER  '
         B     NUMBRANL
         DC    C'DELETE  '
         B     DELETANL
         DC    C'REPL    '
         B     REPLANAL
         DC    C'REPRO   '
         B     REPROANL
         DC    C'ALIAS   '
         B     ALIASANL
COMDEND  DC    C'ENDUP   '             END OF COMMAND WORD TABLE
         B     ENDUPANL
         EJECT
***********************************************************************
***        CONTROL STATEMENT KEYWORD/PARAMETER PROCESSING           ***
***********************************************************************
         SPACE 1
ADDANAL  BAL   GR5,ADDONLY             CHECK ADD/REPL SEQUENCE
         CLI   COMDCD,X'0F'            PREVIOUS COMMAND IN EFFECT
         BE    KEEPPEND                YES- KEEP COMMAND PENDING
         MVI   ADDSW,X'0F'             SET ADD SWITCH ON FIRST ADD
         B     SETNDRET                SET COMMAND SWITCH AND CONTINUE
         SPACE 1
REPLANAL LA    GR5,RETREPL             RETURN FROM SEQUENCE CHECK
REPLADD  TM    CTONLYSW,X'11'     IS THERE AN OLD MASTER         A30480
         BM    INVALOP            NO.. CAN DO ADD ONLY           A30480
ADDONLY  CLI   REPLSW,X'0F'            TWO CONSECUTIVE REPL STATEMENTS
         BE    ABSEQERR                YES- STATEMENT SEQUENCE ERROR
         CLI   ADDSW,X'0F'             CONSECUTIVE REPL-ADD CARDS
         BE    ABSEQERR                YES- STATEMENT SEQUENCE ERROR
         CLI   CHNGESW,X'0F'           CONSECUTIVE COMD CARDS
         BE    ABSEQERR                YES- CONTROL SEQUENCE ERROR
         CLI   FINICCS,X'0F'      SYSIN THRU                     A24189
         BE    TSTSEQ             ENDUP BEING PROCESSED          A24189
         CLI   INPLCSW,X'0F'            UPDATE INPLACE JUST DONE PTM901
         BE    KEEPPEND           YES-FINISH THAT FUNCTION     @ZA01692
TSTSEQ   MVI   IGNORESW,X'00'     CLEAR IGNORE /* AT EACH COMD   A24189
         BAL   GR9,TESTSEQC            TEST IF MULTIPLE COMDS ALLOWED
         BR    GR5                     SEQUENCE IS OK
RETREPL  CLI   COMDCD,X'0F'            PREVIOUS COMMAND IN EFFECT
         BNE   SETREPLS                NO- SET REPLACE SWITCH
KEEPPEND MVI   COMDCD2,X'0F'           SET COMMAND PENDING SWITCH
         MVI   DONOTRDC,X'0F'          DO NOT READ MORE CONTROL CARDS
         B     STOWIT                  RETURN TO PROCESSING CSECT
SETREPLS MVI   REPLSW,X'0F'            FLAG AS REPLACE FUNCTION
SETNDRET MVI   COMDCD,X'0E'            TEMPORARY SETTING OF COMMAND SW
         CLI   FLUSHSW,X'0F'           IS FLUSH ROUTINE PROCESSING
         BNE   TOSCAN                  NO- CONTINUE
         MVI   RIDIT2SW,X'0E'          SET AS COMMAND DURING FLUSH
         B     TOSCAN                  RETURN TO SCAN
CHNGEANL DS    0H                                              @ZA01691
         MVC   LASTCTRC(8),ZEROHEX      REINITIALIZE LAST SEQ. NO.19013
         CLI   COMDCD,X'0F'            PREVIOUS COMMAND IN EFFECT
         BNE   ANYOLD             NO,GO TO SEE IF ANY OLD D/S  @ZA01691
         CLI   ADDSW,X'0F'        CONSECUTIVE COMD CARDS?      @ZA01691
         BE    ABSEQERR           YES,STATEMENT SEQ ERROR      @ZA01691
         CLI   REPLSW,X'0F'       CONSECUTIVE COMD CARDS?      @ZA01691
         BE    ABSEQERR           YES,STATEMENT SEQ ERROR      @ZA01691
         CLI   CHNGESW,X'0F'      CONSECUTIVE COMD CARDS?      @ZA01691
         BE    ABSEQERR           YES,STATEMENT SEQ ERROR      @ZA01691
         B     KEEPPEND           KEEP COMMAND PENDING         @ZA01691
ANYOLD   BAL   GR5,REPLADD        CHECK FOR CONSECUTIVE COMD   @ZA01691
         MVI   CHNGESW,X'0F'           SET CHANGE SWITCH
         B     SETNDRET                SET COMMAND SWITCH AND CONTINUE
ALIASANL CLI   ALIASW,X'0F'            PREVIOUS ALIAS
         MVI   DONOTRDC,X'00'          ALLOW NEXT STATEMENT TO BE READ
         BE    ALIAS2                  SET ALIAS PENDING SWITCH    MA0H
         CLI   PSSWNM,X'00'            IS NEW MASTER SEQUENTIAL
         BE    IGNOREPS                YES- ALIAS HAS NO MEANING
         CLI   INPLCSW,X'00'           IS IT UPDATE=INPLACE        UL0H
         BNE   IGNORALS                THEN PRINT IGNORE ALIAS MSG UL0H
         CLI   ENDONLY,X'0F'           ACCEPT ENDUP ONLY           4929
         BE    STOWIT1                 YES GO READ MORE SYSIN      4929
         MVI   ALIASW,X'0F'            SET ALIAS SWITCH
         B     TOSCAN                  RETURN TO SCAN
ALIASTNM CLI   ALIASW2,X'0F'      NAME ON FIRST ALIAS CARD?    @ZA01682
         BE    MOREALIA           NO,BRANCH TO ADJUST POINTER  @ZA01682
         LA    GR7,ALIASNM        ADDR OF ALIAS NAME SAVE      @ZA01682
         ST    GR7,ALIASNM1       STORE INITIAL POINTER TO     @ZA01682
         ST    GR7,ALIASNM2       USE WHEN SEVERAL ALIAS       @ZA01682
         B     ISNAMEOK                CHECK NAME VALIDITY- STORE IT
MOREALIA L     GR7,ALIASNM1       PICK UP LAST ADDRESS         @ZA01682
         LA    GR7,8(GR7)         AND ADD TO MOVE NEXT NAME    @ZA01682
         ST    GR7,ALIASNM1       SAVE THIS POINTER            @ZA01682
         LA    GR5,ALIASNM1       ADDRESS OF ALIAS SAVE END    @ZA01682
         CR    GR7,GR5            MORE THAN 16 ALIAS?          @ZA01682
         BE    INVALOP            YES,GO TO GIVE MSG           @ZA01682
         B     ISNAMEOK           NO,GO TO CHECK NAME          @ZA01682
ALIAS2   CLI   ALIASW2,X'0F'      ALREADY TWO ALIASES?         @ZA01682
         BE    TOSCAN             YES,SCAN THE STATEMENT       @ZA01682
         OI    ALIASW2,X'0F'      SET SW MORE THAN ONE ALIAS   @ZA01682
         MVI   COMDCD2,X'1F'           TURN ON TO FORCE STOW   @ZA01690
         MVI   DONOTRDC,X'0F'          DO NOT READ SYSIN           MA0H
         B     STOWIT                  PROCESS ALIAS               MA0H
CARDGET  L     GR15,PRIMEWRT           ADDRESS OF MESSAGE ROUTINE
         BALR  GR14,GR15               GIVE MESSAGE
READNOW  MVI   PRINTWRT,X'0F'          NEXT RECORD TO BE READ
         B     STOWIT                  RETURN TO PROCESSING CSECT
IGNOREPS LA    GR11,PSALIAS       ALIAS IGNORED MESSAGE CODE     A30501
         B     ALIASERR                                          A30501
IGNORALS LA    GR11,UPALIAS        ALIAS IGNORED MESSAGE CODE    A30501
ALIASERR MVI   PRINTED,X'00'       CLEAR PRINT SWITCH            A30501
         BAL   GR14,CARDPRNT       PRINT   INVALID STATEMENT     A30501
         L     GR15,PRIMEWRT       ADDRESS OF MESSAGE WRITER     A30501
         MVI   PRINTED,RESET0      CLEAR PRINT  SWITCH           A30501
         BALR  GR14,GR15           GIVE MESSAGE                  A30501
         MVI   PRINTWRT,X'0F'           SET SWITCH CARD PRINT    A41839
         B     STOWIT                                            A30501
         SPACE 1
CHKNEW   CLI   CTONLYSW,X'01'          PARM=NEW SPECIFIED      @ZA01693
         BE    SCANERR            YES INVALID USE OF NEW=PO    @ZA01693
*                                 OR NEW=PS                    @ZA01693
         TM    FIRSENTR,X'0F'   TEST IF 2:ND COMMAND WITH NEW= @ZA07321
         BO    INVALOP          YES,INVALID OPERATION          @ZA07321
         BAL   GR9,PARATEST       GET NEXT CORRECT PARAMETER   @ZA01693
         BAL   GR9,TESTCMD             TEST IF PARAMETER ON A COMD CARD
         LA    GR6,2                   MAXIMUM SIZE OF THIS PARAMETER
         L     GR11,DCBNM              ADDRESS OF NEW MASTER DCB
         CR    GR6,LENGTH              IS PARAMETER SIZE 2 BYTES
         BNE   SCANERR                 NO- ERROR
         OI    PARMSW,X'20'            FLAG AS 'NEW' PARAMETER
         L     GR10,DCBOM              OLD MASTER DCB ADDRESS
         CLC   0(2,SCANADR),NEWPO      IS NEW MASTER TO BE PARTITIONED
         BE    PS2PONOW                NEW MASTER PO, OLD IS PS
         CLC   0(2,SCANADR),NEWPS      IS NEW MASTER TO BE SEQUENTIAL
         BNE   SCANERR                 NO- INVALID PARAMETER
         MVI   BDSORG(GR11),PSDSET     SET DCB ORGANIZATION AS PS  3628
         MVI   BDSORG(GR10),PODSET     SET OLD MASTER DCB AS PO    3628
         MVI   PSSWOM,X'0F'            FLAG OLD MASTER AS PO
         MVI   PSSWNM,X'00'            SET NEW MASTER SWITCH TO PS
         B     TOSCAN                  RETURN TO SCAN
PS2PONOW MVI   BDSORG(GR11),PODSET     SET NEW MASTER AS PO        3628
         MVI   BDSORG(GR10),PSDSET     SET OLD MASTER AS PS        3628
         MVI   PSSWNM,X'0F'            FLAG NEW MASTER AS PO
         MVI   PSSWOM,X'00'            FLAG OLD MASTER AS PS       3628
         B     TOSCAN                  RETURN TO SCAN
CHKLIST  BAL   GR9,PARATEST            GET NEXT PARAMETER
         BAL   GR9,TESTCMD             TEST IF PARAMETER ON A COMD CARD
         LA    GR6,3
         CR    GR6,LENGTH              IS PARAMETER 3 BYTES (ALL)
         BNE   SCANERR                 NO- ERROR
         CLC   0(3,SCANADR),LISTSET    IS LIST SETTING FOR ALL
         BNE   SCANERR                 NO- ERROR
         MVI   FLLISTSW,X'01'          LIST EVERYTHING
         B     TOSCAN                  RETURN TO SCAN
         SPACE 1
CHKINSRT CLI   INPLCSW,X'0F'      UPDATE=INPLACE REQUESTED       A33318
         BE    SCANERR            YES.. INVALID PARAMETER        A33318
         BAL   GR9,PARATEST            GET PARAMETER
         LA    GR6,3
         CR    GR6,LENGTH              IS PARAMETER 3 BYTES
         BNE   SCANERR                 NO- CONTROL STATEMENT ERROR
         CLC   0(3,SCANADR),YESYES     IS SETTING 'YES'
         BNE   SCANERR                 NO- CONTROL STATEMENT ERROR
         CLI   INSERTSW,X'0E'          PREVIOUS INSERT
         BNE   SETINSRT                NO- FLAG AS FIRST INSERT
         MVI   INSRTRD,X'01'           SET TO INDICATE ANOTHER INSERT
         B     TOSCAN                  RETURN TO SCAN
SETINSRT MVI   INSERTSW,X'0F'          SET FIRST INSERT SWITCH SETTING
         B     TOSCAN                  RETURN TO SCAN
         SPACE 1
*              TEST PARAMETERS FOR SCAN ERRORS
PARATEST L     GR15,RDCARD                                         UL0H
         BALR  GR14,GR15               SCAN FOR NEXT PARAMETER     UL0H
         TM    SWITCHRD,BIT80          SCAN ERROR SWITCH ON
         BC    1,SCANERR               YES- CONTROL STATEMENT ERROR
         TM    SWITCHRD,BIT02          IS THIS A PARAMETER
         BC    14,SCANERR              NO- CONTROL STATEMENT ERROR
         LM    LENGTH,SCANADR,STOREG   LOAD POINTERS TO LENGTH AND PARM
         BR    GR9                     RETURN
CHKIDENT MVI   SQFSW,SWF0              BEGIN SEQFLD ANALYSIS    YM07522
         BAL   GR9,PARATEST            GET NEXT PARAMETER       YM07522
         BAL   GR9,TESTCMD             TEST IF PARAMETER ON A COMD CARD
         CLI   SQFSW,SWFC              SEQFLD=(  FORM           YM07522
         BNE   ANSQF1                  NO - ANALIZE FIRST DDL   YM07522
         LTR   LENGTH,LENGTH           LENGTH OF 0              YM07522
         BZ    ANSQF2                  YES - USE DEFAULT        YM07522
ANSQF1   LA    GR9,TESTSEQ             RETURN FROM COLUMN CHECK YM07522
         LA    GR6,3                   VALID PARAMETER LENGTH
         CR    GR6,LENGTH              IS PARAMETER THREE BYTES
         BNE   SCANERR                 NO- CONTROL STATEMENT ERROR
COLMCHKX CLC   0(2,SCANADR),COLUMN80   IS COLUMN GREATER THAN 80
         BH    SCANERR                 YES- CONTROL STATEMENT ERROR
         CLC   0(2,SCANADR),COLUMN01   IS COLUMN LESS THAN 1
         BL    SCANERR                 YES- CONTROL STATEMENT ERROR
         BR    GR9                     RETURN
TESTSEQ  CLI   2(SCANADR),C'8'         IS SEQUENCE SIZE MORE THAN 8
         BH    SCANERR                 YES- CONTROL STATEMENT ERROR
         CLI   2(SCANADR),C'1'         IS SEQUENCE SIZE LESS THAN 1
         BL    SCANERR                 YES- CONTROL STATEMENT ERROR
         MVN   IDENTY+1(1),2(SCANADR)  MOVE SEQUENCE SIZE TO BUFFER
         LA    GR9,STORID              RETURN FROM ROUTINE
         LA    GR7,IDENTXX             START COLUMN SEQUENCE NUMBER
GETCOLMS MVC   TEMPWA(8),ZERO          CLEAR BUFFER
         PACK  TEMPWA+6(2),0(2,SCANADR)   UNPACK COLUMN
         CVB   GR5,TEMPWA              CONVERT TO BINARY
         BCTR  GR5,0                   DECREMENT FOR INDEXING
         STH   GR5,0(GR7)              STORE IN IDENTXX OR UPDATCOL
         LH    GR3,LRECL               PICK UP LOGICAL RECORD LENGTH
         LA    GR5,1(GR5)              RETURN TO ORIGINAL COLUMN
         CR    GR5,GR3                 COLUMN LESS THAN LRECL (IDENTXX)
         BH    SCANERR                 NO- FIELD WILL BE OUT OF LRECL
         BR    GR9                     RETURN TO STORID OR TOSCAN
STORID   SR    GR8,GR8                 PROCESS IDENT FIELD
         IC    GR8,IDENTY+1            SIZE OF SEQUENCE FIELD
         BCTR  GR8,0                   DECREMENT
         SR    GR3,GR8                 MAXIMUM START OF SEQUENCE FIELD
         CR    GR5,GR3                 WILL SEQUENCE RUN OUT OF LRECL
         BH    SCANERR                 YES- ERROR
ANSQF2   CLI   SQFSW,SWFC              NUMERIC DDL SPECIFIED    YM07522
         BE    CKDDL2                  YES - ANALIZE IT         YM07522
         B     TOSCAN                  RETURN TO SCAN
CKDDL2   MVI   DELIMIN,BIT80           SET 2ND PARM FLAG        YM07522
         BAL   GR9,PARATEST            GET NUMERIC DDL          YM07522
         LA    GR6,L3                  SET EXPECTED LENGTH      YM07522
         CR    GR6,LENGTH              IS LENGTH CORRECT        YM07522
         BNE   SCANERR                 NO - ERROR               YM07522
         CLI   SQFSW,SWFE              PROPER SYNTAX            YM07522
         BNE   SCANERR                 NO - ERROR               YM07522
         MVN   IDENTN+D1,D2(SCANADR)   MOVE SIZE TO BUFFER      YM07522
         LA    GR9,STORNM              RETURN FROM ROUTINE      YM07522
         LA    GR7,IDENTNN             START NUMERIC FIELD      YM07522
         B     GETCOLMS                ESTABLISH NUMERIC OFFSET YM07522
TESTCMD  TM    COMDCD,X'0E'            IS PARAMETER ON A COMMAND CARD
         BC    12,SCANERR              NO- CONTROL STATEMENT ERROR
         BR    GR9                     RETURN
STORNM   CLC   IDENTNN(L2),IDENTXX     NUMERIC AFTER SEQ DDL    YM07522
         BL    SCANERR                 NO - ERROR               YM07522
         SR    GR8,GR8                 INITIALIZE WORK REGS     YM07522
         SR    GR5,GR5                                          YM07522
         IC    GR8,IDENTN+D1           GET NUMERIC LENGTH       YM07522
         AH    GR8,IDENTNN             GET END OF NUMERIC       YM07522
         IC    GR5,IDENTY+D1           GET SEQ NO LENGTH        YM07522
         AH    GR5,IDENTXX             GET END OF SEQ NO        YM07522
         CR    GR8,GR5                 NUMERIC WITHIN SEQ NO    YM07522
         BH    SCANERR                 NO - ERROR               YM07522
         LH    GR8,IDENTNN             COMPUTE OFFSET OF        YM07522
         SR    GR5,GR8                 * NUMERIC FROM END       YM07522
         BCTR  GR5,GR0                 * OF SEQ FIELD           YM07522
         STH   GR5,NUMOFFST            SAVE OFFSET              YM07522
         B     TOSCAN                  RETURN TO SCAN           YM07522
REPROANL BAL   GR5,REPLADD             CHECK FOR CONSECUTIVE COMMANDS
         CLI   COMDCD,X'0F'            A COMMAND IN EFFECT
         BE    KEEPPEND                YES- KEEP REPRO PENDING
         MVI   REPROSW,X'0F'           SIGNAL REPRO COMMAND
         B     SETNDRET                SET COMMAND SWITCH AND RETURN
CHKCOLMN BAL   GR9,PARATEST            GET THE PARAMETER
         BAL   GR9,TESTCMD             TEST IF PARAMETER ON A COMD CARD
         LA    GR6,2                   MAXIMUM SIZE OF PARAMETER
         CR    GR6,LENGTH              MAXIMUM OF 2 BYTES
         BE    CHECKOUT                YES- CHECK IF VALID COLUMNS
         BL    SCANERR                 GREATER THAN 2- STATEMENT ERROR
         MVC   COLM1BUF+1(1),0(SCANADR) MOVE TO BUFFER THE 1 CHARACTER
         LA    SCANADR,COLM1BUF        PUT ADDRESS OF 2 CHARACTER AREA
CHECKOUT BAL   GR9,COLMCHKX            CHECK IF COLUMN VALID
         MVI   COLUMNSW,X'0F'          SIGNAL SELECTIVE UPDATE
         LA    GR9,TOSCAN              LOAD RETURN ADDRESS
         LA    GR7,UPDATCOL            BUFFER FOR COLUMN UPDATE START
         B     GETCOLMS                CONVERT COLUMN AND CHECK
CHKNAME  BAL   GR9,PARATEST            GET PARAMETER
         CLI   ALIASW,X'0F'            IS NAME FIELD IN ALIAS STATEMENT
         BE    ALIASTNM                YES- ENTER ALIAS ANALYSIS
         LA    GR7,MEMNAME             OLD MASTER MEMBER NAME BUFFER
         OI    PARMSW,X'10'            FLAG AS 'NAME' PARAMETER
         TM    PARMSW,X'20'            HAS 'NEW' PARAMETER COME IN
         BC    1,ESTNAME               YES- DCB AND SWITCHES SET BY NOW
         L     GR10,DCBOM              ADDRESS OF OLD MASTER DCB   3628
         L     GR11,DCBNM              ADDRESS OF NEW MASTER DCB   3628
         MVI   BDSORG(GR10),PODSET     FLAG DCB OM AS PARTITIONED  3628
         MVI   PSSWNM,X'0F'            FLAG NEW MASTER AS PODS
         MVI   PSSWOM,X'0F'            MUST ALSO FLAG OM FOR TESTS
         MVI   BDSORG(GR11),PODSET     FLAG DCB NM AS PARTITIONED  3628
ESTNAME  BAL   GR9,TESTCMD             TEST IF PARAMETER ON A COMD CARD
ISNAMEOK LA    GR5,BLANKS8             DOUBLE WORD OF BLANKS
         LA    GR9,TESTCHR             RETURN FROM SUBROUTINE
CLRSTOR  C     LENGTH,EIGHT            TEST IF NAME/SEQ MORE THAN 8
         BH    SCANERR                 CONTROL STATEMENT ERROR
         MVC   0(8,GR7),0(GR5)         CLEAR BUFFER TO BLANKS OR ZERO
         BR    GR9                     RETURN
TESTCHR  LR    GR6,LENGTH              IF LENGTH REG IS 0 CANNOT EXEC
         BCTR  GR6,0                   DECREMENT FOR EXECUTE
         EX    GR6,MOVNAME             MOVE NAME LEFT ADJUST
         CLI   0(GR7),C'A'             FIRST CHARACTER ALPHABETIC
         BL    NATLETTR      CHAR NOT ALPHA,MAY BE NAT'L LETTER    3083
         CLI   0(GR7),C'Z'             FIRST CHARACTER ALPHABETIC
         BH    SCANERR                 CONTROL STATEMENT ERROR
VALCHAR  B     TOSCAN        VALID CHARACTER, KEEP SCANNING        3083
NATLETTR CLI   0(GR7),C'$'   IS CHARACTER A DOLLAR SIGN            3083
         BE    VALCHAR       YES, CHARACTER IS VALID               3083
         CLI   0(GR7),C'@'   IS CHARACTER AN "AT" SIGN             3083
         BE    VALCHAR       YES, CHARACTER IS VALID               3083
         CLI   0(GR7),C'#'   IS CHARACTER A POUND SIGN             3083
         BE    VALCHAR       YES, CHARACTER IS VALID               3083
         B     SCANERR       CHARACTER WAS NONE OF THESE, ERROR    3083
CHKSORCE BAL   GR9,TESTCMD             IS THIS KEY WORD ON COMD CARD
         BAL   GR9,PARATEST            GET THE PARAMETER
         LA    GR6,1                   SIZE OF SOURCE DATA
         CR    LENGTH,GR6              MORE THAN 1 BYTE CODE GIVEN
         BNE   SCANERR                 YES- CONTROL STATEMENT ERROR
         CLI   0(SCANADR),C'1'         TEST IF IBM MODIFICATION
         BE    STORCODE                YES- CODE IS OK
         CLI   0(SCANADR),C'0'         TEST IF USER MODIFICATION
         BNE   SCANERR                 NO- INVALID SOURCE CODE
STORCODE MVC   SOURCECD(1),0(SCANADR)  STORE SOURCE CODE
         B     TOSCAN                  RETURN TO SCAN
         SPACE 1
CHKLEVEL BAL   GR9,TESTCMD             IS KEY WORD ON COMD CARD
         BAL   GR9,PARATEST            GET THE PARAMETER
         LA    GR6,2                   MAXIMUM LENGTH THIS PARAMETER
         CR    GR6,LENGTH              IS PARAMETER 2 BYTES (00 - 99)
         BNE   SCANERR                 NO- CONTROL STATEMENT ERROR
         BAL   GR9,TESTSCAN            TEST AND CONVERT TO HEX
         PACK  LEVELXX(2),RRSAVE(3)    STORE CHANGE LEVEL
         B     TOSCAN                  RETURN TO SCAN
         SPACE 1
CHKSSI   BAL   GR9,TESTCMD             IS KEY WORD ON COMD CARD
         BAL   GR9,PARATEST            GET PARAMETER
         C     LENGTH,EIGHT            IS PARAMETER LENGTH 8
         BNE   SCANERR                 NO- CONTROL STATEMENT ERROR
         LA    GR9,PACKSSI             RETURN FROM TEST/CONVERT
TESTSCAN LA    GR3,RRSAVE              ADDRESS OF WORK AREA
         LR    GR8,LENGTH              LOAD COUNT OF REGISTER WITH SIZE
         EX    GR8,MOVESSIL            MOVE SSI OR LEVEL (ONE EXTRA OK)
SCANLOOP TM    0(GR3),C'0'             IS DIGIT NUMERIC
         BO    UPPOINTR                YES- PASS TO NEXT CHARACTER
         CLI   0(GR3),C'A'             IS THIS A HEX CHARACTER (A-F)
         BL    SCANERR                 NO- ERROR
         CLI   0(GR3),C'F'             IS THIS A HEX CHARACTER (A-F)
         BH    SCANERR                 NO- ERROR
         OI    0(GR3),X'20'            CHANGE C ZONE TO E ZONE
UPPOINTR LA    GR3,1(GR3)              POINT TO NEXT CHARACTER
         BCT   GR8,SCANLOOP            CHECK ALL EIGHT CHARACTERS
         TR    RRSAVE(8),ALTTT-225     CONVERT TO HEX
         BR    GR9                     RETURN
PACKSSI  PACK  SSIBUF(5),RRSAVE(9)     PACK SSI INFORMATION
         MVI   SSISW,X'0F'             SET SWITCH SSI TO OVERIDE LEVEL-
         B     TOSCAN                  RETURN TO SCAN
         SPACE 1
UPDATANL TM    CHNGESW,X'0F'           IS PARAMETER ON A CHANGE CARD
         BC    14,SCANERR              NO- ERROR
         CLI   FIRSENTR,BIT10          FIRST ENTRY
         BNE   STOPNOW                 NO- CANNOT MIX INPLACE/REGULAR
         BAL   GR9,PARATEST            GET PARAMETER
         LA    GR6,7
         CR    GR6,LENGTH              TEST IF 7 CHARACTERS
         BNE   SCANERR                 ERROR IF NOT 7
         CLC   0(7,SCANADR),INPLACE    COMPARE IF PARAMETER = 'INPLACE'
         BNE   SCANERR                 NO- CONTROL STATEMENT ERROR
         TM    CTONLYSW,X'11'     IS THERE AN OLD MASTER         A30480
         BM    INVALOP            NO.. CANNOT UPDATE INPL. NM    A30480
         MVI   INPLCSW,X'0F'           FLAG AS UPDATE IN PLACE- NO NM
         B     TOSCAN                  RETURN TO SCAN
         SPACE 1
DELETANL CLI   FLUSHSW,X'0F'           FLUSHING IN PROCESS
         BE    MSGNREAD                YES- GIVE SCAN MESSAGE
         MVI   KEYCHECK+3,X'10'   SET DISPL. FOR KEY START TAB   A33314
         CLI   INPLCSW,X'0F'           UPDATE TYPE RUN
         BE    INVALOP                 INVALID OPERATION DELETE/UPDATE
         TM    CHNGESW,X'0E'           FOLLOWS A CHANGE COMMAND
         BC    12,ABSEQERR             NO- CONTROL SEQUENCE ERROR
         CLI   DELETSW,X'0F'           FOLLOWS A DELET CARD IMMEDIATELY
         BE    DELETPND                YES- KEEP THIS DELET PENDING
         MVI   DELETSW,X'0F'           FLAG AS DELETE STATEMENT
         MVI   CHNGESW,X'0E'           FLAG AS CHANGE PLUS ANOTHER COMD
         B     TOSCAN                  RETURN TO SCAN
DELETPND MVI   DELET2SW,X'0F'          SET 2 CONSECUTIVE DELETS SWITCH
CHKIFOPN MVI   DONOTRDC,X'0F'          DO NOT READ MORE CONTROL CARDS
         CLI   OPENSW,X'0F'            HAS DATA SET BEEN OPENED
         BE    STOWIT                  NO- RETURN TO CSECT2- OPEN IT
         B     READNOW                 RETURN TO PROCESS
         SPACE 1
NUMBRANL CLI   FLUSHSW,X'0F'           FLUSHING IN PROCESS
         BE    MSGNREAD                YES- GIVE SCAN MESSAGE
         MVI   KEYCHECK+3,X'08'   SET DISPL. FOR KEY START TAB   A33314
         TM    CHNGESW,X'0E'           IN SEQUENCE OF CHANGE COMMANDS
         BO    CHNGEOEX                YES- COMMAND WITH CHANGE
         TM    REPLSW,X'0E'            IN SEQUENCE OF REPL COMD
         BO    REPLOEX                 YES- FOLLOWS A REPLACE CARD
         TM    ADDSW,X'0E'             IN SEQUENCE OF ADD COMD
         BC    12,ABSEQERR             DOES NOT FOLLOW ADD-REPL-CHNGE
         CLI   NUMBRSW,X'0F'           PREVIOUS NUMBER STATEMENT   6701
         BE    INVALOP                 ONLY ONE NUMBER FOR ADD     6701
         MVI   ADDSW,X'0E'             RESET TO SHOW NUMBER AFTER ADD
         B     NUMBER01                NEW NUMBER STATEMENT        6701
NUMBRSWS CLI   NUMBRSW,X'0F'           PREVIOUS NUMBER STATEMENT
         BNE   NUMBER01                NO- THIS IS NEW ONE
         TM    INSERTSW,X'0F'                                    A25432
         BM    NUMBER01                                          A25432
         BZ    ONEINSRT                                          A25432
         CLI   FSTINSRT,X'0F'          CONSECUTIVE INSERT=YES ?  A25432
         MVI   FSTINSRT,X'00'          RESET FIRST DATA CARD SW  A25432
         BNE   ABSEQERR                YES, ERROR - DATA MUST    A25432
*                                      FOLLOW INSERT=YES         A25432
ONEINSRT CLI   RENUMALL,X'0F'                                    A25432
         BE    INVALOP                 YES- INVALID OPERATION
         MVI   NUMBR2SW,X'0F'          SET 2 CONSECUTIVE NUMBER SWITCH
         B     CHKIFOPN                SET UP TO CONTINUE PROCESSING
NUMBER01 MVI   NUMBRSW,X'0E'           FLAG AS NUMBER STATEMENT SCANNED
         B     TOSCAN                  RETURN TO SCAN
CHNGEOEX MVI   CHNGESW,X'0E'           RESET TO SHOW NUMBER AFTER CHNGE
         B     NUMBRSWS                SET NUMBER SWITCH
REPLOEX  TM    NUMBRSW,X'0F'           PREVIOUS NUMBER STMT      A25924
         BC    5,INVALOP               ONLY ONE NUMBER FOR REPL  A25924
         MVI   REPLSW,X'0E'     RESET TO SHOW NUMBER AFTER REPL  A25924
         B     NUMBRSWS                SET NUMBER SWITCH
MSGNREAD MVI   RIDIT2SW,X'0F'          SET TO SCAN BEYOND DETAIL CARD
         B     STOWIT                  BACK TO PROCESSING CSECT
         SPACE 1
TESTSEQC CLI   FINICCS,X'0F'           HERE FROM ENDUP ANALYSIS
         BCR   8,GR9                   YES- RETURN TO ENDUP ANALYSIS
         CLI   PSSWOM,X'00'            TEST IF OM PHYSICAL SEQUENTIAL
         BNE   ONENOTPS                OLD MASTER NOT PHYSICAL SEQUENT.
         CLI   PSSWNM,X'00'            TEST IF NM PHYSICAL SEQUENTIAL
         BCR   7,GR9                   NO- RETURN
CHKFIRST CLI   COMDCD,X'0F'            PREVIOUS COMD CARD ENCOUNTERED
         BCR   7,GR9                   NO- OK THIS IS FIRST COMD CARD
QUITNOW  LA    GR11,ERRMSGC            CODE FOR TERMINATE MESSAGE
         B     ALLENDS                 GIVE MESSAGE
STOPNOW  LA    GR11,UPDATMSG           CODE FOR INVALID INPLACE MESSAGE
         MVI   ENDONLY,X'0F'           FLUSH TO END            @AZ27974
ALLENDS  BAL   GR14,CARDPRNT           PRINT INVALID CARD        A33286
         L     GR15,PRIMEWRT           MESSAGE CSECT ADDR        A33286
         MVI   TEMPCON,12              SET TEMPORARY CONDITION CODE
         CLI   ENDONLY,X'0F'           INVALID UPDATE=INPLACE? @AZ27974
         BNE   MSGQUIT                 NO,GIVE MSG AND QUIT    @AZ27974
         CLC   TEMPCON(1),HICONCDE+1   RETURN CODE LESS?       @AZ27974
         BNH   NOTHIGH                 NOT HIGH, USE HICONCDE  @AZ27974
         MVC   HICONCDE+1(1),TEMPCON   SWAP FOR HIGHER CODE    @AZ27974
NOTHIGH  LA    GR14,SETFLUSH           RETURN ADDR AFTER MSG   @AZ27974
         BR    GR15                    PRINT MSG IN IEBUPLOG   @AZ27974
MSGQUIT  LA    GR14,QUITS              RETURN FROM MSG ROUT    @AZ27974
         BR    GR15                    GIVE MESSAGE- TERMINATE
ONENOTPS CLI   PSSWNM,X'00'            TEST IF NM PHYSICAL SEQUENTIAL
         BCR   7,GR9                   NO- RETURN
         B     CHKFIRST                SEE IF FIRST ENTRY- 1 SEQUENTIAL
         SPACE 1
CHKSEQ1  TM    ADDSW,X'0E'             PROCESSING ADD FUNC        20497
         BC    1,INVALOP               IF SO INVALID OPERATION    20497
         TM    REPLSW,X'0E'            PROCESSING REPL FUNCTION   20497
         BC    1,INVALOP               IF SO INVALID OPERATION    20497
         CLI   NUMBRSW,X'0E'           SEQUENCE ON NUMBER CARD     5666
         LA    GR8,TOSCAN              RETURN
         LA    GR7,DELTSEQ1            ADDRESS OF DELETE BUFFER SEQ1
         BE    NUMBR              YES,BRANCH TO SET NUMBER SEQ1 YA01677
         CLI   NUMBR2SW,X'0F'     SEQ1 ON SECOND NUMBER CARD    YA01677
         BNE   TESTPARM           NO, ON DELETE- BRANCH            5666
NUMBR    LA    GR7,NUMRSEQ1       ADDRESS OF NUMBER SEQ1 BUFFER YA01677
         OI    PARMSW,X'01'            FLAG AS 'SEQ1' PARAMETER
         CLI   NUMBR2SW,X'0F'     FIELD ON SECOND NUMBER CARD?  YA01677
         BE    TESTPARM           YES,GO TO TEST PARAMETER      YA01677
TESTUNIT TM    NUMBRSW,X'0E'           FIELD ON NUMBER CARD
         BC    14,SCANERR              NO- INVALID ON ANY OTHER
TESTPARM BAL   GR9,PARATEST            GET PARAMETER
         CLC   0(3,SCANADR),LISTSET    SEE IF SETTING IS 'ALL'
         BE    ALLNUMBR                YES- RENUMBER ENTIRE DATA SET
         LA    GR5,ZEROHEX             DOUBLE WORD OF HEX ZEROS
         BAL   GR9,CLRSTOR             CLEAR BUFFER AND RIGHT ADJUST
         LR    GR5,LENGTH              LENGTH OF PARAMETER      YM07522
         LA    GR7,8(GR7)              ALIGN TO END OF BUFFER
         SR    GR7,GR5                 BACK UP FROM RIGHT       YM07522
         BCTR  GR5,0                   DECREMENT FOR EXECUTE    YM07522
         EX    GR5,MOVNAME             RIGHT ADJUST SEQ NO      YM07522
         BR    GR8                     RETURN                   YM07522
CHKNMRIC EQU   *                       LOOP FOR NUMER. TESTING  OX00189
         CLI   0(GR5),LODIG            TEST IF NUMERIC CHAR     YM07522
         BL    SCANERR                 NO- CONTROL STATEMENT ERROR
         CLI   0(GR5),HIDIG            TEST IF VALID NUMBER     YM07522
         BH    SCANERR                 NO- CONTROL STATEMENT ERROR
         LA    GR5,D1(GR5)             SKIP TO NEXT CHARACTER   YM07522
         BCT   GR6,CHKNMRIC            SCAN NEXT CHAR. IF NECC. OX00189
         BR    GR8                     RETURN TOSCAN OR TESTNUMB
ALLNUMBR TM    NUMBRSW,X'0E'           SEQ1=ALL ON NUMBER STATEMENT
         BC    14,SCANERR              NO- INVALID CONTROL STATEMENT
         MVI   RENUMALL,X'0F'          SET RENUMBER ALL SWITCH
         B     TOSCAN                  RETURN TO SCAN
         SPACE 1
CHKSEQ2  LA    GR7,DELTSEQ2            ADDRESS OF DELETE SEQ2 BUFFER
         TM    ADDSW,X'0E'             PROCESSING ADD FUNC        20497
         BC    1,INVALOP               IF SO INVALID OPERATION    20497
         TM    REPLSW,X'0E'            PROCESSING REPL FUNCTION   20497
         BC    1,INVALOP               IF SO INVALID OPERATION    20497
         CLI   NUMBRSW,X'0E'      SEQUENCE ON NUMBER CARD          5666
         LA    GR8,TOSCAN              RETURN
         BNE   TESTPARM           NO, DELETE CAED - BRANCH         5666
         LA    GR7,NUMRSEQ2            ADDRESS OF NUMBER SEQ2 BUFFER
         OI    PARMSW,X'02'            FLAG AS 'SEQ2' PARAMETER
         B     TESTUNIT                GET PARAMETER AND TEST IT
         SPACE 1
CHKINCR  LA    GR7,INCRNUMB            ADDRESS OF INCREMENT BUFFER
         CLI   INSERTSW,X'0E'          PREVIOUS INSERT IN EFFECT
         BE    SAVEPARM                YES- SAVE THE PARAMETER
         LA    GR11,INCREMWA           INCREMENT FACTOR IN BINARY
FLAGINCR OI    PARMSW,X'04'            FLAG AS 'INCR' PARAMETER
         BAL   GR6,TESTNUMB            TEST IF PARM OK          YM07522
*        THE INCR VALUE MUST BE NUMERIC.                        YM07522
INCRNTST LR    GR5,GR10                POINT TO PARM VALUE      YM07522
         LA    GR6,L8                  SET LENGTH               YM07522
         BAL   GR8,CHKNMRIC            ENSURE NUMERICS          YM07522
         LA    GR6,L7                  SET LENGTH - 1           YM07522
         B     PACKNUM                 PACK PARM VALUE          YM07522
TESTNUMB LR    GR10,GR7                ADDRESS OF BUFFER
         BAL   GR8,TESTUNIT            ADJUST NUMBER            YM07522
         BR    GR6                     RETURN                   YM07522
PACKNUM  EX    GR6,PACKIT              PACK INCR OR NEW1 NO     YM07522
         CVB   GR6,TEMPWA              CONVERT TO BINARY
         ST    GR6,0(GR11)             SEQUENCE OR INCREMENT IN BINARY
         B     TOSCAN                  RETURN TO SCAN
SAVEPARM LA    GR11,TEMPINCR           TEMPORARY INCREMENT AREA
         B     FLAGINCR                CONTINUE- STORE NEW INCREMENT
         SPACE 1
CHKNEW1  LA    GR7,NEW1NUMB            ADDRESS OF NEW1 SEQUENCE NUMBER
         LA    GR11,SEQWA              STARTING SEQUENCE NUMBER BINARY
         CLI   INSERTSW,X'0E'          PREVIOUS INSERT IN EFFECT
         BNE   FLAGNEW1                NO- CONTINUE
         LA    GR11,TEMPSEQA           TEMPORARY SEQUENCE AREA
         LA    GR7,NEW1TEMP            TEMPORARY SEQ NO         YM07522
FLAGNEW1 OI    PARMSW,X'08'            FLAG AS 'NEW1' PARAMETER
         BAL   GR6,TESTNUMB            TEST PARM VALUE          YM07522
* IF NUMERIC SEQFLD COLUMNS SPECIFIED, VALIDATE NUMERIC FIELD   YM07522
         CLI   IDENTN+D1,RESET0        NUMERIC SEQFLD SPECIFIED YM07522
         BE    INCRNTST                TREAT LIKE INCR          YM07522
         LA    GR5,D7(GR10)            SET END OF BUFFER ADDR   YM07522
         SH    GR5,NUMOFFST            SET NUMERIC START        YM07522
         LR    GR10,GR5                SAVE ADDRESS             YM07522
         SR    GR6,GR6                                          YM07522
         IC    GR6,IDENTN+D1           GET NUMERIC SIZE         YM07522
         BAL   GR8,CHKNMRIC            ENSURE NUMERICS          YM07522
         IC    GR6,IDENTN+D1           RESET SIZE               YM07522
         BCTR  GR6,GR0                 SUBTRACT 1               YM07522
         B     PACKNUM                 PACK NUMBER              YM07522
         SPACE 1
CHKMEMBR LA    GR7,MEMBNEW             NEW MEMBER NAME, NEW PODS MADE
         BAL   GR9,PARATEST            GET PARAMETER
         CLI   CTONLYSW,X'01'          PARM=NEW SPECIFIED        A38748
         BE    SCANERR                 YES,INVALID USE OF CONTROLA38748
         MVI   NEWNAME,X'0F'           FLAG NM IS PO
         L     GR11,DCBNM              ADDRESS OF NEW MASTER DCB
         OI    PARMSW,X'40'            FLAG AS 'MEMBER' PARAMETER
         B     ESTNAME                 CHECK VALIDITY OF NAME- STORE IT
CHKTOTAL CLI   TOTALSW,X'00'            FIRST TOTAL PARM           UL0H
         BNE   INVALOP                  IF NOT,ERROR               UL0H
         MVI   TOTALSW,X'F0'            BEGIN TOTAL ANALYSIS       UL0H
         BAL   GR9,PARATEST             TEST FOR PARAMETER         UL0H
         BAL   GR9,TESTCMD              TEST FOR COMMAND           UL0H
         CLI   TOTALSW,X'FC'            HAS NAME BEEN ANALYSED     UL0H
         BNE   SCANERR                  IF NOT,ERROR               UL0H
         C     LENGTH,ZERO              LENCH GREATER THEN ZERO    UL0H
         BNH   INVALCHR                 IF NOT,ERROR               UL0H
         LA    GR5,BLANKS8                                         UL0H
         LA    GR7,TOTALRTN             USER ROUTINE NAME          UL0H
         BAL   GR9,CLRSTOR              CLEAR OUT BUFFER           UL0H
         LR    GR6,LENGTH               PICK UP LENGTH             UL0H
         BCTR  GR6,0                    DECREMENT                  UL0H
         EX    GR6,MOVNAME              AND MOVE IT TO SAVE AREA   UL0H
         BAL   GR14,TESTCHAR           DOES RTN NAME START WITH ALPUL0H
LOADTOTL LOAD  EPLOC=TOTALRTN                                      UL0H
         ST    0,TOTALADR                                          UL0H
         MVI   DELIMIN,X'80'           SET SECOND PARM SWITCH      UL0H
         BAL   GR9,PARATEST             GET CORE SIZE              UL0H
         CLI   TOTALSW,X'FE'            PROPER SYNTAX              UL0H
         BNE   SCANERR                  IF NOT,ERROR               UL0H
         LR    GR6,LENGTH               GET  LENGTH                UL0H
         C     GR6,ZERO                 IS IT GREATER THAN ZERO    UL0H
         BNH   NOCORE                   IF NOT,ERROR               UL0H
         C     GR6,FIVE                 IS IT LONGER THEN FIVE     UL0H
         BH    NOCORE                   IF SO,ERROR                UL0H
 SPACE  1
*     THIS ROUTINE WILL DETERMINE IF LENGTH IS A VALID DECIMAL NO. UL0H
         LA    GR6,0(SCANADR,GR6)                                  UL0H
         BCTR  GR6,0                   POINT TO LAST DIGIT         UL0H
         LR    GR9,LENGTH                                          UL0H
COMPNO   CLI   0(GR6),C'0'             US NO. LESS THEN 'F0'       UL0H
         BL    NOCORE             YES.. PRINT ERROR MESSAGE      A36128
         CLI   0(GR6),C'9'             IS CHARACTER GREATER THEN F9UL0H
         BH    NOCORE             YES.. PRINT ERROR MESSAGE      A36128
         BCTR  GR6,0                   DECREMENT POINTER BY ONE    UL0H
         BCT   LENGTH,COMPNO           DECREMENT COUNTER BY ONE    UL0H
         BCTR  GR9,0                    DECREMENT                  UL0H
         LA    GR9,112(0,GR9)     GET LENGTH FIRST OPERAND       A36128
         EX    GR9,PACKSIZE       PREPARE REQUESTED SIZE         A36128
         CVB   GR5,TEMPTOT                                       A36128
         C     GR5,MAXCORE        REQUESTED MORE THAN 32K        A36128
         BNL   NOCORE             YES.. ERROR PRINT MESSAGE      A36128
         ST    GR5,TOTALSIZ       SAVE REQUESTED SIZE            A36128
         MVC   TOTLPARM+4(4),DCBNM      MOVE IN DCB ADDRESS        UL0H
         LR    GR0,GR5                  PREPARE FOR GETMAIN        UL0H
         GETMAIN R,LV=(0)                                          UL0H
         ST    GR1,TOTLPARM+12          SAVE ADDRESS IN PARMLIST   000I
         STH   GR5,0(GR1)              SAVE AREA ADDRESS IN AREA   UL0H
         MVI   TOTALSW,X'FF'                                       UL0H
      L        GR5,DCBNM               LOAD ADDRESS OF NEW MAST DCBUL0H
         ST    GR5,TOTLPARM+4           SAVE DCB ADDR IN PARM LIST 000I
      L        GR5,EXLIST(GR5)         LOAD ADDRESS OF NM XIT LIST UL0H
         ST    GR1,8(GR5)              STORE CORE ADDRESS IN XIT LSUL0H
         MVI   8(GR5),X'0A'            FLAG EXIT AS ACTIVE         UL0H
         SR    GR5,GR5                  ZERO OUT I/O               000I
         ST    GR5,TOTLPARM+8           ERROR PARAMETER            000I
         B     TOSCAN                  ANALIZE NEXT PARAMETER      UL0H
NOCORE   LA    GR11,BDCORESZ           CORE SIZE ERROR MSG CON     UL0H
         B     TONEXT                  PRINT  CARD AND MESSAGE
ERMSGSTP L     GR15,PRIMEWRT           ADDRESS OF MESSAGE ROUTINE  UL0H
         MVI   QUITALL,X'0F'           SET END OF PROCESS SWITCH   UL0H
         BR    GR15                    PRINT ERROR MSG AND QUIT    UL0H
TEST4OM  TM    CTONLYSW,X'11'     ALL INPUT FROM SYSIN           A30480
         BM    SCANERR            YES.. AN ERROR                 A30480
TEST4NM  BAL   GR9,TESTCMD             TEST IF PARAMETER ON COMD CARD
         BAL   GR9,PARATEST            GET PARAMETER
         LR    GR5,SCANADR             START OF NAME
         TM    0(GR5),ALPHA            TEST IF FIRST CHARACTER A LETTER
         BO    TSLTGTNO                YES- CHECK SOME MORE
INVALCHR LA    GR11,PLUS8MSG           CODE FOR ERROR MESSAGE
         B     TONEXT                  PRINT CARD AND MESSAGE      UL0H
TSLTGTNO BAL   GR14,TESTCHAR           TEST FIRST CHAR FOR ALPHA   UL0H
         LR    GR10,SCANADR            POINTER TO NAME
         LR    GR9,LENGTH              PUT LENGTH INTO CORRECT REGISTER
         BR    GR8                     ENTER HANDLER  HEADER/TRAILER
CHKINHDR BAL   GR8,TEST4OM             IS THERE AN OLD MASTER      UL0H
         LA    GR3,OMHDR               ADDRESS OF RTN NAME/ADDRESS UL0H
         MVI   OMHDRSW,X'0F'           FLAG USER RTN FOR OLD MASTERUL0H
         B     MOVEARND                PREPARE TO LOAD ROUTINE     UL0H
CHKINTRL BAL   GR8,TEST4OM             IS THERE AN OLD MASTER      UL0H
         LA    GR3,OMTRLR              ADDRESS OF RTN NAME/ADDRESS UL0H
         MVI   OMTRLRSW,X'0F'          FLAG USER RTN FOR OLD MASTERUL0H
         B     MOVEARND                PREPARE TO LOAD ROUTINE     UL0H
CHKOUTHD BAL   GR8,TEST4NM             IS THERE AN NEW MASTER      UL0H
         LA    GR3,NMHDR               ADDRESS OF RTN NAME/ADDRESS UL0H
         MVI   NMHDRSW,X'0F'           FLAG USER RTN FOR NEW MASTERUL0H
         B     MOVEARND                PREPARE TO LOAD ROUTINE     UL0H
CKOUTTLR BAL   GR8,TEST4NM             IS THERE AN NEW MASTER      UL0H
         LA    GR3,NMTRLR              ADDRESS OF RTN NAME/ADDRESS UL0H
         MVI   NMTRLRSW,X'0F'          FLAG USER RTN FOR NEW MASTERUL0H
         B     MOVEARND                PREPARE TO LOAD ROUTINE     UL0H
MOVEARND C     GR9,EIGHT               TEST IF LABEL EXCEEDS 8 BYTES
         BH    INVALCHR                YES- ERROR
         MVC   0(8,GR3),BLANKS8        CLEAR BUFFER
         BCTR  GR9,0                   DECREMENT FOR EXECUTE
         EX    GR9,MVINHDEX            MOVE ROUTINE TO PROPER BUFFRUL0H
         LOAD  EPLOC=(GR3)             LOAD ROUTINE INTO CORE      UL0H
         ST    GR0,8(GR3)              STORE ADDRESS BEHIND NAME   UL0H
         B    TOSCAN                   BEGIN ANALIZING NEXT PARM   UL0H
  SPACE  2
*  THIS ROUTINE WILL INTERROGATE FIRST CHARACTER OF NAME FOR VALIDIUL0H
  SPACE 1
TESTCHAR CLI   0(SCANADR),C'#'         IS IT A NATIONAL CHARACTER  UL0H
         BCR   8,GR14                   IF SO,IT IS VALID,RETURN   000I
         CLI   0(SCANADR),C'@'         IS IT A NATIONAL CHARACTER  UL0H
         BCR   8,GR14                   IF SO,IT IS VALID,RETURN   000I
         CLI   0(SCANADR),C'$'         IS IT A NATIONAL CHARACTER  UL0H
         BCR   8,GR14                   IF SO,IT IS VALID,RETUR-   000I
         CLI   0(SCANADR),C'A'          ITS CHARACTER LESS THEN 'A'000I
         BL    INVALCHR                 IF SO,INVALID              000I
         CLI   0(SCANADR),C'I'          ITS CHARACTER LESS THEN I  000I
         BCR   12,GR14                  IF SI,ITIS VALID           000I
         CLI   0(SCANADR),C'J'          IS CHARACTER LESS THEN 'J' 000I
         BL    INVALCHR                 IF SO INVALID              000I
         CLI   0(SCANADR),C'R'          IS CHARACTER LESS THAN 'R' 000I
         BCR   12,GR14                  IF SO IT IS VALID          000I
         CLI   0(SCANADR),C'S'          IS CHARACTER LESS THEN 'S' 000I
         BL    INVALCHR                 IF SO,INVALID              000I
         CLI   0(SCANADR),C'Z'          IS CHARACTER GREATER THEN Z000I
         BH    INVALCHR                 IF SO INVALID
         BR    GR14                    RETURN TO CALLER            UL0H
         SPACE 1
SEQERR   LA    GR11,NUMMSG        INVALID NUMBER RANGE          YA01677
         B     TONEXT             GIVE MESSAGE,FLUSH            YA01677
COMMERR  NI    COL72LG,X'7F'           RESET COL72 SWITCH      @ZA16462
SCANERR  LA    GR11,SCANMSG            CONTROL STATEMENT ERROR     UL0H
         B     TONEXT                  GIVE MESSAGE, FLUSH         UL0H
ABSEQERR LA    GR11,SEQMSG             SEQUENCE ERROR MESSAGE CODE UL0H
         B     TONEXT                  GIVE MESSAGE, FLUSH         UL0H
INVALOP  LA    GR11,ERROP              INVALID OPERATION MESSAGE COUL0H
TONEXT   CLI   PRINTED,X'0F'           HAS CARD ALREADY BEEN PRINTEUL0H
         BE    SKIPPRNT                YES- DO NOT PRINT IT AGAIN  UL0H
         BAL   GR14,CARDPRNT           PRINT INVALID STATEMENT     UL0H
SKIPPRNT L     GR15,PRIMEWRT           ADDRESS OF MESSAGE WRITER   UL0H
         MVI   PRINTED,RESET0          CLEAR PRINT SWITCH          UL0H
         BALR  GR14,GR15               GIVE MESSAGE                UL0H
SETFLUSH MVI   FLUSHSW,X'0F'           SET FLUSH SWITCH        @AZ27974
         MVI   RIDIT2SW,RESET0         NEXT COMMAND IN ERROR TOO   UL0H
         MVI   TEMPCON,X'04'           SET RETURN CODE           A45121
STOWIT   L     GR15,PROCESS            ADDRESS OF CSECT WITH FLUSH UL0H
         BR    GR15                    ENTER CSECT                 UL0H
STOWIT1  MVI   COMDCD,X'00'            RESET COMMAND SWITCH        4929
         B     STOWIT                                              4929
STOWIT2  LA    GR11,ERRMSGC            CANNOT PROCESS MORE THAN ONE4929
STOWIT3  MVI   COMDCD,X'00'            RESET COMMAND SWITCH        4929
         B     SKIPPRNT                GO PRINT ERROR MESSAGE      4329
LABELANL TM    PARAMSW,X'20'           DOES PARAMETER FOLLOW     A45205
         BNO   NORMLAB                 NO,NORMAL PROCESSING      A45205
         MVI   LABEL2SW,X'FF'          SET SWITCH ,NO LABEL PROC A45205
         B     TOSCAN                  CONTINUE SCANNING KEYW    A45205
NORMLAB  BAL   GR14,CARDPRNT            PRINT RECORD             A45205
         CLI   LABELSW,X'00'            IS IT HEADER PROCESSING    UL0H
         BE    HEADERIN                 IF SO,GET CORE             UL0H
         CLI   LABELSW,X'10'            IS IT TRAILER PROCESSING   UL0H
         BNE   INVALOP                  IF NOT,ERROR               UL0H
         MVI   LABELCNT,X'00'           INIT NO. OF LABELS         UL0H
         MVI   LABELSW,X'20'            TRAILER LABELS             UL0H
READLABL CLI   LABELSW,X'20'           PROCESSING TRAILER LABELS   UL0H
         BE    READTRLR                IF SO,GET PROPER SAVE AREA  UL0H
         LA    GR6,OMLABELS            ADDRESS OF O M LABEL SAVE ARUL0H
         B     READLBLS                PREPARE TO READ LABELS      UL0H
READTRLR L     GR6,LABELBUF            ADDRESS OF GETMAIN AREA     UL0H
READLBLS SR    GR7,GR7                 INIT LABEL COUNT FIELD      UL0H
         MVI   LABEL2SW,X'0F'           LABELS TO BE PROCESSED     UL0H
         B     STOWIT                   PREPARE TO ENTER IEBUPDT2  UL0H
HEADERIN CLI   FRSTCOMD,X'FF'          HAS A COMMAND BEEN SEEVICED UL0H
         BNE   HEADLABL                 IF NOT,MUST BE HEADERS     UL0H
         MVI   FRSTCOMD,X'00'           RESET DCB'S OPENED         UL0H
         MVI   LABELSW,X'10'            HEADERS TO CREATE          UL0H
HEADLABL IC    GR15,LABELSW             PICK UP NO OF LABELS       UL0H
         LA    GR15,16(GR15)            ADD TEN TO                 UL0H
         STC   GR15,LABELSW             LABEL SWITCH               UL0H
         GETMAIN    R,LV=720            GET 8 LABEL BUFFER         UL0H
         ST    GR1,LABELBUF             SAVE ITS ADDRESS           UL0H
         B     READLABL                 PREPARE TO READ LABELS     UL0H
CHEKCOMD CLC   0(6,SCANADR),COMDEND     IS COMMAND 'ENDUP'         UL0H
         MVI   LABEL2SW,X'00'          RESET LABEL PROCESS SWITCH  UL0H
         BE    SCANNED                  IF SO,CONTINUE             UL0H
         B     ABSEQERR                 ELSE,ERROR                 UL0H
CHKDATA  BAL   GR9,PARATEST            TEST PARAMETER            A45205
         CLI   LABEL2SW,X'FF'          COMMAND WAS LABEL         A45205
         BNE   SCANERR                 NO,ERROR                  A45205
         LA    GR6,2                   LENGTH OF PARAMETER MUST  A45205
         CR    GR6,LENGTH              BE TWO                    A45205
         BNE   SCANERR                 IF NOT,ERROR              A45205
         CLC   0(2,SCANADR),NO         IS PARAMETER NO           A45205
         BNE   SCANERR                 IF NOT,ERROR              A45205
         L     GR6,DCBOM               LOAD DCB OLD MASTER       A45205
         L     GR6,36(GR6)             LOAD DCBEXIT ADDRESS      A45205
         MVI   0(GR6),X'00'            INACTIVATE INHDR EXIT     A45205
         MVI   4(GR6),X'00'            INACTIVATE INTRL EXIT     A45205
         L     GR6,DCBNM               LOAD DCB NEW MASTER       A45205
         L     GR6,36(GR6)             LOAD DCBEXIT ADDRESS      A45205
         MVI   0(GR6),X'00'            INACTIVATE OUTHDR EXIT    A45205
         MVI   4(GR6),X'00'            INACTIVATE OUTTRL EXIT    A45205
         BAL   GR14,CARDPRNT           PRINT CARD                A45205
         B     STOWIT                  RETURN                    A45205
ENDUPANL CLI   FLUSHSW,X'0F'           EOF DURING FLUSH            4929
         MVI   EODCTSW,X'0F'      SET EOF SWITCH OF SYSIN        A31449
         BE    STOWIT             YES.. FINISH PROCESSING        A31449
         BAL   GR14,CARDPRNT           PRINT ENDUP DETAIL STATEMEMT4929
         MVI   PRINTED,X'0F'           FLAG AS CARD ALREADY PRINTED4929
         CLI   CHNGESW,X'0F'      CHANGE COMMAND?              @YA02571
         BNE   BYPASS             NO,BRANCH TO TEST OTHER COMD @YA02571
         B     INVALOP            BRANCH TO GIVE ERROR MSG     @YA02571
BYPASS   BAL   GR9,TESTCMD        MUST HAVE A COMD CARD        @YA02571
         MVI   FINICCS,X'0F'           FLAG AS SYSIN THRU
         CLI   REPLSW,X'0F'       REPL COMMAND?                @YA02570
         BE    NORECIN            YES,BRANCH TO GIVE ERROR MSG @YA02570
         CLI   ADDSW,X'0F'        ADD COMMAND?                 @YA02570
         BE    NORECIN            YES,BRANCH TO GIVE ERROR MSG @YA02570
         B     STOWIT                  ENTER PROCESSING ROUTINE
NORECIN  LA    GR7,SYSINNM        ADDR OF SYSIN DD NAME        @YA02570
         LA    GR11,ERRMSGN       SYSIN HAS NO RECORDS MSG     @YA02570
         B     TONEXT             ENTER PROCESSING ROUTINE     @YA02570
QUITS    MVI   PRINTED,X'0F'           FLAG AS ENDUP PRINTED
LASTLEG  LA    GR11,EOJMESSG           ADDRESS OF EOJ MESSAGE
         CLC   TEMPCON(1),HICONCDE+1   RETURN CODE LESS
         BNH   RSAME                   EQUAL OR LESS USE HICONCDE
         MVC   HICONCDE+1(1),TEMPCON   SWAP FOR HIGHER CODE
RSAME    MVI   QUITALL,X'0E'           FORCE EOJ AND EOJ MESSAGE
         B     TONEXT                  END JOB
         EJECT
         SPACE 1
LENGTH   EQU   0                       LENGTH OF PARAM REGISTER 0
SCANADR  EQU   1                       ADDRESS OF PARAMETER REGISTER 1
BIT80    EQU   X'80'                   ERROR SWITCH
BIT40    EQU   X'40'                   BYPASS SWITCH
BIT20    EQU   X'20'                   EOF, PARAMETER FOLLOWS SWITCH
BIT10    EQU   X'10'                   FIRST ENTRY, PARTIAL PARAMETER
BIT08    EQU   X'08'                   COMMAND, COLUMN 72 NON-BLANK SW
BIT04    EQU   X'04'                   KEYWORD, COLUMN 72 NON-BLANK SW
BIT02    EQU   X'02'                   PARAMETER SWITCH
BIT01    EQU   X'01'                   LENGTH EQUALS ZERO SWITCH
RESET0   EQU   X'00'                   RESET BITS
MOVESSIL MVC   0(1,GR3),0(SCANADR)     MOVE SSI DATA OR LEVEL TO RRSAVE
SAVE13X  DS    F                       SAVE REGISTER 13
SAVLGTH  DS    F                       SAVES LENGTH REGISTER
COMMCARD DC    CL8'./      '                                   @ZA16462
BLANKS8  DC    CL8' '                  8 HEX BLANKS
ZEROHEX  DC    C'00000000'             DOUBLE WORD HEX ZEROS
PRIMEWRT DC    V(IEBUPLOG)             ADDRESS OF LOG ROUTINE
PROCESS  DC    V(IEBUPDT2)             ADDRESS OF PROCESSING CSECT
RDCARD   DC    V(IEBBSCAN)             ADDRESS OF CARD SCAN RTN    UL0H
SARG     DS    D                       SEARCH ARGUMENT
REGSAVE  DS    18F                     SAVE REGISTERS
RRSAVE   DS    16F                     SAVE AREA USED BY RDCARD
ZERO     DC    D'0'                    BUFFER OF ZEROS
EIGHT    DC    F'8'
MVINHDEX MVC   0(1,GR3),0(GR10)        MOVE LABEL NAME TO BUFFER
OPRLUP3  MVC   SARG(1),0(SCANADR)      MOVE KEYWORD TO SEARCH ARGUMENT
MOVNAME  MVC   0(1,GR7),0(SCANADR)     LEFT ADJUST MEMBER NAME
PACKIT   PACK  TEMPWA(L8),D0(L1,GR10)  PACK INCR OR NEW1        YM07522
ALTTT    DC    X'0A0B0C0D0E0FDDDDDD'   TABLE TO CONVERT TO HEX
         DC    X'DDDDDDDDDDDD'
         DC    X'00010203040506070809'
YESYES   DC    C'YES'                  DESIGNATES INSERT BLOCK
LISTSET  DC    C'ALL'                  ONLY ALLOWABLE 'LIST' PARAMETER
EOFDC    DC    C'EOF'                  ONLY INPUT FOR 'IGNORE' OPTION
NEWPO    DC    C'PO'                   NEW MASTER DS TYPE IDENTIFIER
NEWPS    DC    C'PS'                   NEW MASTER DS TYPE IDENTIFIER
INPLACE  DC    C'INPLACE'              PARAMETER FOR UPDATE INPLACE
COLUMN80 DC    C'80'                   COLUMN 80
COLUMN01 DC    C'01'                   COLUMN 01
DOTSLASH DC    C'./'                   CONTROL CARD INDICATION
NO       DC    C'NO'                   INDICATING NO USER LABEL  A45205
MAXCORE  DC    F'32769'           MAX TOTALSIZE + 1              A36128
PACKSIZE PACK  TEMPTOT(8),0(1,SCANADR)                             UL0H
COLM1BUF DC    C'00'                   HOLDS COLUMN SPEC IF ONLY 1 CHAR
FIVE     DC    F'5'                                                UL0H
TEMPTOT  DS    D                                                   UL0H
COMEBACK DC    F'0'                    SAVE AREA FOR REG14     @ZA16462
PATCH    NOP   PATCH                   PATCH AREA              @ZA16462
         DC    10F'0'                                          @ZA16462
NUMERIC  EQU   X'F0'
ALPHA    EQU   X'C0'
PODSET   EQU   2                       DSORG FIELD SETTING FOR PODS
PSDSET   EQU   X'40'                   DCB SETTING FOR SEQUENTIAL
PODPS    EQU   PODSET+PSDSET           SETTING FOR PODS OR PS DSORG
EXLIST   EQU   X'24'                   OFFSET OF EXIT LIST POINTER DCB
         EJECT
         DCBD  DSORG=PS
BDSORG   EQU   DCBDSORG-IHADCB         DATA SET ORGANIZATION FIELD DCB
         EJECT
         IEBUPCON
         END   IEBASCAN
