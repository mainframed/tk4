ULOG     TITLE 'IEBUPDTE MESSAGE CSECT- IEBUPLOG'
*TITLE- IEBUPDTE MESSAGE CSECT- IEBUPLOG                              *
*                                                                     *
*STATUS- CHANGE LEVEL 001                                             *
*                                                                     *
*FUNCTION/OPERATION- THIS MODULE IS ABLE TO CREATE AND UPDATE         *
*   SYMBOLIC LIBRARIES WHICH ARE EITHER PHYSICAL SEQUENTIAL OR        *
*   PARTITIONED DATA SETS CONTAINED ON DIRECT ACCESS OR TAPE VOLUMES. *
*                                                                     *
*   THIS MODULE HANDLES FIXED OR FIXED-BLOCKED RECORDS. IT HAS THE    *
*   ABITLITY TO HANDLE RECORD OF LOGICAL RECORD LENGTHS UP TO EIGHTY  *
*   BYTES. IF BLOCKSIZE AND/OR LOGICAL RECORD LENGTH ARE NOT GIVEN    *
*   BY OPEN TIME, UNBLOCKED RECORDS OF 80 BYTES IS ASSUMED AND A      *
*   WARNING MESSAGE IS GIVEN ON THE LOG OUT DEVICE.                   *
*                                                                     *
*   THIS MODULE CAN-                                                  *
*                                                                     *
*   1. CREATE A NEW PARTITIONED OR PHYSICAL SEQUENTIAL DATA SET       *
*                                                                     *
*   2. SEQUENCE THE NEW DATA SET OR RESEQUENCE AN OLD ONE             *
*                                                                     *
*   3. REPRODUCE OR REPLACE A MEMBER OR A DATA SET                    *
*                                                                     *
*   4. PRODUCE A PARTITIONED DATA SET FROM A PHYSICAL SEQUENTIAL      *
*   MASTER OR THE CONVERSE.                                           *
*                                                                     *
*   4. ADD A MEMBER TO A PARTITIONED DATA SET.                        *
*                                                                     *
*   5. ASSIGN AN ALIAS NAME TO ANY MEMBER UPDATED                     *
*                                                                     *
*   6. CHANGE RECORDS IN A DATA SET                                   *
*                                                                     *
*   7. HANDLE RECORDS WITH NON-STANDARD SEQUENCE NUMBER FIELDS AND    *
*   LENGTHS OF SEQUENCE NUMBERS FROM 1 TO 8 CHARACTERS.               *
*                                                                     *
*   8. UPDATE ONLY SPECIFIED PORTION OF DATA SET RECORDS.             *
*                                                                     *
*   9. UPDATE INPLACE DATA SETS OR MEMBERS ON DIRECT ACCESS VOLUMES.  *
*                                                                     *
*ENTRY POINTS- ENTERED FROM THE OPERATING SYSTEM AT 'IEBUPDTE'.       *
*                                                                     *
*INPUT- SYSIN WHICH CONSISTS OF CONTROL CARDS AND LOGICAL DATA        *
*   RECORDS FOR INSERTION OR REPLACEMENT, THE INPUT DATA SET.         *
*                                                                     *
*       SYSUT1 WHICH DEFINES THE 'OLD MASTER' DATA SET WHICH MAY BE   *
*   PARTITIONED OR PHYSICAL SEQUENTIAL IN ORGANIZATION.               *
*                                                                     *
*OUTPUT-SYSPRINT WHICH CONTAINS DIAGNOSTIC MESSAGES, THE CONTENTS OF  *
*   SYSIN AND-IF THE 'LIST=ALL' CONDITION IS PRIMED- THE ENTIRE       *
*   'NEW MASTER' IS LISTED ON THE DEVICE DESIGNATED.                  *
*                                                                     *
*       SYSUT2 WHICH DEFINES THE 'NEW MASTER' DATA SET. IT WILL BE A  *
*   COMPOSITE OF THE SYSIN AND SYSUT1 DATA SETS. IT CAN BE EITHER     *
*   PARTITIONED OR PHYSICAL SEQUENTIAL IN ORGANIZATION.               *
*                                                                     *
*EXITS- NORMAL- A RETURN IS GIVEN TO THE SCHEDULAR MODULE OF THE      *
*   OPERATING SYSTEM. CONDITION CODE RETURNED ON A SUCCESSFUL RUN     *
*   WILL BE ZERO.                                                     *
*                                                                     *
*EXITS- ERROR- IF AN ERROR OCCURS, A CODE IS STORED, A MESSAGE        *
*   GIVEN TO THE USER EXPLAINING THE ERROR CONDITION AND A            *
*   RETURN TO THE OPERATING SYSTEM SCHEDULAR MODULE IS GIVEN.         *
*   RETURN CODE GIVEN ARE-                                            *
*   00 NORMAL TERMINATION OF A SUCCESSFUL RUN                         *
*   04 ERROR WITHIN A MEMBER- WILL TRY TO PROCESS NEXT MEMBER IF PODS *
*   12 UNRECOVERABLE ERROR- TERMINATION OF JOB.                       *
*   16 ERROR IN LABEL HANDLING IN USERS ROUTINE- TERMINATION OF JOB   *
*                                                                     *
*EXTERNAL ROUTINES- I/O HANDLED AT READ/WRITE LEVEL, GETMAIN, LINK,   *
*   BLDL, FIND, STOW USED.                                            *
*                                                                     *
*TABLES/WORK AREAS-                                                   *
*   -KEYTAB- LIST OF VALID KEY WORDS USED ON CONTROL CARDS            *
*   -COMDTABL- LIST OF VALID COMMAND WORDS USED ON CONTROL CARDS      *
*   -SWITCHRD- BUFFER CONTAINING CONTROL CARD AND SWITCHES USED       *
*   BY CARD SCAN ROUTINE.                                             *
*   -LOGOUTAR- BUFFER WHICH IS USED BY LOG OUT ROUTINE FOR MESSAGES.  *
*   -STARTAB- TABLE OF SWITCHES WHICH ARE RESET TO ZERO UPON ENTRY    *
*   TO MODULE. ALL SWITCHES NEEDING REINITIALIZATION SHOULD BE PUT    *
*   INTO THIS TABLE.                                                  *
*                                                                     *
*ATTRIBUTES- SERIAL REUSABLE, BLOCK LOADS
*                                                                     *
*  THIS CSECT-   1. WRITES MESSAGES OR RECORDS ON SYSPRINT            *
*                2. PRINTS A TITLE ON THE TOP OF EACH PAGE AND        *
*        NUMBERS THE PAGES.                                           *
*                3. MESSAGE TO BE PRINTED IS DETERMINED BY THE CODE   *
*        IN GR11. THIS CODE CORRESPONDS TO MESSAGE NUMBER. THE        *
*        ADDRESS OF ANY CONSTANT TO BE INSERTED INTO A MESSAGE IS     *
*        PASSED IN GR7.                                               *
*                4. ADDITIONAL MESSAGES MAY BE ADDED BY ASSIGNING     *
*        THEM A UNIQUE 2 DIGIT CODE, PUTTING THE MESSAGE IN THIS      *
*        CSECT, PUTTING AN EQUATE FOR THE MESSAGE SIZE IN THIS CSECT, *
*        PUTTING AN EQUATE FOR THE MESSAGE NUMBER IN THE COMMON       *
*        DSECT- IEBUPCON, AND ADDING THE MESSAGE CONSTANT CODE AND    *
*        HANDLER TO 'MSGSTART' TABLE BEFORE LAST ENTRY OF 'FF'.       *
*                5. SYSPRINT DCB IS CONTAINED IN THIS CSECT. SYSPRINT *
*        IS OPENED WHEN CSECT1 SETS 'PRIMESW' AND GIVES CONTROL TO    *
*        THIS CSECT TO OPEN SYSPRINT AND PRINT THE INITIAL HEADER     *
*        STATEMENT. ANY ERRORS IN OPENING SYSPRINT (USER HAS FORGOTTEN*
*        THE SYSPRINT DD STATEMENT OR HAS SPECIFIED INCORRECT BLOCK-  *
*        SIZE) RESULTS IN THIS CSECT SETTING THE 'LOGBADSW' AND       *
*        RETURNING TO THE INITIAL CSECT WHICH RETURNS CONTROL TO THE  *
*        OPERATING SYSTEM WITH A CONDITION CODE OF 12 IN REGISTER 15. *
*                6. FINAL TERMINATION OF THE PROGRAM IS INITIATED BY  *
*        THIS CSECT. WHEN 'QUITALL' SWITCH IS SET, THIS CSECT GIVES   *
*        THE LAST INFORMATION MESSAGE, GIVES THE IEBUPDAT END OF JOB  *
*        MESSAGE, CLOSES THE SYSPRINT DCB, AND RETURNS CONTROL TO THE *
*        OPERATING SYSTEM.                                            *
*                                                                     *
         EJECT
*     SYMBOLIC REGISTER NOTATION   GR0 - GR15
GR0      EQU   0
GR1      EQU   1
GR2      EQU   2
GR3      EQU   3
GR4      EQU   4                       REGISTER RESERVED FOR CSECT1
GR5      EQU   5
GR6      EQU   6
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10
GR11     EQU   11
GR12     EQU   12
GR13     EQU   13
GR14     EQU   14
GR15     EQU   15
         ENTRY SYNADMSG                                          A27716
         SPACE 1
*********************************************************************
**       THIS IS THE GENERALIZED MESSAGE WRITER WHICH INCLUDES THE
**    ABILITY TO PRINT A HEADER ON EACH PAGE WITH ASCENDING PAGE
**    NUMBERS.
*********************************************************************
         SPACE 1
IEBUPLOG CSECT
*1101046400,060200-062320,063000-096000                            UL0H
*1101098800                                                        UL0H
*1131020200-020800,031600,032600,042200,042800,044800,114000-114200BS0H
*1131059600-059800,073400,091800-092200,103800                     BS0H
* 063100,071500-072500,097800-098000,107400-107600,114200        A27716
* 036000                                                         A31438
*                                                                A23998
* 089112                                                         A32057
* 098800,105930                                                  A34086
         EXTRN SPSYNAD                 SYSPRINT SYNAD ROUTINE    A27716
* 105930                                                         A34086
*C097600                                                       @YA02555
*C 102800,103000                                               @ZA01692
*C 035600                                                      @ZA04364
         ENTRY  CLOSEPRT  ENTRY PT FROM SYSPRINT SYNAD ROUTINE   A27716
         SAVE  (14,12)
         BALR  GR2,0                   ESTABLISH BASE ADDRESS REGISTER
         USING NEXT,GR2
         USING IEBUPCON,GR12           DSECT POINTER
NEXT     ST    GR13,SAVE13X            SAVE REGISTER 13
         LA    GR13,REGSAVE            ADDRESS OF LOG REGISTER SAVE
         CLI   ASISMSW,X'0F'           WRITE RECORD SWITCH
         BE    LOADZERO                YES- CONTINUE
         TM    PRIMESW,X'0F'           INITIAL ENTRY AND/OR HEADER
         BC    4,HEADIT                WRITE HEADER AND STATEMENT
         BC    1,PRIMEHD               INITIAL- OPEN DCB,WRITE HEADER
         TM    QUITALL,X'0F'           LAST ENTRY TO THIS ROUTINE
         BC    4,GIVECC                YES- GIVE CONDITION CODE MESSAGE
         B     MSGSTART                SELECT MESSAGE
MSGWRTE  MVC   LOGOUTAR(7),IEBCON      MOVE IN CONSTANT IEB5  I
         MVC   LOGOUTAR+4(2),0(GR5)    MOVE IDENTIFYING NUMBER IN
         EX    GR6,MOVEMESS            MOVE THE MESSAGE TO PRINT BUFFER
         SPACE 1
LOADZERO CLI   LINECNT,LINES           IS PAGE FULL
         BE    HEADIT                  YES- WRITE HEADER
         LA    GR5,TEXTCTL             PRINT BUFFER
WRITELOG PUT   DCBLOGR,(GR5)            WRITE MESSAGE OR RECORD    BS0H
         IC    GR14,LINECNT            OBTAIN LINE COUNT
         LA    GR14,1(GR14)            ADD 1 TO IT
         MVI   ASISMSW,X'00'           CLEAR WRITE RECORD SWITCH
         STC   GR14,LINECNT            STORE NEW LINE COUNT
         MVI   TEXTCTL,C' '            SET FOR SINGLE SPACE
RETURNX  MVI   LOGOUTAR,C' '           CLEAR PRINT BUFFER
         MVC   LOGOUTAR+1(119),LOGOUTAR
         TM    QUITALL,X'0F'           TEST IF ERROR TERMINATION
         BC    8,GOBACK                NO- NORMAL RETURN
         BC    4,CLOSEPRT              HAVE GIVEN EOJ MESSAGE CLOSE DCB
         MVI   QUITALL,X'0E'           FLAG AS EOJ MESSAGE
         B     MSG19BLD                SAVE TIME BRANCH DIRECT TO MSG
GIVECC   LH    GR6,HICONCDE            LOAD CONDITION CODE
         CVD   GR6,INCREMWA            CONVERT RETURN CODE
         UNPK  LOGOUTAR+35(8),INCREMWA(8)    UNPACK RETURN CODE
         OI    LOGOUTAR+42,X'F0'       CLEAR GARBAGE BYTE
         MVI   QUITALL,X'0F'           SET QUIT FLAG TO FORCE EOJ MSG
         B     MSG18BLD                SAVE TIME BRANCH DIRECT
CLOSEPRT CLOSE (DCBLOGR)               CLOSE MESSAGE DCB       @ZA04364
         FREEPOOL DCBLOGR                                          BS0H
         LH    GR10,HICONCDE      SAVE CONDITIE CODE             A31438
         LA    GR0,DSECTSIZ       GET SIZE OF IEBUPCON           A31438
         LR    GR1,GR12           GET ADDRESS OF IEBUPCON        A31438
         L     GR13,SAVE13        ADDRESS SUPERVISOR REGISTERS   A37865
         FREEMAIN R,LV=(0),A=(1)  FREE AREA USED BY IEBUPCON     A31438
         LR    GR15,GR10          GET CONDITIE CODE              A31438
         RETURN  (14,12),T,RC=(15)
GOBACK   TM    DFLG,X'10'         TEST FOR BLKSIZE ERROR         A23998
         BC    1,GIVECC           GIVE LAST MSG AND QUIT         A23998
         L     GR13,SAVE13X       RESTORE REGS RETURN TO PROG
         RETURN  (14,12)
         SPACE 1
RESTORMS LA    GR5,SAVEMESS            BUFFER ADDRESS
         B     WRITELOG                WRITE THE MESSAGE
HEADIT   LA    GR3,RESTORMS            RETURN FROM HEADER PRINT
         MVC   MESSBUFF(120),LOGOUTAR  SAVE TILL HEADER IS PRINTED
         B     HDRPRINT                PRINT HEADER
PRIMEHD  LA    GR11,DCBLOGR            ADDRESS OF PRINTER DCB
         ST    GR11,DCBLOG             STORE POINTER TO DCB
         MVC   BDDNAM(8,GR11),SYSPRNTN MOVE NAME INTO DCB
         MVI   DFLG,X'00'         INITIALIZE BLKSIZE ERROR FLAG    BS0H
         OPEN  (DCBLOGR,OUTPUT)        OPEN SYSPRINT DCB
         TM    BOFLGS(GR11),X'10'      IS OPEN BIT ON
         BZ    ANERROR                 ERROR- DCB COULD NOT BE OPENED
         TM    DFLG,X'10'               TEST FOR BLKSIZE ERROR     BS0H
         BC    1,BLKSZERR         INVALID BLKSIZE                A23998
         LA    GR3,RETURNX             RETURN FROM PRINTING HEADER
HDRPRINT MVI   PRIMESW,X'00'           CLEAR PRIME SWITCH          3628
         MVI   LOGOUTAR,C' '           CLEAR PRINT BUFFER
         MVC   LOGOUTAR+1(119),LOGOUTAR
         MVI   TEXTCTL,C'1'            SKIP PAGE
         MVC   LOGOUTAR+7(5),SYSINHD   MOVE IN SYSIN REFERENCE
         MVC   LOGOUTAR+40(10),NEWMASTR MOVE IN NEW MASTER REFERENCE
         MVC   LOGOUTAR+90(17),HDSAVE  LOG REFERENCE TO IEBUPDAT
         MVC   LOGOUTAR+108(4),PAGENO  MOVE IN PAGE NUMBER
         PUT   DCBLOGR,TEXTCTL          WRITE HEADER               BS0H
         MVI   ASISMSW,X'00'           CLEAR RECORD SWITCH
         MVI   LINECNT,X'04'           HEADER + 3 SPACES INITIAL LINE 4
         PACK  PGCONVRT(8),PAGENO(4)   PACK PAGE NUMBER            3628
         CVB   GR15,PGCONVRT           CONVERT TO BINARY           3628
         LA    GR15,1(GR15)            ADD ONE TO PAGE NUMBER      3628
         CVD   GR15,PGCONVRT           CONVERT TO DECIMAL          3628
         UNPK  PAGENO(4),PGCONVRT(8)   PUT IN UPDATED PAGE NUMBER  3628
         OI    PAGENO+3,C'0'           CONVERT LAST BYTE           3628
         MVI   TEXTCTL,C'-'            SET TO TRIPLE SPACE
         BR    GR3                     RETURN TO WRITE TEXT OR GO BACK
         SPACE 1
ANERROR  MVI   LOGBADSW,X'0F'          FLAG AS DCBLOGR NOT OPENED
         B     GOBACK                  RETURN
PRINEXIT L     GR11,DCBLOG              LOAD DCB ADDRESS           BS0H
         USING IHADCB,GR11                                         BS0H
         LH    GR5,DCBBLKSI             LOAD BLOCKSIZE             BS0H
         LTR   GR5,GR5                  IS BLOCKSIZE SPECIFIED     BS0H
         BC    2,TESTSIZE               IF SO CHECK FOR MODULARITY BS0H
         MVI   DCBBLKSI+1,X'79'         INSERT BLOCKSIZE           BS0H
         BCR   15,GR14                                             BS0H
TESTSIZE SR    GR4,GR4                  TEST FOR CORRECT BLKSIZE   BS0H
         LA    GR6,121                                             BS0H
         DR    GR4,GR6                                             BS0H
         LTR   GR4,GR4                                             BS0H
         BCR   8,GR14                   IF OK RETURN               BS0H
         MVI   DFLG,X'10'               SET ERROR FLAG             BS0H
         MVI   DCBBLKSI,X'00'     CLEAR BLKSIZE                  A23998
         MVI   DCBBLKSI+1,X'79'   INSERT BLKSIZE FOR ERR MSG     A23998
         BCR   15,GR14                                             BS0H
         DROP  GR11                                                BS0H
BLKSZERR LA    GR7,SYSPRNTN       DD NAME FOR MESSAGE            A23998
         LA    GR11,NTOPMSG       NOT OPEN MSG CONSTANT          A23998
         LA    GR15,12            CONDITION CODE                 A23998
         STH   GR15,HICONCDE      RETURN CON CODE OF 12          A23998
         B     MSGSTART           SELECT MESSAGE                 A23998
         SPACE 2
HDSAVE   DC    C'IEBUPDTE LOG PAGE'
SYSINHD  DC    C'SYSIN'
NEWMASTR DC    C'NEW MASTER'
         DS    0F
DCBLST   DC    X'85'                                               BS0H
         DC    AL3(PRINEXIT)                                       BS0H
DFLG     DS    X                        BLOCKSIZE ERROR FLAG       BS0H
PGCONVRT DC    D'0'                    BUFFER FOR CONVERTING PAGE
SAVEMESS DC    C'-'                    TRIPLE SPACE
MESSBUFF DS    CL120                   HOLDS MESSAGE WHILE HEADER PRINT
REGSAVE  DS    18F                     SAVE REGISTERS ON ENTRY
SAVE13X  DS    F                       HOLDS REGISTER 13 FOR THIS CSECT
MOVEMESS MVC   LOGOUTAR+8(1),2(GR5)    MOVE MESSAGE TO BUFFER
LINECNT  DC    X'00'                   LINES USED
IEBCON   DC    C'IEB8  I'                                          UL0H
TOT      DC    C'TOTALING'                                         UL0H
USERLABL DC    C'USER LABEL'                                       UL0H
IHL      DC    C'INPUT HEADER'                                     UL0H
ITL      DC    C'INPUT TRAILER'                                    UL0H
OHL      DC    C'OUTPUT HEADER'                                    UL0H
OTL      DC    C'OUTPUT TRAILER'                                   UL0H
LBLS     DC    C' LABELS.'             USER LABEL MESSAGE FRAGMENT UL0H
DUBLWORD DS    D                                                   UL0H
LINES    EQU   56                      MAXIMUM LINES/PAGE
         EJECT
*   DETERMINES MESSAGE TO BE PRINTED                                  *
*          MESSAGE CODE ADDRESS + OFFSET POINTS TO CORRECT MSGBLD AREA
*                                                                     *
MSGTEST  LA    GR5,X'FF'               END OF TEST
NEXTCODE SR    GR3,GR3                 CLEAR REGISTER
         IC    GR3,0(GR6)              GET MESSAGE NUMBER
         CR    GR3,GR11                CORRECT MESSAGE NUMBER
         BE    THATSIT                 YES- PREPARE TO ENTER ROUTINE
         CR    GR3,GR5                 END OF TEST TABLE
         BE    GOBACK                  IGNORE- INVALID MESSAGE CODE
         LA    GR6,2(GR6)              GET NEXT CODE
         B     NEXTCODE                TEST THE NEXT CODE
THATSIT  SR    GR3,GR3                 CLEAR REGISTER
         IC    GR3,1(GR6)              GET DISPLACEMENT
         B     0(GR3,GR6)              ENTER MESSAGE HANDLER
         SPACE 1
MSGSTART BAL   GR6,MSGTEST             LINK TO TEST MESSAGE NUMBER
         DC    X'01'                   MESSAGE NUMBER
         DC    AL1(MSG01-MSGSTART-4)   ADDRESS OF MESSAGE 1
         DC    X'02'
         DC    AL1(MSG02-MSGSTART-6)   ADDRESS OF MESSAGE 2  SYSIN I/O
         DC    X'03'
         DC    AL1(MSG03-MSGSTART-8)   ADDRESS OF MESSAGE 3 SYSUT1 I/O
         DC    X'04'
         DC    AL1(MSG04-MSGSTART-10)  ADDRESS OF MESSAGE 4 SYSUT2 I/O
         DC    X'05'
         DC    AL1(MSG05-MSGSTART-12)  ADDRESS OF MESSAGE 5 CONTROL ERR
         DC    X'06'
         DC    AL1(MSG06-MSGSTART-14)  ADDRESS OF MESSAGE 6 SEQUENCE ER
         DC    X'07'
         DC    AL1(MSG07-MSGSTART-16)  ADDRESS OF MESSAGE 7 INVALID OP
         DC    X'08'
         DC    AL1(MSG08-MSGSTART-18)  ADDRESS OF MESSAGE 8 TERMINATION
         DC    X'09'
         DC    AL1(MSG09-MSGSTART-20)  ADDRESS OF MESSAGE 9 SCANNING
         DC    X'10'
         DC    AL1(MSG10-MSGSTART-22)  ADDRESS OF MESSAGE 10 OUT DELETE
         DC    X'11'
         DC    AL1(MSG11-MSGSTART-24)  ADDRESS OF MESSAGE 11 OUT NUMBER
         DC    X'12'
         DC    AL1(MSG12-MSGSTART-26)  ADDRESS OF MESSAGE 12 DIRECT WRT
         DC    X'13'
         DC    AL1(MSG13-MSGSTART-28)  ADDRESS OF MESSAGE 13 DIRECT FUL
         DC    X'14'
         DC    AL1(MSG14-MSGSTART-30)  ADDRESS OF MESSAGE 14 DD NO OPEN
         DC    X'15'
         DC    AL1(MSG15-MSGSTART-32)  ADDRESS OF MESSAGE 15 ONLY 1 PS
         DC    X'16'
         DC    AL1(MSG16-MSGSTART-34)  ADDRESS OF MESSAGE 16 NAME FOUND
         DC    X'17'
         DC    AL1(MSG17-MSGSTART-36)  ADDRESS OF MESSAGE 17 NOT FOUND
         DC    X'18'
         DC    AL1(MSG18-MSGSTART-38)  ADDRESS OF MESSAGE 18 CONCODE
         DC    X'19'
         DC    AL1(MSG19-MSGSTART-40)  ADDRESS OF MESSAGE 19  EOJ
         DC    X'20'
         DC    AL1(MSG20-MSGSTART-42)  ADDRESS OF MESSAGE 20 INPLACE 1
         DC    X'21'
         DC    AL1(MSG21-MSGSTART-44)  ADDRESS OF MESSAGE 21 INVALID NM
         DC    X'22'
         DC    AL1(MSG22-MSGSTART-46)  ADDRESS OF MESSAGE 22 EXIT CODE
         DC    X'23'
         DC    AL1(MSG23-MSGSTART-48)  ADDRESS OF MESSAGE 23 NULL SET
         DC    X'25'
         DC    AL1(MSG25-MSGSTART-50)  ADDRESS OF MESSAGE 25 PS IGNUL0H
         DC    X'26'                                               UL0H
         DC    AL1(MSG26-MSGSTART-52)  ADDRESS OF MESSAGE 26 NAME AUL0H
         DC    X'27'                                               UL0H
         DC    AL1(MSG27-MSGSTART-54)  ADDRESS OF MESSAGE 27 INVALIUL0H
         DC    X'28'                                               UL0H
         DC    AL1(MSG28-MSGSTART-56)  ADDRESS OF MESSAGE 28 BAD PAGE
         DC    X'29'                                               UL0H
         DC    AL1(MSG29-MSGSTART-58)  ADDRESS OF MESSAGE 29 DDNAMEUL0H
         DC    X'30'                                               UL0H
         DC    AL1(MSG30-MSGSTART-60)  ADDRESS OF MESSAGE 30 LRECL UNEQ
         DC    X'31'                                               3341
         DC    AL1(MSG31-MSGSTART-62)  ADDRESS OF MSG BAD DSORGS   3341
         DC    X'32'                                               UL0H
         DC    AL1(MSG32-MSGSTART-64)                              UL0H
         DC    X'33'                                               UL0H
         DC    AL1(MSG33-MSGSTART-66)                              UL0H
         DC    X'34'                                               UL0H
         DC    AL1(MSG34-MSGSTART-68)                              UL0H
         DC    X'35'                                               UL0H
         DC    AL1(MSG35-MSGSTART-70)                              UL0H
         DC    X'36'                                               UL0H
         DC    AL1(MSG36-MSGSTART-72)                              UL0H
         DC    X'37'                                               UL0H
         DC    AL1(MSG37-MSGSTART-74)                              UL0H
         DC    X'38'                                               UL0H
         DC    AL1(MSG38-MSGSTART-76)                              UL0H
         DC    X'39'                                               UL0H
         DC    AL1(MSG39-MSGSTART-78)                              UL0H
         DC    X'40'                                               UL0H
         DC    AL1(MSG40-MSGSTART-80)                              UL0H
         DC    X'41'                                               UL0H
         DC    AL1(MSG41-MSGSTART-82)                              UL0H
         DC    X'42'                                               UL0H
         DC    AL1(MSG42-MSGSTART-84)                              UL0H
         DC    X'43'                                               UL0H
         DC    AL1(MSG43-MSGSTART-86)                              UL0H
         DC    X'44'                                               UL0H
         DC    AL1(MSG44-MSGSTART-88)                              UL0H
         DC    X'45'                                               UL0H
         DC    AL1(MSG45-MSGSTART-90)                              UL0H
         DC    X'46'                                               UL0H
         DC    AL1(MSG46-MSGSTART-92)                              UL0H
         DC    X'FF'                   END OF TABLE
         DC    X'FF'                   LAST TABLE ENTRY
         SPACE 1
MSG01    B     MSG01BLD                BLOCKSIZE NOT GIVEN OR WRONGUL0H
         SPACE 1                                                   UL0H
MSG02    B     MSG02BLD I/O ERROR ON SYSIN,SYSUT1,SYSUT2 OR SYSPRINT
*                                                                A27716
         SPACE 1                                                   UL0H
MSG03    B     MSG03BLD                I/O ERROR ON SYSUT1         UL0H
         SPACE 1                                                   UL0H
MSG04    B     MSG04BLD                I/O ERROR ON SYSUT2         UL0H
         SPACE 1                                                   UL0H
MSG05    B     MSG05BLD                CONTROL STATEMENT ERROR     UL0H
         SPACE 1                                                   UL0H
MSG06    B     MSG06BLD                STATEMENT SEQUENCE ERROR    UL0H
         SPACE 1                                                   UL0H
MSG07    B     MSG07BLD                INVALID OPERATION           UL0H
         SPACE 1                                                   UL0H
MSG08    B     MSG08BLD                TERMINATION OF MEMBER UPDATEUL0H
         SPACE 1                                                   UL0H
MSG09    B     MSG09BLD                SCANNING FOR NEXT MEMBER    UL0H
         SPACE 1                                                   UL0H
MSG10    B     MSG10BLD                NO RECORDS IN DELETE RANGE  UL0H
         SPACE 1                                                   UL0H
MSG11    B     MSG11BLD                NO RECORDS IN NUMBER RANGE  UL0H
         SPACE 1                                                   UL0H
MSG12    B     MSG12BLD                DIRECTORY WRITE ERROR       UL0H
         SPACE 1                                                   UL0H
MSG13    B     MSG13BLD                OUTPUT DIRECTORY FULL       UL0H
         SPACE 1                                                   UL0H
MSG14    B     MSG14BLD                DD NAMED CANNOT BE OPENED   UL0H
         SPACE 1
MSG15    B     MSG15BLD                CANNOT PROCESS MULTIPLE PS DUL0H
         SPACE 1
MSG16    CLI   ASISMSW,X'0E'           SPECIAL BLDL/FIND MESSAGE   UL0H
         LA    GR5,IEBUPD16            MESSAGE ADDRESS             UL0H
         BE    SPCMSG16                YES- SET UP MESSAGE         UL0H
         B     MSG16BLD                NAME FOUND IN DIRECTORY STOWUL0H
         SPACE 1
MSG17    CLI   ASISMSW,X'0E'           SPECIAL BLDL/FIND MESSAGE   UL0H
         LA    GR5,IEBUPD17            MESSAGE ADDRESS             UL0H
         BE    SPCMSG17                YES- SET UP MESSAGE         UL0H
         B     MSG17BLD                NAME NOT FOUND STOW MESSAGE UL0H
         SPACE 1                                                   UL0H
MSG18    B     MSG18BLD                HIGHEST CONDITION CODE      UL0H
         SPACE 1                                                   UL0H
MSG19    B     MSG19BLD                EOJ MESSAGE                 UL0H
         SPACE 1                                                   UL0H
MSG20    B     MSG20BLD                ONE UPDATE INPLACE PER PASS UL0H
         SPACE 1                                                   UL0H
MSG21    B     MSG21BLD                INVALID EXIT NAME           UL0H
         SPACE 1
MSG22    B     MSG22BLD                EXIT RETURN CODE ENDED JOB (UL0H
         SPACE 1
MSG23    B     MSG23BLD                NULL DATA SET IDENTIFIED    UL0H
         SPACE 1
         SPACE 1
MSG25    B     MSG25BLD                ALIAS IGNORE SEQUENTIAL DATAUL0H
         SPACE 1
MSG26    B     MSG26BLD                MEMBER NAME ALIAS STOWED AS UL0H
         SPACE 1
MSG27    B     MSG27BLD                INVALID INPUT PARAMETER     UL0H
         SPACE 1
MSG28    B     MSG28BLD                PAGE NUMBER PARAMETER INVALIUL0H
         SPACE 1
MSG29    B     MSG29BLD                DDNAME PARAMETER IS INVALID UL0H
         SPACE 1
MSG30    B     MSG30BLD                OLD AND NEW MASTER LRECL UNEUL0H
*                                                                  3341
MSG31    B     MSG31BLD            OM AND NM INCOMPATIBLE DSORGS   3341
*                                                                  3341
MSG32    B     MSG32BLD                                            UL0H
MSG33    B     MSG33BLD                                            UL0H
MSG34    B     MSG34BLD                                            UL0H
MSG35    B     MSG35BLD                                            UL0H
MSG36    B     MSG36BLD                                            UL0H
MSG37    B     MSG37BLD                                            UL0H
MSG38    B     MSG38BLD                                            UL0H
MSG39    B     MSG39BLD                                            UL0H
MSG40    B     MSG40BLD                                            UL0H
MSG41    B     MSG41BLD                                            UL0H
MSG42    B     MSG42BLD                                            UL0H
MSG43    B     MSG43BLD                                            UL0H
MSG44    B     MSG44BLD                                            UL0H
MSG45    B     MSG45BLD                                            UL0H
MSG46    B     MSG46BLD                                            UL0H
         SPACE 1
MSG01BLD LA    GR5,IEBUPD01            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG01SIZ            SIZE OF MESSAGE             UL0H
         MVC   2(2,GR5),0(GR7)         MOVE IN CT/0M/NM REFERENCE  UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG02BLD LA    GR5,IEBUPD02                                      A27716
         LA    GR6,MSG02SIZ                                      A27716
         B     MSGWRTE                                           A27716
MSG03BLD LA    GR5,IEBUPD03                                      A27716
         LA    GR6,MSG03SIZ                                      A27716
         B     MSGWRTE                                           A27716
MSG04BLD LA    GR5,IEBUPD04                                      A27716
         LA    GR6,MSG04SIZ                                      A27716
         B     MSGWRTE                                           A27716
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG05BLD LA    GR5,IEBUPD05            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG05SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG06BLD LA    GR5,IEBUPD06            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG06SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG07BLD LA    GR5,IEBUPD07            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG07SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG08BLD LA    GR5,IEBUPD08            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG08SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG09BLD LA    GR5,IEBUPD09            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG09SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG10BLD LA    GR5,IEBUPD10            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG10SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG11BLD LA    GR5,IEBUPD11            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG11SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG12BLD LA    GR5,IEBUPD12            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG12SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG13BLD LA    GR5,IEBUPD13            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG13SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG14BLD LA    GR5,IEBUPD14            ADDRESS OF MESSAGE          UL0H
         MVC   9(8,GR5),0(GR7)         MOVE IN DD NAME             UL0H
         LA    GR6,MSG14SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG15BLD LA    GR5,IEBUPD15            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG15SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG16BLD LA    GR6,MSG16SIZ            SIZE OF MESSAGE             UL0H
         B     INSERTNM                INSERT THE NAME             UL0H
SPCMSG16 LA    GR6,MSG16ASZ            SPECIAL MESSAGE SIZE        UL0H
         B     INSERTNM                INSERT THE NAME             UL0H
SPCMSG17 LA    GR6,MSG17ASZ            SPECIAL MESSAGE SIZE        UL0H
         B     INSERTNM                INSERT THE NAME             UL0H
MSG17BLD LA    GR6,MSG17SIZ            SIZE OF MESSAGE             UL0H
INSERTNM MVC   15(8,GR5),0(GR7)        MOVE MEMBER NAME TO BUFFER  UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG18BLD LA    GR5,IEBUPD18            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG18SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 CONDITION CODE ALREADY IN BUUL0H
MSG19BLD LA    GR5,IEBUPD19            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG19SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG20BLD LA    GR5,IEBUPD20            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG20SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG21BLD LA    GR5,IEBUPD21            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG21SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG22BLD LA    GR5,IEBUPD22            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG22SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG23BLD LA    GR5,IEBUPD23            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG23SIZ            SIZE OF MESSAGE             UL0H
         MVC   2(8,GR5),0(GR7)         MOVE IN DD NAME             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG25BLD LA    GR5,IEBUPD25            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG25SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG26BLD LA    GR5,IEBUPD26            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG26SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG27BLD LA    GR5,IEBUPD27            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG27SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG28BLD LA    GR5,IEBUPD28            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG28SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG29BLD LA    GR5,IEBUPD29            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG29SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG30BLD LA    GR5,IEBUPD30            ADDRESS OF MESSAGE          UL0H
         LA    GR6,MSG30SIZ            SIZE OF MESSAGE             UL0H
         B     MSGWRTE                 GIVE MESSAGE                UL0H
MSG31BLD LA    GR5,IEBUPD31        ADDRESS OF MESSAGE              3341
         LA    GR6,MSG31SIZ        SIZE OF MESSAGE                 3341
         B     MSGWRTE             GIVE MESSAGE                    3341
MSG32BLD LA    GR5,IEBUPD32             ADDRESS OF MESSAGE         UL0H
         LA    GR6,MSG32SIZ             SIZE OF MESSAGE            UL0H
         MVC   2(8,GR5),USERRTN         MOVE USER ROUTINE AND ADDR UL0H
         MVC   31(22,GR5),30(GR5)      CLEAN UP MESSAGE AREA       UL0H
         SR    GR10,GR10               CLEAR BRANCH REGISTE        UL0H
         IC    GR10,USERRTN+8           PICK UP ROUTINE INDICATOR  UL0H
         B     *(GR10)                  BRANCH ON THAT CONSTANT    UL0H
         B     CIHL                     INPUT HEADER ROUTINE       UL0H
         B     CITL                     INPUT TRAILER ROUTINE      UL0H
         B     COHL                     OUTPUT HEADER ROUTINE      UL0H
         B     COTL                     OUTPUT TRAILER ROUTINE     UL0H
CIHL     MVC   31(12,GR5),IHL           BUILD INPUT HEADER         UL0H
         MVC   43(8,GR5),LBLS           LABELS MESSAGE             UL0H
         B     MSGWRTE                  GIVE MESSAGE               UL0H
CITL     MVC   31(13,GR5),ITL          BUILD INPUT TRAILER         UL0H
         MVC   44(8,GR5),LBLS           LABELS MESSAGE             UL0H
         B     MSGWRTE                  GIVE MESSAGE               UL0H
COHL     MVC   31(13,GR5),OHL           BUILD OUTPUT HEADER        UL0H
         MVC   44(8,GR5),LBLS           LABELS MESSAGE             UL0H
         B     MSGWRTE                  GIVE MESSAGE               UL0H
COTL     MVC   31(14,GR5),OTL           BUILD OUTPUT TRAILER       UL0H
         MVC   45(8,GR5),LBLS           LABELS MESSAGE             UL0H
         B     MSGWRTE                  GIVE MESSAGE               UL0H
MSG33BLD LA    GR5,IEBUPD33             ADDRESS OF MESSAGE         UL0H
         LA    GR6,MSG33SIZ             SIZE OF MESSAGE            UL0H
         SR    GR7,GR7                                             UL0H
         IC    GR7,USERCONT             NUMBER OF ENTRANCES TO USERUL0H
         CVD   GR7,DUBLWORD             CONVERT NO. TO DECIMAL     UL0H
         UNPK  2(1,GR5),DUBLWORD+7(1)   AND UNPACK IT              UL0H
         OI    2(GR5),X'F0'             SO THAT IT CAN BE PRINTED  UL0H
         MVC   17(8,GR5),USERRTN        MOVE USER RTN NAME TO MSG  UL0H
         B     MSGWRTE                  GIVE MESSAGE               UL0H
MSG34BLD LA    GR5,IEBUPD34             ADDRESS OF MESSAGE         UL0H
         LA    GR6,MSG34SIZ             SIZE OF MESSAGE            UL0H
         MVC   24(8,GR5),USERRTN        MOVE USER RTN NAME TO MSG  UL0H
         SR    GR7,GR7                                             UL0H
         IC    GR7,LBLRETCD             PICK UP USER RETURN CODE   UL0H
         CVD   GR7,DUBLWORD             CONVERT RETURN CODE        UL0H
         UNPK  37(3,GR5),DUBLWORD+6(2)  TO DECIMAL AND UNPAKE IT   UL0H
         OI    39(GR5),X'F0'            SO THAT IT CAN BE PRINTED  UL0H
         B     MSGWRTE                  GIVE MESSAGE               UL0H
MSG35BLD LA    GR5,IEBUPD35             ADDRESS OF MESSAGE         UL0H
         LA    GR6,MSG35SIZ             SIZE OF MESSAGE            UL0H
         B     *(GR7)                   GO TO PROPER BUILD ROUTINE UL0H
         B     TOTLS                    TOTALING BUILD MESSAGE     UL0H
         B     USRLBLS                  USER LABEL BUILD MSG       UL0H
TOTLS    MVC   4(8,GR5),TOT             MOVE IN TOTAL KEYWORD      UL0H
         B     MSGWRTE                  GIVE  MESSAGE              UL0H
USRLBLS  MVC   2(10,GR5),USERLABL       USER LABELS KEY WORD       UL0H
         B     MSGWRTE                  GIVE MESSAGE               UL0H
MSG36BLD LA    GR5,IEBUPD36             ADDRESS OF MESSAGE         UL0H
         LA    GR6,MSG36SIZ             MESSAGE SIZE               UL0H
         B     MSGWRTE                  GIVE MESSAGE               UL0H
MSG37BLD LA    GR5,IEBUPD37             ADDRESS OF MESSAGE         UL0H
         LA    GR6,MSG37SIZ             SIZE OF MESSAGE            UL0H
         B     MSGWRTE                  GIVE MESSAGE               UL0H
MSG38BLD LA    GR5,IEBUPD38             ADDRESS OF MESSAGE         UL0H
         LA    GR6,MSG38SIZ             SIZE OF MESSAGE            UL0H
         SR    GR7,GR7                                             UL0H
         IC    GR7,USYSNCNT             NUMBER OF LABELS CREATED   UL0H
         CVD   GR7,DUBLWORD             CONVERT TO DECIMAL         UL0H
         UNPK  2(1,GR5),DUBLWORD+7(1)   AND UNPAKE FOR PRINTING    UL0H
         B     MSGWRTE                  GIVE MESSAGE               UL0H
MSG39BLD LA    GR5,IEBUPD39             ADDRESS OF MESSAGE         UL0H
         LA    GR6,MSG39SIZ             MESSAGE SIZE               UL0H
         MVC   2(8,GR5),TOTALRTN        USER TOTAL ROUTINE NAME    UL0H
         B     MSGWRTE                  GIVE MESSAGE               UL0H
MSG40BLD LA    GR5,IEBUPD40             ADDRESS OF MESSAGE         UL0H
         LA    GR6,MSG40SIZ             SIZE OF MESSAGE            UL0H
         MVC   2(8,GR5),TOTALRTN        USER TOTAL ROUTINE NAME    UL0H
         SR    GR7,GR7                 PREPARE FOR HALF A LOAD     000H
         LH    GR7,TOTRTNCD            LOAD TOTAL RETURN CODE      000H
         CVD   GR7,DUBLWORD            CONVERT IT TO DECIMAL       000H
         UNPK  DUBLWORD+5(3),DUBLWORD+6(2)   PREPARE CODE        A32057
         OI    DUBLWORD+7,X'F0'        FIX SIGN TO PRINT OUT     A32057
         CLI   DUBLWORD+5,X'00'        IS CODE 3 CHARS LONG        000H
         BNE   RTNCD3L                 IF SO, MOVE TO MESSAGE      000H
         MVI   DUBLWORD+5,C' '         IF NOT, BLANK OUT THE ZERO  000H
         CLI   DUBLWORD+6,X'00'        IS CODE 2 CHARS LONG        000H
         BNE   RTNCD3L                 IF SO, MOVE TO MESSAGE      000H
         MVI   DUBLWORD+6,C' '         IF NOT, BLANK OUT THE ZERO  000H
RTNCD3L  MVC   IEBUPD40+71(3),DUBLWORD+5        MOVE RETURN CODE TO000H
         B     MSGWRTE                  GIVE MESSAGE               UL0H
MSG41BLD LA    GR5,IEBUPD41             ADDRESS OF MESSAGE         UL0H
         LA    GR6,MSG41SIZ             SIZE OF MESSAGE            UL0H
         MVC   27(8,GR5),TOTALRTN      USER TOTAL ROUTINE NAME     ULOH
         B     MSGWRTE                  GIVE MESSAGE               UL0H
MSG42BLD LA    GR5,IEBUPD42             ADDRESS OF MESSAGE         UL0H
         LA    GR6,MSG42SIZ             SIZE OF MESSAGE            UL0H
         B     MSGWRTE                  GIVE MESSAGE               UL0H
MSG43BLD LA    GR5,IEBUPD43             ADDRESS OF MESSAGE         UL0H
         LA    GR6,MSG43SIZ             SIZE OF MESSAGE            UL0H
         B     MSGWRTE                  GIVE MESSAGE               UL0H
MSG44BLD LA    GR5,IEBUPD44             ADDRESS OF MESSAGE         UL0H
         LA    GR6,MSG44SIZ             SIZE OF MESSAGE            UL0H
         B     COMMONBD                COMMON BUILD ROUTINE FOR 445UL0H
MSG45BLD LA    GR5,IEBUPD45             ADDRESS OF MESSAGE         UL0H
         LA    GR6,MSG45SIZ             SIZE OF MESSAGE            UL0H
COMMONBD SR    GR10,GR10               CLEAR BRANCH REGISTER       UL0H
         IC    GR10,USERRTN+8          PICK UP BRANCH CONSTANT     UL0H
         B     *(GR10)                 BRANCH ON CONSTANT          UL0H
         B     NOHEADER                BUILD NO HEADER LABEL MSG   UL0H
         B     NOTRALER                BUILD NO TRAILER LABEL MSG  UL0H
         B     NOHEADER                BUILD NO KEADER LABEL MSG   UL0H
         B     NOTRALER                BUILD NO TRAILER LABEL MSG  UL0H
NOHEADER MVC   10(7,GR5),IHL+5         PICK UP 'HEADER'            UL0H
         B     MSGWRTE                  GIVE MESSAGE               UL0H
NOTRALER MVC   10(7,GR5),ITL+6         PICK UP 'TRAILER'           UL0H
         B     MSGWRTE                  GIVE MESSAGE               UL0H
MSG46BLD LA    GR5,IEBUPD46             ADDRESS OF MESSAGE         UL0H
         LA    GR6,MSG46SIZ             SIZE OF MESSAGE            UL0H
         B     MSGWRTE                  GIVE MESSAGE               UL0H
*                                                                     *
*                                 IEBUPDTE MESSAGES                   *
*   ALL MESSAGES ARE PRECEDED BY CONSTANT OF IEB5XXI WHERE XX IS EQUAL*
*   TO THE FIRST TWO NUMBERS IN EACH MESSAGE LISTED BELOW.  THE LOG   *
*   ROUTINE 'MSGWRTE' BUILDS THE PROPER CODE FOR EACH MESSAGE.        *
*                                                                     *
         SPACE 1
IEBUPD01 DC    CL36'01CT LRECL AND BLKSIZE ASSUMED 80/80'      @YA02555
IEBUPD02 DC    CL2'02'  I/O ERROR MESSAGE                        A27716
         DC    CL10'I/O ERROR '                                  A27716
SYNADMSG DC    CL80' '      THE PART OF THE I/O ERROR MSG BUILT  A27716
*                        DYNAMICALLY BY THE SYNADAF ROUTINE      A27716
IEBUPD03 DC    CL37'03PERMANENT INPUT ERROR - FIND MACRO'        A27716
IEBUPD04 DC    CL37'04PERMANENT INPUT ERROR - BLDL MACRO'        A27716
IEBUPD05 DC    CL26'05CONTROL STATEMENT ERROR.'
IEBUPD06 DC    CL27'06STATEMENT SEQUENCE ERROR.'
IEBUPD07 DC    CL20'07INVALID OPERATION.'
IEBUPD08 DC    CL34'08TERMINATED THIS MEMBER. IEBUPDTE'          A34086
         DC    CL22' WILL TRY NEXT MEMBER.'
IEBUPD09 DC    CL26'09SCANNING FOR NEXT MEMBER'
IEBUPD10 DC    C'10DELETE RANGE INVALID'
IEBUPD11 DC    C'11NUMBER RANGE INVALID'
IEBUPD12 DC    CL24'12DIRECTORY WRITE ERROR.'
IEBUPD13 DC    CL24'13OUTPUT DIRECTORY FULL.'
IEBUPD14 DC    C'14DDNAME         CANNOT BE '
         DC    C'OPENED.'
IEBUPD15 DC    CL30'15CANNOT PROCESS MORE THAN ONE'
         DC    CL22' PS DATA SET PER PASS.'
IEBUPD16 DC    C'16MEMBER NAME (        ) FOUND '
IEBUP16A DC    C'IN NM DIRECTORY. '    MEMBER IN OM DIRECTORY BAD 4 ADD
         DC    C'TTR IS NOW ALTERED.'
IEBUPD17 DC    C'17MEMBER NAME (        ) NOT '
         DC    C'FOUND '
IEBUP17A DC    C'IN NM DIRECTORY.  STOWED '  MEMBER NOT FOUND BAD CHANG
         DC    C'WITH TTR.'
IEBUPD18 DC    CL28'18HIGHEST CONDITION CODE WAS'
IEBUPD19 DC    C'19END OF JOB IEBUPDTE.'
IEBUPD20 DC    C'20CANNOT PROCESS MORE THAN ONE FUNCTION'      @ZA01692
         DC    C' STMT WHEN UPDATE INPLACE IS SPECIFIED.'      @ZA01692
IEBUPD21 DC    CL32'21 INVALID EXIT NAME. JOB ENDED.'
IEBUPD22 DC    C'22EXIT RETURN CODE ENDED JOB.'
IEBUPD23 DC    CL26'23         HAS NO RECORDS.'
IEBUPD25 DC    C'25ALIAS IGNORED- SEQUENTIAL DATA'
         DC    C' SET.'
IEBUPD26 DC    C'26MEMBER NAME FOUND IN OM '
         DC    C'DIRECTORY AS AN ALIAS- CHANGED '
         DC    C'TO TRUE NAME IN NM DIRECTORY.'
IEBUPD27 DC    CL26'27INVALID INPUT PARAMETER.'
IEBUPD28 DC    CL32'28PAGE NUMBER PARAMETER INVALID.'
IEBUPD29 DC    CL32'29DDNAME PARAMETER IS INVALID.  '
IEBUPD30 DC    C'30OLD AND NEW MASTER LRECL '
         DC    C'UNEQUAL.'
IEBUPD31 DC    C'31 OLD AND NEW MASTER DSORGS INCOMPATIBLE'        3341
IEBUPD32 DC    CL31'32          IS PROCESSING USER '               UL0H
         DC    CL23' '                                             UL0H
IEBUPD33 DC    CL26'33  ENTRANCES TO         .'                    UL0H
IEBUPD34 DC    CL32'34LAST RETURN CODE FROM         '              UL0H
         DC    CL9' WAS    .'                                      UL0H
IEBUPD35 DC    CL42'35           SUPPORT ONLY ON PS DATA SETS.'    UL0H
IEBUPD36 DC    CL44'36TRAILER LABEL PROCESSING NOT SUPPORTED FOR'  UL0H
         DC    CL15' UPDATE=INPLACE'                             A34086
IEBUPD37 DC    CL41'37I/O ERROR WHILE PROCESSING USER LABELS.'     UL0H
IEBUPD38 DC    CL40'38  LABELS CREATED FROM INPUT STREAM.'         UL0H
IEBUPD39 DC    CL36'39         IS TAKING TOTALING EXITS.'          UL0H
IEBUPD40 DC    CL44'40         REQUESTED TERMINATION OF TOTALING'  UL0H
         DC    CL31' EXITS WITH RETURN CODE OF    .'               OOOH
IEBUPD41 DC    CL37'41INVALID RETURN CODE FROM         ,'          UL0H
         DC    CL28'TOTALING EXITS DISCONTINUED.'                  UL0H
IEBUPD42 DC    CL34'42TOTALING EXITS NOT SUPPORTED FOR'            UL0H
         DC    CL24' UPDATE=INPLACE.'                              ULOH
IEBUPD43 DC    CL20'43INVALID CORE SIZE.'                          UL0H
IEBUPD44 DC    CL25'44NO USER         LABELS '                     UL0H
         DC    CL24'EXIST ON INPUT DATA SET.'                      UL0H
IEBUPD45 DC    CL25'45NO USER         LABELS '                     UL0H
         DC    CL27'CREATED ON OUTPUT DATA SET.'                   UL0H
IEBUPD46 DC    CL34'46ALIAS IGNORED FOR UPDATE=INPLACE'            UL0H
IEBLASTM EQU   *                       KEEP EQUATE AFTER LAST MESSAGE
         EJECT
*  TABLE OF SIZES OF MESSAGES                                         *
*      SIZE IS EQUAL TO TOTAL MESSAGE SIZE - 2 BYTES FOR THE INITIAL  *
*  MESSAGE NUMBER CODE - 1 BYTE SO COUNT WILL BE CORRECT FOR EXECUTE  *
*  WHICH MOVES THE MESSAGE TO THE PRINT BUFFER IN 'MSGWRTE' ROUTINE.  *
         SPACE 1
MSG01SIZ EQU   IEBUPD02-IEBUPD01-3    SIZE OF MESSAGE 1          A27716
MSG02SIZ EQU   IEBUPD03-IEBUPD02-3    SIZE OF MESSAGE 2          A27716
MSG03SIZ EQU   IEBUPD04-IEBUPD03-3    SIZE OF MESSAGE 3          A27716
MSG04SIZ EQU   IEBUPD05-IEBUPD04-3    SIZE OF MESSAGE 4          A27716
MSG05SIZ EQU   IEBUPD06-IEBUPD05-3     SIZE OF MESSAGE 5
MSG06SIZ EQU   IEBUPD07-IEBUPD06-3     SIZE OF MESSAGE 6
MSG07SIZ EQU   IEBUPD08-IEBUPD07-3     SIZE OF MESSAGE 7
MSG08SIZ EQU   IEBUPD09-IEBUPD08-3     SIZE OF MESSAGE 8
MSG09SIZ EQU   IEBUPD10-IEBUPD09-3     SIZE OF MESSAGE 9
MSG10SIZ EQU   IEBUPD11-IEBUPD10-3     SIZE OF MESSAGE 10
MSG11SIZ EQU   IEBUPD12-IEBUPD11-3     SIZE OF MESSAGE 11
MSG12SIZ EQU   IEBUPD13-IEBUPD12-3     SIZE OF MESSAGE 12
MSG13SIZ EQU   IEBUPD14-IEBUPD13-3     SIZE OF MESSAGE 13
MSG14SIZ EQU   IEBUPD15-IEBUPD14-3     SIZE OF MESSAGE 14
MSG15SIZ EQU   IEBUPD16-IEBUPD15-3     SIZE OF MESSAGE 15
MSG16SIZ EQU   IEBUPD17-IEBUPD16-3     SIZE OF MESSAGE 16
MSG17SIZ EQU   IEBUPD18-IEBUPD17-3     SIZE OF MESSAGE 17
MSG18SIZ EQU   IEBUPD19-IEBUPD18-3     SIZE OF MESSAGE 18
MSG19SIZ EQU   IEBUPD20-IEBUPD19-3     SIZE OF MESSAGE 19
MSG20SIZ EQU   IEBUPD21-IEBUPD20-3     SIZE OF MESSAGE 20
MSG21SIZ EQU   IEBUPD22-IEBUPD21-3     SIZE OF MESSAGE 21
MSG22SIZ EQU   IEBUPD23-IEBUPD22-3     SIZE OF MESSAGE 22
MSG23SIZ EQU   IEBUPD25-IEBUPD23-3     SIZE OF MESSAGE 23          BS0H
MSG25SIZ EQU   IEBUPD26-IEBUPD25-3     SIZE OF MESSAGE 25
MSG26SIZ EQU   IEBUPD27-IEBUPD26-3     SIZE OF MESSAGE 26
MSG27SIZ EQU   IEBUPD28-IEBUPD27-3     SIZE OF MESSAGE 27
MSG28SIZ EQU   IEBUPD29-IEBUPD28-3     SIZE OF MESSAGE 28
MSG29SIZ EQU   IEBUPD30-IEBUPD29-3     SIZE OF MESSAGE 29
MSG30SIZ EQU   IEBUPD31-IEBUPD30-3      SIZE OF MESSAGE 30         3341
MSG31SIZ EQU   IEBUPD32-IEBUPD31-3                                 UL0H
MSG32SIZ EQU   IEBUPD33-IEBUPD32-3                                 UL0H
MSG33SIZ EQU   IEBUPD34-IEBUPD33-3                                 UL0H
MSG34SIZ EQU   IEBUPD35-IEBUPD34-3                                 UL0H
MSG35SIZ EQU   IEBUPD36-IEBUPD35-3                                 UL0H
MSG36SIZ EQU   IEBUPD37-IEBUPD36-3                                 UL0H
MSG37SIZ EQU   IEBUPD38-IEBUPD37-3                                 UL0H
MSG38SIZ EQU   IEBUPD39-IEBUPD38-3                                 UL0H
MSG39SIZ EQU   IEBUPD40-IEBUPD39-3                                 UL0H
MSG40SIZ EQU   IEBUPD41-IEBUPD40-3                                 UL0H
MSG41SIZ EQU   IEBUPD42-IEBUPD41-3                                 UL0H
MSG42SIZ EQU   IEBUPD43-IEBUPD42-3                                 UL0H
MSG43SIZ EQU   IEBUPD44-IEBUPD43-3                                 UL0H
MSG44SIZ EQU   IEBUPD45-IEBUPD44-3                                 UL0H
MSG45SIZ EQU   IEBUPD46-IEBUPD45-3                                 UL0H
MSG46SIZ EQU   IEBLASTM-IEBUPD46-3                                 UL0H
         SPACE 3
MSG16ASZ EQU   IEBUP16A-IEBUPD16-3     SPECIAL BLDL/FIND ERROR MESSAGE
MSG17ASZ EQU   IEBUP17A-IEBUPD17-3     SPECIAL BLDL/FIND ERROR MESSAGE
         EJECT
*  SYSPRINT DCB    WRITES THE LOG
DCBLOGR  DCB   DSORG=PS,MACRF=(PM),DDNAME=SYSPRINT,RECFM=FBA,      BS0H*
               LRECL=121,EXLST=DCBLST,SYNAD=SPSYNAD              A27716
         EJECT
         IEBUPCON
         EJECT
         DCBD  DSORG=PS
BDDNAM   EQU   DCBDDNAM-IHADCB         DD NAME IN DCB 'BEFORE' OPEN
BOFLGS   EQU   DCBOFLGS-IHADCB         OPEN ERROR FIELD IN DCB
         END   IEBUPLOG
