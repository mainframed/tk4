UPD1     TITLE 'IEBUPDTE INITIALIZATION CSECT- IEBUPDTE'
*TITLE-IEBUPDTE-IEBUPDTE. INITIALIZATION CSECT                     2039
*                                                                     *
*STATUS- CHANGE LEVEL 001                                             *
*                                                                     *
*FUNCTION/OPERATION- THIS MODULE IS ABLE TO CREATE AND UPDATE         *
*   SYMBOLIC LIBRARIES WHICH ARE EITHER PHYSICAL SEQUENTIAL OR        *
*   PARTITIONED DATA SETS CONTAINED ON DIRECT ACCESS OR TAPE VOLUMES. *
*                                                                     *
*   THIS MODULE HANDLES FIXED OR FIXED-BLOCKED RECORDS. IT HAS THE    *
*   ABITLITY TO HANDLE RECORD OF LOGICAL RECORD LENGTHS UP TO EIGHTY  *
*   BYTES. IF BLOCKSIZE AND/OR LOGICAL RECORD LENGTH ARE NOT GIVEN    *
*   BY OPEN TIME, UNBLOCKED RECORDS OF 80 BYTES IS ASSUMED AND A      *
*   WARNING MESSAGE IS GIVEN ON THE LOG OUT DEVICE.                   *
*                                                                     *
*   THIS MODULE CAN-                                                  *
*                                                                     *
*   1. CREATE A NEW PARTITIONED OR PHYSICAL SEQUENTIAL DATA SET       *
*                                                                     *
*   2. SEQUENCE THE NEW DATA SET OR RESEQUENCE AN OLD ONE             *
*                                                                     *
*   3. REPRODUCE OR REPLACE A MEMBER OR A DATA SET                    *
*                                                                     *
*   4. PRODUCE A PARTITIONED DATA SET FROM A PHYSICAL SEQUENTIAL      *
*   MASTER OR THE CONVERSE.                                           *
*                                                                     *
*   4. ADD A MEMBER TO A PARTITIONED DATA SET.                        *
*                                                                     *
*   5. ASSIGN AN ALIAS NAME TO ANY MEMBER UPDATED                     *
*                                                                     *
*   6. CHANGE RECORDS IN A DATA SET                                   *
*                                                                     *
*   7. HANDLE RECORDS WITH NON-STANDARD SEQUENCE NUMBER FIELDS AND    *
*   LENGTHS OF SEQUENCE NUMBERS FROM 1 TO 8 CHARACTERS.               *
*                                                                     *
*   8. UPDATE ONLY SPECIFIED PORTION OF DATA SET RECORDS.             *
*                                                                     *
*   9. UPDATE INPLACE DATA SETS OR MEMBERS ON DIRECT ACCESS VOLUMES.  *
*                                                                     *
*ENTRY POINTS- ENTERED FROM THE OPERATING SYSTEM AT 'IEBUPDTE'.       *
*                                                                     *
*INPUT- SYSIN WHICH CONSISTS OF CONTROL CARDS AND LOGICAL DATA        *
*   RECORDS FOR INSERTION OR REPLACEMENT, THE INPUT DATA SET.         *
*                                                                     *
*       SYSUT1 WHICH DEFINES THE 'OLD MASTER' DATA SET WHICH MAY BE   *
*   PARTITIONED OR PHYSICAL SEQUENTIAL IN ORGANIZATION.               *
*                                                                     *
*OUTPUT-SYSPRINT WHICH CONTAINS DIAGNOSTIC MESSAGES, THE CONTENTS OF  *
*   SYSIN AND-IF THE 'LIST=ALL' CONDITION IS PRIMED- THE ENTIRE       *
*   'NEW MASTER' IS LISTED ON THE DEVICE DESIGNATED.                  *
*                                                                     *
*       SYSUT2 WHICH DEFINES THE 'NEW MASTER' DATA SET. IT WILL BE A  *
*   COMPOSITE OF THE SYSIN AND SYSUT1 DATA SETS. IT CAN BE EITHER     *
*   PARTITIONED OR PHYSICAL SEQUENTIAL IN ORGANIZATION.               *
*                                                                     *
*EXITS- NORMAL- A RETURN IS GIVEN TO THE SCHEDULAR MODULE OF THE      *
*   OPERATING SYSTEM. CONDITION CODE RETURNED ON A SUCCESSFUL RUN     *
*   WILL BE ZERO.                                                     *
*                                                                     *
*EXITS- ERROR- IF AN ERROR OCCURS, A CODE IS STORED, A MESSAGE        *
*   GIVEN TO THE USER EXPLAINING THE ERROR CONDITION AND A            *
*   RETURN TO THE OPERATING SYSTEM SCHEDULAR MODULE IS GIVEN.         *
*   RETURN CODE GIVEN ARE-                                            *
*   00 NORMAL TERMINATION OF A SUCCESSFUL RUN                         *
*   04 ERROR WITHIN A MEMBER- WILL TRY TO PROCESS NEXT MEMBER IF PODS *
*   12 UNRECOVERABLE ERROR- TERMINATION OF JOB.                       *
*   16 ERROR IN LABEL HANDLING IN USERS ROUTINE- TERMINATION OF JOB   *
*                                                                     *
*EXTERNAL ROUTINES- I/O HANDLED AT READ/WRITE LEVEL, GETMAIN, LINK,   *
*   BLDL, FIND, STOW USED.                                            *
*                                                                     *
*TABLES/WORK AREAS-                                                   *
*   -KEYTAB- LIST OF VALID KEY WORDS USED ON CONTROL CARDS            *
*   -COMDTABL- LIST OF VALID COMMAND WORDS USED ON CONTROL CARDS      *
*   -SWITCHRD- BUFFER CONTAINING CONTROL CARD AND SWITCHES USED       *
*   BY CARD SCAN ROUTINE.                                             *
*   -LOGOUTAR- BUFFER WHICH IS USED BY LOG OUT ROUTINE FOR MESSAGES.  *
*   -STARTAB- TABLE OF SWITCHES WHICH ARE RESET TO ZERO UPON ENTRY    *
*   TO MODULE. ALL SWITCHES NEEDING REINITIALIZATION SHOULD BE PUT    *
*   INTO THIS TABLE.                                                  *
*                                                                     *
*ATTRIBUTES- SERIAL REUSABLE, BLOCK LOADS
*                                                                     *
***********************************************************************
*FUNCTION/OPERATION OF THIS CSECT--IEBUPDTE--                      UL0H
*        1.    GETS CORE FOR THE COMMUNICATION REGION IEBUPCON     UL0H
*        2.    GIVES CONTROL TO THE CSECT IEBUPNIT TO INITIALIZE   UL0H
*              DCB'S AND THE COMMUNICATION REGION.                 UL0H
*        3.    THE DCB'S ARE MOVED INTO THIS CSECT                 UL0H
*        4.    CONTROL IS THEN PASSED TO IEBUPDTE2                 UL0H
***********************************************************************
         EJECT
*     SYMBOLIC REGISTER NOTATION   GR0 - GR15
GR0      EQU   0
GR1      EQU   1
GR2      EQU   2
GR3      EQU   3
GR4      EQU   4                       REGISTER RESERVED FOR CSECT1
GR5      EQU   5
GR6      EQU   6
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10
GR11     EQU   11
GR12     EQU   12
GR13     EQU   13
GR14     EQU   14
GR15     EQU   15
         SPACE 1
IEBUPDTE CSECT
*1132059200,077400,078400,080800,081400,082000,094400,095000,      BS0H
*105400-105600,112200,115400                                       BS0H
*1132016800-017600,023430-023480,025200-082000,082600-104600       UL0H
*1132109000-111800,112200,112600-113000,113400-125000              UL0H
*                                                                A38775
         SPACE 2
***********************************************************************
****************************     W A R N I N G     ********************
*        REGISTER 12 IS USED TO CONTAIN THE ADDRESS OF THE COM-    UL0H
*     MUNICATION REGION---DO NOT USE ALTER REGISTER IN ANY CSECT.  UL0H
*                                                                  UL0H
*        REGISTER 4 IS THE BASE REGISTER FOR THE EXIT ROUTINE      UL0H
*     CSECT,IEBUPXIT.DO NOT ALTER THIS REGISTER IN ANY CSECT.      UL0H
*                                                                  UL0H
***********************************************************************
         SPACE 2                                                      *
         SPACE 1
         EXTRN CTERROUT                                            UL0H
         EXTRN CTHDREXT                                            UL0H
         EXTRN CTTRLEXT                                            UL0H
         EXTRN CTDCBEX                                             UL0H
         EXTRN OMEOD                                               UL0H
         EXTRN  OMERROUT                                           UL0H
         EXTRN OMHDREXT                                            UL0H
         EXTRN OMTRLEXT                                            UL0H
         EXTRN OMDCBEX                                             UL0H
         EXTRN EODCT                                               UL0H
         EXTRN NMERROUT                                            UL0H
         EXTRN NMHDREXT                                            UL0H
         EXTRN NMTRLEXT                                            UL0H
         EXTRN NMDCBEX                                             UL0H
         SAVE  (14,12)                                             UL0H
         BALR  GR2,0                                               UL0H
         USING AHEAD,GR2                                           UL0H
         USING IEBUPCON,GR12           POINTER TO DSECT AREA
AHEAD    LR    GR10,GR1                LOAD PARAMETER INTO STABLE REG10
         LA    GR0,DSECTSIZ            GET SIZE OF DSECT
         GETMAIN  R,LV=(GR0)
         LR    GR12,GR1                POINTER TO DSECT AREA
         XC    IEBUPCON(256),IEBUPCON  CLEAR IEBUPCON BEFORE USE A38775
         XC    IEBUPCON+1*256(256),IEBUPCON+1*256                A38775
         XC    IEBUPCON+2*256(256),IEBUPCON+2*256                A38775
         XC    IEBUPCON+3*256(256),IEBUPCON+3*256                A38775
         XC    IEBUPCON+4*256(256),IEBUPCON+4*256                A38775
         XC    IEBUPCON+5*256(256),IEBUPCON+5*256                A38775
         XC    IEBUPCON+6*256(256),IEBUPCON+6*256                A38775
         XC    IEBUPCON+7*256(256),IEBUPCON+7*256                A38775
         XC    IEBUPCON+8*256(232),IEBUPCON+8*256                A38775
         ST    GR1,DSECTLOC            SAVE ADDRESS OF COMMON AREA
         ST    GR13,SAVE13             SAVE SETTING FOR SUPERVISOR
         LA    GR13,SAVEREG            ADDRESS OF THIS CSECT SAVE AREA
         LR    GR1,GR10                 RELOAD PARM LIST ADDRESS   UL0H
         L     GR15,INIT                ADDRESS OF INITIALIZE MODULUL0H
         BALR  GR14,GR15                GIVE CONTROL TO IEBUPNIT   UL0H
         L     GR10,DCBCT               ADDRESS OF SYSIN DCB       UL0H
         LA    GR11,DCBCTR             ADDRESS OF SYSIN DCB        UL0H
         ST    GR11,DCBCT              POINTER TO SYSIN DCB        UL0H
         BAL   GR14,COMPINIT            MOVE SYSIN DCB             UL0H
         L     GR10,DCBOM               ADDRESS OF OM DCB          UL0H
         LA    GR11,DCBOMR             ADDRESS OF SYSUT1 DCB       UL0H
         L     GR9,36(GR10)            ADDR OF DCB EXIT LIST       UL0H
         MVC   OMEXITS(52),0(GR9)       MOVE ALL  EXIT LISTS       UL0H
         ST    GR11,DCBOM              POINTER TO SYSUT1 DCB       UL0H
         BAL   GR14,COMPINIT            MOVE  O.M. DCB             UL0H
         L     GR10,DCBNM               ADDRESS OF N.M. DCB        UL0H
         LA    GR11,DCBNMR             ADDRESS OF SYSUT2 DCB       UL0H
         ST    GR11,DCBNM              POINTER TO SYSUT2 DCB       UL0H
         BAL   GR14,COMPINIT            MOVE N.M. DCB              UL0H
OPENINPT MVI   BLOKSW,X'0'        INITIALIZE BLOCKSIZE FLAG        UL0H
         OPEN DCBCTR              OPEN SYSIN DCB                   UL0H
         L     GR1,DCBCT          ADDRESS OF SYSIN DCB             UL0H
         LA    GR5,SYSINNM        ADDRESS OF DD NAME               UL0H
         TM    BOFLGS(GR1),BIT10  WAS THERE AN OPEN ERROR          UL0H
         BZ    BADOPEN            IF YES,PRINT ERROR AND QUIT      UL0H
         CLI   BLOKSW,X'0'        IS BLOCKSIZE A MULTIPLE OF 80    UL0H
         BNE   BADOPEN1           IF NOT PUT OUT MESSAGE AND QUIT  UL0H
         CLI   LBLRETCD,BIT10     USER ASKED TO TERMINATE          UL0H
         BE    USERTERM           IF SO, DO SO                     UL0H
         LA    GR1,INPUT          ADDRESS OF INPUT BUFFER          UL0H
         ST    GR1,READINAP       SAVE BUFFER ADDRESS              UL0H
ENTRNEXT L     GR15,PROCESS            ADDRESS OF MAIN PROCESSING CSECT
         BR    GR15                    ENTER NEXT STAGE- NO RETURN
COMPINIT MVC   SAVEREG+20(3),37(11)                                UL0H
         MVC   0(98,GR11),0(GR10)                                  UL0H
         MVC   37(3,GR11),SAVEREG+20                               UL0H
         BR    GR14                                                UL0H
         SPACE 2
BADOPEN1 CLOSE  (DCBCTR,LEAVE)                                     UL0H
         FREEPOOL  DCBCTR                                          UL0H
BADOPEN  LA    GR7,SYSINNM        DD NAME FOR MESSAGE              UL0H
         LA    GR11,NTOPMSG       NOT OPEN MSG CONSTANT            UL0H
ALMOSTZ  LA    GR15,12            CONDITION CODE                   UL0H
         LA    GR14,CLOSENOW      PREPARE TO QUIT AFTER MESSAGE    UL0H
         CH    GR15,HICONCDE      HAS HIGHER CON CODE BEEN STORED  UL0H
         BNH   FINERRT            IF SO, USE THAT CODE             UL0H
         STH   GR15,HICONCDE      IF NOT, RETURN CON CODE OF 12    UL0H
FINERRT  L     GR15,PRIMEWRT      ADDRESS OF IEBUPLOG         2    UL0H
         BR    GR15               PRINT MESSAGE AND RETURN HERE    UL0H
CLOSENOW MVI   QUITALL,X'0E'      FLAG AS LAST MESSAGE AND TERMINATUL0H
         L     GR15,PRIMEWRT      ADDRESS OF IEBUPLOG              UL0H
         BR    GR15               IEBUPLOG WILL RETURN TO SYSTEM   UL0H
USERTERM MVI   CTEXITS+8,X'85'    FLAG DCB DCB EXIT AS LAST        UL0H
         LA    GR11,USRTRMSG      PRINT USER REQ TERMINATION MSG   UL0H
         MVI   HICONCDE,X'10'     CON CODE=USER TERMINATION        UL0H
         LA    GR14,CLOSENOW      RETURN TO TERMINATE PROCESSING   UL0H
         B     FINERRT                                             UL0H
*
*        DCB EXIT LISTS FOR SYSIN, SYSUT1 AND SYSUT2 DATA SETS.    UL0H
*
         DS    0F
OMEXITS  DC    X'01'                                               UL0H
         DC    AL3(OMHDREXT)                                       UL0H
         DC    X'03'                                               UL0H
         DC    AL3(OMTRLEXT)                                       UL0H
         DC    A(0)                                                UL0H
         DC    X'85'                                               UL0H
         DC    AL3(OMDCBEX)                                        UL0H
         DC    X'07'                                               UL0H
         DC    AL3(0)                                              UL0H
NMEXITS  DC    X'02'                                               UL0H
         DC    AL3(NMHDREXT)                                       UL0H
         DC    X'04'                                               UL0H
         DC    AL3(NMTRLEXT)                                       UL0H
         DC    X'00'                                               UL0H
         DC    AL3(0)                                              UL0H
         DC    X'85'                                               UL0H
         DC    AL3(NMDCBEX)                                        UL0H
         DC    X'07'                                               UL0H
         DC    AL3(0)                                              UL0H
CTEXITS  DC    X'00'                                               UL0H
         DC    AL3(CTHDREXT)                                       UL0H
         DC    X'00'                                               UL0H
         DC    AL3(CTTRLEXT)                                       UL0H
         DC    X'85'                                               UL0H
         DC    AL3(CTDCBEX)                                        UL0H
         EJECT
*   SYSIN DCB   CONTROL CARD INPUT     REFERRED TO AS CT
         SPACE 1
DCBCTR   DCB   DSORG=PS,MACRF=(GL),DDNAME=SYSIN,RECFM=FB,LRECL=80, BS0H*
               EODAD=EODCT,EXLST=CTEXITS,SYNAD=CTERROUT,BUFNO=1    BS0H
         EJECT
*   SYSUT1 DCB  OLD MASTER             REFERRED TO AS OM
         SPACE 1
DCBOMR   DCB   DSORG=PO,MACRF=R,DDNAME=SYSUT1,NCP=1,               2039C
               EXLST=OMEXITS,EODAD=OMEOD,SYNAD=OMERROUT
         EJECT
*   SYSUT2 DCB  NEW MASTER             REFERRED TO AS NM
         SPACE 1
DCBNMR   DCB   DSORG=PO,MACRF=W,DDNAME=SYSUT2,NCP=1,               2039C
               EXLST=NMEXITS,SYNAD=NMERROUT
         SPACE 1
         EJECT
         SPACE 1
         SPACE 1
         SPACE 1
*   CONSTANTS FOR INITIALIZATION ROUTINE
SAVEREG  DS    18F                     SAVE AREA USED BY READ/WRITE
DSECTLOC DC    F'0'                    COMMON AREA ADDRESS JUST IN CASE
CTERROR  DC    V(CTERROUT)                                         UL0H
PRIMEWRT DC    V(IEBUPLOG)                                         UL0H
         SPACE 1
PROCESS  DC    V(IEBUPDT2)             ENTRY POINT PROCESSING CSECT
         SPACE 1
INIT     DC    V(IEBUPNIT)                                         UL0H
BIT10    EQU   X'10'                                               UL0H
         SPACE 1
         EJECT
         IEBUPCON
         DCBD
BOFLGS  EQU   DCBOFLGS-IHADCB
         SPACE 2
         END   IEBUPDTE
