***********************************************************************
*                                                                     *
*                   *****       I E B B S C A N        *****          *
*                                                                     *
* STATUS. CHANGE LEVEL 000.                                           *
*                                                                     *
* FUNCTION/OPERATION.                                                 *
*        THIS CSECT SCANS CONTROL CARD STATEMENTS,SETS FLAGS IN       *
*        THE COMMUNICATION REGION AT SWITCHRD AND RETURNS POINTERS    *
*        TO IEBASCAN FOR ANALYSIS.                                    *
*                                                                     *
* ENTRY POINTS.                                                       *
*        ENTERED FROM IEBASCAN AT IEBBSCAN.                           *
*                                                                     *
* INPUT.                                                              *
*        CONTROL CARD PARAMETERS TO BE ANALYZED AT SWITCHRD.          *
*                                                                     *
* OUTPUT.                                                             *
*        DIAGNOSTIC MESSAGES FROM IEBUPLOG.                           *
*                                                                     *
* EXITS-NORMAL.                                                       *
*        RETURN TO IEBBSCAN AFTER A PARAMETER HAS BEEN ANALYZED.      *
*        IF A CONTINUATION CARD MUST BE READ,CONTROL IS PASSED TO     *
*        IEBUPDT2 WHICH WILL READ A CARD FROM SYSIN AND PASS          *
*        CONTROL TO IEBBSCAN VIA IEBASCAN.                            *
*                                                                     *
* EXITS-ERROR.                                                        *
*        FLAGS ARE SET AND CONTROL IS RETURNED TO IEBASCAN.           *
*                                                                     *
* EXTERNAL ROUTINES.                                                  *
*        IEBASCAN,IEBUPLOG AND IEBUPDT2.                              *
*                                                                     *
* TABLES/WORK AREAS.                                                  *
*        SWITCHRD WHICH IS A BUFFER IN IEBUPCON CONTAINING THE        *
*        CONTROL CARD UNDERGOING ANALYSIS AND SWITCHES.               *
*                                                                     *
* ATTRIBUTES.                                                         *
*        SERIALLY REUSABLE, BOLCK LOADS.                              *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                      CONTROL STATEMENT ANALYSIS ROUTINES            *
***********************************************************************
         SPACE 1
GR0      EQU   0
GR1      EQU   1
GR2      EQU   2
GR3      EQU   3
GR4      EQU   4
GR5      EQU   5
GR6      EQU   6
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10
GR11     EQU   11
GR12     EQU   12
GR13     EQU   13
GR14     EQU   14
GR15     EQU   15
D0       EQU   0                        DISPLACEMENT OF 0       YM07522
D1       EQU   1                        DISPLACEMENT OF 1       YM07522
LENGTH   EQU   0                       LENGTH OF PARAM REGISTER 0
SCANADR  EQU   1                       ADDRESS OF PARAMETER REGISTER 1
BIT80    EQU   X'80'                   ERROR SWITCH
BIT40    EQU   X'40'                   BYPASS SWITCH
BIT20    EQU   X'20'                   EOF, PARAMETER FOLLOWS SWITCH
BIT10    EQU   X'10'                   FIRST ENTRY, PARTIAL PARAMETER
BIT08    EQU   X'08'                   COMMAND, COLUMN 72 NON-BLANK SW
BIT04    EQU   X'04'                   KEYWORD, COLUMN 72 NON-BLANK SW
BIT02    EQU   X'02'                   PARAMETER SWITCH
BIT01    EQU   X'01'                   LENGTH EQUALS ZERO SWITCH
RESET0   EQU   X'00'                   RESET BITS
EQUAL    EQU   C'='                    HEX 'EQUAL'
COMMA    EQU   C','                    HEX 'COMMA'
BLANKCOL EQU   C' '                    HEX 'BLANK'
SWF0     EQU   X'F0'                    DOUBLE PARM FLAGS       YM07522
SWF8     EQU   X'F8'                                            YM07522
SWFC     EQU   X'FC'                                            YM07522
SWFE     EQU   X'FE'                                            YM07522
         SPACE 1
IEBBSCAN CSECT
*                                                                A27507
*A684500-686700,699000,699500                                    A48723
*C750000                                                         A48723
*A63004-63020,105020-105080,329420-329440,345420-345440         YM07522
*A346020-346080,347020-347080.671020-671080                     YM07522
*C346000,346200,346400,347000,347200,671000,672000              YM07522
*A240100,240500,270100,270200,492500,493000,495500,660500,      YA01700
*A661000,699100,699200,705500,706000,711500,771500,772000       YA01700
*C264000,663000,780000                                          YA01700
*D243000,702000                                                 YA01700
*A 534500-535500                        @XA16051=(ORG)@YA14019=@ZA16462
RDCARD   SAVE  (14,12)
         BALR  GR2,0                    ESTABLISH ADDRESSABILITY
         USING *,GR2                    USE REGISTER 2
         USING IEBUPCON,GR12            COMMUNICATION REGION
         ST    GR13,RRSAVE+4            SAVE OLD SAVE AREA ADDRESS
         LA    GR13,RRSAVE              ESTABLISH NEW SAVE ADDRESS
         L     GR3,STOREG+8            LOAD ADDRESS OF LAST PASS
         CLI   CCSCANSW,BIT01          IS IT RETURN FROM CONTINUATION
         BE    RESETCON                YES-SCAN CONTINUATUION
         NI    SWITCHRD,X'7F'          RESET ERROR BIT
         TM    SWITCHRD,BIT40          TEST BYPASS SWITCH
         BC    1,KBYPO                 IF ON GO TO BYPASS ROUTINE
         TM    PARAMSW,BIT08           TEST READ CARD SWITCH
         BC    1,KGTCD                 IF ON, READ A CARD
         TM    PARAMSW,BIT20           DOES A PARAMETER FOLLOW
         BC    1,SCANR                 YES- SCAN THE CARD
         TM    PARAMSW,BIT40           CONTINUATION SWITCH SET
         BC    1,KCONT                 YES- PROCESS CONTINUED PART
         LA    GR3,SWITCHRD+1          LOAD ADDRESS OF FIRST COLUMN
         CLC   0(2,GR3),DOTSLASH       IS CARD A CONTROL CARD  (./)
         BNE   KTER                    NO- ERROR CONDITION
         LA    GR3,2(GR3)              SKIP TO NAME COLUMN
KNAME    CLI   0(GR3),BLANKCOL         IS COLUMN BLANK
         LA    GR5,LASTCOLM            LOAD 5 WITH COLUMN 71 OF CARD
         BE    KOMMD                   YES- GO TO COMMAND WORD ROUTINE
         CR    GR3,GR5                 TEST IF SCANNED TO COLUMN 71
         BE    KNOP                    YES- END OF CARD REACHED
         LA    GR3,1(GR3)              GO TO NEXT COLUMN
         B     KNAME                   CHECK FOR BLANK
KOMMD    LA    GR3,1(GR3)              SPACE TO NEXT COLUMN AFTER COMD
         OI    PARAMSW,BIT04           SET COMMAND SWITCH PARAM FOLLOW
         OI    SWITCHRD,BIT08          SET COMMAND BIT '08'
         NI    DELIMIN,RESET0          RESET DELIMITER INDICATORS
KABC     CLI   0(GR3),BLANKCOL         IS COLUMN BLANK
         BNE   SCAN                    NO- CHECK PARAMETER
         CR    GR3,GR5                 IS THIS THE END COLUMN
         BE    KNOP                    YES- END OF CARD REACHED
         LA    GR3,1(GR3)              ADJUST POINTER TO NEXT COLUMN
         B     KABC                    CONTINUE SCAN
KCONT    LA    GR3,SWITCHRD+1          FIRST COLUMN OF CONTINUED CARD
         CLC   0(2,GR3),DOTSLASH       IS CARD A CONTROL CARD
         BNE   KTER                    NO- ERROR CONDITION
         LA    GR3,SWITCHRD+16         SET POINTER TO COLUMN 16
         TM    PARAMSW,BIT10      PARTIAL PARAMETER SW SET?    @YA01700
         BO    CONTINUE           CONTINUE SCANNING             YA01700
SCANR    NI    SWITCHRD,X'F7'          RESET COMMAND BIT
SCAN     NI    SWITCHRD,X'98'          RESET ALL BUT ERROR, 1ST ENTRY
         NI    PARAMSW,X'14'           RESET  BUT COMD/PARA FOLL A48723
         MVC   DELIMIN2(1),DELIMIN     SAVE DELIMITER SWITCH SETTINGS
         NI    COL72LG,X'0C'           RESET EXIT CONTROL SWITCH
         NI    DELIMIN,RESET0          CLEAR DELIMITER SWITCH
CONTINUE SR    GR7,GR7                 CLEAR LENGTH REGISTER    YA01700
         LR    GR1,GR3                 SAVE POINTER TO PRESENT COLUMN
         LA    GR5,LASTCOLM            LOAD ADDRESS OF END COLUMN
         CR    GR3,GR5            ALREADY IN COL. 72?           YA01700
         BE    KPART              YES,GO TO PART PARAMETER SAVE YA01700
KOMPAR   CLI   0(GR3),EQUAL            IS CHARACTER A DELIMITER  A48723
         BNH   DELIMIT                 YES-'EQUAL' CHARAC OR LOW A48723
KOMPAR2  LA    GR7,1(GR7)              CHECK ONE FURTHER         A48723
KOMPAR3  CR    GR3,GR5                ALREADY AT COL 72          A48723
         BE    KPART                   YES-GO TO PARTIAL PARAMETER SAVE
         LA    GR3,1(GR3)              UP POINTER TO NEXT COLUMN A48723
         B     KOMPAR                  CONTINUE SCANNING FOR DELIMITER
KNOP     OI    SWITCHRD,BIT80          FLAG AS NO COMD OR PARAM ON CARD
         SR    GR1,GR1                 CLEAR SCAN REGISTER
         SR    GR7,GR7                 CLEAR LENGTH REGISTER
KNOPAR   OI    SWITCHRD,BIT40          NO KEYWORD OR PARAMETERS FOLLOW
KPASS    LA    GR3,1(GR3)              UP POINTER TO NEXT COLUMN
         NI    PARAMSW,X'EB'           CLEAR ALL BUT BYPASS-PART PARAM.
         ST    GR7,STOREG              STORE LENGTH REGISTER
         ST    GR1,STOREG+4            STORE SCANADR REGISTER
         ST    GR3,STOREG+8            STORE ADDRESS REGISTER
         L     GR13,RRSAVE+4
         LM    GR14,GR12,12(GR13)
         BR    GR14                    RETURN TO MAINSTREAM
DELIMIT  CLI   0(GR3),C'$'              CHARACTER A DOLLAR SIGN
         BE    KOMPAR2                  IF SO, OK
         CLI   0(GR3),X'7C'             CHARACTER AN AT SIGN
         BE    KOMPAR2                  IF SO, OK
         CLI   0(GR3),C'#'              CHARACTER A POUND SIGN
         BE    KOMPAR2                  IF SO, OK
         CLI   D0(GR3),C'*'             CHARACTER AN ASTERISK   YM07522
         BE    KOMPAR2                  IF SO, OK               YM07522
         CLI   0(GR3),C'('              CHARACTER A LEFT PARENTHESIS
         BE    LFTPAREN                 IF SO,CHECK FURTHER
         CLI   0(GR3),C''''             CHARACTER A QUOTE
         BE    QUOTE                    IF SO,CHECK FURTHER
         CLI   0(GR3),C')'              CHARCTER A RIGHT PARENTHESIS
         BE    RTPAREN                  IF SO,CHECK FURTHER
DELIMB   CR    GR3,GR5                  END COLUMN
         BE    SETCC2                  YES- SET INDICATOR
         OI    COL72LG,BIT40           SET DELIMITER BEFORE LAST SWITCH
         TM    PARAMSW,BIT10           IS PARTIAL PARAMETER SWITCH ON
         BC    1,KPPAR                 YES-COMPLETE PARAMETER
         B     TESTPAR                 CHECK PARAMETER
QUOTE    CLI   TOTALSW,X'F0'            TOTALING REQUESTED
         BE    LFTPAREN                 IF SO,CHECK FURTHER
         CLI   SQFSW,SWF0               SEQFLD BEING PROCESSED  YM07522
         BE    LFTPAREN                 YES - TREAT AS PAREN    YM07522
         B     RTPAREN                  IF NOT,TREAT AS RGTH PAREN
LFTPAREN CLI   TOTALSW,X'F0'            IS LEFT PAREN EXPECTED
         BE    TOTLP                    YES - FOR TOTAL         YM07522
         CLI   SQFSW,SWF0               FOR SEQFLD              YM07522
         BNE   DELIMB                   NO - ERROR              YM07522
         MVI   SQFSW,SWF8               SET FOR COMMA NEXT      YM07522
         B     LPCONT                   CONTINUE                YM07522
TOTLP    MVI   TOTALSW,SWF8             SET FOR COMMA NEXT      YM07522
LPCONT   LA    GR1,D1(GR1)              UP POINTER TO NEXT CHAR YM07522
         B     KOMPAR3                  ANALYZE NEXT             A48723
RTPAREN  CLI   TOTALSW,X'FC'            IS RIGHT PAREN EXPECTED
         BE    TOTRP                    YES - FOR TOTAL         YM07522
         CLI   SQFSW,SWFC               FOR SEQFLD              YM07522
         BNE   KPASS                    NO -ERROR               YM07522
         MVI   SQFSW,SWFE               RIGHT PAREN FOUND       YM07522
         B     KOMPAR3                  CONTINUE                YM07522
TOTRP    MVI   TOTALSW,SWFE             RIGHT PAREN FOUND       YM07522
         B     KOMPAR3                  PREPARE TO RETURN        A48723
LENGTH0  OI    COL72LG,BIT01           SET LENGTH EQUAL TO ZERO
         B     DELIM                   CHECK DELIMITERS
SETCC2   OI    COL72LG,BIT20           SET DELIMITER IN LAST SWITCH
         TM    PARAMSW,BIT10           IS PARTIAL PARAMETER SWITCH ON
         BC    1,KTERBY                YES- LENGTH IS TOO LONG
TESTPAR  LTR   GR7,GR7                 CLEAR LENGTH REGISTER
         BC    8,LENGTH0               LENGTH EQUALS ZERO, SET SWITCH
         OI    COL72LG,BIT02           SET LENGTH GREATER THAN ZERO
DELIM    CLI   0(GR3),EQUAL            TEST IF DELIMITER IS AN EQUAL
         BE    KEY                     YES- GO TO KEY WORD ROUTINE
         CLI   0(GR3),COMMA            TEST FOR COMMA
         BE    PARAMC                  YES- GO TO PARAMETER ROUTINE
         CLI   0(GR3),BLANKCOL         TEST FOR BLANK
         BE    PARBLANK                YES- GO TO HANDLE BLANK PARAM
         TM    COL72LG,BIT20           IS DELIMITER IN END COLUMN
         BC    1,KSETP                 YES- SET PARAMETER FOLLOWS SW.
KINVAL   OI    SWITCHRD,BIT80          SET INVALID DELIMITER ERROR SW
         LA    GR7,1(GR7)              ADJUST LENGTH REGISTER
         LA    GR3,1(GR3)              ADJUST SCAN POINTER
         CLI   0(GR3),EQUAL            IS CHARACTER A DELIMITER
         BL    KINV                    YES- TWO DELIMITERS IN A ROW
         CR    GR3,GR5                 END COLUMN
         BL    KINVAL                  NO- SCAN SOME MORE
         TM    COL72LG,BIT08           IS COLUMN 72 BLANK
         BC    1,KTERBY                YES
         LA    GR7,1(GR7)              ADJUST LENGTH REGISTER
         B     KPTERR                  ESTABLISH PARAMETER
KINV     TM    PARAMSW,BIT04           IS COMMAND CONTROL SWITCH ON
         BC    1,KPCMD                 YES- ADJUST POINTER TO NEXT ONE
PRFOL    OI    PARAMSW,BIT20           SET PARAMETER FOLLOWS SWITCH
         B     KPASS                   GO TO RETURN
KBYPO    CLI   SWITCHRD+72,BLANKCOL    IS CONTINUATION COLUMN 72 BLANK
         BNE   KGTCD                   NO- GET ANOTHER CARD
         NI    SWITCHRD,BIT10          RESET PARTIAL PARAMETER SWITCH
         NI    PARAMSW,RESET0          RESET ENTRY CONTROL SWITCH
KGTCD    CLI   FIRSENTR,BIT10          VERY FIRST CARD
         BE    GOAHEAD                 YES- PAGE ALREADY SKIPPED
         CLI   COMDCD,X'0E'            IS THIS A COMMAND CARD
         BNE   GOAHEAD                 NO- JUST PRINT CARD
         CLI   FIRSTCON,X'0F'          FIRST PART OF CONTINUATION CARD
         BE    GOAHEAD                 NO- DO NOT SKIP PAGE
         MVI   FIRSTCON,X'0F'          SET AS FIRST CONTINUATION CARD
         LA    GR14,ONTOCC             RETURN ADDRESS FROM 'PRINTY'
         MVI   PRIMESW,X'0E'           FORCE PRINTING OF HEADER
         B     PRINTY                  PRINT FIRST CONTINUATION CARD
GOAHEAD  BAL   GR14,CARDPRNT           PRINT REST OF CONTINUATION CARD
ONTOCC   MVI   CCSCANSW,BIT01          SET SCAN ROUTINE READ SWITCH
         L     GR15,PROCESS            ADDRESS OF CSECT WITH READCC
         TM    PARAMSW,BIT10      IS PARTIAL PARAMETER SW ON?   YA01700
         BC    1,RETURN           YES,SET UP FOR RETURN         YA01700
         BR    GR15                    ENTER THE CSECT
RETURN   BALR  GR7,GR15           ENTER UPDT2 TO GET CONT. CARD YA01700
RESETCON MVI   CCSCANSW,RESET0         CLEAR CONTINUATION SWITCH
         B     KCONT                   ANALYSE CONTINUATION CARD
PARBLANK TM    PARAMSW,BIT04           IS COMMAND SWITCH ON
         BC    1,KPCMD                 YES-GO TO ADJUST POINTER
         OI    SWITCHRD,BIT02          SET PARAMETER PRESENT BIT
         OI    DELIMIN,BIT08           SET BLANK DELIMITER SWITCH
         TM    DELIMIN2,X'80'           WAS LAST A COMMAND WORD
         BC    1,DECID                  IF SO,CHECK SOME MORE
         CLI   SWITCHRD+72,BLANKCOL     IS COL 72 BLANK
         BE    SCANERR                  IF SO,ERROR
         TM    DELIMIN2,BIT40          WAS LAST DELIMITER BLANK    UL0H
         BC    14,SCANERR               IF NOT,ERROR
DECID    TM    COL72LG,BIT20           IS DELIMITER IN END COLUMN
         BC    1,KWENT                 YES- SET TO READ A CARD
         TM    COL72LG,BIT01           IS LENGTH EQUAL TO ZERO
         BC    1,KFORZRO               YES-SET UP FOR ZERO LENGTH
         OI    PARAMSW,BIT20           SET PARAMETER FOLLOWS SWITCH
         TM    DELIMIN,BIT08           IS DELIMITER A BLANK
         BC    8,KPASS                 NO- RETURN WITH POINTERS SET
         CLI   SWITCHRD+72,BLANKCOL    COL72 BLANK?            @ZA16462
         BE    KETBYP                  YES, BRANCH             @ZA16462
         OI    COL72LG,BIT80           SET COL72 NOTBLNK SW    @ZA16462
KETBYP   OI    SWITCHRD,BIT40          SET BYPASS SWITCH
         B     KPASS                   GET SET TO RETURN
KFORZRO  SR    GR1,GR1                 CLEAR SCAN REGISTER
         SR    GR7,GR7                 CLEAR LENGTH REGISTER
         TM    DELIMIN,BIT08           IS DELIMITER A BLANK
         BC    1,KETRDCD               GO TO SET READ CARD AND CONTINUE
         OI    PARAMSW,BIT20           SET PARAMETER FOLLOWS SWITCH
         TM    DELIMIN,BIT80           IS DELIMITER AN EQUAL SIGN
         BC    1,KPPAR                 YES- SET UP ERROR CONDITION
         B     KPASS                   RETURN
KETRDCD  OI    PARAMSW,X'48'           SET TO READ CARD AND CONTINUE
         TM    DELIMIN2,BIT40          IS LAST DELIMITER A COMMA
         BC    1,KGTCD                 YES- READ A CARD
         TM    COL72LG,BIT08           IS COLUMN 72 BLANK
         BC    8,KETBYP                NO- SET BYPASS SWITCH AND RETURN
         LA    GR7,SWITCHRD+16         CARD COLUMN 16
         CR    GR3,GR7                 IS SCAN ON COLUMN 16
         BE    KETBYP                  YES- SET BYPASS SWITCH
         TM    DELIMIN2,EQUAL          WAS LAST DELIMITER AN EQUAL
         BC    1,KETBYP                YES- SET BYPASS SWITCH
KTERBY   OI    SWITCHRD,BIT40          SET BYPASS SWITCH
KTER     OI    SWITCHRD,BIT80          SET ERROR SWITCH
         B     KPASS                   RETURN
KWENT    OI    PARAMSW,X'48'           SET TO READ CARD AND CONTINUE
         TM    COL72LG,BIT08           IS COLUMN 72 BLANK
         BC    1,KETCTBL               YES- SET BYPASS SWITCH AND CHECK
         TM    COL72LG,BIT01           IS LENGTH EQUAL TO ZERO
         BC    1,KZERO                 NO- RETURN
         TM    DELIMIN,BIT08           IS DELIMITER A BLANK
         BC    1,KETBYP                YES- SET BYPASS SWITCH, RETURN
         B     KPASS                   RETURN
KZERO    SR    GR7,GR7                 CLEAR LENGTH REGISTER
         TM    DELIMIN,BIT80           IS DELIMITER AN EQUAL
         BC    1,KTER                  YES- SET ERROR SWITCH
         TM    DELIMIN,BIT40           IS DELIMITER A COMMA
         BC    8,KGTCD                 NO- READ A CARD
         CLC   DELIMIN(1),DELIMIN2     IS DELIMITER SAME AS LAST
         BE    KPASS                   YES- RETURN
KETCTBL  OI    SWITCHRD,BIT40          SET BYPASS SWITCH
         TM    DELIMIN,BIT40           IS DELIMITER A COMMA
         BC    1,KTER                  YES- SET ERROR SWITCH
         B     KPASS                   RETURN
KEY      TM    DELIMIN2,BIT80     LAST DELIMITER ALSO AN EQUAL? YA01700
         BC    1,SCANERR          YES,BRANCH TO GIVE ERROR MSG  YA01700
         OI    SWITCHRD,BIT04     SET KEY WORD SWITCH           YA01700
         OI    DELIMIN,BIT80           SET DELIMITER SWITCH
         CLI   1(GR3),BLANKCOL    IS  A BLANK AFTER THE EQUAL    A27507
         BE    SCANERR    ERROR, NO KEYWORDS BEGIN WITH A BLANK  A27507
         B     DECID                   GO TO CHECK DELIMITER
PARAMC   CLI   TOTALSW,X'F8'            WAS A COMMA FOUND
         BE    PTOTC                    YES - FOR TOTAL         YM07522
         CLI   SQFSW,SWF8               WAS A COMMA FOUND       YM07522
         BNE   PARAMD                   NO - CONTINUE           YM07522
         MVI   SQFSW,SWFC               FLAG FOR SEQFLD         YM07522
         B     PARAMD                   CONTINUE                YM07522
PTOTC    MVI   TOTALSW,SWFC             IF SO,FLAG AS SUCH      YM07522
PARAMD   OI    SWITCHRD,BIT02           FLAG PARAMETER FOUND
         NI    PARAMSW,X'DF'           SET PARAM SW FOLLOW OFF   A48723
         OI    DELIMIN,BIT40           SET DELIMITER COMMA SWITCH
         TM    DELIMIN2,X'80'           LAST A COMMAND
         BC    14,SCANERR              IF NOT, CHECK SOME MORE     UL0H
         B     DECID                   GO TO CHECK DELIMITER
KPART    TM    PARAMSW,BIT10           IS PARTIAL PARAMETER SWITCH ON
         BC    1,KTERBY                YES-SET BYPASS AND ERROR SWITCH
         CLI   0(GR5),BLANKCOL         NO CONTINUATION CHAR      A48723
         BE    KPTERR                  NO CONTINUATION EXPECTED  A48723
CONTINU  TM    PARAMSW,BIT04           COMMAND SCANNING          A48723
         BC    1,SET                   YES SET BIT               A48723
         TM    SWITCHRD,BIT04          KEYWORD SCANNED ALREADY   A48723
         BC    1,KPTERR                YES,DO NOT SET KEYW SW    A48723
         OI    COL72LG,X'04'           SET BIT KEYWORD SCANNING  A48723
         B     KPTERR                  CONTINUE SCANNING         A48723
SET      OI    COL72LG,X'08'           SET BIT COMMAND SCANNING  A48723
KPTERR   OI    PARAMSW,BIT10           SET PARTIAL PARAMETER SWITCH
         TM    COL72LG,BIT08           IS COLUMN 72 BLANK
         BC    1,KLAST                 YES- GO TO SET PARAMETER BIT
         MVI   SAVEPAPR,BIT40          FLAG PARTIAL PARAMETER STORED
         MVC   SAVEPAPR+1(19),SAVEPAPR STORE PARTIAL PARAMETER   A48723
         LTR   GR7,GR7            IS LENGTH ZERO?               YA01700
         BZ    SKIPMOVE           YES,SKIP MOVING PART PARAM    YA01700
         BCTR  GR7,0                   SUBTRACT CONT. CHAR       A48723
         LR    GR10,GR7
         LTR   GR10,GR10          IS LENGTH ZERO?               YA01700
         BZ    SKIPMOVE           YES,SKIP MOVING PART PARAM    YA01700
         BCTR  GR10,0                  DECREMENT FOR EXECUTE
         EX    GR10,MOVEP              MOVE PARTIAL PARAMETER TO BUFFER
SKIPMOVE STC   GR7,SWITCHRD+82    SAVE LENGTH OF PART PARAM     YA01700
         OI    PARAMSW,BIT40           SET CONTINUE BIT
         TM    COL72LG,BIT04           IS COLUMN 72 BLANK
         BC    1,KGTCD                 NO- GET A CARD
         OI    SWITCHRD,BIT80          YES- FLAG AS ERROR
         B     KETBYP                  SET BYPASS SWITCH
KLAST    OI    SWITCHRD,BIT02          SET PARAMETER BIT
         B     KETBYP                  SET BYPASS BIT
KPPAR    SR    GR8,GR8                 CLEAR WORK REGISTER
         IC    GR8,SWITCHRD+82         LENGTH OF FIRST PART PARAMETER
         LTR   GR7,GR7                 IS LENGTH OF SECOND PART ZERO
         BC    8,KOMPL                 YES- THE PARAMETER IS COMPLETE
         LA    GR6,SAVEPAPR            NO- ADDRESS OF FIRST PART
         AR    GR6,GR8                 GET SAVED LENGTH          A48723
         LR    GR10,GR7
         BCTR  GR10,0                  DECREMENT FOR EXECUTE
         EX    GR10,MOVEPP             MOVE SECOND PART TO BUFFER
KOMPL    AR    GR7,GR8                 TOTAL LENGTH OF PARAMETER
         LA    GR1,SAVEPAPR            LOAD PARAMETER BUFFER ADDRESS
         NI    COL72LG,X'0C'           CLEAR LENGTH BITS, LEAVE'72' BIT
         OI    COL72LG,X'42'           SET LENGTH GREATER THAN ZERO
         TM    PARAMSW,BIT10      IS PARTIAL PARAMETER SW ON?   YA01700
         BC    1,AROUND           YES,SKIP DELIMITER SAVE       YA01700
         MVC   DELIMIN2(1),DELIMIN     SAVE DELIMITER SWITCH SETTINGS
         NI    DELIMIN,RESET0          CLEAR DELIMITER BITS
AROUND   NI    PARAMSW,X'EF'      RESET PART PARAMETER SWITCH   YA01700
         B     DELIM                   CHECK DELIMITERS
KPCMD    LA    GR3,1(GR3)              ADJUST POINTER TO NEXT COLUMN
         CLI   0(GR3),BLANKCOL         IS COLUMN BLANK
         BNE   KPCMA                   NO- CHECK FOR COMMA
         CR    GR5,GR3                 IS THIS THE LAST COLUMN
         BE    KNOPAR                  YES- NO PARAMETER FOLLOWS
         B     KPCMD                   CONTINUE SCAN
KPCMA    CLI   0(GR3),COMMA            IS COMMA THE FIRST CHARACTER
         BE    KNOPAR                  YES- NO PARAMETER FOLLOWS
         BCTR  GR3,0                   BACK UP REGISTER 3 TO PARAMETER
         B     PRFOL                   GO TO SET UP FOR PARAMETER
KSETP    OI    PARAMSW,BIT10           SET PARAMETER FOLLOWS SWITCH
         B     CONTINU                 GO TO SET PARTIAL PARAM   A48817
CARDPRNT MVI   ASISMSW,X'0F'           FORCE PRINTING OF CARD
PRINTY   MVC   LOGOUTAR+7(80),SWITCHRD+1  MOVE CARD TO BUFFER
         L     GR15,PRIMEWRT           ADDRESS OF MESSAGE ROUTINE
         BR    GR15                     PRINT IT
SCANERR  LA    GR11,SCANMSG             SCAN ERROR MSG
         CLI   PRINTED,X'0F'              WAS   RECORD PRINTED
         BE    SKIPPRNT                 IF SO DO NOT PRINT IT
         BAL   GR14,CARDPRNT            PRINT RECORD
SKIPPRNT L     GR15,PRIMEWRT            ADDRESS OF LOG ROUTINE
         MVI   PRINTED,X'00'            FLAG AS NOT PRINTED
         BALR  GR14,GR15                PRINT RECORD
         MVI   FLUSHSW,X'0F'            SET FLUSH SWITCH
         MVI   RIDIT2SW,X'00'           RESET SECOND PARM SWITCH
         L     GR15,PROCESS             ADDRESS OF IEBUPDT2
         BR    GR15                     PREPARE TO FLUSH
         EJECT
         SPACE 1
MOVEP    MVC   SAVEPAPR(1),0(GR1)      STORE PARTIAL PARAMETER
MOVEPP   MVC   0(1,GR6),0(GR1)         RECREATES PARAMETER
PROCESS  DC    V(IEBUPDT2)
PRIMEWRT DC    V(IEBUPLOG)
RRSAVE   DS    18F
DOTSLASH DC    C'./'                   CONTROL CARD INDICATION
PODSET   EQU   2
PSDSET   EQU   X'40'
         IEBUPCON
         END   IEBBSCAN
