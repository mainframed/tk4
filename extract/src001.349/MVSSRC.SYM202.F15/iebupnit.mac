***********************************************************************
*                   *****        I E B U P N I T       *****          *
* TITLE.  IEBUPNIT. INITIALIZATION CSECT                              *
*                                                                     *
* STATUS. CHANGE LEVEL 000                                            *
*                                                                     *
* FUNCTION/OPERATION.                                                 *
*        THIS CSECT (IEBUPNIT) WILL PERFORM THE FOLLOWING FUNCTIONS   *
*        IN THE ORDER INDICATED.                                      *
*                                                                     *
*        1.    INITIALIZE THE COMMUNICATION REGION IEBUPCON, THE      *
*              DCB'S AND THE ASSOCIATED EXIT LISTS.                   *
*                                                                     *
*        2.    ANALYZE THE INVOCATION OR EXECUTION PARAMETERS.        *
*                                                                     *
*        3.    OPEN THE INPUT DATA SET SYSIN.                         *
*                                                                     *
* ENTRY POINTS.                                                       *
*        ENTERED FROM IEBUPDTE AT 'IEBUPNIT'.                         *
*                                                                     *
* INPUT.                                                              *
*        THE INPUT PARAMETERS ASSOCIATED WITH THE INPUT DATA SET      *
*        SYSIN.                                                       *
*                                                                     *
* OUTPUT.                                                             *
*        CONTROL IS PASSED TO IEBUPLOG TO PRINT THE INITIAL HEADING,  *
*        THE INITIALIZED COMMUNICATION REGION IEBUPCON.               *
*                                                                     *
* EXITS-NORMAL-.                                                      *
*        1.    DCB AND USER HEADER LABEL EXITS ARE TAKEN WHEN THE     *
*              SYSIN DATA SET IS OPENNED.                             *
*                                                                     *
*        2.    CONTROL IS RETURNED TO IEBUPDTE AFTER SYSIN HAS BEEN   *
*              OPENNED.                                               *
*                                                                     *
* EXITS-ERROR-.                                                       *
*        IF AN ERROR OCCURRES DURING PARAMETER ANALYSIS OR OPEN A     *
*        MESSAGE IS GIVEN TO THE USER EXPLAINING THE ERROR AND A      *
*        RETURN TO THE OPERATING IS GIVEN WITH AN APPROPRIATE         *
*        RETURN CODE.                                                 *
*                                                                     *
*        RETURN CODES ISSUED BY THIS CSECT ARE:                       *
*              12   UNRECOVERABLE  ERROR-TERMINATION OF JOB.          *
*              16   ERROR IN LABEL HANDLING INDICATED BY USER-        *
*                   TERMINATION OF JOB.                               *
*                                                                     *
* EXTERNAL ROUTINES.                                                  *
*        IEBUPLOG- WHICH PRINTS HEADINGS AND ERROR MESSAGES.          *
*                                                                     *
*        IEBUPXIT- WHICH CONTAINS THE DCB AND USER HEADER LABEL       *
*              EXIT ROUTINES.                                         *
*                                                                     *
* TABLES/WORK AREAS.                                                  *
*        IEBUPCON- THE COMMUNICATION REGION.                          *
*                                                                     *
* ATTRIBUTES.                                                         *
*        SERIALLY REUSABLE AND BLOCK LOADS.                           *
*                                                                     *
***********************************************************************
IEBUPNIT CSECT
         EXTRN OMERROUT
         EXTRN NMERROUT
         EXTRN CTERROUT
         EXTRN OMEOD
         EXTRN EODCT
         EXTRN OMHDREXT
         EXTRN OMTRLEXT
         EXTRN OMDCBEX
         EXTRN NMHDREXT
         EXTRN NMTRLEXT
         EXTRN NMDCBEX
         EXTRN CTHDREXT
         EXTRN CTTRLEXT
         EXTRN CTDCBEX
         EJECT
*     SYMBOLIC REGISTER NOTATION   GR0 - GR15
GR0      EQU   0
GR1      EQU   1
GR2      EQU   2
GR3      EQU   3
GR4      EQU   4              REGISTER RESERVED FOR IEBUPXIT
GR5      EQU   5
GR6      EQU   6
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10
GR11     EQU   11
GR12     EQU   12             REGISTER RESERVED FOR IEBUPCON
GR13     EQU   13
GR14     EQU   14
GR15     EQU   15
         SPACE 1
         SAVE  (14,12)
         BALR  GR2,0
         USING *,GR2
         USING IEBUPCON,GR12           POINTER TO DSECT AREA
         L     GR4,IEBEXITS   ESTABLISH ADDRESSABILITY FOR
*                                     IEBUPXIT
         SPACE 1
         ST    GR4,36(13)    STORE FOR PERMANEMT USE FOR IEBUPXIT BASE
         LR    GR10,GR1
         ST    GR13,SAVE13X
         LA    GR13,SAVEREG            ADDRESS OF THIS CSECT SAVE AREA
         MVC   MAXSIZEL(2),EIGHTY      INITIALIZE RECORD SIZE (LRECL)
         MVI   LOGOUTAR,C' '
         MVC   LOGOUTAR+1(119),LOGOUTAR BLANKS INTO LOG OUT AREA
*
*    RESET SWITCHES AND WORK AREAS, SET FIRST ENTRY SWITCH
*
         LA    GR6,SIZETAB             SIZE OF SWITCH TABLE TO CLEAR
         MVC   PAGENO(4),ONEHEX        CLEAR PAGE NUMBER           3628
         EX    GR6,XCINSTR             RESET ALL SWITCHES TO ZERO
         MVI   FIRSENTR,BIT10          SET FIRST ENTRY SWITCH
         MVI   SOURCECD,X'FF'          INITIALIZE SOURCE BUFFER
         MVC   LRECL(2),MAXSIZEL       INITIALIZE LRECL TO 80 IN CASE
         MVI   LEVELXX,C'0'            INITIALIZE LEVEL BUFFER
         XC    DELTSEQ1(32),DELTSEQ1   CLEAR DELETE AND NUMBER SEQ
         XC    NMWRITEP(4),NMWRITEP  CLEAR NM BUFFER ADDRESS     PTM223
         XC    OMREADP(4),OMREADP  CLEAR OM BUFFER ADDRASS       PTM223
         LA    GR3,USERHDR             ADDRESS OF USER HEADER BUFFER
         ST    GR3,USRHDPTR            STORE POINTER IN COMMON AREA
         LA    GR5,USERTLR             ADDRESS OF USER TRAILER BUFFER
         ST    GR5,USRTRPTR            STORE POINTER IN COMMON AREA
         SPACE 1
*
*    ROUTINE TO INITIALIZE DCB'S FOR RE-USABILITY
* INCLUDING DDNAMES, SYSIN BLOCKSIZE, DATA SET TYPE AND USER TRLR EXIT
*
         MVC   SYSINNM(32),SYSINDC     MOVE DD NAMES TO BUFFER
         LA    GR11,DCBCTR             ADDRESS OF SYSIN DCB
         ST    GR11,DCBCT              POINTER TO SYSIN DCB
         MVC   BDDNAM(8,GR11),SYSINDC  MOVE DDNAME INTO DCB (SYSIN)
         MVC   BBLKSI(2,GR11),ZERO     CLEAR BLOCKSIZE IN DCB
         MVI   CTEXITS,X'00'            DEACTIVATE SYSIN HEADER EXT000I
         MVI   CTEXITS+4,X'00'          DEACTIVATE SYSIN TRALER EXT000I
         MVI   CTEXITS+8,X'85'          SYSIN DCB EXIT ALWAYS ACTVE000I
         LA    GR11,DCBOMR             ADDRESS OF SYSUT1 DCB
         ST    GR11,DCBOM              POINTER TO SYSUT1 DCB
         MVC   BBLKSI(2,GR11),ZERO     CLEAR BLOCKSIZE IN DCB
         MVC   BDSORG(2,GR11),ZERO     CLEAR DATA SET CHARACTERISTICS
         MVC   BLRECL(2,GR11),ZERO     CLEAR LOGICAL RECORD LENGTH DCB
         MVC   BDDNAM(8,GR11),SYSUT1DC MOVE DDNAME INTO DCB (SYSUT1)
         MVI   OMEXITS,X'01'            ACTIVATE SYSUT1 HEADER EXIT000I
         MVI   OMEXITS+4,X'03'          ACTIVATE SYSUT1 TRALER EXIT000I
         MVI   OMEXITS+12,X'85'        SYSUT1 DCB EXIT=ACTV/LAST   000I
         MVI   OMEXITS+16,X'07'        ACTVATE SYSUT1 JFCB EXIT    000I
         LA    GR11,DCBNMR             ADDRESS OF SYSUT2 DCB
         ST    GR11,DCBNM              POINTER TO SYSUT2 DCB
         MVC   BDSORG(2,GR11),ZERO     CLEAR DATA SET CHARACTERISTICS
         MVC   BLRECL(2,GR11),ZERO     CLEAR LOGICAL RECORD LENGTH DCB
         MVC   BBLKSI(2,GR11),ZERO     CLEAR BLOCKSIZE IN DCB
         MVC   BDDNAM(8,GR11),SYSUT2DC MOVE DDNAME INTO DCB (SYSUT2)
         MVI   NMEXITS,X'02'            ACTIVATE SYSUT2 HEADER EXIT000I
         MVI   NMEXITS+4,X'04'          ACTIVATE SYSUT2 TRALER EXIT000I
         MVI   NMEXITS+8,X'00'          DEACTIVATE TOTALING EXIT   000I
         MVI   NMEXITS+12,X'85'         ACTIVATE/LAST SYSUT2 DCB EX000I
         MVI   NMEXITS+16,X'07'         ACTIVATE SYSUT2 JFCB EXIT  000I
         L     GR5,0(GR10)             LOAD ADDRESS OF FIRST POINTER
         LTR   GR5,GR5                 TEST FOR MORE THAN 1 POINTER
         BM    OPEN1                   ONLY 1 POINTER
         L     GR5,4(GR10)             LOAD ADDRESS DDNAME LENGTH FIELD
         LH    GR6,0(GR5)              LOAD LENGTH OF LIST
         LTR   GR6,GR6                 TEST IF ANY DDNAMES
         BZ    OPEN1                   NO
*
*    ANALYZE INVOCATION PARAMETERS - DDNAMES
*
         ST    GR6,STOREG              STORE SIZE OF DDNAME LIST
         TM    STOREG+3,X'07'          TEST IF MULTIPLE OF 8
         BZ    IFANYDDS                YES - NAMES ARE 8 CHARACTERS
         MVI   NOT8SW,1                SET INVALID DDNAME SWITCH
         B     OPEN1                   OPEN OUTPUT LOG
*
IFANYDDS CLI   1(GR5),NOUPDD           MORE THAN 8 DDNAMES SPECIFIED
         BNH   OPEN1                   NO - NONE OVERRIDE UPDATE NAMES
         CLI   SYINDD(GR5),X'00'       IF SLOT IS ZERO- NO DD NAME
         BE    DDNAM2                  YES- DUMMY SLOT - CHECK NEXT ONE
         LA    GR11,DCBCTR             ADDRESS OF SYSIN DCB
         ST    GR11,DCBCT              POINTER TO SYSIN DCB
         MVC   BDDNAM(8,GR11),SYINDD(GR5)   OVERRIDE SYSIN NAME
         MVC   SYSINNM(8),SYINDD(GR5)    SAVE NAME OPEN WILL CLOBBER IT
DDNAM2   CLI   1(GR5),ONEDD            SECOND DDNAME
         BNH   OPEN1                   NO- PROCEED
         CLI   SYPRNDD(GR5),X'0'       IF SLOT ZERO - NO DDNAME
         BE    DDNAM3                  YES- DUMMY SLOT
         MVC   SYSPRNTN(8),SYPRNDD(GR5)  SAVE NAME OPEN WILL CLOBBER IT
DDNAM3   CLI   1(GR5),TWODD            ANOTHER DDNAME
         BNH   OPEN1                   NO - PROCEED
         CLI   SYUT1DD(GR5),X'0'       IS SLOT ZERO
         BE    DDNAM4                  YES - DUMMY SLOT
         LA    GR11,DCBOMR             ADDRESS OF SYSUT1 DCB
         ST    GR11,DCBOM              POINTER TO SYSUT1 DCB
         MVC   BDDNAM(8,GR11),SYUT1DD(GR5)  OVERRIDE SYSUT1 NAME
         MVC   SYSUT1NM(8),SYUT1DD(GR5)  SAVE NAME OPEN WILL CLOBBER IT
DDNAM4   CLI   1(GR5),THREEDD          ANOTHER DD NAME
         BNH   OPEN1                   NO - PROCEED
         CLI   SYUT2DD(GR5),X'0'       IS SLOT ZERO
         BE    OPEN1                   YES - DUMMY SLOT
         LA    GR11,DCBNMR             ADDRESS OF SYSUT2 DCB
         ST    GR11,DCBNM              POINTER TO SYSUT2 DCB
         MVC   BDDNAM(8,GR11),SYUT2DD(GR5)  OVERRIDE SYSUT2 NAME
         MVC   SYSUT2NM(8),SYUT2DD(GR5)  SAVE NAME OPEN WILL CLOBBER IT
OPEN1    L     GR5,0(GR10)             LOAD PARAMETER POINTER
         LTR   GR5,GR5                 TEST IF LAST POINTER
         BM    WRTEHEAD                ONLY OPTION PARAMETERS
         L     GR5,4(GR10)             TEST FOR DDNAME POINTER
         LTR   GR5,GR5                 IS IT LAST POINTER          3628
         BM    WRTEHEAD                LAST ACTIVE POINTER
         L     GR5,8(GR10)             LOAD ADDRESS OF PAGE NUMBER
         LH    GR6,0(GR5)              LOAD LENGTH OF PAGE NUMBER
         LTR   GR6,GR6                 TEST IF LENGTH IS ZERO
         BZ    WRTEHEAD                YES- PAGE NUMBER NOT SPECIFIED
         C     GR6,FOUR                COUNT GREATER THAN 4
         BNH   AROUND                  NO- DO NOT HAVE TO TRUNCATE
INVA     MVI   PAGESW,X'01'                                        3628
         B     WRTEHEAD                START WITH ONE              3628
AROUND   LA    GR7,PAGENO+4       LOAD RIGHTMOST ADDRESS OF PAGENO 3628
         SR    GR7,GR6                 ADJUST ADDRESS BY GIVEN AMOUNT
         BCTR  GR6,0                   DECREMENT FOR MVC           3628
MVPAGE   EX    GR6,MOVEPAGE            PUT PAGE NUMBER IN TITLE
WRTEHEAD MVI   PRIMESW,X'0F'           SET WRITE HEADER SWITCH-OPEN LOG
         L     GR15,PRIMEWRT           ADDRESS OF MESSAGE WRITER
         BALR  GR14,GR15               ENTER MESSAGE ROUTINE
         CLI   LOGBADSW,X'0F'          WAS LOG DCB OPENED
         BE    ANERROR                 NO- COULD NOT OPEN PRINTER DCB
         CLI   PAGESW,X'0'             WAS PAGE PARAMETER INVALID
         BE    CHECK8SW                NO- CONTINUE
         LA    GR11,PAGEMSG            ADDRESS OF ERROR MESSAGE
         LA    GR15,4                  SET UP CONDITION CODE
         BAL   GR14,SETCNCD            GO SET CONDITION CODE + MESSAGE
CHECK8SW CLI   NOT8SW,X'00'            WAS THERE AN ERROR IN DDNAMES
         BE    ANALPARM                NO- CONTINUE
         LA    GR11,DDNAMEMG           ADDRESS OF ERROR MESSAGE
ALMOSTZ  LA    GR15,12                 PUT CONDITION CODE INTO REGISTER
         LA    GR14,CLOSENOW           RETURN FROM MESSAGE ROUTINE
SETCNCD  CH    GR15,HICONCDE           COMPARE AGAINST HIGHEST CODE YET
         BNH   FINERRT                 PREVIOUS CODE HIGH OR EQUAL
         STH   GR15,HICONCDE           STORE NEW HIGHEST CODE
FINERRT  L     GR15,PRIMEWRT           ADDRESS OF MESSAGE WRITER
         BR    GR15                    ENTER MESSAGE ROUTINE RETURN SET
CLOSENOW MVI   QUITALL,X'0E'           FORCE CONDITION MESSAGE AND EOJ
         L     GR15,PRIMEWRT           ADDRESS OF MESSAGE WRITER
         BR    GR15                    EOJ- CLOSE DCBLOGR- NO RETURN
ANERROR  L     GR13,SAVE13             RESTORE ORIGINAL CONTENTS OF 13
         LA    GR15,12                 SET CONDITION CODE OF 12
         RETURN (14,12),T,RC=(15)
         SPACE 1
ANALPARM L     GR5,0(GR10)             LOAD PARAMETER POINTER
         LH    GR6,0(GR5)              LOAD PARAMETER COUNT
         LTR   GR6,GR6                 TEST FOR PARAMETERS
         BZ    OPENINPT                NO PARAMETERS- ASSUME MOD
         SR    GR8,GR8                 ZERO COUNT OF CHARACTER REGISTER
         LA    GR5,2(GR5)              UP TO ADDRESS OF PARAMETER FIELD
*                                                                     *
*   ANALYZE INPUT SPECIFICATIONS - CAN BE MOD OR NEW - DEFAULT IS MOD *
*                                                                     *
         CLI   0(GR5),COMMAEQ          IS COMMA FIRST CHARACTER
         BE    ASSUMMOD                YES- ASSUME DEFAULT OPTION MOD
         C     GR6,THREE               IS THERE ONLY ONE PARAMETER
         BE    INSPEC                  YES- ANALYZE INPUT SPECIFICATION
         CLI   3(GR5),COMMAEQ          IS 4TH CHARACTER A COMMA
         BNE   PMSG12                  NO- INVALID PARAMETER
INSPEC   CLC   0(3,GR5),NEW            IS PARAMETER 'NEW'
         BNE   GOON                    NO - CONTINUE TESTS
         MVI   CTONLYSW,X'01'          SET SWITCH ONLY NEW MASTER
         B     THRUINSP                DONE WITH PARAMETER ANALYSIS
ASSUMMOD LA    GR8,1(GR8)              UP COUNT OF CHARACTERS CHECKED
         LA    GR5,1(GR5)              ADJUST POINTER TO NEXT FIELD
         B     IFMORE                  TEST IF MORE FIELDS
GOON     CLC   0(3,GR5),MOD            IS PARAMETER 'MOD'
         BNE   PMSG12                  IS ERROR- GIVE MESSAGE, QUIT
THRUINSP LA    GR8,4(GR8)              INCREASE COUNT OF CHARACTERS
         LA    GR5,4(GR5)              ADJUST POINTER TO NEXT FIELD
IFMORE   CR    GR8,GR6                 MORE CHARACTERS TO ANALYZE
         BNL   OPENINPT                NO- COUNT IS FULFILLED
         ST    GR6,TEMPWA              SAVE COUNT OF CHARACTERS
*                                                                     *
*    TEST IF USER HEADER LABEL HANDLER SPECIFIED                      *
*                                                                     *
         BAL   GR3,GETNAME             GET SYSIN HEADER EXIT NAME
         L     GR6,DCBCT
         LA    GR3,CTHDR               ADDRESS OF CT HEADER NAME/AD000I
         LA    GR7,IFINTLR             RETURN FROM HEADER LABEL SETUP
         CLI   NONAMESW,X'01'          TEST IF NO NAME OR ERROR
         BE    NOINHDR                 SEE IF ERROR
SETUPHDR C     GR9,EIGHT               TEST IF LABEL EXCEEDS 8 BYTES
         MVC   0(8,GR3),BLANKS8        CLEAR NAME FIELD
         BH    TLRER                   ERROR IF MORE THAN 8
         BCTR  GR9,0                   DECREMENT FOR MVC COUNT
         EX    GR9,MVINHDEX            MOVE INHDR NAME TO BUFFER
         LOAD  EPLOC=(GR3)             LOAD ROUTINE BY NAME        000I
         ST    GR0,8(GR3)              STORE CORE ADDR AFTER NAME  000I
         MVI   CTHDRSW,X'0F'           FLAG AS CT HEADER RTN       000I
         MVI   CTEXITS,X'01'           ACTIVATE CT HEADER EXIT     000I
         BR    GR7                     RETURN
*                                                                     *
*    TEST IF USER TRAILER LABEL HANDLER SPECIFIED                     *
*                                                                     *
IFINTLR  BAL   GR3,GETNAME             GET INTLR EXIT NAME IF GIVEN
         L     GR6,DCBCT               ADDRESS OF SYSIN DCB
         LA    GR3,CTTRLR              ADDRESS OF CT TRALER NAME/AD000I
         LA    GR7,CHECKALL            RETURN ADDRESS
         CLI   NONAMESW,X'01'          TEST IF NO NAME OR ERROR
         BE    NOINTLR                 SEE IF ERROR
SETUPTLR C     GR9,EIGHT               TEST IF LABEL EXCEEDS 8 BYTES
         BH    TLRER                   YES- ERROR IN TRAILER NAME
         MVC   0(8,GR3),BLANKS8        CLEAR NAME FIELD
         BCTR  GR9,0                   DECREMENT FOR EXECUTE MVC
         EX    GR9,MVINHDEX            MOVE LABEL TO CORRECT BUFFER
         LOAD  EPLOC=(GR3)             LOAD ROUTINE BY NAME        000I
         ST    GR0,8(GR3)              STORE CORE ADDR AFTER NAME  000I
         MVI   CTTRLRSW,X'0F'          FLAG AS CT TRAILER ROUTINE  000I
         MVI   CTEXITS+4,X'03'         ACTIVATE CT TRALER EXIT     000I
         BR    GR7                     RETURN
*                                                                     *
*    ALL POSSIBLE FIELDS THRU - ARE THERE CHARACTERS NOT ANALYZED     *
*                                                                     *
CHECKALL C     GR8,TEMPWA              ANY MORE CHARACTERS TO ANALYSE
         BNL   OPENINPT                NO EVERYTHING ANALYSED
PMSG12   LA    GR11,BADPARMG           LOAD BAD PARAMETER MESSAGE
         B     ALMOSTZ                 NO NEED TO CONTINUE WILL NOT RUN
NOINHDR  CLI   ERRNAMSW,X'01'          TEST IF ERROR INHDR LABEL
         BNE   IFINTLR                 NO- GO TO INTLR ANALYSIS
         B     TLRER                   GIVE MESSAGE
NOINTLR  CLI   ERRNAMSW,X'01'          TEST IF ERROR INTLR LABEL
         BNE   CHECKALL                NO- GO TO LENGTH ANALYSIS
TLRER    LA    GR11,PLUS8MSG           ADDRESS OF ERROR MESSAGE
         B     ALMOSTZ                 GIVE MESSAGE THEN QUIT
*                                                                     *
*    SUBROUTINE TO GET THE EXIT NAME SPECIFIED                        *
*                                                                     *
GETNAME  MVI   NONAMESW,X'01'          SET SWITCH FOR NO NAME FOUND
         MVI   ERRNAMSW,X'00'          RESET ERROR SWITCH
         C     GR8,TEMPWA              HAVE ALL CHARACTERS BEEN CHECKED
         BCR   8,GR3                   YES- RETURN SHOULD NOT BE HERE
         CLI   0(GR5),COMMAEQ          IS FIRST CHARACTER A COMMA
         BNE   LDNAMLOC                NO- PREPARE TO RETURN NAME
RET1     LA    GR8,1(GR8)              INCREMENT CHARACTER COUNT
         LA    GR5,1(GR5)              INCREMENT PARAMETER POINTER
         BR    GR3                     RETURN
LDNAMLOC LR    GR10,GR5                LOAD ADDRESS OF NAME FIELD
         SR    GR9,GR9                 ZERO NAME SIZE REGISTER
         LA    GR15,CHCKNEXT           RETURN FROM VALIDITY CHECK
         TM    0(GR5),ALPHA            TEST IF FIRST CHARACTER A LETTER
         BO    TSLTGTNO                YES- CHECK SOME MORE
INVALCHR LA    GR11,PLUS8MSG           ADDRESS OF ERROR MESSAGE
         LR    GR7,GR15                SAVE RETURN REGISTER FOR NOW
         L     GR15,PRIMEWRT           ADDRESS OF MESSAGE WRITER
         BALR  GR14,GR15               GIVE MESSAGE
         LR    GR15,GR7                RETORE RETURN REGISTER
         MVI   ERRNAMSW,X'01'          SET ERROR FOUND SWITCH
         BR    GR15                    RETURN
TSLTGTNO TM    0(GR5),X'0F'            TEST IF LOW ORDER FOUR BITS ZERO
         BZ    INVALCHR                IF SO INVALID
         TM    0(GR5),NUMERIC          FIRST CHARACTER CANNOT BE NUMBER
         BO    INVALCHR                IF SO INVALID
         MVI   NONAMESW,X'00'          CLEAR SWITCH VALID NAME FOUND
         BR    GR15                    RETURN
CHCKNEXT LA    GR5,1(GR5)              INCREMENT CHARACTER ADDRESS
         LA    GR8,1(GR8)              INCREMENT TOTAL ANALYZED COUNT
         LA    GR9,1(GR9)              INCREMENT NAME CHARACTER COUNT
         C     GR8,TEMPWA              TOTAL COUNT ANALYSED TEST
         BCR   8,GR3                   IF COUNT FULFILLED RETURN
         CLI   0(GR5),COMMAEQ          DELIMITER COMMA AFTER 1ST NAME
         BE    RET1                    YES- RETURN
         B     CHCKNEXT                KEEP SCANNING
OPENINPT L     GR13,SAVE13X       LOAD RETURN SAVE AREA ADDRESS    000I
         LM    14,12,12(13)
         BR    14
*
*    OLD MASTER AND NEW MASTER AND SYSIN DCB EXIT ROUTINES
*
         DS    0F
OMEXITS  DC    X'01'
         DC    AL3(OMHDREXT)
         DC    X'03'
         DC    AL3(OMTRLEXT)
         DC    A(0)
         DC    X'85'
         DC    AL3(OMDCBEX)
         DC    X'07'
         DC    AL3(0)
NMEXITS  DC    X'02'
         DC    AL3(NMHDREXT)
         DC    X'04'
         DC    AL3(NMTRLEXT)
         DC    X'00'
         DC    AL3(0)
         DC    X'85'
         DC    AL3(NMDCBEX)
         DC    X'07'
         DC    AL3(0)
CTEXITS  DC    X'00'
         DC    AL3(CTHDREXT)
         DC    X'00'
         DC    AL3(CTTRLEXT)
         DC    X'85'
         DC    AL3(CTDCBEX)
         EJECT
*   SYSIN DCB   CONTROL CARD INPUT     REFERRED TO AS CT
         SPACE 1
DCBCTR   DCB   DSORG=PS,MACRF=(GL),DDNAME=SYSIN,RECFM=FB,LRECL=80, 000H*
               EODAD=EODCT,EXLST=CTEXITS,SYNAD=CTERROUT,BUFNO=1    000H
         EJECT
*   SYSUT1 DCB  OLD MASTER             REFERRED TO AS OM
         SPACE 1
DCBOMR   DCB   DSORG=PO,MACRF=R,DDNAME=SYSUT1,NCP=1,               2039C
               EXLST=OMEXITS,EODAD=OMEOD,SYNAD=OMERROUT
         EJECT
*   SYSUT2 DCB  NEW MASTER             REFERRED TO AS NM
         SPACE 1
DCBNMR   DCB   DSORG=PO,MACRF=W,DDNAME=SYSUT2,NCP=1,               2039C
               EXLST=NMEXITS,SYNAD=NMERROUT
         SPACE 1
         EJECT
         SPACE 1
         SPACE 1
         SPACE 1
*   CONSTANTS FOR INITIALIZATION ROUTINE
ZERO     DC    D'0'                    BUFFER OF ZEROS
THREE    DC    F'3'
FOUR     DC    F'4'
EIGHT    DC    F'8'
***   WARNING-- KEEP THE 4 NAMES BELOW IN THIS ORDER
**               SYSIN, SYSPRINT, SYSUT1, SYSUT2                     **
SYSINDC  DC    CL8'SYSIN'
SYSPRNDC DC    CL8'SYSPRINT'
SYSUT1DC DC    CL8'SYSUT1'
SYSUT2DC DC    CL8'SYSUT2'
         SPACE 1
USERHDR  DC    CL8' '                  NAME OF USER HEADER LABEL EXIT
USERTLR  DC    CL8' '                  NAME OF USERTRAILER LABEL EXIT
ZEROHEX  DC    C'00000000'             DOUBLE WORD HEX ZEROS
ONEHEX   DC    C'0001'                 DEFAULT PAGE NUMBER         3628
BLANKS8  DC    CL8' '              DOUBLE WORD OF BLANKS
SAVE13X  DS    F
SAVEREG  DS    18F                     SAVE AREA USED BY READ/WRITE
         SPACE 3
         DS    0D
DSECTLOC DC    F'0'                    COMMON AREA ADDRESS JUST IN CASE
SAVE14   DC    F'0'                    SAVE RETURN ADDRESS FOR OPEN
         SPACE 1
PRIMEWRT DC    V(IEBUPLOG)             ENTRY POINT OF LOG ROUTINE
IEBEXITS  DC    V(IEBUPXIT)
         SPACE 1
*
*                INSTRUCTIONS INVOKED BY EXECUTES
*
MVINHDEX MVC   0(1,GR3),0(GR10)        MOVE HEADER OR TRAILER TO BUFFER
MOVEPAGE MVC   0(1,GR7),2(GR5)         MOVE IN PAGE NUMBER
XCINSTR  XC    STARTAB(1),STARTAB      CLEARS UP TO 256 BYTES SWITCHES
         SPACE 1
EIGHTY   DC    H'80'                   DEFAULT LRECL
NEW      DC    C'NEW'                  TEST FOR PARM=NEW
CTDC     DC    C'CT'                   IDENTIFIES SYSIN IN MESSAGE
MOD      DC    C'MOD'                  PARM SETTING FOR MODIFY
SIXTEEN  DC    H'16'                   CONSTANT FOR HIGHEST ERROR CODE
NOT8SW   DC    X'00'                   SET IF INPUT NAME IN ERROR
PAGESW   DC    X'00'                   SET IF PAGE NUMBER IN ERROR
NONAMESW DC    X'00'                   SET IF NO NAME OR ERROR
BIT01    EQU   X'01'
BIT10    EQU   X'10'
         EJECT
         IEBUPCON
         EJECT
DDNAMSCT DSECT
DDLENGTH DS    CL2
SYSLINDD DS    CL8
MEMBERDD DS    CL8
SYSLMODD DS    CL8
SYSLIBDD DS    CL8
SYSINDD  DS    CL8
SYSPRNDD DS    CL8
SYSPUNDD DS    CL8
SYSUT1DD DS    CL8
SYSUT2DD DS    CL8
SYSUT3DD DS    CL8
         SPACE 3
         DCBD  DSORG=PS
*                             INSERT  EQUATE  STATEMENTS
NOUPDD   EQU   X'20'
ONEDD    EQU   X'28'
TWODD    EQU   X'38'
THREEDD  EQU   X'40'
COMMAEQ  EQU   X'6B'
ALPHA    EQU   X'C0'
NUMERIC  EQU   X'F0'               BITS 1111 0000
BDDNAM   EQU   DCBDDNAM-IHADCB
BBLKSI   EQU   DCBBLKSI-IHADCB
BDSORG   EQU   DCBDSORG-IHADCB
BLRECL   EQU   DCBLRECL-IHADCB         LOGICAL RECORD LENGTH FIELD
SYUT1DD  EQU   SYSUT1DD-DDNAMSCT
SYUT2DD  EQU   SYSUT2DD-DDNAMSCT
SYPRNDD  EQU   SYSPRNDD-DDNAMSCT
SYINDD   EQU   SYSINDD-DDNAMSCT
         END   IEBUPNIT
