IEBUPXIT CSECT
*A145500-146632,536500,536600                                   YA00060
*D140000-141000                                                 YA00060
* 107000-108000,110000-112600,113800-115000,115700-118000,127000 A27716
* 501100-501700                                                  A34107
* 183000                                                         A36139
*A196900,210400                                                  A41838
*D200000                                                         A41838
*C209000                                                         A41838
*C285500                                                         A41838
*A116600                                                         A48819
*A100500                                                        YA01669
*C101000                                                        YA01669
*A158500                                                       @ZA01687
*A 158100                                                      @ZA04376
*A094200-094600,156100-156400,543500    @XA20587,@YA19906,(ORG)@ZA28493
*C156000                                @XA20587,@YA19906,(ORG)@ZA28493
 SPACE 4
***********************************************************************
*                  *****         I E B U P X I T       *****          *
* STATUS. CHANGE LEVEL 000.                                           *
*                                                                     *
* FUNCTION/OPERATION.                                                 *
*        THIS CSECT CONTAINS ALL ASYNCHRONOUS DCB EXIT SUBROUTINES    *
*        FOR  SYSIN,SYSUT1 AND SYSUT2. THIS CSECT CONTAINS THE        *
*        FOLLOWING EXIT ROUTINES FOR EACH DATA SET.                   *
*                                                                     *
*        1.    DCB EXIT ROUTINE.                                      *
*        2.    SYNAD ROUTINE.                                         *
*        3.    EODAD ROUTINE.      *** EXCEPT SYSUT2 ***              *
*        4.    USER HEADER LABEL ROUTINE.                             *
*        5.    USER TRAILER LABEL ROUTINE.                            *
*                                                                     *
* ENTRY POINTS.                                                       *
*        ENTERED FROM QSAM OR BSAM AT ANY ONE OF 14 POINTS.           *
*                                                                     *
*        OLD MASTER (SYSUT1) DATA SET ENTRY POINTS ARE:               *
*        1.    OMDCBEX                                                *
*        2.    OMERROUT;                                              *
*        3.    OMEOD;                                                 *
*        4.    OMHDREXT;                                              *
*        5.    OMTRLEXT.                                              *
*                                                                     *
*        NEW MASTER (SYSUT2) DATA SET ENTRY POINTS ARE:               *
*        1.    NMDCBEX;                                               *
*        2.    NMERROUT;                                              *
*        3.    NMHDREXT;                                              *
*        4.    NMTRLEXT.                                              *
*                                                                     *
*        SYSIN DATA SET ENTRY POINTS ARE:                             *
*        1.    CTDCBEX;                                               *
*        2.    CTERROUT;                                              *
*        3.    EODCT;                                                 *
*        4.    CTHDREXT;                                              *
*        5.    CTTRLEXT.                                              *
*                                                                     *
***********************************************************************
 EJECT
         SPACE 4
***********************************************************************
*                                                                     *
*        THE FOLLOWING NAMES ARE ENTRY POINTS INTO THIS MODULE        *
*                                                                     *
***********************************************************************
         ENTRY      OMHDREXT                                          *
         ENTRY      OMTRLEXT                                          *
         ENTRY      OMDCBEX                                           *
         ENTRY      OMERROUT                                          *
         ENTRY      OMEOD                                             *
         ENTRY      NMHDREXT                                          *
         ENTRY      NMTRLEXT                                          *
         ENTRY      NMDCBEX                                           *
         ENTRY      NMERROUT                                          *
         ENTRY      CTHDREXT                                          *
         ENTRY      CTTRLEXT                                          *
         ENTRY      CTDCBEX                                           *
         ENTRY      CTERROUT                                          *
         ENTRY      EODCT                                             *
         ENTRY     SPSYNAD                                       A27716
***********************************************************************
         SPACE 4
***********************************************************************
*                                                                     *
*        SYMBOLIC REGISTER NOTATION     GR0--GR15                     *
*                                                                     *
***********************************************************************
GR0      EQU   0                                                      *
GR1      EQU   1                                                      *
GR2      EQU   2                                                      *
GR3      EQU   3                                                      *
GR4      EQU   4         BASE REGISTER FOR THIS CSECT                 *
GR5      EQU   5                                                      *
GR6      EQU   6                                                      *
GR7      EQU   7                                                      *
GR8      EQU   8                                                      *
GR9      EQU   9                                                      *
GR10     EQU   10                                                     *
GR11     EQU   11                                                     *
GR12     EQU   12        BASE REGISTER FOR IEBUPCON                   *
GR13     EQU   13                                                     *
GR14     EQU   14                                                     *
GR15     EQU   15                                                     *
***********************************************************************
         EJECT
 SPACE 2
***********************************************************************
*                   *****         W A R N I N G        *****          *
*        REGISTER  4 CONTAINS THE BASE ADDRESS OF THIS MODULE.        *
*        REGISTER  12 CONTAINS THE BASE ADDRESS OF IEBUPCON.          *
*        NO CSECT  SHOULD ALTER THE CONTENTS OF EITHER REGISTER       *
*                                                                     *
***********************************************************************
 SPACE 4
         USING *,GR4
         USING IEBUPCON,GR12
         B     EODCT                    BRANCH AROUND CONSTANT @ZA28493
         DC    CL8'IEBUPXIT'            MODULE NAME            @ZA28493
         DC    CL8'-OZ28493'            LAST MODIFICATION      @ZA28493
***********************************************************************
*        END OF DATA ROUTINES FOR SYSIN AND SYSUT1 DATA SETS          *
***********************************************************************
EODCT    MVI   EODCTSW,X'0F'            SET EOF ON SYSIN  SWITCH
         MVI   LABEL2SW,X'00'          RESET LABEL PROCESSING SWITCH
         B     ENTRNEXT                 INFORM NEXT CSECT OF EOF
OMEOD    MVI   OMEND,X'0F'              SET EOF ON SYSUT1 SWITCH
         MVI   REPROSW,X'00'       RESET REPRO SWITCH AT        YA01669
*                                  REPRO END                    YA01669
ENTRNEXT L     GR15,PROCESS
         BR    GR15                     ENTER NEXT STAGE- NO RETURN
         SPACE 2
***********************************************************************
*        THE FOLLOWING ARE SYNAD ERROR ROUTINES FOR SYSIN,            *
*        SYSUT1, SYSUT2 AND SYSPRINT DCB'S.  THEY PRINT ERROR         *
*        MESSAGES PRODUCED DYNAMICALLY BY THE SYNADAF ROUTINE         *
***********************************************************************
CTERROUT STM   GR14,GR1,SYNSAVER       SAVE SYNAD PARAMETERS     A27716
         SYNADAF    ACSMETH=QSAM                                 A27716
SYNAD2   L     GR7,MACROMSG    ADDRESS OF AREA FOR SYNADAF MSG   A27716
         MVC   0(78,GR7),50(GR1)                                 A34086
SYNAD3   LA    GR11,IOERR              NUMBER OF I/O ERROR MSG   A27716
         MVI   SYNADSW,X'0F'                                     A27716
         B     ALMOSTZ                                           A27716
NMERROUT STM   GR14,GR1,SYNSAVER       SAVE SYNAD PARAMETERS     A27716
         SYNADAF    ACSMETH=BSAM                                 A27716
         L     GR7,MACROMSG     ADDRESS OF AREA FOR SYNAD MSG    A27716
         MVC   0(78,GR7),50(GR1)                                 A27716
         SYNADRLS                                                A27716
         LM    GR14,GR1,SYNSAVER       RESTORE SYNAD PARAMETERS  A27716
         CLI   TOTALSW,X'FE'           IS TOTALING EXIT ACTIVE   A27716
         BNL   LASTOTAL                 IF SO,ENTER FOR LAST TIME  000I
         B     SYNAD3                                            A27716
LASTOTAL ST    GR0,TOTLPARM+8           SAVE STATUS INFO ADDRESS   000I
         MVI   TOTLPARM+8,X'80'         FLAG AS I/O ERROR          000I
         LA    GR1,TOTLPARM             ADDRESS OF PARMLIST
         ST    GR9,TOTLPARM             BUFFER ADDRESS OF I/O ERROR000I
         L     GR15,TOTALADR            USER TOTAL ROUTINE ADDR    000I
         BALR  GR14,GR15                GIVE ROUTINE CONTROL       000I
         B     SYNAD3                                            A27716
OMERROUT STM   GR14,GR1,SYNSAVER       SAVE SYNAD PARAMETERS     A27716
         SYNADAF    ACSMETH=BSAM                                 A27716
         B     SYNAD2                                            A27716
SPSYNAD  STM   GR14,GR1,SYNSAVER       SAVE SYNAD PARAMETERS     A27716
         LA    GR15,12            GET RETURN CODE FOR THIS ERROR A48819
         CH    GR15,HICONCDE      IS THIS THE HIGHEST SO FAR     A48819
         BNH   SPSYNAD1           NO.. DON'T CHANGE              A48819
         STH   GR15,HICONCDE      SAVE HIGEST SO FAR             A48819
SPSYNAD1 EQU   *                                                 A48819
         SYNADAF    ACSMETH=QSAM                                 A27716
         MVC   WTOMSG1+23(25),50(GR1)                            A27716
         MVC   WTOMSG2+12(53),75(GR1)                            A27716
         SYNADRLS                                                A27716
         LA    GR1,WTOMSG1                                       A27716
         WTO   MF=(E,(1))                                        A27716
         LA    GR1,WTOMSG2                                       A27716
         WTO   MF=(E,(1))                                        A27716
         LM    GR14,GR1,SYNSAVER       RESTORE SYNAD PARAMETERS  A27716
         L     GR15,LOGFINSH                                     A27716
         BR    GR15                                              A27716
         SPACE 2
ALMOSTZ  LA    GR15,12                  PUT RETURN CODE INTO REGISTER
         LA    GR14,CLOSENOW            RETURN HERE FROM MSG ROUTINE
SETCNCD  CH    GR15,HICONCDE            COMPARE AGAINST HIGHEST CODE
         BNH   FINERRT                  PREVIOUS CODE HIGH OR EQUAL
         STH   GR15,HICONCDE            STORE NEW HIGHEST CODE
FINERRT  L     GR15,PRIMEWRT            ADDRESS OF MESSAGE WRITER
         BR    GR15
CLOSENOW L     GR2,CLOSEALL                                      A27716
         BALR  GR3,GR2                                           A27716
         MVI   QUITALL,X'0E'  FORCE CONDITION MSG AND EOJ MSG    A27716
         L     GR15,PRIMEWRT
         BR    GR15                     ENTER MESSAGE ROUTINE
         SPACE 4
         SPACE 2
***********************************************************************
*        DCB EXIT ROUTINES FOR SYSIN, SYSUT1 AND SYSUT2               *
***********************************************************************
         SPACE 2
OMDCBEX  L     GR11,DCBOM               ADDRESS OF OLD MASTER DCB
         LA    GR7,OMBLOCKP             BLOCKING FACTOR BUFFER OM
         LA    GR6,OMBLKERR             INCORRECT BLOCKSIZE SWITCH
CHKSIZE  LH    GR9,BBLKSI(GR11)         PICK UP BLOCKSIZE FROM DCB
         SR    GR8,GR8                  SET BLOCKING FACTOR
         LH    GR3,BLRECL(GR11)         OBTAIN LOGICAL RECORD LENGTH
         LTR   GR3,GR3                  IS LOGICAL RECORD LENGTH ZERO
         BZ    NOSIZE                   YES-ASSUME 80
         TM    BRECFM(GR11),X'80'       RECFM FIXED, UNDEFINED  YA00060
         BO    NOTVAR                   YES, MAXSIZE IS 80      YA00060
         TM    BRECFM(GR11),X'40'       RECORD VARIABLE         YA00060
         BZ    NOTVAR                   NO, NOT VARIABLE, BRNCH YA00060
         CH    GR3,MAXSIZEV             VARIABLE LONGER THAN 84 YA00060
         BH    NOSIZE                   PLUG IN DEFAULTS        YA00060
         LTR   GR9,GR9                  IS BLOCKSIZE ZEROS      YA00060
         BZ    NOSIZE                   YES, PICK UP DEFAULTS   YA00060
         STH   GR3,LRECL                SAVE RECORD LENGTH      YA00060
         STH   GR9,FLBLK                SAVE ORIGINAL BLOCKSIZE YA00060
         STH   GR9,SHRTBLK              FOR UPDATE-INPLACE      YA00060
         SH    GR9,VAR4                 DECREASE BY 4           YA00060
         DR    GR8,GR3                  OBTAIN BLOCKING FACTOR  YA00060
         STH   GR9,0(GR7)               BLOCKING FACTOR         YA00060
         STH   GR9,0(GR7)               LOGICAL RECORD COUNT    YA00060
         LTR   GR8,GR8                  ANY REMAINDER           YA00060
         BZ    RRRET                    BLOCKSIZE-LRECL OK      YA00060
         B     NOSIZE                   CANNOT HANDLE           YA00060
NOTVAR   LTR   GR9,GR9                  IS BLOCKSIZE ZERO       YA00060
         BZ    NOSIZE                   YES, SET ERROR FLAG     YA00060
         CH    GR3,MAXSIZEL             MAXIMUM RECORD SIZE EXCEEDED
         BH    NOSIZE                   YES-ASSUME 80
         STH   GR3,LRECL                SAVE RECORD LENGTH
         STH   GR9,FLBLK                SAVE ORIGINAL BLOCKSIZE IN CASE
         STH   GR9,SHRTBLK              UPDATE=INPLACE IS SPECIFIED
         DR    GR8,GR3                  OBTAIN BLOCKING FACTOR
         STH   GR9,0(GR7)               BLOCKING FACTOR
         STH   GR9,2(GR7)               LOGICAL RECORD COUNT
         LTR   GR8,GR8                  ANY REMAINDER
         BZ    RRRET                    NO-RETURN BLOCKSIZE-LRECL OK
NOSIZE   TM    0(GR6),NMOLD             TESTING AN OLD NM D/S? @ZA28493
         BZ    SETDEF                   NO,SET DEFAULT VALUES  @ZA28493
         OI    0(GR6),X'01'             YES,INDICATE ERROR     @ZA28493
         B     RETRNS                   RETURN.GIVE MSG + QUIT @ZA28493
SETDEF   MVI   0(GR6),X'01'             BLKSIZE OR LRECL ERROR @ZA28493
         MVC   BBLKSI(2,GR11),EIGHTY    MAKE BLOCKSIZE 80
         MVC   BLRECL(2,GR11),EIGHTY    MAKE LRECL 80
         MVI   BRECFM(GR11),FIXED       SET RECFM TO FIXED     @ZA04376
         B     CHKSIZE                                         @ZA01687
RRRET    TM    BRECFM(GR11),X'40'       RECFM V OR U             A48750
         BO    DEFAUL                   YES,DEFAULT RECFM TO F/B A48750
         TM    BRECFM(GR11),X'80'       RECFM IS FIXED           A48750
         BO    RETRNS                   YES,RETURN               A48750
DEFAUL   LH    GR3,BBLKSI(GR11)         BLOCKSIZE IN DCB         A48750
         MVI   BRECFM(GR11),FIXED       SET RECFM TO FIXED       A48750
         CH    GR3,LRECL                BLOCKSIZE AND LRECL EQUAL
         BE    RETRNS                   YES,FIXED RECORDS DCB SET UP OK
         MVI   BRECFM(GR11),FIXEDBLK    SET RECFM TO FIXED BLOCK A48750
RETRNS   RETURN
NMDCBEX  L     GR11,DCBNM               ADDRESS OF NEW MASTER DCB
         LA    GR7,NMBLOCKP             BLOCKING FACTOR BUFFER
         LA    GR6,NMBLKERR             INCORRECT BLOCKSIZE SWITCH
         B     CHKSIZE                  CHECK BLOCKING FACTOR
CTDCBEX  L     GR11,DCBCT               ADDRESS OF SYSIN DCB
         USING IHADCB,GR11
         LH    GR7,DCBBLKSI             LOAD BLOCKSIZE
         LTR   GR7,GR7                  CHECK FOR MULTIPLE OF 80
         BC    8,SETBLOK                IF NO BLKSIZ GIVEN,INSERT 80
         SR    GR6,GR6                  ZERO OUT REMAINDER REGISTER
         LA    GR5,80                   BLOCK SIZE MUST BE A
         DR    GR6,GR5                  MULTIPLE OF 80
         LTR   GR6,GR6                  IS BLKSIZ A MULTIPLE OF 80
         BCR   8,GR14                   IF SO,RETURN
         MVI   BLOKSW,X'10'             OTHERWISE SET FLAG
SETBLOK  MVI   DCBBLKSI+1,X'50'         ISNERT BLOCKSIZE
         BR    GR14                     RETURN
         DROP  GR11
         EJECT
         SPACE 2
***********************************************************************
*        USER LABEL HEADER EXITS FOR SYSIN                            *
***********************************************************************
CTHDREXT STM   GR0,GR14,LABLSAVE       SAVE ENTRY REGISTERS
         LA    GR13,USERSAVE            SAVE AREA
         CLI   IOBADSW,X'0F'            I/O ERROR
         BE    TERMZER0                 IF SO,TERMINATE
         CLI   EOVSWTCH,X'0F'           FIRST ENTRY TO EXIT ROUTINE
         BE    CTENTER                  IF NOT,DO NOT INITCONSTANTS
         MVI   EOVSWTCH,X'0F'           SET FIRST ENTRY SWITCH   A41838
         MVC   USERRTN(12),CTHDR        MOVE USER ROUTINE NAME/ADDR
         MVI   USERRTN+8,X'04'         FLAG AS HEADER ROUTINE      000H
CTCOMMON MVI   USERCONT,X'00'           RESET NO. OF LABELS COUNT
         BAL   GR14,PRNTENTM           PRNT 'USER ROUTINE PROCESS'
CTENTER  BAL   GR14,COMONLBL           PASS CONTROL TO USER RUTINE
         LM    GR0,GR14,LABLSAVE
         BR    GR14                     RETURN
         SPACE 3
CTTRLEXT STM   GR0,GR14,LABLSAVE       SAVE ENTRY REGISTERS
         LA    GR13,USERSAVE            SAVE AREA
         CLI   IOBADSW,X'0F'            I/O ERROR
         BE    TERMZER0                IF SO, TERMINATE            UL0H
         CLI   EOVSWTCH,X'FF'           FIRST ENTRY TO TRL. RTN  A41838
         BE    CTENTER                  IF NOT,DO NOT INIT. CONS
         MVI   EOVSWTCH,X'FF'           SET SW. FIRST TRAILER    A41838
         MVC   USERRTN(12),CTTRLR       MOVE USER ROUTINE NAME/ADDR
         MVI   USERRTN+8,X'08'         FLAG AS TRAILER ROUTINE     000H
         B     CTCOMMON                 INITIALIZE CONSTANTS.
PRNT#LBL LA   GR11,LBLENTMS
         L     GR15,PRIMEWRT            LOG ROUTINE
         BR    GR15                     PRINT MESSAGE
PRNTENTM LA    GR11,USRLBLMS            LOAD MESSAGE CONSTANT.
         L     GR15,PRIMEWRT            ADDRESS OF IEBUPLOG
         BR    GR15                     PRINT MESSAGE,RETURN TO
*                                       CALLER VIA REG.14
   SPACE 4
***********************************************************************
*        USER LABEL SUBROUTINES USED BY ALL LABEL EXIT ROUTINES.      *
*        THIS ROUTINE GIVES CONTROL TO USER ROUTINES AND  CHECKS      *
*        FOR MOST TERMINATING CONDITIONS.                             *
***********************************************************************
COMONLBL STM   GR14,GR1,TEMPSAVE        SAVE LINKAGE REGISTERS.
         L     GR15,USERRTN+8           GET ADDR OF USER LBL RUTINE
         BALR  GR14,GR15                GIVE CONTROL TO USER RUTINE
         STC   GR15,LBLRETCD            STORE USER RETURN CODE.
         L     GR1,TEMPSAVE+12          PICK UP POINTER TO PARM LIS
         CLI   8(GR1),X'80'             I/O ERROR?
         BE    IOERROR                  IF SO,PRINT MSG AND RETURN
         IC    GR8,USERCONT            LOAD NO. LABELS PROCESSED
         LA    GR8,1(GR8)              ADD ONE TO THAT COUNT
         STC   GR8,USERCONT            AND RESTORE IT FOR REFERENC
         CLI   LBLRETCD,X'10'          IS USER RETURN CODE=16?
         BE    RETCDE16                 IF SO,PRINT MSG & LET RC=0.
         CLI   LBLRETCD,X'00'          IS USER RETURN CODE=0?
         BE    RETCDEND                 IF SO,PRINT MSG&RETURN.
         CLI   USERCONT,X'08'           HAVE EIGHT LABELS BEEN PROC
         BE    RETCDEND                 IF SO,TREAT AS RC=0.
         CLI   LBLRETCD,X'04'          IS RETURN CODE EQU TO 4     000H
         BE    RETRNLBL                IF SO, IT IS VALID          000H
         CLI   LBLRETCD,X'08'          IS RETURN CODE EQU TO 4     000H
         BE    RETRNLBL                IF SO, IT IS VALID          000H
         CLI   LBLRETCD,X'0C'          IS RETURN CODE EQU TO 4     000H
         BE    RETRNLBL                IF SO, IT IS VALID          000H
         B     RETCDEND                ELSE IT DEFAULTS TO ZERO    000H
RETRNLBL L     GR14,TEMPSAVE            LOAD CALLERS RETURN ADDRESS
         BR    GR14                     RETURN TO CALLER
         SPACE
IOERROR MVI   IOBADSW,X'0F'
         LA    GR11,LBLERMSG            LOAD I/O ERROR MSG CON.
         L     GR15,PRIMEWRT            ADDRESS OF IEBUPLOG.
         BALR  GR14,GR15                PRINT I/O ERROR MSG
         B     RETRNLBL                 PREPARE TO RETURN
RETCDE16 SR    GR15,GR15                FORCE RETURN CODE TO ZERO.
RETCDEND STC   GR15,TEMPSAVE+4          SAVE RETURN CODE.
         LA    GR11,LSTRCMSG            LOAD LAST RET CD MSGCON.
         L     GR15,PRIMEWRT            ADDRESS OF IEBUPLOG.
         BALR  GR14,GR15                PRINT LAST RET CD MSG.
         LA    GR11,LBLENTMS            LOAD NO.OF ENTRANCES MSGCON
         L     GR15,PRIMEWRT            ADDRESS OF IEBUPLOG.
         BALR  GR14,GR15                PRINT NO. ENTR. MSG.
         IC    GR15,TEMPSAVE+4          RELOAD RETURN CODE.
         B     RETRNLBL                 PREPARE TO RETURN.
   SPACE 2
***********************************************************************
*        HEADER LABEL EXIT ROUTINE ON SYSUT1 UPDATE=INPLACE           *
*        IS REQUESTED.
***********************************************************************
UPDATEXT CLI   OMOPENSW,X'0F'          ENTERED DURING OPEN
         BNE   OMENTER                 IF NOT,PASS CONTROL DIRECT UUL0H
         CLC   0(4,GR1),ZERO            IS THERE A LABEL
         BE    OMENTERX                IF NOT,PASS CONTROL DIRECT  UL0H
         L     GR3,0(GR1)               LOAD POINTER TO USER LABEL
         CLI   3(GR3),C'1'              IS IT FIRST OF LABEL GROUP
         BE    INITDOM                  IF SO,INITIALIZE POINTERS
MVDOMLBL L     GR9,OMLBLPTR             LOAD ADDR OF LABEL SAVE ARA
         IC    GR5,OMLBLCNT            PICK UP NO. OF LABELS PROCES
         LA    GR5,1(GR5)              ADD ONE TO THE COUNT
         STC   GR5,OMLBLCNT            STORE IN IN COMMUN REGION
         ST    GR9,UPDTPARM             ADDR OF LABEL IN PARM FELD
         MVC   UPDTPARM+16(4),0(GR1)    MOVE LABEL PTR TO PARMS
         MVC   UPDTPARM+4(12),4(GR1)    MOVE PARMS TO UPDATE PARMS
         LA    GR9,80(GR9)              ADDR IN OMLBLPTR NOW POINTS
         ST    GR9,OMLBLPTR             TO NEXT AVAILABLE LABEL
         CLI   OMHDRSW,X'0F'            IS THERE A USER HEADER RTN
         BE    OMENTER                 IF NOT,PASS CONTROL DIRECT  UL0H
         LM    GR0,GR14,LABLSAVE
         LA    GR15,12                  LOAD RET CD TO PROCESS NEXT
         BR    GR14                     RETURN CONTROL TO OPEN
INITDOM  LA    GR5,OMLABELS             LOAD START ADDR OF SAVE BUF
         ST    GR5,OMLBLPTR             INIT SAVE AREA POINTER
         MVI   USERCONT,X'00'           INIT NO. ENTRANCES COUNT
         MVI   OMLBLCNT,X'00'           INIT NO. LABELS PROCESSED
         MVI   OMSYSNLB,X'F0'          SET LABELS TO BE COPIED SW  000H
         CLI   LABELSW,X'10'           ARE THERE LABELS IN INPUT   000H
         BNE   OMHDRDMP                IF NOT,SWITCH IS PROPERLY ST000H
         MVI   OMSYSNLB,X'0F'          SET TO ACCEPT LABELS IN SYSM000H
         MVC   OMLBLPTR(4),LABELBUF    MOVE GETMAIN POINTER        000H
         MVC   OMLBLCNT(1),LABELCNT    MOVE LABEL COUNT TO COUNTER 000H
OMHDRDMP CLI   OMHDRSW,X'0F'           IS THERE A USER ROUTINE     000H
         BNE   MVDOMLBL                 IF NOT, PREPARE TO SAVE LBL
         BAL   GR14,PRNTENTM        PRINT USER RTN PROCESS MSG   A41838
         B     MVDOMLBL                 PREPARE TO SAVE LABELS
***********************************************************************
*        OLD MASTER (SYSUT1) USER HEADER LABEL ROUTINE.               *
***********************************************************************
OMHDREXT STM   GR0,GR14,LABLSAVE       SAVE ENTRY REGISTERS
         LA    GR13,USERSAVE
         L     GR3,0(GR1)              ADDRESS OF LABEL BUFFER
         CLI   IOBADSW,X'0F'           I/O ERROR DURING LABEL PROC UL0H
         BE    TERMZER0                IF SO, THEN TERMINATE       UL0H
         MVC   USERRTN(12),OMHDR        MOVE USER ROUTINE NAME/ADDR
         MVI   USERRTN+8,X'04'         FLAG AS HEADER ROUTINE      000H
         CLI   INPLCSW,X'0F'            OPEN FOR UPDAT?
         BE    UPDATEXT                 IF SO,GO TO UPDATE EXIT.
         CLI   OMOPENSW,X'0F'           ENTERED DURING OPEN?
         BNE   OMENTER                  IF NOT,PASS CONTROL DIRECT
*                                       TO USER LABEL ROUTINE
         CLC   0(4,GR1),ZERO            IS THERE A LABEL?
         BE    OMENTERX                IF NONE, CHECK FOR USER ROUTUL0H
         CLI   3(GR3),C'1'             IS THIS THE FIRST LABEL     UL0H
         BE    INITOM                   IF SO,INITIALIZE POINTERS
MVEOMLBL CLI   LABELSW,X'10'           LABELS IN INPUT STREAM
         BE    LABLUSER                THEN DO NOT SAVE LABELS
         L     GR9,OMLBLPTR            ADDRESS OF OM LABEL SAVE AREA
         IC    GR5,OMLBLCNT
         LA    GR5,1(GR5)
         STC   GR5,OMLBLCNT
         MVC   0(80,GR9),0(GR3)        MOVE LABEL TO SAVE AREA
         LA    GR9,80(GR9)             ADDR IN OMLBLPTR NOW POINTS
         ST    GR9,OMLBLPTR            TO NEXT AVAILABLE SLOT
LABLUSER  CLI   OMHDRSW,X'0F'          IS THERE A USER ROUTINE
         BE    OMENTER                  IF SO,PASS CONTROL DIRECTLY
         LA    GR15,4                   LOAD RET CD TO PROCESS NEXT
         LM    GR0,GR14,LABLSAVE
         BR    GR14                     RETURN CONTROL TO OPEN
         SPACE
INITOM   LA    GR5,OMLABELS             LOAD START ADDR OF SAVE BUF
         ST    GR5,OMLBLPTR             INITIALIZE SAVE AREA POINT
         MVI   USERCONT,X'00'           INIT NO. ENTRANCE CONSTANT
         MVI   OMLBLCNT,X'00'           INIT NO. LABELS PROCESSED
         MVI   OMSYSNLB,X'F0'          FLAG FOR LABELS IN OM       UL0H
         CLI   LABELSW,X'10'           ANY LABELS IN INPUT STREAM  UL0H
         BNE   OMHDRCMP                IF NOT FLAGS ARE SET        UL0H
         MVI   OMSYSNLB,X'0F'          FLAGAS LABELS IN INPUT      UL0H
OMHDRCMP CLI   OMHDRSW,X'0F'           IS THERE A USER ROUTINE     UL0H
         BNE   MVEOMLBL                 IF NOT,PREPARE TO SAVE A LB
         BAL   GR14,PRNTENTM           PRINT USER RTN PROCESS MSG
         B     MVEOMLBL                 PREPARE TO SAVE LABEL.
         SPACE 2
OMENTERX BAL   GR14,TERMZER2           PRINT USER ROUTINE MESSAGE  UL0H
         CLI   OMHDRSW,X'0F'           IS THERE A USER ROUTINE     UL0H
          BNE   OMENTER                GIVE CONTROL TO USER        UL0H
         BAL   GR14,PRNTENTM           PRINT USER PROCEE MSG       UL0H
OMENTER CLI   LBLRETCD,X'10'           IS USER RETURN CODE =16?
         BNE   OMENTER1                 IF SO,SHOULD TERMINATE NO.
TERMZER0 LA    GR15,0                  LOAD TERNINATE RETURN CODE.
         LM    GR0,GR14,LABLSAVE
         BR    GR14                     RETURN TO OPEN
         SPACE
OMENTER1 CLI   OMHDRSW,X'0F'            IS THERE A USER ROUTINE?
         BNE   TERMZER0                 IF NOT,TERMINATE LABEL PROC
         BAL   GR14,COMONLBL            PASS CONTROL TO USER ROUTIN
OMCOMMON CLC   0(4,GR1),ZERO                                       000H
         BNE   RETOM                   IF SO,IT MAY NOT BE THE LAST000H
         LA    GR14,RETOM              PICK UP ADDRESS OF EXIT CODE000H
         ST    GR14,TEMPSAVE           SAVE ADDRESS IN COMMON'S AREA00H
         LA    GR15,0                  CODE IS SET FOR LAST ENTRY  A00H
         B     RETCDEND                GO TO MESSAGE ROUTINES      000H
RETOM    LM    GR0,GR14,LABLSAVE                                   000H
         BR    GR14                                                000H
         SPACE 3
***********************************************************************
*        OLD MASTER (SYSUT1) USER TRAILER LABEL ROUTINE.              *
***********************************************************************
OMTRLEXT STM   GR0,GR14,LABLSAVE       SAVE ENTRY REGISTERS
         LA    GR13,USERSAVE
         L     GR3,0(GR1)              ADDRESS OF LABEL BUFFER
         CLI   IOBADSW,X'0F'           I/O ERROR DURING LABEL PROC UL0H
         BE    TERMZER0                IF SO, THEN TERMINATE       UL0H
         MVC   USERRTN(12),OMTRLR       MOVE USER ROUTINE NAME/ADDR
         MVI   USERRTN+8,X'08'         FLAG AS TRAILER ROUTINE     000H
         CLC   0(4,GR1),ZERO           ARE TERE ANY LABELS
         BE    OMENT1                  IF NONE, CHECK FOR USER     UL0H
         CLI   3(GR3),C'1'              IS IT THE 1ST LABEL
         BE    INITOMTR                 IF SO,INITIALIZE CONSTANTS
MVEOMTRL L     GR9,OMLBLPTR            LOAD ADDR OF LABEL SAVE AREA
         IC    GR5,OMLBLCNT
         LA    GR5,1(GR5)
         STC   GR5,OMLBLCNT
         MVC   0(80,GR9),0(GR3)        MOVE LABEL TO SAVE AREA
         LA    GR9,80(GR9)             ADDR IN OMLBLPTR NOW POINTS
         ST    GR9,OMLBLPTR            TO NEXT AVAILABLE SLOT
         CLI   OMTRLRSW,X'0F'           IS THERE A USER HEADER RTN?
         BE    OMENTTRL                 IF SO,PASS CONTROL DIRECTLY
         LA    GR15,4                   LOAD RET CD TO PROCESS NEXT
         LM    GR0,GR14,LABLSAVE
         BR    GR14                     RETURN CONTROL TO EOV.
INITOMTR LA    GR5,OMLABELS             LOAD START ADDR OF SAVE BUF
         ST    GR5,OMLBLPTR             INITIALIZE SAVE AREA POINTR
         MVI   USERCONT,X'0'            INIT NO. ENTRANCE CONSTANT
         MVI   OMLBLCNT,X'0'            INIT # LABELS PROCESSED
         MVI   NMSYSNLB,X'F0'          FLAG AS COPY OLD MASTER LABEUL0H
         CLI   LABELSW,X'20'           ARE THERE INPUT LABELS TO PRUL0H
         BNE   OMTRLCMP                THEN FLAGS ARE SET          UL0H
         MVI   NMSYSNLB,X'0F'          FLAG AS LABELS IN INPUT     UL0H
OMTRLCMP CLI   OMTRLRSW,X'0F'          IS THERE A USER ROTINE      UL0H
         BNE   MVEOMTRL                IF NOT, COPY OM LABELS      UL0H
         BAL   GR14,PRNTENTM           PRINT USER HEADER MESSAGE   UL0H
         B     MVEOMTRL                PREPARE TO GENERATE LABELS  UL0H
OMENT1  BAL    GR14,TERMZER2           GIVE NO LABELS MESSAGE      UL0H
         CLI   OMTRLRSW,X'0F'          IS THERE A USER ROUTINE     UL0H
         BNE   OMENTTRL                IF NOT PREPARE TO RETURN    UL0H
         BAL   GR14,PRNTENTM           I PRINT USER ENETER MESS    UL0H
OMENTTRL CLI   OMTRLRSW,X'0F'           IS THERE A USER ROUTINE?
         BNE   TERMZER0                 IF NOT TERMINATE LABEL PROC
         BAL   GR14,COMONLBL            PASS CONTROL TO USER ROUTIN
         B     OMCOMMON                PREPARE TO RETURN TO SYSTEM 000H
         SPACE 2
***********************************************************************
*        NEW MASTER (SYSUT2) USER HEADER LABEL ROUTINE.               *
***********************************************************************
NMHDREXT STM   GR0,GR14,LABLSAVE       SAVE ENTRY REGISTERS
         LA    GR13,USERSAVE
         L     GR3,0(GR1)              ADDRESS OF LABEL BUFFER
         CLI   IOBADSW,X'0F'           I/O ERROR DURING LABEL PROC UL0H
         BE    TERMZER0                IF SO, THEN TERMINATE       UL0H
         MVC   USERRTN(12),NMHDR        MOVE USER RTN NAME/ADDR
         MVI   USERRTN+8,X'0C'         FLAG AS TRAEADER OUTINE     000H
         CLI   NMOPNSWT,X'0F'           ENTERED DURING OPEN?
         BL    NMVOL2HD                HANDLING MULTIVOLUME D.S.   UL0H
         CLI   OMSYSNLB,X'00'           HAS A LABLE BEEN SAVED TO
*                                       OUTPUT TO NEW MASTER
         BE    NMENTERP                PREPARE TO ENTER USER ROUTINUL0H
         CLI   NMOPNSWT,X'1F'           IS THIS THE FIRST ENTRY?
         BE    NMINIT                   IF SO,INITIALIZE POINTERS.
MVENMLBL L     GR9,OMLBLPTR            LOAD ADDR OF LABEL SAVE AREA
         MVC   0(80,GR3),0(GR9)        MOVE SAVED LABEL TO BUFFER
         LA    GR9,80(GR9)             ADDR IN OMLBLPTR NOW POINTS
         ST    GR9,OMLBLPTR            TO NEXT AVAILABLE SLOT
         SR    GR8,GR8                 PREPARE FOR LTR INSTRUCTION 000H
         IC    GR8,OMLBLCNT            LOAD NO. LABELS TO BE PROCES
         BCTR  GR8,0                   DECREMENT COUNT BY ONE
         STC   GR8,OMLBLCNT            STORE NO LABELS REMAINING
         CLI   NMHDRSW,X'0F'            IS THERE A USER ROUTINE?
         BE    NMENTERS                IF SO,CHECK FOR LABEL GENERATION
         LTR   GR8,GR8                 HAS LAST LABEL BEEN GENERATD
         LA    GR15,4             MEANS× PUT THIS LABEL,LAST RETURN000H
         BE    NMRETN             PREPARE TO RETURN FOR LAST TIME  000H
         LA    GR15,8                   RET CODE=CONT.PROCESSLABELS
         LM    GR0,GR14,LABLSAVE
         BR    GR14                     RETURN TO OPEN ROUTINE
NMENTERP CLI   NMHDRSW,X'0F'
         BNE   NMENTERQ                IF NOT PREPARE TO RETURN    000H
         CLI   NMOPNSWT,X'1F'          IS THIS THE FIRST ENTRY     000H
         BNE   NMENTER                 IF SO, PRINT HEADER MESSAGE 000H
         BAL   GR14,PRNTENTM           IF SO,GO TO PRINT ROUTINR   000H
         MVI   NMOPNSWT,X'0F'          SET ALREADY ENTERED SWITCH  000H
         B     NMENTER                 THEN GIVE HIS ROUTINE CNTRL 000H
NMENTERQ CLI   NMOPNSWT,X'1F'          IS THIS ENTRY DUE TO OPEN   000H
         BNE   TERMZER0                RETURN WITH CC=0            UL0H
         B     TERMZER1                PRINT NO LABELS EXIST MSG   UL0H
NMVOL2HD CLI   VOLSWTCH,X'04'          IS THIS FIRST ENTRY         UL0H
         BE    NMENTER                 IF NOT,GIVE CONTROL TO USER UL0H
         CLI   NMHDRSW,X'0F'           IS THERE A USER ROUTINE     UL0H
         BNE   TERMZER0                IF NOT RETURN TO D.M.       UL0H
         MVI   VOLSWTCH,X'04'          SET INITIALIZED SWITCH      UL0H
NMVOLCOM BAL   GR14,PRNTENTM           PRINT ROUTINE HAS BEEN ENTERUL0H
         MVI   USERCONT,X'00'          RESET NO. OF ENTRANCES SWTCHUL0H
         B     NMENTER                 PREPARE TO GIVE CONTROL     UL0H
TERMZER1 LA    GR11,NOULCRTM           PRINT NO USER LABELS EXIST  UL0H
         LA    GR14,TERMZER0           RETURN TO SET CC=0          UL0H
         L     GR15,PRIMEWRT           ADDRESS OF LOG MODULE       UL0H
         BR    GR15                    PRINT MESSAGE               UL0H
TERMZER2 LA    GR11,NOULXSTM           PRINT NO USER LABELS EXIST MUL0H
         L     GR15,PRIMEWRT           PRINT ROUTINE ADDRESS      MUL0H
         BR    GR15                    GO PRINT MESSAGE    S      MUL0H
NMRET04  LA    GR15,4                   RET CODE=LAST LABEL TO PROC
         MVC    TEMPSAVE(4),LABLSAVE+56
         B     RETCDEND                 PRINT MESSAGES AND RETURN
NMRET04X LA    GR15,4
         MVC   TEMPSAVE(4),LABLSAVE+56
         B     RETRNLBL                PREPARE TO RETURM           UL0H
NMINIT   MVI   NMOPNSWT,X'0F'           RESET FIRST ENTRY PORTION
         MVI   USERCONT,X'00'           RESET NO. ENTRANCES COUNT
         LA    GR5,OMLABELS
         ST    GR5,OMLBLPTR
         CLI   OMSYSNLB,X'0F'          ANY LABELS SAVED FROM INPUT UL0H
         BNE   NMUSER                  IF NOT TEST FOR USER ROUTINEUL0H
         MVC   OMLBLCNT(1),LABELCNT    MOVE NO. OF LABELS SAVED    UL0H
NMUSER   CLI   NMHDRSW,X'0F'            IS THERE A USERLABEL ROUTIN
         BNE   MVENMLBL                 IF NOT,BEGIN PROCESSING LBL
         BAL   GR14,PRNTENTM           PRNT 'USER ROUTINE PROCESS'
         B     MVENMLBL                 BEGIN PROCESSING LABELS.
NMENTERS LTR   GR8,GR8                 ANY MORE LABELS TO CREATE
         BNE   NMENTER                 IF MORE, GIVE CONTROL DIRECT
         MVI   OMSYSNLB,X'00'          IF NO MORE, FLAG AS NO MORE
         B     NMENTER                 THEN ENTER USER ROUTINE
NMTNTERS LTR    GR8,GR8                ANY MORE LABELS TO CREATE
         BNE   NMENTER                 IF MORE,DO NOT FLAG AS LAST
         MVI   NMSYSNLB,X'00'          IF NO MORE,FLAG AS NONE SAVED
         B     NMENTER                 THEN ENTER USER ROUTINE
NMENTER  CLI   LBLRETCD,X'10'          IS USER RETURN CODE=16?
         BE    TERMZER0                 IF SO, PREPARE TO TERMINATE
         BAL   GR14,COMONLBL            PASS CONTROL TO USER RUTINE
         CLI   LBLRETCD,X'04'           IS RETURN CODE,EQUAL TO 4
         BE    NMRET04                  IF SO,PRINT TERMINATION MSG
NMRETN   LM    GR0,GR14,LABLSAVE
         BR    GR14                    RETURN CONTROL TO SYSTEM    UL0H
         SPACE
***********************************************************************
*        NEW MASTER (SYSUT2) USER TRAILER LABEL  ROUTINE              *
***********************************************************************
NMTRLEXT STM   GR0,GR14,LABLSAVE       SAVE ENTRY REGISTERS
         LA    GR13,USERSAVE
         L     GR3,0(GR1)              ADDRESS OF LABEL BUFFER
         CLI   IOBADSW,X'0F'           I/O ERROR DURING LABEL PROC UL0H
         BE    TERMZER0                IF SO, THEN TERMINATE       UL0H
         MVC   USERRTN(12),NMTRLR       MOVE USER RTN NAME/ADDR
         MVI   USERRTN+8,X'10'         FLAG AS TRAILERR OUTINE     000H
         CLI   NMCLSESW,X'0F'           ENTERED DURING OPEN?
         BL    NMVOL2TR                CHECK FOR USER ROUTINE      UL0H
         CLI   NMSYSNLB,X'00'           HAS A LABEL BEEN SAVED?
         BE    NMTENTRP                PREPARE TO RETURN TO USER   UL0H
         CLI   NMCLSESW,X'1F'           IS THIS THE FIRST ENTRY?
         BE    NMTINIT                  IF SO,INITIALIZE POINTERS
MVTNMLBL L     GR9,OMLBLPTR
         MVC   0(80,GR3),0(GR9)        MOVE SAVED LABEL TO BUFFER
         LA    GR9,80(GR9)              OMLBLPTR NOW POINTS TO NEXT
         ST    GR9,OMLBLPTR             LABEL TO BE PROCESSED.
         SR    GR8,GR8                 PREPARE FOR LTR INSTRUCTION 000H
         IC    GR8,OMLBLCNT             LOAD NO. LABELS TO PROCESS
         BCTR  GR8,0                    DECREMENT THAT COUNT BY ONE
         STC   GR8,OMLBLCNT             STORE NO. OF LABELS REMAING
         CLI   NMTRLRSW,X'0F'           IS THERE A USER ROUTINE?
         BE    NMTNTERS                IF SO CHECK FOR LABEL GENERATION
         LTR   GR8,GR8                  HAS LAST LABEL BEEN CREATED
         LA    GR15,4             MEANS× PUT THIS LABEL,LAST RETURN000H
         BE    NMRETN             PREPARE TO RETURN FOR LAST TIME  000H
         LA    GR15,8                   RET CD= CONT PROCESS LABLES
         B     NMRETN             RETURN                         A34107
NMTENTRP CLI   NMTRLRSW,X'0F'
         BNE   NMTENTRQ                IF NOT,PREPARE TO RETURN    000H
         CLI   NMCLSESW,X'1F'           IS THIS THE FIRST ENTRY    000H
         BNE   NMENTER                 GIVE CONTROL TO USER ROUTINE
         BAL   GR14,PRNTENTM           IF SO,GO OT PRINT ROUTINE   000H
         MVI   NMCLSESW,X'0F'          SET ALREADY ENETERED SWITCH 000H
         B     NMENTER                 GIVE CONTROL TO USER ROUTINE
NMTENTRQ CLI   NMCLSESW,X'1F'          IS THIS ENTRY DUE TO CLOSE  000H
         BNE   TERMZER0                RETURN A CC=0               UL0H
         B     TERMZER1                PRINT NO LABELS EXIST MSG   UL0H
NMVOL2TR CLI   VOLSWTCH,X'08'          FIRST ENTRY TO THIS ROUTINE UL0H
         BE    NMENTER                 IF NOT,GO DIRECT TO USER
         CLI   NMTRLRSW,X'0F'          IS THERE A USER ROUTINE     UL0H
         BNE   TERMZER0                TERMINATE THIS LABEL PROCESSUL0H
         MVI   VOLSWTCH,X'08'          SET AS ALREADY ENTERED      UL0H
         B     NMVOLCOM                COMMON VOLUME SWTCH RTN     UL0H
NMTINIT  MVI   NMCLSESW,X'0F'           RESET FIRST ENTRY PORTION  000I
         MVI   USERCONT,X'00'           RESET NO. ENTRANCES COUNT  000I
         LA    GR5,OMLABELS
         ST    GR5,OMLBLPTR
         CLI   NMSYSNLB,X'0F'           CHECK FOR SYSIN LABELS     000I
         BNE   NMTUSER                  IF NONE,CHECK FOR USER RTN 000I
         MVC   OMLBLPTR(4),LABELBUF     MOVE ADDR OF SAVED LABELS  000I
         MVC   OMLBLCNT(1),LABELCNT     MOVE NO.OF LABELS TO COUNTR000I
NMTUSER  CLI   NMTRLRSW,X'0F'           IS THERE A USER LBL ROUTINE000I
         BNE   MVTNMLBL                 IF NOT,BEGIN PROCESS LABELS000I
         BAL   GR14,PRNTENTM            PRNT 'USER RUTINE NOW PROC'000I
         B     MVTNMLBL                 BEGIN PROCESSING LABELS    000I
PROCESS  DC    V(IEBUPDT2)              ENTRY POINT OF MAIN PROCESSOR
PRIMEWRT DC    V(IEBUPLOG)              ENTRY POINT OF LOG ROUTINE
CLOSEALL DC    V(DEACTV)  ENTRY PT IN IEBUPDT2 TO CLOSE SYSUT1,  A27716
*                                      SYSUT2, AND SYSIN         A27716
MACROMSG DC    V(SYNADMSG)   ADDRESS OF SYNAD ERROR MSG IN IEBUPLOG
*                                                                A27716
SYNSAVER DS    4F                                                A27716
LOGFINSH DC    V(CLOSEPRT)             ENTRY PT IN IEBUPLOG TO CLOSE
*                                      SYSPRINT AND RETURN       A27716
WTOMSG1  WTO   'IEB802I  I/O ERROR                            ',MF=L,  X
               ROUTCDE=11,DESC=7                                 A27716
WTOMSG2  WTO   'IEB802I                                                X
                                   ',MF=L,ROUTCDE=11,DESC=7      A27716
ZERO     DC    F'0'
EIGHTY   DC    H'80'
MAXSIZEV DC    H'84'                                            YA00060
VAR4     DC    H'4'                                             YA00060
UPDTPARM DS    5F
VOLSWTCH DC    X'00'                   USED TO INDICATE MULTIVOLUMEUL0H
*                                      PROCESSING FOR LABEL ROUTINEUL0H
LABLSAVE DS    18F
USERSAVE DS    18F
TEMPSAVE DS    4F
BIT80    EQU   X'80'
FIXED    EQU   X'80'
FIXEDBLK EQU   X'90'
NMOLD    EQU   X'80'              DISP=OLD IND. IN INTERNAL SW @ZA28493
         IEBUPCON
         DCBD  DSORG=PS
BLRECL   EQU   DCBLRECL-IHADCB
BBLKSI   EQU   DCBBLKSI-IHADCB
BRECFM   EQU   DCBRECFM-IHADCB
         END   IEBUPXIT
