* /* START OF SPECIFICATIONS ***                                      *
*01* PROCESSOR = ASSEM;                                               *
**** END OF SPECIFICATIONS ***/                                       *
UPD2     TITLE 'IEBUPDTE PROCESSING CSECT- IEBUPDT2'
*TITLE: IEBUPDT2-IEBUPDTE MAIN PROCESSING CSECT                    1550
*                                                                     *
*STATUS- CHANGE LEVEL 001                                             *
*                                                                     *
*FUNCTION/OPERATION- THIS MODULE IS ABLE TO CREATE AND UPDATE         *
*   SYMBOLIC LIBRARIES WHICH ARE EITHER PHYSICAL SEQUENTIAL OR        *
*   PARTITIONED DATA SETS CONTAINED ON DIRECT ACCESS OR TAPE VOLUMES. *
*                                                                     *
*   THIS MODULE HANDLES FIXED OR FIXED-BLOCKED RECORDS. IT HAS THE    *
*   ABITLITY TO HANDLE RECORD OF LOGICAL RECORD LENGTHS UP TO EIGHTY  *
*   BYTES. IF BLOCKSIZE AND/OR LOGICAL RECORD LENGTH ARE NOT GIVEN    *
*   BY OPEN TIME, UNBLOCKED RECORDS OF 80 BYTES IS ASSUMED AND A      *
*   WARNING MESSAGE IS GIVEN ON THE LOG OUT DEVICE.                   *
*                                                                     *
*   THIS MODULE CAN-                                                  *
*                                                                     *
*   1. CREATE A NEW PARTITIONED OR PHYSICAL SEQUENTIAL DATA SET       *
*                                                                     *
*   2. SEQUENCE THE NEW DATA SET OR RESEQUENCE AN OLD ONE             *
*                                                                     *
*   3. REPRODUCE OR REPLACE A MEMBER OR A DATA SET                    *
*                                                                     *
*   4. PRODUCE A PARTITIONED DATA SET FROM A PHYSICAL SEQUENTIAL      *
*   MASTER OR THE CONVERSE.                                           *
*                                                                     *
*   4. ADD A MEMBER TO A PARTITIONED DATA SET.                        *
*                                                                     *
*   5. ASSIGN AN ALIAS NAME TO ANY MEMBER UPDATED                     *
*                                                                     *
*   6. CHANGE RECORDS IN A DATA SET                                   *
*                                                                     *
*   7. HANDLE RECORDS WITH NON-STANDARD SEQUENCE NUMBER FIELDS AND    *
*   LENGTHS OF SEQUENCE NUMBERS FROM 1 TO 8 CHARACTERS.               *
*                                                                     *
*   8. UPDATE ONLY SPECIFIED PORTION OF DATA SET RECORDS.             *
*                                                                     *
*   9. UPDATE INPLACE DATA SETS OR MEMBERS ON DIRECT ACCESS VOLUMES.  *
*                                                                     *
*ENTRY POINTS- ENTERED FROM THE OPERATING SYSTEM AT 'IEBUPDTE'.       *
*                                                                     *
*INPUT- SYSIN WHICH CONSISTS OF CONTROL CARDS AND LOGICAL DATA        *
*   RECORDS FOR INSERTION OR REPLACEMENT, THE INPUT DATA SET.         *
*                                                                     *
*       SYSUT1 WHICH DEFINES THE 'OLD MASTER' DATA SET WHICH MAY BE   *
*   PARTITIONED OR PHYSICAL SEQUENTIAL IN ORGANIZATION.               *
*                                                                     *
*OUTPUT-SYSPRINT WHICH CONTAINS DIAGNOSTIC MESSAGES, THE CONTENTS OF  *
*   SYSIN AND-IF THE 'LIST=ALL' CONDITION IS PRIMED- THE ENTIRE       *
*   'NEW MASTER' IS LISTED ON THE DEVICE DESIGNATED.                  *
*                                                                     *
*       SYSUT2 WHICH DEFINES THE 'NEW MASTER' DATA SET. IT WILL BE A  *
*   COMPOSITE OF THE SYSIN AND SYSUT1 DATA SETS. IT CAN BE EITHER     *
*   PARTITIONED OR PHYSICAL SEQUENTIAL IN ORGANIZATION.               *
*                                                                     *
*EXITS- NORMAL- A RETURN IS GIVEN TO THE SCHEDULAR MODULE OF THE      *
*   OPERATING SYSTEM. CONDITION CODE RETURNED ON A SUCCESSFUL RUN     *
*   WILL BE ZERO.                                                     *
*                                                                     *
*EXITS- ERROR- IF AN ERROR OCCURS, A CODE IS STORED, A MESSAGE        *
*   GIVEN TO THE USER EXPLAINING THE ERROR CONDITION AND A            *
*   RETURN TO THE OPERATING SYSTEM SCHEDULAR MODULE IS GIVEN.         *
*   RETURN CODE GIVEN ARE-                                            *
*   00 NORMAL TERMINATION OF A SUCCESSFUL RUN                         *
*   04 ERROR WITHIN A MEMBER- WILL TRY TO PROCESS NEXT MEMBER IF PODS *
*   12 UNRECOVERABLE ERROR- TERMINATION OF JOB.                       *
*   16 ERROR IN LABEL HANDLING IN USERS ROUTINE- TERMINATION OF JOB   *
*                                                                     *
*EXTERNAL ROUTINES- I/O HANDLED AT READ/WRITE LEVEL, GETMAIN, LINK,   *
*   BLDL, FIND, STOW USED.                                            *
*                                                                     *
*TABLES/WORK AREAS-                                                   *
*   -KEYTAB- LIST OF VALID KEY WORDS USED ON CONTROL CARDS            *
*   -COMDTABL- LIST OF VALID COMMAND WORDS USED ON CONTROL CARDS      *
*   -SWITCHRD- BUFFER CONTAINING CONTROL CARD AND SWITCHES USED       *
*   BY CARD SCAN ROUTINE.                                             *
*   -LOGOUTAR- BUFFER WHICH IS USED BY LOG OUT ROUTINE FOR MESSAGES.  *
*   -STARTAB- TABLE OF SWITCHES WHICH ARE RESET TO ZERO UPON ENTRY    *
*   TO MODULE. ALL SWITCHES NEEDING REINITIALIZATION SHOULD BE PUT    *
*   INTO THIS TABLE.                                                  *
*                                                                     *
*ATTRIBUTES- SERIAL REUSABLE, BLOCK LOADS
*                                                                     *
*  THIS CSECT-   1.  OPENS SYSUT1 AND SYSUT2 (OLD AND NEW MASTER) DCB *
*                2.  READS CONTROL CARDS FROM SYSIN AND BRANCHES TO   *
*     CONTROL CARD SCAN AND ANALYSIS CSECT (IEBASCAN) TO PROCESS THEM *
*                3.  READS OLD MASTER RECORDS AND WRITES THE NEW      *
*     MASTER AFTER ANALYSING THE SITUATION.                           *
*                4.  THE 'LASTLEG' ROUTINE IS ENTERED WHEN LAST       *
*     MEMBER OR DATA SET HAS BEEN PROCESSED.                          *
*                5.  THE 'FLUSH' ROUTINE IS ENTERED TO SCAN TO NEXT   *
*     MEMBER IF A SYNTAX ERROR IS ENCOUNTERED ON A CONTROL STATEMENT. *
*                                                                     *
         EJECT
*     SYMBOLIC REGISTER NOTATION   GR0 - GR15
GR0      EQU   0
GR1      EQU   1
GR2      EQU   2
GR3      EQU   3
GR4      EQU   4                       REGISTER RESERVED FOR CSECT1
GR5      EQU   5
GR6      EQU   6
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10
GR11     EQU   11
GR12     EQU   12
GR13     EQU   13
GR14     EQU   14
GR15     EQU   15
D0       EQU   0                       DISPLACEMENT OF 0        YM07522
D1       EQU   1                       DISPLACEMENT OF 1        YM07522
L1       EQU   1                       LENGTH OF 1              YM07522
L4       EQU   4                       LENGTH OF 4              YM07522
L7       EQU   7                       LENGTH OF 7              YM07522
L8       EQU   8                       LENGTH OF 8              YM07522
B4       EQU   4                       SHIFT 4 BITS             YM07522
DIGIT    EQU   C'0'                    DIGIT ZONE               YM07522
         SPACE 1
IEBUPDT2 CSECT
         ENTRY DEACTV    ENTRY PT FROM SYNAD ROUTINE             A27716
*                        IN IEBUPXIT TO CLOSE SYSUT1,            A27716
*                                      SYSUT2, AND SYSIN         A27716
* 115800-116000,118200                                           A22518
* 117400                                                         A22653
*                                                                A22163
* 101600,101800,117600                                           A24812
* 045760                                                         A24428
*                                                                A25310
* 050600,169400,208810,208966                                    A25427
*                                                                A23927
* 101434,101850                                                   M3410
* 068600-069200,72800,218400                                     A27716
* 078800,083800,209696                                           A27600
* 094800                                                         A25432
* 125200                                                         A28728
* 109650-110050,172400,222720-222750,223260                      A29047
* 117402,117700                                                  A30939
* 206496                                                         A31455
* 042600-042800,061200,061400,168800-170100,177450,177460        A30480
* 050500,078640-079000,083700-084000,125400,125600,189000        A34087
* 191800,204400-204600,209196-210400,220315                      A34087
* 135000-135400                                                  A32024
* 175300                                                         A32631
* 188600                                                         A32632
* 179000                                                         A33281
* 044164-044244,044814                                           A34086
* 071400-071800,167800-168000,173830,174000                      A30625
* 221250                                                         A33305
*186400,187000                                                   A36095
*                                                                A36128
*D127900. C128000                                                A38750
*D161400                                                         A41743
*C162400                                                         A41743
*A169400,169520.C168800                                          A41756
*C155950 .A155970-155994                                         A44323
*A172520,175100,219674                                           A45212
*A101720,101730                                                  A48737
*A165400,165600,178900,179300,179320                             A45153
*A03790,037920,101424-101496,101824-101925,101983-1019870,103800YA01187
*A125700-125780,129500-129720,129940,141100-141180,223481-223785YA01187
*A 087300-087340                                                YA01188
*C 087400                                                       YA01188
*A 127900-127940
*C 128000
*A034300,034320,177540,177560,177900-177940                     YA01700
*C177600,177800                                                 YA01700
*D034800,035000                                                 YA01700
*A 127900-127940
*C 128000
*A 184300,186900                                                YA01186
*D 184800                                                       YA01186
*A 148700                                                       YA01677
*A129540,129541                                                 YA01723
*C129760                                                        YA01723
*A22210-22280,30620,91620-91780,91820-91880                     YM07522
*A162020,173804,219774                                          YM07522
*D92000                                                         YM07522
*C91800                                                         YM07522
*A101569,101573,101821,101822,101877-101889,101991,102092      @YA02533
*A102096,120500-120540,129583,129603                           @YA02533
*C093600,093800,101428,101900,101906,102104,120600,129716      @YA02533
*A137520-137584                                                @YA02554
*C184200                                                       @YA02568
*C065200                                                       @YA02584
*D064800,065000                                                @YA02584
*A 221240                                                      @ZA00637
*A025240-025520,171640-171720,189840-189969,197035-197080      @ZA01682
*C190000,197090,203000                                         @ZA01682
*D025400,025600,197060,197150                                  @ZA01682
*A102084,102088,129508                                         @ZA01683
*C102090                                                       @ZA01683
*D 129523,129527                                               @ZA01683
*C 145060                                                       YA01707
*A144900                                                       @ZA01657
*A222710                                                       @ZA01684
*D222720,222750                                                @ZA01684
*A031300-031360                                                @ZA01689
*C027400                                                       @ZA01689
*A026900,026920,029420,029440,189900,189904                    @ZA01690
*C024000,024200,025000,025280,115000,115200,174010             @ZA01690
*C174800,187600,187800,197090                                  @ZA01690
*A 176400-176780                                               @ZA01692
*C 174800                                                      @ZA01692
*A 118140-118180                                               @ZA03382
*C 118120                                                      @ZA03382
*A 094860-094880                                               @ZA04438
*A 171500                                                      @ZA04434
*A 209196-209556                                               @ZA06529
*C 165600                                                      @ZA06529
*A 045500-045540                                               @ZA06546
*C 045600                                                      @ZA06546
*D 090520                                                      @ZA07320
*A 124500-124540                                               @ZA07361
*C 123800                                                      @ZA07361
*A 094800,094820,094860-094892                                 @ZA07377
*C 159000                                                      @ZA07377
*D 094860-094880                                               @ZA07377
*A179330,179340                         @XA19829-@YA19268-(ORG)@ZA24936
*A090430,094900-094920,094940-094950,   (ORG)@XA19639-@YA19583-@ZA27274
*A129529,136810-136910,153210-153350,   (ORG)@XA19639-@YA19583-@ZA27274
*A155400-155450,223521                  (ORG)@XA19639-@YA19583-@ZA27274
*C094930,153200,155500                  (ORG)@XA19639-@YA19583-@ZA27274
*A179368-179392                         (ORG)@XA20208-@YA19886-@ZA28408
*A058270-058620,220806-220818           @XA20587-@YA19906-(ORG)@ZA28493
*C058200,220800                         @XA20587-@YA19906-(ORG)@ZA28493
*D058400,058600                         @XA20587-@YA19906-(ORG)@ZA28493
*A129670                                @XA20392-@YA20223-(ORG)@ZA29830
*A129403-129480                         @XA20867-@YA19583-(ORG)@ZA29907
*C094900,153200,155400                  @XA20867-@YA19583-(ORG)@ZA29907
*D090430,094910-094950,129529,129730    @XA20867-@YA19583-(ORG)@ZA29907
*D136810-136910,153220-153230           @XA20867-@YA19583-(ORG)@ZA29907
*D155450-155500,223521                  @XA20867-@YA19583-(ORG)@ZA29907
*A155430-155490                         @XA20479-@YA20225-(ORG)@ZA29832
*A153200-153320                         @XA20479-@YA20225-(ORG)@ZA29832
*C153400                                @XA20479-@YA20225-(ORG)@ZA29832
*A129413-129415                         @XA22013-@YA20972-(ORG)@ZA31995
*A094854,155410-155420                  @XA22015-@YA20973-(ORG)@ZA32389
*D094840                                @XA22015-@YA20973-(ORG)@ZA32389
*A220793-220796                                  @XA22247-(ORG)@ZA33607
         SAVE  (14,12),T,IEBUPDT2-OZ33607                      @ZA27274
         BALR  GR2,0
         USING  MEMBER,GR2,GR5                                     UL0H
         USING IEBUPCON,12                                         UL0H
MEMBER   L     GR5,SCNDBASE                                        UL0H
         AR    GR5,GR2                                             UL0H
         ST    GR13,SAVEPT13                                       UL0H
         LA    GR13,REGSAVS            POINTER TO SAVE AREA
         TM   COMDCD2,X'0F'           COMMAND PENDING          @ZA01690
         BZ    NEXT1                   NO- TEST SOME MORE      @ZA01690
         TM    REPROSW,X'0E'           REPRO FUNCTION
         BC    1,ANYEOFOM              YES- PROCESS TILL OM IS DONE
         TM    CHNGESW,X'0E'           CHANGE FUNCTION
         BZ    IFALIAS2           ADD OR REPL.TEST IF ALIASES  @ZA01690
         CLI   OMEND,X'0F'             IS OLD MASTER FINISHED
         BNE   PROCEED            NO,CONTINUE PROCESSING       @ZA01682
IFALIAS2 CLI   ALIASW2,X'0F'      IS THERE SEVERAL ALIAS STMT? @ZA01690
         BNE   STOWNAME           NO,FINISH THIS MEMBER        @ZA01682
         MVI   DONOTRDC,RESET0    ALLOW READING OF SYSIN       @ZA01682
         CLI   EODCTSW,X'0F'      IS SYSIN FINISHED?           @ZA01682
         BNE   NEXT1              NO,TEST SOME MORE            @ZA01682
         MVI   FINICCS,X'0F'      SET SYSIN FINISHED           @ZA01682
         B     STOW               BRANCH TO FINISH THIS MEMBER @ZA01682
***********************************************************************
SCNDBASE DC    F'4096'            CONSTANT FOR SECOND BASE REGISTER000H
***********************************************************************
ANYEOFOM CLI   OPENSW,X'0F'            DATA SETS OPEN YET
         BE    STARTUP                 NO- OPEN DCB AND START PROCESS
         CLI   REPROSW,X'0F'           FIRST ENTRY TO REPRO
         BE    RESTREPO                YES- LOCATE AND PROCESS MEMBER
         CLI   OMEND,X'0F'             PREVIOUS MEMBER FINISHED
         BNE   READOM                  NO- FINISH UP BEFORE START REPRO
         CLI   ALIASW2,X'0F'      MULTIPLE ALIAS STMT?         @ZA01690
         BE    IFALIAS2           YES,GO TO SEE IF SYSIN THRU  @ZA01690
         B     OMEODX                  YES- ANOTHER COMMAND PENDING
NEXT1    CLI   CCSCANSW,BIT01          CARD SCAN REQUESTING READ
         BE    TESTEOD            YES,GO TO SEE IF SYSIN THRU  @ZA01689
         CLI   FLUSHSW,X'0F'           ERROR ON CONTROL STATEMENT
         BE    CHKIFEND                CHECK IF SYSIN THRU
         CLI   FIRSENTR,X'0F'          START OF NEW MEMBER
         BE    PROSSOM                 YES- POSITION TO IT
         CLI   PRINTWRT,X'0F'          RECORD TO BE LISTED AND WRITTEN
         BE    READCC                  GET NEXT CARD
         CLI   EODCTSW,X'0F'           /* READ FIRST TIME
         BE    EODCTX                  YES- SET FINICCS SWITCH
         CLI   OMEND,X'0F'             END OF OLD MASTER
         BE    OMEODX                  YES
         CLI   ALIASW2,X'0F'      MULTIPLE ALIAS STMT?         @ZA01690
         BE    NOTEMPTY           YES,GO TO SEE IF SYSIN THRU  @ZA01690
         CLI   LABEL2SW,X'0F'     ARE LABELS TO BE READ IN SYSIN   UL0H
         BE    READCC                  IF SO, READ THEM NOW        UL0H
         CLI   LABEL2SW,X'FF'          LABEL WITH DATA=NO        A45205
         BNE   OPEND                   NO,TEST MORE              A45205
         MVI   LABEL2SW,X'00'          RESET SWITCH              A45205
         B     READCT1                 READ SYSIN  IF FIRST CARD A45205
OPEND    CLI   OPENSW,X'0F'       OPEN DCB YET                   A45205
         BE    OPENTHEM      OPEN DCBS DELETE, NUMBER PENDING    13943
         CLI   FINICCS,X'0F'      ENDUP STATEMENT READ           13943
         BE    ENDUPCC            YES, FINISH OLD MASTER         13943
         MVI   OPENSW,X'0F'            OM AND NM NOT OPENED SWITCH
STARTUP  MVC   IDENTXX(4),INITSEQ      REINITIALIZE SEQUENCE CONSTANTS
         XC    IDENTNN(L4),IDENTNN                              YM07522
         TM    REPROSW,X'0E'           REPRO FIRST COMMAND
         BC    1,OPENTHEM              YES- OPEN DATA SETS
         B     READCT1                 READ SYSIN FIRST TIME
TESTEOD  CLI   EODCTSW,X'0F'       SYSIN THRU?                 @ZA01689
         BNE   READCC              NO,GO TO READ CONT CARDS    @ZA01689
         LA    GR11,SCANMSG        YES,CNTRL STMT ERROR MSG    @ZA01689
         B     GIVENGO             EOD INSTEAD OF CONT CARDS   @ZA01689
RESETRD  MVI   FIRSENTR,RESET0         RESET NEW NUMBER SWITCH SETTING
READCC   CLI   DONOTRDC,X'0F'          SHOULD ANOTHER CARD BE READ
         MVI   PRINTWRT,RESET0         CLEAR SWITCH THAT GOT US HERE
         BE    READOM                  NO- READ OLD MASTER
*                                                                     *
*                        SYSIN  SEQUENTIAL  DATA  SET              BS0H
*                                                                     *
READCT1  L     GR3,READINAP            LOAD START OF BUFFER
         L     GR11,DCBCT              ADDRESS OF DCB
         GET   (GR11)                   GET CONTROL CARDS          BS0H
         MVC   0(80,GR3),0(GR1)         MOVE TO INPUT AREA         BS0H
         MVI   IGNORE1,RESET0          IF DID NOT INTERRUPT, NO /* EOF
BLOCKCC  L     GR3,READINAP            POINTER TO SYSIN RECORD
         CLI   INSRTRD,X'0F'           READING INSERT RECORDS
         BCR   8,GR9                   YES- RETURN TO PROCESS INSERT
         CLI   CCSCANSW,BIT01     DID CARD SCAN ISSUE READ?     YA01700
         BE    MOVECCD            YES,MOVE CARD TO ANALYSIS BUF YA01700
         CLI   FLUSHSW,X'0F'           FLUSHING CARDS TO NEXT COMMAND
         BE    CLEARGO                 YES- CLEAR SWITCHES CHECK CARD
CLEARGO  XC    SWITCHRD(108),SWITCHRD  CLEAR SWITCHES
MOVECCD  MVC   SWITCHRD+1(80),0(GR3)   MOVE CARD TO ANALYSIS BUFFER
         CLI   LABEL2SW,X'0F'           PROCESSING USER LABLES     UL0H
         BE    LBLPROCS                 IF SO,ANALYZE THIS RECORD  UL0H
         CLC   0(2,GR3),DOTSLASH       CONTROL STATEMENT IN BUFFER
         BE    CHKPS                   YES - ANALYSE THEN PRINT    4929
         CLI   FLUSHSW,X'0F'           FLUSH ROUTINE ISSUED READ
         BE    READCC                  YES- CONTINUE TO FLUSH      4929
         CLI   CCSCANSW,BIT01          READ ISSUED BY 'KGTCD' OF SCAN
         BE    RET2SCAN                YES- RETURN TO SCAN
         CLI   FIRSENTR,BIT10          VERY FIRST CARD
         BE    BADCARD                 YES- ERROR DATA BEFORE CONTROL
         LA    GR14,NEXTONE            RETURN FROM PRINT- LAST SYSIN
         CLI   FLLISTSW,X'01'          IS LIST OF ./ AND NEW MASTER
         BCR   8,GR14                  YES- CONTINUE
         TM    CHNGESW,X'0E'           CHANGE OPERATION         YA01187
         BO    NEXTONE                 WAIT WITH PRINTING       YA01187
PRINTCRD MVI   ASISMSW,X'0F'           SET WRITE RECORD SWITCH
         MVC   LOGOUTAR+7(80),0(GR3)   MOVE RECORD TO PRINT BUFFER
         L     GR15,PRIMEWRT           ADDRESS OF MESSAGE ROUTINE
         BR    GR15                    PRINT CARD 14 LOADED WITH RETURN
NEXTONE  CLI   CHNGESW,X'0F'           HAVE A CHANGE COMMAND
         BE    RESETCHG                YES- RESET THIS ON FIRST LRECORD
         TM    REPROSW,X'0E'           HAVE A REPRO COMMAND
         BC    1,INVALOP               INVALID DATA RECORD WITH REPRO
         CLI   REPLSW,X'0F'            HAVE A REPL COMMAND
         BE    RESETRPL                YES- RESET THIS ON FIRST LRECORD
         TM    ADDSW,X'0E'             HAVE AN ADD COMMAND
         BC    8,PROCEED               NO- RESET THIS ON FIRST LRECORD
         MVI   ADDSW,X'0E'             LOGICAL RECORD BEFORE NEXT COMD
PROCEED  CLI   FLUSHSW,X'0E'           COMMAND AFTER FLUSH
         BE    PROSSOM                 YES- ON TO NEXT MEMBER
         CLI   OPENSW,X'00'            HAVE OM AND NM BEEN OPENED
         BNE   OPENTHEM                NO- OPEN THEM- HAVE A COMD CARD
         B     READOM                  CONTINUE PROCESSING
RESETCHG MVI   CHNGESW,X'0E'           LOGICAL RECORD BEFORE NEXT COMD
         B     PROCEED                 CONTINUE PROCESSING
RESETRPL MVI   REPLSW,X'0E'            LOGICAL RECORD BEFORE NEXT COMD
         B     PROCEED                 CONTINUE PROCESSING
OPENTHEM MVI   OPENSW,X'00'            CLEAR OPEN SWITCH
         TM    CTONLYSW,X'11'     ALL INPUT FROM SYSIN ?         A30480
         BZ    OPENOM             NO.. OPEN OLD MASTER           A30480
         CLI   LABEL2SW,X'0F'           PROCESSING USER LABELS     UL0H
         BE    OPENNEW1                 IF SO,ANALYZE THIS RECORD  UL0H
         CLI   ADDSW,X'00'             IS ADD SWITCH ON
         BE    INVALOP                 CAN ONLY ADD NO OM- GIVE MESSAGE
         B     OPENNEW1                OPEN ONLY NEW MASTER
*
*                   OPEN OLD MASTER DATA SET (SYSUT1) DCBOM
*
LBLPROCS STC   GR7,LABELCNT             STORE NO. OF LABELS        UL0H
         CLC   0(2,GR3),DOTSLASH        IS RECORD A CONTROL CARD   UL0H
         BE    CHEKCOMD                 IF SO,PREPARE TO ANALYZE ITUL0H
         MVC   0(80,GR6),0(GR3)         MOVE LABEL INTO BUFFER     UL0H
         BAL   GR14,PRINTCRD            PRINT LABEL                UL0H
         LA    GR6,80(GR6)              UP POINTER BY 80           UL0H
         LA    GR7,1(GR7)               UP COUNT BY ONE            UL0H
         CH    GR7,EIGHT                HAVE 8 LABELS BEEN READ IN UL0H
         BNH   READCT1                  IF NOT,READ ANOTHER RECORD UL0H
         MVI   LABEL2SW,X'00'           RESET LABEL PROCESS FLAG   UL0H
         B     INVALLBL                 ELSE ITS AN ERROR          UL0H
CHEKCOMD CLI   LABELCNT,X'00'          ANY LABELS SAVED YET        UL0H
         BE    INVALLBL                IF NOT, ITS AN ERROR        UL0H
         CLI   LABELSW,X'10'           PROCESSING HEADER LABELS    UL0H
         BE    HEADSCAN                INITIALIZE BEFORE SCANNING  UL0H
         CLI   LABELSW,X'20'           REQUEST FOR USER TRAILER LBLUL0H
         MVI   LABEL2SW,X'F0'          IF SO,ASCAN MUST CHECK NEXT UL0H
         MVI   NMSYSNLB,X'0F'          FLAG AS TRAILERS IN INPUT   UL0H
*                      FUNCTION FOR 'ENDUP', ALL ELSE INVALID      UL0H
         BE    RET2SCAN                IF SO, SCAN COMMAND CARD    UL0H
         B     BADSEQN                 IF NOT, ITS A SEQUENCE ERRORUL0H
HEADSCAN MVI   LABEL2SW,X'00'          DO NOT BYPASS NORMAL PROC   UL0H
         MVC   TESTLABL(1),LABELSW     SAVE VALUE OF LABELSW       UL0H
         MVC   TMPLBLCT(1),LABELCNT    SAVE CURRENT NO. OF LABELS  UL0H
         MVI   OMSYSNLB,X'0F'          FLAG AS  HEADERS IN INPUT   UL0H
         B     RET2SCAN                SCAN CONTROL CARD THEN PROC UL0H
OMLBLTST CLI   OMHDRSW,X'0F'            HEADER RTN ON NON-PODS?    UL0H
         BE    LABLMESG                 IF SO,ERROR                UL0H
         CLI   OMTRLRSW,X'0F'           TRAILER RTN  ON NON-PODS?  UL0H
         BE    LABLMESG                 IF SO,ERROR                UL0H
         CLI   INPLCSW,X'0F'            IS IT UPDATE=INPLACE       UL0H
         BNE   OPENITUP                 IF NOT,OPEN DATA SET       UL0H
         CLI   LABELSW,X'00'           ANY LABELS IN INPUT STREAM  UL0H
         BNE   LABLMESG                IF SO, ITS AND ERROR        UL0H
         CLI   TOTALSW,X'FF'            TOTALING ON NON-PODS?      UL0H
         BNE   OPENUPDT                 IF NOT,OPEN FOR UPDAT      UL0H
         B     NOTOTPDS                 IF SO, ERROR               UL0H
NOTRLBLI LA    GR11,INPLBLMS            TRAILERS NOT SUPPORTED     UL0H
         B     SENDMSGQ                 PRINT MESSAGE              UL0H
LABLMESG LA    GR7,8                    MESG BRANCH CONSTANT       UL0H
SENDQUIT LA    GR11,SEQDSMSG            MESSAGE NUMBER             UL0H
SENDMSGQ L     GR15,PRIMEWRT            ADDRESS OF LOG ROUTINE     UL0H
         BALR  GR14,GR15                PRINT MESSAGE              UL0H
         B     DEACTVCT                 DEACTIVATE EXITS AND QUIT  UL0H
NOTOTPDS LA    GR7,4                                               UL0H
         B     SENDQUIT                 SEND MESSAGE AND STOP PROC UL0H
NOTOTAL  LA    GR11,TOTNTSPT           NO TOTAL ON UPDTA INPLACE   000I
         B     SENDMSGQ                SEND ERROR MESSAGE          000I
OPENOM   L     GR3,DCBOM          ADDRESS OLD MASTER DCB         A34086
         CLI   PSSWOM,X'00'       IS OLD MASTER SEQUENTIAL       A34086
         BNE   OMLBLTST           NO.. TEST FOR INVALID LBL REQ. A34086
         MVI   OMOPENSW,X'0F'     ALLOW LABEL PROCESSING         A34086
         CLI   INPLCSW,X'0F'      UPDATE=INPLACE IN PROCESS      A34086
         BE    OPENUPDT           YES.. OPEN OLD MSTR FOR UPDATE A34086
OPENITUP CLI   INPLCSW,X'0F'           UPDATE INPLACE- NO NEW MASTEUL0H
         MVC   TESTLABL+1(1),LABELSW   SAVE CURRENT VALUE OF SWITCHUL0H
         MVC   TMPLBLCT+1(1),LABELCNT  SAVE CURRENT LABEL COUNT    UL0H
         MVC   LABELSW(1),TESTLABL     GET PREVIOUS VALUE OF SWITCHUL0H
         MVC   LABELCNT(1),TMPLBLCT    SET FOR O.M. COUNT          UL0H
         L     GR3,DCBOM               ADDRESS OF OLD MASTER DCB
         BE    OPENUPDT                YES- OPEN FOR UPDATE
         TM    BDSORG(GR3),PSDSET      SEQUENTIAL DATA SET?    @ZA06546
         BNO   OPENOM1                 NO,BRANCH TO OPEN       @ZA06546
         NI    50(GR3),X'20'           DELETE POINT MACR       @ZA06546
OPENOM1  OPEN  ((GR3))                OPEN OLD MASTER DATA SET @ZA06546
         MVC   LABELSW(1),TESTLABL+1   RETRIEVE CURRENT VALUE      UL0H
OPENCHKT L     GR1,DCBOM               ADDRESS OF OLD MASTER DCB   UL0H
         MVI   OMOPENSW,X'00'                                      UL0H
         LA    GR3,PSSWOM              PHYSICAL SEQUENTIAL SWITCH OM
         LA    GR7,SYSUT1NM            DD NAME SYSUT1 OLD MASTER
         LA    GR10,STORBUFO           RETURN FROM 'OPENCHEK' ROUTINE
OPENCHEK TM    BOFLGS(GR1),X'10'       WAS THERE AN OPEN ERROR
         BZ    BADOPEN                 YES
         CLI   IOBADSW,X'0F'            I/O ERROR DURING LABEL PROCUL0H
         BE    DEACTVCT                 IF SO,PREPARE TO CLOSE     UL0H
         CLI   LBLRETCD,X'10'          HAS USER REQ TERMINATION    UL0H
         BE    HEADRMSG                 IF SO,PREPARE TO TERMINATE UL0H
         TM    BDSORG(GR1),PODSET      PARTITIONED DATA SET
         BC    8,GTMAINON              NO- LEAVE PSSWOM/NM 0 FOR PS
         MVI   0(GR3),X'0F'            SET OM OR NM AS PARTITIONED
GTMAINON LH    GR0,BBLKSI(GR1)         LOAD BLOCKSIZE SYSUT1 OR SYSUT2
         GETMAIN R,LV=(GR0)
         BR    GR10                    RETURN- BUFFER ADDRESS IN GR1
*                                                                     *
*                  OPEN OLD MASTER DATA SET FOR UPDATE INPLACE        *
*                                                                     *
OPENUPDT L     GR9,DSORGRTN   GET ADDRESS OF DSORG CHECK ROUTINE   3341
         BALR  GR8,GR9        GO CHECK DSORG BEFORE OPEN           3341
         CLI   OMTRLRSW,X'0F'           USER TRAILER ROUTINE       UL0H
         BE    NOTRLBLI                 IF SO,ERROR                UL0H
         CLI   NMTRLRSW,X'0F'     TRAILER ROUTINE FOR SYSUT2     A36128
         BE    NOTRLBLI           YES.. PRINT ERROR MESSAGE      A36128
         CLI   TOTALSW,X'00'           IS TOTALING REQUESTED       000I
         BNE   NOTOTAL                 IF SO, IT'S AN ERROR     PTRUT61
         MVI   FRSTCOMD,X'FF'           FLAG AS DCB OPEN           UL0H
         OPEN  ((GR3),UPDAT)                                       3341
         MVC   LABELSW(1),TESTLABL+1   RETRIEVE CURRENT VALUE      UL0H
         MVC   LABELCNT(1),TMPLBLCT+1  SET FOR N.M. COUNT          UL0H
         B     OPENCHKT                CHECK OPEN
BADCARD  BAL   GR14,PRINTCRD           DATA CARD BEFORE FIRST COMMAND
BADSEQN  LA    GR11,SEQMSG             SEQUENCE ERROR MESSAGE
         B     GIVENGO                 GIVE MESSAGE AND GO ON
DUMMYNM  LA    GR7,SYSUT2NM            PUT DDNAME ADDR IN REG    A25310
BADOPEN  LA    GR11,NTOPMSG            CODE FOR MESSAGE
FINERRA  BAL   GR9,GIVEMESS            GIVE MESSAGE
         MVI   HICONCDE+1,12      SET COND. CODE AND GET OUT     A34087
FINERRA2 BAL   GR14,DEACTV                                       A25427
         MVI   COMDCD,X'00'            RESET COMMAND SWITCH        4929
         MVI   FLUSHSW,X'0F'           TURN FLUSH SWITCH ON        4929
          B     READCC                                             4929
STORBUFO ST    GR1,OMREADP             ADDRESS OF BUFFER ASSIGNED OLD
         ST    GR1,OMREADT             INITIALIZE STARTING ADDRESS
         CLI   OMBLKERR,X'00'          IS OLD MASTER BLOCKSIZE VALID
         BE    OPENNEW1                YES- OPEN NEW MASTER
         LA    GR3,OMBLOCKP            OLD MASTER BLOCKING FACTOR
         LA    GR9,OPENNEW1            RETURN FROM MESSAGE BUILDING
         LA    GR7,OMDC                ADDRESS OF 'OM' REFERENCE
*                                                                     *
*       WARNS USER BLOCKSIZE, LRECL ASSUMED 80/80 UNBLOCKED, FIXED    *
*                                                                     *
BILDMESS LA    GR11,BLOKSMSG           CODE FOR WARNING MESSAGE
         LA    GR6,1
         STH   GR6,0(GR3)              ESTABLISH BLOCKING FACTOR OF 1
         STH   GR6,2(GR3)              ESTABLISH TEMPORARY BLOCK AS 1
GIVEMESS L     GR15,PRIMEWRT           ADDRESS OF MESSAGE CSECT
         BALR  GR14,GR15               GIVE MANY DIFFERENT MESSAGES
         BR    GR9                     RETURN USUALLY PROSSOM/OPENNEW1
*
*                   OPEN NEW MASTER DATA SET (SYSUT2) DCBNM
*
OPENNEW1 MVC   SYS1RECL(2),LRECL       SAVE SYSUT1 LOGICAL RECORD SIZE
         CLI   INPLCSW,X'0F'           IS THERE A NEW MASTER
         L     GR11,DCBOM              OLD MASTER DCB ADDRESS
         BE    PROSUPD                 SAVE OM FDAD IF UPDATE INPLACE
OPENNOW  L     GR3,DCBNM               ADDRESS OF NEW MASTER DCB
         L     GR9,DSORGRTN   GET ADDRESS OF DSORG CHECK ROUTINE   3341
         BALR  GR8,GR9        GO CHECK DSORG BEFORE OPEN           3341
         CLI   PSSWNM,X'00'             IS N.M. PHYSICAL SEQUENTIALUL0H
         BE    PREOPEN2                 IF SO,PREPARE TO OPEN      UL0H
NMLBLTST CLI   NMHDRSW,X'0F'            HEADER RTN ON NON-PODS?    UL0H
         BE    LABLMESG                 IF SO,ERROR                UL0H
         CLI   NMTRLRSW,X'0F'           TRAILER RTN ON NON-PODS?   UL0H
         BE    LABLMESG                 IF SO,ERROR                UL0H
         CLI   LABELSW,X'00'            IS LABEL PROCESSING REQ    UL0H
         BNE   LABLMESG                 IF SO,ERROR                UL0H
         CLI   TOTALSW,X'FF'            TOTALING ON NON-PODS?      UL0H
         BE    NOTOTPDS                 IF SO,ERROR                UL0H
         B     OPENSUT2                 IF NOT,PROCESS OPEN        UL0H
PREOPEN2 MVI   NMOPNSWT,X'1F'                                      UL0H
         MVI   FRSTCOMD,X'FF'                                      UL0H
         CLI   LABELSW,X'10'            LABELS IN INPUT STREAM     UL0H
         BNE   OPENSUT2                 IF NOT,OPEN DIRECTLY       UL0H
         MVI   OMSYSNLB,X'0F'                                      UL0H
OPENSUT2 OPEN  ((GR3),OUTPUT)
         MVI   NMOPNSWT,X'00'           RESET OPEN SWITCH          UL0H
         MVC   LABELCNT(1),TMPLBLCT+1  SET FOR TRAILER CT        A24428
         L     GR1,DCBNM               ADDRESS OF NEW MASTER DCB
         LA    GR3,PSSWNM              PHYSICAL SEQUENTIAL SWITCH NM
         LA    GR7,SYSUT2NM            DD NAME SYSUT2 NEW MASTER
         BAL   GR10,OPENCHEK           TEST FOR OPEN ERROR
         ST    GR1,NMWRITEP            STORE ADDRESS OF BUFFER SYSUT2
         ST    GR1,NMWRITET            INITIALIZE STARTING ADDRESS
         BAL   GR8,EXTNTCHK            CHECK EXTENTS               5324
         TM    NMBLKERR,X'01'          BLOCKING FACTOR ERROR?  @ZA28493
         BZ    PROSSOM                 NO- START PROCESSING    @ZA28493
         TM    NMBLKERR,X'80'          ERROR ON AN OLD D/S?    @ZA28493
         BZ    NOTNMOLD                NO,GIVE WARNING MSG     @ZA28493
         LA    GR7,SYSUT2NM            YES,GIVE ERROR MSG      @ZA28493
         B     BADOPEN                 AND QUIT                @ZA28493
NOTNMOLD LA    GR3,NMBLOCKP            NM BLOCKING FACTOR      @ZA28493
         LA    GR7,NMDC                ADDRESS OF 'NM' REFERENCE
         BAL   GR9,BILDMESS            GIVE MESSAGE
*                                                                     *
*                 START OF PROCESSING AFTER DATA SETS ALL OPENED AND  *
*  SOME SYSIN RECORDS READ AND ANALYSED.                              *
*                                                                     *
PROSSOM  CLI   OPENSW,X'0F'            DCBHS OPEN YET              6339
         BE    OPENTHEM                NO-- GO OPEN THEM           6339
         L     GR11,DCBNM              NEW MASTER DCB              6339
         CLI   LABEL2SW,X'F0'           PROCESSING USER LABELS     UL0H
         BNE   PROSUPD                  IF NOT,PROCESS RECORD      UL0H
         MVI   LABEL2SW,X'00'           RESET LABEL PROCESSING SWT UL0H
         B     RET2SCAN                                            UL0H
PROSUPD  MVI   FLUSHSW,RESET0          CLEAR FLUSH SWITCH
         MVI   STARTMEM,X'0F'          HAVE STARTED MEMBER UPDATE
         MVC   FDADSET(8),BFDAD(GR11)  SAVE FDAD IN CASE MEMBER BLOWS
         MVC   TRKBAL(2),TRBAL(GR11)  SAVE TRACK BALANCE           5666
         XC    LASTCTRC(8),LASTCTRC    CLEAR CT SEQUENCE NUMBER BUFFER
         XC    LISTAREA(80),LISTAREA   CLEAR LIST AREA
         TM    CTONLYSW,X'11'     IS THERE AN OLD MASTER         A30480
         BM    TESTCONS           NO.. TEST IF TO NEXT MEMBER    A30480
         CLI   INPLCSW,X'0F'           UPDATE INPLACE
         BE    PROSSOMX         YES,CHECK FOR SEQUENTIAL OR REPRO  4696
         CLC   SYS1RECL(2),BLRECL(GR11) IS OM AND NM RECORD SIZE SAME
         BE    PROSSOMX                YES- CONTINUE
         LA    GR11,SIZEBAD            LRECL SYSUT1 AND SYSUT2 UNEQUAL
         B     FINERRA                 GIVE MESSAGE AND QUIT
TESTCONS CLI   FIRSENTR,X'0F'          START OF NEXT COMMAND
         BE    RESETRD                 YES- GET MORE SYSIN
         B     CHKADREP                NO OLD MASTER- FINISH PROCESSING
BADDELET LA    GR11,DELMSG             INVALID DELETE RANGE MESSAGE
         B     GIVENGO                 GIVE MESSAGE- FLUSH MEMBER
AMESS    BAL   GR9,GIVEMESS            GIVE MESSAGE
INVALOP  LA    GR11,ERROP              INVALID OPERATION
GIVENGO  BAL   GR9,GIVEMESS            GIVE MESSAGE
         MVI   TEMPCON,X'04'           SET TEMPORARY CONDITION CODE
         B     CHKIFEND                CONTINUE IF POSSIBLE
OUTOFSEQ LA    GR3,SWITCHRD+1     ADDRESS OF STATEMENT         @YA02584
         B     BADCARD                 LIST BAD STATEMENT THEN MESSAGE
PROSSOMX CLI   PSSWOM,X'00'            IS OLD MASTER SEQUENTIAL
         BE    READSW1                 YES- SKIP LIST BUILD
         TM    REPROSW,X'0F'           FIRST ENTRY TO REPRO
         BC    8,NOREPRO               NO REPRO AT ALL
         BC    4,READOM                NOT FIRST REPRO
RESTREPO MVI   REPROSW,X'0E'           RESET REPRO SWITCH
NOREPRO  MVC   LISTAREA+4(8),MEMNAME   MOVE MEMBER NAME TO LIST AREA
         MVC   LISTAREA(4),SIZELIST    MOVE SIZELIST CONSTANT  72 BYTES
         CLI   BLDFNDSW,X'0E'        HAVE DONE BLDL FOR THIS MEMB? 2163
         BE    READOM                YES,CONTINUE READING FROM OM  2163
         L     GR1,DCBOM               ADDRESS OF DCB
         BLDL  (GR1),LISTAREA
         STC   GR15,TEMP               STORE RETURN CODE FROM BLDL
         MVI   BLDFNDSW,X'0E'          MARK BLDL AS DONE           2163
         CLI   TEMP,X'00'              IS MEMBER NAME IN OM DIRECTORY
         BE    CHECKADD                YES- SEE IF TRYING TO ADD NAME
         CLI   TEMP,X'04'              IS MEMBER NAME MISSING
         BE    CHECKNA                 YES- SEE IF TRYING TO ADD NAME
BLDLERR  LA    GR11,BLDLMSG     GET ADDRESS OF BLDL I/O ERR MSG  A27716
         B     FINERRA   GO PRINT   MESSAGE                      A27716
FINDERR  LA    GR11,FINDMSG    GET ADDRESS OF FIND I/O ERR MSG   A27716
         B     FINERRA   GO PRINT   MESSAGE                      A27716
CHECKADD CLI   ADDSW,X'00'             IS ADD SWITCH ON
         BE    FINDIT                  NO- NAME IS THERE SO IS OK
         LA    GR11,STOWMSGN           MESSAGE NUMBER NAME FOUND MESSG
         B     MESGNOW                 CANNOT ADD IF IT IS FOUND
CHECKNA  TM    ADDSW,X'0E'             IS ADD SWITCH ON
         BC    1,TESTCONS              TEST IF NEXT MEMBER
         LA    GR11,STOWMSGT           MEMBER NAME NOT FOUND
MESGNOW  LA    GR7,MEMNAME             ADDRESS OF MEMBER NAME
         MVI   ASISMSW,X'0E'           FORCE SPECIAL MESSAGE BUILDING
         B     AMESS                   NAME IS MISSING REPRO/REPL/CHNGE
FINDIT   LA    GR0,LISTAREA+12    GET ADDRESS OF TTRK OF MEMBER  A30625
         L     GR1,DCBOM               ADDRESS OF OLD MASTER DCB
         FIND  (GR1),(GR0),C
         STC   GR15,TEMP               STORE FIND RETURN CODE
         CLI   TEMP,X'00'              I/O ERROR IF NOT SUCCESSFUL
         BNE   FINDERR                                           A27716
         TM    LISTAREA+17,X'80'       TEST IF ALIAS BIT SET
         BC    8,TESTCMDX              NO- CONTINUE
         CLI   PSSWNM,X'00'             IS DATA SET PHY. SEQ.      UL0H
         BE    TESTCMDX                 IF SO,NO ALIAS MSG         UL0H
         LA    GR11,ALTTRUE            ADDRESS OF MESSAGE CODE
         NI    LISTAREA+17,X'7F'       RESET ALIAS BIT
         BAL   GR9,GIVEMESS            GIVE ALIAS TO TRUE MESSAGE
TESTCMDX CLI   FIRSENTR,X'0F'          START OF NEXT COMMAND
         BE    RESETRD                 YES- RESET SWITCH AND READCC
         TM    CHNGESW,X'0E'           TEST IF COMMAND IS CHANGE
         BC    1,READSW1               YES- READ OLD MASTER FIRST TIME
         TM    REPROSW,X'0E'           TEST IF COMMAND IS REPRO
         BC    1,READSW1               YES- READ OLD MASTER FIRST TIME
         B     CHKADREP                ADD OR REPLACE- CT TO NEW MASTER
READOM   CLI   READNOOM,X'0F'          SHOULD ANOTHER OM RECORD BE READ
         BE    CHKOP                   NO- HAVE NOT PROCESSED LAST ONE
         TM    CHNGESW,X'0E'           CHANGE FUNCTION
         BC    7,OKONTOOM              YES- CONTINUE READ OLD MASTER
         TM    REPROSW,X'0E'           REPRO FUNCTION
         BC    8,CHKADREP              NO- SHOULD NOT BE HERE ADD/REPL
OKONTOOM TM    MULTI,X'0F'             IS THIS SECOND PASS         1796
         MVI   MULTI,X'00'        TURN OFF SWITCH                  1796
         BO    READSW1                 YES-READ OLD MASTER         1796
         LH    GR15,OMBLOCKT      LOAD TEMPORARY BLOCKING FACTOR   1796
         BCT   GR15,RRLBOM             DONT READ TILL ALL PROCESSED1796
*                                                                     *
*                  READ OLD MASTER RECORDS                            *
*                                                                     *
READSW1  L     GR8,OMREADP             LOAD START OF BUFFER
         L     GR3,DCBOM               ADDRESS OF OM DCB
         MVC   OMBLOCKT(2),OMBLOCKP    INITIALIZE BLOCKING FACTOR
         ST    GR8,OMREADT             STORE POINTER TO START
         CLI   REPLUPDT,X'0F'          NEW RECORD IN BLOCK TO WRITE
         BE    WRTEREPR                YES- WRITE THIS BLOCK
         MVI   DECBOM+5,X'80'          RESTORE READ CODE IN DCB
         TM    BLDFNDSW,X'0F'          MAKE SURE A BLDL WAS DONE   2163
*                                      FOR THIS MEMBER             2163
         BO    NOREPRO                                             2163
         MVI   MULTI,X'00'          SET SW TO FINISH PROC THIS BLK 2653
         READ  DECBOM,SF,(GR3),(GR8),'S'
         CHECK DECBOM
         MVI   OMEODX+1,X'F0'          MARK OLD MASTER NOT EMPTY A36095
         CLI   NMSYSNLB,X'0F'           WERE EOV LABELS SAVED?     UL0H
         BNE   RDRET16                  IF SO,FLAG AS NON-TRANSMIT-UL0H
         MVI   NMSYSNLB,X'00'           TABLE TO SYSUT2            UL0H
RDRET16  CLI   LBLRETCD,X'10'           DID USER EXT RTN REQ TERM? UL0H
         BE    HEADRMSG                 IF SO,PRINT MSG AND QUIT   UL0H
         CLI   IOBADSW,X'F0'            DID AN I/O ERROR OCCUR     UL0H
         BE    DEACTVCT                 IF SO PREPARE TO QUIT      UL0H
         L     GR14,DECBOM+16          ADDRESS OF OLD MASTER IOB
         LH    GR14,14(GR14)           RESIDUAL COUNT
         LTR   GR14,GR14               TEST IF ANY RESIDUAL COUNT
         BZ    NOTSHORT                NO- CONTINUE
         L     GR10,DCBOM              ADDRESS OF OLD MASTER DCB
         LH    GR15,BBLKSI(GR10)       LOAD BLOCKSIZE
         SR    GR15,GR14               LENGTH OF RECORDS READ
         SR    GR14,GR14
         STH   GR15,SHRTBLK      SAVE NEW BLKSIZE FOR WRITE INPLACE3628
         LH    GR3,BLRECL(GR10)        LOGICAL RECORD LENGTH
         DR    GR14,GR3                COMPUTE VALID RECORD COUNT
         MVC   OMBLOCKT(2),OMBLOCKP    MAXIMUM BLOCK FORCES RRLBOM CODE
         MVI   SHORTBLK,X'0F'          FLAG AS SHORT BLOCK
         B     OKRECORD                PROCESS LAST (SHORT) BLOCK
RRLBOM   STH   GR15,OMBLOCKT           STORE TEMPORARY BLOCK FACTOR
         CLI   SHORTBLK,X'00'          SHORT BLOCK
         BE    NOTSHORT                NO- CONTINUE
         LH    GR15,VALIDREC           ACTUAL VALID RECORD COUNT
         BCT   GR15,OKRECORD           BRANCH IF RECORDS TO PROCESS
         MVI   SHORTBLK,RESET0         CLEAR SHORT BLOCK SWITCH
         B     READSW1            READ NEXT OLD MASTER RECORD    A34087
OKRECORD STH   GR15,VALIDREC           STORE NEW VALID RECORD COUNT
NOTSHORT L     GR14,OMREADT            ADDRESS OF LOGICAL RECORD
         LR    GR7,GR14                SAVE POINTER TO PRESENT RECORD
         ST    GR14,LASTRECB           POINTER TO LAST RECORD PROCESSED
         LH    GR1,LRECL               LOAD LOGICAL RECORD LENGTH
         AR    GR14,GR1                POINT TO NEXT RECORD
         LA    GR6,OMINAREA            OLD MASTER WORK AREA
         ST    GR14,OMREADT            ADDRESS OF NEXT RECORD TO USE
         BCTR  GR1,0                   DECREMENT FOR EXECUTE MVC
         EX    GR1,MOVEPART            MOVE RECORD TO WORK AREA
         LH    GR8,IDENTXX             COLUMN WITH SEQUENCE NUMBER
         LH    GR9,IDENTY              SIZE OF SEQUENCE NUMBER
         AR    GR8,GR6                 POINT TO SEQUENCE NUMBER FIELD
         BCTR  GR9,0                   DECREMENT FOR EXECUTE
         EX    GR9,CHKSEQNB            TEST FOR BLANK SEQUENCE NUMBER
         BNE   CHKOP                   NO- CONTINUE
         TM    CHNGESW,X'0E'           CHANGE OPERATION         YA01188
         BZ    BYPASS                  IF NOT, TEST RENUMBERING YA01188
         MVI   DONOTRDC,X'0F'          SET SWITCH NOT READ SYSINYA01188
BYPASS   CLI   RENUMALL,X'0F'          RENUMBER ALL CARDS       YA01188
         BE    NUMBRCD                 YES- GIVE BLANK SEQ A NUMBER
         B     TESTLIST                WRITE LAST RECORD
CHKOP    TM    CHNGESW,X'0E'           IS COMMAND TYPE CHNGE
         BC    1,CHNGCOMD              YES- CANNOT WRITE DIRECTLY
         TM    REPROSW,X'0E'           REPRODUCE DATA SET OR MEMBER
         BC    1,TESTLIST              YES- COPY EXACTLY AS IT IS
CHKADREP LA    GR6,SWITCHRD+1          ADDRESS INPUT CARD REPL/ADD
         MVI   READNOOM,X'0F'          READ NO MORE FROM OLD MASTER
         CLI   NUMBRSW,X'0F'           NUMBERING TO BE DONE
         BE    NUMBRCD                 YES- NUMBER CARD
         B     TESTLIST                NO- LIST AND WRITE AS IS
*                                                                     *
*                   NUMBERS RECORD- GR6 POINTER TO RECORD             *
*                                                                     *
SETNUM1  OI    SEQ1SW,X'F0'            SET SEQ1 SATISFIED- NUMBER
         MVI   READNOOM,RESET0    CLEAR DONOT-READ OM SW         A23927
NUMBRIT  LR    GR6,GR10                ENTRY FROM ENTNUMB ROUTINE
NUMBRCD  L     GR7,SEQWA               NEW OR INCREMENTED SEQUENCE
         LH    GR3,IDENTXX             SEQUENCE START COLUMN
         CVD   GR7,TEMP                SEQUENCE NUMBER IN DECIMAL
         LH    GR1,IDENTY              SEQUENCE SIZE
         LA    GR7,LASTSEQN            LAST SEQUENCE NUMBER WRITTEN
         CLI   IDENTN+D1,RESET0        NUMERIC SEQFLD SPECIFIED YM07522
         BNE   UPKNSET                 YES                      YM07522
         LA    GR8,L8                  MAXIMUM SEQ NO SIZE      YM07522
         SR    GR8,GR1                 SEQ NO OFFSET            YM07522
         AR    GR7,GR8                 PT TO PERTINENT FIELD    YM07522
         B     UPKSQN                  CONTINUE                 YM07522
UPKNSET  MVC   D0(L8,GR7),NEW1NUMB     SET ALPHANUMERIC SEQ NO  YM07522
         LA    GR7,L7(GR7)             GET NUMERIC PORTION      YM07522
         SH    GR7,NUMOFFST                                     YM07522
         IC    GR1,IDENTN+D1           GET SIZE                 YM07522
UPKSQN   BCTR  GR1,GR0                 SUBTRACT 1               YM07522
         SLL   GR1,B4                  SHIFT FOR EXECUTE        YM07522
         EX    GR1,UPKSN               SET UP NUMERIC PORTION   YM07522
         SRL   GR1,B4                  RESTORE LENGTH           YM07522
         AR    GR7,GR1                 ENSURE LAST DIGIT IS     YM07522
         OI    D0(GR7),DIGIT           * VALID                  YM07522
         LA    GR7,LASTSEQN            RESTORE LAST SEQ NO PTR  YM07522
         LH    GR1,IDENTY              RESTORE SIZE             YM07522
         LA    GR8,8                   MAXIMUM SEQUENCE NUMBER SIZE (8)
         LR    GR10,GR6                SAVE POINTER TO START OF CARD
         SR    GR8,GR1                 OFFSET SEQUENCE NUMBER REFERNCE
         AR    GR7,GR8                 PICK UP PERTINENT PART OF NUMBER
         AR    GR6,GR3                 ID COLUMN IN RECORD
         L     GR11,SEQWA              LAST SEQUNCE NUMBER
         BCTR  GR1,0                   DECREMENT FOR EXECUTE
         TM    INSERTSW,X'30'     INSERT WITH FORCED RENUMB?   @YA02533
         BC    5,TESTIT           BRANCH TO TEST IF CONT RENUM @YA02533
         CLI   INSERTSW,X'0E'          FORCING INSERT IF '0E'
         BNE   NUMBNORM                NOT '0E'- NUMBER CARD AS USUAL
TESTIT   EX    GR1,COMPARES            COMPARE SEQ (7) TO RECORD (6)
         BNH   RESETNS                 INPUT HIGH STOP FORCING RENUMBER
NUMBNORM CLI   FSTINSRT,X'00'          FIRST DATA RECORD AFTER   A25432
*                           A ./ NUMBER WITH INSERT=YES ?        A25432
         BNE   GOON                    NO,DON'T SET SWITCH AGAIN A25432
         CLI   INSERTSW,X'1F'      INSERT=YES 2:ND TIME        @ZA07377
         BE    GOON                YES,BRANCH                  @ZA07377
         TM    INSERTSW,X'0F'      INSERT=YES?                 @ZA07377
         BNO   GOON                NO,BRANCH                   @ZA07377
         MVI   FSTINSRT,X'0F'      1ST DATA REC IS PROCESSED   @ZA32389
         TM    INSERTSW,X'30'      INSERT WITH FORCED RENUMB?  @ZA07377
         BC    5,GOON              YES,BRANCH                  @ZA07377
         LR    GR8,GR6             POINTER TO SEQUENCE FIELD   @ZA07377
         EX    GR1,CHKSEQNB        TEST FOR BLANK SEQ NUMBER   @ZA07377
         BNE   STOPINST            NOT BLANK SEQ, BRANCH       @ZA07377
GOON     EX    GR1,MOVEPART        MOVE NEW SEQ NUMBER TO CARD @ZA29907
         LR    GR6,GR10                RELOAD POINTER TO START OF CARD
         A     GR11,INCREMWA           ADD INCREMENT FACTOR
         ST    GR11,SEQWA              NEXT SEQUENCE NUMBER
         CLI   INSERTSW,X'0F'          INSERT OPTION
         BNE   TESTLIST                WRITE LAST RECORD
         TM    SEQ1SW,X'F0'            SEQUENCE 1 SATISFIED
         BC    8,TESTLIST              NO- CONTINUE TO WRITE OLD MASTER
         LA    GR9,OMINAREA            OLD MASTER BUFFER
         CR    GR6,GR9                 READY TO WRITE OLD MASTER
         BE    TESTLIST                YES- JUST STARTING BLOCK INSERT
         MVI   DONOTRDC,RESET0         ALLOW READING OF SYSIN
         MVI   INSRTRD,X'0F'           FLAG AS INSERT READ PROTECT GR9
         BAL   GR9,READCC              GET NEXT CARD TO INSERT
WRTINSRT LA    GR6,SWITCHRD+1          BUFFER ADDRESS SYSIN RECORD LAST
         ST    GR3,NEXTINSR            POINTER TO NEXT SYSIN RECORD
         B     TESTLIST                WRITE LAST RECORD
RETRINST CLI   FINICCS,X'0F'           MORE SYSIN
         BE    INSTDONE                FINISH UP SYSIN
         L     GR3,NEXTINSR            GET ADDRESS OF NEXT RECORD
         LA    GR6,SWITCHRD+1          START OF RECORD BUFFER
         MVC   0(80,GR6),0(GR3)        MOVE RECORD TO BUFFER
         CLC   0(2,GR3),DOTSLASH       IS INPUT A CONTROL STATEMENT
         BNE   INSRTANY                ANALYSE SEQUENCE FIELD IF BLANK
         LA    GR10,RESCANIT           RETURN FROM INSRTOVR ./ NEXT
         B     INSRTOVR                END BLOCK INSERT WHEN ./ READ
*                                                                     *
*                 LIST THEN WRITE RECORD- RENUMBER IF NECESSARY       *
*                                                                     *
TESTRCD  CLI   DELETSW,X'0F'           IS DELETE IN EFFECT         5944
         BE    DELETCK                 YES, GO CHECK SEQ TO DELETE 5944
LISTREC  CLI   NUMBRSW,X'0F'           NUMBERING TO BE DONE
         BE    NUMBRCK                 YES
TESTLIST TM    CHNGESW,X'0F'           IS THIS A CHANGE OPERATION M3410
         BZ    NOTEST1                 NO , SKIP THE TEST         M3410
         CLI   REPSW,X'0F'             REPLACE,ALREADY PRINTED  YA01187
         BE    RENUMFOR           YES,AVOID PRINTING TWICE     @YA02533
         CLI   REPSW,X'FF'             PARTIALLY UPDATED        YA01187
         BNE   CONTINU                 NO,CONTINUE NORMAL PROC  YA01187
         MVC   LOGOUTAR+15(80),0(GR6)  MOVE UPDATED CARD TO PRINTA01187
         MVC   LOGOUTAR+95(25),UPDTMS  MOVE UPDATING MESS       YA01187
         MVI   ASISMSW,X'0F'           INDICATE RECORD TO PRINT YA01187
         B     PRINTMOD                GO TO PRINT ROUTINE      YA01187
RENUMFOR CLI   INSERTSW,X'0E'     RENUMBERING FORCED?          @YA02533
         BNE   DONOTPRT           NO,BRANCH TO WRITE           @YA02633
CONTINU  LR    GR9,GR6                 SAVE REGISTER CONTENTS     M3410
         AH    GR6,IDENTXX             POINT TO SEQUENCE FIELD   A24812
         LA    GR7,OLDOMSEQ            LAST NM SEQUENCE FIELD    A24812
         LH    GR1,IDENTY              LENGTH OF SEQUENCE FIELD  A24812
         BCTR  GR1,0                   DECREMENT FOR EXECUTE     A24812
         EX    GR1,COMPARES            COMPARE SEQUENCE NUMBERS  A24812
*                                      OF LAST RECORD WRITTEN TO A24812
*                                      RECORD TO BE WRITTEN      A24812
         BL    GOODSEQ                 BRANCH IF LOW             A24812
         LR    GR3,GR9                 PUT RECORD ADDR IN REG    A24812
         BAL   GR14,PRINTCRD           PRINT CARD                A48737
         B     INVALOP                 GO TO MSG RTN             A48737
GOODSEQ  MVC   OLDOMSEQ(8),0(GR6)      SEQUENCE OF RECORD TO BE  A24812
*                                      WRITTEN                   A24812
         LR    GR6,GR9                 RESTORE REGISTER          A24812
         TM    INSERTSW,X'20'     RECORD TO BE MERGED?         @YA02533
         BO    MERGRENU           YES,BRANCH TO GIVE MSG       @YA02533
         TM    INSERTSW,X'10'          RENUMBERING FORCED       YA01187
         BO    RENUMFC                 RENUMBER                 YA01187
         CLI   INSERTSW,X'0E'           RENUMBERING FORCED      YA01187
         BNE   CHKINSRT                NO,GIVE NO MESSAGE       YA01187
RENUMFC  CLI   REPSW,X'0F'        REPLACE GIVEN?               @YA02533
         BNE   MOVEOLD            NO,BRANCH TO GIVE OLD RECORD @YA02533
MERGRENU MVC   LOGOUTAR+15(80),SWITCHRD+1 MOVE NEW RECORD      @YA02533
         NI    INSERTSW,X'DF'     RESET INSERT RENUM SW        @YA02533
         B     MOVEMSG            GIVE MSG RENUMB FORCED       @YA02533
MOVEOLD  MVC   LOGOUTAR+15(80),OMINAREA MOVE OLD RECORD        @YA02533
MOVEMSG  MVC   LOGOUTAR+95(25),RENUMMS  SET MESSAGE RENUMBER   @YA02533
         MVI   ASISMSW,X'0F'           SET SWITCH FOR PRINT     YA01187
         B     PRINTMOD                 PRINT CARD              YA01187
CHKINSRT CLI   READNOOM,X'0F'           SYSIN RECORD            YA01187
         BNE   NOTEST1                 NO,BYPASS INSERT PRINTINGYA01187
         MVC   LOGOUTAR+95(25),INSERTMS  MOVE INSERT MESSAGE    YA01187
         LA    GR6,SWITCHRD+1     ADDRESS OF SYSIN RECORD      @YA02533
SHRTPRT  LH    GR1,LRECL               LOAD RECORD LENGTH       YA01187
         BCTR  GR1,0                   SUBTRACT ONE BEFORE EXEC YA01187
         LA    GR7,LOGOUTAR+15         LOAD PRINT BUFFER        YA01187
         EX    GR1,LOGMOVE             MOVE CARD TO BUFFER      YA01187
         MVI   ASISMSW,X'0F'           SET SWITCH TO PRINT CARD YA01187
PRINTMOD L     GR15,PRIMEWRT           LOAD PRINT MODULE        YA01187
         BALR  GR14,GR15               BRANCH TO PRINT ROUTINE  YA01187
         TM    INSERTSW,X'20'     INSERT RENUM SW SET?         @YA02533
         BNO   DONOTPRT           NO,GO TO WRITE RECORD        @YA02533
         B     NUMBRCD            BRANCH TO RENUMBER           @YA02533
NOTEST1  CLI   REPSW,X'0E'        REPLACED RECORD LISTED?      @ZA01683
         BE    SHRTPRT            YES PRINT REPLACEMENT        @ZA01683
         CLI   FLLISTSW,X'01'     IS FULL LIST TO BE GIVEN     @ZA01683
         BNE   DONOTPRT                                          A24812
         TM    CHNGESW,X'0F'           CHANGE OPERATION         YA01187
         BC    7,SHRTPRT               YES,PRINT NORMAL LIST=ALLYA01187
         LH    GR1,LRECL               LOGICAL RECORD LENGTH
         BCTR  GR1,0                   DECREMENT FOR EXECUTE MVC
         LA    GR7,LOGOUTAR+40         ADDRESS OF PRINT BUFFER
*                     GR6 POINTS TO RECORD TO BE MOVED                *
         EX    GR1,LOGMOVE             MOVE RECORD TO PRINT BUFFER
         MVI   ASISMSW,X'0F'           SET SWITCH TO PRINT RECORD
         L     GR15,PRIMEWRT           ADDRESS OF MESSAGE WRITER
         BALR  GR14,GR15               PRINT RECORD
DONOTPRT MVI   REPSW,X'00'         RESET SWITCH REPLACE /UPDATE YA01187
         CLI   INPLCSW,X'0F'      IS THIS UPDATE INPLACE           1796
         BE    PUTINPLA                YES- PUT RECORD IN PLACE
         LH    GR15,NMBLOCKT           LOAD TEMPORARY BLOCKING FACTOR
         L     GR14,NMWRITET           WRITE BLOCK OR RECORD NOW
         LH    GR1,LRECL               SIZE OF LOGICAL RECORD
         BCTR  GR1,0                   DECREMENT FOR EXECUTE OF MVC
         LR    GR7,GR14                PUT ADDRESS IN PROPER REGISTER
         EX    GR1,LOGMOVE             MOVE RECORD TO BUFFER
         MVI   NMBUFFR,X'0F'           SET SWITCH RECORDS IN NM BUFFER
         BCT   GR15,BLOCKIT            BRANCH IF NOT FULL BLOCK
         MVC   NMBLOCKT(2),NMBLOCKP    RESTORE BLOCKING FACTOR
NMLSTWRT L     GR9,NMWRITEP            START OF NEW MASTER RECORD
         L     GR6,DCBNM               ADDRESS OF DCB
         ST    GR9,NMWRITET            STORE POINTER TO START
NMTOTCHK CLI   TOTALSW,X'FE'            IS TOTALING REQUESTED      UL0H
         BNL   NMTOTRTN                 IF SO,GIVE USER RTN CONTROLUL0H
WRITENM  WRITE DECBNM,SF,(GR6),(GR9),'S'                           UL0H
         CLI   LBLRETCD,X'10'          USER LABEL RETURN CODE = 16 UL0H
         BE    HEADRMSG                IF SO, PRINT MSG AND TERMM  UL0H
         CLI   NMBUFFR,X'01'           LAST WRITE NEW MASTER
         BE    CHECK                   YES- RETURN SET IN GR7
         LA    GR9,CHECK               LOAD RETURN ADDRESS FROM ROUTINE
CHECKRET CLI   INSRTRD,X'0F'           WRITING INSERT RECORDS
         BE    SETUP7                  YES- PROCESS MORE INSERT RECORDS
         TM    REPROSW,X'0E'           REPRODUCING FROM OLD MASTER
         BC    1,SETRET7               YES- READ OLD MASTER
         LA    GR7,READCC              RETURN FOR MORE SYSIN CARDS
         BR    GR9                     RETURN FROM ROUTINE
SETRET7  LA    GR7,READOM              READ OLD MASTER RETURN SET
         BR    GR9                     RETURN FROM ROUTINE
SETUP7   LA    GR7,RETRINST            RETURN TO PROCESS INSERT
         BR    GR9                     MAY GO TO BLOCKIT NOT CHECK
NMTOTRTN CLI   PSSWNM,X'00'             IS DATA SET PHY. SEQ       UL0H
         BNE   NOTOTPDS                 IF NOT,ERROR               UL0H
         CLI   TOTALSW,X'FF'            FIRST ENTRY TO USER        UL0H
         BNE   USRTOTAL                 IF NOT,BYPASS MESG         UL0H
         MVI   TOTALSW,X'FE'            FLAG AS NOT FIRST ENTRY    UL0H
         LA    GR11,TOTENTMS            MESSAGE CONSTANT           UL0H
         L     GR15,PRIMEWRT            ADDRESS OF LOG ROUTINE     UL0H
         BALR  GR14,GR15                GIVE MESSAGE               UL0H
USRTOTAL LA    GR1,TOTLPARM             ADDRESS OF TOTAL PARAMETRS UL0H
         ST    GR9,TOTLPARM             PLACE BUFFER ADDR IN PRMLST000I
         L     GR15,TOTALADR            USER ROUTINE ADDRESS       UL0H
         BALR  GR14,GR15                GIVE USER CONTROL          UL0H
         STH   GR15,TOTRTNCD            STORE USER RETURN CODE     UL0H
         CLC   TOTRTNCD(2),SIXTEEN      RC=16                      UL0H
         BNL    TOT16TRM               IF SO, PREPARE TO TERMINATE 000H
         CLC   TOTRTNCD(2),EIGHT        RC=8                       UL0H
         BE    TOT08TRM                IF SO, CLOSE IT UP          000H
         CLC   TOTRTNCD(2),FOUR+2       RC=4                       UL0H
         BE    WRITENM                  THEN WRITE RECORD          UL0H
         MVI   TOTALSW,X'00'            DISCONTINUE TOTALING       UL0H
         CLC   TOTRTNCD(2),ZERO         RC=0                       UL0H
         BE    ZERORTCD                 IF SO,PRINT  RC  MSG       UL0H
         LA    GR11,TOTBDRCD            INVALID  RETURN CODE  MSG  UL0H
         B     TERMMESG                 PRINT  MSG                 UL0H
ZERORTCD LA    GR11,TOTTERM             TOTAL TERM MSG             UL0H
TERMMESG L     GR15,PRIMEWRT            LOG ROUTINE ADDRESS        UL0H
         BALR  GR14,GR15                GIVE MESSAGE               UL0H
         B     WRITENM                  WRITE RECORD               UL0H
TOT16TRM LA    GR11,TOTTERM            PREPARE TO PRINT TERM MESSAG000H
         L     GR15,PRIMEWRT           ADDRESS OF LOG ROUTUNE      000H
         BALR  GR14,GR15               PRINT MESSAGE               000H
         B     DEACTVCT                PREPARE TO DEACTIVATE EXITS 000H
TOT08TRM LA    GR11,TOTTERM            PREPARE TO PRINT TERM MESSAG000H
         L     GR15,PRIMEWRT           ADDRESS OF LOG ROUTUNE      000
         BALR  GR14,GR15               PRINT MESSAGE               000H
         B     LASTLEG                 PREPARE TO EXIT             000H
CHECK    CHECK DECBNM                  WAIT FOR END OF WRITE
RTRN     BR    GR7                     RETURN TO PROPER ROUTINE    5324
BLOCKIT  STH   GR15,NMBLOCKT           STORE REMAINING RECORD/BLOCK CNT
         L     GR6,DCBNM               ADDRESS OF NEW MASTER DCB
         LH    GR6,BLRECL(GR6)         OBTAIN LOGICAL RECORD LENGTH
         AR    GR14,GR6                POINTER TO NEXT RECORD SLOT
         ST    GR14,NMWRITET           ADDRESS FOR NEXT RECORD
         BAL   GR9,CHECKRET            ESTABLISH RETURN IN GR7
         BR    GR7                     RETURN TO PROPER ROUTINE
WRTLASTB L     GR14,NMWRITET           ADDRESS OF LAST ENTRY
         L     GR15,NMWRITEP           START OF BUFFER
         L     GR11,DCBNM              ADDRESS OF NEW MASTER DCB
         SR    GR14,GR15               SIZE OF LAST BLOCK
         LTR   GR14,GR14               ARE THERE ANY RECORDS       4929
         MVI   NMBUFFR,X'01'           FLAG AS LAST WRITE          4929
         BZ    STOW2                   FINISH UP                   4929
         MVC   SARG(2),BBLKSI(GR11)    SAVE BLOCKSIZE
         STH   GR14,BBLKSI(GR11)       PUT LENGTH OF LAST BLOCK
         BAL   GR7,NMLSTWRT            WRITE LAST BLOCK
         MVC   BBLKSI(2,GR11),SARG     RESTORE BLOCKSIZE
         B     STOW2                   FINISH UP
*                                                                     *
*                 DETERMINE OPERATION FOR CHANGE FUNCTION             *
*                                                                     *
CHNGCOMD CLI   DELETSW,X'0F'           IS DELETE IN EFFECT
         BE    DELETCK                 YES- CHECK RANGE
         CLI   FINICCS,X'0F'           END OF SYSIN
         BE    WRITOLD1                FINISH OLD MASTER
         TM    COMDCD2,X'0F'           COMMAND PENDING         @ZA01690
         BO    WRITOLD1              YES- FINISH UP OLD MASTER @ZA01690
         MVI   DONOTRDC,X'00'          CLEAR DO-NOT-READ CC SWITCH
         CLI   OMEND,X'0F'             ANY MORE OLD MASTER RECORDS
         BNE   BKINSERT               YES-COMPARE SEQUENCE NO      2518
         CLI   CHNGESW,X'0E'          IS THIS A CHANGE FUNCTION    2518
         BNE   CHKADREP               NO-TREAT AS AN ADD           2518
BKINSERT TM    INSERTSW,X'0F'         BLOCK INSERTING              2518
         BC    1,BLOCKINS              YES
         LA    GR7,SWITCHRD+1          ADDRESS OF DATA RECORD
         CLC   0(2,GR7),DOTSLASH       IS A CONTROL CARD IN SWITCHRD
         BE    NEXTDELT                YES- SCAN THE CARD
         LA    GR6,LASTCTRC            PRESENT SYSIN SEQUENCE NUMBER
         BAL   GR9,TESTSEQR            COMPARE NEW SYSIN (7) TO OLD (6)
         BNH   OUTOFSEQ                NEW ONE LOWER- OUT OF SEQUENCE
         LA    GR6,TEMPSEQ       TEMPORARY SEQUENCE BUFFER       A30939
         A     GR6,LENGTH                                          UL0H
         EX    GR1,MOVEPART            MOVE NEW SYSIN (7) TO TEMPSEQ 6
         CLI   OMEND,X'0F'             ANY MORE OM RECORDS         2518
         BNE   COMPSEQ                 NO-MOVE IN LATEST SYSIN SEQ 2518
         MVC   LASTCTRC(8),TEMPSEQ     MOVE IN LATEST SYSIN SEQ    2518
         LA    GR6,SWITCHRD+1          ADDRESS OF SYSIN RECORD     2518
         MVI   READNOOM,X'0F'          OLD MASTER HIGH-SAVE IT     2518
         CLI   INSERTSW,X'0E'          RENUMBERING FORCED?     @ZA03382
         BNE   LISTREC                 NO,INSET SYSIN RECORD?  @ZA03382
         OI    INSERTSW,X'20'          YES,SET INSERT RENUM SW @ZA03382
         B     CHKINSRT                GO TO GIVE INSERT MSG   @ZA03382
COMPSEQ  LA    GR7,OMINAREA            ADDRESS OF OLD MASTER RECORD2518
         AR    GR7,GR3                 POSITION OF SEQUENCE NUMBER OM
         LA    GR6,SWITCHRD+1          ADDRESS OF SYSIN RECORD
         AR    GR6,GR3                 POSITION OF SEQUENCE NUMBER
         CLI   INPLCSW,X'0F'           IS THIS UPDATE IN PLACE
         BE    INPLACEU                YES- CHECK IF EQUAL ONLY
         EX    GR1,COMPARES            COMPARE OM (7) TO SYSIN (6)
         BL    WRITOLD1                OLD MASTER IS LOW- WRITE IT
         LA    GR6,SWITCHRD+1          ADDRESS OF SYSIN RECORD
         MVC   LASTCTRC(8),TEMPSEQ     MOVE IN LATEST SYSIN SEQUENCE
         BE    WRITEREP                REPLACE OLD MASTER WITH SYSIN
         MVI   READNOOM,X'0F'          OLD MASTER HIGH- SAVE IT
         CLI   INSERTSW,X'0E'     RENUMBERING FORCED?          @YA02533
         BNE   LISTREC            NO,INSERT SYSIN RECORD       @YA02533
         OI    INSERTSW,X'20'     SET INSERT RENUM SW          @YA02533
         B     CHKINSRT           GO TO GIVE INSERT MSG        @YA02533
INSTDONE CLI   OMEND,X'0F'             IS OLD MASTER DONE
         BE    STOWNAME                YES- FINISH THIS MEMBER
         BAL   GR10,INSRTOVR           CHECK TO SEE IF MUST RENUMBER
WRITOLD1 CLI   INPLCSW,X'0F'           UPDATE INPLACE
         BE    NUMBRSX                 YES CHECK IF RENUMBER SYSIN DONE
         LA    GR9,LISTREC             RETURN ADDRESS
WRITENOW MVI   DONOTRDC,X'0F'          SET TO INHIBIT READING NEXT CT
         MVI   READNOOM,RESET0         CLEAR DO-NOT-READ OLD MASTER SW
         LA    GR6,OMINAREA            ADDRESS OF OLD MASTER RECORD
         BR    GR9                     RETURN TO LISTREC OR TESTLIST
*                                                                     *
*                UPDATE INPLACE LOGIC- REPLACEMENT RECORDS ONLY       *
*                                                                     *
INPLACEU EX    GR1,COMPARES            EXECUTE COMPARE OM TO SYSIN
         BL    NUMBRSX                 CHECK IF MUST NUMBER
         BH    INVALID            NEVER WILL GET EQUAL COMPARE @ZA07361
         LA    GR6,SWITCHRD+1          LOCATION OF SYSIN RECORD
         MVI   EQUALREC,X'0F'          FLAG AS RECORD REPLACED
         B     WRITEREP                CHECK IF COLUMN UPDATE
INVALID  LA    GR3,SWITCHRD+1     LOAD ADDRESS OF RECORD       @ZA07361
         BAL   GR14,PRINTCRD      GO AND PRINT FAILING RECORD  @ZA07361
         B     INVALOP            GIVE MSG 'INVALID OPERATION' @ZA07361
NUMBRSX  LA    GR6,OMINAREA            OLD MASTER RECORD ADDRESS
         MVI   DONOTRDC,X'0F'          DO NOT READ MORE SYSIN
         CLI   NUMBRSW,X'0F'           RECORD TO BE RENUMBERED
         BE    NUMBRCK                 YES - NUMBER IT, BUT      A28728
*                                      FIRST CHECK THE RANGE     A28728
         CLI   FLLISTSW,X'01'          LIST=ALL SPECIFIED       YA01187
         BNE   READOM                  IF NOT,READ NEXT RECORD  YA01187
         MVC   LOGOUTAR+15(80),0(GR6)  MOVE CARD TO BUFFER      YA01187
         MVI   ASISMSW,X'0F'           FORCE PRINTING           YA01187
         L     GR15,PRIMEWRT           LOAD PRINT MODULE        YA01187
         BALR  GR14,GR15               BRANCH TO PRINT ROUTINE  YA01187
         B     READOM                  NO- ON TO NEXT RECORD
PUTINPLA LH    GR1,LRECL               LOGICAL RECORD LENGTH
         L     GR7,LASTRECB            LOCATION OF BAD RECORD
         BCTR  GR1,0                   DECREMENT FOR EXECUTE
         MVI   REPLUPDT,X'0F'          FLAG AS RECORD UPDATED
         EX    GR1,LOGMOVE             MOVE SYSIN (6) TO REPLACE OM (7)
         MVI   DONOTRDC,X'0F'          DO NOT READ MORE SYSIN
         CLI   EQUALREC,X'0F'          JUST REPLACED A RECORD
         BNE   READOM                  NO- NUMBERED AN OLD ONE
         MVI   EQUALREC,RESET0         RESET RECORD REPLACED SWITCH
         B     READCT1                 READ NEXT SYSIN RECORD
WRTEREPR NOPR  GR0                     INSERT NOOP FOR ZAP REL 19
         NOPR  GR0                     INSERT CODE FOR SZAP REL 19
         NOPR  GR0                     INSERT CODE FOR SZAP REL 19
         WRITE DECBOM,SF,(GR3),(GR8),MF=E WRITE BLOCK WITH REPL  A38750
         MVI   REPLUPDT,X'00'          RESET BAD RECORD SWITCH
         CHECK DECBOM                  WAIT FOR WRITE
         MVC   62(2,GR3),FLBLK    RE-INITIALUZE BLOCKSIZE          3628
         MVC   SHRTBLK(2),FLBLK   MAY READ FULL BLOCK NEXT         3628
         B     READSW1                 GET MORE OLD MASTER RECORDS
*                                                                     *
*               REPLACEMENT OF OLD MASTER RECORDS- PARTIAL UPDATE TEST*
*                                                                     *
WRITEREP MVI   READNOOM,RESET0         BE SURE TO READ MORE OLD MASTER
         CLI   INSERTSW,X'00'          ANY INSERT FUNCTION?    @ZA29907
         BNE   NOTYET                  NO,MARK REC REPLACEMENT @ZA29907
         CLI   RENUMALL,X'0F'          RENUMBERING?            @ZA31995
         BE    NOTYET                  YES,FLAG REC REPLACED   @ZA31995
         LR    GR7,GR6                 ADDR OF SYSIN RECORD    @ZA29907
         LR    GR15,GR6                SAVE THAT ADDRESS       @ZA29907
         LA    GR6,NUMRSEQ2            UPPER LIMIT NUMBERING   @ZA29907
         BAL   GR9,TESTSEQR            TEST IF BEYOND RANGE    @ZA29907
         LR    GR6,GR15                RESTORE REGISTER        @ZA29907
         BNH   NOTYET                  NOT UPPER LIMIT YET     @ZA29907
         NI    SEQ1SW,X'0F'            RESET SEQ1 SATISFIED SW @ZA29907
         MVI   NUMBRSW,RESET0          RESET NUMBER SW         @ZA29907
NOTYET   EQU   *                                               @ZA29907
         MVC   LOGOUTAR+95(25),REPLMSG  MOVE IN REPLACE MESSAGE YA01187
         MVC   LOGOUTAR+15(80),OMINAREA  MOVE CARD              YA01187
         OI    REPSW,X'0E'        SET REPLACED RECORD LISTED   @ZA01683
         MVI   ASISMSW,X'0F'           SET PRINT SWITCH         YA01187
         L     GR15,PRIMEWRT           LOAD PRINT MODULE        YA01187
         BALR  GR14,GR15               BRANCH TO PRINT MODULE   YA01187
         MVC   LOGOUTAR+95(25),REPMES   GIVE REPLACEMENT MESS   YA01187
         CLI   INSERTSW,X'0E'     RENUMBERING FORCED?          @YA02533
         BE    MOVENEW            YES,BRANCH TO MOVE NEW CARD  @YA02533
         CLI   NUMBRSW,X'0F'      IS NUMBERING TO BE DONE?     @YA01723
         BE    NEWSEQ             YES,GO TO INSERT NEW NUMBER  @YA01723
         MVC   OLDOMSEQ(8),0(GR7)  SEQ OF REC TO BE WRITTEN    @ZA29830
MOVENEW  MVC   LOGOUTAR+15(80),SWITCHRD+1 MOVE SYSIN CARD      @YA02533
         MVI   ASISMSW,X'0F'            SET PRINT SWITCH        YA01187
         L     GR15,PRIMEWRT           LOAD PRINT MODULE        YA01187
         BALR  GR14,GR15               BRANCH TO PRINT MODULE   YA01187
         MVI   REPSW,X'0F'             SET SWITCH REPLACED REC  YA01187
NEWSEQ   CLI   COLUMNSW,X'0F'          SELECTIVE COLUMN UPDATE @YA01723
         BNE   LISTREC                 NO- REPLACE ENTIRE CARD
         CLC   UPDATCOL(2),ZERO        CHECK FOR TOTAL CHANGE      3511
         BE    LISTREC                 REPLACE ENTIRE CARD         3511
         MVI   REPSW,X'FF'             SET SWITCH UPDATED RECORDYA01187
         LH    GR1,UPDATCOL            AMOUNT OF CARD NOT CHANGED
         BCTR  GR1,0                   DECREMENT FOR EXECUTE
         LA    GR7,OMINAREA            IMAGE OF OLD MASTER RECORD
         EX    GR1,MOVEPART            MOVE UNCHANGED PART TO SWITCHRD
         B     LISTREC                 WRITE OUT PARTIALLY UPDATED REC
*                                                                     *
*        GENERAL SEQUENCE CHECKING ROUTINES FOR DELETE AND NUMBER     *
*  COMPARE EXECUTED FOR RECORD ADDRESSED BY GR7 AND SEQUENCE NUMBER   *
*  BUFFER ADDRESSED BY GR6. RETURN THEN GIVEN RELATIVE TO GR9 FOR TEST*
*  OF CONDITION CODE SET BY COMPARE.                                  *
*                                                                     *
DELETCK  LA    GR6,DELTSEQ1            STARTING SEQUENCE NUMBER DELET
         LA    GR7,OMINAREA            ADDRESS OF OLD MASTER RECORD
         LA    GR9,SEQ1SET             RETURN FROM ROUTINE
         CLI   OMEND,X'0F'             IS OLD MASTER THROUGH
         BE    BADDELET                YES- ERROR- DELETE NOT DONE
TESTSEQR ST    GR10,TEMP10                                         UL0H
         LA    GR10,8                                              UL0H
         LH    GR3,IDENTXX             SEQUENCE START COLUMN
         LH    GR1,IDENTY              SEQUENCE SIZE
         SR    GR10,GR1                                            UL0H
         AR    GR6,GR10                                            UL0H
         AR    GR7,GR3                 START OF SEQUENCE NUMBER OM
         BCTR  GR1,0                   DECREMENT FOR EXECUTE
         EX    GR1,COMPARES            COMPARE SEQUENCE NUMBERS
         ST    GR10,LENGTH                                         UL0H
         L     GR10,TEMP10                                         UL0H
         BR    GR9                     RETURN FROM COMPARE ROUTINE
SEQ1SET  BE    NUMERON            DELETE THIS RECORD             A32024
         BH    SEQ2SET            TEST FOR DELETE RANGE          A32024
         LA    GR6,LASTCTRC       ADDRESS LAST CT/OM NUMBER      A32024
         A     GR6,LENGTH         JUSTIFY FOR LENGTH             A32024
         EX    GR1,MOVEPART       GET LAST OLD MSTR NUMBER IN IT A32024
         B     WRITOLD1           RETURN WITHOUT DELETE          A32024
SEQ2SET  TM    SEQ1SW,X'0F'       WAS SEQ1 SATISFIED             A32024
         BC    12,BADDELET             NO- RANGE INVALID
         LA    GR7,OMINAREA            OLD MASTER RECORD
         LA    GR6,DELTSEQ2            ENDING SEQUENCE NUMBER DELET
         BAL   GR9,TESTSEQR            COMPARE SEQUENCE NUMBERS
         BL    GETNEXT1                DELETE STATEMENT- WITHIN RANGE
         BH    BADDELET                END OF DELETE- RANGE INVALID
ANYSECND NI    SEQ1SW,X'F0'            RESET SEQ1 SATISFIED- DELETE
         CLI   DELET2SW,X'0F'          CONSECUTIVE DELETE STATEMENTS
         MVI   DELETSW,RESET0          RESET DELETE SWITCH
         BNE   GETNEXT1                DELETE LAST RECORD
         MVI   READNOOM,X'00'  MAKE SURE 1ST DELETE IS EFFECTIVE 12737
         MVC   LOGOUTAR+95(25),DELETMSG SET DELETE MSG         @YA02554
         MVC   LOGOUTAR+15(80),OMINAREA MOVE CARD              @YA02554
         MVI   ASISMSW,X'0F'      SET PRINT SW                 @YA02554
         L     GR15,PRIMEWRT      LOAD PRINT MODULE            @YA02554
         BALR  GR14,GR15          GO TO PRINT MODULE           @YA02554
         MVI   DELET2SW,RESET0         RESET DELETE PENDING SWITCH
NEXTDELT MVI   SWITCHRD,RESET0         CLEAR CARD SCAN SWITCHES
         CLI   NUMBR2SW,X'0F'          NUMBER CARD PENDING
         BE    PARTNUMB                YES- CHECK SEQUENCE NUMBER
         MVI   DONOTRDC,X'00'          READ NEXT CONTROL CARD
         MVI   PARAMSW,RESET0          RESET PARAMETER FOLLOWS SWITCH
         L     GR15,SCANENTR           ENTRY POINT OF SCAN ROUTINE
         BR    GR15                    ENTER SCAN ROUTINE
NUMERON  OI    SEQ1SW,X'0F'            SET SEQ1 SATISFIED- DELETE
         CLI   NUMBRSW,X'0F'           NUMBERING TO BE DONE
         BNE   CHK1AND2                CHECK IF SEQ1 EQUALS SEQ2
         LA    GR7,OMINAREA            ADDRESS OF OLD MASTER RECORD
         LA    GR6,NUMRSEQ1            NUMBER SEQUENCE 1
         BAL   GR9,TESTSEQR            DELETE CARD START NUMBER RANGE
         BE    INVALOP                 CANNOT DELETE START OF NUMBER
CHK1AND2 CLC   DELTSEQ1(8),DELTSEQ2    DOES SEQ1 EQUAL SEQ2
         BE    ANYSECND                YES DELETE DONE
GETNEXT1 MVI   READNOOM,RESET0         ALLOW READ OF OLD MASTER
         MVC   LOGOUTAR+95(25),DELETMSG SET DELETE MESSAGE      YA01187
         MVC   LOGOUTAR+15(80),OMINAREA  MOVE CARD              YA01187
         MVI   ASISMSW,X'0F'              PRINT SWITCH          YA01187
         L     GR15,PRIMEWRT             LOAD PRINT MODULE      YA01187
         BALR  GR14,GR15               GO TO PRINT MODULE       YA01187
         B     READOM                  FINISH DELETE- THEN INSERT
PARTNUMB MVI   DONOTRDC,X'0F'          READ NO MORE SYSIN
         LA    GR6,OMINAREA            OLD MASTER RECORD
*                                                                     *
*    CHECKS FOR TOTAL RENUMBERING, BLOCK INSERTING, OR SPECIFIC       *
*  RENUMBERING.  UPON ENTRY, GR6 POINTS TO START OF RECORD.           *
*                                                                     *
NUMBRCK  CLI   RENUMALL,X'0F'          TOTAL RENUMBERING
         BE    NUMBRCD                 YES
         CLI   OMEND,X'0F'             OLD MASTER FINISHED
         BE    CHECKNM                 YES- CHECK NUMBER RANGE
         TM    INSERTSW,X'0F'          INSERT
         BC    1,BLOCKINS              YES- FIRST LEVEL '0F'
         BC    4,NUMBRCD               RENUMBER FORCED BY EARLY INSERT
         B     ENTNUMB                 CHECK NUMBER RANGE
BLOCKINS LA    GR7,OMINAREA            BLOCK INSERT OR ONLY OM LEFT
         LA    GR6,NUMRSEQ1            STARTING NUMBER
         CLI   OMEND,X'0F'             END OF FILE ON OM YET?    A24812
         BE    CHKSEQ1            YES,DON'T TEST SAME SEQ AGAIN  A24812
         BAL   GR9,TESTSEQR            TEST RANGE
         BL    CHKWRTOM                WRITE OLD MASTER
         BE    STARTIN                  BRANCH                 @ZA01657
CHKSEQ1  TM    SEQ1SW,X'F0'            SEQ1 SATISFIED?           A24812
         BC    14,BADNUMBR             NO- NUMBER RANGE UNSATISFIED
         TM    INSERTSW,X'0F'          INSERTING RECORDS
         BC    1,SETINSRT              RESET INSERT SWITCH
         LA    GR6,NUMRSEQ2            TOP OF NUMBER RANGE
         LA    GR7,OMINAREA            ADDRESS OF OLD MASTER RECORD
         BAL   GR9,TESTSEQR            TEST IF OM OUT OF NUMBER RANGE
         BH    RESETGET                YES- THROUGH WITH NUMBERING
RENUMSOM LA    GR6,OMINAREA            ADDRESS OF OLD MASTER RECORD
         MVI   READNOOM,RESET0         ALLOW READING OF OLD MASTER
         MVI   DONOTRDC,X'0F'          READ NO MORE SYSIN
         B     NUMBRCD                 RENUMBER CARD
RESETGET LA    GR10,WRITEOM            RETURN FROM RESETTING SWITCHES
         B     RESETNMB                RESET SWITCHES
CHECKNM  CLI   INPLCSW,X'0F'           IS THIS UPDATE IN PLACE
         BE    CHKALIAS                YES- FINISH UPDATE INPLACE
         LA    GR7,SWITCHRD+1          DATA CARD BUFFER ADDRESS
         CLC   0(2,GR7),DOTSLASH       A CONTROL CARD IN BUFFER
         BE    RET2SCAN           YES,SCAN THE CARD             YA01677
         LA    GR6,NUMRSEQ1            STARTING NUMBER LOWER LIMIT
         BAL   GR9,TESTSEQR            TEST NUMBER RANGE
         LA    GR6,SWITCHRD+1          ADDRESS OF RECORD
         BNL   NUMBRCD                 NUMBER CARD
BADNUMBR LA    GR11,NUMMSG             INVALID NUMBER RANGE
         B     GIVENGO                 GIVE MESSAGE AND CONTINUE
*                                                                     *
*   CHECK NUMBER RANGE AGAINST RECORD READY TO BE WRITTEN             *
*              SPECIFIC RENUMBERING ROUTINE                           *
*                                                                     *
ENTNUMB  LR    GR7,GR6                 REGISTER 6 POINTS TO RECORD
         LR    GR10,GR6                SAVE POINTER IN STABLE REGISTER
         LA    GR6,NUMRSEQ1            STARTING SEQUENCE NUMBER
         BAL   GR9,TESTSEQR            CHECK LOWER NUMBER RANGE
         BL    WRITEOM                 LOW- WRITE OLD MASTER
         BE    SETNUM1                 SET SEQ1 SATISFIED SWITCH
         TM    SEQ1SW,X'F0'            SEQ1 SATISFIED- NUMBER
         BC    14,BADNUMBR             NO- INVALID NUMBER RANGE
         LA    GR6,NUMRSEQ2            LOAD UPPER NUMBER LIMIT
         LR    GR7,GR10                LOAD POINTER TO RECORD
         BAL   GR9,TESTSEQR            TEST IF BEYOND NUMBER RANGE
         BH    OVERSEQ2                YES,GO AND RESET SEQ1   @ZA29832
         CLI   NUMBR2SW,X'0F'          CONSECUTIVE NUMBER CARD @ZA29832
         BE    SETNUM1                 YES,RESET READNOOM SW   @ZA29832
         B     NUMBRIT                 RENUMBER RECORD         @ZA29907
OVERSEQ2 NI    SEQ1SW,X'0F'            RESET SEQ1 SATISFIED    @ZA29832
         CLI   NUMBR2SW,X'0F'          CONSECUTIVE NUMBER CARDS
         MVI   NUMBRSW,RESET0          RESET NUMBER SWITCH
         BNE   WRITEOM                 WRITE RECORD- NO MORE NUMBERING
         MVI   NUMBR2SW,RESET0         CLEAR NUMBER PENDING SWITCH
         MVI   READNOOM,X'0F'          SAVE OLD MASTER RECORD
         B     NEXTDELT                SCAN THE NUMBER CARD
STARTIN  OI    SEQ1SW,X'F0'            SEQ1 SATISFIED- NUMBER
CHKWRTOM TM    INSERTSW,X'10'          DOING BLOCK INSERT OR RENUMBER
         BC    1,RENUMSOM              YES- RENUMBER RECORD
WRITEOM  LA    GR9,TESTLIST            RETURN FROM WRITENOW    @ZA29907
         CLI   INSERTSW,X'0F'          INSERT OPERATION?       @ZA32389
         BE    WRITENOW                YES,GO AND WRITE RECORD @ZA32389
         CLI   READNOOM,X'0F'          SHOULD WE READ FROM OM? @ZA29832
         LA    GR6,SWITCHRD+1          ADDR OF SYSIN RECORD    @ZA29832
         BE    TESTLIST                NO,DON'T READ FROM OM   @ZA29832
         B     WRITENOW                WRITE OLD MASTER RECORD AS IS
NONBLNKI LA    GR10,CHNGCOMD           NOT ./ NEXT RECORD NON-BLANK SEQ
         LA    GR7,OMINAREA(GR3)  GET OLD MASTER RECORD NUMBER   A35458
         EX    GR1,COMPARES       NEXT OM REC LOW OR EQ AS SYSIN A35458
         BNH   INSRTOVR                DATA SEQ AFTER INSERT OK  A44323
         LA    GR3,SWITCHRD+1          LOAD SYSIN IN BUFFER      A44323
         BAL   GR14,PRINTCRD           PRINT CARD WITH INVAL.SEQ A44323
         B     INVALOP                 PRINT INVALOP MESSAGE     A44323
INSRTOVR MVI   INSRTRD,RESET0          RESET INSERT READ SWITCH
         NI    SEQ1SW,X'0F'       RESET INSERT SWITCH            A35458
         MVI   FSTINSRT,X'00'     RESET FOR NEXT BLOCK INSERT    A25432
         LA    GR7,OMINAREA            NEXT SEQUENCE NUMBER OLD MASTER
         LA    GR6,LASTSEQN            SEQ NUMBER OF LAST INSERT RECORD
         BAL   GR9,TESTSEQR            TEST IF OM GREATER THAN INSERT
         BH    NOMORENB                NO PROBLEM- OM IS HIGH
         MVI   INSERTSW,X'0E'          MUST RENUMBER DATA SET
         BR    GR10                    RETURN- RESCANIT IF ./, CHNGCOMD
INSRTANY LH    GR3,IDENTXX             SEQUENCE COLUMN
         AR    GR6,GR3                 POSITION OF SEQUENCE NUMBER CT
         AR    GR7,GR3                 POSITION OF SEQUENCE NUMBER OM
         LR    GR8,GR6                 POINTER TO CT SEQUENCE FIELD
         LH    GR1,IDENTY              SEQUENCE SIZE
         BCTR  GR1,0                   DECREMENT FOR EXECUTE
         EX    GR1,CHKSEQNB            TEST FOR BLANK SEQUENCE NUMBER
         BE    OKIFINST                YES- OK IF INSERT OPTION
STOPINST TM    INSERTSW,X'0F'         INSERT NON-BLANK SEQ NUM @ZA07377
         BC    1,NONBLNKI              YES- BLOCK INSERT DONE
         CLI   READNOOM,X'0F'          WAS OM RECORD READ
         BNE   COMPARS                 YES- CONTINUE
         MVI   READNOOM,RESET0         RESET SWITCH TO ALLOW OM READ
         B     READOM                  READ OLD MASTER
RESETNS  LR    GR6,GR10                RESTORE POINTER TO CARD
         TM    INSERTSW,X'10'          UP TO NEXT BLOCK INSERT CARD
         LA    GR10,TESTLIST           RETURN FROM THIS ROUTINE
         BC    1,RESET1F               UP TO NEXT BLOCK INSERT CARD
NOMORENB MVI   INSERTSW,RESET0         RESET INSERT SWITCH
RESETNMB MVI   NUMBRSW,RESET0          RESET NUMBER SWITCH
         BR    GR10                    RETURN- RESCANIT IF ./, CHNGCOMD
RESET1F  MVI   INSERTSW,X'0F'          RESET TO NO LONGER FORCE RENUMBR
         MVC   SEQWA(4),TEMPSEQA       SET UP NEW1 FOR NEXT BLOCK INSRT
         MVC   NEW1NUMB(L8),NEW1TEMP                            YM07522
         MVC   INCREMWA(8),TEMPINCR    SET UP INCR FOR NEXT BLOCK INSRT
         BR    GR10                    RETURN                    A41743
COMPARS  EX    GR1,COMPARES            COMPARE SEQUENCE NUMBER
         BNH   RENINSNW                OLD MASTER IS EQUAL OR LOW
STOPRD   MVI   READNOOM,X'0F'          OLD MASTER HIGH- SAVE IT
RENINSNW LA    GR6,SWITCHRD+1          ADDRESS OF NEW RECORD
         MVI   DONOTRDC,RESET0         RESET TO READ NEXT SYSIN RECORD
         B     NUMBRCD                 RENUMBER AND INSERT IT
OKIFINST TM    INSERTSW,X'0F'          TEST IF INSERT OPTION
         BC    14,INVALOP              NO- WILL NOT ACCEPT BLANK SEQ
         B     STOPRD                  NUMBER AND INSERT CARD
SETINSRT MVI   INSERTSW,X'0F'          BE SURE SET FROM '1F' TO '0F'
         B     STOPRD                                            12737
*                                                                     *
*    DETERMINE IF TERMINATE (PS) OR FLUSH TO NEXT MEMBER-  QUIT IF PS
*                                                                     *
CHKIFEND TM    EODCTSW,X'0E'           END OF SYSIN              A45153
         BO    RESTORE            YES-STOP NOW-DO NOT STOW     @ZA06529
FLUSHALL CLI   INPLCSW,X'0F'           UPDATE INPLACE WAS ATTEMPTED
         MVI   TEMPCON,X'04'           SET CONDITION CODE
         BE    STOPNOW                 UPDATE INPLACE- QUIT
         CLI   STARTMEM,X'0F'          MEMBER UPDATE STARTED
         L     GR11,DCBNM              ADDRESS OF NEW MASTER DCB
         BNE   NONEUSED                NO SPACE USED BY BAD MEMBER
         MVC   BFDAD(8,GR11),FDADSET   RESTORE TO START OF AREA
         MVC   TRBAL(2,GR11),TRKBAL   RESTORE PREVIOUS TRACK BAL   5666
NONEUSED TM    RIDIT2SW,X'0F'          TEST IF DELETE/NUMBER OR COMMAND
         BC    1,DETAILGO              DELET/NUMBER SCAN BEYOND
         BC    4,ENDFLUSH              NEXT COMMAND STATEMENT
         CLI   ENDONLY,X'0F'           FLUSHING FOR ENDUP ONLY     4929
         BE    READCC                  YES-PRINT NO MSG-CONTINUE   4929
         CLI   FIRSENTR,BIT10          FIRST ENTRY CARD OUT OF SEQUENCE
         BE    SCANOUT                 YES- NOTHING ANALYSED YET
         LA    GR3,SCANOUT             SEE IF PROCESS ANOTHER MEMBER
GOONTEST CLI   PSSWNM,X'0F'         IS NEW MASTER PARTITIONED    A30480
         BNE   FINERRA2           NO.. QUIT                      A30480
         CLI   PSSWOM,X'0F'       IS OLD MASTER PARTITIONED ?    A30480
         BCR   8,GR3              YES.. RETURN                   A30480
         CLI   CTONLYSW,X'01'           PARM=NEW SPECIFIED       A41756
         BCR   8,GR3                    YES,RETURN               A41756
         OI    CTONLYSW,X'10'     BYPASS OLD MASTER              A30480
         BR    GR3                RETURN                         A30480
ENDFLUSH MVI   RIDIT2SW,RESET0         CLEAR COMMAND STATEMENT SWITCH
         MVI   FIRSENTR,RESET0    CLEAR FIRST ENTER SWITCH         4718
         MVI   FLUSHSW,X'0E'           SET FOR COMMAND AFTER FLUSH
         B     READCC                  CONTINUE- REWRITE OVER BAD ONE
SCANOUT  LA    GR11,FLUSHING           FLUSH MESSAGE CODE
         L     GR15,PRIMEWRT           ADDRESS OF LOG ROUTINE
         BALR  GR14,GR15               GIVE MESSAGE
         L     GR11,DCBNM           ADDRESS OF NEW MASTER DCB  @ZA04434
         MVI   FLUSHSW,X'0F'           SCANNING FOR NEXT COMD SWITCH
         CLI   ALIASW2,X'0F'      SEVERAL ALIAS STMT?          @ZA01682
         BNE   TESTLOC            NO,UP TO NEXT MEMBER         @ZA01682
         MVI   COMDCD2,RESET0     RESET COMMAND PENDING SW     @ZA01682
*                                                                     *
*        REINITIALIZE TO RECYCLE FOR UPDATE OF NEXT MEMBER.           *
*                                                                     *
TESTLOC  CLI   EXTNTSW,X'0F'                                     A29047
         BNE   NOCROSS                                           A29047
         L     GR3,DCBOM                                         A29047
         L     GR6,DCBNM                                         A29047
         CLC   5(1,GR3),5(GR6)                                   A29047
         BE    NOCROSS                                           A29047
         CLOSE ((GR3))                                           A29047
         OPEN  ((GR3))                                           A29047
         TM    BOFLGS(GR3),X'10'                                 A29047
         BZ    BADOPEN                                           A29047
NOCROSS  ST    GR6,SAVEGR6             SAVE REGISTER 6           A45212
         LA    GR6,CONTCLER                                      A45212
         XC    DELTSEQ1(32),DELTSEQ1   CLEAR DELETE AND NUMBER SEQ
         MVC   OMREADT(4),OMREADP      REINITIALIZE OM FOR NEXT PASS
         MVC   NMWRITET(4),NMWRITEP    REINITIALIZE NM FOR NEXT PASS
         MVC   FDADSET(8),BFDAD(GR11)  SAVE FDAD FOR NEW MEMBER    5666
         MVC   TRKBAL(2),TRBAL(GR11)  SAVE TRACK BALANCE           5666
         MVC   OMBLOCKT(2),OMBLOCKP    RESTORE BLOCKING FACTOR OM
         MVC   NMBLOCKT(2),NMBLOCKP    RESTORE BLOCKING FACTOR NM
         XC    LISTAREA(80),LISTAREA   CLEAR LIST BUFFER
         MVC   IDENTXX(4),INITSEQ      REINITIALIZE SEQUENCE CONSTANTS
         XC    IDENTNN(L4),IDENTNN                              YM07522
         XC    OLDOMSEQ(8),OLDOMSEQ                              A24812
         MVI   MULTI,X'0F'        TURN ON SWITCH FOR NEXT PASS   A30625
         TM    COMDCD2,X'0F'      ALREADY TO NEXT COMMAND      @ZA01690
         MVI   SOURCECD,X'FF'          REINITIALIZE SOURCE CODE BUFFER
         MVI   LEVELXX,C'0'            REINITIALIZE LEVEL BUFFER
         MVC   MEMNAME(8),BLANKS8      CLEAR MEMBER NAME BUFFER
         MVI   BLDFNDSW,X'0F'          DO A BLDL FOR THE NEXT MEM  2163
         MVI   OMEODX+1,X'00'           MARK OM EMPTY FOR NEXT   A36095
         BO    UPDAINPL             YES-SEE IF UPDATE INPLACE  @ZA01692
         EX    GR6,COMDCLER            CLEAR CONTROL CARD SWITCHES
         L     GR6,SAVEGR6              RESTORE REGISTER 6       A45212
         B     READCC                  GET NEXT STATEMENT
STOPNOW  BAL   GR14,DEACTV             DEACTIVATE EXITS          A32631
         MVI   COMDCD,X'00'             RESET COMMAND SW         PTM004
         MVI   FLUSHSW,X'0F'            TURN ON FLUSH SW         PTM004
         B     READCC                   GET NEXT STATEMENT       PTM004
DETAILGO MVI   RIDIT2SW,X'00'          RESET FLUSH DELETE/NUMBER SWITCH
         B     READCC                  KEEP FLUS&ING               4929
UPDAINPL CLI   INPLCSW,X'0F'      UPDATE INPLACE JUST DONE     @ZA01692
         BNE   MINFLUSH           NO-GO TO SCAN NEXT           @ZA01692
         LA    GR11,UPDATMSG      INVALID UPDATE INPLACE MSG   @ZA01692
         LA    GR14,FINERRA       RETURN ADDR AFTER STMT PRINT @ZA01692
         MVC   LOGOUTAR+7(80),SWITCHRD+1  MOVE INVALID STMT    @ZA01692
         MVI   ASISMSW,X'0F'      FORCE PRINTING               @ZA01692
         L     GR15,PRIMEWRT      ADDR OF PRINT ROUTINE        @ZA01692
         BR    GR15               GO TO PRINT ROUTINE          @ZA01692
MINFLUSH EX    GR6,COMDCLER            CLEAR CONTROL CARD SWITCHES
         MVI   FIRSENTR,X'0F'          NEW MEMBER IS COMING
RESCANIT MVI   SWITCHRD,RESET0         CLEAR SCAN SWITCHES
         XC    PARAMSW(27),PARAMSW     CLEAR SWITCHES
CHKPS    CLI   FLUSHSW,X'0F'           FLUSHING TO NEXT COMMAND    4929
         BNE   RET2SCAN                NO-BRANCH                   4929
         CLI   FIRSENTR,BIT10                                    A32050
         BE    RET2SCAN                                          A32050
         CLI   INPLCSW,X'0F'           UPDATE INPLACE              4929
         BE    FLSHPSUP                YES-BRANCH                  4929
         TM    CTONLYSW,X'11'     ALL INPUT FROM SYSIN           A30480
         BZ    CHKOMPS            NO.. CHECK DSORG OLD MASTER    A30480
         CLI   PSSWNM,X'0F'            NM SEQUENTIAL               4929
         BE    RET2SCAN                NO-PARTITIONED              4929
         B     FLSHPSUP                SEQUENTIAL                  4929
CHKOMPS  CLI   PSSWOM,X'0F'            OM SEQUENTIAL               4929
         BE    RET2SCAN                NO-PARTITIONED              4929
FLSHPSUP MVI   ENDONLY,X'0F'           ONLY ENDUP ACCEPTED         4929
RET2SCAN TM    PARAMSW,BIT10      IS PARIAL PARAMETER SW ON?    YA01700
         BC    1,RETBSCAN         YES,RETURN TO BSCAN           YA01700
         L     GR15,SCANENTR      LOAD ADDRESS OF ASCAN CSECT   YA01700
         BR    GR15               BRANCH TO ASCAN               YA01700
RETBSCAN L     GR13,SAVEPT13      LOAD ADDRESS OF SAVE AREA     YA01700
         LM    GR14,GR12,12(GR13) PICK UP SAVED REGISTERS       YA01700
         BR    GR7                BRANCH TO BSCAN               YA01700
*                                                                     *
*                              END OF FILE ON SYSIN                   *
*  REAL EOF, /* READ, OR ENDUP STATEMENT RECOGNIZED BY SCAN ROUTINE   *
*                                                                     *
EODCTX   CLI   FIRSENTR,BIT10          ANY RECORDS IN SYSIN
         MVI   EODCTSW,X'0E'           SET SWITCH FOR FURTHER PR A45153
         BE    NODATAS                 NO- SYSIN HAS NO RECORDS- ERROR
         TM    CHNGESW,X'0F'           ONLY CHANGE CARD IN SYSIN A45153
         BO    INVALOP                 NO DATA OR FUNCTION STM   A45153
         TM    ADDSW,X'0F'             STMT'S AFTER ADD STMT?  @ZA24936
         BO    NODATAS                 YES,GIVE MSG IEB823I    @ZA24936
         CLI   INSERTSW,X'0F'     SHOULD INSERT REC'S?         @ZA28408
         BNE   ENDUPCC            NO,TEST IF DURING FLUSH      @ZA28408
         CLI   FSTINSRT,X'00'     NO REC'S IN AN INSERT OP.?   @ZA28408
         BE    INVALOP            YES,GIVE MSG IEB807I         @ZA28408
ENDUPCC  CLI   FLUSHSW,X'0F'           EOF DURING FLUSH
         BE    LASTLEG            YES-STOP NOW-DO NOT STOW       PTM223
         CLI   CCSCANSW,BIT01          EOF READING CONTINUATION CARD
         BE    INVALOP                 YES- INVALID REST OF CARD GONE
REALEOF  CLI   INSRTRD,X'0F'           INSERT ISSUED READ
         MVI   FINICCS,X'0F'           FLAG AS SYSIN THROUGH
         BE    WRTINSRT                YES- RETURN TO PROCESS LAST ONE
         TM    CHNGESW,X'0E'      CHANGE FUNCTION                  1550
         BC    7,CONTINY          YES-CONTINUE                     1550
         CLI   INPLCSW,X'0F'      UPDATE INPLACE                   1550
         BE    RENUMTST                IF SO, CHECK FOR RENUMBERINGUL0H
         TM    REPROSW,X'0E'           REPRO FUNCTION
         BC    8,STOWNAME              NO- ADD OR REPLACE SO FINISH UP
CONTINY  MVI   DONOTRDC,X'0F'          READ NO MORE SYSIN IS DONE
         CLI   OMEND,X'0F'             IS OLD MASTER DONE
         BE    STOWNAME                YES- FINISH UP
         CLI   READNOOM,X'0F'          HAVE AN OM RECORD HANDY
         BNE   PROCEED                 NO- OPEN DATA SET IF NECESSARY
         MVI   READNOOM,RESET0         CLEAR OM PENDING SWITCH
         LA    GR6,OMINAREA            LOCATION OF OLD MASTER RECORD
         B     TESTRCD                 TEST OM RCD FOR DELETION    5944
RENUMTST CLI   NUMBRSW,X'0F'           IS RENUMBERING REQUESTED    UL0H
         BE    READOM      '           IF SO, THEN PROCEED         UL0H
         CLI   OPENSW,X'00'            I HAVE DATA SETS BEEN OPENEDUL0H
         BE    READOM                  IF SO  THEN PROCEED UPDATINGUL0H
         B     NODATAS                 IF NOT, NO SYSIN INPUT      UL0H
NODATAS  LA    GR7,SYSINNM             ADDRESS OF SYSIN DD NAME
         LA    GR3,CLOSEUP        ADDRESS OF CLOSE SYSIN       @YA02568
         MVI   HICONCDE+1,X'0C'        SET CONDITION CODE       YA01186
NODATA   LA    GR11,ERRMSGN            CODE FOR BLANK DATA MESSAGE
         L     GR15,PRIMEWRT           ADDRESS OF MESSAGE CSECT
         BALR  GR14,GR15               GIVE MESSAGE
         BR    GR3                     ENTER FLUSH OR ALMOSTZ ROUTINE
*                                                                     *
*          END OF FILE ENCOUNTERED ON OLD MASTER DATA SET             *
*  STOP IF 'REPRO' OR UPDATE INPLACE- OTHERWISE CONTINUE TILL SYSIN   *
*   DONE OR NEXT FUNCTION ENCOUNTERED.                                *
*                                                                     *
OMEODX   BC    0,NOTEMPTY              TAKE BRANCH IF NOT EMPTY  A36095
         LA    GR7,SYSUT1NM            ADDRESS OF SYSUT1 DD NAME
         LA    GR3,FLUSHALL            ERROR ROUTINE IF SYSUT1 NULL
         MVI   HICONCDE+1,X'04'        SET CONDITION CODE       YA01186
         B     NODATA                  PRINT OM EMPTY MESS       A36095
NOTEMPTY EQU   *                                                 A36095
         TM    FINICCS,X'0F'           SEE IF ANY CONTROL CARDS LEFT
         BC    1,STOWNAME              NO- FINISH
         TM    COMDCD2,X'0F'           UP TO NEXT COMMAND CARD @ZA01690
         BO    STOWNAME                YES- END THIS DS MEMBER @ZA01690
         TM    REPROSW,X'0E'           REPRODUCING DATA SET
         BC    1,STOWNAME              YES- FINISH UP
         CLI   INPLCSW,X'0F'           UPDATE INPLACE JUST DONE
         BE    OUTOFSEQ                YES,GIVE MSG AND QUIT     A32632
         MVI   READNOOM,X'0F'          SET SWITCH TO STOP OM READ
         B     CHKOP                   PROCESS SYSIN RECORDS       7811
*                                                                     *
*        FINISH OF A MEMBER UPDATE- SET UP SSI AND STOW MEMBER NAME.  *
*                                                                     *
STOWNAME CLI   FINICCS,X'0F'      SYSIN FINISHED?              @ZA01682
         BE    STOW               YES,FINISH THIS MEMBER       @ZA01682
         CLI   COMDCD2,X'1F'                                   @ZA01690
         BNE   STOW                                            @ZA01690
         CLI   ALIASW2,X'0F'      SEVERAL ALIAS STMT?          @ZA01682
         BE    RESCANIT           YES,GO ON TO SCAN            @ZA01682
STOW     CLI   FLUSHSW,X'0F'      ENDING UP ON FLUSH?          @ZA01682
         BE    DONOTSTO                YES- DO NOT STOW
         CLI   DELETSW,X'0F'           DELETE STILL IN EFFECT
         BE    BADDELET                YES- CARDS OUT OF ORDER- ERROR
         CLI   OPENSW,X'0F'       ARE DCB'S OPEN YET             PTM295
         BE    NODATAS            NO DATA IN SYSIN               PTM295
         CLI   NMBUFFR,X'0F'           WAS LAST NM BLOCK WRITTEN
         BE    WRTLASTB                NO- WRITE LAST NEW MASTER RECORD
STOW2    CLI   PSSWNM,X'0F'            IS NEW MASTER PARTITIONED
         BNE   LASTLEG                 NO- SEQUENTIAL-FINISH UP-NO STOW
         TM    ALIASW2,X'F0'           HAS STOW BEEN DONE          UL0H
         BO    STOWALAS                YES--ONLY HERE TO STOW      UL0H
         CLI   INPLCSW,X'0F'           UPDATE IN PLACE
         BE    CHKALIAS           YES.. BYPASS STOW
         XC    STOWAREA(80),STOWAREA   CLEAR STOW AREA
         CLI   NEWNAME,X'00'           IS THIS A NEW PODS FROM PS OM
         BNE   STOWNEW                 YES- STOW NEW NAME
         MVC   STOWAREA(8),MEMNAME     MOVE NAME TO STOW AREA
CHECKSSI CLI   SSISW,X'0F'             PUT REPLACEMENT SSI DATA IN
         BE    STOWSSI                 YES
         CLI   LEVELXX,C'0'            IS THERE A LEVEL CHANGE
         BNE   LEVELNEW                YES- CHANGE LEVEL
         CLI   SOURCECD,X'FF'          IS SOURCE CODE CHANGED
         BNE   NEWSORCE                YES- CHECK NEW SOURCE SETTING
STORALLS MVC   STOWAREA+11(64),LISTAREA+17   COPY USER DATA
STOWIT   L     GR1,DCBNM               ADDRESS OF NEW MASTER DCB
STOWALI  STOW  (GR1),STOWAREA,R
         STC   GR15,SARG               STORE STOW RETURN CODE
         TM    SARG,X'0C'              IS DIRECTORY FULL (CODE 12)
         BO    STOWER12                YES- GIVE MESSAGE
         TM    SARG,X'10'              I/O ERROR FROM STOW (CODE 16)
         BO    STOWER16                YES- GIVE MESSAGE
         TM    SARG,X'08'              WAS NAME IN NM DIRECTORY
         LA    GR11,STOWMSGT           LOAD NOT FOUND MESSAGE CODE
         BO    STOWOVER                NAME IN DIRECTORY GIVE MESSAGE
         LA    GR11,STOWMSGN           LOAD MESSAGE FOUND NAME CODE
STOWOVER LA    GR7,STOWAREA            ADDRESS OF MEMBER NAME
         BAL   GR9,GIVEMESS            GIVE APPROPRIATE MESSAGE
CHKALIAS CLI   ALIASW,X'0F'            ALIAS NAME TO BE STOWED
         BE    STOWALAS                YES- STOW ALIAS NAME
         TM    ALIASW2,X'0F'           ALIAS PENDING               MA0H
         BO    STOWADAL           YES,GO TO STOW ADD ALIAS     @ZA01682
         B     DONOTSTO           NO,THIS MEMBER FINISHED      @ZA01682
STOWADAL L     GR7,ALIASNM1       POINTER TO LAST ALIAS NAME   @ZA01682
         L     GR3,ALIASNM2       POINTER OF ALIAS TO MOVE     @ZA01682
         CR    GR3,GR7            LAST ALIAS ALREADY MOVED?    @ZA01682
         BE    NOSTOW             YES,FINISH THIS MEMBER       @ZA01682
         LA    GR3,8(GR3)         POINTER TO NEXT ALIAS NAME   @ZA01682
         ST    GR3,ALIASNM2       SAVE POINTER                 @ZA01682
         MVC   STOWAREA(8),0(GR3) MOVE ALIAS NAME              @ZA01682
         B     STOWALA2           BRANCH TO STOW               @ZA01682
NOSTOW   NI    COMDCD2,X'0F'      RESET COMMAND PENDING SWITCH @ZA01690
         MVI   ALIASW2,X'F0'           SHOW STOW HAS BEEN DONE     MA0H
DONOTSTO TM    FINICCS,X'0F'           ARE CONTROL CARDS DEPLETED
         BC    1,LASTLEG               YES- ALL FINISHED
         BAL   GR3,GOONTEST            SEE IF GO ON TO NEXT MEMBER
         B     TESTLOC                 MUST HAVE PO OM/NM GO ON
STOWSSI  MVC   LISTAREA+18(4),SSIBUF   INSERT SSI INFORMATION
STORSTAT SR    GR3,GR3
         IC    GR3,LISTAREA+17         PICK UP USER DATA COUNT
         C     GR3,TWO                                             5873
         BNL   STORALLS                MORE THAN 2,GO STOW         5873
         OI    LISTAREA+17,X'02'       MAKE COUNT TWO HALFWORDS    5873
         B     STORALLS                SET UP FOR STOW
LEVELNEW MVC   LISTAREA+18(1),LEVELXX  INSERT NEW LEVEL INTO SSI
         CLI   SOURCECD,X'FF'          IS SOURCE CODE CHANGED
         BE    STORSTAT                SET UP SSI
NEWSORCE CLI   SOURCECD,X'F0'          IS SOURCE USER
         BNE   IBMSORCE                NO- IBM CHANGE
         OI    LISTAREA+19,X'20'       SET LOCAL FIX SWITCH IN SSI
         B     STORSTAT                STORE STATUS LISTAREA TO STOW
IBMSORCE NI    LISTAREA+19,X'DF'       RESET LOCAL FIX SWITCH 0 FOR IBM
         B     STORSTAT                STORE STATUS LISTAREA TO STOW
STOWNEW  MVC   STOWAREA(8),MEMBNEW     STOW NEW PO NAME
         B     CHECKSSI                SEE IF STATUS INFORMATION
STOWER12 LA    GR11,ERRDIRF            DIRECTORY FILLED MESSAGE
         B     FINERRA                 GIVE MESSAGE
STOWER16 LA    GR11,ERRDRIO            DIRECTORY I/O ERROR MESSAGE CODE
         B     FINERRA                 GIVE MESSAGE, SET ERROR CODE 12
STOWALAS MVI   ALIASW,RESET0           CLEAR ALIAS SWITCH
         MVC   STOWAREA(8),ALIASNM     MOVE ALIAS NAME TO STOW BUFFER
STOWALA2 OI    STOWAREA+11,BIT80       FLAG AS ALIAS           @ZA01682
         CLI   INPLCSW,X'0F'           UPDATE INPLACE
         BNE   STOWIT                  NO- STOW NM
         L     GR1,DCBOM               ADDRESS OF OM DCB
         MVC   STOWAREA+8(70),LISTAREA+12   MOVE IN TTR AND DATA
         OI    STOWAREA+11,BIT80       SET ALIAS BIT
         B     STOWALI                 STOW OM ALIAS UPDATE INPLACE
         EJECT
***********************************************************************
*                        END OF JOB ROUTINE                           *
***********************************************************************
INVALLBL LA    GR11,ERROP               LOAD ERROR MSG CONSTANT    UL0H
         BAL   GR9,GIVEMESS             GIVE ERROR MESSAGE         UL0H
         B     DEACTVCT                 PREPARE TO CLOSE           UL0H
         SPACE 1
DEACTVCT MVI   TEMPCON,X'08'           RETURN CODE IS 8          A25427
DEACTV   L     GR6,DCBCT               LOAD ADDR OF C.T. DCB     A25427
         L      GR6,EXLIST(GR6)         LOAD ADDR OF C.T. EXIT LISTUL0H
         MVI   0(GR6),RESET0            DEACT. USER HEADER EXIT    UL0H
         MVI   4(GR6),RESET0            DEACT. USER TRAILER EXIT   UL0H
DEACTVOM L     GR6,DCBOM                LOAD ADDR OF O.M. DCB      UL0H
         L     GR6,EXLIST(GR6)          LOAD ADDR OF O.M. EXIT LISTUL0H
         MVI   0(GR6),RESET0            DEACT. USER HEADER EXIT    UL0H
         MVI   4(GR6),RESET0            DEACT. USER TRAILER EXIT   UL0H
        MVI   12(GR6),X'85'            MAKE DCB XIT LAST/ACTIVE    UL0H
        MVI   16(GR6),X'07'            MAKE JFCB EXIT ACTIVE       UL0H
DEACTVNM L     GR6,DCBNM                LOAD ADDR OF N.M. DCB      UL0H
         L     GR6,EXLIST(GR6)          LOAD ADDR OF N.M. EXIT LISTUL0H
         MVI   0(GR6),RESET0            DEACT. USER HEADER EXIT.   UL0H
         MVI   4(GR6),RESET0            DEACT. USER TRAILER EXIT.  UL0H
         MVI   8(GR6),RESET0            DEACT. USER TOTALING EXIT. UL0H
         MVI   12(GR6),X'85'            MAKE DCB EXIT LAST AND ACTVUL0H
         MVI   16(GR6),X'07'            ACTIVATE JFCB EXIT.        UL0H
         CLI   CLOSESW,X'0F'            RETURN FROM CLOSING SYSIN  UL0H
         BE    CLOSEOMT                 &F SO,CLOSE OM             UL0H
         CLI   CLOSESW,X'F0'            RETURN FROM CLOSING OM     UL0H
         BE    CLOSENM                  IF SO,CLOSE NM             UL0H
         CLI   CLOSESW,X'FF'            RETURN FROM CLOSING NM     UL0H
         BE    ALMOSTY                  IF SO,PREPARE TO RETURN    UL0H
         B     CLOSEUP                  IF NOT BEGIN CLOSING       UL0H
RESTORE  CLI   INPLCSW,X'0F'           UPDATE INPLACE?         @ZA06529
         BE    LASTLEG                 YES,BRANCH              @ZA06529
         CLI   STARTMEM,X'0F'          MEMBER UPDATE STARTED   @ZA06529
         BNE   LASTLEG                 NO,BRANCH               @ZA06529
         L     GR11,DCBNM              ADDR OF NEW MASTER DCB  @ZA06529
         MVC   BFDAD(8,GR11),FDADSET  RESTORE TO START OF AREA @ZA06529
         MVC   TRBAL(2,GR11),TRKBAL RESTORE PREVIOUS TRACK BAL @ZA06529
LASTLEG  TM    CTONLYSW,X'01'     IS THERE AN OLD MASTER
         BO    FREENEWM           NO.. FREE UP NM BUFFERS        A30480
         L     GR11,DCBOM              ADDRESS OF OLD MASTER DCB
         L     GR1,OMREADP             ADDRESS OF BUFFER
         LTR   GR1,GR1            IS THERE A BUFFER              PTM223
         BZ    FREENEWM           NO BRANCH                      PTM223
         LH    GR0,BBLKSI(GR11)        LOAD BLOCKSIZE FROM DCB
         FREEMAIN R,LV=(0),A=(1)
FREENEWM CLI   INPLCSW,X'0F'           IS THERE AN NEW MASTER
         BE    CLOSEUP                 NO- CLOSE DATA SETS
         L     GR11,DCBNM              ADDRESS OF NEW MASTER DCB
         L     GR1,NMWRITEP            ADDRESS OF NEW MASTER BUFFER
         LTR   GR1,GR1            IS THERE A BUFFER              PTM223
         BZ    CLOSEUP            NO BRANCH                      PTM223
         LH    GR0,BBLKSI(GR11)        LOAD BLOCKSIZE FROM DCB
         FREEMAIN R,LV=(0),A=(1)
CLOSEUP  L     GR7,DCBCT               ADDRESS OF SYSIN DCB
         CLOSE ((GR7),LEAVE)           CLOSE SYSIN- LEAVE IN CASE TAPE
         FREEPOOL (GR7)                                            BS0H
         LA    GR9,CLOSEOMT            RETURN FROM TRAILER CHECK
         MVI   CLOSESW,X'0F'                                       UL0H
TESTTRAL CLI   LBLRETCD,X'10'                                      UL0H
         BCR   4,GR9                   OK IF LESS THAN 16          UL0H
HEADRMSG LA    GR11,USRTRMSG           CODE IS 16, USER LABEL ERROR
         L     GR15,PRIMEWRT           ADDRESS OF MESSAGE CSECT
         BALR  GR14,GR15               GIVE MESSAGE
         LA    GR15,16                 SET RETURN CODE TO 16
         STH   GR15,HICONCDE           STORE 16 INTO CONDITION CODE
         MVI   LBLRETCD,X'00'           RESET RETURN CODE          UL0H
         B     DEACTVCT                 DEACTIVATE EXITS           UL0H
CLOSEOMT CLI   NMSYSNLB,X'00'          ANY LABELS TO BE OUTPUT     UL0H
         BE    CLOSEOM                 IF NOT, CLOSE NEW MASTER    UL0H
         CLI   INPLCSW,X'0F'            UPDATE=INPLACE             UL0H
         BE    NOTRLBLI                 IF SO,ERROR                UL0H
CLOSEOM  L     GR7,DCBOM               ADDRESS OF OLD MASTER DCB
         CLOSE ((GR7))                 CLOSE OLD MASTER DCB        5200
         MVI   CLOSESW,X'F0'                                       UL0H
         BAL   GR9,TESTTRAL            TEST TRAILER RETURN CODE IF ANY
         CLI   PSSWNM,X'00'             IS NM PHY SEQ              UL0H
         BE    CLOSENM                  IF SO,CLOSE IT             UL0H
         CLI   NMSYSNLB,X'00'          ANY LABEL PROCESSING REQUESTUL0H
         BNE   LABLMESG                IF SO, ITS AN ERROR         UL0H
CLOSENM  MVI   NMCLSESW,X'1F'           FLAG AS DURING CLOSE       UL0H
         CLI   LABELSW,X'20'            LABEL CREATION REQUESTED   16/'
         BNE   PRECLNM                  IF NOT, OPEN               UL0H
         MVI   NMSYSNLB,X'0F'           FLAG AS TRAILERS TO CREATE UL0H
PRECLNM  L     GR7,DCBNM                NM  DCB ADDRESS            UL0H
         CLOSE ((GR7))                 CLOSE NEW MASTER DCB        5200
         MVI   CLOSESW,X'FF'                                       UL0H
         BAL   GR9,TESTTRAL            TEST TRAILER RETURN CODE IF ANY
ALMOSTY  CLC   TEMPCON(1),HICONCDE+1                               UL0H
         BNH   ALMOSTZ                 HIGH OR EQUAL CODE
         MVC   HICONCDE+1(1),TEMPCON   NO  SWAP RETURN CODES
ALMOSTZ  TM    SYNADSW,X'0F'     IS THIS THE END OF A SYNAD EXIT?A27716
         BCR   1,GR3                   YES, GO BACK TO IEBUPXIT  A27716
         L     GR15,PRIMEWRT           ADDRESS OF MESSAGE CSECT  A27716
         MVI   QUITALL,X'0E'           LAST MESSAGE AND CONDITION CODE
         BR    GR15                    GIVE MESSAGE,CLOSE DCBLOG,RETURN
OMIOERR  LA    GR11,LBLERMSG            I/O ERROR DURINT LABEL PRO UL0H
         L     GR15,PRIMEWRT            ADDRESS OF IEBUPLOG        UL0H
         BALR  GR14,GR15                PRINT MESSAGE              UL0H
         B     DEACTVCT                 DEACTIVATE EXITS AND CLOSE UL0H
         EJECT
*                                                                     *
*    CONSTANTS FOR IEBUPDT2 CSECT ONLY                                *
*                                                                     *
DSORGRTN DC    A(CHKDSORG)    ENTRY POINT FOR CHECK DSORG ROUTINE  3341
BLANKS8  DC    CL8' '                  DOUBLE WORD OF HEX BLANKS   UL0H
SAVEPT13 DS    F                       SAVE REGISTER 13            UL0H
SAVEGR6  DS    F                       SAVE REGISTER 6           A45212
SIZELIST DC    X'0001004C'             CONSTANT FOR BLDL LIST      UL0H
REGSAVS  DS    18F                     SAVE REGISTERS              UL0H
PRIMEWRT DC    V(IEBUPLOG)             ENTRY POINT OF LOG ROUTINE  UL0H
SCANENTR DC    V(IEBASCAN)             ENTRY POINT OF SCAN ROUTINE UL0H
MOVEPART MVC   0(1,GR6),0(GR7)         MOVE PARTIAL CARD OM/CT     UL0H
UPKSN    UNPK  D0(L1,GR7),TEMP(L8)     SET NUMERIC IN SEQ NO    YM07522
CHKSEQNB CLC   0(1,GR8),BLANKS8        TEST IF SEQUENCE NUMBER IS BUL0H
LOGMOVE  MVC   0(1,GR7),0(GR6)         MOVE RECORD TO LOG OUT AREA UL0H
COMPARES CLC   0(1,GR7),0(GR6)         COMPARE SEQUENCE NUMBERS    UL0H
COMDCLER XC    COMDCD(1),COMDCD        CLEAR CONTROL CARD SWITCHES UL0H
INITSEQ  DC    X'00480008'             COLUMN 73,SIZE 8 SEQUENCE FIUL0H
SIXTEEN  DC    H'16'                   CONSTANT                    UL0H
SARG     DC    H'0'                    WORK AREA                   UL0H
SYS1RECL DC    H'0'                    LOGICAL RECORD LENGTH SYSUT1UL0H
VALIDREC DC    H'00'                   NUMBER OF RECORDS IN LAST BLUL0H
OMDC     DC    C'OM'                   REFERENCE OLD MASTER        UL0H
NMDC     DC    C'NM'                   REFERENCE NEW MASTSR        UL0H
SLASHAST DC    C'/*'                   EOF INDICATOR SYSIN         UL0H
DOTSLASH DC    C'./'                   IDENTIFIES CONTROL CARD     UL0H
TESTLABL DC    2X'0'                   TEMP STORAGE FOR LABELSW    UL0H
TMPLBLCT DC    2X'0'                   TEMPORARY STORAGE OF LABEL #UL0H
MULTI    DC    X'00'                   SET TO X'0F' FOR NEXT MEMBER1796
NULL     DC    CL8'NULLFILE'                                     A25310
CLOSESW  DC    X'0'                                                UL0H
ZERO     DC    H'0'                    COMPARE FOR TOTAL CHANGE    3511
BLDFNDSW DC    X'00'                   SET TO 00 WHEN BLDL IS DONE 2163
OLDOMSEQ DC    D'0'                    TO HOLD PREVIOUS OM SEQ   A24812
         USING CHKDSORG,GR9   SET UP 2ND BASE AREA ADDRESSABILITY  3341
CHKDSORG DS    0F                                                  3341
         USING IHADCB,GR3         SET UP BASE FOR OM OR NM DCB     3341
         L     GR10,DCBEXLST  GET ADDRESS OF DCB EXIT LIST         3341
         NI    12(GR10),X'05'          FLAG DCB ENTRY AS NOT LAST  UL0H
STORADDR LA    GR7,JFCBAREA   GET ADDRESS OF AREA INTO WHICH JFCB  3341
         ST    GR7,16(GR10)            READ AND PUT INTO EXLIT LISTUL0H
         MVI   16(GR10),X'87'          FLAG AS LAST AND JFCB       UL0H
*                             READ (X'80' PLUS X'07')              3341
         LR    GR7,GR3            TRANSFER DCB ADDRESS TO PRESERVE 3341
*                                 DSECT BASE FOR CONTINUED USE     3341
         CLI   INPLCSW,X'0F'      UPDATE IN PLACE                  3341
         BE    UPDTJFCB                                            3341
*                                                                  3341
         RDJFCB  ((GR7),OUTPUT)   READ JFCB FOR OUTPUT             3341
*                                                                  3341
         CLC   JFCBAREA(8),NULL        IS SYSUT2 A DUMMY SET     A25310
         BE    DUMMYNM                 YES, GO TO MSG RTN        A25310
         TM    JFCBAREA+52,X'20'       A SYSIN/SYSOUT D/S ?    @ZA33607
         BO    YESNEW                  YES,DON'T TEST DISP     @ZA33607
         TM    JFCBAREA+87,X'C0'       IS NM AN NEW D/S?       @ZA28493
         BO    YESNEW                  YES,CONTINUE            @ZA28493
         OI    NMBLKERR,X'80'          NO,INDICATE NM OLD D/S  @ZA28493
YESNEW   OI    12(GR10),X'85'          REST. DCB ENT LAST/ACTV @ZA28493
         MVI   16(GR10),X'07'           MAKE JFCB EXIT NOT LAST    UL0H
         LA    GR11,SYSUT2NM  GET ADDRESS OF SYSUT2 DD NAME        3341
         B     GETYPE              GO CHECK DEVICE TYPE            3341
*                                                                  3341
UPDTJFCB RDJFCB  ((GR7),UPDAT)    READ JFCB FOR UPDATE IN PLACE    3341
         OI    12(GR10),X'85'          RESTORE DCB ENTRY LAST/ACTV UL0H
         MVI   16(GR10),X'07'           MAKE JFCB EXIT NOT LAST    UL0H
         LA    GR11,SYSUT1NM  GET ADDRESS OF SYSUT1 DD NAME        3341
GETYPE   DEVTYPE (GR11),ADEVTYPE                                   3341
         LTR   GR15,GR15           TEST RETURN CODE                3341
         BNZ   DDNOTFND            NOT ZERO, DD NAME NOT FOUND     3341
         CLI   ADEVTYPE+2,X'20'    IS DEVICE TYPE DIRECT ACCESS    3341
         BE    GETDSCB             YES, GO OBTAIN AND CHK DSORG    3341
NOOBTAIN OI    DCBDSORG,PSDSET     NO, SET DSORG AS SEQUENTIAL(PS) 3341
         NI    DCBMACR+1,X'20'         DELETE POINT MACR       @ZA00637
         B     TESTUT2            TEST FOR VALID DSORG           A33305
NOOPEN   CLI   INPLCSW,X'0F'       WAS OBTAIN FOR UPDATE INPLACE   3341
*                                  I.E., FOR SYSUT1 DSCB           3341
         BNE   GETADUT2            NO, GET UT2 DDNAME ADDRESS      3341
         LA    GR7,SYSUT1NM        YES,GET ADDR DDNAME FOR LOG MSG 3341
         B     GOTOLOG             GO GET MSG CODE & GIVE MESSAGE  3341
GETADUT2 LA    GR7,SYSUT2NM        OBTAIN WAS FOR SYSUT2 DDNAME    3341
GOTOLOG  LA    GR11,NTOPMSG        LOAD MESSAGE CODE               3341
         B     FINERRA             GO PRINT MESSAGE, END JOB       3341
*                                                                  3341
DDNOTFND LA    GR11,DDNAMEMG  GET CODE FOR INVALID DDNAME MESSAGE  3341
         B     FINERRA        GO PRINT MESSAGE AND END JOB         3341
*                                                                  3341
GETDSCB  XC    DSCBLIST(16),DSCBLIST  CLEAR LIST AREA FOR OBTAIN   3341
         OI    DSCBLIST,X'C1'     FLAG 1ST BYTE FOR SEARCH         3341
*                                 OPERATION REQUESTED              3341
         LA    GR7,JFCBAREA        PUT ADDRESS OF DATA SET         3341
         ST    GR7,DSCBLIST+4      NAME INTO LIST FOR OBTAIN       3341
         LA    GR7,JFCBAREA+118    PUT ADDRESS OF VOLID INTO LIST  3341
         ST    GR7,DSCBLIST+8      FOR OBTAIN                      3341
         LA    GR7,DSCBAREA        PUT ADDRESS OF 350 BYTE WORK    3341
         ST    GR7,DSCBLIST+12     AREA INTO LIST FOR OBTAIN       3341
         OBTAIN  DSCBLIST                                          3341
         LTR   GR15,GR15           TEST FOR RETURN CODE OF ZERO    3341
         BC    7,NOOPEN              NON-ZERO RETURN, END JOB      3341
         TM    DSCBAREA+38,X'02'   IS DSORG PARTITONED             3341
         BC    1,CHKPODS           YES, GO CHECK IF UPDATE INPLACE 3341
         TM    DSCBAREA+38,X'BF'   DSORG ANYTHING BUT SEQUENTIAL   3341
         BC    7,NOOPEN            YES, ERROR, GO GIVE MESSAGE     3341
*                                  NO,CHECK FOR UPDATE IN PLACE    3341
CKINPLCE CLI   INPLCSW,X'0F'       UPDATE IN PLACE(DSORG WAS PS)   3341
         BE    TESTUT1             YES, GO COMPARE FOR UT1         3341
TESTUT2  CLI   PSSWNM,X'00'        PSSWNM SWITCH SET FOR PS        3341
         BNE   BADSORG             NO, MESSAGE FOR INCOMPATIBLE    3341
*                                  DATA SET ORGANIZATION           3341
         BR    GR8                 RETURN AND OPEN DATA SET        3341
TESTUT1  CLI   PSSWOM,X'00'        PSSWOM SWITCH SET FOR PS        3341
         BNE   BADSORG             NO, INCOMPATIBLE DSORGS         3341
         BR    GR8                 RETURN, ALL OKAY                3341
CHKPODS  CLI   INPLCSW,X'0F'                                       3341
         BE    TESTUT1A                                            3341
TESTUT2A CLI   PSSWNM,X'0F'                                        3341
         BNE   BADSORG             INCOMPATIBLE DSORG, GIVE MSG    3341
         BR    GR8                                                 3341
TESTUT1A CLI   PSSWOM,X'0F'                                        3341
         BNE   BADSORG             INCOMPATIBLE DSORG, GIVE MSG    3341
         BR    GR8                                                 3341
BADSORG  LA    GR11,DSORGBAD       LOAD CODE FOR DSORG MESSAGE     3341
         B     FINERRA             GO GIVE MESSAGE AND END JOB     3341
EXTNTCHK DS    0H                                              @ZA01684
         CLI   INPLCSW,X'0F'           IS THIS UPDATE INPLACE      5324
         BE    BACK                    YES--BRANCH                 5324
         GETMAIN R,LV=176                                          5324
         L     GR6,DCBOM               OLD MASTER DCB ADDRESS      5324
         L     GR6,36(GR6)             EXIT LIST FOR DCB           5324
         NI    12(GR6),X'05'           FLAG DCB AS NOT LAST        UL0H
         ST    GR1,16(GR6)             AREA TO READ JFCB           UL0H
         MVI   16(GR6),X'87'           FLAG AS LAST AND READ JFCB  UL0H
         LR    GR7,GR1                 SAVE ADDRESS OF AREA        5324
         L     GR6,DCBOM               ADDRESS OF OLD MASTER DCB   5324
         RDJFCB ((GR6),INPUT)                                      5324
         L     GR6,36(GR6)             CHANGE FLAGS                5324
         OI    12(GR6),X'85'       RESTORE DCB ENTRY LAST/ACTV   PTM714
         NI    16(GR6),X'00'       RESET JFCB ENTRY TO ZERO      PTM714
         CLC   0(44,GR7),JFCBAREA      ARE DATA SET NAMES THE SAMES5324
         BNE   FREEUP                  NO--BRANCH                  5324
         CLC   118(6,GR7),JFCBAREA+118                           A29047
         BNE   FREEUP                  NO--BRANCH                  5324
         MVI   EXTNTSW,X'0F'           YES-INDICATE EXTENTS CHECK  5324
FREEUP   LR    GR1,GR7                 ADRESS OF AREA TO FREE      5324
         FREEMAIN R,LV=176,A=(1)                                   5324
BACK     BR    GR8                                                 5324
TWO      DC    F'2'                                                5873
FOUR     DC    F'4'                                                UL0H
EIGHT    DC    H'8'                                                UL0H
ENDUP    DC    C'ENDUP '                                           UL0H
REPSW    DC    X'00'                     REPLACE/UPDATE SWITCH  YA01187
TEMP10   DS    F                                                   UL0H
LENGTH   DS    F                                                   UL0H
DELETMSG DC    CL25'     *           DELETED*'                  YA01187
UPDTMS   DC    CL25'     *    UPDATED RECORD*'                  YA01187
REPMES   DC    CL25'     *       REPLACEMENT*'                  YA01187
REPLMSG  DC    CL25'     *          REPLACED*'                  YA01187
INSERTMS DC    CL25'     *          INSERTED*'                  YA01187
RENUMMS  DC    CL25'     *RENUMBERING FORCED*'                  YA01187
         EJECT
         DCBD  DSORG=PS
BDSORG   EQU   DCBDSORG-IHADCB         DATA SET ORGANIZATION FIELD DCB
TRBAL    EQU   DCBTRBAL-IHADCB    DISPLACEMENT FOR DCB TRACK BAL   5666
BLRECL   EQU   DCBLRECL-IHADCB         LOGICAL RECORD LENGTH FIELD DCB
BOFLGS   EQU   DCBOFLGS-IHADCB         OPEN ERROR FIELD IN DCB
BBLKSI   EQU   DCBBLKSI-IHADCB         BLOCKSIZE FIELD IN DCB
IFLGSEOF EQU   DCBIFLGS-IHADCB         EOF FLAGS IN DCB 'AFTER' OPEN
BFDAD    EQU   DCBFDAD-IHADCB          TTR IN MBBCCHHR AT START DCB
EXLIST  EQU   DCBEXLST-IHADCB
PODSET   EQU   2                       SETTING OF DCB DSORG FOR PODS
PSDSET   EQU   X'40'          DCB SETTING FOR SEQUENTIAL           3341
RESET0   EQU   X'00'                   CLEARS SWITCH
BIT80    EQU   X'80'
BIT01    EQU   X'01'
BIT10    EQU   X'10'
         EJECT
         IEBUPCON
         END   IEBUPDT2
