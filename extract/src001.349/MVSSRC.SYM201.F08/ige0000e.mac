IEC1442A START X'00'
*084038400,38500,65600,65800                                       7884
*3178050300,053000-053600                                        A33752
*3221052000,052600,052900,053000,053100,053200,053300,053400,    A36344
*3221053500,053600,064400,066200                                 A36344
*A050204,050208                                                  A48174
*C056800,058600                                                  A48174
*C039260,053590,053610,053660,053700,064300,065300               A48623
         SPACE 2
*        2501 - 2520 - 1442  ERROR RECOVERY PROCEDURE
         SPACE 2
*      FUNCTION     ANALYSE STATUS AND SENSE DATA AND ATTEMPT ERROR
*                   RECOVERY. IF THE ERROR IS CORRECTED,PROCEED IN A
*                   NORMAL MANNER. IF A PERMANENT ERROR IS DETECTED
*                   INDICATE IT TO THE USER BY A COMPLETION CODE IN THE
*                   ECB AND A MESSAGE ON THE SYSTEM CONSOLE.
*         ENTRY:    ER1442    FROM INTERRUPT SUPERVISOR
*         INPUT     REQUEST ELEMENT POINTER IN REGISTER 1
*        OUTPUT     NONE
*       EXT REF     NONE
*         EXITS     1. ERRECXP EXIT TO I/O SUPERVISOR TO RE-EXECUTE
*                      THE CHANNEL PROGRAM IN ERROR.
*                   2. SVC 3 EXIT TO RETURN CONTROL TO THE SUPERVISOR
*                      AND RELINQUISH CONTROL OVER THE TRANSIENT AREA
*                   3. XCTL EXIT TO OUTBOARD RECORDING, STATISTICS UP-
*                      DATE OR WRITE TO OPERATOR. RETURN WILL BE MADE
*                      FROM THESE ROUTINES TO THE SUPERVISOR VIA SVC 3.
*       ERR MSGS    CONSOLE MESSAGES TO INDICATE ERROR CONDITIONS ARE
*                        1. I/O ERR
*                        2. INT REQ
*         NOTES     IOB EXCEPTION AND IOB ERROR FLAGS ARE INTERPRETED
*                   AS FOLLOWS
*                        IOBEX ON AND IOBERR ON, ERR RTNE IN CONTROL.
*                        IOBEX ON AND IOBERR OFF, PERMANENT ERROR.
*                        IOBEX OFF AND IOBERR OFF, NO ERR OR CORRECTED
*        TABLES     STATISTICS TABLE AND IOB
*     OPERATION     THIS ROUTINE CHECKS THE STATUS AND SENSE BYTES
*                   STORED IN THE UCB AND IOB AND TAKES ACTION AS
*                   SPECIFIED IN THE DEVICE STANDARDS. STATISTICS ARE
*                   KEPT AS TO THE KIND AND FREQUENCY OF ERRORS. THESE
*                   STATISTICS ARE USED BY THE CE TO ISOLATED AND
*                   RECOGNIZE ERRORS.
*                   A COMMON INTERPRETER ROUTINE IS USED TO CHECK BOTH
*                   STATUS AND SENSE. ON RETURN FROM THE INTERPRETER
*                   THE PROPER ACTION OF RETRY, RETURN OR WRITE A
*                   MESSAGE TO THE OPERATOR IS TAKEN.
         EJECT
         SPACE 2
*        TITLE  ' INPUT OUTPUT CONTROL BLOCK (IOB) '
*
*
*               *****************************************
*               *    *    *         *    *              *
*             0 *FLG1*FLG2*  SENSE  *ECB *   ECB PTR    *
*               *    *    *         *CODE*              *
*               *****************************************
*               *                                       *
*             8 *        CHANNEL  STATUS  WORD          *
*               *                                       *
*               *****************************************
*               *    *              *         *         *
*            16 * CC *    START     *WGHT*   DCB PTR    *
*               *    *              *    *              *
*               *****************************************
*               *    *              *    *              *
*            24 *MODF*   RESTART    * INCR (2)* ERR CNTS*
*               *    *              *    *TP 1*         *
*               *****************************************
*               *                                       *
*            32 *  M *  B *  B *  C *  C *  H *  H *  R *
*               *                                       *
*               *****************************************
*
*
*              IOB DEFINITION
         SPACE 1
IOBFL1   EQU   0                        FLAGS 1
IOBFL2   EQU   1                        FLAGS 2
IOBSNS   EQU   2                        SENSE DATA
IOBCOD   EQU   4                        ECB CODE
IOBFL3   EQU   8                        IOB FLAG NO 3
IOBCSW   EQU   8                        CSW STORAGE
IOBCC    EQU   16                       CONDITION CODE
IOBST    EQU   17                       START ADDRESS
IOBDCB   EQU   21                       DCB POINTER
IOBMDB   EQU   24                       MODIFIER BYTE
IOBRST   EQU   25                       RESTART POINTER
IOBBCI   EQU   28                       BLOCK COUNT INCR.
IOBTPM   EQU   29                       T.P. EXTENT (M) BYTE
IOBECT   EQU   30                       ERROR COUNTS
         SPACE 2
*   THESE FLAGS ARE IN IOB FLAG 1
IOBCP1   EQU   X'80'                    TYPE OF CHANNEL PROGRAM
IOBCP2   EQU   X'40'                    TYPE OF CHANNEL PROGRAM
IOBERR   EQU   X'20'                    IOB ERROR FLAG
IOBMD1   EQU   X'10'                    MOD1 FLAG - POSITIONING
IOBMD2   EQU   X'08'                    MOD 2 FLAG - CRC
IOBEX    EQU   X'04'                    EXCEPT FLAG
IOBSRS   EQU   X'01'                    START/RESTART FLAG
IOBEXE   EQU   IOBEX+IOBERR             IOB EX + IOB ERROR FLAG
         SPACE 2
*   THESE FLAGS ARE IN IOB FLAG 2
IOBHLT   EQU   X'80'                    HALT I/O ISSUED FLAG
IOBQSM   EQU   X'01'                    QSAM ACCESS METHOD       A36344
         SPACE 2
*   THESE FLAGS ARE IN IOB FLAG 3
IOBENT   EQU   X'40'                    IOB ENTRY BIT
ERRDCT   EQU   X'20'                    READ ERROR CNT.
IOBBOE   EQU   X'10'                    BUS OUT ERROR CNT
IOBDCK   EQU   X'08'                    DATA CHECK FLAG
IOBPRT   EQU   X'08'                    PUNCH RETRY BIT          A36344
IOBMSG   EQU   X'04'                    MESSAGE TYPE
IOBOVR   EQU   X'02'                    OVER RUN CNT.
IOBPR2   EQU   X'02'                    QSAM ERROR COUNT         A36344
IOBLOG   EQU   X'01'                    LOGOUT FLAG
         SPACE 2
*   REGISTER DEFINITIONS
ERREG3   EQU   0                        WORK REG
TSTREG   EQU   1                        TWELVE STAR REG
ERREG2   EQU   2                        WORK REG
CRBREG   EQU   3                        CURRENT RB REG.
CCWREG   EQU   4                        CCW ADDRESS REG
ERPIBRG  EQU   6                        ERPIB PTR REG              CCHA
UCBREG   EQU   7                        UCB ADDRESS REG
COMREG   EQU   8                        COMMUNICATION TABLE REG
SAVREG   EQU   9                        SAVE REG.
IOBRG    EQU   10                       IOB ADDRESS REG
ERREG4   EQU   11                       WORK REG
ERRLNK   EQU   12                       LINK REG
ERREG1   EQU   13                       WORK REG 1
ERRETR   EQU   14                       RETURN REG
BASREG   EQU   15                       BASE REG.
         SPACE 2
*              ECB DEFINITIONS
ECBFL    EQU   0                        FLAGS
         SPACE 2
*              DCB DEFINITION
DCBFL    EQU   44                       FLAGS
DCBPER   EQU   X'C0'                    PERMANENT ERROR FLAGS
DCBEX    EQU   X'40'                    EXCEPTION CONDITION
         SPACE 2
*              TWELVE STAR DEFINITION
TSTLNK   EQU   0                        FORWARD 12* LINK
TSTUCB   EQU   2                        UCB POINTER
TSTTCB   EQU   4                        TCB ID BYTE              A36344
TSTIOB   EQU   5                        IOB POINTER
TSTDEB   EQU   9                        DEB POINTER
         SPACE 2
*              STATUS INDICATORS
CSWSMD   EQU   X'40'                    STATUS MODIFIER
CSWCHE   EQU   X'08'                    CHANNEL END
CSWUCK   EQU   X'02'                    UNIT CHECK
CSWUEX   EQU   X'01'                    UNIT EXCEPTION
CSWIL    EQU   X'40'                    WRONG LENGTH IND.
CSWCCK   EQU   X'01'                    CHAINING CHECK
CSWCDCK  EQU   X'08'                    CHANNEL DATA CHECK         CCHA
CSWCCCK  EQU   X'04'                    CHANNEL CNTRL CHECK      CCHA
CSWICCK  EQU   X'02'                    INTRFCE CNTRL CHECK        CCHA
         SPACE 2
*   UCB DEFINITION
UCBFL1   EQU   6                        UCB FLAGS
UCBSTI   EQU   9                        STATISTICS POINTER
UCBNRY   EQU   X'40'                    UCB NOT READY
         SPACE 2
*              MISC. DEFINITIONS
INTREQ   EQU   X'40'                    INTERVENTION REQUIRED    A36344
IOSTUS   EQU   X'10'                    STATUS BIT
IOCLBT   EQU   X'00'                    CLEAR BYTE
EIOBIN   EQU   X'20'                    BINARY MODE
EXCPER   EQU   15                       ERROR EXCP
ERRBLK   EQU   X'40'                    BLANK
EWRTCM   EQU   X'01'
IOBCC1   EQU   X'10'                    CONDITION CODE OF SIO
LOC016   EQU   16                       COMMUNICATION TABLE
VECTXL   EQU   44                       VECTOR TO XCTL RNT.
VECT68   EQU   68                       INTERPRETER ROUTINE
VECT72   EQU   72                       WRITE TO OPERATOR ROUTINE
STATAB   EQU   112                      INDEX TO STATISTICS TABLE
RETURN   EQU   3                        SVC RETURN
WTORTN   EQU   253                      LOAD NAME TO WTO RTN.
STATUP   EQU   254                      LOAD NAME TO STAT. UPDATE RNT.
IBPTR    EQU   116                      PTR TO PTR TO PTR TO ERPIB CCHA
ENDTBL   EQU   X'FF'                    END OF ERPIB TABLE         CCHA
INTRCPT  EQU   X'7E'                   INTERCEPT CODE              8886
NML      EQU   X'7F'                   NORMAL COMP CODE            8886
NXT      EQU   2                        NEXT CONTROL ENTRY         256D
TFTYSX   EQU   256                      INCREMENT STAT INDEX       256D
ERRMAX   EQU   X'05'                  MAX ERROR RETRIES          A33752
*                                       A3375                    A33752
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  2501, 2520, 1442  ERROR ROUTINE                                    *
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*      WHEN ENTERED THE IOB EX FLAG IN IOB FLAG 1 IS SET TO 1 BY      *
*        IO ERROR INTERFACE.                                          *
*        THE ERROR ROUTIN WIIL SET THE IOB ERROR FLAG INDICATING      *
*        THAT THIS IOB IS IN ERROR                                    *
*                                                                     *
*        THE COMBINATIONS OF THESE TWO BITS WILL BE INTERPRETED AS    *
*        FOLLOWS.                                                     *
*        IOBEX ON AND IOB ERR ON      IOB IN CONTROL BY ERR. ROUTINE  *
*        IOB EX ON AND IOB ERR OFF -  PERMANENT ERROR                 *
*        IOBEX OFF AND IOBERR OFF  -  ERROR CORRECTED                 *
***********************************************************************
         SPACE 3
         USING ER1442,15
ER1442   L     IOBRG,TSTIOB-1(TSTREG)   GET ADDR. OF IOB
         L     COMREG,LOC016            ADDRESS OF COMMUNICATION TABLE
         SPACE 1
         LH    UCBREG,TSTUCB(TSTREG)    GET UCB ADDRESS
         OI    IOBFL1(IOBRG),IOBEXE     SET IOB EX AND ERROR FLAG
         TM    IOBFL1(IOBRG),IOBCP1+IOBCP2    TEST FOR MIXED CH. PROGR
         BC    1,ERR006                 BR-TO SET MESSAGE TYPE
         SPACE 1
         SPACE 1
         L     CCWREG,IOBCSW(IOBRG)     GET CCW ADDRESS
         LA    CCWREG,0(CCWREG)         ZERO HIGH BYTE
         SH    CCWREG,ERRCN1            BACK UP ONE CCW
         TM    IOBECT(IOBRG),IOBPRT     IS PUNCH RETRY FLAG ON   A48623
         BC    1,ERR002                 YES-CHECK CCC+ICC        A36344
         SPACE 1
         TM    IOBFL1(IOBRG),IOBCP2     TEST FOR COMM. CHAINING
         BC    8,ERR002                 BR ON NO CMD. CHAIN
         TM    IOBFL3(IOBRG),IOBENT     TEST IF ENTRY BIT IS ON
         BC    8,ERR001                 BR- ENTRY BIT NOT ON
         NI    IOBMDB(IOBRG),X'00'      CLEAR MODF. BYTE
*    TEST IF ERROR OCCURED ON THE RESTARTED CCW
         LR    ERREG1,CCWREG
         S     ERREG1,IOBMDB(IOBRG)     SUBTRACT RESTART ADDRESS
         BC    8,ERR002                 BR- CCW ADDR. EQU TO RESTART
         MVI   IOBFL3(IOBRG),IOCLBT     CLEAR COUNTERS
ERR001   OI    IOBFL3(IOBRG),IOBENT     SET ENTRY BIT
         OI    IOBFL1(IOBRG),IOBSRS     SET RESTART FLAG
         ST    CCWREG,IOBMDB(IOBRG)     STORE CCW ADDR. IN RESTART
ERR002   TM    IOBCSW+5(IOBRG),CSWCCCK+CSWICCK CH/INTFCE CNTL CK   CCHA
         BC    4+2+1,ERZERIB            YES-ZERO ERPIB             CCHA
         SPACE 1
         TM    IOBCSW+4(IOBRG),CSWUCK   TEST IF UNIT CHECK         CCHA
         BC    8,ER023A                 BR- TO TEST STATUS INFORM.
         SPACE 1
*        INFORMATION FOR STATISTICS IS OBTAINED FROM SENSE BYTE 0
*        AND IS STORED IN THE WORK AREA OF THE STATISTIC TABLE
         SPACE 1
         L     ERREG2,STATAB(COMREG)    GET STATISTICS TABLE ADDRESS
         SR    ERREG4,ERREG4
         IC    ERREG4,UCBSTI(UCBREG)    GET STAT. TAB INDEX        256D
         LR    ERREG1,ERREG2            GET STAT. TAB START ADR    256D
ERRLOOP1 CH    UCBREG,0(0,ERREG1)       DEV SUPPORTED BY THIS SECT 256D
         BC    4,ERRLOOP2           YES,INCR ADJUSTED CORRECTLY    256D
         LA    ERREG1,NXT(0,ERREG1)     INCR TO NXT CTL SECTION    256D
         LA    ERREG4,TFTYSX(0,ERREG4)  INCR STAT INDEX TO NXT SECT256D
         BC    15,ERRLOOP1              LOOP TO CHECK THIS SECT    256D
         SPACE 1
ERRLOOP2 MH    ERREG4,ERRCN2            MULTIPLY BY 10             256D
         AR    ERREG2,ERREG4            ADD TO TABLE ADDRESS       256D
         OC    8(1,ERREG2),IOBSNS(IOBRG) OR SENSE BYTE INTO TABLE
         NI    8(ERREG2),X'7C'             MASK OUT UNWANTED BITS
         SPACE 1
*        LINK TO THE INTERPRETER ROUTINE - THE BYTES FOLLOWING THE BALR
*        INDICATE IN WHAT ORDER THE BITS ARE TO BE TESTED (BIT 3 ON OF
*        THE CODE BYTE INDICATES STATUS) FOLLOWED BY AN INDEX VALUE THE
*        INTERPRETER USES TO RETURN TO THIS ROUTINE
         SPACE 1
ER003A   LR    SAVREG,BASREG            SAVE BASE ADDRESS
         L     BASREG,VECT68(COMREG)    GET ADDR. TO INTERP.ROUTINE
ERR003   BALR  ERRLNK,BASREG            LINK TO INTERPRETER ROUTINE
         DC    X'06'                    SHOULD NOT OCCUR SENSE BIT
         DC    AL1(ERR004-ERR003-2)
         DC    X'03'                    EQUIPMENT CHECK          A24872
         DC    AL1(ERR008-ERR003-4)                              A24872
         DC    X'07'                    SHOULD NOT OCCUR SENSE   A24872
         DC    AL1(ERR004-ERR003-6)                              A24872
         DC    X'01'                    INTERVENTION REQUIRED
         DC    AL1(ERR036-ERR003-8)                                8886
         DC    X'02'                    BUS OUT CHECK
         DC    AL1(ERR013-ERR003-10)
         DC    X'04'                    DATA CHECK
         DC    AL1(ERR017-ERR003-12)
         DC    X'05'                    OVER RUN
         DC    AL1(ERR020-ERR003-14)
         DC    X'00'                    COMMAND REJECT
         DC    AL1(ERR006-ERR003-16)
         DC    X'2F'                    END OF TEST
         DC    AL1(ERR004-ERR003-18)
         SPACE 1
ERR017   B     ERR017A       DATA CHECK                          A48174
ERR020   B     ERR020A       OVER RUN                            A48174
ERZERIB  L     ERPIBRG,IBPTR(COMREG)    PTR TO PTR TO PTR TO ERPIB CCHA
         L     ERPIBRG,0(ERPIBRG)       PTR TO PTR TO ERPIB        CCHA
         LTR   ERPIBRG,ERPIBRG          CCH IN SYSTEM              CCHA
         BC    8,ERR004                 NO-LOG OUT                 CCHA
         L     ERPIBRG,0(ERPIBRG)       PTR TO ERPIB               CCHA
ERCMPR   CH    UCBREG,2(ERPIBRG)        IS ERPIB FOR 1442/2501/2520CCHA
         BC    8,ERZERO                                            CCHA
         SPACE 1
         LA    ERPIBRG,8(ERPIBRG)       ADDR OF NEXT ERPIB         CCHA
         TM    0(ERPIBRG),ENDTBL      END OF ERPID TABLE         A33752
         BC    8+4+2,ERCMPR             NO-TEST NEXT ERPIB         CCHA
         SPACE 1
         BC    1,ERR004                 YES-LOG OUT                CCHA
         SPACE 1
ERZERO   XC    0(8,ERPIBRG),0(ERPIBRG)  ZERO ERPIB                 CCHA
         BC    15,ERR004                LOG OUT                  A36344
ERR05A   TM    IOBSNS(IOBRG),INTREQ     TEST FOR INT REQ         A36344
         BC    1,ERR007                 YES-BYPASS MSG TYPE      A36344
ERR004   OI    IOBFL3(IOBRG),IOBLOG     INDICATE LOG OUT
ERR006   OI    IOBFL3(IOBRG),IOBMSG     SET MESSAGE TYPE
ERR007   LA    ERREG1,WTORTN            GET LOAD NAME TO WTO RTN.
         L     ERRETR,VECTXL(COMREG)    GET ADDR. TO XCTL RTN.
         BCR   15,ERRETR                XCTL TO WTO RTN.
         SPACE
*        INTERVENTION REQUIRED                                     8886
         SPACE
ERR036   CLI   IOBCOD(IOBRG),INTRCPT   IS IOB INTERCEPTED          8886
         BC    7,ERR007                NO-SEND MESSAGE             8886
         NI    IOBFL1(IOBRG),X'FF'-IOBSRS  RESET RESTART FLAG      8886
         MVI   IOBCOD(IOBRG),NML       SET NORM COMPLETION CODE    8886
         BC    15,ERR007               BR TO WTO RTN               8886
*
*      EQUIPMENT CHECK AND CHANNEL DATA CHECK ON READ CMD          CCHA
*
ERR008   CLI   IOBCOD(IOBRG),X'7E'      TEST FOR INTERCEPT       A36344
         BC    8,ERR005                 YES-BYPASS COMMAND TEST  A36344
         LTR   CCWREG,CCWREG            INVALID CCW ADDR?        A36344
        BC    12,ERR004                YES, PERM. ERROR
        TM    0(CCWREG),EWRTCM         IS THIS A WRITE COMMAND
         BC    1,ERR005                 YES-TEST FOR QSAM        A36344
         SPACE 1
         TM    IOBECT+1(IOBRG),ERRMAX  IS READ ERR. CNT EQ. 5    A33752
         BC    1,ERR008A              BR TO CLEAR ERR COUNTER    A33752
         LH    ERREG1,IOBECT(IOBRG)   PICK UP ERROR COUNT        A33752
         LA    ERREG1,1(ERREG1)       INCREMENT BY ONE           A33752
         STH   ERREG1,IOBECT(IOBRG)   STORE BACK ERROR COUNT     A33752
         BC    15,ERR007              GET WTO RTN                A33752
ERR008A  MVI   IOBECT+1(IOBRG),IOCLBT CLEAR ERROR COUNTER        A33752
         BC    15,ERR004              BR TO INDICATE LOG OUT     A33752
ERR005   MVI   IOBCOD(IOBRG),X'7F'      SET CODE TO NORMAL       A36344
         TM    IOBFL2(IOBRG),IOBQSM     TEST FOR QSAM            A36344
         BC    8,ERR05A                 NO-TEST INT REQ          A36344
         TM    IOBECT(IOBRG),IOBPR2     IS ERROR COUNT 1         A48623
         BC    1,ERR004                 YES-LOG OUT              A36344
         OI    IOBECT(IOBRG),IOBPR2     SET QSAM ERROR CNT       A48623
         NI    IOBFL1(IOBRG),X'FF'-IOBSRS CLR RESTART FLAG       A36344
         L     IOBRG,IOBCOD(IOBRG)      GET ECB PTR TO QSAM EXT  A36344
         LA    IOBRG,4(IOBRG)           ADJUST ADDR TO IOB IN    A36344
*                                       ERR                      A36344
         OI    IOBECT(IOBRG),IOBPRT     SET PUNCH RETRY          A48623
         LR    ERREG2,IOBRG             PUT ERROR IOB IN WORKREG A36344
         SH    ERREG2,ERRCN1            GET QSAM LINK FIELD      A36344
         L     ERREG2,0(ERREG2)         GET NEXT IOB EXT ADDR    A36344
         OI    IOBECT+8(ERREG2),IOBPRT  SET PRT IN NEXT IOB      A48623
         OI    IOBFL1(IOBRG),IOBEXE     SET IOB EX AND ERR FLAGS A36344
         IC    ERREG4,TSTTCB(TSTREG)    SAVE TCB ID              A36344
         ST    IOBRG,TSTTCB(TSTREG)     STORE IOB ADDR IN RQE    A36344
         STC   ERREG4,TSTTCB(TSTREG)    RESTORE TCB ID           A36344
         BC    15,ERR007                GET WTO ROUTINE          A36344
         SPACE 1
*      BUS OUT CHECK
*
ERR013   CLI   19(UCBREG),X'04'         IS THIS A 2520
         BC    2,ERR014                 BR-ON YES
         TM    IOBCSW+4(IOBRG),CSWCHE   TEST FOR CHAN. END
         BC    1,ERR004                 BR- TO REQUEST LOG OUT
ERR014   TM    IOBFL3(IOBRG),IOBBOE     IS BUS OUT FLAG ON
         BC    1,ERR004                 BR-TO REQUEST LOG OUT
         OI    IOBFL3(IOBRG),IOBBOE     SET BUS OUT CNT. TO 1
ERR01A   SVC   EXCPER                   RE EXECUTE LAST OPERATION
         SVC   RETURN
*
*      DATA CHECK
*
ERR017A  TM    0(CCWREG),EIOBIN         TEST IF BINARY MODE      A48174
         BC    1,ERR004
         TM    IOBFL3(IOBRG),IOBDCK     TEST IF DATA CHECK FLAG IS ON
         BC    1,ERR004                 BR-TO REQUEST LOG OUT
         OI    IOBFL3(IOBRG),IOBDCK     SET DATA CHECK FLAG ON
         BC    15,ERR007                GET WTO RTN
*
*      OVER RUN
*
ERR020A  TM    IOBFL3(IOBRG),IOBOVR     IS OVER RUN CNT. 1       A48174
         BC    1,ERR004                 BR-TO REQUEST LOG OUT
         OI    IOBFL3(IOBRG),IOBOVR     SET OVER RUN COUNT TO 1
         BC    15,ERR007                GET WTO RTN
         SPACE 1
         SPACE 1
*        LINK TO THE INTERPRETER ROUTINE - THE BYTES FOLLOWING THE BALR
*        INDICATE IN WHAT ORDER THE BITS ARE TO BE TESTED (BIT 3 ON OF
*        THE CODE BYTE INDICATES STATUS) FOLLOWED BY AN INDEX VALUE THE
*        INTERPRETER USES TO RETURN TO THIS ROUTINE
         SPACE 1
ER023A   LR    SAVREG,BASREG            SAVE BASE ADDRESS
         L     BASREG,VECT68(COMREG)    GET ADDR. TO INTERP.ROUTINE
ERR023   BALR  ERRLNK,BASREG            LINK TO INTERPRETER ROUTINE
         DC    X'1C'                    CHANNEL DATA CHECK         CCHA
         DC    AL1(ERR035-ERR023-2)                                CCHA
         DC    X'1F'                    CHAINING CHECK             CCHA
         DC    AL1(ERR029-ERR023-4)                                CCHA
         DC    X'1A'                    PROGRAM CHECK              CCHA
         DC    AL1(ERR029-ERR023-6)                                CCHA
         DC    X'1B'                    PROTECTION CHECK           CCHA
         DC    AL1(ERR029-ERR023-8)                                CCHA
         DC    X'17'                    UNIT EXCEPTION             CCHA
         DC    AL1(ERR029-ERR023-10)                               CCHA
         DC    X'19'                    INCORRECT LENGTH           CCHA
         DC    AL1(ERR029-ERR023-12)                               CCHA
         DC    X'2F'                    END OF TEST                CCHA
         DC    AL1(ERR030-ERR023-14)                               CCHA
         SPACE 1
*        PROGRAM OR PROTECTION CHECK
*        UNIT EXCEPTION OR INCORRECT LENGTH
ERR029   TM    IOBECT(IOBRG),IOBPRT     TEST FOR QSAM RETRY      A48623
         BC    1,ERR030A                YES-UPDATE NEXT IOB      A36344
         NI    IOBFL1(IOBRG),X'FF'-IOBERR CLEAR ERROR FLAG       A36344
         SVC   EXCPER                   ERROR EXCP
         SVC   RETURN                   RETURN
         SPACE 1
ERR030   NI    IOBFL1(IOBRG),X'FF'-IOBEXE-IOBSRS RESET IOB EX,ERR,REST
         TM    IOBECT(IOBRG),IOBPRT     TEST FOR QSAM RETRY      A48623
         MVI   IOBFL3(IOBRG),IOCLBT     CLEAR ERR COUNTS
         BC    8,ERR031                 NO, GO TO STAT UPDATE    A36344
ERR030A  SH    IOBRG,ERRCN1             GET QSAM LINK FIELD      A36344
         L     IOBRG,0(IOBRG)           GET NEXT IOB EXT ADDR    A36344
         LA    IOBRG,8(IOBRG)           ADJ ADDR TO IOB          A36344
         OI    IOBFL1(IOBRG),IOBEXE     SET IOB EX AND ERR FLAGS A36344
         IC    ERREG4,TSTTCB(TSTREG)    SAVE TCB ID              A36344
         ST    IOBRG,TSTTCB(TSTREG)     STORE IOB ADDR IN RQE    A36344
         STC   ERREG4,TSTTCB(TSTREG)    RESTORE TCB ID           A36344
         SVC   EXCPER                   ERROR EXCP               A36344
         SVC   3                        RETURN                   A36344
         SPACE 1
ERR031   LA    ERREG1,STATUP            GET SUR LOAD NAME        A36344
         L     ERRETR,VECTXL(COMREG)    GET ADDR. TO XCTL RNT.
         BCR   15,ERRETR                BR-TO XCTL RNT.
         SPACE 1
ERR035   BC    15,ERR008                RETRY READ ON CH DATA CK   CCHA
         SPACE 2
         SPACE 1
ERRCN1   DC    AL2(8)                   DECIMAL 8
ERRCN2   DC    AL2(10)                  MULTIPLIER CONSTANT
         END
         ORG   START+X'0FFF'
DUMMY    DC    X'00'
