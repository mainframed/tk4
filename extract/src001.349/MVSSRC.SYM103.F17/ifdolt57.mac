IFD57    TITLE '*** IFDOLT57 - OLTEP DISABLED INTERUPT RTN - DIE ***'
IFDOLT57 START
*A-000000-999999                                                @Y02008
*C-318000,808000      VS2-2 PTM 8623                           @YM08623
***********************************************************************
*                                                                     *
*MODULE NAME: IFDOLT57                                                *
*                                                                     *
*DESCRIPITIVE NAME: OLTEP DISABLED INTERRUPT RTN -  DIE               *
*                                                                     *
*COPYRIGHT: NONE                                                      *
*                                                                     *
*STATUS: FIRST RELEASE FOR OS-VS/2 RELEASE 2   VERSION 0              *
*                                                                     *
*FUNCTION: THIS MODULE RECIEVES CONTROL FROM IOS INTERRUPT HANDLER.   *
*      IT IS ENTERED TWICE FOR I/O OPERATIONS WITH DEVICE END POST    *
*   INVOKED, ONCE FOR NON-ENDING STATUS PCI'S, AND TWICE IF CHANNEL   *
*   END-UNIT CHECK COME IN AND DEVICE END POSTING IS ENVOKED WITH     *
*   DEVICE END SEPARATE. IT IS ENTERED AFTER CHANNEL CHECK HANDLER    *
*   PROCESSING AND AFTER UNIT CHECK IS CLEARED WITH A SENSE. IT SAVES *
*   THE CSW FROM THE IOSB AND SAVES THE SENSE INFORMATION FROM THE    *
*   ERP WORK AREA IN THE OECB-AN AREA POINTED TO BY A WORD IN OLTEP'S *
*   CONTROL BLOCK WHICH IS LOCATED BY THE IOSUSE FIELD IN THE IOSB.   *
*      IF ATTENTION HANDLING IS REQUESTED-A BIT IN THE IOSUSE CONTROL *
*   BLOCK, THE UCB BUSY BIT AND THE UCB POST BIT ARE TURNED ON TO     *
*   ALLOW ONE MORE INTERRUPT FROM THE ASSOCIATED DEVICE.              *
*      RETURN TO IOS IS ALWAYS TO REG 14 + 0.                         *
*                                                                     *
*NOTES:                                                               *
*   DEPENDENCIES- NO HARDWARE DEPENDENCIES.                           *
*               - SOFTWARE OF VS2 RELEASE 2 OR GREATER.               *
*                                                                     *
*   RESTRICTIONS- NOT COMPATIBLE FOR VS1.                             *
*                                                                     *
*   REGISTER CONVENTIONS:                                             *
*     ENTRY- ALL REGS ARE SAVED                                       *
*       REG 2 = IOSB POINTER.                                         *
*           7 = UCB POINTER.                                          *
*          13 = POINTER TO A 16 WORD SAVE AREA.                       *
*          14 = RETURN POINT TO IOS.                                  *
*     EXIT- N/A.                                                      *
*                                                                     *
*   SAVE AREA: SINCE NO EXTERNAL CALLS ARE MADE, NO SAVE AREA EXISTS. *
*                                                                     *
*   PATCH AREA LABEL: PATCH57.                                        *
*                                                                     *
*MODULE TYPE: PROCEDURE.                                              *
*                                                                     *
*   PROCESSOR: OS/VS ASSEMBLER.                                       *
*                                                                     *
*   MODULE SIZE: APPROXIMATELY 1.5K BYTES.                            *
*                                                                     *
*   ATTRIBUTES: REENTRANT,PRIVILEGED,DISABLED,KEY 0,FIXED,GLOBAL.     *
*                                                                     *
*ENTRY POINT: AT IFDOLT57.                                            *
*                                                                     *
*   PURPOSE: TO SAVE STATUS AND SENSE TO LATER BE POSTED (BY IFDOLT58)*
*     INTO THE OLT AND PERFORM THE OLT ATTENTION FUNCTION (ALLOW ONE  *
*     MORE INTERRUPT AFTER DEVICE END).                               *
*                                                                     *
*   LINKAGE: FROM IOS INTERRUPT HANDLER FOR EACH EXPECTED OLTEP DEVICE*
*     INTERRUPT.                                                      *
*                                                                     *
*INPUT: THE IOSB WHICH WAS SCHEDULED FOR THE I/O OPERATION.           *
*                                                                     *
*OUTPUT: THE STATUS,AND SENSE IF NEEDED, IS SAVED IN THE OECB-A WORK  *
*     AREA ASSOCIATED WITH THE IOSB.                                  *
*                                                                     *
*EXIT-NORMAL: BACK TO IOS ON REG 14+0.                                *
*                                                                     *
*EXIT-ABNORMAL: N/A                                                   *
*                                                                     *
*EXTERNAL REFERENCES:                                                 *
*                                                                     *
*     ROUTINES: NONE                                                  *
*                                                                     *
*     DATA AREAS: OLTEP EVENT CONTROL BLOCK (OECB).                   *
*                                                                     *
*     CONTROL BLOCK: IOSB IN SUBPOOL 245.                             *
*                                                                     *
*TABLES: NO INTERNAL TABLES.                                          *
*                                                                     *
*MACROS: NONE.                                                        *
*                                                                     *
*CHANGE ACTIVITY: BELOW.                                              *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASEREG1
         B     IFD57000           BRANCH AROUND POINTERS/CONSTANTS
         DC    XL4'80000000'      RETAIN/370 DEB POINTER
         DC    F'0'               INITIALING OLTEP'S TCB POINTER
         DC    A(IFD57999-IFDOLT57)    SIZE OF THIS MODULE
         DC    A(OLTDICB1-IFDOLT57)    DISPLACEMENT TO CNTRL BLK SET 1
         DC    A(OLTDICB2-IFDOLT57)    DISPLACEMENT TO CNTRL BLK SET 2
         DC    A(OLTDICB3-IFDOLT57)    DISPLACEMENT TO CNTRL BLK SET 3
         DC    F'0'               UCB BIT MASK TABLE POINTER
         DC    F'0'               RETAIN/370 UCB ADDRESS
         DC    F'0'               MEMORY ASID FOR THIS OLTEP
         DC    18F'0'             SAVE AREA FOR OLTEP SVC-59'S USE
***********************************************************************
*                   INITIALIZATION                                    *
***********************************************************************
         DC    C'IFDOLT57&SYSDATE'     NAME OF MOD & ASM DATE  @YM03321
         DS    0H                                              @YM03321
IFD57000 EQU   *
         STM   REG0,REG15,0(SAVEREG)   SAVE REGS IN LCCA SAVE AREA
         USING IOSB,IOSBREG       ADDRESSABILITY FOR IOSB
         USING UCB,UCBREG         ADDRESSABILITY FOR THE UCB    YM08623
         EJECT
***********************************************************************
*                  MISC EQUATES                                       *
***********************************************************************
         SPACE 3
PCIBIT   EQU   X'80'              PROGRAM CNTRLD INTRUPT CSW BIT
CEBIT    EQU   X'08'              CHANNEL END CSW BIT
DEBIT    EQU   X'04'              DEVICE END CSW BIT
UCBIT    EQU   X'02'              UNIT CHECK CSW BIT
ATTNBIT  EQU   X'80'              ATTENTION CSW BIT
UEXBIT   EQU   X'01'              UNIT EXCEPTION CSW BIT
INCLHBIT EQU   X'40'              INCORRECT LENGTH CSW BIT
PGMCKBIT EQU   X'20'              PROGRAM CHECK CSW BIT
PROTCBIT EQU   X'10'              PROTECT CHECK CSW BIT
CHDATBIT EQU   X'08'              DATA CHECK CSW BIT
CHCTRBIT EQU   X'04'              CONTROL CHECK CSW BIT
IFCTRBIT EQU   X'02'              INTERFACE CHECK CSW BIT
CHCHNBIT EQU   X'01'              CHAINING CHECK CSW BIT
ALL      EQU   X'FF'              BIT MASK FOR ALL BITS
STATCNT  EQU   6                  STATUS COUNT REGISTER
THREE    EQU   3                  DECIMAL CONSTANT OF 3
ERPWKPTR EQU   4                  RESISTER TO ADDRESS ERP WORK AREA
STATPTR  EQU   5                  REGISTER TO POINT TO OECB STATUS SLOT
EXECUTRG EQU   6                  REGISTER TO USE FOR EXECUTE INSTRUCTS
ONE      EQU   1                  DECIMAL CONSTANT OF 1
EWASNS   EQU   X'20'              DISPLACEMENT TO SENSE IN ERP WORK
*                                 AREA
         EJECT
***********************************************************************
*                  REGISTER EQUATES                                   *
***********************************************************************
         SPACE 2
REG0     EQU   0                  NAME FOR REGISTER 0
IOSBREG  EQU   2                  POINTER TO IOSB FOR THIS INTERRUPT
*                                  FROM IOS FOR THE IOSB DSECT
UCBREG   EQU   7                  POINTER TO UCB FOR THIS INTERRUPT
*                                  FROM IOS FOR THE UCB DSECT
WORKREG  EQU   8                  A WORKING REGISTER
REG12    EQU 12                   NAME FOR REGISTER 12
SAVEREG  EQU   13                 IOS SAVE AREA POINTER REG
REG14    EQU   14                 RETURN POINT TO IOS
REG15    EQU   15                 NAME FOR REGISTER 15
BASEREG1 EQU   15                 ENTRY POINT AND BASE REGISTER
         EJECT
*   IF PCI WITHOUT ENDING STATUS DON'T SAVE THIS CSW. THE PCI EXIT    *
*     POSTS IT DIRECTLY INTO THE OLT'S TECB.
         TM    IOSTSA,CEBIT+DEBIT+UCBIT+UEXBIT      ANY THESE BITS ON
         BNZ   IFD57010           YES-GO SAVE CSW
         TM    IOSTSB,PCIBIT      IS PCI BIT ON
         BNO   IFD57010           NO-GONO-GO SAVE CSW
         TM    IOSTSB,ALL-PCIBIT  ANY BITS OTHER THAN PCI ON?
         BZ    IFD57060                NO-LET PCI EXIT POST IT IN TECB
*   THIS ROUTINE SAVES THE CSW IN THE OECB TO BE LATER POSTED.
IFD57010 EQU   *
         CLI   OINCNT,THREE       IS INTERRUPT > THREE
         BNL   IFD57060           YES-GO RESTORE THE REGS
         XR    STATCNT,STATCNT    ZERO STATUS COUNT REG
         IC    STATCNT,OINCNT     GET COUNT OF EVENTS
         SLL   STATCNT,THREE      ADJUST BYTE COUNT FROM # OF DOUBLE
*                                   WORDS BY MULTIPLYING BY 8
         LA    STATPTR,OCSW1      POINT TO FIRST EVENT LOCATION
         LA    STATPTR,0(STATCNT,STATPTR)   POINT TO LOCATION FOR NEXT
*                                             EVENT
         LTR   STATCNT,STATCNT    IS EVENT COUNT ZERO
         BZ    IFD57030           YES-GO MOVE 1ST EVENT
         SPACE 2
*   THIS POINT IS REACHED IF THIS ISN'T 1ST INTERRUPT
         TM    IOSTSA,CEBIT       IS CHANNEL END IN THIS INTERRUPT
         BNO   IFD57050           NO-GO MOVE THIS EVENT
         OC    OCSWSTS1(L'OCSWSTS1),IOSTATUS     YES-THIS IS OR'D
*                                   STATUS SO POST AS ONE EVENT WITH
*                                   OR'D STATUS
         TM    OCSWSTA1,UCBIT     DID UNIT CHECK OCCUR
         BNO   IFD57060           NO-GO RESTORE REGS AND RETURN
IFD57020 EQU   *                  MOVE THE SENSE FROM ERP WORK TO OECB
         L     ERPWKPTR,IOSERP    GET ERP WORK AREA ADDRESS
         XR    EXECUTRG,EXECUTRG  CLEAR THE EXECUTE REGISTER
         IC    EXECUTRG,OSNSCNT   GET SENSE COUNT OLT EXPECTS
         BCTR  EXECUTRG,0         SUBTRACT 1 FROM COUNT FOR MVC EXECUTE
         EX    EXECUTRG,MOVESNS   MOVE THE SENSE FROM ERP WORK AREA TO
*                                   THE OECB SENSE AREA.
         B     IFD57060           GO RESTORE REGS AND RETURN
         SPACE 3
*   MOVE THE FIRST CSW TO THE OECB - OCSW1
IFD57030 EQU   *
         MVC   OCSW1(L'OCSW1),IOSCC    MOVE SIO COND CODE AND CSW TO
*                                        OECB
         MVI   OINCNT,ONE         SET THE OINCNT EQUAL TO 1
         TM    OCSWSTA1,UCBIT     DID UNIT CHECK OCCUR
         BNO   IFD57040           NO-GO CHECK IF ATTENTION TO BE SET
         TM    OCSWSTA1,DEBIT     YES-DID DEVICE END COME WITH IT
         BO    IFD57020           YES-EVENT DONE-GO GET SENSE-NOTE THE
*                                   ATTN=YES IS IGNORED HERE.
         TM    OCSWSTA1,CEBIT     HAS CHNL END COME IN WITH UNIT
*                                  CHECK ON 1ST INTERRUPT WITH OUT DEV
*                                  END
         BNO   IFD57020           NO-EVENT COMPLETE. GO SAVE SENSE
         TM    IOSOPT,IOSDEP      IS DE POSTING ACTIVE
         BNO   IFD57020           NO-THIS ONLY ENTRY.GO SAVE SENSE
         B     IFD57060           GET SENSE ON ENTRY FOR DEV END.
*                                  SENSE NOT AVAIL NOW. GO RESTORE REGS
         SPACE 5
***********************************************************************
*                                                                     *
*   THIS ROUTINE CHECKS IF ATTENTION=YES REQUESTED. IF YES-ONE MORE   *
*     INTERRUPT IS ALLOWED AFTER THE FIRST INTERRUPT (CHANNEL END AND *
*     DEVICE END TOGETHER). THIS IS DONE BY TURNING ON THE UCB POST   *
*     AND BUSY BITS THEN RETURNING TO IOS.                            *
*                                                                     *
***********************************************************************
IFD57040 EQU   *
         TM    USEFLAGA,ATTN      IS ATTENTION REQUESTED
         BNO   IFD57060           NO-GO RESTORE REGS AND RETURN
         OI    UCBFLA,UCBBSY+UCBPST    TURN ON UCB BUSY AND POST FLAGS
*                                   FOR ATTN FUNCTION
         NI    USEFLAGA,ALL-ATTN  TURN OFF ATTENTION BIT IN USER CB
         B     IFD57060           GO RESTORE REGS AND RETURN
         SPACE 5
IFD57050 EQU   *
*   THIS ROUTINE SAVES THE SECOND EVENT IN THE OECB
         MVC   0(L'OCSW2,STATPTR),IOSCC     SAVE THIS INTERRUPT AND
*                                             SIO COND CODE
         IC    WORKREG,OINCNT     GET THE NUMBER OF EVENTS POSTED COUNT
         LA    WORKREG,ONE(WORKREG)    ADD 1 TO IT
         STC   WORKREG,OINCNT     PUT NEW COUNT INTO OECB
         TM    IOSTSA,UCBIT       IS THERE A UNIT CHECK WITH THIS EVENT
         BO    IFD57020           YES-GO GET SENSE
*   RESTORE IOS REGS AND RETURN TO THEM.
IFD57060 EQU   *
         LM    REG0,REG15,0(SAVEREG)   RESTORE IOS REGS
         BR    REG14              RETURN TO IOS
         SPACE 5
***********************************************************************
*                  EXECUTE  INSTRUCTIONS                              *
***********************************************************************
MOVESNS  MVC   OSNS1(0),EWASNS(ERPWKPTR)    MOVE THE SENSE FROM THE
*                                             ERP WORK AREA TO THE OECB
*                                             SENSE AREA.
         EJECT
***********************************************************************
*                  CONSTANTS FOR THIS MODULE                          *
***********************************************************************
         DS    0F
OLTDICB1 DS    0CL240             OLTEP I/O CONTROL BLOCK 1
         SPACE
DIESRB1  DC    XL44'0'            SRB FOR IOS TO SCHEDULE
DIEIOSB1 DC    XL108'0'           IOSB-OLTEP/IOS COMMUNICATION
DIEUSE1  DC    XL24'0'            OLTEP'S IOSB USER CNTRL BLK  @YM03321
DIEOECB1 DC    XL64'0'            OLTEP'S EVENT CONTROL BLOCK
         SPACE 5
         DS    0F
OLTDICB2 DS    0CL240             OLTEP I/O CONTROL BLOCK 2
         SPACE
DIESRB2  DC    XL44'0'            SRB FOR IOS TO SCHEDULE
DIEIOSB2 DC    XL108'0'           IOSB-OLTEP/IOS COMMUNICATION
DIEUSE2  DC    XL24'0'            OLTEP'S IOSB USER CNTRL BLK  @YM03321
DIEOECB2 DC    XL64'0'            OLTEP'S EVENT CONTROL BLOCK
         SPACE 5
         DS    0F
OLTDICB3 DS    0CL240             OLTEP I/O CONTROL BLOCK 3
         SPACE
DIESRB3  DC    XL44'0'            SRB FOR IOS TO SCHEDULE
DIEIOSB3 DC    XL108'0'           IOSB-OLTEP/IOS COMMUNICATION
DIEUSE3  DC    XL24'0'            OLTEP'S IOSB USER CNTRL BLK  @YM03321
DIEOECB3 DC    XL64'0'            OLTEP'S EVENT CONTROL BLOCK
         SPACE 5
         IFDPATCH
IFD57999 EQU   *                  END OF THIS MODULE
         EJECT
***********************************************************************
*                   DSECTS                                            *
***********************************************************************
         IEFUCBOB PREFIX=YES      DSECT FOR THE SYSTEM UCB
         SPACE 5
         IHASRB                   DSECT FOR THE SYSTEM SRB
         SPACE 3
         IECDIOSB                 DSECT FOR THE SYSTEM IOSB
         SPACE  3
*  DESCT FOR OLTEP'S IOSB USER CONTROL BLOCK AND EVENT CONTROL BLOCK
         SPACE
IOSUSECB EQU   *                  START OF USER CONTROL BLOCK
USERCB   DS    0CL24              START OF IOSB USER CONTROL BLOCK
OECBPTR  DS    F                  POINTER TO OLTEP EVENT CONTROL BLOCK
TECBPTR  DS    F                  OLT'S TECB POINTER FOR THIS EVENT
IODVTBPT DS    F                  POINTER TO THREE WORDS IN OLTEP
*                                  COMMON
*                                   AREA WHICH HAVE:
*                                       0   DEVTAB ENTRY POINTER
*                                       4   POINTER TO THIS IOSB
*                                       8   THE ECB FOR THIS EVENT
TCCWPTR  DS    F                  POINTER TO THE TCCW SYSTEM CONTROL
*                                    BLOCK FOR THE CCW TRANSLATOR
USEFLAGS DS    0F                 A WORD OF FLAGS
USEFLAGA DS    1C                 FIRST FLAG BYTE FOR THIS CB SET
INUSE    EQU   X'80'              THIS IOSB IS IN USE
ATTN     EQU   X'40'              ATTENTION=YES REQUEST (OFF IF DONE)
VIRTCCW  EQU   X'20'              THE CCW'S ARE IN VIRTUAL
OLDTECB  EQU   X'10'              OLD TECB TO BE POSTED
INTRNLCB EQU   X'08'              REQUEST IS INTERNAL (LINE CONNECT,DP)
PURGED   EQU   X'04'              THIS I/O EVENT IS PURGED
USEFLAGB DS    1C                 UNUSED
USEFLAGC DS    1C                 UNUSED
USEFLAGD DS    1C                 UNUSED
USERSVD  DS    F                  RESERVED FOR FUTURE
         SPACE
OECB     DS    0CL64              START OF OLTEP'S EVENT CONTROL BLOCK
OSNSCNT  DS    CL1                OLT'S SENSE BYTES REQUESTED COUNT
OINCNT   DS    CL1                NUMBER OF INTERRUPTS IN OECB
UNUSED   DS    CL2                NOT USED
OCSW1    DS    0CL8               FIRST INTERRUPT POSTED IN OECB
OCSWCC1  DS    CL1                IOS SIO CONDITION CODE
OCSWCMD1 DS    CL3                CSW COMMAND ADDRESS
OCSWSTS1 DS    0CL2               CSW1 STATUS BYTES
OCSWSTA1 DS    CL1                CSW1 STATUS BYTE A
OCSWSTB1 DS    CL1                CSW1 STATUS BYTE B
OCSWBYT1 DS    CL2                CSW1 RESIDUAL BYTE COUNT
OCSW2    DS    0CL8               SECOND INTERRUPT POSTED IN OECB
OCSWCC2  DS    CL1                IOS SIO CONDITION CODE
OCSWCMD2 DS    CL3                CSW COMMAND ADDRESS
OCSWSTS2 DS    0CL2               CSW2 STATUS BYTES
OCSWSTA2 DS    CL1                CSW2 STATUS BYTESA
OCSWSTB2 DS    CL1                CSW2 STATUS BYTESB
OCSWBYT2 DS    CL2                CSW2 RESIDUAL BYTE COUNT
OCSW3    DS    0CL8               THIRD INTERRUPT POSTED IN OECB
OCSWCC3  DS    CL1                IOS SIO CONDITION CODE
OCSWCMD3 DS    CL3                CSW COMMAND ADDRESS
OCSWSTS3 DS    0CL2               CSW3 STATUS BYTES
OCSWSTA3 DS    CL1                CSW3 STATUS BYTESA
OCSWSTB3 DS    CL1                CSW3 STATUS BYTESB
OCSWBYT3 DS    CL2                CSW3 RESIDUAL BYTE COUNT
OSNS1    DS    CL36               SENSE SAVE AREA FOR UNIT CHECK ON
*                                   THIS REQUEST
         END
