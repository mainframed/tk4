         TITLE 'IFDOLTAB DTIME'
IFDOLTAB CSECT
***********************************************************************
*                                                                     *
*  MODULE NAME = IEDOLTAB                                             *
*                                                                     *
*  DESCRIPTIVE NAME = DTIME                                           *
*                                                                     *
*  COPYRIGHT = NONE                                                   *
*                                                                     *
*  STATUS                                                             *
*        CHANGE LEVEL 000                                             *
*                                                                     *
*  FUNCTION                                                           *
*        THIS MODULE SUPPORTS THE $DTIME MACRO. THE TIME IS RETRIEVED *
*        FROM THE SYSTEM AND PASSED TO THE OLT IN THE FORMAT HHMMSS00.*
*                                                                     *
*  ENTRY POINTS                                                       *
*        IFDOLTAB                                                     *
*                                                                     *
*  INPUT                                                              *
*        REG 2 = POINTER TO OLTEP COMMON AREA                         *
*           13 = POINTER TO CALLER'S SAVE AREA                        *
*           14 = RETURN ADDRESS                                       *
*           15 = ENTRY POINT OF THIS MODULE                           *
*                                                                     *
*  OUTPUT                                                             *
*        RETURN CODES                                                 *
*           R15 = 00  SUCCESSFUL OPERATION                            *
*               = 04  TIME NOT AVAILABLE                              *
*        TIME IN                                                      *
*            R1 = HHMMSS00                                            *
*                                                                     *
*  EXTERNAL REFERENCES                                                *
*        ISSUES THE SYSTEM TIME MACRO                                 *
*                                                                     *
*  EXITS NORMAL                                                       *
*        AT THE LABEL - TIME0800                                      *
*                                                                     *
*  EXITS ERROR                                                        *
*        NONE                                                         *
*                                                                     *
*  TABLES AND WORKAREAS                                               *
*        NONE                                                         *
*                                                                     *
*  CHANGE ACTIVITY = AS FOLLOWS:                                      *
*        NONE                                                         *
*                                                                     *
***********************************************************************
         SPACE 3
         SAVE  (14,12),,IFDOLTAB&SYSDATE  SAVE REGS (EXCEPT 13)  X03906
         BALR  BASEREG,ZERO            ESTABLISH ADDRESSABILITY
         USING *,BASEREG               ASSIGN BASE REG
         SPACE
         ST    SAVEREG,SAVEAREA+4      SAVE CALLER'S SAVAREA ADDR
         LA    R15,SAVEAREA            GET MY SAVAREA ADDR
         ST    R15,EIGHT(ZERO,SAVEREG) PUT IT IN CALLER'S SAV AREA
         LR    SAVEREG,R15             KEEP MY SAVE AREA ADDR IN R13
         SPACE
         CLI   1(R1),2                    MACRO CALL LEVEL CHECK YM5400
         BE    GDLEVEL                    GOOD LEVEL MACRO CALL  YM5400
         L     SAVEREG,FOUR(ZERO,SAVEREG) GET POINTER TO CALLERS YM5400
*                                          SAVE AREA             YM5400
         L     R0,20(ZERO,SAVEREG)      RESTORE REG 0            YM5400
         LM    R2,R12,28(SAVEREG)       RESTORE REGS 2-12        YM5400
         RETURN (14),T,RC=4                                      YM5400
         SPACE 4
GDLEVEL  EQU   *                                                 YM5400
         TIME  DEC                     GET PACKED DECIMAL TIME
         SPACE 2
         LR    R1,R0                   GET TIME AS RETURNED FROM OS
         IC    R1,HEX00                ZERO OUT LOW BYTE
         L     SAVEREG,FOUR(ZERO,SAVEREG) GET POINTER TO CALLERS SVAREA
         L     R0,20(ZERO,SAVEREG)      RESTORE REG 0
         LM    R2,R12,28(SAVEREG)       RESTORE REGS 2-12
         RETURN (14),T,RC=0
         SPACE 2
R0       EQU   0                       REGISTER 0
R1       EQU   1                       REGISTER 1
R2       EQU   2                        REGISTER 2
BASEREG  EQU   11                      BASE REGISTER
R12      EQU   12                       REGISTER 12
SAVEREG  EQU   13                      SAVE REGISTER
R15      EQU   15                      REGISTER 15
ZERO     EQU   0                       ZERO DISPLACEMENT
FOUR     EQU   4                       DISPLACEMENT 4
EIGHT    EQU   8                       EIGHT DISPLACEMENT
HEX00    DC    X'00'                   TO ZERO R1, RIGHTMOST BYTE
SAVEAREA DC    18F'0'                   SAVE AREA
         LTORG
         END
