         TITLE 'IFDOLTAA DDATE'
IFDOLTAA CSECT
***********************************************************************
*  MODULE NAME = IFDOLTAA                                             *
*                                                                     *
*  DESCRIPTIVE NAME = DDATE                                           *
*                                                                     *
*  COPYRIGHT = NONE                                                   *
*                                                                     *
*                                                                     *
*  STATUS                                                             *
*        CHANGE LEVEL 000                                             *
*                                                                     *
*  FUNCTION                                                           *
*        THIS MODULE SUPPORTS THE $DDATE MACRO. THE DATE IS RETRIEVED *
*        FROM THE SYSTEM AND PASSED TO THE OLT IN THE FORMAT MM/DD/YY.*
*                                                                     *
*  ENTRY POINTS                                                       *
*        IFDOLTAA                                                     *
*                                                                     *
*  INPUT                                                              *
*        REG 2 = POINTER TO OLTEP COMMON AREA                         *
*           13 = POINTER TO CALLER'S SAVE AREA                        *
*           14 = RETURN ADDRESS                                       *
*           15 = ENTRY POINT OF THIS MODULE                           *
*                                                                     *
*  OUTPUT                                                             *
*        RETURN CODES                                                 *
*           R15 = 00  SUCCESSFUL OPERATION                            *
*               = 04  DATE NOT AVAILABLE                              *
*        DATE IN                                                      *
*           R0  = MM/D                                                *
*           R1  = D/YY                                                *
*                                                                     *
*  EXTERNAL REFERENCES                                                *
*        ISSUES THE SYSTEM TIME MACRO                                 *
*                                                                     *
*  EXITS NORMAL                                                       *
*        AT THE LABEL - DATE0800                                      *
*                                                                     *
*  EXITS ERROR                                                        *
*        NONE                                                         *
*                                                                     *
*  TABLES AND WORKARES                                                *
*        INTERNAL TO THIS MODULE, NOT REFERENCED BY OTHER MODULES.    *
*                                                                     *
*  CHANGE ACTIVITY = AS FOLLOWS:                                      *
*        NONE                                                         *
*                                                                     *
***********************************************************************
         SAVE  (14,12),,IFDOLTAA&SYSDATE   SAVE REGS (EXCEPT 13) X03906
         BALR  BASEREG,ZERO            ESTABLISH ADDRESSABILITY
         USING *,BASEREG               ASSIGN BASE REG
         ST    SAVEREG,SAVEAREA+4      SAVE CALLER'S SVAREA ADDR
         LA    R15,SAVEAREA            MY SAVE AREA ADDR
         ST    R15,EIGHT(ZERO,SAVEREG)  PUT IT IN CALLER'S SVAREA
         LR    SAVEREG,R15             KEEP MY SAVE AREA ADDR IN R13
         SPACE
         CLI   1(R1),2                    MACRO CALL LEVEL CHECK YM5400
         BE    GDLEVEL                    GOOD LEVEL PROCESSING  YM5400
         L     SAVEREG,FOUR(ZERO,SAVEREG) GET POINTER TO CALLERS YM5400
*                                           SAVE AREA            YM5400
         LM    R2,R12,28(SAVEREG)       RESTORE REGS 2-12        YM5400
         RETURN (14),T,RC=4                                      YM5400
         SPACE 4
GDLEVEL  EQU   *                                                 YM5400
         TIME
         SPACE
         ST    R1,SAVE                     SAVE DATE FROM MACRO
         MVO   WORK+SIX(TWO),SAVE+ONE(ONE) GET YEAR
         OI    WORK+SEVEN,HEX0F            CREATE A + SIGN
         UNPK  DATE+SIX(TWO),WORK+SIX(TWO) UNPACK YEAR (PRINTABLE)
         CVB   YEAR,WORK                   GET YEAR IN BINARY
         SR    REMAIN,REMAIN               CLEAR REG FOR DIVIDE
         LA    DIVISOR,FOUR            DIVIDE BY 4 TO CHK FOR LEAP
         DR    DIVIDEND,DIVISOR            DIVIDE YEAR
         LA    TABLBASE,NOT            ASSUME NOT-LEAP TABL ADR-2
         LTR   REMAIN,REMAIN           LEAP YEAR?
         BNZ   DATE0600                NO, ASSUMPTION CORRECT
         LA    TABLBASE,LEAP           YES, USE LEAP-YEAR TBL ADR-2
DATE0600 EQU   *
         MVC   WORK+SIX(TWO),SAVE+TWO  GET DAY
         CVB   DAYS,WORK               CONVERT TO BINARY
         LA    INDEX,TBLENGTH          SET INDEX REG TO TABLE LNGTH
DATE0700 EQU   *
         LH    ENTRY,ZERO(INDEX,TABLBASE) GET ENTRY FROM TABLE
         CR    DAYS,ENTRY              IS THIS THE CORRECT ENTRY?
         BNH   DATE0900                NO, DECR INDEX & KEEP SEARCHING
         SR    DAYS,ENTRY              YES, GET DAY IN THIS MONTH
         CVD   DAYS,WORK               CONVERT  DAY TO PACKED DEC
         UNPK  DATE+THREE(TWO),WORK+SIX(TWO) UNPACK DAY
         OI    DATE+FOUR,HEXF0         MAKE DAY PRINTABLE
         SPACE
         SRL   INDEX,ONE               GET MONTH FROM INDEX REG
         CVD   MONTH,WORK              CONVERT MONTH TO PACKED DEC
         UNPK  DATE(TWO),WORK+SIX(TWO) UNPACK MONTH
         OI    DATE+ONE,HEXF0          MAKE IT PRINTABLE
         L     R0,DATE                 PUT DATE - MM/D IN R0
         L     R1,DATE+4               PUT DATE - D/YY IN R1
         L     SAVEREG,FOUR(ZERO,SAVEREG) GET POINTER TO CALLERS SVAREA
         LM    R2,R12,28(SAVEREG)       RESTORE REGS 2-12
         RETURN (14),T,RC=0
         SPACE 2
DATE0900 EQU   *
         LA    R8,TWO                  DECREMENT VALUE
         SR    INDEX,R8                POINT TO NEXT ENTRY DOWN
         B     DATE0700                GO CHECK OUT THIS ENTRY
R0       EQU   0                       REGISTER 0
R1       EQU   1                       REGISTER 1
R2       EQU   2                        REGISTER 2
REMAIN   EQU   4                       REGISTER 4
DAYS     EQU   4                       REGISTER 4
DIVIDEND EQU   4                        REGISTER 4
YEAR     EQU   5                       REGISTER 5
TABLBASE EQU   5                       REGISTER 5
DIVISOR  EQU   6                       REGISTER 6
MONTH    EQU   6                       REGISTER 6
INDEX    EQU   6                       REGISTER 6
ENTRY    EQU   7                       REGISTER 7
R8       EQU   8                       REGISTER 8
BASEREG  EQU   11                      BASE REGISTER
R12      EQU   12                       REGISTER 12
SAVEREG  EQU   13
R15      EQU   15                      REGISTER 15
ZERO     EQU   0                       ZERO
ONE      EQU   1                       ONE
TWO      EQU   2                       TWO
THREE    EQU   3                       THREE
FOUR     EQU   4                       FOUR
SIX      EQU   6                       SIX
SEVEN    EQU   7                       SEVEN
EIGHT    EQU   8                       EIGHT
HEXF0    EQU   X'F0'                   TO MAKE LAST CHAR PRINTABLE
HEX0F    EQU   X'0F'                   SET + SIGN BIT
WORK     DC    D'0'
SAVE     DC    F'0'
DATE     DC    C'00/00/00'
SAVEAREA DC    18F'0'
LEAP     EQU   *-2                      ADDR OF LEAP TBL-2 FOR BASE
LEAPTABL DC    AL2(0,31,60,91,121,152,182,213,244,274,305,335)
NOT      EQU   *-2                      ADDR OF NOT LEAP TBL-2 (BASE)
LENGTH   EQU   *                        ADDR AT BEG OF TABLE
NONLPTBL DC    AL2(0,31,59,90,120,151,181,212,243,273,304,334)
TBLENGTH EQU   *-LENGTH                LENGTH OF TABLE
         LTORG
         END
