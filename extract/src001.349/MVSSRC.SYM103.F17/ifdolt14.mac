 TITLE  'IFDOLT14  SCAN, HEADER AND DESCRIPTION LINE SECTIONS'
****************************** PROLOGUE *******************************
*                                                                     *
*  MODULE NAME: IFDOLT14                                              *
*                                                                     *
*  DESCRIPTIVE NAME: SCAN, HEADER AND DESCRIPTION LINES SECTION       *
*                                                                     *
*  COPYRIGHT: NONE                                                    *
*                                                                     *
*  STATUS: CHANGE LEVEL - 001                                         *
*                                                                     *
*  FUNCTION: THIS MODULE IS DESIGNED AS FOLLOWS:                      *
*                                                                     *
*            SCAN SECTION                                             *
*            1) DPRINT OPTIONS CHECKED; ALSO FIRST ERROR AND          *
*               CATASTROPHIC ERROR CONDITIONS.                        *
*            2) FORMS CONTROL SETUP                                   *
*            3) SCAN DPRINT CONTROL WORD AND CALL DPRINT MODULES      *
*               REQUIRED FOR UNIT TEST ERROR PRINTOUT.                *
*            4) PRINT FIRST ERROR OR CATASTROPHIC ERROR MESSAGE       *
*                                                                     *
*            HEADER, DESCRIPTION SECTION                              *
*            1) SETUP HEADER LINE FOR PRINT                           *
*            2) SETUP DESCRIPTION LINES FOR PRINT                     *
*                                                                     *
*  ENTRY POINT: IFDOLT14                                              *
*                                                                     *
*  INPUTS: DPRINT PARAMETER LISTS                                     *
*                                                                     *
*  REGISTER USAGE: R1  - ADDRESS OF DPRINT PARAMETERS                 *
*                  R2  - ADDRESS OF SECTION CTRL TABLE IN OLTEP ROOT  *
*                  R3  - BASE REG FOR UNIT TEST SECTION PREFACE       *
*                  R4  - BASE REG FOR THIS MODULE                     *
*                  R5-R11 - MISC.                                     *
*                  R12 - ADDRESS OF DPRINT PARAMETER CTRL WORD        *
*                  R13 - ADDRESS OF CALLERS SAVE AREA; CONVERT ADDR   *
*                  R14 - ADDRESS OF CALLERS RETURN PT; CONVERT ADDR   *
*                  R15 - LINK ADDR; CONVERT COUNT; RETURN CODE        *
*                                                                     *
*  EXTERNAL ROUTINES: IFDOLT39- PARALLEL PRINT OUTPUT WRITER          *
*                                                                     *
*  EXITS - NORMAL: RETURN TO ENTRY-EXIT SECTION OF IFDOLT39           *
*           ERROR: NONE                                               *
*                                                                     *
*  ATTRIBUTES: SERIALLY REUSABLE                                      *
*                                                                     *
* APAR OX01058 SAVE RTN CODES                                  0X01058*
***********************************************************************
*  CHANGE 03/71 - HANDLING OF EP/NEP,FE/NFE,ETC PARAMETERS            *
*               - CHAIN=YES SUPPORT                                   *
*               - SPACE BETWEEN OLT DPRINTS                           *
*               - SYMBOLIC NAME IN HEADER                             *
*                                                                     *
*  CHANGE ACTIVITY: AS FOLLOWS                                        *
*                                                                     *
*    CLEANUP:                                                   Y02906*
*      C170000,A170600,D828000                                        *
*    PTM #05407:                                                YM5407*
*      A936460                                                        *
***********************************************************************
         EJECT
IFDOLT14 CSECT
*A646000                                                         A52641
*        TO FIX ABEND DUE TO FE OPTION,ERROR DPRINT W/O HEADER        *
         SPACE
         USING IFDOLT14,R4         HEADER MODULE BASE REG
         USING CHASCT,R2
         USING RTN#,R3             BASE REG FOR SECTION PREFACE
         SPACE
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
WTOFLG   EQU   X'01'
RSTRFLG  EQU   X'80'
WRTFLG   EQU   X'10'
NFEFLG   EQU   X'08'
DPRFLG   EQU   X'40'
HDRFLG   EQU   X'80'
FERINT   EQU   X'02'
NDRFLG   EQU   X'04'
FFEFLG   EQU   X'20'
FCEFLG   EQU  X'10'
PFEMSG   EQU  X'08'
PCEMSG   EQU  X'04'
NPROPT   EQU   X'04'
NEPOPT   EQU   X'04'
SYMNAME  EQU   X'08'
SYMBFLG  EQU   X'02'
         EJECT
***********************************************************************
*        SCAN DPRINT PARAMETERS                                       *
***********************************************************************
         SPACE
DPR000   SAVE (14,12),,IFDOLT14&SYSDATE
*        ESTABLISH DATE AND SAVE REGS
         SPACE  3
         LR    R4,R15              LOAD BASE REG
         L     R3,SECPTR           BASE REG FOR UNIT TEST SECT PREF.
         AH    R3,10(R3)           ADD DISPLACEMENT FOR RTN# ADDR
         LA    R5,SHDSA            ADDRESS OF MODULE REG SAVE AREA
         ST    R5,8(R13)           STORE IT IN CALLER'S SA
         ST    R13,4(R5)           STORE ADDR OF CALLER'S SA
         L     R1,4(R1)            DPRINT PARAMETER ADDRESS       M4506
         ST    R1,SAVER1           SAVE IT
         SPACE
         XC    DPRTRNCD(4),DPRTRNCD  CLEAR RETURN CODE WORD
         CLI   1(R1),X'03'         MACRO LEVEL OK?
         BNH   DPRSCAN             YES.
         MVI   DPRTRNCD+3,X'04'    NO.  SET RETURN CODE TO 04
         B     DPR111A             RETURN VIA COMM INTERVAL RTN
         SPACE
DPRSCAN  L     R12,4(R1)           DPRINT CONTROL WORD ADDR
         MVI   PRTBUFR+1,C' '      FILL PRINT BUFFER WITH BLANKS
         MVC   PRTBUFR+2(120),PRTBUFR+1
         SPACE
* CHECK DPRINT OPTIONS; FIRST ERROR, CATASTROPHIC ERROR CONDITIONS.
         SPACE
DPR100   TM    0(R12),X'02'        ERROR DPRINT REQUESTED
         BZ    DPR100A             NO.  BRANCH
         OI    CHASCT+3,X'C0'      YES. SET TEST ERR, ASTERIK BITS
DPR100A  TM    CHASCT+3,X'03'      ERROR LOOP OR TEST LOOP REQUESTED
         BZ    DPR100B             NO.  BR IF BOTH OFF
         TM    CHASCT+2,NPROPT     YES. PRINT OPTION ON
         BZ    DPR113              NO.  BYPASS PRINT
         SPACE
DPR100B  TM    0(R12),X'02'        ERROR DPRINT REQUESTED
         BZ    DPR101              NO.  CONTINUE
         TM    CHASCT+3,X'20'      YES. CATASTROPHIC ERROR BIT ON
         BZ    DPR100C             NO.  BRANCH
         TM    CESWT5,FCEFLG       YES. FIRST CAT ERROR FLAG ON
         BNZ   DPR100C             YES. BRANCH
         OI    CESWT5,FCEFLG       NO.  SET IT ON
         OI    CESWT5,PCEMSG       TURN ON PRINT CAT ERR MSG FLAG
         SPACE
DPR100C  TM    CHASCT+2,X'01'      FE COMM OPTION ON
         BNZ   DPR100D             YES.  BRANCH
         TM    CHASCT+3,NEPOPT     NO.  ERROR PRINT OPTION ON
         BZ    DPR101              YES. CONTINUE
         B     DPR100E             BRANCH
DPR100D  TM    CESWT5,FFEFLG       FIRST FE FLAG
         BNZ   DPR100E             YES. BRANCH
         OI    CESWT5,FFEFLG       NO.  SET IT ON
         OI    CESWT5,PFEMSG       TURN ON PRINT FE MSG FLAG
DPR100E  TM    CHASCT+3,NEPOPT     ERROR PRINT OPTION ON
         BZ    DPR101              YES. BRANCH
         TM    CESWT5,PFEMSG+PCEMSG  NO. PRINT FE OR CAT ERR MSG.
         BZ    DPR113
         SPACE
* FORMS CONTROL SETUP
         SPACE
DPR101   SR    R8,R8               CLEAR R8
         IC    R8,0(R12)           GET FORMS CONTROL BITS
         SRL   R8,5                SHIFT BITS
         XC    FORMS(2),FORMS      CLEAR
         STC   R8,FORMS            STORE FORMS CONTROL
         CLI   FORMS,X'05'         RESTORE PAGE ?
         BE    DPR110C             YES.
         MVI   PRTBUFR,X'09'       NO. LOAD CTRL CHARC-SPACE 1 LINE
         SPACE
* CHECK DPRINT CONTROL WORD FOR ACTIVE PARAMETERS
         SPACE
DPR106   TM    0(R12),X'10'        PRINT HEADER LINE
         BZ    DPR107              NO.
         BAL   R14,DPRHDR          YES. HEADER SECTION
         B     DPR108              BRANCH TO CHECK RSLTS PRINTOUT
         SPACE
* TEST FOR DESCRIPTION LINES PRINT
         SPACE
DPR107   TM    2(R12),X'80'        PRINT DESCRIPTION LINES
         BZ    DPR108              NO.
         BAL   R14,DPR030          YES. DESCRIPTION SECTION
         SPACE
* TEST FOR RESULTS PRINT
         SPACE
DPR108   LA    R13,SHDSA           SAVE AREA ADDRESS              M4506
         TM    2(R12),X'7F'        PRINT RESULTS                  M4506
         BZ    DPR109              NO PRINT                       M4506
         B     DPR109A             PRINT                          M4506
DPR109   TM    3(R12),X'EC'        PRINT DATA, CC                 M4506
         BZ    DPR110              NO PRINT                       M4506
DPR109A  BAL   R9,DPRCL15          CALL MODULE ROUTINE            M4506
         CLI   DPRTRNCD+3,X'00'    ANY RETURN CODE ALREADY SET?
         BNE   DPR109B             YES.  BYPASS STORE
         STC   R15,DPRTRNCD+3      NO.  STORE IT
DPR109B  OI    CESWT3,X'08'        SUSPEND DELETION               M4506
         BAL   R9,DPRCL15          CALL MODULE ROUTINE            M4506
         B     DPR110                                             M4506
DPRCL15  IFDMOD CALL='15',LIST=SAVER1                             M4506
         BCR   15,R9               RETURN                         M4506
DPRCL17  IFDMOD CALL='17',LIST=SAVER1                             M4506
         BCR   15,R9               RETURN                       0X01058
         SPACE
* TEST FOR MESSAGE LINES PRINT
         SPACE
DPR110   TM    1(R12),X'01'        PRINT MESSAGES
         BZ    DPR110A             NO.
         LA    R13,SHDSA           SAVE AREA ADDRESS
         BAL   R9,DPRCL17          CALL MODULE ROUTINE            M4506
         LTR   R15,R15      IS RET ZERO?                        0X01058
         BZ    DPRCL17X   IF ZERO DONT SAVE                     0X01058
         ST    R15,DPRTRNCD        SAVE RETURN CODE              XM0796
DPRCL17X OI    CESWT3,X'08'        SUSPEND DELETION               M4506
         BAL   R9,DPRCL17          CALL MODULE ROUTINE            M4506
         SPACE
* FORMS CONTROL- SPACE 2, 3 LINES, OR RESTORE PAGE.
         SPACE
DPR110A  CLI   FORMS,X'04'         PRINT RESTORE ?
         BE    DPR110C             YES.
         CLI   FORMS,X'02'         NO. SPACE 2 LINES ?
         BNE   DPR110B             NO.
         MVI   PRTBUFR,X'09'       YES. LOAD CTRL CHARC FOR 1 LINE
         B     DPR110D
DPR110B  CLI   FORMS,X'03'         SPACE 3 LINES ?
         BNE   DPR111              NO.
         MVI   PRTBUFR,X'11'       YES. LOAD CTRL CHARC FOR 2 LINES
         B     DPR110D
DPR110C  MVI   PRTBUFR,X'8B'       RESTORE PAGR CTRL CHARC
         OI    CESWT4,RSTRFLG
DPR110D  LA    R13,SHDSA           SAVE AREA ADDRESS
         PWRT  SIZE=70             RESTORE PAGE
         MVI   PRTBUFR,X'09'       DEFAULT TO SPACE 1 LINE
         CLI   FORMS,X'05'         RESTORE PAGE OP ?
         BE    DPR113              YES. TO EXIT ROUTINE
         SPACE 2
***********************************************************************
*        FIRST ERROR, CATASTROPHIC ERROR MESSAGES OUTPUT              *
***********************************************************************
         SPACE
DPR111   TM    0(R12),X'08'        DPRINT CHAINING REQUESTED
         BO    DPR113              YES.
         TM    0(R12),X'02'        NO.  IS IT AN ERROR DPRINT     M5076
         BZ    DPR113              BRANCH IF NO                   M5076
         TM    CESWT5,PFEMSG+PCEMSG  PRINT FIRST ERR OR CAT ERR MSG?   X
                                                                  21051
         BZ    DPR113              NO.                            21051
         TM    0(R12),X'10'        YES. HAS HEADER BEEN PRINTED?  21051
         BO    DPR111A             YES.                           21051
         BAL   R9,DPR010           NO. USE HDR RTN TO GET DEVADR, ETC. X
                                                                  21051
DPR111A  TM    CESWT5,PFEMSG       NO.  PRINT FIRST ERROR MSG
         BO    DPR117              YES.  GO PRINT IT
DPR112   TM    CESWT5,PCEMSG       NO.  PRINT FIRST CAT ERROR MSG
         BO    DPR118              YES.  GO PRINT IT.
         SPACE
* RETURN TO ENTRY-EXIT ROOT SECTION
         SPACE
DPR113   L     R13,SHDSA+4         ADDRESS OF CALLER'S SA
         MVC   16(4,R13),DPRTRNCD  RETURN CODE IN R15 SAVE AREA
         LM    R14,R12,12(R13)     RESTORE REGS
         BR    R14                 RETURN TO ENTRY-EXIT DPRINT ROOT
         SPACE
* INITIATE FIRST ERROR COMMUNICATION MESSAGE
         SPACE
DPR117   LA    R1,IFDMSG14         MESSAGE TABLE ADDRESS
         L     R1,0*4(R1)          GET MESSAGE ADDRESS
         L     R7,SECPTR           SECTION PREFACE POINTER
         MVC   38(8,R1),0(R7)      UNIT TEST ID
         SPACE
         MVC   47(3,R1),RTNNUM+1   ROUTINE NUMBER INTO MSG
         MVC   56(4,R1),DEVADR     DEVICE ADDR INTO MSG- EBCDIC
         MVC   61(8,R1),SYMBNM     SYMBOLIC NAME  OR BLANKS
         SPACE
         LA    R13,SHDSA           SAVE AREA ADDRESS
         PWTO  REG=(1)             WRITE TO OPERATOR MESSAGE
         B     DPR112              RETURN HERE
         SPACE
* INITIATE CATASTROPHIC ERROR MESSAGE TO OPERATOR
         SPACE
DPR118   LA    R1,IFDMSG14         MESSAGE TABLE ADDRESS
         L     R1,1*4(R1)          GET MESSAGE ADDRESS
         MVC   41(4,R1),DEVADR     DEVICE ADDRESS - EBCDIC
         MVC   46(8,R1),SYMBNM     SYMBOLIC NAME OR BLANKS
         SPACE
         LA    R13,SHDSA           SAVE AREA ADDRESS
         PWTO  REG=(1)             WRITE TO OPERATOR MESSAGE
         B     DPR113              RETURN HERE
         SPACE
         CNOP  0,8
SHDSA    DS    18F                 REGISTER SAVE AREA
         CNOP  0,4
SAVER1   DC    XL4'0'
RTNNUM   DS    CL4' '              ROUTINE NUMBER- EBCDIC
DEVADR   DS    CL4' '              DEVICE ADDRESS- EBCDIC
SYMBNM   DS    CL8' '              DEVICE SYMBOLIC NAME
DPRTRNCD DC    XL4'0'              RETURN CODE
FORMS    DC    XL2'0'              FORMS CONTROL
         SPACE 2
***********************************************************************
*        HEADER                                                       *
***********************************************************************
         SPACE
DPRHDR   ST    R14,SAVE14          SAVE LINK ADDRESS
         L     R12,4(R1)           ADDRESS OF DPRINT CONTROL WORD
         TM    0(R12),X'10'        PRINT HEADER ?
         BZ    DPR030+4            NO
         BAL   R10,DPRWRT          YES. PRINT BLANK LINE FOR SPACE
         TM    0(R12),X'02'        IS IT AN ERROR HEADER
         BZ    DPR010              NO
         MVI   PB,C'*'             YES.  PREFIX WITH '*'
         OI    3(R2),X'C0'         SET ERROR MSG BITS IN SCT
         SPACE
* GET TEST SECTION ID, ROUTINE NO. FROM SECTION PREFACE.
         SPACE
DPR010   L     R7,SECPTR           POINTER TO SECTION PREFACE
         MVC   PB+1(8),0(R7)       UNIT TEST ID FROM SECT PREF
         MVI   PB+9,X'60'          PUT DASH IN BUFFER
         MVC   PB+10(2),8(R7)      ADD LEVEL TO IT
         MVC   PB+14(3),=C'RTN'    PUT 'RTN' IN PRINT BUFFER
         SR    R7,R7              CLEAR R7.
         IC    R7,RTN#            GET ROUTINE NUMBER.
         CVD   R7,WRK             CONVERT TO DECIMAL.
         SPACE
         LA    R13,WRK+6          GET ADDR OF RTN NO.
         LA    R14,WRK            GET OUTPUT ADDR.
         LA    R15,2              GET COUNT
         BAL   R10,DPRCNV         CONVERT RTN NO. TO EBCDIC.
         MVC   PB+18(3),WRK        PUT RTN NUMBER IN PRINT BUFFER
         MVC   RTNNUM+1(3),WRK     SAVE FOR FIRST ERR MSG
         SPACE
* GET DEVICE ADDRESS FROM EITHER DPRINT PARAM TABLE, TECB, OR SCT.
         SPACE
         MVC   PB+24(11),=C'DEV/LN NONE'  PUT IN PRINT BUFFER
         TM    CESWT2,NDRFLG       IS A DEVICE REQUESTED
         BZ    DPR011              YES
         B     DPR016              BRANCH TO BYPASS DEV ADDRESS LOAD
DPR011   MVC   DPRDEVAD+2(2),9(R12)  GET DEVICE ADDRESS PARAMETER
         CLC   DPRDEVAD+2(2),=X'0000'  IS IT ZERO
         BNE   DPR015              BRANCH IF NO
         L     R1,SAVER1               GET PARM LIST ADDR       A52641
         SPACE
         L     R13,12(R1)         GET TECB ADDRESS.
         LTR   R13,R13            IS A TECB ADDRESS SUPPLIED ?
         BZ    DPR012              NO. GET ADDRESS FROM SCT
         CLI   7(R13),X'00'        YES. IS THERE A POSTED EVENT FIELD
         BE    DPR012              NO.
         MVC   DPRDEVAD+1(3),9(R13)     GET DEV ADDR FROM TECB
         B     DPR015
DPR012   MVC   DPRDEVAD(4),CHASCT+12  GET PRIME DEV ADDR FROM SCT
         SPACE
DPR015   LA    R13,DPRDEVAD+2      INPUT ADDRESS FOR CONVERT
         LA    R14,PB+31           OUTPUT ADDRESS FOR CONVERT
         LA    R15,2              GET COUNT.
         BAL   R10,DPRCNV         CONVERT DEV ADDR TO EBCDIC.
         CLC   DPRDEVAD(4),CHASCT+12   SAME DEV AS IN SCT?
         BNE   DPR016              NO. BYPASS SYMBOLIC NAME CHECK
         TM    CHASCT+11,SYMNAME   YES.  DEV SYMBOLIC NAME GIVEN?
         BZ    DPR016              NO.
         MVC   PB+36(8),CHASCT+24  YES. PUT IT IN PRINT BUFFER
         MVC   SYMBNM(8),CHASCT+24 SAVE FOR FIRST ERR, CAT ERR MSGS
         OI    CESWT5,SYMBFLG      SYMBOLIC NAME FLAG
         B     DPR016A
DPR016   MVI   SYMBNM,C' '         CLEAR WITH BLANKS
         MVC   SYMBNM+1(7),SYMBNM
DPR016A  MVC   DEVADR(4),PB+31     SAVE DEV ADDR FOR FE, CAT ERR MSGS
         TM    0(R12),X'10'        REGULAR PRINT HEADER ENTRY?    21051
         BO    DPR020              YES.                           21051
         BR    R9                  NO.  RETURN TO FIRST ERROR MSG RTN. X
                                                                  21051
         SPACE
* GET REFERENCE NO., ECA NO. FROM SECTION PREFACE.
         SPACE
DPR020   LA    R6,PB+3             GET PRINT BUFR ADDR FOR ADJUSTMENT
         TM    CESWT5,SYMBFLG      IS SYMBOLIC NAME TO BE PRINTED
         BZ    DPR020A             NO
         LA    R6,8(R6)            YES.  MOVE OVER ECA
DPR020A  MVC   35(3,R6),=C'ECA'    PUT 'ECA' IN PRINT BUFFER
         MVC   39(3,R6),ECA#       ALSO ECA NUMBER
         SPACE
DPR023   TM    0(R12),X'01'        IS REFNUM TO BE PRINTED
         BZ    DPR025              BRANCH IF NO
         SPACE
         MVC   44(6,R6),=C'REFNUM'  PUT IN PRINT BUFFER
         MVC   WRK(1),REFNUM      MOVE 1ST 2 DIGITS TO WORK AREA.
         MVC   WRK+1(2),7(R12)     MOVE LAST 3 DIGITS TO WORK AREA
         LA    R13,WRK            GET INPUT ADDR FOR CONVERT.
         LA    R14,WRK+8          GET OUTPUT ADDR FOR CONVERT.
         LA    R15,3              GET COUNT.
         BAL   R10,DPRCNV         CONVERT REFNUM TO EBCDIC.
         MVC   51(2,R6),WRK+8      PUT FIRST 2 DIGITD IN PRINT BUFFER
         MVC   53(3,R6),WRK+11     PUT NEXT 3 DIGITS IN PRINT BUFFER
         SPACE
DPR025   OI    CESWT5,HDRFLG       SET HEADER FLAG
         BAL   R10,DPRWRT          PRINT HEADER LINE
         NI    CESWT5,255-HDRFLG    RESET THIS FLAG
         NI    CESWT5,255-HDRFLG-SYMBFLG  RESET THESE FLAGS
         B     DPR030+4            PRINT DESCRIPTION LINES
         SPACE 2
***********************************************************************
*        DESCRIPTION                                                  *
***********************************************************************
         SPACE
DPR030   ST    R14,SAVE14          SAVE LINK ADDR FOR RETURN
         TM    2(R12),X'80'        PRINT DESCRIPTION LINES?
         BZ    DPR041              NO.  EXIT ROUTINE
         SR    R7,R7               YES.  CLEAR R7
         IC    R7,16(R12)          GET NUMBER OF DESC LINES
         LTR   R7,R7               NO. OF LINES = 0 ?
         BZ    DPR042              YES.  EXIT RTN                 M5076
         LA    R11,60(R1)          POINTER TO DESC LINE ADDRESS
         LA    R8,23(R12)          ADDRESS OF BYTE COUNT
         SPACE
DPR035   SR    R6,R6              CLEAR R6.
         IC    R6,0(R8)           GET COUNT.
         LTR   R6,R6              COUNT = 0 ?
         BZ    DPR040             BRANCH YES.
         LH    R5,MAXCNT          GET MAX NO. BYTES ALLOWED.
         CR    R6,R5              COUNT GREATER THAN MAX ?
         BNH   DPR037             BRANCH IF NO.
         LR    R6,R5               SET COUNT TO MAX
         SPACE
DPR037   L     R5,0(R11)           GET DESC LINE ADDR
         BCTR  R6,0               REDUCE COUNT FOR EXECUTE OF MVC.
         EX    R6,DPR605          MOVE DESC LINE TO PRINT BUFFER.
         BAL   R10,DPRWRT          GO PRINT DESCRIPTION LINE
         SPACE
DPR040   LA    R11,4(R11)         GET ADDR OF DESC LINE 2 ADDR.
         LA    R8,1(R8)            GET NEXT COUNT ADDRESS         M1010
         BCT   R7,DPR035          PROCESS LINE 2.
         SPACE
DPR041   L     R14,SAVE14          RESTORE LINK ADDRESS
         BR    R14                 RETURN TO SCAN SECTION
DPR042   MVI   DPRTRNCD+3,X'0C'    SET RETURN CODE                M5076
         B     DPR041                                             M5076
         SPACE
DPR605   MVC   PB(0),0(R5)         MOVE TO PRINT BUFFER
         EJECT
***********************************************************************
*    PRINT HEADER, DESCRIPTION LINE VIA PARALLEL PRINT OUTPUT WRITER  *
***********************************************************************
         SPACE
DPRWRT   LA    R13,SHDSA           SAVE AREA ADDRESS
         PWRT  SIZE=120                                         SA46329
         MVI   PB,C' '             CLEAR PRINT BUFFER WITH BLANKS
         MVC   PB+1(120),PB
         L     R1,SAVER1           RESTORE DPRINT PARAMETER PTR
         L     R12,4(R1)           ADDRESS OF DPRINT CTRL WORD
         BR    R10                 RETURN TO CALLING ROUTINE
         SPACE
WRK      DS    16D
DPRDEVAD DC    XL4'0'              DEVICE ADDRESS - HEX
SAVE14   DC    XL4'0'
MAXCNT   DC    H'119'              MAX PRINT CHARACTERS           M1010
         SPACE
***********************************************************************
*              DPRINT MESSAGE SECTION                                 *
***********************************************************************
         SPACE 1
IFDMSG14 DC    A(IFD129)
         DC    A(IFD157)
         SPACE
IFD129   WTO   'IFD129I FIRST ERROR COMMUNICATION XXXXXXXX YYY UNIT ZZZX
               Z SSSSSSSS',MF=L,ROUTCDE=10,DESC=7
IFD157   WTO   'IFD157I CATASTROPHIC ERROR ON DEVICE XXXX SSSSSSSS',MF=X
               L,ROUTCDE=10,DESC=7
         EJECT
***********************************************************************
*        DPRCNV - HEX TO EBCDIC CONVERT                               *
*                                                                     *
*              CALL SEQ -  R13 = INPUT ADDRESS                        *
*                          R14 = OUTPUT ADDRESS                       *
*                          R15 = INPUT BYTE COUNT                     *
*                                                                     *
*                          BAL R10,DPRCNV                             *
***********************************************************************
         SPACE
DPRCNV   STM   R5,R6,SAVREGS2      SAVE REGS 5, 6
         STM   R13,R15,SAVREGS3    SAVE REGS 13-15
         SPACE
         LTR   R15,R15            COUNT = 0 ?
         BZ    DPRCNV8            BRANCH IF YES.
         SPACE
         AR    R14,R15         ADD COUNT TO OUTPUT ADDR.
         AR    R14,R15            ADD COUNT AGAIN.
         AR    R13,R15            ADD COUNT TO INPUT ADDR.
         SR    R5,R5              CLEAR R5.
         SR    R6,R6              CLEAR R6.
         SPACE
DPRCNV4  S     R14,=F'2'          GET ADDR OF LAST BYTE OF OUTPUT.
         BCTR  R13,0              GET ADDR OF LAST BYTE OF INPUT.
         IC    R6,0(R13)          GET HEX BYTE.
         LR    R5,R6              PUT IN R5.
         SRL   R6,4               GET BITS 0-3 IN R6.
         N     R5,=F'15'          GET BITS 4-7 IN R5.
         IC    R6,DPREBC(R6)      CONVERT BITS 0-3 TO EBCDIC CHAR.
         IC    R5,DPREBC(R5)      CONVERT BITS 4-7 TO EBCDIC CHAR.
         STC   R6,0(R14)          STORE 1ST EBCDIC CHAR IN OUTPUT.
         STC   R5,1(R14)          STORE 2ND EBCDIC CHAR IN OUTPUT.
         BCT   R15,DPRCNV4        GET NEXT HEX BYTE.
DPRCNV8  LM    R5,R6,SAVREGS2      RESTORE REGS 5, 6
         LM    R13,R15,SAVREGS3    RESTORE REGS 13-15
         BR    R10                RETURN TO CALLER.
         SPACE
DPREBC   DC    C'0123456789ABCDEF' HEX TO EBCDIC CONVERT TABLE.
         CNOP  0,4
SAVREGS2 DC    2F'00'
SAVREGS3 DC    3F'00'
SECPREF  DSECT                         SECTION PREFACE
RTN#     DS    CL1
         DS    CL16
REFNUM   DS    CL1
         DS    CL14
ECA#     DS    CL3
IFDOLT14 CSECT                                                   YM5407
DPRCOM   IFDCOM
PB       EQU   PRTBUFR+1
         SPACE
         SPACE
         END
