 TITLE  'IFDOLT15  RESULTS AND DATA SECTIONS'
*
*
****************************** PROLOGUE *******************************
*                                                                     *
*  MODULE NAME: IFDOLT15                                              *
*                                                                     *
*  DESCRIPTIVE NAME: DPRINT RESULTS AND DATA                          *
*                                                                     *
*  COPYRIGHT: NONE                                                    *
*                                                                     *
*  STATUS: CHANGE LEVEL - 000                                         *
*                                                                     *
*  FUNCTION: THIS MODULE IS DESIGNED AS FOLLOWS:                      *
*                                                                     *
*            1) TEST DPRINT CONTROL FIELD DEFINITION BITS TO          *
*               DETERMINE IF THE FOLLOWING  ARE TO BE PRINTED:        *
*                 CAW, CCW'S, ICW                                     *
*                 EXPECTED, RECEIVED CONDITION CODE                   *
*                 EXPECTED, RECEIVED CSW                              *
*                 EXPECTED, RECEIVED SENSE DATA                       *
*                 EXPECTED, RECEIVED, WRITTEN DATA                    *
*            2) GET THE ADDRESS AND BYTE COUNT FOR THE ITEM.          *
*               IF EITHER IS NOT GIVEN, SET THE RETURN CODE TO 0C.    *
*            3) GET, CONVERT TO EBCDIC, AND LOAD A RESULT OR DATA     *
*               LINE IN THE PRINT BUFFER.                             *
*            4) IDENTIFY AN ERROR LINE WITH AN ASTERISK.              *
*            5) PRINT THE RESULT OR DATA LINE WITH A COMMON           *
*               PWRT INNER MACRO ROUTINE.                             *
*            6) DO THE ABOVE UNTIL ALL LINES HAVE BEEN PRINTED.       *
*                                                                     *
*  ENTRY POINT: IFDOLT15 - FROM THE SCAN SECTION OF IFDOLT14.         *
*                                                                     *
*  INPUT: DPRINT PARAMETER LIST IN TEST SECTION.                      *
*                                                                     *
*  REGISTER USAGE: R1  - ADDRESS OF DPRINT PARAMETER LIST.            *
*                  R2  - ADDRESS OF SECT CTRL TABLE IN OLTEP ROOT.    *
*                  R3  - ADDRESS OF TECB MAP.                         *
*                  R4  - BASE REG FOR THIS MODULE.                    *
*                  R5-R11 - MISCELLANEOUS                             *
*                  R12 - ADDRESS OF DPRINT CONTROL FIELD WORD.        *
*                  R13 - ADDR OF CALLER'S SAVE AREA; ALSO CONVERT RTN *
*                  R14 - ADDR OF CALLER'S RETURN PT; ALSO CONVERT RTN *
*                  R15 - LINK ADDR; CONVERT COUNT; RETURN CODE.       *
*                                                                     *
*  EXTERNAL ROUTINES: IFDOLT39 -PARALLEL PRINT OUTPUT WRITER.         *
*                                                                     *
*  EXITS - NORMAL: RETURN TO SCAN SECTION OF IFDOLT14.                *
*        -  ERROR: NONE                                               *
*                                                                     *
*  ATTRIBUTES: SERIALLY REUSABLE                                      *
*                                                                     *
*  CHANGE ACTIVITY: CLEANUP - C072000,A072300,D696000           Y02906*
*                   PTM #03338 - C313000,D313070-313140,D313600,Y03338*
*                                A313620                        Y03338*
*                   PTM #05407 - A709500                        Y05407*
*                                                                     *
***********************************************************************
         EJECT
         SPACE 2
IFDOLT15 CSECT
*  IMPLEMENT EMPTY TECB FUNCTION                                 Y01906
*D282000                                                         A42315
*A147300,A147600                                                SA53321
         USING IFDOLT15,R4         BASE REG FOR MODULE
         USING CHASCT,R2           BASE REG FOR SECTION CTRL TABLE
         USING TECBMAP,R3
         SPACE
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RSLTFLG  EQU   X'40'
         SPACE
DPRRSLTS SAVE (14,12),,IFDOLR15&SYSDATE
*        ESTABLISH DATE AND SAVE REGS
         SPACE  3
         LR    R4,R15              LOAD BASE REG
         LA    R5,RSLTSA           ADDRESS OF RESULTS SAVE AREA
         ST    R5,8(R13)           STORE IN CALLER'S SA
         ST    R13,4(R5)           STORE ADDRESS OF CALLER'S SA
         L     R5,4(R1)            ADR PTR SAVE AREA              M4506
         L     R1,0(R5)            GET DPRINT PARAMETER PTR
         L     R12,4(R1)           PTR TO DPRINT CTRL FIELD WORD
         L     R3,12(R1)           TECB ADDRESS
         ST    R1,SAVER1           SAVE DPRINT PARAMETER PTR
         XC    RETCODE(4),RETCODE  CLEAR IT
         EJECT
***********************************************************************
*              SET UP DEVICE ADDRESS                                  *
***********************************************************************
         SPACE
DPR001   MVC   DPRDEVAD+2(2),9(R12)  GET DEV ADDR FROM OLT
         CLC   DPRDEVAD+2(2),=X'0000'  IS IT 0?
         BNE   DPR050              BRANCH IF NO
         L     R9,12(R1)           PTR TO TECB
         LTR   R9,R9               IS TECB ADDR GIVEN ?
         BZ    DPR002              BR IF NO
         CLI   TCBEVOC,X'00'       IS THERE A POSTED EVENT FIELD
         BE    DPR002              BR IF NO
         MVC   DPRDEVAD+1(3),TCBFD01+1  PUT DEV ADDR IN BUFFER
         B     DPR050
DPR002   MVC   DPRDEVAD(4),12(R2)  GET PRIME DEV ADDR
         SPACE
***********************************************************************
*   CAW   CCW
***********************************************************************
         SPACE
DPR050   TM    2(R12),X'40'        PRINT CCW?
         BZ    DPR060              BR IF NO
         L     R7,20(R1)           GET CCW ADDRESS
         LTR   R7,R7               IS IT 0?
         BNZ   DPR052              BR IF NO
DPR050A  MVI   DPRTRNCD,X'0C'      SET RETURN CODE 0C
         B     DPR060              GO CHK FOR CC PRINT
         SPACE
DPR052   MVC   PB+32(3),=C'CAW'    PUT ' CAW' IN PRINT BUFFER
         LA    R13,20(R1)          GET CCW ADDRESS
         LA    R14,PB+37           GET OUTPUT ADDR FOR CONVERT
         LA    R15,4               GET COUNT
         BAL   R10,DPRCNV          CONVERT CCW ADDR TO EBCDIC
         SR    R8,R8               CLEAR
DPR052A  IC    R8,11(R12)          GET CCW COUNT
         LTR   R8,R8               IS IT 0?
         BZ    DPR050A             BR IF YES
         SR    R5,R5               CLEAR
         IC    R5,12(R12)          GET NO. OF FAILING CCW
         LA    R6,1                SET CCW NO.
DPR053   MVC   PB+1(3),=C'CCW'     PUT 'CCW' IN PRINT BUFFER
         TM    3(R12),X'10'        ICW ?
         BZ    DPR053A             BR IF NO
         MVI   PB+1,C'I'           PUT 'I' IN BUFFER
DPR053A  CVD   R6,WRK              CONVERT CCW NO. TO DECIMAL
         SPACE
         LA    R13,WRK+6           GET ADDR OF CCW NO.
         LA    R14,WRK             GET OUTPUT ADDR
         LA    R15,2               GET COUNT
         BAL   R10,DPRCNV          CONVERT CCW NO. TO EBCDIC
DPR054   MVC   PB+4(2),WRK+1       PUT CCW NO. IN PRINT BUFFER
         SPACE
DPR056   LR    R13,R7              GET CCW ADDR
         LA    R14,WRK             GET OUTPUT ADDR FOR CONVERT
         LA    R15,8               GET COUNT
         BAL   R10,DPRCNV          CONVERT CCW TO EBCDIC
         MVC   PB+7(2),WRK         MOVE CMND CODE
         MVC   PB+10(6),WRK+2         DATA ADDR
         MVC   PB+17(2),WRK+8           FLAGS
         MVC   PB+20(2),WRK+10             UNUSED FLD
         MVC   PB+23(4),WRK+12               BUTE COUNT TO BUFFER
         SPACE
         CR    R6,R5               IS THIS FAILING CCW?
         BNE   DPR058              BR IF NO
         TM    1(R12),X'40'            IS IT ERROR CCW?         SA53321
         BZ    DPR058                  BR IF NOT                SA53321
         MVI   PB,C'*'             PREFIX CCW WITH *
DPR058   BAL   R10,DPRWRT          GO PRINT CCW LINE
         SPACE
         LA    R6,1(R6)            BUMP CCW NO.
         LA    R7,8(R7)            POINT TO NEXT CCW
         BCT   R8,DPR053           GO PROCESS NEXT CCW
         SPACE
***********************************************************************
*   CONDITION CODE                                                    *
***********************************************************************
         SPACE
DPR060   DS    0H                                                Y01906
         MVI   DPRFLAG,X'00'           CLEAR IT                  Y01906
         TM    3(R12),X'04'            PRINT CC?                 Y01906
         BZ    DPR080              BR IF NO
         SR    R6,R6               CLEAR
         IC    R6,6(R12)           GET EXPECTED CC
         LTR   R6,R6               IS IT GIVEN?
         BZ    DPR070              BR IF NO
         L     R5,16(R1)           GET RECEIVED CC ADDRESS
         LTR   R5,R5               IS IT 0?
         BNZ   DPR061              BR IF NO                       21051
         SPACE
* GET RECEIVED CONDITION CODE FROM A TECB FIELD.
         SPACE
         OI    DPRFLAG,DPRLKCC     TURN ON LOOK FOR CC FLAG       21051
         LTR   R3,R3                   TECB ADDR 0?              Y01906
         BZ    DPR070                  YES-BRANCH                Y01906
         BAL   R10,DPR175          GO FIND FLD IN TECB
         TM    DPRFLAG,DPRSCHFL    SEARCH FAIL?
         BO    DPR061A                 YES-BRANCH                Y01906
         TM    DPRFLAG,DPRQUED         QUEUED?                   Y01906
         BNO   DPR060A                 NO-BRANCH                 Y01906
         MVC   PB+22(6),=C'QUEUED'     YES-SAY SO                Y01906
         B     DPR064                  GO AROUND                 Y01906
DPR060A  DS    0H                                                Y01906
         LR    R5,R13              PUT POINTER IN R5
DPR061   DS    0H                                                 21051
         CLI   0(R5),X'FF'         IS CODE 'FF'
         BNE   DPR062              BR IF NO
DPR061A  DS    0H                                                Y01906
         MVI   PB+22,X'F0'         PUT CC 0 IN PRINT BUFFER
         B     DPR064
         SPACE
DPR062   CLI   0(R5),C'0'          IS CC=0,1,2,3?                 21051
         BE    DPR063                                             21051
         CLI   0(R5),C'1'                                         21051
         BE    DPR063                                             21051
         CLI   0(R5),C'2'                                         21051
         BE    DPR063                                             21051
         CLI   0(R5),C'3'                                         21051
         BNE   DPR070              NO.  SET RETURN CODE TO 0C     21051
DPR063   MVC   PB+22(1),0(R5)      MOVE CC TO PRINT BUFFER
DPR064   STC   R6,PB+9             PUT XPTD CC IN BUFFER
         MVC   PB+1(7),=C'XPTD CC'  PUT 'XPTD CC' IN BUFFER
         MVC   PB+14(7),=C'RCVD CC'  PUT 'RCVD CC' IN BUFFER
         TM    3(R12),X'02'        IS CC IN ERROR?
         BZ    DPR066              BR IF NO
         MVI   PB,C'*'             PUT * IN BUFFER
         SPACE
DPR066   BAL   R10,DPRWRT          GO PRINT CC LINE
         B     DPR080              GO CHK FOR XPTD CSW PRINT
         SPACE
DPR070   MVI   DPRTRNCD,X'0C'      SET RTRN CODE TO 0C
         MVI   DPRFLAG,X'00'           CLEAR IT                  Y01906
         B     DPR080              GO CHK FOR XPTD CSW PRINT
         SPACE
***********************************************************************
*        EXPECTED  CSW                                                *
***********************************************************************
         SPACE
DPR080   DS    0H                                                Y01906
         MVI   DPRFLAG,X'00'           CLEAR IT                  Y01906
         SR    R7,R7                   CLEAR IT                  Y01906
         IC    R7,13(R12)          GET XPTD CSW MASKS
         STC   R7,DPRMASK          SAVE IT
         TM    2(R12),X'20'        PRINT CSW1?
         BNZ   DPR086              BR IF YES
         SPACE
DPR082   LA    R11,DPR100          GET RTRN ADDR
         LA    R5,PB+11            POINT TO FLD IN BUFR
         BAL   R10,DPR092          GO CHK FOR XCSW2 PRINT
         SPACE
         MVC   PB+1(9),=C'XPTD CSW2'  PUT 'XPTD CSW2' IN BUFR
         TM    3(R12),X'01'        PSW PRINT?
         BZ    DPR084              BR IF NO
         MVI   PB+6,C'P'           PUT 'P' IN BUFR
DPR084   B     DPR098              GO PRINT
         SPACE
DPR086   L     R13,24(R1)          GET XPTD CSW1 ADDRESS
         LTR   R13,R13             IS IT 0?
         BNZ   DPR088              BR IF NO
         MVI   DPRTRNCD,X'0C'      SET RTRN CODE 0C
         B     DPR082              BR TO CHK XCSW2
         SPACE
DPR088   MVC   PB+1(9),=C'XPTD CSW1'  PUT 'XPTD CSW1' IN BUFR
         TM    3(R12),X'01'        PSW?
         BZ    DPR090              BR IF NO
         MVI   PB+6,C'P'           PUT 'P' IN BUFR
DPR090   LA    R5,PB+11            GET FLD IN BUFR
         BAL   R11,DPR450          GO SET UP FOR PRINT
         SPACE
         LA    R11,DPR098          GET RTRN ADDR
         LA    R5,PB+37            GET FLD ADDR IN BUFR
         BAL   R10,DPR092          GO CHK FOR XCSW2 PRINT
         MVC   PB+32(4),=C'CSW2'   PUT 'CSW2' IN BUFR
         TM    3(R12),X'01'        PSW ?
         BZ    DPR098              BR IF NO
         MVI   PB+32,C'P'          PUT 'P' IN BUFR
         B     DPR098              BR TO PRINT
         SPACE
DPR092   TM    2(R12),X'08'        PRINT XCSW2?
         BNZ   DPR094              BR IF YES
         BR    R11                 RETURN
         SPACE
DPR094   SR    R7,R7               CLEAR
         IC    R7,DPRMASK          GET MASK
         SLL   R7,28                  FOR XCSW2
         SRL   R7,24
         STC   R7,DPRMASK          PUT BACK
         L     R13,32(R1)          GET XCSW2 ADDRESS
         LTR   R13,R13             IS IT 0?
         BNZ   DPR096              BR IF NO
         MVI   DPRTRNCD,X'0C'      SET RTRN CODE 0C
         BR    R11                 RETURN
         SPACE
DPR096   BAL   R11,DPR450          GO SET UP FOR PRINT
         BR    R10                 RETURN
         SPACE
DPR098   BAL   R10,DPRWRT          GO PRINT EXPECTED CSW LINE
         B     DPR100
         SPACE
***********************************************************************
*        RECEIVED  CSW                                                *
***********************************************************************
         SPACE
DPR100   MVI   DPRMASK,X'FF'           SET MASK TO ONES
         MVI   DPRFLAG,X'00'           CLEAR IT                  Y01906
         TM    2(R12),X'10'            PRINT RCVD CSW1?
         BNZ   DPR102              BR IF YES
DPR101   LA    R11,DPR200          GET RTRN ADDR
         LA    R5,PB+11            GET FIELD ADDR IN PRINT BUFFER
         BAL   R10,DPR150          GO CHK FOR CSW2 PRINT
         BAL   R11,DPR450              GO SET UP                 Y01906
         MVC   PB+1(9),=C'RCVD CSW2'  PUT 'RCVD CSW2 IN PRINT BUFFER
         TM    3(R12),X'01'        PSW PRINT?
         BZ    DPR101A             BR IF NO
         MVI   PB+6,C'P'           PUT 'P' IN BUFFER
DPR101A  B     DPR110B             GO PRINT
         SPACE
DPR102   L     R13,28(R1)          GET RCVD CSW1 ADDRESS
         LTR   R13,R13             IS ADDR GIVEN?
         BNZ   DPR105              BR IF YES
         LTR   R3,R3                   TECB ADDR 0?              Y01906
         BNZ   DPR102A                 NO-BRANCH                 Y01906
         MVI   DPRTRNCD,X'0C'          SET RTRN CODE X'0C'       Y01906
         B     DPR200                  GO CHK FOR SNS PRINT      Y01906
DPR102A  DS    0H                                                Y01906
         BAL   R10,DPR175          GO FIND EVENT IN TECB
         MVC   PB+11(4),DPRNONE        ASSUME NONE               Y01906
         LA    R13,4(R13)              ASSUME SOME               Y01906
         SPACE
DPR105   TM    1(R12),X'20'        RCVD CSW1 ERROR ?
         BZ    DPR110              BR IF NO
         MVI   PB,C'*'             PREFIX CSW WITH '*'
DPR110   MVC   PB+1(9),=C'RCVD CSW1'  PUT 'RCVD CSW1' IN PRINT BUFFER
         TM    3(R12),X'01'        PSW PRINT?
         BZ    DPR110A             BR IF NO
         MVI   PB+6,C'P'           PUT 'P' IN BUFFER
DPR110A  LA    R5,PB+11            GET FLD ADDR IN PRINT BUFFER
         BAL   R11,DPR450          GO SET UP FOR PRINT
         NI    DPRFLAG,255-DPRSCHFL-DPRQUED  CLEAR EM            Y01906
         SPACE
         LA    R11,DPR115          GET RETURN ADDR
         LA    R5,PB+37            GET FLD ADDR IN BUFR
         BAL   R10,DPR150          GO CHK FOR CSW2 PRINT
         BAL   R11,DPR450                                        Y01906
         MVC   PB+32(4),=C'CSW2'   PUT 'CSW2' IN PRINT BUFFER
         TM    3(R12),X'01'        PSW PRINT?
         BZ    DPR110B             BR IF NO
         MVI   PB+32,C'P'          PUT PSW IN BUFFER
DPR110B  TM    1(R12),X'10'        CSW2 ERROR?
         BZ    DPR115              BR IF NO
         MVI   PB,C'*'             PREFIX LINE WITH *
         SPACE
DPR115   BAL   R10,DPRWRT          GO PRINT RECEIVED CSW LINE
         B     DPR200              GO CHK FOR XPTD SNS PRINT
         SPACE
DPR150   TM    2(R12),X'04'        PRINT CSW2 ?
         BNZ   DPR155              BR IF YES
         BR    R11                 RETURN ON R11
DPR155   L     R13,36(R1)          GET RECEIVED CSW2 ADDRESS
         LTR   R13,R13             IS ADDRESS GIGEN
         BCR   7,R10               YES, GO AROUND CHECK          Y03338
DPR160   DS    0H                                                Y01906
         LTR   R3,R3                   TECB ADDR ZERO?           Y01906
         BNZ   DPR160A                 YES-GO AROUND             Y03338
         MVI   DPRTRNCD,X'0C'          NO-SET RTN CODE 0C        Y01906
         BR    R11                     GO BACK                   Y01906
DPR160A  DS    0H                                                Y03338
         MVI   DPRFLAG,DPRSCH02    TURN ON FLG FOR CSW2 SEARCH
         ST    R10,DSAVERGS        SAVE REG 10
         MVC   0(4,R5),DPRNONE         SAY NONE                  Y01906
         BAL   R10,DPR175          GO FIND CSW2 IN TECB
         L     R10,DSAVERGS        RESTORE R10
         LA    R13,4(R13)              ASSUME SOME               Y01906
         BR    R10                 RETURN ON R10
         SPACE
***********************************************************************
* ENTER HERE TO FIND FLD IN TECB FOR CC, CSW, PSW                     *
***********************************************************************
         SPACE
DPR175   SR    R15,R15             CLEAR
         SR    R14,R14             CLEAR
         NI    DPRFLAG,255-DPRSCHFL-DPRQUED  CLEAR EM            Y01906
         IC    R15,TCBEVOC             GET NO FLDS TO CHK        Y01906
         CLC   TCBEVOC(1),TCBFDCT  COMPARE CNT TO EVNT COUNT
         BC    12,DPR177           BR ON EVNT CNT LE TO FLD CNT
         IC    R15,TCBFDCT         GET NO. OF FIELDS TO CHECK
DPR177   DS    0H                                                Y01906
         LTR   R15,R15             IS EVOC 0 ?
         BZ    DPR185              BR IF 0
DPR179   IC    R14,TCBFDLN         GET FLD LNGTH
         LA    R13,TCBFD01         PTR TO FIRST EVENT FLD
DPR181   CLC   1(3,R13),DPRDEVAD+1  EVENT FLD FOR DEV?
         BNE   DPR183              BR IF NO
         TM    DPRFLAG,DPRLKCC     LOOKING FOR CC?                21051
         BZ    DPR181A             NO.                            21051
         TM    0(R13),X'F0'        YES.  IS THERE A CC ENTRY?     21051
         BO    DPR187              YES.                           21051
         B     DPR183              NO.  TRY NEXT FIELD.           21051
         SPACE
DPR181A  TM    3(R12),X'01'        LOOKING FOR A PSW?             21051
         BNZ   DPR183A             YES.                           21051
         CLI   0(R13),X'F0'        NO.  CC=0 FIELD?               21051
         BE    DPR183              YES                            21051
DPR182A  DS    0H                                                 21051
         TM    DPRFLAG,DPRSCH02    THIS THE ONE I WANT?
         BZ    DPR187              BR IF YES
         SPACE
         NI    DPRFLAG,255-DPRSCH02  TURN OFF SEARCH CSW2 FLAG
DPR183   AR    R13,R14             BUMP TO NEXT FLD
         BCT   R15,DPR181          GO CHK IF CORRECT FLD
         SPACE
DPR185   DS    0H                                                Y01906
         TM    TCBFLGS,QUEUED          SIO QUEUED?               Y01906
         BNO   DPR185A                 NO-BRANCH                 Y01906
         OI    DPRFLAG,DPRQUED         TURN ON Q FLG             Y01906
         BR    R10                     GO                        Y01906
DPR185A  DS    0H                                                Y01906
         OI    DPRFLAG,DPRSCHFL    TURN ON SEARCH FAIL FLAG
DPR187   BR    R10                 RETURN
DPR183A  DS    0H                                                 21051
         TM    0(R13),X'F0'        IS IT A PSW ENTRY?             21051
         BNO   DPR182A             BRANCH IF NOT A COND CODE      21051
         B     DPR183              CHECK NEXT ENTRY               21051
         SPACE
***********************************************************************
*        EXPECTED SENSE                                               *
***********************************************************************
         SPACE
         SPACE
DPR200   DS    0H                                                Y01906
         MVI   DPRFLAG,X'00'           CLEAR IT                  Y01906
         TM    2(R12),X'02'            PRINT XPTD SNS?           Y01906
         BZ    DPR210              NO. BRANCH                     M1005
         L     R13,40(R1)          YES. GET EXPECTED SENSE ADDR   M1005
         LTR   R13,R13             IS ADDRESS GIVEN               M1005
         BNZ   DPR201              YES. BRANCH                    M1005
DPR200A  MVI   DPRTRNCD,X'0C'      NO. SET RETURN CODE 0C         M1005
         B     DPR210              GO CHK FOR RCVD SENSE PRINT    M1005
         SPACE
DPR201   SR    R7,R7               CLEAR REG                      M1005
         IC    R7,14(R12)          GET EXPECTED SENSE BYTE COUNT  M1005
         LTR   R7,R7               IS IT 0?                       M1005
         BZ    DPR200A             YES. BRANCH                    M1005
         SPACE
DPR203   MVC   PB+1(8),=C'XPTD SNS'  LOAD IN PRINT BUFFER         M1005
         BAL   R10,DPR218AA        LOAD SENSE DATA IN PB          M1005
         B     DPR210              GO CHK FOR RCVD SENSE PRINT    M1005
         SPACE
***********************************************************************
*        RECEIVED SENSE                                               *
***********************************************************************
         SPACE
DPR210   DS    0H                                                Y01906
         MVI   DPRFLAG,X'00'           CLEAR IT                  Y01906
         TM    2(R12),X'01'            PRINT RCVD SNS?           Y01906
         BZ    DPR230              NO. BRANCH                     M1005
         L     R13,44(R1)          YES. GET RCVD SENSE ADDRESS    M1005
         LTR   R13,R13             IS ADDRESS GIVEN?              M1005
         BZ    DPR211              NO. CHK IF TECB ADDR GIVEN     M1005
         SPACE
         SR    R7,R7               CLEAR REG                      M1005
         IC    R7,15(R12)          GET SENSE BYTE COUNT           M1005
         LTR   R7,R7               IS IT 0?                       M1005
         BZ    DPR212              YES. BRANCH                    M1005
DPR210A  MVC   PB+1(8),=C'RCVD SNS'  LOAD IN PRINT BUFFER         M1005
         TM    1(R12),X'02'        IS THERE A SENSE ERROR?        M1005
         BZ    DPR210B             NO. BRANCH                     M1005
         MVI   PB,C'*'             YES. IDENTIFY IT WITH AN *     M1005
DPR210B  BAL   R10,DPR218AA        LOAD SENSE INFO IN PRINT BUFR  M1005
         B     DPR230              GO CHECK FOR DATA PRINT        M1005
         SPACE
* GET RECEIVED SENSE DATA FROM TECB
         SPACE
DPR211   DS    0H                                                Y01906
         LTR   R3,R3                   ZERO?                     Y01906
         BZ    DPR212                  BR IF YES                  M1005
         BAL   R10,DPR213          GET RCVD SENSE FROM TECB       M1005
         B     DPR210A             GO LOAD IT IN PRINT BUFFER     M1005
DPR212   MVI   DPRTRNCD,X'0C'      SET RETURN CODE 0C             M1005
         B     DPR230              GO CHECK FOR DATA PRINT        M1005
         SPACE
DPR213   ST    R10,SAVER10         SAVE R10                       M1005
         SR    R10,R10             CLEAR REG                      M1005
         SR    R14,R14             CLEAR REG                      M1005
         IC    R10,TCBSNOC             GET NO FLDS TO CHK        Y01906
         CLC   TCBSNOC(1),TCBSNCT  COMPARE EVENT COUNT TO FLD CNT M1005
         BC    12,DPR215               BR IF EVNT CNT LE FLD CNT Y01906
         IC    R10,TCBSNCT         GET NUMBER OF FLDS TO CHECK    M1005
DPR215   LTR   R10,R10             IS IT 0?                       M1005
         BNZ   DPR215B                 NO-BRANCH                 Y01906
DPR215A  DS    0H                                                Y01906
         OI    DPRFLAG,DPRSCHFL        TURN ON FAIL FLAG         Y01906
         B     DPR218                  GO RTRN                   Y01906
DPR215B  DS    0H                                                Y01906
         LH    R14,TCBSNLN         NO. GET FIELD LENGTH           M1005
         SR    R6,R6               CLEAR REG                      M1005
         SR    R7,R7               CLEAR REG                      M1005
         SR    R8,R8               CLEAR REG                      M1005
         IC    R7,TCBFDLN          GET FIELD SIZE                 M1005
         IC    R8,TCBFDCT          GET NUMBER OF FIELDS           M1005
         MR    R6,R8               MULTIPLY BY NUMBER OF FIELDS   M1005
         LA    R8,8                ADD 8 TO                       M1005
         AR    R7,R8               TOTAL LENGTH OF FIELDS         M1005
         AR    R7,R3                   POINT TO 1ST TECB SNSFLD  Y01906
         LR    R13,R7              PUT SENSE FIELD ADDRESS IN R13 M1005
         SPACE
DPR216   CLC   0(3,R13),DPRDEVAD+1  CHK DEV ADDR FOR RIGHT FIELD  M1005
         BE    DPR217              YES. BRANCH TO GET SENSE INFO  M1005
         AR    R13,R14             NO. BUMP TO NEXT FIELD         M1005
         BCT   R10,DPR216          GO CHECK FIELD                 M1005
         B     DPR215A                 NO FLD FOUND              Y01906
         SPACE
DPR217   LA    R13,3(R13)          POINT TO SENSE DATA            M1005
         SR    R7,R7               CLEAR REG                      M1005
         IC    R7,15(R12)          GET RCVD SNS CNT FROM PARM LISTM1005
         LTR   R7,R7               IS IT 0?                       M1005
         BNZ   DPR218              NO. BRANCH                     M1005
         LH    R7,TCBSNLN              GET TECB RCVD SNS CNT     Y01906
         SH    R7,THREE                MINUS 3                   Y01906
         LTR   R7,R7               IS IT 0?                       M1005
         BZ    DPR215A                 YES-BRANCH                Y01906
DPR218   L     R10,SAVER10         RESTORE REG                    M1005
         BR    R10                 RETURN                         M1005
         SPACE
* CONVERT EXPECTED OR RECEIVED SENSE DATA AND LOAD PRINT BUFFER   M1005
         SPACE
DPR218AA ST    R10,SAVER10         SAVE REG                       M1005
DPR218AB DS    0H                                                Y01906
         TM    DPRFLAG,DPRSCHFL        SEARCH FAIL?              Y01906
         BZ    DPR218AC                NO- BRANCH                Y01906
         MVC   PB+12(4),DPRNONE        YES-SAY NONE              Y01906
         OI    DPRFLAG,DPRLINE         TURN ON LAST LINE FLG     Y01906
         B     DPR219A                 GO                        Y01906
DPR218AC DS    0H                                                Y01906
         LA    R14,WRK                 GET OUTPUT ADDR FOR CNVRT Y01906
         CH    R7,MAXSNS           COUNT GREATER THAN LINE MAX?   M1005
         BNH   DPR218A             NO. BRANCH                     M1005
         LH    R15,MAXSNS          YES. PUT MAX COUNT IN R15      M1005
         B     DPR218B                                            M1005
         SPACE
DPR218A  OI    DPRFLAG,DPRLINE     TURN ON LAST SENSE LINE FLAG   M1005
         LR    R15,R7              PUT COUNT IN R15               M1005
DPR218B  BAL   R10,DPRCNV          CONVERT RCVD SENSE TO EBCDIC   M1005
         LA    R6,PB+12            PRINT BUFR ADDR FOR FIRST BYTE M1005
         LA    R5,WRK              GET ADDR OF 1ST SENSE BYTE     M1005
DPR219   MVC   0(2,R6),0(R5)       MOVE SENSE ONE BYTE AT A TIME  M1005
         LA    R6,3(R6)            GET NEXT PRINT BUFR ADDRESS    M1005
         LA    R5,2(R5)            GET NEXT SENSE BYTE ADDRESS    M1005
         BCT   R15,DPR219          MOVE NEXT BYTE                 M1005
DPR219A  DS    0H                                                Y01906
         BAL   R10,DPRWRT          PRINT SENSE LINE               M1005
         TM    DPRFLAG,DPRLINE     LAST LINE?                     M1005
         BZ    DPR220              NO. BRANCH FOR NEXT LINE       M1005
         NI    DPRFLAG,255-DPRLINE  YES.  CLEAR FLAG              M1005
         L     R10,SAVER10         RESTORE R10                    M1005
         BR    R10                 RETURN TO XPTCD/RCVD RTN       M1005
         SPACE
DPR220   AH    R13,MAXSNS          POINT TO NEXT LINE OF SENSE    M1005
         SH    R7,MAXSNS           SUBTRACT FOR REMAINING BYTES   M1005
         B     DPR218AB            GO SET UP NEXT LINE FOR PRINT  M1005
         SPACE
***********************************************************************
*        EXPECTED DATA                                                *
***********************************************************************
         SPACE
DPR230   MVI   DPRFLAG,X'00'           CLEAR IT                  Y01906
         TM    3(R12),X'80'            PRINT XPTD DATA?          Y01906
         BZ    DPR240              BR IF NO
         L     R8,48(R1)           GET EXPECTED DATA ADDRESS
         MVC   WRK(2),17(R12)      GET EXPECTED DATA COUNT
         LH    R7,WRK              GET XPTD DATA COUNT
         LA    R11,DPR240          GET RTRN ADDR
         MVC   PB+1(9),=C'XPTD DATA'  MOVE 'XPTD DATA' TO PRINT BUFFER
         BAL   R9,DPR260           GO SET UP AND PRINT
         SPACE
***********************************************************************
*        RECEIVED DATA                                                *
***********************************************************************
         SPACE
DPR240   TM    3(R12),X'40'        PRINT RCVD DATA?
         BZ    DPR250              BR IF NO
         L     R8,52(R1)           GET RECEIVED DATA ADDRESS
         MVC   WRK(2),19(R12)      GET RECEIVED DATA COUNT
         LH    R7,WRK              GET RCVD DATA COUNT
         LA    R11,DPR250          GET RTRN ADDR
         TM    3(R12),X'08'        RCVD DATA ERROR ?
         BZ    DPR242              BR IF NO
         MVI   PB,C'*'             PUT * IN LINE
DPR242   MVC   PB+1(9),=C'RCVD DATA' MOVE 'RCVD DATA' TO PRINT BUFFER
         BAL   R9,DPR260           GO SET UP AND PRINT
         SPACE
         EJECT
***********************************************************************
*        WRITTEN DATA                                                 *
***********************************************************************
         SPACE
DPR250   TM    3(R12),X'20'        PRINT WRITTEN DATA ?
         BZ    DPREXIT             NO.  EXIT RTN
         L     R8,56(R1)           GET WRITTEN DATA ADDRESS
         MVC   WRK(2),21(R12)      GET WRITTEN DATA COUNT
         LH    R7,WRK              GET WRTN DATA COUNT
         LA    R11,DPREXIT         GET EXIT RTN ADDRESS
         MVC   PB+1(9),=C'WRTN DATA'  PUT 'WRTN DATA INTO PRINT BUFFER
         BAL   R9,DPR260           GO SET UP AND PRINT
         B     DPREXIT             BRANCH TO EXIT MODULE
         SPACE
DPR260   LTR   R8,R8               ADDR GIVEN ?
         BNZ   DPR262              BR IF YES
DPR261   MVI   DPRTRNCD,X'0C'      SET RETURN CODE 0C
         XC    PB(10),PB           CLEAR BUFFER
         BR    R11                 BRANCH TO EXIT RTN             M1005
DPR262   LTR   R7,R7               IS COUNT 0 ?
         BZ    DPR261              BRANCH IF NO                   M1005
         SPACE
DPR262A  TM    1(R12),X'08'        DON'T CONVERT BIT ON?          M1005
         BZ    DPR262B             NO.  BRANCH                    M1005
         MVI   LIMIT+1,50          YES. CHANGE LIMIT TO 50        M1005
DPR262B  CH    R7,LIMIT            MORE THAN 1 LINE?              M1005
         BNH   DPR263              BR IF NO                       M1005
         LH    R15,LIMIT           SET MAX COUNT                  M1005
         B     DPR264              GO SET UP FOR CONVERT          M1005
DPR263   LR    R15,R7              SET COUNT                      M1005
         OI    DPRFLAG,DPRLINE     TURN ON LAST LINE FLAG         M1005
         SPACE
DPR264   TM    1(R12),X'08'        DONT CONVERT BIT ON            M1005
         BZ    DPR264A             YES. BRANCH                    M1005
         BCTR  R15,0               NO. SUBT 1 FOR MVC CNT         M1005
         EX    R15,DPRMVDAT        MOVE DATA TO PRINT BUFFER      M1005
         B     DPR264B                                            M1005
DPR264A  LR    R13,R8                                             M1005
         LA    R14,PB+11           GET OUTPUT ADDR FOR CONVERT    M1005
         BAL   R10,DPRCNV          GO CONVERT TO EBCDIC           M1005
DPR264B  BAL   R10,DPRWRT                                         M1005
         SPACE
         TM    DPRFLAG,DPRLINE     LAST LINE?
         BZ    DPR265              BR IF NO
         NI    DPRFLAG,255-DPRLINE TURN OFF LAST LINE FLAG
         BR    R9                  RETURN
         SPACE
DPR265   AH    R8,LIMIT            POINT TO NEXT LINE OF DATA
         SH    R7,LIMIT            GET NO BYTES
         B     DPR262A             GO SET UP FOR LINE PRINT
         SPACE
DPRMVDAT MVC   PB+11(0),4(R8)                                     M1005
***********************************************************************
*   ENTER HERE TO MOVE CSW/PSW INFO INTO PRINT BUFFER                 *
***********************************************************************
         SPACE
DPR450   ST    R10,DSAVERGS+20     SAVE R10
         TM    DPRFLAG,DPRSCHFL+DPRQUED   FAIL?                  Y01906
         BCR   7,R11                   YES-GO BACK               Y01906
         LA    R14,WRK             GET OUTPUT ADDR FOR CONVERT
         LA    R15,8               GET COUNT
         BAL   R10,DPRCNV          GO CONVERT
         L     R10,DSAVERGS+20     RESOTRE R10
         LA    R6,WRK
         LA    R7,DPRMASK          POINT TO CSW MASK
         TM    3(R12),X'01'        IS IT PSW PRINT?
         BZ    DPR450A             BR IF NO
         MVC   0(16,R5),0(R6)      PUT PSW IN BUFFER
         B     DPR460
DPR450A  MVC   0(19,R5),=C'XX XXXXXX XXXX XXXX'  SET UP FOR UNUSED
         TM    0(R7),X'80'         KEY GIVEN?
         BO    DPR452              BR IF YES
         SPACE
         TM    0(R7),X'40'         CMND ADDR GIVEN?
         BO    DPR454              BR IF YES
         SPACE
         TM    0(R7),X'20'         STATUS GIVEN?
         BO    DPR456              BR IF YES
         SPACE
         TM    0(R7),X'10'         RESIDUAL COUNT GIVEN?
         BO    DPR458              BR IF YES
         BR    R11                 RETURN
         SPACE
DPR452   MVC   0(2,R5),0(R6)       PUT KEY IN PRINT BUFFER
         LA    R6,2(R6)            POINT TO CMND ADDR
         SPACE
DPR454   TM    0(R7),X'40'         CMND ADDR GIVEN?
         BZ    DPR460              BR IF NO
         MVC   3(6,R5),0(R6)       PUT CMND ADDR IN PRINT BUFFER
         LA    R6,6(R6)            POINT TO STATUS
         SPACE
DPR456   TM    0(R7),X'20'         STATUS GIVEN?
         BZ    DPR460              BR IF NO
         MVC   10(4,R5),0(R6)      PUT STATUS IN PRINT BUFFER
         LA    R6,4(R6)            POINT TO RESIDUAL COUNT
         SPACE
DPR458   TM    0(R7),X'10'         RESIDUAL COUNT GIVEN?
         BZ    DPR460              BR IF NO
         MVC   15(4,R5),0(R6)      PUT RESIDUAL COUNT IN PRINT BUFFER
DPR460   BR    R11                 RETURN
         SPACE
         EJECT
***********************************************************************
*              RETURN TO SCAN SECTION                                 *
***********************************************************************
         SPACE
DPREXIT  L     R13,RSLTSA+4        ADDRESS OF CALLER'S SA
         MVC   16(4,R13),RETCODE   RETURN CODE TO R15 SAVE AREA LOC
         LM    R14,R12,12(R13)     RESTORE REGS
         BR    R14                 RETURN
         SPACE
***********************************************************************
*        PRINT RESULTS, DATA LINE VIA PARALLEL PRINT OUTPUT WRITER    *
***********************************************************************
         SPACE
DPRWRT   ST    R13,DSAVERGS        SAVE R13                       M1010
         LA    R13,RSLTSA          SAVE AREA ADDRESS              M1010
         OI    CESWT5,RSLTFLG      SET RESULTS LINE PRINT FLAG
         PWRT  SIZE=120                                         SA46329
         MVI   PB,C' '             CLEAR PRINT BUFFER WITH BLANKS
         MVC   PB+1(120),PB
         L     R1,SAVER1           RESTORE DPRINT PARAMETER PTR
         L     R12,4(R1)           ADDRESS OF DPRINT CTRL WORD
         L     R13,DSAVERGS        RESTORE R13                    M1010
         BR    R10                 RETURN TO CALLING ROUTINE
         EJECT
***********************************************************************
*        DPRCNV - HEX TO EBCDIC CONVERT                               *
*                                                                     *
*              CALL SEQ -  R13 = INPUT ADDRESS                        *
*                          R14 = OUTPUT ADDRESS                       *
*                          R15 = INPUT BYTE COUNT                     *
*                                                                     *
*                          BAL R10,DPRCNV                             *
***********************************************************************
         SPACE
DPRCNV   STM   R5,R6,DSAVERGS      SAVE R5,R6
         STM   R13,R15,DSAVERGS+8  SAVE R13,R14,R15
         SPACE
         LTR   R15,R15            COUNT = 0 ?
         BZ    DPRCNV8            YES. BYPASS CONVERT
         AR    R14,R15         ADD COUNT TO OUTPUT ADDR.
         AR    R14,R15            ADD COUNT AGAIN.
         AR    R13,R15            ADD COUNT TO INPUT ADDR.
         SR    R5,R5              CLEAR R5.
         SR    R6,R6              CLEAR R6.
         SPACE
DPRCNV4  S     R14,=F'2'          GET ADDR OF LAST BYTE OF OUTPUT.
         BCTR  R13,0              GET ADDR OF LAST BYTE OF INPUT.
         IC    R6,0(R13)          GET HEX BYTE.
         LR    R5,R6              PUT IN R5.
         SRL   R6,4               GET BITS 0-3 IN R6.
         N     R5,=F'15'          GET BITS 4-7 IN R5.
         IC    R6,DPREBC(R6)      CONVERT BITS 0-3 TO EBCDIC CHAR.
         IC    R5,DPREBC(R5)      CONVERT BITS 4-7 TO EBCDIC CHAR.
         STC   R6,0(R14)          STORE 1ST EBCDIC CHAR IN OUTPUT.
         STC   R5,1(R14)          STORE 2ND EBCDIC CHAR IN OUTPUT.
         BCT   R15,DPRCNV4        GET NEXT HEX BYTE.
DPRCNV8  LM    R5,R6,DSAVERGS      RESTORE R5,R6
         LM    R13,R15,DSAVERGS+8  RESTORE R13,R14,R15
         BR    R10                RETURN TO CALLER.
         SPACE
DPREBC   DC    C'0123456789ABCDEF' HEX TO EBCDIC CONVERT TABLE.
         SPACE 2
***********************************************************************
*              CONSTANTS, ETC.                                        *
***********************************************************************
         SPACE
RSLTSA   DS    18F                 REG SAVE AREA
LIMIT   DC    H'25'               MAX NO. OF DATA BYTES PER LINE
MAXCNT   DC    H'61'               MAX NO. BYTES TO PRINT
MAXSNS  DC    H'17'               MAX NO. SNS BYTES PER LINE
THREE    DC    H'03'                                             Y01906
DPR600   MVC   PB(0),0(R11)       MOVE MSGE TO PRINT BUFFER.
DPR605   MVC   PB+1(0),0(R5)       MOVE DESC LINE TO PRINT BUFFER
         CNOP  0,8
WRK      DS    12F                 WORKAREA
SAVER1   DC    XL4'0'
DSAVERGS DS    6F                  REG SAVE BUFRS
DPRDEVAD DS    1F                  DEV ADDR
DPROLTSP DC    XL4'0'              TO SAVE OLT SECT PREF ADDR
DPROLTLP DC    XL4'0'              TO SAVE OLT LOAD POINT ADR
RETCODE  DC    XL4'0'              RETURN CODE
DPRTFORM DC    X'00'               FORMS
DPRFLAG  DC    X'00'               FLAGS
DPRMASK  DC    X'00'               CSW MASK
DPRNONE  DC    C'NONE'                                           Y01906
DPRLINE  EQU   X'04'               LINECOUNT FLAG IN DPRFLAG
DPRSCHFL EQU   X'02'               SEARCH FAIL FLAG IN DPRFLAG
DPRSCH02 EQU   X'01'               SEARCH FOR CSW2 FLAG IN DPRFLAG
DPRLKCC  EQU   X'10'               CC FLAG IN DPRFLAG             21051
DPRTRNCD EQU   RETCODE+3
SAVER10  EQU   DSAVERGS+20
DPRQUED  EQU   X'80'                   Q FLAG IN DPRFLAG         Y01906
QUEUED   EQU   X'80'                   Q FLAG IN TECB            Y01906
         SPACE 2
         LTORG
         SPACE
TECBMAP  DSECT
TCBFDCT  DS    C
TCBFDLN  DS    C
TCBSNLN  DS    2C
TCBFLGS  DS    C
TCBSNCT  DS    C
TCBSNOC  DS    C
TCBEVOC  DS    C
TCBFD01  DS    C
         SPACE
IFDOLT15 CSECT                                                   YM5407
DPRCOM   IFDCOM
PB       EQU   PRTBUFR+1
         END
