 TITLE  'IFDOLT17  MESSAGE LINES'
****************************** PROLOGUE *******************************
*                                                                     *
*  TITLE DPRINT MESSAGES (COMMENTS)                                   *
*                                                                     *
*  FUNCTION: THIS MODULE IS DESIGNED TO SETUP UNIT TEST MESSAGE       *
*            LINES FOR PRINTING                                       *
*                                                                     *
*  ENTRY POINT: IFDOLT17                                              *
*                                                                     *
*  INPUTS: DPRINT PARAMETER LIST                                      *
*                                                                     *
*  REGISTER USAGE: R1  - ADDRESS OF DPRINT PARAMETER LIST             *
*                  R2  - ADDRESS OF SECTION CTRL TABLE IN OLTEP ROOT  *
*                  R4  - BASE REGISTER FOR THIS MODULE                *
*                  R5-R11 - MISC.                                     *
*                  R12 - ADDRESS OF DPRINT PARAMETER CTRL WORD        *
*                  R13 - ADDRESS OF CALLERS SAVE AREA                 *
*                  R14 - ADDRESS OF CALLERS RETURN POINT              *
*                  R15 - LINK ADDRESS                                 *
*                                                                     *
*  EXTERNAL ROUTINE: IFDOLT39- PARALLEL PRINT OUTPUT WRITER           *
*                                                                     *
*  EXITS - NORMAL: - RETURN TO SCAN SECTION OF IFDOLT14               *
*           ERROR: - NONE                                             *
*                                                                     *
*  ATTRIBUTES: SERIALLY REUSABLE                                      *
*                                                                     *
***********************************************************************
         SPACE 2
IFDOLT17 CSECT
         USING IFDOLT17,R4         BASE REG FOR THIS MODULE
         USING CHASCT,R2           BASE REG FOR COM SECTION
         SPACE
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
DPRMSG   SAVE (14,12),,IFDOLT17&SYSDATE
*        ESTABLISH DATE AND SAVE REGS
         SPACE  3
         LR    R4,R15              LOAD BASE REG
         LA    R5,MSGSA            ADDRESS OF SAVE AREA
         ST    R5,8(R13)           STORE IN CALLER'S SA
         ST    R13,4(R5)           STORE ADDR OF CALLER'S SA
         L     R5,4(R1)            ADR PTR SAVE AREA              M4506
         L     R1,0(R5)            GET DPRINT PARAMETER PTR
         L     R12,4(R1)           ADDRESS OF DPRINT CTRL WORD
         ST    R1,SAVER1           SAVE DPRINT PARAMETER PTR
         XC    DPRTRNCD(4),DPRTRNCD  CLEAR WORD                   M2413
         SPACE
DPR300  TM    1(R12),X'01'        PRINT MSG ?
         BZ    DPR311              NO.  EXIT RTN.
DPR303A SR    R7,R7               CLEAR R7
         IC    R7,4(R12)          GET NO. OF MSGE LINES.
         LTR   R7,R7              NO. LINES = 0 ?
         BZ    DPR312              YES.  EXIT RTN.                M2413
         SPACE
         L     R8,8(R1)           GET ADDR OF MSGE ADDRESSES.
         SR    R9,R9              CLEAR R9.
         IC    R9,5(R12)          GET DISPLACEMENT TO MSGE FIELDS.
         LA    R5,0(R9,R12)       GET ADDR OF MSGE FIELDS.
         SPACE
DPR305   SR    R9,R9              CLEAR R9.
         IC    R9,0(R5)           GET COUNT.
         LTR   R9,R9              IS COUNT = 0 ?
         BZ    DPR310             BRANCH IF YES
         LH    R6,MAXCNT          GET MAX NO. BYTES ALLOWED.
         CR    R9,R6              COUNT GREATER THAN MAX ?
         BNH   DPR307             BRANCH IF NO.
         LR    R9,R6              SET COUNT = MAX.
DPR307   L     R11,0(R8)          GET ADDR OF MSGE LINE.
         SPACE
DPR309   BCTR  R9,0                REDUCE COUNT FOR MVC INSTR.
         EX    R9,DPR600          EXECUTE MVC TO PLACE MSGE IN BUFFER.
         ST    R5,DSAVE5           SAVE REG 5
         L     R5,DSAVE5           RESTORE REG
         BAL   R10,DPRWRT          GO PRINT COMMENT LINE
         SPACE
DPR310   LA    R8,4(R8)           GET NEXT MSGE ADDR.
         LA    R5,1(R5)            BUMP FOR NEXT ADDR FIELD
         BCT   R7,DPR305          PRINT NEXT LINE.
         SPACE
* RETURN TO SCAN SECTION.
         SPACE
DPR311   L     R13,MSGSA+4         ADDRESS OF CALLER'S SA
         MVC   16(4,R13),DPRTRNCD  RETURN CODE TO R15 SAVE AREA   M2413
         LM    R14,R12,12(R13)     RESTORE REGS
         BR    R14                 RETURN
DPR312   MVI   DPRTRNCD+3,X'0C'    SET RETURN CODE                M2413
         B     DPR311                                             M2413
         SPACE
DPR600   MVC   PB(0),0(R11)        MOVE TO PRINT BUFFER
         SPACE
***********************************************************************
*        PRINT COMMENT LINE VIA PARALLEL PRINT OUTPUT WRITER          *
***********************************************************************
         SPACE
DPRWRT   LA    R13,MSGSA           SAVE AREA ADDRESS
         PWRT  SIZE=120                                         SA46329
         MVI   PB,C' '             CLEAR PRINT BUFFER WITH BLANKS
         MVC   PB+1(120),PB
         L     R1,SAVER1           RESTORE DPRINT PARAMETER POINTER
         L     R12,4(R1)           ADDRESS OF DPRINT CTRL WORD
         BR    R10                 RETURN TO CALLING ROUTINE
         SPACE 4
MSGSA    DS    18F                 REG SAVE AREA
SAVER1   DC    XL4'0'
DSAVE5   DC    XL4'0'
DPRTRNCD DC    XL4'0'              RETURN CODE                    M2413
MAXCNT   DC    H'119'              MAX PRINT CHARACTERS           M1010
DPRCOM   IFDCOM
PB       EQU   PRTBUFR+1
         END
