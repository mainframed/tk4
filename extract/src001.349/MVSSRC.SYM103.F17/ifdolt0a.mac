IFDOLT0A CSECT
R1       EQU   1                       PARMLIST POINTER          Y02008
CVTREG   EQU   3                       CVT POINTER               Y02008
DIEPTR   EQU   4                       DIE POINTER       -       Y02008
R11      EQU   11                      BASE REGISTER             Y02008
R12      EQU   12                      REG 12                    Y02008
R13      EQU   13                      SAVE AREA POINTER         Y02008
R14      EQU   14                      RETURN ADDRESS POINTER    Y02008
R15      EQU   15                      REG 15                    Y02008
D0       EQU   0                       DISPLACEMENT 0            Y02008
D8       EQU   8                       DISPLACEMENT 8            Y02008
D12      EQU   12                      DISPLACEMENT 12           Y02008
D28      EQU   28                      DISPLACEMENT 28           Y02008
D38      EQU   38                      DISPLACEMENT 38           Y02008
FIVENINE EQU   59                      OLTEPS SVC CALL           Y02008
L2       EQU   2                       LENGTH 2                  Y02008
HEX10    EQU   X'10'                   HEX LOCATION 10           Y02008
CLEANUP  EQU   20                      CLEAN UP ACLL CODE        Y02008
         SAVE  (14,12),,IFDOLT0A&SYSDATE                         Y02008
         BALR  R11,0                   ESTABLISH ADDRESSABILITY  Y02008
         USING *,R11                                             Y02008
         ST    R13,SAVEAREA+4          SAVE CALLERS SAVE AREA PTRY02008
*                                      IN MY SAVE AREA           Y02008
         LA    R15,SAVEAREA            GET ADDR OF MY SAVE AREA  Y02008
         ST    R15,D8(R13)             SAVE ADDR OF MY SAVE AREA Y02008
*                                      IN CALLERS SAVE AREA      Y02008
         LR    R13,R15                 PLACE MY SAVE AREA ADDR   Y02008
*                                      IN REG 13                 Y02008
         L     R1,D0(R1)               GET POINTER TO PARM LIST  Y02008
         USING RMPL,R1                 PARMLIST ADDRESSABILITY   Y02008
***********************************************************************
*                         IS THIS OLTEPS MEMORY                       *
***********************************************************************
         USING CVTMAP,CVTREG           CVT ADDRESSABILITY        Y02008
         L     CVTREG,HEX10            GET CVT ADDRESS           Y02008
         L     CVTREG,CVTEXT2          GET CVT EXTENSION ADDRESS Y02008
         L     DIEPTR,D28(CVTREG)      GET DIE PTR FROM CVT OLTEP WORD
         LTR   DIEPTR,DIEPTR           IS DIE THERE              Y02008
         BZ    EXIT                    NO - CLEAN UP HAS BEEN    Y02008
*                                      DONE OR IT IS IMPOSSIBLE  Y02008
*                                      TO DO IT OR IT IS NOT     Y02008
*                                      OLTEPS MEMORY             Y02008
         CLC   D38(L2,DIEPTR),RMPLASID IS THIS OLTEPS MEMORY     Y02008
         BNE   EXIT                    NO, EXIT                  Y02008
         SPACE 4
***********************************************************************
*            THIS IS OLTEPS MEMORY - PERFORM TOTAL CLEANUP            *
***********************************************************************
         LA    R1,CLEANUP              CLEAN UP CALL CODE        Y02008
         SVC   FIVENINE                GO CLEAN UP               Y02008
EXIT     EQU   *                       EXIT ROUTINE              Y02008
         L     R13,SAVEAREA+4          RESTORE CALLERS REGISTERS Y02008
         LM    R14,R12,D12(R13)                                  Y02008
         BR    R14                     RETURN TO CALLER          Y02008
SAVEAREA DC    18F'0'                  SAVE AREA                 Y02008
RMPLMAP  IHARMPL                       PARMLIST DSECT            Y02008
         CVT   SYS=AOS2,DSECT=YES,LIST=YES                       Y02008
         END
