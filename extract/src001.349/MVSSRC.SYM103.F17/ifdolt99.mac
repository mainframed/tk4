         TITLE ' IFDOLT99  OLTEP''S EDITOR MODULE'
IFDOLT99 CSECT
***********************************************************************
*                                                                     *
*                                 PROLOGUE                            *
*                                                                     *
*                                                                     *
*  STATUS:                                                            *
*          THIS IS THE FIRST VERSION OF THIS MODULE         NOV 4 1971*
*                                                                     *
*                                                                     *
*  FUNCTION:                                                          *
*          THE PURPOSE OF THIS MODULE IS TO CREATE A PARTITIONED      *
*          DATA SET (IF JOB DD CARD LABELS SET AS NEW), OR ADD        *
*          ENTRIES TO AN ALREADY CREATED PDS, (IF DDCARE LABELS       *
*          SET AS OLD). INPUT TO THIS MODULE MAY BE TAPE OR CARDS,    *
*          IN EITHER CASE THE 'OBJECT' DECK MODULES WILL BE LINK-     *
*          EDITED TO THE DATA SET IN A FORMAT REQUIRE BY OLTEPS       *
*          LOADER MODULE (IFDOLT12). THIS MODULE WILL NOT ALTER       *
*          THE FOLLOWING INPUT CARDS:                                 *
*                             1. ESD   FIRST CARD OF OBJ DECK         *
*                             2. RLD   RELOCATION DICTIONARY          *
*                             3. REP   REPAIR CARD                    *
*                             4. END   END OF MODULE INDICATOR        *
*                             5. CONTROL CARDS (NAME, SETSSI, SYMBOLIC*
*                                CDS)                                 *
*           HOWEVER, TEXT (TXT) CARDS WILL BE CHANGED BY THIS MODULE. *
*           WHEN A TEXT CARD IS READ BY IFDOLT99, THE TXT INFOR-      *
*           MATION WILL BE BLOCKED INTO SEGMENTS FROM 1 TO 3600 BYTES.*
*           ALSO, A TEXT HEADER CARD WILL BE GENERATED WHICH WILL     *
*           REPRESENT THE BLOCKED DATA (TXT). ALL  RECORDS WRITTEN    *
*           BY THIS MODULE WILL BE 80 BYTES IN LENGTH WITH EXCEPTION  *
*           TO THE BLOCK TEXT RECORDS.                                *
*                                                                     *
*                                                                     *
*  ENTRY POINTS:                                                      *
*           THE MAIN AND ONLY ENTRY POINT TO THIS MODULE IS IFDOLT99. *
*                                                                     *
*                                                                     *
*  INPUT:                                                             *
*           AS INPUT THIS MODULE WILL ACCEPT OBJECT DECKS ON A        *
*           CARD IMAGE  TAPE OR OBJECT DECKS ON CARDS. IF INPUT       *
*           IS FROM A CARD IMAGE TAPE GENERATED BY THE STAND-ALONE    *
*           SUPPORT PROCESSOR, TWO FILES WILL EXIST. THE FIRST        *
*           FILE WILL CONSIST OF THE ONLINE TESTS AND THE LOCAL       *
*           CONFIGURATION DATA SETS,(CDS) LOAD MODULES. THE           *
*           SECOND FILE MAY BE A NULL FILE OR ANOTHER CDS FILE        *
*           FOR THE REMOTE DEVICES CONFIGURATION.  THESE OBJECT       *
*           MODULES WILL BE WRITTEN ON TO THE DATA SET DESCRIBED      *
*           IN THE SYMSYM DD CARD. IF INPUT IS FROM                   *
*           THE CARD READER, THE SYMBOLIC CDS CONTROL CARD MUST BE    *
*           MANUALLY INSERTED BEFORE THE SYMBOLIC CDS LOAD MODULES    *
*           AND FOLLOWING THE OLT-LOCAL CDS LOAD MODULES. THIS        *
*           MODULE WILL ACCEPT LOAD MODULES INDIVIDUALLY OR IN        *
*           MULTIPLES. INPUT DATA WILL BE READ FROM THE DEVICE        *
*           SPECIFIED IN THE CNTRLIN DD CARD.                         *
*                                                                     *
*  OUTPUT:  THIS MODULE WILL OUTPUT MESSAGES TO THE OPERATOR CONSOLE  *
*           AND TO THE SYSTEM PRINTER (DEVICE ON DIAGMSG DD CARD).    *
*           OLTS AND CDS (LOCAL) MODULES WILL BE OUTPUTTED TO         *
*           THE DATA SET SPECIFIED ON THE OLTCDSDD DD CARD.           *
*           SYMBOLIC NAMED CONF DATA SETS WILL BE OUT-PUTTED          *
*           TO THE DATA SET SPECIFIED IN THE SYMCDSDD DD              *
*           CARD.                                                     *
*                                                                     *
*           THE FOLLOWING MESSAGES WILL BE PRINTED TO THE PRINTER     *
*           SPECIFIED ON THE DIAGMSG DD CARD.                         *
*                                                                     *
*           1.  IFD900I:  THIS MESSAGE WILL INDICATE A RECORD         *
*                         READ FROM THE INPUT DEVICE WAS NOT          *
*                         EXPECTED, AND THAT THE CARD WAS             *
*                         REJECTED.                                   *
*                                                                     *
*           2.  IFD901I:  THIS MESSAGE IS PRINTED WHEN A OPEN         *
*                         TO A DCB FAILS. THE MESSAGE WILL CONTAIN    *
*                         THE DD NAME FOR THE FAILING DCB.            *
*                                                                     *
*           3.  IFD902I:  THIS MESSAGE INDICATES THE MODULE           *
*                         WAS CORRECTLY ADDED/REPLACED IN THE         *
*                         DATA SET SPECIFIED.                         *
*                                                                     *
*           4.  IFD903I:  THIS MESSAGE IS OUTPUTTED WHEN A MODULE     *
*                         COULD NOT BE ADDED OR REPLACE ON THE        *
*                         DATA SET.  THE MESSAGE TEXT WILL INDICATE   *
*                         THE CAUSE OF THE ERROR                      *
*                         IN ALL CASES THE JOB WILL BE TERMINATED.    *
*                                                                     *
*                                                                     *
*           5.  IFD904I  THIS MESSAGE INDICATES AN ERROR IN THE       *
*                        OBJECT DECK SEQUENCE NUMBERS WAS FOUND.      *
*                        THE MODULE CONTAINING THE ERROR WILL         *
*                        NOT BE STOWED INTO THE DIRECTORY. THE        *
*                        MESSAGE WILL SHOW THE EXPECTED AND RECIEVED  *
*                        SEQUENCE NUMBERS; AND THE MODULES NAME WHICH *
*                        WAS NOT STORED.                              *
*                                                                     *
*                                                                     *
*           6.  IFD906I  THIS MESSAGE IS OUTPUT WHEN AN ERROR         *
*                        IN A REP CARD WAS DETECTED. THE CARD IS      *
*                        OUTPUTTED IN THE MESSAGE TEXT. ALSO,         *
*                        THE MODULE IS BYPASSED.                      *
*           7. IFD907I  THIS MESSAGE IS OUTPUTTED TO INDICATE         *
*                        THE DATA SET TO WHICH MODULES ARE TO BE      *
*                        ADDED.                                       *
*                                                                     *
*           8.  IFD908I  THIS MESSAGE IS OUTPUTTED TO INDICATE        *
*                        INPUT WAS NOT IN THE CORRECT ORDER.          *
*           THE FOLLOWING MESSAGE WILL BE OUTPUTTED TO THE OPERATOR   *
*           CONSOLE IF THE PRINTER DCB COULD NOT BE OPENED:           *
*                                                                     *
*           1.  IFD901I:  THIS MESSAGE WILL HAVE THE DD NAME          *
*                         OLTEP REQUIRES WHEN OPEN FAILED FOR         *
*                         THE PRINTER.                                *
*                                                                     *
*                                                                     *
*           THIS MODULE WILL RETURN A CODE IN REGISTER  R15           *
*                                                                     *
*                                                                     *
*  EXTERNAL ROUTINES:                                                 *
*           THIS MODULE WILL ISSUE THE FOLLOWING SYSTEM MACROS,       *
*                                                                     *
*           1.  OPEN    TO ALL DCBS DD CARDS REQUIRED                 *
*           2.  CLOSE   TO ALL DCBS OPENED                            *
*           3.  WRITE   TO DATA SET WHERE MODULES WILL RESIDE         *
*           4.  STOW    FOR EACH MODULE                               *
*           5.  PUT     TO PRINTER FOR MESSAGE OUTPUT                 *
*           6.  GET     TO RETRIEVE A RECORD                          *
*           7.  CHECK   FOR WRITE COMMAND                             *
*           8.  RDJFCB  TO INPUT DCB                                  *
*           9.  DCB     FOR ALL FILES                                 *
*          10.  DCBD    DSECT                                         *
*          11.  WTO     WHEN PRINTER DCB FAILS TO OPEN                *
*                                                                     *
*  EXITS NORMAL:                                                      *
*            ALL EXITS FROM THIS MODULE ARE MADE AT THE LABEL         *
*            GEN05000                                                 *
*                                                                     *
*  EXITS ERROR:                                                       *
*            ERROR EXITS FROM THIS MODULE ARE MADE AT THE LABEL       *
*            GEN05000                                                 *
*                                                                     *
*                                                                     *
*  TABLES AND WORKAREAS:                                              *
*            ALL TABLES AND WORKAREAS ARE INTERNAL TO THIS MODULE.    *
*            THESE AREAES ARE NOT REFERENCED BY ANY SYSTEM OR         *
*            OLTEP MODULE OTHER THAN IFDOLT99.                        *
*  FIX TO MSG SIZE AND FIX COMMENTS                             XM1330*
* PTM  3316 ADD V IF NECESSARY TO MSG IFD902I                   XM3316*
* APAR OX01911 RECOGNIZE 3420 TAPES AS VALID INPUT DEVICE      OX01911*
***********************************************************************
         EJECT
         SPACE 5                  SAVE G.P. REGS IN CALLER
*                                 *************************************
*                                 *   REGISTER EQUATES DISPLACEMENTS  *
*                                 *   LOAD ADDRESS CONSTANTS AND MASKS*
*                                 *************************************
HEX00    EQU   X'00'              USED TO RESET SWITCHS
L0       EQU   0
D0       EQU   0                  DISPLACEMENT OF ZERO
ZERO     EQU   0                  REGISTER ZERO
TTREG1   EQU   1                  SAVE TTR REGISTER
D1       EQU   1                  DISPLACEMENT
VIRTUAL  EQU   X'01'                   CAN OLT RUN IN VIRT       XM3316
LETTERV  EQU   C'V'                    LETTER V                  XM3316
POINTOK  EQU   X'01'              IF ON A POINT CAN BE ISSUED
*                                 WITHOUT HITTING THE DITECTORY
REPREAD  EQU   X'01'              WHEN ON A REP HAS BEEN READ
RLDREAD  EQU   X'02'              WHEN ON A RLD HAS BEEN READ
SCANREG  EQU   8                  USED IN CARD IDENTIFYING
HEX02    EQU   X'02'              USED TO FINF CTL CARDS
BCTREG   EQU   2                  USED IN SEQUENSE CHECKING
COMMON   EQU   2                  POINTER TO OLTEPS COMMON AREA
FIRSTCD  EQU   X'04'              IF ON FIRST CARD BEEN READ IN
D20      EQU   20                 DISPLACEMENT FIELD
ENDJOB   EQU   X'02'              SWITCH FOR TOTAL END OF JOB
D2       EQU   2
L2       EQU   2                  LENTH FIELD
TWO      EQU   2                  BYTE COUNT
THREE    EQU   3                  NUMBER OF DCB TO OPEN
HEX03    EQU   X'03'
LINECNT  EQU   3                  PAGE AND LIVE COUNT WORK REG
D4       EQU   4                  LNKAGE CONST
FOUR     EQU   4                  DISP AND NO. BYTES
TXTPTR   EQU   4                  POINTER TO TXT BUFFER AREA
WRTLINK  EQU   5                  BALR REGISTER
D5       EQU   5                  DISPL FIELD
SIX      EQU   6                  USED TO SC AN REP CARD
D6       EQU   6
LINK     EQU   6                  BR REGISTER
L7       EQU   7
WORKREG1 EQU   7                  WORK REGISTER
WORKRG1  EQU   WORKREG1
MSGSIZRG EQU   7                  USED IN MSG EX INST
L8       EQU   8                  LNGTH FIELD CONST
DCBREG   EQU   8                  REGISTER FOR DCB WORK
JFCBREG  EQU   WORKREG1           USED TO REF JFCB VIA DSECT
ADDER    EQU   8                  USED IN SEQUENSE CHECKING
L3       EQU   3                  LNGTH FIELD
MSGADDR  EQU   8                  USED FOR MESSAGES ONLY
SLOT     EQU   9                  REG TO POINT DURING SCANN OF REP CD
ACCUMRG  EQU   9                  NUMBER OF BYTES IN TXTAREA REGISTER
WRTSP1   EQU   9                  CODE TO WRITE AND SPACE ONE LINE
SCANCNT  EQU   10                 WILL CONTAIN COUNT OF CHARS TO SCAN
D10      EQU   10                 DISPL FIELD
BASEREG  EQU   11                 BASE REGISTER
D12      EQU   12                 DISP LENFTH
SAVEREG  EQU   13                 SAVE AREA POINTER
D14      EQU   14                 DISPL FIELD
EP       EQU   15                 WORKREGISTER
STOWRC   EQU   15                 STOW RETURN CODE
D17      EQU   17
D16      EQU    16
OPEN     EQU   X'10'
D24      EQU   24
D21      EQU   21                 DISPLACEMENT OF 21
D25      EQU   25                 DISPLAC FOR TEXTID
LASTPASS EQU   32                 MASK FOR LAST PASS SWITCH
D32      EQU   32                 CONSTANT OF 32
D33      EQU   33                 CONSTANT OF 32
D34      EQU   34                 USED TO MOVE MOD NAME
D36      EQU   36                 USED IN MESSAGE MOVE
HEX38    EQU   X'38'              NUMBER LINES PER PAGE
D39      EQU   39                 DISPLACEMENT FIELD
BLANK    EQU   X'40'              BLACK CHARACTER
D46      EQU   46                 USED TO MOVE SEQ NO.
HEX50    EQU   X'50'
SAVETTRZ EQU   64                 MASK FOR SAVE TTR TEST
D68      EQU   68                 USED IN THEJFCB WORK RTN
D73      EQU   73                 DISPLACEMENT
D76      EQU   76
HEX80    EQU   X'80'              DISPLACEMENT CONST
L118     EQU   118
NEEDNAME EQU   128                MASK FOR NAME CARD REQUIREMENT
RESTORE  EQU   X'8B'              RESTORE CMD CODE
ALL      EQU   255                MASK FOR SWITCH RESET
C1       EQU   X'C1'              USED IN SCAN SUBROUTINE
C6       EQU   X'C6'              USED IN SCAN SUBROUTINE
F0       EQU   X'F0'              USED IN SCAN SUBROUTINE
HEXF0    EQU   X'F0'              CONSTANT OF HEX F0
F9       EQU   X'F9'              USED IN SCAN SUBROUTINE
COMMA    EQU   C','                   USED IN SCAN SUBROUTINE
POUND    EQU   C'#'               ID S OF NEW OLTS
         EJECT
GEN00000 SAVE  (14,12),,IFDOLT99&SYSDATE SAVE MOD NAME AND DATE.
         BALR  BASEREG,ZERO       ESTABLISH ADDRESSABILITY
         USING *,BASEREG
         USING IHADCB,DCBREG      SET REG FOR DCB REFERENCES
         USING INFMJFCB,JFCBREG   USED TO FIND JFCB STUFF
         ST    SAVEREG,SAVEAREA+4 SAVE ADDRESS OF CALLERS SAVE AREA
*                                 IN MY SAVE AREA
         LA    EP,SAVEAREA        GET ADDRESS OF MY SAVE AREA AND
         ST    EP,8(0,SAVEREG)    STORE IN CALLERS SAVE AREA
         LR    SAVEREG,EP         LOAD MY SAVE REG WITH ADDRESS
*                                 OF MY SAVE AREA
         EJECT
GEN00100 EQU   *                  DATA SET VERIFICATION SECTION
         LA    JFCBREG,JFCBAREA   SET PTR TO JFCB AREA
         LA    BCTREG,2           SET BCT LOOP COUNT TO TWO
         SPACE 2
GEN00110 EQU   *                  READ THE JFCB INTO WORKAREA
         RDJFCB  (OUTDASD,(OUTPUT))   READ THE JFCB
         SPACE 2
         CLC   JFCBDSNM(13),LINKLIB    IS THIS AN INVALID DATA SET
         BE    GEN00120           YES, GO END THE JOB WITH MSG 909I
         CLC   JFCBDSNM(12),SVCLIB     IS THIS AN INVALID DATA SET
         BE    GEN00120           YES, GO END THE JOB WITH MSG 909I
         CLC   JFCBDSNM(12),LPALIB     IS THIS AN INVALID DATA SET
         BE    GEN00120           YES, GO END THE JOB WITH MSG 909I
         LA    DCBREG,OUTDASD     SET PTR TO DATA SET CB
         MVC   DCBDDNAM,SYMCDSDD  MOVE IN DD NAME FOR SECOND PASS
         BCT   BCTREG,GEN00110    FALL THRU OR GO TO NEXT PASS
         MVC   DCBDDNAM,OUTDADID  MOVE IN DD NAME FOR OPEN
         B     GEN00130           GO OPEN THE DATA SETS
         SPACE  3
GEN00120 EQU   *                  ENTER HERE IF DATA SET IS INVALID
         SPACE 2
         OPEN  (PRINTER,(OUTPUT)) OPEN PRINT DCB
         SPACE 2
         LA    DCBREG,PRINTER     GET THE DCB ADDR FOR OPEN CHECK
         BAL   LINK,ABORTCK       GO CHECK IF DCB OPENED SUCCESSFULLY
         MVC   BADLIB+D10(12),JFCBDSNM MOVE IN DS NAME
         LA    MSGADDR,BADLIB     GET MESSAGE POINTER
         LA    LINK,GEN05000      GET END OF JOB RTN ADDRESS
         LA    LINECNT,1          SET COUNT TO SKIP A PAGE
         B     GEN04000           GO ISSUE MESSAGE AND END THE JOB
GEN00130 EQU   *                  GO OPEN THE DCBS
         EJECT
         OPEN  (INCARD,INPUT,OUTDASD,(OUTPUT),PRINTER,(OUTPUT))
         SPACE 3
GEN00200 EQU   *                  START LOOP THRU DCBS
         MVI   SWITCHS,HEX00      RESET ALL SWITCHES BEFORE INITIAL
         MVI   SWITCH2,X'00'      ZERO OUT SWITCH
         MVI   SWITCH3,HEX00      ZERO OUT ALL SWITCHES
         LA    DCBREG,PRINTER     GET ADDRESS OF DCB TO BE CHECKED
         BAL   LINK,ABORTCK       GO CHECK DCB OPEN FLAGS AND RETURN
         LA    DCBREG,INCARD      GET ADDRESS OF DCB TO BE TESTED
         BAL   LINK,ABORTCK       GO CHECK DCB OPEN FLAGS AND RETURN
         LA    DCBREG,OUTDASD     GET ADDRESS OF DCB TO BE TESTED
         BAL   LINK,ABORTCK       GO CHECK DCB OPEN FLAGS AND RETURN
*                                 SCAN UNTIL FALL THRU.
*                                 READ
         B     GEN00300           BRANCH TO GET A CARD ROUTINE
         SPACE 5
ABORTCK  TM    DCBOFLGS,OPEN      DID THIS DCB OPEN SUCCESSFULLY
         BNO   GEN99990           GO TO ABORT ROUTINE
         BR    LINK               RETURN TO NEXT DCB ADDRESS CHK
          EJECT
GEN00300 EQU   *                  BEGIN INITAL PROCESSING
         NI    SWITCHS,LASTPASS   RESET ALL SWITCHES OTHER THAN
*                                 LAST PASS INDICATOR
         XC    TXTADR,TXTADR      RESET TXT AREA PTR TO ZEROS
         LA    TXTPTR,TXTAREA     GET POINTER TO TXT BUFFER
         MVC   SEQSAVE,EBCD0001   SET SEQ CTR TO ONE
         SPACE 5
GEN00400 EQU   *                  GET A RECORD FROM THE INPUT (80 BYTE)
*                                 FILE AND HAVE PUT INTO OLTEP
         GET   INCARD,READAREA    WORKAREA FOR EXPLINATION.
         TM    SWITCH3,FIRSTCD    IS THIS THE FIRST CARD
         BO    GEN00410           NO, BRANCH AROUND MESSAGE
         OI    SWITCH3,FIRSTCD    TURN ON FIRST CARD READ SWITCH
         CLC   READAREA(12),SYMCDSID IS THIS A CONTROL CARD
         BE    GEN00410           SECOND FILE. YES SKIP MESSAGE
         LA    LINECNT,1          SET CNT TO SKIP A PAGE
         LA    MSGADDR,LOCALOLT   GET MESSAGE POINTER
         BAL   LINK,GEN04000      GO ISSUE MESSAGE AND RETURN
         SPACE 1
GEN00410 EQU   *                  CONTINUE PROCESSING
         BAL   LINK,GEN00420      GO CHECK THE SEQUENSE NUMBERS
         B     GEN00450           GO CONTINUE PROCESSING IN FILE
         EJECT
GEN00420 EQU   *                  THIS ROUTINE EXAMINES THE ORDER OF
*                                 THINGS.  THE SEQ NO. ARE CHECKED
*                                 TO INSURE STRICT ASCENDING ORDER MIT
*                                 OUT NO GAPS...
*                                 THE FOLLOWING CARDS ARE NOT CHECKED
*                                    1. SETSSI CARDS
*                                    2. NAME CARDS
*                                    3. REP CARDS
*                                    4. SYMBOLIC CDS CONTROL CARDS
*
         SPACE 5
         CLC   READAREA(4),REPID  IS THIS A REP CARD
         BE    GEN00440           YES, GO HANDLE REP CARD
         CLI   READAREA,HEX02     IS THIS AN ASSEMBLER OUTPUT CARD
         BE    GEN00422            YES, GO CHECK SEQ NUMBER
         LA    BCTREG,72          NO, SCAN CARD FOR CONTROL CARD IDS
         LA    SCANREG,READAREA   SET UP POINTER TO CARD AREA
         SPACE  2
GEN00421 EQU   *                  SCAN RECORD FOR CONTROL CARD ISD
         CLC   D0(12,SCANREG),SYMCDSID IS THIS A SYMBOLIC CDS
         BE    GEN02030           CONTROL CARD.  YES, GO HANDLE
         CLC   D0(L7,SCANREG),SETSSI IS THIS A SETSSI CONTROL CARD
         BE    GEN00400           YES, GO GET NEXT CARD
         CLC   D0(5,SCANREG),NAMEID IS THIS A NAME CARD
         BE    GEN00400           YES, GO GET NEXT CARD
         LA    SCANREG,D1(SCANREG) BUMP READ AREA POINTER
         BCT   BCTREG,GEN00421    CONTINUE SCANNING TIL ZERO
         B     GEN00500             BRANCH OUT IF RECORD IS NEITHER
*                                 A CONTROL CARD  NOR A ASSEMBLER
*                                 OUTPUT RECORD
GEN00422 CLC   READAREA+D76(4),SEQSAVE DOES RECVD EQUAL EXPECTED
         BNE   GEN00430           NO, GO TO ERROR ROUTINE
         LA    BCTREG,FOUR        SET LOOP CNT TO FOUR
GEN00423 LA    ADDER,SEQSAVE-1(BCTREG) SET TARGET  ADDRESS
         TR    D0(1,ADDER),TRTAB-240 TRNASLATE IT
         CLI   D0(ADDER),C'0'     DID IT GO TO ZERO
         BNE   GEN00424           NO, EXIT SUBROUTINE
         BCT   BCTREG,GEN00423    YES, CONTINUE LOOP
GEN00424 BR    LINK               EXIT SRQUENSE CHECKER
         EJECT
GEN00430 EQU   *                  THIS ROUTINE PUTS OUT MESSAGE
*                                 IFD904I TO IDENTIFY AN ERROR OCCURED
*                                 THE MESSAGE WILL CONTAIN BOTH THE
*                                 EXPECTED AND RECIEVED SEQUENSE NO.
*                                 THIS ROUTINE WILL ALSO SKIP THE REM
*                                 AINDER OF THE MODULE CONTAINING
*                                 THE SEQUENSE ERROR
         LA    MSGADDR,SEQERROR   GET ADDRESS OF MESSAGE TO BE PRINTED
         MVC   SEQERROR+D24(4),SEQSAVE MOVE IN EXPECTED SEQ NO.
         MVC   SEQERROR+D34(4),READAREA+D76 MOVE IN RECD SEQ NUMBER
         MVC   SEQERROR+D46(8),NAMESAVE MOVE IN MODULE NAME
         XC    NAMESAVE,NAMESAVE  ZAP NAME FIELD CLEAN
         BAL   LINK,GEN04000      GO PUT OUT THE MESSAGE AND RTN
         TM    SWITCH2,POINTOK    IS IT OK TO ISSUE A POINT MACRO
         BNO   GEN00432           NO, BYPASS ISSUING POINT MACRO
         POINT OUTDASD,TTSAVE     ISSUE POINT TO SAVE DASD SPACE
         B     GEN00432           GO CK CARD IN BUFFER
         EJECT
GEN00431 EQU   *                  LOOP THRU MODULE
         GET   INCARD,READAREA    LOOP TO END OF THIS MODULE
GEN00432 EQU   *         CHECK CARD JUST READ IN BUFFER
         CLC   READAREA(4),ESDID  IS THIS AN ESD CARD
         BNE   GEN00431           NO GO GET NEXT CARD
         CLC   READAREA+D14(2),HEX0001 IS THIS THE FIRST ESD OF NEXT
*                                 MODULE
         BNE   GEN00431           NO, SKIP CARD
         MVC   SEQSAVE,EBCD0002   YES PRIME SEQ CKR
         B     GEN00700           GO PROCESS ESD CARD
         EJECT
GEN00440 EQU   *                  REP CARD SCANNER
         TM    SWITCH3,RLDREAD    HAS AN RLD BEEN READ YET.
         BO    GEN01500           YES, GO HANDLE ORDER ERROR
         OI    SWITCH3,REPREAD    SET SWITCH INDICATING REP CARD READ
         SPACE 1
         LA    SLOT,READAREA+D6   GET ADDRESS OF FIELD IN REP CARD
         LA    SCANCNT,SIX        SET COUNT TO NO. OF BYTES TO BE CHKED
         BAL   LINK,GEN00442      GO SCAN ADDR PORTION OF REP CARD
         SPACE 1
         CLC   READAREA+D4(2),BLANKCOL CHECK FIELD FO TWO BLANKS
         BNE   GEN00446           IF NON-BLANK, TAKE BRANCH
         CLC   READAREA+D12(4),BLANKCOL   CHECK FOR FOUR BLKS
         BNE   GEN00446           IF NON-BLANK, TAKE BRANCH
         SPACE 1
         LA    SLOT,READAREA+D16  SET PTR TO FIRST REP CHARACTER
GEN00441 LA    SCANCNT,FOUR       SET LOOP COUNT TO NO. OF BYTES
         BAL   LINK,GEN00442      GO SCAN FIELD AND RETURN IF OK
         B     GEN00444           GO CHECK FOR ENDING CONDITIONS
         SPACE 3
GEN00442 EQU   *                  SCANNER PORTION OF RTN
         CLI   D0(SLOT),F9        IS CHAR POINTED TO BY SLOT G.T. A
         BH    GEN00446           HEX F9.  BRANCH IF YES
         SPACE 1
         CLI   D0(SLOT),C1        IS CHAR LESS THAN A HEX C1
         BL    GEN00446           IF SO BRANCH OUT
         SPACE 1
         CLI   D0(SLOT),C6        IS CHAR BETWEEW A C1 AND C6
         BNH   GEN00443           IF LESS OR EQUAL ALL IS OK
         SPACE 1
         CLI   D0(SLOT),F0        IF CHAR IS G. T. C6 THEN IS IT
*                                 BETWEEN F0 AND F9
         BL    GEN00446           BRANCH IS ILLEGAL
         SPACE 1
GEN00443 EQU   *
         LA    SLOT,D1(SLOT)      BUMP COUNT TO NEXT CHAR
         BCT   SCANCNT,GEN00442   GO CHECK NEXT CHAR OR FALL THRU
         BR    LINK               BRANCH IS CHARACTER IS AOK
         EJECT
GEN00444 EQU   *                 END CONITION CHKER
*                                 ON ENTRY, SLOT WILL POINT TO NEXT
*                                 CHARACTER TO BE CHECKED, THAT IS ,
*                                 A RUDDY BLANK, OR A COMMA.  A COMMA
*                                 WILL CAUSE THE SCAN TO CONTINUE
*                                 A BLANK WILL TERMINATE THE SCAN.
*                                 ANYTHING ELSE IS CONSIDERED AN ERROR
         CLI   D0(SLOT),COMMA     IS THIS A COMA
         BE    GEN00445           BRANCH IF YES
         CLI   D0(SLOT),BLANK     IS THIS A BLANK COL INDIC. END
         BE    GEN01000           IF YES BRANCH OUT OF SCANNER
         B     GEN00446           NO, ERROR EXIT FROM SCANNER
         SPACE 1
GEN00445 EQU   *                  BUMP AND GO
         LA    SLOT,D1(SLOT)      ADD ONE TO POINTER AND
         B     GEN00441           GO HANDLE NEXT REP AREA
         SPACE 1
GEN00446 EQU   *                  ENTER HERE ON A REP CARD ERROR
         TM    SWITCH2,POINTOK    IS IT OK TO ISSUE POINT MACRO
         BNO   GEN00447           NO, BYPASS POINT MACRO
         SPACE  3
         POINT OUTDASD,TTSAVE     SST TTRZ BACK TO ESD 0001 TIME
         SPACE   3
GEN00447 EQU   *                  ENTER HERE ON NO POINT SITUATIONS
         LA    MSGADDR,IGNORECD   SET ADDRESS OF MESSAGE
         MVC   IGNORECD+D17(8),NAMESAVE GET MOD NAME BEING JUNKED
         MVC   IGNORECD+D36(80),READAREA MOVE IN ERRORED CARD FOR MSG
         XC    NAMESAVE,NAMESAVE  ZAP NAME TO NON PRINTABLES
         BAL   LINK,GEN04000      GO PRINT OUT ERROR MESSAGE
         B     GEN00431           GO SCAN TO NEXT VALID ENTRY
*                                 POINT, THAT IS, AN ESD CARD WITH A
*                                 001 IN COLUMNS
         EJECT
GEN00450 EQU   *                  CONTINUE PROCESSING INFILE
         CLC   READAREA(FOUR),ESDID IS THIS AN ESD CARD
         BE    GEN00700           YES,GO CHECK FOR FIRST ESD READ
         SPACE 3
         CLC   READAREA(FOUR),TXTID IS THIS A TEXT CARD
         BE    GEN00800           YES, GO TO TXT CARD HANDLER
         SPACE 3
         CLC   READAREA(FOUR),RLDID IS THIS AN RLD CARD
         BNE   GEN00455           NO, CONTINUE PROCESSING.
         OI    SWITCH3,RLDREAD    SET RLDREAD INDICATOR
         B     GEN01000           GO WRITE RECORD
         SPACE 3
GEN00455 EQU   *                  IS THIS AN END STOW SITUATION
         CLC   READAREA(FOUR),ENDID END CARD IN BUFFER.
         BNE   GEN00500           NO, CHECK FOR SYMBOLIC CDS CARD
         SPACE 3
         OI    SWITCHS,NEEDNAME   SET SWITCH FORCEING NEXT CARD
*                                 TO BE A NAME CARD
         B     GEN01000           GO PROCESS THIS END CARD
         SPACE 3
         EJECT
GEN00500 EQU   *                  ENTER HERE WHEN AN UNEXPECTED
*                                 CARD WAS RECIEVED. THIS ROUTINE
*                                 WILL OUTPUT MSG IFD900I, FLUSH
*                                 THE CARD AND CONTINUE PROCESSING.
         LA    MSGADDR,BADCARD    GET ADDRESS OF MESSAGE
         MVC   D34(80,MSGADDR),READAREA MOVE CARD TO MESSAGE
         BAL   LINK,GEN04000      GO PRINT MESSAGE AND RETURN
         B     GEN00400           GO GET NEXT CARD
         EJECT
         SPACE 5
GEN00700 EQU   *                  ESD CHECK RTN.
         CLC   READAREA+D14(TWO),HEX0001 IS THIS THE
*                                 FIRST ESD CARD
         BNE   GEN01000           NO, GO HANDLE NORMALLY, WRITE CD.
GEN00710 EQU   *                  ENTER HERE WHEN NO STOW REQUIRED
         NI    SWITCH2,ALL-POINTOK          MODULES SWITCHS
         NI    SWITCH3,ALL-REPREAD-RLDREAD FOR THIS MODULE
         OI    SWITCHS,SAVETTRZ   YES, SET SW TO SAVE TTRZ
         MVI   STOWOK+45,BLANK         OLT RUNS IN REAL          XM3316
         CLI   READAREA+D32,POUND IS THIS A NEW OLT
         BE    GEN00720           YES, GO GET NAME TO STOW
         CLI   READAREA+D21,HEXF0 IS THIS A NO. (NEWOLT)
         BL    GEN00720           YES, GO GET NAME TO BE STOWED
         MVC   NAMESAVE,READAREA+D32 MOVE NAME TO BE STOWED
         B     GEN00730           GO CHECK FOR INVALID NAME
         SPACE   3
GEN00720 EQU   *                  ENTER HERE FOR NEW OLTS ONLY
         MVC   NAMESAVE,READAREA+D16 MOVE NAME TO BE STOWED
         MVC   USERINFO(1),READAREA+38  MOVE INTO DIRECTORY THE
*                                 INFO USED AT LOAD TIME TO DETERMINE
*                                 THE V=V OR V=R CAPABILITY
         TM    USERINFO,VIRTUAL        CAN OLT RUN IN VIRTUAL    XM3316
         BNO   GEN00730                NO, CONTINUE              XM3316
         MVI   STOWOK+45,LETTERV       YES, INDICATE IN MSG THAT XM3316
*                                      OLT CAN RUN IN VIRTUAL    XM3316
GEN00730 EQU   *                  OK NAME TO BE STORED.
         CLI   NAMESAVE,BLANK     IS THIS A BLANK
         BNE   GEN01000  CONTINUE PROCESSING - WRITE RECORD
         LA    MSGADDR,BADCARD   GET MSG POINTER
         MVC   D34(80,MSGADDR),READAREA MOVE CARD TO BUFFER
         BAL   LINK,GEN04000      ISSUE MESSAGE
         B     GEN00431           GO SCAN TO NEXT ESD CARD.
         EJECT
GEN00800 EQU   *                  TEXT CARD ROUTINE
         TM    SWITCH3,REPREAD+RLDREAD    HAS A REP OR RLD BEEN READ
         BNZ   GEN01500           IF EITHER IS ON AN ERROR IS HERE
         CLC   READAREA+D10(L2),HEX0000 IS THE COUNT OF TXT BYTES
*                                 ZERO
         BE    GEN00400           YES, GO GET NEXT CARD
*                                 THE DATA (TXT) COUNT IS NOT ZERO
         CL    TXTPTR,TXTADSV     IS THIS FIRST TXT CARD
         BE    GEN00820           YES, GO SETUP TXT HEADER
         CLC   TXTADR+D1(THREE),READAREA+D5 SHOULD THIS CARD GO
*                                 INTO OUTDASD BUFFER
         BNE   GEN00830           IF A HOLE OR NEG REF, GO WRITE REVORD
         SPACE 2
         CLC   TEXTID(2),READAREA+D14 ARE THE TEXT ID S THE SAME
         BNE   GEN00830           NO, GO WRITE TEXT HRD AND BUFFER
         SPACE 2
GEN00810 EQU   *                  TEXT BUFFER MANIPULATOR SECTION
         MVC   TEXTID(2),READAREA+D14  SAVE THE TEXT ID FIELD   XM1330
         LH    ACCUMRG,READAREA+D10  GET DATA CNT
         LR    WORKRG1,ACCUMRG    SAVE IT
         AH    ACCUMRG,DATACNT+1
         CL    ACCUMRG,SIZETXT     SHOULD I WRITE IT
         BH    GEN00830           YES, GO WRITE
GEN00815 EQU   *                  ENTER HERE FROM HOLE FILLER
         STH   ACCUMRG,DATACNT+1  GET ACCUMULATED USED BUFFER COUNT
         BCTR  WORKREG1,ZERO      SUBTRACT ONE FOR EX MOVE
         EX    WORKREG1,DATAMVC   EX MVC DATA INTO BUFFER
         LA    TXTPTR,D1(WORKREG1,TXTPTR) BUMP BUFFER POINTER
*                                 TO NEXT AVAILABLE POINTER
         MVC   TXTADR+D1(L3),READAREA+D5  GET RELATIVE ASSEMBLED
*                                 ADDRESS FOR THIS CARD.
         L     ACCUMRG,TXTADR     OBTAIN BEGINNING REL. TXT ASSMBLED
*                                 ADDRESS
         LA    ACCUMRG,D1(ACCUMRG,WORKREG1) BUMP ASSM ADDR TO WHERE
*                                 THE NEXT TXT BYTE SHOULD OCCUR
         ST    ACCUMRG,TXTADR     SAVE THIS FOR NEXT RECORD PLACEMENT
         B     GEN00400           GO GET NEXT RECORD
         SPACE 5
GEN00820 EQU   *                  CLEAN-UP COUNTS FIRST TXT CD RTN
         XC    DATACNT,DATACNT    RESET THE COUNT
         MVC   DATAADRS,READAREA+D5 SET ADDR OF FIRST BYTE OF TXT
*                                 DATA IN TXT HEADER CARD
         B     GEN00810           GO PROCESS CARD
         SPACE 5
GEN00830 EQU   *                  HEADER WRITE RTN
         BAL   LINK,GEN00900      GO WRITE BOTH TXT HEADER
*                                 AND TXT INFORMATION BECAUSE CARD
         XC    DATACNT,DATACNT     SET DATA COUNT IN TXTHDR REC TO ZIP
         MVC   DATAADRS,READAREA+D5 SET NEW ADDRESS             XM1330
         B     GEN00810           AND THEN GO HANDLE CARD
         EJECT
GEN00900 EQU   *                  PREPARE TO WRITE A TXT HDR RECORD
         LA    WORKRG1,HEX50      GET REC SIZE FOR WRITE
         STH WORKRG1,GENDECB+D6   PUT SIZE INTO MACRO
         LA    WORKRG1,TXTHDR     GET ADDRESS OF AREA TOBE WRITTEN
         MVC   TXTHDR+D25(2),TEXTID PLACE TEXT ID  INTO TXTHRD
         ST    WORKRG1,GENDECB+D12 STORE ADDRESS INTO WRITE MACRO
         BAL   WRTLINK,GEN00920   GO WRITE TEXT HEADER RECORD ON DASD
         SPACE 5
         MVC   GENDECB+D6(2),DATACNT+1  GET SIZE OF TXT REC TO BE
*                                 WRITTEN
         MVC   GENDECB+D12(4),TXTADSV MOVE ADDRESS TO MACRO
GEN00910 EQU   *                  RESET POINTER
         LA    TXTPTR,TXTAREA     SET POINTER TO BEGINNING OF TEXT
*                                 OUTPUT BUFFER.
         XC    TXTADR,TXTADR      ZERO OUT ADDR OF NEXT AVAIL
*                                 BYTE IN BUFFER AREA
         BAL   WRTLINK,GEN00920   GO WRITE RECORD ON DASD
         BR    LINK               CONTINUE PROCESSING RECORD
         SPACE  5
GEN00920 EQU   *                  IO ROUTINE
         WRITE GENDECB,SF,OUTDASD,0,0  CAUSE CD TO WRITE RECORD
*                                 ONTO OUTPUT DADS FILE.
         CHECK GENDECB            WAIT FOR COMPLETION OF I/O OPERATION
         OI    SWITCH2,POINTOK    SET SW TO ALLOW A POINT
         BR    WRTLINK            RETURN ON BALR TO CALLER
         SPACE 5
GEN01000 EQU   *                  HANDLE ESD CARD OTH¡ THAN FIRST
*                                 HANDLE END CARD
*                                 HANDLE RLD CARD
*                                 HANDLE REP CARD
         CL    TXTPTR,TXTADSV     WAS THERE ANY INFORMATION TO BE
*                                 WRITTEN BEFORE THIS CARD.
         BE    GEN01010           NO, BYPASS WRITE
         BAL   LINK,GEN00900      YES, GO WRITE OUT TXT ACCUMULATED
         SPACE 3
         SPACE 2
GEN01010 EQU   *                  PREPARE TO WRITE RECORD OTHER
*                                 THEN TXT RECORD
         LA    WORKRG1,READAREA   GET ADDRESS OF AREA TO BE WRITTEN
         ST    WORKREG1,GENDECB+D12    MOVE ADDRESS TO MACRO
         LA    WORKRG1,HEX50      GET COUNT OF DATA TO BE WRITTEN
         STH   WORKRG1,GENDECB+D6 MOVE COUNT TO MACRO
         BAL   LINK,GEN00910      GO WRITE RECORD
         TM    SWITCHS,NEEDNAME   CAN A STOW BE DONE
         BO    GEN03000           YES GO STOW MODULE TO DIRECTORY
         TM    SWITCHS,SAVETTRZ   SHOULD A NOTE BE ISSUED
         BNO   GEN01011           NOT A ESD 001 NO NOTE NEEDED
         NOTE  OUTDASD            SET FOR POINT IF ERROR SHOWS
         ST    TTREG1,TTSAVE      SAVE TTRZ OF NEXT WRITE
         NI    SWITCHS,ALL-SAVETTRZ  SET SW OFF FOR NEXT READ
GEN01011 EQU   *                  ENTER HERE IF NO NOTE REQ.
         B     GEN00400           GO GET NEXT INPUT RECORD
         SPACE 2
GEN01500 EQU   *                  THIS ROUTINE IS ENTERED WHEN A
*                                 CARD WAS OUT OF ORDER.
         LA    MSGADDR,ORDERERR   GET MSG POINTER
         MVC   ORDERERR+D21(8),NAMESAVE MOVE NAME TO MESSAGE
         MVC   ORDERERR+D39(80),READAREA MOVE BAD CARD TO MESSAGE .
         BAL   LINK,GEN04000      GO ISSUE MESSAGE AND RETURN
         B     GEN00431           GO TO NEXT MODULE FINDER
         EJECT
GEN02030 EQU   *                  START CDS FILE PROCESSING
GEN02031 EQU   *
         CLOSE OUTDASD            CLOSE OLT FILE
         SPACE   2
         LA    DCBREG,OUTDASD     GET ADDR OF DCB FOR OPEN CHK AND
         MVC   DCBDDNAM,SYMCDSDD  MOVE IN DD NAME FOR SECOND FILE
*                                 PRIOR TO OPENING
         OI    SWITCHS,LASTPASS        SET LAST PASS SW          X03008
         OPEN  (OUTDASD,(OUTPUT)) OPEN NEW DATA SET FOR ODS
         TM    DCBOFLGS,OPEN      DID NEW DATA SET OPEN
         BNO   GEN99990           GO TO ABORT ROUTINE
         OI    SWITCHS,LASTPASS   SET LAST PASS SWITCH
         LA    MSGADDR,SYMDATAS   GET MESSAGE ADDRESS
         LA    LINECNT,1          SET NEW PAGE INDICATOR
         BAL   LINK,GEN04000      GO PRINT OUT THE MESSAGE
         MVI   PRTBUFF,HEX03      SET MACHINE CODE TO SKIP A LINE
         XC    PRTBUFF+D1(L'PRTBUFF-1),PRTBUFF+D1 CLEAR OUT BUFFER
         PUT   PRINTER,PRTBUFF
         MVC   SEQSAVE,EBCD0001   RESET SEQ COUNTER TO FIRST CARD
         NI    SWITCH2,POINTOK    TURN OFF POINT OK SW
         NI    SWITCHS,ALL-SAVETTRZ-NEEDNAME TURN OFF TTRZ AND STOW SW
         B     GEN00400           GO PROCESS THE RUDDY FILE
         SPACE 2
         SPACE 3
         EJECT
GEN03000 EQU   *                  UPDATE DIRECTORY
         MVC   STOWNAME,NAMESAVE                         STOW  NAME
         NI    SWITCHS,ALL-NEEDNAME  TURN OFF NAME REQUIREMENT
         STOW  OUTDASD,STOWCON,R  STOW NAME INTO DIRECTORY ON DASD
         LTR   STOWRC,STOWRC      DID STOW TAKE
         LA    MSGADDR,STOWOK     GET MSG ADDRESS FOR SUCCESS
         MVC   STOWOK+36(9),REPCON   MOVE IN REPLACED CONSTANT
         BZ    GEN03010           YES, PRINT OUT MSG.
         LA    WORKRG1,8          GET POSSIBLE RETURN CODE
         CR    WORKRG1,STOWRC       WAS MODULE REPLACED
         BNE   GEN03005           ERROR MSG
         MVC   STOWOK+36(9),ADDCON   MOVE IN ADDED CONSTANT
         LA    MSGADDR,STOWOK     GET MSG ADDRESS FOR SUCCESS
         B     GEN03010           YES, PRINT OUT MSG.
GEN03005 EQU   *                  ERROR
         OI    SWITCH2,ENDJOB     SET BAIL OUT SWITCH
         LA    WORKRG1,D4(WORKRG1)  BUMP REG TO X'0C'
         CR    WORKREG1,STOWRC    IS RETURN CODE A X'0C'
         BE    GEN03006           YES GO HANDLE FULL DIRECTORY COND
         LA    MSGADDR,PERMIOER   GET MESSAGE ADDRESS
         B     GEN03010           GO OUT THE MSG NAME
GEN03006 EQU   *                  HANDLE STOW ERRORS
         LA    MSGADDR,DIRTFULL   SET UP MESSAGE INDICATING ERROR
         SPACE   2
GEN03010 EQU   *                  FAILURE
         MVC   D12(L8,MSGADDR),STOWNAME MOVE NAME OF MODULE
         XC    NAMESAVE,NAMESAVE  CLEAR NAME TO ZEROS
         NI    SWITCH2,ALL-POINTOK  TURN OFF SWITCH ALLOWING POINT
         BAL   LINK,GEN04000      INTO OUT-PUT MSG AND GO
*                                 PRINT IT OUT
         TM    SWITCH2,ENDJOB     IS THIS JOB DONE
         BO    GEN05000           YES, PACK IT IN
         MVC   SEQSAVE,EBCD0001   RESET THE SEQ COUNT TO ONE
         B     GEN00400           GO GET NEXT RECORD
         EJECT
GEN04000 EQU   *                  SET UP FOR LINE PRINTING
         MVI   PRTBUFF+D1,BLANK   BEGIN CLEARING BUFFER TO BLANKS
         MVC   PRTBUFF+D2(L118),PRTBUFF+D1 CLEAR REMAINDER OF BUFFER
         MVI   PRTBUFF,RESTORE    SET BYTE FOR NEW PAGE
         BCT   LINECNT,GEN04010   DECREMENT UNTIL ZIP, THEN
         PUT   PRINTER,PRTBUFF    SKIP TO NEXT PAGE
*                                 GET NEW PAGE
         LA    LINECNT,HEX38      SET COUNT TO 56 LINES PER
*                                 PAGE
GEN04010 EQU   *                  SET CARRIAGE CONTROL
         MVI   PRTBUFF,WRTSP1     TO WRITE AND SKIP ONE LINE
GEN04020 EQU   *                  SET UP MSG TO BE PRINTED
         IC    MSGSIZRG,D0(MSGADDR)  GET SIZE OF MESSAGE
         BCTR  MSGSIZRG,ZERO        DECREMENT OF MSG BY  1
*                                 FOR EX MVC
         EX    MSGSIZRG,MVCPRTBF  MOVE MSG INTO PRINT BUFFER VIA EX
         XR    MSGSIZRG,MSGSIZRG  ZERO OUT MSG LNG REG
         PUT   PRINTER,PRTBUFF    WRITE A LINE
         BR    LINK               RETURN ON BALR
         SPACE 3
         EJECT
GEN05000 EQU   *                  EODAD ROUTINE
         CLOSE (OUTDASD,,PRINTER,,INCARD) PACK IT IN
         L     SAVEREG,D4(SAVEREG) EXIT LINKAGE
         SPACE 3
         RETURN (14,12),T,RC=0    EXIT MODULE
         EJECT
GEN06000 EQU   *                  END OF FILE ROUTINE
*                                 THIS ROUTINE GET CTL ON END OF DATA
*                                 FOR CARD INPUT ONCE AND TWICE FOR
*                                 TAPE INPUT. FOR CARD INPUT THE RUN
*                                 IS COMPLETE
*                                 FOR TAPE A READ JFCB IS ISSUED TO
*                                 BUMP THE FILE SEQ NO. AND AN OPEN
*                                 TYPE=J IS ISSUES TO THE TAPE.
*                                 TE
*                                 THE TAPE WAS 'CLOSED' VIA CLOSE WITH
*                                 A LEAVE PARAMETER
*                                 AFTER THE SECOND EOD RTN IS ENTERED
*                                 THE RUN IS COMPLETE
         TM    SWITCHS,LASTPASS   IF THE SWITCH IS ON
         BO    GEN05000           GO TO END THE JOB AS FILE IS EMPTY
         LA    WORKRG1,INCARD     GET ADDRESS IF DCB FOR INPUT FILE
         MVC   ANSWER(1),D17(WORKRG1) GET DEV TYPE
         NI    ANSWER,ALL-3       ZAP OUT UNUSED BITS           OX01911
         CLI   ANSWER,HEX80       IS INPUT DEVICE A TAPE
         BNE   GEN05000           NO, GO TO END THE JOB
         SPACE  4
         CLOSE  (INCARD,LEAVE)    CLOSE THE RUDDY FILE
         SPACE 2
         RDJFCB (INCARD,INPUT)    READ JOB FILE CTL BLK AND ALTER IT
         SPACE 2
         LA    JFCBREG,JFCBAREA   GET POINTER TO JFCB
         LH    COMMON,JFCBFLSQ    PICK UP THE FILE SEQUENSE NUMBER
         LA    COMMON,D1(COMMON)  AND ADD ONE TO IT FOR
         STH   COMMON,JFCBFLSQ    THE SECOND FILE PROCESS
         MVI    EOV,HEX00         TURN OFF TAPE CTR ROUTINE    @ZA08990
         L      COMMON,#TAPES     GET NUMBER OF TAPES IN FILE  @ZA08990
         STH    COMMON,JFCBVLSQ   PUT IT INTO JFCB             @ZA08990
         SPACE 1
         SPACE  4
         OPEN  (INCARD,INPUT),TYPE=J    OPEN SECOND FILE
         OI    SWITCHS,LASTPASS   SET END INDICATOR FOR NEXT PASS
         SPACE 4
         LA    DCBREG,INCARD      GET PTR TO DCB
         TM    DCBOFLGS,OPEN      DID THE FILE OPEN
         BNO   GEN99990           NO, PACK IN THE JOB
         B     GEN02031           GO, PROCESS SECOND FILE
GEN07000  EQU    *                                             @ZA08990
*                                 THIS ROUTINE GETS CONTROL    @ZA08990
*                                 AT EACH END OF VOLUME IN     @ZA08990
*                                 FILE 1 OF AN INPUT TAPE. IT  @ZA08990
*                                 COUNTS THE NUMBER OF TAPES IN
*                                 FILE 1. THIS COUNT IS USED BY
*                                 THE END OF FILE ROUTINE TO   @ZA08990
*                                 UPDATE THE JFCB SO THAT THE  @ZA08990
*                                 CORRECT TAPE IS USED TO      @ZA08990
*                                 OPEN FILE 2.                 @ZA08990
         L     COMMON,#TAPES      GET # OF TAPES SO FAR        @ZA08990
         LA    COMMON,D1(,COMMON) INCREMENT IT                 @ZA08990
         ST    COMMON,#TAPES      SAVE FOR EOF ROUTINE         @ZA08990
         BR    14                 TO PROCESS NEXT TAPE         @ZA08990
         EJECT
GEN99990 EQU   *                  THIS ROUTINE GETS CONTROL WHEN
*                                 A DCB FAILS TO OPEN, IF THE DCB
*                                 WHICH FAILS TO OPEN IS THE PTINT
*                                 DCB, A 'WTO' MACR IS ISSUED TO
*                                 THE OPERATORS CONSOLE. IF THE
*                                 DCB IS OTHER THAN TO PRINT DCB
*                                 MESSAGE 'IFD901I' IS ISSUED
*                                 TO THE PRINTER WITH THE DD NAME
*                                 CORRESPONDING TO THE FAILED DCB
*                                 IN THE MESSAGE.
         LA    LINK,GEN05000      SET RETURN REGISTER
         LA    COMMON,PRINTER     GET ADDRESS OF PRINTDD DCB
         CR    DCBREG,COMMON      IS THIS FAILING DCB
         BNE   GEN99991           DCB.  NO, GO CHK INPUT DCB
         SPACE  2
         WTO   ' IFD901I    DIAGMSG DDCARD FAILED TO OPEN' ISSUE MSG
         B     GEN05000           GO END THE JOB
         SPACE   2
GEN99991 EQU   *                  CHECK INPUT DD DCB
         LA    COMMON,INCARD      GET ADDR OF INCARD DCB
         CR    DCBREG,COMMON      IS THIS FAILING DCB
         LA    MSGADDR,FAILDCB    GET ADDRESS OF MESSADGE       X03008
         BNE   GEN99992           NO, GO CHECK OUTDASD DCB
         MVC   D12(L8,MSGADDR),INPUTID MOVE IN DD NAME
         B     GEN04000           GO ISSUE MESSAGE
         SPACE   2
GEN99992 EQU   *                  CHECK FOR CORRECT DD NAME
         TM    SWITCHS,LASTPASS   IS THIS THE SYMBOLIC DCB
         BNO   GEN99993           NO, USE OUTDADA1 AS DDNAME
         MVC   D12(L8,MSGADDR),SYMCDSDD MOCE IN DDNAME
         B     GEN04000           GO PUT OUT MESSAGE
         SPACE  2
GEN99993 EQU   *                  MOVE IN DD NAME
         MVC   D12(L8,MSGADDR),OUTDADID MOVE DD NAME TO MSG
         B     GEN04000           GO ISSUE MESSAGE
         SPACE  3
MVCPRTBF MVC   PRTBUFF+D1(ZERO),D1(MSGADDR) MOVE MSG INTO BUFFER
DATAMVC   MVC  D0(L0,TXTPTR),READAREA+16  MOVE DATA TO BUFFER
         SPACE 3
ANSWER   DC    CL1' '             WORK AREA IN JFCB RTN
         DS    0F                 FORCE BOUNDARY
SWITCH2  DC    C' SWITCH2'
SWITCHS  DC    C' SWITCHS'        SWITCHS FOR MODULE
SWITCH3  DC    C' SWITCH3'        INTERNAL SWITCHS
HEX0001  DC    H'0001'
HEX0000  DC    XL2'0000'
ESDID    DS    0CL4               SET SYMBOLIC FOR ADDR
         DC    X'02'              SET 0-9-12
         DC    C'ESD'             SET ESD FOR COMPARE
TXTID    DS    0CL4               SET SYMBOLIC FOR ADDR
         DC    X'02'              SET 0-9-12
         DC    C'TXT'             SET TXT FOR COMPARE
REPID    DS    0CL4               SET SYMBOLIC FOR ADDR
         DC    X'02'              SET CODE
         DC    C'REP'             SET REP FOR COMPARE
RLDID    DS    0CL4               SET SYMBOLIC FOR ADDR
         DC    X'02'              SET CODE
         DC    C'RLD'             SET RLD FOR COMPARE
ENDID    DS    0CL4               SET SYMBOLIC FOR ADDR
         DC    X'02'              SET CODE
         DC    C'END'             SET END FOR COMPARE
NAMEID   DC    C' NAME'           SET NAME FOR COMPARE
         DS    0F
         SPACE 2
TTSAVE   DC    F'0'               SAVE THE TTRZ HERE
SEQSAVE  DC    F'0'               SAVE THE SEQUENCE NO. HERE
TRTAB    DC    C'1234567890'      TRANSLATE TABLE FOR A SEQ CHECKER
         SPACE 2
         DS    0F                 FORCE BOUNDARY
TXTADR   DC    F'0'               ADDRESS OF NEXT BYTE IN BUFFER
TXTADSV  DC    AL4(TXTAREA)
SIZETXT  DC    AL4(3600)
TXTHDR   DS    0CL80              SET SYMBOLIC FOR ADDRESSING
         DC    X'02'              CODE
         DC    C'TXTHDR'          SET NAME/TYPE OF RECORD
         DC    CL9' VS OLTEP'     THIS CON WILL BE 'VS TOLTEP' -
         DC    X'02'              IF TOLTEP UPDATE THE MOD
DATAADRS DC    AL3(0)             SET HDR NAMES FOR
         DC    AL1(32)
DATACNT  DC    AL3(0)             REQUIRE SYMBOLIC ADDRESSING
         ORG   TXTHDR+L'TXTHDR    SET LOCATION CNT TO END OF MSG
         DS    0F
STOWCON  DS    0CL32              SET ASIDE 32 BYTE DIRECTORY SIZE
STOWNAME DC    CL8'        '      EIGHT BYTES FOR NAME FIELD
TTRCONST DC    XL3'00'            THREE BYTES FOR STOW DIRT'RY TTR
DIRTORSZ DC    XL1'0A'            SET INDICATOR FOR TEN HALFWORDS
USERINFO DC    XL20'00'           OF USER DIRECTORY INFORMATION
         SPACE  3
NAMESAVE DC    CL8' '             SAVE AREA FOR NAME
PRTBUFF  DS    0CL121             SET SYMBOLIC FOR ADDRESS
         DC    X'8B'              SET CMND CODE
         DC    CL120' '           SET BUFFER TO BLANKS
         SPACE 2
IGNORECD DC    C' IFD906I REP ERR XXXXXXXX BYPASSED -                  *
                                                                       *
                    '
         ORG   IGNORECD           PUT MESSAGE SIZE AT MSG HEAD
IGNORSIZ DC    AL1(L'IGNORECD-1)  MESSAGESIZE
         ORG   IGNORECD+L'IGNORECD     SET ADDRESS COUNTED
         SPACE 2
FAILDCB  DC    C' IFD901I    XXXXXXXX  DDCARD FAILED TO OPEN ****ERROR**
               ***'
         ORG   FAILDCB
FAILSIZE DC    AL1(L'FAILDCB-1)     FORCE MSG SIZE TO MSG HEAD
         ORG   FAILDCB+L'FAILDCB
STOWOK   DC    C' IFD902I    XXXXXXXX  WAS CORRECTLY           ' XM3316
         ORG   STOWOK             PUT MESSAGE SIZE AT MSG HEAD
OKSTRSIZ DC    AL1(L'STOWOK-1)    MESSAGES SIZE
         ORG   STOWOK+L'STOWOK         SET ADDRESS COUNTED
         SPACE  2
DIRTFULL DC    C' IFD903I    XXXXXXXX  WAS NOT STORED *ERROR* NO SPACE *
               LEFT IN DIRECTORY'
         ORG   DIRTFULL
         DC    AL1(L'DIRTFULL-1)
         ORG   DIRTFULL+L'DIRTFULL REESTABLISH ADDRESSABILITY
         SPACE  2
PERMIOER DC    C' IFD903I    XXXXXXXX  WAS NOT STORED *ERROR* PERMANENT*
                I/O ERROR'                                       XM1330
         ORG   PERMIOER
         DC    AL1(L'PERMIOER-1)
         ORG   PERMIOER+L'PERMIOER
         SPACE 2
SEQERROR DC    C' IFD904I    SEQ ERR EXP=XXXX RECD=XXXX MODULE XXXXXXXX*
                NOT STORED'
         ORG   SEQERROR
MSSIZE   DC    AL1(L'SEQERROR-1)         GET MESSAGE SIZE
         ORG   SEQERROR+L'SEQERROR
         SPACE 3
LOCALOLT DC    C' IFD907I THE FOLLOWING MODULES WERE DIRECTED TO THE OL*
               T AND LOCAL CDS DATA SET.'
         ORG   LOCALOLT
         DC    AL1(L'LOCALOLT-1)        SET MSG SIZ AT MSG HEAD
         ORG   LOCALOLT+L'LOCALOLT    REST IC
SYMDATAS DC    C' IFD907I THE FOLLOWING MODULES WERE DIRECTED TO THE SM*
               MBOLIC (REMOTE) CDS DATA SET.'
         ORG   SYMDATAS
         DC    AL1(L'SYMDATAS-1)   STE MSG SIZ AT MSG HEAD
         ORG   SYMDATAS+L'SYMDATAS   RESET IC
BADCARD  DC    C' IFD900I INVALID INPUT, REJECTED -                    *
                                                                       *
                  '
         ORG   BADCARD
         DC    AL1(L'BADCARD-1)        SET MEAASGE SIZE
         ORG   BADCARD+L'BADCARD
ORDERERR DC    C' IFD908I ORDER ERROR XXXXXXXX BYPASSED -              *
                                                                       *
                         '
         ORG   ORDERERR
         DC    AL1(L'ORDERERR-1)  GET MESSAGE SIZE
         ORG   ORDERERR+L'ORDERERR RESET IC
         SPACE 3
BADLIB   DC    C' IFD909I  XXXX.XXXXXXX  CAN NOT BE USED FOR ONLINE TES*
               T LOAD MODULES, JOB TERMINATED'
         ORG   BADLIB             RESET IC
         DC    AL1(L'BADLIB-1)
         ORG   BADLIB+L'BADLIB    RESET THE IC
         SPACE  3
         SPACE  3
         SPACE 5
JFCBPTR  DS    0F                 SET PTR TO JFCB AREA
EOV      DC    X'06'              END OF VOLUME ID             @ZA08990
         DC    AL3(GEN07000)      ROUTINE TO COUNT TAPES       @ZA08990
         DC    X'87'              SET ID FOR SOME GUY
         DC    AL3(JFCBAREA)      SET THE RUDDY PTR
JFCBAREA DC    CL176'JFCB'
EBCD0001 DC    C'0001'            USED IN SEQUENCE CHECKING
EBCD0002 DC    C'0002'            USED IN SEQUENCE CHECKING
ADDCON   DC    CL9'ADDED'         USED WHEN MODULE WAS ADDED
REPCON   DC    CL9'REPLACED'
TEXTID   DC    XL2'0000'          SAVE AREA FOR CURRENT TEXT ID
READAREA DS    0D                 FORCE BOUNDARY
         DC    CL80' '            SET AREA TO BLANKS
BLANKCOL DC    CL4' '             USED IN SCANNER, DO NOT MOVE ....
*                                 THIS CARD MUST FOLLOW THW READAREA
*                                 SO IF YOU MAINTAIN THIS MODULE
*                                 LEAVE THE ORDER OF THESE TWO CARDS
*                                                  ** ALONE**
SETSSI   DC    CL7' SETSSI'
         DS    0H
SYMCDSID DC    C'SYMBOLIC CDS'    SET ID FOR CDS FILE BEGINNING
SYMCDSDD DC    CL8'SYMSYM  '      DD NAME FOR SYMBOLIC CDS FILE
OUTDADID DC    CL8'OLTCDSDD'      DD NAME FOR OLT CDS FILE
INPUTID  DC    CL8'CNTRLIN '      DD NAME FOR OUT PUT DC CARD
LINKLIB  DC    C'SYS1.LINKLIB '   NAME OF LIBRARY NOT AVAIL TO EDITOR
SVCLIB   DC    C'SYS1.SVCLIB '    NAME OF LIBRARY NOT AVAIL TO EDITOR
LPALIB   DC    C'SYS1.LPALIB '    NAME OF LIBRARY NOT AVAIL TO EDITOR
#TAPES   DC    F'1'               # OF TAPES IN INPUT FILE 1   @ZA08990
*                                 INCREMENTED BY END OF VOLUME @ZA08990
*                                 ROUTINE. USED BY END OF FILE @ZA08990
*                                 ROUTINE TO UPDATE JFCB       @ZA08990
         SPACE  3
INCARD   DCB   DDNAME=CNTRLIN,DSORG=PS,MACRF=GM,LRECL=80,BLKSIZE=80,   *
               EODAD=GEN06000,RECFM=F,EXLST=JFCBPTR
         EJECT
OUTDASD  DCB   BLKSIZE=3600,LRECL=80,DDNAME=OLTCDSDD,DSORG=PO,         *
               MACRF=W,RECFM=UT,OPTCD=W,EXLST=JFCBPTR
         EJECT
PRINTER  DCB   BLKSIZE=121,LRECL=121,DDNAME=DIAGMSG,DSORG=PS,          *
               MACRF=PM,RECFM=FBM
         EJECT
SAVEAREA  DC   18F'0'             SAVE AREA FOR TESTING AND DEBUGGING
         IFDPATCH
TXTAREA  DS    450D               SET TEXT AREA TO 3600 BYTES
DCBNAME  DCBD
         IEFJFCBN
         END
