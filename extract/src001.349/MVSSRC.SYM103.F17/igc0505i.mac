         TITLE 'SVC59 OLTEP HOUSEKEEPING FUNCTION'
IGC0505I CSECT                    CLEAN UP MODULE FOR OLTEP SVC 59
***********************************************************************
*                                                             @Y30LPAW*
* MODULE NAME = IGC0505I                                      @Y30LPAW*
*                                                             @Y30LPAW*
* DESCRIPTIVE NAME = SVC59 MODULE FOR CLEANUP                 @Y30LPAW*
*                                                             @Y30LPAW*
* COPYRIGHT = NONE                                            @Y30LPAW*
*                                                             @Y30LPAW*
* STATUS = CHANGE LEVEL 001                                   @Y30LPAW*
*                                                             @Y30LPAW*
* FUNCTION = TO RESET ANY I/O,UCBS,TABLES BECAUSE OF CHANGE   @Y30LPAW*
*            IN DEVICE FIELD ENTRY AT ENTER D/T/O// TIME,OR   @Y30LPAW*
*           BECAUSE OF ABEND.                                 @Y30LPAW*
*                                                             @Y30LPAW*
* ENTRY POINTS -                                              @Y30LPAW*
*              IGC05CDE IS THE ENTTY POINT                    @Y30LPAW*
*                                                             @Y30LPAW*
* INPUT -                                                     @Y30LPAW*
*        R9 POINTS TO A PARAMETER LIST                        @Y30LPAW*
*        R14 POINTS TO RETURN POINT IN THE CALLER             @Y30LPAW*
*                                                             @Y30LPAW*
* OUTPUT -                                                    @Y30LPAW*
*        THE UCBNALOC BITS TURNED ON BY OLTEP ARE RESET.      @Y30LPAW*
*        3330 AND 3830 SSIDS WHICH WERE VARIED OFF BY OLTEP   @Y30LPAW*
*           HAVE BEEN VARIED ON.                              @Y30LPAW*
*        GETMAIN AREAS HAVE BEEN FREED (TOTAL CLEANUP ONLY).  @Y30LPAW*
*        THE QUIESCE BIT IN THE UCB IS RESET                   @ZA04290
*                                                             @Y30LPAW*
* EXTERNAL REFERENCES :                                       @Y30LPAW*
*        REFERS TO OLTEPS UCB BIT MASK TABLE,SSIDLIS,REI DEB  @Y30LPAW*
*                                                             @Y30LPAW*
* EXITS - NORMAL :                                            @Y30LPAW*
*        TO WTO TO OUTPUT MESSAGE IFD470I WHEN NECESSARY      @Y30LPAW*
*        TO ICBVARY TO VARY ON SSIDS.                         @Y30LPAW*
*        TO FREEMAIN TO FREE CORE ACQUIRED FROM GETMAIN.      @Y30LPAW*
*        TO CALLER AFTER CLEANUP IS DONE.                     @Y30LPAW*
*                                                             @Y30LPAW*
* EXITS - ERROR :                                             @Y30LPAW*
*        NONE                                                 @Y30LPAW*
*                                                             @Y30LPAW*
* TABLE/WORK AREAS :                                          @Y30LPAW*
*        WKFLD - AN AREA IN CORE ACQUIRED THRU GETMAIN.       @Y30LPAW*
*                                                             @Y30LPAW*
* ATTRIBUTES : REENTRANT.                                     @Y30LPAW*
*                                                             @Y30LPAW*
* NOTES : NONE                                                @Y30LPAW*
*                                                             @Y30LPAW*
* CHANGE ACTIVITY : NEW CODE ADDED FOR 3850 MASS STORAGE      @Y30LPAW*
*                   SYSTEM SUPPORT.                           @Y30LPAW*
*                                                             @Y30LPAW*
***********************************************************************
 EJECT
***********************************************************************
*                             EQUATS                                  *
***********************************************************************
 SPACE 2
UCBTAB   EQU   6
ZERO     EQU   0                       REG ZERO
WKREG2   EQU   11                      WORK REG                  Y02008
SCAN     EQU   7
SHIFTER  EQU   8
ODDREG   EQU   9                  USED IN MASK BIT SEARCH
BCTREG   EQU   11
R2       EQU   2                                               @Y30LPAW
R8       EQU   8                                               @Y30LPAW
R9       EQU   9                                               @ZA12053
R10      EQU   10                                              @Y30LPAW
R11      EQU   11                                              @Y30LPAW
R12      EQU   12                                              @Y30LPAW
R14      EQU   14                                              @Y30LPAW
INDEX    EQU   5
R6       EQU   6
R7       EQU   7
D2       EQU   2
R15      EQU   15
REG0     EQU   0
D15      EQU   15
R0       EQU   0
R1       EQU   1
D0       EQU   0
D1       EQU   1
ONE      EQU   1
THREE    EQU   3
D3       EQU   3                   DISPLACEMENT OF THREE       @ZA06016
D4       EQU   4
D5       EQU   5
R5       EQU   5
D6       EQU   6                                               @ZA04290
D8       EQU   8
D20      EQU   20
D24      EQU   24
D32      EQU   32
D11      EQU   11
D12      EQU   12                                              @Y30LPAW
D27      EQU   27
D28      EQU   28
SHIFT16  EQU   16
K72      EQU   X'48'               SIZE OF SSIDLIST            @Y30LPAW
L2       EQU   2
L3       EQU   3
L4       EQU   4                                               @Y30LPAW
L5       EQU   5                                               @Y30LPAW
L8       EQU   8                                               @Y30LPAW
L72      EQU   72                                              @Y30LPAW
L79      EQU   79                                              @Y30LPAW
L100     EQU   100                                             @Y30LPAW
DEBREG   EQU   6
PRDEBREG EQU   7                   CONTAINS ADDRESS OF PREVIOUS DEB
*                                  IN THE TASK DEB QUEUE
CALLREG2 EQU   6                   REGISTER 6 CONTAINS DUPLICATE OF
*                                  CALLING CODE MINUS CONTROL BIT 16
CVTREG   EQU   3                   REGISTER 3 ADDRESS OF CVT
POOLREG  EQU   13                  REGISTER CONTAINS ADDRESS OF 16
D40      EQU   40                      DISPLACEMENT 40
*                                  GETMAIN BYTES IN SUBPOOL
CALLREG1 EQU   1                   REGISTER 1 CONTAINS CALLING CODE OF
*                                  REQUESTED FUNCTION.
PARMREG  EQU   9                   REGISTER 9 CONTAINS ADDRESS OF
*                                  PARAMETER LIST
TCBREG   EQU   4                   REGISTER 4 CONTAINS ADDRESS OF TCB
RETURN   EQU   14                  EXIT ADDR REG
RCREG    EQU   15                  REGISTER 15 CONTAINS RETURN CODE
BASEREG  EQU   12                  SVC BASE REGISTER 12
SAVEREG  EQU   10                  SAVE REGISTER FOR CALLING CODE
WKREG7   EQU   7
LNG20    EQU   20
HEX7F    EQU   X'7F'               OLTEP EXECUTING FLAG MASK   @ZA04290
HEXFB    EQU   X'FB'
HEXFF    EQU   X'FF'
HEXFD    EQU   X'FD'
HEXFE    EQU   X'FE'               UCB QUIESCE BIT MASK        @ZA04290
TABREG   EQU   2
TABDEBAD EQU   11
WKREG8   EQU   8                       THIS IS WORK REG # 8      Y01021
BTAM     EQU   X'1C'                   BTAM DEVICE               YM7254
         EJECT
         USING *,R15                   USE R15 AS BASE REG       Y02008
         B     IGC05CDE                BRANCH AROUND NAME & DATE Y02008
         DC    C'IGC0505I&SYSDATE'     MODULE NAME , ASSEM DATE  Y02008
IGC05CDE EQU   *                       START OF ACTUAL CODE      Y02008
         BALR  BASEREG,REG0         SET BASE REGISTER
         USING *,BASEREG           INDICATE BASE REGISTER BEING USED
         USING CVTDSCTS,CVTREG                                 YM2272
         LR    TABREG,CALLREG2     SAVE CALL PARM              @ZA12053
 EJECT
***********************************************************************
*                             FUNCTION 5                              *
***********************************************************************
 SPACE 2
FUNC5    EQU   *                       OLTEP CLEANUP             X03008
         LR    R15,PARMREG        SAVE R9 FOR LATERON            X03008
         L     UCBTAB,CVTEXT2     GET CVT EXTENTION ADDR         X03008
         L     UCBTAB,D28(UCBTAB) GET CVTOLTEP ADDR              X03008
         LTR   UCBTAB,UCBTAB     IS OLTEP REALLY ACTIVE?         X03008
         BZ    D0(RETURN)        NOPE, THEREFORE DO NOTHING      X03008
         C     TCBREG,D8(UCBTAB)       IS THIS THE FIRST OLTEP   Y02008
         BNE   D0(RETURN)        NO, EXIT AND DO NOTHIN....      X03008
         L     UCBTAB,D28(UCBTAB)      GET MASK TABLE ADDR       Y02008
         L     ZERO,D0(UCBTAB)   GET COUNT FOR FREEMAIN          X03008
         LR    POOLREG,UCBTAB    SAVE SUBPOOL ADDR               X03008
*                                IF A TOTAL CLEANUP SITUATION    X03008
         L     SCAN,CVTILK2       GET IOS UCB LOOKUP TBL         X03008
         SPACE  4
SETCOUNT LA    BCTREG,32          SET LOOP COUNT TO 32           X03008
         LA    UCBTAB,D4(UCBTAB)  BUMP MASK TAB ADDR AS TO PICK  X03008
*                                 UP NEXT WORD OF MASK BITS      X03008
         L     ODDREG,D0(UCBTAB)  GET A WORD OF MASKS            X03008
         SPACE 2
         XR    SHIFTER,SHIFTER    BY ZEROING OUT A REG           X03008
SHIFTIT  EQU   *                                               @ZA12053
         LTR   ODDREG,ODDREG       IF THE BIT IS OFF,          @ZA12053
         BNM   BUMPUCB             CHECK THE NEXT ONE          @ZA12053
         ICM   SHIFTER,B'0011',D0(SCAN) GET A UCB ADDR           X03008
         NI    D6(SHIFTER),HEXFE   RESET THE QUIESCE BIT       @ZA04290
         NI    D1(SHIFTER),HEXFB   AND RESET THE UCBNALOC        X03008
         L     R5,D20(SHIFTER)     GET ADDRESS OF UCB EXT      @ZA06016
         CLI   D3(R5),BTAM         IS THIS A BTAM UCB?         @ZA06016
         BNE   BUMPUCB             NO,GET NEXT UCB               YM7254
         NI    D27(SHIFTER),HEX7F  ZERO OLTEP EXECUTING FLAG     YM7254
BUMPUCB  EQU   *                                                 X03008
         LA    SCAN,D2(SCAN)      BUMP IOS UCBTBL ADDR PTR       X03008
         CLI   D1(SCAN),HEXFF     IS THIS THE END OF CLEANING    X03008
         BE    ZEROPOOL           IF YES, GO ZERO UCB MSK TBL    X03008
         SLL   ODDREG,ONE          SHIFT IT IN                 @ZA12053
         BCT   BCTREG,SHIFTIT     ELSE, GO SHIFT NEX BIT IN REG  X03008
         B     SETCOUNT           AND CONTINUE TO SCAN MASK TAB. X03008
         EJECT
         SPACE 4
ZEROPOOL EQU   *                                                 X03008
         LR    R6,POOLREG          GET SUBPOOL ADDR              X03008
         LA    R6,D4(R6)           BUMP BY 4 SO NOT TO           X03008
*                                  ZERO UCB COUNT AT HEAD        X03008
         LR    R7,R0               GET THE NUMBER OF UCBS        X03008
         SRL   R7,L3                    DIVIDE BY 8            @Y30LPAW
         LA    R7,15(R7)           BUMP BY 15 FOR SIZE           X03008
         SRL   R7,L3               SHIFT OUT                     X03008
         SLL   R7,L3              AND BACK IN TO ROUND           X03008
         LR    R10,R7              SAVE BIT MASK TABLE SIZE    @Y30LPAW
         SH    R7,FOUR            SUBTRACT FOU  FOR HEADER       X03008
*                                 SIZE IN POOL                   X03008
         XR    WKREG8,WKREG8      ZERO OUT R 8                   X03008
         XR    ODDREG,ODDREG      ZERO OUT R 9                   X03008
         MVCL  R6,WKREG8          ZERO THE TABLE LESS THE        X03008
*                                 HEADER                         X03008
         SPACE  4
 EJECT
         LR    R6,POOLREG          POINT TO SSIDLIST           @Y30LPAW
         AR    R6,R10                   SECTION OF MASK TBLE   @Y30LPAW
         ICM   R0,B'1111',SP254A   GET SOME MAIN FOR
         GETMAIN R,LV=(0)          VARYING THE DSID OR         @ZA12053
         LR    R10,R1              SSIDS ON LINE               @ZA12053
         XC    D0(L100,R10),D0(R10) CLEAR THE AREA             @ZA12053
         OC    D0(L4,R6),D0(R6)    IS THERE A DSID?            @Y30LPAW
         BZ    CHKSSIDA            NO-GO AROUND                @Y30LPAW
         USING MAINMAP,R10                                     @Y30LPAW
         MVC   VONLEN(D8),VARYPARM SET UP LNGTH,OPCODE,FLG     @Y30LPAW
         MVC   VONSSID(D2),D0(R6)  MOVE IN DSID                @Y30LPAW
         ICBVARY MF=(E,(10)),TYPE=VON     VARY IT ON           @Y30LPAW
         LTR   R15,R15             HOW DID WE DO?              @Y30LPAW
         BZ    CHKSSIDA            GO AROUND IF OK             @Y30LPAW
          LA    R11,120
          CR    R15,R11            IS UNIT ON ALREADY?
          BE    CHKSSIDA           GO AROUND IF OK
         LA    R11,X'F8'           8 FOR DSID                  @Y30LPAW
         LR    R8,R6               PICK UP PTR TO ID           @Y30LPAW
         BAL   R5,TRPRT            GO TRANSLATE AND PRINT      @Y30LPAW
CHKSSIDA DS    0H                                              @Y30LPAW
         XC    D0(L4,R6),D0(R6)    CLEAR DSID FIELD            @Y30LPAW
         LA    R8,D4(R6)           POINT TO SSID COUNT         @Y30LPAW
         LA    R9,17               SET UP TO BUBBLE THRU       @ZA12053
         SR    R7,R7                    CLEAR IT               @Y30LPAW
         ICM   R7,B'0011',D0(R8)   GET SSID COUNT              @Y30LPAW
         LTR   R7,R7                    ANY THERE?             @Y30LPAW
         BZ    CLRLIST                  IF NOT-GO AROUND       @Y30LPAW
         LA    R8,D2(R8)           POINT TO SSID               @Y30LPAW
CHKSSIDB DS    0H                                              @Y30LPAW
         BCT   R9,CHKSIDB1                                     @ZA12053
         B     CLRLIST             GET OUT IF NO MORE          @Y30LPAW
CHKSIDB1 DS    0H                                              @Y30LPAW
         STCM  R7,B'0011',D4(R6)   SET COUNT                   @Y30LPAW
         OC    D0(L4,R8),D0(R8)    EMPTY FIELD?                @Y30LPAW
         BNZ   VPARMLST            IF NOT-GO SET UP            @Y30LPAW
         LA    R8,D4(R8)           POINT TO NEXT FIELD         @Y30LPAW
         B     CHKSSIDB            GO CHK NEXT ONE             @Y30LPAW
VPARMLST DS    0H                                              @Y30LPAW
         MVC   VONSSID(L2),D0(R8)  PUT IT IN LIST              @Y30LPAW
         MVC   VONLEN(D8),VARYPARM  SET UP LGTH,OPCDE,FLG      @Y30LPAW
         ICBVARY MF=(E,(10)),TYPE=VON     VARY IT ON           @Y30LPAW
         LTR   R15,R15                  IS IT OK               @Y30LPAW
         BZ    CHKSSIDC                 GO AROUND IF YES       @Y30LPAW
          LA    R11,120
          CR    R15,R11            IS UNIT ON ALREADY?
          BE    CHKSSIDC           GO AROUND IF OK
         LA    R11,X'F0'           0 FOR SSID                  @Y30LPAW
         BAL   R5,TRPRT                 GO TRANSLATE, PRINT    @Y30LPAW
CHKSSIDC DS    0H                                              @Y30LPAW
         XC    D0(L4,R8),D0(R8)    CLEAR THE ENTRY             @Y30LPAW
         LA    R8,D4(R8)           POINT TO NEXT SSID          @Y30LPAW
         BCT   R7,CHKSSIDB              IF MORE, GO DO IT      @Y30LPAW
CLRLIST  DS    0H                                              @Y30LPAW
         XC    D0(L72,R6),D0(R6)        CLEAR LIST             @Y30LPAW
         LR    R1,R10              POINT TO FREEMAIN AREA      @Y30LPAW
         ICM   R0,B'1111',SP254A   GET SUBPOOL NUMBER          @Y30LPAW
         FREEMAIN R,LV=(0),A=(1)        FREE IT                @Y30LPAW
PARMSAVE DS    0H                                              @Y30LPAW
         B     FRQUPATH                 AND KEEP GOING         @Y30LPAW
TRPRT    DS    0H                                              @Y30LPAW
         STCM  R0,B'1111',WKFLD    SAVE ERROR CODE             @Y30LPAW
         UNPK  WKFLD+1(L5),WKFLD+2(L3)  USE THE ERROR          @Y30LPAW
         NC    WKFLD+2(L3),=X'0F0F0F0F'   CODE TO TRANSLATE    @Y30LPAW
         TR    WKFLD+2(L3),TRTBLE       INTO EBCDIC AND        @Y30LPAW
         MVC   MSGLEN(L79),IFD470       MOVE THE MSG           @Y30LPAW
         MVC   MSGCODE(L3),WKFLD+2      PUT IN MSG             @Y30LPAW
         MVC   MSGSIDB(L2),D2(R8)  MOVE IN THE ID              @Y30LPAW
         XC    D0(L4,R8),D0(R8)    CLEAR THAT ENTRY            @Y30LPAW
         STC   R11,MSGSIDA          8 FOR DSID,0 FOR SSID      @Y30LPAW
         LA    R11,D12(R10)             POINT TO MSG           @Y30LPAW
         WTO   MF=(E,(R11))         PRINT IT                   @Y30LPAW
         BR    R5                       GO BACK                @Y30LPAW
         DROP  R10                                             @Y30LPAW
         SPACE 5
FRQUPATH SLL   TABREG,SHIFT16      TEST TO SEE IF              @ZA12053
         LTR   TABREG,TABREG       FULL OR PARTIAL             @ZA12053
         BMR   RETURN              CLEANUP                     @ZA12053
         L     TABREG,CVTEXT2     GET CVT EXTENSION POINTER      X03008
         LA    TABREG,D28(TABREG) SET CVTOLTEP POINTER           X03008
         SPACE  4
********  AT THIS POINT TOTAL CLEAN UP WILL BE PERFORMED **************
         SPACE 5
         L     UCBTAB,D0(TABREG)     GET CVTOLTEP PTR            X03008
         TM    D4(UCBTAB),X'40'        WAS REI ACTIVE            Y02008
         BNO   ZEROIT                  NO, DONT TOUCH REI UCB    Y02008
         L     UCBTAB,D32(UCBTAB)      GET REI UCB ADDR          Y02008
         NI    D1(UCBTAB),HEXFB  ELSE, ZERO UCBNALOC             X03008
*****************************************************************Y02008
*                                                                Y02008
*               REMOVE REI DEB FROM TCB DEB CHAIN                Y02008
*                                                                Y02008
*****************************************************************Y02008
         USING DEBBASIC,DEBREG         DEB ADDRESSABILITY        YM5416
         L     DEBREG,ZERO(TABREG)     GET CVTOLTEP PTR          YM5416
         L     DEBREG,D4(DEBREG)       GET PTR TO REI DEB        YM5416
         LA    DEBREG,D0(DEBREG)       CLEAR HIGH ORDER BYTE     YM4984
         L     R1,DEBDCBAD             GET DCB ADDR FOR DEBCHK   YM5416
         DEBCHK (R1),TYPE=DELETE,AM=EXCP REMOVE VALIDITY         YM5416
         USING TCB,TCBREG              TCB ADDRESSABILITY        Y02008
         L     WKREG2,CVTEXT2          GET PTR TO CVT EXTENSION  Y02008
         L     WKREG2,D28(WKREG2)      GET PTR TO IFDOLT57       Y02008
         STM   R0,R15,D40(WKREG2)      SAVE REGS IN IFDOLT57     Y02008
A        SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                        X
               RELATED=(LOCAL,IGC0505I(B)),REGS=USE              Y02008
         LM    R0,R15,D40(WKREG2)      RESTORE ALL REGS          Y02008
         L     WKREG8,TCBDEB           GET PTR TO 1ST DEB ON Q   Y02008
         LA    WKREG8,D0(WKREG8)       CLEAR HIGH ORDER BYTE     YM4984
         LA    R7,TCBDEB               GET PTR TO ADDR OF 1ST DEBY02008
NOTFIRST EQU   *                                                 Y02008
         LTR   WKREG8,WKREG8           IS THIS CHAIN END         YM4984
         BZ    FREELOCK                YES, DEB IS GONE, EXIT    YM4984
         CR    WKREG8,DEBREG           IS THIS DEB THE ONE       Y02008
         BE    FOUNDIT                 BRANCH IF YES             Y02008
         DROP  DEBREG                                            Y02008
         USING DEBBASIC,WKREG8         DEB ADDRESSABILITY        YM5416
         LA    R7,DEBDEBAD             SAVE PTR TO NEXT DEBS ADDRY02008
         L     WKREG8,DEBDEBAD         GET ADDR OF NEXT DEB      Y02008
         LA    WKREG8,D0(WKREG8)       CLEAR HIGH ORDER BYTE     YM4984
         DROP WKREG8                                             Y02008
         USING DEBBASIC,DEBREG         DEB ADDRESSABILITY        YM5416
         B     NOTFIRST                GO SEE IF NEXT DEB IS IT  Y02008
FOUNDIT  EQU   *                                                 Y02008
         L     WKREG8,DEBDEBAD         GET ADDR OF NEXT DEB      Y02008
         CS    DEBREG,WKREG8,D0(R7)    SWAP PTR TO OUR DEB WITH  Y02008
*                                      PTR TO NEXT DEB           Y02008
         BNE   NOTFIRST                BRANCH WITH REG POINTING  Y02008
*                                      TO NEW DEB ADDED TO FRONT Y02008
*                                      OF TCB DEB CHAIN          Y02008
FREELOCK EQU   *                                                 YM4984
         STM   R0,R15,D40(WKREG2)      SAVE REGS IN IFDOLT57     Y02008
B        SETLOCK RELEASE,TYPE=LOCAL,RELATED=(LOCAL,IGC0505I(A)),       X
               REGS=USE                                          Y02008
         LM    R0,R15,D40(WKREG2)      RESTORE ALL REGS          Y02008
         LR    R6,WKREG2               RESTORE PTR TO DIE        Y02008
ZEROIT   XC    D0(D4,TABREG),D0(TABREG) ZERO CVTOLTEP            XM0097
*                                THE FOLLOWIN WILL RELEASE THE   X03008
*                                CORE OBTAINED FOR THE UCBTAB    X03008
         L     R5,D0(POOLREG)      GET SIZE OF CORE TO FREE    @Y30LPAW
         SRL   R5,THREE            DIVIDE BY 3                 @Y30LPAW
         LA    R5,K72(R5)          PLUS SIZE FOR SSIDLIST      @Y30LPAW
         LA    R5,D15(R5)        BUMP BY 15                      X03008
         SRL   R5,THREE          AND ROUND IT                    X03008
         SLL   R5,THREE          OFF TO A DOUDLE WORD            X03008
         ICM   R5,B'1100',SP245        GET SUBPOOL NAME          Y02008
         LR    R0,R5             SET UP FOR FREE MAIN            X03008
         LR    R1,POOLREG        GET POOL ADDR FOR FREEING       X03008
         SPACE 3
         FREEMAIN R,LV=(0),A=(1) RELEASE STG TO SUPERMAN         X03008
         L     R0,12(R6)               GET DIE SIZE              Y02008
         ICM   R0,B'1000',SP245        GET SUBPOOL NAME          Y02008
         LR    R1,R6                   GET ADDRESS TO BE FREED   Y02008
         FREEMAIN R,LV=(0),A=(1)       FREE IFDOLT57             Y02008
          NI     D20(TCBREG),HEXFD    /* RESET TCBOLTEP      @ZA12056*/
         BR    RETURN                  RETURN                    Y02008
 EJECT
***********************************************************************
*                             CONSTANTS                               *
***********************************************************************
 SPACE 2
TSTAREA  DC    X'000000'
SAVEPARM DS    F                   SAVE THE CLEANUP PARAMETER  @ZA06016
         DS    0F                      ESTABLISH WORD BOUNDRY    X03008
SP254    DC    X'FE000010'             10 BYTES - SUBPOOL 254    Y02008
SP245    DC    X'F500'                 SUBPOOL 245               Y02008
END1     DC    X'FFFF'                                           X03008
FOUR     DC    X'0004'                                           X03008
SP254A   DC    X'FE000064'         TO GET 100 BYTES FROMSP254  @Y30LPAW
VARYPARM DC    X'0000000C0A008000'      TO SET UP VARYLIST     @Y30LPAW
TRTBLE   DC    C'0123456789ABCDEF'      TXLATE TABLE           @Y30LPAW
IFD470   WTO   'IFD470I ICBVARY ERROR CODE XXX UNEXPECTED - VARY ON VIRX
               TUAL SXXX FAILED',MF=L,ROUTCDE=10,DESC=7        @Y30LPAW
PATCH05  IFDPATCH                      PATCHAREA
 EJECT
         DS    0D
CVTDSCTS CVT   SYS=AOS2,DSECT=YES                              @Y30LPAW
         IEZDEB LIST=YES                                         Y02008
TCB      IKJTCB SYS=AOS2,DSECT=YES                             @Y30LPAW
         IHAPSA                                                  Y02008
MAINMAP  DSECT                                                 @Y30LPAW
VONLEN   DS    XL4'0'              PARMLIST LENGTH             @Y30LPAW
VONOPCD  DS    XL1'0'              OP CODE FLD                 @Y30LPAW
VONSPARE DS    XL1'0'              NOT USED                    @Y30LPAW
VONFLG   DS    XL2'0'              FLAG FLD                    @Y30LPAW
VONSSID  DS    XL2'0'              SSID FLD                    @Y30LPAW
VONRUA   DS    XL2'0'              REAL UNIT ADDR FLD          @Y30LPAW
MSGLEN   DS    XL2'0'              LENGTH FOR MSG WTO          @Y30LPAW
MSGFLG   DS    XL2'0'              FLG FLD                     @Y30LPAW
MSGTXT1  DS    XL27'0'             PART OF MSG                 @Y30LPAW
MSGCODE  DS    XL3'0'              ERROR CODE FLD              @Y30LPAW
MSGTXT2  DS    XL31'0'             PART OF MSG                 @Y30LPAW
MSGSIDA  DS    XL1'0'              8 FOR DSID, 0 FOR SSID      @Y30LPAW
MSGSIDB  DS    XL2'0'              DSID/SSID                   @Y30LPAW
MSGTXT3  DS    XL7'0'              PART OF MSG                 @Y30LPAW
MSGDESC  DS    XL2'0'              DESC OF MSG                 @Y30LPAW
MSGRTE   DS    XL2'0'              ROUTE CODE                  @Y30LPAW
WKFLD    DS    XL6'0'              WORK FLD TO SET FOR TXLATE  @Y30LPAW
*        DS    XL3'0'              NOT USED                    @Y30LPAW
         END
