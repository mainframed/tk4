         TITLE '   ROOT MODULE FOR SVC 59   OLTEP OWNED '
IGC0005I CSECT     ROOT MODULE FOR SVC 59   OLTEP OWNED
         SPACE 3
****************************************************************X03008*
*                                                               X03008*
* MODULE NAME = IGC0005I.    DECEMBER 20, 1972                  X03008*
*                                                               X03008*
* DESCRIPTIVE NAME = SVC-59, OLTEPS PRIVILIGED SVC.             X03008*
*                                                               X03008*
* COPYRIGHT =                                                   X03008*
*                                                               X03008*
* STATUS = THIS MODULE WAS COMPLETLY REWRITTEN FOR VS1/3        X03008*
*          AND VS2/2.                                           X03008*
*                                                               X03008*
* FUNCTION = THE FUNCTION WHICH THIS SVC WILL PERFORM IS        X03008*
*            DEPENDENT UPON THE CALLING CODE IN REGISTER        X03008*
*            ONE AT ENTRY TIME. THIS IS A TYPE IV SVC AND       X03008*
*            ACCESSES OTHER SVC 59 LOAD MODULES VIA XCTL.       X03008*
*                                                               X03008*
*                                                               X03008*
*                                                               X03008*
*            THE FOLLOWING TABLE WILL IDENTIFY THE FUNCTIONS OF X03008*
*            SVC 59.                                            X03008*
*                                                               X03008*
*            IF R1 = 00 AN OUTSTANDING WTOR WILL BE REMOVED     X03008*
*                    FROM THE RQE CHAIN. A DUMMY RESPONSE       X03008*
*                    OF  'S' WILL BE USED. SVC34 WILL BE        X03008*
*                    USED.                                      X03008*
*                                                               X03008*
*            IF R1 = 04 THE UCBS REQUESTED FOR CUTEST SUPPORT   Y02008*
*                    WILL BE LOOKED-UP. THIS FUNCTION WILL      Y02008*
*                    ALTER THE UCBNALOC.                        Y02008*
*                                                               Y02008*
*            IF R1 = 08 IT WILL BE DETERMINED IF OLTEP IS IN    Y02008*
*                    AN MP ENVIRONMENT                          Y02008*
*                                                               Y02008*
*            IF R1= 0C A 3830 ATTACHED TO A 3850 MASS        @Y30LPAW*/
*                    STORAGE SYSTEM WILL BE VARIED OFF       @Y30LPAW*/
*                                                               Y02008*
*           IF R1 = 10 A 3330 SSID (WHEN ATTACHED TO A       @Y30LPAW*/
*                    3850 MASS STORAGE SYSTEM) WILL BE       @Y30LPAW*/
*                    PUT INTO A LIST FOR US BY CLEANUP       @Y30LPAW*/
*                                                               X03008*
*            IF R1 = 14 THE CLEANUP OF UCBS AND DEB             X03008*
*                    CHAIN WILL OCCUR. THE CVTOLTEP             X03008*
*                    WORD WILL ALSO BE ZEROED.                  X03008*
*                                                               X03008*
*            IF R1 = 18 NO FUNCTION WILL BE PERFORMED           X03008*
*                    AND R15 WILL CONTAIN A 04 ON EXIT.         X03008*
*                                                               X03008*
*            IF R1 = 1C NO FUNCTION WILL BE PERFORMED           Y02008*
*                    AND R15 WILL CONTAIN A 04 ON EXIT          Y02008*
*                                                               X03008*
*            IF R1 = 20 NO FUNCTION WILL BE PERFORMED           X03008*
*                    AND REG15 WILL CONTAIN AN 04 ON EXIT       X03008*
*                                                               X03008*
*            IF R1 = 24 NO FUNCTION WILL BE PERFORMED           Y02008*
*                    AND R15 WILL CONTAIN A 04 ON EXIT          Y02008*
*                                                               Y02008*
*            IF R1 = 28 NO FUNCTION WILL BE PERFORMED           Y02008*
*                    AND R15 WILL CONTAIN A 04 ON EXIT          Y02008*
*                                                               Y02008*
*            IF R1 = 2C NO FUNCTION WILL BE PERFORMED           Y02008*
*                    AND R15 WILL CONTAIN A 04 ON EXIT          Y02008*
*                                                               Y02008*
*            IF R1 = 30 NO FUNCTION WILL BE PERFORMED           Y02008*
*                    AND R15 WILL CONTAIN A 04 ON EXIT          Y02008*
*                                                               Y02008*
*            IF R1 = 34 NO FUNCTION WILL BE PERFORMED           Y02008*
*                    AND R15 WILL CONTAIN A 04 ON EXIT          Y02008*
*                                                               X03008*
*            IF R1 = 38 NO FUNCTION WILL BE PERFORMED AND       X03008*
*                    REGISTER 15 WILL CONTAIN AN 04 ON EXIT     X03008*
*                                                               X03008*
*            IF R1 = 3C A CHECK OF UCB ON-OFFLINE STATUS        X03008*
*                    WILL BE PERFORMED. IF UCB IS ONLINE        X03008*
*                    R15 WILL EQ 'F0'. IF THE UCB IS IN-        X03008*
*                    VALID R15 WILL= 'FF'. ALSO IF R1 =         X03008*
*                    803C THE VOL=SER WILL BE MOVED.            X03008*
*                                                               X03008*
*            IF R1 = 40 A UCBLOOK UP FOR EACH DEVTAB            X03008*
*                    ENTRY WILL BE PERFORMED. THIS              X03008*
*                    FUNCTION WILL USE THE IOSGEN               X03008*
*                    MACRO.                                     X03008*
*                                                               X03008*
*            IF R1 = 44 NO FUNCTION WILL BE PERFORMED           Y02008*
*                    AND R15 WILL CONTAIN A 04 ON EXIT          Y02008*
*                                                               X03008*
*            IF R1 = 48 THE OLTEP REI DEB WILL BE MOVED TO A    Y02008*
*                    PROTECTED SUBPOOL AND PLACED AT THE        Y02008*
*                    BEGINNING OF THE TCB DEB CHAIN.            Y02008*
*                                                               X03008*
*            IF R1 = 4C A REAL ADDRESS WILL BE TRANSLATED TO    Y02008*
*                    A VIRTUAL ADDRESS                          Y02008*
*                                                               X03008*
*            IF R1 = 50 OLTEP WILL PURGE AN IO EVENT AND FREE   Y02008*
*                    NECESSARY CONTROL BLOCKS AND AREAS. IF     Y02008*
*                    R1 = 8050 OLTEP WILL FREE NECESSARY BLOCKS Y02008*
*                    AND AREAS ASSOCIATED WITH A COMPLETE EVENT Y02008*
*                                                               X03008*
*            IF R1 = 54 THE UCB NOTREADY BIT WILL BE TESTED.    X03008*
*                    IF THE NR BIT = ON, R15 WILL BE FF ON      X03008*
*                    EXIT.                                      X03008*
*                                                               X03008*
*            IF R1 = 58 OLTEP INITIALIZATION WILL BE PERFORMED. Y02008*
*                    OLTEP INITIALIZATION INCLUDES              Y02008*
*                       - CHECK FOR 2 OLTEPS                    Y02008*
*                       - SAVE ASCB MEMORY ID IN THE DIE        Y02008*
*                       - MOVE IFDOLT57 TO PROTECTED CORE- SP245Y02008*
*                       - OBTAIN CPU ID AND STORE IN SCT       @ZA15468
*                       - OBTAIN & SET UP UCB MASK TABLE        Y02008*
*                       - OBTAIN SPACE FOR REI DEB              Y02008*
*                       - SET TCBOLTEP FLAG ON IN TCB           Y02008*
*                       - FIND 2955 UCB IF ONE EXISTS           Y02008*
*                       - TELL CALLER IF OLTEP HAS BEEN INVOKED Y02008*
*                         V=R OR V=V                            Y02008*
*                                                               Y02008*
*            IF R1 = 5C AN IO OPERATION WILL BE PERFORMED. IN   Y02008*
*                    ORDER TO PERFORM IO OLTEP BUILDS THE       Y02008*
*                    APPROPRIATE CONTROL BLOCKS AND USES THE    Y02008*
*                    STARTIO MACRO.                             Y02008*
*                                                               Y02008*
*            IF R1 = 60 VERIFICATION OF A CHOSEN CPU AFFINITY   Y02008*
*                    WILL BE DONE                               Y02008*
*                                                               Y02008*
* NOTES = A BRANCH TABLE WILL BE USED TO LOCATE THE REQUIRED    X03008*
*         FUNCTION.                                             X03008*
*                                                               X03008*
*    DEPENDENCIES : THE TESTAUTH MACRO                          X03008*
*                       CVT DSECT                               X03008*
*                       TCB DSECT                               X03008*
*                       UCM DSECT                               X03008*
*                       RQE MAP MUST ALL APPEAR IN THIS CSECT.  X03008*
*                                                               X03008*
* RESTRICTIONS = THE CALL MUST BE AN AUTHORIZED PROGRAM         X03008*
*                AS DEFINED BY THE TESTAUTH MACRO               X03008*
*                                                               X03008*
* REGISTER CONVENTIONS = ON ENTRY:  R0 = ADDRESS OF PARAMETER   X03008*
*                                        LIST                   X03008*
*                                                               X03008*
*                                   R1 = A VALID CALL CODE      X03008*
*                                                               X03008*
*                                   R3 = CVT POINTER            X03008*
*                                                               X03008*
*                                   R4 = TCB POINTER            X03008*
*                                                               X03008*
*                                   R5 = RB  POINTER            X03008*
*                                                               X03008*
*                                   R14= RETURN POINT TO        X03008*
*                                        C.P.                   X03008*
*                                                               X03008*
*   PATCH LABEL = PATCH5I, THIS LABEL WILL BE ON THE OLTEP      X03008*
*                          IFDPATCH.                            X03008*
*                                                               X03008*
* MODULE TYPE = TYPE IV SVC.                                    X03008*
*    PROCESSOR = ANY 370 MACHINE WITH THE 370 INSTRUCTION SET.  X03008*
*                                                               X03008*
*    MODULE SIZE = LESS THAN 2K BYTES.                          X03008*
*                                                               X03008*
*    ATTRIBUTES = REENTRANT.                                    X03008*
*                                                               X03008*
* ENTRY POINT = IGC0005I.                                       X03008*
*                                                               X03008*
*    PURPOSE = PROVIDE OLTEP WITH PRIVILIGED OPERATIONS.        X03008*
*                                                               X03008*
*    LINKAGE = XCTL AND BRANCH TABLE.                           X03008*
*                                                               X03008*
* INPUT = SAME AS REGISTER CONVENTIONS ABOVE.                   X03008*
*                                                               X03008*
* OUTPUT = SEE FUNCTION DESCRIPTION ABOVE.                      X03008*
*                                                               X03008*
* EXIT NORMAL = AT LABEL EXIT00.                                X03008*
*                                                               X03008*
* EXIT ABNORMAL = AT LABEL EXIT04.                              X03008*
*                                                               X03008*
* EXTERNAL REFERENCES = SEE BELOW                               X03008*
*                                                               X03008*
*    ROUTINES = TESTAUTH AND IOSGEN AND SVC34 AND GETMAIN       X03008*
*               AND XCTL AND FREEMAIN MODESET                   X03008*
*    DATA AREA = OLTEP COMMON AREA                              X03008*
*                                                               X03008*
*    CONTROL BLOCKS = CVT TCB RQE UCB UCM                       X03008*
*                                                               X03008*
* TABLES = NONE                                                 X03008*
*                                                               X03008*
* MACROS = SAME AS DEPENDENCIES ABOVE                           X03008*
*                                                               X03008*
* CHANGE ACTIVITY                                               X03008*
*  VS2-2.0 PTM YM08149 ADDED FOR VIRTUAL SUPPORT (3705,REI), OLTEP IS
*  NON-SWAPPABLE IN VIRTUAL STORAGE.                           YM08149
*  VS2R3.7(SU10)--10/10/77--OZ26394                                   *
*                                                               X03008*
****************************************************************X03008*
         SPACE 3                                                 X03008
*****************************************************************X03008
*                                                               *X03008
*  EQUATES, DISPLACEMENTS, MASKS, ADDRESS CONSTANTS AND         *X03008
*  OTHER  STUFF AND THINGS                                      *X03008
*                                                               *X03008
*****************************************************************X03008
D0       EQU   0                       DISPLACEMENT OF ZERO      X03008
POOLADR  EQU   0                       SAVE SUBPOOL ADDR HERE    X03008
POOLSIZE EQU   0                       REG FOR GETMAIN           X03008
L0       EQU   0                                               @ZA12058
R0       EQU   0                       REGISTER ZERO             X03008
REG0     EQU   0                       REGISTER ZERO             X03008
ZEROO    EQU   0                       USED IN BASE REG WORK     X03008
RCREG1   EQU   1                       GENERAL PURPOSE REGISTER 1X03008
ONE      EQU   1                       SHIFT COUNT               X03008
L1       EQU   1                       DISPLACEMENT CONSTANT     X03008
R1       EQU   1                       GENERAL PURPOSE REGISTER  X03008
REG1     EQU   1                       WORK REGISTER 1           X03008
VIRTUAL  EQU   1                       REG 1, IF MINUS V=V ASSUMEX03008
HEX01    EQU   X'01'                   PSEUDO DEVTAB BIT         X03008
D1       EQU   1                       LOW BYTE UCB ADDR NEVER   X03008
*                                      BY INITILIZATION MODULES  X03008
PARMREG  EQU   1                       POINTER TO REQUESTORS     X03008
*                                      PARAMETER LIST.           X03008
WKREG1   EQU   1                       REGISTER WORKING REG.     X03008
HEX00    EQU   X'00'                   OPTION FOR XLATE          Y02008
HEX02    EQU   X'02'                   TCB OLTEP BIT MASK        X03008
WORKREG2 EQU   2                       WORKING REGISTER          X03008
D2       EQU   2                       DISP TO NEXT UCB PTR      X03008
L2       EQU   2                       DISPLACEMENT CONSTANT     X03008
R2       EQU   2                                               @ZA12058
SRBREG   EQU   2                       SRB POINTER               Y02008
CVTOLT   EQU   2                       CVT OLT REG               Y02008
THREE    EQU   3                       USED IN SHIFT INST        Y02008
CVTREG   EQU   3                       POINTER TO SYS CVT.       X03008
L3       EQU   3                       LENGTH ATTRIBUTE          X03008
D3       EQU   3                       DISPLACEMENT OF UCB FLAGS X03008
D4       EQU   4                       DISP INTO XCTL PARM LIST  X03008
BCTREG   EQU   4                       BCT ON 32 BIT MASK        X03008
TCBREG   EQU   4                       POINTER TO OLTEP TCB      X03008
HEX04    EQU   X'04'                   UCBNALOC MASK             X03008
L4       EQU   4                       DISPLACEMENT CONSTANT     X03008
IOSBREG  EQU   5                       IOSB PTR                  Y02008
WORKRG   EQU   5                       WORK REG                  Y02008
UCBTAB   EQU   5                       POINTER REGISTER          X03008
D5       EQU   5                       DISPLACEMENT FIELD        X03008
R5       EQU   5                       GENERAL PURPOSE REGISTER  X03008
HEX06    EQU   X'06'                   ZUES I  MASK IN UCB       X03008
HEXADDR  EQU   6                       DEVICE ADDR IN HEX        X03008
D6       EQU   6                       DISPLACEMENT FIELD        X03008
UCMREG   EQU   6                       USED IN FENC 33 WTOR      X03008
SCAN     EQU   6                       POINTER REGISTER          X03008
WKREG6   EQU   6                       WORKING REGISTER          X03008
DEVTBL   EQU   6                       PTR TO DEVTAB             Y02008
WORKREG  EQU   6                       REG USED IN GETTIN CORE   Y02008
ASCBREG  EQU   6                       DE SECT REG               Y02008
R6       EQU   6                       WORK REG                  Y02008
CSDREG   EQU   6                       CSD POINTER               Y02008
CALLER   EQU   6                       SAVE CALLCODE HERE        X03008
L6       EQU   6                       DISPLACEMENT CONSTANT     X03008
UCBREG   EQU   6                       POINTER TO UCB            X03008
PPLREG   EQU   6                       PTR TO PURGE PARMLIST     Y02008
LDAREG   EQU   6                       LDA REGISTER              Y02008
HEX07    EQU   X'07'                   ZUES II MASK IN UCB       X03008
D7       EQU   7                       LOW BYTE DEV ADDR         X03008
TDEBREG  EQU   7                       USED IN MOVE DEB FUNCTION X03008
R7       EQU   7                       REGISTER SEVEN            Y02008
WKREG7   EQU   7                       WORKING REG               Y02008
SAVE1    EQU   7                       SAVE REG FOR UCBTAB SIZE  Y02008
RQEREG   EQU   7                       USED IN FENC 33 WTOR      X03008
UCBADDR  EQU   7                       POINTER TO UCB            X03008
D8       EQU   8                       DISP TO UCB ADDR IN DEVTABX03008
SHIFTER  EQU   8                       SHIFT REG FOR MASKS       X03008
R8       EQU   8                       GENERAL PURPOSE REGISTER  X03008
TCCWREG  EQU   8                       POINTER TO TCCW           Y02008
DUMMY1   EQU   8                       USED TO CLEAR UCBTAB      Y02008
DIEPTR   EQU   8                       POINTER TO DIE PREFACE    Y02008
MODELREG EQU   8                       POINTER TO MODEL IOSB     Y02008
PARM1    EQU   8                       1ST PARAMETER             Y02008
L8       EQU   8                       DISPLACEMENT CONSTANT     X03008
ERRCODE  EQU   8                       RETURN CODE               X03008
DEB254RG EQU   8                       USED IN MOVE DEB FUNCTION X03008
DEVTAB   EQU   8                       POINTER TO DEVTAB ENTRY   X03008
DEVREG   EQU   DEVTAB                  OLTEP DEVTEB ENTRY POINTERX03008
VOLSERAD EQU   8                       REG POINTER TO VOLSER     X03008
HEX08    EQU   X'08'                   ALLOCATED FLAG IN UCB     X03008
HEX09    EQU   X'09'                   3277 TYPE                 X03008
ODDREG   EQU   9                       HOLDS WORD OF MASKS       X03008
D9       EQU   9                       DISPLACEMENT FIELD        X03008
PARMRG9  EQU   9                       PARM LIST REG             Y02008
PARM2    EQU   9                       PARAMETER REG             Y02008
R9       EQU   9                       WORK REG 9                Y02008
SAVEREG9 EQU   9                       SAVE ROPT ADDR HERE       X03008
DEVCNT   EQU   9                       COUNT OF DEVTAB ENTRIES   X03008
CNTREG   EQU   DEVCNT                  NUMBER OF DEVTABS         X03008
HEX10    EQU   X'10'                   COMMO EQUIP CLASS         X03008
DUMMY2   EQU   9                       USED TO CLEAR UCBTAB      Y02008
LINK     EQU   10                      BALR TO GETMAIN REG       Y02008
PARM3    EQU   10                      PARAMETER REG             Y02008
FREERG   EQU   10                      SAVE AREA FOR 160 BYTE AREA
SAVE     EQU   10                      SAVE REG FOR UCB COUNT    Y02008
SAVEPTR  EQU   10                      POINTER TO 16 WORD SAVE   X03008
R10      EQU   10                                              @ZA12058
BASEREG  EQU   11                      BASE REG FOR IGC0005I     X03008
D11      EQU   11                      DISP TO UCB ATTN INDEX    X03008
HEX0B    EQU   X'0B'                   USED IN ANR CHECKS        X03008
IODEVAD  EQU   12                      PTR TO 12 BYTE BLOCK      Y02008
D12      EQU   12                      DISP TO DEVTAB FLGS       X03008
WRKREG12 EQU   12                      WORK REGISTER             Y02008
L12      EQU   12                      LENGTH 12                 Y02008
COUNT    EQU   12                      USED IN LOOKUP            X03008
INDEX    EQU   13                      USED IN LOOKUP            X03008
FIXED    EQU   13                      POINTER TO ASVE AREA (BEB)Y02008
SAVEM    EQU   13                      POINTER TO DIE SAVE AREA  Y02008
SAVEREG  EQU   SAVEM                   SAVE AREA ADDR            Y02008
L13      EQU   13                      LENGTH ATTRIBUTE          X03008
R13      EQU   13                      USED IN LOOKUP            X03008
REG13    EQU   13                      AREA IN CALLING MODULE.   X03008
RETURN   EQU   14                      EXIT REGISTER             X03008
HEX14    EQU   X'14'                   CALL CODE IF FROM ABEND   X03008
D14      EQU   14                      DISP TO DEVTAB FLGS       X03008
R14      EQU   14                      SYSTEM RETURN REG         Y02008
EXIT     EQU   14                      REG TO RETURN ON          X03008
PKEYREG  EQU   15                      CONTAINS PROT. KEY        X03008
RCREG    EQU   15                      RETURN CODE REGISTER      X03008
R15      EQU   15                      SYSTEM GOTO REG           Y02008
D15      EQU   15                      DISP OF SOME FIFTEENS     Y02008
IOCREG   EQU   15                      REGISTER 15               Y02008
RETCODE  EQU   15                      REG 15, FUNC17 USES IT    X03008
*                                      ODD DIGET                 X03008
RTNCODE  EQU   RETCODE                 CONTAINS RETURN CODE      X03008
MAIN15   EQU   15                      WORK AREA REG ON XCTL     X03008
D16      EQU   16                      DISPLACEMENT 16           Y02008
L16      EQU   16                      LENGTH 16                 Y02008
D18      EQU   18                      DISP TO UCB CDS CLASS     X03008
D19      EQU   19                      DISP TO UCB CDS TYPE      X03008
D20      EQU   20                      DISP TO TCBOLTEP BIT      X03008
HEX0C    EQU   X'0C'                   OPTION TO RE-ENTER XLATE  Y02008
HEX20    EQU   X'20'                   DASD CLASS                X03008
REPLY    EQU   X'22'                   SVC NUMBER                X03008
D24      EQU   24                      DISPLACEMENT FIELD        X03008
D28      EQU   28                      VOL=SER DISP IN UCB       X03008
D31      EQU   31                      DISP TO ZUES EXPOSURE FLD X03008
D32      EQU   32                      DISP TO DEVTAB HEX ADDR   X03008
D35      EQU   35                      DISPLACEMENT FIELD        X03008
D40      EQU   40                      DISPLACEMENT 40           Y02008
HEX40    EQU   X'40'                   A FINE HEX 40             X03008
DEBVALID EQU   X'40'                   VALID DEB IDENTIFIER      X03008
D38      EQU   38                      DISPLACEMENT 38           Y02008
L48      EQU   48                      LENGTH ATTRIBUTE          X03008
D56      EQU   56                      DEVTAB ENTRY SIZE         X03008
D64      EQU   64                      DISPLACEMENT FIELD        X03008
HEX1C    EQU   X'1C'                   BTAM ATTENTION INDEX      X03008
HEX7F    EQU   X'7F'                   USED TO ZAP THE UCB ONLINEX03008
HEX80    EQU   X'80'                   OLTEP EXECUTING FLAG      X03008
HEX82    EQU   X'82'                   ALLOCATED AND ACTIVE OPER X03008
HEX88    EQU   X'88'                   MASK FOR ALLOCATED/ONLINE X03008
*                                      CONSOLE                   X03008
HEXA0    EQU   X'A0'               MASK FOR TAPE OR DASD       @ZM42051
HEXBF    EQU   X'BF'                   UCB NRDY BIT              X03008
HEXFB    EQU   X'FB'                   UCB NACL BIT              X03008
HEXF0    EQU   X'F0'                   SET IF DEV ONLINE         X03008
HEXFF    EQU   X'FF'                   MASK USED AT LOOKUP TIME  X03008
HEX200   EQU   X'200'                  HEX 200                   Y02008
L133     EQU   133                     LENGTH 133                Y02008
SMFREC   EQU   6                       PTR TO SMF RECORD         Y02008
SMCAREG  EQU   12                      SMCA ADDRESSABILITY       Y02008
TYPE11   EQU   11                      SMF RECORD TYPE 11        Y02008
SMFRECSZ EQU   24                      TOTAL SIZE OF SMF RECORD  Y02008
L24      EQU   24                      LENGTH OF 24              Y02008
K72      EQU   X'48'               SIZE OF SSIDLIST            @Y30LPAW
CPUIDOFF EQU   X'49'               OFFSET IN OCA TO CPU ID     @ZA15468
*****************************************************************Y02008
*                       SMF RECORD EQUATES                       Y02008
*****************************************************************Y02008
RECDSCWD EQU   1                       SMF REC DESC WORD         Y02008
RECTYPE  EQU   5                       SMF RECORD TYPE           Y02008
VARLNGTH EQU   19                      LENGTH OF REST OF RECORD  Y02008
UCBCLTYP EQU   20                      UCB CLASS & TYPE INFO     Y02008
DEVADDR  EQU   22                      DEVICE ADDR IN HEX        Y02008
         EJECT
         BALR  BASEREG,ZEROO           SET BASE REGISTER
         USING *,BASEREG               ID BASE REGISTER          X03008
         B     IGC00CDE                BRANCH AROUND NAME & DATE Y02008
         DC    C'IGC0005I&SYSDATE'     MODULE NAME & ASSEM DATE  Y02008
IGC00CDE EQU   *                       START OF ACTUAL CODE      Y02008
         SPACE  4
         USING TCB,TCBREG                                        Y02008
         USING CVTMAP,CVTREG                                     Y02008
         TM    TCBFLGS1,TCBFA          IS ABEND IN PROCESS       X03008
         BNO   AUTHCHCK                BRANCH IF NO,             X03008
         LA    REG1,HEX14              ELSE SET 14 IN REG 1      X03008
AUTHCHCK EQU   *                       ISSUE APF MACR0           X03008
         LR    SAVEREG9,REG0           SAVE PARMLIST ADDR        X03008
         LR    CALLER,REG1             SAVE CALLCODE             X03008
         SPACE 2                                                 X03008
         TESTAUTH  FCTN=D1             CHECK CALLERS AUTHORITY   X03008
         SPACE 2                                                 X03008
         LTR   RETCODE,RETCODE         SET RETURNED COND CODE    X03008
         BNZ   EXIT04                  IF CALL ILLEGAL TAKE BR.  X03008
***********************************************************************
*  THE FOLLOWING BRANCH TABLE WILL DIRECT CONTROL TO THE              *
*  REQUESTED FUNCTION.                                                *
*                                                                     *
***********************************************************************
         LR    REG1,CALLER             RESTORE CALL CODE         X03008
         ICM   REG1,B'0010',ZERO       ZAP OUT POSSIBLE 80 BYTE  X03008
         B     BRTAB(REG1)             LOCATE REQUESTED FUNCTION X03008
BRTAB    B     FUNC33                  REMOVE WTOR          X'00'X03008
         B     XCTLMOD9                $CUTEST              X'04'X03008
         B     FUNC2                   IS THIS MP SYSTEM    X'08'Y02008
         B     XCTLMOD9            $CUTEST SSIDCHK       X'0C' @Y30LPAW
         B     XCTLMOD9            SSIDLIST STORE        X'10' @Y30LPAW
         B     XCTLMOD5                OLTEPS CLEANUP       X'14'X03008
         B     EXIT04                  RESERVED             X'18'X03008
         B     EXIT04                  RESERVED             X'1C'Y02008
         B     EXIT04                  RESERVED             X'20'X03008
         B     EXIT04                  RESERVED             X'24'Y02008
         B     EXIT04                  RESERVED             X'28'Y02008
         B     EXIT04                  RESERVED             X'2C'Y02008
         B     EXIT04                  RESERVED             X'30'X03008
         B     EXIT04                  RESERVED             X'34'Y02008
         B     EXIT04                  RESERVED             X'38'X03008
         B     FUNC15                  VOL=SER AND UCB CHK  X'3C'X03008
         B     FUNC16                  UCBLOOKUP            X'40'X03008
         B     EXIT04                  RESERVED             X'44'Y02008
         B     FUNC18                  MOVE DEB             X'48'X03008
         B     REXLATE                  TRANSLATE ADDR      X'4C'Y02008
         B     PURGERTN                PURGE ROUTINE        X'50'Y02008
         B     FUNC21                  UCB NOT RDY CHK-SET  X'54'X03008
         B     INITALIZ                GO SET UP OLTEP JOB  X'58'Y02008
         B     STARTIO                 GO ISSUE IO OPERATN  X'5C'Y02008
         B     CPUVRFY                 VERIFY CHOSEN AFFN   X'60'Y02008
         SPACE  5
EXIT04   LA    RETCODE,HEX04           SET ABNORMAL COMPLETION   X03008
         BR    EXIT                    CODE AND EXIT MODULE      X03008
         SPACE 4                                                 X03008
XCTLMOD5 L     MAIN15,IGC05            GET IGC0505I ADDRESS      Y02008
         B     GETMAIN                 GO GET XCTL STG           X03008
XCTLMOD9 L     MAIN15,IGC09            GET IGC0905I ADDRESS      Y02008
         SPACE 4
GETMAIN  EQU   *                       GET STG FOR REMOTE LIST   X03008
         BR    MAIN15                  GO TO IGC0505I            Y02008
*                                      OR IGC0905I               Y02008
         EJECT
FUNC15   EQU   *                                                 X03008
         LR    REG1,SAVEREG9           RESTORE ROPT ADDR         X03008
         LM    UCBADDR,VOLSERAD,D0(PARMREG) OBTAIN UCB ADDRESS   X03008
*                                      AND VOLUME  SER RETURN    X03008
*                                      ADDRESS.                  X03008
         XR    RETCODE,RETCODE         ZERO RETURN CODE REGISTER X03008
         SPACE 3
         L     CVTOLT,CVTEXT2          GET PTR TO CVT EXTENSION  Y02008
         L     WKREG6,D28(CVTOLT)      GET PTR TO DIE            Y02008
         STM   ZEROO,R15,D40(WKREG6)   SAVE ALL REGS             Y02008
C   SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,RELATED=(LOCAL,IGC0005I(D)), X
               REGS=USE                                          Y02008
         LM    ZEROO,R15,D40(WKREG6)   RSTR ALL REGS             Y02008
         SPACE 3
         TM    D3(UCBADDR),HEX80       IS THIS FINE UCB ONLINE   X03008
         BNO   VOLSERCK                NO, GO CK VOL=SER REQUEST X03008
         SPACE 3
         SPACE 3
         LA    RETCODE,HEXF0           SET ONLINE INDICATOR      X03008
VOLSERCK EQU   *                       CHECK FOR VOL=SER REQUEST X03008
         SPACE 3
         STM   ZEROO,R15,D40(WKREG6)   SAVE ALL REGS             Y02008
D        SETLOCK RELEASE,TYPE=LOCAL,RELATED=(LOCAL,IGC0005I(C)),       X
               REGS=USE                                          Y02008
         LM    ZEROO,R15,D40(WKREG6)   RSTR ALL REGS             Y02008
         SPACE 3
         CLM   CALLER,B'0010',MASKTBL    IS SECOND BYTE OF CALL  X03008
*                                      CODE A X'80'              X03008
         BNE   FINIS                   NO, DO NOT RETURN VOLSER  X03008
         ICM   WKREG6,B'0001',TCBPKF   GET OLTEP PROTECT KEY     Y02008
         SPKA  0(WKREG6)               GET INTO OLTEP PROTECT KEYY02008
         MVC   D0(L6,VOLSERAD),D28(UCBADDR)  MOVE VOL=SER        X03008
         SR    WKREG6,WKREG6           KEY ZERO                  Y02008
         SPKA  0(WKREG6)               GET BACK IN SUP STATE     Y02008
FINIS    BR    EXIT                    RETURN TO CALLER
         EJECT
FUNC16   EQU   *             CALL CODE = R1 = HEX '40'
         SPACE 1
         LR    REG1,SAVEREG9           RESTORE ROPT ADDR         X03008
         LM    DEVTAB,SAVEPTR,D0(PARMREG) OBTAIN PARAMETER LIST  X03008
*                                      FROM IFDOLT61. AT THIS    X03008
*                                      POINT DEVTAB = ADDR DEVTABX03008
*                                            DEVCNT = NUMBER OF  X03008
*                                                     DEVICES    X03008
*                                            SAVEPTR = ADDR OF   X03008
*                                                      A 16 WORD X03008
*                                                      SAVEAREA  X03008
         LR    REG13,SAVEPTR       MOVE SAVE AREA ADDRESS      @ZA12058
         L     R0,ENQDQGM          GET SPACE FOR ENQ           @ZA12058
         GETMAIN R,LV=(R0)         AND DEQ ON UCB              @ZA12058
         LR    SAVEPTR,R1          SAVE THE ADDRESS            @ZA12058
         SPACE 3                                                 X03008
LOOKUP   L     HEXADDR,D32(DEVTAB)       GET THE TWO BYTE DEVICE X03008
*                                      ADDRESS FROM DEVTAB ENTRY X03008
         SPACE 3                                                 X03008
IOSGEN   IOSGEN  UCBLOOK,REG13         GET A FINE UCB ADDR       Y02008
         SPACE 3                                                 X03008
*                                      ON RETURN FROM IOSGEN THE X03008
*                                      FOLLOWING REGS ARE VALID  X03008
*                                       UCBADDR = ADDRESS OF UCB X03008
*                                       REG 15  = RTN CODE       X03008
*                                                 0= SUCCESS     X03008
*                                                 4 = NO UCB     X03008
         SPACE 3                                                 X03008
         LTR   RTNCODE,RTNCODE         TEST FOR ZERO RETURN      X03008
         BNZ   NODEQ               GO GET NEXT DEVTAB ENTRY    @ZA12058
*                                      AT THIS POINT A VALID UCB X03008
*                                      ADDRESS IS IN UCBADDR REG X03008
         ICM   R6,B'0001',TCBPKF       GET OLTEP PROTECT KEY     Y02008
         SPKA  0(R6)                   GET IN OLTEP PROTECT KEY  Y02008
         ST    UCBADDR,D8(DEVREG)      SAVE UCB POINTER IN DEVTABX03008
         SPKA  0(0)                GET SUPERVISOR STATE AGAIN  @ZA06006
         CLC   D35(L1,DEVREG),D5(UCBADDR) DOES UCB MATCH DEVTAB  X03008
         BE    SETMODE                 YES, CONTINUE             X03008
         SPKA  0(R6)                   GET IN OLTEP PROTECT KEY  Y02008
         XC    D8(L4,DEVREG),D8(DEVREG) CLEAR UCB PTR IN DEVTAB  X03008
         SPKA  0(0)                    GET SUPERVISOR STATE AGAINY02008
         B     NODEQ               GO GET NEXT DEVTAB ENTRY    @ZA12058
SETMODE  EQU   *                                                 X03008
         SPACE  2
***********************************************************************
*                      ENQ ON Q4 FOR UCB-LOOK-UP                      *
***********************************************************************
         LA    WORKRG,10               INITIALIZE COUNT REG      Y02008
         USING UCBOB,UCBADDR       UCB ADDRESSIBILITY          @ZA02796
         CLI   UCBDVCLS,X'08'      UNIT RECORD DEVICE?         @ZA02796
         BNE   ENQIT               NO, ENQ ON UCB              @ZA12058
         CLI   UCBUNTYP,X'42'      IS IT AN MSC?               @ZA04291
         BE    ZEUS                IF YES, BYPASS VARYOFF      @ZA02796
ENQIT    EQU   *                                               @ZA12058
         MVC   D0(L12,SAVEPTR),ENQUCB MOVE THE ENQ             @ZA12058
ENQAGAIN EQU   *                                               @ZA12058
         ENQ   ,MF=(E,(SAVEPTR))   AND EXECUTE IT              @ZA12058
         LTR   R15,R15                 WAS ENQ SUCCESSFUL        Y02008
         BZ    ALOCDCRR                ENQ SUCCESSFUL-CONTINUE   Y02008
         CLI   THREE(RETCODE),HEX08 IS RET CODE AN 8           @ZA06017
         BE    ALOCDCRR                YES, ENQ OK - CONTINUE    Y02008
         STIMER WAIT,DINTVL=FIVE       DELAY FOR 5 SECONDS       Y02008
         BCT   WORKRG,ENQAGAIN         TRY ENQ ON Q4 AGAIN - MAX Y02008
*                                      OF 10 TIMES               Y02008
         OI    D15(DEVTAB),HEX04       INDICATE TO IFDOLT31 THAT Y02008X
                                       ENQ FOR THAT DEVICE WAS   Y02008X
                                       UNSUCCESSFUL              Y02008
         OI    D14(DEVTAB),HEX08   SET NO GO SWITCH            @ZA12058
         B     NODEQ               GO GET NEXT DEVTAB ENTRY    @ZA12058
ALOCDCRR EQU   *                       ENQ ON Q4 OK-CONTINUE     Y02008X
                                       DCRR CODE FOR ALLOCATION  Y02008
         TM    UCBDVCLS,UCB3COMM+UCB3DISP+UCB3UREC  IS THIS      Y02008C
                                       DEVICE TP, GRAPHICS OR    Y02008C
                                       UNIT RECORD               Y02008
         BZ    ZEUS                    NO - GO MAKE ZEUS CHECK   Y02008
         L     R6,UCBEXTPT         GET UCB COMMON              @Z40MIJR
         USING UCBCMEXT,R6         EXTENSION                   @Z40MIJR
         TM    UCBFLP1,UCBERLOG    DOES THIS DEV HAVE A        @Z40MIJR
*                                  BUFFERED ERROR LOG          @Z40MIJR
         BO    ZEUS                YES, DONT VARY OFFLINE      @Z40MIJR
         DROP  R6                                              @Z40MIJR
ALOCDCR1 DS    0H                                              @ZA06006
         TM    UCBSTAT,UCBALOC         IS DEVICE ALLOCATED       Y02008
         BO    ZEUS                    YES, BYPASS ALOC DCRR-DEV Y02008X
                                       DOES NOT MEET REQUIREMENTSY02008
         TM    UCBSTAT,UCBONLI         IS DEVICE ONLINE          Y02008
         BNO   ZEUS                    NO , BYPASS ALOC DCRR-DEV Y02008X
                                       DOES NOT MEET REQUIREMENTSY02008
         TM    UCBSTAT,UCBSYSR         IS THIS ACTIVE CONSOLE    Y02008
         BO    ZEUS                    YES, DONT TAKE DEVICE     Y02008X
                                       OFFLINE                   Y02008
         TM    UCBSTAT,UCBDADI         IS STATUS CHANGING        Y02008
         BO    ZEUS                    YES, BYPASS ALOC DCRR-DEV Y02008X
                                       DOES NOT MEET REQUIREMENTSY02008
         NI    UCBSTAT,HEXFF-UCBONLI-UCBCHGS DEVICE IS OFFLINE & Y02008X
                                       PENDING OFFLINE BIT IS 0  Y02008
         OI    UCBWGT,UCBVORSN         INDICATE TO RE-CONFIG     Y02008C
                                       COMMANDS THAT OPERATOR HASY02008C
                                       TAKEN DEVICE OFFLINE      Y02008
         OI    D15(DEVTAB),HEX08       INDICATE TO IFDOLT31 THAT Y02008X
                                       DEVICE WAS VARIED OFFLINE Y02008X
                                       MSG MUST BE OUTPUTTED     Y02008
**           WRITE SMF RECORD IF IT CAN BE DONE                  Y02008
         USING SMCABASE,SMCAREG        SMCA ADDRESSABILITY       Y02008
         L     SMCAREG,CVTSMCA         GET PTR TO SMF CONTROL BLKY02008
         LTR   SMCAREG,SMCAREG         IS PTR ZERO               Y02008
         BZ    ZEUS                    YES, DONT WRITE RECORD    Y02008
         TM    SMCAOPT,SMCAOPT1        IS JOB ACCOUNTING ACTIVE  Y02008
         BNO   ZEUS                    NO, CANNOT WRITE SMF REC  Y02008
         TM    SMCAMISC,SMCAUSER+SMCAMAN IS SMF RECORDING REQ OR Y02008X
                                         IS THERE A SYS1.MAN DS  Y02008
         BNO   ZEUS                    NO, CANNOT WRITE SMF REC  Y02008
         L     SMFREC,CVTEXT2          USE SAVE AREA IN IFDOLT57 Y02008
         L     SMFREC,D28(SMFREC)      AS WORK AREA TO CONSTRUCT Y02008
         LA    SMFREC,D40(SMFREC)      SMF RECORD.NOTE PTR TO    Y02008X
                                       IFDOLT57 IS IN CVTEXT     Y02008
         XC    D0(L24,SMFREC),D0(SMFREC) CLEAR SMF RECORD AREA   Y02008
         MVI   RECDSCWD(SMFREC),SMFRECSZ PUT TOTAL SIZE OF REC   Y02008X
                                         IN REC DESC WORD        Y02008
         MVI   RECTYPE(SMFREC),TYPE11  PUT RECORD TYPE IN RECORD Y02008
         MVI   VARLNGTH(SMFREC),L6     PUT LENGTH OF REST OF REC Y02008X
                                       IN THE RECORD             Y02008
         MVC   UCBCLTYP(L2,SMFREC),UCBTBYT3 PUT CLASS AND TYPE INY02008X
                                             RECORD              Y02008
         MVC   DEVADDR(L2,SMFREC),UCBCHAN PLACE HEX DEVICE ADDR  Y02008X
                                          IN RECORD              Y02008
         LR    R1,SMFREC               PUT PTR TO SMF REC IN R1  Y02008
         SVC   83                      ISSUE SMF SVC TO WRITE RECY02008
         DROP  SMCAREG
ZEUS     EQU   *                       MAKE SPECIAL ZEUS 2305 CK X03008
         CLI   D18(UCBADDR),HEX20      IS THIS A DASD            X03008
         BNE   SETFALSE                NO, SET FAKE EXPOSURE     X03008
         CLI   D19(UCBADDR),HEX06      YES, IS THIS A ZEUS I     X03008
         BE    EXPOSURE                YES, GO CALCULATE BASE    X03008
*                                      UCB ADDR                  X03008
         CLI   D19(UCBADDR),HEX07      IS THIS A ZEUS II 2305    X03008
         BE    EXPOSURE                                          X03008
SETFALSE EQU   *                       AT THIS POINT A FALSE     X03008
*                                      EXPOSURE WILL BE PLACED   X03008
*                                      IN THE DEVTAB FOR IFDOLT35X03008
         ICM   R6,B'0001',TCBPKF       GET OLTEP PROTCT KEY      Y02008
         SPKA  D0(R6)                  GET INTO OLTEP PROTECT KEYY02008
         MVI   D31(DEVREG),HEXFF       SET FF IN DEVTAB          X03008
         B     ALOCHK                  GO CHECK ALLOCATION       X03008
EXPOSURE EQU   *                       CALCULATE ZEUS BASE UCB   X03008
*                                      ADDRESS AND EXPOSURE      X03008
         ICM   R6,B'0001',TCBPKF       GET OLTEP PROTCT KEY      Y02008
         SPKA  D0(R6)                  GET INTO OLTEP PROTECT KEYY02008
         MVC   D31(L1,DEVREG),D7(DEVREG) MOVE IN EBCDIC CHAR     X03008
         NI    D31(DEVREG),HEX07       CHANGE IT TO AN EXPOSURE  X03008
*                                      NUMBER FROM 00 TO 07 RANGEX03008
         LA    WKREG1,HEX200           GET HEX 200 OFFSET        YM4987
         L     UCBADDR,UCBBASE         GET PTR TO BASE UCB-200   YM4987
         AR    UCBADDR,WKREG1          POINT TO COMMON BASE UCB  YM4987
         ST    UCBADDR,D8(DEVTAB)      SAVE ZEUS UCB ADDRESS
ALOCHK   EQU   *                       UCBNALOC CHECK IS HERE    X03008
         SR    R6,R6                   KEY ZERO                  Y02008
         SPKA  D0(R6)                  GET SUPERVISOR STATE AGAINY02008
         SPACE 3                                                 X03008
         B     ALOCATED                YES, CONTINUE SCAN        YM7205
     EJECT
*                                      THIS SUB ROUTINE WILL     X03008
*                                      DETERMINE IF OLTEP TURNED X03008
*                                      ON THE UCBNALOC FOR A     X03008
*                                      UCB RETURNED FROM IOSGEN  X03008
UNALCCHK EQU   *                                                 YM7205
         SPACE 3
         STM   ZEROO,RCREG,D0(REG13)   SAVE ALL REGS             X03008
         L     UCBTAB,CVTEXT2          GET CVT EXTENTION         X03008
         L     UCBTAB,D28(UCBTAB)      GET CVTOLTEP WORD         X03008
         L     UCBTAB,D28(UCBTAB)      GET MASK TABLE ADDRESS    Y02008
         L     SCAN,CVTILK2            GET IOS UCB LOOK ADDR     X03008
         SPACE  3
SETCOUNT EQU   *                       CHECK EACH UCB            X03008
         LA    UCBTAB,D4(UCBTAB)       BUMP TO MASK BITS (NEXT)  X03008
         LA    BCTREG,32               SET COUNT TO 32 SHIFTS    X03008
         L     ODDREG,D0(UCBTAB)       GET A WORD OF MASK BITS   X03008
         SPACE 2
SHIFTIT  EQU   *                       GET A FINE BIT            X03008
         XR    SHIFTER,SHIFTER         ZERO REG FOR SHIFT        X03008
         SLDL  SHIFTER,ONE             GET THE NEXT BIT          X03008
         LTR   SHIFTER,SHIFTER         WAS THE BIT ON..          X03008
         BZ    BUMPUP                  NO, GO GET NEXT UCB ADDR  X03008
         SPACE 1
         ICM   SHIFTER,B'0011',D0(SCAN) GET UCB ADDR FROM TBL    X03008
         CR    SHIFTER,UCBADDR         IS THE DEVICE OLTEPS..... X03008
         BNE   BUMPUP                  TRY NEXT UCB              YM7205
SHIFTIT1 EQU   *                                               @Y30LPAW
         LM    ZEROO,RCREG,D0(REG13)   RESTORE REGS              YM7205
         B     NEXTUCB                 GO TO NEXT DEVTAB ENTRY   YM7205
         SPACE 4
BUMPUP   EQU   *                       GET THE NEXT UCB ADDR FROMX03008
         LA    SCAN,D2(SCAN)           IOS  LOOK UP TABLE        X03008
         CLI   D1(SCAN),HEXFF          IS THIS THE TABLES END..  X03008
         BE    ERRORDEV                IF YES, NO GO DEVICE      X03008
         SPACE 2
         BCT   BCTREG,SHIFTIT          GO GET NEXT MASK BIT     X03008
         B     SETCOUNT                GO GET NEXT WORD OF MASKS X03008
         SPACE 2
ERRORDEV EQU   *                       AT THIS POINT DEV IS NOT  X03008
*                                      OLTEPS TO TEST..          X03008
         TM    UCBDVCLS,X'08'      UNIT RECORD DEV?            @Y30LPAW
         BZ    ERRORDV1            IF NOT-GO AROUND            @Y30LPAW
         CLI   UCBUNTYP,X'42'      IS IT AN MSC?               @ZA04291
         BE    SHIFTIT1            IF YES-ITS OK               @Y30LPAW
ERRORDV1 EQU   *                                               @Y30LPAW
         LM    ZEROO,RCREG,D0(REG13)   RESTORE ALL REGS          X03008
NOGODEV  EQU   *                                                 Y02008
         OI    D14(DEVTAB),HEX08       YES, SET NO GO SWITCN     X03008
         B     NEXTUCB                 GO TO NEXT DEVTAB ENTRY   X03008
         SPACE  2
ALOCATED TM    D3(UCBADDR),HEX08       IS DEVICE ALLOCATED       X03008
         BNO   ONLINECK                NO, GO CHECK ONLINE STATUSX03008
         OI    D14(DEVTAB),HEX04       SET ALLOCATED BIT IN TAB  X03008
         SPACE  2
ONLINECK TM    D3(UCBADDR),HEX80       IS DEVICE ONLINE          X03008
         BNO   PSEUDO                  NO, GO CHK REI DEVTAB     X03008
         OI    D12(DEVTAB),HEX80       SET ONLINE BIT IN DEVTAB  X03008
         SPACE  2
PSEUDO   TM    D14(DEVREG),HEX01       IS THIS A PSEUDO DEVTAB   X03008
         BO    REICHECK                YES, GET OUT OF MAIN LINE X03008
         SPACE 2
MIXEDCK  TM   D3(UCBADDR),HEX88        IS DEV ALLOC OR ONLINE    X03008
         BNZ  ANRCHECK                 EITHER ON CHECK ANR GUYS  X03008
         SPACE 2
UCBUNALC EQU   *                       SET UCBNALOC ON           Y02008
         TM    UCBFL5,UCBNALOC     IS UCBNALOC ON              @ZA12058
         BO    UNALCCHK            BR IF YES                   @ZA12058
         OI    UCBFL5,UCBNALOC     SET IT ON IF NO             @ZA12058
         L     UCBTAB,CVTEXT2          GET CVT EXTENTION ADDR    X03008
         L     UCBTAB,D28(UCBTAB)      GET CVT OLTEP POINTER     X03008
         L     UCBTAB,D28(UCBTAB)      GET MASK TABLE ADDRESS    Y02008
         L     SCAN,CVTILK2            GET IOS UCBLOOK TAB       X03008
         XR    COUNT,COUNT             ZERO THE REG              X03008
         SPACE 3
MATCHUP  EQU   *                                                 X03008
         CLM   UCBADDR,B'0011',D0(SCAN) IS THIS THE UCB        @ZA12058
         BE    MASKUP                  IF YES GO SET BIT         X03008
         LA    SCAN,D2(SCAN)       IF NO, GET NEXT UCB DISP    @ZA12058
         LA    COUNT,D1(COUNT)         AND BUMP COUNTER          X03008
         B     MATCHUP                 GO CHECK NEW UCB ADDR     X03008
         SPACE 2
MASKUP   EQU   *                                                 X03008
         LR    SCAN,COUNT          SAVE THE OFFSET             @ZA12058
         SRL   COUNT,D3            SHIFT OFF BIT OFFSET        @ZA12058
         N     SCAN,COUNTMSK       MASK OFF BYTE OFFSET        @ZA12058
         LA    SCAN,MASKTBL(SCAN)  LOCATE MASK TO BE USED      @ZA12058
         LA    COUNT,D4(UCBTAB,COUNT) LOCATE BYTE TO BE SET    @ZA12058
         OC    D0(D0,COUNT),D0(SCAN) OR ON THE BIT             @ZA12058
         B     NEXTUCB                                         @ZA12058
         SPACE  2
ANRCHECK EQU   * AT THIS POINT DEV IS ONLINE, ALLOC ,OR BOTH     X03008
         SPACE   3
         CLI   D18(UCBADDR),HEX10      IS THIS COMMO EQUIPMENT   X03008
         BNE   NEXTUCB                 GO GET NEXT DEVTAB ENTRY  X03008
         CLI   D19(UCBADDR),HEX09      IS THIS A 3277 DISPLAY    X03008
         BL    NEXTUCB                 GO GET NEXT DEVTAB ENTRY  X03008
         CLI   D19(UCBADDR),HEX0B      IS THIS A 3284 OR 3286    X03008
         BH    NEXTUCB                 GO GET NEXT DEVTAB ENTRY  X03008
         TM    D3(UCBADDR),HEX82       IS THIS THE ACTIVE OP     X03008
*                                      CONSOLE                   X03008
         BNO   SETOLTEX                NO, GO SET OLTEX FLG = 1  X03008
SETERRON OI    D14(DEVTAB),HEX80       SET ANR NO TEST SWITCH    X03008
         B     NEXTUCB                 GO GET NEXT DEVTAB ENTRY  X03008
         SPACE 2
SETOLTEX EQU   *                       IS THIS A BTAM DEVICE     X03008
         L     R6,UCBEXTPT         GET ADD OF UCB EXT          @ZA06016
         USING UCBCMEXT,R6         EST ADD OF UCB EXT          @ZA06016
         CLI   UCBATI,HEX1C        IS IT A BTAM DEVICE         @ZA06016
         DROP  R6                                              @ZA06016
         BNE   SETERRON                GO SET ANR NO GO SW       X03008
         OI    UCBGCB,UCBOLTEP     TURN ON UCBOLTEP BIT        @ZA12058
         B     UCBUNALC                SET UCBNALOC & MSK BITS   Y02008
         SPACE 3                                                 X03008
         SPACE 3                                                 X03008
REICHECK EQU   * MAKE SPECIAL CKS FOR REI BOX (NON-TEST DEVICE)  X03008
         TM    D3(UCBADDR),HEX08       IS DEVICE ALLOCATED?      X03008
         BO    NEXTUCB                 IF YES, EXIT AS A NO GO   X03008
         NI    D3(UCBADDR),HEX7F       ZERO OUT ONLINE BIT       X03008
         TM    UCBFL5,UCBNALOC     IS THE BIT ON               @ZA12058
         BO    REICHK2             BR IF YES                   @ZA12058
         OI    UCBFL5,UCBNALOC     SET THE BIT ON              @ZA12058
         L     SCAN,CVTEXT2            GET CVT EXTEN ADDR        X03008
         L     SCAN,D28(SCAN)          GET ADDR OF OLTEPS TABLE  X03008
         ST    UCBADDR,D32(SCAN)       SAVE REI UCB ADDR
         B     NEXTUCB                 AND CONTINUE AS IF NON-   X03008
*                                      PSEUDO DEVTAB ENTRY       X03008
REICHK2  EQU   *                                                 Y02008
         L     R6,CVTEXT2              GET CVT EXTENSION PTR     Y02008
         L     R6,D28(R6)              GET CVT OLTEP PTR         Y02008
         TM    D4(R6),X'40'            IS REI ACTIVE             Y02008
         BNO   NOGODEV                 CAN'T USE THIS DEVICE     Y02008
         SPACE  3
         SPACE 3                                                 X03008
NEXTUCB  EQU   *                                               @ZA12058
         SPACE 3                                                 X03008
         MVC   D0(L12,SAVEPTR),DEQUCB MOVE THE DEQ             @ZA12058
         DEQ   ,MF=(E,(SAVEPTR))   AND EXECUTE IT              @ZA12058
NODEQ    EQU   *                                               @ZA12058
         LA    DEVREG,D56(DEVREG)  GET NEXT DEVTAB ENTRY       @ZA12058
         SPACE 3                                                 X03008
         BCT   CNTREG,LOOKUP           GO PROCESS NEXT DEVICE    X03008
         L     R0,ENQDQGM          FREE THE SPACE FOR ENQ AND  @ZA12058
         FREEMAIN R,LV=(R0),A=(SAVEPTR) DEQ                    @ZA12058
         SPACE 3                                                 X03008
         XR    RETCODE,RETCODE         SET SUCCESSFUL RTN        X03008
         BR    EXIT                    LEAVE LOOKUP FUNCTION     X03008
         EJECT
*****************************************************************Y02008
*                                                                Y02008
*                    IS THIS AN MP ENVIRONMENT                   Y02008
*                                                                Y02008
*****************************************************************Y02008
         SPACE 2
FUNC2    EQU   *                       IS THIS MP                Y02008
         USING CSD,CSDREG              ESTABLISH CSD ADDRESSABILITY
         L     CSDREG,CVTCSD           GET CSD POINTER           Y02008
         SR    R1,R1                   ZERO RETURN REG           Y02008
         TM    CSDFLAGS,CSDMP          IS THIS MP                Y02008
         BNO   D0(RETURN)              NO - RETURN 00 IN R1      Y02008
         LA    R1,X'80'                YES, RETURN 80 IN R1      Y02008
         BR    RETURN                  RETURN TO CALLER          Y02008
         EJECT
*****************************************************************Y02008
*                                                                Y02008
* THIS IS FUNCTION 58-----OLTEP INITIALIZATION                   Y02008
*                                                                Y02008
*****************************************************************Y02008
INITALIZ EQU   *                       INITIALIZATIONNNN
*****************************************************************Y02008
*                                                                Y02008
*              OBTAIN STORAGE FOR DISABLED INTERRUPT EXIT      @ZA15468
*                                                              @ZA15468
*              PARM1 POINTS TO ADDRESS OF IFDOLT57             @ZA15468
*              PARM2 POINTS TO ADDR IN OCA FOR MOVED IFDOLT57  @ZA15468
*              PARM3 POINTS TO ADDRESS OF SCT                  @ZA15468
*                                                              @ZA15468
***************************************************************@ZA15468
         LM    PARM1,PARM3,D0(PARMRG9) GET PARM LIST
         L     R0,D12(PARM1)      GET DIE SIZE
         ICM   R0,B'1000',SUB245  GET SUBPOOL NAME '245'
         GETMAIN R,LV=(0)         ISSUE GETMAIN FOR DIE
         STIDP D0(R1)              GET CPU ID                  @ZA15468
         ICM   R6,B'0001',TCBPKF       GET OLTEP PROTECT KEY     Y02008
         SPKA  0(R6)                   GET IN OLTEP PROTECT KEY  Y02008
         ST    R1,D0(PARM2)       SAVE DIE (NEW) ADDR IN OCA
         MVC   CPUIDOFF(D5,PARM3),D1(R1) MOVE CPUID TO OCA     @ZA15468
         SR    R6,R6                   KEY ZERO                  Y02008
         SPKA  0(R6)                   GET SUPERVISOR STATE AGAINY02008
         LR    WORKRG,R1          SAVE ADDR
         SPACE 2
*****************************************************************Y02008
*                                                                Y02008
*                       IS THIS THE 2ND OLTEP                    Y02008
*                                                                Y02008
*****************************************************************Y02008
         L     CVTOLT,CVTEXT2     GET EXTENTION ADDR
         SR    WORKREG,WORKREG         ZERO  WORKREG FOR CS      Y02008
         CS    WORKREG,WORKRG,D28(CVTOLT) IS THIS 2ND OLTEP      Y02008
         BNE   FREEUP1                 YES, TERMINATE            Y02008
         LA    CVTOLT,D28(CVTOLT) SET REG TO CVTOLTEP WORD
         B     MOVEAPDG                MOVE DIE                  Y02008
         SPACE 2
*****************************************************************Y02008
*                                                                Y02008
*                       FREE STORAGE FOR DIE AREA                Y02008
*                                                                Y02008
*****************************************************************Y02008
FREEUP1  EQU   *                  FREE DIE SPACE                 Y02008
         L     R1,D0(PARM2)       GET STG ADDR TO BE FREED
         L     R0,D12(PARM1)      GET DIE SIZE FOR FREEING
         ICM   R0,B'1000',POOL245 SET SUBPOOL ADDR
         FREEMAIN R,LV=(0),A=(1)       RELEASE STORAGE           Y02008
         LA    R15,8              SET NO GO RETURN CODE
         BR    RETURN             AND EXIT MODULE
*****************************************************************Y02008
*                                                                Y02008
*                            MOVE DIE TO SUBPOOL                 Y02008
*                                                                Y02008
*****************************************************************Y02008
MOVEAPDG EQU   *                  MOVE DIE
         L     R0,D0(PARM2)       GET SUBPOOL 245 ADDR   =TO AD
         L     R1,D12(PARM1)      GET DIE SIZE           =TO SZ
         LR    R6,PARM1           GET DIE ADDR           =FROM AD
         LR    R7,R1              GET DIE SIZE           =FROM SZ
         SPACE 2
*****************************************************************Y02008
*                                                                Y02008
*              MOVE THE DIE                                      Y02008
*                                                                Y02008
*****************************************************************Y02008
         MVCL  R0,R6              MOVE IT TO 245
         L     WORKRG,D0(PARM2)   GET SP 245 ADDR
         ST    TCBREG,D8(WORKRG)  SAVE TCB IN DIE PREFACE
         ST    WORKRG,D0(CVTOLT)     PUT 245 ADDR IN CVTOLTEP
         SPACE 2
*****************************************************************Y02008
*                                                                Y02008
*                     SAVE ASCBASID IN DIE                       Y02008
*                                                                Y02008
*****************************************************************Y02008
         USING ASCB,ASCBREG                                      Y02008
         L     R6,CVTTCBP              FIND PTR TO CURRENT ASCB  Y02008
         L     ASCBREG,D12(R6)                                   Y02008
         MVC   D38(L2,WORKRG),ASCBASID SAVE ASID IN DIE          Y02008
         DROP  ASCBREG
         XR    R15,R15                 SET GO RETURN CODE
         SPACE 2
*****************************************************************Y02008
*                                                                Y02008
*         OBTAIN STORAGE AND BUILD UCB MASK TABLE                Y02008
*                                                                Y02008
*****************************************************************Y02008
         L     SCAN,CVTILK2        GET CVT LOOK UP ADDR          X03008
         XR    COUNT,COUNT         ZERO COUNT REG                X03008
         XR    WKREG7,WKREG7           CLEAR OUT WORK REG        Y02008
FETCHUCB ICM   WKREG7,B'0011',D0(SCAN)  GET UCB ADDR             Y02008
         CLM   WKREG7,B'0001',TABEND   IS THIS THE LAST          Y02008
*                                      UCB IN THE SYSTEM         X03008
         BE    GETSTORG                GO GET STORAGE            Y02008
         LA    SCAN,D2(SCAN)           NO, GET NEXT UCB ADDR     X03008
         LA    COUNT,D1(COUNT)         AND BUMP COUNT            X03008
         B     FETCHUCB                THEN COUNT TO END         X03008
GETSTORG EQU   *                       GET STORAGE FOR MASK TABLEY02008
         LR    SAVE,COUNT              SAVE ACTUAL COUNT         X03008
         L     WORKREG,CVTEXT2         GET CVT EXT ADDR          X03008
         L     WORKREG,D28(WORKREG)    GET CVT OLTEP ADDR        X03008
         SRL   COUNT,THREE         DIVIDE BY 8                 @Y30LPAW
         LA    COUNT,K72(COUNT)    PLUS SIZE FOR SSIDLIST      @Y30LPAW
         LA    COUNT,D15(COUNT)        BUMP BY 15                X03008
         SRL   COUNT,THREE         AND ROUND OFF                 X03008
         SLL   COUNT,THREE         TO A DOUBLE WORD              X03008
         LR    SAVE1,COUNT         SAVE UPDATED VERSION          X03008
         ICM   COUNT,B'1100',SP245     GET SUBPOOL NUMBER        Y02008
         LR    R0,COUNT            SET R0 FOR GETMAIN            X03008
         GETMAIN R,LV=(0)          OBTAIN STG FOR UCBMSKTBL      X03008
         ST    R1,D28(WORKREG)         SAVE TABLE ADDR IN DIE    Y02008
         ST    TCBREG,D8(WORKREG)      SAVE TCB ADDR IN DIE      Y02008
         LR    WORKREG,R1              ZERO AREA OBTAINED
         XR    DUMMY1,DUMMY1           ZERO OUT REG
         XR    DUMMY2,DUMMY2           ZERO OUT REG
         MVCL  WORKREG,DUMMY1          MOVE TO SUBPOOL
         ST    SAVE,D0(R1)         SAVE UCB ACTUAL COUNT         X03008
         SPACE 2
*****************************************************************Y02008
*                                                                Y02008
*                   OBTAIN STORAGE FOR REI DEB                   Y02008
*                                                                Y02008
*****************************************************************Y02008
         L     POOLSIZE,POOLCNST       SET REG ZERO FOR SP+SIZE  X03008
         GETMAIN R,LV=(0)              OBTAIN STORAGE FOR        X03008
         LR    WORKRG,R1               SAVE REI DEB  ADDR IN R5 YM08149
         SPACE 3                                                 X03008
         OI    D20(TCBREG),HEX02       SET TCBOLTEP BIT TO ONE   X03008
*                                      FOR ABEND CONDITION       X03008
         SPACE 2
*****************************************************************Y02008
*                                                                Y02008
*           FIND 2955 UCB, IF ONE EXISTS                         Y02008
*                                                                Y02008
*****************************************************************Y02008
         SPACE 3                                                 X03008
         L     UCBREG,CVTILK2          GET UCBADDRS TABLE FROM   X03008
         SR    RETCODE,RETCODE         CVT, SET RI5 TO ZERO      X03008
         SR    WORKREG2,WORKREG2       REG FOR REIUCB ADR       YM08149
         SR    UCBADDR,UCBADDR         AND ZERO UCB ADDR REG     X03008
LOOKUP2  ICM   UCBADDR,B'0011',D0(UCBREG)   GET UCB ADDR         X03008
         LTR   UCBADDR,UCBADDR         SET CONDITION CODE        X03008
         BZ    BUMPUCB                 IS THIS A NULL UCB ADDR   X03008
         TM    D1(UCBREG),HEXFF        IS THIS THE TABLES END    X03008
         BO    VIRTEST                 GO MAKE V=V TESTS         X03008
         CLC   D18(L2,UCBADDR),HEX4014 IS THIS A 2955            X03008
         BNE   BUMPUCB                 NO- GET NEXT UCB ADDR     X03008
         LR    WORKREG2,UCBADDR        SAVE RETAIN UCB ADDRESS  YM08149
         B     VIRTEST                 GO SEE IF VIRTUAL         X03008
         SPACE 3                                                 X03008
BUMPUCB  LA    UCBREG,D2(UCBREG)       GET PTR TO NEXT UCB       X03008
         B     LOOKUP2                 CONTINUE SCAN OF UCB'S    X03008
         SPACE 2
*****************************************************************Y02008
*                                                                Y02008
*                  DETERMINE IF OLTEP IS IN VIRTUAL OR REAL      Y02008
*                                                                Y02008
*****************************************************************Y02008
         SPACE   3
VIRTEST  SR    VIRTUAL,VIRTUAL         ZERO OUT REG FOR V=V CKS  X03008
         TM    TCBFLGS6,TCBRV          IS OLTEP OPERATING V=V    X03008
         BO    NOTVIRT                 NO, DONOT SET VIRT INDICATX03008
         SR     R7,R7                  CLEAR REG 7 FOR ASID    @YM08149
         L     SAVEREG,CVTEXT2     LOAD ADD OF CVT EXT         @ZA09691
         L     SAVEREG,D28(SAVEREG) LOAD ADD OF DIE FROM CVTOL @ZA09691
         LA    SAVEREG,D40(SAVEREG) LOAD ADD OF SA FROM DIE    @ZA09691
LOKIT    SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=SAVE,RELATED=UNLOKT
         LTR   R15,R15             CHECK RETURN CODE           @ZA09691
         BZ    LOKT                ZERO AND                    @ZA09691
         CH    R15,FOUR            FOUR ARE OKAY               @ZA09691
         BE    LOKT                                            @ZA09691
         B     RETCODE4            ALL ELSE BAD                @ZA09691
LOKT     EQU   *                                               @ZA09691
         USING ASCB,ASCBREG            ASCB ADDRESSABILITY     @YM08149
         L     R6,CVTTCBP              GET PTR TO 4 WORD LIST  @YM08149
         L     ASCBREG,D12(R6)         GET ASCB PTR            @YM08149
         OI    ASCBFLG1,ASCBNSWP       NON SWAPPABLE MEMORY    @YM08149
UNLOKT   SETLOCK RELEASE,TYPE=LOCAL,REGS=SAVE,RELATED=LOKIT    @ZA09691
         LTR   R15,R15                                         @ZA09691
         BZ    UNLOCKED                                        @ZA09691
         CH    R15,FOUR                                        @ZA09691
         BE    UNLOCKED                                        @ZA09691
         B     RETCODE4                                        @ZA09691
UNLOCKED EQU   *                                               @ZA09691
         ICM   R7,B'0011',ASCBASID GET ASID IN R7              @ZA09691
         SR    R1,R1                                           @ZA17683
         SYSEVENT REQSWAP,ASID=(R7) SWAP ON                    @ZA17683
         CLM   R1,B'0001',FOUR+1   SWAP OK ?                   @ZA17683
         BH    RETCODE4            NO ERROR                    @ZA17683
*  SWAP-OUT HONORED OR ADDR SPACE NON SWAPPABLE                @ZA17683
         SYSEVENT DONTSWAP,ASID=(R7) MEMORY NOT SWAPPABLE      @ZA17683
         CLM   R1,B'0001',ZERO     EVERYTHING OK               @ZA17683
         BNE  RETCODE4                 NO, FNCTN NOT DONE      @YM08149
         SR    VIRTUAL,VIRTUAL         ZERO OUT REG FOR V=V CKS YM08149
         L     R6,CVTTCBP              GET PTR TO 4 WORD LIST    Y02008
         L     ASCBREG,D12(R6)         GET PTR TO ACTIVE ASCB    Y02008
         L     LDAREG,ASCBLDA          GET PTR TO LDA            Y02008
         USING LDA,LDAREG              LDA ADDRESSABILITY        Y02008
         ST    VIRTUAL,VVREGSZ         ZERO REGION SIZE IN LDA   Y02008
         ST    VIRTUAL,LDALIMIT        ZERO LDA REGION LIMIT     Y02008
HERE     ICM   VIRTUAL,B'1000',HERE    SET RET INDICATING VIRTUALX03008
NOTVIRT  EQU   *                                                 X03008
         LR    R15,WORKREG2            REIUCB ADDR IN R15        Y02008
         LR    POOLADR,WORKRG          REI DEB ADDR IN REG0      Y02008
ENDFUN   BR    14                                                X03008
RETCODE4 LA    R15,4                   SET RC=4 FOR MSG 899I   @YM08149
         BR    14                                              @YM08149
         EJECT
FUNC21   EQU   *                       CALL CODE= 54 UCB CHECKER X03008
         LR    PARMREG,SAVEREG9    RESTORE PARMS POINTER         X03008
         L     UCBADDR,D0(PARMREG)     GET UCB ADDRESS           X03008
         TM    D4(PARMREG),HEX40       SHOULD NRDY BIT BE SET ='1'
*                                                                X03008
         BNO   NORESET                 NO, DONT SET BIT          X03008
         OI    D6(UCBADDR),HEX40       YES, SET BIT ON           X03008
         BR    RETURN                  EXIT THE FINE MODULE      X03008
         SPACE  2
NORESET  EQU   *                       CHECK NRDY BIT            X03008
         TM    D6(UCBADDR),HEX40       IS NTRDY BIT =1           X03008
         BNO   D0(RETURN)              NO, EXIT MODULE           X03008
         TM    D18(UCBADDR),HEXA0  TAPE OR DASD CLASS?         @ZM42051
         BZ    SETFF               NO, GO SET RTN CODE.        @ZM42051
         NI    D6(UCBADDR),HEXBF   TURN OFF THE NTRDY BIT      @ZM42051
         BR    RETURN              EXIT THIS MODULE            @ZM42051
SETFF    LA    RCREG,HEXFF         RETRN CODE FOR IFDOLT52     @ZM42051
         BR    RETURN                  EXIT THIS MODULE          Y02008
         EJECT
*****************************************************************Y02008
*                                                                Y02008
*                            PURGE FUNCTION                      Y02008
*                                                                Y02008
*****************************************************************Y02008
PURGERTN EQU   *                       PURGE ROUTINE             Y02008
         USING IOSB,IOSBREG            IOSB ADDRESSABILITY       Y02008
         USING PPL,PPLREG              PURGE PARMLIST            Y02008
         USING TCCW,TCCWREG            TCCW ADDRESSABILITY       Y02008
         USING IOCOM,IOCREG            ADDRESSABILITY IOCOM AREA Y02008
         L     IOSBREG,D0(SAVEREG9)    GET IOSB PTR              Y02008
         LR    WRKREG12,CALLER         GET CALL CODE IN REG 12   Y02008
         SLL   WRKREG12,16             SHIFT IT TO LEFT 16 BITS  Y02008
         LTR   WRKREG12,WRKREG12       IS IT NEGATIVE            Y02008
         BM    UNFIXRTN                REQUEST WAS CALL CODE 8050Y02008
         L     R0,SUBPOOL              GET 16 BYTES-SUBPOOL 254  Y02008
         GETMAIN R,LV=(0)              GET PURGE PARMLIST        Y02008
         LR    PPLREG,R1               INITIALIZE PPLREG         Y02008
         XC    D0(16,PPLREG),D0(PPLREG) CLEAR PPL AREA           Y02008
         MVI   PPLOPT1,PPLDS+PPLHIO+PPLEXR+PPLRB DSID PRGE,NO RBYM03325X
                                       DO HIO, DONT BUILD A PIRL Y02008X
                                       PPLEXR-OPT BYTE 2 IS VALIDY02008
         MVC   PPLDSIDA(3),IOSDSID+1   PLACE DATA SET IDENTIFIER Y02008X
                                       IN PURGE PARMLIST         Y02008
         MVI   PPLDVRID,IOSOLTID       PLACE DRIVER ID IN PPL    Y02008
         CLI   D4(SAVEREG9),HEXFF      IS THIS A TP DEVICE       Y02008
         BNE   PURGE16                 NO, GO ISSUE SVC 16       Y02008
         L     R1,IOSUCB               PUT UCB ADDR IN REG 1     Y02008X
                                       FOR SVC 33                Y02008
         SVC   33                      PURGE SVC-33              Y02008
         B     UNFIXRTN                GO UNFIX & FREE TCCW BLKS Y02008
PURGE16  EQU   *                       SVC 16 - PURGE            Y02008
         SVC   16                      NORMAL PURGE SVC          Y02008
UNFIXRTN EQU   *                       UNFIX & FREE TCCW BLK RTN Y02008
         TM    USEFLAGA,VIRTCCW        ARE CCWS VIRTUAL          Y02008
         BNO   FREEPPL                 NO FREEING OR UNFIXING    Y02008
*                                      FREE UP IOSB AND EXIT     Y02008
         L     TCCWREG,TCCWPTR         TCCW ADDRESSABILITY       Y02008
         LR    R1,TCCWREG              REG 1 MUST POINT TO TCCW  YM5415X
                                       BLOCK TO GO TO CCW XLATOR YM5415
         MVI   TCCWOPTN,TCCWUNFX       OPTION TO UNFIX AND CHAIN Y02008X
                                       FOR FREEMAIN              Y02008
         L     SAVEREG,CVTEXT2         GET CVT EXTENSION PTR     Y02008
         L     SAVEREG,D28(SAVEREG)    GET CVTOLTEP PTR          Y02008
         LA    SAVEREG,D40(SAVEREG)    GET PTR TO SAVE AREA IN   Y02008X
                                       IFDOLT57 (DIE)            Y02008
         STM   R0,R15,D0(SAVEREG)      SAVE ALL REGS             Y02008
PRGELCK0 EQU   *                       OBTAIN LOCK               Y02008
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                        X
               RELATED=(LOCAL,IGC0005I(PRGELCK1)),REGS=USE       Y02008
         LM    R0,R15,D0(SAVEREG)      RESTORE ALL REGS          Y02008
         L     IOCREG,CVTIXAVL-CVTMAP(CVTREG) IO COM AREA ADDR   Y02008
         L     R15,IOCTCCW-IOCOM(IOCREG) CCW XLATOR ADDRESS      Y02008
         BALR  R14,R15                 GO TO CCW XLATOR TO UNFIX Y02008X
                                       DATA AREAS & CHAIN TCCW   Y02008X
                                       BLKS FOR FREEING          Y02008
PRGELCK1 EQU   *                       RELEASE LOCK              Y02008
         SETLOCK RELEASE,TYPE=LOCAL,                                   X
               RELATED=(LOCAL,IGC0005I(PRGELCK0)),REGS=USE       Y02008
         LM    R0,R15,D0(SAVEREG)      RESTORE ALL REGS          Y02008
         LR    R1,TCCWREG              PUT TCCWPTR IN REG1       Y02008
TCCWFREE EQU   *                       TCCW FREE ROUTINE         Y02008
         L     FREERG,D0(R1)           GET PTR TO NEXT 160 BYTE  Y02008X
                                       BLOCK TO BE FREED         Y02008
         L     R0,SP245                GET SUBPOOL ID & SIZE TO  Y02008X
                                       BE FREED                  Y02008
         FREEMAIN R,LV=(0),A=(1)       FREE BLOCK                Y02008
         LTR   FREERG,FREERG           ARE THERE ANY MORE 160    Y02008X
                                       BYTE BLOCKS TO FREE       Y02008
         BZ    FREEPPL                 GO FREE UP PPL            Y02008
         LR    R1,FREERG               PLACE ADDR OF BLOCK TO BE Y02008X
                                       FREED IN R1               Y02008
         B     TCCWFREE                GO FREE IT UP             Y02008
FREEPPL  EQU   *                       FREE PURGE PARM LIST IF   Y02008X
                                       NEEDED                    Y02008
         LTR   WRKREG12,WRKREG12       DOES PPL NEED TO BE FREED Y02008
         BM    PURGEOK                 NO, WE NEVER HAD ONE      Y02008
         L     R0,SUBPOOL              GET SUBPOOL ID & SIZE     Y02008
         LR    R1,PPLREG               GET ADDRESS OF PURGE AREA Y02008
         FREEMAIN R,LV=(0),A=(1)       FREE IT UP                Y02008
PURGEOK  EQU   *                       THIS IS EXIT RTN          Y02008
         LTR   WRKREG12,WRKREG12       IS THIS PURGE OR FREE REQ Y02008
         BM    PURGEOK1                FREE REQ-DONT TURN PURGED Y02008X
                                       FLAG ON IN IOSB
         OI    USEFLAGA,PURGED         PURGE DONE                Y02008
PURGEOK1 EQU   *                       PART OF EXIT RTN          Y02008
         NI    USEFLAGA,HEXFF-INUSE    IOSB IS FREE TO USE AGAIN Y02008
         BR    EXIT                    RETURN TO CALLER          Y02008
         EJECT
*****************************************************************Y02008
*                             FUNCTION 18                             *
*****************************************************************Y02008
FUNC18   EQU   *                       MOVE DEB  FOR REI ONLY    Y02008
         LR    PARMREG,SAVEREG9         RESTORE PARM LIST PTR    Y02008
         LM    TDEBREG,DEB254RG,D0(PARMREG) PUT PTR TO REI DEB INY02008
*                                       REG 7 & PUT PTR TO SUB-  Y02008
*                                       POOL IN REG 8.           Y02008
         IC    PKEYREG,TCBPKF           GET PROTECT KEY FROM TCB Y02008
         O     PKEYREG,DEVMSK           OR IN DEB ID OF 0000000F Y02008
         STC   PKEYREG,D24(TDEBREG)     STORE PROTECT KEY IN DEB Y02008
         ST    TCBREG,D0(TDEBREG)      PUT TCB ADDR IN DEB       YM4984
         MVC   D20(L1,TDEBREG),TCBDSP   SET DISPATCHING PRIORITY Y02008
*                                       IN DEB                   Y02008
         MVC   D0(L48,DEB254RG),D0(TDEBREG) MOVE DEB TO SUB-POOL Y02008
         L     R9,TCBDEB                PREPARE TO CHAIN DEB TO  Y02008
*                                       TCB                      Y02008
CHAINIT  EQU   *                        CHAIN ROUTINE            Y02008
         ST    R9,D4(DEB254RG)          PLACE NEXT DEB ADDR INTO Y02008
*                                       REI DEB                  Y02008
         CS    R9,DEB254RG,TCBDEB       CHAIN DEBS               Y02008
         BNE   CHAINIT                  CHAIN NOT OK, TRY AGAIN  Y02008
         L     R9,CVTEXT2               GET CVT EXTENSION ADDR   Y02008
         L     R9,D28(R9)               GET PTR TO DIE FROM CVT  Y02008
*                                       OLTEP WORD               Y02008
         TM    D4(R9),DEBVALID          HAS DEB BEEN VALIDATED   Y02008
         BO    0(RETURN)                                        ZA16982
         ST    DEB254RG,D4(R9)          SAVE REI DEB PTR AT DIE+4Y02008
         OI    D4(R9),HEX80             INDICATE THIS IS REI DEB Y02008
*                                       POINTER                  Y02008
         LR    WKREG6,DEB254RG          PREPARE TO SUBTRACT 4    Y02008
*                                       FROM DEB ADDRESS         Y02008
         SH    WKREG6,FOUR              PERFORM SUBTRACTION      Y02008
         XC    D0(L4,WKREG6),D0(WKREG6) ZERO FIRST WORD IN       Y02008
*                                       SUBPOOL FOR DEBCHK       Y02008
         L     R5,D24(DEB254RG)         GET DCB ADDRESS          Y02008
         DEBCHK (R5),TYPE=ADD,AM=EXCP   VALIDATE DEB             Y02008
         OI    D4(R9),DEBVALID          INDICATE DEB IS OK       Y02008
         BR    RETURN                  EXIT                      Y02008
         EJECT
*****************************************************************Y02008
*                                                                Y02008
*              TRANSLATE REAL ADDR TO VIRTUAL ADDRESS            Y02008
*                                                                Y02008
*                                                                Y02008
*              ROPT---------------------------------             Y02008
*                  IOSBPTR                                     Y02008
*                 ----------------------------------             Y02008
*               +4 ADDR TO BE TRANSLATED                       Y02008
*                 ----------------------------------             Y02008
*                                                                Y02008
*****************************************************************Y02008
         SPACE 2
REXLATE  EQU   *                       RE-TRANSLATE ROUTINE      Y02008
         USING TCCW,TCCWREG            TCCW BLK ADDRESSABILITY   Y02008
         USING IOCOM,IOCREG            IO COM AREA ADDRESSABILITYY02008
         USING IOSB,IOSBREG            IOSB ADDRESSABILITY       Y02008
         L     SAVEREG,CVTEXT2         GET CVT EXTENSION PTR     Y02008
         L     SAVEREG,D28(SAVEREG)    GET DIE ADDRESS           Y02008
         LA    SAVEREG,D40(SAVEREG)    GET ADDR OF SAVEAREA IN DIE02008
         STM   ZEROO,R15,D0(SAVEREG)   SAVE REGS TEMPORARILY     Y02008
TRLOCK0  EQU   *                       GET LOCK FOR TRANSLATION  Y02008
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                        X
               RELATED=(LOCAL,IGC0005I(TRLOCK1)),REGS=USE        Y02008
         LM    ZEROO,R15,D0(SAVEREG)   RESTORE ALL REGS          Y02008
         L     IOSBREG,D0(SAVEREG9)    GET IOSB PTR              Y02008
         L     TCCWREG,TCCWPTR         GET ADDR OF TCCW BLK      Y02008
         L     R0,D4(SAVEREG9)         GET ADDR TO BE TRANSLATED Y02008
         MVI   TCCWOPTN,TCCWCSWX       PLACE OPTION BYTE IN TCCW Y02008
         LR    R1,TCCWREG              REG 1 MUST POINT TO TCCW  YM5415X
                                       BLOCK TO GO TO CCW XLATOR YM5415
         L     IOCREG,CVTIXAVL         FIND XLATOR ADDRESS       Y02008
         L     R15,IOCTCCW                                       Y02008
         BALR  R14,R15                 GO TO XLATOR              Y02008
         LR    R15,R0                  RETURN XLATED ADDRESS     Y02008
         STM   ZEROO,R13,D0(SAVEREG)   SAVE  REGS  0-13          YM5413
         ST    R15,60(SAVEREG)         SAVE REG 15               YM5413
TRLOCK1  EQU   *                       FREE LOCK USED FOR TRANS  Y02008
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(LOCAL,IGC0005I(TRLOCK0)), X
               REGS=USE                                          Y02008
         LM    ZEROO,R15,D0(SAVEREG)   RESTORE ALL REGS          Y02008
         BR    RETURN                  EXIT
         EJECT
***********************************************************************
*                             FUNCTION 33                             *
***********************************************************************
 SPACE 2
FUNC33   EQU   *                                                 X03008
         LR   R7,CALLER                SAVE CALL CODE          @YM08149
         L    WKREG6,CVTEXT2           GET CVT EXTENT            X03008
         TM    D4(WKREG6),X'40'        IS REI ACTIVE             Y02008
         BNO   LOADUCM                 NO, GO TO NEXT CHECK      Y02008
         L    WKREG6,D28(WKREG6)       GET CVT OLTEP CONTENTS    X03008
         L     UCBADDR,D32(WKREG6)     GET REI UCB ADDR
         NI   D1(UCBADDR),HEXFB        RESET UCBNALOC TO ZIP     X03008
         XC   D24(L4,WKREG6),D24(WKREG6) ZERO REI UCB ADDR IN    X03008
*                                      OLTEP'S TABLE             X03008
         SPACE  3
LOADUCM  EQU  *                        ZAP OUT HOT MSG IF HOT    X03008
         L     UCMREG,CVTCUCB          LOAD UCM ADDRESS          X03008
         LR    PARMREG,SAVEREG9        SET PARAMETER POINTER     X03008
         USING UCM,UCMREG          SET UCM DSECT BASE
         USING OREF,RQEREG         SET RQE DSECT BASE            Y02008
         L     RQEREG,UCMRPYQ      LOAD RQE ADDRESS
MATCHECB CLC   D1(L3,PARMREG),OREECB+D1 ECB BEING FREED
         BE    PROCRPLY            YES - GO PROCESS DUMMY REPLY
         L     RQEREG,ORELKP       SET NEXT RQE POINTER
         LTR   RQEREG,RQEREG       END OF QUEUE
         BNE   MATCHECB            NO - GO CHECK NEXT ECB ADDRESS
         LA    RCREG1,ERRCODE      YES - SET ERROR CODE
         BR    RETURN              RETURN TO CALLER
PROCRPLY L     REG0,RPLYPOOL       SETUP FOR SUBPOOL 254 GETMAIN
         GETMAIN  R,LV=(0)         ISSUE SUBPOOL 254 GETMAIN
         LR    R8,R1               SAVE SUBPOOL 254 ADDRESS
         MVC   D0(L13,R1),FREEWTOR  SETUP DUMMY REPLY BUFFER
         LH    R5,OREID            LOAD MESSAGE ID
         STH   R5,D6(R1)           STORE ID IN DUMMY REPLY BUFFER
         XR    REG0,REG0           CLEAR REG FOR REPLY AUTH.
         SVC   REPLY               FREE MESSAGE
         L     REG0,RPLYPOOL       SETUP FOR SUBPOOL 254 FREEMAIN
         LR    R1,R8               RESTORE FREEMAIN REGISTER
         FREEMAIN  R,LV=(0),A=(1)  ISSUE SUBPOOL 254 FREEMAIN
         SR    RCREG1,RCREG1       SET NORMAL RETURN CODE
         LR    REG0,R5             SET ID IN RETURN REGISTER
         BR    EXIT                    EXIT TO CALLER            X03008
         EJECT
*****************************************************************Y02008
*                                                                Y02008
*                      STARTIO FUNCTION                          Y02008
*                                                                Y02008
*****************************************************************Y02008
STARTIO  EQU   *                       MVMS STARTIO FUN          Y02008
         USING SRB,SRBREG              GENERAL REG 2             Y02008
         USING IOSB,IOSBREG            GENERAL REG 5             Y02008
         USING ASCB,ASCBREG            GENERAL REG 6             Y02008
         USING TCCW,TCCWREG            GENERAL REG 8             Y02008
         USING IOCOM,IOCREG            GENERAL REG 15            Y02008
         LR    PARMREG,SAVEREG9        RESTORE PARMLIST PTR      Y02008
         LM    IOSBREG,DEVTBL,D0(PARMREG)                        Y02008
         L     IODEVAD,IODEVTAB   GET IO DEVTAB ADDR FROM OCA    Y02008
         L     SRBREG,CVTEXT2     GET CVT EXTENTION POINTER      Y02008
         L     SRBREG,D28(SRBREG) GET DIE ADDR                   Y02008
         LR    DIEPTR,SRBREG      SAVE DIE ADDR                  Y02008
         LA    SAVEREG,D40(DIEPTR)     GET ADDR OF SAVEAREA IN DIE
         LR    R15,IOSBREG             SAVE MODEL IOSB REG       Y02008
         SPACE  4
*****************************************************************Y02008
*          DETERMINE WHICH SET OF CONTROL BLOCKS CAN BE USED     Y02008
*****************************************************************Y02008
         SPACE  4
         A     SRBREG,D16(DIEPTR) GET PTR TO THE FIRST SRB IN    Y02008
*                                      S.P. 245                  Y02008
         LA    IOSBREG,SRBSIZE(SRBREG)   BUMP TO IOSB HEAD       Y02008
         USING IOSB,R15                MODEL IOSB ADDRESSABILITY Y02008
         TM    USEFLAGA,INTRNLCB  IS THIS AN INTERNAL CALL       Y02008
         DROP  R15                     DROP MODEL IOSB           Y02008
         USING IOSB,IOSBREG            REAL IOSB ADDRESSABILITY  Y02008
         BO    INTERNAL           YES, USE FIRST SET OF BLKS     Y02008
         LA    IODEVAD,D12(IODEVAD) BUMP TO NEXT IODEVTAB SET    Y02008
         LR    SRBREG,DIEPTR      GET DIE ADDR                   Y02008
         A     SRBREG,D20(DIEPTR)      GET PTR TO 2ND CTL BLKS   Y02008
         LA    IOSBREG,SRBSIZE(SRBREG) BUMP TO NEXT IOSB         Y02008
         TM    USEFLAGA,INUSE          IS THIS SET ACTIVE        Y02008
         BNO   MOVMODEL                NO, GO MOVE MODEL IOSB    Y02008
         TM    USEFLAGA,PURGED         HAS IO BEEN PURGED        Y02008
         BO    MOVMODEL                YES, GO MOVE MODEL        Y02008
         LA    IODEVAD,D12(IODEVAD)    BUMP TO 3RD SET OF BLKS   Y02008
         LR    SRBREG,DIEPTR           GET DIE ADDR              Y02008
         A     SRBREG,D24(DIEPTR)      GET THIRD IO BLK          Y02008
         LA    IOSBREG,SRBSIZE(SRBREG) BUMP TO NEXT IOSB         Y02008
INTERNAL TM    USEFLAGA,INUSE          IS SET ACTIVE             Y02008
         BNO   MOVMODEL                NO, GO MOVE  MODEL        Y02008
         TM    USEFLAGA,PURGED         HAS IO BEEN PURGED        Y02008
         BO    MOVMODEL                YES, GO MOVE MODEL        Y02008
         B     EXIT04                  RET ON BAD SITUATIONS HEREY02008
         SPACE  4
*****************************************************************Y02008
*              MOVE MODEL & SET UP CONTROL BLOCKS                Y02008
*****************************************************************Y02008
         SPACE  4
MOVMODEL L     MODELREG,D0(PARMREG)    GET PTR TO MODEL IOSB     Y02008
         MVC   D0(L133,IOSBREG),D0(MODELREG) MOVE MODEL          Y02008
         XC    OECB+1(63),OECB+1       ZERO OECB+1               Y02008
         XC    SRB(SRBSIZE),SRB   ZERO SRB                       Y02008
         LA    WKREG7,OECB             PLACE OECB PTR IN USE     Y02008
         ST    WKREG7,OECBPTR          CONTROL BLOCK             Y02008
         LA    WKREG7,IOSUSECB         FIND ADD OF OLTEPS IOS USEY02008
*                                 CONTROL BLOCK                  Y02008
         ST    WKREG7,IOSUSE           PLACE USE CNTRL BLK ADD INY02008
*                                 IOSB                           Y02008
         ST    IODEVAD,IODEVTAB   SET PTR TO IODEVTAB SLOT       Y02008
         ICM   R15,B'0001',TCBPKF      GET OLTEP PROTCT KEY      Y02008
         SPKA  0(R15)                  GET INTO OLTEP PROT KEY   Y02008
         ST    IOSBREG,D4(IODEVAD)  SET PTR TO IOSB IN OCA       Y02008
         XC    D8(L4,IODEVAD),D8(IODEVAD)  ZERO ECB             Y02008
         ST    DEVTBL,D0(IODEVAD)  SAVE DEVTAB PTR               Y02008
         SR    R15,R15                 KEY ZERO                  Y02008
         SPKA  0(R15)                  GET SUPERVISOR STATE AGAINY02008
         EJECT
***********************************************************************
         OI    USEFLAGA,INUSE      SET INUSE BIT                 Y02008
         STM   ZEROO,R15,D0(SAVEM)  SAVE REGS TEMPORARILY        Y02008
         ST    R14,D64(SAVEM)          SAVE R14 FOR EXIT         Y02008
SET      SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                        X
               RELATED=(LOCAL,IGC0005I(LT)),REGS=USE             Y02008
         LM    ZEROO,R15,D0(SAVEM)     SAVE REGS TEMPORARILY     Y02008
         TM    USEFLAGA,VIRTCCW    ARE CCW'S IN V=V              Y02008
         BNO   COPYREAL           NO, GO SET UP IOSB THINGS      Y02008
         BAL   LINK,GETCORE            GO GET STG FROM SP239     Y02008
*                                  TCCW BLK.                     Y02008
         ST    R1,TCCWPTR          PUT TCCW PTR INTO USE BLK     Y02008
         LR    TCCWREG,R1              SAVE TCCW ADDR            Y02008
         BAL   LINK,GETCORE            GO GET STG  FOR BEB       Y02008
         ST    R1,TCCWBEB              SET BEB ADDR IN TCCW      Y02008
         ST    TCBREG,TCCWTCB          SAVE TCB ADDR IN TCCW     Y02008
         BAL   LINK,GETCORE            GO GET STG  FOR FIXLIST   Y02008
         ST    R1,TCCWFIX              PUT FIXLIST ADDR IN TCCW  Y02008
         MVC   TCCWUCB(3),IOSUCB+1     MOVE UCB ADDR TO TCCW     Y02008
         MVC   TCCWFVC(4),IOSVST       MOVE 1ST VIRT CCW ADDRESS Y02008
*                                      TO TCCW                   Y02008
         MVI   TCCWOPTN,HEX00          SET OPTION BYTE           Y02008
         LR    FIXED,R1                GET AREA ADDR WHERE FIX   Y02008
*                                      WILL TAKE PLACE           Y02008
XLATEM   EQU   *                                                 Y02008
         LR    R1,TCCWREG              SET R1 TO CCW XLATE BLOCK Y02008
         L     IOCREG,CVTIXAVL-CVTMAP(CVTREG) GET IO COM AREA    Y02008
*                                      ADDRESS                   Y02008
         L     R15,IOCTCCW-IOCOM(R15)  GET CCW XLATOR ADDR       Y02008
         BALR  R14,R15                 GO XLATE CCWS             Y02008
         LTR   R15,R15                 DID XLATE GO              Y02008
         BZ    SETREAL                 YES, CONTINUE             Y02008
         CH    R15,FOUR                WAS RETURN CODE 4         Y02008
         BE    FREERTN                 YES,                      Y02008
         BAL   LINK,GETCORE            GO GET STG  FOR IOS XLATERY02008
         LR    R0,R1                   PLACE PTR TO ADDITIONAL   Y02008
*                                      AREA IN REG 0             Y02008
         MVI   TCCWOPTN,HEX0C          SET OPTION BYTE FOR       Y02008
*                                      RE-ENTRY                  Y02008
         B     XLATEM                                            Y02008
FREERTN  EQU   *                                                 Y02008
         SR    WRKREG12,WRKREG12       ZERO WORK REG 12          Y02008
         IC    WRKREG12,TCCWOPTN       SAVE OPTION BYTE          Y02008
FREE160  EQU   *                       RTN TO FREE UP 160 BYTES  Y02008
         L     FREERG,D0(R1)           GET PTR TO 1ST BLOCK OF   Y02008
*                                      160 BYTES TO BE FREED     Y02008
         L     R0,SP245                GET SP ID AND SIZE        Y02008
         L     WKREG7,CVTTCBP          GET PTR TO 4 WORD LIST    Y02008
         L     WKREG7,D12(WKREG7)      GET PTR TO CURRENT ASCB   Y02008
         FREEMAIN R,LV=(0),A=(1),BRANCH=YES GO FREE UP CORE      Y02008
         LTR   FREERG,FREERG           IS THIS THE LAST          Y02008
         BZ    SETEXIT                 GO EXIT MODULE IF ZERO    Y02008
         LR    R1,FREERG               GET NEXT ADDR TO BE FREED Y02008
         B     FREE160                 GO FREE NEXT BLK OF 160 BYTES
SETEXIT  EQU   *                                                 Y02008
         LR    R15,WRKREG12            GET OPTION TO BE RETURNED Y02008
         B     LOCKREL                 EXIT WITH ERROR           Y02008
COPYREAL EQU   *                                                 Y02008
         MVC   IOSRST(4),IOSVST        COPY CCW ADDRESS          Y02008
         B     SETASID                 GO SET ASID               Y02008
SETREAL  EQU   *                                                 Y02008
         ST    R0,IOSRST               SET REAL CCW ADDR IN IOSB Y02008
SETASID  EQU   *                                                 Y02008
         L     R6,CVTTCBP              GET PTR TO 4 WORD LIST    Y02008
         L     ASCBREG,D12(R6)         GET PTR TO ACTIVE ASCB    Y02008
         MVC   IOSASID,ASCBASID        SET IOSB ASID FIELD       Y02008
         MVC   IOSPKEY,TCBPKF          SET PROTECT KEY           Y02008
         MVC   IOSCKEY(1),TCBPKF       GET IOS C KEY FROM TCB    Y02008
         LA    R13,16                  INITIALIZE IOSB AFFN REG  Y02008
         SR    R1,R1                   ZERO WORK REG FOR AFFN    Y02008
         ICM   R1,B'0011',ASCBAFFN     GET ASCB AFFN INTO REG    Y02008
*                                      FOR CONVERSION ROUTINE    Y02008
         LTR   R1,R1                   IS THERE ANY AFFN         Y02008
         BNZ   TRYAGAIN                YES, CONTINUE             Y02008
         LA    WRKREG12,HEX04          PUT ERROR CODE IN REG 12  Y02008
         TM    USEFLAGA,VIRTCCW        WHERE CCWS VIRTUAL        Y02008
         BO    FREERTN                 YES, BLOCKS MUST BE FREED Y02008
         LA    R15,HEX04               ERROR RETURN IN REG 15    Y02008
         B     LOCKREL                 RELEASE LOCK AND EXIT     Y02008
TRYAGAIN EQU   *                                                 Y02008
         BCTR  R13,0                   UPDATE POSSIBLE IOSB AFFN Y02008
         SRA   R1,1                    FIND AFFN                 Y02008
         BNZ   TRYAGAIN                AFFN NOT FOUND, TRY AGAIN Y02008
         STC   R13,IOSAFF              AFFN FOUND, PLACE         Y02008
*                                      CONVERTED AFFN IN IOSB    Y02008
         LA    IODEVAD,D1(IODEVAD)     BUMP BY 1 FOR DSID        Y02008
         ST    IODEVAD,IOSDSID         SET PHONY DSID            02008
         MVI   IOSDVRID,IOSOLTID       PUT DRIVER ID IN IOSB     Y02008
         LA    R1,CVTBRET              GET ADDR OF BR 14 INSTRN  Y02008
         ST    R1,IOSPGAD              INITIALIZE IOS PGAD       Y02008
         ST    IOSBREG,SRBPARM         PUT IOSB ADDR IN SRB FOR  Y02008
*                                      STARTIO                   Y02008
         L     SAVEPTR,CVTEXT2         GET PTR TO CVT EXTENSION  Y02008
         L     SAVEPTR,D28(SAVEPTR)    GET PTR TO DIE            Y02008
         LA    SAVEM,D40(SAVEPTR)      GET PTR TO SAVE AREA      Y02008
         STM   ZEROO,R15,D0(SAVEM)     SAVE ALL REGS             Y02008
         STARTIO SRB=(SRBREG),TCB=(TCBREG) DO THE IO  OP         Y02008
         LM    ZEROO,R15,D0(SAVEM)     RSTR ALL REGS             Y02008
         XR    R15,R15                 ZERO R15 FOR GOOD RETURN  Y02008
LOCKREL  EQU   *                                                 Y02008
         L     SAVEPTR,CVTEXT2       GET PTR TO CVT EXTENSION   ZA16980
         L     SAVEPTR,D28(SAVEPTR)   GET PTR TO DIE            ZA16980
         LA    SAVEM,D40(SAVEPTR)    GET PTR TO SAVE AREA       ZA16980
         STM   ZEROO,R15,D0(SAVEM)    SAVE REGS TEMPORARILY
LT       SETLOCK RELEASE,TYPE=LOCAL,RELATED=(LOCAL,IGC0005I(SET)),     X
               REGS=USE                                          Y02008
         LM    ZEROO,R15,D0(SAVEM)     RESTORE ALL REGS          Y02008
         L     R14,D64(SAVEM)          RESTORE R14 FOR RETURN    Y02008
         BR    R14                    EXIT MODULE                Y02008
         EJECT
*****************************************************************Y02008
*                                                                Y02008
*        THIS FINE SUB ROUTINE DOES ALL KINDS OF STUFF           Y02008
*        AND THINGS. IT WILL BE ENTERED DYNAMICALLY              Y02008
*        FROM THE IOS CCW TRANSLATOR AS KEY ZERO, NON-           Y02008
*        PAGEABLE STORAGE BECOMES REQUIRED. IT IS ALSO           Y02008
*        ENTERED TO OBTAIN STG FOR THE  BEB, THE FIX-            Y02008
*        LIST AND THE TCCW BLOCK PRIOR TO CCW XLATE.             Y02008
*        ON ENTRY REG 10 WILL CONTAIN THE RETURN ADDR.           Y02008
*                                                                Y02008
*****************************************************************Y02008
GETCORE  EQU   *                       OBTAIN STORAGE FROM SP239 Y02008
         L     R0,SP245                SET SUBPOOL ID & SIZE     Y02008
         L     WKREG7,CVTTCBP          GET PTR TO 4 WORD LIST    Y02008
         L     WKREG7,D12(WKREG7)      GET PTR TO CURRENT ASCB   Y02008
         SPACE 1
         GETMAIN R,LV=(0),BRANCH=YES GET STG FOR CALLER          Y02008
         SPACE 2
*                                 AT THIS POINT REG 1            Y02008
*                                 CONTAIN THE ADDRESS            Y02008
*                                 OF THE SUBPOOL.                Y02008
*
         BR    LINK                    RETURN TO CALLER ON R10   Y02008
         EJECT
*****************************************************************Y02008
*                                                                Y02008
*                       VERIFY CPU SELECTION                     Y02008
*                                                                Y02008
*****************************************************************Y02008
         SPACE 2
CPUVRFY  EQU   *                       VERIFY CPU SELECTION      Y02008
         USING CSD,CSDREG                                      @ZA09691
         L     SAVEREG,CVTEXT2     ADD OF CVTEXT               @ZA09691
         L     SAVEREG,D28(SAVEREG) ADD OF OLTEPS DIE          @ZA09691
         LA    SAVEREG,D40(SAVEREG) SA OFFSET INTO DIE         @ZA09691
LOKT2  SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=SAVE,RELATED=EXITCPU
         LTR   R15,R15             CHECK RETURN CODE           @ZA09691
         SPACE 1
         BZ    ALIVECHK            IF 0, GO AHEAD              @ZA09691
         CH    R15,FOUR            IF FOUR, GO AHEAD           @ZA09691
         BE    ALIVECHK                                        @ZA09691
         LA    R9,8                ELSE, SET A RETURN CODE OF 8@ZA09691
         SPACE 1
         B     SETRC                                           @ZA09691
         SPACE 1
*****************************************************************Y02008
*                        IS CHOSEN CPU ALIVE                     Y02008
*****************************************************************Y02008
         SPACE 1
ALIVECHK EQU   *                       CPU ALIVE CHECK           Y02008
         LR    R1,SAVEREG9             RESTORE PARMLIST REG      Y02008
         LA    R9,4                    INITIALIZE R9 TO INDICATE Y02008X
                                       FAILURE OF REQUEST        Y02008
         L     R1,D0(R1)               GET PTR TO CHOSEN AFFN    Y02008
         L     CSDREG,CVTCSD           GET CSD POINTER           Y02008
         SR    R7,R7                   CLEAR MASK REG FOR USE IN Y02008
*                                      EXECUTE INSTRUCTION       Y02008
         IC    R7,D0(R1)               GET 1ST MASK FOR CPU ALIVEY02008
*                                      CHECK                     Y02008
         EX    R7,CPUTEST              IS CHOSEN CPU ALIVE       Y02008
         BO    CPUOK                   YES, PLACE IT IN ASCB     Y02008
         LA    CSDREG,D1(CSDREG)       ADD 1 TO CSD REG -- POINT Y02008
*                                      TO 2ND BYTE OF CSD CPU    Y02008
*                                      ALIVE FIELD               Y02008
         IC    R7,D1(R1)               GET 2ND MASK FOR CPU ALIVEY02008
*                                      CHECK                     Y02008
         EX    R7,CPUTEST              IS CHOSEN CPU ALIVE       Y02008
         BNO   EXITCPU                 NO, EXIT TO CALLER WITH   Y02008
*                                      RETURN CODE 0J            Y02008
         SPACE 1
*****************************************************************Y02008
*                  CPU ALIVE - PLACE AFFN IN ASCB                Y02008
*****************************************************************Y02008
         SPACE 1
CPUOK    EQU   *
         USING ASCB,ASCBREG            ASCB ADDRESSABILITY       Y02008
         L     ASCBREG,CVTTCBP         FIND PTR TO CURRENT ASCB  Y02008
         L     ASCBREG,D12(ASCBREG)    THRU CVT                  Y02008
         MVC   ASCBAFFN(2),D0(R1)      PLACE SELECTED AFFN IN    Y02008
*                                      ASCB                      Y02008
         SR    R9,R9                   RETURN 00 TO CALLER       Y02008
         SPACE 1
*****************************************************************Y02008
*                           EXIT ROUTINE                         Y02008
*****************************************************************Y02008
         SPACE 1
EXITCPU  EQU   *                       EXIT CPU ALIVE ROUTINE    Y02008
         SETLOCK RELEASE,TYPE=LOCAL,REGS=SAVE,RELATED=LOKT2    @ZA09691
         LTR   R15,R15             CHECK RETURN CODE           @ZA09691
         BZ    SETRC               IF 0, SET RECTURN CODE      @ZA09691
         CH    R15,FOUR            FOUR IS OK                  @ZA09691
         BE    SETRC                                           @ZA09691
         LA    R9,8                ELSE, SET RC=8              @ZA09691
SETRC    EQU   *                                               @ZA09691
         LR    R15,R9                  DEQ AND PLACE RETURN CODE Y02008
*                                      IN R15                    Y02008
         BR    EXIT                    RETURN TO CALLER          Y02008
         USING CSD,CSDREG              CSD ADDRESSABILITY        YM4763
CPUTEST  TM    CSDCPUAL,X'00'           VALIDATE AFFINITY        YM4763
         DROP  CSDREG                   DROP CSD ADDRESSABILITY  YM4763
         EJECT
***********************************************************************
*                             CONSTANTS                               *
***********************************************************************
 SPACE 2
         DS    0F
ENQUCB   ENQ   (MAJORNAM,MINORNAM,E,2,SYSTEM),RET=HAVE,MF=L    @ZA26394
DEQUCB   DEQ   (MAJORNAM,MINORNAM,2,SYSTEM),RET=HAVE,MF=L      @ZA12058
COUNTMSK DC    X'00000007'                                     @ZA12058
ENQDQGM  DC    X'F5000010'         SP245 FOR 16 BYTES FOR GM   @ZA12058
MASKTBL  DC    X'8040201008040201' MASK TABLR FOR EX INSTR       X03008
         DS    0F
IGC05    DC    V(IGC0505I)             IGC0505I ADDR             Y02008
IGC09    DC    V(IGC0905I)             IGC0905I ADDR             Y02008
         DS    0D
SUBPOOL  DC    X'FE000010'             XCTL POOL ID AND SIZE     Y02008
         DS    0F
POOLCNST DC    X'FE000040'             SP254 FOR 64 BYTES  S     Y02008
SP245    DC    X'F50000A0'             SP245 FOR 160 BYTES       Y02008
         DS    0F                                                Y02008
TRES     DC    X'F0F0F0F0F0F3F0C0'     3 SECONDS                 Y02008
FIVE     DC    X'F0F0F0F0F0F5F0C0'     5 SECONDS                 Y02008
MAJORNAM DC    C'SYSIEFSD'             MAJOR NAME FOR ENQ ALLOC  Y02008C
                                       SYNCRONIZATION            Y02008
MINORNAM DC    C'Q4'                   MINOR NAME FOR ENQ ALLOC  Y02008C
                                       SYNCRONIZATION            Y02008
QNAME    DC    C'SYSZVARY'             QNAME FOR ENQ             Y02008
RNAME    DC    C'CPU'                  RNAME FOR ENQ             Y02008
TABEND   DC    XL1'FF'                 END OF IOS UCB LOOK TBL   Y02008
FOUR     DC    X'0004'                 A HALFWORD 4              Y02008
SUB245   EQU   *                                                 Y02008
POOL245  DC    XL1'F5'                                           Y02008
         DS    0F
DEVMSK   DC    X'0000000F'             SETS VALID DEB ID         X03008
HEX4014  DC    X'4014'                                           X03008
ZERO     DC    X'00'                   WORK BYTE                 X03008
         DS    0F
RPLYPOOL DC    X'FE0000A8'                                       Y02008
FREEWTOR DC    X'000D0000D940F0F06B7DE27D40'
PATCH00  EQU   *                       PATCH AREA FOR IGC0005I
         IFDPATCH                                                X03008
         EJECT
         DS    0D
    DS  0D
         CVT   SYS=AOS2,DSECT=YES
         DS    0D                                                Y02008
         IEESMCA                                                 Y02008
         DS    0D
         IEECUCM FORMAT=NEW
         DS    0D
RQEMAP   DSECT
         IHAORE
         EJECT
TCB IKJTCB SYS=AOS2,DSECT=YES
         EJECT
SRB IHASRB
         EJECT
IOSB  IECDIOSB
         EJECT
IOSUSECB EQU   *
USERCB   DS    0CL24                   START OF IOSB USER CON BLKY02008
OECBPTR  DS    F                  POINTER TO OLTEP EVENT CONTROL BLOCK
TECBPTR  DS    F                  OLT'S TECB POINTER FOR THIS EVENT
IODEVTAB DS    F                  POINTER TO 3 WORDS IN OLTEP COMMON
TCCWPTR  DS    F                  POINTER TO THE TCCW SYSTEM CONTROL
*                                    BLOCK FOR THE CCW TRANSLATOR
USEFLAGS DS    0F                 A WORD OF FLAGS
USEFLAGA DS    1C                 FIRST FLAG BYTE FOR THIS CB SET
INUSE    EQU   X'80'              THIS IOSB IS IN USE
ATTN     EQU   X'40'              ATTENTION=YES REQUEST (OFF IF DONE)
VIRTCCW  EQU   X'20'              THE CCW'S ARE IN VIRTUAL
INTRNLCB EQU   X'08'              REQUEST IS INTERNAL (LINE CONNECT,DP)
PURGED   EQU   X'04'              THIS I/O EVENT IS PURGED
PCIENTRY EQU   X'02'                   PCI EXIT OPERATING
USEFLAGB DS    1C                      UNUSED
USEFLAGC DS    1C                      UNUSED
USEFLAGD DS    1C                      UNUSED
USERSVD  DS    F                       RESERVED FOR FUTURE       Y02008
OECB     DS    0CL64                   OLTEPS OECB
         EJECT
TCCW     IECDTCCW
         EJECT
         IECDIOCM
         EJECT
         IECDIOCX
         EJECT
         IHAPSA
         EJECT
         DS    0D
         IECDLCH
         EJECT
         DS    0D
UCBDSECT DSECT
         IEFUCBOB
         EJECT
         IHACSD                        CSD DSECT                 Y02008
ASCB     IHAASCB
PPL      IECDPPL                       PURGE PARAMETER LIST      Y02008
LDA      IHALDA                        LDA MAP                   Y02008
         END
