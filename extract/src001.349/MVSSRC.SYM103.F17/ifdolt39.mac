 TITLE  'IFDOLT39  ENTRY-EXIT, PARALLEL PRINT OUTPUT WRITER SECTIONS'
****************************** PROLOGUE *******************************
*                                                                     *
*  MODULE NAME: IFDOLT39                                              *
*                                                                     *
*  DESCRIPTIVE NAME: DPRINT ENTRY-EXIT AND PRINT ROUTINES             *
*                                                                     *
*  COPYRIGHT: NONE                                                    *
*                                                                     *
*                                                                     *
*  STATUS: CHANGE LEVEL - 001                                         *
*                                                                     *
*  FUNCTION: THIS MODULE IS DESIGNED AS FOLLOWS:                      *
*                                                                     *
*            ENTRY-EXIT SECTION                                       *
*            1) 5 ENTRY POINTS FOR PRINT REQUESTS BY AN OLT           *
*               OR OLTEP MODULE.                                      *
*            2) LINKAGE ROUTINES TO PARALLEL PRINT OR DPRINT MODULES  *
*            3) CALLING ROUTINE FOR CECOMM ON FIRST ERROR OR          *
*               CATASTROPHIC ERROR CONDITIONS.                        *
*            4) COMMON EXIT RTN FOR RETURNING TO THE CALLING OLT OR   *
*               OLTEP MODULE, WITH THE RETURN CODE IN R15.            *
*            5) REGISTER SAVE AREA 1 USED WHEN CALLING OLTEP MODULES  *
*               IFDOLT14, IFDOLT30.                                   *
*            6) REGISTER SAVE AREA 2 USED WHEN CALLING OS MODULES     *
*               VIA WRITE, SVC.                                       *
*                                                                     *
*            PRINT SECTION                                            *
*            1) PRINTS WTO AND WTOR MESSAGES, REPLIES.                *
*            2) PRINTS OLT RESULTS, DATA, AND MESSAGES.               *
*            3) TRANSLATES REPLIES FOR PRINTING.                      *
*            4) OUTPUTS WTO AND WTOR MESSAGES TO THE CONSOLE.         *
*            5) OUTPUTS OLT RESULTS, ETC TO A CONSOLE WITH LEVEL      *
*               CONTROL WHEN PARALLEL PRINT IS REQUESTED              *
*                                                                     *
*  ENTRY POINTS: DPRINT - DPRINT CALLED - FROM OLT MOD                *
*                PWTO   - WTO ENTRY   )                               *
*                PWRT   - WRITE ENTRY ) - FROM OLTEP MODS             *
*                PRPLY  - REPLY WRITE )                               *
*                PWTOR  - WTOR ENTRY  )                               *
*                                                                     *
*  INPUTS: PARAMETER LISTS OF CALLING MACROS.                         *
*                                                                     *
*  REGISTER USAGE: R1  - ADDRESS OF PARAMETERS OF CALLING MACROS.     *
*                  R2  -  ADDRESS OF SECT CTRL TABLE IN OLTEP ROOT.   *
*                  R4  - BASE REG OF THIS MODULE                      *
*                  R5-R12 - MISC                                      *
*                  R13 - ADDRESS OF CALLER'S SAVE AREA                *
*                  R14 - ADDRESS OF CALLER'S RETURN POINT             *
*                  R15 - ADDRESS OF ENTRY POINT                       *
*                                                                     *
*  EXTERNAL ROUTINES: OS ROUTINES                                     *
*                     IFDOLT14- DPRINT MODULES.                       *
*                                                                     *
*  EXITS - NORMAL: RETURN TO OLT OR OLTEP MODULE                      *
*           ERROR: IFDOLT30- CECOMM MOD
*                                                                     *
*  ATTRIBUTES: SERIALLY REUSABLE                                      *
*                                                                     *
*  CHANGE ACTIVITY: CLEANUP - C137500,D608000                         *
*                                                                     *
***********************************************************************
*  CHANGE 02/71 - RETAIN 370 INTERFACE                                *
*  CHANGE 03/71 - CHAIN=YES SUPPORT                                   *
***********************************************************************
         SPACE
         EJECT
IFDOLT39 CSECT
*C400000                                                        X02906
         ENTRY DPRXLATE
         ENTRY DPRPPOW
         ENTRY DPRDCB                   DIAGMSG DD DCB           Y02008
         USING *,R15               LINK REG AS BASE REG
         USING CHASCT,R2           COM BASE REG
         SPACE
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RSTRFLG  EQU   X'80'
WTOFLG   EQU   X'01'
RPLYFLG  EQU   X'02'
WTORFLG  EQU   X'04'
WRTFLG   EQU   X'10'
DPRFLG   EQU   X'40'
WAITFLG  EQU   X'20'
CPOPT     EQU  X'08'
NPROPT    EQU  X'04'
NEPOPT      EQU  X'04'
FECOPT   EQU   X'01'
NFEFLG   EQU   X'08'
CATFLG   EQU   X'10'
HDRFLG   EQU   X'80'
RSLTFLG  EQU   X'40'
PFEMSG   EQU   X'08'
PCEMSG   EQU   X'04'
FCEFLG   EQU   X'10'
FFEFLG   EQU   X'20'
FERINT   EQU   X'02'
RETAINAC EQU   X'80'                                              M4501
REMERR   EQU   X'40'                                              M4501
REMNOMSG EQU   X'20'                                              M4501
CENOMSG  EQU   X'10'                                              M4501
TRANSMSG EQU   X'08'                                              M4501
IMRESP   EQU   X'04'                                              M4501
EXECOUT  EQU   X'02'                                              M4501
CECOMOUT EQU   X'01'                                              M4501
ILLRESP  EQU   X'80'                                              M4501
EXECANS  EQU   X'40'                                              M4501
CECOMANS EQU   X'20'                                              M4501
INFOMSG  EQU   X'10'                                              M4501
RETRIEVE EQU   X'08'                                              M4501
REMANS   EQU   X'04'                                              M4501
EDOTMSG  EQU   X'02'                                              M4501
SETIM    EQU   X'FB'                                              M4501
SETRET   EQU   X'F7'                                              M4501
ONCOMM   EQU   X'20'                                              M4501
REIENTRY EQU   X'01'                                              M4501
REPLY00  EQU   X'80'                                              M4501
POSTBIT  EQU   X'40'                                              M4501
COMMLOOP EQU   X'10'                                              M4501
WAIT3    EQU   X'08'                                              M4501
REPLY02  EQU   X'40'                                              M4502
PRTONLY  EQU   X'02'                                           X02008
         EJECT
***********************************************************************
*        DPRINT MODULE - ENTRY-EXIT SECTION                           *
***********************************************************************
         SPACE
* ENTRY POINTS FOR DPRINT AND INNER MACROS.
         SPACE
DPRINT   BC    15,DPRENT-*(15)     DPRINT CALLED
         DC    X'0100'
PWTO     BC    15,DPRENT-*(15)     WTO ENTRY
         DC    X'0200'
PWRT     BC    15,DPRENT-*(15)     WRITE ENTRY
         DC    X'0300'
PRPLY    BC    15,DPRENT-*(15)     REPLY WRITE ENTRY
         DC    X'0400'
PWTOR    BC    15,DPRENT-*(15)     WTOR ENTRY
         DC    X'0500'
         DC    C'IFDOLT39&SYSDATE'       IDENTIFIER             Y02906*
DPRENT   STM   R14,R12,12(R13)     SAVE CALLER'S REGS
         BALR  R4,0                BASE REG
         USING *,R4
         TM    CESWTR1,REIENTRY         ENTRY FROM REI           S20203
         BO    DPRENT3                  YES. USE SAVE AREA 3    S20203
         CLI   4(R15),X'01'        ENTRY FROM A UNIT TEST FOR DPRINT
         BNE   DPRENT1             NO.  USE SAVE AREA 2
         LA    R7,DPRSA1           YES. USE SAVE AREA 1
         ST    R7,8(R13)           STORE IN CALLER'S SA
         ST    R13,4(R7)           STORE ADDR OF CALLER'S SA
         NI    CESWT4,X'00'        CLEAR FLAGS
         L     R12,4(R1)           DPRINT CONTROL WORD ADDRESS
         B     DPRENT2
DPRENT3  LA    R7,DPRSA3                SA3 USED FOR REI ENTRY   S20203
         ST    R7,8(R13)                STORE IN CALLER'S SA     S20203
         ST R13,4(R7)                   STORE ADDR OF CALLERS SA S20203
         LR    R13,R7                   ADDRESS OF OWN SA        S20203
         NI    CESWT4,X'E0'             CLEAR SELECTED FLGS      S20203
         B     DPRENT2                                           S20203
DPRENT1  LA    R7,DPRSA2           SA2 USED FOR PWRT,PWTO/R,PRPLY,
         ST    R7,8(R13)           SAVE IT IN CALLER'S SA
         ST    R13,4(R7)           STORE ADDR OF CALLER'S SA
         LA    R13,DPRSA2          ADDRESS OF OWN SA2
         NI    CESWT4,X'E0'        CLEAR FLGS EXCEPT RSTR,WAIT,DPR
DPRENT2  XC    RETCODE(4),RETCODE  CLEAR IT
         SPACE
* SELECT EFFECTIVE ENTRY ROUTINE
         SPACE
         CLI   4(R15),X'01'        DPRINT ?
         BE    DPRPRT
         CLI   4(R15),X'02'        WTO ?
         BE    DPRWTO
         CLI   4(R15),X'03'        WRITE ?
         BE    DPRWRT
         CLI   4(R15),X'04'        REPLY WRITE ?
         BE    DPRRPLY
         CLI   4(R15),X'05'        WTOR ?
         BE    DPRWTOR
         SPACE
* UNIT TEST DPRINT ENTRY
         SPACE
DPRPRT   LA    R13,DPRSA1          SAVE AREA ADDRESS              M4506
         OI    CESWT4,DPRFLG       SET DPRINT OP                  M4506
         NI    CESWT6,X'7F'             CLEAR UTS BIT          XM5847
         BAL   R5,DPRCL14          CALL MODULE ROUTINE            M4506
         ST    R15,RETCODE         RETURN CODE                    M4506
         OI    CESWT3,X'08'        SUSPEND DELETION               M4506
         BAL   R5,DPRCL14          CALL MODULE ROUTINE            M4506
         SPACE
* CALL CECOMM MODULE IF FIRST ERROR OR CATASTROPHIC ERROR CONDITION
         SPACE
         TM    0(R12),X'08'        DPRINT CHAINING REQUESTED
         BO    DPRPRT6             YES.  BYPASS CECOM CALL.
         TM    0(R12),X'02'        NO.  IS IT AN ERROR DPRINT?    M5076
         BZ    DPRPRT6             IF NOT BRANCH TO BYPASS CECOM  M5076
         TM    CESWT5,PFEMSG+PCEMSG   YES.  TEST FOR ERRORS       M4506
         BZ    DPRPRT6             NO ERROR                       M4506
         SPACE
DPRPRT7  NI    CESWT4,255-DPRFLG   CLEAR FLAG
         NI    CESWT5,255-PCEMSG-PFEMSG   CLEAR FLAGS             21937
         OI    CESWT,FERINT        FIRST ERROR INTERVAL
         LA    R13,DPRSA1          SAVE AREA 1 ADDRESS
         L     R15,COMMINT         CECOMM MODULE ADDRESS
         BALR  R14,R15             TO CECOMM
DPRPRT6  NI    CESWT4,255-DPRFLG   CLEAR DPRINT OP FLAG           M4506
         B     DPREXIT1            TO EXIT ROUTINE                M4506
         SPACE
DPRCL14  IFDMOD CALL='14',LIST=(R1) CALL IFDOLT14                 M4506
DLT14    IFDMOD DELETE='14'        DELETE IFDOLT14               YM4982
         BCR   15,R5               RESUME PROGRAM                 M4506
         SPACE
* SET FLAG FOR OLTEP WRITE TO PRINTER.
         SPACE
DPRWRT   OI    CESWT4,WRTFLG       WRITE SET ON
         ST    R1,MSGLGTH          WRITE MESSAGE LENGTH
         STC   R1,MHRPARM+3             MESSAGE LENGTH           S20203
         TM    CESWTR,TRANSMSG                                    M4501
         BZ    DPREI132                                           M4501
         LA    R6,DPRXLATE                                      S20203
         LA    R8,PRTBUFR                                       S20203
         EX    R1,REPXLATE                                        M4501
DPREI132 BAL   R14,DPRPPOW                                        M4501
         TM    CESWTR2,COMMLOOP         IS THIS INTERNAL PWRITE  S20203
         BO    DPREI29                                           S20203
         B     DPREXIT2            RETURN HERE AND EXIT
         SPACE
* SETUP WTO MESSAGE LENGTH, ADDRESS, AND FLAG.
* RESTORE PAGE FOR 'OLTS RUNNING' MESSAGE
         SPACE
DPRWTO   ST    R1,SAVER1           SAVE POINTER
         SR    R7,R7               CLEAR
         IC    R7,1(R1)            MESSAGE LENGTH
         LA    R9,4
         SR    R7,R9               ACTUAL MESSAGE BYTE COUNT
         ST    R7,MSGLGTH          SAVE IT
         STC   R7,MHRPARM+3             MESSAGE LENGTH           S20203
         LA    R7,4(R1)            MESSAGE ADDRESS
         ST    R7,MSGADR           SAVE
         CLC   4(6,R1),OLTRUN      IS IT WTO MSG 'OLTS RUNNING'
         BE    DPRWTO2             YES.  RESTORE PRINT PAGE
DPRWTO1  OI    CESWT4,WTOFLG       NO.  SET WTO FLAG
         BAL   R14,DPRPPOW         TO PARALLEL PRINT OUTPUT WRITER
         B     DPREXIT2            RETURN HERE AND EXIT
         SPACE
DPRWTO2  MVI   PRTBUFR+1,C' '      LOAD PRINT BUFFER WITH BLANKS
         MVC   PRTBUFR+2(120),PRTBUFR+1
         MVI   PRTBUFR,X'8B'       RESTORE PAGE CTRL CHARC
         OI    CESWT4,RSTRFLG      SET RESTORE FLAG
         BAL   R14,DPRPPOW         TO PARALLEL PRINT OUTPUT WRITER
         NI    CESWT4,255-RSTRFLG  CLEAR RESTORE FLAG
         MVI   PRTBUFR,X'09'       RESTORE 1 LINE SPACE
         B     DPRWTO1
         SPACE
* SETUP WTOR MSG AND REPLY LENGTHS, ADDRESSES, FLAG.  ALSO ECB ADDR.
         SPACE
DPRWTOR  ST    R1,SAVER1           SAVE POINTER
         SR    R7,R7               CLEAR
         TM    CESWTR2,COMMLOOP         IS THIS INTERNAL RWTOR    M4502
         BO    DPREI44                  YES. BRANCH               M4502
         OI    CESWTR2,WAIT3            SET WTOR INDICATOR        M4502
         L     R7,0(R1)            REPLY LENGTH AND ADDRESS
         ST    R7,REPLGTAD         SAVE
         ST    R7,RESPBUF               STORE ADDRESS FOR MHR    S20203
DPREI44  L     R7,4(R1)                 REPLY ECB ADDRESS        S20203
         ST    R7,REPECBAD         SAVE
         IC    R7,9(R1)            MESSAGE LENGTH
         LA    R9,4
         SR    R7,R9               ACTUAL MESSAGE BYTE COUNT
         ST    R7,MSGLGTH          SAVE IT
         STC   R7,MHRPARM+3             MESSAGE LENGTH           S20203
         TM    CESWT4,WAITFLG           IS WAIT CALLED FOR     S20203
         BO    *+8                     NO. DONT SET SWITCH     S20203
         OI    CESWTR,IMRESP            SET IM REPLY             S20203
         LA    R7,12(R1)           MESSAGE ADDRESS
         ST    R7,MSGADR           SAVE
         OI    CESWT4,WTORFLG      WTOR FLAG SET ON
         BAL   R14,DPRPPOW         TO PARALLEL PRINT OUTPUT WRITER
         TM    CESWTR2,COMMLOOP         IS THIS INTERNAL PWTOR   S20203
         BO    DPREI40                                           S20203
         NI    CESWTR2,255-WAIT3        ZERO WTOR INDICATOR       M4502
         B     DPREXIT2            RETURN HERE AND EXIT
         SPACE
* SET FLAG FOR REPLY PRINT ONLY.
         SPACE
DPRRPLY  OI    CESWT4,RPLYFLG      REPLY FLAG SET ON
         LA    R8,INBUFR           INBUFR ADDR AS REPLY ADDR      M1370
         ST    R8,REPLGTAD         STORE                          M1370
         STC   R1,REPLGTAD         REPLY LENGTH
         BAL   R14,DPRPPOW         TO PARALLEL PRINT OUTPUT WRITER
         B     DPREXIT2            RETURN HERE AND EXIT
         SPACE
* RETURN TO CALLER.  PUT ANY RETURN CODE IN R15 SAVE AREA.
         SPACE
DPREXIT1 L     R13,DPRSA1+4        ADDR OF CALLER'S SA - UNIT TEST
         MVC   16(4,R13),RETCODE   RETURN CODE TO R15 SAVE AREA
         LM    R14,R12,12(R13)     RESTORE CALLER'S REGS
         BR    R14                 RETURN TO CALLER
DPREXIT2 TM    CESWTR1,REIENTRY         REI ENTRY                S20203
         BZ    DPREXIT3                 SA3 WAS USED             S20203
         L     R13,DPRSA3+4             ADDR OF CALLERS SA - REI S20203
         B     DPREXIT3+4               RETURN                   S20203
DPREXIT3 L     R13,DPRSA2+4             ADDR OF SA - OLTEP MOD   S20203
         MVC   16(4,R13),RETCODE   RETURN CODE TO R15 SAVE AREA
         LM    R14,R12,12(R13)     RESTORE ITS REGS
         BR    R14                 RETURN TO IT
         SPACE
         CNOP  0,4
DPRSA1   DS    18F                 SAVE AREA
DPRSA2   DS    18F                 SAVE AREA 2
RETCODE  DC    XL4'0'              RETURN CODE
MSGLGTH  DC    XL4'0'
MSGADR   DC    XL4'0'
REPLGTAD DC    XL4'0'
REPECBAD DC    XL4'0'
SAVER1   DC    XL4'0'
OLTRUN   DC    C'IFD102'
DPRSA3   DC    18F'0'                   SAVE AREA 3              S20203
RESAVER1 DC    F'0'                                              S20203
         SPACE 2
***********************************************************************
*                PRINT OUTPUT WRITER                                  *
***********************************************************************
         SPACE
         SPACE
DPRPPOW  ST    R14,SAVE14          SAVE LINK ADDRESS
         TM    CESWT4,RSTRFLG      IS THIS A RESTORE PAGE PRINT
         BO    DPR201              YES.
DPR200   TM    CESWT4,RPLYFLG      NO.  IS THIS A REPLY PRINT
         BZ    DPR200A             NO
         B     DPR211              YES. TRANSLATE, MOVE IT TO PRINT BFR
         SPACE
* LOAD PRINT BUFFER FOR PRINT IF A WTO/R
         SPACE
DPR200A  TM    CESWT4,WTOFLG       IS IT AN OPERATOR MESSAGE
         BO    DPR200B             YES
         TM    CESWT4,WTORFLG      NO.  IS IT A WTOR.
         BZ    DPR201              NO
DPR200B  MVI   PRTBUFR+1,C' '      YES. SETUP FOR PARALLEL PRINT
         MVC   PRTBUFR+2(120),PRTBUFR+1  CLEAR WITH BLANKS
         MVI   PRTBUFR,X'09'       LOAD PRINT CTRL - SPACE 1 LINE
         L     R9,MSGLGTH          WTO MESSAGE LENGTH
         BCT   R9,*+4              SUBT 1 TO ADJUST FOR MVC COUNT
         L     R8,MSGADR           WTO MESSAGE ADDRESS
         EX    R9,DPRMVC           MOVE MSG INTO PRINT BUFFER
         SPACE
* OPEN DPRINT IF FIRST PASS.
         SPACE
DPR201   LA    R8,DPRDCB           DPRINT DCB ADDR
         TM    48(R8),X'10'        HAS IT BEEN OPENED
         BO    DPR202              YES. BYPASS OPEN
         OPEN  (DPRDCB,(OUTPUT))   NO.  OPEN IT
         TM    48(R8),X'10'        WAS DCB OPENED ?
         BO    DPR202              YES.
         MVI   RETCODE,X'04'       NO.  SET RETURN CODE TO 04
         B     DPR210              GO TO END
         SPACE
DPR202   PRTOV DPRDCB,12           CHECK FOR OVERFLOW
         SPACE
* EXECUTE WRITE OPERATION.
         SPACE
         LA    R9,PRTBUFR          PRINT BUFFER ADDRESS
         ST    R9,DPRDECB+12       LOAD AS AREA ADDRESS
         WRITE DPRDECB,SF,DPRDCB,PB,'S'    PRINT
         SPACE
         CHECK DPRDECB
         SPACE
* BYPASS WTO IF REPLY OR RESTORE PRINT.
         SPACE
         TM    CESWT4,RSTRFLG      RESTORE PAGE PRINT
         BO    DPR210              YES.  BYPASS WTO
         TM    CESWTR,RETAINAC          IS REI ACTIVE            S20203
         BZ    DPREI10                  NO. CONTINUE             S20203
         TM    CESWTR2,PRTONLY                                 X02008
         BO    DPR210                                          X02008
         TM    CESWTR,REMNOMSG          DOES REMOTE GET MSG.     S20203
         BO    DPREI10                  NO. CONTINUE             S20203
         TM    CESWT4,RPLYFLG           IS THIS REPLY PRINT      S20203
         BZ    DPREI12                  NO. SEND TO REMOTE       S20203
         TM    CESWTR,CENOMSG           DID ONSITE ANSWER        S20203
         BZ    DPR206                   NO. PUT TO CONSOLE       S20203
DPREI12  L     R1,MHRPARM               PUT MSG LENGTH IN R1     S20203
         MVC   RESAVER1(4),SAVER1       SAVE R1 FROM BEFORE      S20203
         MVC   SAVESW4(1),CESWT4        SAVE FLAGS               S20203
DPREI02  L     R15,MHRPTR               GET ADDR OF MSG HANDLER  S20203
         BALR  R14,R15                  BRANCH TO MSG HANDLER    S20203
         TM    CESWTR,REMERR            DID ERROR OCCUR.         S20203
         BZ    DPREI11                  NO. CONTINUE             S20203
         MVC   SAVER1(4),RESAVER1       RESTORE R1 SAVE AREA     S20203
         MVC   CESWT4(1),SAVESW4        RESTORE FLAGS            S20203
         TM    CESWTR1,EDOTMSG          IS IT IFD105D MSG        S20203
         BZ    DPREI11                  NO. CONTINUE.FLAG SET.   S20203
DPREI26  NI    CESWTR,255-REMERR        ZERO ERROR FLAG          S20203
         L     R13,DPRSA2+4             CALLERS SAVE ADDR        S20203
         MVC   12(4,R13),REDEFINE       MOVE IN NEW RETURN ADDR  S20203
         B     DPREXIT2                 GO BACK TO IFDOLT30      S20203
DPREI11  TM    CESWTR1,RETRIEVE         IS THIS RETRIEVE        S20203
         BO    DPREI05                  YES. GO CHECK REPLY      S20203
DPREI10  TM    CESWT4,RPLYFLG           IS THIS A REPLY PRINT    S20203
         BO    DPR210                   YES. BYPASS WTO          S20203
DPREI09  TM    CESWT4,WTOFLG            IS IT A WTO.             S20203
         BO    DPR208              YES
         TM    CESWT4,WTORFLG      IS IT A WTOR
         BO    DPR209              YES
         TM    CESWTR,CENOMSG           DOES ONSITE GET MSG     S20203
         BO    DPR210                   NO. BRANCH TO EXIT      S20203
         SPACE
* PARALLEL PRINT TO CONSOLE WITH LEVEL CONTROL
         SPACE
DPR203   TM    CHASCT+3,X'10'      PARALLEL PRINT REQUESTED       M1005
         BZ    DPR210              NO.  BYPASS WTO
         TM    CESWT4,DPRFLG       YES.  IS IT A DPRINT OP.
         BZ    DPR210                  NO-BYPASS CONSOLE PRINT  X02906
         CLI   DPLEVEL,X'F3'       YES. CHECK LEVEL CTRLS
         BE    DPR206              YES. LOAD FOR WTO
         CLI   DPLEVEL,X'F2'       NO. IS IT LEVEL 2
         BE    DPR204              YES  LOAD IF HEADER OR RESULTS
         CLI   DPLEVEL,X'F1'       NO. IS IT LEVEL 1
         BE    DPR205              YES. LOAD IF HDR, DESC, COMMENT.
         B     DPR204A             LEVEL 0 ASSUMED. TEST IF HEADER
         SPACE
DPR204   TM    CESWT5,RSLTFLG      IS IT A RESULT
         BO    DPR206              YES.
DPR204A  TM    CESWT5,HDRFLG       NO. IS IT A HEADER
         BO    DPR206              YES
         B     DPR210              NO.  BYPASS IF DESC OR MSG LINE
         SPACE
DPR205   TM    CESWT5,RSLTFLG      IS IT A RESULT
         BO    DPR210              YES. BYPASS WTO
         B     DPR206              NO. LOAD HDR, DESC, COMMENT
         SPACE
* LOAD MSG, ROUTING, DESCRIPTOR CODES FOR PARALLEL PRINT TO CONSOLE
         SPACE
DPR206   L     R1,MSGMOD           ADDRESS OF MESSAGE TABLE
         L     R1,0(R1)            MESSAGE ADDRESS
         SR    R9,R9               CLEAR REGS                     M1005
         SR    R7,R7                                              M1005
         CLI   MSGLGTH+3,X'3D'     MSG GREATER THAN 61 CHARCS     M1005
         BH    DPR207              YES. BRANCH                    M1005
         IC    R9,MSGLGTH+3        NO. USE GIVEN LENGTH           M1005
         B     *+8                                                M1005
DPR207   IC    R9,MSGMAX+1         USE MAX- 61 CHARCS             M1005
         LA    R7,12(R9)           SETUP WTO MSG BYTE COUNT       M1005
         STC   R7,1(R1)            PUT IT IN WTO MSG              M1005
         LA    R7,12(R1)           PTR TO START OF ACTUAL MSG     M1005
         AR    R7,R9               POINTER TO ROUTING-DESC CODES  M1005
         BCT   R9,*+4              SUBT 1 TO ADJUST FOR MVC COUNT M1005
         LA    R8,PRTBUFR+1                                       M1005
         CLC   0(7,R8),OURMSGID    IS THIS OLTEP MESSAGE          01906
         BC    7,DPR207A           NO. MOVE MSG INTO WTO AREA     01906
         EX    R9,MSGMVC1          YES. MOVE ID AND MSG INTO WTO AREA
         BC    15,DPR207B          CONTINUE NORMAL PROCESSING     01906
DPR207A  EQU   *                                                  01906
         EX    R9,MSGMVC           MOVE FROM PRINT BUFR TO WTO    M1005
DPR207B  EQU   *                                                  01906
         MVC   0(4,R7),=X'02000040'  LOAD ROUT-DESC CODES         M1005
         B     DPR208+4
         SPACE
* WTO/R EXECUTION
         SPACE
DPR208   L     R1,SAVER1           RESTORE WTO MESSAGE PTR
         WTO   MF=(E,(1))          DO WRITE TO OPERATOR
         L     R5,MSGMOD                ADDR OF MSG MOD         S20203
         L     R5,0(R5)                 MSG 100I ADDR           S20203
         MVI   1(R5),X'54'              MSG LENGTH              S20203
         B     DPR210              TO EXIT
         SPACE
DPR209   L     R1,SAVER1           RESTORE WTOR MESSAGE PTR
         TM    CESWTR,RETAINAC          IS REI ACTIVE            S20203
         BZ    DPREI08                  NO. WTOR                 S20203
         LA    R10,TALKECB              ADDRESS OF TALK ECB      S20203
         ST    R10,ECB1R                STORE IN LIST            S20203
         LA    R10,REIECB               ADDRESS OF REMOTE ECB    S20203
         ST    R10,ECB2R                STORE IN LIST            S20203
         TM    CESWTR,REMNOMSG          DOES REMOTE GET MESSAGE   M4506
         BO    DPREI08                  NO. EXECUTE WTOR          M4506
         TM    CESWTR1,REMANS           WHEN REI ACTIVE IS RE AN S20203
         BZ    DPREI08                  NO.  CONTINUE            S20203
         TM    CESWTR2,REPLY00                                   S20203
         B     DPREI08                                         X02008
DPREI42  L     R10,REPECBAD             LOCAL ECB                S20203
         ST    R10,ECB3R                STORE IN LIST            S20203
         MVI   ECB3R,X'80'              SET HIGH ORDER BIT       S20203
DPREI30  WAIT  1,ECBLIST=ECB1R                                   S20203
         L     R9,ECB3R                 ADDR OF LOCAL ECB        S20203
         TM    0(R9),POSTBIT            HAS IFD105D BEEN ANS     S20203
         BZ    DPREI43                  NO. CHECK NEXT ECB       S20203
         OI    CESWTR,CENOMSG           YES. CE ANSWERED         S20203
         OI    CESWTR2,ONCOMM           ONSITE ANSWERED          S20203
         B     DPREI06                  CONTINUE                 S20203
DPREI43  TM    TALKECB,POSTBIT          DOES ONSITE WANT COMM    S20203
         BZ    DPREI31                  NO. BRANCH                M4501
DPREI24  MVC   RESAVE14(4),SAVE14       DAVE R 14                S20203
         OI    CESWTR2,COMMLOOP         SET SWITCH               S20203
         MVC   CSWTRSV(1),CESWTR        SAVE CESWTR               M4502
         NI    CESWTR,255-REMNOMSG      ZERO REMNOMSG             M4502
         OI    CESWTR,CENOMSG           ONSITE NO MSG            S20203
         LA    R1,71                    LENGTH OF MSG - 1         M4502
         LA    R6,DPRXLATE              ADDR OF TRANS TABLE      S20203
         L     R8,RTALKBUF              ADDR OF DED BUFFER       S20203
         EX    R1,REPXLATE              TRANSLATE MSG            S20203
         MVC   PRTBUFR+1(8),RTALKID     MSG ID                 S20203
         MVC   PRTBUFR+9(72),0(R8)      MOVE MSG TO PRTBUFR      S20203
         MVI   PRTBUFR+81,C' '          BLANK REST OF BUFFER     S20203
         MVC   PRTBUFR+82(41),PRTBUFR+81                         S20203
         NI    CESWTR1,255-RETRIEVE                               M4501
         MVC   SAVESW4(1),CESWT4                                  M4501
         MVI   CESWT4,X'00'                                       M4501
         B     DPRWRT                   BRANCH TO PWRITE         S20203
DPREI29  NI    CESWTR,255-CENOMSG       ZERO FLAG                S20203
         L     R10,RTALKBUF                                       M4501
         MVC   CESWT4(1),SAVESW4                                  M4501
         MVI   0(R10),C' '              CLEAR DED BUF            S20203
         MVC   1(71,R10),0(R10)                                  S20203
         L     R10,MSGMOD               MSG MODULE ADDR          S20203
         L     R1,72(0,R10)             MSG                      S20203
         MVC   1(3,R1),RTALKBUF+1       BUFFER ADDR TO PARMLIST  S20203
         MVI   0(R1),X'48'              LENGTH OF RESPONSE       S20203
         LA    R10,TALKECB              ECB ADDR                 S20203
         ST    R10,4(0,R1)              STORE IN PARMLIST        S20203
         SR    R10,R10                  ZERO REG 10              S20203
         ST    R10,TALKECB              STORE IN PARMLIST        S20203
         OI    CESWTR,REMNOMSG          RE NO MSG                S20203
         NI    CESWTR1,255-RETRIEVE                               M4501
         MVC   SAVESW4(1),CESWT4                                  M4501
         MVI   CESWT4,X'00'                                       M4501
         OI    CESWT4,X'20'             NO WAIT                  S20203
         B     DPRWTOR                                           S20203
DPREI40  MVC   CESWTR(1),CSWTRSV        RESTORE CESWTR            M4502
         MVC   CESWT4(1),SAVESW4                                  M4501
         NI    CESWTR2,255-COMMLOOP     RESET SWITCH             S20203
         MVC   SAVE14(4),RESAVE14       RESTORE R14              S20203
DPREI31  TM    REIECB,POSTBIT           DOES RE WANT COMM        S20203
         BZ    DPREI30                  NO REISSUE WAIT          S20203
DPREI25  OI    CESWTR1,RETRIEVE         SET RETRIEVE FLAG        S20203
         MVC   RESAVE14(4),SAVE14       SAVE R14                  M4502
         L     R1,ECB3R                 ADDR OF LOCAL ECB        S20203
         L     R15,MHRPTR               GET MHR PTR ADDR         S20203
         BALR  R14,R15                  BRANCH TO MSG HANDLER    S20203
         MVC   SAVE14(4),RESAVE14       RESTORE R 14              M4502
         NI    CESWTR1,255-RETRIEVE                               M4501
         TM    CESWTR,REMERR            REI ERROR                S20203
         BZ    DPREI27                  NO.BRANCH                S20203
         TM    CESWTR1,EDOTMSG          IS IT 105D                M4502
         BZ    DPREI45                  NO.DONT ZERO SWITCH       M4502
         NI    CESWTR,255-REMERR        ZERO ERROR FLAG           M4502
DPREI45  L     R9,ECB3R                 LOCAL ECB ADDR            M4502
         B     DPREI07                  BRANCH TO WAIT           S20203
DPREI27  TM    CESWTR1,ILLRESP+INFOMSG  IS REPLY OKAY            S20203
         BZ    DPREI06                  YES.CONTINUE             S20203
         NI    CESWTR1,255-ILLRESP-INFOMSG  ZERO FLAGS           S20203
         B     DPREI30                  REISSUE WAIT             S20203
DPREI08  WTOR  MF=(E,(1))               DO WTOR                  S20203
         TM    CESWT4,WAITFLG      IS A WAIT CALLED FOR
         BO    DPR210              NO.  BRANCH TO EXIT
         L     R9,REPECBAD         GET REPLY ECB ADDRESS
         TM    CESWTR,RETAINAC          IS REI ACTIVE            S20203
         BZ    DPREI07                  NO. GO WAIT FOR REPLY    S20203
         TM CESWTR,IMRESP               IMMEDIATE REPLY           M4502
         BO    DPREI42                  YES. BRANCH              S20203
         OI    CESWTR,CENOMSG           SET CENOMSG FLAG         S20203
DPREI07  WAIT  ,ECB=(R9)                 WAIT FOR REPLY          S20203
         TM    CESWTR1,RETRIEVE         CHECK IF WAIT ON REMOTE  S20203
         BO    DPREI02                  BRANCH TO MSG HANDLER    S20203
DPREI06  OI    CESWT4,RPLYFLG           SET REPLY FLAG           S20203
         NI    CESWT4,255-WTORFLG-WAITFLG  RESET THESE FLAGS
         B     DPR211
DPREI05  TM    CESWTR1,ILLRESP+INFOMSG   IS REPLY OKAY         S20203
         BZ    DPREI04                  YES. CONTINUE            S20203
         NI    CESWTR1,255-ILLRESP-INFOMSG                        M4501
         B     DPREI07                  GO WAIT FOR NEW REPLY    S20203
DPREI04  NI    CESWTR,SETIM             ZERO IMMEDIATE FLAG      S20203
         NI    CESWTR1,SETRET           ZERO RETRIEVE FLAG        M4501
         TM    CESWTR,REMERR            REI ERROR                S20203
         BO    DPR210                   YES. GOTO END            S20203
         B     DPREI06                                            M4501
         SPACE
* CLEAR FLAGS.  EXIT PPOW.
         SPACE
DPR210   NI    CESWT4,X'40'        RESET FLAGS EXCEPT DPRFLG
         NI    CESWT5,255-HDRFLG-RSLTFLG  CLEAR THESE FLAGS
         TM    CESWTR1,REIENTRY         ENTRY FROM MSGHANDLER    S20203
         BO    DPREI41                  YES.DONT ZERO SWITCHES   S20203
         NI    CESWTR,255-CENOMSG           ZERO FLAGS            M4502
         TM    CESWTR2,WAIT3            IS WTOR FLAG ON           M4502
         BZ    DPREI46                  NO. DONT ZERO ONCOMM      M4502
         TM    CESWTR1,EDOTMSG          MSG 105D                  M4502
         BO    DPREI46                  YES. DONT ZERO FLAG       M4502
         NI    CESWTR2,255-ONCOMM       ZERO ONCOMM FLAG          M4502
DPREI46  NI    CESWTR1,255-RETRIEVE                               M4502
         TM    CESWTR2,COMMLOOP         INTERNAL PWT              M4502
         BO    DPREI41                  YES. DONT ZERO SWITCH     M4502
         NI    CESWTR,255-IMRESP        ZERO IM RESP FLAG         M4502
DPREI41  L     R14,SAVE14          RESTORE LINK ADDRESS          S20203
         BR    R14               RETURN TO CALLING ENTRY-EXIT RTN
         SPACE
* TRANSLATE AND MOVE REPLY TO PRINT BUFFER.
         SPACE
DPR211   L     R8,REPLGTAD         REPLY ADDRESS                  01906
         LA    R6,DPRXLATE         TRANSLATE TABLE ADDRESS
         SR    R9,R9               CLEAR
         IC    R9,REPLGTAD         GET REPLY LENGTH FOR TRANSLATE
         ST    R9,MHRPARM               MESSAGE LENGTH           S20203
         EX    R9,REPXLATE         TRANSLATE INBUFR
         SPACE
         MVI   PRTBUFR+1,C' '      LOAD BLANK
         MVC   PRTBUFR+2(120),PRTBUFR+1  CLEAR WITH BLANKS
         MVI   PRTBUFR,X'09'       LOAD PRINT CTRL CHARC- SPACE 1 LINE
         L     R8,REPLGTAD         REPLY ADDRESS                  01906
         SR    R9,R9               CLEAR REG
         CLI   REPLGTAD,X'48'      CHECK REPLY IS NOT OVER 72 CHARCS.
         BNH   DPR211A             NO.  LESS OR EQUAL
         IC    R9,INBUFMAX         YES.  LOAD 72 AS REPLY LENGTH
         B     *+8
DPR211A  IC    R9,REPLGTAD         REPLY LENGTH
         LA    R7,1
         SR    R9,R7               SUBT 1 TO ADJUST MVC COUNT
         EX    R9,DPRMVC           MOVE TO PRINT BUFFER
         B     DPR201              GO PRINT REPLY
         SPACE
DPRMVC   MVC   PRTBUFR+1,0(R8)     MOVE TO PRINT BUFFER
REPXLATE TR    0(0,R8),0(R6)       TRANSLATE REPLY - INBUFR
MSGMVC   MVC   12(0,R1),0(R8)      MOVE TO WTO MESSAGE            M1005
MSGMVC1  MVC   4(0,R1),0(R8)       MOVE TO WTO MESSAGE            01906
ECB1R    DC    F'0'                     ECBLIST FOR MULT WAIT    S20203
ECB2R    DC    F'0'                                              S20203
ECB3R    DC    F'0'                                              S20203
RESAVE14 DC    F'0'                                              S20203
SAVESW4  DC    X'00'                                              M4501
CSWTRSV  DC    X'00'                                              M4502
MHRPARM  DC    F'0'                                               M4501
RTALKID  DC    C'IFD255I '                                        M4501
OURMSGID DC    C'IFD100I'          OUR MESSAGE ID                 01906
         SPACE
         SPACE
DPRDCB   DCB   DSORG=PS,MACRF=WC,DDNAME=DIAGMSG,BLKSIZE=120,RECFM=FM,  X
               SYNAD=DPRERR
         SPACE
* SET RETURN CODE FOR ANY ERROR.
         SPACE
DPRERR   CLI   RETCODE,X'00'       ANY CODE ALREADY SET
         BZ    *+8                 YES   BYPASS
         MVI   RETCODE,X'08'       NO.  SET ERROR RETURN CODE
         BR    R14                 RETURN TO CONTINUE PARALLEL PRINT
         SPACE
         CNOP  0,4
DPRECB1  DC    XL4'0'              DPRINT ECB
WAITECB  DC    XL4'0'
SAVE14   DC    XL4'0'
INBUFMAX DC    X'4800'
MSGMAX   DC    X'003D'             61 CHARACTERS MAX FOR WTO MSG  M1005
PB       EQU   *
         SPACE 2
***********************************************************************
*     TRANSLATE TABLE FOR WTOR REPLY - LOWER TO UPPER CASE EBCDIC     *
***********************************************************************
         SPACE
DPRXLATE DC    256CL1'01'
         ORG   DPRXLATE
         DC    X'00'
         ORG   DPRXLATE+C' '       SPECIAL CHARACTERS             M1005
         DC    C' '
         ORG   DPRXLATE+C'!'
         DC    C'!'
         ORG   DPRXLATE+C'Ö'
         DC    C'Ö'
         ORG   DPRXLATE+C'.'
         DC    C'.'
         ORG   DPRXLATE+C'<'
         DC    C'<'
         ORG   DPRXLATE+C'('
         DC    C'('
         ORG   DPRXLATE+C'+'
         DC    C'+'
         ORG   DPRXLATE+C'×'
         DC    C'×'
         ORG   DPRXLATE+C'&&'
         DC    C'&&'
         ORG   DPRXLATE+C'$'
         DC    C'$'
         ORG   DPRXLATE+C'*'
         DC    C'*'
         ORG   DPRXLATE+C')'
         DC    C')'
         ORG   DPRXLATE+C';'
         DC    C';'
         ORG   DPRXLATE+C'^'
         DC    C'^'
         ORG   DPRXLATE+C'-'
         DC    C'-'
         ORG   DPRXLATE+C'/'
         DC    C'/'
         ORG   DPRXLATE+C','
         DC    C','
         ORG   DPRXLATE+C'%'
         DC    C'%'
         ORG   DPRXLATE+C'_'
         DC    C'_'
         ORG   DPRXLATE+C'>'
         DC    C'>'
         ORG   DPRXLATE+C'?'
         DC    C'?'
         ORG   DPRXLATE+C':'
         DC    C':'
         ORG   DPRXLATE+C'#'
         DC    C'#'
         ORG   DPRXLATE+C'@'
         DC    C'@'
         ORG   DPRXLATE+C''''
         DC    C''''
         ORG   DPRXLATE+C'='
         DC    C'='
         ORG   DPRXLATE+C'"'
         DC    C'"'
         ORG   DPRXLATE+C'a'       LOWER CASE                     M1005
         DC    C'A'
         ORG   DPRXLATE+C'b'
         DC    C'B'
         ORG   DPRXLATE+C'c'
         DC    C'C'
         ORG   DPRXLATE+C'd'
         DC    C'D'
         ORG   DPRXLATE+C'e'
         DC    C'E'
         ORG   DPRXLATE+C'f'
         DC    C'F'
         ORG   DPRXLATE+C'g'
         DC    C'G'
         ORG   DPRXLATE+C'h'
         DC    C'H'
         ORG   DPRXLATE+C'i'
         DC    C'I'
         ORG   DPRXLATE+C'j'
         DC    C'J'
         ORG   DPRXLATE+C'k'
         DC    C'K'
         ORG   DPRXLATE+C'l'
         DC    C'L'
         ORG   DPRXLATE+C'm'
         DC    C'M'
         ORG   DPRXLATE+C'n'
         DC    C'N'
         ORG   DPRXLATE+C'o'
         DC    C'O'
         ORG   DPRXLATE+C'p'
         DC    C'P'
         ORG   DPRXLATE+C'q'
         DC    C'Q'
         ORG   DPRXLATE+C'r'
         DC    C'R'
         ORG   DPRXLATE+C's'
         DC    C'S'
         ORG   DPRXLATE+C't'
         DC    C'T'
         ORG   DPRXLATE+C'u'
         DC    C'U'
         ORG   DPRXLATE+C'v'
         DC    C'V'
         ORG   DPRXLATE+C'w'
         DC    C'W'
         ORG   DPRXLATE+C'x'
         DC    C'X'
         ORG   DPRXLATE+C'y'
         DC    C'Y'
         ORG   DPRXLATE+C'z'
         DC    C'Z'
         ORG   DPRXLATE+C'A'       UPPER CASE                     M1005
         DC    C'A'
         ORG   DPRXLATE+C'B'
         DC    C'B'
         ORG   DPRXLATE+C'C'
         DC    C'C'
         ORG   DPRXLATE+C'D'
         DC    C'D'
         ORG   DPRXLATE+C'E'
         DC    C'E'
         ORG   DPRXLATE+C'F'
         DC    C'F'
         ORG   DPRXLATE+C'G'
         DC    C'G'
         ORG   DPRXLATE+C'H'
         DC    C'H'
         ORG   DPRXLATE+C'I'
         DC    C'I'
         ORG   DPRXLATE+C'J'
         DC    C'J'
         ORG   DPRXLATE+C'K'
         DC    C'K'
         ORG   DPRXLATE+C'L'
         DC    C'L'
         ORG   DPRXLATE+C'M'
         DC    C'M'
         ORG   DPRXLATE+C'N'
         DC    C'N'
         ORG   DPRXLATE+C'O'
         DC    C'O'
         ORG   DPRXLATE+C'P'
         DC    C'P'
         ORG   DPRXLATE+C'Q'
         DC    C'Q'
         ORG   DPRXLATE+C'R'
         DC    C'R'
         ORG   DPRXLATE+C'S'
         DC    C'S'
         ORG   DPRXLATE+C'T'
         DC    C'T'
         ORG   DPRXLATE+C'U'
         DC    C'U'
         ORG   DPRXLATE+C'V'
         DC    C'V'
         ORG   DPRXLATE+C'W'
         DC    C'W'
         ORG   DPRXLATE+C'X'
         DC    C'X'
         ORG   DPRXLATE+C'Y'
         DC    C'Y'
         ORG   DPRXLATE+C'Z'
         DC    C'Z'
         ORG   DPRXLATE+C'0'
         DC    C'0'
         ORG   DPRXLATE+C'1'
         DC    C'1'
         ORG   DPRXLATE+C'2'
         DC    C'2'
         ORG   DPRXLATE+C'3'
         DC    C'3'
         ORG   DPRXLATE+C'4'
         DC    C'4'
         ORG   DPRXLATE+C'5'
         DC    C'5'
         ORG   DPRXLATE+C'6'
         DC    C'6'
         ORG   DPRXLATE+C'7'
         DC    C'7'
         ORG   DPRXLATE+C'8'
         DC    C'8'
         ORG   DPRXLATE+C'9'
         DC    C'9'
         SPACE
         SPACE
         SPACE
DPRCOM   IFDCOM
         END
