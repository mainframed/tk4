         TITLE 'IFDOLT43  FREECORE MODULE'
IFDOLT43 CSECT          ENTRY POINT
***********************************************************************
*                                                                     /
*                             PROLOGUE                                *
*                                                                     *
*   STATUS: THIS IS THE SECOND LEVEL OF THE FREECORE MODULE.          *
*                                                                     *
*                                                                     *
*   FUNCTION;                                                         *
*           THIS MODULE WILL FREE ALLOCATED MAIN STORAGE SPACE        *
*           FROM THE MORECORE AREA. THE MODULE ACCOMPLISHES           *
*           THIS BY SCANNING THE STORAGE QUEUE ELEMENTS CONST-        *
*           RUCTED BY IFDOLT42 IN SEARCH FOR A MATCH BETWEEN          *
*           THE INPUT PARAMETER AND THE CONTENTS OF EACH QUEUE        *
*           ELEMENT. WHEN A MATCH IS FOUND THE INUSE BIT IS           *
*           SET TO ZERO AND THE CORE IS MADE AVAILABLE TO THE         *
*           OLT ON ANY SUBSEQUENT MORECORE MACRO REQUEST.             *
*                                                                     *
*                                                                     *
*   ENTRY POINT;                                                      *
*           IFDOLT43 IS THE MAIN AND ONLY ENTRY POINT TO THIS         *
*           MODULE.                                                   *
*                                                                     *
*                                                                     *
*   INPUT:  REGISTER ONE WILL POINT TO A PARAMETER LIST CONTAINING    *
*           THE ADDRESS OF THE CORE TO BE FREED.                      *
*                                                                     *
*                                                                     *
*   OUTPUT: REGISTER 15 WILL CONTAIN ONE OF THE FOLLOWING RETURN      *
*           CODES                                                     *
*                   A. X'00'  FREECORE WAS SUCCESSFUL                 *
*                                                                     *
*                   B. X'04'  FACILITY NOT AVAILABLE                  *
*                                                                     *
*                   C. X'08' INPUT PARAMETER ERROR.                   *
*                             I.E. ADDRESS NOT IN QUEUE ELEMENT       *
*                                                                     *
*                                                                     *
*   EXTERNAL ROUTINES:                                                *
*           NONE                                                      *
*                                                                     *
*                                                                     *
*   EXITS NORMAL:                                                     *
*           ALL NORMAL EXITS WILL BE AT THE LABEL EXIT00              *
*                                                                     *
*                                                                     *
*   EXITS ERROR;                                                      *
*     ALL ERROR EXITS WILL BE AT THE LABEL EXIT04 OR EXIT08.          *
*                                                                     *
*                                                                     *
*   TABLES/WORKAREAS:                                                 *
*     OLTEPS COMMON AREA IS THE ONLY EXTERNAL TABLE REFERENCED        *
*     BY IFDOLT43                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*                   REGISTER EQUATES FOLLOW                           *
*                                                                     *
***********************************************************************
ZERO     EQU   0
PARMREG  EQU   1                  PARAMETER LIST POINTER
COMMON   EQU   2                  OLTEP COMMON AREA POINTER
FREEADDR EQU   3                  ADDRESS OF AREA TO BE FREED
HEAD     EQU   5                  POINTS TO MORECORE SPACE
TESTREG  EQU   6                  POINTS TO OLTS MORECORE AREA
BASEREG  EQU   11                 IFDOLT43 BASE REGISTER
SAVE     EQU   13                 REG13 FOR SAVING REGISTERS
EXIT     EQU   14                 RETURN REGISTER
RETURNRG EQU   15                 RETURN CODE REGISTER
D0       EQU   0                  DISPLACEMENT VALUE
D1       EQU   1                  DISPLACEMENT VALUE
D4       EQU   4                  DISPLACEMENT VALUE
D8       EQU   8                  DISPLACEMENT VALUE
TWELVE   EQU   12                 DISPLACEMENT OF 12
D12      EQU   12                 DISPLACEMENT VALUE
D20      EQU   20                 DISPLACEMENT VALUE
HEX00    EQU   X'00'              INDICATES FREE SPACE
HEX01    EQU   X'01'              DISPLACEMENT FIELD
HEX02    EQU   X'02'              MACRO LEVEL EXPECTED
HEX08    EQU   X'08'              DISPLACEMENT FIELD
HEX80    EQU   X'80'              INDICATES ALLOCATED SPACE
HEXEF    EQU   X'EF'              END OF CORE SPACE
**********************************************************************
         EJECT
         SPACE  2
         SPACE  2
         SAVE  (14,12),,IFDOLT43&SYSDATE SAVE MOD ID, DATE, GPRS
         BALR  BASEREG,ZERO       ESTABLISH ADDRESSABILITY
         USING *,BASEREG          SET UP ADDRESSABILITY
         USING AAAAA,COMMON       SET UP ADDRESSABALITY FOR COMMON
*                                 AREA
         ST    SAVE,SAVE1+D4      SAVE ADDRESS OF CALLER SAVE AREA
         XR    RETURNRG,RETURNRG  CLEAR RETURN CODE REGISTER
LVLCHK   CLI   D1(PARMREG),HEX02  IS THE MACRO LEVEL OK
         BNE   EXIT04             NO, PACK HIM IN        **ERROR**
         L     HEAD,PLKMOR+D8   GET POINTER TO MORE CORE AREA
         LTR   HEAD,HEAD          HAS A MORE CORE ALLOCATION
*                                 BEEN SUCCESSFULLY ISSUED
         BNM   EXIT08             NO MORECORE SPACE DEFINRD
*                                 CONTROL QUEUE BLOCK.
         L     FREEADDR,D4(PARMREG) OBTAIN ADDRESS TO BE FREED
         L     FREEADDR,D0(FREEADDR) GET ACTUAL ADDRESS TO BE FREED
FREECHK  LA    TESTREG,D8(HEAD)   GET EQUILIVANT  ADDR SUPPLIED
*                                 TO OLT AT MORECORE TIME
         CR    FREEADDR,TESTREG   IS THIS THE ADDRESS TO BE FREED
         BNE   HEADUP             NO, GO GET ADDR OF NEXT BLK
         TM    D0(HEAD),HEX80     WAS THIS CORE ASSGND.TO OLT   XA01373
         BNO   EXIT08             NO****OLT IN ERROR****        XA01373
FREEBLK  MVI   D0(HEAD),HEX00     FREE ALLOCATED SPACE
         B     EXIT00             GO RETURN A SUCCESSFUL  RETURN
HEADUP   EQU   *
         CLI   D0(HEAD),HEXEF     NO, IS THIS THE END OF M.C.
*                                 AREA
         BE    EXIT08             YES, EXIT WITH ERROR
         L     HEAD,D4(HEAD)      GET POINTER TO NEXT BLK
         B     FREECHK            CONTINUE SCANNING
*                                 SET RETURN CODE AS WARRENTS
EXIT08   LA    RETURNRG,D4(RETURNRG)
EXIT04   LA    RETURNRG,D4(RETURNRG)
EXIT00   L     EXIT,D12(SAVE)     LOAD RETURN POINT
         LM    ZERO,TWELVE,D20(SAVE) RESTORE OCT REGS
         BR    EXIT               EXIT THIS MODULE
SAVE1    DC    18F'0'             SAVE AREA FOR DEBUGGING
AAAAA    IFDCOM
         END
