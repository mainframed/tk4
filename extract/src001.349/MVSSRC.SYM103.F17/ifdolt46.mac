         TITLE  'CENTRALIZED EXTRACTOR IFDOLT46'
***********************************************************************
*                                                                     *
*TITLE                                                                *
*    IFDOLT46 - CENTRALIZED EXTRACTOR                                 *
*      A028000,C299400-300100,402000,A403000,660300             XM2361
*                                                                     *
*STATUS                                                               *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION                                                            *
*    THIS MODULE PROVIDES THE FOLLOWING FUNCTIONS:                    *
*        1.READS RECORDS FROM THE RECORD ENTRY AREA OF SYS1.LOGREC    *
*          OR FROM A HISTORY DATA SET                                 *
*        2.READS RECORDS FROM THE SDR AREA OF SYS1.LOGREC             *
*        3.RESETS TO READ FROM THE BEGINNING OF THE DATA SET          *
*        4.PERFORMS AN END OF JOB FUNCTION                            *
*        5.PERFORMS A TEST TO DETERMINE IF MORE RECORDS WERE WRITTEN  *
*          ON THE SYS1.LOGREC DATA SET WHILE THAT SET WAS BEING       *
*          READ BY THE MODULE AND SETS TO READ THESE RECORD IF        *
*          REQUESTED TO DO SO                                         *
*                                                                     *
*ENTRY POINTS                                                         *
*    FROM IFDOLT26                                                    *
*                                                                     *
*INPUT                                                                *
*    WHEN THIS MODULE IS ENTERED REG 1 POINTS TO A PARAMETER LIST     *
*    WHICH IS SET UP AS FOLLOWS:                                      *
*        WORD1:  ADDRESS OF READ BUFFER                               *
*        WORD2:  BYTES 0-1  BYTE COUNT                                *
*                BYTE 2     RESERVED                                  *
*                BYTE 3     OPTION SWITCHES                           *
*        WORD3:  BYTE 0     RECORD TYPE                               *
*                BYTE 1     ADDITIONAL SEARCH KEY                     *
*                BYTES 2-3   NOT USED                                 *
*                                                                     *
*OUTPUT                                                               *
*    REGISTER 15 WILL CONTAIN A RETURN CODE DEPENDING ON THE STATUS   *
*    OF THE OPERATION                                                 *
*                                                                     *
*EXITS                                                                *
*    TO OPEN,CLOSE,ENQ,DEQ,READ,CHECK,EXCP MACROS                     *
*    RETURN TO IFDOLT26                                               *
*                                                                     *
* D579000-580000,C577000                                       YM05878*
***********************************************************************
IFDOLT46 CSECT
         ENTRY IFD46
         ENTRY XDCB                    SERLOG DCB                Y02008
         ENTRY ACCUMDCB                ACCIN DCB                 Y02008
IFD46    DS    0F
         USING *,XBASEREG
***********************************************************************
*                                                                     *
*****                    TABLE OF EQUATES                         *****
*                                                                     *
***********************************************************************
R0       EQU   0
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 2
XMSGMODR EQU   15             CONDITION CODE RETURN REGISTER
XPARMREG EQU   1              PARAMETER REGISTER
XWORK1   EQU   2              WORK REGISTER
XWORK2   EQU   3              WORK REGISTER
XWORK3   EQU   4              WORK REGISTER
XWORK4   EQU   5              WORK REGISTER
XWORK5   EQU   6              WORK REGISTER
XWORK6   EQU   7              WORK REGISTER
XWORK7   EQU   8              WORK REGISTER
XWORK8   EQU   9              WORK REGISTER
LINKNAME EQU   10             LINK NAME POINTER REGISTER
RETURNAD EQU   11             RETURN ADDR REGISTER TO MAIN CODE
XMISCREG EQU   0              MISCELLANEOUS
XGRSAVE  EQU   13             GPR SAVE AREA ADDR REGISTER
XRETREG  EQU   14             RETURN ADDR REGISTER TO SUP
XBASEREG EQU   12             MODULE BASE REGISTER
ZERO     EQU   0
ERR      EQU   X'FF'                   ERROR CODE               XM2361
NORMAL   EQU   X'7F'
INTCEPT  EQU   X'44'
XFOXESX  EQU   X'FF'
ONE      EQU   1
TWO      EQU   2
THREE    EQU   3
FOUR     EQU   4
FIVE     EQU   5
SIX      EQU   6
SEVEN    EQU   7
EIGHT    EQU   8
NINE     EQU   9
TEN      EQU   10
SIXTEEN  EQU   16
NINETEEN EQU   19
TWENTY   EQU   20
TWENTY2  EQU   22
TWENTY6  EQU   26
TWENTY7  EQU   27
TWENTY8  EQU   28
THIRTY   EQU   30
THIRTY7  EQU   37
THIRTY8  EQU   38
FORTY    EQU   40
FORTY8   EQU   48
FIFTY    EQU   50
XTEN     EQU   X'10'
CONCAT   EQU   X'08'
HIST1ST  EQU   X'04'
NOSELECT EQU   X'00'
RECSELCT EQU   X'80'
FULLKEY  EQU   X'40'
INITSW   EQU   X'02'
SYSFIRST EQU   X'20'
XDCBF0   EQU   44
XDEBF0   EQU   36
XDCBF1   EQU   28
EXTENTNG EQU   X'42'
LOGFIRST EQU   X'20'
HISFIRST EQU   X'04'
HISTOFF  EQU   X'FB'
UNITXCPT EQU   X'01'
SYSDONE  EQU   X'10'
LOGRECIN EQU   X'80'
HISTIN   EQU   X'40'
HISTDONE EQU   X'20'
         EJECT
***********************************************************************
*                                                                     *
*****                   B R A N C H    T A B L E                  *****
*                                                                     *
***********************************************************************
         SPACE 2
XREADENV B     RDENVREC            READ ENVIRONMENT RECORDS
XSTAGAIN B     RESTART             RESTART FROM BEGINNING OF DATA SET
XENDJOB  B     ENDOFJOB            PERFORM END OF JOB
XCLEARSY B     XCLEARSY                 EREP USE ONLY
XREADSDR B     XREADSDR                 EREP USE ONLY
XWRITSDR B     XWRITSDR                 EREP USE ONLY
XWRITREA B     XWRITREA                 EREP USE ONLY
XMOREREC B     MOREREC             TEST FOR MORE RECORDS
EXSWITCH DC    X'00'
         EJECT
***********************************************************************
*                                                                     *
*****                    INITIALIZATION                           *****
*                                                                     *
***********************************************************************
         DC    C'IFDOLT46 &SYSDATE'    * ID FOR OBJECT                *
         SPACE 2
INITIALZ TM    EXSWITCH,INITSW               Q/IS THIS INITIAL ENTRY
         BNO   INITLZ01                      YES--INITIALIZE
         BR    XWORK8                        NO--RETURN
INITLZ01 MVI   EXSWITCH,INITSW               TURN ON INITIAL ENTRY SW.
         ST    XPARMREG,PARMSAVE             SAVE PARAMETER REG.
         MVC   XRDCCW+ONE(THREE),ONE(XPARMREG) READ BUFFER ADDR TO CCW
         MVC   XRDCCW+SIX(TWO),FOUR(XPARMREG)  BYTE COUNT TO CCW
         ENQ   (LOGREC,RESID,E,,STEP),RET=HAVE
         RDJFCB (XDCB,INOUT)       READ THE JFCB FOR SERLOG
         CLC   JDSNAME(8),COMPNAME COMPERE DATASET NAME FOR NULLFILE
         BE    SKIPSYS1            DON'T OPEN SYS1.LOGREC
         OPEN  (XDCB,INOUT)
SKIPSYS1 OPEN  (ACCUMDCB,INPUT)        OPEN HISTORY
         L     XPARMREG,PARMSAVE             RESTORE PARM REG
         TM    XDCB+FORTY8,XTEN              Q/ DID SYS1.LOGREC OPEN
         BNO   INITLZ02                      NO--CHECK HISTORY
         OI    EXSWITCH,LOGRECIN             TURN ON LOGREC INPUT SW
         TM    ACCUMDCB+FORTY8,XTEN          Q/ DID HISTORY OPEN
         BNO   INITLZ03                      NO--SYS1.LOGREC ONLY INPUT
         OI    EXSWITCH,HISTIN               TURN ON HISTORY INPUT SW
         OI    EXSWITCH,CONCAT               TURN ON CONCATENATION SW
         TM    SEVEN(XPARMREG),LOGFIRST      Q/ SYS1.LOGREC 1ST INPUT
         BO    INITLZ03                      YES--READ SYS1.LOGREC 1ST
         OI    EXSWITCH,HISFIRST             NO--TURN ON HISTORY 1ST SW
         B     INITLZ03                      PREPARE TO READ HISTORY
INITLZ02 TM    ACCUMDCB+FORTY8,XTEN          Q/ DID HISTORY OPEN
         BNO   INITERR                       NO--ERROR   RETURN
         OI    EXSWITCH,HISFIRST             TURN ON HISTORY 1ST SW
         B     INITLZ04                      PREPARE TO READ HISTORY
INITLZ03 MVC   XRDCCW+ONE(THREE),ONE(XPARMREG) READ BUFFER ADDR TO CCW
         MVC   XRDCCW+SIX(TWO),FOUR(XPARMREG) BYTE COUNT TO CCW
*        MOVE SEEK ADDRESS FROM DEB TO EXCP SEEK BUFFER
         L     XWORK1,XDCB+XDCBF0            LOAD DEB POINTER
         MVC   XSEEKBUF+ONE(SIX),XDEBF0(XWORK1)   MOVE BBCCHH TO IOB
         MVI   XSEEKBUF+SEVEN,ZERO           SET ID FIELD IN SEEKBUF 0
*        STORE IOB ADDRESS IN DCB
         LA    XWORK1,XIOB                   ADDRESS OF IOB
         ST    XWORK1,XDCB+XDCBF1            STORE IOB ADDRESS IN DCB
*        BRANCH TO READ HEADER RECORD
         LA    XWORK6,XRCCWS                 ADDR OF CCW CHAIN
         BAL   RETURNAD,XRDAD                READ SYS1.LOGREC
         B     RDHDRERR                      EOD RETURN
         B     RDHDRERR                      ERROR RETURN
         L     XPARMREG,PARMSAVE             LOAD PARMAMETER REG
         L     XWORK2,ZERO(XPARMREG)         GET READ BUFFER ADDRESS
         MVC   XHDRBUF(FIFTY),ZERO(XWORK2)   SAVE HEADER RECORD
         MVI   XRESTART+SIX,ZERO             RESTART ADDR ID TO 0
         MVC   UCBCNT(TWO),THIRTY7(XWORK2)   NO.OF SYSTEM UCB'S
*        VERIFY AUTHENTICITY OF HEADER RECORD
         CLI   XSAFBYTE,XFOXESX              CHECK SAFETY BYTE
         BNE   RDHDRERR
         BR    XWORK8                        RETURN TO CALLING ROUTINE
INITLZ04 BR    XWORK8
INITERR  LA    R15,12                   BOTH OPENS FAILED
         B     COMRTRN                  COMMON RETURN
RDHDRERR LA    R15,16                   READ HEADER ERROR
         B     COMRTRN                  COMMON RETURN
***********************************************************************
*****       READ FROM STATISTICAL DATA RECORDER ESDRN AREA        *****
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
         EJECT
***********************************************************************
*                                                                     *
*****               READ FROM RECORD ENTRY AREA                   *****
*                                                                     *
***********************************************************************
         SPACE 2
RDENVREC SAVE  (14,12)
         LR    RETURNAD,XGRSAVE        FOLLOW
         ST    XGRSAVE,MYSAVE+FOUR       STANDARD
         LA    XGRSAVE,MYSAVE             LINKAGE
         ST    XGRSAVE,EIGHT(RETURNAD)     CONVENTIONS
         TM    EXSWITCH,INITSW               Q/INITIAL ENTRY
         BO    RDBEGIN                       NO--CONTINUE
         BAL   XWORK8,INITIALZ               INITIALIZE
         MVC   XSEEKBUF+ONE(SEVEN),XHDRBUF+NINETEEN YES-UPDATE SEEK
*                                                    ADDR TO START OF
*                                                     RECORD ENTRY AREA
RDBEGIN  L     XPARMREG,PARMSAVE             RESTORE PARM REG
         LH    XWORK4,FOUR(XPARMREG)    SAVE COUNT               S21052
         L     XWORK2,ZERO(XPARMREG)         GET READ BUFFER ADDRESS
         TM    EXSWITCH,HISFIRST             Q/IS HISTORY 1ST ON
         BNO   READSYS1                      NO--GO READ SYS1.LOGREC
RDENVR02 BAL   XWORK8,XACCINPT               GO READ HISTORY
         B     NORMRTRN                      EQUAL NORMAL RETURN
READSYS1 L     XPARMREG,PARMSAVE             RESTORE PARM REG
         L     XWORK2,ZERO(XPARMREG)         RESTORE BUFFER REG
         CLC   XHDRBUF+THIRTY(SEVEN),XSEEKBUF+ONE  CHECK FOR EOD
         BNE   READTYPE                      NO-- CHK TYPE OF READ
RDSYS001 OI    EXSWITCH,SYSDONE              TURN ON LOGREC READ SW
         TM    EXSWITCH,CONCAT               Q/CONCATENATION
         BNO   ENDDATA                       NO--GIVE END OF DATA RETRN
         NI    EXSWITCH,HISTOFF              TURN OFF HISTORY 1ST SW
         TM    EXSWITCH,HISTDONE             Q/HAS HISTORY BEEN READ
         BO    ENDDATA                       YES--GIVE EOD  RETURN
         B     RDENVR02                 NO--GO READ HISTORY
READTYPE TM    SEVEN(XPARMREG),RECSELCT      Q/ SELECT BY RECORD TYPE
         BNO   READALL                       NO-- SET TO READ ALL REC.
         TM    SEVEN(XPARMREG),FULLKEY       Q/ SELECT BY FULL KEY
         BO    PARMERR                       YES--PARAMETER ERROR
READALL  LA    XWORK6,XRCCWS                 ADDR OF CCW CHAIN
         MVC   XRDCCW+ONE(THREE),ONE(XPARMREG) READ BUFFER ADDR TO CCW
         MVC   XRDCCW+SIX(TWO),FOUR(XPARMREG)  COUNT TO CCW
RDSYS002 BAL   RETURNAD,XRDAD                READ SYS1.LOGREC
         B     RDSYS001                      EOD RETURN
         B     DISKERR                       ERROR RETURN
         B     NORMRTRN                      MORMAL RETURN
** TAPE EOD RETURN
HISTEOD  OI    EXSWITCH,HISTDONE             TURN ON HISTORY READ SW
         TM    EXSWITCH,CONCAT               Q/CONCATENATION
         BNO   ENDDATA                       NO--GIVE EOD RETURN
         NI    EXSWITCH,HISTOFF              YES--TURN OFF HIST 1ST SW
         TM    EXSWITCH,SYSDONE              Q/HAS LOGREC BEEN READ
         BO    ENDDATA                       YES-- GIVE EOD RETURN
         B     READSYS1
DISKERR  LA    R15,20                   DISK READ ERROR RETURN
         B     COMRTRN                  COMMON RETURN
NORMRTRN LA    R15,0                    NORMAL RETURN
COMRTRN  L     XGRSAVE,FOUR(XGRSAVE)    RESTORE REG 13
         L     R14,12(R13)              RESTORE REG 14
         LM    R0,R12,20(R13)           RESTORE REGS
         MVI   12(R13),X'FF'            RETURN INDICATION
         BR    14                       RETURN
ENDDATA  LA    R15,4                    END OF DATA FOR SDR
         B     COMRTRN                  COMMON RETURN
*ERROR  TAPE
TAPERRTN MVI   ERRORSW,ERR             SET ERR RET TO OS        X02008
         BR    R14                     RETURN                   X02008
PARMERR  LA    R15,52                   PARM FIELD ERROR
         B     COMRTRN                  COMMON RETURN
         EJECT
***********************************************************************
*                                                                     *
*****               RESTART FROM BEGINNING OF DATA SET            *****
*                                                                     *
***********************************************************************
         SPACE 2
RESTART  SAVE  (14,12)
         LR    RETURNAD,XGRSAVE        FOLLOW
         ST    XGRSAVE,MYSAVE+FOUR       STANDARD
         LA    XGRSAVE,MYSAVE             LINKAGE
         ST    XGRSAVE,EIGHT(RETURNAD)     CONVENTIONS
         TM    ACCUMDCB+FORTY8,XTEN          Q/ WAS HISTORY INPUT
         BNO   RESTART1                       NO/- BRANCH
         CLOSE (ACCUMDCB,REREAD)                                AZ17685
         OPEN  (ACCUMDCB,INPUT)                                 AZ17685
         TM    ACCUMDCB+FORTY8,XTEN        DID IT OPEN          AZ17685
         BO    RESTAROK            YES                          AZ17685
         LA    R15,8               RESET ERROR                  AZ17685
RESTAROK EQU  *                                                 AZ17685
         NI    EXSWITCH,X'DF'  TURN OFF HISTORY READ SWITCH
RESTART1 TM    XDCB+FORTY8,XTEN              Q/ WASSYS1.LOGREC INPUT
         BNO   RESRTRN                        NO/RETURN
         CLOSE (XDCB,REREAD),TYPE=T          ISSUE TEMP CLOSE
         NI    EXSWITCH,X'EF'  TURN OFF LOGREC READ SWITCH
         MVC   XSEEKBUF+ONE(SEVEN),XHDRBUF+NINETEEN
         MVI   XSEEKBUF+SEVEN,ZERO           SET ID FIELD TO ZERO
RESRTRN  B    NORMRTRN             NORMAL RETURN
         EJECT
***********************************************************************
*                                                                     *
*****               CHECK FOR ADDITIONAL RECORDS                      *
*                                                                     *
***********************************************************************
         SPACE 2
MOREREC  SAVE  (14,12)
         LR    RETURNAD,XGRSAVE        FOLLOW
         ST    XGRSAVE,MYSAVE+FOUR       STANDARD
         LA    XGRSAVE,MYSAVE             LINKAGE
         ST    XGRSAVE,EIGHT(RETURNAD)     CONVENTIONS
         TM    EXSWITCH,INITSW               Q/IS THIS INITIAL ENTRY
         BNO   MORERROR                      YES--INDICATE ERROR
         TM    XDCB+FORTY8,XTEN              Q/IS LOGREC OPEN
         BNO   MORERROR
         L     XWORK2,ZERO(XPARMREG)         GET READ BUFFER ADDRESS
         MVC   XSEEKBUF+THREE(FIVE),ZERO(XWORK2) UPDATE SEEK
         MVC   XSAVEBUF(FORTY),XHDRBUF+TEN   SAVE DATA FIELD OF HDR
         BAL   XWORK8,INITLZ03               RE-READ HDR RECORD
         CLC   XSAVEBUF+TWENTY(SEVEN),XHDRBUF+THIRTY  COMPARE HDR RECS
         BE    NORMRTRN                 NORMAL RETURN
* HDR RECORDS UNEQUAL--UPDATE SEEK ADDRESS
        MVC   XSEEKBUF+THREE(FIVE),XSAVEBUF+TWENTY2             AZ19039
         LA    R15,48                   MORE RECORD RETURN
         B     COMRTRN                  COMMON RETURN
MORERROR B     PARMERR                  PARM ERROR
         EJECT
***********************************************************************
*                                                                     *
*****               ROUTINE TO READ SYS1.LOGREC (DISK)            *****
*                                                                     *
***********************************************************************
         SPACE 2
XRDAD    STM   XMISCREG,XMSGMODR,XGREGSAV    SAVE GPRS
         LR    XWORK1,XWORK6                 ADDR OF CCW CHAIN
*                                             WILL BE ALTERED BY
*                                              READ REA ROUTINE
         ST    XWORK1,XIOBCCW                STORE IN IOB
         MVC   XIOBSEEK(8),XSEEKBUF          MOVE SEEK ADDR TO IOB
XRDAD01  XC    XRWECB(4),XRWECB              CLEAR ECB
         L     XWORK2,ZERO(XPARMREG)         GET READ BUFFER ADDRESS
         LA    XPARMREG,XIOB                 ADDRESS OF IOB
         SVC   EXCP                          EXECUTE CHANNEL PROGRAM
         WAIT  1,ECB=XRWECB
         CLI   XRWECB,NORMAL                 CHECK FOR NORMAL
         BE    XRDADOK                        BRANCH IF READ OK
         CLI   XRWECB,INTCEPT                CHECK FOR INTERCEPT
         BE    XRDAD01                        TRY AGAIN
         CLI   XRWECB,EXTENTNG               CHECK FOR OUT OF EXTENT
         BE    XRDADND                        BRANCH ON OUT OF EXTENT
         TM    XIOBCSW+4,UNITXCPT            TEST FOR UNIT EXCEPTION
         BNO   XRDADNG                        BRANCH NOT EOF
*EOD CONDITION
XRDADND  XC    XDADBUF(EIGHT),XDADBUF        CLEAR DATA BUFFER ON EOF
         LM    XMISCREG,XMSGMODR,XGREGSAV    RESTORE REGS
         BR    RETURNAD                      RETURN TO EOD RETURN
*READ OK
XRDADOK  MVC   XSEEKBUF+THREE(FIVE),ZERO(XWORK2) UPDATE SEEK ADDR
         LM    XMISCREG,XMSGMODR,XGREGSAV    RESTORE REGS
         LA    RETURNAD,EIGHT(RETURNAD)      INCREMENT LINK REG
         BR    RETURNAD                      READ OK RETURN
XRDADNG  MVC   XSEEKBUF+THREE(FIVE),ZERO(XWORK2) UPDATE SEEK ADDR
         LM    XMISCREG,XMSGMODR,XGREGSAV    RESTORE REGS
         LA    RETURNAD,FOUR(RETURNAD)       INCREMENT LINK REG
         BR    RETURNAD                      READ ERROR RETURN
         EJECT
***********************************************************************
*                                                                     *
*****               ROUTINE TO READ FROM HISTORY TAPE             *****
*                                                                     *
***********************************************************************
         SPACE
XACCINPT MVI   ERRORSW,ZERO            CLEAR ERROR SWITCH       X02008
         GET  ACCUMDCB                                         @Z37LIMD
         CLI   ERRORSW,ERR             HAS ERROR OCCURRED?      XM2361
         BC     7,MOVDATA          NO ERROR CONDITION          @Z37LIMD
         LA    R15,28                  YES, ERROR CONDITION     XM2361
         B     COMRTRN                                          XM2361
MOVDATA  LR     XWORK5,XPARMREG       GET ADDR BUFFER          @Z37LIMD
         LH     XWORK6,0(0,XPARMREG)    PICK UP REC LTH READ   @Z37LIMD
         CLR    XWORK4,XWORK6         IS REQUESTED CNT GT ACTUAL 37LIMD
         BH     CNTHIGH               YES                      @Z37LIMD
         LR     XWORK6,XWORK4         USE REQUESTED LTH        @Z37LIMD
         B      MOVDATA1                                       @Z37LIMD
CNTHIGH  LR     XWORK4,XWORK6         USE ACTUAL               @Z37LIMD
MOVDATA1 LA     XWORK3,4(XWORK2)      ADDR OLT BUFFER          @Z37LIMD
         MVCL   XWORK3,XWORK5         MOVE FROM BUFFER TO OLT  @Z37LIMD
         L      XPARMREG,PARMSAVE     RESTORE PARM REG         @Z37LIMD
         BR    XWORK8
       EJECT
***********************************************************************
*                                                                     *
*****               WRITE HEADER RECORD (CLEAR SYS1.LOGREC)       *****
*                                                                     *
***********************************************************************
         SPACE 2
       EJECT
***********************************************************************
*                                                                     *
*****               WRITE INTO RECORD ENTRY AREA                  *****
*                                                                     *
***********************************************************************
         SPACE 2
       EJECT
***********************************************************************
*                                                                     *
*****                    WRITE INTO SDR AREA                      *****
*                                                                     *
***********************************************************************
         SPACE 2
       EJECT
***********************************************************************
*                                                                     *
*****               ROUTINE TO WRITE SYS1.LOGREC (DISK)           *****
*                                                                     *
***********************************************************************
         SPACE
         EJECT
***********************************************************************
*                                                                     *
*****                   END OF JOB ROUTINE                            *
*                                                                     *
***********************************************************************
         SPACE 2
ENDOFJOB SAVE  (14,12)
         LR    RETURNAD,XGRSAVE        FOLLOW
         ST    XGRSAVE,MYSAVE+FOUR       STANDARD
         LA    XGRSAVE,MYSAVE             LINKAGE
         ST    XGRSAVE,EIGHT(RETURNAD)     CONVENTIONS
         TM    EXSWITCH,INITSW               Q/IS THIS INITIAL ENTRY
         BNO   PARMERR                  PARM ERROR
         DEQ   (LOGREC,RESID,,STEP)
         TM    EXSWITCH,CONCAT               Q/IS THIS CONCATENATION
         BNO   EOJ001                        NO--SEE WHICH IS INPUT
         CLOSE (ACCUMDCB,REREAD)                                 SM2412
EOJ002   CLOSE (XDCB,LEAVE)
EOJ003   L     XPARMREG,PARMSAVE             RESTORE PARM REG
         MVI   EXSWITCH,0                    TURN OFF MY SWITCHES
         B     NORMRTRN                 NORMAL RETURN
EOJ001   TM    XDCB+FORTY8,XTEN              Q/ WAS LOGREC INPUT
         BO    EOJ002                        YES--GO CLOSE SYS1.LOGREC
         CLOSE (ACCUMDCB,REREAD)                                 SM2412
         B     EOJ003
         EJECT
***********************************************************************
*                                                                     *
*****          EVENT CONTROL BLOCK AND I/O BLOCK FOR SYS1.LOGREC  *****
*                                                                     *
***********************************************************************
         SPACE 2
XRWECB   DC    1F'0'               EVENT CONTROL BLOCK
         SPACE
XIOB     DC    X'42000000'         IOB FLAGS
XIOBECB  DC    AL4(XRWECB)         ADDRESS OF ECB
XIOBCSW  DC    2F'0'               CSW IN IOB
XIOBCCW  DC    AL4(XRCCWS)         FIRST CCW ADDRESS IN IOB
XIOBDCB  DC    AL4(XDCB)           DCB ADDRESS
XIOBRA   DC    1F'0'               RESTART ADDRESS
XIOBBCEC DC    1F'0'               BLOCK COUNT-ERROR COUNT
XIOBSEEK DC    2F'0'               SEEK ADDRESS
         EJECT
***********************************************************************
*                                                                     *
*****          DATA CONTROL BLOCK FOR SYS1.LOGREC                 *****
*                                                                     *
***********************************************************************
         SPACE 2
XDCB     DCB   DDNAME=SERLOG,                                          C
               DSORG=PS,                                               C
               MACRF=(E),                                        YM5878C
               RECFM=U,                                                C
               DEVD=DA,                                                C
               EXLST=JFCBXLST
         EJECT
***********************************************************************
*                                                                     *
*****          DATA CONTROL BLOCK FOR HISTORY DEVICE              *****
*                                                                     *
***********************************************************************
         SPACE 2
ACCUMDCB DCB   DDNAME=ACCIN,                                           C
               DSORG=PS,                                               C
               MACRF=(GL),                                     @Z37LIMDC
               LRECL=1900,                                     @Z37LIMDC
               EODAD=HISTEOD,                                          C
               SYNAD=TAPERRTN
         EJECT
***********************************************************************
*                                                                     *
*****          CHANNEL COMMAND WORDS USED BY EXCP                 *****
*                                                                     *
***********************************************************************
         SPACE 2
EXCP     EQU   0
SIDE     EQU   X'31'
TIC      EQU   X'08'
RCKDMT   EQU   X'9E'
WDATA    EQU   X'05'
WKD      EQU   X'0D'
RKD      EQU   X'8E'
RCT      EQU   X'92'
CC       EQU   X'40'
SILI     EQU   X'20'
SKE      EQU   X'29'
RR0      EQU   X'16'
ALL      EQU   X'70'
CCSILI   EQU   X'60'
         SPACE 2
XRCCWS   CCW   SIDE,XSEEKBUF+3,CC,5     SEARCH ID EQUAL
         CCW   TIC,XRCCWS,CC,1          TJL
XRDCCW   CCW   RCKDMT,XCNTBUF,SILI,1586 READ COUNT,KEY,DATA-MULTI TRACK
       SPACE
       EJECT
TEMPADDR DC    F'0'
PARMSAVE DC    F'0'
MYSAVE   DC    18F'0'
SEARCHKY DC    X'0'
XSEEKBUF DC    XL8'0'
XHDRBUF  DC    5XL8'0'
XHDRBUFX DC    2XL8'0'
RECDTYPE DC    X'0000'
XCNTBUF  DC    5XL8'0'
UCBCNT   DC    X'0000'
XGREGSAV DC    16F'0'
XSAVEBUF DC    5XL8'0'
TEMP     DC    F'0'
SRCHON2  DC    X'0002'
SRCHON1  DC    X'0001'
TAPEINP  DC    C'HISTORY'
LOGREC   DC    CL8'SERLOG'
RESID    DC    C'SYS1.LOGREC'
ACCUM    DC    CL8'ACCIN'
XDADBUF  DC    5XL8'00'
SRCHKEY  DC    X'0000'
JFCBXLST DS    0F
         DC    X'07'
         DC    AL3(JFCBAREA)
ERRORSW  DC    X'00'                   I/O ERROR SWITSCH        XM2361
JFCBAREA DS    0F
JDSNAME  DS    CL8
         DS    CL168
COMPNAME DC    C'NULLFILE'
XRESTART EQU   XHDRBUF+11          RECORD ENTRY START AREA
XSAFBYTE EQU   XHDRBUF+47
HDRSAVE  EQU   XHDRBUF
PATCH46  DC    20XL2'0700'              PATCH AREA               S21052
         END
