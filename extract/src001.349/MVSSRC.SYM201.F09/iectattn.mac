         TITLE '34XX ATTENTION ROUTINE'
**********************************************************************
*                                                                    *
* STATUS:                                                            *
*    CHANGE LEVEL 0                                                  *
*                                                                    *
* FUNCTION/OPERATION:                                                *
*    THIS MODULE IS ENTERED WHENEVER A UNEXPECTED DEVICE END IS      *
*    RECEIVED FROM A 34XX UNIT. IF THE VOLUME SERIAL IS VALID, THE   *
*    MOUNT MESSAGE HAS NOT BEEN ISSUED AND IF THE UCB IS READY, THIS *
*    MODULE WILL CAUSE IOS TO SCHEDULE THE TAPE ERPS.                *
*                                                                    *
* ENTRY POINTS:                                                      *
*         IECTATTN                                                   *
*                                                                    *
* INPUT:                                                             *
*    GR7 CONTAINS A POINTER TO THE UCB                               *
*                                                                    *
* OUTPUT: N/A                                                        *
*                                                                    *
* EXTERNAL ROUTINES: N/A                                             *
*                                                                    *
* EXITS-NORMAL:                                                      *
*    RETURN TO IOS VIA GR14                                          *
*                                                                    *
* EXITS-ERROR: N/A                                                   *
*                                                                    *
* ATTRIBUTES:                                                        *
*    RE-ENTERABLE                                                    *
*                                                                    *
* NOTES: N/A                                                         *
*                                                                    *
**********************************************************************
         EJECT
IECTATTN CSECT
         SPACE 3
*           REGISTER EQUATES
UCBREG   EQU   7                        UCB ADDRESS
LINKREG2 EQU   14                       RETURN ADDRESS OF IOS
APBSREG  EQU   15                       BASE REGISTER
         SPACE 1
UCBFL1   EQU   6                        OFFSET OF UCB FLAG 1
UCBVOLID EQU   28                       OFFSET OF VOL-ID IN TAPE UCB
SRTEDMCT EQU   35                       OFFSET OF VOLUME MOUNT SWITCH
UCBVOPT  EQU   48                       OFFSET OF DEVICE END INDICATOR
ZERO     EQU   0                        ZERO
HEX01    EQU   X'01'                    DEV END INDICATOR
HEX40    EQU   X'40'                    NOT READY INDICATOR
HEX80    EQU   X'80'                    VOL MOUNT SWITCH
         SPACE 1
         USING *,APBSREG
         CLI   UCBVOLID(UCBREG),ZERO    VOLUME-SERIAL = 0 ?
         BCR   8,LINKREG2               YES, RETURN TO IOS
         SPACE 1
         TM    SRTEDMCT(UCBREG),HEX80   MOUNT MESSAGE ISSUED ?
         BCR   1,LINKREG2               YES, RETURN TO IOS
         SPACE 1
         TM    UCBFL1(UCBREG),HEX40     IS UCB READY ?
         BCR   1,LINKREG2               NO, RETURN TO IOS
         SPACE 1
         OI    UCBVOPT(UCBREG),HEX01    TURN ON BIT TO SCHEDULE ERPS
         BCR   15,LINKREG2              RETURN TO IOS
         SPACE 1
         END
