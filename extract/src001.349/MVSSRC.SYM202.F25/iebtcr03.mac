         TITLE 'IEBTCR03 - JFCB ANALYZER - INTRODUCTORY COMMENTARY'
*                                                                     *
*TITLE 'IEBTCR03' - JOB FILE CONTROL BLOCK ANALYZER                   *
*                                                                     *
*STATUS: CHANGE LEVEL 000                                             *
*                                                                     *
*FUNCTION/OPERATION:  IEBTCR03 PERFORMS FIVE FUNCTIONS:               *
*                                                                     *
*   1) INSERTION OF ALTERNATIVE DDNAMES IN DCB'S                      *
*                                                                     *
*   2) CHECKING OF THE TIOT FOR MISSING DD STATEMENTS                 *
*                                                                     *
*   3) EXTRACTION OF DEVICE, VOLUME, AND JOB IDENTIFICATION FROM      *
*      SYSTEM CONTROL BLOCKS                                          *
*                                                                     *
*   4) ANALYSIS OF JOB FILE CONTROL BLOCKS (JFCB'S) TO DETERMINE      *
*      MISSING DCB PARAMETERS                                         *
*                                                                     *
*   5) ASSIGNMENT OF DEFAULTS TO MISSING PARAMETERS                   *
*                                                                     *
*   THE JOB FILE CONTROL BLOCK ANALYZER USES THE TASK INPUT/OUTPUT    *
*   TABLE (TIOT) AND THE UNIT CONTROL BLOCK (UCB) TABLE TO DETERMINE  *
*   THE DEVICE ALLOCATED TO THE TASK FOR THE SYSUT1 (TCR INPUT) DATA  *
*   SET.  IT EXTRACTS FROM THE JOB FILE CONTROL BLOCK (JFCB) ANY      *
*   EXTERNAL IDENTIFICATION ASSOCIATED WITH THE DATA SET BY MEANS OF  *
*   THE VOLUME=SER=XXXXXX DD STATEMENT PARAMETER. IT REMOVES JOB      *
*   AND STEP IDENTIFICATION FROM THE TIOT. IT STORES ALL OF THIS      *
*   INFORMATION IN THE COMMON COMMUNICATION REGION FOR LATER USE BY   *
*   THE DRIVER (IEBTCRIN) MOUNT ROUTINE.                              *
*                                                                     *
*   IF THE ANALYZER DETECTS A MISSING DD STATEMENT (OTHER THAN SYSUT1),
*   IT SETS A DUMMY FILE INDICATOR FOR THIS DATA SET, SETS AN APPRO-  *
*   PRIATE MESSAGE INDICATOR, AND ASSIGNS A RETURN CODE OF 4.  IF THE *
*   DATA SET IS SYSUT1, THE ANALYZER SETS MESSAGE INDICATORS, ESTA-   *
*   BLISHES A RETURN CODE OF 12, AND RETURNS CONTROL TO THE DRIVER    *
*   WITH A REQUEST FOR TERMINATION.                                   *
*                                                                     *
*   IF THE ANALYZER DETERMINES THAT DCB PARAMETERS WERE OMITTED,      *
*   IT DEVELOPS COMPLETE OR PARTIAL DEFAULTS DEPENDING UPON THE       *
*   DATA SET INVOLVED.   FOR SYSIN AND SYSPRINT, COMPLETE DEFAULTS    *
*   ARE DEVELOPED.  FOR SYSUT2 AND SYSUT3, A RECORD FORMAT INCREMENT  *
*   IS ASSIGNED AS A BASIC DEFAULT FOR LRECL AND BLKSIZE WITH AN      *
*   INDICATION THAT MAXLN MUST BE ADDED BY THE DRIVER. FOR VARIABLE   *
*   LENGTH RECORDS THE INCREMENTS ARE 4 AND 8 BYTES FOR LRECL AND     *
*   BLKSIZE. FOR UNDEFINED OR FIXED-LENGTH RECORDS, THE INCREMENT IS  *
*   ZERO.  THE DEFAULTS ARE INSERTED AT OPEN TIME BY THE DRIVER.      *
*                                                                     *
*   IF THE USER INCLUDES SOME PARAMETERS (E.G., RECFM) BUT OMITS      *
*   OTHERS, THE JFCB ANALYZER ATTEMPTS TO SET DEFAULTS WHICH ARE      *
*   CONSISTENT WITH THE PARAMETERS THAT HAVE BEEN SUPPLIED.  IF       *
*   THE USER PROVIDES A PARAMETER WHICH IS NOT CONSISTENT WITH        *
*   DEFAULTS ESTABLISHED FOR KEYWORDS WHICH ARE MISSING, THE JFCB     *
*   ANALYZER WILL ISSUE A REQUEST FOR TERMINATION.                    *
         EJECT
*ENTRY POINTS:                                                        *
*
*        IEBTCR03 - JOB FILE CONTROL BLOCK ANALYZER                   *
*                                                                     *
*   THIS MODULE IS LINKED TO BY THE DRIVER MODULE (IEBTCRIN):         *
*                                                                     *
*        LINK  EP=IEBTCR03,PARAM=TCRCOM                               *
*                                                                     *
*   PARAMETER DATA IS PASSED BY NAME VIA THE COMMON COMMUNICATION     *
*   REGION AND CONSISTS OF DCB EXIT LISTS, DCB PARAMETER DEFAULT      *
*   TABLES, AND MISCELLANEOUS DATA STORAGE AREAS.                     *
*                                                                     *
*INPUT:                                                               *
*                                                                     *
*   1) THE TASK INPUT/OUTPUT TABLE (TIOT)                             *
*                                                                     *
*   2) JOB FILE CONTROL BLOCKS (JFCB'S) FOR ALL DATA SETS             *
*                                                                     *
*   3) UNIT CONTROL BLOCK (UCB) ASSOCIATED WITH THE SYSUT1 DEVICE     *
*                                                                     *
*   ALL OF THESE CONTROL BLOCKS ARE READ THROUGH USE OF STANDARD      *
*   SYSTEM MACROS.                                                    *
*                                                                     *
*OUTPUT:  DCB PARAMETER DEFAULT TABLES                                *
*                                                                     *
*   THESE TABLES ARE CONSTRUCTED IN THE COMMON COMMUNICATION REGION   *
*                                                                     *
*EXTERNAL ROUTINES:  NONE                                             *
*                                                                     *
*EXITS-NORMAL:  NONE                                                  *
*     -ERROR:   NONE                                                  *
*                                                                     *
*TABLES/WORK AREAS:                                                   *
*                                                                     *
*   1) DCB EXIT LIST TABLES - INEXLST, PREXLST, UT1EXLST, UT2EXLST,   *
*      UT3EXLST - THESE TABLES CONTAIN JFCB WORK AREA ADDRESS ENTRIES *
*      WHICH ARE USED DURING EXECUTION OF THE RDJFCB MACRO INSTRUC-   *
*      TION.                                                          *
*                                                                     *
*   2) DCB DEFAULT TABLES - INLRECL, PRTLRECL, UT2LRECL, UT3LRECL     *
*      THESE TABLES ARE USED FOR CONSTRUCTING DEFAULTS FOR THE DCB    *
*      RECFM, LRECL, AND BLKSIZE PARAMETERS.                          *
*                                                                     *
*ATTRIBUTES:  NON-REENTRANT AND NON-REUSABLE.                         *
*                                                                     *
*NOTES:  THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A PARTI-   *
*   CULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.      *
         TITLE 'IEBTCR03 - JFCB ANALYZER - REGISTER DEFINITIONS'
IEBTCR03 START 0
         SPACE 2
*        REGISTER DEFINITIONS
         SPACE 2
PARMREG  EQU   1                       PARAMETER LIST REGISTER
COMMREG  EQU   2                       COMMUNICATION REGION
UCBREG   EQU   6                       UCB REGISTER
INCREG   EQU   9                       TIOT INCREMENT
TIOTREG  EQU   11                      TIOT REGISTER
BASEREG  EQU   12                      BASE REGISTER
SAVEREG  EQU   13                      SAVE AREA REGISTER
RETREG   EQU   14                      RETURN REGISTER
LNKREG15 EQU   15                      LINK REGISTER
R0       EQU   0                       REGISTER 0
R1       EQU   1                       REGISTER 1
R3       EQU   3                       REGISTER 3
R4       EQU   4                       REGISTER 4
R5       EQU   5                       REGISTER 5
R6       EQU   6                       REGISTER 6
R15      EQU   15                      REGISTER 15
         TITLE 'IEBTCR03 - JFCB ANALYZER - PROCESSING ROUTINES'
*        THIS SECTION SAVES THE CALLING MODULE'S GENERAL REGISTERS,
*        SETS UP A SAVEAREA ADDRESS, AND ESTABLISHES BASE REGISTERS
*        FOR THIS MODULE AND FOR THE COMMON COMMUNICATION REGION.
         SPACE 2
         SAVE  (14,12),T,IEBTCR03      SAVE REGISTERS
         BALR  BASEREG,R0              ESTABLISH BASE REGISTER
         USING *,BASEREG               INDICATE BASE REGISTER
         LR    R15,SAVEREG             SET UP SAVE AREA POINTERS
         ST    R15,SAVEAREA+4
         LA    SAVEREG,SAVEAREA
         ST    SAVEREG,8(R15)
         XR    R15,R15
         ST    R15,0(SAVEREG)
         L     COMMREG,0(PARMREG)      LOAD COMMUNICATION REGION ADDR
         USING TCRCOM,COMMREG          INDICATE COMM. REG. BASE REG
         EJECT
*        DDNAME INSERTION ROUTINE
         SPACE 1
*        THIS ROUTINE TESTS AN  INDICATOR IN THE COMMON COMMUNICATION
*        REGION TO DETERMINE WHETHER OR NOT ALTERNATIVE DDNAMES HAVE
*        BEEN PROVIDED BY THE USER. IF THEY HAVE BEEN, THIS ROUTINE
*        INSERTS THEM IN THE RESPECTIVE DCB'S, REPLACING THE STANDARD
*        NAMES.
         SPACE 2
TCR100   TM    MISCIND,DDON            TEST ALTERNATIVE DDNAMES PRESENT
         BNO   TCR102                  BRANCH NO
         USING IHADCB,R3               INDICATE DCB DSECT REG
         LA    R3,SYSINDCB             LOAD SYSIN DCB ADDRESS
         MVC   DCBDDNAM(DDLENGTH),NAMETAB  STORE ALTER. SYSIN DDNAME
         LA    R3,TCRDCB               LOAD SYSUT1 DCB ADDRESS
         MVC   DCBDDNAM(DDLENGTH),NAMETAB+SYS1DISP  STORE ALTERNATE
*                                      SYSUT1 DDNAME
         LA    R3,SYSPRDCB             LOAD SYSPRINT DCB ADDRESS
         MVC   DCBDDNAM(DDLENGTH),NAMETAB+SYSPDISP  STORE ALTERNATE
*                                      SYSPRINT DDNAME
         LA    R3,UT2DCB               LOAD SYSUT2 DCB ADDRESS
         MVC   DCBDDNAM(DDLENGTH),NAMETAB+SYS2DISP  STORE ALTERNATE
*                                        SYSUT2 DDNAME
         LA    R3,UT3DCB               LOAD SYSUT3 DCB ADDRESS
         MVC   DCBDDNAM(DDLENGTH),NAMETAB+SYS3DISP  STORE ALTERNATE
*                                      SYSUT3 DDNAME
         EJECT
*        JFCB WORK AREA CORE ALLOCATION ROUTINE
         SPACE 1
*        THIS ROUTINE GETMAINS ONE BLOCK OF CORE FOR ALL OF THE
*        JFCB WORK AREAS. IT THEN APPORTIONS IT AMONG THE DCB'S
*        AND STORES THE ADDRESSES IN THE RESPECTIVE EXIT LISTS.
         SPACE 2
TCR102   LA    R1,JFCBLN(0,0)          LOAD TOTAL LENGTH - 720 BYTES
         ST    R1,GETMREQ              STORE MINIMUM LENGTH
         ST    R1,GETMREQ+4            STORE MAXIMUM LENGTH
         GETMAIN   VC,LA=GETMREQ,A=GETMADD
         L     R1,GETMADD              LOAD STORAGE ADDRESS
         LTR   R15,R15                 TEST RETURN CODE
         BZ    TCR103                  BR IF STORAGE ALLOCATED
         OI    ERRIND,ALLOCERR         SET ERROR INDICATOR
         MVI   MSG19,MSGON             SET MSG INDICATOR
         MVI   RETCODE,CODE16          SET RETURN CODE
         B     TCR199                  BR TO RETURN
         SPACE 1
TCR103   ST    R1,JFCBAREA             SAVE STORAGE ADDRESS
         ST    R1,UT1EXLST             STORE CORE ADDRESS IN SYSUT1
         MVI   UT1EXLST,UT1JFCB        EXIT LIST
         ST    R1,INEXLST              STORE ADDRESS OF CORE IN
         MVI   INEXLST,JFCBCODE          SYSIN EXLST
         LA    R1,JFCBINCR(R1)         INCREMENT TO NEXT STORAGE AREA
         ST    R1,PREXLST              STORE ADDR OF NEXT AREA IN
         MVI   PREXLST,JFCBCODE          SYSPRINT EXLST
         LA    R1,JFCBINCR(R1)         INCREMENT TO NEXT STORAGE AREA
         ST    R1,UT2EXLST             STORE ADDR OF NEXT AREA IN
         MVI   UT2EXLST,JFCBCODE         SYSUT2 EXLST
         LA    R1,JFCBINCR(R1)         INCREMENT TO NEXT STORAGE AREA
         ST    R1,UT3EXLST             STORE ADDR OF NEXT AREA IN
         MVI   UT3EXLST,JFCBCODE         SYSUT3 EXLST
         EJECT
*        THIS ROUTINE EXTRACTS THE JOB NAME AND STEP NAME FROM THE
*        TIOT AND STORES IT IN THE COMMON COMMUNICATION REGION FOR
*        LATER INSERTION IN THE MOUNT MESSAGE BY THE DRIVER. IT ALSO
*        TESTS WHETHER THE SYSUT1 DD STATEMENT HAS BEEN PROVIDED.
*        IF IT HAS NOT, IT SETS MSG AND ERROR INDICATORS AND SETS THE
*        RETURN CODE TO 12. IF IT HAS BEEN, IT EXTRACTS THE DEVICE NAME
*        FROM THE UCB AND STORES IT IN THE COMMON COMMUNICATION
*        REGION.
         SPACE 2
         EXTRACT  TIOTADR,FIELDS=TIOT  GET TIOT ADDRESS
         L     TIOTREG,TIOTADR         LOAD TIOT ADDRESS
         MVC   JOBNAME(8),TIOCNJOB(TIOTREG)  STORE JOB NAME
         MVC   STEPNAME(8),TIOCSTP(TIOTREG)  STORE STEP NAME
         MVC   NAMEHOLD,NAMETAB+16     LOAD SYSUT1 DDNAME
         MVI   TIOTDDNM,HEXZERO        CLEAR INDICATOR
         BAL   RETREG,TCR720           LOAD ADDR COMPARE RTN
         TM    TIOTDDNM,NAMEFND        SYSUT1 FOUND
         BO    TCR105                  BR IF FOUND
         OI    ERRIND,JFCBERR          SET ERROR INDICATOR
         MVI   MSG10,MSGON             SET  MSG INDICATOR
         MVI   RETCODE,CODE12          SET RETURN CODE  12
         B     TCR107                  BR TO CONTINUE PROC
         SPACE 1
TCR105   L     UCBREG,UCBDISP(TIOTREG) LOAD ADDR OF UCB
         LA    UCBREG,0(UCBREG)        CLEAR HIGH-ORDER BYTE
         MVC   TCRDEV(DEVLNGTH),DEVDISP(UCBREG)  MOVE DEVICE NAME
         EJECT
*        THIS ROUTINE TESTS WHETHER OR NOT THE SYSPRINT AND SYSIN
*        DD STATEMENTS HAVE BEEN PROVIDED.  IF EITHER OR BOTH HAVE
*        NOT, MESSAGE INDICATORS ARE SET AND THE RETURN CODE IS SET
*        TO 04.
         SPACE 2
TCR107   MVC   NAMEHOLD,NAMETAB+8      LOAD SYSPRINT DDNAME
         MVI   TIOTDDNM,HEXZERO        CLEAR INDICATOR
         BAL   RETREG,TCR720           LOAD ADDR COMPARE RTN
         TM    TIOTDDNM,NAMEFND        SYSPRINT FOUND
         BO    TCR109                  BR IF FOUND
         OI    DUMMYIND,DUMSYSP        SET SYSPRINT DUMMY INDICATOR
         MVI   MSG12,MSGON             SET MSG INDICATOR
         CLI   RETCODE,CODE04          CURRENT RETURN CODE GREATER
*                                        THAN 04
         BNL   TCR109                  BR HIGH OR EQUAL
         MVI   RETCODE,CODE04          SET RETURN CODE 04
*
TCR109   MVC   NAMEHOLD,NAMETAB        LOAD SYSIN DDNAME
         MVI   TIOTDDNM,HEXZERO        CLEAR INDICATOR
         BAL   RETREG,TCR720           LOAD ADDR COMPARE RTN
         TM    TIOTDDNM,NAMEFND        SYSIN FOUND
         BO    TCR111                  BR IF FOUND
         OI    DUMMYIND,DUMSYSIN       SET SYSIN DUMMY INDICATOR
         MVI   MSG11,MSGON             SET MSG INDICATOR
         CLI   RETCODE,CODE04          CURRENT RETURN CODE GREATER
*                                        THAN 04
         BNL   TCR111                  BR HIGH OR EQUAL
         MVI   RETCODE,CODE04          SET RETURN CODE 04
         EJECT
*        THIS ROUTINE TESTS WHETHER SYSUT2 AND SYSUT3 DD STATEMENTS
*        HAVE BEEN PROVIDED. IF THEY HAVE NOT BEEN, MESSAGE INDI-
*        CATORS ARE SET AND THE RETURN CODE IS SET TO 04.
         SPACE 2
*
TCR111   MVC   NAMEHOLD,NAMETAB+24     LOAD SYSUT2 DDNAME
         MVI   TIOTDDNM,HEXZERO        CLEAR INDICATOR
         BAL   RETREG,TCR720           LOAD ADDR COMPARE RTN
         TM    TIOTDDNM,NAMEFND        SYSUT2 FOUND
         BO    TCR113                  BR IF FOUND
         OI    DUMMYIND,DUMSYS2        SET SYSUT2 DUMMY INDICATOR
         MVI   MSG13,MSGON             SET MSG INDICATOR
         MVI   MSG13+1,INSUT2          SET MSG INSERT POINTER
         CLI   RETCODE,CODE04          CURRENT RETURN CODE GREATER
*                                        THAN 04
         BNL   TCR113                  BR HIGH OR EQUAL
         MVI   RETCODE,CODE04          SET RETURN CODE 04
*
TCR113   MVC   NAMEHOLD,NAMETAB+32     LOAD SYSUT3 DDNAME
         MVI   TIOTDDNM,HEXZERO        CLEAR INDICATOR
         BAL   RETREG,TCR720           LOAD ADDR COMPARE RTN
         TM    TIOTDDNM,NAMEFND        SYSUT3 FOUND
         BO    TCR119                  BR IF FOUND
         OI    DUMMYIND,DUMSYS3        SET SYSUT3 DUMMY INDICATOR
         MVI   MSG13,MSGON             SET MSG INDICATOR
         CLI   RETCODE,CODE04          CURRENT RETURN CODE GREATER
*                                        THAN 04
         BNL   TCR115                  BR HIGH OR EQUAL
         MVI   RETCODE,CODE04          SET RETURN CODE 04
TCR115   CLI   MSG13+1,HEXZERO         SYSUT2 INSERT SET
         BE    TCR117                  BR  NO
         MVI   MSG13+1,INSUT23         SET BOTH INSERTS
         B     TCR119                  BR TO CONTINUE PROCESSING
TCR117   MVI   MSG13+1,INSUT3          SET SYSUT3 INSERT ONLY
         EJECT
*        JFCB READ ROUTINE
         SPACE 1
*        THIS ROUTINE READS THE JFCBS INTO THE GETMAINED STORAGE
*        AREAS.
         SPACE 2
TCR119   LA    R3,SYSINDCB
         LA    R4,SYSPRDCB
         LA    R5,UT2DCB
         LA    R6,UT3DCB
         RDJFCB  ((R3),,(R4),,(R5),,(R6))
         EJECT
*        THE FOLLOWING ROUTINES SET DEFAULTS WHERE REQUIRED.
*        IN THE CASE OF SYSUT2 AND SYSUT3 ONLY PARTIAL DEFAULTS
*        ARE CREATED IF LRECL HAS BEEN OMITTED.  THIS DEFAULT
*        CONSISTS OF 4 AND 8 BYTES FOR LRECL AND BLKSIZE, RESP.,
*        IF THE RECORD FORMAT IS VARIABLE.  IF THE FORMAT IS
*        FIXED OR UNDEFINED, THE DEFAULT IS ZERO.
         SPACE 2
*        SYSIN DEFAULT ROUTINE
         SPACE 1
         TM    DUMMYIND,DUMSYSIN       SYSIN DUMMY
         BO    TCR121                  BR IF YES
         LA    R4,INLRECL              LOAD SYSIN DEFAULT LIST ADDR
         L     R5,INEXLST              LOAD JFCB ADDRESS
         LA    R5,0(R5)
         BAL   RETREG,TCR210           LOAD ADDR DEFAULT RTN
         SPACE 1
*        SYSPRINT DEFAULT ROUTINE
         SPACE 1
TCR121   TM    DUMMYIND,DUMSYSP        SYSPRINT DUMMY
         BO    TCR123                  BR IF YES
         LA    R4,PRTLRECL             LOAD SYSPRINT DEFAULT LIST ADDR
         L     R5,PREXLST              LOAD JFCB ADDRESS
         LA    R5,0(R5)
         BAL   RETREG,TCR210           LOAD ADDR DEFAULT RTN
         SPACE 1
*        SYSUT2 DEFAULT ROUTINE
         SPACE 1
TCR123   TM    DUMMYIND,DUMSYS2        SYSUT2 DUMMY
         BO    TCR125                  BR YES
         LA    R4,UT2LRECL             LOAD SYSUT2   DEFAULT LIST ADDR
         L     R5,UT2EXLST             LOAD JFCB ADDRESS
         LA    R5,0(R5)
         BAL   RETREG,TCR210           LOAD ADDR DEFAULT RTN
         TM    MAXIND,MAXLRECL         MAXLN REQUIRED
         BNO   TCR1235                 BR NO
         OI    MISCIND,UT2LRLMX        SET UT2 MAXLN INDICATOR
TCR1235  TM    MAXIND,MAXBLK           MAXLN REQUIRED
         BNO   TCR125                  BR NO
         OI    MISCIND,UT2BLKMX        SET UT2 MAXLN INDICATOR
         SPACE 1
*        SYSUT3 DEFAULT ROUTINE
         SPACE 1
TCR125   TM    DUMMYIND,DUMSYS3        SYSUT3 DUMMY
         BO    TCR130                  BR YES
         LA    R4,UT3LRECL             LOAD SYSUT3  DEFAULT LIST ADDR
         L     R5,UT3EXLST             LOAD JFCB ADDRESS
         LA    R5,0(R5)                CLEAR HIGH-ORDER BYTE
         BAL   RETREG,TCR210           LOAD ADDR DEFAULT RTN
         TM    MAXIND,MAXLRECL         MAXLN REQUIRED
         BNO   TCR127                  BR NO
         OI    MISCIND,UT3LRLMX        SET UT3 MAXLN INDICATOR
TCR127   TM    MAXIND,MAXBLK           MAXLN REQUIRED
         BNO   TCR130                  BR NO
         OI    MISCIND,UT3BLKMX        SET UT3 MAXLN INDICATOR
         EJECT
*        THIS ROUTINE STORES THE VOLUME SERIAL NUMBER IF ONE HAS BEEN
*        PROVIDED
         SPACE 2
TCR130   LA    R3,TCRDCB               LOAD ADDRESS SYSUT1 DCB
         RDJFCB  ((R3))                READ SYSUT1 JFCB
         L     R5,JFCBAREA             LOAD GETMAINED STORAGE ADDRESS
         USING JFCBD,R5                INDICATE JFCB DSECT REG
         CLI   JFCBNVOL,HEXZERO        TEST VOLUME SERIALS PRESENT
         BE    TCR132                  BR IF NO VOLUME SERIALS
         MVC   VOLSER,JFCBVOLS         STORE FIRST VOLUME SERIAL
         B     TCR199                  BR TO RETURN
*
TCR132   MVC   VOLSER,DEFVOL           STORE DEFAULT VOLUME SERIAL
         SPACE 2
*        THIS ROUTINE FREES THE JFCB WORK AREAS AND RETURNS CONTROL
*        TO THE DRIVER AFTER RELOADING THE GENERAL REGISTERS
         SPACE 2
TCR199   L     R1,JFCBAREA
         FREEMAIN  R,LV=720,A=(1)
         L     SAVEREG,4(SAVEREG)
         RETURN (14,12),T,RC=(15)      RETURN
         EJECT
*        DCB PARAMETER DEFAULT CONSTRUCTION SUBROUTINE
         SPACE 1
*        PARAMETERS PASSED ARE AS FOLLOWS:
         SPACE 1
*              REG 4 - DCB DEFAULT LIST ADDRESS
*              REG 5 - JFCB WORK AREA ADDRESS
         SPACE 1
*        THIS ROUTINE FIRST LOOKS AT THE JFCB RECFM PARAMETER TO
*        DETERMINE WHETHER OR NOT THIS PARAMETER WAS SUPPLIED BY THE
*        USER. IF IT WAS SUPPLIED, IT REPLACES THE DEFAULT PARAMETER
*        IN THE DEFAULT TABLE WITH THE JFCB  PARAMETER.  IT THEN TESTS
*        WHETHER THE RESULTING RECORD FORMAT IS VARIABLE.  IF IT IS,
*        IT INCREMENTS THE DEFAULT LRECL PARAMETER BY FOUR BYTES AND
*        THE BLKSIZE PARAMETER BY EIGHT BYTES.  NO MATTER WHAT RECORD
*        FORMAT HAS BEEN REQUESTED, IT NEXT TESTS WHETHER THE LRECL
*        PARAMETER WAS PROVIDED.  IF IT WAS, IT REPLACES THE DEFAULT
*        LRECL AND BLKSIZE PARAMETERS WITH THIS VALUE AND CLEARS THE
*        MAXLN REQUIRED INDICATORS.  IF VARIABLE-LENGTH  RECORDS WERE
*        SPECIFIED, IT INCREMENTS THIS BLKSIZE VALUE BY FOUR BYTES.
*        IT THEN TESTS WHETHER OR NOT THE BLKSIZE PARAMETER WAS
*        PROVIDED.  IF IT WAS PROVIDED, IT REPLACES THE DEFAULT
*        VALUE WITH THE JFCB VALUE AND CLEARS THE MAXLN REQUIRED
*        INDICATOR.   IT NEXT TESTS WHETHER UNDEFINED RECORDS
*        WERE SPECIFIED. IF THEY WERE, THE ROUTINE ZEROES OUT THE
*        LRECL FIELD.
         SPACE 2
TCR210   ST    RETREG,SAVE100
         USING JFCBD,R5                INDICATE JFCB DSECT REG
         MVI   MAXIND,MAXLB            SET MAXLN REQUIRED INDICATOR
         CLI   JFCRECFM,HEXZERO        TEST JFCB RECFM ZERO
         BE    TCR211                  BR IF YES
         MVC   4(1,R4),JFCRECFM        STORE JFCB RECFM IN DEFAULT TABL
         SPACE 1
TCR211   TM    4(R4),VARFM             TEST VARIABLE
         BO    TCR213                  BR NOT VARIABLE
         LH    R6,0(R4)                INCREMENT LRECL BY FOUR
         LA    R6,4(R6)
         STH   R6,0(R4)
         LA    R6,4(R6)                INCREMENT BLOCKSIZE BY EIGHT
         STH   R6,2(R4)
         SPACE 1
TCR213   CLC   JFCLRECL(2),HEXZEROS    TEST LRECL PRESENT
         BE    TCR215                  BR IF NO
         NI    MAXIND,X'FF'-MAXLRECL-MAXBLK CLEAR MAXLN INDICATORS
         MVC   0(2,R4),JFCLRECL        MOVE LRECL TO DEFAULT LRECL
         MVC   2(2,R4),JFCLRECL        MOVE LRECL TO DEFAULT BLKSIZE
         TM    4(R4),VARFM             TEST VARIABLE
         BO    TCR215                  BR NOT VARIABLE
         LH    R6,2(R4)                INCREMENT BLOCKSIZE BY FOUR
         AH    R6,FOUR
         STH   R6,2(R4)
         SPACE 1
TCR215   CLC   JFCBLKSI,HEXZEROS       TEST BLKSIZE PRESENT
         BE    TCR217                  BR IF NO
         MVC   2(2,R4),JFCBLKSI        MOVE BLKSIZE TO DEFAULT BLKSIZE
         NI    MAXIND,X'FF'-MAXBLK     CLEAR MAXLN INDICATOR
TCR217   TM    4(R4),UNDEF             TEST UNDEF FORMAT
         BNO   TCR219                  BR IF NO
         MVC   0(2,R4),HEXZEROS        MOVE ZEROES TO LRECL
         SPACE 1
TCR219   L     RETREG,SAVE100          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
         EJECT
*        TIOT DDNAME CHECKING SUBROUTINE
         SPACE 2
*        PARAMETERS PASSED:
         SPACE 1
*              1) DDNAME - VIA THE STORAGE AREA NAMEHOLD
*              2) FOUND/NOT FOUND INDICATOR - VIA THE STORAGE AREA
*                 TIOTDDNM
         SPACE 1
*        THIS ROUTINE COMPARES THE DDNAME INDICATED TO EACH ENTRY OF
*        THE TASK INPUT/OUTPUT TABLE
         SPACE 2
TCR720   ST    RETREG,SAVE100          SAVE RETURN ADDRESS
         L     TIOTREG,TIOTADR         LOAD TIOT ADDRESS
         LA    TIOTREG,24(TIOTREG)     ADDR OF FIRST DD ENTRY
         XR    INCREG,INCREG           ZERO INCR REGISTER
TCR722   CLC   NAMEHOLD,4(TIOTREG)     COMPARE DDNAMES
         BNE   TCR724                  BR UNEQUAL
         OI    TIOTDDNM,NAMEFND        SET FOUND INDICATOR
         B     TCR729                  BR TO RETURN
         SPACE 1
TCR724   IC    INCREG,0(TIOTREG)       INSERT INCREMENT
         LA    TIOTREG,0(TIOTREG,INCREG)  ADDR OF NEXT DD ENTRY
         CLC   ZERO(ETLNG,TIOTREG),ENDTIOT  END OF TIOT?
         BNE   TCR722                  BR NO
         SPACE 1
TCR729   L     RETREG,SAVE100          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCR03 - JFCB ANALYZER - LITERALS AND STORAGE AREAS'
*        LITERALS
         SPACE 2
NULL     EQU   *                       NULL LABEL
NAMEFND  EQU   X'01'                   DDNAME FOUND IN TIOT
HEXZERO  EQU   X'00'                   HEXIDECIMAL ZERO
ZERO     EQU   0                       ZERO
ETLNG    EQU   4                       END TIOT INDICATOR LENGTH
MSGON    EQU   X'FF'                   MSG REQUIRED
UCBDISP  EQU   16                      UCB DISPLACEMENT
MAXLRECL EQU   X'01'                   LRECL MAY REQUIRE MAXLN
MAXBLK   EQU   X'02'                   BLKSIZE MAY REQUIRE MAXLN
MAXLB    EQU   X'03'                   BOTH MAY REQUIRE MAXLN
DDON     EQU   X'01'                   ALTERNATE DDNAMES
         SPACE 1
JFCBCODE EQU   X'07'                   EXIT LIST JFCB CODE
JFCBINCR EQU   180                     JFCB STORAGE INCREMENT
JFCBLN   EQU   720                     TOTAL JFCB WORK AREA LENGTH
DDLENGTH EQU   8                       DDNAME LENGTH
JFCBERR  EQU   X'10'                   ERROR IN READING JFCB
         SPACE 1
UT3BLKMX EQU   X'02'                   UT3 BLKSIZE REQUIRES MAXLN
UT3LRLMX EQU   X'04'                   UT3 LRECL REQUIRES MAXLN
UT2BLKMX EQU   X'08'                   UT2 BLKSIZE REQUIRES MAXLN
UT2LRLMX EQU   X'10'                   UT2 LRECL REQUIRES MAXLN
         SPACE 1
DUMSYSIN EQU   X'01'                   DUMMY SYSIN
DUMSYSP  EQU   X'02'                   DUMMY SYSPRINT
DUMSYS2  EQU   X'04'                   DUMMY SYSUT2
DUMSYS3  EQU   X'08'                   DUMMY SYSUT3
         SPACE 1
INSUT2   EQU   X'04'                   INSERT SYSUT2
INSUT3   EQU   X'05'                   INSERT SYSUT3
INSUT23  EQU   X'54'                   INSERT SYSUT2 & 3
         SPACE 1
SYSPDISP EQU   8                       SYSPRINT DDNAME DISPLACEMENT
SYS1DISP EQU   16                      SYSUT1 DDNAME DISPLACEMENT
SYS2DISP EQU   24                      SYSUT2 DDNAME DISPLACEMENT
SYS3DISP EQU   32                      SYSUT3 DDNAME DISPLACEMENT
         SPACE 1
UNDEF    EQU   X'C0'                   UNDEFINED RECORD FORMAT
ALLOCERR EQU   X'40'                   STORAGE ALLOCATION ERROR
DEVLNGTH EQU   3                       DEVICE LENGTH
DEVDISP  EQU   13                      DEVICE DISPLACEMENT
VARFM    EQU   X'80'                   NOT VARIABLE IF ON
UT1JFCB  EQU   X'87'                   EXIT LIST CODE
TIOCNJOB EQU   0                       TIOT JOB NAME FIELD
TIOCSTP  EQU   8                       TIOT STEP NAME FIELD
         SPACE 2
*        RETURN CODES
         SPACE 1
CODE04   EQU   X'04'                   RETURN CODE 04
CODE12   EQU   X'0C'                   RETURN CODE 12
CODE16   EQU   X'10'                   RETURN CODE 16
         SPACE 1
*        CONSTANTS, WORK AREAS, SAVE AREAS, INDICATORS
         SPACE 1
GETMREQ  DS    D                       MINIMUM AND MAXIMUM LENGTH
GETMADD  DS    D                       STORAGE ADDRESS AND LENGTH
ENDTIOT  DC    F'0'                    END OF TIOT INDICATOR
TIOTADR  DS    F                       TIOT ADDRESS
NAMEHOLD DS    CL8                     DDNAME HOLD AREA
         SPACE 1
SAVEAREA DS    18F                     REGISTER SAVE AREA
SAVE100  DS    F                       SAVE AREA
         SPACE 1
         DS    0H                      FORCE HALF-WORD ALIGNMENT
HEXZEROS DC    X'0000'                 HEX ZEROES
FOUR     DC    X'0004'                 BINARY FOUR
TIOTDDNM DC    X'00'                   TIOT DDNAME
MAXIND   DC    X'00'                   INCREMENT BY MAXLN INDICATOR
DEFVOL   DC    CL6'TCRINP'             DEFAULT VOLUME IDENTIFICATION
  TITLE 'IEBTCR03 - JFCB ANALYZER - COMMON COMMUNICATION REGION DSECT'
*        COMMON COMMUNICATION REGION
         SPACE 1
*        THIS CSECT CONTAINS TABLES, INDICATORS, CONSTANTS, WORK
*        AREAS, AND DCB'S THAT ARE USED BY TWO OR MORE MODULES.
         SPACE 2
TCRCOM   DSECT
         SPACE 1
         DS    0F                      FORCE FULL-WORD ALIGNMENT
OPTABLE  DS    0CL8                    CONTROL CARD OPTION TABLE
         SPACE 1
IEXITS   DC    X'00'                   USER ROUTINE INDICATORS
         SPACE 1
*              BIT 0 - OUTHDR2
*                  1 - OUTTLR2
*                  2 - OUTHDR3
*                  3 - OUTTLR3
*                  4 - OUTREC
*                  5 - ERROR
*                  6 - USERTAB
*                  7 - NOT USED
         SPACE 1
IPARM1   DC    X'00'                   TYPE AND TRANS INDICATORS
         SPACE 1
*              BIT 0 - TYPE = MTST
*                  1 - TYPE = MTDI
*                  2 - UNUSED
*                  3 - TRANS = STDUC
*                  4 - TRANS = STDLC
*                  5 - TRANS = USERNAME
*                  6 - TRANS = NOTRAN
*                  7 - NOT USED
         SPACE 1
IPARM2   DC    X'00'                   EDIT, VERCHK, AND ERROPT
*                                        INDICATORS
         SPACE 1
*              BIT 0 - EDIT = EDITD
*                  1 - EDIT = EDITR
*                  2 - EDIT = NOEDIT
*                  3 - VERCHK = VOKCHK
*                  4 - VERCHK = NOCHK
*                  5 - ERROPT = NORMAL
*                  6 - ERROPT = NOERR
*                  7 - UNUSED
         SPACE 1
REPL     DC    X'19'                   REPLACE PARAMETER VALUE
MINLN    DC    X'0000'                 MINLN PARAMETER VALUE
MAXLN    DC    X'0000'                 MAXLN PARAMETER VALUE
         EJECT
ADDRTAB  DS    0CL28                   USER ROUTINE ADDRESS TABLE
USERADDR DC    A(0,0,0,0,0,0,0)        TABLE
         SPACE 1
*              WORD 0 - OUTHDR2
*                   1 - OUTTLR2
*                   2 - OUTHDR3
*                   3 - OUTTLR3
*                   4 - OUTREC
*                   5 - ERROR
*                   6 - TRANSLATE TABLE
         SPACE 2
MSGADDR  DC    A(0)                    MESSAGE MODULE ADDRESS
         SPACE 2
*        NAME TABLE
         SPACE 1
*        THIS TABLE CONTAINS DDNAMES AND USER EXIT NAMES
         SPACE 1
NAMETAB  DC    CL8'SYSIN',CL8'SYSPRINT',CL8'SYSUT1',CL8'SYSUT2'
         DC    CL8'SYSUT3',CL8'OUTHDR2',CL8'OUTTLR2'
         DC    CL8'OUTHDR3',CL8'OUTTLR3',CL8'OUTREC',CL8'ERROR'
         DC    CL8'TRANS'
         SPACE 1
*              WORDS 0-1   SYSIN
*                    2-3   SYSPRINT
*                    4-5   SYSUT1
*                    6-7   SYSUT2
*                    8-9   SYSUT3
*                   10-11  OUTHDR2
*                   12-13  OUTTLR2
*                   14-15  OUTHDR3
*                   16-17  OUTTLR3
*                   18-19  OUTREC
*                   20-21  ERROR
*                   22-23  TRANS
         EJECT
*        MESSAGE POINTER TABLE
         SPACE 2
*        THIS TABLE CONTAINS ONE FULL-WORD ENTRY FOR EACH IEB-TYPE
*        MESSAGE (EXCLUDING SYNAD MESSAGES).   MSG01 CORRESPONDS TO
*        IEB901; MSG02 CORRESPONDS TO IEB902; ETC.
         SPACE 1
*              BYTE  1 - MESSAGE REQUEST CODE
*
*                        X'FF' = INDICATES REQUEST
*                        X'00' = NO REQUEST
*
*              BYTE  2 - INSERT CODE
*
*                        X'00' - NO INSERT
*                        X'01' - SYSIN
*                        X'02' - SYSPRINT
*                        X'03' - SYSUT1
*                        X'04' - SYSUT2
*                        X'05' - SYSUT3
*                        X'06' - OUTHDR2
*                        X'07' - OUTTLR2
*                        X'08' - OUTHDR3
*                        X'09' - OUTTLR3
*                        X'0A' - OUTREC
*                        X'0B' - ERROR
*                        X'0C' - USERTAB
*                        X'54' - BOTH SYSUT2 AND SYSUT3
*
*                    NOTE:  IF X'54' IS SPECIFIED, THE MESSAGE IS
*                        OUTPUT TWICE, ONCE WITH SYSUT2 AND ONCE
*                        WITH SYSUT3.
*
*              BYTE  3 - INSERT LOCATION  (MESSAGE DISPLACEMENT)
*
*              BYTE  4 - OUTPUT LOCATION
*
*                        BIT 0 - UNUSED
*                            1 - UNUSED
*                            2 - UNUSED
*                            3 - UNUSED
*                            4 - UNUSED
*                            5 - SYSPRINT IF AVAILABLE, CONSOLE
*                                OTHERWISE
*                            6 - CONSOLE
*                            7 - SYSPRINT
*
*                        NOTE - BITS 6 AND 7 MAY BE ON SIMULTANEOUSLY
         SPACE 1
MSGPTRS  EQU   *                       ASSIGN MSGPTRS NAME TO TABLE
MSG01    DC    X'00000002'
MSG02    DC    X'00000001'
MSG03    DC    X'00000001'
MSG04    DC    X'00000001'
MSG05    DC    X'00000001'
MSG06    DC    X'00000001'
MSG07    DC    X'00000001'
MSG08    DC    X'00000001'
MSG09    DC    X'00000001'
MSG10    DC    X'00030B01'             INSERT LOC = 11
MSG11    DC    X'00010B01'             INSERT LOC = 11
MSG12    DC    X'00020B02'             INSERT LOC = 11
MSG13    DC    X'00000B01'             INSERT LOC = 11
MSG14    DC    X'00002501'             INSERT LOC = 37
MSG15    DC    X'00000F04'             INSERT LOC = 15
MSG16    DC    X'00001504'             INSERT LOC = 21
MSG17    DC    X'00002201'             INSERT LOC = 34
MSG18    DC    X'00001D01'             INSERT LOC = 29
MSG19    DC    X'00000004'
MSG20    DC    X'00000002'
         SPACE 3
MOUNTID  DS    F                       MOUNT MSG ID
IOBADDR  DS    F                       SYSUT1 IOB ADDRESS
TCRBUFL  DC    X'0000'                 TCR BUFFER LENGTH
         SPACE 1
ERRIND   DC    X'00'                   ABNORMAL TERMINATION INDICATORS
         SPACE 1
*              BIT 0 - CONFLICTING DCB PARAMETERS
*                  1 - STORAGE ALLOCATION ERROR
*                  2 - I/O ERROR IN READ AND EDIT
*                  3 - ERROR IN JFCB READ OR MOUNT
*                  4 - ERROR DURING READ AND EDIT
*                  5 - ERROR IN ANALYZING CONTROL CARDS
*                  6 - PERMANENT I/O ERROR ON QSAM GET/PUT
*                  7 - ERROR IN LOADING UTILITY MODULE
         SPACE 1
DUMMYIND DC    X'00'                   DUMMY FILE INDICATORS
         SPACE 1
*              BIT 0 - UNUSED
*                  1 - UNUSED
*                  2 - UNUSED
*                  3 - UNUSED
*                  4 - SYSUT3
*                  5 - SYSUT2
*                  6 - SYSPRINT
*                  7 - SYSIN
         EJECT
*        DATA CONTROL BLOCK (DCB) EXIT LISTS
         SPACE 2
*        SYSIN EXIT LIST
         SPACE 1
         DS    0F                      FORCE FULL-WORD ALIGNMENT
INEXLST  DC    X'07'                   JFCB EXIT
         DC    AL3(0)                  WORK AREA ADDRESS
         DC    X'85'                   DCB EXIT
         DC    AL3(0)                  DCB EXIT ROUTINE
         DC    X'00'                   DUMMY ENTRY
         DC    AL3(INLRECL)            DEFAULT TABLE ADDRESS
         SPACE 2
*        SYSPRINT EXIT LIST
         SPACE 1
PREXLST  DC    X'07'                   JFCB EXIT
         DC    AL3(0)                  WORK AREA ADDRESS
         DC    X'05'                   DCB EXIT
         DC    AL3(0)                  DCB EXIT ROUTINE
         DC    X'00'                   DUMMY ENTRY
         DC    AL3(PRTLRECL)           DEFAULT TABLE ADDRESS
         DC    X'00'                   OUTPUT HEADER LABEL (NULL)
         DC    AL3(0)                  NULL ADDRESS
         DC    X'80'                   OUTPUT TRAILER LABEL (NULL)
         DC    AL3(0)                  NULL ADDRESS
         SPACE 2
*        SYSUT1 EXIT LIST
         SPACE 1
UT1EXLST DC    X'87'                   JFCB EXIT
         DC    AL3(0)                  JFCB EXIT ADDRESS
         SPACE 2
*        SYSUT2 EXIT LIST
         SPACE 1
         DS    0F
UT2EXLST DC    X'07'                   JFCB EXIT
         DC    AL3(0)                  WORK AREA ADDRESS
         DC    X'05'                   DCB EXIT
         DC    AL3(0)                  DCB EXIT ROUTINE
         DC    X'00'                   DUMMY ENTRY
         DC    AL3(UT2LRECL)           DEFAULT TABLE ADDRESS
         DC    X'00'                   OUTPUT HEADER LABEL
         DC    AL3(0)                  NULL ADDRESS
         DC    X'80'                   OUTPUT TRAILER LABEL
         DC    AL3(0)                  NULL ADDRESS
         SPACE 2
*        SYSUT3 EXIT LIST
         SPACE 1
UT3EXLST DC    X'07'                   JFCB EXIT
         DC    AL3(0)                  WORK AREA ADDRESS
         DC    X'05'                   DCB EXIT
         DC    AL3(0)                  DCB EXIT ROUTINE
         DC    X'00'                   DUMMY ENTRY
         DC    AL3(UT3LRECL)           DEFAULT TABLE ADDRESS
         DC    X'00'                   OUTPUT HEADER LABEL
         DC    AL3(0)                  NULL ADDRESS
         DC    X'80'                   OUTPUT TRAILER LABEL
         DC    AL3(0)                  NULL ADDRESS
         SPACE 2
*        DCB PARAMETER DEFAULTS
         SPACE 1
INLRECL  DC    X'0050'                 SYSIN LRECL  - 80
INBLK    DC    X'0050'                 SYSIN BLKSIZE - 80
INRECFM  DC    X'90'                   SYSIN - FIXED BLOCKED
INIDENT  DC    X'01'                   INTERNAL IDENTIFICATION CODE
         SPACE 1
PRTLRECL DC    X'0079'                 SYSPRINT LRECL -  121
PRTBLK   DC    X'0079'                 SYSPRINT BLKSIZE - 121
PRTRECFM DC    X'94'                   SYSPRINT - FIXED BLOCKED ASA
PRTIDENT DC    X'02'                   INTERNAL IDENTIFICATION CODE
         SPACE 1
UT2LRECL DC    X'0000'                 SYSUT2 LRECL
UT2BLK   DC    X'0000'                 SYSUT2 BLKSIZE
UT2RECFM DC    X'50'                   SYSUT2 RECFM - VARIABLE BLOCKED
UT2IDENT DC    X'04'                   INTERNAL IDENTIFICATION CODE
         SPACE 1
UT3LRECL DC    X'0000'                 SYSUT3 LRECL
UT3BLK   DC    X'0000'                 SYSUT3 BLKSIZE
UT3RECFM DC    X'50'                   SYSUT3 RECFM - VARIABLE BLOCKED
UT3IDENT DC    X'05'                   INTERNAL IDENTIFICATION CODE
         EJECT
*        DATA CONTROL BLOCKS (DCB'S)
         SPACE 3
*        SYSUT1 (TCR) DCB
         SPACE 2
TCRDCB   DCB   DSORG=PS,MACRF=(E),DDNAME=SYSUT1,DEVD=TA,RECFM=UA,      X
               BFTEK=S,EXLST=UT1EXLST,IOBAD=NULL
         EJECT
*        SYSUT2 DCB
         SPACE 2
UT2DCB   DCB   DSORG=PS,DDNAME=SYSUT2,DEVD=DA,EXLST=UT2EXLST,          X
               SYNAD=NULL,EROPT=ABE,MACRF=(PM)
         SPACE 3
*        SYSUT3 DCB
         SPACE 2
UT3DCB   DCB   DSORG=PS,DDNAME=SYSUT3,DEVD=DA,EXLST=UT3EXLST,          X
               SYNAD=NULL,EROPT=ABE,MACRF=(PM)
         SPACE 3
*        SYSIN DCB
         SPACE 2
SYSINDCB DCB   DSORG=PS,DDNAME=SYSIN,DEVD=DA,EODAD=NULL,MACRF=(GM),    X
               EXLST=INEXLST,EROPT=ACC,SYNAD=NULL
         SPACE 3
*        SYSPRINT DCB
         SPACE 2
SYSPRDCB DCB   DSORG=PS,DDNAME=SYSPRINT,DEVD=DA,EXLST=PREXLST,         X
               MACRF=(PM)
         EJECT
*        GETMAINED STORAGE ADDRESSES
         SPACE 2
CARDAREA DS    F                       CARD INPUT AREA
JFCBAREA DS    F                       JFCB AREA ADDRESS
PRTAREA  DS    F                       SYSPRINT OUTPUT AREA
PRTLNGTH DS    F                       PRINT AREA LENGTH
COLUMN9  DS    F                       CARD INPUT COLUMN 9 ADDRESS
CARDEND  DS    F                       INPUT AREA ENDING ADDRESS
         SPACE 2
USERNAME DC    7XL8'00'                USER ROUTINE NAME TABLE
           SPACE 1
*              WORDS 0 & 1  - OUTHDR2 ROUTINE
*                    2 & 3  - OUTTLR2 ROUTINE
*                    4 & 5  - OUTHDR3 ROUTINE
*                    6 & 7  - OUTTLR3 ROUTINE
*                    8 & 9  - OUTREC  ROUTINE
*                   10 & 11 - ERROR ROUTINE
*                   12 & 13 - TRANSLATE TABLE
           SPACE 2
MISCIND  DC    X'00'                   MISCELLANEOUS INDICATORS
         SPACE 1
*              BIT 0 - UNUSED
*                  1 - UNUSED
*                  2 - UNUSED
*                  3 - SYSUT2 LRECL REQUIRES MAXLN INCREMENT
*                  4 - SYSUT2 BLKSIZE REQUIRES MAXLN INCREMENT
*                  5 - SYSUT3 LRECL REQUIRES MAXLN INCREMENT
*                  6 - SYSUT3 BLKSIZE REQUIRES MAXLN INCREMENT
*                  7 - ALTERNATE DDNAMES FOUND
             SPACE 2
         SPACE 1
RETCODE  DC    X'00'                   RETURN CODE
TCRDEV   DC    CL3'000'                TCR DEVICE
VOLSER   DC    CL6' '                  VOLUME SERIAL NUMBER
PRTRTN   DC    X'00'                   MESSAGE WRITER OPTION IND
CARRCTRL DC    X'40'                   CARRIAGE CONTROL
         SPACE 1
VTCR220  DS    F                       MESSAGE DISPLAY
CURMSGAD DS    F                       CURRENT SYSPRINT MSG ADDRESS
MSGAD    DS    F                       SINGLE MESSAGE ADDRESS
MSGLN    DS    F                       SINGLE MSG LENGTH
JOBNAME  DC    CL8' '                  JOB NAME
STEPNAME DC    CL8' '                  STEPNAME
         EJECT
         DCBD  DSORG=PS
         EJECT
JFCBD    DSECT
         SPACE 1
         IEFJFCBN
*
         END   IEBTCR03
