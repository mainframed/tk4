  TITLE 'IEBTCRIN - DRIVER MODULE - INTRODUCTORY COMMENTARY'
*                                                                     *
*TITLE 'IEBTCRIN' - TCR DATA SET UTILITY - DRIVER MODULE              *
*                                                                     *
*STATUS:  CHANGE LEVEL 000                                            *
*                                                                     *
* CHANGE ACTIVITY                                                     *
* RELEASE VS2/2.0   ADDITIONS/CHANGES/DELETIONS                       *
*C166000                                                         YM2512
*                                                                     *
*FUNCTION/OPERATION:                                                  *
*                                                                     *
*   IEBTCRIN IS THE DRIVER MODULE OF THE IEBTCRIN DATA SET UTILITY.   *
*   IT IS THE FIRST AND ONLY MODULE BROUGHT INTO CORE UPON INVOCATION *
*   OF THE UTILITY.  IT REMAINS RESIDENT THROUGHOUT OPERATION OF THE  *
*   UTILITY.  IT CONSISTS OF THREE CSECTS - IEBTCRIN, TCR220, AND     *
*   TCRCOM.                                                           *
*                                                                     *
*   IEBTCRIN IS THE GENERAL PROCESSING ROUTINE.  IT PROCESSES THE     *
*   USER DDNAME PARAMETER LIST, LOADS THE MESSAGE MODULE (IEBTCR02),  *
*   LINKS TO THE JFCB ANALYZER (IEBTCR03), OPENS THE SYSPRINT DATA    *
*   SET AND ALLOCATES PRINT AREA STORAGE, OPENS THE SYSIN DATA SET    *
*   IF ONE HAS BEEN PROVIDED AND LINKS TO THE CONTROL CARD ANALYZER   *
*   (IEBTCR04), SETS CONTROL CARD DEFAULTS IF NO SYSIN DATA SET WAS   *
*   PROVIDED, LOADS THE USER TRANSLATE TABLE AND EXIT ROUTINES, INCRE-*
*   MENTS DATA CONTROL BLOCK (DCB) DEFAULTS BY MAXLN, OPENS THE       *
*   SYSUT1 DCB AND ISSUES A MOUNT MESSAGE, LINKS TO THE READ AND      *
*   EDIT PROCESSOR (IEBTCR05), ISSUES SYNAD MESSAGES TO SYSPRINT FOR  *
*   PERMANENT I/O ERRORS, AND RETURNS A COMPLETION CODE TO THE CALL-  *
*   ING TASK.                                                         *
*                                                                     *
*   TCR220 IS THE MESSAGE WRITER FOR THE UTILITY.  IT CONTAINS TWO    *
*   PROCESSING OPTIONS - 1) CONSECUTIVE OUTPUTTING OF ALL IEB9XXX     *
*   MESSAGES AND  2) SINGLE LINE SYSPRINT OUTPUTTINE OF CONTROL       *
*   STATEMENT IMAGES, PAGE HEADINGS, SYNAD MESSAGES, ETC.             *
*                                                                     *
*   TCRCOM IS THE COMMON COMMUNICATION REGION OF THE UTILITY.  IT     *
*   CONTAINS TABLES, INDICATORS, STORAGE AREAS, AND CONSTANT DATA     *
*   AREAS THAT ARE USED BY TWO OR MORE MODULES.                       *
*                                                                     *
*ENTRY POINTS:                                                        *
*                                                                     *
*        IEBTCRIN - UTILITY ENTRY POINT                               *
*                                                                     *
*   MAY BE CALLED BY MEANS OF ATTACH, LINK, OR LOAD AND CALL MACRO-   *
*   INSTRUCTIONS.  THE ADDRESS OF A PARAMETER LIST COMPLYING WITH     *
*   STANDARD OS CONVENTIONS MUST BE PASSED IN REGISTER 1. THIS        *
*   PARAMETER LIST MUST BE OF THE FORM:                               *
*                                                                     *
*                   OPTLIST ADDRESS                                   *
*                   DDNAME LIST ADDRESS                               *
*                                                                     *
*   THE SIGN BIT OF THE LAST FULL WORD OF THE LIST MUST BE SET TO     *
*   ONE TO INDICATE THE END OF THE LIST.  UNUSED ENTRIES MUST BE SET  *
*   TO ZERO.  POSSIBLE INVOKING OPTIONS:                              *
*                                                                     *
*        //       EXEC  PGM=IEBTCRIN                                  *
*                                                                     *
*                 ATTACH EP=IEBTCRIN,PARAM=(OPTLIST,DDLIST),VL=1,    X*
*                                                                     *
*                 LINK  EP=IEBTCRIN,PARAM=(OPTLIST,DDLIST),VL=1       *
*                                                                     *
*                 LOAD  EP=IEBTCRIN                                   *
*                 CALL  IEBTCRIN,(OPTLIST,DDLIST),VL                  *
*                                                                     *
*        TCR220 - MESSAGE WRITER                                      *
*                                                                     *
*   PARAMETER DATA IS PASSED BY NAME BY ONE OF TWO OPTIONS:           *
*                                                                     *
*   1)  BY MEANS OF THE COMMON COMMUNICATION REGION TABLE MSGPTRS     *
*       WHICH CONTAINS ONE ENTRY FOR EACH IEB-TYPE MESSAGE (EXCLUDING *
*       SYNAD MESSAGES).  THIS OPTION IS USED TO SUCCESSIVELY OUTPUT  *
*       IEBXXXX MESSAGES WHICH HAVE BEEN REQUESTED BY VARIOUS PHASES  *
*       OF THE UTILITY.                                               *
*                                                                     *
*   2)  BY MEANS OF MESSAGE ADDRESS AND MESSAGE LENGTH INDICATORS     *
*       IN THE COMMON COMMUNICATION REGION (MSGAD AND MSGLN,          *
*       RESPECTIVELY).                                                *
*                                                                     *
*   THE SECOND OPTION IS REQUESTED BY SETTING BIT 6 OF THE COMMON     *
*   COMMUNICATION REGION INDICATOR PRTRTN TO 1.  BIT 6 SET TO ZERO    *
*   INDICATES OPTION 1.                                               *
*                                                                     *
*   CALLING SEQUENCE IS AS FOLLOWS:                                   *
*                                                                     *
*               L     R15,ATCR220                                     *
*               BALR  R14,R15                                         *
*               .                                                     *
*               .                                                     *
*               .                                                     *
*      ATCR220  DC    A(TCR220)                                       *
*                                                                     *
*INPUT:                                                               *
*                                                                     *
*   1)  STANDARD PARAMETER LIST PASSED VIA REGISTER 1.                *
*                                                                     *
*   2)  SYSIN DATA SET CONTAINING UTILITY CONTROL STATEMENTS.  THE    *
*       DATA SET IS OPENED AND CLOSED BY THE DRIVER.  READING AND     *
*       PROCESSING OF THE STATEMENTS IS PERFORMED BY THE CONTROL      *
*       CARD ANALYZER MODULE (IEBTCR04).                              *
*                                                                     *
*   3)  SYSUT1 TAPE CARTRIDGE READER DATA SET.  THE DATA SET IS       *
*       OPENED BY THE DRIVER.  READING AND EDITING IS PERFORMED BY    *
*       THE READ AND EDIT MODULE (IEBTCR05).                          *
*                                                                     *
*OUTPUT:                                                              *
*                                                                     *
*   1)  SYSPRINT DATA SET.  DATA CONSISTS OF CONTROL STATEMENT        *
*       IMAGES, UTILITY COMPLETION MESSAGES, AND IEBXXXX MESSAGES.    *
*       OUTPUT IS PERFORMED BY MEANS OF THE QSAM ACCESS METHOD.       *
*                                                                     *
*   2)  CONSOLE MESSAGES.  CONSISTS OF A MOUNT MESSAGE AND CERTAIN    *
*       IEB9XXX ERROR MESSAGES.                                       *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*                                                                     *
*        IEBTCR02 - MESSAGE MODULE                                    *
*                                                                     *
*   CONTAINS IEBXXXX MESSAGES, A SYSPRINT PAGE HEADING, AND A         *
*   UTILITY COMPLETION MESSAGE.                                       *
*                                                                     *
*        IEBTCR03 - JFCB ANALYZER                                     *
*                                                                     *
*   ANALYZES AND EXTRACTS INFORMATION FROM THE DATA SET JOB FILE      *
*   CONTROL BLOCKS AND FROM THE TASK INPUT/OUTPUT TABLE (TIOT).       *
*   IT ATTEMPTS TO SET DEFAULTS FOR THOSE DCB PARAMETERS THAT HAVE    *
*   BEEN OMITTED.                                                     *
*                                                                     *
*        IEBTCR04 - CONTROL CARD ANALYZER                             *
*                                                                     *
*   READS AND ANALYZES CONTROL STATEMENTS PROVIDED BY THE USER. IT    *
*   VALIDATES THE STATEMENT CONTENT AND FORMAT AND SETS OPTIONS IN THE*
*   COMMON COMMUNICATION REGION FOR USE OF THE DRIVER (IEBTCRIN)      *
*   AND THE READ/EDIT MODULE (IEBTCR05).                              *
*                                                                     *
*        IEBTCR05 - READ/EDIT MODULE                                  *
*                                                                     *
*   THIS MODULE READS DATA FROM THE SYSUT1 TAPE CARTRIDGE READER      *
*   DATA SET, EDITS IT, AND OUTPUTS IT TO THE SYSUT2 AND SYSUT3       *
*   DATA SETS.                                                        *
*                                                                     *
*EXITS-NORMAL:  TCR720 - DCB EXIT                                     *
*   A DCB EXIT IS TAKEN FOR EACH DATA SET AT OPEN.  DEFAULTS ARE SET  *
*   FOR MISSING DCB PARAMETERS AND A COMPARISON IS MADE OF ALL PARA-  *
*   METERS TO DETERMINE INCONSISTENCIES.                              *
*                                                                     *
*     -ERROR:                                                         *
*                                                                     *
*   1)  TCR740 - QSAM SYNAD ROUTINE.  THIS ROUTINE IS ENTERED WHENEVER*
*       A PERMANENT I/O ERROR OCCURS ON THE SYSIN, SYSPRINT, SYSUT2,  *
*       OR SYSUT3 DATA SET.  THE ROUTINE ISSUES A SYSNADAF MACRO-     *
*       INSTRUCTION AND OUTPUTS THE RESULTING MESSAGE TO THE SYSPRINT *
*       DATA SET.  IF THE ERROR OCCURRED ON SYSPRINT, THE SYNAD       *
*       MESSAGE IS OUTPUT TO THE CONSOLE.  IF THE MESSAGE BEING OUTPUT*
*       AT THE TIME OF THE ERROR IS AN IEB-TYPE MESSAGE, IT IS ALSO   *
*       WRITTEN ON THE CONSOLE.  THE ADDRESS OF THE MESSAGE IS STORED *
*       IN THE COMMON COMMUNICATION REGION FULL-WORD CURMSGAD.        *
*                                                                     *
*   2)  TCR800 - STAE EXIT ROUTINE.   THIS ROUTINE IS ENTERED WHENEVER*
*       AN ABEND OCCURS DURING EXECUTION OF ANY PHASE OF THE UTILITY. *
*       AT ENTRY THE ABEND COMPLETION CODE IS PRESENT EITHER IN A STAE*
*       WORK AREA OR IN REGISTER 1, DEPENDING UPON WHETHER OR NOT A   *
*       WORK AREA WAS PROVIDED.  REGISTER 0 IS SET TO ZERO IF A WORK  *
*       AREA HAS BEEN PROVIDED; TWELVE, IF ONE HAS NOT.  THE ROUTINES *
*       DETERMINE WHETHER THE ABEND WAS CAUSED BY A USER ROUTINE LOAD *
*       ERROR.  IF IT WAS THEY REQUEST THAT A RETRY ROUTINE BE LOADED *
*       WHICH SETS INDICATORS AND RETURNS TO THE ABEND-ING MODULE     *
*       AT A POINT SPECIFIED BY MEANS OF THE STORAGE AREA RETPT.      *
*       IF THE ERROR WAS NOT A USER ROUTINE LOAD ERROR, THE EXIT      *
*       ROUTINE ALLOWS ABEND PROCESSING TO CONTINUE.                  *
*                                                                     *
*TABLES/WORK AREAS:                                                   *
*                                                                     *
*   1)  OPTABLE - CONTAINS CONTROL CARD OPTIONS.  IT IS CONSTRUCTED   *
*       BY THE CONTROL CARD ANALYZER FOR USE BY THE DRIVER AND THE    *
*       READ/EDIT MODULE.                                             *
*                                                                     *
*   2)  ADDRTAB - CONTAINS ADDRESSES OF USER EXIT AND TRANSLATE       *
*       ROUTINES AND OF THE MESSAGE MODULE.                           *
*                                                                     *
*   3)  NAMETAB - CONTAINS DDNAMES AND EXIT NAMES.                    *
*                                                                     *
*   4)  MSGPTRS - CONTAINS ONE ENTRY FOR EACH IEB-TYPE MESSAGE CON-   *
*       TAINED IN THE MESSAGE MODULE.  EACH ENTRY CONTAINS A REQUEST  *
*       BYTE, A NAME INSERT BYTE, AN INSERT LOCATION BYTE, AND AN     *
*       OUTPUT LOCATION BYTE.  IT IS USED FOR REQUESTING OUTPUT OF    *
*       IEB-TYPE MESSAGES.  A DIRECT ONE-TO-ONE CORRESPONDENCE        *
*       EXISTS BETWEEN THIS TABLE AND THE MESSAGE MODULE TABLE.       *
*                                                                     *
*   5)  INLRECL, PRTLRECL, UT2LRECL, UT3LRECL - DCB DEFAULT TABLES    *
*       USED BY IEBTCRIN AND IEBTCR03 FOR CONSTRUCTING DEFAULTS       *
*                                                                     *
*   6)  USERNAME - CONTAINS USER ROUTINE NAMES                        *
*                                                                     *
*ATTRIBUTES:  NON-REENTRANT AND NON-REUSABLE.                         *
*                                                                     *
*NOTES:  IEBTCR03, IEBTCR04, AND IEBTCR05 ALL USE THE COMMON          *
*   COMMUNICATION REGION SET UP WITHIN THE DRIVER MODULE.             *
*                                                                     *
*   CONTROL PASSES TO THE DRIVER MODULE FOR INSERTION OF DCB          *
*   PARAMETERS AT OPEN, FOR CONSTRUCTION OF A SYNAD MESSAGE FOLLOWING *
*   A PERMANENT I/O ERROR, AND DURING ABEND PROCESSING.               *
*                                                                     *
*   A DOM MACRO-INSTRUCTION IS ISSUED BY THE READ/EDIT MODULE         *
*   (IEBTCR05) TO ERASE THE MOUNT MESSAGE ISSUED BY THE DRIVER ROUTINE*
*   TCR180.                                                           *
*                                                                     *
*   THIS MODULE CONTAINS TYPE C CHARACTER CODE DEPENDENCIES.  THE     *
*   OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL REPRESENTATION  *
*   OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT TO THE ONE USED *
*   AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED SO THAT REDEFINI- *
*   TION OF CHARACTER CONSTANTS, BY RE-ASSEMBLY, WILL RESULT IN A     *
*   CORRECT MODULE FOR THE NEW DEFINITIONS.  CHARACTER CONSTANTS      *
*   INVOLVED ARE AS FOLLOWS:                                          *
*                                                                     *
*   1)  COMMA AND HEXSPACE WHICH ARE USED BY ROUTINE TCR180 FOR CON-  *
*       STRUCTING THE MOUNT MESSAGE                                   *
*                                                                     *
*   2)  SYNUR, SYNTA, AND SYNDA WHICH ARE USED FOR DETERMINING THE    *
*       TYPE OF MESSAGE PROVIDED BY THE QSAM SYNADAF INSTRUCTION      *
*       (UNIT RECORD, TAPE, OR DIRECT ACCESS).                        *
*                                                                     *
*   3)  SYNMSGUR, SYNMSGTA, AND SYNMSGDA WHICH CONTAIN MESSAGE        *
*       IDENTIFIERS (IEB920I, IEB921I, IEB922I)                       *
*                                                                     *
*                                                                     *
*   IF WORLD TRADE REQUIREMENTS NECESSITATE MODIFICATION OF THE       *
*   MESSAGE TEXT CONTAINED IN THE MESSAGE MODULE, IT MAY BE NECESSARY *
*   TO CHANGE THE INSERT LOCATION DISPLACEMENTS CONTAINED IN THE      *
*   MESSAGE POINTER TABLE MSGPTRS PRESENT IN THE COMMON COMMUNICATION *
*   REGION CSECT TCRCOM.  INSTRUCTIONS ARE PROVIDED FOR MAKING THIS   *
*   MODIFICATION.                                                     *
*                                                                     *
*   IF CHANGES ARE MADE TO THE MESSAGE IDENTIFIERS CONTAINED IN THE   *
*   MESSAGE MODULE, IT MAY ALSO BE NECESSARY TO CHANGE THE IDENTIFIERS*
*   IEB920I, IEB921I, AND IEB922I WHICH ARE DEFINED BY THE DC'S       *
*   SYNMSGUR, SYNMSGTA, AND SYNMSGDA CONTAINED IN THIS MODULE.        *
*   COMMENTS ARE PROVIDED.                                            *
  TITLE 'IEBTCRIN - DRIVER MODULE - REGISTER DEFINITIONS'
IEBTCRIN START 0
         SPACE 2
*        REGISTER DEFINITIONS
         SPACE 2
LNKREG0  EQU   0                       LOAD MODULE LINKAGE REGISTER
PARMREG  EQU   1                       PARAMETER LIST REGISTER
NAMEREG  EQU   3                       USER NAME TABLE PTR
ADDREG   EQU   4                       USER RTN ADDR TABLE PTR
DCBREG   EQU   5                       DCB REGISTER
INDREG   EQU   5                       INDICATOR MASK REGISTER
MSGCTR   EQU   6                       MSG INSERT INDICATOR
COMMREG  EQU   11                      COMMUNICATION REGION
BASEREG  EQU   12                      BASE REGISTER
SAVEREG  EQU   13                      SAVEAREA REGISTER
RETREG   EQU   14                      RETURN REGISTER
LNKREG15 EQU   15                      SUBROUTINE LINKAGE REGISTER
BASE15   EQU   15                      BASE REGISTER
         SPACE 1
R0       EQU   0                       REGISTER   0
R1       EQU   1                       REGISTER   1
R2       EQU   2                       REGISTER   2
R3       EQU   3                       REGISTER   3
R4       EQU   4                       REGISTER   4
R5       EQU   5                       REGISTER   5
R6       EQU   6                       REGISTER   6
R7       EQU   7                       REGISTER   7
R8       EQU   8                       REGISTER   8
R9       EQU   9                       REGISTER   9
R10      EQU   10                      REGISTER  10
R12      EQU   12                      REGISTER  12
R14      EQU   14                      REGISTER  14
R15      EQU   15                      REGISTER  15
  TITLE 'IEBTCRIN - DRIVER MODULE - MAINLINE'
*        THIS SECTION SAVES THE CALLING MODULE'S BASE REGISTERS,
*        SETS UP A SAVEAREA ADDRESS, AND ESTABLISHES BASE REGISTERS
*        FOR THE MAIN CSECT AND FOR THE COMMUNICATION REGION CSECT.
         SPACE 2
         SAVE  (14,12),T,IEBTCRIN      SAVE REGISTERS
         BALR  BASEREG,R0              ESTABLISH BASE REGISTER
         USING *,BASEREG               INDICATE BASE REGISTER
TCR050   LR    R15,SAVEREG             SET UP SAVEAREA POINTERS
         ST    R15,SAVEAREA+4
         LA    SAVEREG,SAVEAREA
         ST    SAVEREG,8(R15)
         SR    R15,R15
         ST    R15,0(SAVEREG)
         ST    PARMREG,SAVEPARM        SAVE PARAMETER LIST POINTER
         ST    BASEREG,DRIVBASE        STORE DRIVER BASE ADDRESS
         STAE TCR800,CT,PARAM=USERLIST,XCTL=NO  ESTABLISH STAE
*                                      ENVIRONMENT
         LTR   LNKREG15,LNKREG15       SUCCESSFUL CREATION
         BZ    TCR060                  BR YES
         MVI   USERLIST+1,STAEFAIL     INDICATE FAILURE
TCR060   L     COMMREG,ATCRCOM         LOAD BASE REG FOR COMMON AREA
         USING TCRCOM,COMMREG          INDICATE COMM. REGION BASE REG
         EJECT
*        DRIVER MAINLINE
         SPACE 1
*        THIS ROUTINE CONTROLS THE FLOW OF THE ENTIRE UTILITY. IT
*        CALLS THE FOLLOWING PROCESSING ROUTINES AND MODULES:
         SPACE 1
*              TCR120 - PARAMETER LIST PROCESSOR
*              TCR130 - MESSAGE MODULE LOADER
*            IEBTCR03 - JOB FILE CONTROL BLOCK ANALYZER
*              TCR140 - SYSPRINT OPEN AND ALLOCATION
*              TCR150 - CONTROL CARD ANALYZER INTERFACE
*              TCR160 - USER ROUTINE LOADER
*              TCR170 - DCB DEFAULT COMPLETION
*              TCR180 - SYSUT1 OPEN ROUTINE
*            IEBTCR05 - READ/EDIT MODULE
*              TCR190 - SYSUT1 SYNAD ROUTINE
*              TCR220 - MESSAGE WRITER
         SPACE 1
*        FOLLOWING EXECUTION OF EACH CALLED ROUTINE OR MODULE,
*        THE DRIVER MAINLINE ROUTINE TESTS WHETHER OR NOT EXECUTION
*        SHOULD CONTINUE.
         SPACE 2
TCR100   BAL   RETREG,TCR120           LOAD ADDR PARAM PROC ROUTINE
         BAL   RETREG,TCR130           LOAD ADDR MSG MODULE LOAD RTN
TCR102   LINK  EP=IEBTCR03,PARAM=TCRCOM  LINK TO JFCB ANALYZER
         DELETE  EP=IEBTCR03           DELETE JFCB ANALYZER
         BAL   RETREG,TCR140           OPEN SYSPRINT DCB, ALLOCATE
*                                        STORAGE
         TM    ERRIND,ALLONE           ANY ERROR
         BNZ   TCR108                  BR YES
         BAL   RETREG,TCR150           CONTROL CARD ANALYZER INTERFACE
         TM    ERRIND,ALLONE           CONTROL CD ERROR?
         BNZ   TCR108                  BR - YES
         BAL   RETREG,TCR160           LOAD USER EXIT RTN LOADER ADDR
         TM    ERRIND,LOADERR          ERROR LOADING USER EXIT ROUTINES
         BO    TCR108                  BR - YES
         BAL   RETREG,TCR170           COMPLETE DCB DEFAULTS WITH MAXLN
*                                        INCREMENT
         TM    ERRIND,ALLONE           ANY ERROR
         BNZ   TCR108                  BR YES
         BAL   RETREG,TCR180           OPEN TCR AND ISSUE MOUNT MSG
         LOAD  EP=IEBTCR05             LOAD READ/EDIT MODULE     YM2512
         LA    1,*+8                   LOAD ADDRESS OF TCRCOM    YM2522
         B     *+8                     BRANCH AROUND CONSTANT    YM2522
         DC    A(TCRCOM)               ADDRESS OF TCRCOM         YM2522
         LR    LNKREG15,LNKREG0        LOAD REG 15 WITH ADDRESS  YM2512
         BALR  RETREG,LNKREG15         BAL TO READ EDIT MODULE   YM2512
         TM    MSG14+1,UT2             SYSUT2 DCB PARAMETERS MISSING
         BO    TCR105                  BR NO
         PACK  MSG14+1(1),MSG14+1(1)   SWITCH THE TWO INSERT DIGITS
TCR105   TM    ERRIND,IORDERR          PERMANENT I/O ERROR SYSUT1
         BNO   TCR107                  BR NO
         TM    DUMMYIND,DUMSYSP        SYSPRINT DUMMY
         BO    TCR107                  BR YES
         BAL   RETREG,TCR190           ISSUE SYNAD MESSAGE FOR SYSUT1
TCR107   CLOSE  (TCRDCB)               CLOSE SYSUT1 DCB
         DELETE  EP=IEBTCR05           DELETE READ/EDIT MODULE
         TM    ERRIND,RDEDERR          ERROR IN READ & EDIT
         BO    TCR1073                 BR YES
         TM    ERRIND,CONFLERR         INCONSISTENT DCB PARAMETERS
         BNO   TCR108                  BR NO
TCR1073  MVI   RETCODE,CODE16          SET RETURN CODE 16
TCR108   MVI   CARRCTRL,DOUBLESP       SET DOUBLE SPACE
         L     LNKREG15,ATCR220        LOAD MESSAGE WRITER ADDRESS
         BALR  RETREG,LNKREG15         DISPLAY MESSAGES
TCR1083  TM    DUMMYIND,DUMSYSP        SYSPRINT DUMMY
         BO    TCR109                  BR YES
         MVI   CARRCTRL,DOUBLESP       DOUBLE SPACE
         L     R5,MSGADDR              LOAD MSG TABLE ADDRESS
         L     R6,COMPMSG(R5)          LOAD MSG ADDRESS
         LA    R6,0(R6)                CLEAR LENGTH BYTE
         ST    R6,MSGAD                STORE MSG ADDRESS
         MVC   MSGLN(1),COMPMSG(R5)    STORE MSG LENGTH
         OI    PRTRTN,RTN310           SET SINGLE MSG INDICATOR
         L     LNKREG15,ATCR220        LOAD MESSAGE WRITER ADDRESS
         BALR  RETREG,LNKREG15         DISPLAY MESSAGE
         CLOSE (SYSPRDCB)              CLOSE SYSPRINT
         TM    ERRIND,ALLOCERR         GETMAIN ALLOCATION ERROR
         BO    TCR109                  BR YES
         L     R1,PRTAREA              LOAD ADDR PRINT STORAGE
         L     R0,PRTLNGTH             LOAD STORAGE LENGTH
         FREEMAIN  R,LV=(0),A=(1)      FREE SYSPRINT AREA
TCR109   STAE  0
         XR    R15,R15                 CLEAR RETURN CODE REGISTER
         IC    R15,RETCODE             INSERT RETURN CODE
         L     SAVEREG,4(SAVEREG)
         RETURN (14,12),T,RC=(15)
  TITLE 'IEBTCRIN - DRIVER MODULE - PARAMETER LIST PROCESSOR'
*        PARAMETER LIST PROCESSOR
         SPACE 1
*        THIS ROUTINE STORES ANY ALTERNATE DDNAMES THAT MAY HAVE
*        BEEN PROVIDED BY THE INVOKING MODULE.
         SPACE 2
TCR120   ST    RETREG,SAVE100          SAVE RETURN REGISTER
         L     R4,SAVEPARM             LOAD ADDR PARAMETER LIST
         TM    OPTLIST(R4),LASTPARM    PARAMETER LIST VOID?
         BO    TCR129                  BR IF YES
         L     R5,DDLIST(R4)           LOAD ADDR DDLIST
         LTR   R5,R5                   LOAD AND TEST DDLIST ADDR
         BZ    TCR129                  BR IF ADDRESS ZERO
         LH    R9,0(R5)                LOAD COUNT
         LA    R5,DSYSIN-6(R5)         SET AT FIRST DD ENTRY MINUS 8
         LA    R6,NAMETAB-DDLNGTH      LOAD ZERO ENTRY OF DDTABLE
         LA    R7,DSYSIN               INITIALIZE REGISTER TO ZERO
         LA    R8,DDINCR               LOAD INCREMENT
         LA    R3,DSYSPRT+DDINCR       SET BYPASS INDICATOR
TCR122   BXH   R7,R8,TCR126            BR WHEN ALL ENTRIES TESTED
         LA    R5,DDINCR(R5)           INCREMENT INPUT DDLIST ADDR
         LA    R6,DDINCR(R6)           INCREMENT OUTPUT DDLIST ADDR
         CLC   0(DDLNGTH,R5),ZEROES    DDNAME EQUAL TO HEX ZEROES
         BE    TCR124                  BR IF YES
         MVC   0(DDLNGTH,R6),0(R5)     MOVE NEW DDNAME
TCR124   CLR   R7,R3                   SECOND DDNAME?
         BNE   TCR122                  BR IF NO
         LA    R5,DDLNGTH(R5)          BYPASS NEXT TABLE ENTRY
         LA    R7,DDLNGTH(R7)          BYPASS NEXT TABLE ENTRY
         B     TCR122                  BR TO LOOP
TCR126   OI    MISCIND,DDON            SET DDNAME SWAP IND ON
TCR129   L     RETREG,SAVE100          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCRIN - DRIVER MODULE - MESSAGE MODULE LOADER'
*        MESSAGE MODULE LOADER
         SPACE 2
TCR130   ST    RETREG,SAVE100          SAVE RETURN ADDRESS
         MVI   USERLIST,MSGMODL        INDICATE MSG MODULE
         LOAD  EP=IEBTCR02             LOAD MESSAGE MODULE
TCR137   ST    LNKREG0,MSGADDR         SAVE MSG MODULE ADDR
TCR139   L     RETREG,SAVE100          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCRIN - DRIVER MODULE - SYSPRINT OPEN ROUTINE'
*        SYSPRINT OPEN AND STORAGE ALLOCATION
         SPACE 1
*        THIS ROUTINE OPENS THE SYSPRINT DATA SET, ALLOCATES
*        STORAGE FOR A WORKAREA, AND OUTPUTS A SYSPRINT PAGE HEADING.
         SPACE 2
TCR140   ST    RETREG,SAVE100          SAVE RETURN ADDRESS
         TM    DUMMYIND,DUMSYSP        SYSPRINT DUMMY
         BO    TCR149                  BR YES
         LA    R2,SYSPRDCB             LOAD DCB ADDRESS
         USING IHADCB,R2               INDICATE DCB DSECT REGISTER
         OPEN  (SYSPRDCB,(OUTPUT))     OPEN SYSPRINT
         TM    ERRIND,CONFLERR         INCONSISTENT DCB PARAMETERS
         BO    TCR149                  BR YES
         TM    DCBOFLGS,DCBOPEN        OPEN SUCCESSFUL
         BO    TCR1403                 BR YES
         MVI   MSG15,MSGREQ            SET MSG INDICATOR
         MVI   MSG15+1,PRT             SET SYSPRINT INSERT
         MVI   RETCODE,CODE16          SET RETURN CODE 16
TCR1403  LH    R1,DCBLRECL             LOAD LRECL
         LA    R1,8(R1)                ADD 8 BYTES
         ST    R1,PRTLNGTH             STORE NEW LENGTH
         LR    R0,R1                   SET UP PARAMETER REGISTER
         BAL   RETREG,TCR420           GETMAIN STORAGE
         TM    ERRIND,ALLOCERR         ALLOCATION ERROR
         BNO   TCR141                  BR NO
         OI    DUMMYIND,DUMSYSP        SET SYSPRINT DUMMY
         B     TCR149                  BR TO RETURN
TCR141   MVC   PRTAREA(8),GETMADD      STORE ADDRESS AND LENGTH
         L     R1,PRTAREA              LOAD PRINT AREA ADDRESS
         B     TCR145                  BYPASS CLEAR INSTRUCTION
         SPACE 1
TCR142   MVC   1(0,R1),0(R1)           CLEAR PRINT AREA
         SPACE 1
TCR145   LH    R2,DCBLRECL             FIND PRTAREA LENGTH
         LA    R2,6(R2)                INCREMENT 6 BYTES
         EX    R2,TCR142               CLEAR PRINT AREA
         MVI   CARRCTRL,NEWPAGE        SET NEW PAGE CARRIAGE CONTROL
         L     R5,MSGADDR              LOAD MSG TABLE ADDRESS
         L     R6,HDGMSG(R5)           LOAD MSG ADDRESS
         LA    R6,0(R6)                CLEAR LENGTH BYTE
         ST    R6,MSGAD                STORE MSG ADDRESS
         MVC   MSGLN(1),HDGMSG(R5)     STORE MSG LENGTH
         OI    PRTRTN,RTN310           SET SINGLE MSG INDICATOR
         L     LNKREG15,ATCR220        LOAD MESSAGE WRITER ADDRESS
         BALR  RETREG,LNKREG15         DISPLAY MESSAGE
         MVI   CARRCTRL,DOUBLESP       INDICATE DOUBLESPACING FOR
*                                      NEXT MESSAGE
TCR149   L     RETREG,SAVE100          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCRIN - DRIVER MODULE - CTRL CARD ANALYZER INTERFACE'
*        CONTROL CARD ANALYZER INTERFACE
         SPACE 1
*        THIS ROUTINE OPENS THE SYSIN DATA SET IF ONE HAS BEEN
*        PROVIDED, ALLOCATES CORE FOR A WORK AREA, CALLS THE MESSAGE
*        WRITER TO OUTPUT ANY ERROR MESSAGES DEVELOPED UP TO THIS
*        POINT, LINKS TO THE CONTROL CARD ANALYZER, AND CLOSES THE
*        SYSIN DATA SET.
*        IF NO SYSIN DD STATEMENT WAS PROVIDED, THE ROUTINE SETS
*        DEFAULT OPTIONS.
         SPACE 2
TCR150   ST    RETREG,SAVE100          SAVE RETURN ADDRESS
         TM    DUMMYIND,DUMSYSIN       SYSIN DUMMY
         BO    TCR156                  BR TO SET DEFAULTS
         OPEN  (SYSINDCB,(INPUT))      OPEN SYSIN
         TM    ERRIND,CONFLERR         INCONSISTENT DCB PARAMETERS
         BO    TCR159                  BR YES
         LA    R2,SYSINDCB             LOAD SYSIN DCB ADDRESS
         TM    DCBOFLGS,DCBOPEN        OPEN SUCCESSFUL
         BO    TCR151                  BR YES
         MVI   MSG15,MSGREQ            SET MSG INDICATOR
         MVI   MSG15+1,IN              SET SYSIN INSERT
         MVI   RETCODE,CODE16          SET RETURN CODE 16
TCR151   LA    R1,CARDLN(0,0)          LOAD LENGTH 80
         BAL   RETREG,TCR420           GET REQUESTED STORAGE
         TM    ERRIND,ALLOCERR         ALLOCATION ERROR
         BO    TCR159                  BR YES
         MVI   CARRCTRL,DOUBLESP       SET DOUBLE SPACE
         L     LNKREG15,ATCR220        PRINT MESSAGES REQUESTED UP
         BALR  RETREG,LNKREG15           TO THIS POINT
         L     R1,GETMADD              LOAD GETMAINED STORAGE ADDRESS
TCR152   ST    R1,CARDAREA             STORE WORK AREA ADDRESS
         LA    R1,8(R1)                SET AT COLUMN 9
         ST    R1,COLUMN9              STORE COLUMN 9 ADDRESS
         LA    R1,INC63(R1)            SET AT COLUMN 72
         ST    R1,CARDEND              STORE COLUMN 72 ADDRESS
         LINK  EP=IEBTCR04,PARAM=TCRCOM   LINK TO CONTROL CD ANALYZER
         DELETE  EP=IEBTCR04           DELETE CONTROL CD ANALYZER
         L     R1,CARDAREA             LOAD GETMAINED STORAGE ADDRESS
         FREEMAIN  R,LV=80,A=(1)       FREE STORAGE
         TM    ERRIND,QSAMERR          PERMANENT I/O ERROR SYSIN
         BO    TCR159                  BR YES
         CLOSE (SYSINDCB)              CLOSE SYSIN
         B     TCR159                  BR TO RETURN
         SPACE 1
*        SET DEFAULTS
         SPACE 1
TCR156   MVC   OPTABLE+1(2),MTDIDEFT   SET PARM1 AND PARM2 OPTIONS
         MVI   MSG11,MSGREQ            SET MSG INDICATOR
         MVI   RETCODE,CODE04          SET RETURN CODE
TCR159   L     RETREG,SAVE100          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCRIN - DRIVER MODULE - USER ROUTINE LOADER'
*        USER ROUTINE LOADER
         SPACE 1
*        THIS ROUTINE USES THE CONTROL CARD OPTION TABLE (IEXITS)
*        AND THE USER ROUTINE NAME TABLE (USERNAME) TO LOAD THE
*        SPECIFIED USER EXIT ROUTINES AND TRANSLATE TABLE.  IT
*        STORES THE ADDRESSES OF THE LOADED MODULES IN THE COMMON
*        COMMUNICATION REGION ADDRESS TABLE (USERADDR).
*        IF IT FAILS TO FIND A ROUTINE, THE STAE EXIT ROUTINE
*        (TCR800) IS SCHEDULED DURING THE RESULTING ABEND PROCESSING.
*        IF THE STAE EXIT ROUTINE DETERMINES THAT THE ABEND WAS
*        CAUSED BY LOADING A NON-EXISTENT USER MODULE, IT REQUESTS
*        THAT THE STAE RETRY ROUTINE (TCR810) BE SCHEDULED.  THIS
*        ROUTINE RESTORES BASE REGISTERS, SETS ERROR AND MESSAGE
*        INDICATORS, AND RETURNS CONTROL TO THE LOADER AT TCR165.
         SPACE 2
TCR160   ST    RETREG,SAVE100          SAVE RETURN ADDRESS
         LA    MSGCTR,HDR2IND-1        INITIALIZE MSG INDICATOR
         SR    INDREG,INDREG           ZERO REGISTER
         LH    INDREG,INDMSK           LOAD INDICATOR MASK
         LA    NAMEREG,USERNAME-8      LOAD USER ROUTINE NAME TABLE AD
         LA    ADDREG,USERADDR-4       LOAD USER ROUT ADDR TABLE ADDR
TCR162   CLC   TCR164+1(1),LASTROUT    LAST POSS ROUTINE LOADED?
         BE    TCR168                  BR IF YES
         SRL   INDREG,SHIFTLN          SHIFT MASK ONE BIT
         STC   INDREG,TCR164+1         STORE MASK IN TEST MSK INST
         LA    NAMEREG,8(NAMEREG)      INCR TO NEXT NAME ENTRY
         LA    ADDREG,4(ADDREG)        INCR TO NEXT ADDR ENTRY
         LA    MSGCTR,1(MSGCTR)        INCREMENT MSG INDICATOR
TCR164   TM    IEXITS,HEXZERO          MUST THIS ROUTINE BE LOADED
         BZ    TCR162                  BR IF NO
         MVC   ENTPT(8),0(NAMEREG)     SET UP ROUTINE NAME
         STC   MSGCTR,USERLIST         STORE ROUTINE INDICATOR
         LOAD  EPLOC=ENTPT             LOAD NEXT MODULE
         SPACE 1
*        STAE PROCESSING RETURN POINT
         SPACE 1
TCR165   TM    ERRIND,LOADERR          ERROR INDICATOR SET
         BO    TCR169                  BR YES
         LTR   LNKREG0,LNKREG0         ADDRESS STORED
         BZ    TCR169                  BR NO
TCR167   ST    LNKREG0,0(ADDREG)       STORE USER ROUTINE ADDR
         B     TCR162                  BR TO LOOP
         SPACE 1
*        SET USER TRANSLATE TABLE ADDRESS TO ZERO IF TABLE NOT
*        PROVIDED BY USER
         SPACE 1
TCR168   TM    IEXITS,USERTAB          USER TRANSLATE TABLE PRESENT
         BO    TCR169                  BR YES
         MVC   USERADDR+24(4),ZEROES   SET ADDRESS TO ZERO
TCR169   L     RETREG,SAVE100          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCRIN - DRIVER MODULE - DCB DEFAULT COMPLETION'
*        DCB DEFAULT COMPLETION
         SPACE 1
*        THIS ROUTINE INCREMENTS THE BASIC DEFAULTS DEVELOPED BY
*        THE JFCB ANALYZER FOR THE SYSUT2 AND SYSUT3 DCB'S BY THE
*        MAXLN VALUE IF ONE OR MORE PARAMETERS WERE OMITTED BY THE
*        USER.
*        IF EDITING WAS SPECIFIED BY THE USER, AN ADDITIONAL FOUR
*        BYTES ARE ADDED TO THE SYSUT3 DEFAULTS TO ACCOMODATE THE
*        ERROR DESCRIPTOR WORD.
         SPACE 1
TCR170   ST    RETREG,SAVE100
         SPACE 1
*        SET UP MAXLN
         SPACE 1
         CLC   MAXLN(2),ZEROES         MAXLN EQUALS ZERO?
         BNE   TCR172                  BR NO
         MVI   MAXLN+1,DEF120          MOVE DEFAULT TO MAXLN
         SPACE 1
TCR172   TM    MISCIND,UT2LRLMX        DOES SYSUT2 LRECL REQUIRE MAXLN
         BNO   TCR173                  BR NO
         LH    R2,UT2LRECL             ADD MAXLN TO LRECL
         AH    R2,MAXLN
         STH   R2,UT2LRECL             REPLACE LRECL
         SPACE 1
TCR173   TM    MISCIND,UT2BLKMX        SYSUT2 BLKSIZE REQUIRE MAXLN?
         BNO   TCR174                  BR NO
         LH    R2,UT2BLK               ADD MAXLN TO BLKSIZE
         AH    R2,MAXLN
         STH   R2,UT2BLK               REPLACE BLKSIZE
         SPACE 1
TCR174   TM    MISCIND,UT3LRLMX        SYSUT3 LRECL REQUIRE MAXLN?
         BNO   TCR176                  BR NO
         LH    R2,UT3LRECL             ADD MAXLN TO LRECL
         AH    R2,MAXLN
         TM    IPARM2,EDITSPEC         EDIT SPECIFIED
         BZ    TCR175                  BR NO
         LA    R2,4(R2)                ADD 4-BYTE ERROR DESCRIPTOR
TCR175   STH   R2,UT3LRECL             REPLACE LRECL
         SPACE 1
TCR176   TM    MISCIND,UT3BLKMX        DOES SYSUT3 BLKSIZE REQUIRE
         BNO   TCR179                    MAXLN -  BR NO
         LH    R2,UT3BLK               LOAD BLKSIZE
         AH    R2,MAXLN                ADD MAXLN TO BLKSIZE
         TM    IPARM2,EDITSPEC         EDIT SPECIFIED
         BZ    TCR178                  BR NO
         LA    R2,4(R2)                ADD 4-BYTE ERROR DESCRIPTOR
TCR178   STH   R2,UT3BLK               REPLACE BLKSIZE
TCR179   L     RETREG,SAVE100          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN TO MAINLINE
  TITLE 'IEBTCRIN - DRIVER MODULE - SYSUT1 OPEN ROUTINE'
*        SYSUT1 OPEN ROUTINE
         SPACE 1
*        THIS ROUTINE OPENS THE SYSUT1 DATA SET, ESTABLISHES THE
*        BUFFER SIZE, AND ISSUES THE MOUNT MESSAGE.
         SPACE 2
TCR180   ST    RETREG,SAVE100          SAVE RETURN ADDRESS
         OPEN  (TCRDCB,(INPUT))        OPEN SYSUT1 DATA SET
         LA    R2,TCRDCB               LOAD DCB ADDRESS
         XR    R10,R10                 ZERO WKREG
         LH    R10,DCBBUFL             INSERT BUFFER LENGTH
         LTR   R10,R10                 LENGTH ZERO
         BNZ   TCR182                  BR NOT ZERO
         LA    R10,DEFBUFL             LOAD DEFAULT BUF LENGTH
TCR182   STH   R10,TCRBUFL             STORE BUFFER LENGTH
         L     R10,MSGADDR             LOAD  ADDR OF MSG MODULE
         L     R9,0(R10)               LOAD  PTR TO MOUNT MSG
         LA    R9,0(R9)                CLEAR LENGTH
         MVC   14(3,R9),TCRDEV         MOVE DEVICE TYPE
         MVC   18(6,R9),VOLSER         MOVE DATA SET EXTERNAL NAME
         MVI   25(R9),HEXSPACE         MOVE SPACE
         MVC   26(16,R9),25(R9)        CLEAR MSG AREA
         MVC   25(8,R9),JOBNAME        MOVE  JOB NAME
         LA    R5,24(R9)               SET POINTER TO JOB START
         LA    R6,ONE                  LOAD INCREMENT
         LA    R7,32(R9)               LOAD ENDING ADDRESS
TCR184   BXH   R5,R6,TCR186            BR ALL EIGHT CHARACTERS NON-BLK
         CLI   0(R5),HEXSPACE          SPACE
         BNE   TCR184                  BR NO
TCR186   MVI   0(R5),COMMA             INSERT COMMA
         MVC   1(8,R5),STEPNAME        INSERT STEP NAME
         WTO   MF=(E,(R9))             ISSUE MOUNT WTO
         ST    R1,MOUNTID              STORE MOUNT MSG ID
TCR189   L     RETREG,SAVE100          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCRIN - DRIVER MODULE - SYSUT1 SYNAD ROUTINE'
*        SYSUT1 SYNAD ROUTINE
         SPACE 1
*        THIS ROUTINE ISSUES THE SYNADAF MACRO-INSTRUCTION FOR
*        THE SYSUT1 DATA SET AND OUTPUTS THE MESSAGE CONSTRUCTED
         SPACE 2
TCR190   ST    RETREG,SAVE100          SAVE RETURN ADDRESS
         TM    DUMMYIND,DUMSYSP        SYSPRINT DUMMY
         BO    TCR199                  BR YES
         L     R2,IOBADDR              LOAD IOB ADDRESS
TCR192   SYNADAF  ACSMETH=EXCP,PARM1=(R2)   ISSUE SYNADAF
         L     R3,PRTAREA              LOAD PRTAREA ADDRESS
         MVC   SYNIDENT(8,R1),SYNMSGUR MOVE MSG IDENT
         LA    R4,SYNLNGTH             STORE MSG LENGTH
         STC   R4,MSGLN                STORE LENGTH
         LA    R5,SYNIDENT(R1)         STORE MSG ADDRESS
         ST    R5,MSGAD                STORE MSG LENGTH
         OI    PRTRTN,RTN310           SET SINGLE MSG INDICATOR
         MVI   CARRCTRL,DOUBLESP       INDICATE DOUBLE-SPACING
         L     LNKREG15,ATCR220        LOAD MESSAGE WRITER ADDR
         BALR  RETREG,LNKREG15         DISPLAY MESSAGE
         SYNADRLS                      RELEASE SYNAD MESSAGE AREA
TCR199   L     RETREG,SAVE100          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCRIN - DRIVER MODULE - STORAGE ALLOCATION ROUTINE'
*        STORAGE ALLOCATION ROUTINE
         SPACE 1
*        THIS ROUTINE GETMAINS STORAGE FOR ALL DRIVER ROUTINES.
*        PARAMETERS ARE AS FOLLOWS:
         SPACE 1
*              REGISTER 1 - NUMBER OF BYTES REQUESTED
*                 GETMADD - ADDRESS OF ALLOCATED STORAGE
*                  ERRIND - ALLOCATION ERROR  (BIT 1 SET TO 1 INDICATES
*                           ERROR)
         SPACE 1
TCR420   ST    RETREG,SAVE400          SAVE RETURN REGISTER
         ST    R1,GETMREQ              STORE MINIMUM LENGTH
         ST    R1,GETMREQ+4            STORE MAXIMUM LENGTH
         GETMAIN   VC,LA=GETMREQ,A=GETMADD  GETMAIN STORAGE
         LTR   R15,R15                 TEST RETURN CODE
         BZ    TCR429                  BR IF STORAGE ALLOCATED
         OI    ERRIND,ALLOCERR         SET ERROR INDICATOR
         MVI   MSG19,MSGREQ            SET MSG INDICATOR
         MVC   RETCODE,CODE16          SET RETURN CODE
TCR429   L     RETREG,SAVE400          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCRIN - DRIVER MODULE - DCB DEFAULT INSERTION'
*        DCB DEFAULT INSERTION
         SPACE 1
*        THIS ROUTINE INSERTS DEFAULTS FOR ANY DCB PARAMETERS
*        WHICH WERE NOT SUPPLIED BY THE USER VIA EITHER THE DSCB
*        OR THE JFCB.
*        THE ROUTINE CALLS TCR730 FOLLOWING INSERTION OF DEFAULTS
*        TO TEST FOR CONSISTENCY.
         SPACE 2
         DROP  BASEREG                  DROP ORIGINAL BASE REG
         USING *,BASE15                 INDICATE NEW BASE REG
TCR720   L     BASEREG,DRIVBASE        LOAD BASE REGISTER
         DROP  BASE15                  DROP NEW BASE REGISTER
         USING TCR050,BASEREG          INDICATE ORIGINAL BASE REG
         L     COMMREG,ATCRCOM         LOAD BASE REGISTER
         ST    R14,SAVE700             SAVE RETURN ADDRESS
         DROP  R2                      DROP LAST DCB DSECT REG
         USING IHADCB,R1               INDICATE NEW DCB DSECT REG
         L     R2,DCBEXLST             LOAD EXIT LIST ADDR
         LA    R2,0(R2)                CLEAR HIGH-ORDER BYTE
         L     R3,8(R2)                LOAD DEFAULT TABLE ADDRESS
         TM    DCBRECFM,ALLONE         TEST RECFM ZERO
         BNZ   TCR722                  BR NO
         MVC   DCBRECFM,4(R3)          MOVE RECFM DEFAULT
TCR722   CLC   DCBLRECL(2),ZEROES      TEST LRECL ZERO
         BNE   TCR724                  BR NO
         MVC   DCBLRECL(2),0(R3)       MOVE LRECL DEFAULT
         TM    DCBRECFM,UNDEF          TEST UNDEFINED
         BO    TCR724                  BR YES
         BAL   RETREG,TCR728           SET DEFAULT MSG INDICATORS
TCR724   CLC   DCBBLKSI(2),ZEROES      TEST BLKSIZE ZERO
         BNE   TCR726                  BR NO
         MVC   DCBBLKSI(2),2(R3)       MOVE BLKSIZE DEFAULT
         BAL   RETREG,TCR728           SET DEFAULT MSG INDICATORS
TCR726   BAL   RETREG,TCR730           COMPARE LRECL AND BLKSIZE
*                                      PARAMETERS
         L     R14,SAVE700             LOAD RETURN ADDRESS
         RETURN  RC=0                  RETURN
         SPACE 1
*        SET DEFAULT MESSAGE INDICATORS IF SYSUT2 OR SYSUT3
         SPACE 1
TCR728   CLC   5(1,R3),INIDENT         SYSIN
         BE    TCR729                  BR YES
         CLC   5(1,R3),PRTIDENT        SYSPRINT
         BE    TCR729                  BR YES
         MVI   MSG14,MSGREQ            SET REQUEST
         MVI   RETCODE,CODE04          SET RETURN CODE 04
         CLC   5(1,R3),UT2IDENT        SYSUT2
         BNE   TCR7283                 BR NO
         OI    MSG14+1,UT2             SET SYSUT2 INSERT
         B     TCR729                  BR TO RETURN
TCR7283  OI    MSG14+1,UT32            SET SYSUT3 INSERT
TCR729   BR    RETREG                  RETURN
  TITLE 'IEBTCRIN - DRIVER MODULE - DCB PARAMETER COMPARISON'
*        DCB PARAMETER COMPARISON
         SPACE 1
*        THIS ROUTINE TESTS THE DCB PARAMETERS FOR INCONSISTENCIES.
*        IF IT FINDS ANY INCONSISTENCY, IT SETS MSG AND ERROR
*        INDICATORS AND INSERTS DUMMY PARAMETERS IN THE FAULTY DCB
*        TO PREVENT THE OPEN ROUTINE FROM ABEND-ING.
         SPACE 2
TCR730   ST    RETREG,SAVE730          SAVE RETURN ADDRESS
         TM    DCBRECFM,VARFM          TEST VARIABLE
         BO    TCR732                  BR IF NO
         LH    R6,DCBLRECL             INCREMENT LRECL BY 4 BYTES
         LA    R6,4(R6)
         STH   R6,DCBWORK              STORE IN WORK AREA
         CLC   DCBBLKSI(2),DCBWORK     COMPARE BLKSIZE WITH LRECL
         BNL   TCR739                  BR BLKSIZE HIGH OR EQUAL
         B     TCR734                  SET ERROR INDICATORS
TCR732   TM    DCBRECFM,UNDEF          TEST UNDEFINED
         BO    TCR739                  BR IF YES
         LH    R7,DCBBLKSI             LOAD DIVIDEND WITH BLKSIZE
         XR    R6,R6
         LH    R8,DCBLRECL             LOAD DIVISOR WITH LRECL
         DR    R6,R8                   DIVIDE BLKSIZE BY LRECL
         LTR   R6,R6                   TEST REMAINDER
         BZ    TCR739                  BR IF ZERO
         SPACE 1
*        ERROR PROCESSING
         SPACE 1
TCR734   OI    ERRIND,CONFLERR         SET ERROR INDICATOR
         MVI   MSG16,MSGREQ            SET INCONSISTENT MSG POINTER
         MVC   MSG16+1(1),5(R3)        SET MSG INSERT
         MVI   RETCODE,CODE16          SET RETURN CODE 16
TCR736   MVI   DCBRECFM,DUMRECFM       SET DUMMY RECORD FORMAT
         MVC   DCBBLKSI,DUMLRBLK       SET DUMMY BLKSIZE
         MVC   DCBLRECL,DUMLRBLK       SET DUMMY LRECL
         CLC   5(1,R3),PRTIDENT        SYSPRINT DATASET
         BNE   TCR739                  BR NO
         OI    DUMMYIND,DUMSYSP        SET SYSPRINT DUMMY INDICATOR
TCR739   L     RETREG,SAVE730          LOAD RETURN ADDR
         BR    RETREG                  RETURN
  TITLE 'IEBTCRIN - DRIVER MODULE - QSAM SYNAD ROUTINE'
*        QSAM SYNAD ROUTINE
         SPACE 1
*        THIS ROUTINE ISSUES THE SYNADAF MACRO-INSTRUCTION FOR
*        THE SYSIN, SYSPRINT, SYSUT2, OR SYSUT3 DATA SET UPON
*        OCCURRENCE OF A PERMANENT I/O ERROR AND OUTPUTS THE
*        MESSAGE CONSTRUCTED.
         SPACE 2
         DROP  BASEREG                 DROP ORIGINAL BASE REG
         USING *,BASE15                INDICATE NEW BASE REG
TCR740   L     BASEREG,DRIVBASE        LOAD BASE REGISTER
         DROP  BASE15                  DROP NEW BASE REG
         USING TCR050,BASEREG          INDICATE ORIGINAL BASE REG
         L     COMMREG,ATCRCOM         LOAD COMM REGION ADDRESS
         ST    RETREG,SAVE740          SAVE RETURN ADDRESS
         TM    DUMMYIND,DUMSYSP        SYSPRINT DUMMY
         BO    TCR749                  BR YES
         SYNADAF  ACSMETH=QSAM         REQUEST SYNAD MESSAGE
         CLC   75(8,R1),NAMETAB+8      SYSPRINT DATA SET
         BNE   TCR7416                 BR NO
         ST    R1,SYNADSAV             SAVE SYNAD AREA ADDRESS
         OI    DUMMYIND,DUMSYSP        SET SYSPRINT DUMMY
         TM    PRTRTN,RTN310           SINGLE REQUEST
         BO    TCR7413                 BR YES
         L     R6,CURMSGAD             LOAD MSG WTO ADDRESS
         WTO   MF=(E,(R6))
TCR7413  L     R1,SYNADSAV             RESTORE SYNAD AREA POINTER
TCR7416  L     R3,PRTAREA              LOAD PRINTAREA ADDR
         CLC   DSUNIT(2,R1),SYNUR      TEST UR
         BNE   TCR742                  BR NO
         MVC   SYNIDENT(8,R1),SYNMSGUR
TCR742   CLC   DSUNIT(2,R1),SYNTA      TEST TA
         BNE   TCR744                  BR NO
         MVC   SYNIDENT(8,R1),SYNMSGTA MOVE IDENT
TCR744   CLC   DSUNIT(2,R1),SYNDA      TEST DA
         BNE   TCR746                  BR NO
         MVC   SYNIDENT(8,R1),SYNMSGDA MOVE IDENT
TCR746   LA    R4,SYNLNGTH             STORE MSG LENGTH
         STC   R4,MSGLN                STORE MSG LENGTH
         LA    R5,SYNIDENT(R1)         STORE MSG ADDRESS
         ST    R5,MSGAD                STORE MSG ADDRESS
TCR747   CLC   DSNAME(8,R1),NAMETAB+8  SYSPRINT DATA SET
         BNE   TCR7473                 BR NO
         L     R4,MSGADDR              LOAD MSG TABLE ADDRESS
         L     R6,SYNWTO(R4)           LOAD MSG ADDRESS
         LA    R6,0(R6)                CLEAR LENGTH INDICATOR
         EX    R4,TCR751               MOVE MESSAGE
         WTO   MF=(E,(R6))             ISSUE WTO
         B     TCR748                  BYPASS SYSPRINT PROC
TCR7473  OI    PRTRTN,RTN310           SET SINGLE MSG INDICATOR
         MVI   CARRCTRL,DOUBLESP       INDICATE DOUBLESPACING
         L     LNKREG15,ATCR220        LOAD MESSAGE WRITER ADDR
         BALR  RETREG,LNKREG15         DISPLAY MESSAGE
TCR748   SYNADRLS
TCR749   L     RETREG,SAVE740          LOAD RETURN ADDRESS
         OI    ERRIND,QSAMERR          SET QSAM ERROR INDICATOR
         MVI   RETCODE,CODE16          SET RETURN CODE 16
         RETURN  RC=16
TCR751   MVC   4(0,R6),0(R5)           MOVE MSG
  TITLE 'IEBTCRIN - DRIVER MODULE - STAE EXIT ROUTINE'
*        STAE EXIT ROUTINE
         SPACE 1
*        THIS ROUTINE IS SCHEDULED BY THE ABEND/STAE INTERFACE
*        ROUTINE WHENEVER AN ABEND IS SCHEDULED AND A VALID STAE
*        ENVIRONMENT IS IN EXISTENCE (WHEN A STAE CONTROL BLOCK
*        WAS SUCCESSFULLY CREATED).
         SPACE 1
*        THE ROUTINE CHECKS WHETHER OR NOT THE ABEND WAS CAUSED
*        BY ATTEMPTING TO LOAD A NON-EXISTENT USER ROUTINE.  IF
*        IT WAS, IT REQUESTS THAT A STAE RETRY ROUTINE BE SCHEDULED.
*        IF IT WAS CAUSED BY SOME OTHER ERROR, THE ROUTINE ALLOWS
*        ABEND PROCESSING TO CONTINUE.
         SPACE 2
         DROP  BASEREG                 DROP ORIGINAL BASE REG
         USING *,BASE15                INDICATE NEW BASE REG
TCR800   L     BASEREG,DRIVBASE        LOAD BASE REGISTER
         DROP  BASE15                  DROP NEW BASE REGISTER
         USING TCR050,BASEREG          INDICATE ORIGINAL BASE REG
         L     COMMREG,ATCRCOM         LOAD ADDR COMMUNICATION REGION
         LA    R3,CODE12               LOAD RETURN CODE 12
         ST    LNKREG0,SAVECODE        SAVE I/O ACTIVE CODE
         CLR   LNKREG0,R3              WORK AREA AVAILABLE
         BE    TCR802                  BR NO
         MVC   COMPCODE(2),5(PARMREG)  SAVE COMPLETION CODE
         B     TCR803                  BYPASS NO WORK AREA PROCESSING
TCR802   ST    PARMREG,COMPCODE        STORE COMPLETION CODE
         MVC   COMPCODE(2),COMPCODE+1  SHIFT CODE
TCR803   NI    COMPCODE+1,USERCLR      CLEAR USER CODE
         CLC   COMPCODE(2),CODE806     LOAD ERROR
         BNE   TCR808                  BR NO
TCR804   CLI   USERLIST,MSGMODL        MSG MODULE
         BE    TCR808                  BR YES
TCR806   LA    LNKREG15,CODE4          REQUEST STAE RETRY ROUTINE
         LA    LNKREG0,TCR810          LOAD RETRY ROUTINE ADDRESS
         BR    RETREG                  RETURN
TCR808   XR    LNKREG15,LNKREG15       ALLOW ABEND TO CONTINUE
         BR    RETREG                  RETURN
  TITLE 'IEBTCRIN - DRIVER MODULE - STAE RETRY ROUTINE'
*        STAE RETRY ROUTINE
         SPACE 2
*        THIS ROUTINE IS SCHEDULED WHENEVER THE STAE EXIT
*        ROUTINE DETERMINES THAT AN ABEND WAS CAUSED BY THE
*        FAILURE TO FIND A USER EXIT ROUTINE OR TRANSLATE TABLE.
         SPACE 1
*        IT SETS MESSAGE AND ERROR INDICATORS, RESTORES BASE
*        REGISTERS, AND RETURNS CONTROL TO THE INSTRUCTION FOLLOW-
*        ING THE ABEND-ING LOAD INSTRUCTION.  IT FREES THE STAE
*        WORKAREA IF ONE WAS PROVIDED AND RESTORES I/O USING THE
*        THE RESTORE MACRO-INSTRUCTION.
         SPACE 2
         DROP  BASEREG                 DROP ORIGINAL BASE REG
         USING *,BASE15                INDICATE NEW BASE REG
TCR810   L     BASEREG,DRIVBASE        LOAD  BASE REGISTER
         DROP  BASE15                  DROP NEW BASE REG
         USING TCR050,BASEREG          INDICATE ORIGINAL BASE REG
         ST    PARMREG,PARMADDR        STORE PARAMETER REG
         ST    LNKREG0,REG0SAVE        STORE REG 0
         LA    SAVEREG,SAVEAREA        LOAD SAVEAREA ADDRESS
         L     COMMREG,ATCRCOM         LOAD ADDR COMMUNICATION REGION
         CLI   REG0SAVE+3,CODE12       WORK AREA AVAILABLE
         BE    TCR8103                 BR NO
         L     R2,PARMADDR             LOAD WORKAREA ADDRESS
         L     PARMREG,IOBREST(R2)     LOAD RESTORE CHAIN POINTER
         B     TCR8106                 BYPASS NO WORK AREA PROC
TCR8103  ST    R2,SVRESTOR             SET UP IOB CHAIN POINTER
         LA    PARMREG,SVRESTOR        SET UP PARAMETER REGISTER
TCR8106  CLI   SAVECODE+3,CODE4        I/O NOT RESTORABLE
         BE    TCR8107                 BRANCH YES
         CLI   SAVECODE+3,CODE8        I/O NOT ACTIVE
         BE    TCR8107                 BRANCH YES
         RESTORE   (PARMREG)           RESTORE I/O
TCR8107  CLI   REG0SAVE+3,CODE12       WORK AREA AVAILABLE
         BE    TCR811                  BR NO
         L     PARMREG,PARMADDR        LOAD PARAMETER REGISTER
         FREEMAIN R,LV=104,A=(PARMREG)
TCR811   CLC   COMPCODE(2),CODE806     LOAD ERROR
         BE    TCR812                  BR YES
         B     TCR819                  BR TO RETURN
TCR812   MVI   MSG17,MSGREQ            SET MSGIND
         MVC   MSG17+1(1),USERLIST SET INSERT
         OI    ERRIND,LOADERR          SET ERROR INDICATOR
         MVI   RETCODE,CODE16          SET RETURN CODE 16
         B     TCR819                  BR TO RETURN
TCR819   L     RETREG,RETPT            LOAD RETURN POINT ADDRESS
         L     R12,DRIVBASE            LOAD DRIVER BASE REGISTER
         BR    RETREG                  RETURN
  TITLE 'IEBTCRIN - DRIVER MODULE - LITERALS AND STORAGE AREAS'
*        LITERALS
         SPACE 2
NULL     EQU   *                       NULL LABEL FOR COMPILATION
*                                      OF DCB'S
HEXSPACE EQU   X'40'                   HEX SPACE
NAMELN   EQU   8                       INSERT NAME LENGTH
NEWPAGE  EQU   X'F1'                   SKIP TO CHANNEL 1
SINGLESP EQU   X'40'                   SINGLE SPACE CONTROL CHARACTER
DOUBLESP EQU   X'F0'                   DOUBLE SPACE CONTROL CHARACTER
MSGREQ   EQU   X'FF'                   MESSAGE REQUIRED
INSREQ   EQU   X'FF'                   MESSAGE INSERT REQUIRED
VARFM    EQU   X'80'                   NOT VARIABLE IF ON
IN       EQU   X'01'                   SYSIN
PRT      EQU   X'02'                   SYSPRINT
INPRT    EQU   X'03'                   SYSPRINT OR SYSIN
EDITSPEC EQU   X'C0'                   EDIT SPECIFICATION
UT32     EQU   X'50'                   SYSUT3 INSERT
UT2      EQU   X'04'                   SYSUT2
UT3      EQU   X'05'                   SYSUT3
ALLONE   EQU   X'FF'                   ANY ERROR INDICATOR
STAEFAIL EQU   X'FF'                   STAE ENVIRONMENT NOT CREATED
MSGMODL  EQU   X'0D'                   MESSAGE MODULE
USERTAB  EQU   X'02'                   USER TABLE SPECIFIED
DEF120   EQU   X'78'                   MAXLN DEFAULT
SHIFTLN  EQU   1                       SHIFT NO.OF BITS
DDINCR   EQU   8                       DDNAME LENGTH
TABLNG   EQU   76                      MESSAGE POINTER TABLE LENGTH
DDLNGTH  EQU   8                       DDNAME TABLE INCREMENT
DEFBUFL  EQU   2000                    DEFAULT BUFFER LENGTH
PTRLNG   EQU   4                       MESSAGE POINTER TABLE
*                                      ENTRY LENGTH
DDON     EQU   X'01'                   ALTERNATE DDNAMES
RTN310   EQU   X'02'                   SINGLE MESSAGE INDICATOR
UNDEF    EQU   X'C0'                   UNDEFINED RECORD FORMAT
HDR2IND  EQU   6                       OUTHDR2 MSG INDICATOR
USERCLR  EQU   X'F0'                   USER COMPLETION CODE CLEAR
DUMRECFM EQU   X'90'                   DUMMY RECORD FORMAT
DCBOPEN  EQU   X'10'                   DCBOFLAGS OPEN INDICATOR
CODE4    EQU   4                       RETRY ROUTINE REQUEST
CODE8    EQU   8                       I/O NOT ACTIVE CODE
COMMA    EQU   C','                    COMMA
SYNLNGTH EQU   68                      SYNAD MESSAGE LENGTH
COMPMSG  EQU   84                      COMPLETION MESSAGE
HDGMSG   EQU   80                      HEADING MESSAGE
CARDLN   EQU   80                      CARD AREA LENGTH
IOBREST  EQU   100                     IOB RESTORE POINTER
SYNIDENT EQU   60                      SYNAD MSG IDENT
DSUNIT   EQU   72                      DEVICE TYPE
DSNAME   EQU   75                      DATA SET NAME
SYNWTO   EQU   76                      SYNAD MSG WTO LOCATION
INC63    EQU   63                      SIXTY-THREE
HEXZERO  EQU   X'00'                   HEXADECIMAL 0
HEX72    EQU   X'48'                   HEXADECIMAL 72
ONE      EQU   1                       ONE
THREE    EQU   3                       THREE
         EJECT
*        MESSAGE LOCATION DESCRIPTORS
         SPACE 1
MSGSYSP  EQU   X'01'                   MESSAGE OUTPUT TO SYSPRINT
MSGCONS  EQU   X'02'                   MESSAGE OUTPUT TO CONSOLE
MSGAVAIL EQU   X'04'                   MESSAGE OUTPUT TO SYSPRINT
*                                      IF AVAILABLE OTHERWISE TO
*                                      THE CONSOLE
         SPACE 1
*        RETURN CODES
         SPACE 1
CODE04   EQU   4                       CODE  04
CODE12   EQU   12                      CODE  12
CODE16   EQU   16                      CODE  16
         SPACE 2
*        PARM LIST DEFINITION
         SPACE 1
OPTLIST  EQU   0                       OPTION LIST
DDLIST   EQU   4                       DDNAME LIST
LASTPARM EQU   X'80'                   SIGN BIT ON
         SPACE 2
*        TERMINAL ERROR INDICATORS
         SPACE 1
LOADERR  EQU   X'01'                   ERROR IN LOADING USER ROUTINES
QSAMERR  EQU   X'02'                   PERMANENT QSAM I/O ERROR
ANALERR  EQU   X'04'                   ERROR IN CONTROL CD ANALYZER
RDEDERR  EQU   X'08'                   ERROR IN READ AND EDIT MODULE
JFCBERR  EQU   X'10'                   ERROR IN READING JFCB OR MOUNT
IORDERR  EQU   X'28'                   SYSUT1 READ ERROR
ALLOCERR EQU   X'40'                   STORAGE ALLOCATION ERROR
CONFLERR EQU   X'80'                   CONFLICTING DCB PARAMETERS
         SPACE 2
*        DCB MAXLN REQUIREMENTS
         SPACE 1
UT2LRLMX EQU   X'10'                   SYSUT2 LRECL   REQUIRES MAXLN
UT2BLKMX EQU   X'08'                   SYSUT2 BLKSIZE REQUIRES MAXLN
UT3LRLMX EQU   X'04'                   SYSUT3 LRECL   REQUIRES MAXLN
UT3BLKMX EQU   X'02'                   SYSUT3 BKSIZE  REQUIRES MAXLN
         SPACE 2
*        INPUT DDNAME LIST DEFINITION
         SPACE 1
DSYSIN   EQU   32                      SYSIN
DSYSPRT  EQU   40                      SYSPRINT
DSYSUT1  EQU   56                      SYSUT1
DSYSUT2  EQU   64                      SYSUT2
DSYSUT3  EQU   72                      SYSUT3
         SPACE 2
*        MISSING DD STATEMENT INDICATORS
         SPACE 1
DUMSYSIN EQU   X'01'                   SYSIN
DUMSYSP  EQU   X'02'                   SYSPRINT
         EJECT
*        CONSTANTS, HOLD AREAS
         SPACE 1
GETMREQ  DS    D                       MINIMUM & MAXIMUM SIZE REQUIRED
GETMADD  DS    D                       GETMAINED STORAGE ADDRESS
*                                        AND LENGTH
PARMADDR DC    F'0'                    PARAMETER REGISTER STORE
RETPT    DC    A(TCR165)               STAE RETRY RETURN POINT
ENTPT    DC    D'0'                    EXIT ROUTINE NAME
ATCRCOM  DC    A(TCRCOM)               COMMON COMMUNICATION REGION
*                                      ADDRESS
COMPCODE DC    C'    '                 ABEND COMPLETION CODE
CODE806  DC    X'8060'                 LOAD ERROR COMPLETION CODE
INDMSK   DC    X'0100'                 USER INDICATOR TABLE MASK
USERLIST DC    X'0000'                 STAE NAME TABLE POINTER
*                                      X'NNFF' INDICATES STAE NOT
*                                      IN EFFECT
DCBWORK  DC    X'0000'                 LRECL WORK AREA
LASTROUT DC    X'02'                   ALL USER ROUTINES LOADED
ZEROES   DC    X'0000000000000000'     ZEROES
         SPACE 1
SYNUR    DC    CL2'UR'                 UNIT RECORD SYNADAF
SYNTA    DC    CL2'TA'                 TAPE  SYNADAF
SYNDA    DC    CL2'DA'                 DIRECT ACCESS SYNADAF
         SPACE 2
*        THE FOLLOWING CONSTANTS MAY NEED TO BE REPLACED IF WORLD TRADE
*        REQUIREMENTS NECESSITATE MODIFICATION OF THE MESSAGE IDEN-
*        TIFIERS.
         SPACE 1
SYNMSGUR DC    CL8'IEB920I '           UNIT RECORD SYNAD MSG IDENT
SYNMSGTA DC    CL8'IEB921I '           TAPE SYNAD MSG IDENT
SYNMSGDA DC    CL8'IEB922I '           DIRECT ACCESS SYNAD MSG IDENT
         SPACE 1
         DS    0H                      FORCE HALF-WORD ALIGNMENT
MTDIDEFT DC    X'408C'                 MTDI DEFAULT
DUMLRBLK DC    X'0050'                 DUMMY LRECL AND BLKSIZE VALUES
         SPACE 2
*        SAVE AREAS
         SPACE 1
SAVE100  DS    F                       TCR1XX RETURN ADDRESS
SAVE400  DS    F                       TCR4XX RETURN ADDRESS
SAVE700  DS    F                       TCR700 RETURN ADDRESS
SAVE730  DS    F                       TCR730 RETURN ADDRESS
SAVE740  DS    F                       TCR740 RETURN ADDRESS
SYNADSAV DS    F                       SYNAD AREA SAVE ADDRESS
DRIVBASE DS    F                       DRIVER BASE REGISTER
REG0SAVE DS    F                       LNKREG0 SAVE AREA
SVRESTOR DS    F                       IOB RESTORE CHAIN
SAVEAREA DS    18F                     REGISTER SAVE AREA
SAVEPARM DC    F'0'                    PARAMETER REGISTER SAVE AREA
SAVECODE DC    F'0'                    I/O RESTORABLE CODE
  TITLE 'IEBTCRIN - DRIVER MODULE -- TCR220 - MESSAGE WRITER'
*        TCR220 - MESSAGE WRITER
         SPACE 3
*        THE MESSAGE WRITER OUTPUTS ALL MESSAGES FOR THE UTILITY.
*        IT CONTAINS TWO PROCESSING OPTIONS - 1) CONSECUTIVE
*        OUTPUTTING OF ALL IEB9XXX MESSAGES AND 2) SINGLE LINE
*        SYSPRINT OUTPUTTING OF CONTROL STATEMENT IMAGES, PAGE
*        HEADINGS, SYNAD MESSAGES, ETC.
         SPACE 1
*        PARAMETERS ARE AS FOLLOWS:
*
*        OPTION 1:  COMMON COMMUNICATION REGION TABLE MSGPTRS
*
*                   THIS TABLE CONTAINS ONE FULL-WORD ENTRY FOR
*                   EACH IEB-TYPE MESSAGE (EXCLUDING SYNAD MESSAGES).
*
*                   BYTE 1 - MESSAGE REQUEST CODE
*
*                            X'FF' - INDICATES REQUEST
*                            X'00' - OTHERWISE
*
*                   BYTE 2 - INSERT CODE
*
*                            X'00' - NO INSERT
*                            X'01' - SYSIN
*                            X'02' - SYSPRINT
*                            X'03' - SYSUT1
*                            X'04' - SYSUT2
*                            X'05' - SYSUT3
*                            X'06' - OUTHDR2
*                            X'07' - OUTTLR2
*                            X'08' - OUTHDR3
*                            X'09' - OUTTLR3
*                            X'0A' - OUTREC
*                            X'0B' - ERROR
*                            X'0C' - USERTAB
*                            X'54' - BOTH SYSUT2 AND SYSUT3
*
*                      NOTE:  IF X'54' IS SPECIFIED, THE MESSAGE IS
*                            OUTPUT TWICE, ONCE WITH SYSUT2 AND ONCE
*                            WITH SYSUT3.
*
*        OPTION 2:  COMMUNICATION REGION DATA AREAS:
*
*                       MSGAD - MESSAGE ADDRESS
*                       MSGLN - MESSAGE LENGTH
*
*                   COMMUNICATION REGION INDICATOR:
*
*                       PRTRTN - BIT 6 SET TO ONE INDICATES OPTION 2
*                                BIT 6 SET TO ZERO INDICATES OPTION 1
         SPACE 1
*        CALLING SEQUENCE:
*
*                   L     R15,ATCR220       LOAD CSECT ADDRESS
*                   BALR  R14,R15           BRANCH AND LINK
         SPACE 2
         SPACE 3
*        THIS SECTION SAVES THE CALLING MODULE'S GENERAL REGISTERS,
*        SETS UP A SAVEAREA ADDRESS, AND ESTABLISHES BASE REGISTERS
*        FOR THIS MODULE AND FOR THE COMMON COMMUNICATION REGION.
         SPACE 2
IEBTCRMW CSECT
         ENTRY TCR220                  MESSAGE WRITER ENTRY POINT
TCR220   SAVE  (14,12),T,TCR220        SAVE REGISTERS
         BALR  BASEREG,R0              ESTABLISH BASE REGISTER
         USING *,BASEREG               INDICATE BASE REGISTER
         LR    R15,SAVEREG             SET UP SAVEAREA POINTERS
         ST    R15,SAVEAR2+4
         LA    SAVEREG,SAVEAR2
         ST    SAVEREG,8(R15)
         XR    R15,R15
         ST    R15,0(SAVEREG)
         L     COMMREG,ATCRCOM1        LOAD COMMUNICATION REGION ADDR
         USING TCRCOM,COMMREG          INDICATE COMM. REG. BASE REG.
         EJECT
*        THIS SECTION LOOPS THROUGH THE MESSAGE POINTER TABLE.
*        FOR EACH MESSAGE REQUESTED, IT FINDS THE RELATED MESSAGE
*        TEXT IN THE MESSAGE MODULE, INSERTS ANY NAME REQUESTED,
*        AND DEVELOPS A MESSAGE LENGTH AND MESSAGE ADDRESS.
         SPACE 2
         TM    PRTRTN,RTN310           SINGLE MESSAGE REQUEST
         BO    TCR330                  BR YES
         SR    R3,R3                   CLEAR INSERT LOCATION REG
         LA    R7,MSGPTRS-PTRLNG       LOAD ADDR MSG PTR TABLE-4
         L     R4,MSGADDR              LOAD ADDR MSG TABLE
         S     R4,FOUR                 SET AT MSG TABLE-4
         LA    R8,PTRLNG               LOAD INCREMENT
         LA    R9,MSGPTRS+TABLNG       LOAD ENDING ADDRESS
TCR222   BXH   R7,R8,TCR228            BR WHEN ALL MSGS OUTPUT
         LA    R4,4(R4)                INCR VARIABLE TABLE ADDR
         TM    0(R7),MSGREQ            MESSAGE REQUIRED?
         MVI   0(R7),HEXZERO           CLEAR INDICATOR
         BNO   TCR222                  BR NO
         TM    1(R7),INSREQ            MSG INSERT REQUIRED
         BZ    TCR223                  BR NO
         TM    1(R7),TWOINS            TWO INSERTS REQUIRED
         BNO   TCR2225                 BR NO
         MVC   SAVEINS(1),1(R7)        SAVE INSERT
TCR2223  NI    1(R7),CLEARLFT          CLEAR LEFT FOUR BITS
TCR2225  L     R6,0(R4)                LOAD MSG ADDRESS
         IC    R3,2(R7)                INSERT MSG INSERT LOCATION
         AR    R6,R3                   ADD TO STARTING MSG ADDR
         IC    R3,1(R7)                LOAD INSERT NAME INDICATOR
         SH    R3,HFONE                DECREMENT BY ONE
         SLL   R3,THREE                MULTIPLY BY EIGHT
         LA    R2,NAMETAB              LOAD ADDR OF NAME TABLE
         AR    R2,R3                   SET REG AT NAME TO BE INSERTED
         MVC   4(NAMELN,R6),0(R2)      MOVE NAME TO MSG
TCR223   L     R6,0(R4)                LOAD MSG ADDRESS
         LA    R6,0(R6)                REMOVE LENGTH INDICATION
         ST    R6,CURMSGAD             STORE WTO ADDRESS
         LH    R2,0(R6)                LOAD LENGTH
         S     R2,FOUR                 REMOVE LENGTH OF CODES
         EJECT
*        THIS SECTION DETERMINES THE OUTPUT LOCATION(S) FOR THE
*        MESSAGE AND BAL'S TO THE PROPER ROUTINE(S).
         SPACE 2
         TM    3(R7),MSGSYSP           SYSPRINT REQUIRED?
         BNO   TCR224                  BR IF NO
         TM    DUMMYIND,DUMSYSP        SYSPRINT DUMMY?
         BO    TCR224                  BR YES
         LA    R6,4(R6)                BYPASS CODES
         BAL   RETREG,TCR310           DISPLAY ON SYSPRINT
         S     R6,FOUR                 SET AT WTO START
TCR224   TM    3(R7),MSGCONS           CONSOLE REQUIRED?
         BNO   TCR225                  BR - NO
         BAL   RETREG,TCR320           DISPLAY ON CONSOLE
         SPACE 1
*        DISPLAY ON CONSOLE IF BIT 5 OF THE LOCATION CODE IS SET TO
*        ONE AND SYSPRINT IS DUMMY.  DISPLAY ON SYSPRINT IF NOT DUMMY.
         SPACE 1
TCR225   TM    3(R7),MSGAVAIL          CONSOLE IF SYSPRINT DUMMY?
         BNO   TCR227                  BR IF NO
TCR2253  TM    DUMMYIND,DUMSYSP        SYSPRINT DUMMY
         BO    TCR226                  BR YES
         LA    R6,4(R6)                BYPASS CODES
         BAL   RETREG,TCR310           DISPLAY ON SYSPRINT
         S     R6,FOUR                 SET AT WTO START
         B     TCR227                  BYPASS CONSOLE ROUTINE
TCR226   BAL   RETREG,TCR320           DISPLAY ON CONSOLE
TCR227   CLI   SAVEINS,HEXZERO         TWO INSERTS
         BE    TCR222                  BR NO
         PACK  SAVEINS(1),SAVEINS      SWITCH INSERT DIGITS
         MVC   1(1,R7),SAVEINS         SET  IN MSG POINTER
         MVI   SAVEINS,HEXZERO         CLEAR SAVE AREA
         B     TCR2223                 BR TO CONSTRUCT 2ND MSG
         SPACE 3
*        THIS SECTION RELOADS THE CALLING MODULE'S REGISTERS
*        AND RETURNS TO THE CALLER
         SPACE 1
TCR228   NI    PRTRTN,HEXZERO          CLEAR OPTION INDICATOR
         L     SAVEREG,4(SAVEREG)      RELOAD SAVEAREA ADDRESS
         RETURN  (14,12),T,RC=(15)     RELOAD REGISTERS AND RETURN
         EJECT
*        SYSPRINT ROUTINE
         SPACE 1
*        THIS ROUTINE DEVELOPS RECORDS FOR THE SYSPRINT DATA SET.
*        IF THE MESSAGE IS LONGER THAN THE SPACE AVAILABLE WITH THE
*        SPECIFIED DCB PARAMETERS, IT IS TRUNCATED TO FIT IN THE
*        SPACE AVAILABLE.
         SPACE 1
*        PARAMETERS ARE AS FOLLOWS:
*
*                REGISTER 2 - MESSAGE LENGTH
*                REGISTER 6 - MESSAGE ADDRESS
         SPACE 2
TCR310   ST    RETREG,SAVE300          SAVE RETURN ADDRESS
         LA    R1,SYSPRDCB             LOAD SYSPRINT DCB ADDRESS
         USING IHADCB,R1               INDICATE DCB DSECT REG
         L     R10,PRTAREA             LOAD ADDR PRTAREA
         LH    R5,DCBLRECL             LOAD SYSPRINT LRECL
         TM    DCBRECFM,VARFM          TEST VARIABLE LENGTH RECORDS
         BO    TCR312                  BR NO
         SPACE 1
*        DEVELOP LENGTH FOR VARIABLE-LENGTH RECORDS
         SPACE 1
         SH    R5,HFIVE                REMOVE COUNT AND CTRL CHAR LNGTH
         CLR   R5,R2                   COMPARE MAX AVAILABLE TO MSG LNG
         BH    TCR311                  BR HIGH
         SPACE 1
*        DEVELOP LENGTH FOR THOSE VARIABLE-LENGTH MESSAGES WHICH
*        ARE LONGER THAN THE SPECIFIED LRECL VALUE
         SPACE 1
         LR    R3,R5                   SET MSG LENGTH EQUAL TO LRECL-5
         LA    R5,5(R5)                ADD BACK
         STH   R5,0(R10)               STORE COUNT IN SYSPRINT AREA
         SH    R5,HFIVE                REMOVE COUNT AND CTRL CHAR LNGTH
         B     TCR315                  BR TO OUTPUT THE MSG
         SPACE 1
*        THIS SECTION DEVELOPS A LENGTH FOR THOSE VARIABLE-LENGTH
*        MESSAGES WHICH ARE SHORTER THAN OR EQUAL TO THE ALLOWABLE
*        LENGTH.
         SPACE 1
TCR311   LR    R3,R2                   LOAD MSG LENGTH
         LA    R2,5(R2)                ADD 5 BYTES TO MSG LENGTH
         STH   R2,0(R10)               STORE COUNT IN SYSPRINT AREA
         B     TCR315                  BR TO OUTPUT THE MSG
         SPACE 1
*        DEVELOP ALLOWABLE MESSAGE LENGTH FOR FIXED-LENGTH AND
*        UNDEFINED RECORDS.
         SPACE 1
TCR312   SH    R5,HFONE                REMOVE CTRL CHAR COUNT
         CLR   R5,R2                   COMPARE MAX AVAILABLE TO MSG LNG
         BH    TCR313                  BR HIGH
         LR    R3,R5                   SET MSG LENGTH EQUAL TO LRECL-1
         B     TCR315                  BR TO OUTPUT THE MSG
         EJECT
*        THIS SECTION CLEARS THE PRINT AREA, MOVES THE MESSAGE TO
*        THE PRINT AREA, ENTERS A CARRIAGE CONTROL CHARACTER, AND,
*        IF THE DATA SET HAS BEEN SPECIFIED AS VARIABLE-LENGTH,
*        AFFIXES THE VARIABLE-LENGTH RECORD COUNT.
         SPACE 1
TCR313   LR    R3,R2                   LOAD MSG LENGTH
TCR315   MVI   4(R10),HEXSPACE         MOVE SPACE TO PRINT AREA
         SH    R5,HFONE                DEVELOP CLEAR LENGTH
         SH    R3,HFONE                DEVELOP MOVE LENGTH
         EX    R5,TCR3183              CLEAR PRINT AREA
         EX    R3,TCR318               LOAD MSG
         MVC   4(1,R10),CARRCTRL       MOVE TO PRINT AREA
         TM    SYSPRDCB+36,VARFM       TEST VARIABLE LENGTH RECORDS
         BNO   TCR316                  BR YES
         LA    R10,4(R10)              BYPASS VARIABLE COUNT
         SPACE 1
*        THIS SECTION OUTPUTS THE MESSAGE AND RESTORES THE CARRIAGE
*        CONTROL TO SINGLE-SPACING.
         SPACE 1
TCR316   PUT   SYSPRDCB,(R10)          WRITE MSG
         MVI   CARRCTRL,SINGLESP       RESET CARRIAGE CONTROL TO
*                                        SINGLE SPACE
         L     RETREG,SAVE300          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
         SPACE 1
TCR318   MVC   5(0,R10),0(R6)          MOVE MSG
         SPACE 1
TCR3183  MVC   5(0,R10),4(R10)         CLEAR PRINT AREA
         EJECT
*        CONSOLE DISPLAY ROUTINE
         SPACE 2
TCR320   ST    RETREG,SAVE300          SAVE RETURN ADDRESS
         WTO   MF=(E,(R6))             ISSUE WTO
TCR329   L     RETREG,SAVE300          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
         SPACE 5
*        SINGLE MESSAGE REQUEST INTERFACE
         SPACE 2
TCR330   TM    DUMMYIND,DUMSYSP        SYSPRINT DUMMY
         BO    TCR228                  BR YES
         XR    R2,R2                   CLEAR WORK REGISTER
         IC    R2,MSGLN                INSERT MSG LENGTH
         L     R6,MSGAD                LOAD MSG ADDRESS
         BAL   RETREG,TCR310           DISPLAY ON SYSPRINT
         B     TCR228                  BR TO RETURN
         EJECT
*        LITERALS
         SPACE 2
TWOINS   EQU   X'40'                   TWO MSG INSERTS REQUIRED
CLEARLFT EQU   X'0F'                   INSERT CLEAR
         SPACE 2
*        DATA CONSTANTS AND STORAGE AREAS
         SPACE 1
SAVEAR2  DS    18F                     REGISTER SAVE AREA
ATCRCOM1 DC    A(TCRCOM)               COMMUNICATION REGION ADDRESS
SAVE300  DC    F'0'                    TCR3XX SAVE AREA
FOUR     DC    F'04'                   BINARY FULL-WORD FOUR
HFONE    DC    X'0001'                 BINARY HALF-WORD ONE
HFIVE    DC    X'0005'                 BINARY HALF-WORD FIVE
SAVEINS  DC    X'00'                   MSG INSERT SAVE AREA
  TITLE 'IEBTCRIN - DRIVER MODULE -- TCRCOM - COMM. COMMUNICATION REG.'
*        COMMON COMMUNICATION REGION
         SPACE 1
*        THIS CSECT CONTAINS TABLES, INDICATORS, CONSTANTS, WORK
*        AREAS, AND DCB'S THAT ARE USED BY TWO OR MORE MODULES.
         SPACE 2
TCRCOM   CSECT
         SPACE 1
         DS    0F                      FORCE FULL-WORD ALIGNMENT
OPTABLE  DS    0CL8                    CONTROL CARD OPTION TABLE
         SPACE 1
IEXITS   DC    X'00'                   USER ROUTINE INDICATORS
         SPACE 1
*              BIT 0 - OUTHDR2
*                  1 - OUTTLR2
*                  2 - OUTHDR3
*                  3 - OUTTLR3
*                  4 - OUTREC
*                  5 - ERROR
*                  6 - USERTAB
*                  7 - NOT USED
         SPACE 1
IPARM1   DC    X'00'                   TYPE AND TRANS INDICATORS
         SPACE 1
*              BIT 0 - TYPE = MTST
*                  1 - TYPE = MTDI
*                  2 - UNUSED
*                  3 - TRANS = STDUC
*                  4 - TRANS = STDLC
*                  5 - TRANS = USERNAME
*                  6 - TRANS = NOTRAN
*                  7 - NOT USED
         SPACE 1
IPARM2   DC    X'00'                   EDIT, VERCHK, AND ERROPT
*                                        INDICATORS
         SPACE 1
*              BIT 0 - EDIT = EDITD
*                  1 - EDIT = EDITR
*                  2 - EDIT = NOEDIT
*                  3 - VERCHK = VOKCHK
*                  4 - VERCHK = NOCHK
*                  5 - ERROPT = NORMAL
*                  6 - ERROPT = NOERR
*                  7 - UNUSED
         SPACE 1
REPL     DC    X'19'                   REPLACE PARAMETER VALUE
MINLN    DC    X'0000'                 MINLN PARAMETER VALUE
MAXLN    DC    X'0000'                 MAXLN PARAMETER VALUE
         EJECT
ADDRTAB  DS    0CL28                   USER ROUTINE ADDRESS TABLE
USERADDR DC    A(0,0,0,0,0,0,0)        TABLE
         SPACE 1
*              WORD 0 - OUTHDR2
*                   1 - OUTTLR2
*                   2 - OUTHDR3
*                   3 - OUTTLR3
*                   4 - OUTREC
*                   5 - ERROR
*                   6 - TRANSLATE TABLE
         SPACE 2
MSGADDR  DC    A(0)                    MESSAGE MODULE ADDRESS
         SPACE 2
*        NAME TABLE
         SPACE 1
*        THIS TABLE CONTAINS DDNAMES AND USER EXIT NAMES
         SPACE 1
NAMETAB  DC    CL8'SYSIN',CL8'SYSPRINT',CL8'SYSUT1',CL8'SYSUT2'
         DC    CL8'SYSUT3',CL8'OUTHDR2',CL8'OUTTLR2'
         DC    CL8'OUTHDR3',CL8'OUTTLR3',CL8'OUTREC',CL8'ERROR'
         DC    CL8'TRANS'
         SPACE 1
*              WORDS 0-1   SYSIN
*                    2-3   SYSPRINT
*                    4-5   SYSUT1
*                    6-7   SYSUT2
*                    8-9   SYSUT3
*                   10-11  OUTHDR2
*                   12-13  OUTTLR2
*                   14-15  OUTHDR3
*                   16-17  OUTTLR3
*                   18-19  OUTREC
*                   20-21  ERROR
*                   22-23  TRANS
         EJECT
*        MESSAGE POINTER TABLE
         SPACE 2
*        THIS TABLE CONTAINS ONE FULL-WORD ENTRY FOR EACH IEB-TYPE
*        MESSAGE (EXCLUDING SYNAD MESSAGES).  MSG01 CORRESPONDS
*        TO IEB901; MSG02 CORRESPONDS TO IEB902; ETC.
         SPACE 1
*              BYTE  1 - MESSAGE REQUEST CODE
*
*                        X'FF' = INDICATES REQUEST
*                        X'00' = NO REQUEST
*
*              BYTE  2 - INSERT CODE
*
*                        X'00' - NO INSERT
*                        X'01' - SYSIN
*                        X'02' - SYSPRINT
*                        X'03' - SYSUT1
*                        X'04' - SYSUT2
*                        X'05' - SYSUT3
*                        X'06' - OUTHDR2
*                        X'07' - OUTTLR2
*                        X'08' - OUTHDR3
*                        X'09' - OUTTLR3
*                        X'0A' - OUTREC
*                        X'0B' - ERROR
*                        X'0C' - USERTAB
*                        X'54' - BOTH SYSUT2 AND SYSUT3
*
*                    NOTE:  IF X'54' IS SPECIFIED, THE MESSAGE IS
*                        OUTPUT TWICE, ONCE WITH SYSUT2 AND ONCE
*                        WITH SYSUT3.
*
*              BYTE  3 - INSERT LOCATION  (MESSAGE DISPLACEMENT)
*
*                    NOTE:  IF WORLD TRADE REQUIREMENTS NECESSITATE
*                        MODIFICATION OF THE MESSAGE TEXT CONTAINED
*                        IN THE MESSAGE MODULE (IEBTCR02), THESE VALUES
*                        MAY ALSO REQUIRE MODIFICATION.  THE DISPLACE-
*                        MENTS ARE GIVEN IN HEXADECIMAL REPRESENTATION
*                        AND REFERENCE THE START OF THE ACTUAL MESSAGE
*                        TEXT.
*
*                        EXAMPLE:  IF MESSAGE 10 WERE AS FOLLOWS:
*                        'IEB910I NO SYSUT1   DD CARD - JOB TERMINA-
*                        TED', THE 'I' OF 'IEB910' WOULD REPRESENT
*                        DISPLACEMENT 0 AND THE FIRST 'S' OF 'SYSUT1'
*                        WOULD REPRESENT DISPLACEMENT '11'.
*
*
*              BYTE  4 - OUTPUT LOCATION
*
*                        BIT 0 - UNUSED
*                            1 - UNUSED
*                            2 - UNUSED
*                            3 - UNUSED
*                            4 - UNUSED
*                            5 - SYSPRINT IF AVAILABLE, CONSOLE
*                                OTHERWISE
*                            6 - CONSOLE
*                            7 - SYSPRINT
*
*                        NOTE - BITS 6 AND 7 MAY BE ON SIMULTANEOUSLY
         SPACE 1
MSGPTRS  EQU   *                       ASSIGN MSGPTRS NAME TO TABLE
MSG01    DC    X'00000002'
MSG02    DC    X'00000001'
MSG03    DC    X'00000001'
MSG04    DC    X'00000001'
MSG05    DC    X'00000001'
MSG06    DC    X'00000001'
MSG07    DC    X'00000001'
MSG08    DC    X'00000001'
MSG09    DC    X'00000001'
MSG10    DC    X'00030B01'             INSERT LOC = 11
MSG11    DC    X'00010B01'             INSERT LOC = 11
MSG12    DC    X'00020B02'             INSERT LOC = 11
MSG13    DC    X'00000B01'             INSERT LOC = 11
MSG14    DC    X'00002501'             INSERT LOC = 37
MSG15    DC    X'00000F04'             INSERT LOC = 15
MSG16    DC    X'00001504'             INSERT LOC = 21
MSG17    DC    X'00002201'             INSERT LOC = 34
MSG18    DC    X'00001D01'             INSERT LOC = 29
MSG19    DC    X'00000004'
MSG20    DC    X'00000002'
         SPACE 3
MOUNTID  DS    F                       MOUNT MSG ID
IOBADDR  DS    F                       SYSUT1 IOB ADDRESS
TCRBUFL  DC    X'0000'                 TCR BUFFER LENGTH
         SPACE 1
ERRIND   DC    X'00'                   ABNORMAL TERMINATION INDICATORS
         SPACE 1
*              BIT 0 - CONFLICTING DCB PARAMETERS
*                  1 - STORAGE ALLOCATION ERROR
*                  2 - I/O ERROR IN READ AND EDIT
*                  3 - ERROR IN JFCB READ OR MOUNT
*                  4 - ERROR DURING READ AND EDIT
*                  5 - ERROR IN ANALYZING CONTROL CARDS
*                  6 - PERMANENT I/O ERROR ON QSAM GET/PUT
*                  7 - ERROR IN LOADING UTILITY MODULE
         SPACE 1
DUMMYIND DC    X'00'                   DUMMY FILE INDICATORS
         SPACE 1
*              BIT 0 - UNUSED
*                  1 - UNUSED
*                  2 - UNUSED
*                  3 - UNUSED
*                  4 - SYSUT3
*                  5 - SYSUT2
*                  6 - SYSPRINT
*                  7 - SYSIN
         EJECT
*        DATA CONTROL BLOCK (DCB) EXIT LISTS
         SPACE 2
*        SYSIN EXIT LIST
         SPACE 1
         DS    0F                      FORCE FULL-WORD ALIGNMENT
INEXLST  DC    X'07'                   JFCB EXIT
         DC    AL3(0)                  WORK AREA ADDRESS
         DC    X'85'                   DCB EXIT
         DC    AL3(TCR720)             DCB EXIT ROUTINE
         DC    X'00'                   DUMMY ENTRY
         DC    AL3(INLRECL)            DEFAULT TABLE ADDRESS
         SPACE 2
*        SYSPRINT EXIT LIST
         SPACE 1
PREXLST  DC    X'07'                   JFCB EXIT
         DC    AL3(0)                  WORK AREA ADDRESS
         DC    X'05'                   DCB EXIT
         DC    AL3(TCR720)             DCB EXIT ROUTINE
         DC    X'00'                   DUMMY ENTRY
         DC    AL3(PRTLRECL)           DEFAULT TABLE ADDRESS
         DC    X'00'                   OUTPUT HEADER LABEL (NULL)
         DC    AL3(0)                  NULL ENTRY
         DC    X'80'                   OUTPUT TRAILER LABEL (NULL)
         DC    AL3(0)                  NULL ADDRESS
         SPACE 2
*        SYSUT1 EXIT LIST
         SPACE 1
UT1EXLST DC    X'87'                   JFCB EXIT
         DC    AL3(0)                  NULL ADDRESS
         SPACE 2
*        SYSUT2 EXIT LIST
         SPACE 1
         DS    0F                      FORCE FULL-WORD ALIGNMENT
UT2EXLST DC    X'07'                   JFCB EXIT
         DC    AL3(0)                  WORK AREA ADDRESS
         DC    X'05'                   DCB EXIT
         DC    AL3(TCR720)             DCB EXIT ROUTINE
         DC    X'00'                   DUMMY ENTRY
         DC    AL3(UT2LRECL)           DEFAULT TABLE ADDRESS
         DC    X'00'                   OUTPUT HEADER LABEL
         DC    AL3(0)                  NULL ADDRESS
         DC    X'80'                   OUTPUT TRAILER LABEL
         DC    AL3(0)                  NULL ADDRESS
         SPACE 2
*        SYSUT3 EXIT LIST
         SPACE 1
UT3EXLST DC    X'07'                   JFCB EXIT
         DC    AL3(0)                  WORK AREA ADDRESS
         DC    X'05'                   DCB EXIT
         DC    AL3(TCR720)             DCB EXIT ROUTINE ADDR
         DC    X'00'                   DUMMY ENTRY
         DC    AL3(UT3LRECL)           DEFAULT TABLE ADDRESS
         DC    X'00'                   OUTPUT HEADER LABEL
         DC    AL3(0)                  NULL ADDRESS
         DC    X'80'                   OUTPUT TRAILER LABEL
         DC    AL3(0)                  NULL ADDRESS
         SPACE 2
*        DCB PARAMETER DEFAULTS
         SPACE 1
INLRECL  DC    X'0050'                 SYSIN LRECL  - 80
INBLK    DC    X'0050'                 SYSIN BLKSIZE - 80
INRECFM  DC    X'90'                   SYSIN - FIXED BLOCKED
INIDENT  DC    X'01'                   INTERNAL IDENTIFICATION CODE
         SPACE 1
PRTLRECL DC    X'0079'                 SYSPRINT LRECL -  121
PRTBLK   DC    X'0079'                 SYSPRINT BLKSIZE - 121
PRTRECFM DC    X'94'                   SYSPRINT - FIXED BLOCKED ASA
PRTIDENT DC    X'02'                   INTERNAL IDENTIFICATION CODE
         SPACE 1
UT2LRECL DC    X'0000'                 SYSUT2 LRECL
UT2BLK   DC    X'0000'                 SYSUT2 BLKSIZE
UT2RECFM DC    X'50'                   SYSUT2 RECFM - VARIABLE BLOCKED
UT2IDENT DC    X'04'                   INTERNAL IDENTIFICATION CODE
         SPACE 1
UT3LRECL DC    X'0000'                 SYSUT3 LRECL
UT3BLK   DC    X'0000'                 SYSUT3 BLKSIZE
UT3RECFM DC    X'50'                   SYSUT3 RECFM - VARIABLE BLOCKED
UT3IDENT DC    X'05'                   INTERNAL IDENTIFICATION CODE
         EJECT
*        DATA CONTROL BLOCKS (DCB'S)
         SPACE 3
*        SYSUT1 (TCR) DCB
         SPACE 2
TCRDCB   DCB   DSORG=PS,MACRF=(E),DDNAME=SYSUT1,DEVD=TA,RECFM=UA,      X
               BFTEK=S,EXLST=UT1EXLST,IOBAD=NULL
         EJECT
*        SYSUT2 DCB
         SPACE 2
UT2DCB   DCB   DSORG=PS,DDNAME=SYSUT2,DEVD=DA,EXLST=UT2EXLST,          X
               SYNAD=TCR740,EROPT=ABE,MACRF=(PM)
         SPACE 3
*        SYSUT3 DCB
         SPACE 2
UT3DCB   DCB   DSORG=PS,DDNAME=SYSUT3,DEVD=DA,EXLST=UT3EXLST,          X
               EROPT=ABE,SYNAD=TCR740,MACRF=(PM)
         SPACE 3
*        SYSIN DCB
         SPACE 2
SYSINDCB DCB   DSORG=PS,DDNAME=SYSIN,DEVD=DA,EODAD=NULL,MACRF=(GM),    X
               EXLST=INEXLST,EROPT=ACC,SYNAD=TCR740
         SPACE 3
*        SYSPRINT DCB
         SPACE 2
SYSPRDCB DCB   DSORG=PS,DDNAME=SYSPRINT,DEVD=DA,EXLST=PREXLST,         X
               MACRF=(PM)
         EJECT
*        GETMAINED STORAGE ADDRESSES
         SPACE 2
CARDAREA DS    F                       CARD INPUT AREA
JFCBAREA DS    F                       JFCB AREA ADDRESS
PRTAREA  DS    F                       SYSPRINT OUTPUT AREA
PRTLNGTH DS    F                       PRINT AREA LENGTH
COLUMN9  DS    F                       CARD INPUT COLUMN 9 ADDRESS
CARDEND  DS    F                       INPUT AREA ENDING ADDRESS
         SPACE 2
USERNAME DC    7XL8'00'                USER ROUTINE NAME TABLE
           SPACE 1
*              WORDS 0 & 1  - OUTHDR2 ROUTINE
*                    2 & 3  - OUTTLR2 ROUTINE
*                    4 & 5  - OUTHDR3 ROUTINE
*                    6 & 7  - OUTTLR3 ROUTINE
*                    8 & 9  - OUTREC  ROUTINE
*                   10 & 11 - ERROR ROUTINE
*                   12 & 13 - TRANSLATE TABLE
           SPACE 2
MISCIND  DC    X'00'                   MISCELLANEOUS INDICATORS
         SPACE 1
*              BIT 0 - UNUSED
*                  1 - UNUSED
*                  2 - UNUSED
*                  3 - SYSUT2 LRECL REQUIRES MAXLN INCREMENT
*                  4 - SYSUT2 BLKSIZE REQUIRES MAXLN INCREMENT
*                  5 - SYSUT3 LRECL REQUIRES MAXLN INCREMENT
*                  6 - SYSUT3 BLKSIZE REQUIRES MAXLN INCREMENT
*                  7 - ALTERNATE DDNAMES FOUND
             SPACE 2
         SPACE 1
RETCODE  DC    X'00'                   RETURN CODE
TCRDEV   DC    CL3'000'                TCR DEVICE
VOLSER   DC    CL6' '                  VOLUME SERIAL NUMBER
PRTRTN   DC    X'00'                   MESSAGE WRITER OPTION IND
CARRCTRL DC    X'40'                   CARRIAGE CONTROL
         SPACE 1
         DS    0F                      FORCE FULL-WORD ALIGNMENT
ATCR220  DC    A(TCR220)               MESSAGE DISPLAY
CURMSGAD DS    F                       CURRENT SYSPRINT MSG ADDRESS
MSGAD    DS    F                       SINGLE MESSAGE ADDRESS
MSGLN    DS    F                       SINGLE MSG LENGTH
JOBNAME  DC    CL8' '                  JOB NAME
STEPNAME DC    CL8' '                  STEPNAME
  TITLE 'IEBTCRIN - DRIVER MODULE - IHADCB DSECT'
         DCBD  DSORG=PS
         END   IEBTCRIN
