  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - INTRODUCTORY COMMENTARY'  *
*                                                                     *
*TITLE 'IEBTCR04' - CONTROL CARD ANALYZER                             *
*                                                                     *
*STATUS:  CHANGE LEVEL 000                                            *
*                                                                     *
*FUNCTION/OPERATION:  THE CONTROL CARD ANALYZER PERFORMS THE          *
*   FOLLOWING FUNCTIONS:                                              *
*                                                                     *
*   1)  READS UTILITY CONTROL STATEMENTS INPUT VIA THE SYSIN          *
*       DATA SET                                                      *
*                                                                     *
*   2)  SCANS THE CONTROL CARDS FOR FORMAT ERRORS                     *
*                                                                     *
*   3)  VALIDATES THE NAME, OPERATION, AND OPERAND FIELDS OF EACH     *
*       STATEMENT                                                     *
*                                                                     *
*   4)  SETS OPTIONS (INCLUDING DEFAULTS FOR MISSING PARAMETERS)      *
*                                                                     *
*   5)  TESTS THE CONSISTENCY OF THE OPTIONS THAT IT HAS JUST SET     *
*                                                                     *
*   THE CONTROL CARD ANALYZER PROCESSES EACH CONTROL STATEMENT ONE    *
*   CHARACTER AT A TIME.  IT ASSEMBLES EACH OPERATOR, KEYWORD, OR     *
*   PARAMETER IN AN 8-POSITION HOLD AREA.  WHEN A COMPLETE WORD HAS   *
*   BEEN ASSEMBLED, THE ANALYZER PERFORMS ONE OF THE FOLLOWING SETS   *
*   OF OPERATIONS, DEPENDING UPON THE CLASS IN WHICH THE WORD FALLS:  *
*                                                                     *
*   NAME FIELD - CHECKS THAT THE FIRST CHARACTER IS ALPHABETIC        *
*                                                                     *
*   OPERATOR - COMPARES THE WORD TO THE IDENTIFIER ATTACHED TO EACH   *
*        OPERATOR TABLE (TCRGEN OR EXITS). IF A MATCH IS FOUND,       *
*        POINTERS ARE SET TO EACH OF THE TABLES CORRESPONDING TO THIS *
*        OPERATOR. (SEE TABLES/WORK AREAS.)                           *
*                                                                     *
*   KEYWORD - COMPARES THE WORD TO EACH KEYWORD ENTRY OF THE SELECTED *
*        OPERATOR TABLE.  IF A MATCH IS FOUND, POINTERS ARE SET TO THE*
*        LIST OF PARAMETERS CORRESPONDING TO THIS KEYWORD.            *
*                                                                     *
*   PARAMETER - COMPARES THE WORD TO EACH ENTRY OF THE PARAMETER LIST *
*        ASSOCIATED WITH THE PREVIOUSLY IDENTIFIED OPERATOR AND KEY-  *
*        WORD.  IF A MATCH IS FOUND, THE CORRESPONDING DATA TABLE     *
*        ENTRIES ARE USED TO SET THE PROPER OPTION BYTES. (SEE        *
*        TABLES/WORK AREAS.)                                          *
*                                                                     *
*   WHEN ALL STATEMENTS HAVE BEEN PROCESSED, THE ANALYZER SETS        *
*   DEFAULTS FOR THOSE OPTIONS WHICH WERE NOT SPECIFIED AND TESTS     *
*   THE CONSISTENCY OF ALL APPLICABLE OPTIONS.  IF ANY CONFLICTING    *
*   OPTIONS ARE DETECTED, THE ANALYZER SETS THE RETURN CODE TO 12     *
*   AND REQUESTS TERMINATION.                                         *
*                                                                     *
         EJECT                                                        *
*ENTRY POINTS:                                                        *
*                                                                     *
*        IEBTCR04 - ANALYZE CONTROL CARDS AND SET OPTION INDICATORS   *
*                                                                     *
*   THIS MODULE IS LINKED  TO BY THE DRIVER MODULE (IEBTCRIN):        *
*                                                                     *
*        LINK EP=IEBTCR04,PARAM=TCRCOM                                *
*                                                                     *
*   PARAMETER DATA IS PASSED BY NAME VIA THE COMMON COMMUNICATION     *
*   REGION AND CONSISTS OF CONTROL CARD OPTION TABLES, A MESSAGE      *
*   REQUEST TABLE, DATA CONTROL BLOCKS, ADDRESS CONSTANTS, DATA       *
*   CONSTANTS, AND INDICATORS.                                        *
*                                                                     *
*INPUT:  SYSIN DATA SET.  THIS DATA SET IS OPENED AND CLOSED BY THE   *
*   DRIVER MODULE (IEBTCRIN).  INPUT IS PERFORMED BY THE CONTROL CARD *
*   ANALYZER THROUGH USE OF THE QSAM ACCESS METHOD.                   *
*                                                                     *
*OUTPUT:                                                              *
*                                                                     *
*   1)  UPDATED CONTROL CARD OPTION TABLES (WHICH ARE CONTAINED       *
*       IN THE COMMON COMMUNICATION REGION).                          *
*                                                                     *
*   2)  UPDATED MESSAGE REQUEST TABLE (CONTAINED IN THE COMMON        *
*       COMMUNICATION REGION).                                        *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*                                                                     *
*        TCR220 - MESSAGE WRITER                                      *
*                                                                     *
*   THIS ROUTINE IS A SEPARATE  CSECT WITHIN THE DRIVER MODULE        *
*   (IEBTCRIN).  IT PERFORMS TWO FUNCTIONS:                           *
*                                                                     *
*   1)  OUTPUTTING OF CONTROL STATEMENT IMAGES TO THE SYSPRINT DATA   *
*       SET (IF PROVIDED)                                             *
*                                                                     *
*   2)  OUTPUTTING OF IEBXXXX MESSAGES TO THE SYSPRINT DATA SET       *
*       AND/OR THE SYSTEM CONSOLE                                     *
*                                                                     *
*   OPTION 1 IS REQUESTED BY SETTING BIT 6 OF THE COMMUNICATION       *
*   REGION INDICATOR PRTRTN TO ONE AND PASSING THE CONTROL STATEMENT  *
*   ADDRESS AND LENGTH VIA THE COMMUNICATION REGION COMPONENTS MSGAD  *
*   AND MSGLN.                                                        *
*                                                                     *
*   OPTION 2 IS REQUESTED BY SETTING PRTRTN BIT 6 TO ZERO.  THE       *
*   COMMUNICATION REGION MESSAGE POINTER TABLE MUST INDICATE THE      *
*   MESSAGES REQUIRED.                                                *
*                                                                     *
*   EXITS-NORMAL:  NONE                                               *
*        -ERROR:   NONE                                               *
*                                                                     *
        EJECT                                                         *
*TABLES/WORK AREAS:                                                   *
*                                                                     *
*   1)  TCRGEN TABLE (TCRGENOP)                                       *
*                                                                     *
*   *********************                                             *
*   *TCRGEN  *  *  *    *                                             *
*   *********************                                             *
*      ×         ×    ×                                               *
*   OPERATOR     ×   DATA TABLE END ADDRESS                           *
*                ×                                                    *
*          NAME TABLE ENTRY LENGTH                                    *
*                                                                     *
*                                                                     *
*    KEYWORD  DEFAULT    ALTERNATIVES                                 *
*   *************************************                             *
*   *TYPE    *MTDI    *MTST    *        *          NAME TABLE         *
*   *************************************          **********         *
*   *TRANS   *STDUC   *STDLC   *NOTRAN  *                             *
*   *************************************                             *
*   *EDIT    *EDITD   *EDITR   *NOEDIT  *                             *
*   *************************************                             *
*   *VERCHK  *NOCHK   *VOKCHK  *        *                             *
*   *************************************                             *
*   *ERROPT  *NORMAL  *NOERR   *        *                             *
*   *************************************                             *
*   *MINLN   *        *        *        *                             *
*   *************************************                             *
*   *MAXLN   *        *        *        *                             *
*   *************************************                             *
*   *REPLACE *X'19'   *        *        *                             *
*   *************************************                             *
*       ×        ×        ×        ×                                  *
*       ×        ×        ×        ×                                  *
*   *************************************                             *
*   *00* 0* 0*  0140  *  0180  *  0000  *         DATA TABLE          *
*   *************************************         **********          *
*   *04* 0* 0*  0110  *  0108  *  0102  *                             *
*   *************************************                             *
*   *00* 0* 0*  0280  *  0240  *  0220  *                             *
*   *************************************                             *
*   *00* 0* 0*  0208  *  0210  *  0000  *                             *
*   *************************************                             *
*   *00* 0* 0*  0204  *  0202  *  0000  *                             *
*   *************************************                             *
*   *08* 0* 0*  0400  *  0000  *  0000  *                             *
*   *************************************                             *
*   *08* 0* 0*  0600  *  0000  *  0000  *                             *
*   *************************************                             *
*   *0C* 0* 0*  0300  *  0000  *  0000  *                             *
*   *************************************                             *
*    ×      ×        ×-----×-------×                                  *
*   BRANCH  KEYWORD           OPTION TABLE BYTE                       *
*   TABLE   PROCESSED              AND                                *
*   DISP-   INDICATOR         'ON' MASK                               *
*   PLACEMENT                                                         *
*                                                                     *
*   2)  EXITS TABLE (EXITSOP)                                         *
*                                                                     *
*                                                                     *
*   *********************                                             *
*   *EXITS   *  *  *    *                                             *
*   *********************                                             *
*      ×         ×   ×                                                *
*    OPERATOR    ×   NAME TABLE END ADDRESS                           *
*                ×                                                    *
*           NAME TABLE ENTRY LENGTH                                   *
*                                                                     *
*                                                                     *
*                                                                     *
*    NAME TABLE                   DATA TABLE                          *
*                                                                     *
*    **********               *******************                     *
*    *OUTHDR2 *  - - - - - -  *04*00*00*80*00*00*                     *
*    **********               *******************                     *
*    *OUTTLR2 *  - - - - - -  *04*00*00*40*00*08*                     *
*    **********               *******************                     *
*    *OUTHDR3 *  - - - - - -  *04*00*00*20*00*10*                     *
*    **********               *******************                     *
*    *OUTTLR3 *  - - - - - -  *04*00*00*10*00*18*                     *
*    **********               *******************                     *
*    *OUTREC  *  - - - - - -  *04*00*00*08*00*20*                     *
*    **********               *******************                     *
*    *ERROR   *  - - - - - -  *04*00*00*04*00*28*                     *
*    **********               *******************                     *
*        ×                     ×  ×  ×   × -----                      *
*     KEYWORD             BRANCH  ×OPTION×    ×                       *
*                         TABLE   ×TABLE ×   NAME                     *
*                         DISP    × DISP ×  TABLE                     *
*                                 ×      ×     DISP                   *
*                           KEYWORD      ON                           *
*                         PROCESSED     MASK                          *
*                         INDICATOR                                   *
*                                                                     *
*   EACH OPERATOR TABLE IS CONSTRUCTED IN SUCH A WAY THAT A COMMON    *
*   ROUTINE CAN BE USED TO PROCESS BOTH OF THE TABLES.                *
*                                                                     *
*   EACH TABLE CONTAINS THREE SECTIONS:                               *
*                                                                     *
*   A)  A 16-BYTE TABLE CONTAINING THE OPERATOR NAME, NAME TABLE      *
*       ENTRY LENGTH, AND NAME TABLE ENDING ADDRESS                   *
*                                                                     *
*   B)  A NAME TABLE CONTAINING ALL POSSIBLE KEYWORDS AND THEIR       *
*       RELATED PARAMETERS. (IF THE PARAMETERS ARE SPECIAL VALUES     *
*       WHICH MUST BE INTERPRETED BY A SEPARATE ROUTINE, SUCH AS      *
*       THE MINLN VALUE, THEY ARE NOT CONTAINED IN THE TABLE.)        *
*                                                                     *
*   C)  A DATA TABLE CONTAINING BRANCH TABLE DISPLACEMENTS FOR        *
*       LOCATING SPECIAL PROCESSING ROUTINES AND OPTION TABLE DIS-    *
*       PLACEMENTS AND 'ON' MASKS FOR SETTING OPTIONS.                *
*                                                                     *
*   THERE IS A ONE-TO-ONE CORRESPONDENCE BETWEEN THE NAME TABLE
*   AND DATA TABLE ENTRIES.
*
*                                                                     *
*   3)  COMMUNICATION REGION OPTION TABLE                             *
*                                                                     *
*                                                                     *
*                                                                     *
*   **********************************************************        *
*   * BYTES  *     BYTE 0    *    BYTE 1     *     BYTE 2    *        *
*   **********************************************************        *
*   *        *               *     *         *     *VER*ERR* *        *
*   *        *     EXITS     *TYPE *  TRANS  *EDIT *CHK*OPT* *        *
*   *        *************************************************        *
*   *        *O×O×O×O×O×E×U× *M×M× *S×S×U×N× *E×E×N*V×N*N×N* *        *
*   *        *U×U×U×U×U×R×S× *T×T× *T×T×S×O× *D×D×O*O×O*O×O* *        *
*   *        *T×T×T×T×T×R×E× *S×D× *D×D×E×T× *I×I×E*K×C*R×E* *        *
*   *        *H×T×H×T×R×O×R× *T×I× *U×L×R×R× *T×T×D*C×H*M×R* *        *
*   *        *D×L×D×L×E×R×T× * × × *C×C×N×A× *D×R×I*H×K*A×R* *        *
*   *        *R×R×R×R×C× ×A× * × × * × ×A×N× * × ×T*K× *L× * *        *
*   *        *2×2×3×3× × ×B× * × × * × ×M× × * × × * × * × * *        *
*   *        * × × × × × × × * × × * × ×E× × * × × * × * × * *        *
*   **********************************************************        *
*   *DEFAULT *0×0×0×0×0×0×0× *0×1× *0×0×0×0× *1×0×0*0×1*1×0* *        *
*   **********************************************************        *
*                                                                     *
*                                                                     *
*                                                                     *
*   *********************************                                 *
*   *    BYTE 3     *  4-5  *  6-7  *                                 *
*   *********************************                                 *
*   *               *  MIN  *  MAX  *                                 *
*   *    REPLACE    *  LN   *  LN   *                                 *
*   *********************************                                 *
*   *               *       *       *                                 *
*   *               *       *       *                                 *
*   *               *       *       *                                 *
*   *               *       *       *                                 *
*   *********************************                                 *
*   *0*0*0*1*1*0*0*1* 0 * 0 *X'0078'*                                 *
*   *********************************                                 *
*                                                                     *
*                                                                     *
*                                                                     *
*   4)  OPERATOR, KEYWORD, PARAMETER HOLD AREA (HOLDAREA)             *
*                                                                     *
*   THIS WORK AREA IS USED TO ASSEMBLE EACH OPERATOR, KEYWORD,        *
*   AND PARAMETER PRESENTED IN THE INPUT STREAM.                      *
*                                                                     *
*   5)  INVALID TRANSLATE REPLACE PARAMETER CHARACTERS (TRANRPTB)     *
*                                                                     *
*   6)  INVALID EDIT REPLACE PARAMETER CHARACTERS (EDITRPTB)          *
*                                                                     *
*ATTRIBUTES:  NOT RE-ENTRANT.  NOT RE-USABLE.                         *
*                                                                     *
*NOTES:  THIS MODULE USES VARIOUS COMPONENTS OF THE COMMON COMMUNICA- *
*   TION REGION CONTAINED IN THE DRIVER MODULE (IEBTCRIN).            *
*                                                                     *
*   THIS MODULE CONTAINS TYPE C CHARACTER CODE DEPENDENCIES.  THE     *
*   OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL REPRESENTATION  *
*   OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT TO THE ONE USED *
*   AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED SO THAT REDEFINI- *
*   TION OF CHARACTER CONSTANTS, BY RE-ASSEMBLY, WILL RESULT IN A     *
*   CORRECT MODULE FOR THE NEW DEFINITIONS.  CHARACTER CONSTANTS      *
*   INVOLVED ARE AS FOLLOWS:                                          *
*                                                                     *
*   1)  DELIMITERS COMMA, SPACE, EQUALSN, PARENL, AND PARENR USED BY  *
*       THE OPERAND VALIDATION ROUTINE TCR260                         *
*                                                                     *
*   2)  REPLACE PARAMETER CHARACTER X (ALPHAX)                        *
*                                                                     *
*   3)  OPERATOR, KEYWORD, AND PARAMETER TABLES TCRGENOP,             *
*       TCRGENTB, TCRDATA, EXITSOP, EXITSTB, AND EXITDATA             *
*                                                                     *
*   THIS MODULE ALSO CONTAINS TYPE E CHARACTER CODE DEPENDENCIES.     *
*   THE OPERATION OF THIS MODULE DEPENDS UPON THE FOLLOWING PROPER-   *
*   TIES OF THE INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER     *
*   SET:  PARTICULAR BIT PATTERN REPRESENTATION OF EBCDIC CHARACTERS  *
*   AND A PARTICULAR COLLATING SEQUENCE OF THE INTERNAL REPRESENTATION*
*   OF THE CHARACTER SET.  THE SPECIFIC INSTRUCTION SEQUENCES, TABLES,*
*   AND CONSTANTS WHICH REQUIRE MODIFICATION IF THESE PROPERTIES OF   *
*   THE CHARACTER SET ARE CHANGED MAY BE IDENTIFIED BY:               *
*                                                                     *
*   1)  TCR520-TCR528 (MINLN-MAXLN VALIDATION ROUTINE) ASSUMES THAT   *
*       THE NUMERIC DIGITS 0-9 ARE IN ASCENDING COLLATING SEQUENCE    *
*       AND HAVE NO INTERSPERSED CONTROL CHARACTERS.                  *
*                                                                     *
*   2)  TCR240-TCR249 (NAME VALIDATION ROUTINE) ASSUMES THAT THE ALPHA-
*       BETIC CHARACTERS A-Z ARE IN ASCENDING COLLATING SEQUENCE AND  *
*       HAVE NO INTERSPERSED CONTROL CHARACTERS.                      *
*                                                                     *
*  3)  TCR530-TCR542 (REPLACE PARAMETER VALIDATION AND CONVERSION)    *
*      ASSUMES                                                        *
*                                                                     *
*      A)  THAT THE EBCDIC CHARACTERS A-F AND 0-9 ARE REPRESENTED     *
*          INTERNALLY BY X'C1' - X'CF' AND X'F0' - X'F9', RESPECT-    *
*          IVELY.  THIS PARAMETER IS USED TO SPECIFY THE HEXADECIMAL  *
*          REPRESENTATION OF THE CHARACTERS TO BE USED FOR REPLACING  *
*          DATA CHECK BYTES.  FOR EXAMPLE, THE EBCDIC INPUT CHARACTERS*
*          'D9', REPRESENTED INTERNALLY AS THE TWO BYTES              *
*          '11000100''11111001', WOULD BE CONVERTED TO THE ONE BYTE   *
*          '11011001'.                                                *
*                                                                     *
*      B)  THAT THE CHARACTERS A-Z AND 0-9 ARE IN ASCENDING COLLATING *
*          SEQUENCE AND HAVE NO CONTROL CHARACTERS INTERSPERSED AMONG *
*          EITHER GROUP.                                              *
*                                                                     *
*   4)  TCR290 (INVALID REPLACE PARAMETER VALIDATION) ASSUMES THAT THE*
*       PL CODES ARE REPRESENTED BY '01' TO '08' AND ARE IN ASCENDING *
*       COLLATING SEQUENCE.  IT ALSO ASSUMES THAT HEXADECIMAL CODES   *
*       '80' TO 'FF' ARE INVALID WITH TRANSLATION AND ARE IN          *
*       ASCENDING COLLATING SEQUENCE.                                 *
*                                                                     *
*   COMMENTS ARE PROVIDED TO INDICATE THE CONSIDERATIONS INVOLVED AND *
*   THE MECHANISM FOR CHANGING THE CODE.                              *
*                                                                     *
*                                                                     *
*   TO MEET WORLD TRADE REQUIREMENTS, ALL STANDARD OPERATORS, KEY-    *
*   WORDS, AND PARAMETER NAMES HAVE BEEN PLACED IN THE TABLES         *
*   TCRGENOP, TCRGENTB, TCRDATA, EXITSOP, EXITSTB, AND EXITDATA.      *
*   THESE TABLES MAY BE REPLACED IN ENTIRETY.                         *
*                                                                     *
*   IF THE MEANS OF SPECIFYING THE MINLN, MAXLN, OR REPLACE PARA-     *
*   METER VALUES IS CHANGED, MODIFICATIONS TO THE VALIDATION ROUTINES *
*   TCR290, TCR520, AND TCR530 MAY BE REQUIRED.                       *
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - REGISTER DEFINITIONS'
IEBTCR04 START 0
         SPACE 2
*              REGISTER DEFINITIONS
         SPACE 1
PARMREG  EQU   1                       PARAMETER LIST REGISTER
CHARCT   EQU   4                       PARAMETER CHARACTER COUNT
COLUMNRG EQU   4                       CARD INPUT AREA
HOLDREG  EQU   5                       HOLD AREA
PARMTAB1 EQU   6                       PARAMETER TABLE 1
PARMTAB2 EQU   7                       PARAMETER TABLE 2
WKREG    EQU   7                       WORK REGISTER
KEYRG    EQU   8                       KEYWORD-PARAMETER POINTER
TABREG   EQU   8                       REPLACE TABLE
DATARG   EQU   9                       PARAMETER DATA POINTER
CNTREG   EQU   9                       COUNT REGISTER
COMMREG  EQU   10                      COMMUNICATION REGION
DATAHOLD EQU   11                      KEYWORD DATA ENTRY POINTER
BASEREG  EQU   12                      BASE REGISTER
SAVEREG  EQU   13                      SAVE REGISTER
RETREG   EQU   14                      RETURN REGISTER
LNKREG15 EQU   15                      LINK REGISTER
R0       EQU   0                       REGISTER 0
R1       EQU   1                       REGISTER 1
R2       EQU   2                       REGISTER 2
R3       EQU   3                       REGISTER 3
R4       EQU   4                       REGISTER 4
R7       EQU   7                       REGISTER 7
R15      EQU   15                      REGISTER 15
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - MAINLINE'
*        THIS SECTION SAVES THE DRIVER'S GENERAL REGISTERS,
*        SETS UP A SAVEAREA ADDRESS, AND ESTABLISHES BASE REGISTERS
*        FOR THIS MODULE AND FOR THE COMMON COMMUNICATION REGION.
         SPACE 2
         ENTRY IEBTCR04
         SAVE  (14,12),T,IEBTCR04      SAVE REGISTERS
         BALR  BASEREG,R0              ESTABLISH BASE REGISTER
         USING *,BASEREG               INDICATE BASE REGISTER
         LR    R15,SAVEREG             ESTABLISH SAVE AREA
         ST    R15,SAVEAREA+4
         LA    SAVEREG,SAVEAREA
         ST    SAVEREG,8(R15)
         XR    R15,R15
         ST    R15,0(SAVEREG)
         L     COMMREG,0(PARMREG)      LOAD COMMUNICATION REGION ADDR
         USING TCRCOM,COMMREG          INDICATE COMM. REG. BASE REG
         SPACE 3
*        SYSIN EOF ADDRESS INSERTION
         SPACE 1
*        THIS ROUTINE COMPLETES THE EODAD PARAMETER OF THE SYSIN DCB
         SPACE 2
TCR100   LA    R2,SYSINDCB             LOAD SYSIN DCB ADDRESS
         USING IHADCB,R2               INDICATE DCB DSECT REG
         MVC   DCBEODAD+1(3),SYSEODAD  SET EOF ADDRESS TO TCR714
         EJECT
*        CONTROL CARD ANALYZER MAINLINE
         SPACE 1
*        THIS ROUTINE CALLS THE FOLLOWING ROUTINES FOR PROCESSING
*        OF EACH CONTROL STATEMENT
         SPACE 1
*              TCR230 - NEW STATEMENT FETCH
*              TCR240 - NAME VALIDATION
*              TCR250 - OPERATOR VALIDATION
*              TCR250 - OPERAND VALIDATION
         SPACE 1
*        FOLLOWING VALIDATION OF EACH CONTROL STATEMENT, IT
*        CALLS THE MESSAGE WRITER (TCR220) CONTAINED IN THE
*        DRIVER.  FOLLOWING VALIDATION OF ALL CONTROL STATEMENTS
*        IT CALLS ROUTINE TCR270, WHICH TESTS THE CONSISTENCY OF ALL
*        OPTIONS AND SETS THESE OPTIONS IN THE COMMON COMMUNICATION
*        REGION OPTION TABLE.  IT ALSO CALLS ROUTINE TCR290 WHICH
*        TESTS FOR INVALID EDIT AND TRANSLATE REPLACE PARAMETERS.
         SPACE 2
TCR101   BAL   RETREG,TCR230           LOAD ADDR NEW STMT FETCH RTN
         TM    STATUS,EOFSYSIN         EOF SYSIN
         BO    TCR107                  BR YES
         BAL   RETREG,TCR240           LOAD ADDR NAME VALIDATION RTN
         TM    STATUS,INVTERM          INVALID STATEMENT TERMINATION
         BO    TCR103                  BR YES
         BAL   RETREG,TCR250           LOAD ADDR OPERATOR VALIDATION
         TM    STATUS,INVTERM          INVALID TERMINATION
         BO    TCR103                  BR YES
         TM    OPTIND,INVOP            INVALID OPERATOR
         BO    TCR103                  BR YES
         BAL   RETREG,TCR260           LOAD ADDR OPERAND VALIDATION
         TM    STATUS,INVTERM+INVSTMT  INVALID STATEMENT
         BZ    TCR105                  BR NO
TCR103   L     LNKREG15,VTCR220        LOAD MESSAGE WRITER ADDR
         BALR  RETREG,LNKREG15         DISPLAY MESSAGES
TCR105   TM    STATUS,EOFSYSIN         EOF SYSIN
         BNO   TCR101                  BR NO
TCR107   TM    ERRIND,SYSINERR         PERMANENT I/O ERROR SYSIN
         BO    TCR1085                 BR YES
         TM    STATUS,INVSTMT+INVTERM  ALL STATEMENTS VALID
         BNZ   TCR108                  BR NO
         BAL   RETREG,TCR270           SET OPTIONS, TEST CONSISTENCY
         TM    STATUS,INCONOPT         OPTIONS CONSISTENT
         BO    TCR1075                 BR NO
         BAL   RETREG,TCR290           CHECK FOR DISALLOWED REPLACE
*                                        PARAMETERS
         TM    STATUS,INVSTMT          INVALID REPLACE PARAMETER
         BNO   TCR109                  BR NO
TCR1075  L     LNKREG15,VTCR220        LOAD MESSAGE WRITER ADDRESS
         BALR  RETREG,LNKREG15         WRITE MESSAGES
TCR108   MVC   RETCODE,CODE12          SET RETURN CODE 12
TCR1085  OI    ERRIND,ANALERR          SET ERROR INDICATOR
TCR109   L     SAVEREG,4(SAVEREG)
         RETURN (14,12),T,RC=(15)      RETURN
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - NEW STATEMENT FETCH'
*        NEW STATEMENT FETCH ROUTINE
         SPACE 1
*        THIS ROUTINE CALLS THE SYSIN DATA SET READ ROUTINE, CHECKS
*        WHETHER END OF FILE HAS BEEN PRESENTED, AND INITIALIZES
*        POINTERS AND INDICATORS.
         SPACE 2
TCR230   ST    RETREG,SAVE200          SAVE RETURN ADDRESS
         MVI   CARRCTRL,DOUBLESP       SET DOUBLE SPACE
         BAL   RETREG,TCR710           READ SYSIN DATA SET
         TM    STATUS,EOFSYSIN         END OF SYSIN DATA SET
         BO    TCR239                  BR YES
         NI    STATUS,STATINIT         INITIALIZE STATUS INDICATORS
         L     COLUMNRG,CARDAREA       SET COLUMN REGISTER AT COL 1
         L     HOLDREG,AHOLD           INITIALIZE HOLD AREA POINTER
         NI    OPTIND,OPTINIT          INITIALIZE OPTION INDICATOR
         MVI   SVCHAR,SPACE            SET SAVE CHAR TO SPACE
TCR239   L     RETREG,SAVE200          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - NAME VALIDATION'
*        NAME VALIDATION ROUTINE
         SPACE 1
*        THIS ROUTINE TESTS WHETHER OR NOT A NAME FIELD HAS BEEN
*        PROVIDED.  IF ONE HAS, THE ROUTINE CHECKS THAT IT IS EIGHT
*        CHARACTERS OR LESS AND THAT THE FIRST CHARACTER IS ALPHA-
*        BETIC.  IF THE NAME FIELD IS INVALID, THE ROUTINE SETS
*        MESSAGE AND ERROR INDICATORS AND CALLS THE CHARACTER FLUSH
*        ROUTINE (TCR620) TO LOCATE THE FIRST BLANK FOLLOWING THE
*        NAME FIELD.  IF THE NAME FIELD EXTENDS OVER THE ENTIRE
*        CARD, THE ROUTINE CALLS THE STATEMENT FLUSH ROUTINE (TCR630)
*        TO FLUSH ANY RECOGNIZABLE CONTINUATION CARDS.
         SPACE 1
*        THIS ROUTINE ASSUMES THAT THE ALPHABETIC CHARACTERS A-Z
*        ARE IN ASCENDING COLLATING SEQUENCE AND HAVE NO INTERSPERSED
*        CHARACTERS.  ANY REDEFINITION OF THESE VALUES MAY NECESSI-
*        TATE MODIFICATION TO THIS ROUTINE.
         SPACE 2
TCR240   ST    RETREG,SAVE200          SAVE RETURN ADDRESS
         CLI   0(COLUMNRG),BLANK       COLUMN 1 BLANK
         BE    TCR249                  BR YES
         CLI   0(COLUMNRG),ALPHAA      TEST ALPHABETIC
         BL    TCR244                  BR LOWER THAN A
         CLI   0(COLUMNRG),ALPHAZ
         BH    TCR244                  BR HIGHER THAN Z
         SPACE 1
TCR242   LA    COLUMNRG,1(COLUMNRG)    INCREMENT TO NEXT COLUMN
         CLI   0(COLUMNRG),BLANK       COLUMN BLANK
         BE    TCR249                  BR  YES
         CL    COLUMNRG,COLUMN9        COLUMN 9
         BL    TCR242                  BR NO
         SPACE 1
TCR244   OI    MSG02,MSGREQ            SET MSG INDICATOR
         OI    STATUS,INVSTMT          SET INVALID STMT INDICATOR
         MVI   SRCHCHAR,BLANK          SET SEARCH CHARACTER TO BLANK
         BAL   RETREG,TCR620           FLUSH CHARACTERS TO BLANK
         TM    STATUS,INVTERM          INVALID STMT TERMINATION
         BNO   TCR249                  BR NO
         BAL   RETREG,TCR630           FLUSH CARDS THIS STMT
TCR249   L     RETREG,SAVE200          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - OPERATOR VALIDATION'
*        OPERATOR VALIDATION ROUTINE
         SPACE 1
*        THIS ROUTINE FIRST CALLS THE CHARACTER FLUSH ROUTINE
*        (TCR620) TO LOCATE THE FIRST CHARACTER OF THE OPERATION
*        FIELD. IT THEN TRANSFERS EACH CHARACTER OF THE OPERATION
*        FIELD TO AN 8-CHARACTER HOLD AREA UNTIL IT REACHES EITHER
*        THE NINTH CHARACTER OR A BLANK.
*        IF EIGHT OR FEWER CHARACTERS ARE TRANSFERRED, THE ROUTINE
*        COMPARES THESE CHARACTERS TO EACH OF THE ALLOWABLE
*        OPERATORS (TCRGEN  AND EXITS).  IF IT IS ONE OF THESE
*        OPERATORS AND  THE OPERATOR HAS NOT BEEN PRESENTED PRE-
*        VIOUSLY, THE ROUTINE SETS INDICATORS AND POINTERS TO THE
*        KEYWORD AND DATA TABLES.
*        IF THE OPERATOR IS NOT VALID, THE ROUTINE SETS MESSAGE AND
*        ERROR INDICATORS.
         SPACE 2
TCR250   ST    RETREG,SAVE200          SAVE RETURN ADDRESS
         MVC   HOLDAREA(8),BLANKS      CLEAR HOLD AREA
         MVI   SRCHCHAR,NONBLANK       SET SEARCH CHARACTER TO NON-BLK
         BAL   RETREG,TCR620           FLUSH TO NON-BLANK CHARACTER
         TM    STATUS,INVTERM          INVALID TERMINATION
         BO    TCR258                  BR YES
TCR252   CL    HOLDREG,AHOLD9          9TH POSITION HOLD AREA
         BNL   TCR258                  BR YES
         MVC   0(1,HOLDREG),0(COLUMNRG)  TRANSFER NEXT CHARACTER TO
*                                          HOLD AREA
         LA    HOLDREG,1(HOLDREG)      INCREMENT HOLD AREA POINTER
         LA    COLUMNRG,1(COLUMNRG)    INCREMENT INPUT AREA POINTER
         CLI   0(COLUMNRG),BLANK       NEXT INPUT CHAR BLANK
         BNE   TCR252                  BR NO
         L     HOLDREG,AHOLD           LOAD HOLD AREA ADDRESS
         CLC   0(8,HOLDREG),TCRGENOP   OPERATOR  TCRGEN
         BNE   TCR255                  BR NO
         TM    OPTIND,TCRGNPR          PREVIOUS TCRGEN STATEMENT
         BO    TCR254                  BR YES
         LA    PARMTAB1,TCRGENOP       LOAD PARAMETER TABLE ADDR
         LA    PARMTAB2,TCRDATA        LOAD DATA TABLE ADDRESS
         OI    OPTIND,GEN2             SET TCRGEN INDICATORS
         B     TCR256                  BR TO CONTINUE PROCESSING
         SPACE 1
TCR254   MVI   MSG06,MSGREQ            SET INVALID STMT MSG INDICATOR
TCR2543  OI    STATUS,INVSTMT          SET INVALID STMT INDICATOR
         OI    OPTIND,INVOP            SET INVALID OPERATOR INDICATOR
         BAL   RETREG,TCR630           FLUSH CARDS THIS STMT
         B     TCR257                  BR TO RETURN
         SPACE 1
TCR255   CLC   0(8,HOLDREG),EXITSOP    OPERATOR EXITS
         BNE   TCR258                  BR NO
         TM    OPTIND,EXITSPR          PREVIOUS EXITS  STATEMENT
         BO    TCR254                  BR YES
         LA    PARMTAB1,EXITSOP        LOAD KEYWORD TABLE ADDRESS
         LA    PARMTAB2,EXITDATA       LOAD DATA TABLE ADDRESS
         OI    OPTIND,EXITS2           SET  EXITS  INDICATORS
TCR256   MVI   SVCHAR,COMMA            SET SAVE CHARACTER TO COMMA
         SPACE 1
TCR257   L     RETREG,SAVE200          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
TCR258   MVI   MSG03,MSGREQ            SET INVALID OPERATOR MSG IND
         B     TCR2543                 BR TO CONTINUE PROCESSING
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - OPERAND VALIDATION'
*        OPERAND VALIDATION
         SPACE 1
*        THIS ROUTINE FIRST CALLS THE CHARACTER FLUSH ROUTINE
*        (TCR620) TO LOCATE THE FIRST KEYWORD.  IF IT DOES NOT FIND
*        ONE ON THE FIRST CARD OF THE STATEMENT, THE ROUTINE CALLS
*        THE STATEMENT FLUSH ROUTINE (TCR630) TO BYPASS ANY RECOG-
*        NIZABLE CONTINUATION CARDS.
*        IF IT DOES FIND ONE, IT TRANSFERS EACH CHARACTER OF THE KEY-
*        WORD TO THE HOLD AREA UNTIL IT REACHES EITHER THE NINTH
*        CHARACTER,   A VALID DELIMITER (COMMA, SPACE, OR EQUAL SIGN),
*        OR COLUMN 72.
*        IF IT REACHES THE NINTH CHARACTER, IT SETS INVALID PARAMETER
*        AND STATEMENT INDICATORS AND CALLS THE CHARACTER FLUSH ROUTINE
*        (TCR620) TO FLUSH CHARACTERS TO THE NEXT COMMA (NEXT
*        PARAMETER) OR THE NEXT BLANK (END OF STATEMENT).  IF THE
*        FLUSH ROUTINE DOES NOT FIND EITHER OF THESE CHARACTERS, THE
*        OPERAND VALIDATION ROUTINE CALLS THE STATEMENT FLUSH ROUTINE
*        TO FLUSH ANY RECOGNIZABLE CONTINUATION CARDS.
*        IF IT FINDS A VALID DELIMITER, IT CALLS THE ASSOCIATED
*        VALIDATION ROUTINE.
*        IF IT REACHES COLUMN 72, IT CALLS THE END OF CARD VALIDA-
*        TION ROUTINE.  IF THE STATEMENT IS TERMINATED AT THIS POINT,
*        THE ROUTINE RETURNS TO THE MAINLINE ROUTINE.  IF THE STATE-
*        MENT IS NOT TERMINATED AT THIS POINT, THE ROUTINE CALLS THE
*        SYSIN READ ROUTINE TO READ AND VALIDATE THE NEXT CONTINUATION
*        CARD.  IF ANY ERRORS HAVE OCCURRED UP TO THIS POINT WHICH
*        ALLOW PROCESSING TO CONTINUE, THE ROUTINE CALLS THE MESSAGE
*        WRITER TO PRINT ERROR MESSAGES BEFORE CALLING THE READ
*        ROUTINE.
         SPACE 2
TCR260   ST    RETREG,SAVE200          SAVE RETURN ADDRESS
TCR2601  MVC   HOLDAREA(8),BLANKS      CLEAR HOLD AREA
         MVI   SRCHCHAR,NONBLANK       SET SEARCH CHARACTER TO NON-BLK
         BAL   RETREG,TCR620           FLUSH TO NON-BLANK
         TM    STATUS,INVTERM          INVALID TERMINATION
         BNO   TCR261                  BR NO
         L     COLUMNRG,CARDEND        SET POINTER TO COLUMN 72
         MVI   SVCHAR,SPACE            MOVE SPACE TO SAVE CHARACTER
TCR2603  BAL   RETREG,TCR630           FLUSH CARDS THIS STMT
         B     TCR262                  BR TO RETURN
TCR261   CL    COLUMNRG,CARDEND        COLUMN 72
         BL    TCR263                  BR NO
         BAL   RETREG,TCR330           VALIDATE END OF CARD
         TM    STATUS,STMTTERM         STATEMENT TERMINATED
         BNZ   TCR262                  BR YES
         TM    STATUS,INVSTMT          ALL STATEMENTS VALID
         BNO   TCR2613                 BR YES
         L     LNKREG15,VTCR220        LOAD MESSAGE WRITER ADDRESS
         BALR  RETREG,LNKREG15         DISPLAY MESSAGES
TCR2613  BAL   RETREG,TCR610           READ AND VALIDATE NEW CARD
         TM    STATUS,STMTTERM         STATEMENT TERMINATED
         BZ    TCR2601                 BR NO
TCR262   L     RETREG,SAVE200          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
         SPACE 1
*        CHECK FOR DELIMITER
         SPACE 1
TCR263   CLI   0(COLUMNRG),COMMA       COMMA?
         BNE   TCR264                  BR NO
         BAL   RETREG,TCR350           PROCESS PRECEDING PARAMETER
         B     TCR268                  IF ANY
TCR264   CLI   0(COLUMNRG),SPACE       SPACE?
         BNE   TCR265                  BR NO
         BAL   RETREG,TCR360           PROCESS PRECEDING PARAMETER
         B     TCR261                  IF ANY
TCR265   CLI   0(COLUMNRG),EQUALSN     EQUAL SIGN
         BNE   TCR266                  BR NO
         BAL   RETREG,TCR370           PROCESS PRECEDING KEYWORD IF ANY
         TM    STATUS,STMTTERM         STATEMENT TERMINATED
         BZ    TCR2653                 BR NO
         MVI   SVCHAR,SPACE            MOVE SPACE TO SAVE CHARACTER
         B     TCR261                  BR TO CONTINUE PROCESSING
TCR2653  CLI   0(COLUMNRG),SPACE       LAST CHARACTER SPACE
         BNE   TCR268                  BR NO
         L     COLUMNRG,CARDEND        SET AT COLUMN 72
         MVI   SVCHAR,SPACE            MOVE SPACE TO SAVE CHARACTER
         B     TCR261                  BR TO CONTINUE PROCESSING
TCR266   CLI   0(COLUMNRG),PARENL
         BE    TCR267                  BYPASS LEFT PAREN
         CLI   0(COLUMNRG),PARENR
         BE    TCR267                  BYPASS RIGHT PAREN
         CL    HOLDREG,AHOLD9          9TH POSITION HOLD AREA
         BNL   TCR269                  BR YES
         SPACE 1
*        TRANSFER CHARACTER TO HOLD AREA
         SPACE 1
         MVC   0(1,HOLDREG),0(COLUMNRG) TRANSFER NEXT CHARACTER
TCR267   LA    HOLDREG,1(HOLDREG)      INCREMENT HOLD AREA POINTER
TCR268   MVC   SVCHAR(1),0(COLUMNRG)   TRANSFER CHARACTER TO SAVEAREA
         LA    COLUMNRG,1(COLUMNRG)    INCREMENT INPUT AREA POINTER
         B     TCR261                  BR TO CONTINUE PROCESSING
         SPACE 1
*        INVALID PARAMETER OR KEYWORD
         SPACE 1
TCR269   OI    MSG05,MSGREQ            INVALID PARAMETER OR KEYWORD
         OI    STATUS,INVSTMT          SET INVALID STMT INDICATOR
         MVI   PARMCNT+1,HEXZERO       CLEAR PARAMETER COUNT
         MVI   SRCHCHAR,COMMA          SET SEARCH CHARACTER TO COMMA
         MVI   SRCHCHAR+1,SPACE        SET SECOND SEARCH CHARACTER
*                                        TO SPACE
         OI    OPTIND,TWOCHAR          SET TWO CHARACTER SEARCH IND
         BAL   RETREG,TCR620           FLUSH TO COMMA
         TM    STATUS,STMTTERM         STATEMENT TERMINATED
         BNZ   TCR2603                 BR YES
         B     TCR261                  BR TO CONTINUE PROCESSING
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - TEST FOR CONSISTENCY'
*        SET OPTIONS - TEST CONSISTENCY
         SPACE 1
*        THIS ROUTINE SETS OPTIONS IN THE COMMON COMMUNICATION
*        REGION OPTION TABLES AND TESTS FOR CONSISTENCY.  IF AN
*        OPTION WAS NOT SPECIFIED BY THE USER, THE ROUTINE SETS
*        AN APPROPRIATE DEFAULT.
         SPACE 2
TCR270   ST    RETREG,SAVE200          SAVE RETURN ADDRESS
         SPACE 1
*        MAXLN
         SPACE 1
         CLC   TEMPOP+6(2),ZEROS       MAXLN SPECIFIED
         BE    TCR272                  BR NO
         MVC   MAXLN(2),TEMPOP+6       SET MAXLN VALUE
         SPACE 1
*        REPLACE
         SPACE 1
TCR272   TM    OPTIND,REPLZERO         REPLACE EQUAL TO X'00'
         BO    TCR2723                 BR YES
         CLC   TEMPOP+3(1),ZEROS       REPLACE SPECIFIED
         BE    TCR273                  BR NO
TCR2723  MVC   REPL(1),TEMPOP+3        SET REPLACE CHARACTER
         SPACE 1
*        MINLN
         SPACE 1
TCR273   CLC   TEMPOP+4(2),ZEROS       MINLN SPECIFIED
         BE    TCR2732                 BR NO
         MVC   MINLN(2),TEMPOP+4       SET MINLN
         SPACE 1
*        EDIT, VERCHK, ERROPT
         SPACE 1
TCR2732  MVC   IPARM2,TEMPOP+2         SET EDIT, VERCHK, ERROPT OPTIONS
         TM    IEXITS,ERREXIT          ERROR EXIT
         BNO   TCR2735                 BR NO
         TM    IPARM2,ERROP            ERROPT SPECIFIED
         BZ    TCR274                  BR NO
         NI    IPARM2,X'FF'-ERROP      CLEAR ERROPT OPTION
         B     TCR274                  BR TO CONTINUE PROCESSING
         SPACE 1
TCR2735  TM    IPARM2,ERROP            ERROPT SPECIFIED
         BNZ   TCR274                  BR YES
         OI    IPARM2,ERRDEF           SET ERROPT=NORMAL DEFAULT
         SPACE 1
*        SET AND TEST MTST OPTIONS
         SPACE 1
TCR274   TM    TEMPOP+1,MTSTDEF        MTST?
         BNO   TCR276                  BR NO
         MVC   IPARM1,TEMPOP+1         SET MTST OPTION
         TM    TEMPOP+2,NOED           ALL EDIT OPTIONS OFF
         BNZ   TCR284                  BR NO
         CLC   TEMPOP+4(2),ZEROS       MINLN SPECIFIED
         BNE   TCR284                  BR YES
         TM    TEMPOP+1,USERTAB1       USER TRANSLATE TABLE
         BO    TCR289                  BR YES
         TM    TEMPOP+1,TRANSOP        ANY TRANSLATE OPTION SPECIFIED
         BNZ   TCR289                  BR YES
         OI    IPARM1,TRANDEF          SET TRANS=STDUC DEFAULT
         B     TCR289                  BR TO RETURN
         SPACE 1
*        SET AND TEST MTDI OPTIONS
         SPACE 1
TCR276   OI    IPARM1,MTDIDEF          SET MTDI OPTION
         TM    TEMPOP+1,TRANSOP        ALL TRANSLATE OPTIONS OFF
         BNZ   TCR284                  BR NO
         TM    TEMPOP+2,NOEDIT         NOEDIT SPECIFIED
         BO    TCR279                  BR YES
         TM    TEMPOP+2,VEROP          VERCHK SPECIFIED
         BNZ   TCR278                  BR YES
         OI    IPARM2,VERDEF           SET VERCHK=NOCHK DEFAULT
TCR278   TM    TEMPOP+2,EDITOP         ANY EDIT OPTION
         BNZ   TCR289                  BR YES
         OI    IPARM2,EDDEF            SET EDIT=EDITD DEFAULT
         B     TCR289                  BR TO RETURN
TCR279   CLC   TEMPOP+4(2),ZEROS       MINLN SPECIFIED
         BNE   TCR284                  BR YES
         TM    TEMPOP+2,VEROP          VERCHK SPECIFIED
         BZ    TCR289                  BR YES
         SPACE 1
*        SET ERROR INDICATORS
         SPACE 1
TCR284   MVI   MSG08,MSGREQ            SET INCONSISTENT OPTIONS MSG
*                                        INDICATOR
         OI    STATUS,INCONOPT         SET INVALID STMT INDICATOR
         MVI   RETCODE,CODE12          SET RETURN CODE 12
         SPACE 1
TCR289   L     RETREG,SAVE200          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - TEST INVALID REPLACE PARM.'
*        REPLACE PARAMETER VALIDATION - INVALID SPECIFICATION WITH
*        EDITING OR TRANSLATION
         SPACE 1
*        THIS ROUTINE COMPARES THE REPLACE PARAMETER WHICH HAS PREV-
*        IOUSLY BEEN VALIDATED FOR FORMAT ERRORS TO ONE OF TWO TABLES
*        (TRANRPTB OR EDITRPTB)  WHICH CONTAIN HEXADECIMAL CHARACTERS
*        WHICH MAY NOT BE USED WITH TRANSLATION OR EDITING. IT ALSO
*        DOES A HIGH-LOW COMPARISON ON THE HEXADECIMAL CHARACTERS
*        '80'-'FF' WITH TRANSLATION AND '71'-'78' WITH EDITING.
         SPACE 1
*        THIS ROUTINE ASSUMES THAT THE PL CODES ARE REPRESENTED
*        BY '01' TO '08' AND ARE IN ASCENDING COLLATING SEQUENCE.
*        IT ALSO ASSUMES THAT HEXADECIMAL CODES '80' TO 'FF' ARE
*        INVALID WITH TRANSLATION AND ARE IN ASCENDING COLLATING
*        SEQUENCE.  ANY REDEFINITION OF THESE VALUES MAY NECESSITATE
*        MODIFICATION TO THIS ROUTINE.
         SPACE 2
TCR290   ST    RETREG,SAVE200          SAVE RETURN ADDRESS
         TM    IPARM1,TRANSPEC         TRANSLATE SPECIFIED
         BZ    TCR294                  BR NO
         SPACE 1
*        TRANSLATE TEST
         SPACE 1
         CLI   REPL,SRC                COMPARE TO SRC CODE
         BNL   TCR296                  BR HIGH OR EQUAL
TCR291   LA    TABREG,TRANRPTB         LOAD ADDRESS OF INVALID
*                                        TRANSLATE REPLACE CHARACTERS
         XR    CNTREG,CNTREG
         IC    CNTREG,TRANRPLN         INSERT COUNT
         SPACE 1
*        TABLE COMPARISON
         SPACE 1
TCR292   CLC   REPL(1),0(TABREG)       INVALID CHARACTER
         BE    TCR296                  BR YES
         LA    TABREG,1(TABREG)        INCREMENT TO NEXT ENTRY
         BCT   CNTREG,TCR292           BR IF ALL ENTRIES NOT TESTED
         B     TCR298                  BR TO RETURN
         SPACE 1
*        EDIT TEST
         SPACE 1
TCR294   TM    IPARM2,EDITSPEC         EDIT SPECIFIED
         BZ    TCR298                  BR NO
         CLI   REPL,P1                 COMPARE TO P1 CODE
         BL    TCR295                  BR LOW
         CLI   REPL,P8                 COMPARE TO P8 CODE
         BNH   TCR296                  BR LOW OR EQ
TCR295   LA    TABREG,EDITRPTB         LOAD ADDRESS OF INVALID EDIT
*                                        REPLACE CHARACTERS
         XR    CNTREG,CNTREG           CLEAR COUNT REGISTER
         IC    CNTREG,EDITRPLN         INSERT COUNT
         B     TCR292                  BRANCH TO TABLE COMPARISON
         SPACE 1
*        SET ERROR INDICATORS
         SPACE 1
TCR296   MVI   MSG07,MSGREQ            SET INCONSISTENT REPLACE
*                                        PARAMETER INDICATOR
         OI    STATUS,INVSTMT          SET INVALID STATEMENT INDICATOR
TCR298   L     RETREG,SAVE200          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - END OF CARD VALIDATION'
*        END OF CARD VALIDATION ROUTINE
         SPACE 1
*        THIS ROUTINE VALIDATES THE END OF A CARD.  IT CALLS
*        VALIDATION ROUTINES FOR ANY PARAMETERS THAT MAY NOT HAVE
*        HAVE BEEN VALIDATED.   IF THE SAVE CHARACTER WAS A COMMA OR
*        IF COLUMN 72    CONTAINS A NON-BLANK CHARACTER, THE ROUTINE
*        SETS CONTINUATION EXPECTED INDICATORS.  IF COLUMN 72 IS
*        NON-BLANK AND THE LAST SAVE CHARACTER WAS NOT A COMMA,
*        COMMENTS ARE EXPECTED.
         SPACE 2
TCR330   ST    RETREG,SAVE300          STORE RETURN ADDRESS
         CLI   SVCHAR,COMMA            SAVE CHARACTER COMMA
         BE    TCR334                  BR YES
         CLI   0(COLUMNRG),BLANK       COLUMN 72 BLANK
         BNE   TCR336                  BR NO
         CLI   SVCHAR,SPACE            SAVE CHARACTER SPACE
         BE    TCR332                  BR YES
         LH    R2,PARMCNT              LOAD COUNT
         LA    R2,2(R2)                ADD PARAMETER VALUE
         STH   R2,PARMCNT              REPLACE COUNT
         BAL   RETREG,TCR410           VALIDATE LAST PARAMETER
TCR332   OI    STATUS,VALTERM          SET VALID STMT TERMINATION
*                                        INDICATOR
TCR333   L     RETREG,SAVE300          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
TCR334   OI    STATUS,CONTCD           SET CONTINUATION EXPECTED
*                                        INDICATOR - NO COMMENTS
         B     TCR333                  BR TO RETURN
TCR336   CLI   SVCHAR,SPACE            SAVE CHARACTER SPACE
         BE    TCR337                  BR YES
         LH    R2,PARMCNT              LOAD VALIDATION COUNT
         LA    R2,2(R2)                ADD PARAMETER VALUE
         STH   R2,PARMCNT              REPLACE COUNT
         BAL   RETREG,TCR410           VALIDATE LAST PARAMETER
TCR337   OI    STATUS,CONTCOMM         SET CONTINUATION EXPECTED
*                                        INDICATOR - COMMENTS
         B     TCR333                  BRANCH TO RETURN
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - COMMA ROUTINE'
*        COMMA ROUTINE - PARAMETER VALIDATION
         SPACE 1
*        THIS ROUTINE TESTS WHETHER THE LAST PARAMETER HAS BEEN
*        VALIDATED PREVIOUSLY.  IF IT HAS NOT, IT CALLS THE PARAMETER
*        VALIDATION ROUTINE (TCR410) AND THEN SETS THE SAVE CHARACTER
*        TO COMMA.
         SPACE 1
TCR350   ST    RETREG,SAVE300          STORE RETURN ADDRESS
         CLI   SVCHAR,COMMA            PARAMETER FOUND
         BE    TCR359                  BR NO
         LH    R2,PARMCNT              LOAD VALIDATION COUNT
         LA    R2,2(R2)                ADD  COMMA INDICATOR VALUE
         STH   R2,PARMCNT              REPLACE COUNT
         BAL   RETREG,TCR410           VALIDATE PARAMETER
         MVI   SVCHAR,COMMA            TRANSFER COMMA TO SAVE CHARACTER
TCR359   L     RETREG,SAVE300          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - SPACE ROUTINE'
*        SPACE ROUTINE - PARAMETER VALIDATION
         SPACE 1
*        THIS ROUTINE CALLS THE PARAMETER VALIDATION ROUTINE
*        (TCR410) IF THE LAST PARAMETER WAS NOT VALIDATED.  IT
*        SETS THE SAVE CHARACTER TO BLANK AND SETS THE INPUT AREA
*        POINTER TO COLUMN 72 TO BYPASS ANY INTERVENING COMMENTS.
         SPACE 2
TCR360   ST    RETREG,SAVE300          SAVE RETURN ADDRESS
         CLI   SVCHAR,COMMA            PARAMETER FOUND?
         BE    TCR362                  BR NO
         LH    R2,PARMCNT              LOAD VALIDATION COUNT
         LA    R2,2(R2)                ADD COMMA INDICATOR VALUE
         STH   R2,PARMCNT              STORE COUNT
         BAL   RETREG,TCR410           VALIDATE PARAMETER
         MVI   SVCHAR,SPACE            TRANSFER SPACE TO SAVE CHARACTER
TCR362   L     COLUMNRG,CARDEND        SET INPUT AREA POINTER TO
*                                        COLUMN 72
         L     RETREG,SAVE300          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - EQUAL SIGN ROUTINE'
*        EQUAL SIGN ROUTINE - KEYWORD VALIDATION
         SPACE 1
*        THIS ROUTINE COMPARES THE KEYWORD WHICH HAS JUST BEEN
*        ASSEMBLED TO EACH ENTRY OF THE KEYWORD-PARAMETER TABLE
*        ASSOCIATED WITH THE PREVIOUSLY IDENTIFIED OPERATOR.
*        IF IT DOES NOT FIND A MATCH, IT SETS MESSAGE AND ERROR
*        INDICATORS AND FLUSHES ANY PARAMETER FOLLOWING THIS KEYWORD.
         SPACE 2
TCR370   ST    RETREG,SAVE300          SAVE RETURN ADDRESS
         CLI   PARMCNT+1,HEXZERO       COUNT ZERO
         BNE   TCR376                  BR NO
         MVI   PARMCNT+1,HEXONE        SET COUNT EQUAL TO ONE
         LR    KEYRG,PARMTAB1          LOAD PARAMETER TABLE ADDRESS
         LA    KEYRG,16(KEYRG)         SET KEYWORD-PARAMETER TABLE
*                                        POINTER AT FIRST KEYWORD
         LR    DATARG,PARMTAB2         LOAD PARAMETER TABLE ADDRESS
         LA    DATARG,8(DATARG)        SET DATA TABLE POINTER AT FIRST
*                                        KEYWORD
TCR372   CL    KEYRG,12(PARMTAB1)      TABLE END
         BNL   TCR376                  BR YES
         CLC   HOLDAREA(8),0(KEYRG)    KEYWORD FOUND
         BE    TCR374                  BR YES
         AH    KEYRG,10(PARMTAB1)      INCREMENT KEYWORD TABLE POINTER
         AH    DATARG,2(PARMTAB2)      INCREMENT DATA TABLE POINTER
         B     TCR372                  LOOP
TCR374   TM    1(DATARG),KEYFND        KEYWORD PROCESSED PREVIOUSLY?
         BO    TCR375                  BR YES
         OI    1(DATARG),KEYFND        INDICATE KEYWORD FOUND
         L     HOLDREG,AHOLD           RE-INITIALIZE HOLD AREA POINTER
         MVC   HOLDAREA(8),BLANKS      CLEAR HOLD AREA
         B     TCR379                  BR TO RETURN
         SPACE 1
*        SET ERROR INDICATORS
         SPACE 1
TCR375   MVI   MSG08,MSGREQ            SET MSG INDICATOR
         B     TCR377                  BR TO CONTINUE PROCESSING
TCR376   MVI   MSG04,MSGREQ            SET INCONSISTENT OPTIONS
*                                        MSG INDICATOR
TCR377   OI    STATUS,INVSTMT          SET INVALID STMT INDICATOR
         L     HOLDREG,AHOLD           RE-INITIALIZE HOLD AREA POINTER
         MVC   0(8,HOLDREG),BLANKS     CLEAR HOLD AREA
         MVI   PARMCNT+1,HEXZERO       CLEAR PARAMETER COUNT
         MVI   SRCHCHAR,COMMA          TRANSFER COMMA TO SEARCH CHAR
         MVI   SRCHCHAR+1,SPACE        SET SECOND SEARCH CHARACTER
*                                        TO SPACE
         OI    OPTIND,TWOCHAR          SET TWO CHARACTER SEARCH IND
         BAL   RETREG,TCR620           FLUSH TO COMMA
TCR379   L     RETREG,SAVE300          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - PARAMETER VALIDATION'
*        PARAMETER VALIDATION
         SPACE 1
*        THIS ROUTINE COMPARES THE PARAMETER WHICH HAS JUST BEEN
*        ASSEMBLED TO EACH OF THE PARAMETERS ASSOCIATED WITH THE
*        PREVIOUSLY IDENTIFIED OPERATOR AND KEYWORD.  IF IT FINDS
*        A MATCH, IT USES INFORMATION CONTAINED IN THE DATA TABLE
*        TO SET OPTIONS IN THE TEMPORARY OPTION TABLE (TEMPOP).
*        IF IT DOES NOT FIND A MATCH, IT CHECKS WHETHER OR NOT A
*        SPECIAL VALIDATION ROUTINE HAS BEEN INDICATED.  IF ONE
*        HAS, IT CALLS THIS ROUTINE TO CONTINUE VALIDATION.
         SPACE 2
TCR410   ST    RETREG,SAVE400          SAVE RETURN ADDRESS
         CLI   PARMCNT+1,VALNO         ONE KEYWORD AND ONE PARAMETER
         BNE   TCR418                  BR NO
         ST    R7,SAVEREG7             SAVE REGISTER
         LR    WKREG,KEYRG             LOAD KEYWORD ADDRESS
         AH    WKREG,10(PARMTAB1)      DEVELOP ENTRY ENDING ADDRESS
         LR    DATAHOLD,DATARG         SAVE KEYWORD ADDRESS
TCR412   LA    KEYRG,8(KEYRG)          INCREMENT TO NEXT PARAMETER
         LA    DATARG,2(DATARG)        INCREMENT DATA TABLE POINTER
         CLR   KEYRG,WKREG             END OF TABLE ENTRY?
         BNL   TCR416                  BR YES
         CLC   0(8,KEYRG),BLANKS       NEXT TABLE ENTRY BLANK
         BE    TCR416                  BR YES
         CLC   0(8,KEYRG),HOLDAREA     COMPARE INPUT PARAMETER
         BNE   TCR412                  BR UNEQUAL
         L     R7,SAVEREG7             RESTORE REGISTER
         XR    R2,R2                   CLEAR INDEX REGISTER
         IC    R2,0(DATARG)            INSERT OPTION TABLE POINTER
         IC    R3,1(DATARG)            INSERT OPTION ON MASK
         XR    R1,R1                   CLEAR OR-ING REGISTER
         IC    R1,TEMPOP(R2)           INSERT OPTION TABLE BYTE
         OR    R3,R1                   OR CURRENT AND PREVIOUS IND'S
         STC   R3,TEMPOP(R2)           SET OPTION INDICATOR IN TABLE
TCR414   MVI   PARMCNT+1,HEXZERO       CLEAR PARAMETER COUNT
         L     HOLDREG,AHOLD           SET HOLD AREA POINTER AT
*                                        LOCATION 1
         MVC   0(8,HOLDREG),BLANKS     CLEAR HOLD AREA
         L     RETREG,SAVE400          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
TCR416   L     R7,SAVEREG7             RESTORE REGISTER
         CLI   0(DATAHOLD),HEXZERO     SPECIAL ROUTINE PRESENT
         BE    TCR418                  BR NO
         LA    RETREG,TCR414           LOAD RETURN ADDRESS REGISTER
         XR    R2,R2                   CLEAR INDEX REGISTER
         IC    R2,0(DATAHOLD)          INSERT BRANCH TABLE DISP.
         B     TCR417(R2)              BRANCH TO REQUIRED ROUTINE
TCR417   B     TCR414                  NULL
         B     TCR510                  USER TRANSLATE TABLE
         B     TCR520                  MINLN-MAXLN
         B     TCR530                  REPLACE
TCR418   MVI   MSG05,MSGREQ            SET INVALID PARAMETER MSG IND
         OI    STATUS,INVSTMT          SET INVALID STMT INDICATOR
         B     TCR414                  BR TO CONTINUE
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - USER RTN VALIDATION'
*        USER EXIT ROUTINE OR TRANSLATE TABLE NAME VALIDATION
         SPACE 1
*        THIS ROUTINE SETS INDICATORS AND STORES ROUTINE NAMES.
         SPACE 2
TCR510   TM    2(DATAHOLD),HEXONE      USER TRANSLATE TABLE
         BNO   TCR512                  BR NO
         SPACE 1
*        USER TRANSLATE TABLE
         SPACE 1
         MVC   USERNAME+USERDISP(8),HOLDAREA  STORE ROUTINE NAME
         OI    OPTABLE,USERTAB         SET OPTION INDICATOR
         OI    TEMPOP+1,USERTAB1       SET OPTION INDICATOR
         BR    RETREG                  RETURN
         SPACE 1
*        USER EXIT ROUTINE
         SPACE 1
TCR512   XR    R2,R2                   CLEAR INDEX REGISTER
         IC    R2,2(DATAHOLD)          INSERT INDEX VALUE
         LA    R3,OPTABLE              LOAD OPTION TABLE ADDRESS
         AR    R3,R2                   ADD INDEX VALUE BASE TABLE ADDR
         OC    0(1,R3),3(DATAHOLD)     SET INDICATOR ON
         LH    R2,4(DATAHOLD)          LOAD USERNAME TABLE ENTRY
         LA    R3,USERNAME             LOAD NAME TABLE ADDRESS
         AR    R3,R2                   FIND NAME TABLE ENTRY
         MVC   0(8,R3),HOLDAREA        STORE ROUTINE NAME
         BR    RETREG                  RETURN
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - MINLN-MAXLN VALIDATION'
*        MINLN - MAXLN VALIDATION ROUTINES
         SPACE 1
*        THIS ROUTINE CHECKS THAT THE MINLN OR MAXLN PARAMETER
*        IS FIVE OR FEWER CHARACTERS AND IS NUMERIC.  IF IT IS,
*        IT CONVERTS IT FROM EBCDIC TO PACKED DECIMAL AND THEN
*        TO BINARY AND STORES IT IN THE OPTION TABLE.
         SPACE 1
*        THIS ROUTINE ASSUMES THAT THE NUMERIC DIGITS 0-9 ARE IN
*        ASCENDING COLLATING SEQUENCE AND HAVE NO INTERSPERSED
*        CONTROL CHARACTERS.  ANY REDEFINITION OF THESE VALUES MAY
*        NECESSITATE MODIFICATION TO THIS ROUTINE.
         SPACE 2
TCR520   ST    R4,SAVEREG4             SAVE REGISTER 4
         XR    CHARCT,CHARCT           ZERO CHARACTER COUNT
         L     HOLDREG,AHOLD           INITIALIZE HOLD AREA POINTER
TCR522   CLI   0(HOLDREG),BLANK        NEXT CHARACTER BLANK
         BE    TCR523                  BR YES
         CLI   0(HOLDREG),NINE         TEST NUMERIC
         BH    TCR528                  BR NO
         CLI   0(HOLDREG),ZERO
         BL    TCR528                  BR NO
         LA    CHARCT,1(CHARCT)        INCREMENT CHARACTER COUNT
         CL    CHARCT,HEXSIX           SIXTH CHARACTER
         BNL   TCR528                  BR YES
         LA    HOLDREG,1(HOLDREG)      INCREMENT HOLD AREA POINTER
         B     TCR522                  LOOP
TCR523   SH    CHARCT,ONE              DEVELOP LENGTH INDICATOR
         LA    CHARCT,PAKLN7(CHARCT)   INSERT PACKED LENGTH 7
         EX    CHARCT,TCR525           PACK MAXLN OR MINLN
         B     TCR526                  BYPASS PACK INSTRUCTION
         SPACE 1
TCR525   PACK  WORK1(1),HOLDAREA(1)    PACK MAXLN OR MINLN
         SPACE 1
TCR526   NI    WORK1,PLUS              CHANGE SIGN TO PLUS
         CVB   R2,WORK1                CONVERT MINLN OR MAXLN TO BINARY
         XR    R3,R3                   CLEAR INDEX REGISTER
         IC    R3,2(DATAHOLD)          INSERT DISPLACEMENT
         STH   R2,TEMPOP(R3)           STORE MINLN OR MAXLN IN TABLE
         L     R4,SAVEREG4             RESTORE REGISTER 4
         BR    RETREG                  BR TO RETURN
TCR528   MVI   MSG05,MSGREQ            SET INVALID PARAMETER MSG
*                                        INDICATOR
         OI    STATUS,INVSTMT          SET INVALID STATEMENT INDICATOR
         L     R4,SAVEREG4             RESTORE REGISTER 4
         BR    RETREG                  RETURN
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - REPLACE PARM. VALIDATION'
*        REPLACE PARAMETER VALIDATION
         SPACE 1
*        THIS ROUTINE VERIFIES THAT THE REPLACE PARAMETER IS OF
*        THE FORM X'CC'  WHERE C IS EITHER 0-9 OR A-F.  IF IT IS, THE
*        ROUTINE CONVERTS IT FROM EBCDIC TO HEXADECIMAL AND STORES IT
*        IN THE OPTION TABLE.
         SPACE 1
*        THIS ROUTINE ASSUMES
*
*             A) THAT THE EBCDIC CHARACTERS A-F AND 0-9 ARE REPRE-
*                SENTED INTERNALLY BY X'C1' - X'CF' AND X'F0' -
*                X'F9', RESPECTIVELY.  THIS PARAMETER IS USED TO
*                SPECIFY THE HEXADECIMAL REPRESENTATION OF THE CHAR-
*                ACTERS TO BE USED FOR REPLACING DATA CHECK BYTES.
*                FOR EXAMPLE, THE INPUT CHARACTERS 'D9', REPRESENTED
*                INTERNALLY AS THE TWO BYTES '11000100''11111001'
*                WOULD BE CONVERTED TO THE ONE BYTE '11011001'.
*
*             B) THAT THE CHARACTERS A-Z AND 0-9 ARE IN ASCENDING
*                COLLATING SEQUENCE AND HAVE NO CONTROL CHARACTERS
*                INTERSPERSED AMONG EITHER GROUP.
         SPACE 1
*        ANY REDEFINITION OF THE VALUES SPECIFIED IN A  ABOVE
*        WILL NECESSITATE DEVELOPMENT OF A DIFFERENT CONVERSION
*        ALGORITHM.
*
*        REDEFINITION OF B ABOVE MAY REQUIRE DIFFERENT VALIDATION
*        PROCEDURES.
         SPACE 2
TCR530   ST    RETREG,SAVE500          SAVE RETURN ADDRESS
         L     HOLDREG,AHOLD           INITIALIZE HOLD AREA ADDRESS
         CLI   0(HOLDREG),ALPHAX       FIRST CHAR X?
         BNE   TCR538                  BR NO
         CLI   1(HOLDREG),QUOTE        SECOND CHARACTER QUOTE
         BNE   TCR538                  BR NO
         LA    HOLDREG,2(HOLDREG)      SET POINTER TO 3RD CHARACTER
         BAL   RETREG,TCR540           TEST 0-9 OR A-F
TCR532   LA    HOLDREG,1(HOLDREG)      SET POINTER TO 4TH CHARACTER
         BAL   RETREG,TCR540           TEST 0-9 OR A-F
         CLI   1(HOLDREG),QUOTE        FIFTH CHARACTER QUOTE
         BNE   TCR538                  BR NO
         SPACE 1
*        TRANSLATE AND STORE REPLACE BYTE
         SPACE 1
         TM    HOLDAREA+3,HEXF         NUMERIC
         BO    TCR535                  BR YES
TCR534   LH    R2,CBASE                DEVELOP HEX VALUE FOR
         AH    R2,HOLDAREA+2             ALPHABETIC CHARACTER
         STC   R2,HOLDAREA+3
TCR535   TM    HOLDAREA+2,HEXF         NUMERIC
         BO    TCR536                  BR YES
         LH    R2,CBASE                DEVELOP HEX VALUE FOR
*                                        ALPHABETIC CHARACTER
         MVC   HALFST+1(1),HOLDAREA+2  MOVE TO HALF WORD STORE
         AH    R2,HALFST
         STC   R2,HOLDAREA+2
TCR536   MVO   HOLDAREA+3(1),HOLDAREA+2(1)  INSERT FIRST DIGIT IN
*                                      LEFT FOUR BITS OF SECOND DIGIT
         XR    R2,R2                   CLEAR INDEX REGISTER
         IC    R2,2(DATAHOLD)          INSERT OPTION TABLE INDEX
         XR    R3,R3                   CLEAR WORK REGISTER
         IC    R3,HOLDAREA+3           MOVE REPLACE BYTE TO TEMPORARY
         STC   R3,TEMPOP(R2)             OPTION TABLE
         XR    R2,R2                   SET REGISTER TO ZEROES
         CLR   R3,R2                   REPLACE EQUAL TO X'00'
         BNE   TCR539                  BRANCH NO
         OI    OPTIND,REPLZERO         SET REPLACE EQUAL TO ZERO IND
         B     TCR539                  BR TO RETURN
TCR538   MVI   MSG05,MSGREQ            SET INVALID PARAMETER MSG
*                                        INDICATOR
         OI    STATUS,INVSTMT          SET INVALID STATEMENT INDICATOR
TCR539   L     RETREG,SAVE500          RETURN
         BR    RETREG                  RETURN
         SPACE 2
*        TEST CHARACTER 0-9 OR A-F
         SPACE 1
TCR540   CLI   0(HOLDREG),NINE         GREATER THAN 9
         BH    TCR538                  BR YES
         CLI   0(HOLDREG),ALPHAA       LOWER THAN A
         BL    TCR538                  BR YES
         CLI   0(HOLDREG),ZERO         LOWER THAN ZERO
         BNL   TCR542                  BR NO
         CLI   0(HOLDREG),ALPHAF       HIGHER THAN F
         BH    TCR538                  BR YES
TCR542   BR    RETREG                  RETURN
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - CONTINUATION CARD VALID.'
*        CONTINUATION CARD READ AND VALIDATION
         SPACE 1
*        THIS ROUTINE CALLS THE READ ROUTINE TO READ CONTINUATION
*        CARDS AND VALIDATES THEM FOR FORMAT ERRORS.
         SPACE 2
TCR610   ST    RETREG,SAVE610          SAVE RETURN ADDRESS
TCR611   BAL   RETREG,TCR710           READ A CARD
         TM    STATUS,EOFSYSIN         EOF - SYSIN?
         BO    TCR612                  BR YES
         CLC   0(3,COLUMNRG),BLANKS    COLUMNS 1-3 BLANK
         BNE   TCR615                  BR NO
         TM    STATUS,COMMIMMA         COMMENTS IMMATERIAL
         BO    TCR619                  BR YES
         TM    STATUS,CONTCOMM         COMMENTS EXPECTED
         BO    TCR613                  BR YES
         CLC   3(13,COLUMNRG),BLANKS   COLUMNS 4-16 BLANK
         BNE   TCR619                  BR NO
         B     TCR615                  BR TO SET ERROR INDICATORS
TCR612   OI    STATUS,INVTERM          SET INVALID TERMINATION INDICATR
         MVI   MSG09,MSGREQ            SET INVALID CONTINUATION
*                                        INDICATOR
         B     TCR617                  BR TO RETURN
TCR613   CLI   COLUMN72(COLUMNRG),BLANK   COLUMN 72 BLANK
         BNE   TCR611                  BR NO
         OI    STATUS,VALTERM          SET VALID TERMINATION
*                                        INDICATOR
         B     TCR619                  BR TO RETURN
TCR615   MVI   MSG09,MSGREQ            SET INVALID CONTINUATION
*                                        INDICATOR
         BAL   RETREG,TCR630           FLUSH CARDS THIS STATEMENT
TCR617   OI    STATUS,INVSTMT          SET INVALID STMT INDICATOR
TCR619   L     RETREG,SAVE610          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - CHARACTER FLUSH ROUTINE'
*        CHARACTER FLUSH ROUTINE
         SPACE 1
*        THIS ROUTINE FLUSHES CHARACTERS UNTIL IT FINDS THE
*        REQUESTED CHARACTER.  THE FOLLOWING REQUESTS MAY BE MADE:
         SPACE 1
*        1)  ANY NON-BLANK  - INDICATED BY SETTING SRCHCHAR TO X'FF'
*        2)  ONE SEARCH CHARACTER - INDICATED BY SETTING BIT 2 OF
*            OPTIND TO ZERO AND MOVING THE REQUESTED CHARACTER TO
*            THE FIRST BYTE OF SRCHCHAR
*        3)  TWO SEARCH CHARACTERS - INDICATED BY SETTING BIT 2 OF
*            OPTIND TO ONE AND MOVING THE TWO REQUESTED CHARACTERS TO
*            BYTES ONE AND TWO OF SRCHCHAR.
          SPACE 1
*        IF THE ROUTINE DOES NOT FIND  THE REQUESTED CHARACTER(S), IT
*        SETS AN INVALID STATEMENT INDICATOR.
         SPACE 2
TCR620   ST    RETREG,SAVE620          SAVE RETURN ADDRESS
         MVI   PARMCNT+1,HEXZERO       CLEAR VALIDATION COUNT
         NI    OPTIND,X'FF'-NONBLIND   CLEAR NON-BLANK INDICATOR
         TM    SRCHCHAR,NONBLANK       SEARCH CHARACTER NON-BLANK
         BNO   TCR621                  BR NO
         OI    OPTIND,NONBLIND         SET NON-BLANK INDICATOR
TCR621   CL    COLUMNRG,CARDEND        COLUMN 72
         BNL   TCR628                  BR YES
         TM    OPTIND,NONBLIND         NON-BLANK REQUESTED
         BO    TCR625                  BR YES
         CLC   0(1,COLUMNRG),SRCHCHAR  SEARCH CHARACTER FOUND
         BE    TCR629                  BR YES
         TM    OPTIND,TWOCHAR          TWO SEARCH CHARACTERS REQUESTED
         BNO   TCR623                  BR NO
         CLC   0(1,COLUMNRG),SRCHCHAR+1  COMPARE TO SECOND SEARCH
*                                          CHARACTER
         BE    TCR629                  BR EQUAL
TCR623   LA    COLUMNRG,1(COLUMNRG)    INCREMENT TO NEXT CHARACTER
         B     TCR621                  LOOP
TCR625   CLI   0(COLUMNRG),SPACE       CHARACTER NON-BLANK
         BE    TCR623                  BR NO
         B     TCR629                  BR TO RETURN
TCR628   OI    STATUS,INVTERM          SET INVALID TERMINATION IND
TCR629   NI    OPTIND,X'FF'-TWOCHAR    CLEAR TWO CHARACTER INDICATOR
         L     RETREG,SAVE620          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - STATEMENT FLUSH ROUTINE'
*        STATEMENT FLUSH ROUTINE
         SPACE 1
*        THIS ROUTINE READS ALL RECOGNIZABLE CONTINUATION CARDS.
         SPACE 2
TCR630   ST    RETREG,SAVE630          SAVE RETURN ADDRESS
         L     LNKREG15,VTCR220        LOAD MESSAGE WRITER ADDRESS
         BALR  RETREG,LNKREG15         WRITE MESSAGES
         OI    STATUS,INVTERM          SET INVALID TERMINATION INDICATR
         OI    STATUS,INVSTMT          SET INVALID STMT INDICATOR
         L     COLUMNRG,CARDAREA       INITIALIZE INPUT AREA POINTER
TCR632   CLI   COLUMN72(COLUMNRG),BLANK   COLUMN 72 BLANK
         BE    TCR634                  BR YES
         BAL   RETREG,TCR710           READ A CARD
         TM    STATUS,EOFSYSIN         EOF - SYSIN
         BNO   TCR632                  BR NO
TCR634   L     RETREG,SAVE630          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - SYSIN DATA SET READ RTN'
*        SYSIN DATA SET READ ROUTINE
         SPACE 1
*        THIS ROUTINE READS AND PRINTS EACH CONTROL CARD.  IT
*        CALLS THE MESSAGE WRITER SINGLE MESSAGE ROUTINE TO DO
*        THE PRINTING.
*        IF A PERMANENT I/O ERROR OCCURS ON SYSIN, THE ROUTINE SETS
*        THE END OF FILE INDICATOR.  THIS CAUSES PROCESSING TO STOP
*        AT ALL CALLING LEVELS.
         SPACE 2
TCR710   ST    RETREG,SAVE700          SAVE RETURN ADDRESS
         L     R2,CARDAREA             LOAD CARD AREA ADDRESS
         GET   SYSINDCB,(2)            READ CONTROL STATEMENT
         TM    ERRIND,SYSINERR         PERMANENT I/O ERROR SYSIN
         BO    TCR714                  BR YES
         LR    COLUMNRG,R2             SET INPUT AREA POINTER TO
*                                        COLUMN 1
         TM    DUMMYIND,DUMSYSP        SYSPRINT DUMMY
         BO    TCR719                  BR YES
         LA    R2,CDLENGTH             LOAD  CARD LENGTH
         STC   R2,MSGLN                STORE MSG LENGTH
         MVC   MSGAD,CARDAREA          STORE MSG ADDRESS
         OI    PRTRTN,RTN310           SET SINGLE MSG INDICATOR
         L     LNKREG15,VTCR220        LOAD MSG WRITER ADDRESS
         BALR  RETREG,LNKREG15         PRINT CONTROL STATEMENT
         B     TCR719                  BR TO RETURN
         SPACE 1
*        END OF FILE
         SPACE 1
TCR714   OI    STATUS,EOFSYSIN         SET EOF INDICATOR
TCR719   L     RETREG,SAVE700          LOAD RETURN ADDRESS
         BR    RETREG                  RETURN
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - LITERALS AND WORK AREAS'
*        LITERALS
         SPACE 2
NULL     EQU   *                       NULL LABEL
COMMA    EQU   C','                    COMMA
SPACE    EQU   C' '                    SPACE
EQUALSN  EQU   C'='                    EQUAL SIGN
PARENL   EQU   C'('                    LEFT PARENTHESIS
PARENR   EQU   C')'                    RIGHT PARENTHESIS
HEXZERO  EQU   X'00'                   HEXADECIMAL
DOUBLESP EQU   X'F0'                   DOUBLE SPACE CONTROL CHARACTER
HEXONE   EQU   X'01'                   HEXADECIMAL ONE
ZERO     EQU   X'F0'                   EBCDIC ZERO
NINE     EQU   X'F9'                   EBCDIC NINE
BLANK    EQU   X'40'                   BLANK
QUOTE    EQU   C''''                   QUOTE
ALPHAA   EQU   C'A'                    ALPHABETIC A
ALPHAF   EQU   C'F'                    ALPHABETIC F
HEXF     EQU   X'F0'                   HEXADECIMAL F
PLUS     EQU   X'FC'                   PLUS SIGN
ALPHAX   EQU   C'X'                    ALPHABETIC X
ALPHAZ   EQU   C'Z'                    ALPHABETIC Z
MTDIDEF  EQU   X'40'                   MTDI DEFAULT
MTSTDEF  EQU   X'80'                   MTST DEFAULT
CDLENGTH EQU   80                      CARD LENGTH
KEYFND   EQU   X'01'                   KEY FOUND
VALNO    EQU   X'03'                   CORRECT VALIDATION COUNT
USERTAB1 EQU   X'04'                   USER TRANSLATE TABLE INDICATED
NOEDIT   EQU   X'20'                   EDIT=NOEDIT
EDITOP   EQU   X'E0'                   ALL EDIT OPTIONS
VEROP    EQU   X'18'                   ALL VERCHK OPTIONS
ERREXIT  EQU   X'04'                   ERROR EXIT INDICATED
ERROP    EQU   X'06'                   ALL ERROPT OPTIONS
ANALERR  EQU   X'04'                   ERROR IN CONTROL ANALYZER
SYSINERR EQU   X'02'                   PERMANENT I/O ERROR SYSIN
DUMSYSP  EQU   X'02'                   DUMMY SYSPRINT
MSGREQ   EQU   X'FF'                   MSG REQUIRED
CODE12   EQU   X'0C'                   RETURN CODE 12
NONBLANK EQU   X'FF'                   SEARCH FOR NON-BLANK CHARACTER
USERTAB  EQU   X'02'                   USER TRANSLATE TABLE INDICATED
RTN310   EQU   X'02'                   SINGLE MSG INDICATOR
EDDEF    EQU   X'80'                   EDIT DEFAULT
VERDEF   EQU   X'08'                   VERCHK DEFAULT
ERRDEF   EQU   X'04'                   ERROPT DEFAULT
TRANDEF  EQU   X'10'                   TRANS DEFAULT
NOED     EQU   X'F8'                   NO EDIT
TRANSOP  EQU   X'1E'                   ALL TRANS OPTIONS
TRANSPEC EQU   X'1C'                   TRANSLATE
EDITSPEC EQU   X'C0'                   EDIT
P1       EQU   X'71'                   PROGRAM LEVEL 1 CODE
P8       EQU   X'78'                   PROGRAM LEVEL 8 CODE
SRC      EQU   X'80'                   SEARCH CODE
USERDISP EQU   48                      TRANSLATE TABLE NAME DISP
USERDSP2 EQU   88                      TRANSLATE TABLE NAME DISP
COLUMN72 EQU   71                      COLUMN 71 POINTER
PAKLN7   EQU   112                     BINARY 7 IN LEFT HALF OF BYTE
         SPACE 1
*        OPTIND INDICATORS
         SPACE 1
REPLZERO EQU   X'80'                   REPLACE EQUALS ZERO
INVOP    EQU   X'40'                   INVALID OPERATOR
TWOCHAR  EQU   X'20'                   TWO SEARCH CHARACTERS REQUESTED
NONBLIND EQU   X'10'                   NON-BLANK SEARCH INDICATOR
TCRGNPR  EQU   X'04'                   TCRGEN PROCESSED
EXITSPR  EQU   X'08'                   EXITS PROCESSED
GEN2     EQU   X'05'                   BOTH TCRGEN INDICATORS
EXITS2   EQU   X'0A'                   BOTH EXITS INDICATORS
OPTINIT  EQU   X'0C'                   OPTIND INITIALIZATION
         SPACE 1
*        STATUS INDICATORS
         SPACE 1
EOFSYSIN EQU   X'01'                   EOF - SYSIN
INVTERM  EQU   X'02'                   INVALID STATEMENT END
CONTCD   EQU   X'08'                   CONTINUATION EXPECTED
CONTCOMM EQU   X'04'                   CONTINUATION EXPECTED - COMMENTS
INVSTMT  EQU   X'10'                   INVALID STATEMENT
INCONOPT EQU   X'20'                   INCONSISTENT OPTIONS
STATINIT EQU   X'10'                   STATUS BYTE INITIALIZATION
VALTERM  EQU   X'40'                   VALID STATEMENT TERMINATION
STMTTERM EQU   X'42'                   STATEMENT TERMINATION
COMMIMMA EQU   X'80'                   COMMENTS IMMATERIAL
         SPACE 1
*        CONSTANTS
         SPACE 1
         DS    0H                      FORCE HALF-WORD ALIGNMENT
ONE      DC    X'0001'                 ONE
CBASE    DC    X'0009'                 HEX C BASE
ZEROS    DC    X'0000'                 ZEROES
HEXSIX   DC    F'6'                    SIX
HALFST   DC    X'0000'                 HALF WORD STORE
BLANKS   DC    CL13' '                 BLANKS
         SPACE 1
*        SAVE AREAS
         SPACE 1
SAVEAREA DS    18F                     REGISTER SAVE AREA
SAVE200  DS    F                       RETURN ADDRESS SAVE AREA
SAVE300  DS    F                       RETURN ADDRESS SAVE AREA
SAVE400  DS    F                       RETURN ADDRESS SAVE AREA
SAVE500  DS    F                       RETURN ADDRESS SAVE AREA
SAVE610  DS    F                       RETURN ADDRESS SAVE AREA
SAVE620  DS    F                       RETURN ADDRESS SAVE AREA
SAVE630  DS    F                       RETURN ADDRESS SAVE AREA
SAVE700  DS    F                       RETURN ADDRESS SAVE AREA
SAVEREG4 DS    F                       REGISTER 4 SAVE AREA
SAVEREG7 DS    F                       REGISTER 7 SAVE AREA
         EJECT
*        WORK AREAS
         SPACE 1
TEMPOP   DC    D'0'                    TEMPORARY OPTION TABLE
WORK1    DC    PL8'0'                  PACKED MINLN-MAXLN
PARMCNT  DC    X'0000'                 PARAMETER COUNT
HOLDAREA DC    CL9' '                  KEYWOR-PARAMETER HOLD AREA
SRCHCHAR DC    X'0000'                 SEARCH CHARACTER
SVCHAR   DC    X'00'                   SAVE CHARACTER
         SPACE 2
*        ADDRESS CONSTANTS
         SPACE 1
AHOLD    DC    A(HOLDAREA)             HOLD AREA ADDRESS
AHOLD9   DC    A(HOLDAREA+8)           9TH CHARACTER HOLD AREA
SYSEODAD DC    AL3(TCR714)             SYSIN DCB EODAD ADDRESS
         SPACE 1
*        INDICATORS
         SPACE 1
STATUS   DC    X'00'                   STATUS INDICATOR
*
*              BIT 0 - COMMENTS IMMATERIAL
*                  1 - VALID STMT TERMINATION
*                  2 - INCONSISTENT OPTIONS
*                  3 - INVALID  STATEMENT
*                  4 - CONTINUATION EXPECTED - NO COMMENTS
*                  5 - CONTINUATION EXPECTED - COMMENTS
*                  6 - INVALID STMT TERMINATION
*                  7 - END OF FILE - SYSIN DATASET
*
OPTIND   DC    X'00'                   OPTION INDICATORS
*
*              BIT 0 - REPLACE BYTE ZEROES
*                  1 - INVALID OPERATOR
*                  2 - TWO SEARCH CHARACTERS
*                  3 - NON-BLANK SEARCH INDICATOR
*                  4 - EXITS PROCESSED
*                  5 - TCRGEN PROCESSED
*                  6 - EXITS STATEMENT
*                  7 - TCRGEN STATEMENT
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - TABLES'
*        TCRGEN KEYWORD-PARAMETER NAME TABLE
         SPACE 1
*        TO SATISFY WORLD TRADE REQUIREMENTS THE FOLLOWING TCRGEN
*        AND EXITS NAME TABLES MAY BE REPLACED IN ENTIRETY.
*
*
*
*              KEYWORD  DEFAULT    ALTERNATIVES
*             *************************************
*             *TYPE    *MTDI    *MTST    *        *
*             *TRANS   *STDUC   *STDLC   *NOTRAN  *
*             *EDIT    *EDITD   *EDITR   *NOEDIT  *
*             *VERCHK  *NOCHK   *VOKCHK  *        *
*             *ERROPT  *NORMAL  *NOERR   *        *
*             *MINLN   *        *        *        *
*             *MAXLN   *        *        *        *
*             *REPLACE *        *        *        *
*             *************************************
*
*
         DS    0F
TCRGENOP DC    CL8'TCRGEN'             OPERATION
         DS    H                       NOT USED
         DC    X'0020'                 TABLE ENTRY LENGTH - 32 BYTES
         DC    A(TAB1D)                TABLE END ADDRESS
*
TCRGENTB DC    CL8'TYPE',CL8'MTDI',CL8'MTST',CL8' '
         DC    CL8'TRANS',CL8'STDUC',CL8'STDLC',CL8'NOTRAN'
         DC    CL8'EDIT',CL8'EDITD',CL8'EDITR',CL8'NOEDIT'
         DC    CL8'VERCHK',CL8'NOCHK',CL8'VOKCHK',CL8' '
         DC    CL8'ERROPT',CL8'NORMAL',CL8'NOERR',CL8' '
         DC    CL8'MINLN',CL24' '
         DC    CL8'MAXLN',CL24' '
         DC    CL8'REPLACE',CL24' '
*
TAB1D    EQU   *                       TABLE ENDING ADDRESS
*
*        TCRGEN DATA TABLE - EACH ENTRY CORRESPONDS TO ONE
*        ENTRY OF THE KEYWORD-PARAMETER TABLE
*
*
*                 HALFWORD 1
*                    BYTE 1  - EXCEPTIONAL VALIDATION ROUTINE
*                              BRANCH TABLE DISPLACEMENT
*
*                    BYTE 2  - KEYWORD PROCESSED INDICATOR
*                      X'00' - NOT PROCESSED
*                      X'01' - PROCESSED
*
*                 HALFWORDS 2,3,4
*                    BYTE 1  - OPTION TABLE DISPLACEMENT
*                    BYTE 2  - 'ON' MASK
*
TCRDATA  DS    H                       NOT USED
         DC    X'0008'                 ENTRY LENGTH
         DC    A(TAB2D)                TABLE END ADDRESS
         SPACE 1
GENTB2   DC    X'0000',X'0140',X'0180',X'0000'    TYPE
         DC    X'0400',X'0110',X'0108',X'0102'    TRANS
         DC    X'0000',X'0280',X'0240',X'0220'    EDIT
         DC    X'0000',X'0208',X'0210',X'0000'    VERCHK
         DC    X'0000',X'0204',X'0202',X'0000'    ERROPT
         DC    X'0800',X'0400',X'0000',X'0000'    MINLN
         DC    X'0800',X'0600',X'0000',X'0000'    MAXLN
         DC    X'0C00',X'0300',X'0000',X'0000'    REPLACE
         SPACE 1
TAB2D    EQU   *                       TABLE ENDING ADDRESS
         EJECT
*        EXITS KEYWORD NAME TABLE
         SPACE 1
         DS    0F                      FORCE FULL-WORD BOUNDARY
EXITSOP  DC    CL8'EXITS'              OPERATION
         DS    H                       NOT USED
         DC    X'0008'                 ENTRY LENGTH
         DC    A(TAB3D)                TABLE END ADDRESS
         SPACE 1
EXITSTB  DC    CL8'OUTHDR2',CL8'OUTTLR2',CL8'OUTHDR3',CL8'OUTTLR3'
         DC    CL8'OUTREC',CL8'ERROR'
         SPACE 1
TAB3D    EQU   *                       TABLE END ADDRESS
*
*        EXITS DATA TABLE - EACH ENTRY CORRESPONDS TO ONE
*        ENTRY OF THE KEYWORD TABLE
*
*
*                   BYTE 1  - EXCEPTIONAL VALIDATION ROUTINE BRANCH
*                             TABLE ADDRESS
*
*                   BYTE 2  - KEYWORD PROCESSED INDICATOR
*                     X'00' - NOT PROCESSED
*                     X'01' - PROCESSED
*
*                   BYTE 3  - COMMUNICATION TABLE LOCATION
*
*                   BYTE 4  - ON MASK
*
*              BYTES 5 & 6  - NAME TABLE DISPLACEMENT
*
*
EXITDATA DS    H                       NOT USED
         DC    X'0006'                 ENTRY LENGTH
         DC    A(TAB4D)                TABLE END ADDRESS
         SPACE 1
EXITTB   DC    X'0400',X'0080',X'0000'       OUTHDR2
         DC    X'0400',X'0040',X'0008'       OUTTLR2
         DC    X'0400',X'0020',X'0010'       OUTHDR3
         DC    X'0400',X'0010',X'0018'       OUTTLR3
         DC    X'0400',X'0008',X'0020'       OUTREC
         DC    X'0400',X'0004',X'0028'       ERROR
         SPACE 1
TAB4D    EQU   *                       TABLE END ADDRESS
         EJECT
*        INVALID TRANSLATE REPLACE PARAMETER TABLE
         SPACE 1
TRANRPTB DC    X'10'                   CODE 10
         DC    X'11'                   CODE 11
         DC    X'13'                   CODE 13
         DC    X'14'                   CODE 14
         DC    X'15'                   CODE 15
         DC    X'17'                   CODE 17
         DC    X'51'                   CODE 51
         DC    X'55'                   CODE 55
         SPACE 1
TRANRPLN DC    AL1(TRANRPLN-TRANRPTB)  TABLE LENGTH
         SPACE 1
*        INVALID EDIT REPLACE PARAMETER TABLE
         SPACE 1
EDITRPTB DC    X'00'                   CODE 00
         DC    X'11'                   CODE 11
         DC    X'12'                   CODE 12
         DC    X'18'                   CODE 18
         DC    X'1D'                   CODE 1D
         DC    X'1E'                   CODE 1E
         DC    X'3C'                   CODE 3C
         SPACE 1
EDITRPLN DC    AL1(EDITRPLN-EDITRPTB)  TABLE LENGTH
         SPACE 1
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - COMM. COMMUNICATION REGION'
*        COMMON COMMUNICATION REGION
         SPACE 1
*        THIS CSECT CONTAINS TABLES, INDICATORS, CONSTANTS, WORK
*        AREAS, AND DCB'S THAT ARE USED BY TWO OR MORE MODULES.
         SPACE 2
TCRCOM   DSECT
         SPACE 2
         DS    0F                      FORCE FULL-WORD ALIGNMENT
OPTABLE  DS    0CL8                    CONTROL CARD OPTION TABLE
         SPACE 1
IEXITS   DC    X'00'                   USER ROUTINE INDICATORS
         SPACE 1
*              BIT 0 - OUTHDR2
*                  1 - OUTTLR2
*                  2 - OUTHDR3
*                  3 - OUTTLR3
*                  4 - OUTREC
*                  5 - ERROR
*                  6 - USERTAB
*                  7 - NOT USED
         SPACE 1
IPARM1   DC    X'00'                   TYPE AND TRANS INDICATORS
         SPACE 1
*              BIT 0 - TYPE = MTST
*                  1 - TYPE = MTDI
*                  2 - UNUSED
*                  3 - TRANS = STDUC
*                  4 - TRANS = STDLC
*                  5 - TRANS = USERNAME
*                  6 - TRANS = NOTRAN
*                  7 - NOT USED
         SPACE 1
IPARM2   DC    X'00'                   EDIT, VERCHK, AND ERROPT
*                                        INDICATORS
         SPACE 1
*              BIT 0 - EDIT = EDITD
*                  1 - EDIT = EDITR
*                  2 - EDIT = NOEDIT
*                  3 - VERCHK = VOKCHK
*                  4 - VERCHK = NOCHK
*                  5 - ERROPT = NORMAL
*                  6 - ERROPT = NOERR
*                  7 - UNUSED
         SPACE 1
REPL     DC    X'19'                   REPLACE PARAMETER VALUE
MINLN    DC    X'0000'                 MINLN PARAMETER VALUE
MAXLN    DC    X'0000'                 MAXLN PARAMETER VALUE
         EJECT
ADDRTAB  DS    0CL28                   USER ROUTINE ADDRESS TABLE
USERADDR DC    A(0,0,0,0,0,0,0)        TABLE
         SPACE 1
*              WORD 0 - OUTHDR2
*                   1 - OUTTLR2
*                   2 - OUTHDR3
*                   3 - OUTTLR3
*                   4 - OUTREC
*                   5 - ERROR
*                   6 - TRANSLATE TABLE
         SPACE 2
MSGADDR  DC    A(0)                    MESSAGE MODULE ADDRESS
         SPACE 2
*        NAME TABLE
         SPACE 1
*        THIS TABLE CONTAINS DDNAMES AND USER EXIT NAMES
         SPACE 1
NAMETAB  DC    CL8'SYSIN',CL8'SYSPRINT',CL8'SYSUT1',CL8'SYSUT2'
         DC    CL8'SYSUT3',CL8'OUTHDR2',CL8'OUTTLR2'
         DC    CL8'OUTHDR3',CL8'OUTTLR3',CL8'OUTREC',CL8'ERROR'
         DC    CL8'TRANS'
         SPACE 1
*              WORDS 0-1   SYSIN
*                    2-3   SYSPRINT
*                    4-5   SYSUT1
*                    6-7   SYSUT2
*                    8-9   SYSUT3
*                   10-11  OUTHDR2
*                   12-13  OUTTLR2
*                   14-15  OUTHDR3
*                   16-17  OUTTLR3
*                   18-19  OUTREC
*                   20-21  ERROR
*                   22-23  TRANS
         EJECT
*        MESSAGE POINTER TABLE
         SPACE 2
*        THIS TABLE CONTAINS ONE FULL-WORD ENTRY FOR EACH IEB-TYPE
*        MESSAGE (EXCLUDING SYNAD MESSAGES).  MSG01 CORRESPONDS TO
*        IEB901; MSG02 CORRESPONDS TO IEB902; ETC.
         SPACE 1
*              BYTE  1 - MESSAGE REQUEST CODE
*
*                        X'FF' = INDICATES REQUEST
*                        X'00' = NO REQUEST
*
*              BYTE  2 - INSERT CODE
*
*                        X'00' - NO INSERT
*                        X'01' - SYSIN
*                        X'02' - SYSPRINT
*                        X'03' - SYSUT1
*                        X'04' - SYSUT2
*                        X'05' - SYSUT3
*                        X'06' - OUTHDR2
*                        X'07' - OUTTLR2
*                        X'08' - OUTHDR3
*                        X'09' - OUTTLR3
*                        X'0A' - OUTREC
*                        X'0B' - ERROR
*                        X'0C' - USERTAB
*                        X'54' - BOTH SYSUT2 AND SYSUT3
*
*                    NOTE:  IF X'54' IS SPECIFIED, THE MESSAGE IS
*                        OUTPUT TWICE, ONCE WITH SYSUT2 AND ONCE
*                        WITH SYSUT3.
*
*              BYTE  3 - INSERT LOCATION  (MESSAGE DISPLACEMENT)
*
*              BYTE  4 - OUTPUT LOCATION
*
*                        BIT 0 - UNUSED
*                            1 - UNUSED
*                            2 - UNUSED
*                            3 - UNUSED
*                            4 - UNUSED
*                            5 - SYSPRINT IF AVAILABLE, CONSOLE
*                                OTHERWISE
*                            6 - CONSOLE
*                            7 - SYSPRINT
*
*                        NOTE - BITS 6 AND 7 MAY BE ON SIMULTANEOUSLY
         SPACE 1
MSGPTRS  EQU   *                       ASSIGN MSGPTRS NAME TO TABLE
MSG01    DC    X'00000002'
MSG02    DC    X'00000001'
MSG03    DC    X'00000001'
MSG04    DC    X'00000001'
MSG05    DC    X'00000001'
MSG06    DC    X'00000001'
MSG07    DC    X'00000001'
MSG08    DC    X'00000001'
MSG09    DC    X'00000001'
MSG10    DC    X'00030B01'             INSERT LOC = 11
MSG11    DC    X'00010B01'             INSERT LOC = 11
MSG12    DC    X'00020B02'             INSERT LOC = 11
MSG13    DC    X'00000B01'             INSERT LOC = 11
MSG14    DC    X'00002501'             INSERT LOC = 37
MSG15    DC    X'00000F04'             INSERT LOC = 15
MSG16    DC    X'00001504'             INSERT LOC = 21
MSG17    DC    X'00002201'             INSERT LOC = 34
MSG18    DC    X'00001D01'             INSERT LOC = 29
MSG19    DC    X'00000004'
MSG20    DC    X'00000002'
         SPACE 3
MOUNTID  DS    F                       MOUNT MSG ID
IOBADDR  DS    F                       SYSUT1 IOB ADDRESS
TCRBUFL  DC    X'0000'                 TCR BUFFER LENGTH
         SPACE 1
ERRIND   DC    X'00'                   ABNORMAL TERMINATION INDICATORS
         SPACE 1
*              BIT 0 - CONFLICTING DCB PARAMETERS
*                  1 - STORAGE ALLOCATION ERROR
*                  2 - I/O ERROR IN READ AND EDIT
*                  3 - ERROR IN JFCB READ OR MOUNT
*                  4 - ERROR DURING READ AND EDIT
*                  5 - ERROR IN ANALYZING CONTROL CARDS
*                  6 - PERMANENT I/O ERROR ON QSAM GET/PUT
*                  7 - ERROR IN LOADING UTILITY MODULE
         SPACE 1
DUMMYIND DC    X'00'                   DUMMY FILE INDICATORS
         SPACE 1
*              BIT 0 - UNUSED
*                  1 - UNUSED
*                  2 - UNUSED
*                  3 - UNUSED
*                  4 - SYSUT3
*                  5 - SYSUT2
*                  6 - SYSPRINT
*                  7 - SYSIN
         EJECT
*        DATA CONTROL BLOCK (DCB) EXIT LISTS
         SPACE 2
*        SYSIN EXIT LIST
         SPACE 1
         DS    0F                      FORCE FULL-WORD ALIGNMENT
INEXLST  DC    X'07'                   JFCB EXIT
         DC    AL3(0)                  WORK AREA ADDRESS
         DC    X'85'                   DCB EXIT
         DC    AL3(0)                  DCB EXIT ROUTINE ADDRESS
         DC    X'00'                   DUMMY ENTRY
         DC    AL3(INLRECL)            DEFAULT TABLE ADDRESS
         SPACE 2
*        SYSPRINT EXIT LIST
         SPACE 1
PREXLST  DC    X'07'                   JFCB EXIT
         DC    AL3(0)                  WORK AREA ADDRESS
         DC    X'05'                   DCB EXIT
         DC    AL3(0)                  DCB EXIT ROUTINE ADDRESS
         DC    X'00'                   DUMMY ENTRY
         DC    AL3(PRTLRECL)           DEFAULT TABLE ADDRESS
         DC    X'00'                   OUTPUT HEADER LABEL (NULL)
         DC    AL3(0)                  NULL ADDRESS
         DC    X'80'                   OUTPUT TRAILER LABEL (NULL)
         DC    AL3(0)                  NULL ADDRESS
         SPACE 2
*        SYSUT1 EXIT LIST
         SPACE 1
UT1EXLST DC    X'87'                   JFCB EXIT
         DC    AL3(0)                  JFCB WORK AREA ADDRESS
         SPACE 2
*        SYSUT2 EXIT LIST
         SPACE 1
         DS    0F
UT2EXLST DC    X'07'                   JFCB EXIT
         DC    AL3(0)                  WORK AREA ADDRESS
         DC    X'05'                   DCB EXIT
         DC    AL3(0)                  DCB EXIT ROUTINE ADDRESS
         DC    X'00'                   DUMMY ENTRY
         DC    AL3(UT2LRECL)           DEFAULT TABLE ADDRESS
         DC    X'00'                   OUTPUT HEADER LABEL
         DC    AL3(0)                  NULL ADDRESS
         DC    X'80'                   OUTPUT TRAILER LABEL
         DC    AL3(0)                  NULL ADDRESS
         SPACE 2
*        SYSUT3 EXIT LIST
         SPACE 1
UT3EXLST DC    X'07'                   JFCB EXIT
         DC    AL3(0)                  WORK AREA ADDRESS
         DC    X'05'                   DCB EXIT
         DC    AL3(0)                  DCB EXIT ROUTINE ADDRESS
         DC    X'00'                   DUMMY ENTRY
         DC    AL3(UT3LRECL)           DEFAULT TABLE ADDRESS
         DC    X'00'                   OUTPUT HEADER LABEL
         DC    AL3(0)                  NULL ADDRESS
         DC    X'80'                   OUTPUT TRAILER LABEL
         DC    AL3(0)                  NULL ADDRESS
         SPACE 2
*        DCB PARAMETER DEFAULTS
         SPACE 1
INLRECL  DC    X'0050'                 SYSIN LRECL  - 80
INBLK    DC    X'0050'                 SYSIN BLKSIZE - 80
INRECFM  DC    X'90'                   SYSIN - FIXED BLOCKED
INIDENT  DC    X'01'                   INTERNAL IDENTIFICATION CODE
         SPACE 1
PRTLRECL DC    X'0079'                 SYSPRINT LRECL -  121
PRTBLK   DC    X'0079'                 SYSPRINT BLKSIZE - 121
PRTRECFM DC    X'94'                   SYSPRINT - FIXED BLOCKED ASA
PRTIDENT DC    X'02'                   INTERNAL IDENTIFICATION CODE
         SPACE 1
UT2LRECL DC    X'0000'                 SYSUT2 LRECL
UT2BLK   DC    X'0000'                 SYSUT2 BLKSIZE
UT2RECFM DC    X'50'                   SYSUT2 RECFM - VARIABLE BLOCKED
UT2IDENT DC    X'04'                   INTERNAL IDENTIFICATION CODE
         SPACE 1
UT3LRECL DC    X'0000'                 SYSUT3 LRECL
UT3BLK   DC    X'0000'                 SYSUT3 BLKSIZE
UT3RECFM DC    X'50'                   SYSUT3 RECFM - VARIABLE BLOCKED
UT3IDENT DC    X'05'                   INTERNAL IDENTIFICATION CODE
         EJECT
*        DATA CONTROL BLOCKS (DCB'S)
         SPACE 3
*        SYSUT1 (TCR) DCB
         SPACE 2
TCRDCB   DCB   DSORG=PS,MACRF=(E),DDNAME=SYSUT1,DEVD=TA,RECFM=UA,      X
               BFTEK=S,EXLST=UT1EXLST,IOBAD=NULL
         EJECT
*        SYSUT2 DCB
         SPACE 2
UT2DCB   DCB   DSORG=PS,DDNAME=SYSUT2,DEVD=DA,EXLST=UT2EXLST,          X
               SYNAD=NULL,EROPT=ABE,MACRF=(PM)
         SPACE 3
*        SYSUT3 DCB
         SPACE 2
UT3DCB   DCB   DSORG=PS,DDNAME=SYSUT3,DEVD=DA,EXLST=UT3EXLST,          X
               SYNAD=NULL,EROPT=ABE,MACRF=(PM)
         SPACE 3
*        SYSIN DCB
         SPACE 2
SYSINDCB DCB   DSORG=PS,DDNAME=SYSIN,DEVD=DA,EODAD=NULL,MACRF=(GM),    X
               EXLST=INEXLST,EROPT=ACC,SYNAD=NULL
         SPACE 3
*        SYSPRINT DCB
         SPACE 2
SYSPRDCB DCB   DSORG=PS,DDNAME=SYSPRINT,DEVD=DA,EXLST=PREXLST,         X
               MACRF=(PM)
         EJECT
*        GETMAINED STORAGE ADDRESSES
         SPACE 2
CARDAREA DS    F                       CARD INPUT AREA
JFCBAREA DS    F                       JFCB AREA ADDRESS
PRTAREA  DS    F                       SYSPRINT OUTPUT AREA
PRTLNGTH DS    F                       PRINT AREA LENGTH
COLUMN9  DS    F                       CARD INPUT COLUMN 9 ADDRESS
CARDEND  DS    F                       INPUT AREA ENDING ADDRESS
         SPACE 2
USERNAME DC    7XL8'00'                USER ROUTINE NAME TABLE
           SPACE 1
*              WORDS 0 & 1  - OUTHDR2 ROUTINE
*                    2 & 3  - OUTTLR2 ROUTINE
*                    4 & 5  - OUTHDR3 ROUTINE
*                    6 & 7  - OUTTLR3 ROUTINE
*                    8 & 9  - OUTREC  ROUTINE
*                   10 & 11 - ERROR ROUTINE
*                   12 & 13 - TRANSLATE TABLE
           SPACE 2
MISCIND  DC    X'00'                   MISCELLANEOUS INDICATORS
         SPACE 1
*              BIT 0 - UNUSED
*                  1 - UNUSED
*                  2 - UNUSED
*                  3 - SYSUT2 LRECL REQUIRES MAXLN INCREMENT
*                  4 - SYSUT2 BLKSIZE REQUIRES MAXLN INCREMENT
*                  5 - SYSUT3 LRECL REQUIRES MAXLN INCREMENT
*                  6 - SYSUT3 BLKSIZE REQUIRES MAXLN INCREMENT
*                  7 - ALTERNATE DDNAMES FOUND
             SPACE 2
         SPACE 1
RETCODE  DC    X'00'                   RETURN CODE
TCRDEV   DC    CL3'000'                TCR DEVICE
VOLSER   DC    CL6' '                  VOLUME SERIAL NUMBER
PRTRTN   DC    X'00'                   MESSAGE WRITER OPTION IND
CARRCTRL DC    X'40'                   CARRIAGE CONTROL
         SPACE 1
VTCR220  DS    F                       MESSAGE DISPLAY
CURMSGAD DS    F                       CURRENT SYSPRINT MSG ADDRESS
MSGAD    DS    F                       SINGLE MESSAGE ADDRESS
MSGLN    DS    F                       SINGLE MSG LENGTH
JOBNAME  DC    CL8' '                  JOB NAME
STEPNAME DC    CL8' '                  STEPNAME
  TITLE 'IEBTCR04 - CONTROL CARD ANALYZER - IHADCB DSECT'
         DS    0F                      FORCE FULL-WORD ALIGNMENT
         DCBD  DSORG=PS
         END   IEBTCR04
