          TITLE 'IGE0011A - 2495 ERROR RECOVERY PROCEDURES'
* RELEASE VS2/ 2.0      ADDITION/CHANGES/DELETIONS               Y02992
*A306000,810000                                                  Y02992
*C087000,116000,156000,161000,165000,167000,169000,171000,179000 Y02992
*C189000,199000,232000,248000,333000-336000,347000,352000,356000 Y02992
*C359000,362000,365000-367000,374000,376000,388000-390000,       Y02992
*C392000-393000,400000,437000,468000,470000,472000-473000,481000 Y02992
*C483000-484000,486000-487000,490000,517000,523000-524000,533000 Y02992
*C553000-554000,558000,560000,565000,567000,569000-571000,573000 Y02992
*C574000-575000,579000-589000,600000,604000,638000,640000        Y02922
*C642000-645000,648000,650000,654000-659000,663000,666000,670000 Y02992
*C672000-675000,681000-682000,690000,719000,721000,723000,       Y02992
*C725000-726000,729000,731000,745000,752000-753000,759000-760000 Y02992
*C763000,765000-766000,777000,780000-789000,796000,798000,       Y02992
*C80000,801000,                                                  Y02992
*C352000,365000,470000,472000-473000,524000,558000,560000,567000 YM2509
*C589000,640000,666000,721000,745000,765600                      YM2509
*D117000-138000,208000-220000,235000-236000,238000-244000,       Y02992
*D250000-257000,378000-387000,700000,140000-160000               Y02922
*                                                                     *
*TITLE 'IGE0011A' - 2495 ERROR RECOVERY PROCEDURES                    *
*                                                                     *
*STATUS:  VS2/ RELEASE 2.0   CHANGE LEVEL 000                    Y02922
*                                                                     *
*FUNCTION/OPERATION:  IGE0011A IS SCHEDULED FOR EXECUTION WHENEVER THE*
*   I/O SUPERVISOR DETECTS AN ERROR ASSOCIATED WITH THE 2495 TAPE     *
*   CARTRIDGE READER THAT REQUIRES ERROR RECOVERY PROCEDURES.  THE    *
*   ROUTINES ARE NORMALLY SCHEDULED FOR THE FOLLOWING ERRORS:         *
*                                                                     *
*                 PROGRAM CHECK                                       *
*                 PROTECTION CHECK                                    *
*                 UNIT CHECK                                          *
*                 CHANNEL DATA CHECK                                  *
*                 UNIT EXCEPTION                                      *
*                 INCORRECT LENGTH                                    *
*                                                                     *
*   IF THE CHANNEL CHECK HANDLER (CCH) IS IN THE SYSTEM, THE ERROR    *
*   RECOVERY PROCEDURE IS ALSO ENTERED FOR CHANNEL CONTROL CHECKS AND *
*   INTERFACE CONTROL CHECKS.                                         *
*                                                                     *
*   THE ROUTINES MAY ALSO BE ENTERED WITH THE FOLLOWING CONDITIONS    *
*   FOLLOWING SUCCESSFUL RE-TRY OF A RECOVERABLE ERROR.  THESE        *
*   CONDITIONS ARE CONSIDERED NORMAL AND ARE TREATED ACCORDINGLY.     *
*                                                                     *
*                 BUSY                                                *
*                 CHANNEL END                                         *
*                 DEVICE END                                          *
*                                                                     *
*   THE ERROR ROUTINES USE THE STANDARD ERROR INTERPRETER ROUTINE     *
*   FOR TESTING OF SENSE BITS AND CSW STATUS BITS.  THE FOLLOWING     *
*   ACTIONS ARE TAKEN DEPENDING UPON THE NATURE OF THE ERROR:         *
*                                                                     *
*   1)  NORMAL CONDITION OR SUCCESSFUL RE-TRY OF RECOVERABLE ERROR    *
*       CONDITION                                                     *
*                                                                     *
*       IOB EXCEPTION AND ERROR FLAGS ARE CLEARED AND CONTROL IS      *
*       TRANSFERRED TO THE STATISTICS UPDATE ROUTINE.                 *
*                                                                     *
*   2)  RECOVERABLE ERROR                                             *
*                                                                     *
*       IOB ERROR FLAG IS SET.                                        *
*       IOB EXCEPTION FLAG IS CLEARED.                                *
*       ERROR COUNTS ARE UPDATED.                                     *
*       REPOSITION AND RESIDUAL READ CCW'S MAY BE CONSTRUCTED IN THE  *
*       UCB EXTENSION.                                                *
*       CONTROL IS PASSED TO THE I/O SUPERVISOR VIA SVC 15 AND SVC 3  *
*       TO RE-TRY THE CHANNEL PROGRAM.                                *
*                                                                     *
*   3)  PERMANENT ERROR                                               *
*                                                                     *
*   IOB ERROR AND EXCEPTION FLAGS ARE SET.                            *
*   IOB MESSAGE AND LOGOUT FLAGS MAY BE SET.                          *
*   CONTROL IS RETURNED TO THE I/O SUPERVISOR EITHER DIRECTLY         *
*   THROUGH ISSUANCE OF AN SVC 15 AND AN SVC 3 OR INDIRECTLY BY       *
*   FIRST PASSING CONTROL TO THE WTO ROUTINE WHICH IN TURN PASSES     *
*   CONTROL TO THE STATISTICS UPDATE ROUTINE AND THE I/O OUTBOARD     *
*   RECORDING ROUTINE, IF REQUIRED, AND EVENTUALLY TO THE I/O         *
*   SUPERVISOR.                                                       *
*                                                                     *
*   IF THE CHANNEL CHECK HANDLER (CCH) IS IN THE SYSTEM AND THE       *
*   ERROR RECOVERY PROCEDURE IS ENTERED FOR A CHANNEL CONTROL         *
*   CHECK OR AN INTERFACE CONTROL CHECK, THE ROUTINES ZERO OUT        *
*   THE ERROR RECOVERY PROCEDURE INTERFACE BYTES (ERPIB) CREATED      *
*   BY THE CCH AND HANDLE THE ERROR AS A PERMANENT ERROR.             *
*                                                                     *
*   STATISTICS ARE KEPT FOR THE FOLLOWING ERROR CONDITIONS:           *
*                                                                     *
*            INTERVENTION REQUIRED                                    *
*            EQUIPMENT CHECK                                          *
*            DATA CHECK                                               *
*            POSITION CHECK                                           *
*            CHANNEL DATA CHECK                                       *
*                                                                     *
*ENTRY POINTS:                                                        *
*                                                                     *
*        IGE0011A - ERROR RECOVERY PROCEDURE ENTRY POINT              *
*                                                                     *
*   THE ROUTINES ARE ENTERED INTO THE I/O SUPERVISOR TRANSIENT        *
*   AREA AND SCHEDULED FOR EXECUTION BY THE I/O INTERRUPTION          *
*   SUPERVISOR.                                                       *
*                                                                     *
*   DATA PASSED TO THE ROUTINES CONSISTS OF STANDARD SYSTEM CONTROL   *
*   BLOCKS.                                                           *
*                                                                     *
*INPUT:  IOSB POINTER IN REGISTER ONE                            Y02922
*                                                                     *
*OUTPUT:  I/O ERROR AND INTERVENTION REQUIRED MESSAGES IN SOME        *
*   INSTANCES                                                         *
*                                                                     *
*EXTERNAL ROUTINES:  NONE                                             *
*                                                                     *
*EXITS-NORMAL:  XCTL TO STATISTICS UPDATE ROUTINE FOLLOWING           *
*   SUCCESSFUL RECOVERY OF A RE-TRYABLE ERROR OR UPON DETECTION OF    *
*   A NORMAL CONDITION                                                *
*                                                                     *
*     -ERROR:                                                         *
*                                                                     *
*   1)  SVC 15 AND SVC 3 EXIT TO I/O SUPERVISOR FOR RE-EXECUTION      *
*   OF THE CHANNEL PROGRAM UPON DETECTION OF A RE-TRYABLE ERROR       *
*                                                                     *
*   2)  XCTL TO WTO ROUTINE FOR ISSUANCE OF AN INTERVENTION           *
*   REQUIRED MESSAGE AND QUEUEING OF THE I/O REQUEST IN THE CASE OF   *
*   INTERVENTION REQUIRED                                             *
*                                                                     *
*   3)  XCTL TO WTO ROUTINE FOR ISSUANCE OF A PERMANENT I/O ERROR     *
*   MESSAGE AND UPDATING OF STATISTICS AND LOGREC IN THE CASE OF      *
*   SOME PERMANENT ERRORS.                                            *
*                                                                     *
*   4)  SVC 15 AND SVC 3 EXIT TO I/O SUPERVISOR IN THE CASE OF SOME   *
*   PERMANENT I/O ERRORS.                                             *
*                                                                     *
*TABLES/WORK AREAS:                                                   *
*                                                                     *
*   1)   INPUT/OUTPUT SYSTEM BLOCK(IOSB)                         Y02922
*                                                                     *
*   2)   ERP WORK AREA                                           Y02922
*                                                                     *
*                                                                     *
*       *****************************************************         *
*   +40 *         BACKSPACE OR REPOSITION CCW                    Y02922
*       *****************************************************         *
*   +48 *         RESIDUAL READ CCW OR TIC CCW                   Y02922
*       *****************************************************         *
*   +56 *                   TIC CCW                              Y02922
*       *****************************************************         *
*   +64 *               IOSB CSW STORE                           Y02922
*       *****************************************************         *
*                                                                     *
*                                                                     *
*   3)      STATISTICS UPDATE TABLE                                   *
*                                                                     *
*ATTRIBUTES:  RE-ENTRANT                                              *
*                                                                     *
*NOTES:  32 BYTES IN THE ERP WORK AREA IS USED FOR CONSTRUCTION  Y02922
*   A BACKSPACE OR REPOSITION CCW, A RESIDUAL READ CCW, A TIC         *
*   COMMAND, AND A CSW STORAGE AREA FOR RE-TRYING OF POSITION CHECK,  *
*   DATA CHECK, AND CHANNEL DATA CHECK ERRORS.                        *
*                                                                     *
         EJECT
IGE0011A START 0
         SPACE 3
*        REGISTER DEFINITIONS
         SPACE 2
IOBREG   EQU   1                       IOSB REGISTER             Y02922
UCBEXT   EQU   2                       UCB EXTENSION
CRBREG   EQU   3                       CURRENT RB REG
CCWREG   EQU   4                       CCW ADDRESS REGISTER
LOGRCREG EQU   5                       LOGREC DCB ADDRESS
WKREG1   EQU   5                       WORK REGISTER
WKREG2   EQU   6                       WORK REGISTER
UCBREG   EQU   7                       UCB REG
CVTREG   EQU   8                       CVT REG
SAVEREG  EQU   9                       SAVE REG
ERPREG   EQU   10                      ERP WORK AREA REGISTER    Y02922
LNKREG   EQU   12                      LINK REG
WKREG3   EQU   13                      WORK REGISTER
RETREG   EQU   14                      RETURN REG
BASEREG  EQU   15                      BASE REG
         SPACE 2
*        IOB DEFINITION
         SPACE 1
IOBFLAG1 EQU   0                       FLAGS 1
         SPACE 1
*        IOBFLAG1 INDICATORS
         SPACE 1
IOBCP1   EQU   X'80'                   DATA CHAINING
IOBCP2   EQU   X'40'                   COMMAND CHAINING
IOBERR   EQU   X'20'                   IOB ERROR FLAG
IOBEX    EQU   X'04'                   IOB EXCEPTION FLAG
         EJECT
*        IOBFLAG3 INDICATORS
         SPACE 2
IOBCDCK  EQU   X'20'                   CHANNEL DATA CHECK COUNT
IOBBUS   EQU   X'10'                   BUS-OUT ERROR COUNT
IOBENT   EQU   X'04'                   IOB ENTRY BIT             Y02922
         SPACE 2
*        UCB DEFINITION
         SPACE 1
UCBSTI   EQU   9                       STATISTICS POINTER
         SPACE 2
         SPACE 2
*        CSW INDICATORS
         SPACE 1
CSWUCK   EQU   X'02'                   UNIT CHECK
CSWCHE   EQU   X'08'                   CHANNEL END
CSWDVE   EQU   X'04'                   DEVICE END
CSWCEDE  EQU   X'0C'                   CHANNEL OR DEVICE END
CSWCDK   EQU   X'08'                   CHANNEL DATA CHECK
         SPACE 2
*        CCW FLAGS AND COMMANDS
         SPACE 1
CCFLAG   EQU   X'40'                   COMMAND CHAINING CCW FLAG
CCWFLAGS EQU   X'30'                   CCW SLI AND SKIP MASK
CTRLCMD  EQU   X'03'                   CONTROL COMMAND
REPOCMD  EQU   X'27'                   CONTROL BACKSPACE
READCMD  EQU   X'02'                   READ
TICCMD   EQU   X'08'                   TIC
         EJECT
*        MISCELLANEOUS DEFINITIONS
         SPACE 1
FOUR     EQU   4                       FOUR
TEN      EQU   10                      TEN
HEXTEN   EQU   X'0A'                   HEXADECIMAL TEN
POSCLR   EQU   X'0F'                   POSITION CHECK COUNT CLEAR
DCKCLR   EQU   X'F0'                   DATA CHECK COUNT CLEAR
EXCPER   EQU   15                      ERROR EXCP
RETURN   EQU   3                       SVC RETURN
IOBCC1   EQU   X'10'                   CONDITION CODE OF SIO
LOC016   EQU   16                      COMMUNICATION TABLE
VECTXL   EQU   44                      VECTOR TO XCTL ROUTINE
VECINT   EQU   68                      VECTOR TO INTERPRETER ROUTINE
VECWTO   EQU   72                      VECTOR TO WTO ROUTINE
STATAB   EQU   112                     INDEX TO STATISTICS TABLE
WTORTN   EQU   253                     LOAD NAME TO WTO RTN
STATUP   EQU   254                     LOAD NAME TO STAT. UPDATE RTN
IOBEXER  EQU   IOBEX+IOBERR            IOB EX + IOB ERR FLAG
IOBINCPT EQU   X'7E'                   INTERCEPT CONDITION
ZERO     EQU   X'00'                   ZERO
STABIT   EQU   X'7A'                   SENSE BITS REQUIRING STATISTICS
INTREQ   EQU   X'40'                   INT. REQ. BIT IN SENSE BYTE
ERRORECB EQU   X'7F'                   IOB ECB ERROR COMPLETION CODE
SNSDCK   EQU   X'08'                   DATACHECK
CVTDCB   EQU   116                     LOGREC DCB POINTER
LASTERIB EQU   X'FF'                   LAST ERIB TABLE ENTRY
TICTEST1 EQU   X'07'                   TIC COMMAND TEST 1
TICTEST2 EQU   X'08'                   TIC COMMAND TEST 2
CHADCK   EQU   X'01'                   STAT TABLE CHAN DATA CHK ENTRY
LEFTTEN  EQU   X'A0'                   HEXIDECIMAL TEN
CCCICC   EQU   X'06'                   CHANNEL CTRL CHK OR INT CTRL CHK
MASK1    EQU   X'C0'                   MASK OUT IOSFLC           Y02922
         EJECT
*        2495 ERROR RECOVERY PROCEDURES
         SPACE 1
*        THE FOLLOWING IS A LIST OF ERRORS HANDLED BY THIS ERROR
*        RECOVERY PROCEDURE AND THE MAIN PROCESSING ROUTINE ENTRY
*        POINT ASSOCIATED WITH EACH.
*
*                 CHANNEL CONTROL CHECK - ERR130
*               INTERFACE CONTROL CHECK - ERR130
*                         PROGRAM CHECK - ERR040
*                      PROTECTION CHECK - ERR040
*                            UNIT CHECK - ERR045
*                    CHANNEL DATA CHECK - ERR070
*                        UNIT EXCEPTION - ERR120
*                      INCORRECT LENGTH - ERR120
*                       EQUIPMENT CHECK - ERR055
*                         BUS-OUT CHECK - ERR050
*                 INTERVENTION REQUIRED - ERR065
*                        POSITION CHECK - ERR080
*                            DATA CHECK - ERR100
*                        COMMAND REJECT - ERR060
         SPACE 2
*        THIS SECTION SETS UP CONTROL BLOCK POINTERS AND TESTS FOR
*        DATA CHAINING AND MIXED CHAINING.
         SPACE 2
         USING ER2495,BASEREG          INDICATE BASE REGISTER
         USING IOSB,IOBREG             BASE REG FOR IOSB         Y02922
         USING EWA,ERPREG              BASE REG FOR ERP WORK AREAY02922
ER2495   L     ERPREG,IOSERP           LOAD ERP WORKAREA ADDR    Y02922
         L     UCBREG,IOSUCB           LOAD UCB ADDRESS          Y02922
         L     CVTREG,LOC016           LOAD CVT ADDRESS          Y02922
         OI    IOBFLAG1(IOBREG),IOBEXER  SET IOB EX AND ERROR FLAGS
         TM    IOBFLAG1(IOBREG),IOBCP1 DATA CHAINING OR MIXED CHAINING
         BO    ERR130                  BR YES
         SPACE 2
*        THIS SECTION DEVELOPS THE FAILING CCW ADDRESS, TESTS
*        WHETHER OR IT IS ZERO OR NEGATIVE, AND TESTS FOR THE SAME
*        ERROR IF THIS IS NOT THE FIRST ENTRY TO THE ERP.  IF IT
*        IS NOT THE SAME ERROR, THIS SECTION ZEROES ALL RECOVERABLE
*        ERROR COUNTS AND STORES THE NEW CSW
         SPACE 2
ERR015   L     CCWREG,IOSCSW-1         LOAD CCW ADDRESS          Y02922
         LA    CCWREG,0(CCWREG)        ZERO HIGH-ORDER BYTE
         SH    CCWREG,CCWLNG           BACK UP ONE CCW
         LTR   CCWREG,CCWREG           CCW ADDRESS ZERO OR NEGATIVE
         BNP   ERR150                  BR YES, INTERCEPT CONDITION
         TM    EWAFLG3,IOBENT          ENTRY BIT ON              YM2509
         BZ    ERR020                  BR NO
         TM    IOBFLAG1(IOBREG),IOBCP2  COMMAND CHAINING SPECIFIED
         BZ    ERR025                  BR NO
         LA    WKREG1,EWCCCW1          TEST CURRENT CCW WITHIN   Y02922
         CLR   WKREG1,CCWREG             EXTENSION
         BE    ERR025                  BR YES
         LA    WKREG1,EWCCCW2          2ND CCW                   Y02922
         CLR   WKREG1,CCWREG
         BE    ERR025                  BR YES
         CLC   EWCCSW+1(3),IOSCSW      SAME CCW WITHIN           Y02922
*                                        CHAIN
         BE    ERR025                  BR YES
         MVI   EWAFLG3,ZERO            CLEAR COUNTERS            YM2509
         MVI   EWACNTR4,ZERO           CLEAR COUNTERS            Y02922
ERR020   MVC   EWCCSW+1(7),IOSCSW      STORE NEW CSW             Y02922
         SPACE 2
*        IF UNIT CHECK IS ON, THIS SECTION OR'S THE SENSE BYTE INTO
*        THE FIRST BYTE OF THE STATISTICS TABLE WORK AREA.  IF
*        CHANNEL DATA CHECK IS ON, IT SETS BIT 7 OF BYTE TWO OF THE
*        STATISTICS TABLE WORK AREA TO ONE.
         SPACE 2
ERR025   TM    IOSTSB,CSWCDK           CHANNEL DATA CHECK        Y02922
         BO    ERR026                  BRANCH IF YES
         TM    IOSTSA,CSWUCK           TEST IF UNIT CHECK        Y02922
         BZ    ERR030                  BR IF NO
ERR026   OC    EWASTUP(1),IOSSNS       OR SENSE BYTE INTO TABLE  Y02922
         NI    EWASTUP,STABIT          MASK OUT UNWANTED BITS    Y02922
         TM    IOSTSB,CSWCDK           CHANNEL DATA CHECK        Y02922
         BZ    ERR030                  BRANCH IF NO
         OI    EWASTUP+1,CHADCK        SET BIT 7 OF STATISTICS   Y02922
*                                      TABLE WORKAREA BYTE 2 TO 1Y02922
         EJECT
*        THIS SECTION USES THE COMMON INTERPRETER ROUTINE TO ANALYZE
*        CSW STATUS BITS.
         SPACE 2
ERR030   LR    SAVEREG,BASEREG         SAVE BASE ADDRESS
         L     BASEREG,VECINT(CVTREG)  LOAD ADDR INTERPRETER RTN
ERR033   BALR  RETREG,BASEREG          LINK TO INTERPRETER RTN   Y02922
         SPACE 1
         DC    X'1D'                   CHANNEL CONTROL CHECK ID
         DC    AL1(ERR034-ERR033-2)    CHANNEL CONTROL CHECK DISP
         DC    X'1E'                   INTERFACE CONTROL CHECK ID
         DC    AL1(ERR034-ERR033-4)    INTERFACE CONTROL CHECK DISP
         DC    X'1A'                   PROGRAM CHECK ID
         DC    AL1(ERR040-ERR033-6)    PROGRAM CHECK DISPLACEMENT
         DC    X'1B'                   PROTECTION CHECK IDENT
         DC    AL1(ERR040-ERR033-8)    PROTECTION CHECK DISPLACEMENT
         DC    X'16'                   UNIT CHECK ID
         DC    AL1(ERR045-ERR033-10)   UNIT CHECK DISPLACEMENT
         DC    X'1C'                   CHANNEL DATA CHECK ID
         DC    AL1(ERR035-ERR033-12)   CHANNEL DATA CHECK DISP
         DC    X'17'                   UNIT EXCEPTION ID
         DC    AL1(ERR036-ERR033-14)   UNIT EXCEPTION DISPLACEMENT
         DC    X'19'                   INCORRECT LENGTH ID
         DC    AL1(ERR036-ERR033-16)   INCORRECT LENGTH DISPL
         DC    X'2F'                   END OF TEST ID
         DC    AL1(ERR037-ERR033-18)   END OF TEST DISPLACEMENT
         SPACE 1
ERR034   B     ERR130                  CCC AND ICC PROCESSING
ERR035   B     ERR070                  CHANNEL DATA CHECK PROCESSING
ERR036   B     ERR120                  U.E. AND I.L. PROCESSING
ERR037   B     ERR125                  CORRECTED ERROR PROCESSING
         SPACE 2
*        PERMANENT ERROR EXIT - NO LOGOUT OR MESSAGE OUTPUT
         SPACE 1
ERR040   NI    IOBFLAG1(IOBREG),X'FF'-IOBERR  CLEAR ERROR FLAG
         SVC   EXCPER                  ERROR EXCP
         SVC   RETURN                  RETURN
         EJECT
*        THIS SECTION USES THE COMMON INTERPRETER ROUTINE TO ANALYZE
*        CSW STATUS BITS AND SENSE BITS ON A UNIT CHECK CONDITION.
         SPACE 1
ERR045   LR    SAVEREG,BASEREG         SAVE BASE ADDRESS
         L     BASEREG,VECINT(CVTREG)  LOAD ADDR INTERPRETER RTN
ERR046   BALR  RETREG,BASEREG          LINK TO INTERPRETER RTN   Y02922
         SPACE 1
         DC    X'05'                   SENSE BIT 5 (NOT USED)
         DC    AL1(ERR060-ERR046-2)    SENSE BIT 5 DISPLACEMENT
         DC    X'07'                   SENSE BIT 7 (NOT USED)
         DC    AL1(ERR060-ERR046-4)    SENSE BIT 7 DISPLACEMENT
         DC    X'03'                   EQUIPMENT CHECK ID
         DC    AL1(ERR055-ERR046-6)    EQUIPMENT CHECK DISP
         DC    X'1C'                   CHANNEL DATA CHECK ID
         DC    AL1(ERR070-ERR046-8)    CHANNEL DATA CHECK DISP
         DC    X'02'                   BUS-OUT CHECK ID
         DC    AL1(ERR050-ERR046-10)   BUS-OUT CHECK DISP
         DC    X'01'                   INTERVENTION REQUIRED ID
         DC    AL1(ERR065-ERR046-12)   INTERVENTION REQ'D DISP
         DC    X'06'                   POSITION CHECK ID
         DC    AL1(ERR047-ERR046-14)   POSITION CHECK DISP
         DC    X'04'                   DATA CHECK ID
         DC    AL1(ERR048-ERR046-16)   DATA CHECK DISP
         DC    X'00'                   COMMAND REJECT ID
         DC    AL1(ERR060-ERR046-18)   COMMAND REJECT DISP
         DC    X'2F'                   END OF TEST ID
         DC    AL1(ERR060-ERR046-20)   PERMANENT ERROR PROCESSING
         SPACE 1
ERR047   B     ERR080                  POSITION CHECK PROCESSING
ERR048   B     ERR100                  DATA CHECK PROCESSING
         SPACE 2
*        BUS-OUT CHECK PROCESSING
         SPACE 1
*        IF THE BUS-OUT ERROR OCCURS AT INITIAL SELECTION, THE
*        THE FAILING CCW IS RE-EXECUTED ONE TIME.
         SPACE 2
ERR050   TM    IOSTSA,CSWCEDE          CHANNEL OR DEVICE END     Y02922
         BNZ   ERR055                  BR-YES  (NOT INITIAL SELECTION)
         TM    EWAFLG3,IOBBUS          ERROR COUNT EQUAL TO ONE  YM2509
         BO    ERR055                  BR-YES
         OI    EWAFLG3,IOBBUS          SET ERROR CNT EQUAL TO 1  YM2509
ERR053   OI    EWAFLG3,IOBENT          SET ENTRY BIT ON          YM2509
         SVC   EXCPER                  ERROR EXCP
         SVC   RETURN                  RETURN
         EJECT
*        PERMANENT ERROR EXIT - WITH WTO MESSAGE AND LOGOUT
         SPACE 1
*        IF THE FAILING CCW IS THE READ CCW WITHIN THE EXTENSION,
*        THE IOB CSW ADDRESS IS REPLACED BY THE ORIGINAL FAILING CCW
*        ADDRESS WHICH HAS BEEN STORED IN THE ERP WORK AREA      Y02922
         SPACE 2
ERR055   OI    IOSFLB,IOSLOG           SET LOG OUT BIT           Y02922
ERR060   OI    IOSFLB,IOSMSG           SET MESSAGE BIT           Y02922
         NI    IOBFLAG1(IOBREG),X'FF'-IOBERR  CLEAR ERROR FLAG
         LA    WKREG1,EWCCCW2          SET POINTER TO READ CCW   Y02922
*                                      WITHIN WORK AREA          Y02922
         CLR   CCWREG,WKREG1           IS IT READ CCW
         BNE   ERR062                  BR NO
         MVC   IOSCSW(3),EWCCSW+1      RESTORE ORGINAL CSW       Y02922
ERR062   LA    WKREG3,WTORTN           GET LOAD NAME TO WTO RTN
ERR064   L     RETREG,VECTXL(CVTREG)   GET ADDRESS XCTL ROUTINE
         BR    RETREG                  XCTL TO WTO RTN
         EJECT
*        INTERVENTION REQUIRED
         SPACE 2
*        ONE OF FOUR  SITUATIONS IS POSSIBLE:
*
*        1)  THE INTERVENTION REQUIRED OCCURRED AT INITIAL SELECTION
*            (NO CHANNEL OR DEVICE END)
*
*        2)  THE INTERVENTION REQUIRED OCCURRED DURING TRANSFER
*            OF DATA.  (INDICATED BY CHANNEL END ALONE OR BY CHANNEL
*            END AND DEVICE END IF COMMAND CHAINING WAS USED)
*
*        3)  INTERVENTION REQUIRED OCCURRED FOLLOWING A TRANSFER
*            OF DATA DURING WHICH A UNIT CHECK CONDITION OCCURRED
*            BUT PRIOR TO ISSUANCE OF THE SENSE COMMAND (INDICATED
*            BY CHANNEL END, UNIT CHECK, AND SOME OTHER ERROR)
*
*        4)  INTERVENTION REQUIRED OCCURRED AT DEVICE END.  IN THIS
*            CASE, THE STATUS IS STORED IN THE UCB UNTIL THE NEXT
*            SIO IS ISSUED.  AT THAT TIME THE CSW IS TRANSFERRED TO
*            THE IOB AND AN INTERCEPT CONDITION IS INDICATED. (THE
*            IOB ECB CONDITION CODE IS SET TO '7E'.)
           SPACE 2
ERR065   TM    IOSTSA,CSWCEDE          CHANNEL OR DEVICE END     Y02922
         BNZ   ERR067                  BR YES
         SPACE 1
*        ON INITIAL SELECTION XCTL TO THE WTO ROUTINE TO ISSUE THE
*        INTERVENTION REQUIRED MESSAGE AND RETRY THE COMMAND
         SPACE 1
ERR066   NI    IOSFLC,X'FF'-IOSMSG     SET IOSB MSG BIT TO ZERO  Y02922
         OI    EWAFLG3,IOBENT          SET ENTRY BIT ON          YM2509
         B     ERR062                  BRANCH TO XCTL TO WTO ROUTINE
         SPACE 1
*        ON A COMPLETED READ (CHANNEL END OR DEVICE END PRESENT)
*        IGNORE THE INTERVENTION REQUIRED AND CONTINUE CHECKING AS
*        INDICATED IN THE STANDARDS CHART
         SPACE 1
ERR067   LR    SAVEREG,BASEREG         SAVE BASE ADDRESS
         L     BASEREG,VECINT(CVTREG)  LOAD ADDR INTERPRETER RTN
ERR068   BALR  RETREG,BASEREG          LINK TO INTERPRET RTN     Y02922
         SPACE 1
         DC    X'06'                   POSITION CHECK ID
         DC    AL1(ERR080-ERR068-2)    POSITION CHECK DISP
         DC    X'04'                   DATA CHECK ID
         DC    AL1(ERR0683-ERR068-4)   DATA CHECK DISP
         DC    X'00'                   COMMAND REJECT ID
         DC    AL1(ERR0684-ERR068-6)   COMMAND REJECT DISP
         DC    X'2F'                   END OF TEST
         DC    AL1(ERR0685-ERR068-8)   CORRECTED ERROR PROCESSING
         SPACE 1
ERR0683  B     ERR100                  DATA CHECK
ERR0684  B     ERR060                  PERMANENT ERROR
ERR0685  B     ERR125                  CORRECTED ERROR
         SPACE 1
         EJECT
*        CHANNEL DATA CHECK PROCESSING
         SPACE 1
*        IF THE CHANNEL DATA CHECK OCCURRED ON A READ COMMAND,
*        A REPOSITION CCW AND A TIC TO THE FAILING CCW ARE CONSTRUCTED
*        IN THE ERP WORKAREA. SVC 15 AND SVC 3 ARE ISSUED TO     Y02922
*        RETRY THE OPERATION ONE TIME.                           Y02922
         SPACE 2
ERR070   TM    0(CCWREG),CTRLCMD       CONTROL COMMAND
         BO    ERR075                  BR-YES
         TM    EWAFLG3,IOBCDCK         ERROR COUNT EQUAL TO ONE  YM2509
         BO    ERR055                  BR-YES
         OI    EWAFLG3,IOBCDCK         SET COUNT EQUAL TO ONE    YM2509
         SPACE 1
*        IF THE CURRENT ERROR OCCURRED ON A RETRY OPERATION AND THE
*        PREVIOUS ERROR WAS DIFFERENT, CLEAR ALL OTHER COUNTS.
         SPACE 1
         CLC   EWCCSW+6(2),IOSCSWRC    COMPARE RESIDUAL COUNT    Y02922
         BE    ERR071                  BR EQUAL
         NI    EWAFLG3,X'FF'-IOBBUS-IOBCDCK                      YM2509
*                                      AND CHANNEL DATA CHECK COUNTS
         MVI   EWACNTR3,ZERO           CLEAR POSITION AND DATA   Y02922
*                                      CHECK COUNTS              Y02922
         MVC   EWCCSW+6(2),IOSCSWRC    STORE NOW COUNT           Y02922
ERR071   LH    WKREG2,6(CCWREG)        LOAD ORIGINAL COUNT
         SH    WKREG2,IOSCSWRC         SUBTRACT RESIDUAL COUNT   Y02922
         STH   WKREG2,EWCCCW1+6        STORE REPOSITION COUNT    Y02922
         LA    WKREG1,EWCCCW2          SET PTR TO 2ND CCW WITHIN Y02922
*                                        EXTENSION
         CLR   CCWREG,WKREG1           CURRENT CCW WITHIN EXTENSION
         BE    ERR073                  BR YES
         MVI   EWCCCW1,REPOCMD         SET UP REPOSTION CMND     Y02922
         MVC   EWCCCW1+1(3),1(CCWREG)  SET DUMMY ADDRESS         Y02922
         MVI   EWCCCW1+4,CCWFLAGS      SET SLI AND SKIP MASK     Y02922
         NC    EWCCCW1+4(1),4(CCWREG)  SET FLAGS IF REQUIRED     Y02922
         OI    EWCCCW1+4,CCFLAG        SET COMMAND CHAINING FLAG Y02922
         LRA   WKREG2,0(CCWREG)        LOAD REAL CCW ADDRESS     Y02922
         ST    WKREG2,EWCCCW2          STORE CCW ADDRESS IN TIC  Y02922
         MVI   EWCCCW2,TICCMD          SET UP TIC COMMAND        Y02922
ERR072   LRA   WKREG1,EWCCCW1          LOAD REAL ADDRESS REPO CCWY02922
         ST    WKREG1,IOSRST           STORE ADDRESS IN RESTART  Y02922
         OI    EWAFLG3,IOBENT          SET ENTRY BIT ON          YM2509
ERR073   SVC   EXCPER                  ERROR EXCP
         SVC   RETURN                  RETURN
         EJECT
*        CHANNEL DATA CHECK - CONTROL COMMAND
         SPACE 1
*        IF THE CHANNEL DATA CHECK OCCURRED ON A CONTROL COMMAND,
*        CONTINUE TESTING THE REMAINING SENSE BITS.  IF NO OTHER
*        ERROR IS FOUND, IGNORE THE CHANNEL DATA CHECK AND
*        CONTINUE.
         SPACE 2
ERR075   TM    IOSTSA,CSWUCK           TEST IF UNIT CHECK        Y02922
         BZ    ERR125                  BR NO
         LR    SAVEREG,BASEREG         SAVE BASE ADDRESS
         L     BASEREG,VECINT(CVTREG)  LOAD ADDR INTERPRETER RTN
ERR077   BALR  RETREG,BASEREG          LINK TO INTERPRETER RTN   Y02922
         SPACE 1
         DC    X'02'                   BUS OUT IDENT
         DC    AL1(ERR078-ERR077-2)    BUS-OUT DISPLACEMENT
         DC    X'01'                   INTERVENTION REQUIRED ID
         DC    AL1(ERR0783-ERR077-4)   INTERVENTION REQ'D DISP
         DC    X'06'                   POSITION CHECK ID
         DC    AL1(ERR080-ERR077-6)    POSITION CHECK DISP
         DC    X'04'                   DATA CHECK ID
         DC    AL1(ERR100-ERR077-8)    DATA CHECK DISPLACEMENT
         DC    X'00'                   COMMAND REJECT ID
         DC    AL1(ERR0785-ERR077-10)  COMMAND REJECT DISPLACEMENT
         DC    X'2F'                   END OF TEST
         DC    AL1(ERR125-ERR077-12)   CORRECTED ERROR PROCESSING
         SPACE 1
ERR078   B     ERR050                  BUS-OUT
ERR0783  B     ERR065                  INTERVENTION REQ'D PROCESSING
ERR0785  B     ERR060                  COMMAND REJECT PROCESSING
         EJECT
*        POSITION CHECK PROCESSING
         SPACE 1
*        IF THE POSITION CHECK OCCURRED ON A READ COMMAND,
*        BACKSPACE CCW AND A RESIDUAL READ CCW ARE CONSTRUCTED
*        IN THE UCB EXTENSION AND SVC 15 AND SVC 3 ARE ISSUED TO
*        RE-TRY THE OPERATION.  IF COMMAND CHAINING WAS SPECIFIED,
*        A TIC COMMAND WHICH BRANCHES TO THE NEXT COMMAND IN THE
*        CHAIN IS ALSO CONSTRUCTED.
         SPACE 2
ERR080   TM    0(CCWREG),CTRLCMD       CONTROL COMMAND
         BO    ERR055                  BR-YES
         SPACE 1
*        IF THE CURRENT ERROR OCCURRED ON A RETRY OPERATION AND IS
*        DIFFERENT FROM THE ORIGINAL ERROR, CLEAR ALL OTHER COUNTS.
         SPACE 1
         CLC   EWCCSW+6(2),IOSCSWRC    COMPARE RESIDUAL COUNT    Y02922
         BE    ERR082                  BR EQUAL
         NI    EWAFLG3,X'FF'-IOBCDCK-IOBBUS CLEAR BUSOUT         YM2509
*                                        AND CHANNEL DATA CHECK COUNTS
         MVI   EWACNTR3,ZERO           CLEAR DATA CHECK AND      Y02922
*                                      POSTION CHECK COUNTS      Y02922
         MVC   EWCCSW+6(2),IOSCSWRC    STORE NEW COUNT           Y02922
ERR082   TM    EWACNTR3,LEFTTEN        COUNT EQUAL TEN           Y02922
         BO    ERR055                  BR YES
         XR    WKREG1,WKREG1           CLEAR REGISTER
         IC    WKREG1,EWACNTR3         INSERT ERROR COUNT        Y02922
         LA    WKREG1,16(WKREG1)       INCREMENT POSITION CHECK COUNT
         STC   WKREG1,EWACNTR3         STORE NEW COUNT           Y02922
         SPACE 1
*        CONSTRUCT BACKSPACE CCW
         SPACE 1
ERR085   MVI   EWCCCW1,REPOCMD         SET UP BACKSPACE COMMAND  Y02922
         MVC   EWCCCW1+1(3),1(CCWREG)  SET UP DUMMY ADDRESS      Y02922
         MVI   EWCCCW1+4,CCWFLAGS      SET SLI AND SKIP MASK     Y02922
         NC    EWCCCW1+4(1),4(CCWREG)  SET FLAGS IF REQUIRED     Y02922
         OI    EWCCCW1+4,CCFLAG        SET COMMAND CHAINING FLAG Y02922
         MVC   EWCCCW1+6(2),BACKCNT    SET COUNT EQUAL TO ONE    Y02922
         SPACE 1
*        CONSTRUCT RESIDUAL READ CCW
         SPACE 1
         LH    WKREG2,IOSCSWRC         LOAD RESIDUAL COUNT       Y02922
         LA    WKREG2,1(WKREG2)        INCREMENT BY ONE
         LH    WKREG1,6(CCWREG)        LOAD ORIGINAL COUNT
         STH   WKREG2,EWCCCW2+6        STORE RESIDUAL READ COUNT YM2509
         SR    WKREG1,WKREG2           SUBTRACT RESIDUAL COUNT FROM
*                                        ORIGINAL COUNT
         L     WKREG2,0(CCWREG)        LOAD ORIGINAL DATA ADDRESS
         N     WKREG2,MASK2            MASK OUT HIGH ORDER BYTE  Y02922
         AR    WKREG2,WKREG1           INCREMENT TO LAST BYTE FILLED
         ST    WKREG2,EWCCCW2          STORE NEW DATA ADDRESS    Y02922
         MVI   EWCCCW2,READCMD         SET UP READ COMMAND       Y02922
         LA    WKREG1,EWCCCW2          SET POINTER TO READ CCW   Y02922
*                                      WITHIN EXTENSION          Y02922
         CLR   CCWREG,WKREG1           CURRENT CCW WITHIN EXTENSION
         BE    ERR073                  BR YES
         SPACE 1
*        SET UP CCW FLAGS
         SPACE 1
         MVI   EWCCCW2+4,CCWFLAGS      SET SLI MASK              Y02922
         NC    EWCCCW2+4(1),4(CCWREG)  SET SLI FLAG IF REQUIRED  Y02922
         SPACE 1
*        ON COMMAND CHAINING CONSTRUCT TIC TO NEXT CCW
         SPACE 1
         TM    IOBFLAG1(IOBREG),IOBCP2  COMMAND CHAINING SPECIFIED
         BZ    ERR072                  BR NO
         TM    4(CCWREG),IOBCP2        COMMAND CHAINING THIS CCW
         BZ    ERR072                  BR-NO
         OI    EWCCCW2+4,CCFLAG        SET COMMAND CHAINING FLAG Y02922
         SPACE 1
*        IF NEXT INSTRUCTION IS TIC, REMOVE ADDRESS AND INSERT IN
*        TRANSFERRING TIC
         SPACE 1
         TM    8(CCWREG),TICTEST1      COMMAND CODE BITS 5,6,7 ALL ZERO
         BNZ   ERR090                  BRANCH NO
         TM    8(CCWREG),TICTEST2      COMMAND CODE BIT 4 SET
         BZ    ERR090                  BRANCH NO
         L     WKREG2,8(CCWREG)        LOAD ADDR OF NEXT CCW     Y02922
         N     WKREG2,MASK2            CLEAR HIGH ORDER BYTE     Y02922
         B     ERR092
ERR090   LRA   WKREG2,8(CCWREG)        LOAD FAILING CCW REAL ADDRY02922
ERR092   ST    WKREG2,EWCTIC           STORE NEXT CCW ADDR IN TICY02922
         MVI   EWCTIC,TICCMD           SET UP TIC COMMAND        Y02922
         B     ERR072                  BR TO CONTINUE PROCESSING
         EJECT
*        DATA CHECK PROCESSING
         SPACE 1
*        IF THE DATA CHECK OCCURRED ON A READ COMMAND, CONSTRUCT
*        A BACKSPACE CCW AND A RESIDUAL READ CCW IN THE UCB EXTENSION
*        AND RETRY THE OPERATION.
         SPACE 2
ERR100   TM    0(CCWREG),CTRLCMD        CONTROL COMMAND
         BO    ERR055                     BR - YES
         SPACE 1
*        IF THIS ERROR OCCURRED ON A RETRY OPERATION AND IS DIFFERENT
*        FROM THE ORIGINAL ERROR, CLEAR ALL OTHER COUNTS.
         SPACE 1
         CLC   EWCCSW+6(2),IOSCSWRC    COMPARE RESIDUAL COUNT    Y02992
         BE    ERR110                  BR EQUAL
         NI    EWAFLG3,X'FF'-IOBCDCK-IOBBUS  CLEAR BUSOUT        YM2509
*                                      AND CHANNEL DATA CHECK COUNTS
         MVI   EWACNTR3,ZERO           CLEAR POSITION CHECK+DATA Y02922
*                                      CHECK COUNTS
         MVC   EWCCSW+6(2),IOSCSWRC    STORE NEW COUNT           Y02922
ERR110   TM    EWACNTR3,HEXTEN         COUNT EQUAL TO TEN        Y02922
         BO    ERR055                  BR YES
         XR    WKREG1,WKREG1
         IC    WKREG1,EWACNTR3         INSERT ERROR COUNT        Y02922
         LA    WKREG1,1(WKREG1)        INCREMENT DATA CHECK COUNT
         STC   WKREG1,EWACNTR3         REPLACE OLD COUNT         Y02922
         B     ERR085                  BR TO SET UP BACKSPACE
*                                      AND RESIDUAL READ CCW'S
         SPACE 3
*        UNIT EXCEPTION OR INCORRECT LENGTH
         SPACE 1
*        IF UNIT EXCEPTION OR INCORRECT LENGTH OCCURS ON THE FIRST
*        ENTRY TO THE ERP, IT SET AS A PERMANENT ERROR.  IF IT OCCURS
*        FOLLOWING THE RETRY OF ANOTHER ERROR, IT IS SET AS A
*        CORRECTED ERROR TO FORCE THE I/O SUPERVISOR TO SCHEDULE
*        NORMAL UNIT EXCEPTION AND INCORRECT LENGTH PROCESSING (SUCH
*        AS ENTERING APPENDAGES).   WHEN THE ERP IS RE-SCHEDULED, THE
*        ERROR IS THEN INDICATED AS A PERMANENT ERROR.
         SPACE 2
ERR120   TM    EWAFLG3,IOBENT          ENTRY BIT ON              YM2509
         BZ    ERR040                  BR NO
         EJECT
*        SUCCESSFUL RECOVERY - RESET ALL ERROR INDICATORS AND COUNTS
         SPACE 1
ERR125   NI    IOBFLAG1(IOBREG),X'FF'-IOBEXER CLEAR IOB EXCEPTION AND
*                                      ERROR FLAGS
         MVI   EWACNTR3,ZERO           CLEAR OTHER ERROR COUNTS  Y02922
         SPACE 1
*        IF THE ERROR OCCURRED ON THE READ CCW WITHIN THE UCB EXTEN-
*        SION, REPLACE THE CURRENT IOB CSW ADDRESS WITH THE ORIGINAL
*        CSW ADDRESS WHICH WAS STORED IN THE UCB EXTENSION.
         SPACE 1
        LA     WKREG1,EWCCCW2          SET POINTER TO READ CCW   Y02922
*                                      WITHIN EXTENSION          Y02922
         CLR   CCWREG,WKREG1           CURRENT CCW READ CCW
         BNE   ERR127                  BR NO
         MVC   IOSCSW(3),EWCCSW+1      RESTORE ORIGINAL CSW      Y02922
*                                      ADDRESS
ERR127   NC    IOSTATUS(2),CSWMASK     CLEAR ABNORMAL CSW BITS   Y02922
ERR128   NI    EWAFLG3,X'FF'-IOBENT    CLEAR ENTRY ,LOGOUT,AND   YM2509
         NI    IOSFLB,X'FA'                                      Y02922
*                                      MSG BITS AND ERROR COUNTS
         LA    WKREG3,STATUP           GET LOAD NAME TO STATISTICS
*                                      UPDATE
         L     RETREG,VECTXL(CVTREG)   GET ADDRESS XCTL ROUTINE
         BR    RETREG                  XCTL TO STATISTICS UPDATE RTN
         EJECT
*        CLEAR ERPIB ON CHANNEL CONTROL CHECK, INTERFACE CONTROL
*        CHECK, DATA CHAINING, MIXED CHAINING, OR UNRECOVERABLE
*        INTERCEPT CONDITION
         SPACE 2
ERR130   TM    IOSTSB,CCCICC           CHANNEL CONTROL CHECK OR  Y02922
*                                      INTERFACE CONTROL CHECK
         BZ    ERR055                  BR NO
         LA    WKREG2,EWAERPIB         LOAD ADDR OF ERPIB        Y02922
ERR135   XC    0(8,WKREG2),0(WKREG2)   CLEAR ERPIB
ERR140   B     ERR060                  BR TO PERMANENT ERROR PROC
         SPACE 4
*        INTERCEPT CONDITION --- IF INTERVENTION REQUIRED IS ON,
*        HANDLE IT NORMALLY.  OTHERWISE, UNRECOVERABLE INTERCEPT
         SPACE 2
ERR150   TM    IOSTSA,CSWUCK           UNIT CHECK?               Y02922
         BZ    ERR130                      BRANCH IF NO
        CLI    IOSSNS,INTREQ           INT. REG.?                Y02922
         BNE   ERR130                      BRANCH IF NO
         MVI   IOSCOD,ERRORECB         RESET ECB CODE TO '7F'    Y02922
         B     ERR066                      BRANCH TO NORMAL INT REQ RTN
         EJECT
*        CONSTANTS
         SPACE 1
CCWLNG   DC    H'8'                    CCW LENGTH
STALNG   DC    H'10'                   STATISTICS TABLE ENTRY LENGTH
BACKCNT  DC    H'1'                    BACKSPACE COUNT
CSWMASK  DC    X'FDC0'                 CSW ABNORMAL INDICATOR
*                                        RESET MASK
         DS    0F                                                Y02922
MASK2    DC    X'00FFFFFF'             MASK OUT HIGH ORDER BYTE  Y02922
         IECDIOSB                                                Y02922
         IECDERWA
         ORG   EWAIERP                                           Y02922
EWCSNS   DS    CL8                     EXTENDED SENSE BYTES      Y02922
EWCCCW1  DS    XL8                     BACKSPACE - REPOSITION CCWY02922
EWCCCW2  DS    XL8                     RESIDUAL READ CCW-TIC CCW Y02922
EWCTIC   DS    XL8                     TIC CCW                   Y02922
EWCCSW   DS    XL8                     IOSB CSW STORE            Y02922
         END
