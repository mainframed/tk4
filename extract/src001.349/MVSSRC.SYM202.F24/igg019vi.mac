* /* START OF SPECIFICATIONS ****
*01* PROCESSOR = ASSEMXF-370R
**** END OF SPECIFICATIONS ***/
IGG019VI CSECT                    ENTRY POINT FOR RDLNE MODULE
         TITLE  'IGG019VI  RDLNE MODULE---(OCR)---QSAM'
***********************************************************************
*MODULE-NAME                                                   @YM00638
* IGG019VI                                                     @YM00638
*                                                              @YM00638
*DESCRIPTIVE-NAME                                              @YM00638
* OCR READ A LINE MODULE                                       @YM00638
*                                                              @YM00638
*COPYRIGHT                                                     @YM00638
* NONE                                                         @YM00638
*                                                              @YM00638
*CHANGE-ACTIVITY                                               @YM00638
* RELEASE 20 ADDITIONS/CHANGES/DELETIONS
* RELEASE 21 ADDITIONS/CHANGES/DELETIONS
* RELEASE 22 ADDITIONS/CHANGES/DELETIONS
* RELEASE 23 ADDITIONS/CHANGES/DELETIONS
* RELEASE 24 ADDITIONS/CHANGES/DELETIONS
* RELEASE 25 ADDITIONS/CHANGES/DELETIONS
* RELEASE 26 ADDITIONS/CHANGES/DELETIONS
* RELEASE 27 ADDITIONS/CHANGES/DELETIONS
* RELEASE 28 ADDITIONS/CHANGES/DELETIONS
*                                                                     *
*STATUS - RELEASE 20 CHANGE LEVEL 000                                 *
*                                                                     *
*FUNCTION/OPERATION - READ A JOURNAL TAPE LINE WITH ON-LINE CORRECTION*
*                                                                     *
*ENTRY POINTS - IGG019VI CSECT     CALLING SEQUENCE -                 *
*                                  LOAD DCB ADDRESS IN REGISTER 1     *
*                                  L  15,12(1) GET ADDRESS OF RDLNE   *
*                                              MODULE                 *
*                                  BALR 14,15  LINK TO RDLNE MODULE   *
*                                                                     *
*INPUT - IOB                                                          *
*         IOB+CSW-1 - ADDRESS OF FAILING CCW                          *
*        DCB                                                          *
*         DCBIOBA - IOB ADDRESS                                       *
*                                                                     *
*OUTPUT - COMPLETION CODE IN REGISTER 15                              *
*         UPDATED IOB ERROR COUNTERS                                  *
*         IF UNIT EXCEPTION OCCURS, EOF(X'40') IS POSTED IN DCBORBYT  *
*         IF ANY PERMANENT ERRORS OCCUR, THE EIB IS UPDATED           *
*                                                                     *
*EXTERNAL ROUTINES - IOS VIA EXCP                                     *
*                    ERROR COUNTER UPDATE IN SYNCH ROUTINE            *
*                    EIB UPDATE ROUTINE IN SYNCH ROUTINE              *
*                    COMPLETION CODE ROUTINE IN SYNCH ROUTINE         *
*                                                                     *
*EXITS/NORMAL - RETURN TO CALLER - BR 14                              *
*                                                                     *
*EXITS/ERROR - NONE                                                   *
*                                                                     *
*TABLES/WORK AREAS - USER SAVE AREA POINTED AT BY REGISTER 13         *
*                                                                     *
*ATRIBUTES - REENTRANT,REUSABLE,PROBLEM PROGRAM MODE                  *
*                                                                     *
*NOTES - N/A                                                          *
*                                                                     *
***********************************************************************
***********************************************************************
*                                                                     *
*        REGISTER EQUATES                                             *
*                                                                     *
***********************************************************************
REG1     EQU   1                  REGISTER 1
DCBR     EQU   2                  POINTER TO DCB
IOBR     EQU   3                  POINTER TO IOB
CCWADR   EQU   4                  ADDRESS OF CCW IN QUESTION
WKREG1   EQU   5                  WORK REGISTER
WKREG2   EQU   6                  WORK REGISTER
REG8     EQU   8                  REGISTER 8
BASR     EQU   9                  BASE REGISTER
REG9     EQU   9                  REGISTER 9
SAVR     EQU   13                 ADDRESS OF SAVE AREA
RETURN   EQU   14                 CONTAINS RETURN ADDRESS
RETCODE  EQU   15                 USED TO PASS RETURN CODE
***********************************************************************
*                                                                     *
*        IOB EQUATES                                                  *
*                                                                     *
***********************************************************************
ECBCODE  EQU   4                  DISPLACEMENT TO ECB CODE IN THE IOB
CSW      EQU   17                 DISPLACEMENT TO CSW IN IOB
UNEXCP   EQU   20                 DISP TO CSW STATUS IN IOB    @YA00412
IOBSTADR EQU   25                 DISPLACEMENT TO START ADDRESS IN IOB
OFFSET   EQU   40                 OFFSET TO CCW IN IOB
CCWFLAG  EQU   44                 DISPLACEMENT TO CCW FLAGS IN IOB
BYTECNT  EQU   46                 DISPLACEMENT TO CCW BYTE COUNT IN IOB
OFFSET1  EQU   48                 CCW OFFSET IN DCB
UNEXCPON EQU   X'01'              TEST FOR UNIT EXCEPTION
PERMERR  EQU   X'41'              TEST FOR PERMANENT ERRORS
CHAINOFF EQU   X'BF'              TURN OFF COMMAND CHAINING BIT
***********************************************************************
*                                                                     *
*        DCB EQUATES                                                  *
*                                                                     *
***********************************************************************
EOFON    EQU   X'40'              SET EOF IN DCBORBYT
ERRFLOFF EQU   X'CF'              SET ERROR FLAGS IN DCB OFF
***********************************************************************
*                                                                     *
*        SAVE AREA EQUATES                                            *
*                                                                     *
***********************************************************************
RETSAVE  EQU   12                 DISPLACEMENT TO START OF USER SAVE
SAV12    EQU   12                 POINTER FOR SAVING REGISTERS
NORMSAVE EQU   24                 POINT TO REGS 2-9 IN SAVE AREA
***********************************************************************
*                                                                     *
*        MISCELLANEOUS EQUATES                                        *
*                                                                     *
***********************************************************************
RBOCOP   EQU   X'4C'              RBOC COMMAND CODE
MOVECCW  EQU   8                  LENGTH OF CCW TO MOVE
ACTIOB   EQU   8                  USED TO POINT TO ACTUAL IOB
ERRCNT   EQU   24                 DISPLACEMENT TO ADDRESS OF ERROR     C
                                  COUNT UPDATE ROUTINE
EIBUPDAT EQU   8                  DISPLACEMENT TO ADDRESS OF EIB UPDATEC
                                  ROUTINE
RETCD    EQU   16                 DISPLACEMENT TO ADDRESS OF RETURN    C
                                  CODE POSTING ROUTINE
         USING IGG019VI,RETCODE   ESTABLISH ADDRESSIBILITY
         USING IHADCB,DCBR        POINTER TO DCB
         STM   RETURN,SAV12,RETSAVE(SAVR) SAVE USER REGISTERS
         LR    BASR,RETCODE       GET BASE ADDRESS
         DROP  RETCODE            DROP BASE REGISTER
         USING IGG019VI,BASR      ESTABLISH ADDRESSIBILITY
         LR    DCBR,REG1          GET DCB ADDRESS
         L     IOBR,DCBIOBA       GET IOB ADDRESS FOM DCB
***********************************************************************
* PREPARE TO ISSUE RBOC COMMAND                                       *
***********************************************************************
         L     CCWADR,CSW-1(IOBR)  GET CCW ADDRESS
         LA    CCWADR,0(CCWADR)   CLEAR HIGH ORDER BYTE
         SH    CCWADR,EIGHT       BACK UP ONE CCW
         MVC   OFFSET(MOVECCW,IOBR),0(CCWADR) MOVE CCW TO IOB+40
         NI    CCWFLAG(IOBR),CHAINOFF TURN CHAINING BIT OFF
         L     REG8,CSW-1(IOBR)   SAVE ADDRESS OF FAILING CCW
         MVI   DCBOFFSR,OFFSET    MOVE CCW OFFSET TO DCB
         LA    WKREG1,OFFSET(IOBR) GET CCW OFFSET
         ST    WKREG1,IOBSTADR-1(IOBR) PUT CCW ADDR IN IOB START ADDR
         MVI   OFFSET(IOBR),RBOCOP SET CCW COMMAND CODE TO RBOC
         L     WKREG1,OFFSET(IOBR) GET DATA ADDRESSS
         LH    WKREG2,BYTECNT(IOBR) GET CCW BYTE COUNT
         BCTR  WKREG2,0           SUBTRACT ONE
         SR    WKREG1,WKREG2      DECREMENT DATA ADDR BY BYTE COUNT
         EX    WKREG2,CLEAR1      CLEAR INPUT AREA
         LA    REG1,ACTIOB(IOBR)  POINT TO ACTUAL IOB
***********************************************************************
* EXCP AND WAIT                                                       *
***********************************************************************
         EXCP  (REG1)             ISSUE RBOC
         WAIT  ECB=4(IOBR)       WAIT FOR COMPLETION
         ST    REG8,CSW-1(IOBR)   RESTORE POINTER TO FAILING CCW
         MVI   DCBOFFSR,OFFSET1   RESTORE DCB OFFSET
         LA    WKREG1,OFFSET1(IOBR) GET ORIGINAL CCW OFFSET
         ST    WKREG1,IOBSTADR-1(IOBR) PUT CCW ADDR IN IOB START ADDR
***********************************************************************
* LINK TO COUNTER UPDATE ROUTINE IN SYNCH MODULE
***********************************************************************
         L     RETCODE,DCBGERR     LOAD ADDRESS OF GERR ROUTINE
         BAL   RETURN,ERRCNT(RETCODE) LINK TO COUNTER UPDATE ROUTINE
NOCOUNT  CLI   ECBCODE(IOBR),PERMERR DID ANY PERMANENT ERRORS OCCUR
         BE    ERRORS             YES TO ERROR PROCESSOR
         SR    RETCODE,RETCODE    SET RETURN CODE TO ZERO
DONE     LM    REG1,REG9,NORMSAVE(SAVR) RESTORE USER REGISTERS
         L     RETURN,RETSAVE(SAVR) RESTORE RETURN ADDRESS
         BR    RETURN             RETURN TO CALLER
***********************************************************************
* LINK TO SET EIB ROUTINE IN SYNCH MODULE                             *
***********************************************************************
ERRORS   L     RETCODE,DCBGERR    GET ADDRESS OF EIB ROUTINE - 8
         BAL   RETURN,EIBUPDAT(RETCODE) LINK TO EIB ROUTINE
***********************************************************************
* LINK TO COMPLETION CODE ROUTINE IN SYNCH MODULE                     *
***********************************************************************
         L     RETCODE,DCBGERR    GET ADDRESS OF RETURN CODE ROUTINE-16
         BAL   RETURN,RETCD(RETCODE) LINK TO RETURN CODE ROUTINE
         TM    UNEXCP(IOBR),UNEXCPON DID UNIT EXCEPTION OCCUR
         BZ    NOUNEXCP           NO BYPASS EOF SET
         OI    DCBORBYT,EOFON     SET EOF BIT ON
NOUNEXCP NI    DCBIFLGS,ERRFLOFF  SET ERROR FLAGS OFF
         B     DONE               RETURN TO USER
EIGHT    DC    H'8'               CONSTANT OF EIGHT
CLEAR1   XC    0(1,WKREG1),0(WKREG1) EXECUTED INSTRUCTION TO CLEAR AREA
IHADCB   DCBD  DSORG=PS,DEVD=OR   DCB DSECT                    @YM00635
         END   IGG019VI
