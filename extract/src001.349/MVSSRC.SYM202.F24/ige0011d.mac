* /* START OF SPECIFICATIONS ****
*01* PROCESSOR = ASSEMXF-370R
**** END OF SPECIFICATIONS ***/
IGE0011D CSECT                    ENTRY POINT FOR 1288 ERP
         TITLE 'IGE0011D - 1288 OPTICAL CHARACTER READER ERP'
***********************************************************************
*MODULE-NAME                                                   @YM00638
* IGE0011D                                                     @YM00638
*                                                              @YM00638
*DESCRIPTIVE-NAME                                              @YM00638
* OCR ERP MODULE FOR 1288 DEVICE                               @YM00638
*                                                              @YM00638
*COPYRIGHT                                                     @YM00638
* NONE                                                         @YM00638
*                                                              @YM00638
*CHANGE-ACTIVITY                                               @YM00638
* RELEASE 20 ADDITIONS/CHANGES/DELETIONS
*C503000                                                         A33926
*D360000                                                         A33936
*C482000,533000                                                  A33936
*
* RELEASE 21 ADDITIONS/CHANGES/DELETIONS
*C17600,180000                                                    21797
*C379000-382000                                                  A39680
*
* RELEASE VS/2 2.0 ADDITIONS / CHANGES / DELETIONS
*A258000                                                         YM2510
*A222000                                                         Y02967
*C046000,069000,073000,075000,079000,096000-097000,150000,151000 Y02967
*C162000,256000-257000,261000-262000,266000,268000-272000,273000 Y02967
*C276000,283000-284000,286000-288000,290000-292000,305000-307000 Y02967
*C310000,330000,347000,350000,356000,363000-365000,369000,372000 Y02967
*C379000-382000                                                  A01004
*C376000,403000,404000,409000-411000,415000,430000-431000,433000 Y02967
*C435000,437000-439000,441000-442000,448000-457000,459000-467000 Y02967
*C471000,473000-4740008478000,480000-481000,497000,499000-502000 Y02967
*C506000,508000,511000,513003-514000,520000,524000,526000-527000 Y02967
*C534000,                                                        Y02967
*C174000,433000,435000                                           YM2510
*C247000,430400,448800,450400                                    YM2511
*D071000-072000,093000-095000,098000-103000,116000-124000        Y02937
*D132000-138000,207000-210000,294000-304000,
*TITLE 'IGE0011D' - 1288 ERROR RECOVERY PROCEDURES                    *
*                                                                     *
* STATUS RELEASE VS2 / 2.0 CHANGE LEVEL 0000
*                                                                     *
*FUNCTION/OPERATION   IGE0011D IS SCHEDULED FOR EXECUTION WHENEVER THE*
*   I/O SUPERVISOR DETECTS AN ERROR ASSOCIATED WITH THE 1288 OPTICAL  *
*   CHARACTER READER THAT REQUIRES ERROR RECOVERY PROCEDURES.         *
*                                                                     *
*                                                                     *
*   THE ERROR ROUTINE USES THE STANDARD ERROR INTERPRETER ROUTINE FOR *
*   TESTING OF SENSE BITS AND CSW STATUS BITS. THE FOLLOWING ACTIONS  *
*   ARE TAKEN DEPENDING UPON THE NATURE OF THE ERROR                  *
*                                                                     *
*   1)  NORMAL CONDITION OR SUCCESSFUL RETRY OF RECOVERABLE ERROR     *
*       CONDITION                                                     *
*                                                                     *
*       IOSB EXCEPTION, ERROR FLAGS ARE CLEARED AND CONTROL IS @YM00636
*       TRANSFERRED TO THE STATISTICS UPDATE ROUTINE.                 *
*   2)  RECOVERABLE ERROR                                             *
*                                                                     *
*       IOSB ERROR FLAG IS SET.                                @YM00636
*       IOSB EXCEPTION FLAG IS CLEARED.                        @YM00636
*       ERROR COUNTS ARE UPDATED.                                     *
*       CONTROL IS PASSED TO THE I/O SUPERVISOR VIA SVC 15 AND SVC 3  *
*       TO RETRY THE CHANNEL PROGRAM.                                 *
*                                                                     *
*   3)  PERMANENT ERROR                                               *
*       IOSB ERROR AND EXCEPTION FLAGS ARE SET.                @YM00636
*       IOSB MESSAGE AND LOGOUT FLAGS MAY BE SET.              @YM00636
*       CONTROL IS RETURNED TO THE I/O SUPERVISOR EITHER DIRECTLY     *
*       THROUGH ISSUANCE OF AN SVC 15 AND AN SVC 3 OR INDIRECTLY BY   *
*       FIRST PASSING CONTROL TO THE WTO ROUTINE WHICH IN TURN PASSES *
*       CONTROL TO THE STATISTICS UPDATE ROUTINE AND THE I/O OUTBOARD *
*       RECORDING ROUTINE, IF REQUIRED, AND EVENTUALLY TO THE I/O     *
*       SUPERVISOR.                                                   *
*                                                                     *
*ENTRY POINTS                                                         *
*       IGE0011D - ERROR RECOVERY PROCEDURE ENTRY POINT               *
*                                                                     *
*       DATA PASSED TO THE ROUTINE CONSISTS OF STANDARD SYSTEM CONTROL*
*       BLOCKS.                                                       *
*                                                                     *
*INPUT  IOSB POINTER IN REGISTER 1                               Y02967
*                                                                     *
*OUTPUT I/O ERROR AND INTERVENTION REQUIRED MESSAGES IN SOME INSTANCES*
*                                                                     *
*EXTERNAL ROUTINES  ERROR INTERPRETER ROUTINE                         *
*                                                                     *
*EXITS-NORMAL  XCTL TO STATISTICS UPDATE ROUTINE FOLLOWING SUCCESSFUL *
*       RECOVERY OF A RETRYABLE ERROR OR UPON DETECTION OF A NORMAL   *
*       CONDITION.                                                    *
*                                                                     *
*     -ERROR                                                          *
*                                                                     *
*   1)  SVC 15 AND SVC 3 EXIT TO I/O SUPERVISOR FOR RE-EXECUTION OF   *
*       THE CHANNEL PROGRAM UPON DETECTION OF A RETRYABLE ERROR.      *
*                                                                     *
*   2)  XCTL TO WTO ROUTINE FOR ISSUANCE OF A PERMANENT I/O ERROR     *
*       MESSAGE AND UPDATING OF STATISTICS AND SYS1.LOGREC IN THE CASE*
*       OF SOME PERMANENT ERRORS.                                     *
*                                                                     *
*   3)  SVC 15 AND SVC 3 EXIT TO I/O SUPERVISOR IN THE CASE OF SOME   *
*       PERMANENT I/O ERRORS.                                         *
*                                                                     *
*TABLES/WORK AREAS                                                    *
*   1)  INPUT/OUTPUT SYSTEM BLOCK (IOSB)                         Y02967
*                                                                     *
*   3)  DYNAMIC WORK AREA                                        Y02967
*                                                                     *
*   4)  STATISTICS UPDATE TABLE                                  Y02967
*                                                                     *
*ATTRIBUTES                                                           *
*                                                                     *
*       RE-ENTRANT,SUPERVISOR MODE                               Y02967
*                                                                     *
*NOTES                                                                *
*                                                                     *
*       N/A                                                           *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT                                                 @YA00406
***********************************************************************
*                                                                     *
*        STATUS INDICATORS                                            *
*                                                                     *
***********************************************************************
CSWUCK   EQU   X'02'              UNIT CHECK
***********************************************************************
*                                                                     *
*        REGISTER DEFINITION                                          *
*                                                                     *
***********************************************************************
IOSBREG  EQU   1                  IOSB REG                     @YM00636
RVREG    EQU   1                  WORK REGISTER                  Y02967
CCWREG   EQU   2                  CCW ADDRESS REGISTER
WORKR3   EQU   3                  WORK REGISTER                @YM00636
ERREG    EQU   4                  WORK REGISTER
WORKR4   EQU   4                  WORK REGISTER                @YM00636
WORKR5   EQU   5                  WORK REGISTER                @YM00636
WORKR6   EQU   6                  WORK REGISTER                @YM00636
COMREG   EQU   8                  COMMUNICATION TABLE REG
SVREG    EQU   9                  WORK REGISTER
WORKR9   EQU   9                  WORK REGISTER                @YM00636
ERPREG   EQU   10                 DYNAMIC WORKAREA REG           Y02967
VTLREG   EQU   11                 ADDR OF VIRTUAL I/O AREA       Y02967
LINKR    EQU   12                 WORK REGISTER
ERRE1    EQU   13                 WORK REGISTER
ERRETR   EQU   14                 RETURN REG
BASREG   EQU   15                 BASE REGISTER
***********************************************************************
*                                                                     *
*        RETRY COUNTS                                                 *
*                                                                     *
***********************************************************************
BOCOUNT  EQU   X'08'              TEST FOR BUS OUT CHECK RETRIED
CDCCNT   EQU   X'08'              TEST FOR CHANNEL DATA CHK    @YM00636
CCCNT    EQU   5                  TEST FOR CHAINING CHECK RETRY
EQCNT    EQU   X'03'              TEST FOR EQUIPMENT CHECK RETRY  21797
DCCOUNT1 EQU   X'03'              DOCUMENT DATA CHECK RETRY COUNT
INCCOUNT EQU   16                 INCREMENT FOR OVERRUN RETRY COUNT
ORCOUNT  EQU   X'50'              OVERRUN RETRY COUNT (FIVE)
ILCOUNT  EQU   X'03'              INCORRECT LENGTH RETRY COUNT    21797
         EJECT                                                 @YA00406
***********************************************************************
*                                                                     *
*        MISCELLANEOUS DEFINITIONS                                    *
*                                                                     *
***********************************************************************
VEXCTL   EQU   44                 ADDR OF XCTL ROUTINE
STAUPRTN EQU   254                STATISTICS UPDATE ROUTINE
CVTADDR  EQU   16                 POINTER TO CVT ADDRESS
BYTECNT  EQU   6                  DISPLACMENT TO BYTE COUNT IN CCW
CLRCNT   EQU   X'03'              BYTE COUNT FOR CLEARING COUNTERS
CHINTCHK EQU   X'06'              TEST FOR CHANNEL OR INTERFACE CHECK
SNSLEN   EQU   2                  LENGTH OF SENSE BYTES TO PUT IN STATA
EOP      EQU   X'40'              TEST FOR END OF PAGE
NODOC    EQU   X'20'              TEST FOR NO DOCUMENT FOUND
INVSNS   EQU   X'97'              TEST FOR INVALID SENSE BITS
INVSNS1  EQU   X'01'              TEST FOR INVALID SENSE BIT
CLEARCNT EQU   X'02'              COUNT OF ERROR COUNTER BYTES TO CLEAR
ERRORSVC EQU   15                 ERROR EXCP
RETSVC   EQU   3                  RETURN TO IOS SVC
ERPIBLEN EQU   8                  ERPIB LENGTH
DC       EQU   0                  DISPLACEMENT TO DATA CHECK COUNTS
ILC      EQU   1                  DISPLACEMENT TO INCORRECT LENGTH CNTS
EC       EQU   2                  DISPLACEMENT TO EQUIPMENT CHECK CNTS
ZEROCNT  EQU   X'0F'              TEST FOR FIRST RETRY, INC LENGTH
VFCT68   EQU   68                 INDEX TO INTERPRETER ROUTINE
WTORTN   EQU   253                LOAD NAME TO WTO RTN.
VFCF120  EQU   X'0120'            DISPLACEMENT TO TRANSLATE RTN  Y02967
CCWFLAG  EQU   4                  DISPLACEMENT TO CCW FLAG BYTE  Y02967
IDA      EQU   X'04'              TEST FOR INDIRECT ADDRESS      Y02967
IOB      EQU   X'05'              DISPLACEMENT TO IOB          @YM00636
IOSERRCT EQU   X'1C'              DISP TO IOSB ERROR CNT       @YM00636
ADDRLN   EQU   3                  LENGTH OF 3 USED FOR COMPARE @YA00406
         EJECT                                                 @YA00406
         USING *,BASREG
         USING EWA,ERPREG         BASE FOR WORK AREA             Y02967
         USING IOSB,IOSBREG       BASE REG FOR IOSB            @YM00636
         L     ERPREG,IOSERP      GET WORK AREA ADDRESS          Y02967
         L     COMREG,CVTADDR     GET CVT ADDRESS
         OI    IOSFLA,IOSEX       SET EXCEPTION CONDITION ON   @YM00636
         TM    IOSFLA,IOSERR      ERP IN CONTROL               @YM00636
         BO    ERR000             YES CONTINUE
* CLEAR IOSB ERROR COUNTERS                                    @YM00636
         XC    EWACNTR1(CLRCNT),EWACNTR1 CLEAR COUNT             Y02967
ERR000   CLI   IOSCOD,IOSFINTC    IS IS AN INTERCEPT CONDITION @YM00636
         BE    ERR0012            YES SEE IF CHANNEL ERROR
         CLI   IOSCOD,IOSERRC     CHECK FOR X'41' IN ECB       @YA00406
         BNE   ERR001A            NO,CONTINUE                  @YA00406
         CLC   IOSCSWCA(ADDRLN),WKAREA IS CCW ADDRESS VALID?   @YA00406
         BE    ERR007             NO,PERMANENT ERROR           @YA00406
ERR001A  TM    IOSFLA,IOSACHN     IS IT MIXED CHAINING         @YA00406
         BO    ERR0012            YES SEE IF CHANNEL ERROR
         TM    IOSCC,IOSCC1       CODE EQUAL 10                @YM00636
         BZ    ERR002             NO CONTINUE
         L     CCWREG,IOSVST      LOAD VIRTUAL ADDRESS OF CCW    Y02967
         B     ERR003             CONTINUE                       Y02967
ERR0012  TM    IOSTSB,CHINTCHK    CHANNEL OR INTERFACE CHK     @YM00636
         BZ    ERR007             NO EXIT TO WTO ROUTINE
         B     ERR010             YES TO CHANNEL+INTERFACE CHK RTN
ERR002   L     CCWREG,IOSCSW-1    GET CCW ADDRESS                Y02967
         LA    CCWREG,0(CCWREG)   CLEAR HIGH ORDER BYTE
         SH    CCWREG,ERRON1      BACK UP ONE CCW
         TM    IOSFLA,IOSCCHN     IS COMMAND CHAINING USED     @YM00636
         BZ    ERR003             NO SET UP FOR WTO
         TM    IOSFLA,IOSERR      ENTERED AFTER RETRY          @YM00636
         BZ    ERR002A            NO CONTINUE
         LRA   ERREG,0(CCWREG)    LOAD REAL ADDR OF CCW          Y02967
         S     ERREG,IOSRST       SUBTRACT RESTART ADDRESS       Y02967
         BE    ERR003             IF EQUAL ERROR
         LA    WORKR5,EWACNTR1    POINT TO IOSB ERROR CNTRS    @YM00636
         LA    WORKR6,EWCCNTRS    POINT TO TEMPORARY ERROR     @YM00636
*                                 COUNTERS                     @YM00636
         BAL   LINKR,ERR018       LINK TO COUNTER UPDATE ROUTINE
ERR002A  LRA   ERREG,0(CCWREG)    LOAD REAL ADDR OF CHANNEL PGM  Y02967
         ST    ERREG,IOSRST       SAVE CCW START ADDRESS         Y02967
ERR003   TM    IOSTSA,CSWUCK      IS IT  UNIT CHECK            @YM00636
         BZ    ERR004             NO DETERMINE ERROR TYPE
         OC    EWASTUP(SNSLEN),IOSSNS OR SENSE BITS              Y02967
         NC    EWASTUP(SNSLEN),MASKOUT MASK UNWANTED BITS        Y02967
         NI    EWASTUP+1,EOP      CLEAR POSSIBLE END OF PAGE     Y02967
ERR004   LR    SVREG,BASREG       SAVE BASE REGISTER
         L     BASREG,VFCT68(COMREG) GET INTERPRETER ROUTINE ADDRESS
ERR0041  BALR  ERRETR,BASREG      LINK TO INTERPRETER            Y02967
         DC    X'1D'              TEST FOR CHANNEL CONTROL CHECK
         DC    AL1(ERR010L-ERR0041-2) BRANCH TO CCC ACTION
         DC    X'1E'              TEST FOR INTERFACE CONTROL CHECK
         DC    AL1(ERR010L-ERR0041-4) BRANCH TO ICC ACTION
         DC    X'1C'              TEST FOR CHANNEL DATA CHECK
         DC    AL1(ERR200L-ERR0041-6) BRANCH TO CDC ACTION
         DC    X'10'              TEST FOR ATTENTION
         DC    AL1(ERR007-ERR0041-8) BRANCH TO ATTENTION ACTION
         DC    X'12'              TEST FOR CONTROL UNIT END
         DC    AL1(ERR007-ERR0041-10) BRANCH TO CUE ACTION
         DC    X'16'              TEST FOR UNIT CHECK
         DC    AL1(ERR006-ERR0041-12) BRANCH TO UNIT CHECK ACTION
         DC    X'2F'              END OF LIST
         DC    AL1(ERR004B-ERR0041-14)  PASS CONTROL TO ERR004B
* FOLLOWING BRANCHES GO TO ROUTINES MORE THAN 128 BYTES AWAY
ERR010L  B     ERR010             GO TO ERR010
ERR200L  B     ERR200             GO TO CDC ROUTINE
ERR004B  LR    SVREG,BASREG       SAVE BASE REGISTER
         L     BASREG,VFCT68(COMREG) GET INTERPRETER ROUTINE ADDRESS
ERR004B1 BALR  ERRETR,BASREG      LINK TO INTERPRETER            Y02967
         DC    X'1F'              TEST FOR CHAINING CHECK
         DC    AL1(ERR011L-ERR004B1-2) BRANCH TO CC ACTION
         DC    X'1A'              TEST FOR PROGRAM CHECK
         DC    AL1(ERR007A-ERR004B1-4) BRANCH TO PC ACTION
         DC    X'1B'              TEST FOR PROTECTION CHECK
         DC    AL1(ERR007A-ERR004B1-6) BRANCH TO PC ACTION
         DC    X'19'              TEST FOR INCORRECT LENGTH
         DC    AL1(ERR017L-ERR004B1-8) BRANCH TO IL ACTION
         DC    X'17'              CHECK FOR UNIT EXCEPTION
         DC    AL1(ERR013L-ERR004B1-10) BRANCH TO UE ACTION
         DC    X'2F'              END OF LIST
         DC    AL1(ERR004B2-ERR004B1-12) PASS CONTROL TO ERR004B2
* THESE BRANCHS GO TO ROUTINES MORE THAN 128 BYTES AWAY
ERR011L  B     ERR011             GO TO CC ACTION
ERR017L  B     ERR017             GO TO IL ACTION
ERR013L  B     ERR013             GO TO UE-ACTION
ERR004B2 TM    IOSTSA,CSWUCK      IS UNIT CHECK POSTED         @YM00636
         BZ    ERR005             NO TO STATISTICS UPDATE ROUTINE
ERR004C  TM    IOSSNS+1,EOP       IS IT END OF PAGE              Y02967
         BO    ERR008             YES EXIT TO IOS
ERR004D  TM    IOSSNS+1,NODOC     IS NO DOCUMENT POSTED          Y02967
         BO    ERR005B            YES CLEAN UP
ERR005   NI    IOSFLA,X'FF'-IOSEX CLEAR EXCEPTION, ERR CORRECT @YM00636
         B     ERR005B            CONTINUE CLEAN UP
ERR005B  NI    IOSFLA,X'FF'-IOSERR CLEAR IOSERR                @YM00636
         LA    ERRE1,STAUPRTN     LOAD NAME OF STATISTICS UPDATE RTN
ERR005C  LA    WORKR5,EWCCNTRS    POINT TO TEMPORARY ERROR     @YM00636
*                                 COUNTERS                     @YM00636
         LA    WORKR6,EWACNTR1    POINT TO IOSB ERROR COUNTERS   Y02967
         BAL   LINKR,ERR018       LINK TO COUNTER UPDATE ROUTINE
         L     ERRETR,VEXCTL(COMREG) GET ADDRESS OF XCTL ROUTINE
         BR    ERRETR             EXIT VIA XCTL
ERR006   TM    IOSSNS+1,INVSNS    ANY INVALID SENSE BITS ON      Y02967
         BC    5,ERR007           YES ALL DONE-CLEAN UP
ERR006A  TM    IOSSNS,INVSNS1     INVALID SENSE BITS ON          Y02967
         BO    ERR007             YES GO CLEAN UP
ERR006B  LR    SVREG,BASREG       SAVE BASE ADDRESS
         L     BASREG,VFCT68(COMREG) GET INTERPRETER ROUTINE ADDRESS
ERR006C  BALR  ERRETR,BASREG      LINK TO INTERPRETER ROUTINE    Y02967
         DC    X'03'              TEST FOR EQUIPMENT CHECK
         DC    AL1(ERR012L-ERR006C-2) BRANCH TO EC ROUTINE     @YM00636
         DC    X'06'              TEST FOR NONRECOVERY           A01004
         DC    AL1(ERR007-ERR006C-4)   BRANCH TO NOCREC ROUTINE  A01004
         DC    X'01'              TEST FOR INTERVENTION REQUIRED A01004
         DC    AL1(ERR006D-ERR006C-6)  BRANCH TO IR ROUTINE      A01004
         DC    X'02'              TEST FOR BUS OUT CHECK
         DC    AL1(ERR015L-ERR006C-8) BRANCH TO BUS OUT CHECK ROUTINE
         DC    X'04'              TEST FOR DATA CHECK
         DC    AL1(ERR014L-ERR006C-10) BRANCH TO DATA CHECK ROUTINE
         DC    X'05'              TEST FOR OVERRUN
         DC    AL1(ERR016L-ERR006C-12) BRANCH TO OVERRUN ROUTINE
         DC    X'00'              TEST FOR COMMAND REJECT
         DC    AL1(ERR007A-ERR006C-14) BRANCH TO COMMAND REJECT ROUTINE
         DC    X'0C'              TEST FOR INVALID OPERATION
         DC    AL1(ERR007-ERR006C-16)  BRANCH TO INVALID OPERATION RTN
         DC    X'2F'              END OF LIST
         DC    AL1(ERR004BL-ERR006C-18) CONTINUE CHECKING FOR ERRORS
ERR012L  B     ERR012             GO TO EC ACTION
ERR015L  B     ERR015             GO TO BUS OUT ACTION
ERR014L  B     ERR014             GO TO DATA CHECK ACTION
ERR016L  B     ERR016             GO TO OVERRUN ACTION
ERR004BL B     ERR004B            CONTINUE ERROR CHECKING
ERR006D  OI    IOSFLA,IOSERR      SET IOSERR ON                @YM00636
         B     ERR007B            GO WRITE TO OPERATOR
* EXIT TO WTO ROUTINE
ERR007   OI    IOSFLB,IOSLOG      SET LOGOUT FALG                Y02967
         NI    EWAFLG2,X'FF'-EWAMDR    SET FLAG BIT FOR OBR      Y02967
ERR007A  OI    IOSFLB,IOSMSG      SET MESSAGE FLAG               Y02967
         NI    IOSFLA,X'FF'-IOSERR CLEAR IOSERR FLAG           @YM00636
ERR007B  LA    ERRE1,WTORTN       LOAD NAME OF WTO ROUTINE
         B     ERR005C            GET OUT VIA XCTL
ERR008   NI    IOSFLA,X'FF'-IOSERR CLEAR IOSERR FLAG           @YM00636
         LA    WORKR5,EWCCNTRS    POINT TO TEMPORARY ERROR       Y02967
*                                 COUNTERS                     @YM00636
         LA    WORKR6,EWACNTR1    POINT TO IOSB CNTRS          @YM00636
         BAL   LINKR,ERR018       LINK TO COUNTER UPDATE ROUTINE
         SVC   ERRORSVC           ISSUE ERROR EXCP
         SVC   RETSVC             RETURN TO IOS VIA SVC 3
*   UPDATE EWA ERROR COUNTERS ROUTINE                            Y02967
ERR018   SR    WORKR4,WORKR4      ZERO OUT                     @YM00636
         SR    WORKR9,WORKR9      WORK REGISTERS               @YM00636
         IC    WORKR4,DC(WORKR5)  GET DATA                     @YM00636
         IC    WORKR9,DC(WORKR6)  CHECK COUNTS                 @YM00636
         AR    WORKR4,WORKR9      ADD COUNTS                   @YM00636
         STC   WORKR4,DC(WORKR6)  UPDATE DATA CHECK COUNT      @YM00636
         IC    WORKR4,ILC(WORKR5) GET INCORRECT                @YM00636
         IC    WORKR9,ILC(WORKR6) LENGTH COUNTS                @YM00636
         AR    WORKR4,WORKR9      ADD COUNTS                   @YM00636
         STC   WORKR4,ILC(WORKR6) UPDATE INCORRECT LENGTH COUNT
         IC    WORKR4,EC(WORKR5)  GET EQUIPMENT CHECK          @YM00636
         IC    WORKR9,EC(WORKR6)  CHECK COUNTS                 @YM00636
         AR    WORKR4,WORKR9      ADD COUNTS                   @YM00636
         STC   WORKR4,EC(WORKR6)  UPDATE EQUIPMENT CHECK COUNT @YM00636
CLEAR1   XC    0(CLEARCNT,WORKR5),0(WORKR5) CLEAR ERROR CNTRS    Y02967
         TM    IOSDVRID,IOSXCPID  DRIVER ID EXCP               @YM00636
         BNO   ERR018A            NO,IOSB DOES NOT EXIST       @YM00636
         L     WORKR4,IOSUSE      LOAD ADDR OF RQE             @YM00636
         L     WORKR4,IOB-1(WORKR4) LOAD ADDRESS OF IOB        @YM00636
         MVC   IOSERRCT(4,WORKR4),0(WORKR6) MOVE ERROR COUNTERS  Y02967
ERR018A  BR    LINKR              RETURN                       @YM00636
* CHANNEL DATA CHECK ROUTINE                                   @YM00636
ERR200   TM    EWACNTR3,CDCCNT    CHANNEL DATA CHECK           @YM00636
         BO    ERR007             YES EXIT TO WTO ROUTINE
         OI    EWACNTR3,CDCCNT    SET TO INDICATE RETRIED      @YM00636
* RETRY ROUTINE
ERR009   SR    WORKR3,WORKR3      CLEAR REGISTER               @YM00636
               XC    IOSSNS(2),IOSSNS                          @ZA00415
         IC    WORKR3,0(CCWREG)   GET OP CODE                  @YM00636
         N     WORKR3,CLEARHI     CLEAR TOP FOUR BITS          @YM00636
         CLM   WORKR3,1,RBTEST    IS COMMAND READ BACKWARD     @YM00636
         BE    ERR009B            YES CLEAR INPUT AREA & RETRY
         N     WORKR3,BITS56      CLEAR BITS 5+6               @YM00636
         CLM   WORKR3,1,RFTEST    IS COMMAND READ FORWORD      @YM00636
         BE    ERR009B            YES CLEAR INPUT AREA & RETRY
ERR009A  OI    IOSFLA,IOSERR      SET IOSERR ON                @YM00636
         SVC   ERRORSVC           RETRY CCW
         SVC   RETSVC             RETURN TO IOS VIA SVC 3
* CLEAR INPUT AREA
*   THE REAL DATA ADDRESS IN THE CCW IS TRANSLATED TO A          Y02967
*   VIRTUAL ADDRESS. IF INDIRECT ADDRESSING IS SPECIFIED IN      Y02967
*   THE CCW,EACH ADDRESS IN THE IDA LIST IS TRANSLATED.          Y02967
ERR009B  LR    WORKR4,IOSBREG     SAVE IOSB REGISTER           @YM00636
         L     RVREG,0(CCWREG)    GET REAL INPUT AREA ADDR       Y02967
         N     RVREG,MASK1        CLEAR OUT COMMAND CODE         Y02967
         LR    SVREG,BASREG       SAVE BASE REGISTER             Y02967
         L     BASREG,VFCF120(COMREG)  GET ADDR OF TRANSLATE RTN YM2511
         BALR  ERRETR,BASREG      LINK TO TRANSLATE ROUTINE      Y02967
         LTR   BASREG,BASREG      ADDRESS TRANSLATED ?         @ZA04474
         LR    BASREG,SVREG       RESTORE BASE REGISTER          Y02967
         BZ    ERR009B1           NO, DO NOT RETRY CCW         @ZA04474
         LR    IOSBREG,WORKR4     RESTORE IOSB REGISTER        @ZA04474
         NI    IOSFLA,X'FF'-IOSERR SET IOS ERROR FLAG OFF      @ZA04474
         SVC   ERRORSVC           RETRY CCW                    @ZA04474
         SVC   RETSVC             RETURN TO IOS VIA SVC 3      @ZA04474
ERR009B1 EQU   *                                               @ZA04474
         LR    ERRE1,RVREG        SAVE VIRTUAL ADDR OF IO LIST   Y02967
         LR    VTLREG,RVREG       SAVE VITUAL ADDR OF IO AREA    Y02967
         LR    IOSBREG,WORKR4     RESTORE IOSB REGISTER        @YM00636
         LH    WORKR6,BYTECNT(CCWREG) GET BYTE COUNT             Y02967
         TM    CCWFLAG(CCWREG),IDA INDIRECT ADDRESSING SPECIFED  Y02967
         BZ    ERR009H            NO, IO ADDRESS CORRECT         Y02967
*                                                                Y02967
*   IDA IS SPECIFIED. TRANSLATE EACH ADDRESS IN LIST             Y02967
*                                                                Y02967
ERR009C  L     RVREG,0(ERRE1)     GET ADDRESS IN IDA LIST        Y02967
         LR    SVREG,BASREG       SAVE BASE REGISTER             Y02967
         L     BASREG,VFCF120(COMREG)  GET ADDR OF TRANSLATE RTN YM2511
         BALR  ERRETR,BASREG      LINK TO TRANSLATE ROUTINE      Y02967
         LTR   BASREG,BASREG      ADDRESS TRANSLATED ?         @ZA04474
         LR    BASREG,SVREG       RESTORE BASE REGISTER          Y02967
         BZ    ERR009C1           NO, DO NOT RETRY CCW         @ZA04474
         LR    IOSBREG,WORKR4     RESTORE IOSB REGISTER        @ZA04474
         NI    IOSFLA,X'FF'-IOSERR TURN OFF IOS ERROR FLAG     @ZA04474
         SVC   ERRORSVC           RETRY CCW                    @ZA04474
         SVC   RETSVC             RETURN TO IOS VIA SVC 3      @ZA04474
ERR009C1 EQU   *                                               @ZA04474
         LR    VTLREG,RVREG       SAVE ADDRESS OF IO AREA        Y02967
ERR009D  LR    IOSBREG,WORKR4     RESTORE IOSB REGISTER        @YM00636
*   THE VIRTUAL ADDRESS IS DIVIDED BY THE PAGE SIZE(4096)  THE   Y02967
*   REMAINDER IS USED TO DETERMINE HOW MANY BYTES CAN BE         Y02967
*   CLEARED IN THE NEXT EXECUTE INSTRUCTION.                     Y02967
         LR    WORKR4,VTLREG      LOAD VITUAL ADDR OF IO ADRA    Y02967
         SRDA  WORKR4,32(0)       SHIFT INTO ODD REGISTER        Y02967
         D     WORKR4,CON4096     DIVIDE ADDR BY PAGE SIZE       Y02967
         LA    WORKR4,1(WORKR4)   ADJUST REMAINDER               Y02967
         CLM   WORKR3,1,RFTEST    IS COMMAND READ FORWARD      @YM00636
         BE    ERR009F            YES, ADDRESS CORRECT           Y02967
         CR    WORKR4,WORKR6      REMAINDER GE BYTE COUNT        Y02967
         BNL   ERR009E            YES, CLEAR ENTIRE IO AREA      Y02967
         SR    WORKR6,WORKR4      SUBTRACT NO. OF BYTES IN PAGE  Y02967
         BCTR  WORKR4,0           DECREMENT COUNT BY ONE         Y02967
         SR    VTLREG,WORKR4      ADJUST IO ADDRESS              Y02967
         B     ERR009G            GO CLEAR INPUT AREA            Y02967
ERR009E  BCTR  WORKR6,0           DECREMENT COUNT BY ONE         Y00620
         SR    VTLREG,WORKR6      ADJUST IO ADDRESS              Y00620
         B     ERR009I            GO CLEAR INPUT AREA            Y02967
ERR009F  S     WORKR4,CON4096     COMPUTE NO. OF                 Y02967
         LPR   WORKR4,WORKR4      BYTES LEFT IN PAGE             Y02967
         LA    WORKR4,1(WORKR4)   ADJUST NO. OF BYTES            Y02967
         CR    WORKR4,WORKR6      REMAINDER GE BYTE COUNT        Y02967
         BNL   ERR009H            YES, CLEAR ENTIRE IO AREA      Y02967
         SR    WORKR6,WORKR4      SUBTRACT NO. OF BYTES IN PAGE  Y02967
         BCTR  WORKR4,0           DECREMENT COUNT BY ONE         Y02967
ERR009G  EX    WORKR4,CLEAR       CLEAR INPUT AREA               Y02967
         LA    ERRE1,4(ERRE1)     INCREMENT TO NEXT IO ADDRESS   Y02967
         LR    WORKR4,IOSBREG     SAVE IOSB REGISTER           @YM00636
         B     ERR009C            GO TRANSLATE NEXT ADDRESS      Y02967
ERR009H  BCTR  WORKR6,0           DECREMENT COUNT BY ONE         Y02967
         CLM   WORKR3,1,RFTEST    IS COMMAND READ FORWARD      @YM00636
         BE    ERR009I            YES,IO ADDRESS OK              Y02967
         SR    VTLREG,WORKR6      ADJUST IO ADDRESS              Y02967
ERR009I  EX    WORKR6,CLEAR       CLEAR INPUT AREA               Y02967
         B     ERR009A            RETRY BAD CCW                  Y02967
* CHANNEL CHECK ROUTINE
ERR010   LA    WORKR5,EWAERPIB    LOAD ADDRESS OF ERPIB          Y02967
ERR010B  XC    0(ERPIBLEN,WORKR5),0(WORKR5) CLEAR ERPIB        @YM00636
         B     ERR007             GO TO LOGOUT ROUTINE
* CHAINING CHECK ROUTINE
ERR011   IC    WORKR5,EWACNTR4    GET CHAINING CK COUNT        @YM00636
         LA    WORKR5,1(WORKR5)   INCREMENT COUNT              @YM00636
         STC   WORKR5,EWACNTR4    UPDATE COUNT                 @YM00636
         TM    EWACNTR4,CCCNT     IS COUNT FIVE                @YM00636
         BO    ERR007             YES TO LOGOUT
         B     ERR009             NO RETRY
* EQUIPMENT CHECK
ERR012   IC    WORKR5,EWACNTR3    GET EQUIPMENT CK COUNT       @YM00636
         LA    WORKR5,1(WORKR5)   INCREMENT COUNT              @YM00636
         STC   WORKR5,EWACNTR3    UPDATE COUNTERS              @YM00636
         CLI   EWACNTR3,EQCNT     IS COUNT NOW TEN               Y02967
         BE    ERR008             YES TO IOS                     A33936
         B     ERR009             NO RETRY
* UNIT EXCEPTION
ERR013   TM    IOSFLA,IOSERR     IOSERR ON-ENTERED AFTER RETRY @YM00636
         BZ    ERR005B            NO EXIT TO STATISTICS UPDATE
ERR013A  NI    IOSFLA,X'FF'-IOSEX SET IOSEX OFF                @YM00636
         B     ERR005B            TO STATISTICS UPDATE ROUTINE
* DATA CHECK ROUTINE
ERR014   IC    WORKR5,EWACNTR1    GET DATA CHECK ERROR COUNT     Y02967
         LA    WORKR5,1(WORKR5)   INCREMENT COUNT              @YM00636
         STC   WORKR5,EWACNTR1    UPDATE DATA CHECK ERROR COUNT  Y02967
         TM    EWACNTR1,DCCOUNT1  IS COUNT NOW THREE             Y02967
         BO    ERR008             YES TO IOS                     A33926
         B     ERR009             NO RETRY
* BUSOUT CHECK
ERR015   TM    EWACNTR4,BOCOUNT   BUSOUT CHECK COUNT EQUAL ONE   Y02967
         BO    ERR007             YES TO WTO ROUTINE
         OI    EWACNTR4,BOCOUNT   SET COUNT TO ONE               Y02967
         B     ERR009             RETRY
* OVERRUN
ERR016   IC    WORKR5,EWACNTR4    GET OVERRUN ERROR COUNT      @YM00636
         LA    WORKR5,INCCOUNT(WORKR5) INCREMENT COUNT         @YM00636
         STC   WORKR5,EWACNTR4    UPDATE ERROR COUNT           @YM00636
         TM    EWACNTR4,ORCOUNT   DOES COUNT EQUAL FIVE          Y02967
         BO    ERR007             YES EXIT TO WTO ROUTINE
         B     ERR009             NO RETRY
* INCORRECT LENGTH
ERR017   TM    IOSFLA,IOSERR      ENTERED AFTER RETRY          @YM00636
         BZ    ERR017A            NO
         TM    EWACNTR2,ZEROCNT   FIRST TIME HERE                Y02967
         BC    5,ERR017A          NO CONTINUE
         NI    IOSFLA,X'FF'-IOSEX CLEAR IOSEX                  @YM00636
         B     ERR008             EXIT TO IOS
ERR017A  IC    WORKR5,EWACNTR2    GET INCORRECT LENGTH COUNT     Y02967
         LA    WORKR5,1(WORKR5)   INCREMENT ERROR COUNT        @YM00636
         STC   WORKR5,EWACNTR2    UPDATE COUNTERS                Y02967
         CLI   EWACNTR2,ILCOUNT   IS COUNT NOW FIVE              Y02967
         BE    ERR008             YES EXIT TO IOS
         B     ERR009             NO RETRY
         DS    0H                 FORCE HALF WORD ALIGNMENT
ERRON1   DC    AL2(8)             CONSTANT OF 8
MASKOUT  DC    X'267F'            MASK FOR CLEARING UNWANTED   @YM00636*
                                  SENSE DATA                   @YM00636
WKAREA   DC    XL3'00'            CHECK FOR VALID CCW ADDR     @YA00406
         DS    0F                                                Y02967
CON4096  DC    X'00001000'        PAGE SIZE                      Y02967
MASK1    DC    X'00FFFFFF'        MASK FOR HIGH ORDER BYTE       Y02967
CLEARHI  DC    XL4'0F'            CLEAR FOUR BITS OF BYTE        Y02967
BITS56   DC    XL4'03'            CLEAR BITS 5 + 6               Y02967
RFTEST   DC    X'02'              TEST FOR READ FORWARD COMMAND  Y02967
RBTEST   DC    X'0C'              TEST FOR READ BACKWARD COMMAND Y02967
CLEAR    XC    0(0,VTLREG),0(VTLREG)    CLEAR INPUT AREA         Y02967
         IECDIOSB                                                Y02967
         IECDERWA
*****************************************************************Y02967
*                                                                Y02967
*   THIS DSECT DESCRIBES THE OPTICAL CHARACTER READER DEVICE     Y02967
*   DEPENDENT SECTION OF THE ERP WORKAREA                        Y02967
*                                                                Y02967
*****************************************************************Y02967
         ORG   EWAIERP                                           Y02967
EWCSNS   DS    XL8                EIGHT  BYTES OF SENSE DATA     Y02967
EWCCNTRS DS    XL4                FOUR BYTES OF ERROR COUNTERS   Y02967
         END   IGE0011D
