* /* START OF SPECIFICATIONS ****
*01* PROCESSOR = ASSEMXF-370R
**** END OF SPECIFICATIONS ***/
         TITLE 'IGG019VF - READ ROUTINE-(OCR)'
IGG019VF CSECT
***********************************************************    @YM00638
*MODULE-NAME                                                   @YM00638
* IGG019VF                                                     @YM00638
*                                                              @YM00638
*DESCRIPTIVE-NAME                                              @YM00638
* OCR READ ROUTINE                                             @YM00638
*                                                              @YM00638
*COPYRIGHT                                                     @YM00638
* NONE                                                         @YM00638
*                                                              @YM00638
*CHANGE-ACTIVITY                                               @YM00638
*
* RELEASE 20 ADDITIONS/CHANGES/DELETIONS
* RELEASE 21 ADDITIONS/CHANGES/DELETIONS
* RELEASE 22 ADDITIONS/CHANGES/DELETIONS
* RELEASE 23 ADDITIONS/CHANGES/DELETIONS
* RELEASE 24 ADDITIONS/CHANGES/DELETIONS
* RELEASE 25 ADDITIONS/CHANGES/DELETIONS
* RELEASE 26 ADDITIONS/CHANGES/DELETIONS
* RELEASE 27 ADDITIONS/CHANGES/DELETIONS
*
*
* STATUS - CHANGE LEVEL 00  RELEASE 20
*
*
*FUNCTION-READ ROUTINE
*                  1) SET CORRECT CCW,IOB,ECB ADDRESSES
*                  2) LINK TO EOB TO SCHEDULE INPUT EVENT
*
*ENTRY POINT
*        LABEL  OCR010
*
*ENTRY INSTRUCTION SEQUENCE
*              LA   1,DECB
*              LA   14,DCB
*              L    15,48(14)
*              BALR 14,15
*
* INPUT FROM CALLING ROUTINE
*        REG   0    N/A
*        REG   1    DECB ADDRESS
*        REG   2-12 N/A
*        REG   13   SAVE AREA
*        REG   14   RETURN ADDRESS
*        REG   15   BASE ADDRESS
*
*OUTPUT TO CALLING ROUTINE
*        REG   0-1  N/A
*        REG   2-12 SAME AS WHEN ENTERED
*        REG   13   SAVE AREA
*        REG   14   RETURN ADDRESS
*        REG   15   ZERO
*
*EXTERNAL ROUTINES
*        IGG019CC   EXTERNAL ROUTINE WHICH SCHEDULES INPUT REQUESTS
*
*EXITS -NORMAL
*        OCR020     BRANCH AND LINK TO END OF BLOCK ROUTINE
*        OCR040     BRANCH TO CALLING ROUTINE FROM READ
*
*TABLES/WORKAREA
*
*        USER  SAVE/WORK AREA
*        DCB
*        IOB
*        DECB
*         +0   ECB ADDRESS
*         +4   N/A
*         +8   DCB ADDRESS
*         +12  CCW ADDRESS
*         +16  IOB ADDRESS
*
*ATTRIBUTES--REENTRANT,REUSABLE, PROBLEM PROGRAM MODE
*
*
*
***********************************************************************
*
*        ALL REGISTER EQUATES HAVE AN 'R' AT THE END OF THE SYMBOL
*
***********************************************************************
PARA0R   EQU   0                   PARAMETER REGISTER 0
DECBR    EQU   1                   DECB  ADDRESS
DCBR     EQU   2                   DCB ADDRESS
IOBADR   EQU   3                   IOB ADDRESS
WORK1R   EQU   7                   WORK REGISTER
BASER    EQU   8                   BASE REGISTER
WORK2R   EQU   14                  WORK REGISTER
SAVR     EQU   13                  USER SAVE AREA
RETR     EQU   14                  RETURN ADDRESS
ENTRYR   EQU   15                  ENTRY ADDRESS
***********************************************************************
EOFBIT   EQU   X'40'               EOF BIT SWITCH IN DCBORBYT
ERRFLGS  EQU   X'3F'               ERROR FLAGS IN DCBIFLGS-X'C0'
START    EQU   24                  CCW ADDRESS IN IOB
CCWADR   EQU   12                  CCW ADDRESS IN DECB
RDCNT    EQU   28                  OFFSET TO READ COUNT IN USER AREA
ADDRESS  EQU   4                   LENGTH OF AN ADDRESS IN DECB
USERREG  EQU   20                  SAVE AREA OFFSET FOR USER REGS
DCBOFF   EQU   8                   DCB OFFSET IN DECB
IOSIOB   EQU   8                   OFFSET FROM ACCESS METHOD IOB
IOBOFF   EQU   16                  IOB OFFSET IN DECB
ECBOFF   EQU   12                  ECB ADDRESS OFFSET IN IOB
ONE      EQU   1                   USED AS A CONSTANT OF ONE
 EJECT
         USING IGG019VF,BASER      USE REG 8 AS A BASE REGISTER
         USING IHADCB,DCBR         USE REG 2 AS A  DCB BASE
OCR010   STM   RETR,BASER,USERREG(SAVR) SAVE USER REGISTERS
         LR    BASER,ENTRYR        ESTABLISH BASE REGISTER
*
         L     DCBR,DCBOFF(DECBR)  GET DCB ADDRESS
*
         TM    DCBORBYT,EOFBIT     TEST IF END OF FILE BIT IS ON
         BO    OCR034              IF ON, BRANCH TO RETURN TO USER
*
         L     IOBADR,DCBIOBA      GET IOB ADDRESS
*
         LA    PARA0R,IOSIOB(IOBADR) GET IOS IOB POINTER
         ST    PARA0R,IOBOFF(DECBR) STORE IOB ADDRESS OF DECB
         ST    DECBR,ECBOFF(IOBADR) STORE ECB ADDRESS TO DECB
*
         NI    DCBIFLGS,ERRFLGS    INSURE ERROR FLGS ARE OFF
         MVC   START(ADDRESS,IOBADR),CCWADR(DECBR) MOVE C P TO IOB
         L     ENTRYR,DCBEOBR      LOAD ADDRESS OF EOB ROUTINE
*
OCR020   BALR  RETR,ENTRYR         BRANCH AND LINK TO EOB ROUTINE
*
         L     WORK1R,DCBERRCN     LOAD ADDRESS OF ERROR COUNT AREA
         LTR   WORK1R,WORK1R       ARE COUNTERS SPECIFIED
         BZ    OCR034              NO, BRANCH
*
         L     WORK2R,RDCNT(WORK1R) LOAD READ COUNT
         LA    WORK2R,ONE(WORK2R)  INCREMENT BY 1
         ST    WORK2R,RDCNT(WORK1R)  STORE READ COUNT
*
OCR034   LM    RETR,BASER,USERREG(SAVR) RESTORE USER REGISTERS
         SR    ENTRYR,ENTRYR       CLEAR RETURN CODE REGISTER
*
OCR040   BR    RETR                RETURN TO USER
*
         CNOP  0,8                 BAD TO DOUBLE WORD BOUNDARY
         DCBD  DSORG=PS,DEVD=OR                                @YM00635
         END
