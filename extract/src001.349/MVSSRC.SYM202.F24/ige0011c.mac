* /* START OF SPECIFICATIONS ****
*01*  PROCESSOR=ASSEMXF-370R
**** END OF SPECIFICATIONS ***/
IGE0011C CSECT                    1287 OCR ERP ENTRY POINT
         TITLE 'IGE0011C - 1287 OPTICAL CHARACTER READER ERP'
***********************************************************************
*MODULE-NAME                                                   @YM00638
* IGE0011C                                                     @YM00638
*                                                              @YM00638
*DESCRIPTIVE-NAME                                              @YM00638
* OCR ERP MODULE FOR 1287 DEVICE                               @YM00638
*                                                              @YM00638
*COPYRIGHT                                                     @YM00638
* NONE                                                         @YM00638
*                                                              @YM00638
*CHANGE-ACTIVITY                                               @YM00638
* RELEASE 20 ADDITIONS/CHANGES/DELETIONS
*C510000,503000,506000,508000                                    A33926
*D357000                                                         A33936
*C480000,537000                                                  A33936
*
* RELEASE 21 ADDITIONS/CHANGES/DELETIONS
*A171500,173500,478100-478600,530100-530600                       21797
*C377000-380000                                                  A39680
*                                                                     *
* RELEASE VS2/1.6 ADDITIONS/CHANGES/DELETIONS                         *
*A397000                                                              *
* RELEASE VS2/ 2.0  ADDITIONS/CHANGES/DELETIONS                  Y02967
*A224000,                                                        Y02967
*A178000,260000,408000                                           YM2510
*A367000,538000                                                  YM2518
*C046000,069000,073000,079000,096000-097000,148000,149000,160000 Y02967
*C258000-259000,263000-264000,268000,270000-274000,275000,278000 Y02967
*C285000-286000,288000-290000,292000-294000,307000-308000,311000 Y02967
*C329000,347000,349000,351000,353000,360000-362000,366000,368000 Y02967
*C370000,374000,401000-402000,407000-409000,413000,428000-429000 Y02967
*C431000,433000,435000-437000,439000-440000,446000-455000,       Y02967
*C457000-465000,469000,471000-472000,476000,478000-478100,478300 Y02967
*C479000,487000,489000-491000,495000,497000-498000,502000,505000 Y02967
*C507000,511000,513000,516000,518000-519000,525000,528000        Y02967
*C530000-530100,530300,531000,538000,                            Y02967
*C431000,433000                                                  YM2510
*C249000,428400,446800,448400,507000                             YM2511
*C397000                                                         YM2518
*D070000-071000,093000-095000,098000-103000,116000-122000,       Y02967
*D131000-137000,205000-208000,296000-306000,
*D213000,413500                                                  YM2510
*                                                                     *
*TITLE 'IGE0011C' - 1287 ERROR RECOVERY PROCEDURES                    *
*                                                                     *
*STATUS - RELEASE VS2/ 2.0  CHANGE LEVEL 000                     Y02967
*                                                                     *
*FUNCTION/OPERATION   IGE0011C IS SCHEDULED FOR EXECUTION WHENEVER THE*
*   I/O SUPERVISOR DETECTS AN ERROR ASSOCIATED WITH THE 1287 OPTICAL  *
*   CHARACTER READER THAT REQUIRES ERROR RECOVERY PROCEDURES.         *
*                                                                     *
*                                                                     *
*   THE ERROR ROUTINE USES THE STANDARD ERROR INTERPRETER ROUTINE FOR *
*   TESTING OF SENSE BITS AND CSW STATUS BITS. THE FOLLOWING ACTIONS  *
*   ARE TAKEN DEPENDING UPON THE NATURE OF THE ERROR                  *
*                                                                     *
*   1)  NORMAL CONDITION OR SUCCESSFUL RETRY OF RECOVERABLE ERROR     *
*       CONDITION                                                     *
*                                                                     *
*       IOSB EXCEPTION, ERROR FLAGS ARE CLEARED; CONTROL IS    @YM00636
*       TRANSFERRED TO THE STATISTICS UPDATE ROUTINE.                 *
*   2)  RECOVERABLE ERROR                                             *
*                                                                     *
*       IOSB ERROR FLAG IS SET.                                @YM00636
*       IOSB EXCEPTION FLAG IS CLEARED.                        @YM00636
*       ERROR COUNTS ARE UPDATED.                                     *
*       CONTROL IS PASSED TO THE I/O SUPERVISOR VIA SVC 15 AND SVC 3  *
*       TO RETRY THE CHANNEL PROGRAM.                                 *
*                                                                     *
*   3)  PERMANENT ERROR                                               *
*       IOSB ERROR AND EXCEPTION FLAGS ARE SET.                @YM00636
*       IOSB MESSAGE AND LOGOUT FLAGS MAY BE SET.              @YM00636
*       CONTROL IS RETURNED TO THE I/O SUPERVISOR EITHER DIRECTLY     *
*       THROUGH ISSUANCE OF AN SVC 15 AND AN SVC 3 OR INDIRECTLY BY   *
*       FIRST PASSING CONTROL TO THE WTO ROUTINE WHICH IN TURN PASSES *
*       CONTROL TO THE STATISTICS UPDATE ROUTINE AND THE I/O OUTBOARD *
*       RECORDING ROUTINE, IF REQUIRED, AND EVENTUALLY TO THE I/O     *
*       SUPERVISOR.                                                   *
*                                                                     *
*ENTRY POINTS                                                         *
*       IGE0011C - ERROR RECOVERY PROCEDORE ENTRY POINT               *
*                                                                     *
*       DATA PASSED TO THE ROUTINE CONSISTS OF STANDARD SYSTEM CONTROL*
*       BLOCKS.                                                       *
*                                                                     *
*INPUT  IOSB POINTER IN REGISTER 1                               Y02967
*                                                                     *
*OUTPUT I/O ERROR AND INTERVENTION REQUIRED MESSAGES IN SOME INSTANCES*
*                                                                     *
*EXTERNAL ROUTINES  ERROR INTERPRETER ROUTINE                         *
*                                                                     *
*EXITS-NORMAL  XCTL TO STATISTICS UPDATE ROUTINE FOLLOWING SUCCESSFUL *
*       RECOVERY OF A RETRYABLE ERROR OR UPON DETECTION OF A NORMAL   *
*       CONDITION.                                                    *
*                                                                     *
*     -ERROR                                                          *
*                                                                     *
*   1)  SVC 15 AND SVC 3 EXIT TO I/O SUPERVISOR FOR RE-EXECUTION OF   *
*       THE CHANNEL PROGRAM UPON DETECTION OF A RETRYABLE ERROR.      *
*                                                                     *
*   2)  XCTL TO WTO ROUTINE FOR ISSUANCE OF A PERMANENT I/O ERROR     *
*       MESSAGE AND UPDATING OF STATISTICS AND SYS1.LOGREC IN THE CASE*
*       OF SOME PERMANENT ERRORS.                                     *
*                                                                     *
*   3)  SVC 15 AND SVC 3 EXIT TO I/O SUPERVISOR IN THE CASE OF SOME   *
*       PERMANENT I/O ERRORS.                                         *
*                                                                     *
*TABLES/WORK AREAS                                                    *
*   1)  INPUT/OUTPUT SYSTEM BLOCK (IOSB)                         Y02967
*                                                                     *
*   3)  DYNAMIC WORK AREA                                        Y02967
*                                                                     *
*   4)  STATISTICS UPDATE TABLE                                       *
*                                                                     *
*ATTRIBUTES                                                           *
*                                                                     *
*       RE-ENTRANT,SUPERVISOR MODE                               Y02967
*                                                                     *
*NOTES                                                                *
*                                                                     *
*       N/A                                                           *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT                                                 @YA00406
***********************************************************************
*                                                                     *
*        STATUS INDICATORS                                            *
*                                                                     *
***********************************************************************
CSWUCK   EQU   X'02'              UNIT CHECK
***********************************************************************
*                                                                     *
*        REGISTER DEFINITION                                          *
*                                                                     *
***********************************************************************
IOSBREG  EQU   1                  IOSB REG                     @YM00636
RVREG    EQU   1                  WORK REGISTER                  Y02967
CCWREG   EQU   2                  CCW ADDRESS REGISTER
WORKR3   EQU   3                  WORK REGISTER                @YM00636
ERREG    EQU   4                  WORK REGISTER
WORKR4   EQU   4                  WORK REGISTER                @YM00636
WORKR5   EQU   5                  WORK REGISTER                @YM00636
WORKR6   EQU   6                  WORK REGISTER                @YM00636
COMREG   EQU   8                  COMMUNICATION TABLE REG
WORKR9   EQU   9                  WORK REGISTER                @YM00636
SVREG    EQU   9                  WORK REGISTER
ERPREG   EQU   10                 DYNAMIC WORKAREA REG           Y02967
VTLREG   EQU   11                 ADDR OF VIRTUAL I/O AREA       Y02967
LINKR    EQU   12                 WORK REGISTER
ERRE1    EQU   13                 WORK REGISTER
ERRETR   EQU   14                 RETURN REG
BASREG   EQU   15                 BASE REGISTER
***********************************************************************
*                                                                     *
*        RETRY COUNTS                                                 *
*                                                                     *
***********************************************************************
ILCOUNT  EQU   5                  INCORRECT LENGTH RETRY COUNT @YM00636
ILCOUNT2 EQU   3                  DOCUMENT INCORRECT LENGTH COUNT 21797
CCCNT    EQU   5                  TEST FOR CHAINING CHECK RETRISE
EQCNT    EQU   10                 TEST FOR EQUIPMENT CHECK RETRIES
EQCNT2   EQU   3                  DOCUMENT EQUIPMENT CHECK COUNT  21797
DCCOUNT1 EQU   3                  DOCUMENT DATA CHECK RETRY COUNT
DCCOUNT2 EQU   5                  TAPE DATA CHECK COUNT        @YM00636
INCCOUNT EQU   16                 INCREMENT FOR OVERRUN RETRY COUNT
ORCOUNT  EQU   X'50'              OVERRUN RETRY COUNT (FIVE)
BOCOUNT  EQU   X'08'              TEST FOR BUS OUT CHECK RETRIED
CDCCNT   EQU   X'08'              TEST FOR CHANNEL DATA CHK    @YM00636
         EJECT                                                 @YA00406
***********************************************************************
*                                                                     *
*        MISCELLANEOUS DEFINITIONS                                    *
*                                                                     *
***********************************************************************
VEXCTL   EQU   44                 ADDR OF XCTL ROUTINE
STAUPRTN EQU   254                STATISTICS UPDATE ROUTINE
BYTECNT  EQU   6                  DISPLACMENT TO BYTE COUNT IN CCW
CVTADDR  EQU   16                 POINTER TO CVT ADDRESS
NODOC    EQU   X'20'              TEST FOR NO DOCUMENT FOUND
LATESS   EQU   X'40'              TEST FOR LATE STACKER SELECT
KC       EQU   X'01'              TEST FOR KEYBOARD CORRECTION
SNSLEN   EQU   2                  LENGTH OF SENSE BYTES TO PUT IN STATA
CHINTCHK EQU   X'06'              TEST FOR CHANNEL OR INTERFACE CHECK
CLRCNT   EQU   X'03'              BYTE COUNT FOR CLEARING COUNTERS
OLC      EQU   X'40'              TEST FOR ON-LINE CORRECTION
ZEROCNT  EQU   X'0F'              TEST FOR FIRST RETRY, INC LENGTH
ERPIBLEN EQU   8                  ERPIB LENGTH
DC       EQU   0                  DISPLACEMENT TO DATA CHECK COUNTS
ILC      EQU   1                  DISPLACEMENT TO INCORRECT LENGTH CNTS
EC       EQU   2                  DISPLACEMENT TO EQUIPMENT CHECK CNTS
RETSVC   EQU   3                  RETURN TO IOS SVC
ERRORSVC EQU   15                 ERROR EXCP
CLEARCNT EQU   X'02'              COUNT OF ERROR COUNTER BYTES TO CLEAR
INVSNS1  EQU   X'01'              TEST FOR INVALID SENSE BIT
INVSNS   EQU   X'17'              TEST FOR INVALID SENSE BITS
TM       EQU   X'80'              TEST FOR TAPE MODE
VFCT68   EQU   68                 INDEX TO INTERPRETER ROUTINE
WTORTN   EQU   253                LOAD NAME TO WTO RTN.
VFCF120  EQU   X'0120'            DISPLACEMENT TO TRANSLATE RTN  Y02967
CCWFLAG  EQU   4                  DISPLACEMENT TO CCW FLAG BYTE  Y02967
IDA      EQU   X'04'              TEST FOR INDIRECT ADDRESS      Y02967
IOB      EQU   X'05'              DISPLACEMENT TO IOB            Y02967
IOSERRCT EQU   X'1C'              DISP TO IOSB ERROR CNT       @YM00636
ADDRLN   EQU   3                  LENGTH OF 3 USED FOR COMPARE @YA00406
         EJECT                                                 @YM00636
         USING *,BASREG
         USING EWA,ERPREG         BASE FOR WORK AREA             Y02967
         USING IOSB,IOSBREG       BASE REG FOR IOSB            @YM00636
         L     ERPREG,IOSERP      GET WORK AREA ADDRESS          Y02967
         L     COMREG,CVTADDR     GET CVT ADDRESS
         OI    IOSFLA,IOSEX       SET EXCEPTION CONDITION ON   @YM00636
         TM    IOSFLA,IOSERR      ERP IN CONTROL               @YM00636
         BO    ERR000             YES CONTINUE
* CLEAR IOB ERROR COUNTERS
         XC    EWACNTR1(CLRCNT),EWACNTR1 CLEAR COUNT             Y02967
ERR000   CLI   IOSCOD,IOSFINTC    IS IT AN INTERCEPT CONDITION @YM00636
         BE    ERR0012            YES SEE IF CHANNEL ERROR
         CLI   IOSCOD,IOSERRC     CHECK FOR X'41' IN ECB       @YA00406
         BNE   ERR001A            NO,CONTINUE                  @YA00406
         CLC   IOSCSWCA(ADDRLN),WKAREA IS CCW ADDRESS VALID?   @YA00406
         BE    ERR007             NO, PERMANENT ERROR          @YA00406
ERR001A  TM    IOSFLA,IOSACHN     IS IT MIXED CHAINING         @YA00406
         BO    ERR0012            YES SEE IF CHANNEL ERROR
         TM    IOSCC,IOSCC1       CODE EQUAL X'10'             @YM00636
         BZ    ERR002             NO CONTINUE
         L     CCWREG,IOSVST      LOAD VIRTUAL ADDR OF CHL PGM   Y02967
         B     ERR003             CONTINUE                       Y02967
ERR0012  TM    IOSTSB,CHINTCHK    CHANNEL OR INTERFACE CHK     @YM00636
         BZ    ERR007             NO EXIT TO WTO ROUTINE
         B     ERR010             YES TO CHANNEL+INTERFACE CHK RTN
ERR002   L     CCWREG,IOSCSW-1    GET CCW ADDRESS                Y02967
         LA    CCWREG,0(CCWREG)   CLEAR HIGH ORDER BYTE
         SH    CCWREG,ERRON1      BACK UP ONE CCW
         TM    IOSFLA,IOSCCHN     IS COMMAND CHAINING USED     @YM00636
         BZ    ERR003             NO SET UP FOR WTO
         TM    IOSFLA,IOSERR      ENTERED AFTER RETRY          @YM00636
         BZ    ERR002A            NO CONTINUE
         LRA   ERREG,0(CCWREG)    LOAD REAL ADDR OF CCW          Y02967
         S     ERREG,IOSRST       SUBTRACT RESTART ADDRESS       Y02967
         BE    ERR003             IF EQUAL ERROR
         LA    WORKR6,EWCCNTRS    POINT TO TEMPORARY ERROR     @YM00636
*                                 COUNTERS                     @YM00636
         LA    WORKR5,EWACNTR1    POINT TO IOSB ERROR CNTRS    @YM00636
         BAL   LINKR,ERR018       LINK TO COUNTER UPDATE ROUTINE
ERR002A  LRA   ERREG,0(CCWREG)    LOAD REAL ADDR OF CHANNEL PGM  Y02967
         ST    ERREG,IOSRST       SAVE CCW START ADDRESS         Y02967
ERR003   TM    IOSTSA,CSWUCK      IS IT  UNIT CHECK            @YM00636
         BZ    ERR004             NO DETERMINE ERROR TYPE
         OC    EWASTUP(SNSLEN),IOSSNS OR SENSE BITS              Y02967
         NC    EWASTUP(SNSLEN),MASKOUT MASK UNWANTED BITS        Y02967
ERR004   LR    SVREG,BASREG       SAVE BASE REGISTER
         L     BASREG,VFCT68(COMREG) GET INTERPRETER ROUTINE ADDRESS
ERR0041  BALR  ERRETR,BASREG      LINK TO INTERPRETER            Y02967
         DC    X'1D'              TEST FOR CHANNEL CONTROL CHECK
         DC    AL1(ERR010L-ERR0041-2) BRANCH TO CCC ACTION
         DC    X'1E'              TEST FOR INTERFACE CONTROL CHECK
         DC    AL1(ERR010L-ERR0041-4) BRANCH TO ICC ACTION
         DC    X'1C'              TEST FOR CHANNEL DATA CHECK
         DC    AL1(ERR200L-ERR0041-6) BRANCH TO CDC ACTION
         DC    X'12'              CHECK FOR CONTROL UNIT END
         DC    AL1(ERR007L-ERR0041-8) BRANCH TO CUE ACTION
         DC    X'16'              CHECK FOR UNIT CHECK
         DC    AL1(ERR006-ERR0041-10) BRANCH TO UNIT CHECK ACTION
         DC    X'2F'              END OF LIST
         DC    AL1(ERR004B-ERR0041-12) PASS CONTROL TO ERR004B
ERR010L  B     ERR010             GO TO ERR010
ERR200L  B     ERR200             GO TO CDC ROUTINE
ERR007L  B     ERR007             EXIT TO WTO
ERR004B  LR    SVREG,BASREG       SAVE BASE REGISTER
         L     BASREG,VFCT68(COMREG) GET INTERPRETER ROUTINE ADDRESS
ERR004B1 BALR  ERRETR,BASREG      LINK TO INTERPRETER            Y02967
         DC    X'1F'              TEST FOR CHAINING CHECK
         DC    AL1(ERR011L-ERR004B1-2) BRANCH TO CC ACTION
         DC    X'1A'              TEST FOR PROGRAM CHECK
         DC    AL1(ERR007A-ERR004B1-4) BRANCH TO PC ACTION
         DC    X'1B'              TEST FOR PROTECTION CHECK
         DC    AL1(ERR007A-ERR004B1-6) BRANCH TO PC ACTION
         DC    X'19'              TEST FOR INCORRECT LENGTH
         DC    AL1(ERR017L-ERR004B1-8) BRANCH TO IL ACTION
         DC    X'17'              CHECK FOR UNIT EXCEPTION
         DC    AL1(ERR013L-ERR004B1-10) BRANCH TO UE ACTION
         DC    X'2F'              END OF LIST
         DC    AL1(ERR004B2-ERR004B1-12) PASS CONTROL TO ERR004BI
* FOLLOWING BRANCHS GO TO ROUTINES MORE THAN 128 BYTES AWAY
ERR011L  B     ERR011             GO TO CC ACTION
ERR017L  B     ERR017             GO TO IL ACTION
ERR013L  B     ERR013             GO TO UE-ACTION
ERR004B2 TM    IOSTSA,CSWUCK      IS UNIT CHECK POSTED         @YM00636
         BZ    ERR005             NO TO STATISTICS UPDATE ROUTINE
ERR004E1 TM    IOSSNS,KC          IS KEY BOARD POSTED            Y02967
         BO    ERR008             YES EXIT TO IOS
         TM    IOSSNS+1,LATESS    IS LATE STACKER SELECT POSTED  Y02967
         BO    ERR005B            YES GO CLEAN UP
ERR004D  TM    IOSSNS+1,NODOC     IS NO DOCUMENT POSTED          Y02967
         BO    ERR005B            YES CLEAN UP
ERR005   NI    IOSFLA,X'FF'-IOSEX CLEAR IOB EXCPT, ERR CORRECT @YM00636
         B     ERR005B            CONTINUE CLEAN UP
ERR005B  NI    IOSFLA,X'FF'-IOSERR CLEAR IOSERR                @YM00636
         LA    ERRE1,STAUPRTN     LOAD NAME OF STATISTICS UPDATE RTN
ERR005C  LA    WORKR5,EWCCNTRS    POINT TO TEMPORARY ERROR     @YM00636
*                                 COUNTERS                     @YM00636
         LA    WORKR6,EWACNTR1    POINT TO IOSB ERROR COUNTERS   Y02967
         BAL   LINKR,ERR018       LINK TO COUNTER UPDATE ROUTINE
         L     ERRETR,VEXCTL(COMREG) GET ADDRESS OF XCTL ROUTINE
         BR    ERRETR             EXIT VIA XCTL
ERR006   TM    IOSSNS+1,INVSNS    ANY INVALID SENSE BITS ON      Y02967
         BC    5,ERR007           YES ALL DONE-CLEAN UP
         CLC   IOSSNS(2),ZERO     UNIT CHECK NO SENSE          @YM00636
         BE    ERR007             YES, LOGOUT AND MSG          @YM00636
         CLC   IOSSNS(2),JTBITON  UNIT CHECK TAPE SENSE        @ZA00772
         BE    ERR007             YES  SET PERMANENT ERROR     @ZA00772
ERR0061  TM    IOSSNS+1,TM        IS IT TAPE MODE                Y02967
         BO    ERR006B            YES DETERMINE ERROR TYPE
ERR006A  TM    IOSSNS,INVSNS1     INVALID SENSE BITS ON          Y02967
         BO    ERR007             YES GO CLEAN UP
ERR006B  LR    SVREG,BASREG       SAVE BASE ADDRESS
         L     BASREG,VFCT68(COMREG) GET INTERPRETER ROUTINE ADDRESS
ERR006C  BALR  ERRETR,BASREG      LINK TO INTERPRETER            Y02967
         DC    X'03'              TEST FOR EQUIPMENT CHECK
         DC    AL1(ERR012L-ERR006C-2) BRANCH TO EC ROUTINE     @YM00636
         DC    X'06'              TEST FOR NONRECOVERY         @YM00634
         DC    AL1(ERR007-ERR006C-4) BRANCH TO NONREC ROUTINE  @YM00634
         DC    X'01'              TEST FOR INTERVENTION REQ'ED @YM00634
         DC    AL1(ERR006D-ERR006C-6) BRANCH TO IR ROUTINE     @YM00634
         DC    X'02'              TEST FOR BUS OUT CHECK
         DC    AL1(ERR015L-ERR006C-8) BRANCH TO BUS OUT CHECK ROUTINE
         DC    X'04'              TEST FOR DATA CHECK
         DC    AL1(ERR014L-ERR006C-10) BRANCH TO DATA CHECK ROUTINE
         DC    X'05'              TEST FOR OVERRUN
         DC    AL1(ERR016L-ERR006C-12) BRANCH TO OVERRUN ROUTINE
         DC    X'00'              TEST FOR COMMAND REJECT
         DC    AL1(ERR007A-ERR006C-14) BRANCH TO COMMAND REJECT ROUTINE
         DC    X'0C'              TEST FOR INVALID OPERATION
         DC    AL1(ERR007-ERR006C-16)  BRANCH TO INVALID OPERATION RTN
         DC    X'2F'              END OF LIST
         DC    AL1(ERR004BL-ERR006C-18) CONTINUE CHECKING FOR ERRORS
ERR012L  B     ERR012             GO TO EC ACTION
ERR015L  B     ERR015             GO TO BUS OUT ACTION
ERR014L  B     ERR014             GO TO DATA CHECK ACTION
ERR016L  B     ERR016             GO TO OVERRUN ACTION
ERR004BL B     ERR004B2           CONTINUE CHECKING            @YM00636
ERR006D  OI    IOSFLA,IOSERR      SET IOSERR ON                @YM00636
         B     ERR007B            GO WRITE TO OPERATOR
* EXIT TO WTO ROUTINE
ERR007   OI    IOSFLB,IOSLOG      SET LOGOUT FALG                Y02967
ERR007A  OI    IOSFLB,IOSMSG      SET MESSAGE FLAG               Y02967
         NI    EWAFLG2,X'FF'-EWAMDR  SET FLAG BIT FOR OBR        Y02967
         NI    IOSFLA,X'FF'-IOSERR CLEAR IOSERR FLAG           @YM00636
ERR007B  LA    ERRE1,WTORTN       LOAD NAME OF WTO ROUTINE
         B     ERR005C            GET OUT VIA XCTL
ERR008   NI    IOSFLA,X'FF'-IOSERR CLEAR IOSERR FLAG           @YM00636
         LA    WORKR5,EWCCNTRS    POINT TO TEMPORARY ERROR     @YM00636
*                                 COUNTERS                     @YM00636
         LA    WORKR6,EWACNTR1    POINT TO IOSB ERROR COUNT    @YM00636
         BAL   LINKR,ERR018       LINK TO COUNTER UPDATE ROUTINE
         SVC   ERRORSVC           ISSUE ERROR EXCP
         SVC   RETSVC             RETURN TO IOS VIA SVC 3
*   UPDATE EWA ERROR COUNTERS ROUTINE                            Y02967
ERR018   SR    WORKR4,WORKR4      ZERO OUT                     @YM00636
         SR    WORKR9,WORKR9      WORK REGISTERS               @YM00636
         IC    WORKR4,DC(WORKR5)  GET DATA                     @YM00636
         IC    WORKR9,DC(WORKR6)  CHECK COUNTS                 @YM00636
         AR    WORKR4,WORKR9      ADD COUNTS                   @YM00636
         STC   WORKR4,DC(WORKR6)  UPDATE DATA CHECK COUNT      @YM00636
         IC    WORKR4,ILC(WORKR5) GET INCORRECT                @YM00636
         IC    WORKR9,ILC(WORKR6) LENGTH COUNTS                @YM00636
         AR    WORKR4,WORKR9      ADD COUNTS                   @YM00636
         STC   WORKR4,ILC(WORKR6) UPDATE INCORRECT LENGTH COUNT
         IC    WORKR4,EC(WORKR5)  GET EQUIPMENT CHECK          @YM00636
         IC    WORKR9,EC(WORKR6)  CHECK COUNTS                 @YM00636
         AR    WORKR4,WORKR9      ADD COUNTS                   @YM00636
         STC   WORKR4,EC(WORKR6)  UPDATE EQUIPMENT CHECK COUNT @YM00636
CLEAR1   XC    0(CLEARCNT,WORKR5),0(WORKR5) CLEAR ERROR CNTRS    Y02967
         TM    IOSDVRID,IOSXCPID  DRIVER ID EXCP                 Y02967
         BNO   ERR018A            NO,IOB DOES NOT EXIST          YM2511
         L     WORKR4,IOSUSE      LOAD ADDRESS OF RQE            Y02967
         L     WORKR4,IOB-1(WORKR4) LOAD ADDRESS OF IOB          Y02967
         MVC   IOSERRCT(4,WORKR4),0(WORKR6) MOVE ERROR COUNTERS  Y02967
ERR018A  BR    LINKR              RETURN                         Y02967
* CHANNEL DATA CHECK ROUTINE                                   @YM00636
ERR200   TM    EWACNTR3,CDCCNT    CHANNEL DATA CHECK           @YM00636
         BO    ERR007             YES EXIT TO WTO ROUTINE
         OI    EWACNTR3,CDCCNT    SET TO INDICATE RETRIED      @YM00636
* RETRY ROUTINE
ERR009   SR    WORKR3,WORKR3      CLEAR REGISTER               @YM00636
               XC    IOSSNS(2),IOSSNS                          @ZA00415
         IC    WORKR3,0(CCWREG)   GET OP CODE                  @YM00636
         N     WORKR3,CLEARHI     CLEAR TOP FOUR BITS          @YM00636
         CLM   WORKR3,1,RBTEST    IS COMMAND READ BACKWARD     @YM00636
         BE    ERR009B            YES CLEAR INPUT AREA & RETRY
         N     WORKR3,BITS56      CLEAR BITS 5+6               @YM00636
         CLM   WORKR3,1,RFTEST    IS COMMAND READ FORWORD      @YM00636
         BE    ERR009B            YES CLEAR INPUT AREA & RETRY
ERR009A  OI    IOSFLA,IOSERR      SET IOSERR ON                @YM00636
         SVC   ERRORSVC           RETRY CCW
         SVC   RETSVC             RETURN TO IOS VIA SVC 3
* CLEAR INPUT AREA
*   THE REAL DATA ADDRESS IN THE CCW IS TRANSLATED TO A          Y02967
*   VIRTUAL ADDRESS. IF INDIRECT ADDRESSING IS SPECIFIED IN      Y02967
*   THE CCW,EACH ADDRESS IN THE IDA LIST IS TRANSLATED.          Y02967
ERR009B  LR    WORKR4,IOSBREG     SAVE IOSB REGISTER           @YM00636
         L     RVREG,0(CCWREG)    GET REAL INPUT AREA ADDR       Y02967
         N     RVREG,MASK1        CLEAR OUT COMMAND CODE         Y02967
         LA    RVREG,1(RVREG)     INCREMENT ADDR BY ONE          Y02967
         LR    SVREG,BASREG       SAVE BASE REGISTER             Y02967
         L     BASREG,VFCF120(COMREG)  GET ADDR OF TRANSLATE RTN YM2511
         BALR  ERRETR,BASREG      LINK TO TRANSLATE ROUTINE      Y02967
         LR    BASREG,SVREG       RESTORE BASE REGISTER          Y02967
         BCTR  RVREG,0            DECREMENT ADDR BY ONE          Y02967
         LR    ERRE1,RVREG        SAVE VITUAL ADDR OF IO LIST    Y02967
         LR    VTLREG,RVREG       SAVE VITUAL ADDR OF IO AREA    Y02967
         LR    IOSBREG,WORKR4     RESTORE IOSB REGISTER        @YM00636
         LH    WORKR6,BYTECNT(CCWREG) GET BYTE COUNT             Y02967
         TM    CCWFLAG(CCWREG),IDA INDIRECT ADDRESSING SPECIFED  Y02967
         BZ    ERR009H            NO, IO ADDRESS CORRECT         Y02967
*                                                                Y02967
*   IDA IS SPECIFIED. TRANSLATE EACH ADDRESS IN LIST             Y02967
*                                                                Y02967
ERR009C  L     RVREG,0(ERRE1)     GET ADDRESS IN IDA LIST        Y02967
         LA    RVREG,1(RVREG)     INCREMENT ADDR BY ONE          Y02967
         LR    SVREG,BASREG       SAVE BASE REGISTER             Y02967
         L     BASREG,VFCF120(COMREG)  GET ADDR OF TRANSLATE RTN YM2511
         BALR  ERRETR,BASREG      LINK TO TRANSLATE ROUTINE      Y02967
         LR    BASREG,SVREG       RESTORE BASE REGISTER          Y02967
         BCTR  RVREG,0            DECREMENT ADDR BY ONE          Y02967
         LR    VTLREG,RVREG       SAVE ADDRESS OF IO AREA        Y02967
ERR009D  LR    IOSBREG,WORKR4     RESTORE IOSB REGISTER        @YM00636
*   THE VIRTUAL ADDRESS IS DIVIDED BY THE PAGE SIZE(4096)  THE   Y02967
*   REMAINDER IS USED TO DETERMINE HOW MANY BYTES CAN BE         Y02967
*   CLEARED IN THE NEXT EXECUTE INSTRUCTION.                     Y02967
         LR    WORKR4,VTLREG      LOAD VITUAL ADDR OF IO ADRA    Y02967
         SRDA  WORKR4,32(0)       SHIFT INTO ODD REGISTER        Y02967
         D     WORKR4,CON4096     DIVIDE ADDR BY PAGE SIZE       Y02967
         LA    WORKR4,1(WORKR4)   ADJUST REMAINDER               Y02967
         CLM   WORKR3,1,RFTEST    IS COMMAND READ FORWARD      @YM00636
         BE    ERR009F            YES, ADDRESS CORRECT           Y02967
         CR    WORKR4,WORKR6      REMAINDER GE BYTE COUNT        Y02967
         BNL   ERR009E            YES, CLEAR ENTIRE IO AREA      Y02967
         SR    WORKR6,WORKR4      SUBTRACT NO. OF BYTES IN PAGE  Y02967
         BCTR  WORKR4,0           DECREMENT COUNT BY ONE         Y02967
         SR    VTLREG,WORKR4      ADJUST IO ADDRESS              Y02967
         B     ERR009G            GO CLEAR INPUT AREA            Y02967
ERR009E  BCTR  WORKR6,0           DECREMENT COUNT BY ONE         Y00620
         SR    VTLREG,WORKR6      ADJUST IO ADDRESS              Y00620
         B     ERR009I            GO CLEAR INPUT AREA            Y02967
ERR009F  S     WORKR4,CON4096     COMPUTE NO. OF                 Y02967
         LPR   WORKR4,WORKR4      BYTES LEFT IN PAGE             Y02967
         LA    WORKR4,1(WORKR4)   ADJUST NO. OF BYTES            Y02967
         CR    WORKR4,WORKR6      REMAINDER GE BYTE COUNT        Y02967
         BNL   ERR009H            YES, CLEAR ENTIRE IO AREA      Y02967
         SR    WORKR6,WORKR4      SUBTRACT NO. OF BYTES IN PAGE  Y02967
         BCTR  WORKR4,0           DECREMENT COUNT BY ONE         Y02967
ERR009G  EX    WORKR4,CLEAR       CLEAR INPUT AREA               Y02967
         LA    ERRE1,4(ERRE1)     INCREMENT TO NEXT IO ADDRESS   Y02967
         LR    WORKR4,IOSBREG     SAVE IOSB REGISTER           @YM00636
         B     ERR009C            GO TRANSLATE NEXT ADDRESS      Y02967
ERR009H  BCTR  WORKR6,0           DECREMENT COUNT BY ONE         Y02967
         CLM   WORKR3,1,RFTEST    IS COMMAND READ FORWARD      @YM00636
         BE    ERR009I            YES,IO ADDRESS OK              Y02967
         SR    VTLREG,WORKR6      ADJUST IO ADDRESS              Y02967
ERR009I  EX    WORKR6,CLEAR       CLEAR OUT DATA AREA            Y02967
         B     ERR009A            RETRY BAD CCW                  Y02967
* CHANNEL CHECK ROUTINE
ERR010   LA    WORKR5,EWAERPIB    LOAD ADDR OF ERPIB             Y02967
ERR010B  XC    0(ERPIBLEN,WORKR5),0(WORKR5) CLEAR ERPIB        @YM00636
         B     ERR007             GO TO LOGOUT ROUTINE
* CHAINING CHECK ROUTINE
ERR011   IC    WORKR5,EWACNTR4    GET CHAINING CK COUNT        @YM00636
         LA    WORKR5,1(WORKR5)   INCREMENT COUNT              @YM00636
         STC   WORKR5,EWACNTR4    UPDATE COUNT                 @YM00636
         TM    EWACNTR4,CCCNT     IS COUNT FIVE                @YM00636
         BO    ERR007             YES TO LOGOUT
         B     ERR009             NO RETRY
* EQUIPMENT CHECK
ERR012   IC    WORKR5,EWACNTR3    GET EQUIPMENT CK COUNT       @YM00636
         LA    WORKR5,1(WORKR5)   INCREMENT COUNT              @YM00636
         STC   WORKR5,EWACNTR3    UPDATE COUNTERS              @YM00636
         TM    IOSSNS+1,TM        IS IT TAPE MODE                Y02967
         BO    ERR012B            YES TEST FOR A FIVE COUNT    @YM00636
         CLI   EWACNTR3,EQCNT2    IS COUNT NOW THREE             Y02967
         BE    ERR008             YES EXIT TO IOS              @YM00636
         B     ERR009             NO RETRY                     @YM00636
ERR012B  EQU   *                                                  21797
         CLI   EWACNTR3,EQCNT     IS COUNT NOW TEN               Y02967
         BE    ERR008             YES TO IOS                     A33936
         B     ERR009             NO RETRY
* UNIT EXCEPTION
ERR013   TM    IOSFLA,IOSERR      IOSERR ON                    @YM00636*
                                  (ENTERED AFTER RETRY)        @YM00636
         BZ    ERR013B            NO CONTINUE
ERR013A  NI    IOSFLA,X'FF'-IOSEX SET IOBEX OFF                @YM00636
         B     ERR005B            TO STATISTICS UPDATE ROUTINE
ERR013B  TM    IOSCC,IOSCC1       SIO CONDITION 01             @YM00636
         BO    ERR005B            YES TO STATISTICS UPDATE ROUTINE
         SR    WORKR3,WORKR3      CLEAR WORK REGISTER          @YM00636
         IC    WORKR3,0(CCWREG)   GET COMMAND CODE             @YM00636
         N     WORKR3,BITS56      CLEAR BITS 5+6               @YM00636
         CLM   WORKR3,1,RFTEST    IS COMMAND READ FORWARD      @YM00636
         BE    ERR009B            YES GO CLEAR INPUT AREA AND RETRY
         B     ERR005B            NO TO STATISTICS UPDATE ROUTINE
* DATA CHECK ROUTINE
ERR014   IC    WORKR5,EWACNTR1    GET DATA CHECK ERROR COUNT     Y02967
         LA    WORKR5,1(WORKR5)   INCREMENT COUNT              @YM00636
         STC   WORKR5,EWACNTR1    UPDATE DATA CHECK ERROR COUNT  Y02967
         TM    IOSSNS+1,TM        IS IT TAPE MODE                Y02967
         BO    ERR014B            YES PROCESS TAPE
ERR014A  TM    0(CCWREG),OLC      IS IT ON-LINE CORRECTION
         BO    ERR008             YES TO IOS                     A33926
         TM    EWACNTR1,DCCOUNT1  IS COUNT NOW THREE             Y02967
         BO    ERR008             YES TO IOS                     A33926
         B     ERR009             NO RETRY
ERR014B  TM    IOSSNS,KC          IS IT KEYBOARD CORRECTION      Y02967
         BO    ERR008             YES TO IOS                     A33926
         TM    EWACNTR1,DCCOUNT2  DATA CHECK COUNT EQUAL 5     @YM00636
         BO    ERR008             YES TO IOS                     A33926
         B     ERR009             NO RETRY
* BUSOUT CHECK
ERR015   TM    EWACNTR4,BOCOUNT   BUSOUT CHECK COUNT EQUAL ONE   Y02967
         BO    ERR007             YES TO WTO ROUTINE
         OI    EWACNTR4,BOCOUNT   SET COUNT TO ONE               Y02967
         B     ERR009             RETRY
* OVERRUN
ERR016   IC    WORKR5,EWACNTR4    GET OVERRUN ERROR COUNT      @YM00636
         LA    WORKR5,INCCOUNT(WORKR5) INCREMENT COUNT         @YM00636
         STC   WORKR5,EWACNTR4    UPDATE ERROR COUNT           @YM00636
         TM    EWACNTR4,ORCOUNT   DOES COUNT EQUAL FIVE          Y02967
         BO    ERR007             YES EXIT TO WTO ROUTINE
         B     ERR009             NO RETRY
* INCORRECT LENGTH
ERR017   TM    IOSFLA,IOSERR ENTERED AFTER RETRY               @YM00636
         BZ    ERR017A            NO
         TM    EWACNTR2,ZEROCNT   FIRST TIME HERE                Y02967
         BC    5,ERR017A          NO CONTINUE
         NI    IOSFLA,X'FF'-IOSEX CLEAR IOBEX                  @YM00636
         B     ERR008             EXIT TO IOS
ERR017A  IC    WORKR5,EWACNTR2    GET INCORRECT LENGTH COUNT     Y02967
         LA    WORKR5,1(WORKR5)   INCREMENT ERROR COUNT        @YM00636
         STC   WORKR5,EWACNTR2    UPDATE COUNTERS                Y02967
         TM    IOSSNS+1,TM        IS IT TAPE MODE                Y02967
         BO    ERR017B            YES TEST FOR A FIVE COUNT    @YM00636
         CLI   EWACNTR2,ILCOUNT2  IS COUNT NOW THREE             Y02967
         BE    ERR008             YES EXIT TO IOS              @YM00636
         B     ERR009             NO RETRY                     @YM00636
ERR017B  EQU   *                                                  21797
         CLI   EWACNTR2,ILCOUNT   IS COUNT NOW FIVE              Y02967
         BE    ERR008             YES EXIT TO IOS
         B     ERR009             NO RETRY
         DS    0H                 FORCE HALF WORD ALIGNMENT
ERRON1   DC    AL2(8)             CONSTANT OF 8
MASKOUT  DC    X'267F'            MASK FOR CLEARING            @YM00636
*                                 UNWANTED SENSE DATA          @YM00636
WKAREA   DC    XL2'00'            CHECK FOR VALID CCW ADDR     @YA00406
ZERO     DC    XL2'00'            TEST FOR UC NO SENSE         @YM00636
JTBITON  DC    XL2'0080'          TEST FOR UC TAPE SENSE       @ZA00772
         DS    0F                                                Y02967
MASK1    DC    X'00FFFFFF'        MASK FOR HIGH ORDER BYTE       Y02967
CON4096  DC    X'00001000'        PAGE SIZE                      Y02967
CLEARHI  DC    XL4'0F'            CLEAR FOUR BITS OF BYTE        Y02967
BITS56   DC    XL4'03'            CLEAR BITS 5 + 6               Y02967
RFTEST   DC    X'02'              TEST FOR READ FORWARD COMMAND  Y02967
RBTEST   DC    X'0C'              TEST FOR READ BACKWARD COMMAND Y02967
CLEAR    XC    0(0,VTLREG),0(VTLREG)    CLEAR INPUT AREA         Y02967
         IECDIOSB                                                Y02967
         IECDERWA
*****************************************************************Y02967
*                                                                Y02967
*   THIS DSECT DESCRIBES THE OPTICAL CHARACTER READER DEVICE DEPEY02967
*   SECTION OF THE ERP WORKAREA                                  Y02967
*                                                                Y02967
*****************************************************************Y02967
         ORG   EWAIERP                                           Y02967
EWCSNS   DS    XL8                EXTENDED SENSE BYTES           Y02967
EWCCNTRS DS    XL4                TEMPORARY ERROR COUNTERS       Y02967
         END   IGE0011C
