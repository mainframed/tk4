NUC      TITLE 'HASP NUCLEUS PROLOG'                           @OZ18212
***********************************************************************
*                                                                     *
* MODULE NAME = HASJES20 ( HASPNUC CSECT )                            *
*                                                                     *
* DESCRIPTIVE NAME = HASPNUC CSECT OF JES2 MAIN MODULE                *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = OS/VS2 MVS   --  SEE &VERSION (BELOW) FOR JES2 LEVEL       *
*                                                                     *
* FUNCTION = THE HASPNUC CSECT PROVIDES THE BASIC PROCESSOR CONTROL   *
*            BLOCKS AND SERVICE ROUTINES FOR THE MAIN JES2 TASK.      *
*            IT ALSO CONTAINS BASIC COMMUNICATIONS WITH DAUGHTER      *
*            TASKS.                                                   *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = EXCP ACCESS METHOD, DYNAMIC ALLOCATION,           *
*                   DYNAMIC UNALLOCATION, TIMER INTERFACE, AND        *
*                   ABNORMAL END EXIT INTERFACE.                      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS = SEE ENTRY POINT DOCUMENTATION             *
*                                                                     *
*    PATCH LABEL = $PATCHSP                                           *
*                                                                     *
* MODULE TYPE = PROCEDURE, TABLE ( CSECT TYPE )                       *
*                                                                     *
*    PROCESSOR = ASSEMBLER F                                          *
*                                                                     *
*    MODULE SIZE = SEE $DLENGTH MACRO EXPANSION(S) AT END OF ASSEMBLY *
*                                                                     *
*    ATTRIBUTES = 1ST 4K NOT REUSABLE, REMAINDER READ ONLY AND        *
*                 HASP REENTRANT                                      *
*                                                                     *
* ENTRY POINT =    HASP     - INITIAL ENTRY TO INITIALIZE THE JES2    *
*                             SYSTEM FOLLOWING BRANCH TO HASPINGO.    *
*                                                                     *
*                  $WAIT    - ENTRY TO PUT PCE IN $WAIT STATE         *
*                             AND DISPATCH ANOTHER JES2 PROCESSOR     *
*                             OR WAIT FOR AN OS POST.                 *
*                                                                     *
*                  $WAITR   - ENTRY TO PUT PCE IN $WAIT STATE ON      *
*                             A RESOURCE WAIT QUEUE AND DISPATCH      *
*                             ANOTHER JES2 PROCESSOR OR WAIT FOR      *
*                             AN OS POST.                             *
*                                                                     *
*                  $POST    - ENTRY TO PUT AN INDIVIDUAL JES2         *
*                             PROCESSOR ON THE READY QUEUE FOR        *
*                             DISPATCHING.                            *
*                                                                     *
*                  $EXCPR   - ENTRY TO EXCP ACCESS METHOD             *
*                             INTERFACE ROUTINE FOR SCHEDULLING       *
*                             I/O OPERATIONS.                         *
*                                                                     *
*                  $QADD    - ENTRY TO JES2 JOB QUEUE MANAGER QADD    *
*                             SERVICE ROUTINE TO ADD A NEW JOB TO     *
*                             THE SYSTEM.                             *
*                                                                     *
*                  $QGET    - ENTRY TO JES2 JOB QUEUE MANAGER QGET    *
*                             SERVICE ROUTINE TO GET A QUEUED JOB     *
*                             MAKING IT ACTIVE.                       *
*                                                                     *
*                  $QPUT    - ENTRY TO JES2 JOB QUEUE MANAGER QPUT    *
*                             SERVICE ROUTINE TO PUT A JOB ON A       *
*                             LOGICAL QUEUE.                          *
*                                                                     *
*                  $QREM    - ENTRY TO JES2 JOB QUEUE MANAGER QREM    *
*                             SERVICE ROUTINE TO REMOVE A JOB FROM    *
*                             THE SYSTEM.                             *
*                                                                     *
*                  $QLOC    - ENTRY TO JES2 JOB QUEUE MANAGER QLOC    *
*                             SERVICE ROUTINE TO LOCATE A JOB QUEUE   *
*                             ENTRY FOR A GIVEN JOB BY NUMBER.        *
*                                                                     *
*                  $QCKPT   - ENTRY TO JES2 JOB QUEUE MANAGER QCKPT   *
*                             SERVICE ROUTINE TO SCHEDULE A           *
*                             CHECKPOINT OF A JOB QUEUE ELEMENT.      *
*                                                                     *
*                  $QMOD    - ENTRY TO JES2 JOB QUEUE MANAGER QMOD    *
*                             SERVICE ROUTINE TO MODIFY CHAIN         *
*                             FIELDS OF JOB QUEUE ELEMENTS ON         *
*                             BEHALF OF A JOB.                        *
*                                                                     *
*                  $QSUSE   - ENTRY TO REQUEST ACCESS (FOR POTENTIAL  *
*                             CHANGE) TO ANY INFORMATION IN THE JOB   *
*                             AND JOT QUEUE CHECKPOINTED RECORDS.     *
*                                                                     *
*                  $GETUNIT - ENTRY TO JES2 UNIT SERVICES TO          *
*                             ALLOCATE A DEVICE CONTROL TABLE         *
*                             (DCT) FOR USE BY A PROCESSOR.           *
*                                                                     *
*                  $FREUNIT - ENTRY TO JES2 UNIT SERVICES TO          *
*                             FREE A DEVICE FOR USE BY ANOTHER        *
*                             PROCESSOR, PLACE THE DEVICE IN A        *
*                             HOLD STATUS, OR DRAIN THE DEVICE        *
*                             WITH POSSIBLE OS UNALLOCATION.          *
*                                                                     *
*                  $STIMER  - ENTRY TO JES2 INTERVAL TIMER MANAGER    *
*                             TO QUEUE A JES2 TIMER QUEUE ELEMENT.    *
*                                                                     *
*                  $TTIMER  - ENTRY TO JES2 INTERVAL TIMER MANAGER    *
*                             TO DETERMINE REMAINING TIME AND         *
*                             OPTIONALLY TERMINATE THE INTERVAL.      *
*                                                                     *
*                  $GETSMFB - ENTRY TO JES2 SMF MANAGEMENT TO GET     *
*                             A FREE SMF BUFFER.                      *
*                                                                     *
*                  $QUESMFB - ENTRY TO JES2 SMF MANAGEMENT TO QUEUE   *
*                             A SMF BUFFER FOR OUTPUT BY THE SMF      *
*                             INTERFACE DAUGHTER TASK.                *
*                                                                     *
*                  $PURGER  - ENTRY TO DIRECT ACCESS TRACK MANAGEMENT *
*                             TO PURGE TRACKS FREEING SPACE FOR       *
*                             USE BY OTHERS.                          *
*                                                                     *
*                  $BFRBLDR - ENTRY TO BUFFER PREFIX-BUILD ROUTINE    *
*                             TO CONSTRUCT AN IOB OR RPL AT THE       *
*                             BEGINNING OF A BUFFER.                  *
*                                                                     *
*                  $XMPOSTR - ENTRY TO ROUTINE TO PERFORM A BRANCH-   *
*                             ENTRY POST OF AN ECB.                   *
*                                                                     *
*                  $PGRLSER - ENTRY TO JES2 PAGE SERVICES TO DO A     *
*                             PAGE RELEASE VIA BRANCH ENTRY TO MVS    *
*                             PAGE SERVICES.                          *
*                                                                     *
*                  $PGFREER - ENTRY TO JES2 PAGE SERVICES TO DO A     *
*                             PAGE FREE VIA BRANCH ENTRY TO MVS       *
*                             PAGE SERVICES.                          *
*                                                                     *
*                  $PGFIXR  - ENTRY TO JES2 PAGE SERVICES TO DO A     *
*                             PAGE FIX VIA BRANCH ENTRY TO MVS        *
*                             PAGE SERVICES.                          *
*                                                                     *
*                  $TRACKR  - ENTRY TO DIRECT ACCESS MANAGEMENT       *
*                             TO ASSIGN THE NEXT RECORD ADDRESS TO    *
*                             WRITE SPOOL RECORDS.                    *
*                                                                     *
*                  $GETBUFR - ENTRY TO BUFFER POOL MANAGEMENT TO      *
*                             GET ONE OR MORE FREE BUFFERS.           *
*                                                                     *
*                  $FREEBFR - ENTRY TO BUFFER POOL MANAGEMENT TO      *
*                             FREE ONE OR MORE BUFFERS.               *
*                                                                     *
*                  $GETCELR - ENTRY TO CELL SERVICES                  *
*                             TO CLAIM A COMMON STORAGE AREA CELL.    *
*                                                                     *
*                  $FRECELR - ENTRY TO CELL SERVICES TO FREE A        *
*                             COMMON STORAGE AREA CELL.               *
*                                                                     *
*                  $GETLOKR - ENTRY TO LOCK SERVICES TO GET THE       *
*                             OS CMS LOCK.                            *
*                                                                     *
*                  $FRELOKR - ENTRY TO LOCK SERVICES TO FREE THE      *
*                             OS CMS LOCK.                            *
*                                                                     *
*                  HASPATTN - ENTRY TO UNSOLICITED DEVICE END         *
*                             ATTENTION ROUTINE.                      *
*                                                                     *
*                  $IOAPPEN - INPUT/OUTPUT APPENDAGE VECTOR TABLE     *
*                             FOR JES2 $EXCP SERVICES AND APPENDAGE   *
*                             ROUTINES.                               *
*                                                                     *
*                  $ASYNC   - ENTRY TO ASYNCHRONOUS INPUT/OUTPUT      *
*                             PROCESSOR.                              *
*                                                                     *
*                  $TIMER   - ENTRY TO JES2 TIMER PROCESSOR.          *
*                                                                     *
*                  $DYN     - ENTRY TO INTERFACE TO OS ALLOCATE       *
*                             AND UNALLOCATE UNIT RECORD DEVICES.     *
*                                                                     *
*                  $#BLD    - ENTRY TO JES2 JOT SERVICES TO BUILD     *
*                             A PAIR OF WORK AND CHARACTERISTICS      *
*                             JOES.                                   *
*                                                                     *
*                  $#ADD    - ENTRY TO JES2 JOT SERVICES TO COPY      *
*                             A PAIR OF WORK AND CHARACTERISTICS      *
*                             JOES INTO THE JOT.                      *
*                                                                     *
*                  $#GET    - ENTRY TO JES2 JOT SERVICES TO SELECT    *
*                             A WORK JOE FROM THE JOT BASED UPON      *
*                             DEVICE CHARACTERISTICS.                 *
*                                                                     *
*                  $#PUT    - ENTRY TO JES2 JOT SERVICES TO RETURN    *
*                             A WORK JOE TO THE JOT FOR LATER         *
*                             PROCESSING.                             *
*                                                                     *
*                  $#REM    - ENTRY TO JES2 JOT SERVICES TO REMOVE    *
*                             A WORK JOE FROM THE JOT FOR WHICH       *
*                             ALL OUTPUT HAS BEEN PROCESSED.          *
*                                                                     *
*                  $#CAN    - ENTRY TO JES2 JOT SERVICES TO REMOVE    *
*                             FROM THE JOT ALL NON-BUSY JOES FOR      *
*                             A SPECIFIED JOB.                        *
*                                                                     *
*                  $#JCTRDR - ENTRY TO JES2 JOT SERVICES TO OBTAIN    *
*                             THE BUFFER ADDRESS OF A RESIDENT JCT    *
*                             FOR A SPECIFIED JOB, OBTAINING THE JCT  *
*                             FROM SPOOL IF NECESSARY.                *
*                                                                     *
*                  $#JCTWTR - ENTRY TO JES2 JOT SERVICES TO WRITE     *
*                             TO SPOOL A RESIDENT JCT.                *
*                                                                     *
*                  $#JCTFRE - ENTRY TO JES2 JOT SERVICES TO RELEASE   *
*                             OWNERSHIP OF A RESIDENT JCT.            *
*                                                                     *
*                  $#WTR    - ENTRY TO JES2 SERVICE ROUTINE TO POST   *
*                             ANY EXTERNAL WRITERS WAITIJG ON JOT.    *
*                                                                     *
*                  $$WTO    - ENTRY TO JES2 ROUTINE TO ISSUE WTO      *
*                             AND WTOR MESSAGES DIRECTLY.             *
*                                                                     *
*                  $DISTERR - ENTRY TO DISASTEROUS ERROR ROUTINE      *
*                             TO ISSUE $WTO MESSAGE.                  *
*                                                                     *
*                  $ERROR   - ENTRY TO CATASTROPHIC ERROR ROUTINE     *
*                             TO ABNORMALLY TERMINATE PROCESSING.     *
*                                                                     *
*                  $HEXIT   - ENTRY TO CATASTROPHIC ERROR ROUTINE     *
*                             TO NORMALLY TERMINATE PROCESSING.       *
*                                                                     *
*                  $IOERROR - ENTRY TO I/O ERROR ROUTINE TO FORMAT    *
*                             AND DISPLAY MESSAGE.                    *
*                                                                     *
*                  $ABEND   - ENTRY TO ABNORMAL END EXIT ROUTINE.     *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = SEE ENTRY POINT DOCUMENTATION                          *
*                                                                     *
* INPUT = SEE ENTRY POINT DOCUMENTATION                               *
*                                                                     *
* OUTPUT = SEE ENTRY POINT DOCUMENTATION                              *
*                                                                     *
* EXIT-NORMAL = SEE ENTRY POINT DOCUMENTATION                         *
*                                                                     *
* EXIT-ERROR = CATASTROPHIC ERROR ROUTINE ISSUES MESSAGE AND          *
*              RESPONDS TO OPERATORS REQUEST.                         *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = SVC 1, SVC 2, SVC 0, SVC 46, SVC 47, SETLOCK,         *
*               SVC 35, SVC 3, SVC 62, $SVTRAK2, $SVGCMNS, $SVGCELL,  *
*               $SVFCELL, $WTOR, $FRECMBR, SVC 87                     *
*                                                                     *
*    DATA AREAS = SEE $HASPCB MACRO EXPANSION                         *
*                                                                     *
*    CONTROL BLOCKS = SEE $HASPCB MACRO EXPANSION                     *
*                                                                     *
* TABLES = SEE BELOW                                                  *
*                                                                     *
* MACROS = EXCP, WAIT, POST, WTO, WTOR, TTIMER, STIMER, SETLOCK,      *
*          MODESET, DETACH, DOM                                       *
* CHANGE ACTIVITY                                                     *
*                                                                     *
*     RELEASE 4.0 = OZ03342,OZ04345,OZ05783,OZ07436,OZ09024,OZ09095   *
*                                                                     *
*     RELEASE 4.1 = OZ09073,OZ11742,OZ11744,OZ11751,OZ11763,OZ12274,  *
*                   OZ12298,OZ14413                                   *
*                                                                     *
***********************************************************************
         TITLE 'HASP CONTROL BLOCK GENERATION MACRO'
*
***** $HASPCB *****           GENERATE HASP CONTROL BLOCKS
*
*
         MACRO
         $HASPCB &DOC=NO,&LIST=NO
         GBLC  &PRINT,&GEN,&DATA
         PUSH  PRINT
         PRINT &PRINT
         $ASCB LIST=&LIST          GENERATE OS ASCB DSECT      @OZ18608
         $PSA  LIST=&LIST          GENERATE OS PSA DSECT
         $CVT  LIST=&LIST          GENERATE OS CVT DSECT
         $SSOB (SO),LIST=&LIST     GENERATE OS SSOB DSECT            R4
         $SRB  LIST=&LIST          GENERATE OS SRB DSECT
         $DCB  LIST=&LIST          GENERATE OS DCB DSECT
         $DEB  LIST=&LIST          GENERATE OS DEB DSECT
         $IOSB LIST=&LIST          GENERATE OS IOSB DSECT
         $RPL  LIST=&LIST          GENERATE OS RPL DSECT             R4
         $UCB  LIST=&LIST          GENERATE OS UCB DSECT
         $DYN  LIST=&LIST          GENERATE OS DYN DSECT
         $UCM  LIST=&LIST          GENERATE OS UCM DSECT             R4
         $IOCM LIST=&LIST          GENERATE OS IOCM DSECT
         $TCB  LIST=&LIST          GENERATE OS TCB DSECT       @OZ32566
         $TIOT LIST=&LIST          GENERATE OS TIOT DSECT      @OZ32566
         $SDWA LIST=&LIST          GENERATE OS SDWA DSECT      @OZ32566
         $EWA  LIST=&LIST          GENERATE OS EWA DSECT             R4
         $TED  DOC=&DOC            GENERATE HASP TED DSECT
         $TGB  DOC=&DOC            GENERATE HASP TGB DSECT
         $TGM  DOC=&DOC            GENERATE HASP TGM DSECT
         $TAB  DOC=&DOC            GENERATE HASP TAB DSECT           R4
         $PCIE DOC=&DOC            GENERATE HASP PCIE DSECT          R4
         $SVT  DOC=&DOC            GENERATE HASP SSVT DSECT
         $SJB  DOC=&DOC            GENERATE HASP SJB DSECT
         $PCE  DOC=&DOC            GENERATE HASP PCE DSECT
         $BUFFER DOC=&DOC          GENERATE HASP BUFFER DSECT
         $BPM  DOC=&DOC            GENERATE HASP BPM DSECT           R4
         $CMB  DOC=&DOC            GENERATE HASP CMB DSECT
         $SMF  DOC=&DOC            GENERATE HASP SMF DSECT
         $ICE  DOC=&DOC            GENERATE HASP ICE DSECT          R41
         $FMH  DOC=&DOC            GENERATE HASP FMH DSECT          R41
         $JQE  DOC=&DOC            GENERATE HASP JQE DSECT
         $JOE  DOC=&DOC            GENERATE HASP JOE DSECT           R4
         $JOT  DOC=&DOC            GENERATE HASP JOT DSECT           R4
         $QSE  DOC=&DOC            GENERATE HASP QSE DSECT
         $JCT  DOC=&DOC            GENERATE HASP JCT DSECT
         $PDDB DOC=&DOC            GENERATE HASP PDDB DSECT          R4
         $IOT  DOC=&DOC            GENERATE HASP IOT DSECT           R4
         $SCAT DOC=&DOC            GENERATE OS SCAT DSECT            R4
         $DCT  DOC=&DOC            GENERATE HASP DCT DSECT
         $TQE  DOC=&DOC            GENERATE HASP TQE DSECT
         $CCA  DOC=&DOC            GENERATE HASP CCA DSECT           R4
         $CCE  DOC=&DOC            GENERATE HASP CCE DSECT           R4
         $PSO  DOC=&DOC            GENERATE HASP PSO DSECT           R4
         $DTE  DOC=&DOC            GENERATE HASP DTE DSECT
         $EEL  DOC=&DOC            GENERATE HASP EEL DSECT     @OZ32566
         $PPPWORK DOC=&DOC         GENERATE HASP PPPWORK DSECT       R4
         POP   PRINT
         PRINT &GEN,&DATA          SET ASSEMBLY PRINT OPTIONS
         $HCT  DOC=&DOC            GENERATE HASP HCT
         SPACE 1
         MEND
         TITLE 'HASP DAUGHTER TASK ELEMENT FORMAT GENERATION MACRO'
         SPACE 5
*
***** $DTE *****              DEFINE HASP DAUGHTER-TASK ELEMENT
*
*
         MACRO
         $DTE  &DOC=NO
         TITLE 'HASP DAUGHTER TASK ELEMENT (DTE) DSECT'
         SPACE 5
DTEDSECT DSECT
DTETCB   DS    A                   SUB-TASK TCB ADDRESS
DTETECB  DS    F                   SUB-TASK TERMINATION ECB
DTEWECB  DS    F                   SUB-TASK WORK ECB
&SYSECT  CSECT                     END OF DTE DSECT
         MEND
         TITLE 'HASP ESTAE ELEMENT (EEL) MACRO DEFINITION'     @OZ32566
         SPACE 3                                               @OZ32566
*                                                              @OZ32566
***** $EEL *****                   DEFINE HASP ESTAE ELEMENT   @OZ32566
*                                                              @OZ32566
*                                                              @OZ32566
         MACRO -- $EEL -- HASP ESTAE ELEMENT DSECT             @OZ32566
        $EEL   &DOC=NO                                         @OZ32566
         TITLE 'HASP ESTAE ELEMENT (EEL) DSECT'                @OZ32566
         SPACE 2                                               @OZ32566
EELDSECT DSECT                     HASP ESTAE ELEMENT DSECT    @OZ32566
         SPACE 1                                               @OZ32566
EELFLAGS DS    X                   FLAG BYTE                   @OZ32566
EELILC   DS    X                   INSTR. LNGTH CODE (IN BYTES)@OZ32566
         DS    H                   RESERVED FOR FUTURE USE     @OZ32566
EELCODE  DS    CL4                 ABEND OR $ERROR CODE        @OZ32566
EELFADDR DS    A                   ADDRESS OF FAILURE          @OZ32566
EELPCE   DS    A                   ADDRESS OF FAILING PCE      @OZ32566
EELPSW   DS    2A                  PSW AT TIME OF ABEND        @OZ32566
EELREGS  DS    XL(16*4)            REGISTERS AT TIME OF ABEND  @OZ32566
         DS    F                   RESERVED                    @OZ32566
         SPACE 1                                               @OZ32566
EELSIZE  EQU   *-EELDSECT          SIZE OF AN EEL              @OZ32566
         SPACE 4                                               @OZ32566
*        EELFLAGS                                              @OZ32566
         SPACE 2                                               @OZ32566
EELJESAB EQU   B'10000000'   80    JES2 $ERROR                 @OZ32566
EELSYSAB EQU   B'01000000'   40    JES2 SYSTEM ABEND           @OZ32566
EELREGSA EQU   B'00100000'   20    REGISTERS AVAILABLE IN EEL  @OZ32566
&SYSECT  CSECT                                                 @OZ32566
.END     MEND                                                  @OZ32566
         TITLE 'HASP NUCLEUS'                                  @OZ32566
         SPACE 5                                               @OZ32566
HASPNUC  START 0                   HASP NUCLEUS                @OZ32566
         SPACE 5
*
*                             EXTERNAL REFERENCES
*
         SPACE 3
         ENTRY HASP                MAIN ENTRY
         ENTRY $QINDEX             ADDRESS OF JOB QUEUE INDEX TABLE
         ENTRY $IOAPPEN            I/O APPENDAGE TABLE
         ENTRY $ABEND              HASP STAE EXIT ROUTINE
         ENTRY $GETCELR            ADDRESS OF GET CELL ROUTINE
         ENTRY $FRECELR            ADDRESS OF FREE CELL ROUTINE
         ENTRY $GETLOKR            ADDRESS OF GET LOCK ROUTINE
         ENTRY $FRELOKR            ADDRESS OF FREE LOCK ROUTINE
         ENTRY $DYN                ADDRESS OF ALLOCATION ROUTINE
         ENTRY $HEXIT              ADDRESS OF HASP EXIT ROUTINE
         ENTRY HASPATTN            ADDRESS OF HASP ATTENTION APPENDAGE
         ENTRY $ERRORSA            ADDRESS OF ERROR EXIT REGISTER SAVE
         SPACE 5
         COPY  $HASPGEN            COPY HASPGEN PARAMETERS
         SPACE 5                                                     R4
         GBLB  &IECIHDR            IOSGEN DOCUMENTATION OPTION       R4
         GBLB  &IECITP             IOSGEN DOCUMENTATION OPTION       R4
         TITLE 'HASP CONTROL BLOCKS'
*
*                             DOCUMENTATION OPTIONS FOR THIS ASSEMBLY
*
         SPACE 3
        $SYSPARM (OFF,GEN,NODATA,NO,NO)
         SPACE 5
*
*                             GENERATE HASP CONTROL BLOCKS
*
         SPACE 3
        $HASPCB DOC=&DOC,LIST=&LIST  GENERATE HASP CONTROL BLOCKS
         TITLE 'HASP DISPATCHER'
***********************************************************************
*                                                                     *
*        $WAIT - WAIT ON A SPECIFIC $POST                             *
*                                                                     *
*        R11   = HCT ADDRESS (BASE1)                                  *
*        R13   = PCE ADDRESS (SAVE)                                   *
*        R15   = NEW ENTRY ADDRESS                                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
$WAITS   NULL                      $WAIT SAVE=YES FOR AN EVENT       R4
         ST    R14,PCELINK         SAVE PROCESSORS                   R4
         STM   R0,R12,PCER0            REGISTERS                     R4
         SPACE 1                                                     R4
$WAIT    NULL                      $WAIT SAVE=NO FOR AN EVENT        R4
         ST    R15,PCER15          SAVE RE-ENTRY ADDRESS             R4
         LM    R2,R3,PCEPCEA       POINT TO NEXT AND PREVIOUS PCE
         ST    R2,PCEPCEA-PCEDSECT(,R3) REMOVE PCE
         ST    R3,PCEPCEB-PCEDSECT(,R2) FROM QUEUE
         ST    SAVE,PCEPCEA        SET NEXT WAITING TO ITSELF
         ST    SAVE,PCEPCEB        SET PREVIOUS WAITING TO ITSELF
         B     HASPDISP            DISPATCH NEXT READY               R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        $WAITR - WAIT ON A RESOURCE ( REGISTERS SAVED BY MACRO )     *
*                                                                     *
*        R1    = WAIT CHAIN ELEMENT ADDRESS MINUS (PCEPCEA-PCEDSECT)  *
*        R11   = HCT ADDRESS (BASE1)                                  *
*        R13   = PCE ADDRESS (SAVE)                                   *
*        R15   = NEW ENTRY ADDRESS                                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
$WAITR   NULL                      $WAIT SAVE=NO FOR A RESOURCE      R4
         ST    R15,PCER15          SAVE RE-ENTRY ADDRESS             R4
         SPACE 1                                                     R4
$WAITRQ  LM    R2,R3,PCEPCEA       POINT TO NEXT AND PREVIOUS PCES   R4
         ST    R2,PCEPCEA-PCEDSECT(,R3) REMOVE PCE
         ST    R3,PCEPCEB-PCEDSECT(,R2) FROM QUEUE
         L     R2,PCEPCEB-PCEDSECT(,R1) POINT TO LAST OF NEXT QUEUE
         ST    SAVE,PCEPCEA-PCEDSECT(,R2) PUT WAITER ON END
         ST    SAVE,PCEPCEB-PCEDSECT(,R1) POINT QUEUE END TO CURRENT
         ST    R2,PCEPCEB          POINT CURRENT PCE TO PREVIOUS
         ST    R1,PCEPCEA          POINT TO CURRENT QUEUE HEADER
         EJECT
***********************************************************************
*                                                                     *
*        DISPATCH READY PCES                                          *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
HASPDISP LA    R1,$READY-(PCEPCEA-PCEDSECT) POINT TO ACTIVE PCE ZERO R4
         L     SAVE,PCEPCEA-PCEDSECT(,R1) POINT TO FIRST READY PCE
         ST    SAVE,$CURPCE        SET CURRENT PCE POINTER
         CR    SAVE,R1             IS IT PCE ZERO
         BE    HASPLOOK            LOOK FOR MORE WORK IF PCE ZERO
         LM    R14,R12,PCELINK     PICK UP REGISTERS
         MVI   PCEEWF,$EWFPOST     SET NORMAL POST INHIBITOR
         BR    R15                 ENTER PROCESSOR
         SPACE 2                                                     R4
***********************************************************************
*                                                                     *
*        LOOK FOR MORE WORK                                           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  2,8                                                   R4
HASPLOOK CLC   $HASPECF,=X'FFFF'   HAS ANY RESOURCE BEEN $POSTED...  R4
         BE    HASPSPEC            TEST $$POST IF NOT
         SPACE 1                                                     R4
HASPOST  LA    R7,$EWQ1-(PCEPCEA-PCEDSECT) POINT TO FIRST PCE ZERO
         LA    R4,8                GET OFFSET
         LA    R5,$EWQL-(PCEPCEA-PCEDSECT) POINT TO LAST PCE ZERO
         LH    R6,$HASPECF         PICK UP POST MASK
         SLL   R6,31-($EWQL-$EWQ1)/8 ALIGN CORRESPONDING BITS
         SPACE 1                                                     R4
HASPOSTL ALR   R6,R6               SHIFT ONE AND CHECK OVERFLOW
         BC    2+1,HASPOSTC        DO NOT $POST IF CARRY
         LR    R1,R7               POINT TO RESOURCE PCE ZERO
         BAL   LINK,$POSTR         POST RESOURCE
         SPACE 1                                                     R4
HASPOSTC BXLE  R7,R4,HASPOSTL      LOOP
         XC    $HASPECF,MHASPECF   CHECK IF MLLM IS WAITING FOR      R4
         NC    $HASPECF,MHASPECF    ANY OF NOW POSTED RESOURCES      R4
         BZ    HASPOSTR            DON'T POST IF NONE AWAITING       R4
         XC    MHASPECF,$HASPECF   RESET APPROPRIATE RESOURCE BITS   R4
         TM    MHASPECF+$EWBPOST,$EWFPOST TEST IF POST REQUIRED      R4
         BZ    HASPOSTR            BRANCH IF NO IMMEDIATE POST       R4
         L     R1,$MLLMPCE         GET MLLM PCE ADDRESS              R4
         BAL   LINK,HASPOSTW       POST THE LINE MANAGER             R4
         SPACE 1                                                     R4
HASPOSTR MVC   $HASPECF,=X'FFFF'   RESET POST FLAGS                  R4
         B     HASPDISP            DISPATCH ANY PCES
         EJECT
***********************************************************************
*                                                                     *
*        HANDLE ANY $$POSTS                                           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING $SVDSECT,R10        PROVIDE SSVT ADDRESSABILITY       R4
         SPACE 1                                                     R4
         CNOP  2,8                                                   R4
HASPSPEC L     R10,$SSVT           GET ADDRESS OF SSVT               R4
         SPACE 1                                                     R4
HASPDL   L     R1,$HASPECB         RESET HASP                        R4
         MVI   0(R1),0              ECB                              R4
         TM    $SVPOSTW(R1),X'80'  IS ANY WORK REQUIRED...           R4
         BZ    HNOSPEC             BRANCH IF NOT.
         MVI   $SVPOSTW(R1),0      ELSE CLEAR INDICATOR              R4
         L     R1,$SVECF           PICK UP ECF FIELD
         SLR   R0,R0               SET MASK FOR CS INSTRUCTION
         CS    R1,R0,$SVECF        RESET POST FLAGS
         BNE   *-4                 LOOP UNTIL RESET
         LCR   R1,R1               GET TWOS COMPLIMENT
         BCTR  R1,0                BACK TO ONES COMPLIMENT
         STH   R1,$HASPECF         SET MASTER EVENT CONTROL
         LA    R0,$SVPCENO         SET NO OF PCES TO POST      @OZ19496
         LA    R1,$SVCOMM          POINT TO FIRST ELEMENT      @OZ19496
         CLI   $ASYNCP,255         ASYNC PROCESSOR POSTED...   @OZ19496
         BNE   HPPCEL              BR IF NO                    @OZ19496
         SPACE 1                                               @OZ19496
HPPOST   L     R3,$SVASYNC         GET ASYNC POST ELEMENT      @OZ19496
         LA    R2,255              GET MASK FOR $$POST         @OZ19496
         SLL   R2,24               ALIGN MASK                  @OZ19496
         OR    R2,R3               SET REGISTER FOR SWAP       @OZ19496
         CS    R3,R2,$SVASYNC      CONVERT POST TO $$POST      @OZ19496
         BNE   HPPOST              LOOP UNTILL POSTED          @OZ19496
         MVI   $ASYNCP,0           RESET POST INDICATOR        @OZ19496
         SPACE 1                                                     R4
HPPCEL   CLI   0(R1),X'FF'         TEST FOR POST REQUIRED
         BNE   HPNPCE              SKIP THIS PCE IF NOT REQUIRED
         L     SAVE,0(0,R1)        PICK UP PCE ADDRESS
         MVI   0(R1),X'80'         SET TO NOT POST UNTIL REQUIRED
         LA    SAVE,0(0,SAVE)      PURIFY ADDRESS
         $POST (SAVE),WORK         POST PCE
         SPACE 1                                                     R4
HPNPCE   LA    R1,4(0,R1)          UP TO NEXT ELEMENT
         BCT   R0,HPPCEL           LOOP
         EJECT                                                       R4
HPIRDR   TM    $SVIRDR,X'80'       $SVIRDR --- IS ACTION REQD...
         BZ    HPNEXT              BRANCH IF NO ACTION.
         MVI   $SVIRDR,0           CLEAR TS BYTE.
         L     R1,$SVIRDRS         POINT TO FIRST INTRDR DCT.
         SPACE 1                                                     R4
         USING DCTDSECT,R1         USE THE DCT DSECT.
         SPACE 1                                                     R4
HPIRDR1  LTR   R1,R1               ARE WE OUT OF DCTS...
         BZ    HPNEXT              BRANCH IF SO.
         CLI   DCTDEVTP,DCTINR     ARE WE STILL IN INTRDRS...
         BNE   HPNEXT              BRANCH IF NOT.
         TM    RIDFLAGS,RIDPOST    DOES PCE REQUIRE IO POST...
         BZ    HPIRDR2             BRANCH IF NOT.
         NI    RIDFLAGS,255-RIDPOST  RESET THE POST SWITCH.
         L     SAVE,DCTPCE         YES.  POINT TO THE PCE.
         LA    SAVE,0(0,SAVE)      PURIFY ADDRESS
         $POST (SAVE),IO           POST IT FOR I/O
         SPACE 1                                                     R4
HPIRDR2  L     R1,DCTCHAIN         POINT TO THE NEXT DCT
         B     HPIRDR1             AND LOOP.
         SPACE 1                                                     R4
         DROP  R1                  KILL DCT ADDRESSABILITY
         SPACE 1                                                     R4
HPNEXT   LA    R1,$EWQABIT-(PCEPCEA-PCEDSECT)  POINT TO WAIT A BIT   R4
         BAL   LINK,$POSTR         POST THE RESOURCE
         B     HASPDISP            DISPATCH WHAT WE HAVE
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        LOOK FOR ALL AVAILABLE FUNCTIONS COMPLETE AND WAIT           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
HNOSPEC  CLI   $ACTIVE,0           TEST ACTIVE COUNT                 R4
         BNE   HASPACT             BRANCH IF FUNCTIONS ARE ACTIVE
         LH    R1,$EXCPCT          TEST OUTSTANDING I/O
         A     R1,$BUSYQUE         TEST
         A     R1,$SVPIDLE         ADD $SVPIDLE COUNT
         BNZ   HASPWAIT            BRANCH IF SYSTEM IS ACTIVE
         TM    $STATUS,$ALMSGSW    TEST SYSTEM STATUS
         BO    HASPWAIT            BRANCH IF MESSAGE HAS BEEN ISSUED
        $WTO   HASPALL,L'HASPALL,JOB=NO,WAIT=NO,                     R4C
               ROUTE=$ALL,CLASS=$ALWAYS,PRI=$HI    FUNCTIONS COMPLETE
         BZ    HASPWAIT            $WTO WAS UNSUCCESSFUL, WAIT
         OI    $STATUS,$ALMSGSW    SET MESSAGE ISSUED SWITCH
         B     HASPLOOK            LOOK FOR MORE WORK
         SPACE 1                                                     R4
HASPALL  $MSG  099,'ALL AVAILABLE FUNCTIONS COMPLETE'
         SPACE 1                                                     R4
HASPACT  NI    $STATUS,255-$ALMSGSW     RESET MESSAGE ISSUED SWITCH
         SPACE 1                                                     R4
HASPWAIT L     R1,$HASPECB         GET HASP ECB POINTER              R4
         WAIT  ECB=(1)             WAIT FOR SOMETHING TO DO          R4
         B     HASPDL              DISPATCH ELIGIBLE PROCESSORS
         SPACE 1                                                     R4
         DROP  R10                 DROP SSVT ADDRESSABILITY.
         EJECT
***********************************************************************
*                                                                     *
*        $POST - PUT A SINGLE PCE ON READY QUEUE                      *
*                                                                     *
*        R1    = PCE ADDRESS                                          *
*        R2    = WORK                                                 *
*        R3    = WORK                                                 *
*        R11   = HCT ADDRESS (BASE1)                                  *
*        R14   = RETURN ADDRESS                                       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
HASPOSTW NI    PCEEWF-PCEDSECT(R1),255-$EWFWORK RESET WORK INHIBIT   R4
         BNZR  LINK                SKIP QUEUEING IF STILL INHIBITED  R4
         SPACE 1                                                     R4
$POST    MVI   PCEEWF+$EWBPOST-PCEDSECT(R1),$EWFPOST  SET INHIBIT    R4
         LM    R2,R3,PCEPCEA-PCEDSECT(R1) POINT TO NEXT AND PREVIOUS
         ST    R2,PCEPCEA-PCEDSECT(,R3) PULL PCE OFF NEXT CHAIN
         ST    R3,PCEPCEB-PCEDSECT(,R2) PULL PCE OFF PREVIOUS CHAIN
         LR    R3,R1               MAKE FIRST AND
         LR    R2,R1               LAST POINT TO CURRENT
         SPACE 1                                                     R4
HASPREDY L     R1,$READY+(PCEPCEB-PCEPCEA) POINT TO LAST ACTIVE PCE
         MVC   PCEPCEA-PCEDSECT(,R3),PCEPCEA-PCEDSECT(R1) FIX NEXT
         ST    R3,$READY+(PCEPCEB-PCEPCEA) SET NEW LAST
         ST    R1,PCEPCEB-PCEDSECT(,R2) POINT PREVIOUS TO OLD LAST
         ST    R2,PCEPCEA-PCEDSECT(,R1) SET NEXT IN OLD LAST
         BR    LINK                RETURN
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        $POSTR - PUT A GROUP OF PCES ON READY QUEUE                  *
*                                                                     *
*        R1    = RESOURCE HEADER ADDRESS MINUS (PCEPCEA-PCEDSECT)     *
*        R2    = WORK                                                 *
*        R3    = WORK                                                 *
*        R11   = HCT ADDRESS (BASE1)                                  *
*        R14   = RETURN ADDRESS                                       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
$POSTR   LM    R2,R3,PCEPCEA-PCEDSECT(R1)  POINT TO 1ST AND LAST PCE R4
         CR    R2,R1               IS FIRST ALSO PCE ZERO
         BER   LINK                NOTHING TO POST, EXIT
         ST    R1,PCEPCEA-PCEDSECT(,R1) REMOVE PCES FROM QUEUE
         ST    R1,PCEPCEB-PCEDSECT(,R1) REMOVE LAST ALSO
         B     HASPREDY            PUT ON READY QUEUE
         SPACE 3
         TITLE 'HASP INPUT/OUTPUT SUPERVISOR'
***********************************************************************
*                                                                     *
*        $EXCP - EXECUTE CHANNEL PROGRAM SUBROUTINE                   *
*                                                                     *
*        R0    = WORK, UNPREDICTABLE ON EXIT                          *
*        R1    = DCT ADDRESS, UNPREDICTABLE ON EXIT IF WAIT=NO        *
*                             BUFFER ADDR   ON EXIT IF WAIT=YES       *
*        R11   = HCT ADDRESS (BASE1)                                  *
*        R13   = PCE ADDRESS (SAVE)                                   *
*        R14   = RETURN ADDRESS (HIGH ORDER BIT ON IF EXCPVR)         *
*        R15   = ENTRY POINT ADDRESS, UNPREDICTABLE ON EXIT           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         ENTRY $EXCPR              PROVIDE $EXCP ROUTINE ENTRY       R4
         USING DCTDSECT,R1         PROVIDE DCT ADDRESSABILITY        R4
         USING BUFDSECT,WA         PROVIDE BUFFER ADDRESSABILITY     R4
         SPACE 1                                                     R4
$EXCPR   STM   WA,WB,ESAVE         SAVE WORK REGISTERS               R4
         L     WA,DCTBUFAD         FROM DCT POINT TO THE IOB/BUFFER.
         $TRACE                    WITH R1 AND WA LOADED, TRACE.
         MVI   IOBFLAG1,X'42'      SHOW CMD CHAINING, UNRELATED IN IOB.
         MVC   BUFEWF,DCTEWF       MOVE INFO FOR $ASYNC INTO THE IOB.
         STCM  R1,7,BUFDCT+1       MAKE THE IOB POINT TO THE DCT.
         IC    WB,DCTBUFCT         ADD 1 TO THE COUNT OF BUFFERS
         LA    WB,1(,WB)           PENDING COMPLETION UNDER THIS DCT.
         STC   WB,DCTBUFCT         (THE COUNT IS USED BY $ASYNC.)
         MVC   IOBDCBPT+1(3),DCTDCB+1  SET DCB ADDRESS IN IOB.
         CLI   DCTDEVTP,PCEDAWR    TEST FOR DIRECT ACCESS REQUEST
         BH    ESENDIT             BR IF NO
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*           EXTRA PROCESSING FOR ALL DIRECT-ACCESS REQUESTS           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         MVI   IOBCCW4,5           SET CHANNEL COMMAND TO WRITE
         BE    *+8                 BR IF WRITE REQUEST
         MVI   IOBCCW4,6           SET CHANNEL COMMAND TO READ
         MVC   IOBDCBPT+1(3),$HASPDCB+1  POINT TO HASP DA DCB
         CLC   PCESEEK(1),$NUMDA   TEST FOR VALID EXTENT             R4
         BNL   EXCPERR             BR IF NO TO CAUSE X'42' POST      R4
         L     WB,PCESEEK          PREPARE TO SET MBBCCHHR IN IOB
         LA    R0,0(,WB)           GET MTTR AND ISOLATE TTR IN R0.
         XR    WB,R0               THEN ISOLATE M IN WB.
         ST    WB,IOBXTENT         SET 'M000' IN THE IOB.
         STC   R0,IOBSEEK+6        MBB0000R                          R4
         SRL   WB,24               SHIFT 'M' (EXTENT NR) FOR MULTIPLY.
         MH    WB,=AL2(TEDSIZ)     COMPUTE ADDRESS OF EXTENT DATA
         A     WB,$TEDADDR         APPLICABLE TO THE I/O REQUEST.
         EJECT                                                       R4
         DROP  R1                  KILL DCT ADDRESSABILITY
         USING TEDDSECT,WB         PROVIDE TED ADDRESSABILITY        R4
         SPACE 1                                                     R4
         SRDL  R0,40               SHIFT 'TT' FOR DIVIDE.
         L     R15,TNTC            IF INVALID                        R4
         LTR   R15,R15              TED ENTRY,                       R4
         BZ    EXCPERR               BR TO CAUSE X'42' POST          R4
         DR    R0,R15              COMPUTE CYLINDER AND HEAD NUMBERS R4
         STCM  R1,3,IOBSEEK+2      MBBCC00R                          R4
         STCM  R0,3,IOBSEEK+4      MBBCCHHR                          R4
         TM    $RUNOPTS,$RPS       TEST RPS OPTION                   R4
         BZ    ESENDIT             BR IF NOT SELECTED                R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*           COMPUTE RPS SECTOR NUMBER IF APPLICABLE                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         L     WB,TRPS             GET ADDRESS OF RPS TABLE.
         MVI   IOBCCW1,X'03'       SET FIRST CHANNEL COMMAND TO NOP.
         LTR   WB,WB               DOES EXTENT SUPPORT RPS...
         BZ    ESENDIT             IF NOT, SKIP RPS COMPUTATION.
         MVI   IOBCCW1,X'23'       SET CHANNEL COMMAND TO SET SECTOR.
         SR    R1,R1               ZERO R1 FOR INSERT-CHARACTER.
         IC    R1,IOBSEEK+6        GET RECORD NUMBER FROM IOB.
         IC    R1,0(R1,WB)         GET CORRESPONDING SECTOR NUMBER.
         STC   R1,IOBCCW1+5        SET SECTOR NUMBER IN SET-SECTOR.
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        ISSUE EXCP/EXCPVR FOR ALL REQUESTS HERE                      *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
ESENDIT  MVI   BUFECBCC,0          SET COMPLETION CODE TO ZERO       R4
         LA    R1,IOBFLAG1         SET IOB ADDRESS AS ARGUMENT TO EXCP.
         LH    WB,$EXCPCT          ADD ONE
         LA    WB,1(,WB)             TO THE
         STH   WB,$EXCPCT              $EXCP COUNTER.
         LM    WA,WB,ESAVE         RESTORE WORK REGISTERS            R4
         LTR   LINK,LINK           TEST REQUEST TYPE                 R4
         BM    ESENDVR             BR IF EXCPVR                      R4
         EXCP  (1)                 CALL THE OS I/O SUPERVISOR.
         B     EXCPXIT             THEN BR TO EXIT                   R4
         SPACE 1                                                     R4
ESENDVR  EXCPVR (1),SUBSYS         CALL THE OS I/O SUPERVISOR        R4
         SPACE 1                                                     R4
EXCPXIT  CLI   0(LINK),0           WAIT FOR I/O...                   R4
         BNER  LINK                RETURN IF NO                      R4
         L     R1,PCEBUFAD         GET BUFFER ADDRESS                R4
         SPACE 1                                                     R4
EXCPIO  $WAIT  IO                  WAIT FOR I/O TO COMPLETE          R4
         TM    BUFECBCC-BUFDSECT(R1),X'7F'  TEST COMPLETION CODE     R4
         BO    2(,LINK)            RETURN IF COMPLETE AND OK TO +2   R4
         BZ    EXCPIO              LOOP IF NOT COMPLETE              R4
         LA    LINK,2(,LINK)       ADVANCE RETURN ADDRESS            R4
         L     R15,$IOERROR        GET I/O ERROR ROUTINE ADDRESS     R4
         BR    R15                 BR TO ISSUE I/O ERROR MSG AND RETURN
         SPACE 1                                                     R4
EXCPERR  MVI   IOBSEEK,X'FF'       SET INVALID BB IN IOB             R4
         MVI   IOBXTENT,X'00'      INSURE EXTENT IS VALID      @OZ33184
         B     ESENDIT             BR TO CAUSE X'42' I/O ERROR POST  R4
         SPACE 1                                                     R4
         DROP  WA,WB               KILL CTL BLOCK ADDRESSABILITY
         SPACE 1                                                     R4
ESAVE    EQU   $CSAVREG            SAVE AREA FOR REGS WA,WB          R4
         TITLE 'HASP JOB QUEUE MANAGER'
***********************************************************************
*                                                                     *
*        $QADD - ADD A JOB TO THE ACTIVE JOB QUEUE                    *
*                                                                     *
*        R0    = QUEUE ID, UNPREDICTABLE ON EXIT                      *
*        R1    = JOB QUEUE ELEMENT IMAGE ADDRESS, JQE ADDRESS ON EXIT *
*        R11   = HCT ADDRESS (BASE1)                                  *
*        R13   = PCE ADDRESS (SAVE)                                   *
*        R14   = RETURN ADDRESS                                       *
*        R15   = WORK, UNPREDICTABLE ON EXIT                          *
*                                                                     *
***********************************************************************
         SPACE 1
         USING JQEDSECT,R1         PROVIDE JQE ADDRESSABILITY
         SPACE 1
$QADD   $QSUSE                     REQUEST ACCESS TO CHECKPOINT DATA
         STM   WA,WB,PCEWA         PRESERVE WORK REGISTERS
         LH    WB,$JQFREE          GET OFFSET
         N     WB,=X'0000FFFF'      OF 1ST FREE JQE
         BZ    QBACK2              BR IF NO FREE JQES
         SLL   WB,2                 ELSE CONVERT OFFSET
         AL    WB,$JOBQPTR           TO ABSOLUTE ADDRESS
         MVC   $JQFREE,QUECHAIN(WB)  REMOVE JQE FROM FREE CHAIN
         MVC   0(JQELNGTH,WB),JQE     AND INITIALIZE NEW QUEUE ENTRY
         SPACE 1
QADD     LR    R1,WB               RELOAD JQE ADDRESS
         STC   R0,JQETYPE          SET NEW QUEUE TYPE
         TM    JQEFLAGS,QUEPURGE+QUEOPCAN  TEST FOR CANCEL
         BZ    QADDIT              BR IF NO
         TM    JQEFLAGS,QUEPURGE   IF PURGE NOT REQUESTED,
         BZ    QOPCAN               BR TO TEST CURRENT QUEUE
         MVI   JQETYPE,$HARDCPY      ELSE QUEUE JOB FOR $HARDCPY
         LH    R15,JQEJOECT        TEST JOB FOR
         AH    R15,JQEHLDCT         HELD OR JOT OUTPUT
         BNZ   QADDIT              BR IF SO
         MVI   JQETYPE,$PURGE       ELSE QUEUE JOB FOR $PURGE
         B     QADDIT              THEN BR TO QUEUE THE JOB
         EJECT                                                       R4
         CNOP  0,8
QOPCAN   TM    JQETYPE,$XEQ        TEST FOR CNVT OR EXECUTE          R4
         BZ    QADDIT              BR IF NO                          R4
         MVI   JQETYPE,$OUTPUT      ELSE RE-QUEUE FOR $OUTPUT        R4
         SPACE 1                                                     R4
QADDIT   SLR   WA,WA               GET
         IC    WA,JQETYPE           INDEX
         IC    WA,$QINDEX(WA)        TO NEW
         LTR   WA,WA                  QUEUE
         BZ    Q02                 BR IF NO SUCH QUEUE
         LA    WA,$JQHEADS-2-QUECHAIN(WA)  ELSE PREPARE TO SCAN Q
         SPACE 1
QNEXT1   LR    R15,WA              RELOAD CHAIN ADDRESS
         LH    WA,QUECHAIN(,WA)    GET OFFSET
         N     WA,=X'0000FFFF'      OF NEXT JQE
         BZ    QINSERT             BR IF END OF QUEUE
         SLL   WA,2                 ELSE CONVERT OFFSET
         AL    WA,$JOBQPTR           TO ABSOLUTE ADDRESS
         CLC   JQEPRIO,QUEPRIO(WA) TEST CURRENT PRIORITY
         BNH   QNEXT1              BR IF NEW ELEMENT NOT HIGHER
         SL    WA,$JOBQPTR          ELSE REDUCE ADDRESS TO
         SRL   WA,2                  RELATIVE WORD OFFSET
         SPACE 1
QINSERT  SL    R1,$JOBQPTR         REDUCE NEW ELEMENT ADDRESS TO
         SRL   R1,2                 RELATIVE WORD OFFSET
         STH   R1,QUECHAIN(,R15)   INSERT NEW QUEUE ELEMENT
         STH   WA,QUECHAIN(,WB)     INTO CURRENT JOB QUEUE
         LR    R1,R15              POINT TO MODIFIED JQE
         BAL   R15,QCKPT            AND FORCE CHECKPOINT OF JQE
         B     QPOST2              THEN BR TO $POST JOB
         EJECT
***********************************************************************
*                                                                     *
*        $QGET - OBTAIN CONTROL OF A JQE FROM LOGICAL QUEUE           *
*                                                                     *
*        R0    = WORK, UNPREDICTABLE ON EXIT                          *
*        R1    = QUEUE TYPE, JQE ADDRESS (0 IF NONE) ON EXIT          *
*        R11   = HCT ADDRESS (BASE1)                                  *
*        R13   = PCE ADDRESS (SAVE)                                   *
*        R14   = RETURN ADDRESS                                       *
*        R15   = WORK, UNPREDICTABLE ON EXIT                          *
*                                                                     *
***********************************************************************
         SPACE 1
         CNOP  4,8                                                   R4
$QGET   $QSUSE                     REQUEST ACCESS TO CHECKPOINT DATA
         N     R1,=A(255)          MASK OFF HI-ORDER BYTES
         IC    R1,$QINDEX(R1)      GET JOB QUEUE HEADER OFFSET
         TM    $STATUS,$DRAINED    IF SYSTEM NOT DRAINING,
         BZ    *+6                  BR TO VALIDATE QUEUE TYPE
         SLR   R1,R1                 ELSE ENSURE INVALID TYPE
         LTR   R1,R1               IF INVALID QUEUE TYPE,
         BZR   LINK                 RETURN WITH CC = 0
         LA    R1,$JQHEADS-2-QUECHAIN(R1)  SCAN REQUESTED JOB QUEUE
         SPACE 1
QNEXT    LH    R1,JQECHAIN         GET OFFSET
         N     R1,=X'0000FFFF'      OF NEXT JQE
         BZR   LINK                RETURN IF END OF QUEUE WITH CC=0
         SLL   R1,2                 ELSE CONVERT OFFSET
         AL    R1,$JOBQPTR           TO ABSOLUTE ADDRESS
         TM    JQEFLAGS,QUEHOLDA+QUEHOLD1+QUEHOLD2+QUEBUSY
         BNZ   QNEXT               BR IF JOB ACTIVE OR HELD
*              NOTE -- MASK ON NEXT INSTR SET AT END OF INITIALIZATION
$QGETAFF TM    JQEFLAG2,*-*        MAY JOB RUN ON THIS SYSTEM
         BZ    QNEXT               BR IF NO TO TEST NEXT JQE
         SPACE 1                                                     R4
QIND     TM    $STATUS,$INDMODE    CPU IN INDEPENDENT MODE...       R41
         BO    QCPUIND             BR IF YES                        R41
         TM    JQEFLAG2,QUEINDAF   JOB IN INDEPENDENT MODE...       R41
         BO    QNEXT               BR IF YES TO CHECK NEXT JQE      R41
         SPACE 1                                                    R41
QGETCONT DS    0H                                                   R41
         SPACE 1                                                     R4
QGOT     OC    JQEFLAGS,$SIDBUSY   MAKE JQE ACTIVE                   R4
         ST    R1,PCEJQE           SET JQE ADDRESS IN PCE      @OZ32566
         BAL   R15,QCKPT           FORCE CKPT OF JQE (SET CC)
         L     R1,PCEJQE           RESTORE JQE ADDRESS         @OZ32566
         BR    LINK                 AND RETURN
         SPACE 1                                                    R41
QCPUIND  TM    JQEFLAG2,QUEINDAF   JOB IN INDEPENDENT MODE...       R41
         BZ    QNEXT               BR IF NO TO CHECK NEXT JQE       R41
         B     QGETCONT             ELSE CONTINUE QGET              R41
         EJECT
***********************************************************************
*                                                                     *
*        $QPUT - RELEASE CONTROL OF JQE TO LOGICAL QUEUE              *
*                                                                     *
*        R0    = QUEUE ID, UNPREDICTABLE ON EXIT                      *
*        R1    = JQE ADDRESS, UNPREDICTABLE ON EXIT                   *
*        R11   = HCT ADDRESS (BASE1)                                  *
*        R13   = PCE ADDRESS (SAVE)                                   *
*        R14   = RETURN ADDRESS                                       *
*        R15   = WORK, UNPREDICTABLE ON EXIT                          *
*                                                                     *
***********************************************************************
         SPACE 1
         CNOP  0,8
$QPUT   $QSUSE                     REQUEST ACCESS TO CHECKPOINT DATA
         CLM   R0,1,JQETYPE        IF QUEUE TYPE HAS CHANGED,
         BNE   QMOD                 BR TO RE-QUEUE THE JOB
         TM    JQEFLAGS,QUEPURGE+QUEOPCAN  IF JOB BEING CANCELLED,
         BNZ   QMOD                         BR TO RE-QUEUE IT
SKIP20   STM   WA,WB,PCEWA         PRESERVE WORK REGISTERS
         NI    JQEFLAGS,255-QUEBUSY  RESET BUSY BITS
         LR    WB,R1               RE-LOAD JQE ADDRESS
         EJECT
         USING JQEDSECT,WB         PROVIDE JQE ADDRESSABILITY
         SPACE 1
QPOST2   TM    JQEFLAGS,QUEBUSY    IS JOB BEING PROCESSED
         BNZ   QPOST3              BR IF SO
         CLI   JQETYPE,$XEQ        IS JOB QUEUED FOR CONVERSION...
         BE    QPOSTCNV            BR IF SO
         TM    JQETYPE,$XEQ        IS JOB QUEUED FOR EXECUTION...
         BO    QPOSTXEQ            BR IF SO
         CLI   JQETYPE,$OUTPUT     IS JOB QUEUED FOR OUTPUT...
         BE    QPOSTOUT            BR IF SO
         CLI   JQETYPE,$PURGE      IS JOB QUEUED FOR PURGE...
         BE    QPOSTPRG            BR IF SO
         SPACE 1
QPOST2A $POST  $HASPECF,JOB        $POST ALL FOR JOB                 R4
         OI    $AQSE,QSEPJOB       INDICATE CROSS-SYSTEM $POST
         B     QPOST3              THEN BR TO $POST CKPT
         SPACE 1
QPOSTCNV L     R1,$JCLPCE          GET CONVERSION PROCESSOR PCE ADDR R4
         OI    $AQSE,QSEPCNV       INDICATE CROSS-SYSTEM $POST
         B     Q$POST               AND BR TO $POST UNIQUE PROCESSOR
         SPACE 1
QPOSTXEQ L     R1,$SSVT                   $$POST EXECUTION
         OI    $SVJOB-$SVDSECT(R1),X'80'   PROCESSOR                 R4
         L     R1,$EXECPCE         GET EXECUTION PROCESSOR PCE ADDR  R4
         OI    $AQSE,QSEPXEQ       INDICATE CROSS-SYSTEM $POST
         B     Q$POST               AND BR TO $POST UNIQUE PROCESSOR
         SPACE 1
QPOSTOUT L     R1,$OUTPCE          GET OUTPUT PROCESSOR PCE ADDR     R4
         OI    $AQSE,QSEPOUT       INDICATE CROSS-SYSTEM $POST
         B     Q$POST               AND BR TO $POST UNIQUE PROCESSOR
         SPACE 1
QPOSTPRG NI    JQEFLAGS,255-QUEHOLDA-QUEHOLD1-QUEHOLD2  RELEASE JOB  R4
         OI    JQEFLAG2,QUESYSAF   SET AFFINITY FOR ALL SYSTEMS     R41
         L     R1,$PRGPCE          GET PURGE PROCESSOR PCE ADDR      R4
         OI    $AQSE,QSEPPRG       INDICATE CROSS-SYSTEM $POST
         SPACE 1
Q$POST  $POST  (R1),JOB            $POST UNIQUE PROCESSOR FOR JOB
         SPACE 1
QPOST3  $POST  $HASPECF,CKPW       $POST CHECKPOINT WRITER           R4
         LR    R1,WB               POINT TO MODIFIED ELEMENT
         BAL   R15,QCKPT           CAUSE CHECKPOINT
         SPACE 1
QBACK2   LTR   R1,WB               R1 = QUEUE ENTRY (OR 0), SET CC
         LM    WA,WB,PCEWA         RESTORE REGISTERS
         BR    LINK                RETURN
         SPACE 1
         DROP  WB                  KILL LOCAL JQE ADDRESSABILITY
         EJECT
***********************************************************************
*                                                                     *
*        $QREM - REMOVE A JOB FROM THE ACTIVE JOB QUEUE               *
*                                                                     *
*        R0    = UNPREDICTABLE ON EXIT                                *
*        R1    = JQE ADDRESS, UNPREDICTABLE ON EXIT                   *
*        R11   = HCT ADDRESS (BASE1)                                  *
*        R13   = PCE ADDRESS (SAVE)                                   *
*        R14   = RETURN ADDRESS                                       *
*        R15   = WORK, UNPREDICTABLE ON EXIT                          *
*                                                                     *
***********************************************************************
         SPACE 1
         CNOP  2,8                                                   R4
$QREM   $QSUSE                     REQUEST ACCESS TO CHECKPOINT DATA
         STM   WA,WB,PCEWA         PRESERVE WORK REGISTERS
         BAL   R15,QREM            REMOVE JQE FROM CURRENT QUEUE
         LA    WA,$JQFREE-QUECHAIN PREPARE TO SCAN FREE QUEUE
         SPACE 1
QNEXT3   LR    R1,WA               RELOAD CHAIN ADDRESS
         LH    WA,JQECHAIN         GET OFFSET
         N     WA,=X'0000FFFF'      OF NEXT JQE
         BZ    QREMQIT             BR IF END OF QUEUE
         SLL   WA,2                 ELSE CONVERT OFFSET
         AL    WA,$JOBQPTR           TO ABSOLUTE ADDRESS
         CLR   WB,WA               TEST CURRENT ADDRESS
         BH    QNEXT3              BR IF FREE ELEMENT STILL HIGHER
         SPACE 1
QREMQIT  LH    R0,$JQFREE          SAVE OFFSET OF 1ST FREE JQE
         LR    R15,WB              INSERT NEW
         SL    R15,$JOBQPTR         FREE ELEMENT
         SRL   R15,2                 INTO
         STH   R15,JQECHAIN           FREE CHAIN
         LTR   WA,WA               IF NEW ELEMENT AT END OF CHAIN,
         BZ    *+12                 BR AROUND NEXT 2 INSTRUCTIONS
         SL    WA,$JOBQPTR           ELSE RECONNECT
         SRL   WA,2                   REMAINING
         STH   WA,QUECHAIN(,WB)        FREE ELEMENTS
         LTR   R0,R0               IF FREE QUEUE HAD BEEN EMPTY,
         BZ    QPOST2A              BR TO $POST JOB
         BAL   R15,QCKPT             ELSE FORCE CKPT OF MODIFIED JQE
         B     QPOST3                 AND BR TO $POST CKPT
         EJECT
         CNOP  2,8
QREM     SLR   WA,WA               GET
         IC    WA,JQETYPE           INDEX
         IC    WA,$QINDEX(WA)        TO CURRENT
         LTR   WA,WA                  JOB QUEUE
         BZ    Q02                 BR IF NO SUCH QUEUE
         LA    WA,$JQHEADS-2-QUECHAIN(WA)  ELSE PREPARE TO SCAN Q
         LR    WB,R1               SAVE JQE ADDRESS
         SPACE 1
QNEXT3A  LR    R1,WA               RELOAD CHAIN ADDRESS
         LH    WA,JQECHAIN         GET OFFSET
         N     WA,=X'0000FFFF'      OF NEXT JQE
         BZ    Q01                 BR IF JOB NOT ON QUEUE
         SLL   WA,2                 ELSE CONVERT OFFSET
         AL    WA,$JOBQPTR           TO ABSOLUTE ADDRESS
         CLR   WA,WB               IF NOT THE CURRENT JQE,
         BNE   QNEXT3A              BR TO TEST NEXT JQE
         SPACE 1
         MVC   JQECHAIN,QUECHAIN(WB)  DECHAIN CURRENT JQE
         B     QCKPT                   AND BR TO CKPT MODIFIED JQE
         SPACE 1
Q01     $ERROR                     JOB QUEUE ERROR
         SPACE 1
Q02     $ERROR                     ILLEGAL JOB QUEUE TYPE
         EJECT
         SPACE 3
***********************************************************************
*                                                                     *
*        $QLOC - LOCATE A JQE BY JOB NUMBER                           *
*                                                                     *
*        R0    = WORK, UNPREDICTABLE ON EXIT                          *
*        R1    = JOB NUMBER, JQE ADDRESS (0 IF NONE) ON EXIT          *
*        R11   = HCT ADDRESS (BASE1)                                  *
*        R14   = RETURN ADDRESS                                       *
*        R15   = UNPREDICTABLE ON EXIT                                *
*                                                                     *
***********************************************************************
         SPACE 1
         CNOP  4,8                                                   R4
$QLOC   $QSUSE                     REQUEST ACCESS TO CHECKPOINT DATA
         LR    R0,R1               SAVE JOB NUMBER
         LA    R15,$JQTYPES*2      INITIALIZE QUEUE HEADER OFFSET
         SPACE 1
QLOC     LA    R1,$JQHEADS-2-QUECHAIN(R15)  PREPARE TO SCAN QUEUE
         SPACE 1
QNEXT4   LH    R1,JQECHAIN         GET OFFSET
         N     R1,=X'0000FFFF'     OF NEXT JQE
         BZ    QLOOP               BR IF END OF QUEUE
         SLL   R1,2                 ELSE CONVERT OFFSET
         AL    R1,$JOBQPTR           TO ABSOLUTE ADDRESS
         CH    R0,JQEJOBNO         IF NOT THE CURRENT JQE
         BNE   QNEXT4               BR TO TEST NEXT JQE
         LTR   R1,R1                 ELSE SET CC
         BR    LINK                   AND RETURN
         SPACE 1
         CNOP  0,8
QLOOP    BCTR  R15,0               IF ANOTHER JOB QUEUE,
         BCT   R15,QLOC             BR TO SCAN IT
         BR    LINK                  ELSE RETURN WITH CC = 0
         SPACE 1
         EJECT
***********************************************************************
*                                                                     *
*        $QCKPT - SCHEDULE CHECKPOINT FOR ALTERED JQE                 *
*                                                                     *
*        R0    = WORK, UNPREDICTABLE ON EXIT                          *
*        R1    = JQE ADDRESS                                          *
*        R11   = HCT ADDRESS (BASE1)                                  *
*        R13   = PCE ADDRESS (SAVE)                                   *
*        R15   = RETURN ADDRESS                                       *
*                                                                     *
***********************************************************************
         SPACE 1
$QCKPT   STM   WA,WB,PCEWA         PRESERVE REGISTERS
         LR    WB,R1               POINT TO MODIFIED ELEMENT
         B     QPOST3              CAUSE CHECKPOINT
         SPACE 1
*                                  THIS CARD DELETED BY APAR   @OZ20010
QCKPT    S     R1,$JOBQPTR         MAKE POINTER RELATIVE       @OZ20010
         BMR   R15                 RETURN IF BELOW JOB QUEUE
         C     R1,$JOBQSIZ         CHECK FOR WITHIN JOB QUEUE        R4
         BHR   R15                 RETURN IF ABOVE QUEUE
         LR    R0,R1               REMEMBER JQE OFFSET         @OZ20010
         SRL   R1,12               DIVIDE BY LENGTH OF A PAGE  @OZ20010
         AL    R1,$JOBCTLB         POINT TO CHECKPOINT CONTROL       R4
         MVC   0(1,R1),$SIDAFF     TURN ON CKPT INDICATOR      @OZ20010
         LR    R1,R0               RELOAD JQE OFFSET           @OZ20010
         LA    R1,JQELNGTH-1(,R1)  LOCATE END OF JQE           @OZ20010
         SRL   R1,12               DIVIDE BY PAGE LENGTH       @OZ20010
         AL    R1,$JOBCTLB         POINT TO CHECKPOINT CONTROL @OZ20010
         MVC   0(1,R1),$SIDAFF     TURN ON CHKPT INDICATOR     @OZ20010
         BR    R15                 RETURN
         EJECT
***********************************************************************
*                                                                     *
*        $QMOD - MODIFY JOB QUEUE CHAIN FIELD FOR ALTERED JQE         *
*                                                                     *
* REGISTERS                                                           *
*                                                                     *
*        R0    = QUEUE TYPE, UNPREDICTABLE ON EXIT                    *
*        R1    = JQE ADDRESS                                          *
*        R11   = HCT ADDRESS (BASE1)                                  *
*        R13   = PCE ADDRESS (SAVE)                                   *
*        R14   = RETURN ADDRESS                                       *
*        R15   = WORK, UNPREDICTABLE ON EXIT                          *
*                                                                     *
* NOTES                                                               *
*                                                                     *
*        CHECKPOINT MUST NOT BE ACTIVE ON ENTRY                       *
*        THIS ROUTINE IS USED TO ALTER QUEUE TYPE AND PRIORITY        *
*        IF ONLY QUEUE TYPE IS TO BE ALTERED $QPUT SHOULD BE USED     *
*                                                                     *
***********************************************************************
         SPACE 1
         CNOP  0,8
$QMOD    LA    R1,0(,R1)           CLEAR HI-ORDER BYTE
        $QSUSE TYPE=TEST           MAY QUEUES BE USED...             R4
         BNZ   Q01                 BR IF NO (LOGIC ERROR)
         SPACE 1
QMOD     ST    R0,PCER0            STORE QUEUE TYPE                  R4
         SPACE 1                                                     R4
QMODA    STM   WA,WB,PCEWA         PRESERVE WORK REGISTERS           R4
         NI    JQEFLAGS,255-QUEBUSY  RESET BUSY BITS
         BAL   R15,QREM            REMOVE JQE FROM CURRENT QUEUE
         L     R0,PCER0            RESTORE NEW QUEUE TYPE
         B     QADD                 AND BR TO ADD JQE TO NEW QUEUE
         SPACE 1
         DROP  R1                  KILL JQE ADDRESSABILITY
         EJECT
***********************************************************************
*                                                                     *
*        $QSUSE - REQUEST ACCESS TO SHARED QUEUES                     *
*                                                                     *
*        R15   = RETURN ADDRESS                                       *
*        ALL OTHER REGISTERS ARE PRESERVED                            *
*                                                                     *
*        EXITS BY BRANCHING INTO THE $WAIT ROUTINE                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8
$QSUSES  DS    0H                  $QSUSE SAVE=YES ENTRY             R4
         STM   LINK,BASE2,PCELINK  SAVE REGISTERS                    R4
         SPACE 1                                                     R4
$QSUSE   DS    0H                  $QSUSE SAVE=NO ENTRY              R4
         TM    $STATUS,$CKPTACT    IS CHECKPOINT ACTIVE
         BO    QSU1                BRANCH IF YES
         $POST $HASPECF,CKPW       ACTIVATE CHECKPOINT PROCESSOR
         SPACE 1                                                     R4
QSU1     MVI   PCEEWF,$EWFPOST     INHIBIT SPECIFIC $POSTS
         LA    R1,$EWQCKPT-(PCEPCEA-PCEDSECT)  POINT TO PCE ZERO
         B     $WAITR              $WAIT FOR CKPT RESOURCE           R4
         TITLE 'HASP UNIT ALLOCATOR / DE-ALLOCATOR'
***********************************************************************
*                                                                     *
*        $GETUNIT - OBTAIN CONTROL OF A HASP DEVICE                   *
*                                                                     *
*        R0    = RESERVED, UNPREDICTABLE ON EXIT                      *
*        R1    = DCT ADDRESS                                          *
*        R11   = HCT ADDRESS (BASE1)                                  *
*        R13   = PCE ADDRESS (SAVE)                                   *
*        R14   = RETURN ADDRESS                                       *
*        R15   = RESERVED, UNPREDICTABLE ON EXIT                      *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING DCTDSECT,R1         PROVIDE DCT ADDRESSABILITY        R4
         SPACE 1                                                     R4
         CNOP  0,8
$GETUNIT DS    0H
         TM    DCTSTAT,DCTINUSE+DCTDRAIN+DCTHOLD+DCTPAUSE  TEST DCT  R4
         BZ    UNIAVAIL            BRANCH IF AVAILABLE               R4
         SR    R0,R0               SET UNSUCCESSFUL CONDITION CODE   R4
         BR    LINK                 AND RETURN                       R4
         SPACE 3                                                     R4
UNIAVAIL OI    DCTSTAT,DCTINUSE    INDICATE DEVICE IS IN USE         R4
         LA    R1,0(,R1)           PURIFY DCT ADDRESS                R4
         BR    LINK                RETURN TO CALLER                  R4
         EJECT
***********************************************************************
*                                                                     *
*        $FREUNIT - RELEASE CONTROL OF A HASP DEVICE                  *
*                                                                     *
*        R0    = WORK, UNPREDICTABLE ON EXIT                          *
*        R1    = DCT ADDRESS                                          *
*        R11   = HCT ADDRESS (BASE1)                                  *
*        R13   = PCE ADDRESS (SAVE)                                   *
*        R14   = RETURN ADDRESS                                       *
*        R15   = WORK, UNPREDICTABLE ON EXIT                          *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8
UNIFREE  $WAIT BUF                 WAIT FOR BUFFERS TO BE RELEASED
         SPACE 1                                                     R4
$FREUNIT CLI   DCTBUFCT,0          CHECK BUFFERS ASSOCIATED WITH DCT R4
         BNE   UNIFREE             BRANCH IF NOT ZERO
         NI    DCTSTAT,255-DCTINUSE TURN OFF USE BIT
         TM    DCTDEVTP,DCTRJE     TEST DEVICE TYPE                  R4
         BZ    UNINORJE            BRANCH IF NOT RJE                R41
         TM    MDCTTYPE,DCTPSNA    TEST DEVICE TYPE                 R41
         BZ    UNINORDV            BR IF NOT SNA               @OZ20025
         L     R15,MDCTICE         PICK UP POTENTIAL ICE ADDRESS    R41
         LTR   R15,R15             TEST FOR ICE ADDRESS (PRE-ALLOC) R41
         BZ    UNINOICP            BRANCH IF NO ICE PRESENT         R41
         SLR   R0,R0               CLEAR REGISTER                   R41
         CL    R1,ICERDCT-ICEDSECT(,R15) IS ICE PREALLOCATED        R41
         BNE   UNINOICA            NO, BRANCH TO CLEAR DCT PTR      R41
         ST    R0,ICERDCT-ICEDSECT(,R15) CLEAR ICE POINTER TO DCT   R41
         SPACE 1                                                    R41
UNINOICA ST    R0,MDCTICE          CLEAR DCT POINTER TO ICE         R41
         SPACE 1                                                    R41
UNINOICP TM    DCTDEVTP,DCTDEV     TEST FOR RMT DEVICE              R41
         BZ    UNINORDV            NO, SKIP DEVICE RESETS           R41
         NI    MDCTSTAT,255-DCTADS RESET DEVICE RELATED FLAGS       R41
         SPACE 1                                                    R41
UNINORDV NI    MDCTSTAT,255-DCTEOF-DCTPOST-DCTABORT-DCTETX RESET    R41
         SPACE 1                                                    R41
UNINORJE TM    DCTSTAT,DCTDRAIN    TEST FOR $P COMMAND              R41
         BZR   LINK                RETURN IF UNIT NOT DRAINED
         ST    LINK,PCESAVEA       SAVE LINK REGISTER
        $ALLOC (R1)                UNALLOCATE THE DEVICE             R4
         ST    R1,PCER1            SAVE DCT ADDRESS                  R4
         EJECT                                                      R41
UNIWTO   MVC   UDRMSG(8),DCTDEVN   SET UP MESSAGE                    R4
        $WTO   UDRMSN,L'UDRMSN,WAIT=NO,JOB=NO,  ISSUE DRAIN MSG      R4C
               ROUTE=$LOG+$UR+$TP,CLASS=$TRIVIA,PRI=$ST              R4
         L     R1,PCER1            RESTORE DCT ADDRESS               R4
         BZ    UNIWAIT             BR IF $WTO UNSUCCESSFUL           R4
         L     LINK,PCESAVEA        ELSE RESTORE RETURN ADDRESS      R4
         BR    LINK                  AND RETURN                      R4
         SPACE 1                                                     R4
UNIWAIT $WAIT  CMB                 WAIT FOR MESSAGE BUFFER           R4
         B     UNIWTO              TRY AGAIN TO ISSUE MESSAGE        R4
         SPACE 1                                                     R4
         DROP  R1                  KILL DCT ADDRESSABILITY
         SPACE 1
UDRMSN   $MSG  097,'******** IS DRAINED',SYMB=UDRMSG
                                   PRINT OFF - SECTION DELETED @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
                                   PRINT ON -- SECTION DELETED @OZ20010
                                   PRINT ON -- SECTION DELETED @OZ20010
         TITLE 'HASP REMOVE SMF BUFFER FROM QUEUE ROUTINE'
***********************************************************************
*                                                                     *
* FUNCTION/OPERATION-                                                 *
*                                                                     *
*      THE FOLLOWING SUBROUTINE TAKES A HASP SMF BUFFER FROM          *
*      $SMFFREE FREE BUFFER POOL AND RETURNS. IF THERE ARE            *
*      NO BUFFERS AVAILABLE THE PROCESSOR IS $WAITED FOR SMF,         *
*      WHICH IS EQUATED TO CMB, UNTIL A BUFFER IS AVAILABLE,          *
*      UNLESS THE CALLING ROUTINE REQUESTS IMMEDIATE RETURN.          *
*      GOTTEN BUFFER IS CLEARED TO ZERO PRIOR TO RETURN.              *
*                                                                     *
* INPUT -                                                             *
*                                                                     *
*      R14 - RETURN ADDRESS OF CALLING ROUTINE                        *
*      R1  - ZERO     = DO NOT WAIT IF NO BUFFERS AVAILABLE           *
*            NON-ZERO = $WAIT SMF IF NO BUFFERS AVAILABLE             *
*                                                                     *
* OUTPUT -                                                            *
*      R1  - ZERO = NO BUFFER AVAILABLE                               *
*            NON-ZERO = BUFFER ADDRESS                                *
*      CC  - SET BY LTR R1,R1 PRIOR TO RETURN                         *
*                                                                     *
***********************************************************************
         SPACE 1
         CNOP  0,8
SWAITBF $WAIT  SMF
         SPACE 1
$GETSMFB OC    $SMFFREE,$SMFFREE   TEST FOR AVAILABLE BUFFER
         BNZ   SGETBUF             CONTINUE IF YES
         LTR   R1,R1               TEST FOR WAIT FOR BUFFER
         BNZ   SWAITBF             BR IF YES
         BR    LINK                 ELSE RETURN TO CALLER
         SPACE 1
         CNOP  0,8
SGETBUF  L     R1,$SMFFREE         CHAIN STARTER
         STM   WA,WB,$POSTSAV      SAVE WORK REGISTERS               R4
         SPACE 1
SRECYCLE L     R15,0(,R1)          NEXT CHAIN POINTER
         CS    R1,R15,$SMFFREE     UPDATE CHAIN
         BNE   SRECYCLE            TRY AGAIN IF NOT SUCCESSFUL
         LA    WB,SMFLNG           CLEAR                             R4
         LR    WA,R1                ENTIRE                           R4
         SLR   R15,R15               SMF                             R4
         MVCL  WA,R14                 BUFFER                         R4
         LM    WA,WB,$POSTSAV      RESTORE WORK REGISTERS            R4
         LTR   R1,R1               SET CONDITION CODE
         BR    LINK                RETURN
         TITLE 'HASP PUT SMF BUFFER ON BUSY QUEUE ROUTINE'
***********************************************************************
*                                                                     *
* FUNCTION/OPERATION -                                                *
*                                                                     *
*      THE FOLLOWING SUBROUTINE PLACES A SMF BUFFER AT BEGINNING      *
*      OF THE $SMFBUSY CHAIN AND ISSUES AN OS POST FOR HASPACCT       *
*      SUBTASK TO PROCESS SMF RECORD.                                 *
*                                                                     *
* INPUT  R1    = HASP SMF BUFFER ADDRESS                              *
*        R14   = RETURN ADDRESS                                       *
*                                                                     *
* OUTPUT       = NONE                                                 *
*                                                                     *
***********************************************************************
         SPACE 1
         CNOP  0,8
$QUESMFB L     R15,$SMFBUSY        GRAB HEAD CHAIN POINTER
         SPACE 1
SUPDTPTR ST    R15,0(,R1)          COMPLETE NEW CHAIN
         CS    R15,R1,$SMFBUSY     RESTORE HEAD CHAIN PTR. WITH
         BNE   SUPDTPTR               NEW ADDRESS
         LA    R1,$ACCTECB         POST HASPACCT SUBTASK FOR WORK
         POST  (1)                      FOR WORK
         BR    LINK                RETURN
         TITLE 'HASP DIRECT ACCESS PURGE ROUTINE'
***********************************************************************
*                                                                     *
*        $PURGER - RELEASE SPOOL SPACE TO THE MASTER TRACK GROUP MAP  *
*                                                                     *
*        R0    = RESERVED, UNPREDICTABLE ON EXIT                      *
*        R1    = USER TRACK GROUP MAP TGMDSECT                        *
*        R11   = HCT ADDRESS (BASE1)                                  *
*        R13   = PCE ADDRESS (SAVE)                                   *
*        R14   = RETURN ADDRESS                                       *
*        R15   = RESERVED, UNPREDICTABLE ON EXIT                      *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
$PURGER $TRACE                     TRACE THIS ACTIVITY               R4
        $QSUSE                     REQUEST ACCESS TO CHECKPOINT DATA R4
         STM   LINK,R1,PCELINK     SAVE REGISTERS                    R4
         LA    R15,TGMAP-TGMDSECT(,R1)  POINT TO USER MAP            R4
         L     R0,$CYLMAPL         GET LENGTH OF TRACK GROUP MAP     R4
         L     R1,$TGMAP           FREE USER'S TRACKS FROM           R4
        $VFL   OC,(R1),(R15),(R0)   MASTER TRACK GROUP MAP           R4
         L     R1,PCER1              RESTORE POINTER                 R4
         LA    R0,TGMAP-TGMDSECT(,R1) TO USER MAP                    R4
         L     R1,$CYLMAPL         CLEAR USER                        R4
         SLR   R15,R15              TRACK GROUP MAP                  R4
         MVCL  R0,R14                TO ZEROES                       R4
        $POST  $HASPECF,CKPW       REQUEST CHECKPOINT                R4
         LM    LINK,R1,PCELINK     RESTORE REGISTERS                 R4
         BR    LINK                 AND RETURN                       R4
         TITLE 'HASP CONTROL SERVICE PROGRAM LITERAL POOL'           R4
         SPACE 5                                                     R4
***********************************************************************
*                                                                     *
*        HASP CONTROL SERVICE PROGRAM LITERAL POOL                    *
*                                                                     *
***********************************************************************
         SPACE 5                                                     R4
         LTORG                                                       R4
         TITLE 'HASP BASE1-ADDRESSABLE PATCH SPACE'                  R4
***********************************************************************
*                                                                     *
*        HASP BASE1 (HCT) ADDRESSABLE PATCH SPACE                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING HASP+4096,R1        PROVIDE DUMMY ADDRESSABILITY      R4
         SPACE 1                                                     R4
$PATCHSP $PATCHSP 300              GENERATE PATCH SPACE              R4
         SPACE 1                                                     R4
         DROP  R1                  KILL DUMMY ADDRESSABILITY         R4
         SPACE 1                                                     R4
         ORG   HASP+4094           OVERLAY NON-ADDRESSABLE SPACE     R4
         TITLE 'HASP JOB QUEUES INDEX TABLE'                         R4
***********************************************************************
*                                                                     *
*        THIS TABLE IS USED BY THE HASP JOB QUEUE MANAGEMENT          *
*        ROUTINES TO LOCATE THE OFFSET OF THE JOB QUEUE HEADER        *
*        (RELATIVE TO $JQHEADS-2) BASED UPON THE JQETYPE FIELD.       *
*        AN OFFSET OF ZERO IS INTERPRETED TO MEAN THAT NO SUCH        *
*        QUEUE HEADER EXISTS.                                         *
*                                                                     *
***********************************************************************
         SPACE 5                                                     R4
$QINDEX  DC    AL1(2)                                      $PURGE    R4
         DC    AL1(4)                                      $HARDCPY  R4
         DC    AL1(6,0)                                    $OUTPUT   R4
         DC    AL1(8,0,0,0)                                $RECEIVE  R4
         DC    AL1(10),7AL1(0)                             $SETUP    R4
         DC    AL1(12),15AL1(0)                            $XMIT     R4
         DC    AL1(14),31AL1(0)                            $INPUT    R4
         DC    AL1(16)                                     $XEQ      R4
         DC    AL1(24,26,28,30,32,34,36,38,40),6AL1(0)     CLASS A-I R4
         DC    AL1(20)                                     STC CLASS R4
         DC    AL1(42,44,46,48,50,52,54,56,58),6AL1(0)     CLASS J-R R4
         DC    AL1(22,0)                                   TSU CLASS R4
         DC    AL1(60,62,64,66,68,70,72,74),6AL1(0)        CLASS S-Z R4
         DC    AL1(76,78,80,82,84,86,88,90,92,94),6AL1(0)  CLASS 0-9 R4
         DC    AL1(18),127AL1(0)                           $DUMMY    R4
         TITLE 'HASP BRANCH-ENTRY XM POST ROUTINE'                   R4
***********************************************************************
*                                                                     *
*        $XMPOST - PERFORM BRANCH-ENTRY XM POST                       *
*                                                                     *
* INPUT  R1    - ADDRESS OF 3-WORD POST ELEMENT                       *
*                                                                     *
*                   +0    ECB ADDRESS                                 *
*                   +4    ASCB ADDRESS                                *
*                   +8    ERROR ROUTINE ADDRESS                       *
*                                                                     *
*        R13   - PCE ADDRESS                                          *
*        R14   - RETURN ADDRESS                                       *
*        R15   - ENTRY ADDRESS                                        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         ENTRY $XMPOSTR            PROVIDE ENTRY FOR $XMPOST ROUTINE R4
         USING $XMPOSTR,R15        PROVIDE LOCAL ADDRESSABILITY      R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
$XMPOSTR STM   LINK,R12,PCELINK    SAVE CALLER'S REGISTERS           R4
         L     R11,0(,R1)          GET ADDRESS OF ECB TO BE POSTED   R4
         L     R2,0(,R11)          THEN LOAD THE ECB                 R4
         SPACE 1                                                     R4
XMTEST   LTR   R2,R2               IF WAIT HAS BEEN ISSUED,          R4
         BM    XMPOST               BR TO SLOW-POST THE ECB          R4
         L     R3,=A(X'40000000')    ELSE GET POST CODE              R4
         CS    R2,R3,0(R11)           AND FAST-POST THE ECB          R4
         BNE   XMTEST              BR IF UNSUCCESSFUL TO TRY AGAIN   R4
         LM    WA,R11,PCEWA         ELSE RESTORE CALLER'S REGISTERS  R4
         BR    LINK                  AND RETURN                      R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
XMPOST   LR    R9,SAVE             SAVE CALLER'S SAVE AREA ADDRESS   R4
         ICM   R11,8,=AL1(X'80')   SET HI-ORDER BIT FOR XMPOST       R4
         L     R12,8(,R1)          GET ADDRESS OF ERROR ROUTINE      R4
         L     R13,4(,R1)          GET ADDRESS OF RELATED ASCB       R4
         L     R10,=A(X'40000000') SET XMPOST CODE                   R4
         L     R15,CVTPTR          GET CVT ADDRESS                   R4
         L     R15,CVT0PT01-CVT(,R15) GET ADDR OF BR ENTRY TO XMPOST R4
         MODESET EXTKEY=ZERO       SET PROTECT KEY FOR XMPOST        R4
         BALR  LINK,R15            ENTER XMPOST ROUTINE              R4
         MODESET EXTKEY=HASP       RESTORE HASP'S PROTECT KEY        R4
         LR    SAVE,R9             RESTORE CALLER'S SAVE AREA ADDR   R4
         LM    LINK,R12,PCELINK    RESTORE CALLER'S REGISTERS        R4
         BR    LINK                 AND RETURN                       R4
         SPACE 1                                                     R4
         DROP  R15                 KILL LOCAL ADDRESSABILITY         R4
         TITLE 'HASP VIRTUAL PAGE SERVICES ROUTINES'                 R4
***********************************************************************
*                                                                     *
*        PGRLSE (BRANCH ENTRY)     PAGE RELEASE ROUTINE               *
*        PGFREE (BRANCH ENTRY)     PAGE FREE ROUTINE                  *
*        PGFIX  (BRANCH ENTRY)     PAGE FIX ROUTINE                   *
*                                                                     *
* INPUT  R0    - PAGE AREA LENGTH                                     *
*        R1    - ADDRESS OF 1ST BYTE OF PAGE AREA                     *
*        R2    - ADDRESS OF WAIT ECB (PGFIX ONLY) - NOTE - CALLER     *
*                MUST DO OWN WAIT BASED ON RC RETURNED IN R15         *
*        R11   - HCT ADDRESS (BASE1)                                  *
*        R14   - RETURN ADDRESS                                       *
*        R15   - ENTRY ADDRESS                                        *
*                                                                     *
* OUTPUT       - RETURN CODE IN R15                                   *
*                                                                     *
* NOTE   -     ONLY 1ST 32 BYTES OF $CSAVREG MAY BE USED BY THIS RTN  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         ENTRY $PGRLSER            SET UP ENTRY POINT                R4
         USING $PGRLSER,R15        LOCAL ADDRESSABILITY              R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
$PGRLSER $TRACE                                                      R4
         STM   R14,R5,$CSAVREG     SAVE WORK REGISTERS               R4
         SLR   R5,R5               SHOW NO ECB                       R4
         LA    R1,0(,R1)           CLEAR PARAMETER VALUE       @OZ20010
         LA    R15,$PGFIXR         RESET BASE ADDRESS AND BRANCH     R4
         B     PGRTNRLS-$PGFIXR(,R15)  TO MVS PAGE SERVICES    @OZ20010
         SPACE 1                                                     R4
         ENTRY $PGFREER            SET UP ENTRY POINT                R4
         USING $PGFREER,R15        LOCAL ADDRESSABILITY              R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
$PGFREER $TRACE                                                      R4
         STM   R14,R5,$CSAVREG     SAVE WORK REGISTERS               R4
         SLR   R5,R5               SHOW NO ECB                       R4
         ICM   R1,8,=AL1(X'20')    FLAG1 = PGFREE                    R4
         LA    R15,$PGFIXR         RESET BASE ADDRESS AND BRANCH     R4
         B     PGRTN-$PGFIXR(,R15)  TO MVS VIRTUAL PAGE SERVICES     R4
         SPACE 1                                                     R4
         ENTRY $PGFIXR             SET UP ENTRY POINT                R4
         USING $PGFIXR,R15         LOCAL ADDRESSABILITY              R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
$PGFIXR  $TRACE                                                      R4
         STM   R14,R5,$CSAVREG     SAVE WORK REGISTERS               R4
         LR    R5,R2               ADDR OF WAIT ECB                  R4
         XC    0(4,R5),0(R5)       CLEAR ECB                         R4
         LTR   LINK,LINK           RELEASE=Y...                @OZ20010
         BM    PGRTNRLS            BR IF YES                   @OZ20010
         ICM   R1,8,=AL1(X'42')    FLAG1 = LONG PGFIX                R4
         CLI   0(R1),*-*           FETCH PAGE INTO STORAGE           R4
         EJECT                                                       R4
PGRTN    LTR   LINK,LINK           RELEASE=Y...                @OZ20010
         BNM   *+8                 BRANCH IF NOT               @OZ20010
PGRTNRLS O     R1,=A(X'08000000')  SET RELEASE OPTION          @OZ20010
         L     R4,$HASPTCB         R4= HASP'S TCB ADDRESS      @OZ20010
         LR    R3,R1               R3 = A(1ST BYTE OF PAGE AREA)     R4
         LR    R2,R0               R2 = ADDRESS OF END OF            R4
         ALR   R2,R1                     PAGE AREA (+1)              R4
         ICM   R2,8,=AL1(X'80')    FLAG2 = END OF SUB-AREA LIST      R4
         MODESET EXTKEY=ZERO       SET PROTECT KEY FOR PG SERVICES   R4
PGO      SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,   GET LOCAL LOCK     R4C
               REGS=USE,RELATED=($PGSRVC,HASPNUC(PGR))               R4
         LR    R0,R5               ADDR OF ECB, OR ZERO              R4
         LR    R1,R3               RE-LOAD R1 FOR PG SERVICES        R4
         L     R15,CVTPTR          GET ADDRESS OF BRANCH ENTRY TO    R4
         L     R15,CVTVPSIB-CVT(,R15)   MVS VIRTUAL PAGE SERVICES    R4
         BALR  R14,R15             AND GO DO IT                      R4
         ST    R15,$CSAVREG+4      PUT RC IN R15 IN SAVE AREA        R4
PGR      SETLOCK RELEASE,TYPE=LOCAL,    GIVE UP LOCAL LOCK           R4C
               REGS=USE,RELATED=($PGSRVC,HASPNUC(PGO))         @OZ20010
         MODESET EXTKEY=HASP       GET BACK OUR PROTECT KEY          R4
         L     R15,$PGFIX          RELOAD BASE REGISTER             R41
         CLM   R3,8,=AL1(X'42')    WAS THIS PAGE-FIX REQUEST...     R41
         BNE   *+8                 BR IF NOT                        R41
         CLI   0(R3),*-*            ELSE REF. PAGE TO ENSURE FIX    R41
         LM    R14,R5,$CSAVREG     RESTORE WORK REGISTERS            R4
         BR    R14                  AND RETURN                       R4
         SPACE 1                                                     R4
         DROP  R15                 KILL LOCAL ADDRESSABILITY         R4
         TITLE 'HASP DIRECT ACCESS SPACE ALLOCATION ROUTINE'         R4
***********************************************************************
*                                                                     *
* $TRACK - HASP VERSION OF SPOOL SPACE ALLOCATION                     *
*                                                                     *
*                                                                     *
* FUNCTION ---                                                        *
*                                                                     *
*                                                                     *
*  (1)  IF CALL IS FOR INITIAL ALLOCATION, A TRACK GROUP              *
*       IS OBTAINED FROM THE TRACK GROUP BLOCK (TGB) MAINTAINED       *
*       BY THE JES2 CHECKPOINT PROCESSOR.                             *
*                                                                     *
*  (2)  IF CALL IS NOT FOR INITIAL ALLOCATION, AN ATTEMPT             *
*       IS MADE TO SUPPLY AN MTTR BASED ON THE TRACK CELL (TAB)       *
*       REFERENCED BY R1.  IF SUCCESSFUL, THE MTTR IS RETURNED        *
*       IN R1.  IF NOT, WE TRY TO OBTAIN A NEW TRACK CELL FROM        *
*       THE CURRENT TRACK GROUP.  IF THIS IS SUCCESSFUL, AN           *
*       MTTR IS RETURNED IN R1.  IF NOT, A SEARCH FOR A NEW TG        *
*       IS PERFORMED USING THE TGB.                                   *
*                                                                     *
*  (3)  IF THE TGB IS EMPTY, THE CALLER IS $WAITED.  THE JES2         *
*       CHECKPOINT PROCESSOR IS $POSTED TO CAUSE A REFILLING          *
*       OF THE TGB.                                                   *
*                                                                     *
*                                                                     *
* INPUT ---                                                           *
*                                                                     *
*  (1)  R1 CONTAINS THE ADDRESS OF A MASTER TAB (SEE TABDSECT).       *
*                                                                     *
*  (2)  THE FIRST WORD OF THE MASTER TAB IS NOT USED.  SINCE          *
*       THE MASTER TAB IS IN THE IOT, WE CAN USE THE MASTER           *
*       TAB'S ADDRESS TO GET THE ADDRESS OF THE TGM.  THE             *
*       SECOND WORD CONTAINS AN MTTR OF THE LAST ALLOCATED            *
*       BUFFER FROM THE TRACK CELL ASSIGNED TO THE TAB.  THE          *
*       THIRD WORD CONTAINS A FLAG BYTE TO TELL WHAT KIND OF          *
*       TAB THIS IS,  THE SUB-PERMUTATION NUMBER ASSOCIATED WITH      *
*       THE CURRENT TRACK CELL (SEE $STRAK IN HASPSSSM FOR A          *
*       COMPLETE DISCUSSION), THE MAXIMUM NUMBER OF RECORDS THAT      *
*       WILL FIT ON A TRACK IN THE CURRENT TRACK CELL, AND THE        *
*       NBR OF BUFFERS LEFT IN THE TRACK CELL.  IF THE BUFFER         *
*       COUNT IS 0, AND THE LAST ALLOCATED BUFFER IS 0, THE CALL      *
*       IS FOR THE INITIAL ALLOCATION OF A BUFFER.                    *
*                                                                     *
*                                                                     *
* OUTPUT ---                                                          *
*                                                                     *
*  (1)  R1 CONTAINS AN UPDATED MTTR.  THIS IS THE SAME VALUE AS       *
*       IS SET IN THE 2ND WORD OF THE INPUT TAB.                      *
*                                                                     *
*  (2)  CC IS SET TO ZERO IF A NEW TG HAS BEEN ALLOCATED.             *
*       CC IS SET TO NON-ZERO IF AN OLD TG HAS BEEN ALLOCATED.        *
*                                                                     *
***********************************************************************
         EJECT                                                       R4
*                                                                    R4
***********************************************************************
*                                                                     *
*        $TRACK -- GET A NEW MTTR FOR CALLER                          *
*                                                                     *
***********************************************************************
*                                                                     *
         SPACE 1                                                     R4
         USING TABDSECT,R3         TAB ADDRESSABILITY                R4
         USING TGMDSECT,R4         TGM ADDRESSABILITY                R4
         USING $TRACKR,BASE2       PROVIDE LOCAL ADDRESSABILITY      R4
         ENTRY $TRACKR             PROVIDE ENTRY FOR $TRACK ROUTINE  R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
$TRACKR  STM   LINK,R12,PCELINK    SAVE CALLER'S REGISTERS           R4
         LR    BASE2,R15           RELOAD BASE                       R4
         LR    R3,R1               SETUP TAB DSECT                   R4
         LA    R4,IOTTGMAP-IOTMSTAB(,R1)  OBTAIN ADDRESS OF TGM      R4
         SPACE 1                                                     R4
TRECORD  ICM   R6,1,TABUFCNT       NBR OF BUFFERS LEFT IN TRAKCELL   R4
         BZ    TCELL               NONE LEFT - NEED A NEW TRAKCELL   R4
         BCTR  R6,0                SUBTRACT 1                        R4
         STC   R6,TABUFCNT         SAVE NEW BUFFER COUNT             R4
         SLR   R1,R1               ZERO OUT WORK FIELD               R4
         IC    R1,$RECINCR         PICK UP &RECINCR                  R4
         AL    R1,TABMTTR          PT AT POTENTIAL NEW BUFFER        R4
         ST    R1,TABMTTR           AND SAVE ITS MTTR                R4
         CLC   TABMTTR+3(1),TABMAXR  HAVE WE GONE OFF THE TRACK...   R4
         BNH   T1RETURN            NO - MTTR IS VALID - EXIT         R4
         IC    R1,TABSPN           YES - GET CURRENT SUB-PERM NBR    R4
         AL    R1,=F'1'            ADD 1 TO IT                       R4
         STC   R1,TABSPN           SAVE NEW SUB-PERM NBR             R4
         STC   R1,TABMTTR+3        PUT NEW VALUE OF R IN MTTR        R4
         SPACE 1                                                     R4
T1RETURN ST    R1,PCER1            PUT NEW MTTR IN SAVE AREA         R4
         LM    LINK,R12,PCELINK    RESTORE CALLER'S REGISTERS        R4
         LTR   R15,R15             SET CONDITION CODE                R4
         BR    R14                 RETURN TO CALLER                  R4
         SPACE 2                                                     R4
TTRKF256 DC    F'256'              FULLWORD CONSTANT OF 256          R4
         EJECT                                                       R4
         CNOP  0,8                                                   R4
TCELL    ICM   R1,15,TGMCELL       MTTR OF NEXT AVAILABLE TRAKCELL   R4
         BZ    TBLOBA              NOTHING THERE - 1ST TIME THRU     R4
         CLM   R1,1,TGMCYMXM+3     R OF TGMCELL GT MAX R OF TG...    R4
         BH    TTRACK              YES - NO MORE TRAKCELLS ON THIS   R4C
                                   TRACK - NEED A NEW TRACK          R4
         SPACE 1                                                     R4
TCRETRY  ST    R1,TABMTTR          SAVE MTTR OF 1ST BFR IN NEW TKCEL R4
         MVC   TABSPN,TGMCYMXM     GET SUB-PERM FOR NEW TRAKCELL     R4
         MVC   TABMAXR,TGMCYMXM+3  GET MAX R OF TRACK GROUP          R4
         SLR   R6,R6               ZERO OUT CTR FOR FINDING TABUFCNT R4
         SLR   R0,R0               CLEAR WORK REG                    R4
         IC    R0,$RECINCR         PICK UP &RECINCR                  R4
         SPACE 1                                                     R4
TCLOOP   ALR   R1,R0               POINT TO NEXT BUFFER IN TRAKCELL  R4
         LA    R6,1(,R6)           ADD 1 TO BUFCNT                   R4
         SPACE 1                                                     R4
TCCHECK  CLM   R1,1,TGMCYMXM+3     R GONE PAST MAX R FOR THE TG...   R4
         BNH   TCLOOPND            NO - HAVE WE REACHED $TCELSIZ     R4
         CLC   TGMCYMXM(1),$RECINCR     IS SUB-PERM STILL VALID...   R4
         BNL   TCEND               NO - FINISH UP.  NOTE THAT        R4C
                                   TGMCELL WILL NOW HAVE AN INVALID  R4C
                                   MTTR (I.E. - R IS TOO LARGE)      R4
         IC    R1,TGMCYMXM         PICK UP TGMSPN                    R4
         AL    R1,=F'1'            UP IT BY ONE                      R4
         STC   R1,TGMCYMXM         SAVE NEW TGMSPN - WE'VE ALSO      R4C
                                   PUT NEW R IN MTTR OF TGMCELL      R4C
                                   BEING CALCULATED                  R4
         B     TCCHECK             SEE IF R IS STILL VALID           R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
TCLOOPND CLM   R6,1,$TCELSIZ       DO WE HAVE A FULL TRACK CELL...   R4
         BL    TCLOOP              NO - COUNT ANOTHER BUFFER         R4
         SPACE 1                                                     R4
TCEND    ST    R1,TGMCELL          SAVE MTTR OF NEXT AVALIABLE TKCEL R4
         BCTR  R6,0                SUBTRACT 1 SINCE 1ST BUFFER IN    R4C
                                   NEW TRAKCELL IS ALLOCATED         R4
         STC   R6,TABUFCNT         SAVE NEW BUFFERS LEFT COUNT       R4
         L     R1,TABMTTR          GET READY TO EXIT                 R4
         B     T1RETURN             AND LEAVE                        R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
TTRACK   AL    R1,TTRKF256         INCREASE TT IN TGMCELL BY 1       R4
         CLM   R1,6,TGMCYMXM+1     OUT OF OUR TRACK GROUP...         R4
         BH    TBLOBA              YES - GET A NEW TRACK GROUP       R4
         IC    R1,TTRKF256+2       MAKE R IN TGMCELL A 1             R4
         MVI   TGMCYMXM,1          SET TGMSPN BACK TO 1              R4
         B     TCRETRY             INITIALIZE NEXT TRAKCELL          R4
         TITLE 'HASP DA SPACE ALLOCATION (TG FROM TGB)'
***********************************************************************
*                                                                     *
*              ALLOCATE MTTR FROM TGB                                 *
*                                                                     *
***********************************************************************
         SPACE 1
         USING $SVDSECT,R10        PROVIDE SSVT ADDRESSABILITY       R4
         SPACE 1                                                     R4
         CNOP  4,8                                                   R4
TBLOBA   L     R10,$SSVT           R10 = SSVT ADDRESS                R4
         SLR   R2,R2               R2 = 0 (FOR CDS)
         SLR   R3,R3               R3 = 0 (FOR CDS)
         SPACE 1                                                     R4
TBLOBB   LM    R5,R7,$SVTTGBA      R5=1ST, R6=SIZE, R7=LAST TGB
         SPACE 1                                                     R4
         USING TGBDSECT,R5         TGB ADDRESSABILITY
         SPACE 1                                                     R4
TBLOBC   LM    R0,R1,TGBENTRY      FETCH TGB ENTRY
         LTR   R1,R1               TEST FOR AVAILABLE TG
         BZ    TBLOBD              BRANCH IF NOT
         CDS   R0,R2,TGBENTRY      TRY TO ALLOCATE
         BE    TBLOBE              BRANCH IF SUCCESSFUL
         SPACE 1                                                     R4
TBLOBD   BXLE  R5,R6,TBLOBC        TRY NEXT ENTRY
         SPACE 1
***********************************************************************
*                                                                     *
*              TGB DEPLETED. FORCE RE-BLOB.                           *
*                                                                     *
***********************************************************************
         SPACE 1
        $POST  $HASPECF,CKPW       TELL CHECKPOINT PROCESSOR         R4
         TM    PCELINK,X'80'       DID USER WANT TO WAIT...          R4
         BZ    TBWAIT              BR IF YES TO $WAIT                R4
         LM    LINK,R12,PCELINK    RESTORE CALLER'S REGISTERS        R4
         SR    R1,R1               SET MTTR TO ZERO                  R4
         BR    LINK                RETURN WITH NO TRACKS             R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
TBWAIT  $WAIT  TRAK,SAVE=NO        WAIT ON RESOURCE                  R4
         L     BASE2,$TRACK        RESTORE BASE REGISTER             R4
         LA    R4,IOTTGMAP-IOTMSTAB(,R1)     A(TGM) FOR TGMDSECT     R4
         B     TBLOBA              TRY AGAIN
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*              TGB ALLOCATED. ESTABLISH TED ADDRESSABILITY            *
*                                                                     *
***********************************************************************
         SPACE 1
         CNOP  0,8                                                   R4
TBLOBE   LR    R2,R1               R2 = MTTR
         SRL   R2,24               R2 = 000M
         MH    R2,=AL2(TEDSIZ)     R2 = M * TEDSIZE
         AL    R2,$TEDADDR         R2 = ADDRESS OF TED
         SPACE 1                                                     R4
         USING TEDDSECT,R2         TED ADDRESSABILITY
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*              SHOW TRACK GROUP ALLOCATION IN USERS MAP               *
*                                                                     *
***********************************************************************
         SPACE 1
         LR    R3,R0               R3 = OFFSET & BIT MASK
         SRL   R3,16               R3 = OFFSET TO ALLOCATION BYTE
         IC    R8,TGMAP(R3)        T8 = MAP BYTE
         OR    R8,R0               SHOW ALLOCATION
         STC   R8,TGMAP(R3)         FOR THIS TG
         SPACE 1
***********************************************************************
*                                                                     *
*              DEVELOP NEW PTTR FROM MTTR & TED                       *
*                                                                     *
***********************************************************************
         SPACE 1
         LR    R0,R1               R0 = NEW MTTR
         SRL   R0,8                R0 = 0MTT
         AH    R0,TNTG             R0 = MAX TT + 1
         BCTR  R0,0                R0 = MAX TT
         SLL   R0,8                R0 = MAX TT0
         AH    R0,TNRT             R0 = MAX TTR
         ICM   R0,8,=X'01'         R0 = PTTR
         SPACE 1                                                     R4
         DROP  R2                  FORGET TED
         SPACE 1
***********************************************************************
*                                                                     *
*              STORE NEW PTTR (MAX) & MTTR IN TGM                     *
*                                                                     *
***********************************************************************
         SPACE 1
         LM    R2,R3,TGMCYMXM      FETCH CURRENT VALUE
         CDS   R2,R0,TGMCYMXM      REPLACE WITH NEW
         L     R3,PCER1            A(MSTR TAB) FROM R1 IN SAVE AREA  R4
         XC    PCER15,PCER15       SHOW A NEW TG WAS OBTAINED        R4
         B     TCRETRY             INITIALIZE A NEW TRAKCELL         R4
         SPACE 1
         DROP  R3,R4,R5,R10,BASE2  FORGET ALL                        R4
         TITLE 'HASP BUFFER POOL MANAGEMENT'                         R4
***********************************************************************
*                                                                     *
*        $GETBUF - GET SOME KIND OF JES2 BUFFER                       *
*                                                                     *
*        R0    - NUMBER OF BUFFERS REQUESTED, IF GREATER THAN 1       *
*        R1    - TYPE OF BUFFER WANTED (INPUT), 1ST BUF ADDR (OUTPUT) *
*        R11   - HCT ADDRESS (BASE1)                                  *
*        R14   - RETURN ADDRESS                                       *
*        R15   - ENTRY POINT ADDRESS, UNPREDICTABLE ON EXIT           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING BUFDSECT,R4         PROVIDE BUFFER ADDRESSABILITY     R4
         USING BPMDSECT,WB         PROVIDE BPM ADDRESSABILITY        R4
         USING $GETBUFR,R15        PROVIDE LOCAL ADDRESSABILITY      R4
         ENTRY $GETBUFR            ENTRY TO GENERAL BUFFER GET RTN   R4
         SPACE 1                                                     R4
$BUFSAVE EQU   $CSAVREG+32         USE LAST 32 BYTES OF $CSAVREG FOR R4C
                                   STORING REGS BECAUSE OTHER        R4C
                                   SERVICE ROUTINES WILL BE CALLED   R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
$GETBUFR $TRACE                                                      R4
         STM   LINK,R5,$BUFSAVE    SAVE WORK REGS                    R4
         XC    $BUFSAVE+12(4),$BUFSAVE+12  ZERO R1 IN SAVE AREA      R4
         LA    R4,$BUFSAVE+12-(BUFCHAIN-BUFDSECT)  INITIAL CHAIN ADR R4
         ST    R1,$BUFSAVE+4       SAVE BUFTYPE IN R15 IN SAVE AREA  R4
         LA    R5,1                ASSUME REQUEST FOR SINGLE BUFFER  R4
         TM    $BUFSAVE+7,BUFMULT  TEST ASSUMPTION                   R4
         BZ    SKIP40              BR IF VALID                       R4
         LR    R5,R0               SET UP LOOP CTR                   R4
SKIP40   N     R1,=F'7'            EXTRACT $BPMTABL REL ENTRY NUMBER R4
         SLL   R1,2                MULTIPLY BY 4                     R4
         L     WB,$BPMTABL(R1)     GET PROPER BUFFER POOL            R4
         L     WB,0(,WB)            MAP ADDRESS                      R4
         LA    WB,0(,WB)           CLEAR HI-ORDER BYTE               R4
         CH    R5,BPMBUFCT         ENOUGH BUFFERS AVAILABLE...       R4
         BNH   BUFGET              BR IF YES TO ALLOCATE BUFFERS     R4
         TM    $BUFSAVE+7,BUFTP    TEST FOR TP BUFFER                R4
         BZ    SKIP50              BR IF NO                          R4
         MVI   $TPBFMAP,0           ELSE INDICATE NO TP BUFFERS      R4
SKIP50   LM    R2,R5,$BUFSAVE+16   RESTORE WORK REGISTERS            R4
         TM    $BUFSAVE,X'40'      DOES CALLER WISH TO WAIT...       R4
         BZR   LINK                RETURN IF NO WITH CC = 0          R4
         L     R1,$BUFSAVE+4       RESTORE R1                        R4
        $WAIT  BUF                 WAIT FOR BUFFER(S)                R4
         L     R15,$GETBUF         RESTORE ENTRY POINT ADDRESS       R4
         B     $GETBUFR             AND BR TO RETRY REQUEST          R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        BUFFER AVAILABLE - COMPUTE ADDRESS AND RECONTRUCT IOB        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
BUFGET   SLR   R1,R1               CLEAR WORK                        R4
         SLR   WA,WA                REGISTERS                        R4
         TRT   BPMMAP,BUFTRTBL     TEST FOR AVAILABLE BUFFER         R4
         BNZ   BUFAVAIL            BR IF YES                         R4
B02     $ERROR                     AVAILABLE BUFFER COUNT INVALID    R4
         SPACE 1                                                     R4
BUFAVAIL LH    WA,BUFTBL1-2(WA)    ALLOCATE                          R4
         EX    WA,BUFALLOC          THE BUFFER                       R4
         SRL   WA,8                RELATIVE BUFFER WITHIN MASK BYTE  R4
         SL    R1,BPMAPADR         R1 = NUMBER OF                    R4
         SLL   R1,3                      PAGES OF                    R4
         ALR   R1,WA                      BUFFERS PRECEEDING         R4
         SLR   R0,R0                       NEWLY ALLOCATED           R4
         D     R0,BPMPGBFS                  BUFFER                   R4
         SLL   R1,12               R1 = ADDRESS                      R4
         MH    R0,BPMBFSIZ               OF NEWLY                    R4
         ALR   R1,R0                      ALLOCATED                  R4
         ALR   R1,WB                       BUFFER                    R4
         ST    R1,BUFCHAIN         CHAIN BUFFER TO PREVIOUS BUFFER   R4
         LR    R4,R1               PREV BUFFER PTR AND DSECT         R4
         IC    R0,$BUFSAVE+7       GET BUFFER TYPE                   R4
        $BFRBLD (R1),TYPE=(R0)     CONSTRUCT BUFFER                  R4
         L     R15,$GETBUF         RESTORE BASE REGISTER             R4
         TM    BUFTYPE,BUFFIX      PAGE-FIX THIS BUFFER...           R4
         BZ    BUFGET1             NO                                R4
         LH    R0,BPMBFSIZ         LENGTH OF A BUFFER                R4
         LA    R2,BUFSTART         GET ADDRESS OF PSEUDO-ECB         R4
         XC    0(4,R2),0(R2)        AND CLEAR FOR $PGFIX             R4
        $PGSRVC FIX,(R1),(R0),(R2) PAGE-FIX THE BUFFER               R4
         L     R15,$GETBUF         RESTORE BASE REGISTER             R4
         CLI   BUFSTART,0          ENSURE BUFFER NOW IN MAIN STORAGE R4C
                                   (AND HENCE FIXED) -- NO NEED THEN R4C
                                   TO WAIT ON PSEUDO ECB.            R4
         EJECT                                                       R4
BUFGET1  LH    R1,BPMBUFCT         DECREMENT                         R4
         BCTR  R1,0                 BUFFER                           R4
         STH   R1,BPMBUFCT           COUNT                           R4
         BCT   R5,BUFGET           IF WANTED, GET ANOTHER BUFFER     R4
         SPACE 1                                                     R4
         XC    BUFCHAIN,BUFCHAIN   SHOW END OF BUFFER CHAIN          R4
         LM    LINK,R5,$BUFSAVE    RESTORE REGS                      R4
         LTR   LINK,LINK           SET NON-ZERO CC                   R4
         BR    LINK                RETURN                            R4
         SPACE 1                                                     R4
BUFALLOC NI    0(R1),*-*           *** EXECUTE ONLY ***              R4
         SPACE 1                                                     R4
BUFTBL1  DC    AL1(0,255-X'80',1,255-X'40',2,255-X'20',3,255-X'10')  R4
         DC    AL1(4,255-X'08',5,255-X'04',6,255-X'02',7,255-X'01')  R4
         SPACE 1                                                     R4
BUFTRTBL DC    AL1(0,16,14,14),4AL1(12),8AL1(10)                     R4
         DC    16AL1(8),32AL1(6),64AL1(4),128AL1(2)                  R4
         SPACE 1                                                     R4
         DROP  R4,WB,R15           KILL LOCAL ADDRESSABILITY         R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        $FREEBUF - RELEASE BUFFER TO FREE POOL                       *
*                                                                     *
*        R0    - WORK, UNPREDICTABLE ON EXIT                          *
*        R1    - BUFFER ADDRESS, UNPREDICTABLE ON EXIT                *
*        R11   - HCT ADDRESS (BASE1)                                  *
*        R14   - RETURN ADDR (HI-ORDER BIT ON IF MULTIPLE BFR FREE)   *
*        R15   - ENTRY POINT ADDRESS, UNPREDICTABLE ON EXIT           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING BUFDSECT,R1         PROVIDE BUFFER ADDRESSABILITY     R4
         USING BPMDSECT,WB         PROVIDE BFR MAP ADDRESSABILITY    R4
         USING $FREEBFR,R15        PROVIDE LOCAL ADDRESSABILITY      R4
         ENTRY $FREEBFR            ENTRY TO BUFFER FREE ROUTINE      R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
$FREEBFR $TRACE                                                      R4
         STM   LINK,R5,$BUFSAVE    SAVE WORK REGS                    R4
         LTR   LINK,LINK           FREE MULTIPLE BUFFERS...          R4
         BM    SKIP60              BR IF YES                         R4
         XC    BUFCHAIN,BUFCHAIN   NO - FORCE 1 BUFFER TO BE FREED   R4
SKIP60   IC    R5,BUFTYPE          PICK UP BUFFER TYPE               R4
         N     R5,=F'7'            EXTRACT $BPMTABL REL ENTRY NUMBER R4
         SLL   R5,2                MULTIPLY BY 4                     R4
         L     WA,$BPMTABL(R5)     GET PROPER BUFFER POOL            R4
         L     WB,0(,WA)            MAP ADDRESS AND                  R4
         LA    WB,0(,WB)             CLEAR HI-ORDER BYTE             R4
         TM    BUFTYPE,BUFTP       IS THIS A TP BUFFER               R4
         BZ    FREEBF1             BR IF NO                          R4
         MVI   0(WA),X'80'         SHOW TP BUFFER PRESENT FOR RTAM   R4
         SPACE 1                                                     R4
FREEBF1  L     R5,BUFCHAIN         SAVE BUFCHAIN                     R4
         LA    R5,0(,R5)           CLEAR HI-ORDER BYTE               R4
         LA    R4,0(,R1)           RELOAD PURIFIED BUFFER ADDRESS    R4
         CL    R4,BPMBFR1          IF BUFFER                         R4
         BL    FREERROR             ADDRESS IN                       R4
         CL    R4,BPMLAST            VALID RANGE,                    R4
         BNH   BUFVALID               BR TO CONTINUE                 R4
         SPACE 1                                                     R4
FREERROR L     R14,$BUFSAVE        RESTORE FOR DEBUG PURPOSES        R4
B01     $ERROR                     INVALID BUFFER ADDRESS            R4
         SPACE 1                                                     R4
         DROP  R1                  KILL BUFFER ADDRESSABILITY        R4
         EJECT                                                       R4
         USING BUFDSECT,R4         PROVIDE BUFFER ADDRESSABILITY     R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
BUFVALID LR    WA,R4               WA = NUMBER OF BUFFERS            R4
         SLR   WA,WB                     IN PAGES PRECEEDING         R4
         SRL   WA,12                      PAGE CONTAINING            R4
         MH    WA,BPMPGBFS+2               CURRENT BUFFER            R4
         SLR   R0,R0               R1 = RELATIVE                     R4
         N     R1,BUFMASK1               BUFFER                      R4
         LH    R14,BPMBFSIZ               NUMBER                     R4
         DR    R0,R14                      WITHIN                    R4
         ALR   R1,WA                        BUFFER POOL              R4
         LR    R14,R1              R14 = ADDR OF MAP BYTE            R4
         SRL   R14,3                      REPRESENTING BUFFER        R4
         AL    R14,BPMAPADR                BEING FREED               R4
         N     R1,BUFMASK2         R1 = RELATIVE BIT WITHIN MAP BYTE R4
         LA    R1,BUFTBL2(R1)      POINT TO ALLOCATION MASK BYTE     R4
         OC    0(1,R14),0(R1)      MAKE BUFFER AVAILABLE             R4
        $POST  $HASPECF,BUF        INDICATE BUFFER AVAILABLE         R4
         TM    BUFTYPE,BUFFIX      WAS THE BUFFER FIXED...           R4
         BZ    FREEBF2             BR IF NO                          R4
         LH    R0,BPMBFSIZ         LENGTH OF A BUFFER                R4
         LR    R1,R4               ADDR OF BUFFER TO BE FREED        R4
        $PGSRVC FREE,(R1),(R0)     PAGE-FREE THE BUFFER              R4
         LM    R14,R15,$BUFSAVE    RESTORE DESTROYED REGS            R4
         SPACE 1                                                     R4
FREEBF2  LR    R1,WA               R1 = ADDRESS OF MASK BYTE         R4
         SRL   R1,3                      REPRESENTING 1ST BUFFER IN  R4
         AL    R1,BPMAPADR                PAGE CONTAING FREED BUFFER R4
         N     WA,BUFMASK2         R0 = BITS FROM BIT MAP,           R4
         ICM   R0,12,0(R1)               LEFT JUSTIFIED,             R4
         SLL   R0,0(WA)                   REPRESENTING BUFFERS       R4
         ST    R0,$BUFSAVE+8               IN PAGE CONTAINING        R4
         IC    R1,BPMMASK                   BUFFER JUST FREED        R4
         EX    R1,BUFTEST          TEST OTHER BFRS IN PAGE (IF ANY)  R4
         BO    BUFFREE             BR IF PAGE CAN BE RELEASED        R4
         SPACE 1                                                     R4
FREEBF3  LH    R1,BPMBUFCT         INCREMENT                         R4
         LA    R1,1(,R1)            BUFFER                           R4
         STH   R1,BPMBUFCT           COUNT                           R4
         LTR   R1,R5               POINT TO NEXT BUFFER IN CHAIN     R4
         BNZ   FREEBF1             FREE NEXT BUFFER                  R4
         LM    LINK,R5,$BUFSAVE    RESTORE REGS                      R4
         BR    LINK                 AND RETURN WITH CC = 0           R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        PAGE-RELEASE PAGE CONTAINING FREED BUFFER                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
BUFFREE  LR    R1,R4               ADDR OF BUFFER IN PG TO BE FREED  R4
         N     R1,BUFMASK3         DROP DOWN TO PAGE BNDRY           R4
         L     R0,BUFPGSIZ         PAGE SIZE                         R4
        $PGSRVC RLSE,(R1),(R0)     RELEASE THE PAGE                  R4
         LM    R14,R15,$BUFSAVE    RESTORE DESTROYED REGS            R4
         B     FREEBF3             CHECK FOR ANOTHER BUFFER          R4
         SPACE 1                                                     R4
BUFTEST  TM    $BUFSAVE+8,*-*      *** EXECUTE ONLY ***              R4
         SPACE 1                                                     R4
BUFPGSIZ DC    F'4096'             OS/VS2 PAGE SIZE                  R4
BUFMASK1 DC    X'00000FFF'         ADDRESS MASK                      R4
BUFMASK2 DC    X'00000007'         ADDRESS MASK                      R4
BUFMASK3 DC    X'00FFF000'         ADDRESS MASK                      R4
         SPACE 1                                                     R4
$BPMTABL DS    0A                  TABLE OF BPMAP ADDRESSES          R4
         DC    A($BFRMAP)          REGULAR JES2 BUFFERS              R4
         DC    A($TPBFMAP)         TP BUFFERS                        R4
         DC    A($PGBFMAP)         PAGE BUFFERS                      R4
         DC    A($PPBFMAP)         PP BUFFERS                        R4
         SPACE 1                                                     R4
BUFTBL2  DC    X'8040201008040201' ALLOCATION MAP BYTE MASK          R4
         SPACE 1                                                     R4
         DROP  R4,WB,R15           KILL LOCAL ADDRESSABILITY         R4
         TITLE 'HASP BUFFER BUILD ROUTINE'                           R4
***********************************************************************
*                                                                     *
*        $BFRBLD - BUILD A BUFFER (INITIALIZE PREFIX)                 *
*                                                                     *
*        R0    - BFR TYPE IF ENTERED VIA BAL, UNPREDICTABLE ON EXIT   *
*        R1    - ADDRESS OF THE BUFFER                                *
*        R11   - HCT ADDRESS (BASE1)                                  *
*        LINK  - RETURN ADDRESS, HI-ORDER BIT ON IF BFR TYPE IN R0    *
*        R15   - ENTRY POINT ADDRESS (USED AS LOCAL BASE)             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         ENTRY $BFRBLDR            PROVIDE ENTRY FOR $BFRBLD ROUTINE R4
         USING $BFRBLDR,R15        PROVIDE LOCAL ADDRESSABILITY      R4
         USING BUFDSECT,R1         PROVIDE BUFFER ADDRESSABILITY     R4
         SPACE 3                                                     R4
         CNOP  0,8                                                   R4
$BFRBLDR $TRACE                                                      R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        DETERMINE TYPE OF BUFFER TO BUILD                            *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         LTR   LINK,LINK           WAS BUFFER TYPE PROVIDED...       R4
         BM    SKIP70              BR IF YES                         R4
         LA    R0,BUFHASP           ELSE SUPPLY DEFAULT              R4
SKIP70   STC   R0,BUFTYPE          STORE BUFFER TYPE IN BUFFER       R4
         TM    BUFTYPE,BUFRPL      TEST FOR RPL TYPE BUFFER          R4
         BO    BFRBLRPL            BUILD AN RPL IF YES               R4
         TM    BUFTYPE,BUFIOB      TEST FOR IOB TYPE BUFFER          R4
         BNOR  LINK                RETURN TO CALLER IF NOT           R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        CONSTRUCT AN IOB IN THE BUFFER                               *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
BFRBLIOB DS    0H                  BUILD IOB IN FRONT OF BUFFER      R4
         XC    BUFDSECT(BUFSTART-BUFDSECT),BUFDSECT  ZERO BUFDSECT   R4
         STC   R0,BUFTYPE          RESTORE BUFTYPE                   R4
         MVI   IOBFLAG1,X'42'      SET IOBFLAG1 (CMD CHAIN, UNREL)   R4
         MVC   IOBECBPT,$HASPECB   SET HASP ECB POINTER              R4
         MVI   IOBSEEK+6,1         RECORD 1 INDICATION               R4
         LA    R0,IOBCCW1          SET ADDRESS OF START              R4
         ST    R0,IOBSTART          OF CHANNEL PROGRAM               R4
         LA    R0,IOBCCW1+5        FIRST CCW                         R4
         ST    R0,IOBCCW1           SET TO                           R4
         MVI   IOBCCW1,3             NOP                             R4
         MVI   IOBCCW1+4,X'40'        WITH                           R4
         MVI   IOBCCW1+7,1             COMMAND CHAINING              R4
         LA    R0,IOBSEEK+2        SECOND CCW                        R4
         ST    R0,IOBCCW2           SET TO                           R4
         MVI   IOBCCW2,X'31'         SEARCH ID EQUAL                 R4
         MVI   IOBCCW2+4,X'40'        WITH                           R4
         MVI   IOBCCW2+7,5             COMMAND CHAINING              R4
         LA    R0,IOBCCW2          THIRD CCW SET                     R4
         ST    R0,IOBCCW3           TO TIC TO                        R4
         MVI   IOBCCW3,8             SECOND CCW                      R4
         LA    R0,BUFSTART         FOURTH CCW SET                    R4
         ST    R0,IOBCCW4           TO READ/WRITE                    R4
         MVC   IOBCCW4+6(2),$BUFSIZE INTO/FROM BUFSTART              R4
         BR    LINK                RETURN TO CALLER                  R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        CONSTRUCT AN RPL IN THE BUFFER                               *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
BFRBLRPL DS    0H                  BUILD RPL IN FRONT OF BUFFER      R4
         XC    BUFDSECT(RPLSIZE),BUFDSECT  CLEAR RPL BUFFER          R4
         STC   R0,BUFTYPE          RESTORE BUFTYPE                   R4
         SPACE 1                                                     R4
         USING RPLDSECT,R1         PROVIDE RPL ADDRESSABILTIY        R4
         SPACE 1                                                     R4
         MVI   RPLSTYP,RPLSVTAM    SET SUBTYPE = VTAM                R4
         LA    R0,RPLLNGTH         GET LENGTH OF VTAM RPL            R4
         STC   R0,RPLLEN           STORE IN RPL LENGTH FIELD         R4
         SPACE 1                                                     R4
         LA    R0,RPLBUFST                 AREA = RPLBUFST           R4
         ST    R0,RPLAREA                                            R4
         LH    R0,$BFSZSNA              AREALEN = $BFSZSNA           R4
         ST    R0,RPLBUFL                                            R4
         MVI   RPLVTFL2,RPLSCHED+RPLEX     POST = SCHED              R4C
                                        RESPOND = (EX,NFME,NRRN)     R4
         MVI   RPLCNTDF,RPLDATA         CONTROL = DATA               R4
         MVI   RPLOBSQ,RPLOSET           OBSQAC = SET                R4
         MVI   RPLIBSQ,RPLISET           IBSQAC = SET                R4
         MVI   RPLOPT2,RPLKEY             OPTCD = CA                 R4
         MVI   RPLOPT5,RPLNODE                  = RELEASE            R4
         MVI   RPLOPT6,RPLCOND                  = COND               R4
         MVI   RPLOPT7,RPLCNALL+RPLQOPT         = CONALL             R4C
                                                = Q                  R4
         MVI   RPLOPT8,RPLODACP                 = ACCEPT             R4
         MVI   RPLOPT11,RPLSTART                = START              R4
         MVI   RPLOPT12,RPLNIBTK                = NIBTK              R4C
                                                = NFMHDR             R4
         SPACE 1                                                     R4
         BR    LINK                RETURN TO CALLER                  R4
         SPACE 1                                                     R4
         DROP  R1,R15              RELEASE ADDRESSABILITY            R4
         SPACE 3                                                     R4
         LTORG                                                       R4
         TITLE 'HASP CELL CONTROL AND LOCK SERVICES'
***********************************************************************
*                                                                     *
*        $GETCELL - SUBROUTINE TO GET A COMMON SERVICE AREA CELL      *
*                                                                     *
* REGISTERS -                                                         *
*                                                                     *
*        R0    = ADDRESS OF SJB                                       *
*        R1    = ADDRESS OF TCB OR ZERO (ADDRESS OF CELL)             *
*        R4    = LENGTH OF CELL                                       *
*        R11   = ADDRESS OF HCT (BASE1) - SSVT                        *
*        R14   = RETURN                                               *
*        R15   = ENTRY BASE                                           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING CCEDSECT,R7
         USING $GETCELR,BASE3      PROVIDE $GETCELR ADDRESSABILITY   R4
         SPACE 1                                                     R4
$GETCELR STM   R0,R15,$CSAVREG     SAVE CALLER'S REGISTERS           R4
         LR    BASE3,R15           SET BASE
         LR    R10,BASE1           COPY BASE 1
         L     R11,$SSVT           POINT TO SSVT
         USING HASP,R10
         USING SSVT,R11
         L     R15,$SVGCELL        POINT TO GET CELL
         BALR  R14,R15             GET CELL
         B     GCGM                IF NO CELL GET MORE      +0
GCEND    ST    R1,$CSAVREG+R1*4    SET ANSWER               +4
         LM    R0,R15,$CSAVREG     RETURN REGISTERS
         LTR   R1,R1               SET CONDITION CODE
         BR    R14                 EXIT
         SPACE 1                                                     R4
GCGM     L     R15,$SVGCMNS        POINT TO GET MAIN
         LA    R0,1                SET CLAIM CODE -- R4 = (B-1)/256  R4
         BALR  R14,R15             ENTER ROUTINE
         LTR   R1,R1               TEST FOR GOTTEN
         BZ    GCEND               EXIT SO CALLER CAN REACT
         LM    R0,R1,$CSAVREG      PICK UP CALLER ID REGS
         STM   R0,R1,CCESJB+CCETCB-CCETCB SET USERS ID INTO CCE
         L     R1,CCECLOC-1        POINT TO CELL
         LA    R1,0(0,R1)          PURIFY
         B     GCEND               RETURN
         SPACE 1                                                     R4
         USING HASP,BASE1
         DROP  BASE3
         DROP  R10
         EJECT
***********************************************************************
*                                                                     *
*        $FRECEL - SUBROUTINE TO FREE A CELL                          *
*                                                                     *
* REGISTERS -                                                         *
*                                                                     *
*        R0    = RESERVED                                             *
*        R1    = ADDRESS OF STORAGE CELL TO FREE. CELL POINTS TO CCE  *
*        R11   = ADDRESS OF HCT (BASE1) - SSVT                        *
*        R14   = RETURN                                               *
*        R15   = ENTRY BASE                                           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING $FRECELR,R15        PROVIDE $FRECELR ADDRESSABILITY   R4
         SPACE 1                                                     R4
$FRECELR STM   R0,R15,$CSAVREG     SAVE CALLER'S REGISTERS           R4
         LR    R10,BASE1
         L     R11,$SSVT           POINT TO SSVT
         USING HASP,R10
         USING SSVT,R11
         L     R15,$SVFCELL        POINT TO FREE CELL ROUTINE
         BALR  R14,R15             ENTER IT
         LM    R0,R15,$CSAVREG     RESTORE REGISTERS
         BR    R14                 RETURN
         SPACE 1                                                     R4
         USING HASP,BASE1
         DROP  R10
         DROP  R15
         EJECT
***********************************************************************
*                                                                     *
*        $GETLOK - SUBROUTINE TO GET THE CMS LOCK                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING $GETLOKR,BASE3      PROVIDE $GETLOKR ADDRESSABILITY   R4
         SPACE 1                                                     R4
$GETLOKR STM   R0,R15,$CSAVREG     SAVE CALLER'S REGISTERS           R4
         LR    BASE3,R15           COPY BASE
         LR    WA,BASE1            SAVE BASE1
         MODESET EXTKEY=ZERO       GET KEY 0
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,RELATED=($GETLOK,*-*,$FRC
               ELOKR)
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,RELATED=($GETLOK,*-*,$FRELC
               OKR)
         MODESET EXTKEY=HASP       GET KEY 1
         LM    R0,R15,$CSAVREG-HASP(WA) RESTORE REGISTERS
         BR    R14                 RETURN
         SPACE 1                                                     R4
         DROP  BASE3
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        $FRELOK - SUBROUTINE TO FREE THE LOCK                        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING $FRELOKR,BASE3      PROVIDE $FRELOKR ADDRESSABILITY   R4
         SPACE 1                                                     R4
$FRELOKR STM   R0,R15,$CSAVREG     SAVE CALLER'S REGISTERS           R4
         LR    BASE3,R15           COPY BASE
         LR    WA,BASE1            SAVE BASE1
         MODESET EXTKEY=ZERO       GET KEY 0
         SETLOCK RELEASE,TYPE=CMS,RELATED=($FRELOK,*-*,$GETLOKR)
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=($FRELOK,*-*,$GETLOKR)
         MODESET EXTKEY=HASP       GET KEY 1
         LM    R0,R15,$CSAVREG-HASP(WA) RESTORE REGISTERS
         BR    R14                 RETURN
         SPACE 1                                                     R4
         DROP  BASE3
         DROP  R7
         TITLE 'HASP INPUT/OUTPUT SUPERVISOR APPENDAGES'
***********************************************************************
*                                                                     *
*        HASPATTN - UNSOLICITED DEVICE END INTERRUPT HANDLER          *
*                                                                     *
*        THIS ROUTINE RUNS UNDER CONTROL OF AN SRB SCHEDULED          *
*        DUE TO UNSOLICITED DEVICE ENDS ON HASP UNIT RECORD DEVICES.  *
*        IT RESETS THE DCTHOLD AND DCTPAUSE BITS OF THE DCT           *
*        CORRESPONDING TO THE ATTENTION DEVICE AND SIMULATES A        *
*        $$POST FOR UNIT. THE ADDRESS OF THIS ROUTINE IS MADE KNOWN   *
*        TO THE SYSTEM BY ROUTINES IN HASPINIT.                       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING UCBDSECT,R2         ESTABLISH UCB ADDRESSABILITY      R4
         USING DCTDSECT,R3         ESTABLISH DCT ADDRESSABILITY
         USING $SVDSECT,R4         ESTABLISH SSVT ADDRESSABILITY
         USING HASPATTN,R15        ESTABLISH LOCAL ADDRESSABILITY
         SPACE 1                                                     R4
         CNOP  4,8                                                   R4
HASPATTN STM   R0,R15,0(R13)       SAVE ALL REGISTERS                R4
         L     BASE1,EASYBASE      SET UP STANDARD BASE1
         L     R2,IOSUCB-IOSB(,R1) GET ADDRESS OF UCB FROM IOSB
         ICM   R3,15,$DCTPOOL      POINT TO 1ST DCT                  R4
         BZ    ARETURN             RETURN IF NONE                    R4
         SPACE 1
ATESTDEV TM    DCTSTAT,DCTATTN     TEST DEVICE STATUS
         BZ    ANEXTDCT            BR IF NOT SET FOR ATTN PROCESSING
         L     R4,DCTDCB           GET ADDRESS OF DCB
         L     R4,DCBDEBAD-DCBDSECT(,R4)  GET ADDRESS OF DEB
         CLM   R2,3,DEBSUCBA+2-DEBDSECT(R4)  COMP UCB ADDRESSES
         BNE   ANEXTDCT            BRANCH IF UCB ADDRESSES DO NOT MATCH
         NI    DCTSTAT,255-DCTPAUSE  RESET PAUSE FLAG
         CLI   DCTDEVTP,DCTRDR     TEST DEVICE TYPE
         BNE   *+8                 BRANCH IF NOT READER
         NI    DCTSTAT,255-DCTHOLD  RESET HOLD FLAG
         TM    DCTSTAT,DCTINUSE+DCTDRAIN+DCTHOLD  TEST
         BZ    APOSTDEV            BRANCH IF DEVICE IS AVAILABLE
         SPACE 1
ANEXTDCT ICM   R3,7,DCTCHAIN+1     GET ADDRESS OF NEXT DCT
         BNZ   ATESTDEV            BRANCH IF NOT END OF DCT CHAIN
         B     ARETURN             AVAILABLE DEVICE NOT FOUND, RETURN
         EJECT
APOSTDEV LA    R0,$EWFUNIT*((255-$EWBUNIT)/255*255+1)  SET UNIT FLAGS
         L     R4,$SSVT            GET ADDRESS OF SSVT
         L     R1,$SVECF           PICK UP OLD $$POST ECF VALUE
         OR    R0,R1               COMBINE FLAGS
         CS    R1,R0,$SVECF        SET NEW FLAGS
         BNE   *-6                 TRY AGAIN IF NOT SET
         L     R10,EPOSTM          GET POST COMPLETION CODE
         L     R12,$HASPECB        GET ADDRESS  OF HASP ECB          R4
         MVI   $SVPOSTW(R12),X'FF' $$POST HASP DISPATCHER            R4
         L     R1,0(,R12)          GET CONTENTS OF HASP ECB          R4
         SPACE 1                                                     R4
APOSTEST LTR   R1,R1               TEST ECB
         BM    APOSTECB            BRANCH IF HASP IS WAITING
         CS    R1,R10,0(R12)       TRY FAST POST                     R4
         BNE   APOSTEST            BRANCH IF FAST POST NOT SUCCESSFUL
         B     ARETURN             FAST POST SUCCESSFUL, RETURN
         SPACE 1
APOSTECB L     R12,$HASPTCB        GET ADDRESS OF HASP TCB
         L     R11,$HASPECB        GET ADDRESS OF HASP ECB           R4
         L     R15,CVTPTR          GET ADDRESS OF CVT
         L     R15,CVT0PT01-CVTDSECT(,R15)  GET ADDRESS OF POST
         BALR  R14,R15             TAKE BRANCH ENTRY TO POST
         SPACE 1
ARETURN  LM    R0,R15,0(R13)       RESTORE REGISTERS
         BR    R14                  AND RETURN
         SPACE 1                                                     R4
         DROP  R2,R3,R4,R15        DROP ADDRESSABILITY
         EJECT
***********************************************************************
*                                                                     *
*        $IOAPPEN - INPUT/OUTPUT APPENDAGE VECTOR TABLE               *
*                                                                     *
***********************************************************************
         SPACE 3
$IOAPPEN DC    A(ERETURN)          END OF EXTENT
         DC    A(ERETURN)          SIO / PGFIX                       R4
         DC    A(EPCI)             PCI                               R4
         DC    A(ECHANEND)         CHANNEL END
         DC    A(EABCHEND)         ABNORMAL CHANNEL END
         SPACE 3                                                     R4
ERETURN  BR    R14                 RETURN TO IOS                     R4
         SPACE 5                                                     R4
***********************************************************************
*                                                                     *
*        HASP INPUT/OUTPUT SUPERVISOR APPENDAGES                      *
*                                                                     *
* INPUT  R1    = ADDRESS OF RQE                                       *
*        R2    = ADDRSSS OF IOB                                       *
*        R3    = ADDRSSS OF DEB                                       *
*        R4    = ADDRESS OF DCB                                       *
*        R7    = ADDRESS OF UCB                                       *
*        R13   = ADDRESS OF 16 FULLWORD SAVE AREA                     *
*        R14   = RETURN ADDRESS                                       *
*        R15   = ENTRY ADDRESS                                        *
*                                                                     *
***********************************************************************
         SPACE 2                                                     R4
         USING BUFDSECT,R2         PROVIDE IOB/BUFFER ADDRESSABILITY R4
         USING DCBDSECT,R4         PROVIDE DCB ADDRESSABILITY        R4
         USING UCBDSECT,R7         PROVIDE UCB ADDRESSABILITY        R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        ***  ABNORMAL CHANNEL END APPENDAGE  ***                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING EABCHEND,R15        PROVIDE LOCAL ADDRESSABILITY      R4
         SPACE 1                                                     R4
EABCHEND TM    IOBFLAG1,X'01'      SHOULD WE BYPASS APPENDAGE...     R4
         BNZR  R14                 RETURN IF YES                     R4
         TM    IOBSENS0,X'40'      INTERVENTION REQ...         @OZ29138
         BNO   EABCHRET            BR IF NO                    @OZ29138
         CLC   UCBTBYT3(2),EUCB1403  IS THIS A 1403...         @OZ29138
         BE    EINTREQ               BR IF YES                 @OZ29138
         CLC   UCBTBYT3(2),EUCB3211  IS THIS A 3211...         @OZ29138
         BE    EINTREQ               BR IF YES                 @OZ29138
         CLC   UCBTBYT3(2),EUCB3800  IS THIS A 3800...         @OZ29138
         BNE   EABCHRET              BR IF NO                  @OZ29138
EINTREQ  L     R12,28(,R1)         GET ADDRESS OF SRB FROM RQE @OZ29138
         USING SRBDSECT,R12        ESTAB. SRB ADDRESSABILITY   @OZ29138
         L     R12,SRBPARM         GET ADDRESS OF IOSB FROM SRB@OZ29138
         USING IOSB,R12            ESTAB. IOSB ADDRESSABILITY  @OZ29138
         TM    IOSFLA,IOSERR       IS THIS THE FIRST ENTRY...  @OZ29138
         BNZ   EABCHRET            BR IF NO, CSW ALREADY STORED@OZ29138
         L     R12,BUFDCT          GET DCT POINTER             @OZ29138
         USING DCTDSECT,R12        ESTAB. DCT ADDRESSABILITY   @OZ29138
         CLC   DCTCSW,=F'0'        2ND INTERVENTION REQUIRED...@OZ29138
         BNE   EABCHRET            BR IF YES                   @OZ29138
         MVC   DCTCSW+1(3),IOBCSW  SAVE CSW IN DCT             @OZ29138
         DROP  R12                                             @OZ29138
EABCHRET DS    0H                                              @OZ29138
         TM    IOBECBCC,X'20'      TEST COMPLETION CODE
         BCR   NZ,R14              RETURN IF NO ERROR INDICATED
         MVI   IOBFLAG2,0          RESET IOBFLAG2
         XC    IOBERRCT,IOBERRCT    IOBERRCT
         NI    DCBIFLGS,X'3F'        AND DCBIFLGS
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        HANDLE 3800 DATA-LOST CONDITION (PAPER JAM)                  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING DCTDSECT,R13        PROVIDE DCT                       R4
         L     R13,BUFDCT           ADDRESSABILITY                   R4
         SPACE 1                                                     R4
         CLC   UCBTBYT3(2),EUCB3800  TEST FOR 3800 PRINTER           R4
         BNE   ETST3211            BR IF NOT                         R4
         SPACE 1                                                     R4
         LA    R11,X'10'           3800 CANCEL KEY STATUS BIT        R4
         L     R12,28(,R1)         ADDR OF SRB FROM RQE              R4
         USING SRBDSECT,R12        ESTABLISH SRB ADDRESSABILITY      R4
         L     R12,SRBPARM         ADDR OF IOSB                      R4
         USING IOSB,R12            ESTABLISH IOSB ADDRESSABILITY     R4
         L     R12,IOSERP          ADDR OF ERP WORK AREA             R4
         USING EWADSECT,R12        ESTABLISH EWA ADDRESSABILITY      R4
         SPACE 1                                                     R4
         TM    EWASNS+3,X'08'      TEST FOR SYS RESTART BIT          R4
         BZ    ECANKEY             BR IF NOT                         R4
         OI    DCTPPSW2,DCTNIJAM   INDICATE DATA LOST CONDITION      R4
         MVC   DCTJAMCT,EWASNS+20  OBTAIN PAGES-LOST COUNT           R4
         OI    EWAFLG3,EWAJAM      BYPASS INERVENTION REQ MSG        R4
         B     ECANKEY             GO TEST FOR CANCEL KEY            R4
         SPACE 1                                                     R4
ETST3211 B     EABCONT             *** NOP FOR 3211 CANCEL SUPPORT ***
         CLC   UCBTBYT3(2),EUCB3211  TEST FOR 3211 PRINTER           R4
         BNE   EABCONT             BR IF NOT 3211 OR 3800 PRINTER    R4
         LA    R11,X'02'           3211 CANCEL KEY STATUS BIT        R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        IF 3211/3800 CANCEL KEY -- SIMULATE FWD-SPACE DATA SET       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
ECANKEY  TM    IOBSENS0,X'10'      UNIT EXCEPTION...                 R4
         BZ    EABCONT             BR IF NO                          R4
         EX    R11,ECANTEST        CANCEL KEY SENSE BIT...           R4
         BZ    EABCONT             BR IF NO                          R4
         SPACE 1                                                     R4
         OI    DCTFLAGS,DCTBKSP          SIMULATE                    R4
         L     R12,DCTPCE                 FORWARD                    R4
         L     R11,EALLFFS                 SPACE                     R4
         ST    R11,PDDBSKIP-PCEDSECT(,R12)  DATA SET                 R4
         B     EABCONT             CONTINUE                          R4
         SPACE 2                                                     R4
         DROP  R12,R13             SUSPEND EWA/DCT ADDRESSABILITY    R4
         SPACE 2                                                     R4
EALLFFS  DC    F'-1'               FWD-SPACE DSET CONSTANT           R4
ECANTEST TM    IOBSENS1,*-*        *** EXECUTE ONLY ***              R4
         SPACE 1                                                     R4
EUCB3800 DC    AL1(UCB3UREC,UCB3800)  3800 PRINTER UCB TYPE          R4
EUCB3211 DC    AL1(UCB3UREC,UCB3211)  3211 PRINTER UCB TYPE          R4
EUCB1403 DC    AL1(UCB3UREC,UCB1403)  1403 PRINTER UCB TYPE    @OZ29138
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        ***  PROGRAM CONTROLLED INTERRUPT APPENDAGE  ***             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING EPCI,R15            ESTABLISH LOCAL ADDRESSABILITY    R4
         USING PCIDSECT,R13        ESTABLISH PCIE ADDRESSABILITY     R4
         SPACE 1                                                     R4
EPCI     STM   R0,R15,0(R13)       SAVE REGISTERS                    R4
         LR    R9,R13              MOVE SAVE AREA ADDR TO R9         R4
         L     R13,PPBPCIE         LOCATE ACTIVE PCIE                R4
         L     R5,IOBCSW-1         COMPUTE ADDRESS             @OZ29106
         SH    R5,EBACKUP8           OF LAST CCW               @OZ29106
         SLR   R5,R2               COMPUTE OFFSET INTO IOB     @OZ29106
         SRL   R5,3                DIVIDE BY 8                 @OZ29106
         CH    R5,PPBDISPL         PCI OCCURRED IN HIGH HALF.  @OZ29106
         BNL   EPCIHIGH            YES, BRANCH                 @OZ29106
         CLI   PPBFLAG1,C'L'           LOW, WAS LAST LOW...    @OZ29106
         MVI   PPBFLAG1,C'L'       SET LOW                     @OZ29106
         BE    EPCIRET             LAST WAS LOW, RETURN        @OZ29106
         B     EPCIDONE            CONTINUE                    @OZ29106
EPCIHIGH CLI   PPBFLAG1,C'H'       HIGH, WAS LAST HIGH...      @OZ29106
         MVI   PPBFLAG1,C'H'       SET HIGH                    @OZ29106
         BE    EPCIRET             LAST WAS HIGH, RETURN       @OZ29106
EPCIDONE DS    0H                  CONTINUE                    @OZ29106
         TM    PCISGNAL,PCIBUSY    TEST FOR PREVIOUS ENTRY           R4
         BZ    EPCIRET             RETURN IF YES                     R4
         NI    PCISGNAL,255-PCIBUSY  ELSE, SHOW PCIE NO LONGER BUSY  R4
         STCM  R2,7,PCIBUFAD       SET IOB ADDR IN PCIE FOR $ASYNC   R4
         TM    PCI1FLGS,X'40'      TEST IF NEXT CCW AREA IS READY    R4
         BZ    EPCIASYN            BRANCH IF COMMAND-CHAIN NOT ON    R4
         L     R12,PPBCCWNX        ELSE, UPDATE IOBSTART             R4
         ST    R12,IOBSTART         TO NEW CCW AREA, AND             R4
         NI    4(R12),255-X'08'      RESET PCI BIT IN CASE OF RETRY  R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        ALERT MISSING INTERRUPT HANDLER (MIH) OF CHANNEL PROGRESS    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
EPCIASYN NI    UCBFLC,255-UCBTICBT SHOW CHANNEL PGM PROGRESS TO MIH  R4
         L     BASE1,EASYBASE      PROVIDE HCT ADDRESSABILITY        R4
         L     R12,$ASYPCIQ        PICK UP OLD PCIE QUEUE CHAIN      R4
         SPACE 1                                                     R4
EPCINOK  STCM  R12,7,PCICHAIN      PUT PREVIOUS ON BEHIND            R4
         CS    R12,R13,$ASYPCIQ    QUEUE IT                          R4
         BNE   EPCINOK             TRY AGAIN UNTIL QUEUED            R4
         SPACE 1                                                     R4
         L     R13,$HASCB          SET ASCB ADDRESS                  R4
         MVI   $ASYNCP,X'FF'       SET ASYNC POST INDICATION         R4
         L     R10,EPOSTM          GET POST COMPLETION CODE          R4
         L     R11,$HASPECB        GET ADDRESS  OF HASP ECB          R4
         L     R12,0(,R11)         GET CONTENTS OF HASP ECB          R4
         MVI   $SVPOSTW(R11),X'FF' $$POST HASP DISPATCHER            R4
         SPACE 1                                                     R4
EPOSTEST LTR   R12,R12             TEST HASP ECB                     R4
         BM    EPCIPOST            BR IF HASP WAITING                R4
         CS    R12,R10,0(R11)      TRY QUICK POST                    R4
         BNE   EPOSTEST            BR IF POST NOT SUCCESSFUL         R4
         B     EPCIRET             GO RETURN                         R4
         SPACE 1                                                     R4
EPCIPOST ICM   R11,B'1000',EPCIX80 SET HI-BIT FOR XMPOST             R4
         L     R10,EPOSTM          SET POST CODE                     R4
         L     R12,CVTPTR          POINT TO BR 14                    R4
         LA    R12,CVTBRET-CVTDSECT(,R12)  IN CVT                    R4
         ICM   R12,B'1000',EPCIX80 SET HI-BIT FOR XMPOST             R4
         L     R15,CVTPTR          GET ADDRESS OF                    R4
         L     R15,CVT0PT01-CVT(,R15)  POST ROUTINE FROM CVT         R4
         BALR  R14,R15             GO POST                           R4
         SPACE 1                                                     R4
EPCIRET  LM    R0,R15,0(R9)        RESTORE REGISTERS                 R4
         BR    R14                  AND RETURN TO IOS                R4
         SPACE 1                                                     R4
EPCIX80  DC    X'80'               MASK FOR HI-ORDER BYTE            R4
         SPACE 1                                                     R4
         DROP  R13,R15             KILL PCIE, LOCAL ADDRESSABILITY   R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        ***  NORMAL CHANNEL END APPENDAGE  ***                       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING ECHANEND,R15        PROVIDE LOCAL ADDRESSABILITY      R4
ECHANEND TM    IOBFLAG1,X'04'+X'01' IS ERROR RTN IN CONTROL OR       R4C
                                     IS BYPASS APPENDAGE SW ON...    R4
         BCR   NZ,R14              RETURN IF YES
         SPACE 1                                                     R4
         L     R13,BUFDCT          ADDR OF DCT                       R4
         TM    DCTDEVTP-DCTDSECT(R13),DCTPRPU  TEST FOR PRINT/PUNCH  R4
         BZ    EABCONT             BR IF NOT                         R4
         L     R12,IOBCSW-1        ADDR OF ACTUAL PCIE NOP+8         R4
         SH    R12,EBACKUP8        ADDR OF ACTUAL PCIE               R4
         L     R13,PPBPCIE         ADDR OF EXPECTED PCIE             R4
         L     R10,PCI1FLGS-PCIDSECT(,R12) GET CCW FLAGS ETC   @OZ20015
ECCTSTLP L     R11,ECCBIT          ISOLATE COMMAND CHAIN       @OZ20015
         NR    R11,R10                FLAG IN R11              @OZ20015
         BNZ   EREDRIVE                  AND REDRIVE IF SET    @OZ20015
         LR    R11,R10             COPY REGISTER               @OZ20015
         N     R11,ENOTBUSY        RESET BUSY FLAG             @OZ20015
         CS    R10,R11,PCI1FLGS-PCIDSECT(R12) CHK IF CHANGED   @OZ20015
         BNE   ECCTSTLP            IT CHANGED TEST AGAIN       @OZ20015
         NI    PCISGNAL-PCIDSECT(R13),255-PCIBUSY  SHOW NOT BUSY     R4
EABCONT  BALR  R15,0               ESTABLISH
         USING *,R15                NEW LOCAL ADDRESSABILITY
         L     BASE1,EASYBASE      SET UP STANDARD BASE1
         L     R10,$SSVT           POINT TO SSVT
         USING SSVT,R10
         LA    R2,0(0,R2)          CLEAR HIGH ORDER BYTE OF BUFREG   R4
         TM    BUFTYPE,BUFTP       CHECK BUFFER TYPE                 R4
         BO    ECHENDTP            BRANCH IF TP BUFFER               R4
         L     R12,$ASYNCQ         PICK UP OLD CHAIN
ENOK     ST    R12,BUFCHAIN        PUT PREVIOUS BUFFERS ON BEHIND
         CS    R12,R2,$ASYNCQ      QUEUE IT
         BNE   ENOK                IF NOT QUEUED TRY AGAIN
         MVI   $ASYNCP,X'FF'       SET ASYNC POST INDICATION         R4
         B     EPOSTJES            BRANCH TO POST JES                R4
ECHENDTP L     R12,$RJECHEQ        PICK UP OLD CHAIN                 R4
         ST    R12,BUFCHAIN        PUT PREVIOUS BUFFER ON BEHIND     R4
         CS    R12,R2,$RJECHEQ     ATTEMPT TO QUEUE IT               R4
         BNE   ECHENDTP+4          RETRY IF UNSUCCESSFUL             R4
         MVI   $SVMLLM,X'FF'       SHOW LINE MANAGER POST REQ        R4
EPOSTJES L     R13,EPOSTM          PICK UP POST MASK                 R4
         L     R10,$HASPECB        GET ADDRESS  OF HASP ECB          R4
         L     R12,0(,R10)         GET CONTENTS OF HASP ECB          R4
         MVI   $SVPOSTW(R10),X'FF' $$POST HASP DISPATCHER            R4
EPNOK    LTR   R12,R12             TEST FOR WAITING
         BCR   M,R14               LET IOS POST IF WAITING
         CS    R12,R13,0(R10)      TRY QUICK POST                    R4
         BNE   EPNOK               IF NOT SAME TRY AGAIN
         B     4(0,R14)            TELL IOS TO SKIP POST
         SPACE 2                                               @OZ20015
EREDRIVE DS    0H    RESTART CHAINED NOP **BASE NOT NEEDED**   @OZ20015
         LA    R12,0(,R12)         ZERO BYTE THE FIRST         @OZ20015
         ST    R12,IOBSTART        TELL IOS TO START HERE      @OZ20015
         MVI   IOBFLAG1,X'42'      UNRELATED+CHAINING          @OZ20015
         MVI   IOBFLAG2,0          CLEAR IOB FIELDS            @OZ20015
         MVI   IOBSENS0,0             IN ORDER TO              @OZ20015
         MVI   IOBSENS1,0                REDRIVE I/O           @OZ20015
         XR    R12,R12             CLEAR TWO REGS              @OZ20015
         LR    R13,R12               TO ZERO                   @OZ20015
         STM   R12,R13,IOBCSW-1    RESET IOB CSW               @OZ20015
         B     8(,R14)             GO RESTART THE I/O          @OZ20015
         SPACE 1                                                     R4
EPOSTM   DC    A(X'40000000')      POST MASK FOR QUICK POSTS
ECCBIT   EQU   EPOSTM              COMMAND CHAINING BIT        @OZ20015
ENOTBUSY DC    AL1(255,255,255,255-PCIBUSY) BUSY FLAG OFF MASK @OZ20015
EASYBASE DC    A(HASP)             STANDARD BASE1 ADDRESS
EBACKUP8 DC    H'8'                USED IN CSW ADJUSTMENT            R4
         SPACE 1                                                     R4
         DROP  R2,R4,R7,R10,R15
         TITLE 'HASP ASYNCHRONOUS INPUT/OUTPUT PROCESSOR'
***********************************************************************
*                                                                     *
*        $ASYNC - ASYNCHRONOUS INPUT/OUTPUT PROCESSOR                 *
*                                                                     *
* FUNCTION                                                            *
*                                                                     *
*    THE PRIMARY FUNCTION OF THIS PROCESSOR IS TO SYNCHRONIZE         *
*    EVENTS DUE TO CHANNEL ENDS FOR I/O REQUESTED BY PROCESSORS       *
*    WITHIN THE MAIN HASP TASK. SECONDARY FUNCTIONS INCLUDE           *
*    FREEING OF UNUSED COMMON STORAGE AREA CELLS IN A MANNER SO AS    *
*    TO GRADUALLY REDUCE THE UNUSED STORAGE COMMITMENT, GET NEW       *
*    STORAGE CELLS ON DEMAND, AND REQUEUE CMBS QUEUED TO THE          *
*    $DOMQUEA TO THE $DOMQUE FOR THE HASP COMMUNICATIONS DAUGHTER     *
*    TASK.                                                            *
*                                                                     *
* INPUT REGISTERS                                                     *
*                                                                     *
*        R11   = HCT ADDRESS (BASE1)                                  *
*        R12   = BASE ADDRESS (BASE2)                                 *
*        R13   = PCE ADDRESS (SAVE)                                   *
*                                                                     *
***********************************************************************
         SPACE 3                                                     R4
         USING PCEDSECT,SAVE       PROVIDE PCE ADDRESSABILITY        R4
         USING SSVT,R10            PROVIDE SSVT ADDRESSABILITY       R4
         USING CCEDSECT,R7         PROVIDE CCE ADDRESSABILITY        R4
         USING CCADSECT,BASE3      PROVIDE CCA ADDRESSABILITY        R4
         SPACE 3                                                     R4
$ASYNC  $ENTRY BASE=BASE2          PROVIDE PROCESSOR ENTRY POINT     R4
         SPACE 1                                                     R4
         L     R10,$SSVT           POINT TO SSVT                     R4
         L     BASE3,$CCAREA       POINT TO CCA                      R4
         EJECT
***********************************************************************
*                                                                     *
*        FREE UNUSED COMMON STORAGE AREA CELLS                        *
*                                                                     *
* REGISTERS                                                           *
*                                                                     *
*        R0    = WORK, PRIMARY CLAIM CODE                             *
*        R1    = WORK                                                 *
*        R2    = PRIMARY CLAIM CODE                                   *
*        R3    = SECONDARY CLAIM CODE, WORK                           *
*        R5    = LOCAL LINKAGE                                        *
*        R6    = WORK, CCE ADDRESS                                    *
*        R7    = CCE ADDRESS                                          *
*        R8    = CCA ADDRESS                                          *
*        R10   = SSVT ADDRESS                                         *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
ALOOP    L     R0,$SVASYNC         GET ASYNC POST ELEMENT      @OZ19496
         LR    R1,R0               SET REGISTER FOR SWAP       @OZ19496
         LA    R1,0(,R1)           CLEAR HIGH ORDER BYTE       @OZ19496
HFRESET  CS    R0,R1,$SVASYNC      RESET POST FLAG             @OZ19496
         BNE   HFRESET             LOOP UNTILL RESET           @OZ19496
         SPACE 1                                               @OZ19496
         STCK  CCACURT             STORE CLOCK
         BZ    HFCCKT              CHECK TIME
         SPACE 1                                                     R4
HFCVAR   CLI   $SVVARF,X'0'        ANY VARIABLE FREE
         BE    HFNCELL             EXIT IF NOT
         SPACE 1                                                     R4
* VARIABLE FREE REQUIRED
         SPACE 1                                                     R4
         LA    R7,$SVCELLS-(CCECCE-CCEDSECT) POINT TO HEADER
         SPACE 1                                                     R4
HFCELL   L     R7,CCECCE           POINT TO NEXT
         LTR   R7,R7               CHECK FOR END
         BZ    HFNCELL             EXIT IF END
         CLI   CCECSIZ,(512-1)/256 CHECK FOR IN RANGE
         BNH   HFCELL              LOOP
         SPACE 1                                                     R4
* WITHIN RANGE
         SPACE 1                                                     R4
HFCVARGO MVI   $SVVARF,X'0'        FORCE FLAG ZERO
         SPACE 1                                                     R4
HFCELVN  LM    R2,R3,CCESJB+CCETCB-CCETCB PICK UP ALLOCATION CELLS
         LTR   R2,R2               CLAIMED
         BZ    HFCELC              EXIT IF FREEABLE
         SPACE 1                                                     R4
HFCELVC  L     R7,CCECCE           POINT TO NEXT
         LTR   R7,R7               CHECK FOR END
         BNZ   HFCELVN             LOOP
         B     HFNCELL             EXIT ON END
         SPACE 1                                                     R4
HFCELC   BAL   R5,HFCFREE          FREE CELL
         B     HFCELVC             CONTINUE
         SPACE 1                                                     R4
HFCCE    SLR   R6,R6               SET NOT FOUND
         EJECT                                                       R4
HFCCEL   L     R7,CCECCE           POINT TO NEXT CCE
         LTR   R7,R7               CHECK FOR END
         BZ    HFCCELH             EXIT IF END
         CLM   R0,1,CCECSIZ        CHECK CELL SIZE
         BH    HFCCEL              LOOP
         BL    HFCCELH             EXIT IF CCE HIGH
         LM    R2,R3,CCESJB+CCETCB-CCETCB PICK UP ALLOCATION WORDS
         LTR   R2,R2               TEST FOR FREE
         BNZ   HFCCEL              LOOP
         LR    R6,R7               COPY LOCATION
         B     HFCCEL              LOOP
         SPACE 1                                                     R4
HFCCELH  LTR   R6,R6               DID WE GET ANY
         BZR   R5                  EXIT
         LR    R7,R6               POINT TO ONE TO FREE
         SLR   R2,R2               ZERO SJB POINTER
         SPACE 1
* CLAIM AND FREE CELL SUBROUTINE
         SPACE 1
HFCFREE  LA    R0,1                SET CLAIM CODE
         L     R3,CCETCB           PICK UP TCB FIELD
         CDS   R2,R0,CCESJB        CLAIM
         BNZR  R5                  RETURN IF NOT CLAIMED
         SLR   R3,R3               ZERO WORK
         IC    R3,CCECSIZ          PICK UP SIZE INDICATOR
         LA    R0,1(0,R3)          (B-1)/256+1=SIZE IN 256B BLOCKS
         SLA   R0,8                SIZE OF BLOCK
         ICM   R0,8,=AL1(231)      SET SUBPOOL
         ICM   R1,7,CCECLOC        PICK UP LOCATION
         FREEMAIN R,LV=(0),A=(1)   FREE STORAGE
         L     R2,=X'FF000000'     GET FREE FLAG
         ST    R2,CCESJB           FREE IT FOR A GET MAIN TASK
         BR    R5                  RETURN
         SPACE 1                                                     R4
HFCCKT   CLC   CCAPROJT,CCACURT    CHECK HIGH PART EXPIRED
         BH    HFCVAR              CHECK FOR VARIABLE FREE
         L     R0,CCACURT          PICK UP HIGH PART
         AL    R0,CCADELT          SET NEXT FREE TIME
         ST    R0,CCAPROJT         INTO PROJECTED
         LA    R7,$SVCELLS-(CCECCE-CCEDSECT) POINT TO QUEUE HEAD
         LA    R0,(256-1)/256      SET SIZE ID
         BAL   R5,HFCCE            CHECK CCES
         LTR   R7,R7               TEST FOR END
         BZ    HFNCELL             EXIT IF YES
         LA    R0,(512-1)/256      SET SIZE ID
         BAL   R5,HFCCE            CHECK CCES
         LTR   R7,R7               TEST FOR END
         BZ    HFNCELL             EXIT IF YES
         CLI   $SVVARF,X'0'        ARE THERE ANY VARIABLE TO FREE
         BNE   HFCVARGO            DO THEM
         SPACE 1                                                     R4
HFNCELL  DS    0H
         EJECT
***********************************************************************
*                                                                     *
*        GET CELLS FOR WAITING USERS                                  *
*                                                                     *
* REGISTERS                                                           *
*                                                                     *
*        R0    = PRIMARY CLAIM CODE, WORK                             *
*        R1    = WORK                                                 *
*        R2    = PRIMARY CLAIM CODE, WORK                             *
*        R4    = CELL SIZE INDICATOR                                  *
*        R8    = CCA ADDRESS                                          *
*        R10   = SSVT ADDRESS                                         *
*        R11   = HCT/SSVT ADDRESS                                     *
*        R15   = WORK,ENTRY ADDRESS TO SUBROUTINES                    *
*        R14   = LINKAGE                                              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CLI   $SVCPOST,X'FF'      DOES SOME ONE WANT CELL
         BE    HCNOCEL             EXIT IF NO CELLS DESIRED
         $GETLOK ,                 LOCK
         MVC   CCAPOST(CCACPL),$SVCPOST COPY REQUEST
         MVI   $SVCPOST,X'FF'      SET SWITCH FOR NEXT TIME
         $FRELOK ,                 UNLOCK
         L     R4,CCACPREQ         GET SIZE INFORMATION
         LR    R11,R10             SET SSVT BASE
         L     R15,$SVGCMNS        POINT TO GET MAIN FOR CELLS
         LA    R0,1                SET CLAIM CODE
         BALR  R14,R15             LINK TO GET MAIN
         L     BASE1,=A(HASP)      RESTORE BASE1                     R4
         LTR   R1,R1               DID WE GET IT
         LA    R0,1                SET ABNORMAL POST CODE
         BZ    HCPOST              IF NOT POST ABNORMAL
         SLR   R2,R2               FREE CELL INDICATOR
         ST    R2,CCESJB-CCEDSECT(,R7) FREE THE CELL
         SLR   R0,R0               ZERO POST CODE
         SPACE 1                                                     R4
HCPOST   POST  ,(0),MF=(E,CCAPOST) POST USER
         DROP  R7
         DROP  R10
         SPACE 1                                                     R4
HCNOCEL  DS    0H
         EJECT
***********************************************************************
*                                                                     *
*        REQUEUE CMBS FROM $DOMQUEA TO $DOMQUE                        *
*                                                                     *
* REGISTERS                                                           *
*                                                                     *
*        R0    = WORK                                                 *
*        R1    = WORK, CMB ADDRESS                                    *
*        R2    = CMB ADDRESS                                          *
*        R4    = DOMID ( WHEN $DOM HAS ALREADY BEEN ISSUED )          *
*        R8    = CCA ADDRESS                                          *
*        R14   = LINKAGE                                              *
*        R15   = WORK, CMB ADDRESS                                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING CMBDSECT,R2
         SPACE 1                                                     R4
HWNXTCMB L     R2,$DOMQUEA         PICK UP DOM QUEUE (ACTIVE)
         SPACE 1                                                     R4
HWL      LTR   R2,R2               TEST FOR EMPTY
         BZ    HWNOCMB             EXIT IF EMPTY
         L     R1,CMBCMB           POINT TO NEXT                     R4
         CS    R2,R1,$DOMQUEA      REMOVE FIRST IN CHAIN
         BNZ   HWL                 LOOP IF NOT QUEUED
         TM    CMBLEVEL,$DOMACT    IS ACTION STILL ON                R4
         BZ    HWFCMB              IF NOT DO FREE
         LA    R1,$DOMQUE-(CMBCMB-CMB) POINT TO QUEUE HEAD           R4
         SPACE 1                                                     R4
HWLCMB   LR    R15,R1              SAVE PREVIOUS
         ICM   R1,7,CMBCMB+1-CMB(R1)  POINT TO NEXT CMB              R4
         BZ    HWQCMB              IF END QUEUE CMB
         CLC   CMBDOMID,CMBDOMID-CMB(R1) CHECK FOR ORDER             R4
         BH    HWLCMB              LOOP
         SPACE 1                                                     R4
HWQCMB   ST    R1,CMBCMB           PUT HIGH ON END                   R4
         ST    R2,CMBCMB-CMB(,R15) PUT CURRENT ON                    R4
         B     HWNXTCMB            LOOP
         SPACE 1                                                     R4
HWFCMB   ICM   R4,15,CMBDOMID      PICK UP DOMID                     R4
         $FRECMB CMB=(R2)          FREE CMB
         LR    R1,R4               SET DOM ID
         DOM   MSG=(1)             DELETE MESSAGE
         B     HWNXTCMB            LOOP
         SPACE 1                                                     R4
HWNOCMB  DS    0H
         SPACE 1                                                     R4
         DROP  R2
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        HANDLE PROGRAM CONTROLLED INTERRUPTS                         *
*                                                                     *
* REGISTERS                                                           *
*                                                                     *
*        R0    = WORK                                                 *
*        R1    = PCIE ADDRESS                                         *
*              = BUFFER ADDRESS                                       *
*        R2    = WORK                                                 *
*        R3    = WORK                                                 *
*        R8    = CCA ADDRESS                                          *
*        R10   = SSVT ADDRESS                                         *
*        R11   = HCT ADDRESS                                          *
*        R12   = $ASYNC PROCESSOR BASE (BASE2)                        *
*        R15   = WORK                                                 *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         L     R1,$ASYPCIQ         GET PCIE QUEUE ENTRY              R4
         LTR   R1,R1               TEST QUEUE HEAD                   R4
         BZ    APCIEOQ             BR IF END OF QUEUE                R4
         SPACE 1                                                     R4
         USING PCIDSECT,R1         R1 = QUEUED PCIE                  R4
         SPACE 1                                                     R4
APCINOK  L     WA,PCICHAIN-1       POINT TO NEXT PCIE                R4
         LA    WA,0(,WA)           CLEAR HI-ORDER BYTE               R4
         CS    R1,WA,$ASYPCIQ      ATTEMPT TO DE-QUEUE THE PCIE      R4
         BNZ   APCINOK             LOOP UNTIL SUCCESSFUL             R4
         XC    PCICHAIN(3),PCICHAIN  CLEAR PCICHAIN FIELD      @OZ31088
         L     R1,PCIBUFAD-1       PICK UP BUFFER ADDRESS FROM PCIE  R4
         SPACE 1                                                     R4
         USING BUFDSECT,R1         R1 = QUEUED BUFFER                R4
         SPACE 1                                                     R4
         L     WA,BUFEWF           GET USER EWF                      R4
        $TRACE                                                       R4
        $POST  (WA),IO             POST FOR I/O EVENT                R4
         B     ALOOP               LOOP                              R4
         SPACE 1                                                     R4
         DROP  R1                  SUSPEND BUFFER ADDRESSABILITY     R4
         SPACE 1                                                     R4
APCIEOQ  DS    0H                                                    R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        HANDLE CHANNEL END COMPLETIONS                               *
*                                                                     *
* REGISTERS                                                           *
*                                                                     *
*        R0    = WORK                                                 *
*        R1    = BUFFER ADDRESS                                       *
*        R2    = WORK                                                 *
*        R3    = WORK                                                 *
*        R8    = CCA ADDRESS                                          *
*        R10   = SSVT ADDRESS                                         *
*        R12   = PROCESSOR BASE (BASE2)                               *
*              = SYNCHRONIZED PROCESSOR APPENDAGE BASE (BASE2)        *
*        R15   = WORK                                                 *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         L     R1,$ASYNCQ          GET QUEUE ENTRY
         LTR   R1,R1               ANYTHING IN QUEUE
         BZ    AEOQ                BR IF NO
         SPACE 1                                                     R4
         USING BUFDSECT,R1         PROVIDE BUFFER ADDRESSABILITY     R4
         SPACE 1                                                     R4
ANOK     L     WA,BUFCHAIN         POINT TO NEXT BUFFER
         CS    R1,WA,$ASYNCQ       PULL BUFFER OFF THE QUEUE
         BNZ   ANOK                LOOP IF NOT OFF
         LH    WA,$EXCPCT          DECREMENT
         S     WA,AFONE             MASTER I/O COUNT
         BM    E01                 ERROR IF NEGATIVE
         STH   WA,$EXCPCT          UPDATE MASTER I/O COUNT
         EJECT                                                       R4
         MVC   BUFECBCC,IOBECBCC   SAVE CC FOR USER                  R4
         L     WA,BUFDCT           WA = DCT ASSOCIATED WITH BUFFER
         USING DCTDSECT,WA         ESTABLISH DCT ADDRESSABILITY
         SR    WB,WB               ZERO WB
         IC    WB,DCTBUFCT         WB = DCT BUFFER COUNT
         S     WB,AFONE            DECREMENT BY ONE
         BM    A01                 COUNT SHOULD NEVER GO MINUS
         STC   WB,DCTBUFCT         SET NEW BUFFER COUNT
         L     WB,DCTPCE           USER PCE ADDRESS
         CLI   DCTDEVTP,PCEDAWR    TEST FOR DIRECT ACCESS DCT
         BH    *+12                BR IF NO
         LA    WB,PCEDADCT-PCEDSECT  GET DCT OFFSET WITHIN PCE
         SLR   WA,WB               GET PCE ADDRESS IN WA
         LR    WB,WA               THEN TRANSFER TO WB
         DROP  WA                  KILL DCT ADDRESSABILITY
         L     WA,BUFEWF           GET USER EWF
         LTR   WA,WA               *
         BM    AENTER              MINUS = ASYNC. EXIT
         BZ    AFREE               ZERO  = NO ACTION
        $TRACE
         $POST (WA),IO             POST FOR I/O COMPLETION
         B     ALOOP               LOOP
         SPACE 1                                                     R4
AENTER   NULL                      REQ. FOR ASYNC. ENTRY
        $TRACE
         DROP  BASE2               TO PREVENT LATER ERRORS
         L     BASE2,PCEBASE2-PCEDSECT(WB)   ESTABLISH USER ADDR
         BALR  R15,WA              ENTER USER
         USING *,R15               ESTABLISH TEMPORARY ADDRESSABILITY
         L     BASE2,AA$ASYNC      RESET BASE2 FOR $ASYNC
         DROP  R15                 DROP TEMPORARY ADDRESSABILITY
         USING $ASYNC,BASE2        RE-ESTABLISH $ASYNC ADDRESSABILITY
         B     $ASYNC              AND GO AGAIN
         EJECT                                                       R4
AFREE    NULL
        $TRACE
         CLI   BUFECBCC,X'7F'      TEST I/O COMPLETION CODE
         BE    AFREE1              BRANCH IF NO ERROR
        $IOERROR (R1)              ERROR DETECTED
         SPACE 1                                                     R4
AFREE1  $FREEBUF (R1)              FREE BUFFER
         B     ALOOP               LOOP
         SPACE 1                                                     R4
AEOQ     NULL                      END OF CHAIN REACHED
        $WAIT  WORK                WAIT
         B     ALOOP               LOOP
         SPACE 1                                                     R4
A01     $ERROR                     NEGATIVE DCT BUFFER COUNT DISCOVERED
         SPACE 1                                                     R4
E01     $ERROR                     NEGATIVE MASTER I/O COJNT
         SPACE 1                                                     R4
AFONE    DC    F'1'                CONSTANT
AA$ASYNC DC    A($ASYNC)           ADDRESSABILITY CONSTANT
         SPACE 1                                                     R4
         DROP  R1                  KILL BUFFER ADDRESSABILITY
         DROP  BASE3               KILL CCA ADDRESSABILITY           R4
         SPACE 1                                                     R4
         LTORG                                                       R4
         TITLE 'HASP $TIMER PROCESSOR'
***********************************************************************
*                                                                     *
* PROCESSOR NAME -- $TIMER                                            *
*                                                                     *
* DESCRIPTIVE NAME -- JES2 INTERVAL TIMER RESET PROCESSOR             *
*                                                                     *
* FUNCTION -- RESET THE INTERVAL TIMER AFTER A TIMER INTERRUPT        *
*                                                                     *
***********************************************************************
         SPACE 5
$TIMER  $ENTRY BASE=BASE2          PROVIDE PROCESSOR ENTRY POINT     R4
         SPACE 1                                                     R4
         L     WC,$SSVT            POINT TO SSVT               @OZ20010
         SPACE 1                                                     R4
TIMERL   MVI   $SVTIMER-SSVT(WC),0 RESET POST FLAGS            @OZ20010
         BAL   WA,IADJUST          POST COMPLETED STQE(S)
         BAL   WA,ISETINT          SET NEXT INTERVAL
        $WAIT  WORK                WAIT FOR NEXT INTERVAL
         B     TIMERL              LOOP                        @OZ20010
         SPACE 2                                               @OZ20010
         DROP  BASE2               DROP PROCESSOR BASE REG     @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
         TITLE 'HASP INTERVAL TIMER MANAGER'                   @OZ20010
***************************************************************@OZ20010
*                                                              @OZ20010
*        $STIMER - SET INTERVAL TIMER                          @OZ20010
*                                                              @OZ20010
*        R0    = WORK, UNPREDICTABLE ON EXIT                   @OZ20010
*        R1    = STQE ADDRESS, UNPREDICTABLE ON EXIT           @OZ20010
*        R11   = HCT ADDRESS (BASE1)                           @OZ20010
*        R14   = RETURN ADDRESS (LINK)                         @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*        R15   = WORK, UNPREDICTABLE ON EXIT                   @OZ20010
*                                                              @OZ20010
***************************************************************@OZ20010
         SPACE 1                                               @OZ20010
         USING $STIMER,WD          PROVIDE LOCAL ADDRESSABILITY@OZ20010
         ENTRY $STIMER             ENTRY POINT FOR $STIMER     @OZ20010
         CNOP  4,8                                             @OZ20010
$STIMER $TRACE                                                 @OZ20010
         ST    LINK,ITSAVE         SAVE                        @OZ20010
         STM   WA,WD,ITSAVE1        REGISTERS                  @OZ20010
         L     WD,$STIMERA         LOAD BASE ADDRESS           @OZ20010
         LA    WC,0(,R1)           SAVE PURIFIED STQE ADDRESS  @OZ20010
         LA    WB,ITCHAIN-ICHAIN   PREPARE TO SCAN STQE CHAIN  @OZ20010
         SPACE 1                                               @OZ20010
INXTTQE  LR    WA,WB               RELOAD STQE ADDRESS         @OZ20010
         L     WB,ICHAIN(,WB)      GET NEXT STQE               @OZ20010
         LTR   WB,WB               END OF STQE CHAIN...        @OZ20010
         BZ    ISETTQE             BR IF YES                   @OZ20010
         CR    WB,WC               IS THIS STQE OURS...        @OZ20010
         BNE   INXTTQE             BR IF NO                    @OZ20010
         MVC   ICHAIN(4,WA),ICHAIN(WB) DEQUEUE OUR STQE        @OZ20010
         SPACE 1                                               @OZ20010
ISETTQE  MVI   IPOST(R1),0         ZERO POST BIT               @OZ20010
         L     WB,ITIME(,R1)       GET REQD TIME IN SECOND     @OZ20010
         LCR   WB,WB               COMPLEMENT AND TEST         @OZ20010
         BP    *+8                 NO CONVERSION IF ORIGINALLY @OZ20010
         MH    WB,=H'-100'         CONVERT TO 100THS OF SECOND @OZ20010
         ST    WB,ITIME(,R1)       AND OVER STORE              @OZ20010
         LR    WC,R1               SAVE USERS TQE ADDRESS      @OZ20010
         BAL   WA,IADJUST          POST ANY EXPIRED STQE'S     @OZ20010
         L     R0,ITIME(,WC)       REQUESTED INTERVAL          @OZ20010
         LA    WA,ITCHAIN-ICHAIN   FAKE 1ST CHAIN              @OZ20010
         SPACE 1                                               @OZ20010
INEXT    L     R1,ICHAIN(,WA)      GET NEXT STQE               @OZ20010
         LTR   R1,R1               IS THIS END                 @OZ20010
         BZ    INSERT              BRANCH IF YES TO INSERT     @OZ20010
         CL    R0,ITIME(,R1)       IS REQUESTED INTERVAL LESS..@OZ20010
         BL    INSERT              THAN THIS... BRANCH IF YES  @OZ20010
         LR    WA,R1               NO... TO NEXT STQE          @OZ20010
         B     INEXT               FOR CHECK                   @OZ20010
         EJECT                                                 @OZ20010
         CNOP  0,8                                             @OZ20010
INSERT   ST    WC,ICHAIN(,WA)      INSERT INTO CHAIN           @OZ20010
         ST    R1,ICHAIN(,WC)      CONTINUE CHAIN              @OZ20010
         BAL   WA,ISETINT          GO START TIMER              @OZ20010
         SPACE 1                                               @OZ20010
IRETURN  L     LINK,ITSAVE         RESTORE                     @OZ20010
         LM    WA,WD,ITSAVE1        REGISTERS                  @OZ20010
         BR    LINK                  AND RETURN                @OZ20010
         EJECT                                                 @OZ20010
***************************************************************@OZ20010
*                                                              @OZ20010
*        $TTIMER - RETURN TIME REMAINING AND OPTIONALLY CANCEL @OZ20010
*                                                              @OZ20010
*        R0    = TIME REMAINING (IN SECONDS) ON EXIT           @OZ20010
*        R1    = STQE ADDRESS (COMPLEMENTED IF CANCEL REQD)    @OZ20010
*        R11   = HCT ADDRESS (BASE1)                           @OZ20010
*        R14   = RETURN ADDRESS (LINK)                         @OZ20010
*        R15   = WORK, UNPREDICTABLE ON EXIT                   @OZ20010
*                                                              @OZ20010
***************************************************************@OZ20010
         SPACE 1                                               @OZ20010
         ENTRY $TTIMER             ENTER POINT FOR $TTIMER     @OZ20010
         CNOP  2,8                                             @OZ20010
$TTIMER $TRACE                                                 @OZ20010
         ST    LINK,ITSAVE         SAVE                        @OZ20010
         STM   WA,WD,ITSAVE1        REGISTERS                  @OZ20010
         L     WD,$STIMERA         LOAD COMMON TIMER BASE ADDR @OZ20010
         LR    WC,R1               SAVE INPUT ADDRESS          @OZ20010
         L     WA,ITCHAIN          START OF CHAIN              @OZ20010
         LTR   R0,WA               IS IT ZERO                  @OZ20010
         BZ    IRETURN             YES... RETURN WITH NO TIME  @OZ20010
         BAL   WA,IADJUST          ADJUST INTERVALS            @OZ20010
         LPR   R1,WC               GET ADDRESS OF STQE         @OZ20010
         LA    WA,ITCHAIN-ICHAIN   FAKE STQE                   @OZ20010
         SPACE 1                                               @OZ20010
INEXT2   L     WB,ICHAIN(,WA)      GET NEXT                    @OZ20010
         LTR   R0,WB               IS THIS END                 @OZ20010
         BZ    IRET1               YES... RESTART AND RETURN   @OZ20010
         CR    WB,R1               NO... IS THIS RQSTED STQE   @OZ20010
         BE    IHERE               BRANCH IF YES               @OZ20010
         LR    WA,WB               UPDATE CHAIN                @OZ20010
         B     INEXT2              AND KEEP LOOKING            @OZ20010
         SPACE 1                                               @OZ20010
         CNOP  0,8                                             @OZ20010
IHERE    L     R0,ITIME(,WB)       GET TIME REMAINING          @OZ20010
         LTR   WC,WC               WAS CANCEL REQUESTED        @OZ20010
         BP    IRET1               BRA IF NO                   @OZ20010
         MVC   ICHAIN(4,WA),ICHAIN(WB)  YES... UNCHAIN         @OZ20010
         SPACE 1                                               @OZ20010
IRET1    LR    WC,R0               SAVE  R0                    @OZ20010
         BAL   WA,ISETINT          GO RESET TIMER              @OZ20010
         SR    WA,WA               CLEAR FOR DIVIDE            @OZ20010
         LR    WB,WC               TIME IN TIMER UNITS         @OZ20010
         LA    WC,100              CONVERSION FACTOR FOR SECS  @OZ20010
         DR    WA,WC               CONVERT                     @OZ20010
         SRL   WC,1                HALVE                       @OZ20010
         CR    WA,WC               CHECK FOR REMAINDER         @OZ20010
         LR    R0,WB               FOR USER                    @OZ20010
         BL    *+8                 BR IF REMAINDER LT 1/2 SEC  @OZ20010
         LA    R0,1(,WB)           OTHERWISE ROUND             @OZ20010
         B     IRETURN             AND EXIT                    @OZ20010
         SPACE 1                                               @OZ20010
         TITLE 'HASP INTERVAL TIMER MANAGER -- SUBROUTINES'    @OZ20010
*                                                              @OZ20010
*        SUBROUTINE TO SET SYSTEM TIMER TO LOWEST INTERVAL     @OZ20010
*                                                              @OZ20010
         SPACE 1                                               @OZ20010
         CNOP  0,8                                             @OZ20010
ISETINT  L     WD,$STIMERA         LOAD COMMON BASE            @OZ20010
         L     WB,ITCHAIN          GET FIRST STQE              @OZ20010
         CL    WB,ISTQE            TEST                        @OZ20010
         BER   WA                  RETURN IF ALREADY ACTIVE    @OZ20010
         ST    WB,ISTQE            SAVE ADDRESS OF ACTIVE STQE @OZ20010
         LTR   WB,WB               TEST AGAIN                  @OZ20010
         BZR   WA                  RETURN IF NO STQE           @OZ20010
         STIMER REAL,ITIMEUP,BINTVL=ITIME(,WB)  SET TIMER      @OZ20010
         BR    WA                  AND RETURN                  @OZ20010
         EJECT                                                 @OZ20010
*                                                              @OZ20010
*        SUBROUTINE TO POST PROCESSORS WHOSE TIME HAS EXPIRED  @OZ20010
*                                                              @OZ20010
         SPACE 1                                               @OZ20010
         CNOP  4,8                                             @OZ20010
IADJUST  L     WD,$STIMERA         RELOAD COMMON BASE          @OZ20010
         LM    R0,R1,ICLOCK        GET PREVIOUS CLOCK VALUE    @OZ20010
         STCK  ICLOCK              STORE CURRENT CLOCK         @OZ20010
         BCR   7,WA                RETURN IF CLOCK INVALID     @OZ20010
         LM    R14,R15,ICLOCK      GET CURRENT CLOCK           @OZ20010
         SLR   R14,R0              SUBTRACT                    @OZ20010
         SLR   R15,R1               PREVIOUS CLOCK             @OZ20010
         BNM   *+6                   TO GET                    @OZ20010
         BCTR  R14,0                  ELAPSED INTERVAL         @OZ20010
         LA    R14,0(,R14)         PREVENT DIVIDE CHECK        @OZ20010
         D     R14,=F'40960000'    CONVERT TO 100THS OF SECOND @OZ20010
         LA    WB,ITCHAIN-ICHAIN   FAKE 1ST STQE               @OZ20010
         SPACE 1                                               @OZ20010
IPOSTIT  L     WB,ICHAIN(,WB)      GET FIRST STQE              @OZ20010
         LTR   WB,WB               TEST                        @OZ20010
         BZR   WA                  RETURN IF DONE              @OZ20010
         L     R1,ITIME(,WB)       GET TIME                    @OZ20010
         SR    R1,R15              COMPUTE TIME INTERVAL       @OZ20010
         ST    R1,ITIME(,WB)       STORE TIME INTERVAL         @OZ20010
         BP    IPOSTIT             LOOP IF TIME NOT EXPIRED    @OZ20010
         SPACE 1                                               @OZ20010
         L     R1,IPOST(,WB)       OTHERWISE GET ADDR OF PCE   @OZ20010
         $POST (R1),WORK           AND POST IT                 @OZ20010
         MVI   IPOST(WB),X'80'     SHOW TIMER POST             @OZ20010
         MVC   ITCHAIN+1(3),ICHAIN+1(WB)     UPDATE STQE CHAIN @OZ20010
         B     IPOSTIT             AND CHECK NEXT...           @OZ20010
         SPACE 1                                               @OZ20010
*                                                              @OZ20010
*                        $STIMER STORAGE AND CONSTANTS         @OZ20010
*                                                              @OZ20010
         SPACE 1                                               @OZ20010
ICLOCK   DC    D'0'                LAST CLOCK VALUE            @OZ20010
ITSAVE   EQU   $CSAVREG,4          LINK REGISTER SAVE AREA     @OZ20010
ITSAVE1  EQU   $CSAVREG+4,16       REGISTERS WA-WD             @OZ20010
ITCHAIN  EQU   $TQEQUE                                         @OZ20010
         SPACE 2                                               @OZ20010
         DROP  WD                  DROP COMMON TIMER RTN BASE  @OZ20010
         TITLE 'HASP ASYNCHRONOUS TIMER INTERRUPT EXIT'        @OZ20010
*                                                              @OZ20010
*                        ASYNCHRONOUS TIMER EXIT ROUTINE       @OZ20010
*                                                              @OZ20010
         SPACE 1                                               @OZ20010
         CNOP  0,8                                             @OZ20010
ITIMEUP  NULL                                                  @OZ20010
         USING *,R15               SYSTEM PROVIDED BASE        @OZ20010
         STM   BASE1,R2,ISAVE      SAVE REGISTERS              @OZ20010
         L     BASE1,=A(HASP)      SWITCH                      @OZ20010
         LR    BASE2,R15            TO                         @OZ20010
         DROP  R15                   STANDARD                  @OZ20010
         USING ITIMEUP,BASE2          ADDRESSABILITY           @OZ20010
         XC    ISTQE,ISTQE         INDICATE NO ACTIVE TQE      @OZ20010
       $$POST   ELMT=$SVTIMER,R11=HCT $$POST TIMER PCE         @OZ20010
         LM    BASE1,R2,ISAVE      RESTORE REGISTERS           @OZ20010
         BR    R14                  AND RETURN                 @OZ20010
         SPACE 1                                               @OZ20010
         DROP  BASE2               DROP ADDRESSABLITY
         SPACE 1
ISAVE    DS    8F                  REGISTERS BASE1-R2          @OZ20010
ISTQE    DC    A(*-*)              ADDRESS OF ACTIVE STQE      @OZ20010
         SPACE 1                                               @OZ20010
         LTORG ,                   GENERATE LITERAL POOL       @OZ20010
   TITLE 'HASP JOB OUTPUT TABLE SERVICES -- JOE BUILD ROUTINE' @OZ20010
***************************************************************@OZ20010
*                                                              @OZ20010
*        $#BLD - CONSTRUCT JOB OUTPUT ELEMENT (JOE)            @OZ20010
*                                                              @OZ20010
* INPUT  R0    - ADDRESS OF PERIPHERAL DATA DEFINITION BLOCK   @OZ20010
*        R1    - ADDRESS OF WORK AREA IN WHICH TO BUILD 2 JOES @OZ20010
*        R11   - HCT ADDRESS (BASE1)                           @OZ20010
*        R13   - PCE ADDRESS                                   @OZ20010
*        R14   - RETURN ADDRESS                                @OZ20010
*        R15   - JQE OFFSET IN BYTES                           @OZ20010
*                                                              @OZ20010
***************************************************************@OZ20010
         SPACE 1                                               @OZ20010
         ENTRY $#BLD               ENTRY FOR $#BLD ROUTINE     @OZ20010
         USING PDBDSECT,WA         PDDB ADDRESSABILITY         @OZ20010
         USING JOEDSECT,R1         JOE ADDRESSABILITY          @OZ20010
         USING $#BLD,R15           $#BLD ADDRESSABILITY        @OZ20010
         SPACE 1                                               @OZ20010
         CNOP  0,8                                             @OZ20010
$#BLD   $TRACE                                                 @OZ20010
         STM   WA,WD,PCEWA         SAVE WORK REGISTERS         @OZ20010
         LR    WA,R0               MAKE PDDB ADDRESSABLE       @OZ20010
         LR    WB,R15              SAVE JQE OFFSET             @OZ20010
         L     R15,$JOEBLD         LOAD $#BLD ADDRESSABILITY        R41
         XC    0(2*JOESIZE,R1),0(R1)  CLEAR JOE WORK AREA            R4
         SRL   WB,2                CONVERT JQE OFFSET TO WORD OFFSET R4
         STH   WB,JOEJQE           STORE JQE WORD OFFSET             R4
         TM    $PRTOPTS,$DMNDSET   IF DEMAND-SETUP                   R4
         BZ    BLD10                OPTION CHOSEN,                  R41
         OI    JOEFLAG2,$JOEDMND     THEN FLAG WORK-JOE              R4
         SPACE 1                                                    R41
BLD10    TM    PDBFLAG2,PDB2TCEL   SET TRACK-CELL'ED                R41
         BZ    BLD20                STATUS IF PDDB WAS              R41
         OI    JOEFLAG2,$JOETCEL     OF A TRACK-CELLED CLASS         R4
         SPACE 1                                                    R41
BLD20    MVC   JOESEC,PDBSEC       SET SECURITY ID                  R41
         IC    R0,PDBCLASS         GET OUTPUT CLASS FROM PDDB        R4
         STC   R0,JOEPDBCL         SET OUTPUT CLASS IN JOE 'CURRENT' R4
         STC   R0,JOECURCL          AND 'ORIGINAL' OUTPUT CLASS      R4
         LH    R0,PDBDEST          GET ROUTE CODE FROM PDDB          R4
         STH   R0,JOEROUT          SET ROUTE CODE IN JOE 'CURRENT'   R4
         STH   R0,JOEDEST           AND 'ORIGINAL' ROUTE FIELDS      R4
         LTR   R0,R0               TEST FOR EXPLICIT ROUTING         R4
         BNZ   BLDCPUID            BR IF YES                         R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        ROUTING NOT SPECIFIED -- USE JOB ROUTING                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         N     WB,=A(X'FFFF')      DEVELOP                           R4
         SLL   WB,2                 FULL JQE                         R4
         AL    WB,$JOBQPTR           ADDRESS                         R4
         L     R0,QUEPRTRT(,WB)    GET PRINT AND PUNCH ROUTE CODES   R4
         SLR   WB,WB               DEVELOP ADDRESS                   R4
         IC    WB,PDBCLASS          OF SCAT ENTRY                    R4
         AL    WB,$SSVT              FOR OUTPUT CLASS                R4
         TM    $SVSCAT-SSVT(WB),SCATPNCH  TEST FOR PUNCH CLASS       R4
         BO    SKIP100             BR IF YES                         R4
         SRL   R0,16                ELSE SHIFT OUT PUNCH ROUTE CODE  R4
SKIP100  STH   R0,JOEROUT          RESET JOE 'CURRENT' ROUTE FIELD   R4
         N     R0,=A(X'FFFF')      TEST 'CURRENT' ROUTE CODE         R4
         BNZ   BLDCPUID            BR IF NON-ZERO                    R4
         MVC   JOEROUT(1),$OWNSYS  SET 'CURRENT' ROUTE TO 'LOCAL'    R4
         SPACE 1                                                     R4
BLDCPUID MVC   JOECPU,PDBCPU       SET CPU ID                        R4
         SLR   R0,R0               CLEAR WORK REGISTER              R41
         SLR   WC,WC               CLEAR COPY-GROUPS SUM            R41
         LA    WD,8                SET MAX COPY-GROUPS              R41
         SPACE 1                                                    R41
BLDLOOP  IC    R0,PDBCOPYG-1(WD)   COMPUTE TOTAL                    R41
         ALR   WC,R0                COPY COUNT OF                   R41
         BCT   WD,BLDLOOP            3800 COPY-GROUPS               R41
         CLM   WC,1,PDBCOPYS       USE NORMAL                       R41
         BNL   BLDRECCT             COPY COUNT                      R41
         IC    WC,PDBCOPYS           IF LARGER                      R41
         SPACE 1                                                    R41
BLDRECCT L     WD,PDBRECCT         SET TOTAL                        R41
         MR    WC,WC                LINES/CARDS                     R41
         ST    WD,JOERECCT           IN WORK-JOE                    R41
         SPACE 1                                                    R41
         LA    R1,JOESIZE(,R1)     POINT TO CHARACTERISTICS JOE      R4
         MVC   JOEFORM(20),PDBFORMS  SET FORMS, FCB, UCS AND WTR IDS R4
         MVC   JOEFLASH,PDBFLASH   SET FLASH FRAME ID                R4
         TM    PDBFLAG2,PDB2BRST   SET                               R4
         BZ    BLDEXIT              BURST                           R41
         OI    JOECFLAG,$JOEBRST     CHARACTERISTIC                  R4
         SPACE 1                                                    R41
BLDEXIT  LM    WA,WD,PCEWA         RESTORE WORK REGISTERS           R41
         BR    LINK                 AND RETURN                       R4
         SPACE 1                                                     R4
         DROP  R1,WA,R15           KILL $#BLD ADDRESSABILITY         R4
         TITLE 'HASP JOB OUTPUT TABLE SERVICES -- JOE ADD ROUTINE'   R4
***********************************************************************
*                                                                     *
*        $#ADD -- ADD JOE TO JOT                                      *
*                                                                     *
* INPUT  R0    - ADDRESS OF PROTOTYPE WORK JOE                        *
*        R1    - ADDRESS OF PROTOTYPE CHARACTERISTICS JOE             *
*        R11   - HCT ADDRESS (BASE1)                                  *
*        R13   - PCE ADDRESS                                          *
*        R14   - RETURN ADDRESS                                       *
*        R15   - ENTRY POINT ADDRESS, UNPREDICTABLE ON EXIT           *
*                                                                     *
* OUTPUT       - CC     ZERO -- $#ADD SUCCESSFUL                      *
*              - CC NON-ZERO -- $#ADD UNSUCCESSFUL                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
JOE      EQU   WE                  JOE ADDRESS REGISTER              R4
JOT      EQU   WG                  JOT ADDRESS REGISTER              R4
         SPACE 1                                                     R4
         ENTRY $#ADD               PROVIDE ENTRY FOR $#ADD ROUTINE   R4
         USING JOEDSECT,JOE        PROVIDE JOE ADDRESSABILITY        R4
         USING JOTDSECT,JOT        PROVIDE JOT ADDRESSABILITY        R4
         USING $#ADD,BASE2         SET JOT SERVICES ADDRESSABILITY   R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
$#ADD    STM   LINK,R12,PCELINK    SAVE CALLER'S REGISTERS           R4
        $QSUSE SAVE=NO             REQUEST ACCESS TO CHECKPOINT DATA R4
         L     JOT,$JOTABLE        LOAD JOB OUTPUT TABLE ADDRESS     R4
         L     BASE2,$JOEADD       LOAD JOT SERVICES ADDRESSABILITY  R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        ENSURE SUFFICIENT JOES TO PROCESS REQUEST                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         LH    R2,JOTFREC          NUMBER OF AVAILABLE JOE'S         R4
         BCTR  R2,R0               JOE FOR A CHAR-JOE                R4
         BCTR  R2,R0               JOE FOR A WORK-JOE                R4
         CH    R2,$MINJOES         BELOW MINIMUM FREE LIMIT...       R4
         BNL   ADDOK               BRANCH IF NO                      R4
         LM    WA,R12,PCEWA        RELOAD CALLER'S REGISTERS         R4
         BR    LINK                RETURN TO CALLER WITH NON-ZERO CC R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        SCAN CHARACTERISTICS QUEUE FOR A MATCH WITH THIS REQUEST     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  4,8                                                   R4
ADDOK    LH    JOE,JOTCHRQ         CHARACTERISTICS QUEUE 1ST DISPL   R4
         SPACE 1                                                     R4
ADCHQSCN N     JOE,=F'65535'       CLEAR LEFT HALFWORD               R4
         BZ    ADBLDC              BRANCH IF END OF QUEUE            R4
         SLL   JOE,2               EXPAND TO BYTE OFFSET             R4
         ALR   JOE,JOT             ADDRESS CHAR-JOE                  R4
         CLC   JOEFORM(JOEACTPR-JOEFORM),JOEFORM-JOEDSECT(R1)        R4
         BNE   ADCHQNXT            BR IF CHAR-JOE'S NOT MATCHING     R4
         TM    JOECFLAG-JOEDSECT(R1),$JOEBRST  VERIFY                R4
         BO    ADCHQBRS                         STACKER              R4
         TM    JOECFLAG,$JOEBRST                 SELECTION           R4
         BO    ADCHQNXT                           MATCHING           R4
         B     ADCHEHIT                            ON                R4
ADCHQBRS TM    JOECFLAG,$JOEBRST                    BOTH             R4
         BO    ADCHEHIT                              CHAR-JOE'S      R4
         SPACE 1                                                     R4
ADCHQNXT LH    JOE,JOENEXT         GET NEXT ELEMENT DISPL            R4
         B     ADCHQSCN            LOOP THROUGH CHAR QUEUE           R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        GET A JOE IN WHICH TO BUILD A NEW CHAR-JOE                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  6,8                                                   R4
ADBLDC   BAL   WF,GETJOE           GET AN AVAILABLE JOE              R4
         LR    JOE,R3              RELOAD JOE ADDRESS                R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        INITIALIZE NEW CHARACTERISTICS JOE WITH CALLER'S DATA        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         SLR   R3,R3               CLEAR REGISTER                    R4
         STH   R3,JOEUSE           CLEAR USE COUNT                   R4
         MVC   JOEFORM(JOEACTPR-JOEFORM),JOEFORM-JOEDSECT(R1)        R4
         MVI   JOECFLAG,0          CLEAR CHARACTERISTICS FLAG        R4
         TM    JOECFLAG-JOEDSECT(R1),$JOEBRST  SET FOR               R4
         BZ    SKIP120                          CONT. FORMS OR       R4
         OI    JOECFLAG,$JOEBRST                 BURSTED FORMS       R4
SKIP120  STH   R3,JOEACTPR         CLEAR ACTIVE DEVICE COUNTERS      R4
         LA    R2,JOTCHRQ-(JOENEXT-JOEDSECT)  PREPARE TO SCAN CHAR Q R4
         SPACE 1                                                     R4
ADCHSCN  LH    R3,JOENEXT-JOEDSECT(,R2)  DISPL OF NEXT CHAR-JOE      R4
         N     R3,=F'65535'        CLEAR LEFT HALFWORD               R4
         BZ    ADCHINS             BRANCH IF END OF QUEUE            R4
         SLL   R3,2                EXPAND TO BYTE OFFSET             R4
         ALR   R3,JOT              ADDRESS OF NEXT CHAR-JOE          R4
         CLC   JOEFORM(JOEACTPR-1-JOEFORM),JOEFORM-JOEDSECT(R3)      R4
         BL    ADCHINS             BRANCH IF SHOULD INSERT HERE      R4
         LR    R2,R3               STEP TO NEXT CHAR-QUEUE ENTRY     R4
         B     ADCHSCN             CONTINUE CHAR-QUEUE SCAN          R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
ADCHINS  LH    R4,JOENEXT-JOEDSECT(,R2)  GET OLD NEXT ENTRY          R4
         STH   R4,JOENEXT          STORE INTO NEW ELEMENT            R4
         LR    R4,JOE              COPY NEW ELEMENT ADDR             R4
         SLR   R4,JOT              OFFSET OF NEW ELEMENT             R4
         SRL   R4,2                REDUCE TO FULLWORD OFFSET         R4
         STH   R4,JOENEXT-JOEDSECT(,R2)  RECONNECT CHAR-JOE CHAIN    R4
         BAL   WF,CKPTJOEA         CKPT PREVIOUS CHAR-JOE            R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        HAVE A CHARACTERISTICS JOE -- CHECKPOINT IT                  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
ADCHEHIT LH    R2,JOEUSE           INCREMENT                         R4
         LA    R2,1(,R2)            CHAR-JOE                         R4
         STH   R2,JOEUSE             USE COUNT                       R4
         LR    R2,JOE              COPY CHAR-JOE ADDRESS             R4
         BAL   WF,CKPTJOEA         CKPT CHAR-JOE                     R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        OBTAIN AND INITIALIZE NEW WORK-JOE                           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         BAL   WF,GETJOE           GET NEXT FREE JOE                 R4
         LR    JOE,R3              RELOAD JOE ADDRESS                R4
         LR    R3,R0               COPY WORK-JOE DATA ADDRESS        R4
         MVC   JOEDSECT(JOESIZE),0(R3)  USER SPECS TO WORK JOE       R4
         L     R14,$SSVT           POINT REG 14 TO PRIORITY         R41
         LA    R14,$SVXPRI-4-SSVT(,R14) CONVERSION TABLE IN SSVT    R41
         LA    R0,10               SET LOOP COUNTER (10 ENTRIES)    R41
         SPACE 1                                                    R41
ADPRILP  LA    R14,4(,R14)         BUMP TABLE POINTER               R41
         L     R15,0(,R14)         PICK UP PRTY/LINECOUNT,          R41
         LA    R15,0(,R15)           DELETE PRTY BYTE FOR COMP      R41
         CL    R15,JOERECCT        SEE IF RIGHT PRTY SLOT,          R41
         BNL   ADPRIGOT              EXIT LOOP IF SO                R41
         BCT   R0,ADPRILP          LOOP BACK                        R41
         SPACE 1                                                    R41
ADPRIGOT MVC   JOEPRIO,0(R14)      MOVE PRIORITY TO JOE             R41
         SRL   R2,2                REDUCE TO FULLWORD OFFSET         R4
         STH   R2,JOECHAR          SAVE CHAR-JOE POINTER             R4
         NI    JOEFLAG,$JOESPIN+$JOEHOLD  RESET ALL BUT SPIN, HOLD   R4
         SLR   R3,R3               CLEAR R3                          R4
         STH   R3,JOECKPT          CLEAR CHECKPOINT AREA POINTER     R4
         SPACE 1                                                     R4
ADCLS    IC    R3,JOECURCL         PICK UP CURRENT SYSOUT CLASS      R4
         IC    R3,CNVCLS-C'A'(R3)  LOAD CLASS QUEUE OFFSET           R4
         LA    R3,JOTCLSQ(R3)      POINT TO PROPER CLASS QUEUE       R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        ADD WORK-JOE TO SELECTED QUEUE                               *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
ADADD    LH    R2,0(,R3)           FIRST QUEUE ENTRY                 R4
         STH   R2,JOENEXT          MAKE NEW JOE 1ST                  R4
         SLR   JOE,JOT             OFFSET OF WORK-JOE IN JOT         R4
         SRL   JOE,2               REDUCE TO FULLWORD OFFSET         R4
         STH   JOE,0(,R3)          ADD WORK-JOE TO HEAD OF QUEUE     R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        INCREMENT JOE COUNTER IN HASP JOB QUEUE ELEMENT              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING JQEDSECT,R1         PROVIDE JQE ADDRESSABILITY        R4
         SPACE 1                                                     R4
         SLL   JOE,2               EXPAND TO BYTE OFFSET             R4
         LH    R1,JOEJQE(JOT)      JOB QUEUE ELEMENT OFFSET          R4
         N     R1,=F'65535'        CLEAR LEFT HALFWORD               R4
         SLL   R1,2                EXPAND TO FULL ADDRESS OFFSET     R4
         AL    R1,$JOBQPTR         ADD JOB QUEUE ORIGIN              R4
         LH    R3,JQEJOECT         INCREMENT                         R4
         LA    R3,1(,R3)            JOE COUNT                        R4
         STH   R3,JQEJOECT           FOR JOB                         R4
         LM    WA,WB,PCEWA         RESTORE REGS USED BY $QCKPT       R4
        $QCKPT (R1)                CKPT THE JOB QUEUE ELEMENT        R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        POST PRINT/PUNCH ROUTINES WAITING FOR JOT SERVICE            *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
$#EXIT  $POST  $HASPECF,JOT        POST JOT SERVICES AVAILABLE       R4
         OI    $AQSE,QSEPJOT       INDICATE CROSS-SYSTEM $POST       R4
         LM    LINK,R12,PCELINK    RELOAD CALLER'S REGISTERS         R4
         CLI   $WTRWTCT,0          ANY EXTERNAL WRITERS WAITING...   R4
         BER   LINK                RETURN IF NO WITH CC = 0          R4
         BALR  R15,0                ELSE FIRST POST                  R4
         B     $#WTR-*(,R15)         WAITING WRITERS                 R4
         SPACE 1                                                     R4
         DROP  R1,JOE              KILL JQE, JOE ADDRESSABILITY      R4
         TITLE 'HASP JOB OUTPUT TABLE SERVICES -- JOE GET ROUTINE'   R4
***********************************************************************
*                                                                     *
*        $#GET -- GET JOE FROM JOT                                    *
*                                                                     *
* INPUT  R1    - DCT ADDRESS                                          *
*        R11   - HCT ADDRESS (BASE1)                                  *
*        R13   - PCE ADDRESS                                          *
*        R14   - RETURN ADDRESS                                       *
*        R15   - ENTRY POINT ADDRESS, UNPREDICTABLE ON EXIT           *
*                                                                     *
* OUTPUT R0    - CHARACTERISTICS JOE ADDRESS                          *
*        R1    - JOB QUEUE ELEMENT ADDRESS                            *
*        R15   - WORK JOE ADDRESS                                     *
*              - CC NON-ZERO -- $#GET SUCCESSFUL                      *
*              - CC     ZERO -- $#GET UNSUCCESSFUL                    *
*                                                                     *
* NOTES        - IF HIGH-ORDER BIT OF R1 ON AT ENTRY, ONLY CC VALID   *
*                ON EXIT (I.E. JOE NOT OBTAINED, IF PRESENT)          *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         ENTRY $#GET               PROVIDE ENTRY FOR $#GET ROUTINE   R4
         USING JOEDSECT,JOE        PROVIDE JOE ADDRESSABILITY        R4
         USING $#GET,R15           PROVIDE LOCAL ADDRESSABILITY      R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
$#GET    STM   LINK,R12,PCELINK    SAVE CALLER'S REGISTERS           R4
         LTR   R1,R1               GET IN HAVE = NO MODE...         R41
         BM    GTCKPT              BRANCH IF YES               @OZ20010
                                   PRINT OFF - SECTION DELETED @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
*                                  THIS CARD DELETED BY APAR   @OZ20010
                                   PRINT ON  - SECTION DELETED @OZ20010
        $QSUSE SAVE=NO             REQUEST ACCESS TO CKPT DATA @OZ20010
         SPACE 1                                                     R4
GTCKPT   L     JOT,$JOTABLE        LOAD JOB OUTPUT TABLE ADDRESS     R4
         SPACE 1                                                     R4
         L     BASE2,$JOEADD       ESTABLISH ADDRESSABILITY TO       R4
         DROP  R15                  ALL JOT MANAGEMENT ROUTINES      R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        RETURN IF HASP SYSTEM IS DRAINING ($P OR $PHASP)             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         TM    $STATUS,$DRAINED    IS HASP SYSTEM DRAINING...        R4
         BO    GTNOT               BRANCH IF YES                     R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        ARE THERE ENOUGH AVAILABLE JOES TO COMPLETE THIS REQUEST...  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         LH    R2,JOTFREC          NUMBER OF AVAILABLE JOES          R4
         BCTR  R2,R0               JOE FOR A CHECKPOINT ELEMENT      R4
         LTR   R2,R2               IS FREE JOE QUEUE VOID...         R4
         BP    GETOK               BRANCH IF NO                      R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        REQUEST MUST BE RETRYED LATER                                *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
GTNOT    LM    LINK,R12,PCELINK    RELOAD CALLER'S REGISTERS         R4
         SR    R15,R15             SET NON-PROCESS INDICATION        R4
         BR    LINK                RETURN TO CALLER                  R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        SCAN WORK QUEUE(S) FOR PIECE OF WORK                         *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING DCTDSECT,R5         PROVIDE DCT ADDRESSABILITY        R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
GETOK    LR    R5,R1               COPY DCT ADDRESS                  R4
         LA    R1,DCTCLASS         CLASS MASK ADDRESS                R4
         XC    PCER1,PCER1         CLEAR BEST CHOICE SLOT            R4
         MVI   PCER0,36            SET CLASS ORDER PRIORITY TO 36    R4
         SPACE 1                                                     R4
GTCLASS  CLI   0(R1),C' '          END OF CLASS MASK...              R4
         BE    GTCMEND             BRANCH IF YES                     R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        CONVERT CLASS QUEUE ID TO CLASS QUEUE ADDRESS                *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         SLR   R3,R3               CLEAR REGISTER                    R4
         IC    R3,0(,R1)           GET A CLASS ID FROM THE MASK      R4
         IC    R3,CNVCLS-C'A'(R3)  GET CLASS QUEUE OFFSET            R4
         LA    R3,JOTCLSQ-(JOENEXT-JOEDSECT)(R3)  CLASS QUEUE ADDR   R4
         LR    JOE,R3              COPY QUEUE ADDRESS TO JOE         R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        SELECT A WORK-JOE FROM THE QUEUE                             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
GTWKJOE  NI    PCER0,X'3F'         CLEAR PRIORITY BITS               R4
         LH    JOE,JOENEXT         SELECT NEXT WORK-JOE              R4
         N     JOE,=F'65535'       CLEAR LEFT HALFWORD               R4
         BNZ   GTJOEA              BRANCH IF NOT END OF QUEUE        R4
         LA    R1,1(,R1)           ADVANCE CLASS MASK POINTER        R4
         IC    JOE,PCER0           DECREMENT                         R4
         BCTR  JOE,0                CLASS ORDER                      R4
         STC   JOE,PCER0             PRIORITY                        R4
         B     GTCLASS             CONTINUE TO NEXT CLASS QUEUE      R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        TRY TO DISQUALIFY THIS JOE FROM SELECTION                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
GTJOEA   STH   JOE,PCER0+2         SAVE WORK-JOE DISPL               R4
         SLL   JOE,2               EXPAND TO BYTE OFFSET             R4
         ALR   JOE,JOT             ADD JOB OUTPUT TABLE ORIGIN       R4
         TM    JOEFLAG,$JOEBUSY    IS THIS WORK-JOE BUSY...          R4
         BNZ   GTWKJOE             BRANCH IF YES - REJECT            R4
         LH    R2,JOEJQE           JOB QUEUE ELEMENT OFFSET          R4
         N     R2,=F'65535'        CLEAR LEFT HALFWORD               R4
         SLL   R2,2                EXPAND TO BYTE OFFSET             R4
         AL    R2,$JOBQPTR         ADD JOB QUEUE ORIGIN              R4
         SPACE 1                                                     R4
         USING JQEDSECT,R2         PROVIDE JQE ADDRESSABILITY        R4
         SPACE 1                                                     R4
         TM    JQEFLAGS,QUEHOLDA+QUEHOLD1  IS JOB HELD...            R4
         BNZ   GTWKJOE             BRANCH IF YES - REJECT            R4
         MVI   PCER0+1,X'FF'       PRESET MAX PRIORITY              R41
*              THIS LINE DELETED BY APAR NUMBER                @OZ27490
*              THIS LINE DELETED BY APAR NUMBER                @OZ27490
         SLR   R14,R14             LOAD PRIORITY                    R41
         IC    R14,JOEPRIO           FROM JOE                       R41
         LA    R15,1*16            USE '1' FOR                      R41
         CLI   JQETYPE,$HARDCPY      JQE PRIORITY                   R41
         BNE   GTJOEP1             IF JOB STILL EXECUTING      @OZ25785
         TM    JQEPRIO,X'F0'       TEST FOR JQE PRIORITY = 15, @OZ27490
         BO    GTJOEP2               BRANCH AROUND IF SO       @OZ27490
         IC    R15,JQEPRIO         LOAD PRIORITY FROM JQE           R41
         SPACE 1                                                    R41
GTJOEP1  ALR   R14,R15             ADD JQE AND JOE PRIORITIES,      R41
         SRL   R14,1                 MAKE SURE SUM FITS IN 1 BYTE,  R41
         STC   R14,PCER0+1           SAVE RESULT                    R41
         SPACE 1                                                    R41
GTJOEP2  DS    0H                  END OF PRIORITY COMPUTATION      R41
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        CHECK ROUTE, CPU ID, SECURITY                                *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CLC   DCTNO,JOEROUT       DOES DS ROUTE MATCH DEVICE...     R4
         BNE   GTWKJOE             BRANCH IF NO                      R4
         LH    R15,JOECHAR         CHAR-JOE DISPL                    R4
         N     R15,=F'65535'       CLEAR LEFT HALFWORD               R4
         SLL   R15,2               EXPAND TO BYTE OFFSET             R4
         ALR   R15,JOT             ADD JOB OUTPUT TABLE ORIGIN       R4
         SPACE 1                                                     R4
         DROP  R2                  KILL JQE ADDRESSABILITY           R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        CHECK EXTERNAL WRITER NAME                                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING JOEDSECT,R15        PROVIDE TEMP JOE ADDRESSABILITY   R4
         SPACE 1                                                     R4
GTNEXT   CLI   JOEWTRID,X'40'      EXTERNAL WRITER NAME...           R4
         BNE   GTWKJOE             BRANCH IF YES                     R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        CHECK FORMS ID                                               *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CLC   DCTFORMS,JOEFORM    FORMS MATCH...                    R4
         BNE   GTNOMCH             BRANCH IF NO                      R4
         SPACE 1                                                     R4
         TM    DCTPPSW2,DCTNIPRT   CHECK FOR NON-IMPACT PRINTER      R4
         BO    GTNIPRT             BRANCH IF YES                     R4
         SPACE 1                                               @OZ25970
         TM    DCTDEVTP,DCTPUN     IS DEVICE A PUNCH...        @OZ25970
         BO    GTMATCH             BR. YES, NO UCS/FCB CHECK.  @OZ25970
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        CHECK FCB ID, UCS ID -- FOR IMPACT PRINTERS                  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CLC   DCTFCB,JOEFCB       FCB MATCH...                      R4
         BE    GTUCS               BRANCH IF YES                     R4
         CLC   JOEFCB,=C'****'     STANDARD FCB...                   R4
         BNE   GTNOMCH             BRANCH IF NO                      R4
         TM    DCTPPSW,DCTPPSWB    IS DEVICE FCB STANDARD...         R4
         BO    GTNOMCH             BRANCH IF NO                      R4
         SPACE 1                                                     R4
GTUCS    CLC   DCTUCS,JOEUCS       UCS MATCH...                      R4
         BE    GTMATCH             BRANCH IF YES                     R4
         CLC   JOEUCS,=C'****'     STANDARD UCS...                   R4
         BNE   GTNOMCH             BRANCH IF NO                      R4
         TM    DCTPPSW,DCTPPSWU    IS DEVICE UCS STANDARD...         R4
         BO    GTNOMCH             BRANCH IF NO                      R4
         SPACE 1                                                     R4
GTMATCH  OI    PCER0,X'80'         SET MATCH PRIORITY BIT            R4
         B     GTPRIO              GO COMPARE PRIORITIES             R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        CHECK FLASH FRAME ID, BURSTING -- FOR NON-IMPACT PRINTERS    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
GTNIPRT  CLC   DCTFLASH,JOEFLASH   FLASH MATCH...                    R4
         BNE   GTNOMCH             BR IF NO                          R4
         TM    JOECFLAG,$JOEBRST   CHECK                             R4
         BZ    SKIP130              BURST                            R4
         TM    DCTPPSW2,DCTNIBRS     SPECIFICATION                   R4
         BZ    GTNOMCH                AGAINST                        R4
         B     GTNIPMCH                CURRENT                       R4
SKIP130  TM    DCTPPSW2,DCTNIBRS        BURSTER                      R4
         BO    GTNOMCH                   STATUS                      R4
         SPACE 1                                                     R4
GTNIPMCH OI    PCER0,X'80'         SET MATCH PRIORITY BIT            R4
         CLC   DCTFCB,JOEFCB       ADDITIONALLY, FCB MATCH...        R4
         BNE   GTPRIO              BRANCH IF NO                      R4
         OI    PCER0,X'40'         SET HI-PRIORITY MATCH BIT         R4
         B     GTPRIO              GO COMPARE PRIORITIES             R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        NO MATCH FOUND -- CHECK FOR AUTOMATIC MODE                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
GTNOMCH  TM    DCTPPSW,DCTPPSWF    IS DEVICE IN AUTO MODE...         R4
         BO    GTWKJOE             BRANCH IF NO - REJECT             R4
         TM    DCTDEVTP,DCTLNE     IS REQUESTOR REMOTE...            R4
         BO    GTPRIO              BRANCH IF YES                     R4
         LA    R3,JOEACTPR         ASSUME PRINTER                    R4
         TM    DCTDEVTP,DCTPUN     IS REQUEST FOR A PUNCH...         R4
         BNO   SKIP140             BRANCH IF NO                      R4
         LA    R3,JOEACTPU         SET PUNCH                         R4
SKIP140  CLI   0(R3),X'00'         IS ANY DEVICE ACTIVE...           R4
         BNE   SKIP150             BRANCH IF YES                     R4
         OI    PCER0,X'40'         SET NOT IN USE PRIORITY BIT       R4
SKIP150  CLC   $STDFORM,JOEFORM    IS THIS STANDARD                  R4
         BE    GTPRIO              BRANCH IF YES                     R4
         TM    PCER0,X'40'         NON-STD IN USE...                 R4
         BZ    GTWKJOE             BRANCH IF YES - REJECT            R4
         SPACE 1                                                     R4
         DROP  R15                 KILL TEMP JOE ADDRESSABILITY      R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        IS CURRENT PRIORITY GE PREVIOUS BEST CHOICE...               *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
GTPRIO   LR    R3,R1               PUT CLASS POINTER IN R3           R4
         SPACE 1                                                     R4
GTNPRIO  CLC   PCER0(2),PCER1      IS CURRENT A BETTER CHOICE...     R4
         BL    GTWKJOE             BRANCH IF NO - REJECT             R4
         L     R3,PCER0            TAKE CURRENT WORK-JOE             R4
         ST    R3,PCER1            AS NEW BEST CHOICE                R4
         B     GTWKJOE             CONTINUE SCAN OF CLASS QUEUES     R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        END OF QUEUE SCANNING - HAVE WE SELECTED NEW WORK...         *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
GTCMEND  OC    PCER1,PCER1         IS BEST CHOICE CELL ZERO...       R4
         BZ    GTNOT               BRANCH IF YES - NO WORK           R4
         LH    JOE,PCER1+2         GET BEST CHOICE JOE DISPL         R4
         N     JOE,=F'65535'       CLEAR LEFT HALFWORD               R4
         SLL   JOE,2               EXPAND TO BYTE OFFSET             R4
         ALR   JOE,JOT             ADD JOB OUTPUT TABLE ORIGIN       R4
         LH    R2,JOECHAR          CHAR-JOE DISPL                    R4
         N     R2,=F'65535'        CLEAR LEFT HALFWORD               R4
         SLL   R2,2                EXPAND TO BYTE OFFSET             R4
         LA    R0,0(R2,JOT)        CHAR-JOE ADDR TO RETURN REG       R4
         LH    R1,JOEJQE           RETURN                            R4
         N     R1,=F'65535'         JQE                              R4
         SLL   R1,2                  ADDRESS                         R4
         AL    R1,$JOBQPTR            IN R1                          R4
         LTR   R5,R5               TEST FOR HAVE = NO MODE           R4
         BM    GTEST               BR IF YES TO JUST TEST FOR JOE    R4
         ST    R1,PCEJQE            ELSE STORE JQE ADDR IN PCE @OZ32566
         TM    DCTDEVTP,DCTLNE     IS REQUESTOR REMOTE...            R4
         BNZ   GTRMT               BRANCH IF YES                     R4
         NI    JOEFLAG,255-$JOEPRT-$JOEPUN  RESET TYPE FLAGS         R4
         OI    JOEFLAG,$JOEPRT     SET PRINT TYPE                    R4
         LA    R4,JOEACTPR-JOEDSECT(R2,JOT)  ASSUME PRINTER          R4
         TM    DCTDEVTP,DCTPUN     IS REQUEST FOR A PUNCH...         R4
         BNO   GTPRT               BRANCH IF NO                      R4
         NI    JOEFLAG,255-$JOEPRT-$JOEPUN   RESET TYPE FLAGS        R4
         OI    JOEFLAG,$JOEPUN     SET PUNCH TYPE                    R4
         LA    R4,JOEACTPU-JOEDSECT(R2,JOT)  SET PUNCH               R4
         SPACE 1                                                     R4
GTPRT    SLR   R3,R3               CLEAR REGISTER                    R4
         IC    R3,0(,R4)           INCREMENT                         R4
         LA    R3,1(,R3)            ACTIVE DEVICE                    R4
         STC   R3,0(,R4)             COUNT                           R4
         BAL   WF,CKPTJOE          CKPT CHAR-JOE                     R4
         EJECT                                                       R4
GTRMT    MVC   JOEDEVID,DCTDEVID   MOVE DEVICE ID TO WORK-JOE        R4
         OC    JOEFLAG,$SIDBUSY    SET WORK JOE BUSY/SYSTEM ID       R4
         LR    R2,JOE              RELOAD WORK-JOE ADDRESS           R4
         BAL   WF,CKPTJOEA         CKPT WORK-JOE                     R4
         CLC   JOECKPT,=H'0'       IS CKPT-JOE PTR ZERO...           R4
         BNZ   GTHOT               BRANCH IF NO                      R4
         BAL   WF,GETJOE           GET A FREE JOE                    R4
         SLR   R3,JOT              REDUCE TO BYTE OFFSET             R4
         SRL   R3,2                REDUCE TO FULLWORD OFFSET         R4
         STH   R3,JOECKPT          ASSIGN CKPT-JOE                   R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        INVOKE CHECKPOINT OF JOB OUTPUT TABLE                        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
GTHOT   $POST  $HASPECF,CKPW       $POST CHECKPOINT WRITER           R4
         SPACE 1                                                     R4
GTEST    LTR   R15,JOE             RELOAD WORK JOE ADDR, SET CC      R4
         L     LINK,PCELINK        RESTORE RETURN REGISTER           R4
         LM    WA,R12,PCEWA        RELOAD CALLER'S REGISTERS         R4
         BR    LINK                RETURN TO CALLER                  R4
         SPACE 1                                                     R4
         DROP  R5,JOE              KILL DCT, JOE ADDRESSABILITY      R4
         TITLE 'HASP JOB OUTPUT TABLE SERVICES -- JOE PUT ROUTINE'   R4
***********************************************************************
*                                                                     *
*        $#PUT -- RETURN JOE TO JOT                                   *
*                                                                     *
* INPUT  R0    - CHECKPOINT JOE ADDRESS OR ZERO                       *
*        R1    - WORK JOE ADDRESS                                     *
*        R11   - HCT ADDRESS (BASE1)                                  *
*        R13   - PCE ADDRESS                                          *
*        R14   - RETURN ADDRESS                                       *
*        R15   - ENTRY POINT ADDRESS, UNPREDICTABLE ON EXIT           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         ENTRY $#PUT               PROVIDE ENTRY FOR $#PUT ROUTINE   R4
         USING JOEDSECT,R1         PROVIDE JOE ADDRESSABILITY        R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
$#PUT    STM   LINK,R12,PCELINK    SAVE CALLER'S REGISTERS           R4
        $QSUSE SAVE=NO             REQUEST ACCESS TO CHECKPOINT DATA R4
         L     JOT,$JOTABLE        LOAD JOB OUTPUT TABLE ADDRESS     R4
         L     BASE2,$JOEADD       LOAD JOT SERVICES ADDRESSABILITY  R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        DECREMENT ACTIVE DEVICE COUNTER FOR THIS CHAR-JOE            *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         LR    R2,R1               RELOAD WORK-JOE ADDRESS           R4
         BAL   WF,CKPTJOEA         CKPT WORK-JOE                     R4
        $POST  $HASPECF,CKPW       $POST CHECKPOINT WRITER           R4
         TM    PCEID,PCELCLID      IS REQUEST FOR A REMOTE DEVICE... R4
         BNO   PTRMT               BRANCH IF YES                     R4
         LH    R2,JOECHAR          CHAR-JOE DISPL                    R4
         N     R2,=F'65535'        CLEAR LEFT HALFWORD               R4
         SLL   R2,2                EXPAND TO BYTE OFFSET             R4
         LA    R4,JOEACTPR-JOEDSECT(R2,JOT)  ASSUME PRINTER          R4
         TM    PCEID,PCEPRSID      IS REQUESTOR A PRINTER...         R4
         BO    SKIP160             BRANCH IF YES                     R4
         LA    R4,JOEACTPU-JOEDSECT(R2,JOT)  SET PUNCH               R4
SKIP160  SLR   R3,R3               CLEAR REGISTER                    R4
         IC    R3,0(,R4)           DECREMENT                         R4
         BCTR  R3,R0                ACTIVE DEVICE                    R4
         STC   R3,0(,R4)             COUNT                           R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        CHECKPOINT THE CHARACTERISTICS JOE                           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         BAL   WF,CKPTJOE          CKPT CHAR-JOE                     R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        RESET CKPT-JOE VALID FLAG AND FREE CKPT-JOE IF PRC ABSENT    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
PTRMT    NI    JOEFLAG,255-$JOEBUSY  RESET WORK-JOE BUSY FLAG        R4
         LTR   R2,R0               WAS CKPT-JOE SPECIFIED...         R4
         BNZ   PTPRC               BRANCH IF YES                     R4
         NI    JOEFLAG,255-$JOECKV RESET CKPT-JOE VALID FLAG         R4
         LH    R2,JOECKPT          DISPL OF CKPT-JOE                 R4
         N     R2,=F'65535'        CLEAR LEFT HALFWORD               R4
         BZ    $#EXIT              EXIT IF NO CKPT-JOE               R4
         SLL   R2,2                EXPAND TO BYTE OFFSET             R4
         BAL   WF,FREEJOE          ADD JOE TO FREE QUEUE             R4
         XC    JOECKPT,JOECKPT     CLEAR POINTER                     R4
         B     $#EXIT              BR TO ALERT WAITING PROCESSORS    R4
         SPACE 1                                                    R41
         USING JOEDSECT,R2         ALTER JOE ADDRESSABILITY         R41
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
PTPRC    SLR   WF,WF               CLEAR JOE                        R41
         ST    WF,JOETLNC           LINE AND                        R41
         ST    WF,JOETPCT            PAGE COUNTS                    R41
         MVI   JOETNDS,0           CLEAR JOE DATA SET COUNT         R41
         BAL   WF,CKPTJOEA         CKPT CKPT-JOE                     R4
         ALR   R2,JOT              RESTORE CKPT JOE ADDRESS    @OZ27207
         L     R4,JOECRECN         GET NO. RECORDS PROCESSED        R41
         LR    R2,R1               ADDRESS WORK-JOE AGAIN           R41
         L     R3,JOERECCT         COMPUTE NO. RECORDS STILL        R41
         SR    R3,R4                TO BE PRINTED/PUNCHED           R41
         BM    $#EXIT              IGNORE IF NEGATIVE               R41
         L     R14,$SSVT           POINT TO OUTPUT                  R41
         LA    R14,$SVXPRI-SSVT-4(,R14)  PRIORITY TABLE             R41
         LA    R0,10               SET LOOP COUNTER                 R41
         SPACE 1                                                    R41
PTLOOP   LA    R14,4(,R14)         BUMP TABLE POINTER               R41
         L     R15,0(,R14)         PICK UP TABLE ENTRY              R41
         LA    R15,0(,R15)         DELETE PRIORITY BYTE             R41
         CLR   R15,R3              CHECK FOR CORRECT SLOT           R41
         BNL   PTPRIO              BR IF YES                        R41
         BCT   R0,PTLOOP            ELSE LOOP                       R41
         SPACE 1                                                    R41
PTPRIO   MVC   JOEPRIO,0(R14)      MOVE PRIORITY TO JOE             R41
         B     $#EXIT              BR TO ALERT WAITING PROCESSORS    R4
         SPACE 1                                                     R4
         DROP  R1,R2               KILL JOE ADDRESSABILITYT         R41
        TITLE 'HASP JOB OUTPUT TABLE SERVICES -- JOE REMOVE ROUTINE' R4
***********************************************************************
*                                                                     *
*        $#REM -- REMOVE A JOE FROM THE JOT                           *
*                                                                     *
*        R0    - WORK, UNPREDICTABLE ON EXIT                          *
*        R1    - WORK JOE ADDRESS, UNPREDICTABLE ON EXIT              *
*        R11   - HCT ADDRESS (BASE1)                                  *
*        R13   - PCE ADDRESS                                          *
*        R14   - RETURN ADDRESS                                       *
*        R15   - ENTRY POINT ADDRESS, UNPREDICTABLE ON EXIT           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         ENTRY $#REM               PROVIDE ENTRY FOR $#REM ROUTINE   R4
         USING JOEDSECT,JOE        PROVIDE JOE ADDRESSABILITY        R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
$#REM    STM   LINK,R12,PCELINK    SAVE CALLER'S REGISTERS           R4
        $QSUSE SAVE=NO             REQUEST ACCESS TO CHECKPOINT DATA R4
         L     JOT,$JOTABLE        LOAD JOB OUTPUT TABLE ADDRESS     R4
         L     BASE2,$JOEADD       LOAD JOT SERVICES ADDRESSABILITY  R4
         LR    JOE,R1              RELOAD WORK-JOE ADDRESS           R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        LOCATE HEAD OF WORK QUEUE FOR WORK-JOE                       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         SLR   R3,R3               CLEAR FOR INSERT                  R4
         IC    R3,JOECURCL         GET CURRENT SYSOUT CLASS          R4
         IC    R3,CNVCLS-C'A'(R3)  GET CLASS QUEUE OFFSET            R4
         LA    R3,JOTCLSQ(R3)      ADD CLASS QUEUE ORIGIN            R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        LOCATE ADDRESS OF PRECEEDING WORK-JOE                        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
RMWORKB  LA    R2,0(,JOE)          OFFSET OF                         R4
         SLR   R2,JOT               WORK-JOE IN JOT                  R4
         SLR   R3,JOT              OFFSET OF QUEUE IN JOT            R4
         LH    R4,0(R3,JOT)        DISPL OF 1ST JOE IN QUEUE         R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
RMWQSCN  N     R4,=F'65535'        CLEAR LEFT HALFWORD               R4
         SLL   R4,2                EXPAND TO BYTE OFFSET             R4
         CLR   R2,R4               IS NEXT JOE TO BE FREED...        R4
         BE    RMWKEL              BRANCH IF YES                     R4
         LR    R3,R4               MAKE NEXT PREVIOUS                R4
         LH    R4,JOENEXT-JOEDSECT(R4,JOT)  GET NEXT JOE ON QUEUE    R4
         B     RMWQSCN             CONTINUE SCAN OF QUEUE            R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        REMOVE WORK-JOE FROM THE WORK QUEUE                          *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
RMWKEL   LA    R4,0(R3,JOT)        POINT TO PRECEEDING JOE           R4
*                                  THIS CARD DELETED BY APAR   @OZ20010
         BAL   WD,REMWORK          REMOVE WORK-JOE FROM WORK QUEUE   R4
         LTR   BASE2,BASE2         TEST JOT POST REQ'T FLAG    @OZ20010
         BM    $#EXIT              BRANCH IF POST REQUIRED     @OZ20010
         LM    LINK,R12,PCELINK    RESTORE CALLER'S REGISTERS        R4
         BR    LINK                 AND RETURN                       R4
         SPACE 1                                                     R4
         DROP  JOE                 KILL JOE ADDRESSABILITY           R4
        TITLE 'HASP JOB OUTPUT TABLE SERVICES -- JOE CANCEL ROUTINE' R4
***********************************************************************
*                                                                     *
*        $#CAN -- CANCEL ALL NON-BUSY JOES FOR JOB                    *
*                                                                     *
* INPUT  R0    - WORK, UNPREDICTABLE ON EXIT                          *
*        R1    - JOB QUEUE ELEMENT ADDRESS                            *
*        R11   - HCT ADDRESS (BASE1)                                  *
*        R13   - PCE ADDRESS                                          *
*        R14   - RETURN ADDRESS                                       *
*        R15   - ENTRY POINT ADDRESS, UNPREDICTABLE ON EXIT           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         ENTRY $#CAN               PROVIDE ENTRY FOR $#CAN ROUTINE   R4
         USING JOEDSECT,JOE        PROVIDE JOE ADDRESSABILITY        R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
$#CAN    STM   LINK,R12,PCELINK    SAVE CALLER'S REGISTERS           R4
        $QSUSE SAVE=NO             REQUEST ACCESS TO CHECKPOINT DATA R4
         L     JOT,$JOTABLE        LOAD JOB OUTPUT TABLE ADDRESS     R4
         L     BASE2,$JOEADD       LOAD JOT SERVICES ADDRESSABILITY  R4
         EJECT                                                       R4
         SL    R1,$JOBQPTR         REDUCE TO RELATIVE BYTE OFFSET    R4
         SLL   R1,14               REDUCE TO RELATIVE WORD OFFSET    R4
         SRA   R1,16                ALLOWING FOR HI-ORDER BIT        R4
         LA    R10,L'JOTRDYWQ      INDEX TO WORK QUEUES              R4
*                                  THIS CARD DELETED BY APAR   @OZ20010
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        SCAN QUEUE FOR WORK-JOE BELONGING TO THIS JOB                *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  4,8                                                   R4
CNQUEUE  LA    JOE,JOTRDYWQ-2-(JOENEXT-JOEDSECT)(R10)  NEXT QUEUE    R4
         SPACE 1                                                     R4
CNJOES   LR    R4,JOE              SAVE PREVIOUS WORK-JOE ADDR       R4
         LH    JOE,JOENEXT         PICK NEXT WORK-JOE DISPL          R4
         N     JOE,=F'65535'       CLEAR LEFT HALFWORD               R4
         BZ    CNCLSX              BRANCH IF END OF QUEUE            R4
         SLL   JOE,2               EXPAND TO BYTE OFFSET             R4
         ALR   JOE,JOT             ADDRESS OF NEXT WORK-JOE          R4
         CH    R1,JOEJQE           IS WORK-JOE FOR CANCELED JOB...   R4
         BNE   CNJOES              BRANCH IF NO                      R4
         TM    JOEFLAG,$JOEBUSY    IS THIS WORK-JOE BUSY...          R4
         BNZ   CNJOES              BRANCH IF YES                     R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        REMOVE WORK-JOE FROM THE WORK QUEUE                          *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         BAL   WD,REMWORK          REMOVE WORK-JOE FROM WORK QUEUE   R4
         LH    R1,JOEJQE           RELOAD JQE OFFSET                 R4
         LR    JOE,R4              MAKE PREVIOUS WORK-JOE CURRENT    R4
         B     CNJOES              CONTINUE CLASS QUEUE SCAN         R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        STEP TO THE NEXT WORK QUEUE                                  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
CNCLSX   BCTR  R10,0               DECREMENT QUEUE INDEX BY ONE      R4
         BCT   R10,CNQUEUE         BRANCH IF NOT AT QUEUE END        R4
         SPACE 1                                                     R4
         LTR   BASE2,BASE2         TEST JOT POST REQ'T FLAG    @OZ20010
         BM    $#EXIT              BRANCH IF POST REQUIRED     @OZ20010
         LM    LINK,R12,PCELINK    RESTORE CALLER'S REGISTERS        R4
         BR    LINK                 AND RETURN                       R4
         SPACE 1                                                     R4
         DROP  JOE                 KILL JOE ADDRESSABILITY           R4
         TITLE 'HASP JOB OUTPUT TABLE SERVICES -- COMMON ROUTINES'   R4
***********************************************************************
*                                                                     *
*        REMWORK -- ROUTINE TO REMOVE WORK JOE FROM JOT               *
*                                                                     *
* INPUT  R0    - WORK                                                 *
*        R1    - WORK                                                 *
*        R2    - WORK                                                 *
*        R3    - WORK                                                 *
*        R4    - ADDRESS OF JOE PRECEEDING WORK JOE TO BE REMOVED     *
*        WD    - RETURN ADDRESS                                       *
*        WF    - WORK/LINK REGISTER                                   *
*        JOE   - ADDRESS OF JOE TO BE REMOVED                         *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING JOEDSECT,JOE        PROVIDE JOE ADDRESSABILITY        R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        FREE CHECKPOINT JOE IF ONE WAS ALLOCATED                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
REMWORK  LH    R2,JOECKPT          DISPL OF CHECKPOINT ELEMENT       R4
         N     R2,=F'65535'        CLEAR LEFT HALFWORD               R4
         SLL   R2,2                EXPAND TO BYTE OFFSET             R4
         BZ    SKIP170             BRANCH IF ELEMENT NOT ALLOCATED   R4
         BAL   WF,FREEJOE          RETURN CKPT-JOE TO FREE QUEUE     R4
SKIP170  LH    R2,JOECHAR          CHAR-JOE DISPLACEMENT             R4
         N     R2,=F'65535'        CLEAR LEFT HALFWORD               R4
         SLL   R2,2                EXPAND TO BYTE OFFSET             R4
         TM    JOEFLAG,$JOEBUSY    WAS THIS JOE SELECTED...          R4
         BZ    RMRMT               BRANCH IF NO                      R4
         TM    PCEID,PCELCLID      IS REQUEST FOR A REMOTE DEVICE... R4
         BNO   RMRMT               BRANCH IF YES                     R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        DECREMENT ACTIVE DEVICE COUNTER FOR THIS CHAR-JOE            *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         LA    R3,JOEACTPR-JOEDSECT(R2,JOT)  ASSUME PRINTER          R4
         TM    PCEID,PCEPRSID      IS REQUESTOR A PRINTER...         R4
         BO    SKIP180             BRANCH IF YES                     R4
         LA    R3,JOEACTPU-JOEDSECT(R2,JOT)  SET PUNCH               R4
SKIP180  SLR   WF,WF               CLEAR REGISTER                    R4
         IC    WF,0(,R3)           DECREMENT                         R4
         BCTR  WF,R0                ACTIVE DEVICE                    R4
         STC   WF,0(,R3)             COUNT                           R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        DECREMENT CHARACTERISTICS JOE USE COUNT                      *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
RMRMT    LH    R3,JOEUSE-JOEDSECT(R2,JOT)  DECREMENT                 R4
         BCTR  R3,R0                        CHAR-JOE                 R4
         STH   R3,JOEUSE-JOEDSECT(R2,JOT)    USE COUNT               R4
         BAL   WF,CKPTJOE          CKPT CHAR-JOE                     R4
         LTR   R3,R3               IS NUMBER OF USERS ZERO...        R4
         BNZ   RMWORK              BRANCH IF NO                      R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        REMOVE CHARACTERISTICS-JOE FROM THE CHAR-QUEUE               *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         LA    R3,JOTCHRQ          ADDRESS OF CHAR-QUEUE             R4
         SLR   R3,JOT              OFFSET OF CHAR QUEUE IN JOT       R4
         LH    WF,JOTCHRQ          DISPL OF 1ST JOE IN CHAR QUEUE    R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
RMCQSCN  N     WF,=F'65535'        CLEAR LEFT HALFWORD               R4
         SLL   WF,2                EXPAND TO BYTE OFFSET             R4
         CLR   R2,WF               IS NEXT JOE TO BE FREED...        R4
         BE    RMCHEL              BRANCH IF YES                     R4
         LR    R3,WF               MAKE NEXT PREVIOUS                R4
         LH    WF,JOENEXT-JOEDSECT(WF,JOT)  GET NEW NEXT JOE         R4
         B     RMCQSCN             CONTINUE CHAR SCAN                R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
RMCHEL   BAL   WF,REMJOE           DEQUEUE AND FREE CHAR-JOE         R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        REMOVE WORK-JOE FROM THE WORK QUEUE                          *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
RMWORK   LR    R2,JOE              COPY WORK-JOE ADDRESS             R4
         SLR   R2,JOT              REDUCE TO BYTE OFFSET             R4
         LR    R3,R4               COPY ADDRESS OF PREVIOUS WORK-JOE R4
         SLR   R3,JOT              REDUCE TO BYTE OFFSET             R4
         BAL   WF,REMJOE           DEQUEUE AND FREE WORK-JOE         R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        DECREMENT PENDING JOE COUNTER IN HASP JOB QUEUE ELEMENT      *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         LH    R1,JOEJQE           JOB QUEUE ELEMENT OFFSET          R4
         N     R1,=F'65535'        CLEAR LEFT HALFWORD               R4
         SLL   R1,2                EXPAND TO BYTE OFFSET             R4
         AL    R1,$JOBQPTR         ADD JOB QUEUE ORIGIN              R4
         SPACE 1                                                     R4
         USING JQEDSECT,R1         PROVIDE JQE ADDRESSABILITY        R4
         SPACE 1                                                     R4
         LH    WF,JQEJOECT         DECREMENT                         R4
         BCTR  WF,0                 JOE COUNT                        R4
         STH   WF,JQEJOECT           FOR JOB                         R4
         LM    WA,WB,PCEWA         RESTORE REGS USED BY $QCKPT       R4
        $QCKPT (R1)                CKPT THE JOB QUEUE ELEMENT        R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        MOVE JOB TO $PURGE IF PENDING JOE COUNT WAS ZERO             *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         AH    WF,JQEHLDCT         TEST JOE/HOLD COUNT               R4
         BNZR  WD                  RETURN IF NON-ZERO                R4
         CLI   JQETYPE,$HARDCPY    IS JOB IN $HARDCPY QUEUE...       R4
         BNER  WD                  RETURN IF NO                      R4
        $QPUT  (R1),$PURGE          ELSE RE-QUEUE JOB FOR PURGE      R4
         BR    WD                  THEN RETURN                       R4
         SPACE 1                                                     R4
         DROP  R1,JOE              KILL JQE, JOE ADDRESSABILITY      R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        GETJOE -- ROUTINE TO GET A JOE FROM THE FREE QUEUE           *
*                                                                     *
*                                                                     *
* INPUT  WF    - RETURN ADDRESS                                       *
*                                                                     *
* OUTPUT R3    - JOE ADDRESS                                          *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
GETJOE   LH    R3,JOTFREC          DECREMENT                         R4
         BCTR  R3,R0                FREE JOE                         R4
         STH   R3,JOTFREC            COUNT                           R4
         LH    R3,JOTFREQ          DISPL OF 1ST FREE JOE             R4
         N     R3,=F'65535'        CLEAR LEFT HALFWORD               R4
         SLL   R3,2                EXPAND TO BYTE OFFSET             R4
        $#CKPT JOE=0(,R3),HEAD=YES CKPT JOE BEING GOTTEN + HEADER    R4
         ALR   R3,JOT              ADDRESS OF 1ST FREE JOE           R4
         MVC   JOTFREQ,JOENEXT-JOEDSECT(R3) REATTACH REMAINING QUEUE R4
         BR    WF                  RETURN TO CALLER                  R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        REMJOE -- REMOVE JOE FROM CURRENT QUEUE                      *
*                                                                     *
* INPUT  R0    - WORK                                                 *
*        R2    - BYTE OFFSET OF JOE TO BE REMOVED                     *
*        R3    - BYTE OFFSET OF JOE PRECEEDING JOE TO BE REMOVED      *
*        WF    - RETURN ADDRESS                                       *
*                                                                     *
* NOTES        - THE BYTE AT $CSAVREG+8 IS SET TO NON-ZERO BY THIS    *
*                ROUTINE IF THE RESULTING FREE JOE COUNT INDICATES    *
*                THAT THERE MAY BE PROCESSORS WAITING FOR FREE JOES   *
*                                                                     *
*              - THIS ROUTINE EXITS TO THE FREEJOE ROUTINE TO         *
*                RETURN REMOVED JOE TO THE FREE QUEUE                 *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
REMJOE   LH    R0,JOENEXT-JOEDSECT(R2,JOT)  GET OFFSET OF NEXT JOE   R4
         STH   R0,JOENEXT-JOEDSECT(R3,JOT)  REMOVE JOE FROM QUEUE    R4
        $#CKPT JOE=0(,R3)          CHECKPOINT THE PREVIOUS JOE       R4
         LH    R0,JOTFREC          GET FREE JOE COUNT                R4
         SH    R0,$MINJOES         MINUS THE MINIMUM FREE LIMIT      R4
         SH    R0,=H'4'            MINUS 4 FOR POSSIBLE WAITING      R4
         BNM   FREEJOE             BR IF NOBODY WAITING              R4
         ICM   BASE2,8,*           INDICATE JOT POST REQUIRED  @OZ20010
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        FREEJOE -- ROUTINE TO RETURN A JOE TO THE FREE QUEUE         *
*                                                                     *
* INPUT  R0    - WORK                                                 *
*        R2    - BYTE OFFSET OF JOE TO BE RETURNED                    *
*        R3    - WORK REGISTER                                        *
*        WF    - RETURN ADDRESS                                       *
*                                                                     *
*        NOTE  - THIS ROUTINE EXITS TO THE CKPTJOEA ROUTINE TO        *
*                CHECKPOINT THE FREE JOE AFTER WHICH THE REMOVED      *
*                JOE IS INSERTED.                                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING JOEDSECT,R3         PROVIDE JOE ADDRESSABILITY        R4
         SPACE 1                                                     R4
FREEJOE  LH    R3,JOTFREC          INCREMENT                         R4
         LA    R3,1(,R3)            FREE JOE                         R4
         STH   R3,JOTFREC            COUNT                           R4
        $#CKPT JOE=0(,R2),HEAD=YES CKPT JOE BEING FREED + HEADER     R4
         SRL   R2,2                REDUCE TO FULLWORD OFFSET         R4
         LA    R3,JOTFREQ-(JOENEXT-JOEDSECT)  PREPARE TO SCAN FREE Q R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
FRESCN   CLM   R2,3,JOENEXT        IS JOE OFFSET LESS THAN NEXT...   R4
         BL    FREHIT              BRANCH IF YES                     R4
         LH    R0,JOENEXT          DISPL OF NEXT FREE JOE            R4
         N     R0,=F'65535'        CLEAR HIGH HALF WORD              R4
         BZ    FREHIT              BRANCH IF END OF FREE QUEUE       R4
         LR    R3,R0               RELOAD FREE JOE DISPL             R4
         SLL   R3,2                EXPAND TO BYTE OFFSET             R4
         ALR   R3,JOT              ADDRESS OF NEXT FREE JOE          R4
         B     FRESCN              CONTINUE SEARCH                   R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
FREHIT   LH    R0,JOENEXT          GET OLD NEXT FREE JOE DISPL       R4
         STH   R2,JOENEXT          STORE NEW DISPL AS NEXT FREE      R4
         N     R2,=F'65535'        CLEAR HIGH HALF WORD              R4
         SLL   R2,2                EXPAND TO BYTE OFFSET             R4
         STH   R0,JOENEXT-JOEDSECT(R2,JOT)  RE-CONNECT CHAIN         R4
         LR    R2,R3               RELOAD ADDR OF PREVIOUS FREE JOE  R4
         SPACE 1                                                     R4
         DROP  R3                  KILL JOE ADDRESSABILITY           R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        CKPTJOEA/CKPTJOE -- ROUTINE TO CHECKPOINT A JOE              *
*                                                                     *
* INPUT  R2    - ADDRESS OF JOE (JOECKPTA)                            *
*              - BYTE OFFSET OF JOE (JOECKPT)                         *
*        WF    - RETURN ADDRESS                                       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
CKPTJOEA SLR   R2,JOT              REDUCE TO BYTE OFFSET             R4
         SPACE 1                                                     R4
CKPTJOE $#CKPT JOE=0(,R2)          CHECKPOINT THE JOE                R4
         BR    WF                  RETURN TO CALLER                  R4
         SPACE 1                                                     R4
         DROP  JOT                 KILL JOT ADDRESSABILITY           R4
         DROP  BASE2               KILL LOCAL ADDRESSABILITY         R4
   TITLE 'HASP JOB OUTPUT TABLE SERVICES -- JOE CKPT ROUTINE'  @OZ20010
***************************************************************@OZ20010
*              $JOECKPT -- SCHEDULE CHECKPOINT FOR ALTERED JOE @OZ20010
*                                                              @OZ20010
*              REG    ENTRY VALUE               EXIT VALUE     @OZ20010
*                                                              @OZ20010
*              R0     N/A                       UNCHANGED      @OZ20010
*              R1     JOE ADDR IF ENTERED AT                   @OZ20010
*                     $JOECKPT. ELSE JOE OFFSET                @OZ20010
*              R2-R10 N/A                       UNCHANGED      @OZ20010
*              R11    HCT ADDRESS               UNCHANGED      @OZ20010
*              R12    N/A                       UNCHANGED      @OZ20010
*              R13    PCE ADDRESS               UNCHANGED      @OZ20010
*              R14    RETURN ADDR (HI ORDER     UNCHANGED      @OZ20010
*                     BIT ON IF HEAD=YES)                      @OZ20010
*              R15    ENTRY ADDRESS             UNCHANGED      @OZ20010
*                                                              @OZ20010
* THIS ROUTINE INSURES A CHECKPOINT FOR AN ALTERED JOE EVEN    @OZ20010
* IF IT CROSSES PAGE BOUNDRIES.                                @OZ20010
*                                                              @OZ20010
***************************************************************@OZ20010
         SPACE 1                                               @OZ20010
         ENTRY $JOECKPT            PROVIDE $JOECKPT ENTRY      @OZ20010
         SPACE 1                                               @OZ20010
$JOECKPT LA    R1,0(,R1)           PURIFY JOE ADDRESS          @OZ20010
         SL    R1,$JOTABLE         GET THE OFFSET              @OZ20010
         SPACE 1                                               @OZ20010
         ENTRY $JOEOFF             PROVIDE $JOEOFF ENTRY       @OZ20010
         SPACE 1                                               @OZ20010
$JOEOFF  LR    R0,R1               SAVE JOE OFFSET             @OZ20010
         SRL   R1,12               DIVIDE BY LENGTH OF PAGE    @OZ20010
         AL    R1,$JOTCTLB         POINT TO CHECKPOINT CONTROL @OZ20010
         MVC   0(1,R1),$SIDAFF     TURN ON CKPT INDICATOR      @OZ20010
         LR    R1,R0               RELOAD JOE OFFSET           @OZ20010
         LA    R1,JOESIZE-1(,R1)   LOCATE THE END OF THIS JOE  @OZ20010
         SRL   R1,12               DIVIDE BY PAGE LENGTH       @OZ20010
         AL    R1,$JOTCTLB         POINT TO CHECKPOINT CONTROL @OZ20010
         MVC   0(1,R1),$SIDAFF     TURN ON CKPT INDICATOR      @OZ20010
         LTR   R14,R14             TEST FOR HEAD=YES           @OZ20010
         BPR   R14                 RETURN IF NO                @OZ20010
         L     R1,$JOTCTLB         GET CKPT CONTROL BYTE       @OZ20010
         MVC   0(1,R1),$SIDAFF     TURN ON CKPT INDICATOR      @OZ20010
         BR    R14                 RETURN                      @OZ20010
         TITLE 'HASP JOB OUTPUT TABLE SERVICES -- EXTERNAL WRITER POST C
               ROUTINE'                                        @OZ20010
***************************************************************@OZ20010
*                                                              @OZ20010
*        $#WTR -- POST EXTERNAL WRITERS WAITING ON JOT         @OZ20010
*                                                                     *
*        R11   - HCT ADDRESS (BASE1)                                  *
*        R14   - RETURN ADDRESS                                       *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         ENTRY $#WTR               PROVIDE ENTRY FOR $#WTR ROUTINE   R4
         USING SJBDSECT,R1         PROVIDE SJB ADDRESSABILITY        R4
         USING $SVDSECT,WB         PROVIDE SSVT ADDRESSABILITY       R4
         SPACE 1                                                     R4
         CNOP  2,8                                                   R4
$#WTR    STM   LINK,WB,$CSAVREG    SAVE CALLER'S REGISTERS           R4
         L     WB,$SSVT            GET SSVT ADDRESS                  R4
         SPACE 1                                                     R4
         BALR  WA,0                RE-ESTABLISH                      R4
         USING *,WA                 LOCAL ADDRESSABILITY             R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        SCAN EXECUTING STARTED TASKS FOR EXTERNAL WRITERS            *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
WTRLOOP  LA    R1,$SVJXNUM-(SJBXQCHN-SJBDSECT)  PREPARE TO SCAN SJBS R4
         SPACE 1                                                     R4
WTRNEXT  L     R1,SJBXQCHN         GET ADDRESS OF NEXT SJB           R4
         LTR   R1,R1               TEST FOR END OF CHAIN             R4
         BZ    WTRDONE             BRANCH IF YES                     R4
         TM    SJBPSOP,X'80'       TEST FOR EXTERNAL WRITER JOT WAIT R4
         BZ    WTRNEXT             BRANCH IF NO                      R4
         NI    SJBPSOP,255-X'80'   TURN OFF WAIT BIT                 R4
         L     R1,SJBPSOP          ADDRESS PSO                       R4
         SPACE 1                                                     R4
         USING PSODSECT,R1         PROVIDE PSO ADDRESSABILITY        R4
         SPACE 1                                                     R4
        $XMPOST PSOECBP            POST A WAITING EXTERNAL WRITER    R4
         IC    R0,$WTRWTCT         DECREMENT NUMBER                  R4
         BCTR  R0,0                 OF EXTERNAL WRITERS              R4
         STC   R0,$WTRWTCT         WAITING ON JOT POST               R4
         B     WTRLOOP             CONTINUE                          R4
         SPACE 1                                                     R4
WTRDONE  LM    LINK,WB,$CSAVREG    RESTORE CALLER'S REGISTERS        R4
         BR    LINK                RETURN TO CALLER                  R4
         SPACE 1                                                     R4
         DROP  R1,WA,WB            KILL ADDRESSABILITY               R4
         TITLE 'HASP JOB OUTPUT TABLE SERVICES -- DATA AREAS'        R4
***********************************************************************
*                                                                     *
*        SYSOUT CLASS ID TO CLASS QUEUE ADDRESS CONVERSION            *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
CNVCLS   DC    AL1(0,2,4,6,8,10,12,14,16),7AL1(0)      A-I           R4
         DC    AL1(18,20,22,24,26,28,30,32,34),8AL1(0) J-R           R4
         DC    AL1(36,38,40,42,44,46,48,50),6AL1(0)    S-Z           R4
         DC    AL1(52,54,56,58,60,62,64,66,68,70)      0-9           R4
         SPACE 5                                                     R4
         LTORG                                                       R4
         TITLE 'HASP JOB OUTPUT TABLE SERVICES -- JCT READ ROUTINE'  R4
***********************************************************************
*                                                                     *
*        PROVIDE JCT IN BUFFER FOR PRINT/PUNCH/EXTERNAL WRITER JOB    *
*                                                                     *
* INPUT  R0    - WORK, UNPREDICTABLE ON EXIT                          *
*        R1    - WORK, UNPREDICTABLE ON EXIT                          *
*        R10   - JQE ADDRESS                                          *
*        R14   - RETURN ADDRESS                                       *
*        R15   - ENTRY POINT ADDRESS                                  *
*                                                                     *
* OUTPUT R10   - JCT ADDRESS OR ZERO                                  *
*              - CC NON-ZERO -- JCT READ SUCCESSFUL                   *
*              - CC     ZERO -- JCT READ UNSUCCESSFUL                 *
*                                                                     *
* NOTE   -     BECAUSE OF $WAITS IN THIS ROUTINE, THE PCE AND FIXED   *
*              SAVE AREAS ARE INELLIGIBLE FOR USE.  THEREFORE, R15    *
*              MUST BE USED AS A BASE REGISTER, BEING RESTORED AFTER  *
*              EACH USE BY ANOTHER SERVICE ROUTINE.                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         ENTRY $#JCTRDR            PROVIDE ENTRY FOR $#JCTRDR RTN    R4
         USING $#JCTRDR,R15        PROVIDE LOCAL ADDRESSABILITY      R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        SEARCH RESIDENT JCT QUEUE FOR REQUESTED JCT                  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING JQEDSECT,JCT        PROVIDE JQE ADDRESSABILITY        R4
         SPACE 1                                                     R4
         CNOP  4,8                                                   R4
$#JCTRDR ST    LINK,PCESAVEA       SAVE RETURN ADDRESS               R4
         MVC   PCESEEK,JQETRAK     JCT TRACK ADDRESS TO DA DCT       R4
         SPACE 1                                                     R4
         DROP  JCT                 KILL JQE ADDRESSABILITY           R4
         SPACE 1                                                     R4
         LH    R0,$#JCTQCT         ENTRIES IN RESIDENT JCT QUEUE     R4
         L     R1,$#JCTQ           POINT TO 1ST QUEUE ENTRY          R4
         SLR   R14,R14             CLEAR FIRST FREE POINTER          R4
         SPACE 1                                                     R4
RDFIND   CLM   JCT,7,5(R1)         DOES THIS JQE MATCH REQUEST...    R4
         BE    RDHIT               BRANCH IF YES                     R4
         CLI   0(R1),X'00'         IS THIS ENTRY FREE...             R4
         BNE   RDNEXT              BRANCH IF NO                      R4
         LTR   R14,R14             HAS FIRST FREE BEEN FOUND...      R4
         BNZ   RDNEXT              BRANCH IF YES                     R4
         LR    R14,R1              SAVE FIRST FREE ELEMENT ADDRESS   R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        STEP TO NEXT JCT QUEUE ENTRY - CONTINUE SEARCH               *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
RDNEXT   LA    R1,8(,R1)           ADDRESS NEXT JCT QUEUE ENTRY      R4
         BCT   R0,RDFIND           BRANCH IF NOT AT END OF QUEUE     R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        REQUESTED JCT NOT FOUND - SETUP NEW QUEUE ENTRY              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         LR    R1,R14              USE FIRST FREE ELEMENT            R4
         STCM  JCT,7,5(R1)         STORE JQE ADDRESS                 R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        REQUESTED JCT FOUND - INCREMENT USE COUNT                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
RDHIT    L     JCT,0(,R1)          AL1(USE COUNT),AL3(JCT BUFFER)    R4
         AL    JCT,=X'01000000'    INCREMENT USE COUNTER             R4
         ST    JCT,0(,R1)          REPLACE ENTRY IN JCT QUEUE        R4
         LR    JCT,R1              HOLD JCT QUEUE ELEMENT ADDRESS    R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        LOCK ELEMENT FOR JCT READ - OR WAIT FOR LOCK                 *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
RDLOCK   TM    4(JCT),X'80'        IS ELEMENT LOCKED...              R4
         BZ    RDJQE               BRANCH IF NO                      R4
        $WAIT  ABIT                WAIT FOR LOCK TO FREE             R4
         L     R15,$JCTRDR         RESTORE BASE                      R4
         B     RDLOCK              TRY LOCK AGAIN                    R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        SET JCT QUEUE ELEMENT LOCK, GET A BUFFER                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
RDJQE    OI    4(JCT),X'80'        SET JCT ELEMENT LOCK              R4
         ICM   R1,7,1(JCT)         TEST BUFFER ADDRESS               R4
         BZ    RDGETB              BR IF NOT IN POOL                R41
         TM    PCESAVEA,X'80'      TEST FOR REFRESH=YES             R41
         BZ    RDJCTOK             BR IF NOT                        R41
         EJECT                                                      R41
***********************************************************************
*                                                                     *
*        REFRESH=YES -- RE-READ JCT INTO NEW BUFFER                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                    R41
        $GETBUF WAIT=YES           GET NEW BUFFER                   R41
         L     R15,$JCTRDR         RESTORE BASE                     R41
         STCM  R1,7,PCEBUFAD+1     BUFFER ADDR TO DA DCT            R41
         MVI   PCEDEVTP,PCEDARD    SETUP FOR DA READ                R41
         LA    R1,PCEDADCT         ADDRESS DA DCT                   R41
        $EXCP  (R1),WAIT=YES       RE-READ LATEST COPY OF JCT       R41
         L     R15,$JCTRDR         RESTORE BASE                     R41
         NI    4(JCT),X'7F'        RELEASE JCT ELEMENT LOCK         R41
         MVC   0(8,R1),0(JCT)      COPY JCT Q ELEMENT               R41
         MVI   0(R1),1             SHOW 1 USER                      R41
         LR    JCT,R1              USE COPY OF Q ELEMENT            R41
         TM    BUFECBCC-BUFDSECT(R1),X'7F'  TEST I/O COMPLETION     R41
         BM    RDBADX              BR IF I/O ERROR                  R41
         B     RDJCTOK             ELSE GO REFRESH JCT BUFFER       R41
         SPACE 2                                                    R41
RDGETB  $GETBUF WAIT=YES           GET BUFFER FOR JCT               R41
         L     R15,$JCTRDR         RESTORE BASE                      R4
         SPACE 1                                                    R41
***********************************************************************
*                                                                     *
*        JCT NOT IN POOL -- READ IT IN FROM SPOOL                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         STCM  R1,7,1(JCT)         BUFFER ADDRESS TO JCT ELEMENT     R4
         STCM  R1,7,PCEBUFAD+1     BUFFER ADDRESS TO DA DCT          R4
         MVI   PCEDEVTP,PCEDARD    SET DA DCT TO READ                R4
         LA    R1,PCEDADCT         ADDRESS DA DCT                    R4
        $EXCP  (R1),WAIT=YES       READ JCT INTO BUFFER              R4
         L     R15,$JCTRDR         RESTORE BASE                      R4
         BM    RDBADX              BR IF I/O ERROR                   R4
         EJECT                                                      R41
***********************************************************************
*                                                                     *
*        VALIDITY CHECK - JCT MUST CONTAIN JQE OFFSET                 *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
RDJCTOK  SLR   R0,R0               CLEAR REGISTER                    R4
         ICM   R0,7,5(JCT)         ADDRESS JQE                       R4
         SL    R0,$JOBQPTR         CONVERT TO JQE OFFSET             R4
         SPACE 1                                                     R4
         USING JCTDSECT,R1         PROVIDE JCT ADDRESSABILITY        R4
         SPACE 1                                                     R4
         CL    R0,JCTJQE           IS JCT VALID...                   R4
         BNE   RDBADX              BRANCH IF NO                      R4
         SPACE 1                                                    R41
         CLR   R1,JCT              REFRESH=YES...                   R41
         BNE   RDRESET             BR IF NOT                        R41
         LA    R0,JCTSTART         ADDRESS NEW JCT COPY             R41
         LH    R1,$BUFSIZE         LENGTH OF JCT                    R41
         L     R14,0(JCT)          ADDRESS ORIGINAL                 R41
         LA    R14,JCTSTART-JCTDSECT(,R14)  JCT BUFFER              R41
         LR    R15,R1              LENGTH OF JCT                    R41
         MVCL  R14,R0              REFRESH JCT BUFFER               R41
         L     R15,$JCTRDR         RESTORE BASE                     R41
         L     JCT,0(,JCT)         GET ADDRESS OF JCT BUFFER        R41
         LA    JCT,0(,JCT)         PURIFY                           R41
        $FREEBUF PCEBUFAD          FREE THE UNUSED JCT BUFFER       R41
         L     R15,$JCTRDR         RESTORE BASE                     R41
         B     RDEXIT               AND BR TO EXIT                  R41
         SPACE 1                                                     R4
         DROP  R1                  KILL JCT ADDRESSABILITY           R4
         SPACE 1                                                     R4
RDRESET  NI    4(JCT),X'7F'        RELEASE JCT ELEMENT LOCK         R41
         LR    JCT,R1              SET JCT BUFFER ADDRESS            R4
         SPACE 1                                                     R4
RDEXIT   L     LINK,PCESAVEA       RESTORE RETURN ADDRESS            R4
         LTR   JCT,JCT             SET CALLER RETURN CODE            R4
         BR    LINK                RETURN TO CALLER                  R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        READ ERROR INDICATED BY ECB COMPLETION CODE                  *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
RDBADX   MVC   PCESEEK,PCESAVEA    SAVE RETURN ADDRESS               R4
         SPACE 1                                                     R4
$#JCTRER $DISTERR                  INDICATE CONTROL BLOCK ERROR      R4
         MVC   PCESAVEA,PCESEEK    RESTORE RETURN ADDRESS            R4
         L     R15,$JCTRDR         RESTORE BASE                      R4
         SLR   R1,R1               CLEAR REGISTER                    R4
         IC    R1,0(,JCT)          GET ELEMENT USE COUNT             R4
         BCT   R1,RDNFREE          LOWER COUNT AND FREE IF ZERO      R4
         XC    0(8,JCT),0(JCT)     CLEAR JCT QUEUE ELEMENT           R4
        $FREEBUF PCEBUFAD          FREE HASP BUFFER                  R4
         L     R15,$JCTRDR         RESTORE BASE                      R4
         B     RDCLR               GO CLEAR JCT POINTER              R4
         SPACE 1                                                     R4
RDNFREE  STC   R1,0(,JCT)          SAVE RESIDUAL USE COUNT           R4
         NI    4(JCT),X'7F'        RESET ELEMENT BUSY FLAG           R4
         SPACE 1                                                     R4
RDCLR    SLR   JCT,JCT             SET NO JCT INDICATION             R4
         B     RDEXIT              TAKE EXIT TO CALLER               R4
         SPACE 1                                                     R4
         DROP  R15                 KILL LOCAL ADDRESSABILITY         R4
         TITLE 'HASP JOB OUTPUT TABLE SERVICES -- JCT WRITE ROUTINE' R4
***********************************************************************
*                                                                     *
*        $#JCTWTR -- COPY RESIDENT JCT TO ITS PLACE ON SPOOL VOLUME   *
*                                                                     *
* INPUT  R0    - WORK, UNPREDICTABLE ON EXIT                          *
*        R1    - WORK, UNPREDICTABLE ON EXIT                          *
*        R10   - JCT ADDRESS                                          *
*        R14   - RETURN ADDRESS                                       *
*        R15   - ENTRY POINT ADDRESS                                  *
*                                                                     *
* NOTE   -     BECAUSE OF $WAITS IN THIS ROUTINE, THE PCE AND FIXED   *
*              SAVE AREAS ARE INELLIGIBLE FOR USE.  THEREFORE, R15    *
*              MUST BE USED AS A BASE REGISTER, BEING RESTORED AFTER  *
*              EACH USE BY ANOTHER SERVICE ROUTINE.                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         ENTRY $#JCTWTR            PROVIDE ENTRY FOR $#JCTWTR RTN    R4
         USING JCTDSECT,JCT        PROVIDE JCT ADDRESSABILITY        R4
         USING $#JCTWTR,R15        PROVIDE LOCAL ADDRESSABILITY      R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
$#JCTWTR ST    LINK,PCESAVEA       SAVE RETURN ADDRESS               R4
         ST    JCT,PCEBUFAD        JCT BUFFER ADDRESS TO DA DCT      R4
         LH    R0,$#JCTQCT         ENTRIES IN RESIDENT JCT QUEUE     R4
         L     R1,$#JCTQ           POINT TO 1ST QUEUE ENTRY          R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        SEARCH RESIDENT JCT QUEUE FOR THE JCT BEING WRITTEN          *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
WRSRCH   CLM   JCT,7,1(R1)         IS THIS ENTRY FOR THE JCT...      R4
         BE    WRLOCK              BRANCH IF YES                     R4
         LA    R1,8(,R1)           STEP TO NEXT ENTRY                R4
         BCT   R0,WRSRCH           BRANCH IF NOT AT END OF QUEUE     R4
         MVC   PCESEEK,PCESAVEA    SAVE RETURN ADDRESS               R4
         B     $#JCTWER            JCT NOT FOUND - ERROR             R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        QUEUE ENTRY FOR THIS JCT FOUND - WAIT IF LOCKED              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
WRLOCK   TM    4(R1),X'80'         IS ELEMENT LOCKED...              R4
         BZ    WRJQE               BRANCH IF NO                      R4
        $WAIT  ABIT                WAIT FOR ELEMENT LOCK             R4
         L     R15,$JCTWTR         RESTORE BASE                      R4
         B     WRLOCK              TRY AGAIN                         R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        SET ELEMENT LOCK AND WRITE JCT TO SPOOL                      *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
WRJQE    OI    4(R1),X'80'         SET ELEMENT LOCK                  R4
         LR    JCT,R1              HOLD JCT QUEUE ELEMENT ADDRESS    R4
         L     R1,4(,JCT)          ADDRESS JQE FOR THIS JCT          R4
         SPACE 1                                                     R4
         USING JQEDSECT,R1         PROVIDE JQE ADDRESSABILITY        R4
         SPACE 1                                                     R4
         MVC   PCESEEK,JQETRAK     JCT TRACK ADDRESS TO DA DCT       R4
         SPACE 1                                                     R4
         DROP  R1                  KILL JQE ADDRESSABILITY           R4
         SPACE 1                                                     R4
         MVI   PCEDEVTP,PCEDAWR    SET DA DCT TO WRITE               R4
         LA    R1,PCEDADCT         ADDRESS DA DCT                    R4
        $EXCP  (R1),WAIT=YES       WRITE JCT TO SPOOL                R4
         L     R15,$JCTWTR         RESTORE BASE                      R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        CHECK STATUS OF JCT WRITE                                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         BO    WRJCTOK             BRANCH IF COMPLETE AND GOOD       R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        WRITE ERROR INDICATED BY ECB COMPLETION CODE                 *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         MVC   PCESEEK,PCESAVEA    SAVE RETURN ADDRESS               R4
         SPACE 1                                                     R4
$#JCTWER $DISTERR                  INDICATE CONTROL BLOCK ERROR      R4
         MVC   PCESAVEA,PCESEEK    RESTORE RETURN ADDRESS            R4
         L     R15,$JCTWTR         RESTORE BASE                      R4
         SPACE 1                                                     R4
WRJCTOK  NI    4(JCT),X'7F'        RESET ELEMENT LOCK FLAG           R4
         L     JCT,PCEBUFAD        ADDRESS JCT BUFFER                R4
         L     LINK,PCESAVEA       RESTORE RETURN ADDRESS            R4
         BR    LINK                RETURN TO CALLER                  R4
         SPACE 1                                                     R4
         DROP  JCT,R15             KILL JCT, LOCAL ADDRESSABILITY    R4
         TITLE 'HASP JOB OUTPUT TABLE SERVICES -- JCT FREE ROUTINE'  R4
***********************************************************************
*                                                                     *
*        $#JCTFRE -- RELEASE OWNERSHIP OF RESIDENT JCT                *
*                                                                     *
* INPUT  R0    - WORK, UNPREDICTABLE ON EXIT                          *
*        R1    - WORK, UNPREDICTABLE ON EXIT                          *
*        R10   - JCT ADDRESS                                          *
*        R14   - RETURN ADDRESS                                       *
*        R15   - ENTRY POINT ADDRESS                                  *
*                                                                     *
* NOTE   -     BECAUSE OF $WAITS IN THIS ROUTINE, THE PCE AND FIXED   *
*              SAVE AREAS ARE INELLIGIBLE FOR USE.  THEREFORE, R15    *
*              MUST BE USED AS A BASE REGISTER, BEING RESTORED AFTER  *
*              EACH USE BY ANOTHER SERVICE ROUTINE.                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         ENTRY $#JCTFRE            PROVIDE ENTRY FOR $#JCTFRE RTN    R4
         USING JCTDSECT,JCT        PROVIDE JCT ADDRESSABILITY        R4
         USING $#JCTFRE,R15        PROVIDE LOCAL ADDRESSABILITY      R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
$#JCTFRE LTR   JCT,JCT             IS BUFFER BASE ZERO...            R4
         BZR   LINK                RETURN TO CALLER IF YES           R4
         ST    LINK,PCESAVEA       SAVE RETURN ADDRESS               R4
         LH    R0,$#JCTQCT         ENTRIES IN RESIDENT JCT QUEUE     R4
         L     R1,$#JCTQ           POINT TO 1ST QUEUE ENTRY          R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        SEARCH RESIDENT JCT QUEUE FOR THE JCT BEING FREED            *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
FRSRCH   CLM   JCT,7,1(R1)         IS THIS ENTRY FOR THE JCT...      R4
         BE    FRFIND              BRANCH IF YES                     R4
         LA    R1,8(,R1)           STEP TO NEXT ENTRY                R4
         BCT   R0,FRSRCH           BRANCH IF NOT AT END OF QUEUE     R4
         L     JCT,PCESAVEA        SAVE RETURN ADDRESS               R4
         SPACE 1                                                     R4
$#JCTFER $DISTERR                  INDICATE CONTROL BLOCK ERROR      R4
         ST    JCT,PCESAVEA        RESTORE RETURN ADDRESS            R4
         L     LINK,PCESAVEA       RESTORE RETURN ADDRESS            R4
         BR    LINK                RETURN TO CALLER                  R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        QUEUE ENTRY FOR THIS JCT FOUND - DECREMENT USE COUNT         *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
FRFIND   SLR   R0,R0               CLEAR REGISTER                    R4
         IC    R0,0(,R1)           GET USE COUNT                     R4
         BCT   R0,FRNZ             DECREMENT AND BRANCH IF NOT ZERO  R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        RESULTANT USE COUNT IS ZERO - FREE BUFFER AND QUEUE ENTRY    *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         XC    0(8,R1),0(R1)       FREE QUEUE ENTRY                  R4
        $FREEBUF (JCT)             FREE BUFFER CONTAINING JCT        R4
         L     LINK,PCESAVEA       RESTORE RETURN ADDRESS            R4
         BR    LINK                RETURN TO CALLER                  R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        RESULTANT USE COUNT IS NOT ZERO - STORE NEW VALUE            *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
FRNZ     STC   R0,0(,R1)           STORE UPDATED USE COUNT           R4
         L     LINK,PCESAVEA       RESTORE RETURN ADDRESS            R4
         BR    LINK                RETURN TO CALLER                  R4
         SPACE 1                                                     R4
         DROP  JCT,R15             KILL JCT, LOCAL ADDRESSABILITY    R4
         TITLE 'HASP $$WTO/$$WTOR PROCESSING ROUTINE'                R4
***********************************************************************
*                                                                     *
*        $$WTO - ISSUE DIRECT WTO/WTOR TO OPERATOR                    *
*                                                                     *
*        R1    - ADDRESS OF WTO/WTOR MESSAGE                          *
*        R13   - SAVE AREA ADDRESS                                    *
*        R14   - RETURN ADDRESS                                       *
*        R15   - ENTRY POINT ADDRESS                                  *
*                                                                     *
* NOTES                                                               *
*                                                                     *
*        THE MESSAGE TEXT MAY BE ALTERED BY THIS ROUTINE              *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING $$WTOR,R15          PROVIDE LOCAL ADDRESSABILITY      R4
         ENTRY $$WTOR              ENTRY TO $$WTO/$$WTOR ROUTINE     R4
         SPACE 1                                                     R4
$$WTOR   STM   R14,R2,12(R13)      SAVE WORK REGISTERS               R4
         SPACE 1                                                     R4
         DROP  R15                 RE-ESTABLISH                      R4
         BALR  R2,0                 LOCAL                            R4
         USING *,R2                  ADDRESSABILITY                  R4
         SPACE 1                                                     R4
         LR    R14,R1              SAVE MESSAGE ADDRESS              R4
         CLI   0(R1),0             TEST FOR WTO                      R4
         BE    SKIP190             BR IF YES                         R4
         LA    R1,8(,R1)            ELSE STEP OVER WTOR INFO         R4
SKIP190  MVC   4(1,R1),$CCOMCHR    SET MESSAGE ID CHARACTER          R4
         TM    $RUNOPTS,$MSGID     TEST MESSAGE ID OPTION            R4
         BO    WTOWTO              BR IF CURRENTLY SELECTED          R4
         LA    R0,13               ASSUME MLWTO OR NO FLAGS    @OZ30033
         TM    3(R1),X'40'         TEST FOR MLWTO              @OZ30033
         BO    *+16                BR IF YES                   @OZ30033
         TM    2(R1),X'80'         TEST FOR ROUTCDE/DESC FLAGS @OZ30033
         BZ    *+8                 BR IF NO                    @OZ30033
         LA    R0,9                OVERLAY                     @OZ30033
         LH    R15,0(,R1)            HASPNNN                         R4
         SLR   R15,R0                 MESSAGE                        R4
         EX    R15,WTOOLAY             ID                            R4
         TM    3(R1),X'40'         TEST FOR MLWTO                    R4
         BO    WTOMLWTO            BR IF YES                         R4
         LH    R15,0(,R1)           ELSE                       @OZ30033
         SH    R15,=H'7'             UPDATE                    @OZ30033
         STH   R15,0(,R1)             MSG LENGTH               @OZ30033
         B     WTOWTO              BR TO ISSUE MESSAGE               R4
         SPACE 1                                                     R4
WTOOLAY  MVC   5(*-*,R1),12(R1)    *** EXECUTE ONLY ***              R4
         EJECT                                                       R4
WTOMLWTO ALR   R15,R1              PAD END OF 1ST LINE OF MLWTO      R4
         MVC   6(7,R15),=CL7' '     WITH BLANKS                      R4
         SPACE 1                                                     R4
WTOWTO   SLR   R0,R0               GET                               R4
         L     R15,CVTPTR           UCM                              R4
         L     R15,CVTCUCB-CVT(,R15) ID                              R4
         SL    R15,=F'4'              OF                             R4
         L     R15,0(,R15)             CURRENT                       R4
         L     R15,0(,R15)              MASTER                       R4
         IC    R0,UCMID-UCMLIST(,R15)    CONSOLE                     R4
         LR    R1,R14              RELOAD MESSAGE ADDRESS            R4
         WTO   MF=(E,(1))          ISSUE WTO/WTOR                    R4
         LM    R14,R2,12(R13)      RESTORE WORK REGISTERS            R4
         BR    R14                  AND RETURN                       R4
         SPACE 1                                                     R4
         DROP  R2                  KILL LOCAL ADDRESSABILITY         R4
         SPACE 1                                                     R4
         LTORG                                                       R4
         TITLE 'HASP ALLOCATE/UNALLOCATE UNIT ROUTINE'               R4
***********************************************************************
*                                                                     *
*        $DYN  - DYNAMICALLY ALLOCATE OR UNALLOCATE DEVICES           *
*                                                                     *
*                                                                     *
* FUNCTION/OPERATION-                                                 *
*                                                                     *
*        $DYN DYNAMICALLY ALLOCATES OR UNALLOCATES A UCB TO HASP      *
*        BASED UPON THE SETTING OF THE DCTUNAL BIT OF DCTSTAT.        *
*        $DYN ALSO SETS OR RESETS THE ATTENTION INDEX IN THE UCB      *
*        IF THE DCTATTN BIT IS ON IN DCTSTAT. IF THE DEVICE IS        *
*        A 3211 PRINTER, $DYN GETS/FREES AN ERROR LOG AREA            *
*                                                                     *
* INPUT - ADDRESS OF DCT IN R1                                        *
*                                                                     *
* OUTPUT - CC=1 IF $DYN WAS SUCCESSFUL AND ALTERED DCTUNAL BIT IN     *
*               DCTSTAT                                               *
*          CC=0 IF AN ERROR WAS DETECTED IN DCT BIT SETTINGS          *
*               OR BY SVC 99                                          *
*                                                                     *
* REGISTERS -                                                         *
*                                                                     *
*        R1    = ADDRESS OF DCT                                       *
*        WA    = UCB ADDRESS                                          *
*        WB    = ALLOCATE PARAMETER ADDRESS                           *
*        WC    = BASE ADDRESS                                         *
*        WD    = DCT ADDRESS                                          *
*        R14   = RETURN                                               *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING DCTDSECT,WD         PROVIDE DCT ADDRESSABILITY        R4
         USING $DYN,WC             PROVIDE $DYN ADDRESSABILITY       R4
         SPACE 1                                                     R4
         CNOP  0,8                                                   R4
$DYN     STM   LINK,WE,$CSAVREG    SAVE WORK REGISTERS         @OZ20685
         LR    WC,R15              $DYN BASE                         R4
         LR    WD,R1               DCT ADDRESSABILITY                R4
         TM    DCTSTAT,DCTUNAL+DCTDRAIN  TEST DCT STATUS             R4
         BNM   DYNRTN              BR IF SHOULD TAKE NO ACTION       R4
         CLI   DCTDEVTP,DCTLNE     IS DEVICE A LINE...               R4
         BE    DYNGTUCB            BR IF YES                         R4
         TM    DCTDEVTP,DCTRJE+DCTINT  TEST DEVICE TYPE              R4
         BNZ   DYNRTN              BRANCH IF REMOTE OR INTERNAL      R4
         SPACE 1                                                     R4
DYNGTUCB L     WA,DCTDCB           GET DCB ADDRESS                   R4
         L     WA,DCBDEBAD-DCBDSECT(,WA) GET DEB ADDRESS FROM DCB    R4
         ICM   WA,7,DEBSUCBB-DEBDSECT(WA)  GET UCB ADDR FROM DEB     R4
         BNZ   DYNGTWRK            BR IF VALID UCB ADDRESS     @OZ20685
         SR    WE,WE               CLEAR REGISTER FOR RETURN   @OZ20685
         TM    DCTSTAT,DCTUNAL     ALLOCATE OR UNALLOCATE...         R4
         BO    DYNUNORM            ALLOCATE IS ABNORMAL RETURN       R4
         B     DYNSTRTN            BR TO UNALLOCATE AND RETURN       R4
         SPACE 2                                               @OZ20685
*        GET A BUFFER FOR DYNAMIC ALLOCATION WORK AREA         @OZ20685
         SPACE 1                                               @OZ20685
DYNGTWRK DS    0H                                              @OZ20685
         GETMAIN R,LV=DYNLEN       GET AN ALLOCATION WORK AREA @OZ20685
         LR    WE,R1               SET REGISTER FOR WORK AREA  @OZ20685
         SPACE 1                                               @OZ20685
         USING DYNDSECT,WE         SET WORK AREA ADDRESSABILITY@OZ20685
         MVC   DYNRSAV,$CSAVREG    SAVE REGS 14-6              @OZ20685
         SPACE 1                                               @OZ20685
*        INITIALIZE DYNAMIC ALLOCATION REQUEST BLOCK POINTER   @OZ20685
         SPACE 1                                               @OZ20685
         LA    R1,4(,WE)           POINT TO SVC99 REQUEST BLK  @OZ20685
         ST    R1,DYN99PTR         SAVE REQUEST BLOCK ADDRESS  @OZ20685
         OI    DYN99PTR,X'80'      SET HIGH ORDER BIT ON       @OZ20685
         SPACE 2                                               @OZ20685
*        INITIALIZE TEXT UNIT POINTERS                         @OZ20685
         SPACE 1                                               @OZ20685
         LA    R1,DYNUCBTX         GET UCB TEXT ADDRESS        @OZ20685
         ST    R1,DYNUCBTP          AND SAVE IN TEXT POINTER   @OZ20685
         LA    R1,DYNDDNTX         GET DDNAME TEXT ADDRESS     @OZ20685
         ST    R1,DYNDDNTP          AND SAVE IN TEXT POINTER   @OZ20685
         OI    DYNLASTP,X'80'      SET ON HIGH ORDER BIT IN    @OZ20685X
                                    LAST TEXT UNIT             @OZ20685
         SPACE 2                                               @OZ20685
&IECIHDR SETB  1                   TURN OFF IOSGEN DOC.        @OZ20685
&IECITP  SETB  1                   TURN OFF IOSGEN DOC.        @OZ20685
         USING UCBDSECT,WA         PICK UP UCB ADDRESSABILIY   @OZ20685
         SPACE 1                                               @OZ20685
*        INITIALIZE TEXT UNITS                                 @OZ20685
         SPACE 1                                               @OZ20685
         MVC   DYNTXT,DYNTEXT      MOVE IN TEXT UNIT TEMPLATE  @OZ20685
         MVC   DYNDDNNM,DCTDEVN    STORE DDNAME IN TEXT UNIT   @OZ20685
         TM    DCTSTAT,DCTUNAL     ALLOCATE OR UNALLOCATE...   @OZ20685
         BNO   DYN99RB             BR IF UNALLOCATE            @OZ20685
         MVC   DYNUCBNM,UCBNAME    SAVE UCB UNIT NAME          @OZ20685
         SPACE 1                                               @OZ20685
*        INITIALIZE SVC 99 REQUEST BLOCK                       @OZ20685
         SPACE 1                                               @OZ20685
         USING S99RB,WB            PICK UP SVC99 ADDRESSABILITY@OZ20685
         SPACE 1                                               @OZ20685
DYN99RB  LA    WB,DYNRB            GET REQUEST BLOCK ADRESS    @OZ20685
         MVI   S99RB,20            SET SIZE OF REQUEST BLOCK   @OZ20685
         XC    S99VERB(S99RBEND-S99VERB),S99VERB CLEAR AREA    @OZ20685
         TM    DCTSTAT,DCTUNAL     ALLOCATE OR UNALLOCATE...   @OZ20685
         BO    DYNAL               BR IF ALLOCATE              @OZ20685
         LA    R1,DYNDDNTP         GET DDNAME TEXT POINTER     @OZ20685
         ST    R1,S99TXTPP         STORE TEXT POINTER IN RB    @OZ20685
         OI    S99VERB,S99VRBUN    SET UNALLOC VERB ON IN RB   @OZ20685
         B     DYNSET              GO CHECK FOR INITIALIZATION @OZ20685
DYNAL    TM    UCBSTAT,UCBALOC     DEVICE ALREADY ALLOCATED... @OZ20685
         BO    DYNUNORM            BR TO ERROR RETURN IF YES   @OZ20685
         TM    UCBSTAT,UCBONLI+UCBCHGS DEVICE ONLINE...        @OZ20685
         BNM   DYNUNORM            BR TO ERROR RETURN IF YES   @OZ20685
         LA    R1,DYNUCBTP         GET UCB TEXT POINTER        @OZ20685
         ST    R1,S99TXTPP         STORE TEXT POINTER IN RB    @OZ20685
         OI    S99VERB,S99VRBAL    SET ALLOCATE VERB IN RB     @OZ20685
         SPACE 1                                               @OZ20685
DYNSET   CLC   $DYNTCBA,=F'0'      IS THIS INITIALIZATION...   @OZ20685
         BE    DYNDYN              BR IF YES, NO SUBTASK       @OZ20685
         SPACE 1                                               @OZ20685
DYNWAITA TM    $DYNECB,X'40'       IS ALLOC TASK BUSY...       @OZ20685
         BNO   DYNDYNA             BR IF NO                    @OZ20685
         $WAIT ALOC                WAIT FOR ALLOC TASK $$POST  @OZ20685
         B     DYNWAITA            TRY AGAIN                   @OZ20685
         SPACE 1                                               @OZ20685
         SPACE 1                                               @OZ20685
DYNDYNA  LR    R1,WE               GET RB ADDRESS              @OZ20685
         POST  $DYNECB,(R1)        POST WITH DCT ADDRESS       @OZ20685
DYNWAITB $WAIT ALOC                WAIT FOR ALLOC TASK $$POST  @OZ20685
         MVC   $CSAVREG(L'DYNRSAV),DYNRSAV RESTORE REGS 14-6   @OZ20685
         TM    DYN99PTR,X'7F'      CHECK FOR GOOD RETURN       @OZ20685
         BZ    DYNWAITB            BR IF NOT COMPLETE          @OZ20685
         BO    DYNCKATN            BR IF OK                    @OZ20685
         B     DYNUNORM            RETURN IF NOT               @OZ20685
*        THIS LINE DELETED BY APAR NUMBER                      @OZ20685
*        THIS LINE DELETED BY APAR NUMBER                      @OZ20685
*        THIS LINE DELETED BY APAR NUMBER                      @OZ20685
*        THIS LINE DELETED BY APAR NUMBER                      @OZ20685
         SPACE 1                                                     R4
DYNDYN   DS    0H                                              @OZ20685
         LA    R1,DYN99PTR         GET REQUEST BLOCK POINTER   @OZ20685
         DYNALLOC                  CALL DYNAMIC ALLOCATE/UNALOCATE   R4
         LTR   R15,R15             NORMAL RETURN...                  R4
         BZ    DYNCKATN            BR IF YES                         R4
         SPACE 1                                                     R4
DYNUNORM LTR   WE,WE               DOES A WORK AREA EXIST...   @OZ20685
         BZ    DYNUNRET            BR IF NO                    @OZ20685
         FREEMAIN R,LV=DYNLEN,A=(WE) FREE WORK AREA            @OZ20685
DYNUNRET LM    LINK,WE,$CSAVREG    RESTORE REGISTERS           @OZ20685
         SR    R15,R15             CC=0 FOR ERROR RETURN             R4
         BR    LINK                RETURN                            R4
         SPACE 1                                                     R4
DYNCKATN DS    0H                  GOOD ALLOCATION RESUMES     @OZ20685
         FREEMAIN R,LV=DYNLEN,A=(WE) FREE WORK AREA            @OZ20685
         TM    DCTSTAT,DCTATTN     SHOULD ATTN INDEX BE SET..  @OZ20685
         BZ    DYNCKPRT            BR IF NO TO CONTINUE              R4
         SLR   WB,WB               CLEAR WB                          R4
         TM    DCTSTAT,DCTUNAL     WAS REQUEST ALLOCATE...           R4
         BZ    DYNATTN             BR IF NO TO REMOVE ATT'N INDEX    R4
         LA    WB,IECITMOD         SET ATTENTION INDEX TO HASP       R4
         EJECT                                                       R4
DYNATTN  LR    LINK,WA             UCB ADDRESS                       R4
         MODESET EXTKEY=ZERO       SET ZERO PROTECT KEY              R4
         IOSGEN TP,UCB=(LINK),VAR=ATNMOD,TABLE=(WB) SET ATTN INDEX   R4
         MODESET EXTKEY=HASP       RESET HASP PROTECT KEY            R4
         SPACE 1                                                     R4
DYNCKPRT CLI   DCTDEVTP,DCTPRT     IS DEVICE A LOCAL PRINTER...      R4
         BNE   DYNSTRTN            BR IF NO                          R4
         CLI   UCBTBYT4,UCB3211    IS DEVICE A 3211...               R4
         BE    DYNPRNTR            BR IF YES                         R4
         CLI   UCBTBYT4,UCB3800    TEST FOR 3800 PRINTER             R4
         BE    DYNNIPRT            BRANCH IF YES                     R4
         SPACE 1                                                     R4
DYNSTRTN XI    DCTSTAT,DCTUNAL     ALLOCATE OR UNALLOCATE DCT        R4
         SPACE 1                                                     R4
DYNRTN   LM    LINK,WE,$CSAVREG    RESTORE REGISTERS           @OZ20685
         OI    $CSAVREG,1          CC=1 FOR NORMAL RETURN            R4
         BR    LINK                RETURN                            R4
         SPACE 1                                                     R4
DYNPRNTR L     WA,UCBXTADR         ADDRESS UCB EXTENSION             R4
         SPACE 1                                                     R4
         USING UCBUCS,WA           UCB EXTENSION ADDRESSABILITY      R4
         SPACE 1                                                     R4
         MVC   $CSAVREG+36(4),UCBERADR  SAVE UCBERADR                R4
         LA    WA,UCBERADR         SET ERR LOG ADDR (3211)           R4
         B     DYNCOM              BRANCH TO COMMON                  R4
         SPACE 1                                                     R4
         USING UCBDSECT,WA         RESTORE UCB ADDRESSABILITY        R4
         SPACE 1                                                     R4
DYNNIPRT L     WA,UCBXTADR         GET UCB EXT ADDRESS               R4
         SPACE 1                                                     R4
         USING UCB3800X,WA         SET UCB EXT ADDRESSABILITY        R4
         SPACE 1                                                     R4
         MVC   $CSAVREG+36(4),UCBMDRBF  SAVE UCBMDRBF                R4
         LA    WA,UCBMDRBF         SET ADDR FOR 3800 PROCESSING      R4
         ICM   WA,8,=X'FF'         SET SWITCH FOR 3800               R4
         OI    DCTPPSW2,DCTNINIT   SHOW 3800 NEEDS INITIALIZATION    R4
         SPACE 1                                                     R4
         DROP  WA                  KILL ADDRESSABILITY               R4
         EJECT                                                       R4
DYNCOM   TM    DCTSTAT,DCTUNAL     ALLOCATE OR UNALLOCATE...         R4
         BO    DYNPRTAL            BR IF ALLOCATE                    R4
         CLI   $CSAVREG+36,X'00'   IS OPEN DCB COUNT ZERO...         R4
         BZ    DYNSTRTN            BRANCH IF ZERO                    R4
         MVI   $CSAVREG+36,0       CLEAR OPEN DCB COUNT              R4
         ICM   LINK,15,$CSAVREG+36 GET LOG AREA ADDRESS              R4
         BZ    DYNSTERA            BRANCH IF NO LOG AREA ADDR.       R4
         LTR   WA,WA               FREEMAIN FOR 3800...              R4
         BM    DYNF3800            BR IF YES                         R4
         SPACE 1                                                     R4
DYNF3211 FREEMAIN R,LV=DYNL3211,A=(LINK),SP=255 FREE LOG AREA       R41
         B     DYNFCOM             BRANCH TO COMMON                  R4
         SPACE 1                                                     R4
DYNF3800 FREEMAIN R,LV=DYNL3800,A=(LINK),SP=245 FREE MDR AREA       R41
         SPACE 1                                                     R4
DYNFCOM  SLR   LINK,LINK           ZERO LINK                         R4
         ST    LINK,$CSAVREG+36    CLEAR UCB LOG AREA ADDRESS        R4
         B     DYNSTERA            GO UPDATE UCBERADR                R4
         SPACE 1                                                     R4
DYNPRTAL MVI   $CSAVREG+36,1       SET OPEN DCB COUNT                R4
         LA    LINK,DYNGETMN       GETMAIN RETURN ADDRESS            R4
         LTR   WA,WA               GETMAIN FOR 3800...               R4
         BM    DYNG3800            BR IF YES                         R4
         EJECT                                                       R4
DYNG3211 GETMAIN EC,LV=DYNL3211,A=(LINK),SP=255 GET LOG AREA        R41
         B     DYNGCOM             BRANCH TO COMMON                  R4
         SPACE 1                                                     R4
DYNG3800 GETMAIN EC,LV=DYNL3800,A=(LINK),SP=245 GET MDR AREA        R41
         SPACE 1                                                     R4
DYNGCOM  MVC   $CSAVREG+37(3),DYNGETMN+1 SAVE GETMAIN AREA ADDR.     R4
         LTR   R15,R15             TEST GETMAIN                      R4
         BZ    DYNSTERA            BR IF NORMAL RETURN               R4
         XC    $CSAVREG+36(4),$CSAVREG+36 PRETEND NO AREA GOTTEN     R4
         SPACE 1                                                     R4
DYNSTERA MODESET EXTKEY=ZERO       SET ZERO PROTECT KEY              R4
         MVC   0(4,WA),$CSAVREG+36 SET UCB ERR LOG ADDRESS           R4
         MODESET EXTKEY=HASP       RESET HASP PROTECT KEY            R4
         B     DYNSTRTN            RETURN                            R4
         SPACE 1                                                     R4
DYNGETMN EQU   $CSAVREG+60,4       GETMAIN ADDRESS AREA              R4
DYNL3211 EQU   570                 SIZE OF 3211 LOG AREA       @OZ20685
DYNL3800 EQU   164                 SIZE OF 3800 MDR AREA       @OZ20685
DYNRQST  DS    0F                  DYNAMIC ALLOCATION PARM LIST      R4
DYNTEXT  DC    X'001500010003'     UNIT NAME TEXT LIST         @OZ20685
         DS    CL3                 SPACE FOR UCB NAME          @OZ20685
         DC    X'000100010008'     DDNAME TEXT LIST            @OZ20685
         SPACE 2                                               @OZ20685
DYNDSECT DSECT                     ALLOCATION REQUEST BLOCK    @OZ20685
         SPACE 2                                               @OZ20685
*        SVC 99 REQUEST BLOCK POINTER                          @OZ20685
         SPACE 1                                               @OZ20685
DYN99PTR DS    F                   ADDRESS OF SVC99 RQST BLOCK @OZ20685
         SPACE 2                                               @OZ20685
*        SVC 99 REQUEST BLOCK                                  @OZ20685
         SPACE 1                                               @OZ20685
DYNRB    DS    CL(S99RBEND-S99RB)  ALLOCATION REQUEST BLOCK    @OZ20685
*              ALLOCATION TEXT UNIT POINTERS                   @OZ20685
DYNTXTP  DS    0CL8                FIRST TEST UNIT POINTER     @OZ20685
DYNUCBTP DS    F                   TEXT PTR FOR UCB            @OZ20685
DYNDDNTP DS    F                   TEXT PTR FOR DDNAME         @OZ20685
DYNLASTP EQU   *-4,4               LAST TEXT UNIT POINTER      @OZ20685
*              ALLOCATION TEXT UNIT                            @OZ20685
DYNTXT   DS    0CL23               DYNAMIC ALLOCATE TEXT UNITS @OZ20685
DYNUCBTX DS    XL6                 UNIT NAME TEXT LIST         @OZ20685
DYNUCBNM DS    XL3                 UCB NAME                    @OZ20685
DYNDDNTX DS    XL6                 DDNAME TEXT LIST            @OZ20685
DYNDDNNM DS    XL8                 DDNAME FROM DCT             @OZ20685
         DS    0F                  ALIGN TO WORD BOUNDARY      @OZ20685
DYNRSAV  DS    CL(4*9)             SAVE AREA FOR 9 REGISTERS   @OZ20685
         SPACE 1                                               @OZ20685
DYNLEN   EQU   *-DYNDSECT          LENGTH OF WORK AREA         @OZ20685
         SPACE 1                                               @OZ20685
HASPNUC  CSECT                                                 @OZ20685
*              THIS LINE DELETED BY APAR NUMBER                @OZ20685
*              THIS LINE DELETED BY APAR NUMBER                @OZ20685
         SPACE 1                                                     R4
         DROP  WB,WC,WD                                              R4
         TITLE 'HASP DISASTROUS ERROR ROUTINE'                       R4
***********************************************************************
*                                                                     *
*        $DISTERR - DISPLAY THE HASP DISASTROUS ERROR MESSAGE         *
*                                                                     *
*        R0    - WORK, UNPREDICTABLE ON EXIT                          *
*        R1    - WORK, UNPREDICTABLE ON EXIT                          *
*        R11   - HCT ADDRESS (BASE1)                                  *
*        R13   - PCE ADDRESS                                          *
*        R14   - RETURN ADDRESS                                       *
*        R15   - ENTRY POINT ADDRESS, UNPREDICTABLE ON EXIT           *
*                                                                     *
* NOTES                                                               *
*                                                                     *
*        THIS ROUTINE IS USED TO INDICATE TO THE OPERATOR SERIOUS     *
*        UNRECOVERABLE ERRORS HAVE OCCURRED.                          *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING $DSTERR,R15         PROVIDE LOCAL ADDRESSABILITY      R4
         ENTRY $DSTERR             ENTRY TO DISASTROUS ERROR RTN     R4
         SPACE 1                                                     R4
$DSTERR  STM   LINK,R1,PCELINK     SAVE WORK REGISTERS              R41
        $GETCMB NUMCMB=1           GET A CMB                        R41
         L     R15,$DISTERR        RESTORE BASE REGISTER            R41
         BNZ   DSTCMB              BR IF GOT A CMB                  R41
         LM    LINK,R1,PCELINK      ELSE RESTORE REGISTERS          R41
        $WAIT  CMB                   AND WAIT FOR A CMB             R41
         L     R15,$DISTERR        RESTORE ORIGINAL BASE            R41
         BR    R15                  AND TRY AGAIN                   R41
         SPACE 1                                                    R41
DSTCMB   L     LINK,PCELINK        RESTORE LINK                     R41
         LA    R1,CMBJOBN-CMB(,R1)  GET CMB MSG TEXT ADDRESS        R41
         MVC   0(DISASTRL,R1),DISASTER  MOVE TEXT TO CMB            R41
         MVC   DISASSYM-DISASTER(,R1),0(LINK)  SET SYMBOL NAME      R41
         MVC   DISASMOD-DISASTER(,R1),8(LINK)  SET MODULE NAME      R41
         SL    R1,=A(CMBJOBN-CMB)  RESTORE CMB ADDRESS              R41
        $WTO   (R1),DISASTRL,JOB=NO,CMB=YES,  ISSUE ERROR MSG       R41C
               ROUTE=$ERR+$LOG,CLASS=$ALWAYS,PRI=$HI                 R4
         LM    LINK,R1,PCELINK     RESTORE REGISTERS                R41
         B     16(,LINK)            AND RETURN TO CALLER             R4
         SPACE 1                                                     R4
         DROP  R15                 KILL LOCAL ADDRESSABILITY         R4
         SPACE 2                                                     R4
DSTOFFST DC    A(CMBJOBN-CMB)      OFFSET OF MSG TEXT IN CMB        R41
DISASTER $MSG  096,'DISASTROUS ERROR AT SYMBOL '                     R4
DISASSYM DC    CL8'ERRORSYM',C' IN MODULE '                          R4
DISASMOD DC    CL8'ERRORMOD'                                         R4
DISASTRL EQU   *-DISASTER          LENGTH OF MESSAGE                 R4
         TITLE 'HASP INPUT/OUTPUT ERROR ROUTINE'                     R4
***********************************************************************
*                                                                     *
*        $IOERROR - FORMAT AND DISPLAY HASP I/O ERROR MESSAGE         *
*                                                                     *
*        R0    - WORK, UNPREDICTABLE ON EXIT                          *
*        R1    - BUFFER ADDRESS (IOB FOR EXCP / RPL FOR VTAM)         *
*        R11   - HCT ADDRESS (BASE1)                                  *
*        R13   - PCE ADDRESS                                          *
*        R14   - RETURN ADDRESS                                       *
*        R15   - ENTRY POINT ADDRESS, UNPREDICTABLE ON EXIT           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         USING BUFDSECT,R1         PROVIDE BUFFER ADDRESSABILITY     R4
         USING $IOERRTN,WA         PROVIDE LOCAL ADDRESSABILITY      R4
         ENTRY $IOERRTN            ENTRY TO I/O ERROR ROUTINE        R4
         SPACE 1                                                     R4
$IOERRTN STM   LINK,WA,PCELINK     SAVE WORK REGISTERS               R4
         LR    WA,R15              RELOAD BASE                       R4
        $GETCMB NUMCMB=1           GET A CMB                        R41
         BNZ   ERRCMB              BR IF GOT A CMB                  R41
         LM    LINK,R1,PCELINK     RESTORE REGISTERS                R41
         CLI   PCEID+1,PCEASYID    TEST PROCESSOR TYPE              R41
         BE    ERREXIT             EXIT IF $ASYNC PROCESSOR         R41
         CLI   PCEID+1,PCEMLMID    TEST PROCESSOR TYPE              R41
         BE    ERREXIT             EXIT IF LINE MANAGER             R41
         CLI   PCEID+1,PCECONID    TEST PROCESSOR TYPE              R41
         BE    ERREXIT             EXIT IF COMMAND PROCESSOR        R41
         L     WA,PCEWA             ELSE RESTORE WA                 R41
        $WAIT  CMB                   AND WAIT FOR A CMB             R41
         L     R15,$IOERROR        RESTORE ORIGINAL BASE            R41
         BR    R15                  AND TRY AGAIN                   R41
         EJECT                                                      R41
         USING ERRMSG,LINK         SET MSG TEXT ADDRESSABILITY      R41
         SPACE 1                                                    R41
ERRCMB   LA    LINK,CMBJOBN-CMB(,R1) POINT TO MESSAGE AREA          R41
         MVI   ERRMSG,C' '                BLANK OUT                 R41
         MVC   ERRMSG+1(ERRMSGL-1),ERRMSG  ENTIRE MESSAGE AREA      R41
         MVC   ERRMSG(L'ERRMSGNO),ERRMSGNO  MOVE IN MSG TEXT        R41
         L     R1,PCER1            RESTORE BUFFER ADDRESS           R41
         TM    BUFTYPE,BUFRPL      CHECK BUFFER TYPE                 R4
         BO    ERRRPLBF            BRANCH IF VTAM BUFFER            R41
         LA    R0,ERRMUL           SET UNIT RECORD MESSAGE LENGTH    R4
         L     R15,BUFDCT          R15 = DCT ADDRESS                 R4
         USING DCTDSECT,R15        ESTABLISH DCT ADDRESSABILITY      R4
         MVC   ERRDEVN,DCTDEVN     SET UP HASP DEVICE NAME           R4
         L     R15,IOBDCBPT        R15 = DCB ADDRESS                 R4
         USING DCBDSECT,R15        ESTABLISH DCB ADDRESSABILITY      R4
         L     R15,DCBDEBAD        R15 = DEB ADDRESS                 R4
         USING DEBDSECT,R15        ESTABLISH DEB ADDRESSABILITY      R4
         XC    PCER1,PCER1         CLEAR WORK AREA                  R41
         MVO   PCER1+2(2),IOBXTENT GET 16 * EXTENT NUMBER           R41
         AL    R15,PCER1           ADJUST ADDRESS FOR EXTENT        R41
         ST    R1,PCER1            RESTORE IOB ADDRESS IN PCER1     R41
         L     R15,DEBSUCBA        R15 = UCB ADDRESS                 R4
         USING UCBDSECT,R15        ESTABLISH UCB ADDRESSABILITY      R4
         TM    UCBTBYT3,UCB3DACC   TEST DEVICE TYPE                  R4
         BZ    ERRNOTDA            BRANCH IF NOT DIRECT ACCESS       R4
         MVC   ERRVOLID,SRTEVOLI   SET UP VOLUME SERIAL              R4
         MVC   ERRVOLID+6(2),=C'  '  BLANK OUT REST OF FIELD        R41
         LA    R0,ERRML            SET LENGTH OF MESSAGE             R4
         SPACE 1                                                     R4
ERRNOTDA MVC   ERRNAME,UCBNAME     SET UP UNIT ADDRESS               R4
         MVI   ERRNAME+L'ERRNAME,C',' INSERT COMMA                  R41
         DROP  R15                 KILL UCB ADDRESSABILITY           R4
         L     R15,IOBCSW-1        R15 = ADDRESS OF                  R4
         LA    R15,0(,R15)          LAST                             R4
         S     R15,=F'8'              COMMAND                       R41
         BM    ERRNOIO             USE IOBSTART IF NEGATIVE          R4
         TM    BUFTYPE,BUFTP       TEST BUFFER TYPE                  R4
         BO    ERRRJE              BRANCH IF RJE ERROR               R4
         TM    IOBECBCC,X'01'      TEST COMPLETION CODE              R4
         BZ    ERRNOIO             BRANCH IF NO I/O HAS OCCURRED     R4
         SPACE 1                                                     R4
ERRRJE   TM    IOBSIOCC,X'10'      TEST SIO COMPLETION               R4
         BZ    ERRSIOOK            BRANCH IF SUCCESSFUL SIO          R4
         SPACE 1                                                     R4
ERRNOIO  L     R15,IOBSTART        GET FIRST COMMAND ADDRESS         R4
         EJECT                                                       R4
ERRSIOOK UNPK  ERRCOM,0(2,R15)     SET UP COMMAND CODE               R4
         UNPK  ERRSTAT,IOBCSW+3(3) SET UP STATUS BYTES               R4
         UNPK  ERRSENS,IOBSENS0(3) SET UP SENSE BYTES                R4
         UNPK  ERRSEEK,IOBSEEK(8)  SET UP SEEK ADDRESS               R4
         TM    BUFTYPE,BUFTP       TEST FOR TP ERROR                 R4
         BNO   ERRNOTTP            BRANCH IF NOT TP ERROR            R4
         UNPK  ERRTPCDE,5(2,R15)   SET UP TP COMMAND SEQUENCE TYPE   R4
         UNPK  ERRTPCDE+2,LCBACK(2)     SET UP CURRENT BSC ACK       R4
         LA    R0,ERRMBSCL         GET BSC TP ERROR MSG LENGTH       R4
         B     ERRNOTTP            BRANCH FOR FURTHER MSG SETUP      R4
         SPACE 1                                                     R4
         USING RPLDSECT,R1         PROVIDE RPL ADDRESSABILITY        R4
ERRRPLBF L     R15,RPLICE          GET ICE ADDRESS                  R41
         LTR   R15,R15             TEST ICE ADDRESS                 R41
         BZ    ERRRPL              IF NO ICE GO USE DEVICE DCT      R41
         L     R15,ICERDCT-ICEDSECT(,R15) GET REMOTE DCT            R41
         LTR   R15,R15             TEST REMOTE DCT                  R41
         BNZ   ERRDNAME            IF THERE, USE REMOTE DCT         R41
ERRRPL   L     R15,RPLDCT          GET ADDR OF LOGON/LINE DCT        R4
         USING DCTDSECT,R15        TEMPORARY DCT ADDRESSABILITY      R4
ERRDNAME MVC   ERRDEVN,DCTDEVN     MOVE DEVICE NAME TO MSG          R41
         DROP  R15                 RELEASE TEMPORARY BASE REG        R4
         MVC   ERRNAME(L'ERRNAME+1),=C'SNA,' USE 'SNA' AS ADDR      R41
         UNPK  ERRCOM,RPLREQ(2)    SET UP RPL REQUEST CODE           R4
         UNPK  ERRSTAT,RPLRLEN+2(3)    SET R.U. LENGTH               R4
         UNPK  ERRSENS,RPLSSNSI(3) SET UP SYSTEM SENSE INFORMATION   R4
         UNPK  ERRTPCD1,RPLSEQTP(5)  COPY SEQTTYPE, & RTNCD/FDBK2   R41
         TM    RPLOPT12,RPLFMHDR   TEST FOR FM HEADER PRESENT       R41
         BZ    ERRRPL1             BR IF NO, SKIP FMH PROCESSING    R41
         UNPK  ERRTPCD2,RPLBUFST+FMHBYTE1-FMHDSECT(2) COPY FMH TYPE R41
ERRRPL1  UNPK  ERRTPCD3,RPLRH3(5)  COPY BRAKET, CHAIN & SRTYPE      R41
         BZ    ERRRPL2             BRACNH IF NOT FM HEADER          R41
         CLI   RPLBUFST+FMHBYTE1-FMHDSECT,FMHTYPE1  TEST FMH TYPE   R41
         BNE   ERRRPL2             BRANCH IF NOT TYPE 1             R41
         UNPK  ERRTPCD4,RPLBUFST+FMHPROPS-FMHDSECT(2) COPY TYPE1    R41C
                                                    ATTRIBUTES      R41
ERRRPL2  UNPK  ERRTPCD5,RPLVTFL2(5)  COPY VTAM FLAGS & DFC BITS     R41
         LA    R0,ERRMSNAL         GET SNA TP ERROR MSG LENGTH       R4
         SPACE 1                                                     R4
ERRNOTTP TR    ERRINFO,$HEXTRAN    TRANS INFORMATION TO EBCDIC @OZ32566
         MVI   ERRCOM+2,C','       INSERT                            R4
         MVI   ERRSTAT+4,C','       SEPARATION                       R4
         MVI   ERRSENS+4,C','        COMMAS                          R4
         SL    LINK,=A(CMBJOBN-CMB)  RESTORE CMB ADDRESS            R41
        $WTO   (LINK),(R0),JOB=NO,CMB=YES,  ISSUE ERROR MESSAGE     R41C
               ROUTE=$LOG+$ERR+$TP,CLASS=$ALWAYS,PRI=$HI             R4
         LM    LINK,R1,PCELINK     RESTORE REGISTERS                 R4
         EJECT                                                      R41
ERREXIT  L     WA,PCEWA            RESTORE WA                        R4
         USING BUFDSECT,R1         ESTABLISH BUFFER ADDRESSABILITY   R4
         TM    BUFECBCC,X'7F'      RESET I/O CONDITION CODE          R4
         BR    LINK                 AND RETURN                       R4
         SPACE 1                                                     R4
         DROP  WA                  KILL LOCAL ADDRESSABILITY         R4
         DROP  R1                  KILL BUFFER ADDRESSABILITY        R4
         DROP  LINK                KILL MSG TEXT ADDRESSABILITY     R41
         SPACE 2                                                    R41
ERRMSGNO $MSG  094,'I/O ERROR ON ' ERROR MESSAGE                    R41
         SPACE 2                                                    R41
ERRMSG   DSECT                                                      R41
ERRPRFIX DC    CL(L'ERRMSGNO)' '   ERROR MESSAGE HEADER             R41
ERRVOLID DC    0CL6' '             VOLUME SERIAL (DA ONLY)           R4
ERRDEVN  DC    CL8' ',C' '         HASP DEVICE NAME (NON-DA)         R4
ERRNAME  DC    CL3' ',C','         UNIT ADDRESS                      R4
ERRCOM   DC    CL3' '              COMMAND CODE                      R4
ERRSTAT  DC    CL5' '              ERROR STATUS                      R4
ERRSENS  DC    CL5' '              SENSE INFORMATION                 R4
ERRMUL   EQU   *-ERRMSG-1                                            R4
ERRMBSCL EQU   *-ERRMSG+4                                            R4
ERRML    EQU   *-ERRMSG+14                                           R4
ERRSEEK  DC    0CL15' '            SEEK ADDRESS (DA ONLY)            R4
ERRTPCDE DC    0CL3' '             BSC                              R41
ERRTPCD1 DC    0CL9' '              AND                             R41
         DC    CL6' '                SNA                            R41
ERRTPCD2 DC    0CL3' '                EXTENDED                      R41
         DC    CL2' '                  REMOTE                       R41
ERRTPCD3 DC    0CL9' '                  JOB                         R41
         DC    CL6' '                    ENTRY                      R41
ERRTPCD4 DC    0CL3' '                    TELEPROCESSING            R41
         DC    CL2' '                      IAGNOSIC               R41
ERRTPCD5 DC    CL9' '                        INFORMATION            R41
         SPACE 2                                                    R41
ERRINFO  EQU   ERRCOM,*-ERRCOM     BYTES REQUIRING HEX XLATE        R41
ERRMSNAL EQU   *-ERRMSG-1                                            R4
ERRMSGL  EQU   *-ERRMSG            MAXIMUM MESSAGE LENGTH           R41
HASPNUC  CSECT                                                      R41
         SPACE 2                                                    R41
         LTORG                                                 @OZ32566
         TITLE 'HASP CATASTROPHIC ERROR SERVICES'              @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        $ERROR - JES2 CATASTROPHIC ERROR ROUTINE              *OZ32566
*                                                              *OZ32566
*        R0    - ADDRESS OF 4-BYTE ERROR CODE                  *OZ32566
*                (POINTS TO 1ST BYTE FOLLOWING CALLING SEQ)    *OZ32566
*        BASE1 - ENTRY POINT ADDRESS                           *OZ32566
*                                                              *OZ32566
* NOTE   - THIS ROUTINE IS ENTERED BY THE JES2 MAIN TASK VIA   *OZ32566
*          THE $ERROR MACRO WHEN A CATASTROPHE IS DETECTED     *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
         ENTRY $ERRORTN            PROVIDE $ERRORTN ENTRY      @OZ32566
         USING $ERRORTN,BASE1      PROVIDE LOCAL ADDRESSABILITY@OZ32566
         SPACE 1                                               @OZ32566
$ERRORTN STM   R0,R15,ERRORSAV     SAVE REGISTERS              @OZ32566
         LA    BASE2,$ABEND        LOAD BASE REGISTER          @OZ32566
         LR    WA,R0               SAVE ADDR OF ERROR CODE     @OZ32566
         LA    WA,0(,WA)           PURIFY                      @OZ32566
         MVI   HEXITFLG,HEXJES2    SHOW $ERROR                 @OZ32566
         B     EROUTINE             AND BR TO CONTINUE         @OZ32566
         SPACE 1                                               @OZ32566
         USING HASP,BASE1          RESTORE HCT ADDRESSABILITY  @OZ32566
         SPACE 2                                               @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        $ABEND - JES2 ABEND PROCESSING  (ESTAE EXIT)          *OZ32566
*                                                              *OZ32566
*        R1    - ADDRESS OF SDWA         IF R0 NOT = 12        *OZ32566
*              - ABEND CODE (NO SDWA)    IF R0     = 12        *OZ32566
*        R14   - RETURN ADDRESS (TO MVS)                       *OZ32566
*        R15   - ENTRY POINT ADDRESS                           *OZ32566
*                                                              *OZ32566
* NOTE - THIS ROUTINE IS ENTERED BY THE OPERATING SYSTEM WHEN A*OZ32566
*        JES2 ABEND OCCURS                                     *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
         USING $ABEND,R15          PROVIDE LOCAL ADDRESSABILITY@OZ32566
         SPACE 1                                               @OZ32566
$ABEND   STM   R0,R15,ERRORSAV     SAVE REGISTERS              @OZ32566
         LR    BASE2,R15           LOAD BASE REGISTER          @OZ32566
         MVI   HEXITFLG,HEXSYS     SHOW SYSTEM ABEND           @OZ32566
         SPACE 1                                               @OZ32566
         DROP  R15                 RE-ESTABLISH                @OZ32566
         USING $ABEND,BASE2,R10     $ABEND ADDRESSABILITY      @OZ32566
         EJECT                                                 @OZ32566
         USING SDWA,WF             PROVIDE SDWA ADDRESSABILITY @OZ32566
         USING EELDSECT,WG         PROVIDE EEL  ADDRESSABILITY @OZ32566
         SPACE 1                                               @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        OBTAIN AND INITIALIZE JES2 ESTAE ELEMENT (EEL)        *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
EROUTINE LA    R10,2048(,BASE2)    LOAD SECOND                 @OZ32566
         LA    R10,2048(,R10)       BASE REGISTER              @OZ32566
         SPACE 1                                               @OZ32566
         L     BASE1,MAPNUCA       ENSURE HCT ADDRESSABILITY   @OZ32566
         LA    R13,ERRXSAVE        PROVIDE EXTERNAL SAVE AREA  @OZ32566
         SPACE 1                                               @OZ32566
         SLR   WF,WF               CLEAR SDWA ADDRESS          @OZ32566
         LA    WG,EEL              LOAD EEL ADDRESS            @OZ32566
         LR    R0,WG               CLEAR                       @OZ32566
         LA    R1,EELSIZE           ESTAE                      @OZ32566
         MVCL  R0,R14                ELEMENT                   @OZ32566
         TM    HEXITFLG,HEXJES2    JES2 ABEND ($ERROR)...      @OZ32566
         BZ    ERRSYS              BR IF NO                    @OZ32566
         OI    EELFLAGS,EELJESAB    ELSE SHOW VOLUNTARY CRUMP  @OZ32566
         MVC   EELCODE+1(3),0(WA)  SET JES2 ERROR CODE         @OZ32566
         MVI   EELCODE,C'$'        INSERT $ TO SHOW VOL CRUMP  @OZ32566
         ST    BASE1,ERRORSAV+BASE1*4  ENSURE BASE1 IS HCT ADDR@OZ32566
         MVC   EELREGS,ERRORSAV    SET REGISTERS AT $ERROR TIME@OZ32566
         OI    EELFLAGS,EELREGSA   SHOW REGS AVAILABLE         @OZ32566
         SH    WA,ERRMACL          SET FAILURE                 @OZ32566
         ST    WA,EELFADDR          ADDRESS                    @OZ32566
         B     ERRCHPCE              AND BR TO CONTINUE        @OZ32566
         SPACE 1                                               @OZ32566
ERR     $ERROR                     DUMMY $ERROR EXPANSION      @OZ32566
ERRMACL  DC    AL2(*-ERR-4)        LENGTH OF $ERROR EXPANSION  @OZ32566
         EJECT                                                 @OZ32566
ERRSYS   OI    EELFLAGS,EELSYSAB   SHOW SYSTEM ABEND           @OZ32566
         CLI   ERRORSAV+R0*4+3,12  IS SDWA AVAILABLE...        @OZ32566
         BNE   ERRSYSDW            BR IF YES                   @OZ32566
         UNPK  EELCODE+1(3),ERRORSAV+R1*4+1(2)  SET ABEND CODE @OZ32566
         B     ERRSYSC              AND BR TO CONTINUE         @OZ32566
         SPACE 1                                               @OZ32566
ERRSYSDW L     WF,ERRORSAV+R1*4    ADDRESS OF SDWA             @OZ32566
         UNPK  EELCODE+1(3),SDWACMPC(2)  SET ABEND CODE        @OZ32566
         MVC   EELREGS,SDWAGRSV    SET REGISTERS AT ABEND TIME @OZ32566
         OI    EELFLAGS,EELREGSA   SHOW REGS AVAILABLE         @OZ32566
         SLR   WB,WB               GET LENGTH OF FAILING       @OZ32566
         IC    WB,SDWAILC1          INSTRUCTION (IN BYTES)     @OZ32566
         STC   WB,EELILC           SET ILC                     @OZ32566
         MVC   EELPSW(8),SDWAEC1   SET PSW                     @OZ32566
         L     WA,EELPSW+4         SET                         @OZ32566
         SLR   WA,WB                FAILURE                    @OZ32566
         ST    WA,EELFADDR           ADDRESS                   @OZ32566
         SPACE 1                                               @OZ32566
ERRSYSC  LA    WA,EELCODE          EDIT                        @OZ32566
         OI    3(WA),X'F0'          ABEND                      @OZ32566
         TR    1(3,WA),$HEXTRAN      CODE                      @OZ32566
         MVI   0(WA),C'S'          INSERT 'S' FOR SYSTEM ABEND @OZ32566
         SPACE 1                                               @OZ32566
         DROP  WF                  KILL SDWA ADDRESSABILITY    @OZ32566
         SPACE 1                                               @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        VERIFY THAT R13 WAS A PCE ADDRESS                     *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
         DROP  R13                 PROVIDE LOCAL               @OZ32566
         USING PCEDSECT,R1          PCE ADDRESSABILITY         @OZ32566
         SPACE 1                                               @OZ32566
ERRCHPCE L     R1,EELREGS+R13*4    GET R13 AT ABEND-TIME       @OZ32566
         LTR   R1,R1               ZERO...                     @OZ32566
         BZ    ERRSVT              BR IF YES                   @OZ32566
         ST    R1,EELPCE            ELSE SAVE PCE ADDRESS      @OZ32566
         SPACE 1                                               @OZ32566
         DROP  R1                  RESTORE STANDARD            @OZ32566
         USING PCEDSECT,R13         PCE ADDRESSABILITY         @OZ32566
         EJECT                                                 @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        SIGNAL ALL PROCESSORS THAT JES2 IS GOING DOWN         *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
ERRSVT   L     R1,$SSVT            SET                         @OZ32566
         SLR   R0,R0                COMPLETION                 @OZ32566
         BCTR  R0,0                  CODE INTO                 @OZ32566
         ST    R0,$SVHASP-SSVT(,R1)   SSVT                     @OZ32566
         SPACE 3                                               @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        ISSUE 'JES2 CATASTROPHIC ERROR' MSG WITH ABEND/$ERROR *OZ32566
*        CODE                                                  *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
         MVC   ERRORMCD,EELCODE    MOVE CODE TO MESSAGE        @OZ32566
         MVC   ERMSGWRK(ERRORMSL),ERRORMSG MOVE MSG TO WORK    @OZ32566
         BAL   WE,ERRWTO           TYPE CATASTROPHIC ERROR MSG @OZ32566
         SPACE 3                                               @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        PRODUCE JES2 ABEND ANALYSIS ON CONSOLE                *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
         BAL   LINK,ERRATOP        PRODUCE JES2 ABEND ANALYSIS @OZ32566
         EJECT                                                 @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        ASK FOR JES2 TERMINATION OPTION                       *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
ERRWTOR  MVI   ERRDWORK,0          INITIALIZE ECB              @OZ32566
         MVC   EROPTION,$BLANKS     AND CLEAR                  @OZ32566
         MVI   ERDUMPT,C' '                 REPLY              @OZ32566
         MVC   ERDUMPT+1(L'ERDUMPT-1),ERDUMPT  AREA            @OZ32566
         MVC   ERMSGWRK(ERTERMSL),ERTERMSG MOVE MSG TO WORK    @OZ32566
       $$WTOR  ERMSGWRK            ASK OPERATOR FOR OPTIONS    @OZ32566
         WAIT  ECB=ERRDWORK        WAIT FOR OPERATOR REPLY     @OZ32566
         MVC   ERRDWORK(4),EROPTION  SAVE OPTION               @OZ32566
         OC    ERRDWORK(4),$BLANKS    IN UPPER-CASE            @OZ32566
         SPACE 1                                               @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        SCAN OPTION TABLE AND GIVE CONTROL TO PROCESSING      *OZ32566
*        ROUTINE                                               *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
         LA    WA,8                GET OPTION TABLE ENTRY SIZE @OZ32566
         LA    WB,EROPTABB         GET ADDR OF LAST  ENTRY     @OZ32566
         LA    R1,EROPTAB          GET ADDR OF FIRST ENTRY     @OZ32566
         SPACE 1                                               @OZ32566
EROPTLUP L     LINK,4(,R1)         SAVE ADDR OF OPTION HANDLER @OZ32566
         CLC   0(4,R1),ERRDWORK    IS THIS THE OPTION...       @OZ32566
         BNE   EROPTBLX            BR IF NO                    @OZ32566
         BALR  LINK,LINK            ELSE ENTER OPTION HANDLER  @OZ32566
         B     ERRWTOR               AND THEN BR TO ASK AGAIN  @OZ32566
         SPACE 1                                               @OZ32566
EROPTBLX BXLE  R1,WA,EROPTLUP      INDEX TO NEXT TABLE ENTRY   @OZ32566
         B     ERRWTOR             TRY AGAIN IF UNKNOWN OPTION @OZ32566
         SPACE 3                                               @OZ32566
EROPTAB  DS    0F                  TABLE OF ABEND OPTIONS      @OZ32566
         SPACE 1                                               @OZ32566
         DC    CL4'DUMP',A(ERRORDMW)  PRODUCE SYSTEM DUMP      @OZ32566
         DC    CL4'EXIT',A(ERRORRET)  QUICK EXIT               @OZ32566
         DC    CL4'PURG',A($HEXIT)    CLEAN                    @OZ32566
         DC    CL4'SNAP',A(ERRATOP)   PRODUCE ABEND ANALYSIS   @OZ32566
         SPACE 1                                               @OZ32566
EROPTABB EQU   *-8                 LAST TABLE ENTRY            @OZ32566
         EJECT                                                 @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        'DUMP' - PRODUCE SYSTEM DUMP WITH TITLE               *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
ERRORDMW ST    LINK,ERRLINK        SAVE RETURN ADDRESS         @OZ32566
         MVC   ERMSGWRK(ERDMPNGL),ERDMPING MOVE MSG TO WORK    @OZ32566
         BAL   WE,ERRWTO           TELL OPER WE'RE TAKING DUMP @OZ32566
         L     LINK,ERRLINK        RESTORE RETURN ADDRESS      @OZ32566
         SPACE 1                                               @OZ32566
         CLC   ERDUMPT(8),$BLANKS  WAS A TITLE PROVIDED...     @OZ32566
         BNE   ERDSDUMP            BR IF YES                   @OZ32566
         LTR   WG,WG               DOES ESTAE ELEMENT EXIST... @OZ32566
         BNZ   ERDTITLE            BR IF YES                   @OZ32566
         MVC   ERDUMPT(L'ERRORMS1),ERRORMS1  ELSE SET DEFAULT  @OZ32566
         B     ERDSDUMP                       AND BR TO DUMP   @OZ32566
         SPACE 1                                               @OZ32566
ERDTITLE MVC   ERDUMPT1,ERANLAB0   SET ABEND CODE AND LOCATION @OZ32566
         CLI   ERANLDS1,C' '       ERROR DESCRIPT AVAILABLE... @OZ32566
         BE    *+10                BR IF NO                    @OZ32566
         MVC   ERDUMPT2,ERANLDS1    ELSE SET DESCRIPTION       @OZ32566
         MVC   ERDUMPT3,ERANLSY0   SET SUSBSYS IDENTIFICATION  @OZ32566
         SPACE 1                                               @OZ32566
ERDSDUMP MVI   ERDUMPT-1,L'ERDUMPT SET TITLE LENGTH            @OZ32566
         SDUMP HDRAD=ERDUMPT-1,    TAKE A SYSTEM DUMP          @OZ32566C
               SDATA=(PSA,NUC,RGN,TRT,SQA,CSA,LPA,SWA)         @OZ32566
         LTR   R15,R15             DUMP SUCCESSFUL...          @OZ32566
         BZR   LINK                RETURN IF YES               @OZ32566
         MVC   ERMSGWRK(ERDUMPNL),ERDUMPNG ELSE NOTIFY         @OZ32566
         BAL   WE,ERRWTO                    OPERATOR           @OZ32566
         L     LINK,ERRLINK                  AND               @OZ32566
         BR    LINK                           RETURN           @OZ32566
         EJECT                                                 @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        'SNAP' - PROVIDE JES2 ABEND ANALYSIS                  *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
         USING PSADSECT,R0         PROVIDE PSA ADDRESSABILITY  @OZ32566
         SPACE 1                                               @OZ32566
ERRATOP  ST    LINK,ERRLINK        SAVE RETURN ADDRESS         @OZ32566
         MVC   ERMSGWRK(ERANTOPL),ERANLTOP TYPE HEADING LINE   @OZ32566
         BAL   WE,ERRWTO                                       @OZ32566
         SPACE 1                                               @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        SUBSYSTEM ID, VERSION, MODULE NAME                    *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
         L     R1,PSATOLD          GET TCB ADDRESS             @OZ32566
         SPACE 1                                               @OZ32566
         USING TCBDSECT,R1         PROVIDE TCB ADDRESSABILITY  @OZ32566
         L     WA,TCBJPQ           SAVE CDE ADDRESS            @OZ32566
         L     R1,TCBTIO           GET TIOT ADDRESS            @OZ32566
         SPACE 1                                               @OZ32566
         USING TIOT,R1             PROVIDE TIOT ADDRESSABILITY @OZ32566
         MVC   ERANLSY1,TIOCNJOB   SET SUBSYSTEM ID            @OZ32566
         DROP  R1                  KILL TIOT ADDRESSABILITY    @OZ32566
         SPACE 1                                               @OZ32566
         MVC   ERANLSY3,8(WA)      SET MODULE NAME FROM CDE    @OZ32566
         MVC   ERANLSY2,ERANLVER   SET JES2 VERS ID (&VERSION) @OZ32566
         CLC   =C'JES',ERANLSY2    &VERSION DUPLICATE 'JES'... @OZ32566
         BNE   ERRASIDL            BR IF NO                    @OZ32566
         MVC   ERANLSY2(4),=C'REL '     ELSE BUILD             @OZ32566
         MVC   ERANLSY2+4(3),ERANLVER+5  RELEASE               @OZ32566
         MVI   ERANLSY2+7,C' '            IDENTIFICATION       @OZ32566
         SPACE 1                                               @OZ32566
ERRASIDL MVC   ERMSGWRK(ERANSYSL),ERANLSYS TYPE SYSTEM-ID      @OZ32566
         BAL   WE,ERRWTO                    LINE               @OZ32566
         EJECT                                                 @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        CURRENT DATE, TIME                                    *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
         TIME  DEC                 GET CURRENT TIME AND DATE   @OZ32566
         STM   R0,R1,ERRDWORK      SAVE TIME AND DATE          @OZ32566
         MVC   ERANLDT1-1(7),ERANLMD     EDIT                  @OZ32566
         ED    ERANLDT1-1(7),ERRDWORK+5   DATE                 @OZ32566
         MVC   ERANLDT2-1(9),ERANLMT       AND                 @OZ32566
         ED    ERANLDT2-1(9),ERRDWORK       TIME               @OZ32566
         SPACE 1                                               @OZ32566
         MVC   ERMSGWRK(ERANLDTL),ERANLDT TYPE DATE AND        @OZ32566
         BAL   WE,ERRWTO                   TIME LINE           @OZ32566
         SPACE 1                                               @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        ABEND DESCRIPTION                                     *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
         MVI   ERANLDS1,C' '                  CLEAR DESCRIPTION@OZ32566
         MVC   ERANLDS1+1(L'ERANLDS1-1),ERANLDS1  AREA         @OZ32566
         LA    R1,$ERCODET         ADDRESS OF ERROR CODE TABLE @OZ32566
         SLR   WA,WA               CLEAR TEXT LENGTH           @OZ32566
         SLR   WD,WD               CLEAR INFO-EXIT ADDRESS     @OZ32566
         SPACE 1                                               @OZ32566
ERRADLUP IC    WA,8(,R1)           GET LENGTH OF TEXT          @OZ32566
         CLC   EELCODE,0(R1)       DOES ERROR CODE MATCH...    @OZ32566
         BE    ERRATDES            BR IF YES                   @OZ32566
         LA    R1,4+4+1(WA,R1)      ELSE INCR TO NEXT ENTRY    @OZ32566
         CLI   0(R1),FF            LAST ENTRY...               @OZ32566
         BNE   ERRADLUP            LOOP IF NO                  @OZ32566
         B     ERRABND              ELSE SKIP DESCRIPTION      @OZ32566
         SPACE 1                                               @OZ32566
ERRADMOV MVC   ERANLDS1(*-*),4+4+1(R1)  *** EXECUTE ONLY ***   @OZ32566
         SPACE 1                                               @OZ32566
ERRATDES BCTR  WA,0                GET MACHINE LENGTH OF TEXT  @OZ32566
         EX    WA,ERRADMOV         MOVE ERROR TEXT TO MSG      @OZ32566
         L     WD,4(,R1)           OBTAIN INFO-EXIT ADDRESS    @OZ32566
         MVC   ERMSGWRK(ERANDESL),ERANLDES TYPE ABEND          @OZ32566
         BAL   WE,ERRWTO                    DESCRIPTION LINE   @OZ32566
         EJECT                                                 @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        ABEND CODE, FAILING MODULE (BASE + OFFSET)            *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
ERRABND  MVC   ERANLAB0(6),=C'ABEND '   SET 'ABEND' PREFIX     @OZ32566
         TM    EELFLAGS,EELSYSAB        WAS IT SYSTEM ABEND... @OZ32566
         BO    *+10                     BR IF YES              @OZ32566
         MVC   ERANLAB0(5),=C'ERROR'    ELSE SET 'ERROR' PREFIX@OZ32566
         MVC   ERANLAB1,EELCODE         SET ABEND OR $ERROR    @OZ32566
         MVC   ERANLAB3,ERANLAB3-1 CLEAR LOCATION TEXT         @OZ32566
         L     R1,EELFADDR         GET FAILING ADDRESS         @OZ32566
         LTR   R1,R1               CAN WE DETERMINE...         @OZ32566
         BNZ   ERRABMAP            BR IF YES                   @OZ32566
         MVC   ERANLAB2(21),=C'AT (UNKNOWN LOCATION)' SET TEXT @OZ32566
         B     ERRABT               AND BR TO TELL OPERATOR    @OZ32566
         SPACE 1                                               @OZ32566
ERRABMAP BAL   WE,ERMODMAP         FIND MODULE MAP ENTRY       @OZ32566
         MVC   ERANLAB2(14),=C'IN LOW-STORAGE'   LOW-STORAGE...@OZ32566
         BL    ERRABT                             BR IF YES    @OZ32566
         MVC   ERANLAB2(15),=C'IN HIGH-STORAGE'  HIGH-STORAGE..@OZ32566
         BH    ERRABT                             BR IF YES    @OZ32566
         MVC   ERANLAB2,=C'AT'      ELSE FORMAT                @OZ32566
         LA    WA,ERANLAB3           ASSEMBLY NAME,            @OZ32566
         BAL   WE,ERBASOFF            BASE, AND OFFSET         @OZ32566
         SPACE 1                                               @OZ32566
ERRABT   MVC   ERMSGWRK(ERANLABL),ERANLAB TYPE ERROR CODE      @OZ32566
         BAL   WE,ERRWTO                   AND LOCATION LINE   @OZ32566
         EJECT                                                 @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        CALLING SEQUENCE                                      *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
         L     WB,EELPCE           GET ADDR OF FAILING PCE     @OZ32566
         LTR   WB,WB               VALID PCE ADDRESS...        @OZ32566
         BNZ   ERRAFIST            BR IF YES                   @OZ32566
         MVC   ERMSGWRK(ERANR13L),ERANLR13 ELSE TELL OF        @OZ32566
         BAL   WE,ERRWTO                    INVALID R13        @OZ32566
         B     ERRAFIST                      BR TO CONTINUE    @OZ32566
         SPACE 1                                               @OZ32566
         DROP  R13                 PROVIDE LOCAL               @OZ32566
         USING PCEDSECT,WB          PCE ADDRESSABILITY         @OZ32566
         SPACE 1                                               @OZ32566
*              THIS LINE DELETED BY APAR NUMBER                @OZ32566
*              THIS LINE DELETED BY APAR NUMBER                @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        FAILING INSTRUCTION                                   *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
ERRAFIST TM    EELFLAGS,EELSYSAB   SYSTEM ABEND...             @OZ32566
         BZ    ERRAPCE             BR IF NO ($ERROR)           @OZ32566
         L     R1,EELFADDR         ADDRESS OF FAILURE          @OZ32566
         LTR   R1,R1               VALID ADDRESS...            @OZ32566
         BZ    ERRAPSW             BR IF NO                    @OZ32566
         UNPK  ERANLIN1(13),0(7,R1)  ELSE FORMAT               @OZ32566
         TR    ERANLIN1,$HEXTRAN      FAILING                  @OZ32566
         MVI   ERANLIN1+12,C' '        INSTRUCTION             @OZ32566
         SLR   R1,R1               USE                         @OZ32566
         IC    R1,EELILC            ILC FOR                    @OZ32566
         ALR   R1,R1                 LENGTH OF                 @OZ32566
         LA    R1,ERANLIN1(R1)        INSTRUCTION              @OZ32566
         MVC   0(8,R1),$BLANKS         DISPLAY                 @OZ32566
         MVC   ERMSGWRK(ERANINSL),ERANLINS TYPE FAILING        @OZ32566
         BAL   WE,ERRWTO                    INSTRUCTION LINE   @OZ32566
         SPACE 1                                               @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        PSW, ILC                                              *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
ERRAPSW  OC    EELPSW(8),EELPSW    PSW VALID...                @OZ32566
         BZ    EELPCE              BR IF NO                    @OZ32566
         UNPK  ERANLPS1(9),EELPSW(5)   FORMAT                  @OZ32566
         UNPK  ERANLPS2(9),EELPSW+4(5)  LEFT AND               @OZ32566
         TR    ERANLPS1,$HEXTRAN         RIGHT                 @OZ32566
         TR    ERANLPS2,$HEXTRAN          HALVES OF            @OZ32566
         MVI   ERANLPS1+L'ERANLPS1,C' '    PSW AT TIME         @OZ32566
         MVI   ERANLPS2+L'ERANLPS2,C' '     OF ABEND           @OZ32566
         MVC   ERANLPS3,EELILC     FORMAT                      @OZ32566
         OI    ERANLPS3,X'F0'       ILC                        @OZ32566
         MVC   ERMSGWRK(ERANPSWL),ERANLPSW TYPE PSW AND        @OZ32566
         BAL   WE,ERRWTO                    ILC LINE           @OZ32566
         EJECT                                                 @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        PCE NAME, PCE ADDRESS, JOB NAME, JOB NUMBER           *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
ERRAPCE  L     WD,EELPCE           FAILING PCE ADDRESS         @OZ32566
         LTR   WD,WD               PCE ADDRESS VALID...        @OZ32566
         BZ    ERRAREGS            BR IF NO                    @OZ32566
         MVC   ERANLPC6,ERANLPC6-1 CLEAR JOB-PORTION OF MESSAGE@OZ32566
         LA    WA,=CL8'INVALID'    POINT TO ERROR NAME         @OZ32566
         SLR   R1,R1               GET UNIQUE                  @OZ32566
         IC    R1,PCEID+1           PCE ID                     @OZ32566
         SLL   R1,3                 ELSE GET PCE               @OZ32566
         LA    R1,$PCENAMT(R1)       NAME TABLE ENTRY          @OZ32566
         CLI   0(R1),C' '          VALID ENTRY...              @OZ32566
         BE    ERRAPNAM            BR IF NO                    @OZ32566
         LR    WA,R1               POINT TO PCE NAME ENTRY     @OZ32566
         CLI   0(R1),C'*'          DEVICE PCE...               @OZ32566
         BNE   ERRAPNAM            BR IF NO                    @OZ32566
         USING DCTDSECT,R1         PROVIDE DCT ADDRESSABLITY   @OZ32566
         SPACE 1                                               @OZ32566
         L     R1,PCEDCT           ADDR OF PCE'S DCT           @OZ32566
         LA    WA,DCTDEVN          POINTER TO DEVICE NAME      @OZ32566
         SPACE 1                                               @OZ32566
ERRAPNAM MVC   ERANLPC1,0(WA)      SET PCE NAME OR 'INVALID'   @OZ32566
         UNPK  ERANLPC2(7),EELPCE+1(4)  FORMAT                 @OZ32566
         TR    ERANLPC2,$HEXTRAN         PCE                   @OZ32566
         MVI   ERANLPC2+6,C')'            ADDRESS              @OZ32566
         SPACE 1                                               @OZ32566
         USING JQEDSECT,R1         PROVIDE JQE ADDRESSABILITY  @OZ32566
         SPACE 1                                               @OZ32566
         L     R1,PCEJQE           GET CURRENT JQE ADDRESS     @OZ32566
         LTR   R1,R1               ANY JQE AVAILABLE...        @OZ32566
         BZ    ERRAPTYP            BR IF NO                    @OZ32566
         MVC   ERANLPC5,JQEJNAME   SET JOB NAME                @OZ32566
         LH    R1,JQEJOBNO         GET INTERNAL JOB NUMBER     @OZ32566
         SPACE 1                                               @OZ32566
         DROP  R1,WB               KILL JQE/PCE ADDRESSABILITY @OZ32566
         EJECT                                                 @OZ32566
         USING PCEDSECT,R13        RESTORE PCE ADDRESSABILITY  @OZ32566
         SPACE 1                                               @OZ32566
         MVC   ERANLPC3,=C'JOB'    ASSUME                      @OZ32566
         CH    R1,=H'10000'         BATCH JOB                  @OZ32566
         BL    ERRAPJNO            BR IF CORRECT               @OZ32566
         MVC   ERANLPC3,=C'TSU'     ELSE ASSUME                @OZ32566
         SH    R1,=H'20000'          TSU JOB                   @OZ32566
         BNM   ERRAPJNO            BR IF CORRECT               @OZ32566
         MVC   ERANLPC3,=C'STC'     ELSE IT'S AN               @OZ32566
         AH    R1,=H'10000'          STC JOB                   @OZ32566
         SPACE 1                                               @OZ32566
ERRAPJNO CVD   R1,ERRDWORK            FORMAT                   @OZ32566
         UNPK  ERANLPC4,ERRDWORK+5(3)  JOB                     @OZ32566
         OI    ERANLPC4+3,X'F0'         NUMBER                 @OZ32566
         SPACE 1                                               @OZ32566
ERRAPTYP MVC   ERMSGWRK(ERANPCEL),ERANLPCE TYPE PCE AND        @OZ32566
         BAL   WE,ERRWTO                    JOB LINE           @OZ32566
         SPACE 1                                               @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        REGISTERS AT TIME OF ABEND                            *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
ERRAREGS TM    EELFLAGS,EELREGSA   REGISTERS AVAILABLE         @OZ32566
         BZ    ERRABOT             BR IF NO                    @OZ32566
         LA    WB,EELREGS          POINT TO REGISTERS          @OZ32566
         LA    WC,4                TYPE 4 LINES                @OZ32566
         SPACE 1                                               @OZ32566
ERRARG1  LA    WD,ERANLRG2         POINT TO MESSAGE AREA       @OZ32566
         LA    WE,4                TYPE 4 PER LINE             @OZ32566
         LA    R1,0(WC,WC)         INSERT                      @OZ32566
         LA    R1,ERANLRNO-2(R1)    REGISTER                   @OZ32566
         MVC   ERANLRG1,0(R1)        NUMBER                    @OZ32566
         SPACE 1                                               @OZ32566
ERRARG2  UNPK  0(9,WD),0(5,WB)     EDIT                        @OZ32566
         MVI   8(WD),C' '           NEXT                       @OZ32566
         TR    0(8,WD),$HEXTRAN      REGISTER                  @OZ32566
         LA    WB,4(,WB)           INCR SOURCE POINTER         @OZ32566
         LA    WD,9(,WD)           INCR TARGET POINTER         @OZ32566
         BCT   WE,ERRARG2          LOOP FOR THIS LINE          @OZ32566
         SPACE 1                                               @OZ32566
         MVC   ERMSGWRK(ERANREGL),ERANLREG TYPE REGISTER       @OZ32566
         BAL   WE,ERRWTO                    LINE               @OZ32566
         BCT   WC,ERRARG1          LOOP FOR ALL 4 LINES        @OZ32566
         SPACE 2                                               @OZ32566
ERRABOT  MVC   ERMSGWRK(ERANBOTL),ERANLBOT TYPE BOTTOM LINE    @OZ32566
         BAL   WE,ERRWTO                    LINE               @OZ32566
         SPACE 1                                               @OZ32566
ERRARET  L     LINK,ERRLINK        RESTORE RETURN ADDRESS      @OZ32566
         BR    LINK                 AND RETURN TO CALLER       @OZ32566
         EJECT                                                 @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        ERMODMAP - SUBROUTINE TO LOCATE JES2 MODULE MAP ENTRY *OZ32566
*                                                              *OZ32566
*        R1    - ADDRESS TO LOCATE                             *OZ32566
*        WE    - RETURN ADDRESS                                *OZ32566
*                                                              *OZ32566
* EXIT   CC    - LOW  - ADDRESS IS BELOW JES2 TASK (LOW-STOR)  *OZ32566
*              - HIGH - ADDRESS IS ABOVE JES2 TASK (HIGH-STOR) *OZ32566
*              - ZERO - R1 = MODULE MAP ENTRY                  *OZ32566
*                       R0 = DISPLACEMENT WITHIN ASSEMBLY      *OZ32566
*        WA-WC,WF - UNPREDICTABLE                              *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
         USING PSADSECT,R0         PROVIDE PSA ADDRESSABILITY  @OZ32566
         USING TCBDSECT,WA         PROVIDE TCB ADDRESSABILITY  @OZ32566
         SPACE 1                                               @OZ32566
ERMODMAP L     WA,PSATOLD          ADDRESS OF TCB              @OZ32566
         L     WA,TCBJPQ           ADDRESS OF CDE              @OZ32566
         SPACE 1                                               @OZ32566
         DROP  WA                  KILL TCB ADDRESSABILITY     @OZ32566
         SPACE 1                                               @OZ32566
         L     WA,20(,WA)          ADDR OF EXTENTS LIST (XTLST)@OZ32566
         L     WC,12(,WA)          WC = START OF JES2 MODULE   @OZ32566
         L     WF,8(,WA)           WF = END OF                 @OZ32566
         LA    WF,0(WC,WF)               JES2 MODULE           @OZ32566
         CLR   R1,WC               LOW STORAGE...              @OZ32566
         BLR   WE                  RETURN IF YES               @OZ32566
         CLR   R1,WF               HIGH STORAGE...             @OZ32566
         BHR   WE                  RETURN IF YES               @OZ32566
         LR    WA,R1               SAVE DESIRED ADDRESS        @OZ32566
         L     R1,$HASPMAP         ADDRESS OF JES2 MODULE MAP  @OZ32566
         LA    R1,MAPENTL(,R1)      AFTER HASPABS ENTRY        @OZ32566
         LA    WB,MAPMODS-1        COUNT OF ENTRIES (- HASPABS)@OZ32566
         MVI   ERRDWORK,FF         INITIALIZE OFFSET AND    +0 @OZ32566
         MVC   ERRDWORK+4(4),$ZEROS  MODMAP-ADDRESS ENTRIES +4 @OZ32566
         EJECT                                                 @OZ32566
ERMODLUP CLI   MAPNAME+4(R1),C'S'  SKIP SUBSYSTEM-             @OZ32566
         BE    ERMODINC             INTERFACE ENTRIES          @OZ32566
         CLC   MAPDISPL(,R1),$ZEROS  SKIP SECONDARY            @OZ32566
         BNE   ERMODINC               ENTRY POINTS             @OZ32566
         LR    R0,WA               COMPUTE OFFSET              @OZ32566
         S     R0,MAPADDR(,R1)      WITHIN THIS ASSEMBLY       @OZ32566
         BM    ERMODINC            SKIP IF BEFORE THIS ASSEMBLY@OZ32566
         CL    R0,ERRDWORK         LESS THAN PREVIOUS OFFSET...@OZ32566
         BH    ERMODINC            SKIP IF NO                  @OZ32566
         STM   R0,R1,ERRDWORK       ELSE SAVE NEW-BEST-CHOICE  @OZ32566
         SPACE 1                                               @OZ32566
ERMODINC LA    R1,MAPENTL(,R1)     INCR TO NEXT MODMAP ENTRY   @OZ32566
         BCT   WB,ERMODLUP         LOOP THRU ALL ENTRIES       @OZ32566
         SPACE 1                                               @OZ32566
         LM    R0,R1,ERRDWORK      GET OFFSET AND MODMAP ENTRY @OZ32566
         CLI   ERRDWORK,00         WAS ASSEMBLY FOUND...       @OZ32566
         BER   WE                  RETURN IF YES (CC=ZERO)     @OZ32566
         CLI   *,FF                 ELSE RETURN                @OZ32566
         BR    WE                    WITH CC=LOW               @OZ32566
         SPACE 1                                               @OZ32566
         DROP  R0                  KILL PSA ADDRESSABILITY     @OZ32566
         EJECT                                                 @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        ERBASOFF - SUBROUTINE TO FORMAT ASSEMBLY BASE AND     *OZ32566
*                   OFFSET                                     *OZ32566
*        R0    - ASSEMBLY OFFSET                               *OZ32566
*        R1    - ADDR OF JES2 MODULE MAP ENTRY                 *OZ32566
*        WE    - RETURN ADDRESS                                *OZ32566
*        WA    - ADDR OF AREA FOR FORMATTED OUTPUT AS FOLLOWS..*OZ32566
*                                                              *OZ32566
* EXAMPLE      - HASPRTAM (0974E0) + X'2E20'    (27 CHARS)     *OZ32566
*                ---------------------------                   *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
ERBASOFF ST    R0,ERRDWORK         STORE ASSEMBLY OFFSET       @OZ32566
         MVC   00(8,WA),MAPNAME(R1)  SET ASSEMBLE NAME         @OZ32566
         MVC   08(2,WA),=C' ('        SET                      @OZ32566
         UNPK  10(7,WA),MAPADDR+1(,R1) ASSEMBLY                @OZ32566
         TR    10(6,WA),$HEXTRAN        BASE                   @OZ32566
         MVC   16(6,WA),=C') + X'''      ADDRESS               @OZ32566
         UNPK  22(5,WA),ERRDWORK+2(3)     AND OFFSET           @OZ32566
         TR    22(4,WA),$HEXTRAN           WITHIN              @OZ32566
         MVI   26(WA),C''''                 ASSEMBLY           @OZ32566
         BR    WE                  RETURN TO CALLER            @OZ32566
         SPACE 2                                               @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        ERRWTO - ISSUE MESSAGE TO OPERATOR VIA $$WTOR         *OZ32566
*                                                              *OZ32566
*        R1    - ADDRESS OF LIST-FORM WTO                      *OZ32566
*        WE    - RETURN ADDRESS                                *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
ERRWTO $$WTO   ERMSGWRK            ISSUE MESSAGE TO OPERATOR   @OZ32566
         BR    WE                  RETURN TO CALLER            @OZ32566
         EJECT                                                 @OZ32566
         PUSH  PRINT               SAVE ASM PRINT STATUS       @OZ32566
         SPACE 1                                               @OZ32566
         AIF   ('&PRINT' NE 'OFF').ERPR                        @OZ32566
         PRINT NOGEN               SUPPRESS MACRO EXPANSIONS   @OZ32566
.ERPR    ANOP                                                  @OZ32566
         SPACE 1                                               @OZ32566
        $MID   088                 SET MESSAGE ID              @OZ32566
ERANLTOP WTO   '&MID.---------  JES2 ABEND ANALYSIS  ----------',      C
               MF=L                                            @OZ32566
ERANTOPL EQU   *-ERANLTOP          LENGTH OF MSG               @OZ32566
         SPACE 1                                               @OZ32566
ERANLSYS WTO   '&MID.SUBSYS = **** ********   MODULE = ********',      C
               MF=L                                            @OZ32566
ERANLSY0 EQU   *-33,4+1+8          SUBSYSTEM ID + VERSION ID   @OZ32566
ERANLSY1 EQU   *-33,4              SUBSYSTEM ID                @OZ32566
ERANLSY2 EQU   *-28,8              VERSION ID                  @OZ32566
ERANLSY3 EQU   *-8,8               MODULE NAME                 @OZ32566
ERANLVER DC    CL8'&VERSION'       &VERSION ID                 @OZ32566
ERANSYSL EQU   *-ERANLSYS          LENGTH OF MSG               @OZ32566
         SPACE 1                                               @OZ32566
ERANLDT  WTO   '&MID.DATE   = **.***          TIME   = **.**.**',      C
               MF=L                                            @OZ32566
ERANLDT1 EQU   *-33,5              DATE OF ABEND               @OZ32566
ERANLDT2 EQU   *-8,8               TIME OF ABEND               @OZ32566
ERANLMD  DC    X'4021204B202020'      MASK FOR DATE - YY.DDD   @OZ32566
ERANLMT  DC    X'4020214B20204B2020'  MASK FOR TIME - HH.MM.SS @OZ32566
ERANLDTL EQU   *-ERANLDT           LENGTH OF MSG               @OZ32566
         SPACE 1                                               @OZ32566
ERANLDES WTO   '&MID.DESC = ***********************************',      C
               MF=L                                            @OZ32566
ERANLDS1 EQU   *-35,35             ERROR DESCRIPTION           @OZ32566
ERANDESL EQU   *-ERANLDES          LENGTH OF MSG               @OZ32566
         SPACE 1                                               @OZ32566
ERANLAB  WTO   '&MID.ABEND S*** AT ******** (******) + X/****/',       C
               MF=L                                            @OZ32566
ERANLAB0 EQU   *-41,41             ENTIRE MSG TEXT             @OZ32566
ERANLAB1 EQU   *-35,4              ABEND CODE                  @OZ32566
ERANLAB2 EQU   *-30,2              'AT' OR 'IN'                @OZ32566
ERANLAB3 EQU   *-27,27             JES2 ASSEMBLY ID            @OZ32566
ERANLABL EQU   *-ERANLAB           LENGTH OF MSG               @OZ32566
         SPACE 1                                               @OZ32566
ERANLCAL WTO   '&MID.--- CALLED BY ******** (******) + X/****/',       C
               MF=L                                            @OZ32566
ERANLCL1 EQU   *-27,27             CALLING ASSEMBLY ID         @OZ32566
         SPACE 1                                               @OZ32566
ERANLINS WTO   '&MID.FAILING INSTR WAS ************        ',  @OZ32566C
               MF=L                                            @OZ32566
ERANLIN1 EQU   *-20,12             FAILING INSTRUCTION         @OZ32566
ERANINSL EQU   *-ERANLINS          LENGTH OF MSG               @OZ32566
         SPACE 1                                               @OZ32566
ERANLPSW WTO   '&MID.PSW  = ******** ********          ILC = *',       C
               MF=L                                            @OZ32566
ERANLPS1 EQU   *-34,8              ABEND PSW   (LEFT HALF)     @OZ32566
ERANLPS2 EQU   *-25,8              ABEND PSW   (RIGHT HALF)    @OZ32566
ERANLPS3 EQU   *-1,1               LENG ABNDING INST IN H-WRDS @OZ32566
ERANPSWL EQU   *-ERANLPSW          LENGTH OF MSG               @OZ32566
         EJECT                                                 @OZ32566
ERANLPCE WTO   '&MID.PCE  = ******** (******) JOB **** ********',      C
               MF=L                                            @OZ32566
ERANLPC1 EQU   *-35,8              PCE NAME                    @OZ32566
ERANLPC2 EQU   *-25,6              PCE ADDRESS                 @OZ32566
ERANLPC3 EQU   *-17,3              'JOB', 'STC', 'TSU'         @OZ32566
ERANLPC4 EQU   *-13,4              JOB NUMBER                  @OZ32566
ERANLPC5 EQU   *-8,8               JOB NAME                    @OZ32566
ERANLPC6 EQU   ERANLPC3,*-ERANLPC3 ENTIRE JOB TEXT             @OZ32566
ERANPCEL EQU   *-ERANLPCE          LENGTH OF MSG               @OZ32566
         SPACE 1                                               @OZ32566
ERANLREG WTO   '&MID.R**  = ******** ******** ******** ******** ',     C
               MF=L                                            @OZ32566
ERANLRG1 EQU   *-(4*9)-6,2         STARTING REG NO. ON THIS LNE@OZ32566
ERANLRG2 EQU   *-(4*9),4*9         REGISTER CONTENTS           @OZ32566
ERANLRNO DC    C'128 4 0 '         REGISTER NUMBERS            @OZ32566
ERANREGL EQU   *-ERANLREG          LENGTH OF MSG               @OZ32566
         SPACE 1                                               @OZ32566
ERANLBOT WTO   '&MID.------------------------------------------',      C
               MF=L                                            @OZ32566
ERANBOTL EQU   *-ERANLBOT           LENGTH OF MSG              @OZ32566
        $MID   080                                             @OZ32566
ERDMPING WTO   '&MID.JES2 SYSTEM DUMP IN PROGRESS',MF=L        @OZ32566
ERDMPNGL EQU   *-ERDMPING           LENGTH OF MSG              @OZ32566
         SPACE 1                                               @OZ32566
        $MID   081                                             @OZ32566
ERDUMPNG WTO   '&MID.JES2 SYSTEM DUMP UNSUCCESSFUL',MF=L       @OZ32566
ERDUMPNL EQU   *-ERDUMPNG           LENGTH OF MSG              @OZ32566
         SPACE 1                                               @OZ32566
ERANLR13 WTO   '&MID.R13 NOT A VALID PCE ADDRESS',MF=L         @OZ32566
ERANR13L EQU   *-ERANLR13           LENGTH OF MSG              @OZ32566
         SPACE 1                                               @OZ32566
        $MID   084                                             @OZ32566
ERANLSVE WTO   '&MID.INVALID SAVE AREA CHAIN',MF=L             @OZ32566
         SPACE 1                                               @OZ32566
        $MID   095                                             @OZ32566
ERRORMSG WTO   '&MID.JES2 CATASTROPHIC ERROR.  CODE = ****  ', @OZ32566C
               MF=L                                            @OZ32566
ERRORME  EQU   *                                               @OZ32566
ERRORMS1 EQU   *-39,39             DEFAULT DUMP TITLE          @OZ32566
ERRORMCD EQU   *-6,4               ABEND CODE                  @OZ32566
ERRORMSL EQU   *-ERRORMSG          LENGTH OF MSG               @OZ32566
         SPACE 1                                               @OZ32566
        $MID   098                                             @OZ32566
ERTERMSG WTOR  '&MID.ENTER TERMINATION OPTION',  ASK OPERATOR  @OZ32566C
               EROPTION,L'EROPTION+L'ERDUMPT,ERRDWORK,MF=L     @OZ32566
ERTERMSL EQU   *-ERTERMSG          LENGTH OF MSG               @OZ32566
EROPTION DS    CL4                 TERMINATION OPTION          @OZ32566
ERDUMPT  DS    0CL100              DUMP TITLE (MAX 100 BYTES ) @OZ32566
ERDUMPT1 DS    CL(L'ERANLAB0)           ABEND AND LOCATION     @OZ32566
         DS    CL4                                             @OZ32566
ERDUMPT2 DS    CL(L'ERANLDS1)           ABEND DESCRIPTION      @OZ32566
         DS    CL2                                             @OZ32566
ERDUMPT3 DS    CL(L'ERANLSY0)           SUBSYS IDENTIFICATION  @OZ32566
         ORG   ERDUMPT+100              PAD OUT FOR 100 BYTES  @OZ32566
ERMSGWRK DS    0F                  ALIGN MSG WORK AREA FULLWRD @OZ32566
         DS    CL100               $$WTO MSG WORK AREA         @OZ32566
EEL      DS    XL(EELSIZE)         ESTAE ELEMENT               @OZ32566
         SPACE 1                                               @OZ32566
         POP   PRINT               RESTORE ASM PRINT STATUS    @OZ32566
         EJECT                                                 @OZ32566
EXITSAVE DS    18F                 SAVE AREA FOR EXIT RTNS     @OZ32566
$ERRORSA DS    0D                                              @OZ32566
         DC    CL8'ERRORSAV'       EYE-CATCHER FOR DUMP        @OZ32566
ERRORSAV DC    16A(*-*)            REGISTERS AT ENTRY          @OZ32566
ERRXSAVE DC    18A(*-*)            SAVE AREA FOR XTERN ROUTINES@OZ32566
ERRLINK  DC    01A(*-*)            LINKAGE REGISTER SAVE AREA  @OZ32566
ERRDWORK DS    D                   DOUBLE WORD SAVE AREA       @OZ32566
         EJECT                                                 @OZ32566
***********************************************************************
*                                                                     *
*        $HEXIT - ENTRY FROM COMMAND PROCESSOR TO WITHDRAW            *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
$HEXIT   BALR  R13,0               RE-ESTABLISH                      R4
         USING *,R13                EXIT                             R4
         TM    HEXITFLG,HEXJES2+HEXSYS ENTERED VIA $PJES2...   @OZ32566
         BNZ   *+8                 BR IF NO TO SKIP NEXT INST  @OZ32566
         OI    HEXITFLG,HEXPJES2    SHOW ENTERED VIA $PJES2    @OZ32566
         L     R13,=A(EXITSAVE)      ROUTINE                         R4
         USING EXITSAVE,R13           ADDRESSABILITY                 R4
         SPACE 1                                                     R4
         OI    $STATUS,$SYSEXIT    TELL DAUGHTERS WE ARE GOING DOWN  R4
         L     R10,$SSVT           POINT TO SSVT                     R4
         USING SSVT,R10                                              R4
         USING SJBDSECT,WA                                           R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        REMOVE THE ATTENTION INDEXES FROM OUR UCBS.                  *
*        FOR 3211 OR 3800 PRINTERS, CLEAR THE ERROR LOG AREA POINTER  *
*        AND RESET THE OPEN DCB COUNT TO ZERO. NOTE THAT THE 3211     *
*        ERROR LOG AREAS WILL BE FREED AUTOMATICALLY BY THE SYSTEM    *
*        (SUBPOOL 255).                                               *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         SPACE 1                                                     R4
IECITMOD EQU   X'18'               HASP ATTENTION INDEX              R4
         SPACE 1                                                     R4
*              THIS LINE DELETED BY APAR NUMBER                @OZ18608
*              THIS LINE DELETED BY APAR NUMBER                @OZ18608
         SLR   WC,WC               SET NEW INDEX                     R4
         L     WE,PSAAOLD-PSA      ADDRESS OF OUR ASCB         @OZ18608
         LH    WE,ASCBASID-ASCB(WE) OUR ASID                   @OZ18608
         L     WA,CVTPTR           POINT TO CVT                      R4
         L     WA,CVTILK2-CVT(,WA) POINT TO UCB LOOK-UP TABLE        R4
         MODESET EXTKEY=ZERO       GET KEY ZERO                      R4
         SPACE 1                                                     R4
         USING UCBDSECT,R14        PROVIDE UCB ADDRESSABILITY  @OZ18608
         SPACE 1                                                     R4
HEXATTNL SR    R14,R14             CLEAR REG FOR UCB INSERT    @OZ18608
         ICM   R14,3,0(WA)         INSERT UCB ADDRESS          @OZ18608
         LA    WA,2(,WA)           UP TO NEXT ENTRY JUST IN CASE     R4
*              THIS LINE DELETED BY APAR NUMBER                @OZ18608
         BZ    HEXATTNL            LOOP IF DUMMY                     R4
         CLM   R14,3,=H'-1'        TEST FOR LAST UCB           @OZ18608
         BE    HEXATTNE            EXIT IF END                       R4
         L     WB,UCBEXTPT         POINT TO UCB EXTENSION            R4
         TM    $SVSTUS,$SVSTUSP    WE ARE THE PRIMARY SUBSYS   @OZ18608
         BO    HEXPRIM             SKIP ALLOC TASK IF YES      @OZ18608
         EJECT                                                 @OZ18608
         CLI   UCBTBYT3,UCB3UREC   LOOP IF NOT                 @OZ18608
         BNE   HEXATTNL             UNIT RECORD                @OZ18608
         TM    UCBSTAT,UCBALOC     LOOP IF                     @OZ18608
         BNO   HEXATTNL             DEVICE IS NOT ALLOCATED    @OZ18608
         CH    WE,UCBASID-UCBCMEXT(WB) LOOP IF DEVICE IS       @OZ18608
         BNE   HEXATTNL                 ALLOC TO SOMEONE ELSE  @OZ18608
         B     HEXEREP             SKIP ATTENTION INDEX TEST   @OZ18608
HEXPRIM  DS    0H                                              @OZ18608
         CLI   UCBATI-UCBCMEXT(WB),IECITMOD IS THE ATTENTION OURS    R4
         BNE   HEXATTNL            LOOP IF NOT                       R4
         SPACE 1                                               @OZ18608
HEXEREP  DS    0H                                              @OZ18608
         L     WB,UCBXTADR              ASSUME                       R4
         LA    WD,UCBERADR-UCBUCS(,WB)  3211 PRINTER UCB             R4
         CLI   UCBTBYT4,UCB3211    TEST ASSUMPTION                   R4
         BE    HEXRERAD            BR IF TRULY 3211                  R4
         LA    WD,UCBMDRBF-UCB3800X(,WB)  ELSE, ASSUME 3800          R4
         CLI   UCBTBYT4,UCB3800    TEST ASSUMPTION                   R4
         BNE   HEXREATI            BR IF NOT 3211 OR 3800            R4
         L     WB,0(,WD)           GET 3800 MDR AREA ADDR            R4
         FREEMAIN R,LV=DYNL3800,A=(WB),SP=245 FREE 3800 MDR AREA    R41
HEXRERAD XC    0(4,WD),0(WD)       RESET LOG AREA & OPEN DCB COUNT   R4
         SPACE 1                                                     R4
HEXREATI TM    $SVSTUS,$SVSTUSP    LOOP IF WE ARE NOT          @OZ18608
         BNO   HEXATTNL             THE PRIMARY SUB SYS        @OZ18608
         IOSGEN TP,UCB=(R14),VAR=ATNMOD,TABLE=(WC) RESET ATTN  @OZ18608
         B     HEXATTNL            LOOP                              R4
         SPACE 1                                                     R4
         DROP R14                  SUSPEND UCB ADDRESSABILITY  @OZ18608
         EJECT                                                       R4
HEXATTNE MODESET EXTKEY=HASP       GET KEY 1                         R4
         SPACE 1                                                     R4
HEXCSML  $GETLOK ,                 SERIALIZE WITH DEQUEUERS          R4
         LM    WA,WB,$SVTSCS       PICK UP CANCEL/STATUS QUEUE       R4
         SPACE 1                                                     R4
HEXSCL   LTR   WA,WA               TEST FOR EMPTY                    R4
         BZ    HEXSCQE             STATUS CANCEL QUEUE EMPTY         R4
         L     R0,SJBTCHN          PICK UP CHAIN                     R4
         LR    R1,WB               COPY HASH                         R4
         CDS   WA,R0,$SVTSCS       DEQUEUE ELEMENT                   R4
         BNE   HEXSCL              LOOP                              R4
         $FRELOK ,                 RELEASE LOCK                      R4
         POST  ,1,MF=(E,SJBECBP)   POST ABNORMAL COMPLETION          R4
         B     HEXCSML             LOOP                              R4
         SPACE 1                                                     R4
HEXPSML  $GETLOK ,                 SERIALIZE WITH DEQUEUERS          R4
         SPACE 1                                                     R4
HEXSCQE  LM    WA,WB,$SVPSOQ       PICK UP PROCESS SYSOUT            R4
         EJECT                                                       R4
HEXPSOL  LTR   WA,WA               TEST FOR EMPTY                    R4
         BZ    HEXPSOE             PSO QUEUE EMPTY                   R4
         L     R0,SJBTCHN          PICK UP CHAIN                     R4
         LR    R1,WB               COPY HASP                         R4
         CDS   WA,R0,$SVPSOQ       DEQUEUE ELEMENT                   R4
         BNE   HEXPSOL             LOOP                              R4
         $FRELOK ,                 RELEASE LOCK                      R4
         POST  ,1,MF=(E,SJBECBP)   POST ABNORMAL COMPLETION          R4
         B     HEXPSML             LOOP                              R4
         SPACE 1                                                     R4
HEXPSOE  CLI   $SVCPOST,X'FF'      ANY TASK WANT A CELL              R4
         BE    HEXNOCEL            BRANCH IF NOT                     R4
         L     WA,$CCAREA          POINT TO CCA                      R4
         USING CCADSECT,WA                                           R4
         MVC   CCAPOST(CCACPL),$SVCPOST COPY REQUEST                 R4
         MVI   $SVCPOST,X'FF'      SET SWITCH                        R4
         $FRELOK ,                 RELEASE LOCK                      R4
         LA    R0,1                SET ABNORMAL POST CODE            R4
         POST  ,(0),MF=(E,CCAPOST) POST USER                         R4
         B     HEXNCEL             SKIP NEXT                         R4
         SPACE 1                                                     R4
HEXNOCEL $FRELOK ,                 RELEASE LOCK                      R4
         DROP  WA                                                    R4
         EJECT                                                       R4
HEXNCEL  CLI   $NUMSMFB,2          TEST SMF BUFFER COUNT             R4
         BL    HEXDTACH            BR IF SMF NOT SUPPORTED           R4
* WRITE STOP HASP SMF RECORD TYPE 45                                 R4
         USING SMFDSECT,R1         ACCESS SMF BUFFER                 R4
         LA    R1,HSMFBUF          POINT TO EXIT SMF 'BUFFER'        R4
         MVI   SMFHDRTY,SMFPSSTP   STOP HASP RECORD TYPE             R4
         MVI   SMFRDW+1,SMF45END-SMFRDW      SIZE OF SMF RECORD      R4
         MVI   SMFSSID+1,SMFHSPID  HASP SUBSYSTEM ID                 R4
         MVI   SMFSSLEN+1,SMF45END-SMF45IND  LENGTH OF SECTION       R4
         TM    HEXITFLG,HEXSYS     IS THIS A NORMAL RETURN...  @OZ32566
         BNO   HNORML              BR, YES THIS IS NORMAL RET  @OZ32566
         OI    SMF45IND,SMF45ABN   NO                                R4
         XR    LINK,LINK           CLEAR LINK                        R4
         ICM   LINK,7,ERRORSAV+5   GET SDWA ADDR. OR ABEND CODE      R4
         LA    WA,12               IS THERE A SDWA                   R4
         C     WA,ERRORSAV         SDWA TEST                         R4
         BNE   HSDWA               YES                               R4
         SRL   LINK,12             SHIFT COMPLETION CODE             R4
         STH   LINK,SMF45CC        PUT COMPLETION CODE INTO SMF      R4
         B     HNORML              WRITE STOP HASP RECORD            R4
         SPACE 1                                                     R4
HSDWA    ICM   WA,7,5(LINK)        GET ABEND CODE FROM SDWA          R4
         SRL   WA,12               SHIFT COMPLETION CODE             R4
         STH   WA,SMF45CC          SAVE ABEND CODE FROM SDWA         R4
         SPACE 1                                                     R4
HNORML   NULL                      WRITE TYPE 45 RECORD              R4
        $QUESMFB                   QUEUE TYPE 45 TO BE WRITTEN       R4
         DROP  R1                                                    R4
         SPACE 1                                                     R4
* DETACH DAUGHTER TASKS                                              R4
         SPACE 1                                                     R4
HEXDTACH LA    WA,$DAWTER          POINT TO 1ST DAUGHTER ELEMENT     R4
         USING DTEDSECT,WA         PROVIDE DTE ADDRESSABILITY        R4
         LA    WB,$DAWTERS         SET NUMBER OF ELEMENTS            R4
         EJECT                                                       R4
HEXDETL  OC    DTETCB,DTETCB       IS TCB ACTIVE                     R4
         BZ    HNOTSK              NO, SKIP DETACH                   R4
         LA    R1,DTEWECB          POINT TO WORK ECB                 R4
         POST  (1)                 POST DAUGHTER                     R4
         LA    R1,DTETECB          POINT TO TERMINATION ECB          R4
         WAIT  ECB=(1)             WAIT                              R4
         LA    R1,DTETCB           POINT TO TCB ADDRESS POINTER      R4
         DETACH (1)                REMOVE TCB                        R4
         SPACE 1                                                     R4
HNOTSK   LA    WA,$DAWTRLN(,WA)    POINT TO NEXT ELEMENT             R4
         BCT   WB,HEXDETL          LOOP                              R4
         EJECT                                                 @OZ26939
*        DETACH 3800 IMAGE-LOADER DAUGHTER TASKS               @OZ26939
         SPACE 1                                               @OZ26939
         USING DCTDSECT,WB         PROVIDE DCT ADDRESSABILITY  @OZ26939
         SPACE 1                                               @OZ26939
         LA    WB,$PRTDCT-(DCTCHAIN-DCTDSECT) LOCAL PRINTERS   @OZ26939
         SPACE 1                                               @OZ26939
ERRNIDET ICM   WB,7,DCTCHAIN+1     ADDR OF NEXT DCT            @OZ26939
         BZ    ERRORRET            BR IF NONE LEFT             @OZ26939
         CLI   DCTDEVTP,DCTPRT     LOCAL PRINTER...            @OZ26939
         BNE   ERRORRET            DONE IF NOT                 @OZ26939
         TM    DCTPPSW2,DCTNIPRT   3800...                     @OZ26939
         BZ    ERRNIDET            BR IF NOT                   @OZ26939
         L     WA,DCTPCE           ADDR OF PCE                 @OZ26939
         ICM   WA,7,PRIMGDTE-PCEDSECT+1(WA)  DTE EXIST...      @OZ26939
         BZ    ERRNIDET            BR IF NO                    @OZ26939
         OC    DTETCB,DTETCB       SUBTASK ACTIVE...           @OZ26939
         BZ    ERRNIDET            BR IF NO                    @OZ26939
         LA    R1,DTEWECB          ADDR OF WORK-ECB            @OZ26939
         POST (1)                  POST HASPIMAG FOR SHUTDOWN  @OZ26939
         LA    R1,DTETECB          ADDR OF TERMINATION-ECB     @OZ26939
         WAIT  ECB=(1)             WAIT FOR SHUTDOWN COMPLETE  @OZ26939
         LA    R1,DTETCB           ADDR OF TCB POINTER         @OZ26939
         DETACH (1)                REMOVE DAUGHTER TASK        @OZ26939
         B     ERRNIDET            LOOP                        @OZ26939
         EJECT                                                 @OZ26939
ERRORRET L     R10,$SSVT           POINT TO SSVT                     R4
         OI    $SVSTUS,$SVSTUST    SET TERMINATION COMPLETE          R4
         L     R1,$SVHECBA         ADDR OF ECB AND $$POST WORK FLAG  R4
         MVI   $SVPOSTW(R1),X'FF'  SET FLAGS FOR POSSIBLE RESTART    R4
         MVC   ERMSGWRK(HEXITMSL),HEXITMSG MOVE MSG TO WORK    @OZ32566
       $$WTO   ERMSGWRK            SHOW TERMINATION COMPLETE   @OZ32566
         SLR   R15,R15             SET ZERO RETURN CODE        @OZ32566
         TM    HEXITFLG,HEXPJES2   $PJES2...                   @OZ32566
         BO    ERRORXIT             EXIT IF YES                @OZ32566
         LA    R15,24                ELSE SET RC               @OZ32566
         SPACE 1                                               @OZ32566
ERRORXIT SVC   3                   EXIT                        @OZ32566
         SPACE 2                                               @OZ32566
HEXITFLG DC    X'00'               EXIT FLAGS                  @OZ32566
HEXJES2  EQU   B'10000000'         JES2 ABEND ($ERROR)         @OZ32566
HEXSYS   EQU   B'01000000'         SYSTEM ABEND                @OZ32566
HEXPJES2 EQU   B'00100000'         $PJES2 ABEND                @OZ32566
         SPACE 2                                               @OZ32566
        $MID   085                                             @OZ32566
HEXITMSG WTO   '&MID.JES2 TERMINATION COMPLETE',MF=L           @OZ32566
HEXITMSL EQU   *-HEXITMSG          LENGTH OF MSG               @OZ32566
         SPACE 1                                               @OZ32566
         DROP  WA,WB,R10,BASE2     KILL ADDRESSABILITY         @OZ32566
         EJECT                                                 @OZ32566
         LTORG                                                 @OZ32566
         SPACE 1                                               @OZ32566
HSMFBUF  DC    XL36'00'            EXIT SMF 'BUFFER'           @OZ32566
         TITLE 'HASP ABEND / $ERROR CODE TABLE'                @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        $ERCODET - HASP ERROR CODE TABLE                      *OZ32566
*                                                              *OZ32566
* FORMAT - CL4'CODE'      - ABEND CODE (S***) OR               *OZ32566
*                           $ERROR CODE ($***)                 *OZ32566
*          AL4(ROUTINE)   - ADDRESS OF ROUTINE TO PROVIDE      *OZ32566
*                           ADDITIONAL INFORMATION             *OZ32566
*          AL1(LEN)       - LENGTH OF ERROR DESCRIPTION TEXT   *OZ32566
*          C'TEXT'        - DESCRIPTION TEXT                   *OZ32566
*                                                              *OZ32566
* NOTE   - MAXIMUM LENGTH OF TEXT IS 35 BYTES                  *OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
         ENTRY $ERCODET            PROVIDE $ERCODET ENTRY      @OZ32566
         SPACE 1                                               @OZ32566
$ERCODET DS    0H                                              @OZ32566
         SPACE 1                                               @OZ32566
 DC C'S0C1',AL4(*-*),AL1(15),C'INVALID OP-CODE'                @OZ32566
 DC C'S0C2',AL4(*-*),AL1(13),C'PRIVILEGED-OP'                  @OZ32566
 DC C'S0C3',AL4(*-*),AL1(18),C'EXECUTE OF EXECUTE'             @OZ32566
 DC C'S0C4',AL4(*-*),AL1(20),C'PROTECTION EXCEPTION'           @OZ32566
 DC C'S0C5',AL4(*-*),AL1(20),C'ADDRESSING EXCEPTION'           @OZ32566
 DC C'S0C6',AL4(*-*),AL1(23),C'SPECIFICATION EXCEPTION'        @OZ32566
 DC C'S0C7',AL4(*-*),AL1(14),C'DATA EXCEPTION'                 @OZ32566
 DC C'S0C9',AL4(*-*),AL1(16),C'DIVISION BY ZERO'               @OZ32566
 DC C'$A01',AL4(*-*),AL1(32),C'TOO MANY CHANNEL-ENDS FOR DEVICE'
 DC C'$B01',AL4(*-*),AL1(33),C'$FREEBUF - BUFFER ADDRESS INVALID'
 DC C'$B02',AL4(*-*),AL1(26),C'$GETBUF - BPMMAP NOT VALID'     @OZ32566
 DC C'$E01',AL4(*-*),AL1(34),C'$EXCP-COUNT LESS THAN CHANNEL-ENDS'
 DC C'$H01',AL4(*-*),AL1(30),C'GETMAIN FOR BUFFER POOL FAILED' @OZ32566
 DC C'$K01',AL4(*-*),AL1(34),C'CKPT BUFFER ALTERED WITHOUT $QSUSE'
 DC C'$K02',AL4(*-*),AL1(34),C'PERMANENT ERROR READING CHECKPOINT'
 DC C'$K03',AL4(*-*),AL1(34),C'DUPL SYS IDS, OR RSRVE/RLSE FAILED'
 DC C'$K04',AL4(*-*),AL1(34),C'PERMANENT ERROR WRITING CHECKPOINT'
 DC C'$K05',AL4(*-*),AL1(32),C'CKPT UPDATE NOT FLAGGED FOR CKPT'
 DC C'$M01',AL4(*-*),AL1(33),C'TOO MANY CHANNEL-ENDS ON RJE LINE'
 DC C'$M02',AL4(*-*),AL1(31),C'ATTEMPT TO OVERLAP RJE LINE I/O'
 DC C'$M03',AL4(*-*),AL1(28),C'SNA/RJE INTERNAL LOGIC ERROR'   @OZ32566
 DC C'$Q01',AL4(*-*),AL1(35),C'JQE SERVICES INVLD ARG OR NO $QSUSE'
 DC C'$Q02',AL4(*-*),AL1(22),C'INVALID JOB QUEUE TYPE'         @OZ32566
 DC C'$R01',AL4(*-*),AL1(22),C'SNA/RJE RPL PROCESSING'         @OZ32566
 DC C'$V02',AL4(*-*),AL1(30),C'PURGE PROCESSOR GETMAIN FAILED'
         SPACE 1                                               @OZ32566
 DC X'FF'                            ** END OF TABLE **        @OZ32566
         TITLE 'HASP PCE NAME TABLE'                           @OZ32566
****************************************************************OZ32566
*                                                              *OZ32566
*        $PCENAMT - HASP PCE NAME TABLE - BASED ON PCEID+1     *OZ32566
*                                                              *OZ32566
* NOTE - PCEMAXID IS THE MAXIMUM PCE ID                        *OZ32566
*      - POINTED TO BY $PCENAMS IN HCT                         *OZ32566
*      - 1ST BYTE OF NAME ENTRY = C' ' - INVALID ID (RESERVED) *OZ32566
*                               = C'*' - DEVICE PROCESSOR      *OZ32566
*                                      - NAME IN ASSOCIATED DCT*OZ32566
*                                                              *OZ32566
****************************************************************OZ32566
         SPACE 1                                               @OZ32566
         ENTRY $PCENAMT            PROVIDE $PCENAMT ENTRY      @OZ32566
         SPACE 1                                               @OZ32566
$PCENAMT DS    0C                PCEID  (SECOND BYTE)          @OZ32566
         DC    CL8'$ASYNC  '       00 - ASYNCH I/O PROCESSOR   @OZ32566
         DC    CL8'*READER '   *   01 - READER PROCESSORS      @OZ32566
         DC    CL8'$SETUP  '       02 - SETUP PROCESSOR        @OZ32566
         DC    CL8'HASPCNVT'       03 - JCL CONVERS PROCESSOR  @OZ32566
         DC    CL8'HASPEXEC'       04 - EXECUTION PROCESSOR    @OZ32566
         DC    CL8'HASPPSO '       05 - PROCESS SYSOUT PROCESS @OZ32566
         DC    CL8'HASPHOPE'       06 - OUTPUT PROCESSOR       @OZ32566
         DC    CL8'*PRINTER'   *   07 - PRINTER PROCESSORS     @OZ32566
         DC    CL8'*PUNCH  '   *   08 - PUNCH PROCESSORS       @OZ32566
         DC    CL8'HASPVPRG'       09 - PURGE PROCESSOR        @OZ32566
         DC    CL8'HASPCOMM'       10 - COMMAND PROCESSOR      @OZ32566
         DC    CL8'HASPMLLM'       11 - LINE MANAGER PROCESSOR @OZ32566
         DC    CL8'$TIMER  '       12 - TIMER PROCESSOR        @OZ32566
         DC    CL8'HASPCKPT'       13 - CHECKPOINT PROCESSOR   @OZ32566
         DC    CL8'HASPGPRC'       14 - PRIORITY AGING PROCESS @OZ32566
         DC    CL8'HASPWARM'       15 - WARM START PROCESSOR   @OZ32566
         TITLE 'HASP INITIAL ENTRY POINT PROCESSING ROUTINE'         R4
***********************************************************************
*                                                                     *
*        HASPINGO -- HASP INITIAL ENTRY POINT PROCESSING ROUTINE      *
*                                                                     *
*        THIS ROUTINE CALLS HASPINIT TO PERFORM HASP INITIALIZATION   *
*        PROCESSING.  ON RETURN FROM HASPINIT, IT CLEANS UP ANY       *
*        REMAINING INITIALIZATION PROCESSING AND EXITS TO THE ASYNCH  *
*        PROCESSOR.  CONTROL THEN, SHORTLY AFTER, PASSES TO THE HASP  *
*        WARM START PROCESSOR TO CONCLUDE HASP PREPARATORY PROCES-    *
*        SING.                                                        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         ENTRY HASPINGO            PROVIDE ENTRY POINT               R4
         USING HASPINGO,BASE2      PROVIDE LOCAL ADDRESSABILITY      R4
         SPACE 1                                                     R4
HASPINGO STM   R14,R12,12(R13)     SAVE SYSTEM'S REGISTERS           R4
         LR    BASE2,R15           ESTABLISH BASE                    R4
         L     R15,=A(EXITSAVE)    POINT TO SAVE AREA                R4
         ST    R15,8(,R13)         STORE FORWARD POINTER             R4
         ST    R13,4(,R15)         STORE BACKWARD POINTER            R4
         LR    R13,R15             SWITCH TO NEW SAVE AREA           R4
         LR    WB,R1               SAVE ORIGINAL R1                  R4
         SPACE 1                                                     R4
         L     BASE1,MAPNUCA       ESTABLISH HCT BASE                R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        OBTAIN SUPERVISOR STATE, SET HASP PROTECT KEY                *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         MODESET MODE=SUP          OBTAIN SUPERVISOR STATE           R4
         MODESET EXTKEY=HASP       SET HASP PROTECT KEY              R4
         EJECT                                                       R4
         ICM   WC,15,MAPINITA      IS HASPINIT ALREADY LOADED...     R4
         BNZ   CALLINIT            BR IF YES                         R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        HASPINIT NOT LOADED -- LOAD IT NOW                           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         LOAD  EPLOC=MAPINIT,ERRET=LOADERR  LOAD HASPINIT            R4
         ST    R0,MAPINITA         SET POINTER TO HASPINIT           R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        CALL HASPINIT TO CONTINUE INITIALIZATION PROCESSING          *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
CALLINIT LR    R1,WB               RESTORE ORIGINAL R1               R4
         L     R15,MAPINITA        POINT TO HASPINIT                 R4
         CALL  (15)                CALL HASPINIT                     R4
         LTR   WC,WC               WAS HASPINIT LOADED...            R4
         BNZ   CLEANUP             BR IF PART OF LOAD MODULE         R4
         DELETE EPLOC=MAPINIT       ELSE DELETE HASPINIT             R4
         ST    WC,MAPINITA         DELETE HASPINIT REP TABLE ENTRY   R4
         EJECT                                                       R4
***********************************************************************
*                                                                     *
*        CLEAN UP INITIALIZATION PROCESSING                           *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
CLEANUP  L     R1,=A($QGETAFF)     SET MASK FOR 'TM'                 R4
         MVC   1(1,R1),$SIDAFF      OPERATION IN $QGET ROUTINE       R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        END OF BASIC INITIALIZATION -- ENTER ASYNCH PROCESSOR        *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
         L     R14,4(,SAVE)        GET ADDRESS OF SYSTEM SAVE AREA   R4
         L     SAVE,$ASYNCP        GET ASYNCH PROCESSOR PCE ADDRESS  R4
         ST    R14,4(,SAVE)        SET BACKWARD CHAIN                R4
         ST    SAVE,8(,R14)        SET FORWARD CHAIN                 R4
         LM    LINK,R12,12(SAVE)   LOAD PCE REGISTERS                R4
         BR    R15                  AND ENTER ASYNCH PROCESSOR       R4
         SPACE 1                                                     R4
***********************************************************************
*                                                                     *
*        UNABLE TO LOAD HASPINIT -- INFORM OPERATOR AND QUIT          *
*                                                                     *
***********************************************************************
         SPACE 1                                                     R4
LOADERR $$WTO  LOADMSG             ISSUE ERROR MESSAGE TO OPERATOR   R4
        $$WTO  QUITMSG             ISSUE QUIT MESSAGE TO OPERATOR    R4
         L     R13,4(,R13)         POINT TO SYSTEM SAVE AREA         R4
         LM    R14,R12,12(R13)     RESTORE SYSTEM REGISTERS          R4
         LA    R15,20              SET RETURN CODE                   R4
         BR    R14                  AND RETURN                       R4
         EJECT                                                       R4
        $MID   428                 SET MESSAGE ID                    R4
QUITMSG  WTO   '&MID.CORRECT THE ABOVE PROBLEMS AND RESTART JES2',MF=L
         SPACE 1                                                     R4
        $MID   448                 SET MESSAGE ID                    R4
LOADMSG  WTO   '&MID.UNABLE TO LOAD MODULE HASPINIT',MF=L            R4
         SPACE 2                                                     R4
         LTORG                                                       R4
         SPACE 5                                                     R4
         ORG   HASPNUC+(*-HASPNUC+31)/32*32  32-BYTE ALIGNMENT       R4
         SPACE 2                                                     R4
        $MODMAP DSECT=NO           GENERATE HASP MODULE MAP          R4
         SPACE 1                                                     R4
         DROP  ,                   SUSPEND ADDRESABILITY       @OZ20685
         TITLE 'HASP ALLOC/UNALLOC SERVICE -- ALLOCATION TASK' @OZ20685
         SPACE 1                                               @OZ20685
*************************************************************  @OZ20685
*                                                              @OZ20685
*        HASP DYNAMIC ALLOCATION/UNALLOCATION SUBTASK          @OZ20685
*                                                              @OZ20685
*************************************************************  @OZ20685
         SPACE 1                                               @OZ20685
HOSALLOC $ENTRY BASE=R15           ALLOCATION MAIN ENTRY       @OZ20685
         DROP  R15                 SUSPEND LOCAL ADDRESSABILITY@OZ20685
         SAVE  (14,12)             SAVE CALLER'S REGISTERS     @OZ20685
         LR    BASE2,R15           ESTABLISH BASE REGISTER     @OZ20685
         USING HOSALLOC,BASE2      LOCAL ADDRESSABILITY        @OZ20685
         LR    BASE1,R1            RELOAD HCT ADDRESS          @OZ20685
         USING HASP,BASE1          HCT ADDRESSABILITY          @OZ20685
         ST    R13,ALLOCSAV+4      STORE BACKWARD POINTER      @OZ20685
         LR    R4,R13              HOLD CALLER'S SAVE ADDRESS  @OZ20685
         LA    R13,ALLOCSAV        ADDRESS MY SAVE AREA        @OZ20685
         ST    R13,8(,R4)          STORE FWD POINTER           @OZ20685
         SPACE 1                                               @OZ20685
*        SIGNAL ATTACHOR THAT DYNAMIC ALLOCATE SUBTASK IS UP.  @OZ20685
         SPACE 1                                               @OZ20685
         POST  $PDYNECB            POST ATTACHOR               @OZ20685
         SPACE 1                                               @OZ20685
*        WAIT FOR WORK POST OR $PJES2 POST                     @OZ20685
         SPACE 1                                               @OZ20685
ALWAIT   DS    0H                                              @OZ20685
         WAIT  ECB=$DYNECB         WAIT FOR WORK OR $PJES2 PST @OZ20685
         SLR   R1,R1               CLEAR R1                    @OZ20685
         ICM   R1,7,$DYNECB+1      IS WORK ECB POSTED          @OZ20685
         MVI   $DYNECB,0           RESET ECB FOR NEXT USER     @OZ20685
         BZ    ALTERM              BRANCH IF NO, SHUTDOWN      @OZ20685
         SPACE 1                                               @OZ20685
*        DYNAMIC ALLOCATION                                    @OZ20685
         SPACE 1                                               @OZ20685
         LR    WD,R1               GET WORK AREA ADDRESS       @OZ20685
         DYNALLOC                  CALL DYN ALLOC/UNALLOC      @OZ20685
         LR    R1,WD               GET WORK AREA ADDRES AGAIN  @OZ20685
         LTR   R15,R15             NORMAL RETURN...            @OZ20685
         BZ    ALLOK               BR IF YES                   @OZ20685
         MVI   0(R1),X'41'         SET RETURN CODE             @OZ20685
         B     *+8                 GO POST JES2                @OZ20685
ALLOK    MVI   0(R1),X'7F'         SET RETURN CODE             @OZ20685
       $$POST  TYPE=ALOC,R11=HCT   POST JES2                   @OZ20685
         B     ALWAIT              WAIT FOR POST               @OZ20685
         SPACE 1                                               @OZ20685
*        SHUTDOWN ASSUMED -- $PJES2 POST                       @OZ20685
         SPACE 1                                               @OZ20685
ALTERM   DS    0H                                              @OZ20685
         L     R13,ALLOCSAV+4      RESTORE CALLERS  R13        @OZ20685
         RETURN (14,12),RC=0       RETURN WITH ZERO COMP CODE  @OZ20685
         SPACE 1                                               @OZ20685
ALLOCSAV DS    18F                 ALLOCATION SAVE AREA        @OZ20685
         SPACE 1                                               @OZ20685
         LTORG                     ALLOC TASK LITERAL POOL     @OZ20685
         SPACE 5                                               @OZ20685
$NUCLEN $DLENGTH                   COMPUTE CONTROL SECTION LENGTH    R4
         SPACE 5                                                     R4
APARNUM  DC    CL5'33184'          APAR NUMBER
         END   HASP                ENTRY POINT FOR HASP SYSTEM       R4
