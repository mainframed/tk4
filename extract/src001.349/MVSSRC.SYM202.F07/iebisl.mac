 TITLE 'IEBISL-IEBISAM UTILITY PROGRAM - LOAD ROUTINE '
*STATUS: CHANGE LEVEL 000                                             *
*FUNCTION/OPERATION: THIS ROUTINE RECONSTRUCTS AN INDEXED SEQUENTIAL  *
*   DATASET FROM THE INFORMATION CONTAINED IN AN UNLOADED VERSION OF  *
*   THE DATASET. FOR EACH OF THE FOLLOWING FIELDS,THE CORRESPONDING   *
*   VALUE OBTAINED FRON THE DATASET WHEN IT WAS UNLOADED WILL BE EM-  *
*   PLOYED IN RECONSTRUCTING THE NEW DATASET: OPTCD,RECFM,LRECL,BLK-  *
*   SIZE,RKP,NTM,KEYLEN,AND CYLOFL.                                   *
*                                                                     *
*INPUT: LENGTH AND ADDRESS OF THE RECORD TO BE CONSTRUCTED. DATA      *
*   PASSED BY LOCATION AND A RETURN CODE.                             *
*                                                                     *
*   REGISTER 13 MUST CONTAIN THE ADDRESS OF THHE CALLING PROGRAM'S    *
*   SAVE AREA,REGISTER 15 MUST CONTAIN THE ADDRESS OF THIS MODULE     *
*   FOR EVERY ENTRY TI THIS MODULE(EXCEPT FROM IEBISSI).
*                                                                     *
*OUTPUT: INDEXED SEQUENTIAL DATASET,COMPLETION CODE OR MESSAGE NUMBER *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*   IEBISSI-TO GET THE LENGTH AND ADDRESS OF THE RECORD.              *
*                                                                     *
*EXITS-NORMAL:                                                        *
*   IEBISF- AFTER THE DATASET HAS BEEN RECONSTRUCTED SUCCESSFULLY.    *
*                                                                     *
*      -ERROR:                                                        *
*   IEBISF- WHEN EVER ANY ERROR CONDITIONS ARE ENCOUNTERED FOR INPUT. *
*   AND/OR OUTPUT WITH A MESSAGE NUMBER AND/OR A COMPLETION CODE      *
*                                                                     *
*ATTRIBUTES: REENTRANT                                             000G
*                                                                     *
*TABLES/WORKAREAS:                                                    *
*   QISAM- WORK AREA FOR DCB INFORMATION MAINTAINED WHEN THE DATASET  *
*   WAS UNLOADED.                                                     *
*                                                                     *
***********************************************************************
*
*  REGISTER ASSIGNMENTS
*
***********************************************************************
BASE     EQU   10                  BASE REGISTER
MADDR    EQU   15                  ADDRESSIBILITY REGISTER
SAVERG   EQU   13                  SAVE AREA REGISTER
PARAM1   EQU   1                   PARAMETER REGISTER
PARAM0   EQU   0                   PARAMETER REGISTER-LIMIT
RETREG   EQU   14                  RETURN REGISTER
LINKRG   EQU   15                  LINK REGISTER
WORKAREA EQU   11                       WORKAREA REGISTER          000G
WK3      EQU   3                   MESSAGE NUMBER REGISTER
WK4      EQU   4                   COMMON AREA ADDRESS REGISTER
WK2      EQU   2                   DCB ADDRESSING REGISTER
WK6      EQU   6                   UNLOADED DCB ADDRESSING REGISTER
WK8      EQU   8                   EVEN REGISTER FOR DIVISION
WK5      EQU   5                   WORK REGISTER
WK9      EQU   9                  ODD REGISTER FRO DIVISION
RCODE    EQU   15                  RETURN CODE REGISTER
*
***********************************************************************
         EJECT
IEBISL   CSECT
***********************************************************************
*                  FIXES THIS MODULE                                  *
*                     LATEST FIRST                                    *
***********************************************************************
*
*A 404500-405000                             @XA16077=@YA15383=@ZA18349
*C 135000                                    @XA16077=@YA15383=@ZA18349
*D 371200-375200                             @YA13318=@XA14197=@ZA10932
*C 136000,369600                             @YA13318=@XA14197=@ZA10932
*A 134500-135000,370900-374400               @YA13318=@XA14197=@ZA10932
*
*A334400-335000                                                 OY01167
*D334400-335300                                                 OY01167
*
*A221000,222200,520000-520400,521200,535000,536000,538300,      OY01166
*A538600,541000                                                 OY01166
*D521200-526000,520000                                          OY01166
*
*0516                                                              000H
* 342000                                                         A32042
*A222100-224000                                                  A41745
*A159000,344000,520000-521000                                    A45167
*D344000,520000                                                  A45167
*A212500-212560,326500,327000,340500-341500,500500,500600,502900 A48756
*A510000-514708                                                  A48756
*C318000,368000,378000,424000,476000,500800                      A48756
*D328000,334000,380000-384000,428000,462000,480000-484000        A48756
*A212600,327500-334400,335300-335440,266000                      A52028
*D212500-218000,222100-222200,266000,334400,335600,653500        A52028
***********************************************************************
         EJECT
         SAVE  (14,12),T,ISL-V1-66           SAVE REGISTERS
         USING IEBISL,BASE                   ESTABLEIH BASE REGISTER
         LR    BASE,MADDR                     REGISTER
         B     APARNO                   BRANCH AROUND APAR NO  @ZA10932
         DC    C'IEBISL OZ18349'        LAST APAR THIS MODULE  @ZA18349
APARNO   LR    WORKAREA,PARAM1          PICK UP WORK AREA PTR  @ZA10932
         USING IEBWORK,WORKAREA
         ST    SAVERG,SAVE1BK           SET BACK CHAIN POINTER     000G
         LR    SAVERG,WORKAREA          UPDATE TO NEW SAVE AREA    000G
         LA    WK2,SAVE2                SET UP FORWRD CHAIN POINTER000G
         ST    WK2,SAVE1FD                 TO NEXT SAVE AREA       000G
***********************************************************************
*
*  THIS SECTION PASSES THE ADDRESS OF THE IEHISR COMMON WORK AREA TO  *
*THE INPUT ROUTINE IEHISSI.                                           *
*                                                                     *
***********************************************************************
         SR    PARAM0,PARAM0                 INDICATE INITIAL ENTRY
         LR    PARAM1,WORKAREA          LOAD COMMON AREA ADDRESS   000G
         MVI   ISSWITCH,X'00'      ZERO SWITCH BEFORE TESTING    A45167
TOWARD   L     LINKRG,VSSI                   LOAD IEHISSI ADDRESS
         BALR  RETREG,LINKRG                 BRANCH TO IEHISSI
***********************************************************************
*                                                                     *
*  THIS SECTION DETERMINES THE RETURN CODE PASSED BY IEHISSI, FOR ANY *
*RETURN CODE DIFFERENT FROM ZERO, A MESSAGE NUMBER IS SET IN THE IS-  *
*MESS FIELD OF THE IEBISR COMMON WORK AREA OR A COMPLETION CODE IS    *
*PLACED IN THE ISCOMP FIELD,THE OUTPUT DATASET IS CLOSED,SAVE AREA IS *
*POPPED UP AND AN EXIT IS MADE TO ISF.                                *
*                                                                     *
***********************************************************************
         STC   RCODE,DOUBLE+7           STORE RETURN CODE          000G
         CLI   DOUBLE+7,X'00'           IS RETURN CODE 0           000G
         BE    COMPET                        YES,BRANCH
         CLI   DOUBLE+7,X'04'           IS RETURN CODE 4           000G
         BE    SETA                          YES BRANCH
         CLI   DOUBLE+7,X'08'           IS RETURN CODE 8           000G
         BE    SETAS                         YES,BRANCH
         CLI   DOUBLE+7,X'12'           IS RETURN CODE 12          000G
         BE    SETAL                         YES,BRANCH
         CLI   DOUBLE+7,X'16'           IS RETURN CODE 16          000G
         BE    SETAL1                        YES,BRANCH
         CLI   DOUBLE+7,X'20'           IS RETURN CODE 20          000G
         BE    SETSW                         YES,BRANCH
         CLI   DOUBLE+7,X'24'           RETURN CODE 24             000G
         BE    SETAL2                   INVALID DCB PARAMETERS     000G
         LA    WK3,0                   MESSAGE NO = 0  NO ERRORS   000G
FINI     STH   WK3,REENTAR+32           STORE MESSAGE NUMBER       000G
         MVC   ISMESS(2),REENTAR+32     MOVE NUMBER TO ISMESS      000G
         B     SHORTBR                  TEST FOR ERRORS          A52028
MITE     MVC   REENTAR(OPENL-CLOSEL),CLOSEL   MOVE L FORM OF CLOSE 000G
         OI    ISMESS,X'10'       SET FLAG FOR SYSNAD ROUTINE   OY01166
         CLOSE (DCBAR2),MF=(E,REENTAR) CLOSE OUTPUT DATA SET       000G
         NI    ISMESS,X'00'       SET OFF FLAG FOR SYSNAD ROUT. OY01166
         FREEPOOL DCBAR2                FREEPOOL OUTPUT BUFFER   A41745
BYPASS   SR    WK3,WK3                  SET UP XCTL AREA         A41745
         ST    WK3,XCTLAREA+4                                      000G
         MVI   WHICH+6,C' '             MOVE INTO XCTLAREA NAME OF 000G
         MVI   WHICH+5,C'F'                NEXT MODULE - IEBISF    000G
         L     SAVERG,4(0,SAVERG)            POP UP SAVE AREA
         L     RETREG,12(0,SAVERG)           RESTORE REGISTER FOURTEEN
         LR    PARAM1,WORKAREA          LOAD ADDRESS OF WORKAREA   000G
         XCTL  (2,12),MF=(E,(1)),SF=(E,XCTLAREA)  BRANCH TO IEHISF 000G
SETA     LA    WK3,2                   MESSAGE NUMBER = 2, SYN-    000G
         B     FINI                           CHRONOUS ERROR-INPUT
SETAS    LA    WK3,0                         MESSAGE NUMBER=20, OPERA-
         B     FINI                           TION COMPLETED
SETAL    LA    WK3,4                   MESAGE NUMBER = 4, LIMIT    000G
         B     FINI                           EXCEEDED
SETAL1   LA    WK3,9                    MESSAGE NUMBER=9,INPUT     000H
         B     FINI                           OUT OF SEQUENCE
SETAL2   LA    WK3,1                    INVALID DCB PARAMETERS     000G
         B     FINI                  C                             000G
SETSW    MVC   ISCOMP(2),H16            COMPLETION CODE = 16       000G
         LA    WK3,7                    SET MESSAGE FOR MISSING DD 000G
         STH   WK3,ISMESS                                          000G
         B     BYPASS                   SYSUT1 NOT OPEN,SO BYP.  A52028
***********************************************************************
*                                                                     *
*  THIS SECTION, UPON FIRST RETURN FROM IEHISSI,PASS THE LENGTH OF THE*
*ISAM DCB AS THE LIMIT AND THE ADDRESS OF 'QISAM',FOR THE RETRIEVAL OF*
*THE ISAM DCB WRITTEN OUT WHEN DATA SET WAS UNLOADED.                 *
*                                                                     *
***********************************************************************
COMPET   LTR   PARAM0,PARAM0                 FIRST RETURN
         BNE   TSTALL                        NO,BRANCH
         LA    PARAM0,154
         LA    PARAM1,QISAM                  LOAD QISAM ADDRESS
         MVI   DOUBLE+6,X'FF'           INDICATE OUTPUT NOT OPEN   000G
         B     TOWARD                        GO GET WRITTEN DCB
TSTALL   CLI   DOUBLE+6,X'FF'           OUTPUT OPEN                000G
         BNE   RESUME1
***********************************************************************
*                                                                     *
*  THIS SECTION NOVES THE DDNAME FROM IEHISR ISUT2 FIELD INTO THE OUT-*
*PUT DCB,AND OPENS THE DATASET. A CHECK FDR A SUCCESSFUL OPEN IS MADE.*
*                                                                     *
***********************************************************************
         MVC   DCBAR2(LABEL-DCBISL),DCBISL                         000G
         USING IHADCB,WK2
         LA    WK2,DCBAR2                                          000G
         MVC   DCBDDNAM(8),ISUT2        MOVE DDNAME FROM ISUT2     000G
         MVC   REENTAR(EIGHT-OPENL),OPENL   MOVE L FORM OF OPEN TO 000G
*                                               WORKAREA           000G
         OPEN  (DCBAR2,(OUTPUT)),MF=(E,REENTAR) OPEN OUTPUT DATASET000G
         TM    DCBOFLGS,X'10'                WAS OPEN SUCCESSFUL
         BZ    SETDOUBL           NO,BRANCH                      A48756
***********************************************************************
*  THIS SECTION CHECKS TO SEE IF THE INCONSISTENCY INDICATOR FOR THE  *
*SPECIFIED FIELDS IN THE OUTPUT DCB IS ON. IF SO A MESSAGE NUMBER IS  *
*SET AND A BRANCH TO CLOSE AND EXIT IS TAKEN.                         *
*                                                                     *
***********************************************************************
         B     RESUME             CONT, SYSUT2 DCB ERROR WILL    A48756
*                                 BE CHECKED IN IEBISSI          A48756
SHORTBR  LTR   WK3,WK3            MESSAGE NUMBER 0 ?             A52028
         BNZ   CHSYSUT2           NO, BRANCH                     A52028
         TM    DCBOFLGS,X'10'     SYSUT2 DCB OPEN ?             OY01167
         BO    MITE               YES, CLOSE SYSUT2 DATA SET    OY01167
         MVI   ISMESS+1,X'09'     SET MSG NBR --OUT OF SEQ--    OY01167
CHSYSUT2 MVC   ISCOMP(2),EIGHT          COMPLETION CODE = 8      A52028
         TM    DCBOFLGS,X'10'      SYSUT2 OPENED ?               A52028
         BZ    BYPASS              NO, DO NOT CLOSE SYSUT2       A52028
         B     MITE                YES, CLOSE SYSUT2             A52028
***********************************************************************
*  THIS SECTION GETS THE ADDRESS OF THE NEXT AVAILABEL OUTPUT AREA AND*
*LOADS THE LIMIT FOR IEBISSI.                                         *
*                                                                     *
***********************************************************************
SETDOUBL MVI   DOUBLE+7,X'20'     SET ERROR BIT, NO OPEN UT2     A48756
         LA    PARAM0,100         SET REG 0 UNEQ. ZERO           A48756
         B     TOWARD             BRANCH TO IEBISSI              A48756
RESUME   MVI   ISSWITCH,X'FF'      MARK SYSUT2 OPEN              A45167
         LH    PARAM0,DOUBLE+4          LOAD LIMIT ADDRESS IN 1    000G
         AH    PARAM0,REENTAR+34        ADD KEYLENGTH TO RECORD    000G
         B     TOWARD
***********************************************************************
*  THIS IS THE OUTPUT DCB EXIT. THE NTM,CYLOFL AND OPTCD DCB FIELDS OF*
*THE UNLOADED DATASET ARE COPIED IN THE RESPECTIVE FIELDS OF THE NEW  *
*DATASET. IF THE RECORD FORMAT IN THE UNLOADRD(OLD) AND NEW DATASETS  *
*IS NOT THE SAME AND THE LOGICAL RECORD LENGTH FIELD OF THE NEW DATA  *
*SET IS GREATER THAN THAT OF THE OLD, A BRANCH IS MADE TO INDICATE IN-*
*CONSISTENCY. IF NO BLKSIZE IS SPECIFIED FOR THE NEW DATASET THE      *
*LOGICAL RECORD LENGTH IS USED AS THE LIMIT.  IF RECFM SPECIFIES      *
*VARIABLE THEN THE RKP MUST BE EQUAL TO OR GREATER THAN 4             *
***********************************************************************
ISLEXT   LA    WK6,QISAM                     ADDRESS OF UNLOADED DCB
         XC    DOUBLE+6(2),DOUBLE+6     SET CONSIST FLAGS TO 0   A48756
         OC    DCBNTM(1),DCBNTM         NTM SPECIFIED              000G
         BNZ   OPTCD                    YES, NTM SPECIFIED     @ZA10932
         MVC   DCBNTM(1),54(WK6)        GET OLD NTM FIELD          000G
OPTCD    OC    DCBOPTCD(1),DCBOPTCD     OPTCD SPECIFIED        @ZA10932
         BNZ   CYLOF                    YES, OPTCD SPECIFIED   @ZA10932
         MVC   DCBOPTCD(1),52(WK6)      GET OLD OPTCD          @ZA10932
CYLOF    TM    DCBOPTCD,X'08'           OTPCD=Y SPECIFIED      @ZA10932
         BZ    TLRECL                   NO,THEN NO CYLOF       @ZA10932
         OC    DCBCYLOF(1),DCBCYLOF     CYLOF SPECIFIED        @ZA10932
         BNZ   TLRECL                   YES, CYLOF SPECIFIED   @ZA10932
         MVC   DCBCYLOF(1),55(WK6)      GET OLD CYLOF          @ZA10932
TLRECL   CLC   DCBLRECL(2),ZERO        LRECL FIELD = ZERO?         000G
         BE    MVERECL                  YES,USE UNLOADED VALUE   A48756
***********************************************************************
*                                                                     *
*  THIS SECTION DETERMINES THE RECORD FORMAT. IF FIXED, AND THE BLOCK *
*SIZE IS NOT A MULTIPLE OF THE LOGICAL RECORD LWNGTH,A BRANCH IS MADE *
*TO INDICATE INCONSISTEMCY. OTHERWISE,THE LOGICAL RECORD LENGTH IS    *
*USED AS THE LINIT.                                                   *
***********************************************************************
TBLKSI   LH    WK9,DCBBLKSI                  LOAD BLOCKSIZE
         LTR   WK9,WK9
         BZ    MVEBLK                        YES,USE UNLOADED VALUE
TRECFM   CLI   DCBRECFM,X'00'                RECFM FIELD=0?
         BE    MVEFM                         YES,BRANCH
         TM    DCBRECFM,X'08'           NEW RECFM VBS OR FBS   @ZA18349
         BO    NONCON                   YES, INVALID FOR ISAM  @ZA18349
CHECKF   TM    DCBRECFM,X'80'           NEW RECFM FIXED            000G
         BZ    TVARY                         NO,BRANCH
         TM    36(WK6),X'80'                 OLD RECFM FIXED?
         BO    TMULTPLE                      YES,BRANCH
         MVI   DOUBLE+6,X'01'           SET INVALID INDICATOR      000G
TMULTPLE LH    WK5,DCBLRECL                  LOAD LOGICAL RECORD LENT
         SR    WK8,WK8
         DR    WK8,WK5                       IS BLOCK SIZE A MULTIPLE
         LTR   WK8,WK8                        OF LOGICAL RECORD LENGTH
         BE    TKEYLFLD
         MVI   DOUBLE+6,X'01'           SET INVALID INDICATOR      000G
         B     TKEYLFLD
MVEFM    MVC   DCBRECFM(1),36(WK6)           GET OLD REFM FIELD
         B     CHECKF                                              000G
MVERECL  MVC   DCBLRECL(2),82(WK6)           GET OLD LRECL VALUE
         B     TBLKSI
MVEBLK   MVC   DCBBLKSI(2),62(WK6)           GET OLD BLKSIZE
         LH    WK9,DCBBLKSI                  LOAD BLOCKSIZE
         B     TRECFM
***********************************************************************
*                                                                     *
*  THIS SECTION USES THE BLKSIZE AS THE LIMIT FOR RECORDS OF VARIABLE *
*FORMAT,IF THE BLOCK SIZE IS GREATER THAN OR EQUAL TO THE LOGICAL RE- *
*CORD LENGTH.                                                         *
***********************************************************************
TVARY    TM    36(WK6),X'40'                 OLD RECFM VARIABLE?
         BZ    NONCON                        NO,BRANCH
VARBLE   CH    WK9,DCBLRECL                  BLKSI GREATER THAB OR
         BL    NONCON                         EQUAL TO LRECL? NO,BRANCH
***********************************************************************
*                                                                     *
*   THIS SECTION DETERMINES IF THE RKP IS GREATER THAN THE LGICAL RE- *
*CORD LENGTH MINUS THE KEYLEN. IF SO, THE INCONSISTENCY INDICATOR IS  *
*SET.AND A RETURN TO THE OPEN ROUTINE IS MADE.                        *
*                                                                     *
***********************************************************************
TKEYLFLD CLI   DCBKEYLE,X'00'                KEYLEN FIELD ZERO?
         BNE   TMINUS                   NO, DON'T USE OLD KEYLE  A48756
         MVC   DCBKEYLE(1),16(WK6)           USE OLD KEYLEN
TMINUS   LH    WK8,DCBLRECL                  LOAD LOGICAL RECRD LENGTH
         SR    WK5,WK5
         IC    WK5,DCBKEYLE                  GET KEY LENGTH
         STC   WK5,REENTAR+35
         SR    WK8,WK5                       SUBTRACT KEYLE FROM LRECK
         LH    WK5,DCBRKP                    LOAD ELATIVE KEY POSITION
         LTR   WK5,WK5                       RKP=0?
         BZ    TSTOLD
TRKP     CLI   DCBRECFM,X'80'     SYSUT2 HAS RECFM F ?           A48756
         BNE   RKPTEST            NO, CHECK FOR VALID RKP        A48756
         LTR   WK5,WK5                  IS RKP=0                 A28558
         BZ    RTOPEN                   YES, DO NOT SET          A28558
*                                       NONCONSISTENCY INDICATOR A28558
RKPTEST  EQU   *                                                 A48756
         CH    WK8,DCBRKP               RKP FIELD GT             A28558
*                                             LRECL MINUS THE KEYLEN
         BNL   RTOPEN                        NO, BRANCH
NONCON   MVI   DOUBLE+6,X'01'           SET NONCONSISTENCY INDICATR000G
RTOPEN   CLI   DOUBLE+6,X'00'     DCB ERROR ENCOUNTERED ?        A48756
         BNE   ERRDCB             YES, RETURN TO OPEN            A48756
         TM    DCBRECFM,X'C0'     RECFM SYSUT2 U ?               A48756
         BO    ERRDCB1            YES, PRINT ERRMSG IEB601I      A48756
         TM    36(WK6),X'40'      RECFM SYSUT1 V ?               A48756
         BZ    FIXRECFM           NO, RECFM IS F/FB              A48756
         CLC   DCBLRECL(2),82(WK6) LRECL UT2 < LRECL UT1 ?       A48756
         BL    ERRDCB1            YES, ERROR                     A48756
         LH    WK5,DCBLRECL       LOAD UT2 LRECL                 A48756
         LA    WK5,4(WK5)         ADD 4 TO UT2 LRECL             A48756
         CH    WK5,DCBBLKSI       LRECL+ 4 > BLKSI ?             A48756
         BC    12,RETURN2         NO, OKE                        A48756
         STH   WK5,DCBBLKSI       SET DEFAULT BLKSI              A48756
         B     ERRDCB1            SET ERRBIT                     A48756
TSTL1L2  CLC   DCBLRECL(2),82(WK6) UT1 LRECL EQ. UT2 LRECL ?     A48756
         BNE   ERRDCB1            NO, PRINT ERRMSG IEB601I       A48756
         B     RETURN2            RETURN TO OPEN SYSUT2          A48756
FIXRECFM SR    WK5,WK5            ZERO REGISTER 5                A48756
         IC    WK5,16(WK6)        LOAD KELENGTH IN REGISTER 4    A48756
         LH    WK8,82(WK6)        LOAD UT2 LRECL IN REGISTER 6   A48756
         CLC   60(2,WK6),ZERO     UT1 RKP=0 ?                    A48756
         BNE   TSTRKP2            NO, TEST DCB VALUES            A48756
         CLC   DCBRKP(2),ZERO     UT2 RKP=0 ?                    A48756
         BNE   TSTRECF1           NO, TEST UT1 DCB VALUES        A48756
         CLC   DCBRECFM(1),36(WK6) RECFM UT1 EQ. RECFM UT2       A48756
         BE    TSTL1L2            YES, TEST FOR EQUAL LRECL      A48756
         TM    36(WK6),X'90'      UT1 RECFM = F ?                A48756
         BM    COMPL1L2           YES, COMPARE UT1 & UT2 LRECL'S A48756
NOEQL1L2 SR    WK8,WK5            SET WK6 : LRECL1 MINUS KEYLEN1 A48756
         CH    WK8,DCBLRECL       LRECL2 EQ.TO LRECL1 MINUS KEY? A48756
         BC    8,RETURN2          YES, OKE RETURN TO OPEN UT2    A48756
         STH   WK8,DCBLRECL       SET LRECL2 = LRECL1 MINUS KEY1 A48756
         STH   WK8,DCBBLKSI       SET BLKS2 = LRECL1 MINUS KEY1  A48756
         B     RETURN2            RETURN TO OPEN SYSUT2          A48756
COMPL1L2 AR    WK8,WK5            SET WK6: LRECL1 PLUS KEYLEN1   A48756
         CH    WK8,DCBLRECL     UT2(RCFM=FB) EQ. LRC1 PLUS KEY ? A48756
         BC    8,RETURN2          YES, RETURN TO OPEN SYSUT2     A48756
         STH   WK8,DCBLRECL       LRECL2=LRECL1 PLUS KEYLEN1     A48756
         MR    WK8,WK8            MULTIPLY R9 AND R8             A48756
         STH   WK9,DCBBLKSI       BLKSI2= R9 * (LRECL1 PLUS KEY) A48756
         B     RETURN2            RETURN TO OPEN SYSUT2          A48756
TSTRKP2  CLC   DCBRKP(2),ZERO     UT2 RKP=0 ?                    A48756
         BNE   TSTL1L2            NO, TEST FOR EQUAL LRECL'S     A48756
         TM    DCBRECFM,X'90'     UT2 RECFM=FB ?                 A48756
         BO    TSTL1L2            YES, TEST FOR EQUAL LRECL'S    A48756
         B     NOEQL1L2           NO, CHECK FOR UNEQUAL LRECL'S  A48756
TSTRECF1 TM    36(WK6),X'90'      UT1 RECFM = FB ?               A48756
         BO    TSTL1L2            YES, TST FOR EQ. LRECL'S       A48756
         B     COMPL1L2           NO, CHECK FOR UNEQUAL LRECL'S  A48756
ERRDCB   MVI   DCBKEYLE,X'10'        SET DEFAULT                 A48756
         MVI   DCBRECFM,X'80'        VALUES                      A48756
         MVC   DCBRKP(2),ZERO        TO ASSURE CORRECT           A48756
         NI    DCBBLKSI,X'00'        OPENING                     A48756
         MVI   DCBBLKSI+1,X'50'      OF SYSUT2                   A48756
         MVC   DCBLRECL(2),DCBBLKSI  DATA SET.                   A48756
ERRDCB1  MVI   DOUBLE+6,X'01'        PRINT ERRMSG IEB601I        A48756
RETURN2  MVC   DOUBLE+4(2),DCBLRECL  USE LRECL AS LIMIT          A48756
         RETURN
         DS    0F
TESTS    DC    X'85'
         DC    AL3(ISLEXT)
***********************************************************************
*  THIS IS THE OUTPUT DATASET SYNAD ROUTINE                           *
***********************************************************************
MESS     TM    ISMESS+1,X'FF'      ALREADY MESS NBR SET ?       OY01166
         BCR   4,RETREG           YES, RETURN TO CLOSE          OY01166
         TM    ISMESS,X'10'       ARE WE IN CLOSE ?             OY01166
         BC    1,DONTRES          YES, DONT RESTORE REGISTERS   OY01166
         L     SAVERG,4(SAVERG)   ADDRESS SAVE AREA ISL REGIST. OY01166
         LM    14,12,12(SAVERG)    GET ISL REGISTERS             A45167
DONTRES  EQU   *                  DONT RESTORE ISL REGISTERS    OY01166
ENUFROOM SYNADAF ACSMETH=QISAM,PARM1=DCBAR2,PARM2=(0)              000G
         MVC   MESSAGE+8(79),49(PARAM1)   MOVE IN MESSAGE          000G
         MVI   ISMESS+1,X'02'     SET SYNAD MSG INDICATOR       OY01166
         MVI   ISCOMP+1,X'08'     SET CONDITION CODE            OY01166
         SYNADRLS                                                  000G
         TM    ISMESS,X'10'       ARE WE IN SYSUT2 CLOSE ?      OY01166
         BCR   1,RETREG           YES, RETURN TO CLOSE          OY01166
         MVC   REENTAR(OPENL-CLOSEL),CLOSEL MOVE L FORM FOR CLSE A24745
         CLOSE (DCBAR1),MF=(E,REENTAR) CLOSE INPUT DATA SET      A24745
         LA    WK3,2              LOAD SYNAD MSG NBR IN REG 3   OY01166
         B     SETA                                                000G
TSTOLD   CLC   60(2,WK6),ZERO         OLD RKP FIELD = 0 ?          000G
         BE    CHECKD                   YES, BRANCH                000G
         MVC   DCBRKP(2),60(WK6)             GET OLD RKP
CHECKD   TM    DCBRECFM,X'80'           NEW RECFM FIXED            000G
         BC    1,TRKP                   YES,NEW RECFM FIXED
         CLI   DCBRKP+1,X'04'           RKP LESS THAN 4            000G
         BL    NONCON                   YES RKP LESS THAN 4        000G
         B     TRKP                                                000G
***********************************************************************
*
*CONSTANTS
*
***********************************************************************
CLOSEL   CLOSE (,DISP),MF=L
OPENL    OPEN  (,OUTPUT),MF=L
EIGHT    DC    H'8'               COMPLETION CODE CONSTANT
ZERO     DC    H'0'                                                000G
VSSI     DC    V(IEBISSI)         IEBISSI ADDRESS-INPUT ROUTINE
H16      DC    H'16'
*
DCBISL   DCB   DSORG=IS,MACRF=PL,DDNAME=SYSUT2,                        *
               SYNAD=MESS,EXLST=TESTS
LABEL    DS    0F                      DUMMY LABEL                 000G
         DCBD  DSORG=(IS)
IEBWORK  DSECT
SAVE1    DS    1F
SAVE1BK  DS    1F
SAVE1FD  DS    1F
         DS    15F
SAVE2    DS    1F
SAVE2BK  DS    1F
SAVE2FD  DS    1F
         DS    15F
SAVE3    DS    1F
SAVE3BK  DS    1F
SAVE3FD  DS    1F
         DS    15F
***********************************************************************
*IEHISR WORKAREA
***********************************************************************
ISINVOK  DS    F                        ADDRESS OF PARAMETER LIST AT
*                                          INVOCATION
ISPRINT  DS    2F                       DDNAME FOR SYSPRINT
ISUT1    DS    2F                       DDNAME FOR SYSUT1
ISUT2    DS    2F                       DDNAME FOR SYSUT2
ISCOMP   DS    H                        COMPLETION CODE
ISMESS   DS    H                        MESSAGE NUMBER
ISPAGE   DS    H                        PAGE NUMBER FOR SYSPRINT
ISCNTRL  DS    CL1                      CONTROL BYTE
***********************************************************************
* MASKS FOR TESTING CONTROL BYTE - ISCONTRL
***********************************************************************
ISSWC    EQU   X'80'                    COPY
ISSWU    EQU   X'40'                    UNLOAD
ISSWL    EQU   X'20'                    LOAD
ISSWP    EQU   X'10'                    PRINT
ISSWN    EQU   X'08'                    NO CONVERT REQUESTED
ISSWPI   EQU   X'04'                    PRINT INDEX
ISSWPP   EQU   X'02'                    PRINT PRIME
ISSWPO   EQU   X'01'                    PRINT OVERFLOW
ALLMASK  EQU   X'07'
RESUME1  EQU   RESUME                                            A32042
ISSWITCH DS    CL1                      EXTRANEOUS SWITCHES
FIRSTTM  EQU   X'80'                    FIRST TIME SWITCH
*
XCTLAREA DS    2F
WHICH    DS    2F
ALIGN    DS    1F
DOUBLE   DS    1D
EXITADDR DS    2F
REENTAR  DS    9F
DCBAR1   DS    64F
DCBAR2   DS    64F
MESSAGER DS    CL1
MESSAGE  DS    CL120
RECADDR  DS    1F                                                  000G
         DS    0D
QISAM    DS    CL250                    UNLOADED DCB WORK AREA
HEADERC  DS    CL1
HEADERT  DS    CL144                    HEADER AREA
TARGET   DS    2F                       DATE HOLD AREA
PAGENO   DS    1F                       HOLD PAGE NUMBER FOR A HEADING
PAGEPTR  DS    1F                       HOLD POINTER TO WHERE PAGE
*                                       NUMBER IS TO BE PLACED
         END
