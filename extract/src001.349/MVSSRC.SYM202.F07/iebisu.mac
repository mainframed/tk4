 TITLE 'IEBISU-IEBISAM UTILITY PROGRAM-UNLOAD ROUTINE'
*STATUS: CHANGE LEVEL 000                                             *
*FUNCTION/OPERATION: THIS MODULE RETRIEVES LOGICAL RECORDS OF AN IN-  *
*   DEXED SEQUENTIAL DATASET AND PASSES THE LENGTH OF DATA RECORD AND *
*   THE ADDRESS OF THE RECORD TO IEHISSO.                             *
*ENTRY POINTS                                                         *
*   IEBISU-TO RETRIEVE A LOGICAL RICORD FROM AN ISAM DATASET          *
*   DATA IS PASSED BY LOCATION                                        *
*INPUT: ADDRESS OF IEHISR COMMON WORKAREA.                            *
*   FIELDS NEED:                                                      *
*       ISUT1  DDNAME FOR SYSUT1                                      *
*       ISUT2  DDNAME FOR SYSUT2                                      *
*       ISCOMP COMPLETION CODE                                        *
*       ISMESS MESSAGE NUMBER                                         *
*                                                                     *
*   REGISTER 13 MUST CONTAIN THE ADDRESS OF THHE CALLING PROGRAM'S    *
*   SAVE AREA,REGISTER 15 MUST CONTAIN THE ADDRESS OF THIS MODULE     *
*   FOR EVERY ENTRY TI THIS MODULE(EXCEPT FROM IEBISSO).
*OUTPUT: MODIFICATION OF IEBISR WORK AREA                             *
*EXTERNAL ROUTINES:                                                   *
*   IEBISSO: OUTPUT SUBROUTINE - TO OUTPUT AN UNLOAD ED RECORD        *
*EXITS-NORMAL:                                                        *
*   TO IEBISSO WHEN A LOGICAL RECORD HAS BEEN RETRIEVED AN PROCESSED  *
*    THE ADDRESS OF THE RECORD IN REGISTER1,ITS LENGTH IN REGISTER 0. *
*   TO IEBISF WITH A COMPLETION CODE OF ZERO IF MODULE WAS ABLE TO COM*
*    PLETE ITS OPERATION WITHOUT ENCOUNTERING ANY ERROR CONDITIONS.   *
*     -ERROR                                                          *
*   TO IEBISF WITH THE FOLLOWING COMPLETION CODES                     *
*     COMPLETION CODE = 8  PROGRAM UNABLE TO COMPLETE ITS OPERATION F *
*     OR EITHER OF THE FOLLOWING REASONS:                             *
*     INPUT OR OUTPUT SYNCHRONOUS ERROR                               *
*                                                                     *
*     COMPLETION CODE = 16 SYSUT1 OR SYSUT2 DD STATEMENT OMITTED      *
*                                                                     *
*ATTRIBUTES-REENTRANT                                              000G
*                                                                     *
*  REGISTER ASSIGNMENTS                                               *
*                                                                     *
BASE     EQU   10                  BASE REGISTER
SAVERG   EQU   13                  SAVE AREA ADDRESS REGISTER
ADDREG   EQU   15                  ADDRESSIBILITY REGISTER
PARA1    EQU   1                   PARAMETER REGISTER
WORKAREA EQU   11                       WORKAREA REGISTER          000G
WK2      EQU   2                   DCB ADDRESSING REGISTER
PARA0    EQU   0                   PARAMETER REGISTER
LINKRG   EQU   15                  LINKAGE REGISTER TO  IEHISSO
RETREG   EQU   14                  RETURN REGISTER FOR IEHISSO
RCODE    EQU   15                  RETURN CODE REGISTER FOR IEHISSO
WK3      EQU   3                   MESSAGE NUMBER REGISTER
GETREG   EQU   1                   RECORD ADDRESS REGISTER
WK5      EQU   5                   WORK REGISTER
*                                                                     *
IEBISU   CSECT
*1770528000-540000                                                 IV0I
*A384100-387000                                                  A41745
         SAVE  (14,12),T,ISU-66-V1           SAVE REGISTERS
         USING IEBISU,BASE                   ESTABLISH BASE REGISTER
         LR    BASE,ADDREG                    BASE REGISTER
         LR    WORKAREA,PARA1           PICK UP WORK AREA POINTER  000G
         USING IEBWORK,WORKAREA                                    000G
         ST    SAVERG,SAVE1BK           SET BACK CHAIN POINTER     000G
         LR    SAVERG,WORKAREA          UPDATE TO NEW SAVE AREA    000G
         LA    WK2,SAVE2                SET UP FORWARD CHAIN POINTR000G
         ST    WK2,SAVE1FD                 TO NEXT SAVE AREA       000G
*
*  THIS SECTION LOADS THE ADDRESS OF THE IEHISR COMMON WORK AREA,MOVES*
*THE DDNAME FROM THE ISUT1 FIELD OF IEBISR WORK AREA TO THE INPUT DCB-*
*DDNAME FIELD. DURING OPEN A DCB EXIT IS TAKEN. IF OPEN WAS SUCCESSFU-*
*LY COMPLETED AND THE DATASET IS OF INDEXED SEQUENTIAL ORGANIZATION,  *
*THE RECORD FORMAT IS CHECKED FOR FIXED OR VARIABLE FORMAT. A SWITCH  *
*IS SET TO INDICATE THE FORMAT ,ZERO IS PLACED IN REGISTER ZERO AND A *
*BRANCH IS MADE OT THE SUBROUTINE IEBISSO.                            *
         MVC   DCBAR1(LABEL-DCBISU),DCBISU                         000G
         USING IHADCB,WK2
         LA    WK2,DCBAR1                                          000G
         MVC   DCBDDNAM(8),ISUT1        MOVE DDNAME FROM ISUT1     000G
         MVC   REENTAR(CLOSEL-OPENL),OPENL   MOVE L FORM OF OPEN TO000G
*                                               WORKAREA           000G
         XC    DOUBLE(8),DOUBLE
         XC    EXITADDR(8),EXITADDR
         OPEN  (DCBAR1,(INPUT)),MF=(E,REENTAR)                     000G
         TM    DCBOFLGS,X'10'                WAS OPEN SUCCESSFUL
         BZ    SETAL                         IF NOT,BRANCH
         TM    DCBRECFM,X'80'                YES,RECORD FORMAT FIXED
         BZ    VARY                          NO,BRANCH
         TM    DCBRECFM,X'10'                BLOCKED RECORDS?
         BO    VARY+4                        YES,BRANCH
         MVI   DOUBLE+6,X'01'          SET FORMAT SWITCH = 01
         LH    WK3,DCBRKP                    LOAD RELATIVE KEY POSITIO
         LTR   WK3,WK3                       RKP=0
         BNE   VARY+4                        NO, BRANCH
         OI    DOUBLE+6,X'10'           FORMAT SWITCH = X'11'      000G
         MVI   DOUBLE,X'FF'             INDICATE UNBLOCKED         000G
         MVC   DOUBLE+4(1),DCBKEYLE     SAVE KEY LENGTH            000G
         B     VARY+4
VARY     MVI   DOUBLE+6,X'02'           SET FORMAT SWITCH = '02'   000G
         SR    PARA0,PARA0
         LR    PARA1,WORKAREA           LOAD ADDRESS OF WORKAREA   000G
OUSE     L     LINKRG,VSSO                   LOAD ADDRESS OF IEHISSO
         BALR  RETREG,LINKRG                 BRANCH TO IEHISSO
*                                                                     *
*  THIS SECTION DETERMINES THE RETURN CODE PASSED BY IEHISSO AND THEN *
*SETS THE APPROPRIATE COMPLETION CODE OR MESSAGE NUMBER, FOR ANY RE-  *
*TURN CODE DIFFERENT FROM ZERO AND CLOSES THE INPUT DATA SET BEFORE   *
*PASSING CONTROL TO IEBISF,                                           *
*                                                                     *
         STC   RCODE,DOUBLE+7           STORE RETURN CODE          000G
         CLI   DOUBLE+7,X'00'           IS RETURN CODE 0           000G
         BE    OBTAIN                        YES,BRANCH
         CLI   DOUBLE+7,X'04'           IS RETURN CODE 4           000G
         BE    SETAS                         YES,BRANCH
         CLI   DOUBLE+7,X'08'           IS RETURN CODE 8           000G
         BE    SETAL
         LA    WK3,1                   DCB FIELDS INCONSISTENT     000G
EXIT     STH   WK3,REENTAR+32           INVALID RETURN CODE        000G
         MVC   ISMESS(2),REENTAR+32     MOVE IN MESSAGE NUMBER     000G
         LTR   WK3,WK3                       MESSAGE NUMBER = 0
         BZ    COMPLY                        YES,BRANCH
         MVC   ISCOMP(2),EIGHT          SET COMPLETION CODE = 8    000G
COMPLY   MVC   REENTAR(EIGHT-CLOSEL),CLOSEL    MOVE L FORM OF CLOSE000G
*                                                 TO WORKAREA      000G
         CLOSE (DCBAR1),MF=(E,REENTAR)  CLOSE INPUT DATA SET       000G
         CLI   ISMESS+1,X'07'           OPEN NO GOOD             A41745
         BE    BYPASS                   NO OPEN SO BYPASS FREEP  A41745
         FREEPOOL DCBAR1                FREEPOOL INPUT BUFFER    A41745
BYPASS   SR    WK3,WK3                  SET UP XCTL AREA         A41745
         ST    WK3,XCTLAREA+4                                      000G
         MVI   WHICH+6,C' '             MOVE INTO XCTLAREA NAME OF 000G
         MVI   WHICH+5,C'F'                NEXT MODULE - IEBISF    000G
         L     SAVERG,4(SAVERG)              POPUP SAVE AREA
         L     14,12(0,SAVERG)               RESTORE REGISTER FOURTEEN
         LR    PARA1,WORKAREA           LOAD ADDRESS OF WORK AREA  000G
         XCTL  (2,12),MF=(E,(1)),SF=(E,XCTLAREA)   BRANCH TO IEHISF000G
SETAS    LA    WK3,2                   SYNAD ROUTINE ENTERED       000G
         B     EXIT
SETAL    MVC   ISCOMP(2),H16            SET COMPLETION CODE OF 16  000G
         LA    WK3,7                   SET MESSAGE NUMBER = 7 FOR  000G
         STH   WK3,ISMESS                  MISSING SYSUT DD CARD   000G
         B     COMPLY                        BRANCH TO CLOSE
*                                                                     *
*  THIS SECTION PASSES THE LENGTH AND ADDRESS FO THE QISAM DCB TO THE *
*OUTPUT ROUTINE (IEHISSO) TO BE WRITTEN OUT                           *
*                                                                     *
OBTAIN   LTR   PARA0,PARA0                   INITIAL ENTRY FROM IEHISSO
         BNE   OBTAIN1                       NO, BRANCH
         LA    WK3,154                       LOAD DCB LENTH
         STH   WK3,REENTAR+34           SAVE LENGTH                000G
         LA    WK3,DCBAR1              LOAD DCB ADDRESS            000G
         ST    WK3,RECADDR              SAVE ADDRESS OF RECORD     000G
         MVI   DOUBLE+5,X'11'          UNLOAD DCB INDICATOR        000G
         B     FXREC
*                                                                     *
*  THIS SECTION GETS A LOGICAL RECORD,SAVES THE ADDRESS, AND THE LEN- *
*GTH OF THE RECORD.                                                   *
*                                                                     *
OBTAIN1  GET   DCBAR1                  GET RECORD ADDRESS          000G
         ST    GETREG,RECADDR           SAVE ADDRESS OF RECORD     000G
         XC    DOUBLE+5(1),DOUBLE+5     CLEAR DCB UNLOAD INDICATOR 000G
         ST    PARA0,DOUBLE                                        000G
         MVC   REENTAR+34(2),DCBLRECL   SAVE RECORD LENGTH         000G
*                                                                     *
*  THIS SECTION PROCESSES A RECORD ACCORDING TO THE RECORD FORMAT. THE*
*ADDRESS OF THE RECORD IS PLACED IN REGISTER ONE; THE LENGTH OF THE   *
*RECORD IN REGISTER ZERO FOR THE IEBISSO SUBROUTINE.                  *
*                                                                     *
         CLI   DOUBLE+6,X'01'           IS RECORD FORMAT FIXED     000G
         BE    FXREC                         YES,BRANCH
         CLI   DOUBLE+6,X'11'           UNBLOCKED RECORDS          000G
         BE    ADDKEY
         CLI   DOUBLE+6,X'00'           CHECK FOR FIXED BLOCKED
         BE    FXREC                    YES,FIXED BLOCKED
         L     WK3,RECADDR              GET RECORD ADDRESS         000G
         MVC   REENTAR+34(2),0(WK3)    SAVE CURRENT RECORD LENGTH  IV0I
         ST    WK3,RECADDR              SAVE ADDRESS OF RECORD     000G
FXREC    LH    PARA0,REENTAR+34         LOAD RECORD LENGTH         000G
         L     PARA1,RECADDR            LOAD RECORD ADDRESS        000G
         B     OUSE
ADDKEY   SR    WK3,WK3
         IC    WK3,DOUBLE+4             GET KEY LENGTH             000G
         AH    WK3,REENTAR+34           ADD KEY LENGTH TO          000G
         STH   WK3,REENTAR+34            RECORD LENGTH             000G
         B     FXREC
OUT      LA    WK3,0                         SET MESSAGE NUMBER FOR OK
         LH    PARA0,EIGHT
         MVI   DOUBLE+5,X'FF'           INDICATE END OF INPUT      000G
         L     LINKRG,VSSO                   LOAD IEBISSO ADDRESS
         BALR  RETREG,LINKRG                 BRANCH TO IEBISSO
         LA    WK3,0
         B     EXIT                          END OF INPUT DATA;GO CLOSE
RUIN     LA    WK3,2                   SYNAD ROUTINE ENTERED       000G
         TM    DCBEXCD2,X'20'                CLOSE BIT ON?
         BCR   7,RETREG                 RETURN TO CLOSE
         SYNADAF ACSMETH=QISAM,PARM1=DCBAR1,PARM2=(0)              000G
         MVC   MESSAGE+8(79),49(PARA1)    MOVE IN MESSAGE          000G
         SYNADRLS                                                  000G
         MVC   REENTAR(EIGHT-CLOSEL),CLOSEL  MOVE L FORM OF CLOSE  000G
         CLOSE (DCBAR2),MF=(E,REENTAR)   CLOSE OUTPUT CATA SET     000G
         B     SETAS                    RETURN TO CLOSE OUT
*  CONSTANTS                                                          *
*                                                                     *
OPENL    OPEN  (,INPUT),MF=L                                       000G
CLOSEL   CLOSE (,DISP),MF=L                                        000G
EIGHT    DC    H'8'                          COMPLETION CODE CONSTANT
VSSO     DC    V(IEBISSO)         IEBISSO SYMBOLIC ENTRY
H16      DC    H'16'
DCBISU   DCB   DSORG=IS,MACRF=GL,DDNAME=SYSUT1,                        *
               SYNAD=RUIN,EODAD=OUT
LABEL    DS    0F                                                  000G
         DCBD  DSORG=(IS)
IEBWORK  DSECT
SAVE1    DS    1F
SAVE1BK  DS    1F
SAVE1FD  DS    1F
         DS    15F
SAVE2    DS    1F
SAVE2BK  DS    1F
SAVE2FD  DS    1F
         DS    15F
SAVE3    DS    1F
SAVE3BK  DS    1F
SAVE3FD  DS    1F
         DS    15F
*IEHISR WORKAREA
ISINVOK  DS    F                        ADDRESS OF PARAMETER LIST AT
*                                          INVOCATION
ISPRINT  DS    2F                       DDNAME FOR SYSPRINT
ISUT1    DS    2F                       DDNAME FOR SYSUT1
ISUT2    DS    2F                       DDNAME FOR SYSUT2
ISCOMP   DS    H                        COMPLETION CODE
ISMESS   DS    H                        MESSAGE NUMBER
ISPAGE   DS    H                        PAGE NUMBER FOR SYSPRINT
ISCNTRL  DS    CL1                      CONTROL BYTE
* MASKS FOR TESTING CONTROL BYTE - ISCONTRL
ISSWC    EQU   X'80'                    COPY
ISSWU    EQU   X'40'                    UNLOAD
ISSWL    EQU   X'20'                    LOAD
ISSWP    EQU   X'10'                    PRINT
ISSWN    EQU   X'08'                    NO CONVERT REQUESTED
ISSWPI   EQU   X'04'                    PRINT INDEX
ISSWPP   EQU   X'02'                    PRINT PRIME
ISSWPO   EQU   X'01'                    PRINT OVERFLOW
ALLMASK  EQU   X'07'                                               000G
ISSWITCH DS    CL1                      EXTRANEOUS SWITCHES
FIRSTTM  EQU   X'80'                    FIRST TIME SWITCH
*
XCTLAREA DS    2F
WHICH    DS    2F
ALIGN    DS    1F
DOUBLE   DS    1D
EXITADDR DS    2F
REENTAR  DS    9F
DCBAR1   DS    64F
DCBAR2   DS    64F
MESSAGER DS    CL1                                                 000G
MESSAGE  DS    CL120
RECADDR  DS    1F                                                  000G
         DS    0D
QISAM    DS    CL250                    UNLOADED DCB WORK AREA
HEADERC  DS    CL1
HEADERT  DS    CL144                    HEADER AREA
TARGET   DS    2F                       DATE HOLD AREA
PAGENO   DS    1F                       HOLD PAGE NUMBER FOR A HEADING
PAGEPTR  DS    1F                       HOLD POINTER TO WHERE PAGE
*                                       NUMBER IS TO BE PLACED
         END
