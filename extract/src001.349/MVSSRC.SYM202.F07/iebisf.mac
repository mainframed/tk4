 TITLE 'IEBISF-IEBISAM UTILITY PROGRAM - MESSAGE WRITER'
*STATUS & CHANGE LEVEL 0
*
*FUNCTION - THIS MODULE OPENS THE SYSPRINT DATA SET AND THE APPROPRIATE
*           MESSAGE WRITTEN,AND THE DATA SET CLOSED. THE APPROPRIATE
*           CONDITION CODE IS RETURNED.
*
*ENTRY POINTS- THE ONLY ENTRY POINT TO THIS MODULE IS IEBISF. ENTRY IS
*              MADE FROM ANY MODULE OF THE IEBISAM UTILITY PROGRAM
*
*INPUT - AT ENTRY TO THIS MODULE REGISTER 13 POINTS TO A REGISTER SAVE
*        AREA, REGISTER 14 CONTAINS THE FINAL RETURN ADDRESS, REGISTER
*        1 POINTS TO THE COMMON WORK AREA.
*        THE WORK AREA CONTAINS-
*        ISINVOK - ADDRESS OF PARAMETER LIST FOR INVOCATION
*        ISPRINT - DDNAME FOR SYSPRINT DATA SET
*        ISCOMP - COMPLETION CODE TO BE RETURNED
*        ISMESS  - MESSAGE NUMBER OF MESSAGE TO BE WRITTEN
*        ISPAGE  - PAGE NUMBER TO BE USED FOR MESSAGE DATA SET
*        MESSAGE - IF ERROR WAS ENCOUNTERED DURING I/O THE INFORMATION
*                  RETURNED FROM SYNADAF WILL BE PRESENT.
*
*OUTPUT - A MESSAGE IS WRITTEN ON THE 'SYSPRINT' DATA SET.
*
*EXTERNAL ROUTINES - NONE
*
*EXITS - CONTROL IS RETURNED TO THE CALLER
*
*CONDITION CODES -
*
*        0     SUCCESSFUL COMPLETION
*
*        8     ERROR OTHER THAN ABSENCE OF SYSUT1 OR SYSUT2 DD CARD
*
*        16    ABSENCE OF SYSUT1 &/OR SYSUT2 DD CARD
*
*A490100-490500,500000                                           A41745
*C577000                                                         A41801
*REGISTER USAGE
PAREG0   EQU   0                        PARAMETER REGISTER
PAREG1   EQU   1                        PARAMETER REGISTER
CONS     EQU   2                        WORK REGISTER
WK3      EQU   3                        WORK REGISTER
WK4      EQU   4                        WORK REGISTER
WK5      EQU   5                        WORK REGISTER
BASEREG  EQU   10                       BASE REGISTER
WORKAREA EQU   11                       WORK AREA REGISTER
SAVREG   EQU   13                       SAVE AREA REGISTER
RETREG   EQU   14                       RETURN AREA REGISTER
ADDR     EQU   15                       BASE ADDRESSABILITY/RETURN CODE
         EJECT
IEBISF   CSECT
         SAVE  (14,12),T
         LR    BASEREG,ADDR             ESTABLISH ADDRESSABILITY
         USING IEBISF,BASEREG
         LR    WORKAREA,PAREG1          ESTABLISH WORK AREA ADDRESSA-
*                                           BILITY
         USING IEBWORK,WORKAREA
         LA    WK3,DCBAR1               SET UP ADDRESSABILITY TO DCB
         USING IHADCB,WK3
         EXTRN MESSTBL
         ST    SAVREG,SAVE1BK           SET BACK CHAIN POINTER
         LR    SAVREG,WORKAREA          UPDATE TO NEW SAVE AREA
         LA    WK4,SAVE2                SET UP FORWARD CHAIN POINTER
         ST    WK4,SAVE1FD                 TO NEXT SAVE AREA
         MVC   DCBAR1(HEADER-DCBPRINT),DCBPRINT MOVE DCB TO WORK AREA
*                                       TO MAINTAIN REENTRABILITY
         MVC   DCBDDNAM(8),ISPRINT      MOVE IN DDNAME FOR SYSPRINT
         MVC   REENTAR(CLOSEL-OPENL),OPENL  MOVE OPEN L-FORM TO WORK
*                                          AREA TO MAINTAIN REENTRA-
*                                          BILITY
         OPEN  (DCBAR1,(OUTPUT)),MF=(E,REENTAR)
         TM    DCBOFLGS,X'10'           CHECK FOR SUCCESSFUL OPEN
         BZ    SETSWOT                  NO, SET CONDITION CODE
         CLI   EXITADDR,X'01'           CHECK FOR INVALID DCB PARAMETER
*                                       IN SYSPRINT DCB
         BE    CLOSEOUT                 YES, TO TO CLOSE
* THIS SECTION SETS UP THE HEADER AND WRITES IT
         MVI   HEADERC,X'F1'            SET TO SKIP TO NEXT PAGE
         MVC   HEADERT(120),HEADER      MOVE IN  HEADER
         LH    WK5,ISPAGE               PICK UP PAGE NUMBER IN BINARY
         CVD   WK5,DOUBLE                  AND CONVERT TO DECIMAL
         UNPK  HEADERT+110(3),DOUBLE+6(2)
         OI    HEADERT+112,X'F0'
         MVC   HEADERT+90(8),TARGET     MOVE IN DATE
         PUT   DCBAR1,HEADERC
*THIS SECTION SETS UP MESSAGE
         LH    WK3,ISMESS               PICK UP MESSAGE NUMBER
*THIS SECTION MOVES THE MESSAGE NUMBER FOR I/O ERRORS INTO THE MESSAGE
*AREA. THE MESSAGE TEXT IS PROVIDED BY THE SYNADAF MACRO
         CH    WK3,=H'2'                CHECK FOR I/O ERROR MESSAGE
         BE    SPECIAL                  YES, PICK UP SPECIAL MESSAGE
*                                          NUMBER
         SLL   WK3,2                    MULTIPLY BY 4
         L     WK4,MESSTBLA             PICK UP BEGINNING ADDRESS OF
*                                          MESSAGE TABLE
         AR    WK3,WK4                  INDEX INTO TABLE
         L     WK3,0(0,WK3)             PICK UP MESSAGE ADDRESS
         MVC   MESSAGE(120),0(WK3)      MOVE IN MESSAGE
PUTOUT   MVI   MESSAGER,X'40'           SET TO SKIP A LINE AND PRINT
         PUT   DCBAR1,MESSAGER
CLOSEOUT MVC   REENTAR(MESSTBLA-CLOSEL),CLOSEL  MOVE CLOSE TO WORK AREA
*                                          TO MAINTAIN REENTRABILITY
         CLOSE (DCBAR1,LEAVE),MF=(E,REENTAR)
         CLI   ISCOMP+1,X'10'           WAS OPEN  CORRECT        A41745
         BE    BYPASS                   OPEN INCORRECT SO NO FRE A41745
         FREEPOOL  DCBAR1  FREE BUFFERS                          A41745
*SET UP TO RETURN CONTROL TO THE CALLER
BYPASS   L     SAVREG,4(SAVREG)         RESTORE SAVE AREA POINTERA41745
         L     WK3,ISINVOK              REESTABLISH POINTER TO PARA-
*                                          METERS FOR INVOCATION
         SR    WK4,WK4                  INSERT COMPLETION CODE
         IC    WK4,ISCOMP+1
         FREEMAIN R,LV=1536,A=(11)
         LR    PAREG1,WK3
         LR    ADDR,WK4                 PICK UP COMPLETION CODE
         L     RETREG,12(0,SAVREG)      RESTORE RETURN ADDRESS
         RETURN (2,12),T
SPECIAL  MVC   MESSAGE(8),IONUMBER      MOVE IN SPECIAL MESSAGE NUMBER
         MVC   MESSAGE+96(24),COMPCODE  MOVE COMPLETION CODE TO MESSAGE
         B     PUTOUT
         EJECT
*THIS SECTION IS THE SYNAD ROUTINE FOR THE SYSPRINT DATA SET
ERROR    MVI   ISCOMP+1,X'08'           SET COMPLETION CODE TO 8
         B     CLOSEOUT
SETSWOT  MVI   ISCOMP+1,X'10'           SET COMPLETION CODE
         B     CLOSEOUT
DCBEXIT  LA    WK3,DCBAR1               SET UP ADDRESSABILITYTO DCB
         MVI   EXITADDR,X'00'           SET SWITCH FOR SUCCESS
         OC    DCBBLKSI(2),DCBBLKSI     BLOCK SIZE SPECIFIED
         BZ    MOVELREC                 NO, NO BLOCKSIZE SPECIFIED
         SR    WK4,WK4
         LH    WK5,DCBBLKSI             PICK UP PROVIDED BLOCKSIZE
         LH    CONS,DCBLRECL            PICK UP LRECL
         DR    WK4,CONS
         LTR   WK4,WK4                  CHECK FOR BLOCKSIZE NOT A
*                                          MULTIPLE OF LRECL
         BNE   MOVELREC      ERROR, SET DEFAULT                  A41801
         RETURN
MOVELREC MVC   DCBBLKSI(2),DCBLRECL     USE DCB LRECL FOR BLOCK SIZE
         RETURN
SETCODE  MVI   ISMESS+1,X'01'           SET UP MESSAGE CODE
         MVI   ISCOMP+1,X'08'           SET UP COMPLETION CODE
         MVI   EXITADDR,X'01'           SET FOR CHECK AFTER OPEN
         RETURN
OPENL    OPEN  (,OUTPUT),MF=L
CLOSEL   CLOSE (,LEAVE),MF=L
MESSTBLA DC    A(MESSTBL)
DCBPRINT DCB   DSORG=PS,MACRF=(PM),DDNAME=SYSPRINT,RECFM=FBA,LRECL=121,*
               SYNAD=ERROR,EXLST=DDDEX   EXLST  NAME CHANGED (AOS)
DDDEX    DS    0F        LAB. NAME CHANGED , BEC. OF AOS DCBD MACRO
         DC    X'85'
         DC    AL3(DCBEXIT)
HEADER   DC    CL32'     DATA SET UTILITY - IEBISAM '
         DC    CL32'                                '
         DC    CL32'                                '
         DC    CL24'                        '
IONUMBER DC    CL8'IEB602I'
COMPCODE DC    CL24'COMPLETION CODE=08      '
         LTORG
         EJECT
IEBWORK  DSECT
SAVE1    DS    1F
SAVE1BK  DS    1F
SAVE1FD  DS    1F
         DS    15F
SAVE2    DS    1F
SAVE2BK  DS    1F
SAVE2FD  DS    1F
         DS    15F
SAVE3    DS    1F
SAVE3BK  DS    1F
SAVE3FD  DS    1F
         DS    15F
ISINVOK  DS    F                        ADDRESS OF PARAMETER LIST AT
*                                          INVOCATION
ISPRINT  DS    2F                       DDNAME FOR SYSPRINT
ISUT1    DS    2F                       DDNAME FOR SYSUT1
ISUT2    DS    2F                       DDNAME FOR SYSUT2
ISCOMP   DS    H                        COMPLETION CODE
ISMESS   DS    H                        MESSAGE NUMBER
ISPAGE   DS    H                        PAGE NUMBER FOR SYSPRINT
ISCNTRL  DS    CL1                      CONTROL BYTE
* MASKS FOR TESTING CONTROL BYTE - ISCONTRL
ISSWC    EQU   X'80'                    COPY
ISSWU    EQU   X'40'                    UNLOAD
ISSWL    EQU   X'20'                    LOAD
ISSWP    EQU   X'10'                    PRINT
ISSWN    EQU   X'08'                    NO CONVERT REQUESTED
ISSWPI   EQU   X'04'                    PRINT INDEX
ISSWPP   EQU   X'02'                    PRINT PRIME
ISSWPO   EQU   X'01'                    PRINT OVERFLOW
ALLMASK  EQU   X'07'
ISSWITCH DS    CL1                      EXTRANEOUS SWITCHES
FIRSTTM  EQU   X'80'                    FIRST TIME SWITCH
*
XCTLAREA DS    2F
WHICH    DS    2F
ALIGN    DS    1F
DOUBLE   DS    1D
EXITADDR DS    2F
REENTAR  DS    9F
DCBAR1   DS    64F
DCBAR2   DS    64F
MESSAGER DS    CL1
MESSAGE  DS    CL120
RECADDR  DS    1F                                                  000F
         DS    0D
QISAM    DS    CL250                    UNLOADED DCB WORK AREA
HEADERC  DS    CL1
HEADERT  DS    CL144                    HEADER AREA
TARGET   DS    2F                       AREA  TO HOLD DATE
PAGENO   DS    1F                       HOLD PAGE NUMBER FOR HEADING
PAGEPTR  DS    1F                       HOLD POINTER TO WHERE PAGE
*                                          NUMBER IS TO BE PLACED
* MACRO DEFINITIONS
         DCBD  DSORG=(PS)
         END
