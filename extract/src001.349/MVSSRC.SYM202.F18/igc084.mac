         TITLE 'IGC084  ---- STORE BUFFER RESTART ADDRESS'  D11
*STATUS: CHANGE LEVEL 000
*FUNCTION/OPERATION: STORES THE BUFFER RESTART ADDRESS IN THE UCB
*ENTRY POINT:  IGC084                                       D11
*INPUT: IN REGISTER 0 -THE 16 HIGH ORDER BITS CONTAIN THE BUFFER
*   RESTART ADDRESS; THE 16 LOW ORDER BITS CONTAIN THE ADDRESS OF
*   THE UCB.
*       IN REGISTER 1-ADDRESS DCB                            LG @ZM2358
*OUTPUT: THE BUFFER RESTART ADDRESS STORED IN THE UCB
*EXTERNAL ROUTINES: DEBCHK(VIA BRANCH ENTRY)                D11
*EXITS-NORMAL: RETURN TO GIOCR VIA REG14
*     -ERRORS: NONE
*TABLES/WORK AREAS: NONE
*ATTRIBUTES: TYPE 1, RESIDENT, SUPERVISOR STATE, REENTRANT
*NOTES - NONE
         EJECT
IGC084   CSECT
*C630000,840000                                               LF YM4067
*A84100-849500,A720500-723000,A540500-54900,A550000          LG @ZM2358
*D570000-630000                                              LG @ZM2358
*A547650-547950,660200,847200                               D11 ZA04879
*C546000-546500,A547000                                     D11 ZA04880
*A547960-547980,A660300                                     D11 ZA13198
*A54220002,C54760002,A72125002                              D11 ZA13201
         EJECT                                              D11
         BALR  BASE,0              EST BASE REG
         USING *,BASE              AM USING REG 12
         B     MODID               HOP AROUND EYECATCHER
         DC    C' IGC084.VS2R3.&SYSDATE'    MODULE EYECATCHER
MODID    LR    R8,REGZERO            SAVE REGISTER 0         LG @ZM2358
         LR    R5,REGONE             SAVE REGISTER 1         LG @ZM2358
         LR    R6,REG14              SAVE REGISTER 14        LG @ZM2358
         L     DEBREG,DCBDEB(DCBREG) LD DEB ADDR             LG @ZM2358
         LR    R2,DEBREG THO I WALK THRU THE SHADOW OF MVS  D11 ZA13201
         USING CVT,CVTREG              BASE FOR CVT          LG @ZM2358
         USING CVTXTNT2,CVTEXTRG       BASE FOR EXT2         LG @ZM2358
         L     CVTREG,CVTPTR           LD ADDR CVT           LG @ZM2358
         L     CVTEXTRG,CVTEXT2        LD ADDR EXT2          LG @ZM2358
         L     TCBREG,CVTTCBP          LD TCB PTRS           LG @ZM2358
         L     ASCBREG,CRNTASCB(TCBREG) LD ADR CURR ASCB     LG @ZM2358
         L     TCBREG,CRRNTTCB(TCBREG)  LD ADR CURR TCB      LG @ZM2358
         L     BRREG,CVTDEBVR          LD ADDR DEBVERIFY     LG @ZM2358
         SR    REGZERO,REGZERO         CLR REG 0             LG @ZM2358
         BALR  REG14,BRREG             GO TO DEB VERIFY      LG @ZM2358
         B     GOODEB              DEB IS GOOD (+0)         D11 ZA04880
         B     LEAVE               DEB IS INVALID (+4)      D11 YA04880
GOODEB   SRDL  R8,SIXTEEN            SHIFT UCB TO REG 9     D11 YA04880
         SRL   R9,SIXTEEN            RIGHT JUSTIFY UCB@      LG @ZM2358
         LR    R5,R2          I SHALL NOT FEAR DEBCHECK     D11 ZA13201
         SR    R11,R11             FOR IC                   D11 YA04879
         IC    R11,NUMUCB(R5)      GET NUMUCBS FROM DEB     D11 YA04879
         SLL   R11,2               X4(EACH DSCRPTR 4 LONG)  D11 YA04879
         LA    R11,32(R11,R5) DEBADR+DEBLEN+NUMUCBS(EACH4)  D11 YA04879
* PREV INSTR POINTS R11 TO 1ST SBRTN LOADED BY OPEN         D11 YA04879
         CLC   0(2,R11),SBNAME     BETTER BE 'OA'!!         D11 YA04879
         BNE   LEAVE  ITS NOT,SO NOT GAM,SO INVALID DEB     D11 YA04879
         CLC   GUCBTYP(2,R9),G2250 PASSED UCB A 2250?       D11 ZA13198
         BNE   LEAVE               NO,SORRY BOUT THAT       D11 ZA13198
         CLM   R9,SEVEN,DEBUCB(R5)   IS PASSED UCB VALID?    LG @ZM2358
         BNE   LEAVE                 NO...LEAVE              LG @ZM2358
         STH   R8,UCBBSTR(R9)        STR RESTART ADDRESS     LG @ZM2358
LEAVE    EQU   *
         LR    REG14,R6              RESTORE RETURN REG      LG @ZM2358
         BR    REG14                    RETURN TO GIOCR
SBNAME   DC    C'OA' LST 2CHRS 1ST GAM MOD LOADED BY OPEN   D11 YA04879
G2250    DC    X'1002'             2250 DEVTYPE IN UCB      D11 ZA13198
PATCH    DS    0F                                           D11 @ZM2358
         DC    40X'FF'            40 BYTE PATCH AREA        D11 @ZM2358
REGZERO  EQU   0                        REGISTER 0
REGONE   EQU   1                        REGISTER 1
DCBREG   EQU   1                                             LG @ZM2358
DEBREG   EQU   1                                             LG @ZM2358
R2       EQU   2              SAFE HOLDER FOR DEBADDR       D11 ZA13201
CVTREG   EQU   3                                             LG @ZM2358
CVTEXTRG EQU   4                                             LG @ZM2358
R5       EQU   5                                             LG @ZM2358
R6       EQU   6                  GENERAL REG 6              LG @ZM2358
ASCBREG  EQU   7                                             LG @ZM2358
TCBREG   EQU   10                                            LG @ZM2358
BASE     EQU   12                       BASE REGISTER
REG14    EQU   14                       RETURN REGISTER
SIXTEEN  EQU   16                       CONSTANT
UCBBSTR  EQU   24              BUFFER RESTART ADDR            LF YM4067
GUCBTYP  EQU   18                  UCBTYPE DISPL IN UCB     D11
R8       EQU   8                     WORK REGISTER           LG @ZM2358
R9       EQU   9                     WORK REGISTER           LG @ZM2358
R11      EQU   11                    WORK REGISTER           LG @ZM2358
R15      EQU   15                    WORK REGISTER           LG @ZM2358
SEVEN    EQU   7                       VALUE OF 7            LG @ZM2358
DEBUCB   EQU   33                      DISP OF UCB IN DEB    LG @ZM2358
DCBDEB   EQU   44                      DISP DEB IN DCB       LG @ZM2358
NUMUCB   EQU   16     DISPL IN DEB OF NUMUCBS BYTE          D11 YA04879
CRNTASCB EQU   12                                            LG @ZM2358
BRREG    EQU   15                                            LG @ZM2358
CRRNTTCB EQU   4                                             LG @ZM2358
CVT      DSECT                                               LG @ZM2358
         CVT   LIST=YES                                      LG @ZM2358
         END
