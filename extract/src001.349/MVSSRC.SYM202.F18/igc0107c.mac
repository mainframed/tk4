***********************************************************************
* MODULE NAME: IGC0107C (SPAR MESSAGE MODULE)
*
* STATUS: CHANGE LEVEL 0
*
* FUNCTION: ISSUE WRITE-TO-PROGRAMMER MESSAGES FOR ERRORS DISCOVERED BY
* IGC0007C(SPAR)
*
* ENTRY POINT: IGC0107C VIA XCTL FROM IGC0007C
*
* INPUT: REG4 - ADDRESS OF SPAR PARAMETER LIST
*        REG5 - ADDRESS OF GACB BEING PROCESSED
*        REG7 - RETURN CODE SET BY SPAR; INDEXES MSGLIST
*
* OUTPUT: WRITE-TO-PROGRAMMER MESSAGE
*         REGISTER 15 - RETURN CODE 04 NON-GRAPHIC DCB
*                                   08 GACB ALREADY SPECIFIED
*                                   0C NO GACB ADDRESS FOR SPAR
*                                   10 DCB NOT OPEN
*                                   14 SPAR NOT IN TASK THAT OPENED
*
*         REGISTER 1 - ADDRESS OF GACB BEING PROCESSED,EXCEPT FOR
*                      RETURN  CODE 0C; THEN,ADDR OF PARAM LIST
*
* EXIT: VIA SVC 3
*
* TABLES: MSGLIST - DISPLACEMENTS TO LIST FORMS OF WTO FOR MESSAGES
*
* ATTRIBUTES: SUPERVISOR STATE,REENTRANT,TRANSIENT
*
***********************************************************************
IGC0107C CSECT
* REGISTER EQUATES
R0       EQU   0
R1       EQU   1
R2       EQU   2                       POINTER TO SVRB EXT
RGACB    EQU   4                       ADDR GACB BEING PROCESSED
RPARM    EQU   5                       ADDR PARAM LIST
RCODE    EQU   7                       MSGLIST OFFSET(INPUT)
RWORK    EQU   8
RMSG     EQU   9                       ADDR OF MSG
RBUF     EQU   10                      POINTER TO MSG BUFFER
RLNGTH   EQU   11                      FOR LENGTH OF MSG
RBASE    EQU   12                      BASE REG
R15      EQU   15
         SPACE 3
* MISC. EQUATES
X0C      EQU   X'0C'                   RETURN CODE COMPARAND
EXIT     EQU   3                       SVC NUMBER FOR EXIT RTN
CVTPTR   EQU   16                      CVT POINTER
D0       EQU   0                       DISPLACEMENT
D4       EQU   4                       DISPLACEMENT
D96      EQU   96                      DISPLACEMENT
         SPACE 3
         BALR  RBASE,R0                LOAD BASE REG
         USING *,RBASE
ORG      EQU   *
         L     RMSG,MSGLIST(RCODE)     DISPLACEMENT OF MSG
         LA    RMSG,R0(RMSG,RBASE)     ADDRESS OF MSG
         L     R2,CVTPTR               CVT ADDRESS
         L     R2,D0(R2)               ADDRRSS OF TCB POINTERS
         L     R2,D4(R2)               CURRENT TCB ADDR
         L     R2,D0(R2)               CURRENT RB ADDR
         LA    R2,D96(R2)              ADDR OF SVRB EXT
         MVC   D4(GMLNG,R2),GMLIST     MOVE GM LIST TO SVRB EXT
         LA    RWORK,D4(R2)            ADDR OF GM PARM LIST
         GETMAIN A=(R2),MF=(E,(RWORK))  GET MSG BUFFER
         LTR   R15,R15                 DID GM SUCCEED
         BNZ   SKIPWTO                 BRANCH IF GM FAILED
         L     RBUF,D0(R2)             ADDR OF MSG BUFFER
         LH    RLNGTH,D0(RMSG)         LENRTH OF MSG
         LA    RLNGTH,D4(RLNGTH)       +4 FOR DESC,RTG CODES
         EX    RLNGTH,MOVEMSG          MOVE MSG TO BUFFER
         WTO   MF=(E,(RBUF))           ISSUE MSG
         FREEMAIN A=(R2),MF=(E,(RWORK))  FREE BUFFER
SKIPWTO  EQU   *
         LR    R1,RGACB                CURRENT GACB ADDR
         LA    RWORK,X0C               RETURN CODE COMPARAND
         CR    RWORK,RCODE             IS ERROR FOR SHORT PARM LIST
         BNE   LEAVE                   IF NOT,RETURN GACB ADDR
         LR    R1,RPARM                RETURN ADDR OF BAD PARM LIST
LEAVE    EQU   *
         LR    R15,RCODE               SET RETURN CODE
         SVC   EXIT
         EJECT
MOVEMSG  MVC   D0(D0,RBUF),D0(RMSG)
GMLIST   GETMAIN EC,LV=80,MF=L         GM PARM LIST
GMLNG    EQU   *-GMLIST                LENGTH OF GMLIST
         DS    0F
MSGLIST  EQU   *-4                     MESSAGE SELECTION TABLE
* RCODE PASSED AS INPUT IS DISPLACEMENT FROM MSGLIST ORIGIN
         DC    A(MSG1-ORG)
         DC    A(MSG2-ORG)
         DC    A(MSG3-ORG)
         DC    A(MSG4-ORG)
         DC    A(MSG5-ORG)
MSG1     WTO   'IFF303I SPAR, USING GACB INPUT, FOUND DCB NOT GRAPHIC',X
               ROUTCDE=11,DESC=7,MF=L
MSG2     WTO   'IFF304I SPAR ISSUED FOR GACB ALREADY SPECIFIED BY SPAR'X
               ,ROUTCDE=11,DESC=7,MF=L
MSG3     WTO   'IFF301I SPAR PARAMETER LIST TOO SHORT TO CONTAIN GACB AX
               DDRESS',ROUTCDE=11,DESC=7,MF=L
MSG4     WTO   'IFF302I SPAR, USING GACB INPUT, FOUND DCB NOT OPENED', X
               ROUTCDE=11,DESC=7,MF=L
MSG5     WTO   'IFF305I SPAR ISSUED BY TASK OTHER THAN ONE FOR WHICH DCX
               B WAS OPENED',ROUTCDE=11,DESC=7,MF=L
         END
