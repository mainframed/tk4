         TITLE 'GRAPHIC ATTENTION SERVICE ROUTINE'
***********************************************************************
*
* MODULE NAME:           IGC070 (OS/VS2)
*
* DESCRIPTIVE NAME:      GRAPHIC ATTENTION SERVICE ROUTINE
*
* COPYRIGHT:             NONE
*
* STATUS:                RELEASE 2.0
*
* FUNCTION:              THIS MODULE PERFORMS FUNCTIONS FOR
*FILL IN FROM SPECS******CONTINUED*NEXT*PAGE***************************
         EJECT
         ENTRY SVC070
IGC070   CSECT
*   SEE LABEL 'MODID' FOR LAST CHANGE DATE; FORMAT:  NAME.SYSREL.DATE
* SEQ NBRS MUST BE RESPECIFIED BECAUSE OF RESEQ FOR VS
*C010000                                                         Y01021
*A007100,A009820,A010120                                     LD YA00813
*A008240-008280,A007120-007180,A009920-010200                LG @ZM2359
*A005700,A006300,A006500,A006900                             LG @ZM2359
*C010100-010130,A010160                                     D11 ZA04881
*A008290,A010350-010800                                     D11 ZA04882
*C012000,A012200-012240,015430-015460                       D11 ZA13197
*C010250                                                    E12 ZA25652
*A011205,A012185,A015925                                    E12 ZA26470
*
*
*STATUS:CHANGE LEVEL 0
*FUNCTION/OPERATION:THIS ROUTINE RESETS TO 0  BIT 6 OR BIT 7 OR BOTH
*   THAT ARE LOCATED IN THE GRAPHIC CONTROL BYTE APPENDAGE TO THE
*   UNIT CONTROL BLOCK(UCB).
*ENTRY POINTS:SVC  070  OR USE GSERV MACRO.RESET BITS 6 AND/OR 7 IN GCB
*INPUT:GR#1 IS THE PARAMETER REGISTER.ITS CONTENTS ARE AS FOLLOWS:
*   ******************
*   MASK*DCBLIST ADR*  MASK=01 FOR BIT7,02 FOR BIT6,03 FOR BOTH.
*   ******************
*   DCBLIST ADR CONTAINS THE FOLLOWING INFORMATION:
*   ****************
*   * IX * DCB ADR * IX=DEB EXTENT TO ACCESS UCB ADDRESS.
*   ****************
*OUTPUT:N/A
*EXTERNAL ROUTINES:DEBVERIFY VIA BRANCH ENTRY.
*EXITS NORMAL:RETURN MADE TO CALLING ROUTINE VIA SVC 3.
*EXITS ERROR:ABNORMAL TERMINATION OF TASK.SYSTEM COMPLETION CODES ARE
*   AS FOLLOWS:
*               056=INVALID UNIT CONTROL BLOCK(UCB).
*               057=NOT A GRAPHIC UCB.
*TABLES/WORK AREAS:N/A
*ATTRIBUTES:RE-ENTRANT
*NOTES:N/A
         EJECT
*
*                                  STANDARD SYSTEM REGISTERS ---
*
PARMRG   EQU   1                        PARAMETER REGISTER
R1       EQU   1                       WORK REGISTER         LG @ZM2359
BASERG   EQU   2                        WORK REG
SVCRG1   EQU   3                        WORK REG
SVCRG2   EQU   4                        WORK REG
CVTREG   EQU   4                                             LG @ZM2359
SVCRG3   EQU   5                        WORK REG
CVTEXTRG EQU   5                                             LG @ZM2359
SVCRG4   EQU   6                        WORK REG
SVCRG5   EQU   7                        WORK REG
ASCBREG  EQU   7                                             LG @ZM2359
SVCRG6   EQU   8                        WORK REG
R8       EQU   8                  WORK REGISTER              LG @ZM2359
SVCRG7   EQU   9                        WORK REG             LD YA00813
TCBREG   EQU   10                      LG @ZM2359
R0       EQU   0                     WORK REGISTER           LG @ZM2359
R11      EQU   11                    WORK REGISTER           LG @ZM2359
R14      EQU   14                    WORK REGISTER           LG @ZM2359
BRREG    EQU   15                    WORK REGISTER           LG @ZM2359
R15      EQU   15                 GENERAL REGISTER 15        LG @ZM2359
*
*          CONSTANTS USED IN THIS MODULE                              *
*                                                                     *
ZERO     EQU   0                       ZERO DISPLACEMENT      LB Y01021
UCBADR   EQU   32                       START OF UCB LIST IN DEB
DEBADR   EQU   44                       START OF DEB ADDRESS IN DCB
GCBADR   EQU   24                       START OF UCB GRAPHIC EXTENSION
EXIT     EQU   3
GUCBTYP  EQU   18                  LOCATION OF DEVTYPE IN UCB
RC4      EQU   4                     RETCD VALUE OF 4        LG @ZM2359
CRNTASCB EQU   12                                            LG @ZM2359
CRRNTTCB EQU   4                                             LG @ZM2359
NUMUCB   EQU   16                  DISPL IN DEB OF NUMUCBS  D11 ZA04882
         EJECT
SVC070   DS    0D                      ALIGN SVC TO A DBLE WORD BNDRY
         BALR  BASERG,0                 SET UP BASE REGISTER
         USING *,BASERG                 DEFINE BASE REGISTER
         B     MODID2              HOP AROUND EYECATCHER
MODID    DC    C'IGC070.VS2R3.&SYSDATE'         MODULE EYECATCHER ID
MODID2   L     SVCRG4,0(0,PARMRG)       LOAD DCB ADDRESS AND INDX FACTR
         LR    SVCRG1,SVCRG4            TRANSFER INDEX FACTOR
         SRL   SVCRG1,24                POSITION INDEX FACTOR
         LR    SVCRG7,PARMRG            SAVE REG1            LD YA00813
         LA    SVCRG4,ZERO(SVCRG4)     DCB ADDRESS            LB Y01021
         LR    R8,R14                SAVE REG 14             LG @ZM2359
         L     R1,DEBADR(SVCRG4)       LD DEB ADDR           LG @ZM2359
         USING CVT,CVTREG              BASE FOR CVT          LG @ZM2359
         USING CVTXTNT2,CVTEXTRG       BASE FOR CVT EXT2     LG @ZM2359
         L     CVTREG,CVTPTR           LD ADDR CVT           LG @ZM2359
         L     CVTEXTRG,CVTEXT2        LD ADDR CVT EXT2      LG @ZM2359
         L     TCBREG,CVTTCBP          LD TCB PTRS           LG @ZM2359
         L     ASCBREG,CRNTASCB(TCBREG)  LD ADDR CURR ASCB   LG @ZM2359
         L     TCBREG,CRRNTTCB(TCBREG)  LD ADDR CURR TCB     LG @ZM2359
         L     BRREG,CVTDEBVR          LD ADDR DEB VERIFY    LG @ZM2359
         SR    R0,R0                   CLEAR REGISTER        LG @ZM2359
         BALR  R14,BRREG               GO DO DEBVERIFY       LG @ZM2359
         B     GOODCB              DEB IS VALID (+0)        D11 ZA04881
         LR    R14,R8              RESTORE R14              D11 ZA04881
         B     BADDCB              DEB INVALID              D11 ZA04881
GOODCB   LR    R14,R8              RESTORE R14              D11 ZA04881
         LR    SVCRG4,R1               PUT DEB ADDR IN R4   E12 ZA25652
         LR    PARMRG,SVCRG7            RESTORE REG1         LD YA00813
         CLM   SVCRG1,1,NUMUCB(SVCRG4) USERUCB GT DEBUCB?   D11 ZA04882
         BH    BADDCB              YES,INVALID PARAMETER    D11 ZA04882
         SLL   SVCRG1,2            X4(EACH UCB IN DEB LN=4) D11 ZA04882
         L     SVCRG4,UCBADR(SVCRG1,SVCRG4)  LOAD UCB ADDR  D11 ZA04882
         TM    2(SVCRG4),X'FF'          TEST FOR VALID UCB  ID
         BO    GCBCHK                   BRANCH IF VALID     D11
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(IEAVESVC)      E12 ZA26470
         WTO   'IFF421I GSERV FOUND INVALID UCB',                S21016X
               ROUTCDE=11,DESC=7                                 S21016
         LA    1,X'056'                LOAD ABEND ERROR CODE
         SLL   1,12                    POSITION CODE
         ABEND (1),DUMP
BADDCB   EQU   *                                             LG @ZM2359
         LA    R15,RC4               LD RETCD OF 4           LG @ZM2359
         BR    R14                   RETURN                  LG @ZM2359
         EJECT
GCBCHK   CLC   GUCBTYP(2,SVCRG4),G2250  IS UCB FOR A 2250?  D11 ZA13197
         BE    RESET                    BRANCH IF VALID UCB D11 ZA13197
         CLC   GUCBTYP(2,SVCRG4),G2260  IS UCB FOR A 2260?  D11 ZA13197
         BE    RESET                    BRANCH IF VALID UCB D11 ZA13197
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(IEAVESVC)      E12 ZA26470
         WTO   'IFF422I DEVICE NOT SUPPORTED BY GAM',       D11 ZA13197X
               ROUTCDE=11,DESC=7                                 S21016
         LA    1,X'057'                LOAD ABEND ERROR CODE
         SLL   1,12                    POSITION CODE
         ABEND (1),DUMP
RESET    SRL   PARMRG,24                POSITION BIT INDICATOR
         LA    SVCRG1,3                 LOAD PROTECT MASK FOR GRAPHIC
*                                       SUPERVISOR BITS IN GCB
         NR    PARMRG,SVCRG1            MASK OUT ANY USER REQUEST TO
*                                       RESET PROTECTED GRAPHIC
*                                       SUPERVISOR BITS IN GCB
         LA    SVCRG1,X'FF'             LOAD INITIAL RESET MASK
         XR    PARMRG,SVCRG1            DETERMINE FINAL RESETMASK
         L     SVCRG1,GCBADR(0,SVCRG4)  LOAD GCB
         NR    PARMRG,SVCRG1            RESET SPECIFIED GCB BITS
         STC   PARMRG,GCBADR+3(0,SVCRG4)     STORE NEW GCB BYTE
         LA    PARMRG,0                 INDICATE NORMAL RETURN
         BR    14
G2250    DC    X'1002'             UCBTYPE FOR 2250         D11 ZA13197
G2260    DC    X'1003'             UCBTYPE FOR 2260         D11 ZA13197
PATCH    DS    0F                 40 BYTE PATCH AREA        D11 @ZM2359
         DC    40X'FF'                                      D11
         CNOP  0,8
         EJECT
         CVT   DSECT=YES
         IHAPSA                                             E12 ZA26470
         END
