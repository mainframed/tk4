CEAF     TITLE 'GRAPHIC ACCESS METHOD CHANNEL END AND EXCEPTIONAL CHANN-
               EL END MODULE'
***********************************************************************
*                                                                     *
*        GRAPHIC CHANNEL END AND EXCEPTIONAL CHANNE END APPENDAGE     *
*                                                                     *
*STATUS CHANGE LEVEL 1
*                                                                     *
* FUNCTION:                                                           *
*        CHANNEL END                                                  *
*            1 IF THE DEVICE IS A FILM UNIT GO TO 5, ELSE GO TO 2.    *
*            2 IF GIOCR IS WAITING FOR AN IOB, POST THE SPECIAL ECB   *
*              AND GO TO 3.                                           *
*            3 TURN OFF THE AVAILABILITY BIT IN THE IOB AND GO TO 4.  *
*            4 NORMAL RETURN TO I/O SUPERVISOR.                       *
*            5 TURN OFF THE DEVICE END POST FLAG AND GO TO 2.         *
*        ABNORMAL END APPENDAGE                                       *
*            1 IF GRAPHIC DEVICE PERFORMING VARIABLE LENGTH INPUT     *
*              COMMAND HAS CAUSED INCORRECT LENGTH STATUS, RETRIEVE   *
*              RESIDUAL COUNT AND GO TO 3,  OTHERWISE GO TO 2.        *
*            2 IF FILM UNIT WITH PERMANENT ERROR CONDITION, ESTABLISH *
*              STATUS INDICATOR POINTER AND GO TO 3, ELSE GO TO 3.    *
*            3 GO TO CHANNEL-END-FUNCTION-2.                          *
*                                                                     *
* ENTRY POINT: IGG019OB, CEAPPEND VIA BALR IN IOS                     *
*                                                                     *
* INPUT  USE OF REGISTERS OF IOS                                   3574
*                                                                  3574
*              IN REGISTER  1 TWELVE STAR ADDRESS (RQE) IF THERE   3574
*                                           IS ONE                 3574
*              IN REGISTER  2 IOB ADDRESS                          3574
*              IN REGISTER  3 DEB ADDRESS                          3574
*              IN REGISTER  4 DCB ADDRESS                          3574
*              IN REGISTER  6 UNIT ADDRESS                         3574
*              IN REGISTER  7 UCB ADDRESS                          3574
*              IN REGISTER 14 RETURN ADDRESS TO IOS                3574
*              IN REGISTER 15 ENTRY POINT ADDRESS OF APPENDAGE     3574
*                                                                     *
* OUTPUT: N/A                                                         *
*                                                                     *
* EXTERNAL ROUTINES: N/A                                              *
*                                                                     *
* EXITS  NORMAL RETURN TO IOS                                      3574
*                                                                     *
* TABLE: N/A                                                          *
*                                                                     *
* WORK AREA: N/A                                                      *
*                                                                     *
* ATTRIBUTES: PROBLEM PROGRAM RESIDENCE, SUPERVISOR ENTERED           *
*                                                                     *
* NOTES: N/A                                                          *
*                                                                     *
*2928,005400,006600,014800,015200-016400,017600-020800,            3574
*     022000-022400,024800-027200,029400,031000-033600,            3574
*     039000-039200,039800-040000                                  3574
*
*1933,010660,010720                                            PTM 2173
* 014400,014700,015180-015480,022600-030800                       AOS/1
* A19800-20200,C21000,D21600                                    YA03230
* A11900,C38800,A38802-38818,C39330                         D11 ZA15423
*A010420-010440,A11820-11880,A12140-12141,C16265            E12 ZA26484
*A16270-16520,A17400,C17702,A17750,C17760-17850             E12 ZA26484
*D039330,A039360                                            E12 ZA29494
*C010800,C011600,A011801,D012600,A013002-013010,A039340     E12 ZA29656
***********************************************************************
*
*
*       THESE ARE THE SYMBOLIC REGISTERS
*
REG0     EQU   0
REG1     EQU   1
RIOB     EQU   2
RDEB     EQU   3
RDCB     EQU   4
RWORK    EQU   5                                            E12 ZA26484
RTCB     EQU   6                                            E12 ZA26484
RUCB     EQU   7
RBASE    EQU   9                       BASE ADDRESS         E12 ZA29656
REG10    EQU   10
REG11    EQU   11
REG12    EQU   12
RCCW     EQU   12                       CHANNEL COMMAND WORD   PTM 2173
*                                       REGISTER               PTM 2173
RSAVE    EQU   13                      SAVE AREA ADDRESS    E12 ZA29656
RRETRN   EQU   14
RENTRY   EQU   15                      ENTRY ADDRESS        E12 ZA29656
*                   DISPLACEMENTS FOR CVT,TCB               E12 ZA26484
CVTPTR   EQU   16                      CVT ADDRESS          E12 ZA26484
TCBPTR   EQU   0                       CVT DOUBLE WORD      E12 ZA26484
CURRTCB  EQU   4                       ADDRESS CURR TCB     E12 ZA26484
*
*                                                                  3574
*                                                                  3574
*                  IOB DISPLACEMENTS                               3574
*                                                                  3574
*                                                                  3574
SENSE2   EQU   3                       SECOND SENSE BYTE           3574
ECBCODE  EQU   4                       EVENT CONTROL BLOCK POST    3574
*                                      CODE                        3574
STATUS   EQU   12                      FIRST BYTE OF CHANNEL       3574
*                                      STATUS WORK                 3574
STATUS2  EQU   13                      SECOND BYTE OF CHANNEL      3574
*                                      STATUS WORK                 3574
CSWCOUNT EQU   14                      BYTE COUNT FIELD OF CHANNEL 3574
*                                      STATUS WORK                 3574
CCWSTAD  EQU   16                       CHANNEL COMMAND START      3574
*                                       ADDRESS AT TOP OF THE      3574
*                                       CHAIN                      3574
IOBCOUNT EQU   30                      ERROR RECOVERY FIELD USED   3574
*                                      FOR COMMUNICATION BETWEEN   3574
*                                      THE ABNORMAL END APPENDAGE  3574
*                                      AND THE ERROR RECOVERY      3574
*                                      PROGRAM                     3574
*                                                                  3574
*                                                                  3574
*                     CHANNEL COMMAND WORD FIELDS AND FLAGS        3574
*                                                                  3574
COMMCODE EQU   0                        CHANNEL COMMAND WORD       3574
*                                       COMMAND CODE DISPLACEMENT  3574
CCWFLAGS EQU   4                        CHANNEL COMMAND WORD FLAG  3574
*                                       FIELD DISPLACEMENT         3574
CMDCHAIN EQU   X'40'                    COMMAND CHAINING BIT OF    3574
*                                       CHANNEL COMMAND WORD       3574
*                                                                  3574
*                   UNIT CONTROL BLOCK DISPLACEMENTS               3574
*                                                                  3574
UNITTYPE EQU   19                      UNIT TYPE FIELD             3574
*                  UNIT TYPE MASKS                                 3574
A2250    EQU   X'02'                   2250 TYPE CODE              3574
A2260    EQU   X'03'                   2260 TYPE CODE              3574
*                                                                  3574
*                                                                  3574
*                        DATA EVENT CONTROL BLOCK DISPLACEMENTS    3574
*                                                                  3574
STATUSPT EQU   16                       STATUS INDICATOR POINTER   3574
*                                       (IOB)                      3574
*                                                                  3574
IOSERFG  EQU   44                      IOS ERROR FLAGS             3574
*                   FLAG MEANING                                   3574
ERRCRT   EQU   X'40'                   ERROR CORRECTION IN         3574
*                                      PROGRESS                    3574
PECOND   EQU   X'80'                   PERMANENT ERROR CONDITION   3574
*                                      EXISTS                      3574
*                                                                  3574
*                   CHANNEL STATUS WORD STATUS MASKS               3574
*                                                                  3574
ATTN     EQU   X'80'                   ATTENTION                   3574
INCLNGTH EQU   X'40'                   INCORRECT LENGTH            3574
UNITCK   EQU   X'02'                   UNIT CHECK                  3574
ATTNUC   EQU   ATTN+UNITCK             ATTENTION PLUS UNIT CHECK   3574
ICC      EQU   X'02'              INTERFACE CONTROL CHECK          3574
*                                      CSW INDICATOR               3574
CCC      EQU   X'04'              CHANNEL CONTROL CHECK            3574
*                                      CSW INDICATOR               3574
CDC      EQU   X'08'              CHANNEL DATD CHECK               3574
*                                      CSW INDICATOR               3574
CHNLERR  EQU   ICC+CCC+CDC        CHANNEL ERROR CONDITIONS         3574
*                                 STATUS INDICATORS                3574
*                                                                  3574
*                   OTHER MASKS                                    3574
XFF      EQU   X'FF'                   ALL ONES BYTE MASK   E12 ZA26484
ZEROMASK EQU   X'00'                   ALL ZEROS BYTE MASK         3574
ZERO     EQU   0                       A VALUE OF ZERO             3574
ANEIGHT  EQU   8                        DISPLACEMENT VALUE OF 8    3574
PRMERR   EQU   X'02'                   PERMANENT ERROR FLAG IN     3574
*                                      IOB ERROR COUNT FIELD       3574
ERPENTRY EQU   X'01'                    ERP HAS BEEN ENTERED       3574
*                                       INDICATOR                  3574
PRMERRCD EQU   X'41'                   PERMANNNT I/O ERROR CODE    3574
*                                      TO BE PLACED IN ECB CODE    3574
*                                      FIELD                       3574
NOERRORS EQU   X'7F'                    SUCCESSFUL COMPLETION OF   3574
*                                       I/O                        3574
*                   READ COMMAND                                   3574
READCUR  EQU   X'06'                   GREAD COMMAND CODE FOR      3574
*                                      READ TO CORSOR TYPE ON      3574
*                                      2250                        3574
HEX04    EQU   X'04'                   HEX VALUE OF FOUR    E12 ZA26484
         EJECT                                                     3574
IGG019OB CSECT
         ENTRY CEAPPEND
         USING *,RBASE
CEAPPEND EQU   *                                            E12 ZA29656
         STM   REG0,RENTRY,ZERO(RSAVE) SAVE IOS REGS        E12 ZA29656
         LR    RBASE,RENTRY             SET UP BASE REG     E12 ZA29656
         B     BEGIN                   BRANCH AROUND ID     E12 ZA29656
MODID    DC    C'IGG019OB.VS2R3.&SYSDATE'  MODULE ID        E12 ZA29656
*
*       IF ABNORMAL CHANNEL END, TEST FOR INCORRECT LENGTH AND
*       TURN ON OR OFF APPROPIATE BITS FOR THE ERROR ROUTINE RETRY
*       CHECKING FOR 2250 AND 2260 ERROR CONDITIONS
*
BEGIN    TM    0(RIOB),X'04'            TEST IOB EXCEPTION BIT
         BZ    OHAIOB                  NORMAL CHANNEL END         AOS/1
         TM    30(RIOB),X'02'           TEST FOR ABNORMAL END APPEND.
         BO    OHAIOB                  PERMANENT ERROR SET BY ERP AOS/1
         OI    30(RIOB),X'02'           TURN ON AEA BIT
         TM    STATUS2(RIOB),CHNLERR   WAS THERE AN ERROR IN THE   3574
*                                      CHANNEL                     3574
         BNZ   OHARTN                  CHANNEL ERROR-GO TO ERP    AOS/1
CKUNITCK TM    STATUS(RIOB),UNITCK      WAS THE ERROR IN THE UNIT  3574
         BZ    ZEROSNSE                NO TAKE BRANCH              3574
         TM    SENSE2(RIOB),XFF         DID SENSE FAIL      E12    3574
         BNO   TST2260                 IF DID NOT FAIL TAKE BRANCH 3574
ZEROSNSE NI    SENSE2(RIOB),ZEROMASK        ZERO OUT SECOND SENSE  3574
*                                      BYTE                        3574
TST2260  CLI   UNITTYPE(RUCB),A2260          IS DEVICE A 2260      3574
         BNE   OHALPD                  IF NOT TAKE BRANCH          3574
         TM    STATUS(RIOB),UNITCK     WAS THE ERROR IN THE UNIT   3574
         BO    OHARTN                  YES TAKE BRANCH             3574
         CLI   STATUS2(RIOB),INCLNGTH        WAS ERROR INCORRECT   6574
*                                      LENGTH ONLY                 3574
         BNE   OHARTN                  IF NOT TAKE BRANCH          3574
STORECSW EQU   *                                            E12 ZA26484
         USING IHADCB,RDCB             DCB ADDRESSIBILITY   E12 ZA26484
         USING TCB,RTCB                TCB ADDRESSIBILITY   E12 ZA26484
         L     RTCB,CVTPTR             GET CVT ADDR         E12 ZA26484
         L     RTCB,TCBPTR(RTCB)       GET DBLWRD ADDR      E12 ZA26484
         L     RTCB,CURRTCB(RTCB)      GET CURRENT TCB      E12 ZA26484
         LH    REG10,CSWCOUNT(RIOB)    COUNT FROM IOB CSW   E12 ZA26484
         L     REG11,4(0,RIOB)          ECB ADDRESS FROM IOB
         LA    REG11,0(0,REG11)         DROP HIGH ORDER BYTE
         MODESET  EXTKEY=TCB,WORKREG=5                      E12 ZA26484
         ST    REG10,16(0,REG11)        COUNT IN DECB
         MVI   16(REG11),X'43'          INCORRECT LENGTH CODE TO DECB
CLRDCBFG NI    DCBIFLGS,XFF-DCBIFEC             RESET IOS   E12 ZA26484
*                                        ERROR FLAGS IN DCB E12 ZA26484
         MODESET  EXTKEY=SUPR          RETURN TO KEY ZERO   E12 ZA26484
         NI    0(RIOB),XFF-HEX04     TURN OFF IOB EXEC BIT  E12 ZA26484
         MVI   ECBCODE(RIOB),NOERRORS  SET IOBECBCC TO      E12 ZA26484
*                         INDICATE SUCCESSFUL OPERATION     E12 ZA26484
         B     OHAIOB                  GO RELEASE IOB       E12 ZA26484
OHALPD   CLI   UNITTYPE(RUCB),A2250         IS DEVICE A 2250       3574
         BNE   OHARTN                  IF NOT TAKE BRANCH          3574
         CLI   STATUS(RIOB),ATTNUC     IS STATUS ATTENTION PLUS    3574
*                                      UNIT CHECK                  3574
         BE    OHAOFF                  IF YES TAKE BRANCH          3574
         TM    STATUS(RIOB),UNITCK     WAS THE ERROR IN THE UNIT   3574
         BO    OHARTN                  IF YES TAKE BRANCH          3574
         CLI   STATUS2(RIOB),INCLNGTH        WAS ERROR INCORRECT   3574
*                                      LENGTH ONLY                 3574
         BNE   OHARTN                  IF NOT TAKE BRANCH          3574
         L     RCCW,CCWSTAD(RIOB)      GET CCW START ADDRESS       3574
         TM    CCWFLAGS(RCCW),CMDCHAIN       IS FIRST COMMAND      3574
*                                      CHAINED TO THE SECOND       3574
         BZ    OHARTN                  IF NOT TAKE BRANCH          3574
         LA    RCCW,ANEIGHT(RCCW)      GET NEXT CCW ADDRESS        3574
         CLI   COMMCODE(RCCW),READCUR        WAS COMMAND READ      3574
*                                       TO CURSOR                  3574
         BNE   OHARTN                  IF NOT TAKE BRANCH          3574
         B     STORECSW                GO STORE COUNT INTO DECB    3574
OHAOFF   EQU   *                                                YA03230
         NI    0(RIOB),XFF-HEX04       RESET IOB EXCEPT BIT E12 ZA26484
         TM    3(RIOB),X'80'            LIGHT PEN               YA03230
         BZ    OHARTN                                       E12 ZA26484
*          ONE LINE DELETED BY APAR-------------------->    E12 ZA26484
*                                                                     *
*       NORMAL CHANNEL END FOR RELEASE OF IOB AND POSTING COMPLETE
*       IF GIOCR WAITING FOR AN AVAILABLE IOB
*
*        TEST IF GIOCR WAITING FOR IOB TO BECOME AVAILABLE            *
*                                                                     *
OHAIOB   L     REG11,28(0,RDCB)         FIRST IOB ADDRESS
         S     REG11,FOUR               ECB ADDRESS
         TM    0(REG11),X'80'           TEST IF WAITING IN GIOCR
         BZ    OHANEXT
*                                                                     *
*        POST WAITING ECB BY GIOCR COMPLETE                           *
*                                                                     *
         SR    REG10,REG10              SET COMPLETION CODE TO ZERO
         L     REG12,0(RDEB)            PLACE TCB ADDRESS
         L     15,16(0,REG10)           CVT ADDRESS
         L     15,152(0,15)             POST ROUTINE
         LR    RIOB,RRETRN              SAVE RETURN ADDRESS
         BALR  14,15                    POST ROUTINE
         LR    RRETRN,RIOB              RESTORE RETURN ADDR
         L     RIOB,4(0,REG1)           RESTORE RIOB REG
*                                                                     *
*        TURN OFF NON-AVAILABLE BIT IN IOB WHICH WILL MAKE THIS       *
*        IOB NOW AVAILABLE                                            *
*                                                                     *
OHANEXT  S     REG11,FOUR          BACK UP TO IOB SAVEWORD  D11 ZA15423
         L     REG10,0(REG11)      AND LOAD ADR OF SAVED IOBD11 ZA15423
         LTR   REG10,REG10         IS THERE A PREVIOUS IOB? D11 ZA15423
         BZ    NOIOB               NO,SAVE CURR-DONT FREE   D11 ZA15423
         L     REG12,28(0,RDCB)    ADDR 1ST IOB FROM DCB    D11 ZA15423
CHKIOB   CLM   REG12,7,1(REG11)   DCBCHAIN IOB EQ SAVED IOB?D11 ZA15423
         BE    MAKAVAIL            YES,MAKE SAVED IOB AVAIL D11 ZA15423
         L     REG12,36(REG12)     POINT TO NEXT CHAINIOB   D11 ZA15423
         LTR   REG12,REG12         ANY MORE IOBS ON CHAIN?  D11 ZA15423
         BZ    NOIOB               NO,GO SAVE CURRENT       D11 ZA15423
         B     CHKIOB              YES,GO CHECK NEXT IOB    D11 ZA15423
MAKAVAIL NI    36(REG10),X'00'     FREE SAVED (FOUND) IOB   D11 ZA15423
NOIOB    ST    RIOB,0(REG11)       SAVE CURIOB IN IOBSAVEWD D11 ZA15423
*                                  CLEAR PERMANENT ERROR AND       3574
*                                  ERP ENTRY FLAGS IN              3574
*                                  IOB ERROR COUNT FIELD           3574
         NI    IOBCOUNT(RIOB),XFF-PRMERR-ERPENTRY           E12    3574
*                                                                  3574
*              ERP HAS THE RESPONSIBILITY FOR TURNING OFF          3574
*              ALL OTHER FLAGS IN THE IOB ERROR COUNT FIELD.       3574
*                                                                  3574
*                                                                  3574
*                                                                  3574
*              THE APPENDAGE WILL BE ENTERED TWICE FOR AN ERROR    3574
*              RECOVERY PROCESS ONCE AT IOS FINDING THE ERROR      3574
*              CONDITION AND THE SECOND JUST BEFORE IOS GOES TO    3574
*              POST THE PROBLEM PROGRAM ON COMPLETION OF I/O.      3574
*              THE APPENDAGE HAS THE RESPONSIBILITY OF FINDING OUT 3574
*              IF THE INCORRECT LENGTH ON A READ OPERATION IS A    3574
*              VALID ERROR OR EXPECTED.                            3574
*              A PROBLEM ARISES WITH THE ERP ABILITY TO RECOVER    3574
*              FROM CHANNEL ERRORS BUT STILL HAVE INCORRECT LENGTH.3574
*              THE ERP MUST LET THE APPENDAGE KNOW THAT THERE IS AN3574
*              ERROR CONDITION THAT MIGHT BE EXPECTED.             3574
*                                                                  3574
*              SO THE ERP HAS BEEN MODIFIED TO TREAT THE           3574
*              INCORRECT LENGTH AS A PERMANENT ERROR BUT TO        3574
*              LET THE APPENDAGE HAVE THE LAST TRY AT CHECKING     3574
*              THE ERROR OVER AND INDICATING THAT THERE IS NOT     3574
*              AN ERROR CONDITION.                                 3574
*              THIS IS DONE BY THE ERP BIT SETTINGS SWITCHES IN    3574
*              THE IOB ERROR COUNT FIELD IN THE FIRST BYTE , THE   3574
*              LAST TWO BITS AND THE IOB FLAGS 2 FIELD             3574
*              THE MAIN INTENT IS SO THAT THE APPENDAGE WILL       3574
*              KNOW THAT THE ERP HAS CHECKED OVER THE ERROR        3574
*              CONDITION SO WHEN THE APPENDAGE RETURNS TO IOS THE  3574
*              ERROR RECOVERY IS OVER AND THE IOB MUST BE PLACED   3574
*        BACK ON TO THE FREE LIST TO KEEP FROM BEING LOST OR       3574
*        HAVING GAM IN THE WAIT STATE WAITING ON THE IOB.          3574
*                                                                  3574
*              THE BIT SETTINGS AND THERE MEANING ARE              3574
*                  IOB ERROR COUNT FIELD IN HEX                    3574
*                                                                  3574
*              '00'     NORMAL CHANNEL END NO ERROR                3574
*                       RECOVERY DONE                              3574
*                  '03'   PERMANENT ERROR FOUND BY ERP             3574
*                        IOB EXECPTION BIT WILL BE ON ALSO         3574
*                   '01'   ERP HAS BEEN ENTERED SO IOB MUST BE     3574
*                          FREED                                   3574
*                          WITH IOB EXCEPTION BIT ON THERE COULD   3574
*                                 BE AN ERROR CONDITION            3574
*                                      WITH IOB EXECPTION BIT OFF  3574
*                                      ERROR RECOVERY SUCCESSFUL   3574
OHARTN   TM    IOBCOUNT(RIOB),ERPENTRY       HAS ERP BEEN ENTERED  3574
         BO    OHAIOB                  YES GO FREE IOB             3574
         LM    REG0,RENTRY,ZERO(RSAVE)  RESTORE REGS        E12 ZA29656
         BR    RRETRN           RETURN TO IOS               E12 ZA29498
*
FOUR     DC    F'4'
*
         CNOP  0,8
         IKJTCB                                             E12 ZA26484
         IHADCB                                             E12 ZA26484
         END
