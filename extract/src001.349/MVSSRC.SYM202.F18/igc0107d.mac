***********************************************************************
* MODULE NAME: IGC0107D (DAR MESSAGE MODULE)
*
* STATUS: CHANGE LEVEL 0
*
* FUNCTION: ISSUE WRITE-TO-PROGRAMMER MESSAGES FOR ERRORS DISCOVERED BY
* IGC0007D (DAR)
*
* ENTRY POINT: IGC0107D VIA XCTL FROM IGC0007D
*
* INPUT: REG3 - ADDRESS OF DAR PARAMETER LIST
*        REG6 - ADDRESS OF GACB BEING PROCESSED
*        REG7 - RETURN CODE SET BY DAR; INDEXES MSGLIST
*
* OUTPUT: WRITE-TO-PROGRAMMER MESSAGE
*         REGISTER 15 - RETURN CODE 04 NO DCB ADDRESS IN GACB
*                                   08 GACB NOT SPEC BY SPAR
*                                   0C NO GACB ADDRESS FOR DAR
*                                   10 ATTN RTN STILL ACTIVE
*                                   14 DAR NOT IN SAME TASK AS SPAR
*                                   18 DCB NOT OPEN
*                                   1C NON-GRAPHIC DCB
*                                   20 NOT BASIC ATTN ENVIRONMENT
*
*         REGISTER 1 - ADDRESS OF GACB BEING PROCESSED,EXCEPT FOR
*                      RETURN  CODE 0C; THEN,ADDR OF PARAM LIST
*
* EXIT: VIA SVC 3
*
* TABLES: MSGLIST - DISPLACEMENTS TO LIST FORMS OF WTO FOR MESSAGES
*
* ATTRIBUTES: SUPERVISOR STATE,REENTRANT,TRANSIENT
*
***********************************************************************
IGC0107D CSECT
* REGISTER EQUATES
R0       EQU   0
R1       EQU   1
R2       EQU   2                       POINTER TO SVRB EXT
RPARM    EQU   3                       PARAM LIST  ADDR          S21016
RGACB    EQU   6                       ADDR OF GACB              S21016
RCODE    EQU   7                       MSGLIST OFFSET(INPUT)
RWORK    EQU   8
RMSG     EQU   9                       ADDR OF MSG
RBUF     EQU   10                      POINTER TO MSG BUFFER
RLNGTH   EQU   11                      FOR LENGTH OF MSG
RBASE    EQU   12                      BASE REG
R14      EQU   14
R15      EQU   15
         SPACE 3
* MISC. EQUATES
X80      EQU   X'80'                   DUMP CODE
X01      EQU   X'01'
X061     EQU   X'061'                  ABEND CODE/CLOSE-DAR BAD
SHFT12   EQU   12                      SHIFT COUNT
SHFT24   EQU   24                      SHIFT COUNT
CVTBTERM EQU   52                      DISP OF ABTERM ADDR IN CVT
D47      EQU   47                      DISP OF GACB FLAG BYTE
X0C      EQU   X'0C'                   RETURN CODE COMPARAND
EXIT     EQU   3                       SVC NUMBER FOR EXIT RTN
CVTPTR   EQU   16                      CVT POINTER
D0       EQU   0                       DISPLACEMENT
D4       EQU   4                       DISPLACEMENT
D96      EQU   96                      DISPLACEMENT
         SPACE 3
         BALR  RBASE,R0                LOAD BASE REG
         USING *,RBASE
ORG      EQU   *
         LTR   RCODE,RCODE             CHK FOR CLOSE-DAR BAD
         BP    GETDISP                 BR IF NOT CLOSE-DAR
         L     RMSG,CLMSGDSP           DISP OF CLOSEMSG
         B     GETADDR                 BR TO GET ADDRESS OF MSG
GETDISP  EQU   *
         L     RMSG,MSGLIST(RCODE)     DISPLACEMENT OF MSG
GETADDR  EQU   *
         LA    RMSG,R0(RMSG,RBASE)     ADDRESS OF MSG
         L     R2,CVTPTR               CVT ADDRESS
         L     R2,D0(R2)               ADDRRSS OF TCB POINTERS
         L     R2,D4(R2)               CURRENT TCB ADDR
         L     R2,D0(R2)               CURRENT RB ADDR
         LA    R2,D96(R2)              ADDR OF SVRB EXT
         MVC   D4(GMLNG,R2),GMLIST     MOVE GM I&ST TO SVRB EXT
         LA    RWORK,D4(R2)            ADDR OF GM PARM LIST
         GETMAIN A=(R2),MF=(E,(RWORK))  GET MSG BUFFER
         LTR   R15,R15                 DID GM SUCCEED
         BNZ   SKIPWTO                 BRANCH IF GM FAILED
         L     RBUF,D0(R2)             ADDR OF MSG BUFFER
         LH    RLNGTH,D0(RMSG)         LENGTH OF MSG
         LA    RLNGTH,D4(RLNGTH)       +4 FOR DESC,RTG CODES
         EX    RLNGTH,MOVEMSG          MOVE MSG TO BUFFER
         WTO   MF=(E,(RBUF))           ISSUE MSG
         FREEMAIN A=(R2),MF=(E,(RWORK))  FREE BUFFER
SKIPWTO  EQU   *
         LTR   RCODE,RCODE             CHK FOR CLOSE-DAR BAD
         BNP   CLOSEDAR                BR IF CLOSE-DAR BAD
         LR    R1,RGACB                CURRENT GACB ADDR
         LA    RWORK,X0C               RETURN CODE COMPARAND
         CR    RWORK,RCODE             IS ERROR FOR SHORT PARM LIST
         BNE   LEAVE                   IF NOT,RETURN GACB ADDR
         LR    R1,RPARM                RETURN ADDR OF BAD PARM LIST
LEAVE    EQU   *
         LR    R15,RCODE               SET RETURN CODE
         SVC   EXIT
CLOSEDAR EQU   *                       ABTERM TASK THAT ISSUED SPAR
         LA    R0,X80                  LOAD DUMP CODE
         SLL   R0,SHFT24               DUMP CODE TO HI-BYTE
         LA    R1,X061                 LOAD ABEND CODE
         SLL   R1,SHFT12               SHIFT ABEND CODE
         OR    R1,R0                   DUMP CODE TO HI-BYTE
         LR    R0,RCODE                GET REB TCB ADDRESS
         L     R15,CVTPTR              ADDR OF CVT
         L     R15,CVTBTERM(R15)       EP ADDRESS OF ABTERM
         BALR  R14,R15                 BRANCH TO ABTERM
         XI    D47(RGACB),X01          TURN OFF FLAG SET BY CLOSE
         SVC   EXIT                    EXIT
         EJECT
MOVEMSG  MVC   D0(D0,RBUF),D0(RMSG)
GMLIST   GETMAIN EC,LV=80,MF=L         GM PARM LIST
GMLNG    EQU   *-GMLIST                LENGTH OF GMLIST
         DS    0F
MSGLIST  EQU   *-4                     MESSAGE SELECTION TABLE
* RCODE PASSED AS INPUT IS DISPLACEMENT FROM MSGLIST ORIGIN
         DC    A(MSG1-ORG)
         DC    A(MSG2-ORG)
         DC    A(MSG3-ORG)
         DC    A(MSG4-ORG)
         DC    A(MSG5-ORG)
         DC    A(MSG6-ORG)
         DC    A(MSG7-ORG)
         DC    A(MSG8-ORG)
MSG1     WTO   'IFF342I DAR FOUND ZERO IN DCB ADDRESS FIELD OF GACB',  X
               ROUTCDE=11,DESC=7,MF=L
MSG2     WTO   'IFF346I DAR ISSUED FOR GACB NOT SPECIFIED BY SPAR MACROX
               ',ROUTCDE=11,DESC=7,MF=L
MSG3     WTO   'IFF341I DAR PARAMETER LIST TOO SHORT TO CONTAIN GACB ADX
               DRESSES',ROUTCDE=11,DESC=7,MF=L
MSG4     WTO   'IFF349I DAR, USING GACB INPUT, FOUND ATTENTION ROUTINE X
               STILL ACTIVE',ROUTCDE=11,DESC=7,MF=L              S21016
MSG5     WTO   'IFF347I DAR NOT ISSUED BY SAME TASK THAT ISSUED SPAR FOX
               R THE GACB',ROUTCDE=11,DESC=7,MF=L
MSG6     WTO   'IFF343I DAR, USING GACB INPUT, FOUND DCB NOT OPEN',    X
               ROUTCDE=11,DESC=7,MF=L
MSG7     WTO   'IFF344I DAR, USING GACB INPUT, FOUND DCB NOT GRAPHIC', X
               ROUTCDE=11,DESC=7,MF=L
MSG8     WTO   'IFF345I DAR ISSUED FOR GACB, BUT ATTENTION ENVIRONMENT X
               NOT BASIC',ROUTCDE=11,DESC=7,MF=L
CLMSGDSP DC    A(CLOSEMSG-ORG)         DISP OF CLOSEMSG
CLOSEMSG WTO   'IFF348I DAR ISSUED BY CLOSE, BUT GACB STILL SPECIFIED BX
               Y ANOTHER TASK',ROUTCDE=11,DESC=7,MF=L
         END
