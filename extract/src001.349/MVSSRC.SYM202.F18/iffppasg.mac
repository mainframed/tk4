         TITLE 'OFF SCREEN-GRID ROUTINE'
*STATUS. CHANGE LEVEL 0                                               *
*                                                                     *
*FUNCTION/OPERATION. GOFFSG ROUTINE COMPUTES THE INTERSECTION POINT   *
*   COORDINATES OF A VECTOR WITH THE USER-DESIRED DISPLAY BOUNDARY    *
*                                                                     *
*ENTRY POINTS. GOFFSG VIA CALL OR LINK MACRO   ALIAS NAME IFFPPASG    *
*                                                                     *
*INPUT. STARTING ADDRESS OF BOUNDARY LIMITS USED AND THE COORDINATES  *
*   OF THE START AND END POINTS OF THE VECTOR THAT INTERSECTS THE     *
*   BOUNDARY                                                          *
*                                                                     *
*OUTPUT. THE NUMBER OF INTERSECTION POINT(S) FOUND AND ITS COORDINATES*
*                                                                     *
*EXTERNAL ROUTINES. N/A                                               *
*                                                                     *
*EXIT. COMPLETION OF TASK EXIT VIA RETURN MACRO                       *
*                                                                     *
*TABLES/WORK AREAS. POR PROVIDES A PARAMETER TABLE(PARTAB) WHICH      *
*   CONTAINS THE ABOVE MENTIONED INPUT INFORMATION,GOFFSG OUTPUT      *
*   RESPONSE AREA,AND SCRATCH AREA.                                   *
*                                                                     *
*ATTRIBUTES. READ ONLY,REENTRANT                                      *
*                                                                     *
OFFSG    CSECT
GOFFSG   SAVE  (14,12),T,*              SAVE REGISTERS
         BALR  9,0
         USING *,9
         L     SABREG,0(PATREG)         GET START ADDR OF BOUNDARY
        USING  LIMITB,SABREG
         USING PARTAB,PATREG
*
*   CALCULATE POINTS OF INTERSECTIONS
CALCUL   L     DTXREG,X2                COMPUTE DX
         S     DTXREG,X1
         C     DTXREG,ZERO              IS ?X=O
         BNE   DELTAY                   IF NOT GO TO TEST ?Y
         MVC   X3,X1                    OTHERWISE SET PT3=X1,YL
         LH    WRKREG,YL
         ST    WRKREG,Y3
         MVC   X4,X1                              SET PT4=X1,YH
         LH    WRKREG,YH
         ST    WRKREG,Y4
         B     SETNI2                   GO TO SET NI VALUE TO 2
DELTAY   L     DTYREG,Y2                COMPARE ?Y OI PT1 AND PT2
         S     DTYREG,Y1
         C     DTYREG,ZERO              IS ?=O
         BNE   SETNI4                   IF NOT GO TO SET NI=4
         LH    WRKREG,XL                OTHERWISE SET PT3=XL,Y1
         ST    WRKREG,X3
         MVC   Y3,Y1
         LH    WRKREG,XH                AND SET PT4=XH,Y1
         ST    WRKREG,X4
         MVC   Y4,Y1
SETNI2   MVC   NI,TWO                   SET NI=2
         B     ELIMIN                   BRANCH TO ELIMINATE OUTSIDE PTS
SETNI4   MVC   NI,FOUR                  SET NI=4
         L     BEEREG,Y1                COMPUTE: B=Y1-MX1
         LR    MDVREG,DTYREG
         SRDA  MDVREG,32
         M     MDVREG,X1                 WHERE MX1=X1*DY/DX
         DR    MDVREG,DTXREG
         SR    BEEREG,PDQREG            Y1-MX1
         LH    WRKREG,XL
         C     WRKREG,X1                COMPARE XL TO X1
         BE    SETPT3A                  IF EQUAL GO TO SET PT3=PT1
         C     WRKREG,X2
         BE    SETPT3B                  IF EQUAL GO TO SET PT3=PT2
         ST    WRKREG,X3
         LR    MDVREG,DTYREG            Y3=MX2+B
         SRDA  MDVREG,32
         MR    MDVREG,WRKREG             WHERE MX2=X1*DY/DX
         DR    MDVREG,DTXREG
         AR    PDQREG,BEEREG            PLUS B TO MX2
         ST    PDQREG,Y3                SAVE COMPUTED PT3 Y COODINATE
CKPT4    LH    WRKREG,XH                CHECK IF X1 OR X2=XH
         C     WRKREG,X1
         BE    SETPT4A                  IF X1=XH GO TO SET PT4=PT1
         C     WRKREG,X2
         BE    SETPT4B                  IF X2=XH GO TO SET PT4=PT2
         ST    WRKREG,X4                OTHERWISE X4=XH
         LR    MDVREG,DTYREG             Y4=MXH+B
         SRDA  MDVREG,32
         MR    MDVREG,WRKREG
         DR    MDVREG,DTXREG
         AR    PDQREG,BEEREG
         ST    PDQREG,Y4
CKPT5    LH    WRKREG,YL                CHECK IF Y1 OR Y2=YL
         C     WRKREG,Y1
         BE    SETPT5A                  IF Y1=YL GO TO SET PT5=PT1
         C     WRKREG,Y2
         BE    SETPT5B                  IF Y2=YL GO TO SET PT5=PT2
         ST    WRKREG,Y5                OTHERWISE Y5=YL
         SR    WRKREG,BEEREG            X5=(YL-B)/M OR (YL-B)*DX/DY
         LR    MDVREG,DTXREG
         SRDA  MDVREG,32
         MR    MDVREG,WRKREG
         DR    MDVREG,DTYREG
         ST    PDQREG,X5
CKPT6    LH    WRKREG,YH                CHECK IF Y1 OR Y2=YH
         C     WRKREG,Y1
         BE    SETPT6A                  IF Y1=YH GO TO SET PT6=PT1
         C     WRKREG,Y2
         BE    SETPT6B                  IF Y2=YH GO TO SET PT6=PT2
         ST    WRKREG,Y6                OTHERWISE Y6=YH
          SR   WRKREG,BEEREG            X6=(YH-B)/M OR(YH-B)*DX/DY
         LR    MDVREG,DTXREG            WHERE M IS EQUAL TO DY/DX
         SRDA  MDVREG,32
         MR    MDVREG,WRKREG
         DR    MDVREG,DTYREG
         ST    PDQREG,X6                SAVE COMPUTED X6
         B     ELIMIN                   BRANCH TO ELIMATION SUBRT
SETPT3A  MVC   PT3,PT1                  SET PT3=PT1
         B     CKPT4
SETPT3B  MVC   PT3,PT2                  SET PT3=PT2
         B     CKPT4
SETPT4A  MVC   PT4,PT1                  SET PT4=PT1
         B     CKPT5
SETPT4B  MVC   PT4,PT2                  SET PT4=PT2
         B     CKPT5
SETPT5A  MVC   PT5,PT1                  SET PT5=PT1
         B     CKPT6
SETPT5B  MVC   PT5,PT2                  SET PT5=PT2
         B     CKPT6
SETPT6A  MVC   PT6,PT1                  SET PT6=PT1
         B     ELIMIN
SETPT6B  MVC   PT6,PT2                  SET PT6=PT2
*   ELIMINATE PTS OUSIDE BOUNDARY AND/OR LINE LIMITS SUBROUTINE
*
ELIMIN   L     ICTREG,ONE               LOAD ONE TO I COUNT
         SR    INXREG,INXREG            CLEAR INDEX REGISTER
         SR    OUTREG,OUTREG            LOAD ZERO TO ZERO COUNT
         SR    POSREG,POSREG            CLEAR POSITION REGISTER
         L     WRKREG,X1                LOAD X1
         C     WRKREG,X2                COMPARE X1 TO X2
         BH    REVERSE                   AND SET XR=MIN(X1,X2)
         MVC   XR,X1                             XS=MAX(X1,X2)
         MVC   XS,X2
         B     SETYSR
REVERSE  MVC   XR,X2
         MVC   XS,X1
SETYSR   L     WRKREG,Y1                LOAD Y1
         C     WRKREG,Y2                COMPARE Y1 TO Y2
         BH    REVERSEY                  AND SET YR=MIN(Y1,Y2)
         MVC   YR,Y1                             YS=MAX(Y1,Y2)
         MVC   YS,Y2
         B     TSLINE
REVERSEY MVC   YS,Y1
         MVC   YR,Y2
TSLINE   L     XIWREG,X3(INXREG)        GET COMPUTED PTS
         LH    WRKREG,XL
         CR    XIWREG,WRKREG            CHECK IF PT OUTSIDE LEFT LIMIT
         BL    SQEEZE                   IF YES TO SQEEZE
         C     XIWREG,XR                COMPARE WITH LINE RANGE
         BL    SQEEZE                   MASK IT IF NOT IN RANGE
         LH    WRKREG,XH
         CR    XIWREG,WRKREG            COMPARE THE PT WITH BOUNDARY
         BH    SQEEZE                   MASK IT IF OUTSIDE BOUNDARY
         C     XIWREG,XS                CHECK IF OUTSIDE LINE RANGE
         BH    SQEEZE                    IF YES GO TO SQEEZE
         L     YIWREG,Y3(INXREG)
         LH    WRKREG,YL
         CR    YIWREG,WRKREG            IS THE PT OUTSIDE LINE
         BL    SQEEZE
         C     YIWREG,YR                IS YI.YR
         BL    SQEEZE
         LH    WRKREG,YH
         CR    YIWREG,WRKREG            IS THE PT OUTSIDE BOUNDARY
         BH    SQEEZE
         C     YIWREG,YS                IS YI>YS
         BH    SQEEZE
         C     XIWREG,X1                IS THIS PT SAME AS P1
         BNE   P2TEST                   IF NOT COMPARE TO P2
         C     YIWREG,Y1                IS Y COORDINATE ALSO EQUAL Y1
         BE    SQEEZE                   IDENTICAL TO PT1 ELIMINATE IT
P2TEST   C     XIWREG,X2                X COORDINATE SAME AS PT2
         BNE   BUILDUP                  IF NOT GO TO BUILD UP INTS
         C     YIWREG,Y2                COMPARE Y COOR
         BE    SQEEZE                   IF ALSO EUQAL GO TO ELIMINATE
BUILDUP  STH   XIWREG,INTS1(POSREG)     BUILD UP INTERSECTIONS
         STH   YIWREG,INTS1+2(POSREG)
         A     POSREG,FOUR              INDEXING BY FOUR
         B     UPDATEI
SQEEZE   A     OUTREG,ONE               ADD ONE TO OUT COUNT
UPDATEI  A     ICTREG,ONE               I=I+1
         C     ICTREG,NI                IS I>NI
         BH    FINALNI                   IF YES GO TO COMPUTE FINAL NI
         A     INXREG,EIGHT              OTHERWISE INDEX AND CHECK NEXT
         B     TSLINE
FINALNI  L     WRKREG,NI                NI=NI-OUT
         SR    WRKREG,OUTREG
         ST    WRKREG,NI
ANALYNI  CLC   NI(4),TWO                IS NI=2
         BH    ELIMDUP                   IF YES ELIMINATE DUPLICATE
         BE    TWOPTS
         B     RET
TWOPTS   CLC   INTS1,INTS2              CHECK IF TWO PTS ARE SAME
         BE    ELIMONE                  GO TO ELIMINATE A DUPLICATE
         B     RET                     OTHERWISE RETURN
ELIMONE  MVC   NI,ONE                   SET NI=1
RET      RETURN (14,12),T,RC=(15)
*
*   SUBROUTINE FOR ELIMINATING DUPLICATE POINTS
*
ELIMDUP  CLC   INTS1,INTS2
         BE    TAKE23                   IF THESE TWO PT EQUAL TAKE 2&3
SETNI    MVC   NI,TWO
         B     RET                      GO TO RETURN
TAKE23   MVC   INTS1(8),INTS2           TAKE INTS2&3
         B     SETNI                    BRANCH TO RESET NI
ZERO     DC    F'0'                     CONSTANT ZERO
ONE      DC    F'1'                     CONSTANT ONE
TWO      DC    F'2'                     CONSTANT TWO
FOUR     DC    F'4'
EIGHT    DC    F'8'                     CONSTANT EIGHT
PARTAB   DSECT
SABL     DS    CL4
PT1      DS    CL8
         ORG   PT1
X1       DS    CL4
Y1       DS    CL4
PT2      DS    CL8
         ORG   PT2
X2       DS    CL4
Y2       DS    CL4
NI       DS    CL4
INTS1    DS    CL4
INTS2    DS    CL4
INTS3    DS    CL4
INTS4    DS    CL4
PT3      DS    CL8
         ORG   PT3
X3       DS    CL4
Y3       DS    CL4
PT4      DS    CL8
         ORG   PT4
X4       DS    CL4
Y4       DS    CL4
PT5      DS    CL8
         ORG   PT5
X5       DS    CL4
Y5       DS    CL4
PT6      DS    CL8
         ORG   PT6
X6       DS    CL4
Y6       DS    CL4
XR       DS    CL4
XS       DS    CL4
YR       DS    CL4
YS       DS    CL4
LIMITB   DSECT
XL       DS    CL2
YL       DS    CL2
XH       DS    CL2
YH       DS    CL2
PATREG   EQU   1
DTXREG   EQU   2
DTYREG   EQU   3
XIWREG   EQU   2
YIWREG   EQU   3
WRKREG   EQU   4
BEEREG   EQU   5
INXREG   EQU   5
MDVREG   EQU   6
PDQREG   EQU   7
OUTREG   EQU   6
ICTREG   EQU   7
PTAREG   EQU   6
PTBREG   EQU   7
POSREG   EQU   8
SABREG   EQU   10
LINREG   EQU   11
         END
