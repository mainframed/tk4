*TITLE 'ATTENTION INQUIRY ROUTINE'
*STATUS:CHANGE LEVEL 0
*FUNCTION:MODE DEPENDENT
*        1.MODE=R-RELINQUISH CONTROL FOR USER ATTENTION ROUTINE IF EP
*         IN GACB NOT EQUAL TO ZERO. WHEN USER GAINS CONTROL IT WILL
*         BE GIVEN AT NSI AFTER MODE=R.IF EP IN GACB=0 R IS TREATED AS
*         MODE=W.
*        2.MODE=W-IF REQUESTED DATA NOT AVAILABLE IT WILL PLACE ISSUING
*          ROUTINE IN WAIT STATE.IF DATA IS AVAILABLE,MOVES DATA TO
*          USER COMAREA AND RETURNS CONTROL TO NSI AFTER MODE=W.
*        3.MODE=C-IF DATA DESIRED IS AVAILABLE IT IS SELECTIVELY
*          DEQUEUED AND PLACED IN COMAREA;CONTROL RETURNED VIA BRANCH
*          ADDRESS.IF DATA NOT AVAILABLE CONTROL RETURNED TO NSI
*          AFTER MODE=C.
*        4.MODE=CLEAR-REMOVES ALL DATA FROM USER INPUT QUEUE(MUST LEAVE
*          ONE IQE ON USER IRB RBIQE FIELD FOR SYSTEM TO REMOVE)AND
*          RETURNS CONTROL TO THE ROUTINE THAT WAS INTERRUPTED TO GIVE
*          ATTENTION ROUTINE CONTROL.
*ENTRY POINT:VIA BALR 14,15 FROM ISSUING ROUTINE.
*INPUT:GR#1 POINTS TO PARAMETER LIST;GRS#13,14,15 AS PER CONVENTIONS.
*OUTPUT:MODE DEPENDENT;SEE FUNCTION.
*EXTERNAL ROUTINES:DEQUEUE(SVC 72)
*EXITS:VIA RETURN MACRO OR SVC 3.
*TABLES/WORK AREAS:N/A
*ATTRIBUTES:1.RESIDENT WITH GRAPHIC JOB
*           2.RE-ENTRANT
*           3.PROBLEM PROGRAM STATE
*NOTES:N/A
*
*        REGISTER DEFINITION
*
PAR0     EQU   0                       PARAM REG 0
PAR1     EQU   1                       PARAM REG 1
BASERG   EQU   2                       BASE REG
RGACB    EQU   3                       GACB ADR REG
REBRG    EQU   4                       ROUTINE ENTRY BLOCK ADR REG
RLIST    EQU   5                       LIST DSECT REG
RPFK     EQU   6                       SAVE REG FOR PFK MASK
RSAVE    EQU   7                       SAVE REG FOR GACB MASK ATTNTYP
R8       EQU   8                       WORK REG
R9       EQU   9                       WORK REG
R10      EQU   10                      WORK REG
R11      EQU   11                      WORK REG
R12      EQU   12                      WORK REG
SAVRG    EQU   13                      SAVE AREA PTR
RTNRG    EQU   14                      RETURN REG
RCODE    EQU   15                      RETURN CODE REG
*
*
FOUR     EQU   4                       FOUR
ZERO     EQU   0                       ZERO
REL      EQU   X'01'                   RELINQUISH MODE
WAIT     EQU   X'02'                   WAIT MODE
COND     EQU   X'04'                   CONDITIONAL MODE
CLEAR    EQU   X'08'                   CLEAR MODE
ANY      EQU   X'00'                   TYPE ANY(USE GACB MASK)
END      EQU   X'01'                   END OR A/N TYPE
LP       EQU   X'02'                   LIGAT PEN TYPE
EOS      EQU   X'03'                   END ORDER SEQUENCE TYPE
CANCEL   EQU   X'04'                   CANCEL TYPE
AE       EQU   X'05'                   ASYNCHRONOUS ERROR
A2260    EQU   X'06'                   2260 TYPE
POSTED   EQU   X'00'                   WAIT/POST BYTE OF ECB    YA03228
IRB      EQU   16                      IRB ADR IN REB
CVTADR   EQU   16                      ADR OF CVT TABLE
TCBP     EQU   0                       TABLE OF TCB ADRS
TCBCUR   EQU   4                       CURRENT TCB ADR
TCBIRB   EQU   0                       CURRENT(ACTIVE)RB
EXIT     EQU   3                       EXIT FROM ROUTINE SVC NO.
DEQUEUE  EQU   75                      DEQUEUE RTN SVC NO.
WAITST   EQU   X'80'                   WAIT BIT
RTN      EQU   12                      OFF SET TO RTN REG IN SAVE AREA
RTNIRB   EQU   16                      IRB ADR
RTNTCB   EQU   32                      OFFSET FOR REB TCB ADR
SIX      EQU   6                       SIX
EIGHT    EQU   8                       EIGHT
ONE      EQU   1                       ONE
RBIQE    EQU   24                      NEXT IQE OFF IRB
ATTNERR  EQU   X'41'                                          LI F40815
IGG019OK CSECT
* 465000-470000,516000                                           A31230
*A232000,A442000-445000,A541000-542000,A697000                LI F40815
*A186500,A443500-443600,A516500,D528000                         YA03228
*C496000-497000,A684020-687050                              E12 ZA20103
*
*
         SAVE  (14,12)
         BALR  BASERG,PAR0             SET UP BASE REG
         USING *,BASERG                DEFINE BASE REG
         USING PARLIST,RLIST           DEFINE DSECT REG
         USING GACB,RGACB              DEFINE DSECT REG
         LR    RLIST,PAR1              LOAD PARAM LIST REG
         L     RGACB,GACBADR           LOAD GACB ADDRESS
         LA    RGACB,ZERO(0,RGACB)     ZERO HIGH ORDER BYTE
         LTR   RGACB,RGACB             REG=0
         BC    7,AT01                  BRANCH IF NO
         WTO   'IFF321I ATTNINQ FOUND NO GACB ADDRESS IN PARAMETER LISTX
               ',ROUTCDE=11,DESC=7                               S21016
         LA    RCODE,X'10'              NO GACB ADDR IN            JFCC
         B     RTNUSCD                  PARAM LIST--RETURN         JFCC
AT01     L     R8,EP2                  LOAD EP2 ADR
         LA    R8,ZERO(0,R8)           ZERO HI ORDER BYTE
         LTR   R8,R8                   EP2=0
         BC    8,AT02                  BRANCH IF YES
         WTO   'IFF322I ATTNINQ FOUND NON-ZERO VALUE IN WORD SIX OF GACX
               B',ROUTCDE=11,DESC=7                              S21016
         LA    RCODE,X'14'              EP2 CONTAINS ERRONEOUS     JFCC
         B     RTNUSCD                  DATA--RETURN               JFCC
AT02     L     REBRG,REBADR            LOAD REB ADR FROM GACB
         LA    REBRG,ZERO(0,REBRG)      ZERO HI ORDER BYTE
         LTR   REBRG,REBRG              ADDRESS EQUAL ZERO
         BC    7,AT00                  BRANCH IF NOT ZERO
         WTO   'IFF323I ATTNINQ FOUND GACB NOT INITIALIZED BY SPAR MACRX
               O',ROUTCDE=11,DESC=7                              S21016
         LA    RCODE,X'18'              GACB NOT SPARED            JFCC
         B     RTNUSCD                  RETURN                     JFCC
*
*                   GET CURRENT TCB ADDRESS
*
AT00     L     R8,CVTADR               LOAD CVT ADR
         L     R8,TCBP(R8)             LOAD OLD/NEW TCB
         L     R8,TCBCUR(R8)           LOAD CURRENT TCB ADR
         LA    R8,ZERO(R8)             CLEAR HI-ORDER BYTE
         L     R9,RTNTCB(REBRG)        LOAD REB TCB ADR
         LA    R9,ZERO(R9)             CLEAR HI-ORDER BYTE
         CR    R8,R9                   DOES CURRENT TCB ADR = REBTCB
         BE    AT14                    BRANCH IF WQUAL
         WTO   'IFF324I ATTNINQ NOT ISSUED IN SAME TASK AS SPAR MACRO FX
               OR GACB',ROUTCDE=11,DESC=7                        S21016
         LA    RCODE,X'1C'              ATTNINQ ISSUED FROM OTHER  JFCC
         B     RTNUSCD                  THAN SPARED TASK--RETURN   JFCC
*
AT14     CLI   MODE,REL                 MODE=R
         BC    7,AT03                  BRANCH IF NO
         L     R8,EP1                  LOAD EP1 ADR FROM GACB
         LA    R8,ZERO(0,R8)           ZERO HI ORDER BYTE
         LTR   R8,R8                   EP1=0
         BC    8,AT04                  BRANCH IF YES
         L     R8,IRB(0,REBRG)         LOAD ROUTINE IRB ADR
         LA    R8,ZERO(0,R8)           ZERO HIGH ORDER BYTE
         L     R9,CVTADR               LOAD CVT TABLE ADR
         L     R9,TCBP(0,R9)           LOAD OLD/NEW TCBS
         L     R9,TCBCUR(0,R9)         LOAD CURRENT TCB ADR
         L     R9,TCBIRB(0,R9)         LOAD CURRENT IRB ADR
         LA    R9,ZERO(0,R9)           ZERO HI ORDER BYTE
         CLR   R8,R9                   IRB ADRS COMPARE
         BC    7,AT04                  BRANCH IF NO
         ST    RTNRG,EP2               STORE IN LINE RETURN ADR OF
*                                      CALLING ROUTINE IN EP2 FIELD
*                                      OF GACB
         ST    SAVRG,REG13             STORE CALLING ROUTINES SAVE
*                                      AREA PTR IN GACB
         SVC   EXIT                    RETURN TO IOS
AT03     CLI   MODE,CLEAR              MODE=CLEAR
         BC    8,ATCLEAR               BRANCH IF YES
         CLI   MODE,COND               MODE=C
         BC    8,ATCOND                BRANCH IF YES
         CLI   MODE,WAIT               MODE=W
         BC    8,AT04                  BRANCH IF YES
         WTO   'IFF325I ATTNINQ FOUND INVALID MODE FIELD IN PARAMETER LX
               IST',ROUTCDE=11,DESC=7                            S21016
         LA    RCODE,X'20'              INVALID MODE IN PARAM      JFCC
         B     RTNUSCD                  LIST--RETURN               JFCC
AT04     TM    ECB,WAITST              ALREADY IN WAIT        LI F40815
         BO    AT04A                   YES, BRANCH            LI F40815
         CLI   ECB,POSTED            ECB ALREADY POSTED?        YA03228
         BNE   AT04A                 YES,DON'T INITIALIZE       YA03228
         MVC   ECB(FOUR),INIT          INITIALIZE ECB         LI F40815
AT04A    LR    PAR1,RGACB              GACB ADDRESS IN REG 1  LI F40815
         LA    PAR0,ZERO               ZERO PAR REG0
         IC    R10,RSVD                INSERT OFFSET
         SLL   R10,24                  POSITION OFFSET
         OR    PAR1,R10                ADD OFF SET TO PARAM
         SVC   DEQUEUE                 CALL DEQUEUE ROUTINE
         CLI   MODE,COND               MODE=C
         BNE   AT06                    BRANCH IF NO              A31230
         CLI   TYPE,ANY                ATTNINQ TYPE=ANY?         A31230
         BC    7,AT05                  BRANCH IF NO TO RESTORE GACB
*                                      MASKS
AT06     LA    R8,ZERO                 LOAD ZERO
         CLR   RCODE,R8                RETURN CODE=0
         BC    8,AT07                  BRANCH IF YES
         LA    R8,FOUR                 LOAD FOUR
         CLR   RCODE,R8                RETURN CODE=4
         BC    8,AT08                  BRANCH IF YES
DEQUEBAD WTO   'IFF326I ATTNINQ UNSUCCESSFUL DUE TO INTERNAL DEQUEUE ERX
               ROR',ROUTCDE=11,DESC=7                       E12 ZA20103
         LA    RCODE,X'24'              RTN CODE FROM DEQUEUE      JFCC
         B     RTNUSCD                  NOT ZERO--RETURN           JFCC
AT07     CLI   MODE,WAIT               MODE=W
         BC    8,AT09                  BRANCH IF YES
         CLI   MODE,REL                MODE=R
         BC    8,AT09                  BRANCH IF YES
RTNUSER  SR    RCODE,RCODE             SET RC=0 - OK             A31230
         ST    RCODE,ECB             CLEAR ECB                  YA03228
RTNUSCD  RETURN (14,12),RC=(15)
AT09     TM    ECB,WAITST              RTN ALREADY IN WAIT STATE
         BC    1,AT10                  BRANCH IF YES
         SRL   R10,24                  POSITION OFFSET
         STC   R10,RESERVED            STORE OFFSET IN GACB
         LA    PAR1,ECB                LOAD ECB ADR
         WAIT  ECB=(1)
         CLI   ECB,ATTNERR             COMP. CODE=X'41'       LI F40815
         BE    AT04A                   YES, RETRY             LI F40815
         B     RTNUSER                 BRANCH
AT10     EQU   *
         WTO   'IFF327I ATTNINQ FOR MODE=R OR W FOUND ATTENTION ROUTINEX
                ALREADY WAITING',ROUTCDE=11,DESC=7               S21016
         LA    RCODE,X'28'              ROUTINE ALREADY IN       JFCC
         B     RTNUSCD                  WAIT STATE--RETURN         JFCC
AT05     ST    RPFK,PFKMSK             RESTORE PF MASK IN GACB
         ST    RSAVE,ATNTYP            RESTORE ATTNTYP MASK IN GACB
         B     AT06                    BRANCH
AT08     CLI   MODE,WAIT               MODE=W
         BC    8,RTNUSER               RETURN IN LINE TO CALLER
         CLI   MODE,REL                MODE=R
         BC    8,RTNUSER               BRANCH IF YES
         L     R8,BRADR                LOAD BRANCH ADR
         LA    R8,ZERO(0,R8)           ZERO HIGH ORDER BYTE
         LTR   R8,R8                   BRANCH ADR=0
         BC    8,AT11                  BRANCH IF YES
         ST    R8,RTN(SAVRG)           REPLACE IN LINE ADR WITH
*                                      BRANCH ADR
         B     RTNUSER                 BRANCH
AT11     EQU   *
         WTO   'IFF328I ATTNINQ FOR MODE=C FOUND NO BRANCH ADDRESS IN PX
               ARAMETER LIST',ROUTCDE=11,DESC=7                  S21016
         LA    RCODE,X'2C'              MODE 'C' BR ADDR          JFCC
         B     RTNUSCD                  EQUAL ZERO--RETURN         JFCC
ATCOND   CLI   TYPE,ANY                ATTNINQ TYPE=ANY
         BC    8,AT04                  BRANCH IF YES
         L     RPFK,PFKMSK             SAVE PFK MASK FROM GACB
         L     RSAVE,ATNTYP            SAVE ATNTYP MASK FROM GACB
         SR    R8,R8                   CLEAR REGISTER
         IC    R8,TYPE                 INSERT TYPE FIELD
         LA    R9,SIX                  LOAD CONSTANT
         CLR   R8,R9                   COMPARE
         BC    2,AT13                  BRANCH IF TYPE GREATER THAN SIX
         LA    R9,ONE                  LOAD CONSTANT
         SR    R8,R9                   SUBTRACT ONE FROM TYPE
         SLL   R9,0(R8)                DETERMINE SEARCH ARGUMENT
         ST    R9,ATNTYP               PUT NEW TYPE IN GACB
         LA    R9,ZERO                 LOAD ZERO
         ST    R9,PFKMSK               PUT NEW TYPE IN GACB
         B     AT04                    BRANCH
AT13     LA    R9,EIGHT                LOAD CONSTANT
         SR    R8,R9                   DETERMINE KEY NUMBER
         L     R9,PFKCODE              LOAD MASK
         SRL   R9,ZERO(R8)             DETERMINE PF KEY ARGUMENT
         ST    R9,PFKMSK               STORE NEW PF MASK IN GACB
         LA    R9,ZERO                 ZERO REG
         ST    R9,ATNTYP               STORE NEW ATNTYP MASK IN GACB
         B     AT04                    BRANCH
ATCLEAR  LR    PAR1,RGACB              GACB ADR TO PARAM REG
         L     R8,RTNIRB(0,REBRG)      LOAD IRB ADR
         LA    R8,RBIQE(0,R8)          LOAD RBIQE ADR
         LR    PAR0,R8                 ADR TO PARAM REG
         SVC   DEQUEUE             CALL DEQUEUE ROUTINE
         LTR   RCODE,RCODE             RETURN CODE = ZERO   E12 ZA20103
         BZ    RTNUSCD             RETURN TO USER ATTN RTN  E12 ZA20103
         B     DEQUEBAD               GIVE MSG,NON-ZERO RC  E12 ZA20103
         DS    0F
PFKCODE  DC    X'80'
         DC    AL3(0)
INIT     DC    X'00FFFFFF'                                    LI F40815
         CNOP  0,8
PARLIST  DSECT
GACBADR  DS    1F
MODE     DS    BL1
RSVD     DS    BL1
TYPE1    DS    BL1
TYPE     DS    BL1
BRADR    DS    1F
GACB     DSECT
COMARA   DS    1F
DCB      DS    1F
PFKMSK   DS    1F
ATNTYP   DS    1F
EP1      DS    1F
EP2      DS    1F
REG13    DS    1F
PFKEY    DS    1F
ATNTYPS  DS    1F
ECB      DS    1F
REBADR   DS    1F
RESERVED DS    1F
         END
