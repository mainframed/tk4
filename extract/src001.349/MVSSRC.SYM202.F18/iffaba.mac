         TITLE 'GRAPHICS ATTENTION HANDLER, FIRST LEVEL'
***********************************************************************
*
* MODULE NAME:           IFFABA (OS/VS2)
*
* DESCRIPTIVE NAME:      GRAPHICS ATTENTION HANDLER
*
* COPYRIGHT:             NONE
*
* STATUS:                RELEASE 2.0
*
* FUNCTION:              FIRST LEVEL ATTENTION HANDLING IS PROVIDED.
*
*                        ATTENTION PROCESSING IS DEPENDENT UPON THE
*                          TYPE OF ATTENTION HANDLING SPECIFIED BY
*                          THE USER IN HIS GRAPHICS DCB:
*
*                          1. FOR EXPRESS (ANALOGOUS TO POLLING):
*                               A BIT INDICATING THE TYPE OF ATTN
*                               IS SET IN THE UCBGCB FIELD.
*
*                          2. FOR BASIC (USER ASYNCH RTN SPECIFIED):
*                               THE SECOND LEVEL ATTN HANDLER, GAR
*                               (IGG019OE), IS SCHEDULED THROUGH THE
*                               ASYNCHRONOUS EXIT EFFECTOR, STAGE II,
*                               FOR DETERMINING THE DISPOSITION OF THE
*                               ATTENTION.
*
******************* PROLOGUE*CONTINUED*NEXT*PAGE **********************
         EJECT
* NOTES:
*
*     DEPENDENCIES:      NORMAL PROGRAM OPERATION AS DESCRIBED BELOW;
*                        CVT POINTER AT STORAGE LOCATION HEX 10.
*
*     RESTRICTIONS:      NONE
*
*     RESISTERS:         SEE EQUATE DEFINITIONS FOLLOWING PROLOG
*
*     PATCH LABEL:       USE COMMON NUCLEUS PATCH AREA, IEAPATCH;
*                        MODID, 24 BYTES, CAN ALSO BE USED.
*
* MODULE TYPE:           EXECUTABLE CODE
*
*    PROCESSOR:          ASSEMBLER XF
*
*    MODULE SIZE:        SEE PAGE 1 OF THIS LISTING
*
*    ATTRIBUTES:         REENTRANT   LOCKED   KEY 0    SUPERVISOR STATE
*
* ENTRY POINT:           IFFIOM
*
*    PURPOSE:            SEE FUNCTION
*
*    LINKAGE:
*                        REGISTER:         CONTENTS:
*                            1     ADDR OF:  IOSB
*                           13               LOCAL LOCK SAVE AREA
*                           14               RETURN TO IOS
*                           15               IFFIOM ENTRY POINT
*
* INPUT:                 SEE LINKAGE TOPIC & DSECTS AT END OF LISTING
*
* OUTPUT:                FOR EXPRESS:   SETS UCBGCB BITS:
*                                             6 IF LP OR ASYNCH ERROR
*                                             7 IF KEYBOARD ATTN
*
*                        FOR BASIC:   IGG019OE SCHEDULED USING
*                                     COMPLEMENTED ADDR OF IQE
         EJECT
*
* EXITS:
*
*    NORMAL:             RETURN TO ADDRESS IN REG 14
*
*                        FOR RESCHEDULING GAH IN USER'S MEMORY:
*                          RETURN TO ADDRESS IN REG 14, PLUS FOUR
*
*    ERROR:              RETURN TO ADDRESS IN REG 14; ATTN DISCARDED
*
*  EXTERNAL REFERENCES:          (BASIC PROCESSING ONLY):
*
*     ROUTINES:         POST STATUS TO RESCHEDULE GAH IN USER'S MEMORY;
*                       AEE, STAGE II, TO SCHEDULE GAR (IGG019OE).
*
*     DATA AREAS:       SEE EQUS FOLLOWING PROLOG
*
*     CONTROL BLOCKS:   SEE USING STATEMENTS FOLLOWING PROLOG
*                       AND DSECTS (IN ALPHABETICAL ORDER)
*
* TABLES:               NONE LOCAL TO MODULE
*
* MACROS:               NONE
*
* CHANGE ACTIVITY:      MODULE HAS BEEN REWRITTEN TO USE THE NEW
*                       IOS INTERFACE WITH IOSB FROM POST STATUS.
*                       CSECT FLAGS UPDATED WHERE APPROPIATE:
         SPACE 3
IFFABA   CSECT
*   SEE LABEL 'MODID' FOR LAST CHANGE DATE; FORMAT:  NAME.SYSREL.DATE
*A083200-083400,C084000                                     D11 YA07646
*C110500-111000  APAR ZA07644 REMOVED BY APAR ----------->  E12 ZA34015
*C101500,106000,116000                                        LF YM5682
*A083500                                                      LF YM4067
*C83500                                                       LG YM5694
* VS2/R2                                                      LF YM0160
* OS/R20                                                        SA32068
* OS/R19                                                        SA28435
         EJECT
*        GENERAL REGISTERS
         SPACE 3
RADR     EQU   1      PARAMETER REGISTER FOR ADDRESSES
RBASE    EQU   2
         SPACE 2
RASCB    EQU   3        SYSTEM CONTROL BLOCK ADDRESSES
RCVT     EQU   4           NAME INDICATES CONTENTS
RIOSB    EQU   5
RIQE     EQU   6            (SEPARATE REGS USED)
RIRB     EQU   7           (TO PRESERVE CONTENTS)
RSRB     EQU   8              (FOR DEBUGGING)
RTCB     EQU   9
RTEB     EQU   10
RUCB     EQU   11
RUCBEXT  EQU   12
         SPACE 2
R13SAVE  EQU   13       SYSTEM CONVENTIONAL REGISTERS
R14BACK  EQU   14          STANDARD BAL REGISTERS
R15EP    EQU   15
         SPACE 3
         USING ASCB,RASCB
         USING CVT,RCVT
         USING IOSB,RIOSB
         USING IQE,RIQE
         USING RBBASIC,RIRB
         USING SRB,RSRB
         USING TCB,RTCB
         USING TEB,RTEB
         USING UCBOB,RUCB
         USING UCBCMEXT,RUCBEXT
         EJECT
*        MISCELLANEOUS EQUATES AND DSECT EQUATES
         SPACE 2
ZERO     EQU   0
TWO      EQU   2
FOUR     EQU   4
         SPACE 5
*     CHANNEL STATUS WORD (CSW) EQUATES
         SPACE 2
CSWATN   EQU   X'80'                   ATTENTION BIT
CSWUC    EQU   X'02'                   UNIT CHECK BIT
         EJECT
*                      INITIALIZATION  SETUPS
*
*          DETERMINE TYPE OF ATTENTION HANDLING SPECIFIED BY USER
*               1.  IF EXPRESS, SET UCBGCB BITS
*               2.  IF BASIC,   SCHEDULE IGG019OE
         SPACE 3
         ENTRY IFFIOM                  VCON NAME FOR IOS ENTRY
         USING *,15
         B     *+24
MODID    DC    C'IFFABA.VS2R2.&SYSDATE  ' MODULE EYECATCHER ID
IFFIOM   STM   0,15,ZERO(R13SAVE)      SAVE REGISTERS
         DROP  15
         BALR  RBASE,ZERO              SET UP NEW BASE REGISTER
         USING *,2                     DEFINE BASE REGISTER
         SPACE 3
*     GET UCB FROM IOSB INPUT AND CHECK ATTENTION HANDLING TYPE
         SPACE
         LR    RIOSB,RADR         KEEP INPUT ADDR
         L     RUCB,IOSUCB        GET UCB ASSOCIATED WITH THIS ATTN
         OC    IOSSNS(FOUR),IOSSNS ANY SENSE HERE?          D11 ZA07646
         BZ    CHKBASIC            NO,DONT MOVE IT          D11 ZA07646
         MVC   UCBSNS(FOUR),IOSSNS  SAVE SENSE DATA       YM5694,YM4067
CHKBASIC EQU   *                                            D11 ZA07646
         TM    UCBGCB,UCBGBAS     CHECK IF BASIC ATTN HANDLING
         BO    GAH0030              IF YES, DO BASIC VALIDITY CHECKS
         EJECT
*                      EXPRESS ATTENTION HANDLING
*
*                1.  DETERMINE ATTENTION STATUS FROM CSW BITS
*                2.  SET UCBGCB,  BIT  6  FOR LITE PEN & ASYNCH ERROR
*                                      7      KEYBOARD (PFK OR ANR)
         SPACE 3
         TM    IOSTATUS,CSWATN     CHECK FOR ATTN STATUS IN CSW
         BNO   GAH0020               IF NOT, DISCARD INTERUPT
         TM    IOSTATUS,CSWUC      SEE IF UNIT CHECK STATUS IS IN CSW
         BNO   GAH0010               IF NO, ATTN IS KEYBOARD TYPE
         OI    UCBGCB,UCBGLPAE       IF YES, ATTN IS LITE PEN OR ASYNCH
         B     GAH0020             RETURN TO IOS
GAH0010  OI    UCBGCB,UCBGKBRD       SET KEYBOARD ATTN BIT
GAH0020  LM    0,15,ZERO(R13SAVE)  RESTORE REGS
         BR    R14BACK             RETURN TO SUPV
         EJECT
*                         BASIC ATTENTION HANDLING
*
*        SECOND LEVEL ATTENTION HANDLER, GAR (IGG019OE), IS SCHEDULED
*        IN THE USER'S MEMORY.  IF GAH IS NOT EXECUTING IN USER MEMORY,
*        RETURN TO IOS FOR RESCHEDULING BY PROVIDING USER ID FROM
*        THE UCB EXTENSION.
*
*        VALIDITY CHECKS FOR BASIC ATTENTION HANDLING INCLUDE
*             1.  DEVICE HAS BEEN OPENED  (UCBGINIT BIT SET)    SA32068
*             2.  TEB EXISTS              (UCBTEB IS NON-ZERO)
*             3.  TEB ID FLAG OK          (TEFLAGS, BIT 0 IS SET)
*             4.  TCB IS NOT ABENDING     (TCBFLGS, BIT 0 IS NOT SET)
*
*        IF ANY CHECK FAILS, THE ATTENTION IS DISCARDED AND
*                            GAH RETURNS TO IOS
         SPACE 3
GAH0030  L     RSRB,IOSSRB        GET ADDR OF:  SRB              YM5682
         L     RASCB,SRBASCB                    ASCB
         L     RUCBEXT,UCBEXTPT                 UCB EXTENSION    YM0160
         LA    RUCBEXT,ZERO(RUCBEXT)              CLEAR HI BYTE  YM0160
         CLC   ASCBASID(TWO),UCBASID   CK IF IN USER MEMORY
         BE    GAH0040                   IF YES, OK TO CONTINUE
         MVC   IOSASID(TWO),UCBASID      IF NO,  GET USER ID
         LM    0,15,ZERO(R13SAVE)                RESTORE REGS
         B     FOUR(R14BACK)                     RESCHEDULE GAH
GAH0040  TM    UCBGCB,UCBGINIT    HAS OPEN INITZ DEVICE          YM5682
         BZ    GAH0020                IF NO, DISCARD ATTN AND RETURN
         L     RTEB,UCBTEB        CHECK IF TEB EXISTS
         LA    RTEB,0(RTEB)           ZERO HI ORDER BYTE
         LTR   RTEB,RTEB
         BZ    GAH0020                IF NONE, DISCARD ATTN
         TM    TEFLGS,TEFLAG      IS TEB ID FLAG OK
         BNO   GAH0020                IF NO, DISCARD ATTN
         L     RTCB,TETCB         GET TCB IN CONTROL OF DEVICE  SA28435
         TM    TCBFLGS,TCBFA      IS ABEND IN PROGRESS?     E12 ZA34015
         BO    GAH0020            YES,DON'T SCHEDULE ATTN   E12 ZA34015
         EJECT
*                    BASIC ATTENTION HANDLING
*
*               1.  GET NEXT AVAILABLE IQE FROM GAR'S IRB
*               2.  INITIALIZE IQE PARM FIELD:
*                      BYTES:      0:  CSW STATUS BITS
*                                1-3:  UCB ADDR
*
         SPACE 3
         L     RIRB,TEGARIRB     GET GAR IRB FROM TEB           YM5682
         L     RIQE,RBNEXAV      CHECK IF IQE AVAIL TO HANDLE ATN
         LA    RIQE,0(RIQE)         ZERO HI ORDER BYTE
         LTR   RIQE,RIQE
         BZ    GAH0020              IF NONE, DISCARD ATTN
         MVC   RBNEXAV(FOUR),IQELNK IF YES, REMOVE IQE FOR SCHEDULING
         ST    RUCB,IQEPARM       INITZ IQE FOR GAR
         MVC   IQEPARM(1),IOSTATUS     PARM IS CSW STATUS+UCB ADDR
         SPACE 3
*    GET AEE, STAGE II, ADDRESS FROM CVT AND SCHEDULE GAR
         SPACE
         L     RCVT,CVTPTR          GET CVT ADDR FROM FIXED LOCATION
         L     R15EP,CVT0EF00       GET AEE-II ENTRY POINT
         LNR   RADR,RIQE            USE COMPLEMENTED ADR TO INDIC IQE
         BALR  R14BACK,R15EP          LINK TO AEE
         B     GAH0020                RETURN TO IOS
         EJECT
***********************************************************************
*
*                        DSECTS
*
***********************************************************************
ASCB     IHAASCB
         EJECT
         CVT   DSECT=YES
         EJECT
         IECDIOSB
         EJECT
IQE         DSECT
IQELNK   DS    F     ADDR OF:              NEXT IQE ON CHAIN
IQEPARM  DS    F     PARAMETER CONTENTS:   CSW STATUS BITS & UCB ADDR
IQEADIRB DS    F     ADDR OF:              CONTROLLING IRB
IQEADTCB DS    F                                       TCB
         EJECT
         IHARB
         EJECT
SRB      IHASRB
         EJECT
         IKJTCB
         EJECT
TEB      DSECT
TESCKIRB DS    F       ADDR OF:     IRB FOR SYSTEM CANCEL KEY RTN
TEREB    DS    F                    GAM ROUTINE ENTRY BLOCK
TETCB    DS    F                    TCB
TESCKQE  DS    F                    LIST OF CANCEL KEY IQES
TEUSECT  DS    F       USE COUNT:   NBR OF UCBS USING THIS TEB
TEFLGS   DS    F       FLAG BYTES:
TEFLAG   EQU   X'80'                TEB ID FLAG
TEGARIRB DS    F       ADDR OF:     GAR'S IRB
TEGEIR   DS    F                    GEIR (IGG019OJ) ENTRY POINT
         EJECT
UCB      DSECT
         IEFUCBOB
**********************************************************************
*                                                                     *
*                    ADDITIONAL UCB EQUATES FOR DSECT                 *
*                                                                     *
***********************************************************************
         SPACE 3
*          UCBTYP, BYTE 4, BIT SETTINGS
         SPACE
UCBT2250 EQU   X'02'      UNIT TYPE ID FOR 2250 DEVICE
UCBT2260 EQU   X'03'                   FOR 2260
UCBT1053 EQU   X'04'                   FOR 1053
         SPACE 2
*          UCBGCB, BIT SETTINGS
         SPACE
UCBGINIT EQU   X'40'      BIT 2: DEVICE INITZ BY OPEN; ATNS OK   A32068
UCBGBAS  EQU   X'08'      BIT 4: DEVICE OPENED FOR BASIC ATTNS
UCBGLPAE EQU   X'02'      BIT 6: LITE PEN OR ASYNCH ERR REC'D
UCBGKBRD EQU   X'01'      BIT 7: KEYBOARD ATTN REC'D
         END
