         TITLE 'GRAPHIC ENTRY INTERFACE ROUTINE'
***********************************************************************
*
* MODULE NAME:           IGG019OJ (OS/VS2)
*
* DESCRIPTIVE NAME:      GRAPHIC ENTRY INTERFACE ROUTINE
*
* COPYRIGHT:             NONE
*
* STATUS:                RELEASE 2.0
*
* FUNCTION:              THIS MODULE PERFORMS FUNCTIONS FOR
*
*FILL IN FROM SPECS******CONTINUED*NEXT*PAGE***************************
         EJECT
IGG019OJ CSECT
*   SEE LABEL 'MODID' FOR LAST CHANGE DATE; FORMAT:  NAME.SYSREL.DATE
* SEQ NBRS MUST BE RESPECIFIED BECAUSE OF RESEQ FOR VS
*STATUS:CHANGE LEVEL 0
*FUNCTION:THIRD LEVEL GRAPHIC CONTROL PROGRAM THAT GIVES CONTROL TO
*         USER SPECIFIED ATTENTION ROUTINE AND PLACES ATTENTION DATA
*         IN USERS COMAREA.
*         1.DETERMINE IF USER ROUTINE DESIRES THE ATTENTION TYPE BEING
*         SERVICES.IF NOT,THE DATA IS DISCARDED.
*         2.DETERMINE WHERE TO GIVE USER CONTROL IE:VIA EP2 OR EP IN
*         GACB.
*         3.PLACE DATA IN COMAREA.
*ENTRY POINT:SCHEDULED BY SECOND LEVEL GRAPHIC CONTROL PROGRAM VIA AEE
*         STAGE 2.
*INPUT:1.GR#0=IQE ADDRESS.
*      2.GR#1=GACB ADDRESS.
*      3.GR'S 13,14,15 AS PER CONVENTIONS.
*OUTPUT:DATA TO USER'S COMAREA.
*EXTERNAL ROUTINES:1.USER ATTENTION ROUTINE.
*                  2.SVC 3 (EXIT SVC)
*EXITS:1.TO USER VIA GR#15 ON ACCEPTED ATTENTIONS.
*      2.TO IOS VIA SVC 3 ON USER REJECTED ATTENTIONS.
*TABLES/WORKAREAS:1.USER COMAREA
*                 2.USER GACB
*ATTRIBUTES:1.RESIDENT WITH GRAPHIC JOB(LOADED AT OPEN TIME).
*           2.RE-ENTRANT
*NOTES:N/A
         EJECT
*
*        REGISTER DEFINITION
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
RBASE    EQU   3
RPFK     EQU   4
RTYP     EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
RIQE     EQU   11
RGACB    EQU   12
RSAVE    EQU   13
RTNRG    EQU   14
ENTRG    EQU   15
*
*        GACB  TYPE VALUES
*
ENDAN    EQU   X'01'                   END KEY OR A/N ATTN
PFKB     EQU   X'02'
*
*
*
ONE      EQU   1                       ONE
TWO      EQU   2                       TWO
ZERO     EQU   0                       ZERO
         EJECT
         SAVE  (14,12)
         BALR  RBASE,R0                SET UP BASE REG
         USING *,RBASE                 DEFINE BASE REG
         B     *+24
MODID    DC    C'IGG019OJ.VS2R2.73270'          MODULE EYECATCHER ID
         LR    RGACB,R1                SAVE PARAM
         LR    RIQE,R0                 SAVE PARM
         USING IQEFLDS,RIQE
         USING GACBFLDS,RGACB
         LA    RPFK,ZERO               ZERO REG
         L     RTYP,SEARCH             LOAD ATNTYP SEARCH MASK
         CLI   TYPE,ENDAN              END KEY
         BC    8,GEIR01                BRANCH IF YES
         CLI   TYPE,PFKB               PF KEY ATTN
         BC    8,GEIR02                BRANCH IF YES
         SR    R2,R2                   CLEAR REG
         IC    R2,TYPE                 INSERT TYPE FIELD
         LA    R6,TWO                  LOAD CONSTANT
         SR    R2,R6                   DETERMINE SHIFT COUNT
         SLL   RTYP,ZERO(R2)           DETERMINE SEARCH ARGUMENT
         B     GEIR01                  BRANCH TO SEARCH
GEIR02   LA    RTYP,ZERO               ZERO REG
         SR    R6,R6                   ZERO REG
         IC    R6,KEY                  INSERT KEY NO.
         L     RPFK,PFKSRCH            LOAD PFK SEARCH ARGUMENT
         SRL   RPFK,ZERO(R6)           DETERMINE PF SEARCH ARGUMENT
GEIR01   L     R6,PFKMSK               LOAD ARGUMENT
         L     R7,ATNTYP               LOAD ARGUMENT
         NR    R6,RPFK                 SEE IF MATCH
         NR    R7,RTYP                 SEE IF MATCH
         LTR   R6,R6                   WAS THERE A MATCH
         BC    7,GEIR07                BRANCH IF YES
         LTR   R7,R7                   WAS THERE AMATCH
         BC    7,GEIR07                BRANCH IF YES
GEIROUT  SVC   3                       RETURN TO SUPV
         EJECT
GEIR07   L     R7,EP2                        LOAD ADR
         LTR   R7,R7                   EP2=0
         BC    7,GEIR08                BRANCH IF NO
         L     R7,EP1                  EP1=0
         LTR   R7,R7                   EP1=0
         BC    8,GEIROUT               BRANCH IF YES
         LR    ENTRG,R7                ENTRY ADR TO REG
         L     R10,COMARA              LOAD COMAREA ADR
         MVC   ZERO(12,R10),RSVD       DATA TO COMAREA
         LM    0,12,20(13)             RESTORE REGS
         BR    ENTRG                   TO USER ATTENTION ROUTINE
GEIR08   LR    ENTRG,R7                ADR TO ENTRY REG
         LA    R7,ZERO                 ZERO REG
         ST    R7,EP2                  ZERO EP2
         L     R10,COMARA              LOAD COMAREA ADR
         MVC   ZERO(12,R10),RSVD       MOVE DATA TO COMAREA
         L     RSAVE,REG13             LOAD REG 13
         LM    0,12,20(13)             RESTORE REGS
         BR    ENTRG                   TO USER ATTENTION ROUTINE
         EJECT
         DS    0F
PFKSRCH  DC    X'80'
         DC    AL3(0)
SEARCH   DC    AL3(0)
         DC    X'01'
         SPACE 3
IQEFLDS  DSECT
IQELNK   DS    1F
IQEPARM  DS    1F
IQEIRB   DS    1F
IQETCB   DS    1F
RSVD     DS    BL1
OV       DS    BL1
KEY      DS    BL1
TYPE     DS    BL1
SENSEMI  DS    1F
         SPACE 3
GACBFLDS DSECT
COMARA   DS    1F
DCB      DS    1F
PFKMSK   DS    1F
ATNTYP   DS    1F
EP1      DS    1F
EP2      DS    1F
REG13    DS    1F
PFKEY    DS    1F
ATNTYPS  DS    1F
ECB      DS    1F
Q2       DS    1F
RESERVED DS    1F
         END
