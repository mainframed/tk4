*TITLE 'GRAPHIC ATTENTION ANALYSIS ROUTINE'
*STATUS:CHANGE LEVEL 0
*FUNCTION/OPERATION:THIS ROUTINE DETERMINES IF AN ATTENTION HAS OCCURED
*   BY POLLING BITS 6 AND 7 IN THE INDIVIDUAL GRAPHIC CONTROL BYTES
*   (GCB).THIS ROUTINE DETERMINES THE TYPE OF ATTENTION THAT OCCURED
*   BY USE OF GCB BITS 6 AND 7 AND THE SENSE BYTES IN THE UNIT CONTROL
*   BLOCK(UCB).A UNIQUE RETURN CODE FOR EACH TYPE OF ATTENTION IE.LITE
*   PEN,KEYBOARD,END ORDER SEQUENCE OR ASYNCH.ERROR IS PUT IN GR#15.
*   CODES ARE PLACED IN GR#15 FOR PARAMETERS MISSING,WORD 5 IN TABLE
*   OF ADDRESSES MISSING OR NO ATTENTIONS OCCURED.WHEN RETURN IS
*   MADE TO THE CALLING PROGRAM THE REST OF THE CODING GENERATED BY
*   THE ANALYZ MACRO DECODES GR#15 AND EITHER PASSES CONTROL TO A USER
*   ATTENTION ROUTINE OR GENERATES A RETURN CODE TO PLACE IN GR#15
*   AS PER THE SRL.REGISTERS 1 THRU 12 ARE SAVED ON ENTRY AND RESTORED
*   UPON EXIT.SAVE AREA POINTED TO BY GR#13 AS PER CONVENTIONS.
*ENTRY POINTS:IFFANA ALIAS ANLZ.POLL FOR AN ATTENTION.
*       L    15,ADDR
*       BALR 14,15
*               X
*               X
*   ADR DC   V(ANLZ)
*               X
*               X
*               X
*INPUT:GR#1 CONTAINS ADR OF PARAMETER LIST WHICH IS CONSTRUCTED AS
*   FOLLOWS:
*       ******************
*   LIST* DCBLST ADDRESS *(0)
*       ******************
*       * POINTR ADDRESS *(4)
*       ******************
*       * RESERVED       *(8)
*       ******************
*OUTPUT:GR#0 CONTAINS ADDRESS OF OUTPUT AREA(IN CASE OF MULTIPLE OUTPUT
*   AREAS DEFINED BY MULTIPLE ANALYZ MACROS IN A SINGLE ASSEMBLY).
*   OUTPUT AREA IS AS FOLLOWS:
*       ****************
*   OUT * SENSE INFO   * (0)
*       ****************
*       * IX * DCB ADR * (4)
*       ****************
*         IX=VALID USE ONLY FOR MULTIPLE 2260S ASSIGNED TO A SINGLE
*   DATA CONTROL BLOCK(DCB).GIVES THE NUMBER OF THE UCB ADR EXTENT TO
*   THE DATA EXTENT BLOCK IE 0,1,2 ETC.
*   SENSE INFO VALID ONLY FOR LITE PEN,END ORDER SEQUENCE AND ASYNCH.
*   ERROR ATTENTIONS.
*   GR#15 CONTAINS RETURN CODES AS DESCRIBED IN EXITS NORMAL AND ERROR.
*   RETURN CODES ARE FOR DECODING BY ANALYZ MACRO EXPANSION.
*EXTERNAL ROUTINES:N/A
*EXITS NORMAL:IN ALL CASES CONTROL IS RETURNED TO THE ANALYZ MACRO
*   EXPANSION CODING IN THE CALLING PROG.WITH A RETURN CODE IN GR#15.
*   THE RETURN CODES ARE AS FOLLOWS:
*   LITE PEN=00               X
*   KEYBOARD(AN OR PF)=04     X
*   ASYNCHRONOUS ERROR=08     XXCODES ARE IN HEX
*   END ORDER SEQUENCE=0C     X
*   NO ATTENTIONS PRESENT=10  X
*   LA  15,RETURN CODE
*   BR  14
*EXITS-ERROR:IN ALL CASES CONTROL IS RETURNED TO THE ANALYZ MACRO
*   EXPANSION CODING IN THE CALLING PROG.WITH A RETURN CODE IN GR#15.
*   THE RETURN CODES ARE AS FOLLOWS:
*   PARAMETER MISSING=18                      XCODES ARE IN HEX
*   WORD 5 IN ADDRESS TABLE(POINTR)MISSING=20 X
*   LA  15,RETURN CODE
*   BR  14
*TABLES/WORK AREAS:A TABLE OF USER ATTENTION ROUTINE ADDRS. AS POINTED
*   TO BY THE POINTR PARAMETER OF THE ANALYZ MACRO.IT IS CONSTRUCTED AS
*   FOLLOWS.
*   **************************
*   * LITE PEN ADR           *(0)
*   **************************
*   * KEYBOARD ADR           *(4)
*   **************************
*   * ASYNCHRONOUS ERROR ADR *(8)
*   **************************
*   * END ORDER SEQUENCE ADR *(12)
*   **************************
*  * OUTPUT AREA ADR         *(16)  OUTPUT AREA CONSTRUCTED AS SHOWN
*   **************************      IN OUTPUT HEADING ABOVE.
*ATTRIBUTES:RE-ENTRANT
*NOTES:THE RETURN CODES AS GENERATED BY THIS ROUTINE ARE FOR INTERNAL
*   USE ONLY AND ARE NOT THE RETURN CODES LOGICALLY PRESENTED TO THE
*   CALLING PROBLEM PROGRAM.THE RETURN CODES LOGICALLY PRESENTED  TO
*   THE PROBLEM PROGRAM ARE AS FOLLOWS:
*   NORMAL RETURN=00                                  X
*   NO ATTENTIONS=04                                  X
*   ATTENTION OCCURED BUT NO USER ROUTINE PROVIDED 08 XCODES ARE IN HEX
*   PARAMETER MISSING=0C                              X
*   WORD 5 IN ADDRESS TABLE MISSING=10                X
*   IT IS THE RESPONSIBILITY OF THE USER ATTENTION ROUTINES TO SAVE AND
*   RESTORE REGISTERS ONCE CONTROL IS GIVEN TO THEM.
*
*        REGISTER DEFINITION
REG0     EQU   0                       PARAM REGISTER
LISTRG   EQU   1                        POINTER TO PARAMETER LIST
BASERG   EQU   2                        BASE REGISTER
TABREG   EQU   3                        POINTER TO TABLE
DCBREG   EQU   4                        POINTER TO DCB LIST
INDXRG   EQU   5                        INDEX REGISTER
GBREG1   EQU   6                        WORK REG
GBREG2   EQU   7                        WORK REG
GBREG3   EQU   8                        WORK REG
GBREG4   EQU   9                        WORK REG
GBREG5   EQU   10                       WORK REG
GBREG6   EQU   11                       WORK REG
GBREG7   EQU   12                       WORK REG
SAVEREG  EQU   13                      SAVE AREA REGISTER
RETREG   EQU   14                       RETURN REGISTER
BRNCHREG EQU   15                      BRANCH TO ADDRESS REGISTER
ERCODE   EQU   15                      RETURN CODE REGISTER
*
UCBADR   EQU   32                       CONSTANT TO ACCESS START OF
*                                       UCB LIST IN DEB
DEBADR   EQU   44                      CONSTANT TO ACCESS DEB ADDRESS
SENSE    EQU   32                       SENSE DATA            LF YM4067
GCBADR   EQU   27                       DISPLACEMENT CONSTANT TO GIVE
*                                       GCB BYTE AT END OF UCB
GASR     EQU   70                      GSERV SVC ID
*
IFFANA   CSECT
*C023200                                                      LF YM4067
* 040600,041400,042200,025800                                    S21016
         ENTRY ANLZ
ANLZ     STM   LISTRG,GBREG7,24(SAVEREG) SAVE REGISTERS CONTENTS
         BALR  BASERG,0                 SET UP BASE REGISTER
         USING *,BASERG                 DEFINE BASE REGISTER
RECYCLE  CLC   1(3,LISTRG),ZERO        TEST FOR ZERO IN DCBLST PARAM
         BC    8,EXIT1                  BRANCH IF ZERO
         CLC   5(3,LISTRG),ZERO        TEST FOR ZERO IN POINTR PARAM
         BE    EXIT6                   BRANCH IF ZERO            S21016
         L     TABREG,4(0,LISTRG)       LOAD POINTER PARAMETER
         L     DCBREG,0(0,LISTRG)       LOAD DCB LIST POINTER
         LA    DCBREG,0(0,DCBREG)      ZERO HIGH ORDER BYTE
         CLC   9(3,LISTRG),ZERO        IS RESUME PTR ZERO
         BC    8,PTR0                   BRANCH IF YES
         CLC   9(3,LISTRG),1(DCBREG)   IS RESUME PTR GREATER THAN
*                                       LAST DCBLST ENTRY ADDRESS
         BC    2,PTR0                   BRANCH IF RESUME PTR GREATER
         L     DCBREG,8(0,LISTRG)       LOAD RESUME PTR
         LA    DCBREG,0(0,DCBREG)      ZERO HIGH ORDER BYTE
         BC    15,IDCHEK                BRANCH
PTR0     LA    DCBREG,4(0,DCBREG)      INCREMENT ADDRESS
         ST    DCBREG,8(0,LISTRG)       STORE ADDRESS IN RESUME PTR
IDCHEK   CLC   17(3,TABREG),ZERO       TEST FOR ZERO IN TABLE WORD 5
         BC    8,EXIT3                  BRANCH IF ZERO
BACK     L     GBREG1,0(DCBREG)         LOAD DCB ADDRESS
         L     GBREG1,DEBADR(GBREG1)    LOAD DEB ADDRESS
         L     INDXRG,0(DCBREG)         LOAD INDEX FACTOR FOR UCB ADR
*                                       LOCATED IN DEB
         SRL   INDXRG,24                LOCATE INDEX FOR MULTIPLICATION
         LA    GBREG3,0                 ZERO EVEN REGISTER
         LA    GBREG4,4                LOAD MULTIPLICAND
         MR    GBREG3,INDXRG            DETERMINE DEB DISPLACEMENT
*                                       FACTOR FOR PROPER UCB ADR.
*                                       PRODUCT IS IN GBREG3 AND GBREG4
         L     GBREG2,UCBADR(GBREG4,GBREG1)  LOAD UCB ADDRESS
         TM    GCBADR(GBREG2),X'03'     TEST FOR POSTED GCB
         BC    5,POSTED                 BRANCH IF EITHER OR BOTH
*                                       BITS POSTED.
         L     GBREG3,0(0,LISTRG)       LOAD DCB LIST PTR
         L     GBREG3,0(0,GBREG3)      LOAD END OF LIST POINTER
         LA    GBREG3,0(0,GBREG3)      ZERO HIGH ORDER BYTE
         CLR   GBREG3,DCBREG           COMPARE PRESENR DCBLST POINTER
*                                      WITH LAST DCBLST ADDRESS
         BC    8,LAST1                  BRANCH IF PRESENT EQUAL TO
*                                       LAST IN LIST
         LA    DCBREG,4(0,DCBREG)      INCREMENT POINTER
COMPAR   L     GBREG6,8(0,LISTRG)      LOAD RESUME PTR
         LA    GBREG6,0(0,GBREG6)      ZERO HIGH ORDER BYTE
         CLR   DCBREG,GBREG6           COMPARE TO SEE IF COMPLETE
*                                      LIST WAS POLLED
         BC    8,EXIT4                  NO GCBS POSTED
         BC    15,BACK
LAST1    L     DCBREG,0(0,LISTRG)       LOAD DCB LIST POINTER
         LA    DCBREG,4(0,DCBREG)      INCREMENT POINTER
         BC    15,COMPAR                BRANCH
POSTED   LA    GBREG5,4(0,DCBREG)      BUMP PTR TO SERVICE NEXT DCB
*                                      UPON RE-ENTRY
         ST    GBREG5,8(0,LISTRG)      STORE ADR OF NEXT DCBLST ENTRY
*                                      TO SERVICE IN RESUME PTR
         L     GBREG5,16(0,TABREG)     LOAD OUTPUT AREA ADDRESS
         MVC   0(4,GBREG5),SENSE(GBREG2)  MOVE SENSE DATA TO OUTPUT
         L     GBREG3,0(0,DCBREG)       LOAD DCB ADDRESS
         ST    GBREG3,4(0,GBREG5)      STORE DCB ADDRESS IN OUTPUT
         TM    GCBADR(GBREG2),X'01'     TEST FOR KEYBOARD ATTENTION
         BC    1,TEST0                  BRANCH IF KYBD.ATTN.
         LA    LISTRG,2                LOAD BIT 6 RESET MASK
         SLL   LISTRG,24               POSITION BIT MASK
         AR    LISTRG,DCBREG           ADD DCBLST ADDRESS TO PARAMETER
         SVC   GASR                    RESET BIT 6 IN GCB
         LR    REG0,GBREG5             OUTPUT AREA ADDRESS TO REG 0
         BC    15,NOTKEY
TEST0    LA    LISTRG,1                LOAD BIT 7 RESET MASK
         SLL   LISTRG,24               POSITION BIT MASK
         AR    LISTRG,DCBREG           ADD DCBLST ADDRESS TO PARAMETER
         SVC   GASR                    RESET BIT 7 IN GCB
         LR    REG0,GBREG5             OUTPUT AREA ADDRESS TO REG 0
         BC    15,EXIT2                BRANCH
NOTKEY   TM    1(GBREG5),X'40'         TEST FOR END OF ORDERS SEQUENCE
         BC    1,EOS                    BRANCH IF SET
         TM    1(GBREG5),X'80'         TEST FOR LITE PEN ATTENTION
         BC    1,LPA                    BRANCH IF SET
         BC    15,EXIT5                BRANCH
EXIT1    EQU   *                                                 S21016
         WTO   'IFF401I ANALYZ FOUND NO POLST ADDRESS IN PARAMETER LISTX
               ',ROUTCDE=11,DESC=7                               S21016
         LA    ERCODE,24               POLST PARAM MISSING
         B     LPA+4                   BRANCH
EXIT2    LA    ERCODE,4                KYBD
         B     LPA+4                   BRANCH
EXIT3    EQU   *                                                 S21016
         WTO   'IFF403I ANALYZ FOUND NO OUTPUT AREA ADDRESS IN ATTENTIOX
               N ROUTINE LIST',ROUTCDE=11,DESC=7                 S21016
         LA    ERCODE,32               OUTPUT AREA ADDR MISSING  S21016
         B     LPA+4                   BRANCH
EXIT4    LA    ERCODE,16               NONE POSTED
         B     LPA+4                   BRANCH
EXIT5    EQU   *                                                 S21016
         WTO   'IFF404I ANALYZ DETERMINED AN ASYNCHRONOUS ERROR ATTENTIX
               ON OCCURRED',ROUTCDE=11,DESC=7                    S21016
         LA    ERCODE,8
         B     LPA+4                   BRANCH
EXIT6    EQU   *                                                 S21016
         WTO   'IFF402I ANALYZ FOUND NO ATTENTION ROUTINE LIST',ROUTCDEX
               =11,DESC=7                                        S21016
         LA    ERCODE,24                                         S21016
         B     LPA+4                   GO RETURN                 S21016
EOS      LA    ERCODE,12               EOS
         B     LPA+4                   BRANCH
LPA      LA    ERCODE,0                LP
         LM    LISTRG,GBREG7,24(SAVEREG) RESTORE REGISTERS
         BCR   15,14                   RETURN
         DS    0F
ZERO     DC    4XL1'00'                 ZERO
         CNOP  0,8
         END
