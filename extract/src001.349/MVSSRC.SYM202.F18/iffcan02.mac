         TITLE ' GRAPHICS CANCEL KEY DUMP FORMATTING ROUTINE '
***********************************************************************
*
* MODULE NAME:           IFFCAN02 (OS/VS2)
*
* DESCRIPTIVE NAME:      GRAPHICS CANCEL KEY DUMP FORMATTING ROUTINE
*
* COPYRIGHT:             NONE
*
* STATUS:                RELEASE 2.0
*
* FUNCTION:              FORMAT INTO AN ABDUMP TYPE FORMAT AND PLACE
*                        ON THE SPECIFIED SEQUENTIAL DEVICE OR DATA
*                        SET THE 2250 BUFFER SECTIONS, EACH 256
*                        BYTES IN SIZE, THAT HAVE BEEN ASSIGNED TO
*                        THE JOBSTEP REQUESTING THE DUMP.
*
******************* PROLOGUE*CONTINUED*NEXT*PAGE **********************
         EJECT
***********************************************************************
*
* NOTES:
*    DEPENDENCIES:       NONE
*
*     RESTRICTION:       SYSBFDMP CARD MUST SPECIFY A SEQUENTIAL DEVICE
*
*     RESISTERS:         SEE EQUATE DEFINITIONS FOLLOWING PROLOG
*
*     PATCH LABEL:       PATCH, A 50 BYTE DC AREA AT END OF MODULE
*
* MODULE TYPE:           EXECUTABLE CODE
*
*    PROCESSOR:          ASSEMBLER XF
*
*    MODULE SIZE:        SEE PAGE 1 OF THIS LISTING
*
*    ATTRIBUTES:
*       AT ENTRY:        REENTRANT  ENABLED  SUPERVISOR STATE IN KEY 0
*
* ENTRY POINT:           IFFCAN02
*
*    PURPOSE:            SEE 'FUNCTION' HEADING ABOVE
*
*    LINKAGE:            SEE 'INPUT', NEXT HEADING
*
* INPUT:                 REGISTER 1 POINTS TO CKQE FOR DEVICE
*                        REQUESTING THE DUMP
*
* OUTPUT:                SEE 'FUNCTION' HEADING ABOVE
*
* EXITS:
*
*    NORMAL:             RETURN, USING ADDRESS IN REGISTER 14
*
*    ERROR:              RETURN, USING ADDRESS IN REGISTER 14
*                          RETURN CODE 4 IN REGISTER 15
*
******************* PROLOGUE*CONTINUED*NEXT*PAGE **********************
         EJECT
***********************************************************************
*
* EXTERNAL REFERENCES:
*
*     ROUTINES:          IGG019OA TO READ IN THE 2250 BUFFER
*                        READ/WRITE (BSAM) TO WRITE BUFFER CONTENTS
*
*     DATA AREAS:        SEE DSECT SECTION AT END OF LISTING
*
*     CONTROL BLOCKS:    SEE DSECT SECTION AT END OF LISTING
*
* TABLES:                CKCB     CKQE     BUFFER TABLE
*                        OPCB     TIOT
*
* MACROS:                CHECK         OPEN           SYNADRLS
*                        CLOSE         RETURN         WTO
*                        GETMAIN       SAVE
*                        GREAD         SYNADAF
*
* CHANGE ACTIVITY:       1. BLOCKING FACTOR CHANGED TO A SINGLE LINE
*                        2. DCB IS OPENED AND CLOSED EVERY ENTRY
*
*                        FIXES DROPPED LIST:     GJP UNSUPPORTED IN VS
*
***********************************************************************
         SPACE 3
IFFCAN02 CSECT
*   SEE LABEL 'MODID' FOR LAST CHANGE DATE; FORMAT:  NAME.SYSREL.DATE
* 507000,520500-522000,536500-539000                             A33603
*D631000-634500                                                  A39421
*A616100,A616200                                                 A39421
*C119500,D120000                                                 X01007
*D64500,D79000,D129000,D134500,D318000,D323000                LI A39419
*A64200-64400,A76200,A78900,A128700-129100,A134200-134800     LI A39419
*A317800-318100,A322800-323100,A713050-713450                 LI A39419
*C666000                                                      LI A39419
*C519500,520000                                              LD YA00801
*A315600-315997,A624100,C665500                               LG YM5686
*D231500,A231600-231700,D263000,A263100-263200               LG Z30AALG
*D278500,A278600-278700,D303000,A303100-303200               LG Z30AALG
*D433500,A433600-433620                                      LG Z30AALG
*A297700-297996,A658100-658420                               LG @ZM2360
*C390500-392500                                             D11 ZA14043
*
*
         EJECT
***********************************************************************
*                                                                     *
*                        EQUATES                                      *
*                                                                     *
***********************************************************************
WORKR0   EQU   0                        WORKING REGISTER
WORKR1   EQU   1                        WORKING REGISTER
WORKR2   EQU   2                        WORKING REGISTER
BUFTABR  EQU   2                        BUFFER TABLE
WORDCTR  EQU   2                        WORD COUNT REGISTER
WORKR3   EQU   3                        WORKING REGISTER
BUFINDXR EQU   3                        DISPLACEMENT INTO BUFFER TABLE
INTRTNR  EQU   4                        INTERNAL RETURN
DCBR     EQU   5                        DATA CONTROL BLOCK
BUFFSECR EQU   7                        ADDRESS OF 256 BUFFER SECTION
UCBR     EQU   5                        UNIT CONTROL BLOCK
HEXR     EQU   5                        HEXADECIMAL DATA
BUFSTR   EQU   6                        ADDRESS OF BUFFER RESTART
*                                            GENERATION ADDRESS
MOVELNGR EQU   6                        LENGTH OF INTERPERT AREA
INDEXR   EQU   6                        INDEX VALUE
TIOTR    EQU   6                        TASK INPUT/OUTPUT TABLE
HALFLNR  EQU   7                        HALF LINE COUNTER
CKCBR    EQU   7                        CANCEL KEY CONTROL BLOCK
TCBR     EQU   7                        TASK CONTROL BLOCK
DEBR     EQU   8                        DATA EVENT BLOCK ADDRESS
DECBR    EQU   8                        DATA EVENT CONTROL BLOCK
EBCDICR  EQU   8                        EBCDIC DATA
CVTR     EQU   8                        COMMUNICATION VECTOR TABLE
LINER    EQU   9                        CURRENT OUTPUT LINE IN USE
TIOTDPLR EQU   9                        TASK I/O TABLE DISPLACEMENT
OPCBR    EQU   10                       OUTPUT CONTROL BLOCK
CKQER    EQU   11                       OUTPUT CONTROL BLOCK ADDRESS
BASER    EQU   12                       CANCEL KEY QUEUE ELEMENT ADD.
SAVEPTR  EQU   13                       BASE REGISTER
RETURNR  EQU   14                       RETURN REGISTER EXTERNAL
WORKRE   EQU   14                       WORKING REGISTER
ENTRYR   EQU   15                       ENTRY POINT ADDRESS
RTNCODER EQU   15                       RETURN CODE
WORKRF   EQU   15                       WORKING REGISTER
DEBR2    EQU   15                       DEB OTHER THAN SYSFBDMP DEB
SUMR     EQU   15                       ACCUMULATOR
XDUMY    EQU   X'00'                   IMMED. COMPARAND       LB  AOS/1
         EJECT
***********************************************************************
*                                                                     *
*                        ENTRY INTERFACE                              *
*                                                                     *
***********************************************************************
         SPACE 2
         USING *,ENTRYR
         B     *+24                     BRANCH AROUND ID
MODID    DC    C'IFFCAN02.VS2R2.74324'
         SAVE  (14,12)                  SAVE REGISTERS
         DROP  ENTRYR                   ESTABLISH
         BALR  BASER,0
         USING *,BASER                       ADDRESSABILITY
         LR    CKQER,WORKR1             ESTABLISH ADDRESSABILITY
         USING CKQE,CKQER               FOR CKQE
         USING CKCB,CKCBR               ESTABLISH ADDRESSABILITY FOR
*                                            CANCEL KEY CONTROL BLOCK
         L     CKCBR,CKQECKCB           LOAD POINTER TO CKCB
*                             TRY TO GET CORE FOR OPCB
         LA    CKCBR,ZERO(CKCBR)        CLEAR HIGH ORDER BYTE
         LA    WORKR1,CKCBWORK         PT TO LIST IN CKCB     LI A39419
         GETMAIN EC,LV=OPCBLEN,A=(CKCBR),SP=252,MF=(E,(1))    LI A39419
         SR    WORKR2,WORKR2           SET INDEX TO WTO TABLE
         LTR   RTNCODER,RTNCODER       WAS GETMAIN SUCCESSFUL
         BNZ   COMMON                  NO-ISSUE ERROR MESSAGE
*                                      IF GETMAIN FAILS RETURN CODE
*                                         IN REGISTER 15 IS ALREADY
*                                         NOT ZERO SO WILL NOT HAVE
*                                         TO SET IT FOR ABNORMAL
*                                         RETURN INDICATOR
         OI    CKQEFLG2,CKFOPCB         INDICATE THAT
*                                            GETTMAIN WAS ISSUED
*                                            SUCCESSFULLY
         L     OPCBR,CKCBOPCB           ESTABLISH ADDRESSABILITY
         USING OPCB,OPCBR                    FOR OPCB
         USING SAVEAREA,SAVEPTR
         LA    WORKR1,OPCBSVAR          GET POINTER TO MY SAVE AREA
         ST    WORKR1,LSA               DOING THE BACKWORD
         DROP  SAVEPTR                         AND
         USING SAVEAREA,WORKR1                     THE
         ST    SAVEPTR,HSA              FOREWORD SAVE AREA CHAINING
         LR    SAVEPTR,WORKR1           SET POINTER TO MY SAVE AREA
         DROP  WORKR1
         USING SAVEAREA,SAVEPTR
         SPACE 2
*                                       GET INPUT BUFFER
         LA    WORKR1,CKCBWORK         PT TO LIST IN CKCB     LI A39419
         SPACE 2
         LA    BUFFSECR,OPCBINBF        GET ADDRESS OF WHERE INPUT
*                                            BUFFER ADDRESS IS TO GO
         SPACE 2
*                                       SEE IF CORE AVAILABLE
         GETMAIN EC,LV=INBUFLN,A=(BUFFSECR),SP=252,MF=(E,(1)) LI A39419
         SR    WORKR2,WORKR2           SET INDEX TO WTO TABLE
         LTR   RTNCODER,RTNCODER       WAS GETMAIN SUCCESSFUL
         BNZ   COMMON                  NO-ISSUE ERROR MESSAGE
*                                      IF GETMAIN FAILS RETURN CODE
*                                         IN REGISTER 15 IS ALREADY
*                                         NOT ZERO SO WILL NOT HAVE
*                                         TO SET IT FOR ABNORMAL
*                                         RETURN INDICATOR
         OI    CKQEFLG2,CKFINBUF        INDICATE THAT INPUT BUFFER
*                                            GOTTEN
         EJECT
***********************************************************************
*                                                                     *
*                             MAIN BODY                               *
*                                                                     *
***********************************************************************
         SPACE 2
*                   LOCATE CURRENT TCB BY WAY OF CVT
         SPACE 2
         USING CVT,CVTR                 ESTABLISH ADDRESSABILITY FOR
*                                          COMMUNICATION VECTOR TABLE
         USING TCB,TCBR                 ESTABLISH ADDRESSABILITY FOR
*                                            TASK CONTROL BLOCK
         LA    CVTR,SYSCVTPT            GET LOCATION OF CVT POINTER
         L     CVTR,ZERO(CVTR)          GET CVT ADDRESS
         L     TCBR,CVTTCBP             GET NEW/CURRENT WORDS ADDRESS
         L     TCBR,CURRTCB(TCBR)       GET CURRENT TCB POINTER
         SPACE 2
*                   IS SYSTEM MVT IF YES USE JOBSTEP'S TCB
         SPACE 2
         TM    CVTDCB,MVTSYS            IS THIS A MVT SYSTEM
         BNO   GETTIOT                  IF NOT MVT TAKE BRANCH
         L     TCBR,TCBJSTCB            GET JOB STEP TCB POINTER
GETTIOT  L     TIOTR,TCBTIO             GET TASK TIOT ADDRESS
         USING TIOT,TIOTR               ESTABLISH ADDRESSABILITY FOR
*                                            TASK I/O TABLE
         USING TIOTDDEN,WORKR1          ESTABLISH ADDRESSABILITY
*                                            FOR TASK I/O TABLE
*                                            DD ENTRY
         LA    TIOTDPLR,TIOTHEAD             BYPASS TIOT IDENTIFIERS
         B     FIRSTENT                 FOR FIRST DD ENTRY SPECIAL
*                                            ENTRY TO LOOP
         SPACE 2
*                   LOOP TO SEARCH TIOT FOR DDENTRY 'SYSBFDMP'
         SPACE 2
TIOTLOOP IC    WORKR1,TIOELNGH          GET LENGTH OF THIS DD ENTRY
         SLL   WORKR1,X24BITS           ZERO OUT OTHER
         SRL   WORKR1,X24BITS                THREE BYTES OF REGISTER
         AR    TIOTDPLR,WORKR1          UP TIOT DISPLACEMENT INTO
*                                            NEXT DD ENTRY
FIRSTENT LR    WORKR1,TIOTR             MOVE TIOT ADDRESS TO WORKING
*                                            PLACE
         AR    WORKR1,TIOTDPLR          GET ADDRESS OF NEXT DD ENTRY
*                                            TIOT
*                                       IS THIS END OF TIOT (I.E. FULL
*                                            WORD OF ZEROS)
         OC    TIOELNGH(X4BYTES),TIOELNGH      END OF TABLE
         LA    WORKR2,TWELVE           SET INDEX TO WTO TABLE
         BZ    COMMON                  ISSUE ERROR MESSAGE
         CLC   TIOEDDNM,SYSBFDMP        IS DD ENTRY 'SYSBFDMP'
         BNE   TIOTLOOP                 IF NOT TAKE BRANCH
         DROP  WORKR1
         DROP  TIOTR
         SPACE 2
DCBNOPEN LA    DCBR,OPCBDCB             PLACE FOR DCB POINTER FROM
*                                            GETMAIN.
*                        TRY TO GET CORE FOR OUTPUT DCB
         L     WORKR1,CKQECKCB         PT TO CKCB             LI A39419
         LA    WORKR1,196(WORKR1)      PT TO LIST IN CKCB     LI A39419
         GETMAIN EC,LV=DCBLEN,A=(DCBR),SP=252,MF=(E,(1))      LI A39419
         SR    WORKR2,WORKR2           SET INDEX TO WTO TABLE
         LTR   RTNCODER,RTNCODER       WAS GETMAIN SUCCESSFUL
         BNZ   COMMON                  ISSUE ERROR MESSAGE
*                                      IF GETMAIN FAILS RETURN CODE
*                                         IN REGISTER 15 IS ALREADY
*                                         NOT ZERO SO WILL NOT HAVE
*                                         TO SET IT FOR ABNORMAL
*                                         RETURN INDICATOR
         L     DCBR,OPCBDCB             GET DCB NEW LOCATION
         USING DCB,DCBR            ADDRESSABILITY FOR DCB
         MVC   DCB(DCBLEN),DCBCOPY      MOVE DCB INTO CORE
*                                                 JUST GOTTEN
         L     WORKR1,CKQECKCB         PT TO CKCB             LI A39419
         LA    WORKR1,196(WORKR1)      PT TO LIST IN CKCB     LI A39419
         MVI   ZERO(WORKR1),X'8F'      INDICATE LAST DCB      LI A39419
OPENDCB  OPEN  ((DCBR),(OUTPUT)),MF=(E,(1))                   LI A39419
         TM    DCBOFLGS,OPEN            IS DCB OPEN SUCCESSFUL
         LA    RTNCODER,ABNORMAL        SET RETURN CODE FOR ERROR
*                                            CONDITION INCOUNTERED
*                                            IF BRANCH TAKEN
         BZ    EXIT                     IF NOT TAKE BRANCH
         OI    CKQEFLG2,CKFOPEN         NOTE DCB OPEN
         SPACE 2
         EJECT
***********************************************************************
*                                                                     *
*                   WILL INITIALIZE OPCB
*
***********************************************************************
START    EQU   *
         MVC   OPCBPCNT(OPCBHEAD),OPCBCOPY        MOVE OPCB COPY TO
*                                                 GOTTEN CORE AREA
         MVC   OPCBDECB,DECBCOPY       MOVE DECB TO OPCB
         LA    WORKR3,OPCBL1            GET ADDRESS OF LINE 1 BUFFER
         USING LINE,WORKR3              ESTABLISHING ADDRESSABILITY FOR
*                                            LINE AREA
         LA    WORKR2,OPCBLN2I          GET LINE 2 INDEX VALUE
         SR    WORKR2,OPCBR             BY LINE POINTER MINUS OPCB
*                                       ADDRESS
         SLL   WORKR3,X8BITS            COMBINE INDEX AND LINE ADDRESS
         SLDL  WORKR2,X24BITS                INTO ONE WORD
         ST    WORKR2,OPCBCRLN-X1BYTE   PLACE POINTER IN CURRENT
*                                            LINE POINTER POSITION
         ST    WORKR2,OPCBLN1-X1BYTE    PLACE LINE 1 ADDRESS INTO
*                                            LINE TABLE
         LA    WORKR3,OPCBL2            GET ADDRESS OF LINE 2 BUFFER
         LA    WORKR2,OPCBLN3I          GET LINE 3 INDEX
         SR    WORKR2,OPCBR             BY LINE POINTER MINUS OPCB
*                                       ADDRESS
         ST    WORKR3,OPCBLN2-X1BYTE    PLACE LINE ADDRESS INTO LINE
*                                            TABLE
         STC   WORKR2,OPCBLN2I          PLACE INDEX TO NEXT LINE TO BE
*                                            USED INTO TABLE
         LA    WORKR3,OPCBL3            GET ADDRESS OF LINE 3 BUFFER
         LA    WORKR2,OPCBLN1I         GET LINE ONE INDEX
         SR    WORKR2,OPCBR             BY LINE POINTER MINUS OPCB
*                                       ADDRESS
         ST    WORKR3,OPCBLN3-X1BYTE    PLACE LINE ADDRESS INTO LINE
*                                            TABLE
         STC   WORKR2,OPCBLN3I          PLACE INDEX TO NEXT LINE TO BE
*                                            USED INTO TABLE
         DROP  WORKR3
         EJECT
***********************************************************************
*                                                                     *
*                   EJECT OUTPUT AND FORMAT HEADER AND PRINT IT       *
*                                                                     *
***********************************************************************
         SPACE 2
         USING LINE,LINER               ESTABLISHING ADDRESSABILITY
*                                            FOR OUTPUT LINES
         L     LINER,OPCBCRLN-X1BYTE    GET CURRENT LINE ADDRESS
         BAL   INTRTNR,EJECT            GO SPACE OUTPUT TO NEW PAGE
         MVC   LINETEXT(MSG#1LN),MSG#1  MOVE TEXT OF HEADER MESSAGE
*                                            TO OUTPUT LINE
         BAL   INTRTNR,WRITESP2         GO OUTPUT LINE
         LA    CVTR,SYSCVTPT            LOCATION OF CVT POINTER
         L     CVTR,ZERO(CVTR)          GET CVT ADDRESS
         USING CVT,CVTR                 ESTABLISH ADDRESSABILITY FOR
*                                            COMMUNICATION VECTOR TABLE
         L     TCBR,CVTTCBP             GET NEW/CURRENT WORDS ADDRESS
         L     TCBR,CURRTCB(TCBR)       GET CURRENT TCB POINTER
         L     TIOTR,TCBTIO             GET TASK TIOT POINTER
*                                       ESTABLISH ADDRESSABILITY
         USING TIOT,TIOTR                    FOR TASK I/O TABLE
         MVC   LNJOBNM,TXTJOBNM         PLACE 'JOB' ON TO LINE
         MVC   LNJOBNAM,TIOTNJOB        MOVE JOB NAME TO LINE
         MVC   LNSTPNM,TXTSTPNM         PLACE 'STEP' ON LINE
         MVC   LNSTPNAM,TIOTSTP         MOVE STEP NAME TO LINE
         MVC   LNTMNM,TXTTMNM           PLACE 'TIME' ON LINE
         MVC   LNDTNM,TXTDTNM           PLACE 'DATA' ON LINE
         DROP  TIOTR
         DROP  CVTR
         TIME  DEC                      GET DATE AND TIME
         IC    WORKR0,PLUSSIGN          CHANGE TO PACKED DECIMAL OF
         SRL   WORKR0,X4BITS               HOURS MINUTES AND SECONDS
         ST    WORKR0,LINEWORK          PLACE TIME INTO LINE WORK AREA
         MVC   LNTIME,TIMEMASK               PLACE TIME EDIT MASK ON
*                                                 LINE
         ED    LNTIME,LINEWORK               MAKE TIME PRINTABLE
         ST    WORKR1,LINEWORK          PLACE DATE INTO LINE WORK AREA
         MVC   LNDATE,DATEMASK               PLACE DATE EDIT MASK ON
*                                                 LINE
         ED    LNDATE,LINEWORK+X1BYTE        MAKE DATE PRINTABLE
         MVC   LINEWORK,LINEWORK+X5BYTES          MAKE LINE WORK
*                                            AREA BLANKS AGAIN
         BAL   INTRTNR,WRITESP          GO OUTPUT LINE
         MVC   LNDPMSG,TXTDUMP          PLACE 'DUMP OF' ON LINE
         USING UCB,UCBR                 ESTABLISHING ADDRESSABILITY
*                                            FOR UNIT CONTROL BLOCK
         SR    UCBR,UCBR               CLEAR REGISTER        LG Z30AALG
         ICM   UCBR,THREE,CKQEUCB      GET UCB ADDRESS       LG Z30AALG
         MVC   LNDVAD,UCBNAME           MOVE DEVICE ADDRESS TO LINE
         DROP  UCBR
         BAL   INTRTNR,WRITESP          GO OUTPUT LINE
         MVC   LNBFNM,TXTBFAD           PLACE 'BUFFER START
*                                            REGENERATION ADDRESS'
*                                            ON LINE
         LA    EBCDICR,LNSTRGAD         POINT TO LINE WHERE BUFFER
*                                            ADDRESS IS TO GO
         L     CKCBR,CKQECKCB           ESTABLISH ADDRESSABILITY FOR
         USING CKCB,CKCBR                    CANCEL KEY CONTROL BLOCK
         LA    HEXR,CKCBREST+X2BYTES    POINT TO USERS BUFFER RESTART
*                                            ADDRESS
         LA    MOVELNGR,BUFADDLN        LENGTH OF BUFFER ADDRESS
         BAL   INTRTNR,HEXTOEBC         GO CONVERT BUFFER ADDRESS TO
*                                            PRINTABLE CHARACTERS AND
*                                            PLACE ON OUTPUT LINE
         BAL   INTRTNR,WRITESP2         GO OUTPUT LINE
         SPACE 2
*                       CHECK ROLL OUT
         SPACE 2
         TM    CKQEFLG2,CKFROLUT        HAS USERS BUFFER BEEN ROLLED
*                                            OUT
         BNO   STARTDMP                 IF NOT TAKE BRANCH
         SPACE 2
*                   SINCE USERS BUFFER HAS BEEN ROLLED OUT FOR US
*                        WILL USE IT IN FORMATING DUMP
         SPACE 2
         LA    DECBR,CKCBDECB           ESTABLISH ADDRESSABILITY FOR
         USING DECB,DECBR                    DATA EVENT CONTROL BLOCK
         L     WORKR1,DECBBFAD-X1BYTE        ADDRESS OF WHERE BUFFER
*                                            SECTION ADDRESS IS
         MVC   ZERO(X2BYTES,WORKR1),CKCBBFAD MOVE IN ADDRESS
         MVC   DECBAREA,CKCBROLL            INPUT AREA ADDRESS
         SPACE 2
*                                       WILL CALCULATE BUFFER INDEX
*                                            FOR FIRST BUFFER SECTION
*                                            ASSIGNED TO DEVICE
*                                            REQUESTING DUMP. (I.E.
*                                            BUFFER ADDRESS OF OPTION
*                                            PANEL IS FIRST ASSIGNED
*                                            SO INDEX IS ADDRESS
*                                            DIVIDED BY 256 BUFFER
*                                            SECTION SIZE)
         LH    WORKR1,CKCBBFAD         GET BUFFER ADDRESS
         SLL   WORKR1,X16BITS          SHIFT OUT SIGN BITS
         SRL   WORKR1,X16BITS          RESTORE REG VALUE
         LA    WORKR2,NUMONE            GET ONE TO
         MR    WORKR0,WORKR2                 MULTIPLY BY TO SET UP FOR
*                                            DIVIDE
         LA    WORKR2,NUM256            GET BUFFER SECTION SIZE AND
         DR    WORKR0,WORKR2                 DIVIDE TO GET NUMBER OF
         LR    BUFINDXR,WORKR0               SECTION READ IN
         SPACE 2
         B     LINEFORM                 GO FORMAT THIS BUFFER SECTION
         DROP  DECBR
         DROP  CKCBR
         EJECT
***********************************************************************
*                                                                     *
*              FIND NEXT BUFFER SECTION AND READ IT IN                *
*                                                                     *
***********************************************************************
         SPACE 2
STARTDMP SR    BUFINDXR,BUFINDXR        ZERO OUT REGISTER
CKASSIGN EQU   *                                             LG Z30AALG
         SR    UCBR,UCBR               CLEAR REGISTER        LG Z30AALG
         ICM   UCBR,THREE,CKQEUCB      GET UCB ADDRESS       LG Z30AALG
         USING BUFTABLE,BUFTABR         ESTABLISH ADDRESSABILITY FOR
*                                            BUFFER TABLE
         USING UCB,UCBR                 ESTABLISH ADDRESSABILITY FOR
*                                            UNIT CONTROL BLOCK
         L     BUFTABR,UCBBFTAB-X1BYTE       GET BUFFER TABLE ADDRESS
         CLI   UCBTYP,MODEL1            IS THIS A 2250 MODEL 1
         BNE   MOD2OR3                  IF NOT TAKE BRANCH
         LA    WORKR1,NUMONE            FOR MODEL 1 ONLY HAVE ONE
*                                            DEVICE ENTRY IN BUFFER
*                                            TABLE
         B     ADDHEAD
MOD2OR3  LA    WORKR1,NUM4              FOR MODEL 2 OR 3 HAVE FOUR
*                                            DEVICE ENTRIES IN TABLE
*                                            NO MATTER HOW MANY DEVICES
*                                            ARE ON THE 2840
ADDHEAD  LA    WORKR1,NUMONE(WORKR1)    ADD ONE ENTRY FOR HEADER THEN
         SLL   WORKR1,X3BITS                 MULTIPLY BY 8 TO HAVE
*                                            DISPLACEMENT TO DEVICE
*                                            ASSIGNMENT SECTIONS
         AR    BUFTABR,WORKR1           ADD DISPLACEMENT TO START OF
*                                            TABLE
         AR    BUFTABR,BUFINDXR         ADD SECTIONS DISPLACEMENT TO
*                                            SEE IF ASSIGNED
         CLC   UCBDEVI,ZERO(BUFTABR)         IS THIS BUFFER SECTION
*                                            ASSIGNED TO 2250 UNDER
*                                            CONTROL OF CANCEL KEY
*                                            ROUTINE.
         BE    READIN                   IF YES TAKE BRANCH
CKNEXTAS LA    BUFINDXR,NUMONE(BUFINDXR)     UP INDEX TO NEXT BUFFER
*                                            DEVICE ASSIGN BYTE
         SR    UCBR,UCBR               CLEAR REGISTER        LG Z30AALG
         ICM   UCBR,THREE,CKQEUCB      GET UCB ADDRESS       LG Z30AALG
         L     BUFTABR,UCBBFTAB-X1BYTE  GET BUFFER TABLE ADDRESS
         CH    BUFINDXR,BFTLN           HAVE SEARCHED TOTAL TABLE
         BNH   CKASSIGN                 IF NOT TAKE BRANCH
         BAL   INTRTNR,WRITE            WRITE BLANK LINE
         MVC   LNEOD,MSGEOD             PLACE 'END OF DUMP' ON LINE
         BAL   INTRTNR,WRITE            GO WRITE LAST LINE
         BAL   INTRTNR,WRITE           AGAIN FOR BLOCKED RECORDS
         BAL   INTRTNR,WRITE           TWICE
         SR    RTNCODER,RTNCODER        SET RETURN CODE FOR NORMAL
*                                            COMPLETION (I.E. = 0)
         B     EXIT                     GO DO HOUSE KEEPING AND RETURN
*                                            TO CALLER
         DROP  UCBR
READIN   L     CKCBR,CKQECKCB           GET POINTER TO CKCB
         USING CKCB,CKCBR               ESTABLISH ADDRESSABILITY FOR
*                                            CANCEL KEY CONTROL BLOCK
         LR    WORKR2,BUFINDXR          TAKE INDEX VALUE AND
         SLL   WORKR2,X8BITS                 MULTIPLY BY 256 TO GET
*                                            DISPLACEMENT OF BUFFER
*                                            SECTION IN BYTES
         ST    WORKR2,CKCBWRKA          PLACE ADDRESS INTO WORK AREA
         LA    WORKR2,CKCBWRKA+X2BYTES       GET POINTER TO ADDRESS
*                                            VALUE JUST STORED
         LA    DECBR,CKCBDECB           GET DECB ADDRESS
         USING DECB,DECBR               ESTABLISH ADDRESSABILITY FOR
*                                            DATA EVENT CONTROL BLOCK
         XC    DECBECB,DECBECB          CLEAR WAIT-POST FLAGS IN ECB
         DROP  CKCBR
         L     BUFFSECR,OPCBINBF        GET ADDRESS OF READ INPUT
*                                            BUFFER
         L     BUFSTR,DECBSTAD-X1BYTE   GET RESTART GENERATION ADDRESS
*                                            POINTER
         L     DCBR,CKQEDCB             GET DCB ADDRESS
*                                       READ IN NEXT SECTION OF 2250
*                                            BUFFER
         LR    WORKR1,DECBR             GET DATA EVENT CONTROL BLOCK
*                                            ADDRESS INTO REGISTER U
*                                            ADDRESS INTO REGISTER 1
*                                            FOR MACRO
         SR    GIOCRR1,GIOCRR1         CLR REGISTER          LG @ZM2360
         ICM   GIOCRR1,THREE,CKQEUCB  GET UCB ADDRESS        LG @ZM2360
         L     GIOCRR1,TEB(GIOCRR1)  GET TEB ADDRESS         LG @ZM2360
         CLC   GIOCR(3,GIOCRR1),DCBGIOCR(DCBR)  ADDR IN DCB CORRECT?
         BE    STREGEN               YES,ISSUE READ          LG @ZM2360
         LA    WORKR2,MSG#9          NO,LD MSG DISP FOR WTO  LG @ZM2360
         B     COMMON                GO ISSUE WTO            LG @ZM2360
STREGEN  EQU   *                                             LG @ZM2360
         GREAD (WORKR1),STR,(DCBR),256,(BUFFSECR),(WORKR2),(BUFSTR),   C
               MF=E
         LTR   RTNCODER,RTNCODER        WAS I/O STARTED
         LA    WORKR2,FOUR             SET INDEX TO WTO TABLE
         BNE   COMMON                  ISSUE ERROR MESSAGE
         WAIT  1,ECB=(DECBR)            WAIT ON I/O TO COMPLETE
         CLI   DECBECB,SUCCESS          WAS I/O SUCCESSFUL
         BE    LINEFORM                 GO FORMAT BUFFER SECTION
         LA    WORKR2,FOUR             SET INDEX TO WTO TABLE
*
***********************************************************************
*                                                                     *
*        THIS ROUTINE WILL ISSUE WTO'S FOR ERROR CONDITIONS           *
*                                                                     *
***********************************************************************
COMMON   L     CKCBR,CKQECKCB
         USING  CKCB,CKCBR
         L     WORKR1,CKCBERMT         LOAD POINTER TO WTO TABLE
         L     WORKR1,ZERO(WORKR2,WORKR1)    GET POINTER TO MSG
         SR    UCBR,UCBR               CLEAR REGISTER        LG Z30AALG
         ICM   UCBR,THREE,CKQEUCB      GET UCB ADDRESS       LG Z30AALG
         USING UCB,UCBR                 ESTABLISHING ADDRESSABILITY
*                                            FOR UNIT CONTROL BLOCK
         USING WTOMSG,WORKR1            ESTABLISH ADDRESSABILITY FOR
*                                            WRITE TO OPERATOR MESSAGE
         MVC   CKCBWRKA(NUM57),WTOMSG   MOVE MSG TO WORK AREA TO
*                                      MAINTAIN REENTERABILITY
         MVC   CKCBWRKA+TWELVE(NUM3),UCBNAME    PUT UCBNAME IN WTO
         LA    WORKR1,CKCBWRKA         GET ADDR OF MSG FOR WTO MACRO
         WTO   MF=(E,(1))               ISSUE WTO
         LA    RTNCODER,ABNORMAL        SET RETURN CODE AS ERROR
*                                            CONDITION EXISTS
* GO CLEAN UP AND RETURN TO CALLER
         DROP  UCBR
         EJECT
***********************************************************************
*                                                                     *
*                    EXIT ROUTINE FOR NORMAL AND ADNORMAL RETURN      *
*                                                                     *
***********************************************************************
         SPACE 2
*    NOTE:  THE RETURN CODE MUST BE IN REGISTER (15)
*                                 UPON ENTRY TO THIS SUB-PROGRAM
         SPACE 2
EXIT     OI    CKQEFLG1,CKFEXIT         INDICATE THAT EXIT IS NOW
*                                            IN PROCESS
         L     CKCBR,CKQECKCB           GET CKCB ADDRESS
         ST    RTNCODER,CKCBWRKA+X24BYTES         SAVE RETURN CODE
         TM    CKQEFLG2,CKFOPEN    IS DCB OPEN?               LG YM5686
         BNO   FREEBUFF            NO,DO NOT ATTEMPT TO CLOSE LG YM5686
         L     DCBR,OPCBDCB        GET DCB ADDRESS(SYSBFDMP)  LG YM5686
         LA    WORKR1,CKCBWORK     GET ADDRESS OF LIST        LG YM5686
         MVI   ZERO(WORKR1),X'8F'  INDICATE LAST DCB          LG YM5686
         CLOSE ((DCBR)),MF=(E,(1))                            LG YM5686
         NI    CKQEFLG2,CLOSED     NOTE DCB CLOSED            LG YM5686
         LA    WORKR1,CKCBWORK     GET ADDRESS OF LIST        LG YM5686
         LA    DCBR,OPCBDCB        GET ADDRESS OF DCB ADDRESS LG YM5686
         FREEMAIN E,LV=DCBLEN,A=(DCBR),SP=252,MF=(E,(1))      LG YM5686
FREEBUFF EQU   *                                              LG YM5686
         TM    CKQEFLG2,CKFINBUF        HAS INPUT BUFFER BEEN GOTTEN
         BNO   EXCKOPCB                 IF NOT TAKE BRANCH
         LA    WORKR2,OPCBINBF          GET ADDRESS OF BUFFER ADDRESS
*                                            IN BYTES
         LA    WORKR1,CKCBWORK         PT TO LIST IN CKCB     LI A39419
         FREEMAIN E,LV=INBUFLN,A=(WORKR2),SP=252,MF=(E,(1))   LI A39419
         NI    CKQEFLG2,FULLMASK-CKFINBUF         TURN OFF BUFFER
*                                            ACQUIRED SWITCH
EXCKOPCB TM    CKQEFLG2,CKFOPCB         HAS THE OPCB GETMAIN BE ISSUED
         BNO   EXRTN                    IF NOT TAKE BRANCH
         LA    DECBR,OPCBDECB          GET LINE DECB ADDR FOR CHECK
EXFROPCB L     WORKRE,HSA               GET CALLERS SAVE AREA
         LA    WORKR2,CKCBOPCB          GET ADDRESS OF OPCB'S ADDRESS
         LA    WORKR3,OPCBLEN           GET LENGTH OF OPCB LENGTH
*                                       FREE UP OPCB AREA
         LA    WORKR1,CKCBWORK         PT TO LIST IN CKCB     LI A39419
         FREEMAIN E,LV=(WORKR3),A=(WORKR2),SP=252,MF=(E,(1))  LI A39419
         NI    CKQEFLG2,FULLMASK-CKFOPCB    RESET BIT IN CKQE
         LR    SAVEPTR,WORKRE           PLACE SAVEAREA ADDRESS INTO
*                                            PROPER REGISTER
EXRTN    NI    CKQEFLG1,FULLMASK-CKFEXIT     TURN OFF EXIT IN PROCESS
         L     RTNCODER,CKCBWRKA+X24BYTES        GET RETURN CODE SAVED
*                                                 EARLIER
         RETURN     (14,12),T,RC=(15)             RETURN TO CALLER
*
*
*
         EJECT
***********************************************************************
*                                                                     *
*                   PRINT BUFFER SECTION ROUTINE                      *
*                                  AND                                *
*                             FORMAT A LINE OF  BUFFER DUMP           *
*                                                                     *
***********************************************************************
         SPACE 2
LINEFORM L     CKCBR,CKQECKCB           GET CKCB ADDRESS
         LA    DECBR,CKCBDECB           ESTABLISH ADDRESSABILITY
         USING DECB,DECBR               FOR DATA EVENT CONTROL BLOCK
         L     HEXR,DECBAREA            GET INPUT BUFFER ADDRESS
         SR    WORKRF,WORKRF            ZERO OUT REGISTER
         L     WORKRE,DECBBFAD-X1BYTE        GET BUFFER SECTION ADDRESS
         IC    WORKRF,X1BYTE(WORKRE)         GET BUFFER SECTION
*                                            DISPLACEMENT OF 0 TO 256
         AR    HEXR,WORKRF              ADD TO START OF 256 BYTE
*                                            SECTION READ IN BUFFER
*                                            ADDRESS
         MVC   LININTA,ZERO(HEXR)            MOVE BUFFER DATA
*                                            INTO OUTPUT LINE TO WORK
*                                            FROM AND LATER TO BE
*                                            INTERPRETED IN.
         LA    MOVELNGR,X4BYTES         SET CHARACTER COUNT TO 4
         L     HEXR,DECBBFAD-X1BYTE     GET ADDRESS OF BUFFER SECTION
*                                            FOR FIRST BYTE OF LINE
         LA    EBCDICR,LINEBFAD         GET BUFFER ADDRESS POSITION
*                                            ON OUTPUT LINE
         BAL   INTRTNR,HEXTOEBC         GO PLACE PRINTABLE BUFFER
*                                            ADDRESS ON LINE
         LA    EBCDICR,X2BYTES(EBCDICR)      SPACE UP TO NEXT FIELD
*                                            TO BE FILLED IN
         LA    HALFLNR,NUM2             SET LOOP REGISTER FOR HALF LINE
*                                            COUNTER  (I.E. 2)
         LA    HEXR,LININTA             SET POINTER TO BUFFER DATA
*                                            TO BE CONVERTED
HALFLINE LA    WORDCTR,NUM4             SET LOOP REGISTER FOR WORDS IN
*                                            HALF LINE (I.E. 4)
WORD     LA    MOVELNGR,X8BYTES         SET CHARACTER COUNT TO 8
         LA    EBCDICR,X1BYTE(EBCDICR)  SPACE TO START OF NEXT FIELD
         BAL   INTRTNR,HEXTOEBC         GO HAVE WORD CONVERTED
         BCT   WORDCTR,WORD             HAVE DONE HALF LINE  (I.E. FOUR
*                                            FULL WORDS) ?    IF NOT
*                                            LOOP BACK
         LA    EBCDICR,X3BYTES(EBCDICR) SPACE UP OUTPUT LINE POINTER
         BCT   HALFLNR,HALFLINE         HAVE DONE FULL LINE OR
*                                            TWO HALF LINES? IF NOT
*                                            LOOP BACK TO D SECOND
*                                            HALF
         TR    LININTA,TTRTABLE                   INTERPERT BUFFER DATA
         MVI   LINEFATK,ASTERISK        PLACE * AROUND INTERPERTED
         MVI   LINEBATK,ASTERISK             DATA
         BAL   INTRTNR,WRITE            OUTPUT LINE
         LA    WORKRF,X32BYTES          GET 32 BYTE COUNT
         L     CKCBR,CKQECKCB           GET POINTER TO CKCB
         LA    DECBR,CKCBDECB           GET POINTER TO DATA EVENT
*                                            CONTROL BLOCK
         L     WORKRE,DECBBFAD-X1BYTE   GET BUFFER SECTION ADDRESS
         AH    WORKRF,ZERO(WORKRE)      UP BUFFER DATA ADDRESS BY 32
         STH   WORKRF,ZERO(WORKRE)      RESET BUFFER DATA ADDRESS
         CLI   X1BYTE(WORKRE),BYTEZERO       HAVE WE FORMATED A FULL
*                                            BUFFER SECTION (I.E. IS
*                                            ADDRESS MULTIPLE OF 256
*                                            AND ADDRESS LOW ORDER
*                                            BYTE ZERO)
         BE    CKNEXTAS                 IF YES TAKE BRANCH
         B     LINEFORM                 GO FORMAT LINE OF THIS
*                                           BUFFER SECTION
         EJECT
***********************************************************************
*                                                                     *
*                   OUTPUT CONTROL SUB-PROGRAM                        *
*                                                                     *
***********************************************************************
         SPACE 2
*  INPUT:      ADDRESS IN THE OPCBR REGISTER OF THE OPCB
         SPACE 2
*  ENTRY:      EJECT - TO WRITE LINE IN CURRENT LINE OF OPCB AND
*                             EJECT TO NEW PAGE OF OUTPUT
*
*              WRITE - TO WRITE LINE IN CURRENT LINE OF OPCB AND
*                             SPACE TO NEXT LINE OF OUTPUT
*
*              WRITESP - TO WRITE LINE IN CURRENT LINE OF OPCB AND
*                             SKIP ONE LINE OF OUTPUT
*
*              WRITESP2 - TO WRITE LINE IN CURRENT LINE OF OPCB AND
*                             SKIP TWO LINES OF OUTPUT
*
*  EXIT:       BRANCH BACK ON RETURN ADDRESS OF INTERNAL RETURN
*                   REGISTER INTRTNR
*
*  OUTPUT:     TAKE CURRENT LINE AND ISSUE I/O FOR IT. PLACE NEXT
*                   LINE TO BE USED INTO CURRENT LINE POINTER OF
*                   OPCB
*
*  FUNCTION:   TAKE CURRENT LINE AND ISSUE I/O FOR IT. PLACING PROPER
*                   CONTROL CODES INTO FIRST BYTE OF LINE. (THE CONTROL
*                   CODE IS DEPENDENT ON ENTRY POINT TO SUB-PROGRAM)
*              IF THE NEXT LINE NEEDS TO HAVE A CHECK MADE BEFORE
*                   CONTROL IS RETURNED TO WHERE IT IS CALLED FROM
*                   THE CHECK WILL BE ISSUED.
*              LINE COUNT IS KEEP SO WHEN LINE COUNT EXCEEDS MAXIMUM
*                   LIMIT PER PAGE THEN AN AUTOMATIC EJECT WILL TAKE
*                   PLACE WITH WRITING OF PAGE NUMBER ON TOP LINE OF
*                   NEW PAGE.
         SPACE 2
WRITE    MVI   LNCNTCD,WRTSP1        SET CONTROL CODE TO WRITE
*                                        AND SPACE
         LA    SUMR,NUM1                GET ONE TO
PAGEFULL AH    SUMR,OPCBLCNT            ADD TO LINE COUNT
         STH   SUMR,OPCBLCNT            SAVE LINE COUNT
UPLINE   SR    INDEXR,INDEXR           CLEAR REGISTER
         IC    INDEXR,OPCBLNI          GET NEXT LINE INDEX VALUE
         L     WORKR1,ZERO(INDEXR,OPCBR)    GET NEXT LINE POINTER
         ST    WORKR1,OPCBCRLN-X1BYTE  MAKE CURRENT NEXT LINE
         CLI   OPCBLCNT+X1BYTE,NUM55    IS PAGE FULL (I.E. PAGE COUNT
*                                            > OR = 55)
         BL    CKBLOCK                 IF PAGE NOT FULL TAKE BRANCH
SETEJECT MVI   LNCNTCD,EJECTCD          SET CONTROL CODE TO WRITE AND
*                                            EJECT
CKBLOCK  L     DCBR,OPCBDCB            GET O/P DCB ADDRESS
         USING DCB,DCBR            ZA14043 LOGICALLY DELETES 5 LINES
*        TM    DCBDEVT,DCBDTDA        O/P TAPE OR DA??      D11 ZA14043
*        BZ    ZEROECB             NO-DONT BLOCK RECS       D11 ZA14043
*        C     LINER,OPCBLN3-X1BYTE LAST LINE IN BLOCK??    D11 ZA14043
*        BNE   BLANKOUT                NO-TAKE BRANCH       D11 ZA14043
*        SPACE 2                                            D11 ZA14043
*                        PREPARE TO OUTPUT THIS LINE AND ISSUE I/O
         SPACE 2
ZEROECB   XC    OPCBECB,OPCBECB      ZERO O/P ECB
         LA    DECBR,OPCBDECB          GET ADDR OF O/P DECB
         L     LINER,OPCBLN1-X1BYTE    GET BUFFER ADDR
         ST    LINER,OPCBCRLN-X1BYTE   SET CURRENT LINE AS LINE ONE
*                                      INCASE O/P NOT BLOCKED
         WRITE      (DECBR),SF,(DCBR),(LINER),'S',MF=E      WRITE LINE
*                                       CHECK CURRENT LINE FOR
*                                            SUCCESSFUL I/O LAST TIME
*                                            USED
         CHECK      (DECBR)
         CLI   OPCBECB,SUCCESS         I/O SUCCESSFUL
         BNE   BLANKOUT                 IF NOT TAKE BRANCH
         NI    OPCBERRF,FULLMASK-IOERBSAM         NOTE THAT THIS I/O
*                                            WAS SUCCESSFUL (I.E.
*                                            INDICATE THAT DO NOT HAVE
*                                            CONSECUTIVE I/O ERRORS)
*
BLANKOUT L     LINER,OPCBCRLN-X1BYTE   GET ADDR OF NEXT LINE
         MVI   LINE,BLANK              MOVE IN BLANK CHAR
         MVC   LINETEXT,LINE                 MAKE ALL OF LINE BLANKS
         CLI   OPCBLCNT+X1BYTE,NUM55    IS PAGE FULL
*
         BCR   LOW,INTRTNR              IF NOT TAKE BRANCH
         XC    OPCBLCNT,OPCBLCNT        SET LINE COUNT TO ZERO
         AP    OPCBPCNT,OPCBDEC1        UP PAGE COUNT BY ONE
         MVC   LNPAGNM,TXTPAGE          MOVE 'PAGE' TO LINE
*                                            LINE
         ED    LNPAGNO,OPCBPCNT         CHANGE DECIMAL PAGE COUNT TO
*                                            ECBDIC
         B     WRITESP2                 GO WRITE OUT LINE
         SPACE 3
EJECT    MVI   LINE,BLANK               MOVE IN BLANK CHARACTER
         MVC   LINETEXT,LINE                 MAKE ALL CHARACTERS BLANKS
         MVI   OPCBLCNT+X1BYTE,NUM55    SET LINE COUNT TO PAGE FULL
         B     UPLINE                  SET CONTROL CODE FOR EJECT
         SPACE 3
WRITESP  MVI   LNCNTCD,WRTSPCD          SET CONTROL CODE TO WRITE AND
*                                            SPACE TWICE
         LA    SUMR,NUM2                GET TWO TO
*                                       ADD TO LINE COUNT
         B     PAGEFULL                 GO CHECK TO SEE IF PAGE FULL
         SPACE 3
WRITESP2 MVI   LNCNTCD,WRTSP2CD         SET CONTROL CODE TO WRITE AND
*                                            SPACE THREE TIMES
         LA    SUMR,NUM3                GET THREE TO
*                                       ADD TO LINE COUNT
         B     PAGEFULL                 GO CHECK TO SEE IF PAGE FULL
         EJECT
***********************************************************************
*                                                                     *
*                   BSAM I/O ERROR ANALYSIS ROUTINE                   *
*                                                                     *
***********************************************************************
*                        THE CHECK ROUTINE IGG019BB GIVES US CONTROL
*                        UPON PERMANENT I/O ERROR CONDITION. CHECK
*                        MAY USE REGISTERS 14-8 BUT SAVED THEM BY
*                        STANDARD CONVENTIONS IN MY SAVE AREA POINTER
*                        TO BY REGISTER 13.  SINCE CHECK WILL NOT
*                        REINITIALIZE 9-13 FOR US WE SHOULD NOT
*                        ALTER THEM
ERFORMAT TM    CKQEFLG1,CKFEXIT         IS EXIT IN PROCESS
         BCR   ONES,RETURNR             IF YES TAKE BRANCH
*                   IF THE ERROR IS THE SECOND IN A ROW THEN BSAM
*                        PROCESSING WILL BE STOPPED AND WE WILL RESTORE
*                        REGISTERS 14-8 FROM OUR SAVE AREA POINTED TO
*                        BY REGISTER 13.  THESE ARE OUR REGISTERS
*                        AT TIME OF GOING TO CHECK.
         TM    OPCBERRF,IOERBSAM        IS THIS CONSECTIVE ERROR
         BZ    GETMSG                   IF NOT TAKE BRANCH
*                   IF THIS IS NOT A CONSECUTIVE ERROR WILL ISSUE
*                        SYNADAF MACRO AND THEN ISSUE WRITE TO OPERATOR
*                        TO NOTE ERROR AND THEN SET SWITCH THAT BSAM
*                        I/O ERROR HAS OCCURED.
         LM    RETURNR,DEBR,R0                    RESTORE REGISTERS
*                                            TO THE WAY THEY WERE
*                                            BEFORE CHECK WAS ISSUED
         B     EXIT                     GO CLEAN UP AND STOP
GETMSG   SYNADAF    ACSMETH=BSAM             FORMAT OUTPUT OF ERROR
         MVC   ZERO(FOUR,WORKR1),LENFLGS1    MOVE WTO LENGTH
*                                            AND MCSFLGS TO
*                                            START OF WTO
         MVC   FOUR(SEVEN,WORKR1),MSGHDRID    MOVE MSG ID TO WTO
         SR    UCBR,UCBR               CLEAR REGISTER        LG Z30AALG
         ICM   UCBR,THREE,CKQEUCB      GET UCB ADDRESS       LG Z30AALG
         USING UCB,UCBR                ESTABLISH ADDRESSABILITY
*                                      FOR UNIT CONTROL BLOCK
         MVC   TWELVE(THREE,WORKR1),UCBNAME     ADD UCB NAME TO WTO
         MVC   SIXTN(FORTY,WORKR1),FIFTY(WORKR1)     SHIFT MSG
         MVC   X56BYTES(FOUR,WORKR1),RCDC    MOVE ROUTING AND
*                                      DESCRIPTOR CODES TO WTO
         LA    WORKR2,SIXTY(WORKR1)    GET WTO AREA ADDR FOR MSG2
         MVC   ZERO(FOUR,WORKR2),LENFLGS2    MOVE WTO LENGTH AND
*                                            MCSFLGS TO WTO 2
         MVC   FOUR(SEVEN,WORKR2),MSGHDRID    MOVE MSG ID TO WTO
         MVC   TWELVE(THREE,WORKR2),UCBNAME   ADD UCBNAME TO WTO
         MVC   SIXTN(X37BYTES,WORKR2),X91BYTES(WORKR2)   SHIFT MSG
         MVC   X53BYTES(FOUR,WORKR2),RCDC    MOVE ROUTING AND
*                                    DESCRIPTOR CODES TO WTO
         WTO   MF=(E,(1))               ISSUE WTO
         LR    WORKR1,WORKR2           LOAD REG ONE FOR WTO
         WTO   MF=(E,(1))              ISSUE WTO
         OI    OPCBERRF,IOERBSAM        NOTE HAVE HAD I/O ERROR
         SYNADRLS                       CLEAN UP SAVE AREA CHAINING
         LTR   RTNCODER,RTNCODER        WAS FREEING OF SAVE AREA
*                                            SUCCESSFUL
*                                       IF YES TAKE BRANCH
         BCR   EQUAL,RETURNR            GO BACK TO CHECK AND CONTINUE
*                                       PROCESSING BSAM OUTPUT
*                   IF RELEASE OF SAVE AREA AND RESTORING OF REGISTER
*                        13 TO MY SAVE AREA HAS FAILS WILL TRY TO
*                        RECOVER BY LOADING REGISTER 13 FROM MY SAVE
*                        AREA DISPLACEMENT IN OPCB THEN GO BACK TO
*                        CHECK.  THIS IS A LAST EFFERATE AND IF FAILS
*                        CAN ONLY BE SORRY FOR TRIED TO KEEP RUNNING
*                        AS BEST POSIBLE. CHECK MUST POINT TO OUR
*                        SAVE AREA TO RESTORE REGISTERS BEFORE GOING
*                        BACK TO MAIN PROGRAM TO CONTINUE.
*
         LA    SAVEPTR,OPCBSVAR         GET POINTER TO SAVE AREA
         BR    RETURNR                  HOPFULLY GO BACK TO CHECK
         EJECT
***********************************************************************
*                                                                     *
*                   SUB-PROGRAM TO CONVERT HEX  TO EBCDIC             *
*                                                                     *
***********************************************************************
         SPACE 2
*  INPUT:      REGISTER MOVELGNR NUMBER OF BYTES OF EBCDIC INFROMATION
*                        TO BE PRODUCED
*              REGISTER HEXR  THE HEX  DATA TO BE CONVERTED TO EBCDIC
*
*              REGISTER EBCDICR THE PLACE WHERE THE EBCDIC INFORMATION
*                        IS TO GO UPON CONVERSION
*  NOTE:       OUTPUT AREA MUST BE TWICE THE SIZE OF THE HEX DATA TO
*                        BE CONVERTED
*              AT COMPLETION OF THE MOVE OPERATION THE HEXR WILL POINT
*                   TO THE BYTE NEXT TO BE CONVERTED IF AN EVEN NUMBER
*                   OF BYTES ARE SPECIFIED OR TO THE BYTE HALF
*                   CONVERTED IF AN ODD NUMBER WAS SPECIFIED.
*                   THE EBCDICR REGISTER WILL POINT TO THE NEXT BYTE
*                   FIELD TO BE STORED INTO IN THE LINE.
         SPACE 2
HEXTOEBC LTR   MOVELNGR,MOVELNGR        IS MOVE LENGTH ZERO
         BCR   EQUAL,INTRTNR            IF YES TAKE BRANCH
         SR    WORKRE,WORKRE            ZERO OUT REGISTER
MOVEMORE IC    WORKRE,ZERO(HEXR)        GET TWO HEX DIGITS
         SRDL  WORKRE,X4BITS            MOVE OUT SECOND DIGITS
         IC    WORKRF,EBCDICTB(WORKRE)       GET EBCDIC FOR HEX
         STC   WORKRF,ZERO(EBCDICR)     PLACE EBCDIC CHARACTER IN LINE
         LA    EBCDICR,NUMONE(EBCDICR)       UP OUTPUT POINTER
         BCT   MOVELNGR,SCNDHALF        LOWER NUMBER OF OUTPUT REQUEST
*                                            COUNT BY ONE AND IF HAVE
         BR    INTRTNR                       CONVERTED ALL THAT WAS
*                                            REQUESTED RETURN TO WHERE
*                                            CALLED
SCNDHALF SR    WORKRE,WORKRE            ZERO OUT REGISTER
         SLDL  WORKRE,X4BITS            GET SECOND HALF OF BYTE
         IC    WORKRF,EBCDICTB(WORKRE)       GET EBCDIC FOR HEX
         STC   WORKRF,ZERO(EBCDICR)     PLACE EBCDIC CHARACTER ON LINE
         LA    EBCDICR,NUMONE(EBCDICR)       UP OUTPUT POINTER BY ONE
         LA    HEXR,NUMONE(HEXR)        UP INPUT POINTER TO NEXT BYTE
*
         BCT   MOVELNGR,MOVEMORE        LOWER NUMBER OF BYTES TO BE
*                                            CONVERTED BY ONE AND IF
         BR    INTRTNR                       HAVE CONVERTED ALL THAT
*                                            WAS REQUESTED RETURN TO
*                                            WHERE CALLED FROM
         EJECT
***********************************************************************
*                   THE TABLE USED BY THE TEST AND TRANSLATE          *
*                        INSTRUCTION TO INTERPRET THE DUMP CORE       *
*                                                                     *
***********************************************************************
         SPACE 2
*                                       HEX BYTE  TO BE PRINTED AS
TTRTABLE DC    X'4B'                      '00'          '.'
         DC    X'4B'                      '01'          '.'
         DC    X'4B'                      '02'          '.'
         DC    X'4B'                      '03'          '.'
         DC    X'4B'                      '04'          '.'
         DC    X'4B'                      '05'          '.'
         DC    X'4B'                      '06'          '.'
         DC    X'4B'                      '07'          '.'
         DC    X'4B'                      '08'          '.'
         DC    X'4B'                      '09'          '.'
         DC    X'4B'                      '0A'          '.'
         DC    X'4B'                      '0B'          '.'
         DC    X'4B'                      '0C'          '.'
         DC    X'4B'                      '0D'          '.'
         DC    X'4B'                      '0E'          '.'
         DC    X'4B'                      '0F'          '.'
         DC    X'4B'                      '10'          '.'
         DC    X'4B'                      '11'          '.'
         DC    X'4B'                      '12'          '.'
         DC    X'4B'                      '13'          '.'
         DC    X'4B'                      '14'          '.'
         DC    X'4B'                      '15'          '.'
         DC    X'4B'                      '16'          '.'
         DC    X'4B'                      '17'          '.'
         DC    X'4B'                      '18'          '.'
         DC    X'4B'                      '19'          '.'
         DC    X'4B'                      '1A'          '.'
         DC    X'4B'                      '1B'          '.'
         DC    X'4B'                      '1C'          '.'
         DC    X'4B'                      '1D'          '.'
         DC    X'4B'                      '1E'          '.'
         DC    X'4B'                      '1F'          '.'
         DC    X'4B'                      '20'          '.'
         DC    X'4B'                      '21'          '.'
         DC    X'4B'                      '22'          '.'
         DC    X'4B'                      '23'          '.'
         DC    X'4B'                      '24'          '.'
         DC    X'4B'                      '25'          '.'
         DC    X'4B'                      '26'          '.'
         DC    X'4B'                      '27'          '.'
         DC    X'4B'                      '28'          '.'
         DC    X'4B'                      '29'          '.'
         DC    X'4E'                      '2A'          '+'
         DC    X'4B'                      '2B'          '.'
         DC    X'4B'                      '2C'          '.'
         DC    X'4B'                      '2D'          '.'
         DC    X'4B'                      '2E'          '.'
         DC    X'4B'                      '2F'          '.'
         DC    X'4B'                      '30'          '.'
         DC    X'4B'                      '31'          '.'
         DC    X'4B'                      '32'          '.'
         DC    X'4B'                      '33'          '.'
         DC    X'4B'                      '34'          '.'
         DC    X'4B'                      '35'          '.'
         DC    X'4B'                      '36'          '.'
         DC    X'4B'                      '37'          '.'
         DC    X'4B'                      '38'          '.'
         DC    X'4B'                      '39'          '.'
         DC    X'4B'                      '3A'          '.'
         DC    X'4B'                      '3B'          '.'
         DC    X'4B'                      '3C'          '.'
         DC    X'4B'                      '3D'          '.'
         DC    X'4B'                      '3E'          '.'
         DC    X'4B'                      '3F'          '.'
         DC    X'40'                      '40'          ' '      A33603
         DC    X'4B'                      '41'          '.'
         DC    X'4B'                      '42'          '.'
         DC    X'4B'                      '43'          '.'
         DC    X'4B'                      '44'          '.'
         DC    X'4B'                      '45'          '.'
         DC    X'4B'                      '46'          '.'
         DC    X'4B'                      '47'          '.'
         DC    X'4B'                      '48'          '.'
         DC    X'4B'                      '49'          '.'
         DC    X'4A'                      '4A'          ''
         DC    X'4B'                      '4B'          '.'
         DC    X'4C'                      '4C'          '<'
         DC    X'4D'                      '4D'          '('
         DC    X'4E'                      '4E'          '+'
         DC    X'4F'                      '4F'          ''
         DC    X'50'                      '50'          '&'
         DC    X'4B'                      '51'          '.'
         DC    X'4B'                      '52'          '.'
         DC    X'4B'                      '53'          '.'
         DC    X'4B'                      '54'          '.'
         DC    X'4B'                      '55'          '.'
         DC    X'4B'                      '56'          '.'
         DC    X'4B'                      '57'          '.'
         DC    X'4B'                      '58'          '.'  LD YA00801
         DC    X'4B'                      '59'          '.'  LD YA00801
         DC    X'5A'                      '5A'          ' '      A33603
         DC    X'5B'                      '5B'          '$'      A33603
         DC    X'5C'                      '5C'          '*'      A33603
         DC    X'5D'                      '5D'          ')'      A33603
         DC    X'5E'                      '5E'          ';'
         DC    X'5F'                      '5F'          '^'
         DC    X'60'                      '60'          '-'
         DC    X'61'                      '61'          '/'
         DC    X'4B'                      '62'          '.'
         DC    X'4B'                      '63'          '.'
         DC    X'4B'                      '64'          '.'
         DC    X'4B'                      '65'          '.'
         DC    X'4B'                      '66'          '.'
         DC    X'4B'                      '67'          '.'
         DC    X'4B'                      '68'          '.'
         DC    X'4B'                      '69'          '.'
         DC    X'4B'                      '6A'          '.'
         DC    X'6B'                      '6B'          ','
         DC    X'6C'                      '6C'          '%'
         DC    X'6D'                      '6D'          '_'
         DC    X'6E'                      '6E'          '>'
         DC    X'6F'                      '6F'          '?'
         DC    X'4B'                      '70'          '.'
         DC    X'4B'                      '71'          '.'
         DC    X'4B'                      '72'          '.'
         DC    X'4B'                      '73'          '.'
         DC    X'4B'                      '74'          '.'
         DC    X'4B'                      '75'          '.'
         DC    X'4B'                      '76'          '.'
         DC    X'4B'                      '77'          '.'
         DC    X'4B'                      '78'          '.'
         DC    X'4B'                      '79'          '.'
         DC    X'7A'                      '7A'          ':'      A33603
         DC    X'7B'                      '7B'          '#'      A33603
         DC    X'7C'                      '7C'          '@'      A33603
         DC    X'7D'                      '7D'          '''      A33603
         DC    X'7E'                      '7E'          '='      A33603
         DC    X'7F'                      '7F'          '"'      A33603
         DC    X'4B'                      '80'          '.'
         DC    X'C1'                      '81'          'A'
         DC    X'C2'                      '82'          'B'
         DC    X'C3'                      '83'          'C'
         DC    X'C4'                      '84'          'D'
         DC    X'C5'                      '85'          'E'
         DC    X'C6'                      '86'          'F'
         DC    X'C7'                      '87'          'G'
         DC    X'C8'                      '88'          'H'
         DC    X'C9'                      '89'          'I'
         DC    X'4B'                      '8A'          '.'
         DC    X'4B'                      '8B'          '.'
         DC    X'4B'                      '8C'          '.'
         DC    X'4B'                      '8D'          '.'
         DC    X'4B'                      '8D'          '.'
         DC    X'4B'                      '8F'          '.'
         DC    X'4B'                      '90'          '.'
         DC    X'D1'                      '91'          'J'
         DC    X'D2'                      '92'          'K'
         DC    X'D3'                      '93'          'L'
         DC    X'D4'                      '94'          'M'
         DC    X'D5'                      '95'          'N'
         DC    X'D6'                      '96'          'O'
         DC    X'D7'                      '97'          'P'
         DC    X'D8'                      '98'          'Q'
         DC    X'D9'                      '99'          'R'
         DC    X'4B'                      '9A'          '.'
         DC    X'4B'                      '9B'          '.'
         DC    X'4B'                      '9C'          '.'
         DC    X'4B'                      '9D'          '.'
         DC    X'4B'                      '9E'          '.'
         DC    X'4B'                      '9F'          '.'
         DC    X'4B'                      'A0'          '.'
         DC    X'4B'                      'A1'          '.'
         DC    X'E2'                      'A2'          'S'
         DC    X'E3'                      'A3'          'T'
         DC    X'E4'                      'A4'          'U'
         DC    X'E5'                      'A5'          'V'
         DC    X'E6'                      'A6'          'W'
         DC    X'E7'                      'A7'          'X'
         DC    X'E8'                      'A8'          'Y'
         DC    X'E9'                      'A9'          'Z'
         DC    X'4B'                      'AA'          '.'
         DC    X'4B'                      'AB'          '.'
         DC    X'4B'                      'AC'          '.'
         DC    X'4B'                      'AD'          '.'
         DC    X'4B'                      'AE'          '.'
         DC    X'4B'                      'AF'          '.'
         DC    X'4B'                      'B0'          '.'
         DC    X'4B'                      'B1'          '.'
         DC    X'4B'                      'B2'          '.'
         DC    X'4B'                      'B3'          '.'
         DC    X'4B'                      'B4'          '.'
         DC    X'4B'                      'B5'          '.'
         DC    X'4B'                      'B6'          '.'
         DC    X'4B'                      'B7'          '.'
         DC    X'4B'                      'B8'          '.'
         DC    X'4B'                      'B9'          '.'
         DC    X'4B'                      'BA'          '.'
         DC    X'4B'                      'BB'          '.'
         DC    X'4B'                      'BC'          '.'
         DC    X'4B'                      'BD'          '.'
         DC    X'4B'                      'BE'          '.'
         DC    X'4B'                      'BF'          '.'
         DC    X'4B'                      'B0'          '.'
         DC    X'C1'                      'C1'          'A'
         DC    X'C2'                      'C2'          'B'
         DC    X'C3'                      'C3'          'C'
         DC    X'C4'                      'C4'          'D'
         DC    X'C5'                      'C5'          'E'
         DC    X'C6'                      'C6'          'F'
         DC    X'C7'                      'C7'          'G'
         DC    X'C8'                      'C8'          'H'
         DC    X'C9'                      'C9'          'I'
         DC    X'4B'                      'CA'          '.'
         DC    X'4B'                      'CB'          '.'
         DC    X'4B'                      'CC'          '.'
         DC    X'4B'                      'CD'          '.'
         DC    X'4B'                      'CE'          '.'
         DC    X'4B'                      'CF'          '.'
         DC    X'4B'                      'D0'          '.'
         DC    X'D1'                      'D1'          'J'
         DC    X'D2'                      'D2'          'K'
         DC    X'D3'                      'D3'          'L'
         DC    X'D4'                      'D4'          'M'
         DC    X'D5'                      'D5'          'N'
         DC    X'D6'                      'D6'          'O'
         DC    X'D7'                      'D7'          'P'
         DC    X'D8'                      'D8'          'Q'
         DC    X'D9'                      'D9'          'R'
         DC    X'4B'                      'DA'          '.'
         DC    X'4B'                      'DB'          '.'
         DC    X'4B'                      'DC'          '.'
         DC    X'4B'                      'DD'          '.'
         DC    X'4B'                      'DE'          '.'
         DC    X'4B'                      'DF'          '.'
         DC    X'4B'                      'E0'          '.'
         DC    X'4B'                      'E1'          '.'
         DC    X'E2'                      'E2'          'S'
         DC    X'E3'                      'E3'          'T'
         DC    X'E4'                      'E4'          'U'
         DC    X'E5'                      'E5'          'V'
         DC    X'E6'                      'E6'          'W'
         DC    X'E7'                      'E7'          'X'
         DC    X'E8'                      'E8'          'Y'
         DC    X'E9'                      'E9'          'Z'
         DC    X'4B'                      'EA'          '.'
         DC    X'4B'                      'EB'          '.'
         DC    X'4B'                      'EC'          '.'
         DC    X'4B'                      'ED'          '.'
         DC    X'4B'                      'EE'          '.'
         DC    X'4B'                      'EF'          '.'
         DC    X'F0'                      'F0'          '0'
         DC    X'F1'                      'F1'          '1'
         DC    X'F2'                      'F2'          '2'
         DC    X'F3'                      'F3'          '3'
         DC    X'F4'                      'F4'          '4'
         DC    X'F5'                      'F5'          '5'
         DC    X'F6'                      'F6'          '6'
         DC    X'F7'                      'F7'          '7'
         DC    X'F8'                      'F8'          '8'
         DC    X'F9'                      'F9'          '9'
         DC    X'4B'                      'FA'          '.'
         DC    X'4B'                      'FB'          '.'
         DC    X'4B'                      'FC'          '.'
         DC    X'4B'                      'FD'          '.'
         DC    X'4B'                      'FE'          '.'
         DC    X'4B'                      'FF'          '.'
*
         EJECT
***********************************************************************
*                                                                     *
*                        EBCDIC LOOK UP TABLE FOR HEX TO EBCDIC       *
*                             CONVERSION                              *
*                                                                     *
***********************************************************************
         SPACE 2
*                                  HEX VALUE TO BE PRINTED AS
EBCDICTB DC    C'0'                  '0000'          '0'
         DC    C'1'                  '0001'          '1'
         DC    C'2'                  '0010'          '2'
         DC    C'3'                  '0011'          '3'
         DC    C'4'                  '0100'          '4'
         DC    C'5'                  '0101'          '5'
         DC    C'6'                  '0110'          '6'
         DC    C'7'                  '0111'          '7'
         DC    C'8'                  '1000'          '8'
         DC    C'9'                  '1001'          '9'
         DC    C'A'                  '1010'          'A'
         DC    C'B'                  '1011'          'B'
         DC    C'C'                  '1100'          'C'
         DC    C'D'                  '1101'          'D'
         DC    C'E'                  '1110'          'E'
         DC    C'F'                  '1111'          'F'
DISABLED DC    X'00'             MASK TO DISABLE ALL INTERRUPTS  A39421
ENABLED  DC    X'FF'                   MASK TO ENABLE ALL INTER  A39421
         EJECT
         SPACE 2
*                        PRINTER CONTROL CODES
         SPACE 2
WRTSP1   EQU   X'09'                    WRITE AND SPACE ONE LINE
WRTSPCD  EQU   X'11'                    WRITE AND SPACE TWO LINES
WRTSP2CD EQU   X'19'                    WRITE AND SPACE THREE LINES
EJECTCD  EQU   X'89'                    WRITE AND SKIP TO CHANNEL 1
NUM1     EQU   1                        HEX VALUE OF ONE
NUM2     EQU   2                        HEX VALUE OF TWO
NUM3     EQU   3                        HEX VALUE OF THREE
NUM4     EQU   4                        HEX VALUE OF FOUR
NUM55    EQU   55                       HEX VALUE OF 55
NUM57    EQU   57                      HEX VALUE OF 57
RCDC     DC    X'10004020'             ROUTING/DESCRIPTOR CODES
LENFLGS1 DC    X'00388000'             LENGTH/MCSFLGS FOR WTO 1
LENFLGS2 DC    X'00358000'             LENGTH/MCSFLGS FOR WTO 2
MSGHDRID DC    C'IFF005I'              MSG ID
NUM256   EQU   256                      HEX VALUE OF 256
BLANK    EQU   X'40'                    EBCDIC BLANK CHARACTER
CKNEEDED EQU   X'FF'                    CHECK NEEDED FOR THIS LINE ECB
CLOSED   EQU   X'F7'             MASK TO SET DCB CLOSED FLGS  LG YM5686
TXTPAGE  DC    C'PAGE'
PAGEMASK DC    X'20202020'              MASK USED IN EDIT OF PAGE COUNT
CKFERROR EQU   X'02'                    HAVE HAD ONE I/0 ERROR ON
*                                            BSAM OUT PUT
LOW      EQU   4                        CONDITION CODE FOR LOW (4)
EQUAL    EQU   8                        CONDITION CODE OF 8 (EQUAL)
NOTHIGH  EQU   13                       CONDITION CODE OF 13 (NOT HIGH)
ZERO     EQU   0                        DISPLACEMENT OF ZERO
NUMONE   EQU   1                        DISPLACEMENT OF ONE
X4BITS   EQU   4                        MOVEMENT OF 4 BITS
X8BITS   EQU   8                        SHIFT OF 8 BITS (1 BYTE)
*                                       ALSO MULTIPLY BY 256
ABNORMAL EQU   4                        ADNORMAL RETURN CODE
SUCCESS  EQU   X'7F'                    POST CODE FOR SUCCESSFUL I/O
FULLMASK EQU   X'FF'                    FULL BYTE OF ONES MASK
BYTEZERO EQU   X'00'                    BYTE OF ZEROS
*                                       BRANCH IF TAKEN
IOERBSAM EQU   X'FF'                    BSAM ERROR CONSECTATIVE ERROR
*                                            INDICATOR
ONES     EQU   1                        CONDITION CODE OF ONE
WTOHEAD  DC    AL1(91)                  WTO MESSAGE LENGTH
         DC    X'00'                    FILLER
         DC    C'IFF005I '              MESSAGE ID
WTOUNIT  DC    C'   '                   UNIT ADDRESS
WTOUNITL EQU   *-WTOUNIT                LENGTH OF UNIT ADDRESS
WTOUNDPL EQU   WTOHEAD-WTOUNIT          DISPLACEMENT INTO WTO WHERE
*                                            UNIT ADDRESS IS PLACED
WTOLN    EQU   *-WTOHEAD                LENGTH OF WTO HEADER
*
*                                       WORDS USED IN DUMP FORMAT
*
MSG#1    DC    C'*2250 BUFFER DUMP REQUESTED *'
MSG#1LN  EQU   *-MSG#1                  LENGTH OF MESSAGE 1 TEXT
TXTJOBNM DC    C'JOB'
TXTSTPNM DC    C'STEP'
TXTTMNM  DC    C'TIME'
TXTDTNM  DC    C'DATE'
TXTBFAD  DC    C'BUFFER START REGENERATION ADDRESS'
TXTDUMP  DC    C'DUMP OF'
TIMEMASK DC    X'2020204B20204B2020'    MASK USED IN EDIT OF TIME
DATEMASK DC    X'20204B202020'          MASK USED IN EDIT OF DATE
ASTERISK EQU   C'*'                     PRINTABLE ASTERISK CHARACTER
MSGEOD   DC    C'END OF 2250 BUFFER DUMP'         TEXT MESSAGE
SYSBFDMP DC    C'SYSBFDMP'              DUMP OUTPUT DDNAME
PATCH    DC    C'IFFCAN02 50 BYTE PATCH AREA.'
         DS    0H                  LINEUP SO CAN USE IT ALL D11
         DC    40X'FF'                                      D11
*                        STORAGE CONSTANTS
X1BYTE   EQU   1
X2BYTES  EQU   2                        DISPLACEMENT OF TWO
X3BYTES  EQU   3                        DISPLACEMENT OF THREE BYTES
X4BYTES  EQU   4                        LENGTH OF 4 BYTES
X5BYTES  EQU   5                       DISP OF 5 BYTES
X8BYTES  EQU   8                        DISPLACEMENT VALUE OF 8
X24BYTES EQU   24                       DISPLACEMENT VALUE OF 24
X32BYTES EQU   32                       DISPLACEMENT VALUE OF 32
X36BYTES EQU   36                       DISPLACEMENT VALUE OF 36
X3BITS   EQU   3                        MULTIPLY BY 8 (MULTIPIER)
*                                           (I.E. 3 SHIFT BITS)
X16BITS  EQU   16                      SHIFT VALUE OF HALF WORD
X24BITS  EQU   24                       SHIFT VALUE OF 24 BITS
FOUR     EQU   4                       DISPLACEMENT
SEVEN    EQU   7                       LENGTH OF SEVEN
TWELVE   EQU   12                      DISPLACEMENT
THREE    EQU   3                       DISPLACEMENT
SIXTN    EQU   16                      DISPLACEMENT
FORTY    EQU   40                      DISPLACEMENT
FIFTY    EQU   50                      DISPLACEMENT
NUM52    EQU   52
SIXTY    EQU   60
X37BYTES EQU   37
X53BYTES EQU   53
X56BYTES EQU   56
X91BYTES EQU   91
MSG#2AD  EQU   8                        DISPLACEMENT INTO ERROR
*                                            MESSAGE TABLE FOR IFF002I
*                                            MESSAGE ADDRESS
MSG#9    EQU   20                    DISP INTO MSG TABLE     LG @ZM2360
GIOCR    EQU   33                    DISP INTO TEB           LG @ZM2360
TEB      EQU   28                    DISP INTO UCB           LG @ZM2360
DCBGIOCR EQU   49                    DISP INTO DCB           LG @ZM2360
GIOCRR1  EQU   15                    WORK REG                LG @ZM2360
PLUSSIGN DC    X'C0'                    PLUSS SIGN FOR PACKED DECIMAL
         LTORG
         SPACE 2
*                             OPCB HEADER VALUES
         SPACE 2
OPCBCOPY DC    X'000C'                  DECIMAL PLUS ZERO PAGE COUNT
         DC    X'0000'                  HEX ZERO LINE COUNT
         DC    X'001C'                  DECIMAL PLUS ONE PAGE ADDER
         DC    X'00'                    BSAM ERROR COUNTER
         EJECT
         EJECT
         WRITE DECBCOPY,SF,,,'S',,,MF=L      DECB COPY
DECBLN   EQU   *-DECBCOPY               LENGTH OF DECB
         EJECT
DCBCOPY  DCB   DSORG=PS,MACRF=W,RECFM=MBF,LRECL=124,BLKSIZE=124,       C
               DDNAME=SYSBFDMP,BFALN=F,SYNAD=GETMSG    LG YM5686,A39419
DCBLEN   EQU   *-DCBCOPY                LENGTH OF DCB
         EJECT
CKQE     DSECT         SYSTEM CANCEL KEY QUEUE ELEMENT
         SPACE 2
CKQECKQE DS    F                        POINTER TO NEXT CKQE ON
*                                            ACTIVE LIST
CKQEFLG1 DS    X                        FLAG FIELD BYTE ONE
CKFGJP   EQU   X'80'                    IS THIS CALL TO SYSTEM CANCEL
*                                            KEY ROUTINES FROM GJP
*                                            ABEND HOOK
CKFABDMP EQU   X'40'                    A ABEND DUMP IS REQUESTED BY
*                                            THE 2250 OPERATOR
CKFATTWT EQU   X'20'                    SYSTEM CANCEL KEY ROUTINES
*                                            ARE EXPECTING AN ATTENTION
*                                            FROM DEVICE ASSOCIATED
*                                            WITH THIS CKQE
CKFCKCB  EQU   X'10'                    CANCEL KEY CONTROL BLOCK
*                                            POINTER IS VALID IN CKQE
CKFEXIT  EQU   X'08'                    EXIT IN PROCESS
CKFECB   EQU   X'04'                    GJP ECB POINTER IS VALID IN
*                                            CKQE AND GJP IS WAITING
*                                            TO BE POSTED
CKFDEQUE EQU   X'02'                    SYSTEM CANCEL KEY ROUTINE HAS
*                                            PERFORMED ALL REQUESTS
*                                            FOR DEVICE ASSOCIATED
*                                            WITH THIS CKQE AND THE
*                                            CKQE SHOULD BE DEQUEUED
CKFBFRUN EQU   X'01'                    THE 2250 BUFFER WAS RUNNING
*                                            AT THE TIME THE SYSTEM
*                                            CANCEL KEY FUNCTION WAS
*                                            REQUESTED
CKQEFLG2 DS    X                        SECOND BYTE OF FLAG FIELD
CKFROLBF EQU   X'80'                    GETMAIN HAS BEEN ISSUED TO
*                                            ROLL OUT THE USERS
*                                            BUFFER SECTION
CKFROLUT EQU   X'40'                    USERS BUFFER HAS BEEN ROLLED
*                                            OUT
CKFINBUF EQU   X'20'                    GETMAIN FOR INPUT BUFFER FOR
*                                            BUFFER DUMP REQUEST HAS
*                                            BEEN ISSUED SUCCESSFULLY
CKFOPCB  EQU   X'10'                    GETMAIN FOR OUTPUT CONTROL
*                                            BLOCK HAS BEEN ISSUED
CKFOPEN  EQU   X'08'                    SYSBFDMP OUTPUT DCB IS OPEN
CKFLPATN EQU   X'04'                    THIS IS AN ENTRY SCHEDULED
*                                            BY GAR WITH A LIGHTPEN
*                                            ATTENTION.
CKFWTCNT EQU   X'02'                    THE REQUESTOR OF A        M2878
*                                            SYSTEM CANCEL KEY    M2878
*                                            FUNCTION REQUEST     M2878
*                                            BLOCK'S WAIT COUNT   M2878
*                                            HAS BEEN INCREMENTED M2878
*                                            BY ONE               M2878
CKQEUCB  DS    H                        UNIT CONTROL BLOCK THAT IS NOW
*                                            UNDER CONTROL OF THE
*                                            SYSTEM CANCEL KEY ROUTINES
CKQECKCB DS    F                        POINTER TO CANCEL KEY CONTROL
*                                            BLOCK ASSOCIATED WITH THIS
*                                            CKQE
CKQEDCB  DS    F                        POINTER TO THE OPEN DATA
*                                            CONTROL BLOCK FOR THE
*                                            DEVICE REQUESTING THE
*                                            SYSTEM CANCEL KEY FUNCTION
         EJECT
CKCB     DSECT         SYSTEM CANCEL KEY CONTROL BLOCK
         SPACE 2
CKCBOPCB DS    F                        ADDRESS OF OUTPUT CONTROL BLOCK
*                                            ASSOCIATED WITH THIS CKCB
CKCBECB  DS    F                        ADDRESS OF GJP'S ECB TO BE
*                                            POSTED WHEN SYSTEM CANCEL
*                                            KEY FUNCTION REQUESTED BY
*                                            GJP IS COMPLETE
CKCBSVAR DS    18F                      SAVE AREA FOR IFFCAN01
CKCBBFAD DS    H                        BUFFER ADDRESS WHERE SYSTEM
*                                            CANCEL KEY 256 BYTE BUFFER
*                                            SECTION STARTS.  OPTION
*                                            PANEL WILL BE PLACED INTO
*                                            THIS SECTION
CKCBBFLG DS    H                        BUFFER LENGTH OF THE DATA
*                                            WRITTEN TO THE 2250 FOR
*                                            THE OPTIONS PANEL
CKCBWRKA DS    CL64                    WORK AREA FOR:
*                                            READ MI INPUT
*                                            READ SENSE INPUT
*                                            BUFFER INQUIRY TABLE
*                                            WRITE TO OPERATOR MESSAGES
CKCBDECB DS    8F                       DATA EVENT CONTROL BLOCK
*                                            USED IN I/O OPERATIONS
*                                            TO THE 2250
CKCBREST DS    F                        ADDRESS OF 2250 RESTART
*                                            GNERATION OF THE USERS
*                                            BUFFER PROGRAM
CKCBROLL DS    F                        ADDRESS OF 256 BYTE AREA USED
*                                            TO ROLL OUT USERS BUFFER
*                                            IF NO OTHER BUFFER SPACE
*                                            AVAILABLE FOR THE DEVICE
CKCBERMT DS    F                        POINTER TO TABLE OF ERROR
*                                            MESSAGES TO BE SENT TO
*                                            THE SYSTEM OPERATOR UPON
*                                            ERROR CONDITION.
*
CKDCBSTR DS    F                       SAVE AREA - DCB BUFFER LI  M6619
*                                      START ADDRESS          LI A39419
CKCBWORK DS    F                       LENGTH FOR E GETMAIN   LI A39419
         DS    F                       ADDR OF ADDR LIST      LI A39419
         DS    C                       EC MODE                LI A39419
         DS    C                       SP VALUE               LI A39419
CKCBBFDP DS    H                       STORAGE FOR SENSE DATA LI A34790
*                                      FROM UCB ON LP ATTN    LI A34790
         EJECT
UCB      DSECT         UNIT CONTROL BLOCK
         DS    0F
         SPACE 2
UCBIJI   DS    X                        INTERNAL JOB IDENTIFICATION
UCBACM   DS    X                        ALLOCATION CHANNEL MASK
UCBID    DS    X                        UCB IDENTIFICATION - HEX FF
UCBSTBYA DS    X                        STATUS BYTE A
UCBCHA   DS    X                        CHANNEL ADDRESS
UCBUA    DS    X                        UNIT ADDRESS
UCBFL1   DS    X                        FLAG BYTE 1
UCBDTI   DS    X                        INDEX TO DEVICE TABLE
UCBETI   DS    X                        ERROR ROUTINE KEY
UCBSTI   DS    X                        STATISTICS TABLE INDEX
UCBLCI   DS    X                        CHANNEL TABEL INDEX
UCBATI   DS    X                        ATTENTION TABLE INDEX
UCBWGT   DS    X                        FLAGS AND CHANNEL MASK
UCBNAME  DS    3X                       UNIT NAME IN EBCDIC
UCBTYP   DS    F                        DEVICE TYPE
MODEL1   EQU   X'31'                    MODEL 1 2250 TYPE CODE
UCBLTR   DS    H                        LAST REQUEST ELEMENT
UCBSNS   DS    2H                       SENSE INFORMATION
UCBUCNT  DS    X                        USE COUNT
UCBGCB   DS    X                        GRAPHIC CONTROL BYTE
UCBTEB   DS    F                        TASK ENTRY BLOCK ADDRESS
UCBREST  DS    F                        RESTART ADDRESS
UCBDEVI  DS    X                        DEVICE INDEX FOR BUFFER TABLE
UCBBFTAB DS    3X                       BUFFER TABLE ADDRESS
         EJECT                     BUFFER TABLE
BUFTABLE DSECT
         DS    0F
         SPACE 2
BFTLN    DS    H                        TOTAL LENGTH OF BUFFER TABLE
BFTNMDEV DS    H                        TOTAL NUMBER OF DEVICES
BFTEXBUF DS    H                        EXPRESS BUFFER SIZE
BFTTOTAV DS    H                        TOTAL AVAILABLE SECTIONS
         EJECT
OPCB     DSECT         OUTPUT CONTROL BLOCK
         SPACE 2
         DS    0F
OPCBLNI  DS    X                        INDEX OF LINE IN USE
OPCBCRLN DS    3X                       ADDRESS OF LINE NOW IN USE
OPCBLN1I DS    X                        INDEX OF LINE ONE (I.E.
*                                            DISPLACEMENT TO NEXT
*                                            LINE TO BE USED)
OPCBLN1  DS    3X                       ADDRESS OF LINE ONE
OPCBLN2I DS    X                        INDEX OF LINE TWO
OPCBLN2  DS    3X                       ADDRESS OF LINE TWO
OPCBLN3I DS    X                        INDEX OF LINE THREE
OPCBLN3  DS    3X                       ADDRESS OF LINE THREE
OPCBPCNT DS    H                        PAGE COUNT OF OUTPUT IN DECIMAL
OPCBLCNT DS    H                        LINE COUNT OF CURRENT PAGE
*                                            IN HEX
OPCBDEC1 DS    H                        HALF WORD OF A DECIMAL ONE
OPCBERRF DS    X                        BSAM ERROR FLAG FIELD COUNTER
OPCBHEAD EQU   *-OPCBPCNT               LENGTH OF OPCB HEADER
OPCBSVAR DS    18F                      SAVE AREA FOR IFFCAN02
OPCBL1   DS    CL124                   LINE ONE BUFFER AREA
OPCBL2   DS    CL124                   LINE TWO BUFFER AREA
OPCBL3   DS    CL124                   LINE THREE BUFFER AREA
         DS    0F
OPCBECB  DS    F                       O/P ECB
         ORG   OPCBECB
OPCBDECB DS    CL20                    O/P DECB
         DS    0F
OPCBDCB  DS    F                        ADDRESS OF DCB ASSOCIATED WITH
*                                            SYSBFDMP DD CARD (I.E.
*                                            BSAM OUTPUT)
OPCBINBF DS    F                        ADDRESS OF 256 BYTE INPUT AREA
*                                            FOR BUFFER SECTIONS TO
*                                            BE READ INTO.
OPCBERSV DS    3F                      SAVE AREA FOR BSAM ERROR RTN
OPCBLEN  EQU   *-OPCB                   LENGTH OF OPCB
         EJECT
*                             INPUT BUFFER
INPUTBUF DSECT
         DC    CL256' '                 ONE BASIC BUFFER SECTION
INBUFLN  EQU   *-INPUTBUF               LENGTH OF BUFFER IN BYTES
*                                            INPUT BUFFER AREA
         EJECT
SAVEAREA DSECT         SAVE AREA
         SPACE 2
WD1      DS    F                        FIRST WORD
HSA      DS    F                        HEAD SAVE AREA POINTER
LSA      DS    F                        LAST SAVE AREA POINTER
RET      DS    F                        RETURN ADDRESS
EPA      DS    F                        ENTRY POINT ADDRESS
R0       DS    F                        REGISTER 0  CONTENTS
R1       DS    F                        REGISTER 1  CONTENTS
R2       DS    F                        REGISTER 2  CONTENTS
R3       DS    F                        REGISTER 3  CONTENTS
R4       DS    F                        REGISTER 4  CONTENTS
R5       DS    F                        REGISTER 5  CONTENTS
R6       DS    F                        REGISTER 6  CONTENTS
R7       DS    F                        REGISTER 7  CONTENTS
R8       DS    F                        REGISTER 8  CONTENTS
R9       DS    F                        REGISTER 9  CONTENTS
R10      DS    F                        REGISTER 10 CONTENTS
R11      DS    F                        REGISTER 11 CONTENTS
R12      DS    F                        REGISTER 12 CONTENTS
         EJECT
TIOT     DSECT         TASK INPUT / OUTPUT TABLE
         SPACE 2
TIOTNJOB DS    D                        JOB NAME
TIOTSTP  DS    D                        JOB STEP OR PROCEDURE STEP NAME
TIOTPRC  DS    D                        PROCEDURE JOB STEP NAME
TIOTHEAD EQU   *-TIOT                   LENGTH OF TIOT HEADER
         SPACE 2
TIOTDDEN DSECT                          DD ENTRY IN TIOT
         SPACE 2
TIOELNGH DS    X                        LENGTH OF DD ENTRY
TIOESTTA DS    X                        STATUS BYTE A
TIOENTCT DS    X                        NUMBER OF DEVICES REQUESTED
TIEOLINK DS    X                        ALLOCATION: LINE-CLOSE: FLAG
TIOEDDNM DS    CL8                      DD NAME
TIOEJFCB DS    3X                       RELATIVE ADDRESS OF JFCB
TIOESTIC DS    X                        STATUS BYTE C
TIOESTIB DS    X                        STATUS BYTE B
TIOEFSRT DS    3X                       UNIT CONTROL BLOCK ADDRESS
         EJECT
TCB      DSECT         TASK CONTROL BLOCK
         SPACE 2
TCBRBP   DS    F                        PROGRAM BLOCK POINTER
TCBDIE   DS    F                        PROGRAM INTERRUPT ELEMENT
TCBDEB   DS    F                        DATA EVENT BLOCK QUEUE
TCBTIO   DS    F                        TASK I/O TABLE
         ORG   TCB+124
TCBJSTCB DS    F                        ADDRESS OF JOB STEP TCB (MVT)
         EJECT
DEB      DSECT         DATA EVENT BLOCK
         DS    0F
         SPACE 2
DEBNMSUB DS    X                        NUMBER OF SUBROUTINES
DEBTCBAD DS    3X                       TASK CONTROL BLOCK ADDRESS
DEBAMLNG DS    X                        LENGTH OF ACCESS METHOD SECTION
DEBDEBAD DS    CL3                      NEXT DEB ON DEB QUEUE
         ORG   DEB+16
DEBNMEXT DS    X                        NUMBER OF EXTENTS
         ORG   DEB+25
DEBDCBAD DS    3X                       DATA CONTROL BLOCK ADDRESS
         ORG   DEB+33
DEBUCBAD DS    3X                       UNIT CONTROL BLOCK ADDRESS
         EJECT
DCB      DSECT
         DS    0F
         SPACE 2
         ORG   DCB+17
DCBDEVT  DS    X                       DEVICE TYPE
DCBDTAPE EQU   X'80'                   TAPE
DCBDDA   EQU   X'20'                   DIRECT ACCESS
DCBDTDA  EQU   DCBDTAPE+DCBDDA         TAPE OR DA
         ORG   DCB+40
DCBTIOT  DS    H                        OFFSET OF DD ENTRY IN TIOT
         ORG   DCB+45
DCBDEBAD DS    3X                       DATA EVENT BLOCK ADDRESS
         ORG   DCB+48
DCBOFLGS DS    X                        OPEN FLAGS
OPEN     EQU   X'10'                    SUCCESSFUL OPEN FOR THIS DCB
         EJECT
*                        DATA EVENT CONTROL BLOCK (GRAPHIC)
DECB     DSECT
         SPACE 2
DECBECB  DS    F                        EVENT CONTROL BLOCK
DECBTYPE DS    F                        TYPE OF I/O FIELD
DECBDCB  DS    F                        DATA CONTROL BLOCK ADDRESS
DECBAREA DS    F                        I/O BUFFER AREA ADDRESS
DECBHXCD DS    X                        HEX CODE ON I/O COMPLETNESS
DECBCNT  DS    3X                       CHANNEL STATUS WORD COUNT
DECBSTA  DS    X                        STATUS BIT A
DECBLNGH DS    3X                       LENGTH OF I/O DATA IN BYTES
DECBSTB  DS    X                        STATUS BIT B
DECBSTAD DS    3X                       ADDRESS OF START REGENERATION
*                                           ADDRESS
DECBUNIX DS    X                        UNIT INDEX
DECBBFAD DS    3X                       ADDRESS OF DATA TRANSFER TO OR
*                                            FROM BUFFER ADDRESS
         EJECT
SYSCVTPT EQU   16                       STORAGE LOCATION OF CVT ADDRESS
         SPACE 2
CVT      DSECT        COMMUNICATION VECTOR TABLE
         DS    0F
         SPACE 2
CVTTCBP  DS    F                        ADDRESS OF NEXT AND CURRENT
*                                            TCB POINTERS
         ORG   CVT+116
CVTDCB   DS    X                        SYSTEM CONFIGURATION
MVTSYS   EQU   X'10'                    SYSTEM IS MVT
CURRTCB  EQU   4                        DISPLACEMENT TO CURRENT TCB
*                                            POINTER IN NEW OLD TCB
*                                            WORDS
         EJECT
LINE     DSECT         OUTPUT LINE FORMAT
         SPACE 2
LNCNTCD  DS    X                        CONTROL CODE FOR PRINTER
*                                       MESSAGE NUMBER ONE FORMAT
         DS    2X
LNJOBNM  DC    C'JOB'
         DS    X
LNJOBNAM DC    CL8' '                   JOBNAME
         DS    3X
LNSTPNM  DC    C'STEP'
         DS    X
LNSTPNAM DC    CL8' '                   STEPNAME
         DS    3X
LNTMNM   DC    C'TIME'
LNTIME   DC    CL9' '                   TIME OF DAY
         DS    3X
LNDTNM   DC    C'DATE'
         DS    X
LNDATE   DC    CL6' '                   DATE
         DS    2X
LINEWORK DC    F'0'                     WORK AREA
         SPACE 2
*                                       MESSAGE NUMBER TWO FORMAT
         SPACE 2
         ORG   LINE+3
LNDPMSG  DC    C'DUMP OF'
         DS    X
LNDVAD   DC    C'XXX'                   UNIT ADDRESS
         SPACE 2
*                                       MESSAGE NUMBER THREE FORMAT
         SPACE 2
         ORG   LINE+3
LNBFNM   DC    C'BUFFER START REGENERATION ADDRESS'
         DS    X
LNSTRGAD DC    4C' '                    BUFFER RESTART ADDRESS
BUFADDLN EQU   *-LNSTRGAD               LENGTH OF BUFFER ADDRESS
*                                            IN BYTES
         SPACE 2
*                                       MESSACE NUMBER FOUR FORMAT
         SPACE 2
         ORG   LINE+1
LINEBFAD DC    4C' '                    BUFFER ADDRESS
         ORG   LINE+86
LINEFATK DC    C'*'                     FRONT ASTERISK ARROUND
LININTA  DC    CL32' '                  INTERPERTED BUFFER
LINTLNG  EQU   *-LININTA                LENGTH OF INTERPERT BUFFER
LINEBATK DS    C'*'                     FOLLOWING ASTERISK
         SPACE 2
*                                       MESSAGE LINE
         SPACE 2
         ORG   LINE+1
LINETEXT DC    CL123' '                LINE SIZE
LINELNG  EQU   *-LINETEXT               LENGTH OF OUTPUT LINE TEXT
         SPACE 2
*                                  PAGE COUNT
         SPACE 2
         ORG   LINE+113
LNPAGNM  DC    C'PAGE    '
         ORG   LNPAGNM+4
LNPAGNO  DC    C'    '                  PAGE COUNT
         SPACE 2
*                             LAST LINE ('END OD DUMP')
         SPACE 2
         ORG   LINE+1
LNEOD    DC    C'END OF 2250 BUFFER DUMP'
         EJECT
WTOMSG   DSECT                WRITE TO OPERATOR ERROR MESSAGE
         SPACE 2
WTOLNG   DS    X                        LENGTH OF MESSAGE
         DS    X
WTOID    DC    C'IFF000I'               MESSAGE ID
         DS    X
WTOUNTAD DC    C'XXX'                   UNIT ADDRESS
         DS    X
WTOTEXT  DC    64C' '                   MESSAGE TEXT
         END
