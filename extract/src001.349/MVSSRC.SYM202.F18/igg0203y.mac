     TITLE ' GRAPHICS CLOSE EXECUTOR '
***********************************************************************
*
* MODULE NAME:           IGG0203Y (OS/VS2)
*
* DESCRIPTIVE NAME:      GRAPHICS CLOSE EXECUTOR
*
* COPYRIGHT:             NONE
*
* STATUS:                RELEASE 2.0
*
* FUNCTION:              THIS MODULE PERFORMS ALL CLOSE FUNCTIONS FOR
*                        THE 2250S AND 2260S SUPPORTED BY GPS.
*
*                        THIS INCLUDES:
*                          1. VALIDITY CHECKS.
*                                A. UCB IS A GRAPHICS SUPPORTED DEVICE
*                                     1. 2250 )   ALL MODELS SUPPORTED
*                                     2. 2260 )))       AND
*                                     3. 1053 )   ALL LOCALLY ATTACHED
*                                B. CORRECT TASK IS REQUESTING CLOSE IF
*                                   DCB SHOWS BASIC ATTN HDLING IS USED
*                          2. DELETING SPECIAL ROUTINES NOT HANDLED BY
*                             SYSTEM CLOSE.
*                               A. IFFCAN03 FOR BASIC 2250 CANCEL KEY
*                          3. RESET OR RELEASE CONTROL BLOCKS
*                               A. DCB
*                               B. IOB (PURGE & FREE)
*                               C. UCB
*                               D. BASIC REBS & TEB FOR THIS DCB
*                               E. BASIC IRBS & IQES FOR:
*                                    1. IGG019OE
*                                    2. IFFCAN03
*                               F. EXPRESS USER POLL LIST
*                          4. SHUTDOWN I/O FOR 2250S
*                               A. SOUND ALARM TO NOTIFY 2250 OPERATOR
*                               B. HALT REGENERATION OF SCREEN
*                               C. TURN OFF PFK LIGHTS
*                               D. RELEASE 2250 BUFFER
*
******************* PROLOGUE*CONTINUED*NEXT*PAGE **********************
         EJECT
***********************************************************************
* NOTES:
*
*     DEPENDENCIES:      KEY 5 CONTROL IS RECEIVED FROM SYSTEM CLOSE.
*                        USER DCB ADDRESS, KEY AVAIL FROM SYS WORKAREA.
*                        SYSTEM NUCLEUS, KEY 0, IS NOT FETCH PROTECTED.
*                        IF FIRST UCB ON TIOT IS GRAPHIC TYPE,
*                           ALL FOLLOWING UCBS WILL BE GRAPHIC.
*
*     RESTRICTION:       FOR 2250S, ONE DEVICE PER DCB WHICH RESULTS
*                                   IN ONE UCB PER DEB.
*
*     RESISTERS:         SEE EQUATE DEFINITIONS FOLLOWING PROLOG
*
*     PATCH LABEL:       PATCH, A 50 BYTE DC AREA AT END OF MODULE
*
* MODULE TYPE:           EXECUTABLE CODE
*
*    PROCESSOR:          ASSEMBLER XF
*
*    MODULE SIZE:        SEE PAGE 1 OF THIS LISTING
*
*    ATTRIBUTES:
*       AT ENTRY:        REENTRANT  ENABLED  SUPERVISOR STATE IN KEY 5
*       ASSUMED KEYS:    0, USER (AS RECORDED IN SYSTEM CLOSE WORKAREA)
*
* ENTRY POINT:           IGG0203Y
*
*    PURPOSE:            SEE 'FUNCTION' HEADING ABOVE
*
*    LINKAGE:            SEE 'INPUT', NEXT HEADING
*
* INPUT:                 REGISTERS 5 THRU 8 AS FOLLOWS:
*                          5 ADDR OF: TOP OF DCB LIST BEING PROCESSED
*                          6          TOP OF WHERE-TO-GO TABLE
*                          7          CURRENT ENTRY OF DCB LIST
*                          8          CURRENT ENTRY OF WHERE-TO-GO TAB
*                            NOTE: 4 CONTAINS A VALID WORKAREA ADDR
*                               ONLY IF CONTROL RECEIVED FROM SYS CLOSE
*
* OUTPUT:                SEE 'FUNCTION' HEADING ABOVE
*
* EXITS:
*
*    NORMAL:             XCTL, USING WTG TABLE FOR MODULE ID
*
*    ERROR:              XCTL, USING PROBLEM DETERMINATION MACRO
*                          DMABCOND FOR MODULE ID
*
******************* PROLOGUE*CONTINUED*NEXT*PAGE **********************
         EJECT
***********************************************************************
*
* EXTERNAL REFERENCES:
*
*     ROUTINES:          EXCP     TO  SHUTDOWN 2250
*                        IGC0007A     RELEASE 2250 BUFFER
*                        IGC0007D     RELEASE REBS ON TEB FOR THIS DCB
*                        PURGE        STOP I/O PENDING
*
*     DATA AREAS:        SEE DSECT SECTION AT END OF LISTING
*
*     CONTROL BLOCKS:    SEE DSECT SECTION AT END OF LISTING
*
* TABLES:                TABLE OF TCB/ASCB ADDRESSES
*                        WHERE-TO-GO TABLE (SEE IECDSECS DSECT)
*
* MACROS:                ABEND   DEBCHK   DELETE   FREEMAIN   GETMAIN
*                        MODESET RLSEBFR  SETLOCK  WAIT       XCTL
*
* CHANGE ACTIVITY:       MODULE HAS BEEN RESTRUCTURED TO CONSOLIDATE
*                        THE TWO PREVIOUS CLOSE LOADS INTO A SINGLE
*                        LOAD MODULE.  CSECT FLAGS UPDATED WHERE
*                        APPROPIATE.
*
*                        FIXES DROPPED LIST:
*                         YA01037 OBSOLETE CLOSE MODULE, IGG0203X, DID
*                                 NOT RECOGNIZE 1053 DEVICE.
*                         SA54346 MODULES LOADED BY OPEN ARE DELETED BY
*                                 SYSTEM CLOSE USING DEBSUBID FIELD.
*                         SA42281 GRAPHICS 2280 DEVICES NOT SUPPORTED
*                                 IN VS SYSTEMS.
*                         SA37923 OBSOLETE CLOSE MODULE, IGG0203X, DID
*                                 NOT RECOGNIZE EXPRESS ATTN HANDLING.
*
***********************************************************************
         SPACE
IGG0203Y CSECT
*   SEE LABEL 'MODID' FOR LAST CHANGE DATE; FORMAT:  NAME.SYSREL.DATE
*D271100,A277000                                              LF YM7703
*A136000                                                      LF YM4062
*A102000-102500,154000-154500,157000                         LD YA01258
*C123500,124000                                               LB Y01021
*A135000,C343000-345000                                       LB  AOS/1
*C180000-181000                                               LC S21016
*A179000-179500                                              LI SA45407
*A155000-155500,157500,339000-340500                         LI SA42814
*C267000-269500                                              LI SA42542
*A125500,126000                                              LI SA37402
*C235500-236500,C239000                                      LG ZA00500
*C345000,A392100                                             LG @ZM2360
*A157600                                                    L5 @ZA03992
         EJECT
***********************************************************************
*                                                                     *
*                        REGISTER ASSIGNMENTS                         *
*                                                                     *
*               TEMPORARY ASSIGNMENTS IN LOW NUMBER REGS              *
*               PERMANENT ASSIGNMENTS IN HIGH NUMBER REGS             *
*               INPUT NAMES IDENTICAL TO THOSE IN SYSTEM OPEN         *
***********************************************************************
         SPACE
RE       EQU   0    TEMPORARY:         ENTRY POINT ADDRESSES
RSIZE    EQU   0                       GETMAIN SIZES
RADR     EQU   1                       ADDR WORK REG
RCALC    EQU   1                       CALCULATIONS
RCCW1    EQU   5                       *  CCWS FOR I/O SHUTDOWN  *
RCCW2    EQU   8                             *  HELD IN REGS  *
RCCW3    EQU   10                         *  DURING KEY CHANGE  *
RBACK    EQU   14                      RETURN ADDRESS FOR LINKAGES
RLINK    EQU   15                      BRANCH ADDRESS FOR LINKAGES
RCODE    EQU   15                      RETURN CODES FROM LINKAGES
         SPACE
RBASE    EQU   2    CONVENTIONS:       BASE REGISTER
RDCB     EQU   3                       DCB ADDRESS REGISTER
RGMTCB   EQU   4                       TCB   FOR GETMAIN
RASCB    EQU   7                       ASCB  FOR GETMAIN
         SPACE
RCTR     EQU   4    CALCULATIONS:      GENERAL COUNTER
RLOC     EQU   5                       LOCATION REG FOR INDEXING
RLAST    EQU   6                       ADDR OF LAST ENTRY IN POLL LIST
RNEXT    EQU   7                       ADDR OF NEXT ENTRY IN POLL LIST
REXT     EQU   8                       NUMBER OF UCB EXTENTS FROM DEB
RCOUNT   EQU   10                      LOOP COUNTER FOR NBR OF IQES
         SPACE
RCORE    EQU   4   INPUT ADDRESSES:    WORKAREA ONLY IF FROM SYS OPEN
RPAR     EQU   5                       PARAMETER LIST OF DCB ADDRS
RWTG     EQU   6                       WTG TABLE, EG. LABEL 'DXXWTG'
RPARC    EQU   7                       CURRENT PARAMETER LIST ENTRY
RWTGC    EQU   8                       CURRENT WGT TABLE ENTRY
         SPACE
RGACB    EQU   5   ADDRESSES:          GACB
RIRB     EQU   6                       IRB FOR IQES BEING RELEASED
RREB     EQU   6                       REB
RDEB     EQU   9                       DEB FOR DEVICES BEING CLOSED
RDECB    EQU   10                      DECB FOR I/O
RTCB     EQU   10                      CURRENT TCB FROM CVT TCB TABLE
RIOB     EQU   11                      IOB USED FOR SHUTDOWN I/O
RTEB     EQU   11                      TEB BEING DEACTIVATED
RUCB     EQU   12                      CURRENT UCB
RSAVE    EQU   13                      SAVE AREA FOR OPEN
RSCB1    EQU   14                      TEMP SYS CB ADDRESSES
RSCB2    EQU   15                      TEMP SYS CB ADDRESSES
        EJECT
***********************************************************************
*                                                                     *
*                        EQUATES                                      *
*                                                                     *
***********************************************************************
ACURASCB EQU   12     CURRENT ASCB ADDR LOC IN TCB TABLE
ACURTCB  EQU   4      CURRENT TCB ADDR LOC IN TCB TABLE
ADR      EQU   4
DCBADR   EQU   0      CURRENT DCB ADDR FROM INPUT PLIST
IOBSIZE  EQU   72     IOB BLOCK SIZE IN BYTES
THRTWO   EQU   32
TTROFF   EQU   14
WAOFF    EQU   32     OFFSET OF FIRST ENTRY OF WTG TAB
WGOFF    EQU   8      OFFSET OF WTG ENTRIES
         SPACE
ZERO     EQU   0      DECIMAL/HEX NUMBERS FOR GENERAL USE
ONE      EQU   1
TWO      EQU   2
THREE    EQU   3
FOUR     EQU   4
SIX      EQU   6
SEVEN    EQU   7
EIGHT    EQU   8
         SPACE
CCW24    EQU   24                  LENGTH OF HALT+PFK CCW    LD YA01258
CCW16    EQU   16                  LENGTH OF HALT CCW        LD YA01258
         SPACE
EXCP     EQU   0      SVC NUMBERS
DAR      EQU   74
         EJECT
***********************************************************************
*                                                                     *
*                        INITIALIZATION                               *
*                                                                     *
*         1. USING STATEMENTS IDENTIFY DSECT BASE REGISTERS           *
*         2. BASE ESTABLISHED FOR ADDRESSABILITY OF MODULE            *
*         3. MODULE EYECATCHER ID FOR RELEASE LEVEL VERIFICATION      *
*                                                                     *
***********************************************************************
         SPACE 2
         USING CVT,RSCB1         CVT DSECT INITZ FROM LOC 16
         USING FORCORE,RCORE     IECDSECS, KEY 0, REFERENCED ONLY
         USING IHADCB,RDCB       DCB DSECT; KEY 5: COPIED; KEY >7: USER
         USING DEBBASIC,RDEB     DEB DSECT, KEY 5, CREATED BY GAM OPEN
         USING SAVEAREA,RSAVE    SAVE/WORK AREA, KEY 5, FOR GAM CLOSE
         USING TCB,RTCB          STD DSECTS, KEY 0:  TCB
         USING TEB,RTEB                              TEB
         USING UCB,RUCB                              UCB
         SPACE 3
         BALR  RBASE,0                 SET UP CSECT BASE REGISTER
         USING *,RBASE                 DEFINE CSECT BASE REGISTER
         B     *+24
MODID    DC    C'IGG0203Y.VS2R2.75106'          MODULE EYECATCHER ID
         EJECT
***********************************************************************
*                                                                     *
*                        INITIALIZATION                               *
*                                                                     *
*         4. SAVE/WORK AREA IS USED TO PRESERVE REENTRANT STATUS.     *
*            INPUT REGS ARE SAVED NOW TO EXPAND NUMBER OF WORK REGS.  *
*                                                                     *
***********************************************************************
         L     RSIZE,SAVSPSIZ     GET SAVE/WORK AREA FOR GRAPHIC OPEN
         GETMAIN R,LV=(0)
         LR    RSAVE,RADR
CLOS0010 EQU   *
         STM   RPAR,RWTGC,R5         SAVE INPUT/UPDATED REGS
         L     RDCB,ZERO(RPARC)      LOAD: COPIED DCB ADDRESS
         L     RCORE,ADR(RWTGC)            WORK AREA FOR THIS DCB
         L     RSCB1,DXUDCBAD              USER'S DCB ADDRESS
         LA    RSCB1,ZERO(RSCB1)              CLEAR HIGH BYTE
         ST    RCORE,R4                       KEEP AVAIL FOR USE
         ST    RSCB1,DECBDCB                  KEEP AVAIL FOR USE
         EJECT
***********************************************************************
*                                                                     *
*                          VALIDITY CHECKS                            *
*                                                                     *
*         1.  VALID DEVICE TYPE                                       *
*               A.  ASSUME ALL UCBS OK IF FIRST OK                    *
*                                                                     *
***********************************************************************
         DEBCHK (RSCB1),TYPE=VERIFY,AM=GAM    VALIDATE DEB    LB Y01021
         LR    RDEB,RADR             INITIALIZE DEB BASE      LB Y01021
         LA    RDEB,ZERO(RDEB)         CLEAR HIGH ORDER BYTE
         ST    RDEB,R9                 KEEP AVAIL FOR USE
         L     RUCB,DEBSUCBA         GET FIRST UCB ADR
         CLI   UCBTBYT3,UCB3DISP     IS DEVICE GRAPHICS TYPE  LF A37402
         BNE   CLOS0240                IF NO, SKIP PROCESSING LI A37402
         CLI   UCBTBYT4,UCBT1053     IS DEVICE 1053 TYPE
         BE    CLOS0020                IF YES,  OK TO PROCESS
         CLI   UCBTBYT4,UCBT2260     IS DEVICE 2260 TYPE
         BE    CLOS0020                IF YES,  OK TO PROCESS
         CLI   UCBTBYT4,UCBT2250     IS DEVICE 2250 TYPE
         BNE   CLOS0240                IF NO, SKIP ALL PROCESSING
         EJECT
***********************************************************************
*                                                                     *
*               COMMON PROCESSING FOR EVERY CLOSE ENTRANCE            *
*                                                                     *
*        1.  DELETE SPECIAL ROUTINES LOADED BY OPEN (IGG0193Y)        *
*            AND NOT DELETED BY SYSTEM CLOSE USING DEBSUBID           *
*               A.  IFFCAN03 LOADED FOR 2250 BASIC ATTN HANDLING UCBS *
*        2.  DECREMENT DCBS OPEN COUNT IN UCB (KEY 0)                 *
*                                                                     *
***********************************************************************
         TM    DCBGTYPE,DCBGTBAS   IS THIS A BASIC SYSTEM?
         BNO   CLOS0020              BRANCH IF EXPRESS
         DELETE EP=IFFCAN03                                       AOS/1
CLOS0020 EQU   *
         MODESET EXTKEY=ZERO                                  LF YM4062
         SR    RCTR,RCTR           CLEAR REG FOR UCB CT INSERT
         SR    REXT,REXT                     FOR NBR OF UCBS
         SR    RLOC,RLOC                     FOR INDEXING DEBUCBAD
         IC    REXT,DEBNMEXT                 GET NBR OF UCBS
CLOS0030 EQU   *
         L     RUCB,DEBSUCBA(RLOC) LOAD FIRST/NEXT UCB ADDRESS
         IC    RCTR,UCBOPEN          GET DCB OPEN COUNT
         LTR   RCTR,RCTR                    IS OPEN COUNT ALREADY ZERO
         BZ    CLOS0040                       IF YES, GET NEXT UCB
         BCTR  RCTR,0                         IF NO, REDUCE BY ONE
         STC   RCTR,UCBOPEN          REPLACE NEW USECT IN UCB
         LTR   RCTR,RCTR           IS USE CT NOW ZERO
         BP    CLOS0040               IF NO, SKIP BIT RESETS
         NI    UCBGCB,UCBGSET         IF YES, RESET TO IGNORE   SA32068
CLOS0040 LA    RLOC,FOUR(RLOC)           INCREMENT INDEX
         BCT   REXT,CLOS0030             BRANCH IF MORE UCBS TO SERVICE
         CLI   UCBTBYT4,UCBT2250      IS THIS A 2250
         BNE   CLOS0090                IF NO, SKIP 2250 I/O
         CLI   UCBOPEN,ZERO            IS 2250 STILL BEING USED
         BNE   CLOS0090                 IF YES, SKIP 2250 SHUTDOWN I/O
         EJECT
***********************************************************************
*                                                                     *
*                        2250 I/0 SHUTDOWN                            *
*                                                                     *
*          1.    SOUND ALARM TO NOTIFY 2250 OPERATOR OF SHUTDOWN      *
*          2.    HALT REGENERATION OF SCREEN                          *
*          3.    TURN OFF PFK LIGHTS IF FEATURE INSTALLED             *
*                                                                     *
***********************************************************************
         L     RCORE,R4           GET CURRENT WORK AREA ADDR
         LM    RCCW1,RCCW3,HLTCCW
         L     RIOB,DCBIOBAD      IOB ADDR FROM COPY DCB
         MODESET  KEYADDR=DXUKEY,WORKREG=15
         USING DXIOB,RIOB         TEMP BASE FOR IOB I/O
CLOS0050 TS    IOBAVAIL           CHECK IF IOB AVAILABLE FOR USE
         BZ    CLOS0060            IF YES, USE FOR SHUTDOWN I/O
         LR    RSCB1,RIOB          IF NO, SAVE & USE IF NO OTHER AVAIL
         L     RIOB,IOBNXTPT              CHECK IF ANOTHER IOB ON CHAIN
         LA    RIOB,ZERO(RIOB)                  CLEAR HIGH BYTE
         LTR   RIOB,RIOB                        CHECK FOR ADDR
         BNZ   CLOS0050                     IF YES, CHECK IF AVAILABLE
         LR    RIOB,RSCB1                   IF NO, USE LAST IOB
CLOS0060 LA    RSCB1,IOBCCW4
         ST    RSCB1,IOBECBPT      INITZ IOB WITH ECB ADDR       SM2858
         TM    UCBTBYT2,UCBTPFK   IS PFK FEATURE INSTALLED   LD YA01258
         BZ    CLOS0070             IF NO, SKIP PFK I/O      LD YA01258
         STM   RCCW1,RCCW3,IOBCCW1      CCWS INTO IOB           SA42814
         LA    RLOC,ZEROS           LOAD ADDRESS OF ZEROS       SA42814
         STCM  RLOC,SEVEN,IOBCCW3+1 PUT MASK ADR IN CCW         SA42814
         B     CLOS0080                 ISSUE EXCP FOR I/O
CLOS0070 STM   RCCW1,RCCW2,IOBCCW1      CCWS INTO IOB           SA42814
         NI    IOBCCW1+12,X'9F'    TURN OFF CMD CHAIN       L5 @ZA03992
CLOS0080 LR    RADR,RIOB                PUT IOB ADDR IN REG 1 &
         SVC   EXCP                     ISSUE EXCP
         WAIT  ECB=IOBCCW4              WAIT ON I/O              SM2858
         DROP  RIOB
         EJECT
***********************************************************************
*                                                                     *
*                        2250 I/0 SHUTDOWN                            *
*                                                                     *
*          4.    RELEASE BUFFER IF FEATURE INSTALLED                  *
*                                                                     *
***********************************************************************
         MODESET EXTKEY=DATAMGT
         CLC   UCBBTB(THREE),ZEROS DOES BUFFER TABLE EXIST
         BE    CLOS0090                 IF NO, SKIP BFR RELEASE
         L     RSCB1,DXUDCBAD      GET USER DCB
         LA    RDECB,DECBECB
         RLSEBFR    (RSCB1),ALL,MF=(E,(RDECB))   RELEASE BUFFER  SM2858
         EJECT
***********************************************************************
*                                                                     *
*                   ATTENTION HANDLING SHUTDOWN                       *
*                                                                     *
*         1.  DETERMINE TYPE OF ATTENTION HANDLING BEING USED         *
*               A.  IF BASIC, RELEASE CONTROL BLOCKS                  *
*               B.  IF EXPRESS, RELEASE/COMPRESS POLLING LIST         *
*                                                                     *
***********************************************************************
CLOS0090 EQU   *
         MODESET EXTKEY=ZERO
         L     RDEB,R9              GET DEB FOR LATER USE
         L     RSCB1,CVTPTR         GET CVT ADDR FROM FIXED LOCATION
         L     RTCB,CVTTCBP         GET ADDRS OF: TCB TABLE
         L     RSCB2,ACURASCB(RTCB)               ASCB
         ST    RSCB2,ASCBADR                        KEEP FOR BR ENTRIES
         L     RTCB,ACURTCB(RTCB)                 CURRENT TCB
         TM    DCBGTYPE,DCBGTBAS    IS THIS A BASIC SYSTEM?
         BNO   EAH0280                IF NO, DO EXPRESS PROCESSING
         EJECT
***********************************************************************
*                                                                     *
*                   BASIC ATTENTION HANDLING SHUTDOWN                 *
*                                                                     *
*        1.  VALIDATE CORRECT TASK IS REQUESTING CLOSE                *
*              A.  IF INCORRECT AND NOT ABENDING, ISSUE ABEND         *
*                                                                     *
***********************************************************************
         USING TEB,RTEB
         L     RTEB,UCBTEB       DOES TEB EXIST
         LTR   RTEB,RTEB           IF NO, SKIP BASIC PROCESSING &
         BZ    CLOS0210                   CK IF OK TO PURGE IOBS
         L     RSCB1,TEBTCB        IF YES, TEB TCB AND CUR TCB
         CR    RSCB1,RTCB                  MUST BE EQUAL OR ABENDING
         BE    BAH0100                 IF EQUAL, OK TO PROCESS
         TM    TCBFLGS1,TCBFA    IS ABEND IN PROGRESS        LI SA45407
         BO    CLOS0240            IF YES, DONE PROCESSING   LI SA45407
         LA    RADR,X'D14'              LOAD ABEND CODE       LC S21016
         SLL   RADR,12                  SHIFT CODE            LC S21016
         ABEND (1),DUMP                 ABEND TASK            LC S21016
         EJECT
***********************************************************************
*                                                                     *
*                   BASIC ATTENTION HANDLING SHUTDOWN                 *
*                                                                     *
*            2.    DAR ANY REBS ASSOCIATED WITH THE DCB               *
*                                                                     *
***********************************************************************
BAH0100  EQU   *
         USING GACB,RGACB
         USING REB,RREB
         L     RREB,TEBREB            LOAD FIRST REB
         LTR   RREB,RREB              ANY REBS ON THIS TEB
         BE    BAH0130                  IF NO, SKIP DAR PROCESSING
BAH0110  EQU   *
         L     RGACB,REBGACB          LOAD GACB ADDRESS
         L     RREB,REBL              LOAD NEXT REB ADDR
         L     RSCB1,DECBDCB          GET USER DCB
         L     RSCB2,GACBDCB          GET DCB ADR
         LA    RSCB2,ZERO(RSCB2)        CLEAR HIGH ORDER BYTE
         L     RCORE,R4                 GET WORK AREA
         CLR   RSCB1,RSCB2            DO  DCB ADRS MATCH
         BNE   BAH0120                   IF NO, SKIP DAR & CK NEXT REB
         MODESET  KEYADDR=DXUKEY,WORKREG=15
         OI    GACBFLGS+1,GACBCLOS     SET BIT FOR DAR TO CLOSE
         MODESET EXTKEY=ZERO
         LA    RADR,DARPLGH            GET ADDR OF DAR'S PLIST
         LA    RE,TWO                  LOAD LIST LENGTH
         ST    RGACB,DARGACB            STORE GACB ADR IN PARM LIST
         ST    RE,DARPLGH               STORE LENGTH IN PARM LIST
         SVC   DAR                     ISSUE SVC FOR DAR (IGC0007D)
BAH0120  EQU   *
         LTR   RREB,RREB               ARE THERE MORE REBS
         BNZ   BAH0110                    IF YES, REPEAT PROCESSING
         DROP  RGACB
         DROP  RREB
         EJECT
***********************************************************************
*                                                                     *
*                   BASIC ATTENTION HANDLING SHUTDOWN                 *
*                                                                     *
*          3.     FREE IQES ON IRB FOR CANCEL KEY                     *
*                                                                     *
***********************************************************************
         USING RBBASIC,RIRB
BAH0130  EQU   *
         CLI   UCBOPEN,ZERO      CHECK DCB OPEN COUNT
         BNE   CLOS0210            IF NO, SKIP FREEMAINS
         SR    REXT,REXT           IF YES, OK TO FREE IQES, ETC
         IC    REXT,DEBNMEXT         GET NBR OF UCBS FROM DEB
         LA    RLOC,DEBSUCBA         GET PTR TO FIRST UCB
         LR    RGMTCB,RTCB            CURRENT TCB ADDR
         L     RASCB,ASCBADR          CURRENT ASCB ADDR
LL1ON    SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,RELATED=(LOCAL,IGG0203Y(X
               LL1OFF)),REGS=SAVE
BAH0140  EQU   *
         L     RUCB,ZERO(RLOC)        LOAD UCB ADDRESS
         LA    RUCB,ZERO(RUCB)        CLEAR HIGH ORDER BYTE
         CLI   UCBTBYT4,UCBT2250      IS DEVICE A 2250
         BNE   BAH0150                 IF NO, ONLY ONE IQE TO FREE
         LA    RCOUNT,THREE            IF YES, THREE IQES TO FREE
         L     RIRB,TEBCKRB         LOAD CANCEL KEY IRB FROM TE
         L     RADR,RBNEXAV              LOAD CANCEL KEY IQE ADDRESS
         LTR   RADR,RADR          DOES CKQE EXIST
         BZ    BAH0160                 IF NO, CHECK GAR'S IRB
         MVC   RBNEXAV(FOUR),ZERO(RADR)       UPDATE CKIQE FLD
         L     RE,IQESIZE              LOAD IQE BYTE REQUEST
         FREEMAIN  R,LV=(0),A=(1),BRANCH=YES      FREE CANCEL KEY IQE
         B     BAH0160                 NOW CHECK GAR'S IRB
         EJECT
***********************************************************************
*                                                                     *
*                   BASIC ATTENTION HANDLING SHUTDOWN                 *
*                                                                     *
*       4.   FREE IQES ON IRB FOR GAR                                 *
*           A. IF 2250S:  ONE IQE FOR CANCEL KEY'S IRB                *
*                         THREE IQES FOR GAR'S IRB (THREE ATTN TYPES) *
*           B. IF 2260S:  ONE IQE FOR GAR'S IRB (ONE ATTN TYPE)       *
*                                                                     *
***********************************************************************
BAH0150  EQU   *
         LA    RCOUNT,ONE                  LOAD 2260 COUNT
BAH0160  EQU   *
         L     RIRB,TEBGARB         LOAD GAR'S IRB ADDRESS FROM TEB
BAH0170  EQU   *
         L     RADR,RBNEXAV         LOAD CANCEL KEY IQE ADDRESS
         LTR   RADR,RADR                  ANY MORE IQES
         BZ    BAH0180                 NO
         MVC   RBNEXAV(FOUR),ZERO(RADR)    UPDATE CKIQE FLD
         L     RE,IQESIZE              LOAD IQE BYTE REQUEST
         FREEMAIN R,LV=(0),A=(1),BRANCH=YES      FREE IQE
         BCT   RCOUNT,BAH0170              BRANCH IF MORE IQES TO FREE
         EJECT
***********************************************************************
*                                                                     *
*                   BASIC ATTENTION HANDLING SHUTDOWN                 *
*                                                                     *
*          5.     CLEAR UCBTEB FIELD                                  *
*          6.     TURN OFF UCBGCB BITS, GINIT AND GBAS                *
*          7.     DECREMENT TEB UCB USE COUNT                         *
*                   A.  IF TEB UCB USE COUNT IS ZERO                  *
*                         1).  FREE IRBS                              *
*                         2).  FREE TEB                               *
*                                                                     *
***********************************************************************
BAH0180  EQU   *
         XC    UCBTEB(FOUR),UCBTEB    CLEAR UCBTE FIELD
         NI    UCBGCB,UCBGSET         RESET TO IGNORE ATTNS     SA32068
         L     RCOUNT,TEBUCBCT        LOAD TEB UCB COUNT     LG ZA00500
         BCTR  RCOUNT,ZERO            DECREMENT USECNT (TE)  LG ZA00500
         ST    RCOUNT,TEBUCBCT        RESTORE USECNT         LG ZA00500
         LA    RLOC,FOUR(RLOC)             INCREMENT UCB PTR ADR
         BCT   REXT,BAH0140              BRANCH IF MORE UCBS
LL1OFF   SETLOCK RELEASE,TYPE=LOCAL,RELATED=(LOCAL,IGG0203Y(LL1ON)),   X
               REGS=SAVE
         LTR   RCOUNT,RCOUNT             IS TEB STILL IN USE LG ZA00500
         BNE   CLOS0210                  IF YES, DO ABEND CK & PURGE
         EJECT
***********************************************************************
*                                                                     *
*                   BASIC ATTENTION HANDLING SHUTDOWN                 *
*                                                                     *
*       8.   FREE IRBS (INCLUDING THEIR PP SAVE AREA) AND TEB         *
*                                                                     *
*     NOTE: IRBS THAT ARE ACTIVE CAN NOT BE FREED; THE IRB ON  SM4120 *
*           THE TASK RB CHAIN IS LEFT FOR ABEND TO FREE.       SM4120 *
*                                                                     *
***********************************************************************
         L     RIRB,TEBCKRB          IS THERE A CANCEL KEY IRB   SM4120
         LTR   RIRB,RIRB                                         SM4120
         BZ    BAH0190                  IF NO,CK FOR A GAR IRB   SM4120
         TM    RBSTAB2,RBFACTV       IS THIS IRB ACTIVE ?        SM4120
         BO    BAH0190                  IF YES, DO NOT REMOVE    SM4120
         L     RSIZE,REQUEST2           LOAD PPSVAREA REQUEST    SM4120
         L     RADR,ZERO(RIRB)          LOAD PPSVAREA ADDRESS    SM4120
         FREEMAIN   R,LV=(0),A=(1)      FREE PROB PROG SAVE AREA SM4120
         L     RSIZE,IRBSIZE              LOAD IRB BYTE REQUEST
         LR    RADR,RIRB                  LOAD IRB ADDRESS TO PARM REG
         FREEMAIN  R,LV=(0),A=(1)      FREE CANCEL KEY IRB
BAH0190  L     RIRB,TEBGARB     IS THERE A GAR IRB               SM4120
         LTR   RIRB,RIRB          IF NO, CK FOR A TEB            SM4120
         BZ    BAH0200                                           SM4120
         TM    RBSTAB2,RBFACTV     IS THIS IRB ACTIVE ?          SM4120
         BO    BAH0200                  IF YES, DO NOT REMOVE    SM4120
         L     RSIZE,REQUEST2           LOAD PPSVAREA REQUEST    SM4120
         L     RADR,ZERO(RIRB)         LOAD PPSVAREA ADDRESS FROM IRB
         FREEMAIN  R,LV=(0),A=(1)      FREE PROB PROG SAVE AREA
         L     RSIZE,IRBSIZE              LOAD IRB BYTE REQUEST
         LR    RADR,RIRB                  LOAD IRB ADDRESS TO PARM REG
         FREEMAIN  R,LV=(0),A=(1)      FREE GAR IRB
BAH0200  L     RSIZE,TEBSIZE            LOAD TE BYTE REQUEST      M4120
         LR    RADR,RTEB                 LOAD TE ADDRESS IN PARM REG
         FREEMAIN  R,LV=(0),A=(1)          FREE TE
         EJECT
***********************************************************************
*                                                                     *
*                COMMON PROCESSING IF NOT ABENDING                    *
*                                                                     *
*     1.  PURGE, THEN FREEMAIN, ALL IOBS                              *
*                                                                     *
***********************************************************************
CLOS0210 EQU   *
         LR    RTCB,RGMTCB
         TM    TCBFLGS1,TCBFA ABEND IN PROGRESS
         BO    CLOS0240
CLOS0220 EQU   *
         MODESET  EXTKEY=DATAMGT
         L     RCORE,R4      GET ADDR OF WORK AREA
         ST    RDEB,DXCCW6         STORE DEB ADDRESS         LI SA42542
         MVI   DXCCW6,X'A0'        PURGE OPTION              LI SA42542
*      PURGE OPTIONS: SPECIFIED ONLY; DON'T POST; HIO; RELATED ONLY
         XC    DXCCW6+4(EIGHT),DXCCW6+4 CLEAR FIELDS         LI SA42542
         LA    RADR,DEBNMEXT    GET IOB PURGE CHAINING ADDRESS FROM DEB
         ST    RADR,DXCCW6+8       FOR PURGE TO CHAIN IOB    LI SA42542
         LA    RADR,DXCCW6         GET ADDRESS OF LIST
         SVC   16
         WAIT  ECB=DXCCW6+4        WAIT ON PURGE ECB
         SR    RCTR,RCTR                 ZERO REGISTER
         IC    RCTR,DCBGNCP              LOAD NO.OF IOBS
         CLI   DCBGNCP,DCBGNCPH          IS GNCP EQUAL TO MAXIMUM
         BE    CLOS0230                     IF YES, DO NOT ADJUST CTR
         LA    RCTR,ONE(RCTR)                       FOR SPARE IOB
CLOS0230 EQU   *
         LA    RCALC,IOBSIZE              LOAD IOB BLOCK SIZE
         MR    RSIZE,RCTR                  CALC SIZE TO RELEASE
         LA    RSIZE,EIGHT(RCALC)             ADD FINAL SIZE FACTOR
         O     RSIZE,IOBSPOOL             INDICATE IOB POOL
         L     RADR,DCBIOBAD             LOAD IOB ADDRESS
         S     RADR,IOBECB                                    LF YM7703
         FREEMAIN R,LV=(0),A=(1)
         EJECT
***********************************************************************
*                                                                     *
*                COMMON PROCESSING IF NOT ABENDING                    *
*                                                                     *
*     2.  RESTORE DCB FIELDS TO PRE-OPEN STATUS; I. E., ZERO FIELDS:  *
*          A.  BUFFER RELATED FIELDS:  STARTING ADDRESSES AND SIZE    *
*          B.  IOB ADDRESS                                            *
*                                                                     *
***********************************************************************
         SR    RADR,RADR             INITIALIZE FIELDS TO ZEROS:
         STH   RADR,DCBBRSA              BUFFER RESTART ADDRESS
         ST    RADR,DCBBFRST             BUFFER STARTING ADDR & SIZE
         ST    RADR,DCBIOBAD             IOB CHAINING ADDRESS
         EJECT
***********************************************************************
*                                                                     *
*               CLOSE CHECK FOR ADDITIONAL DCBS TO PROCESS            *
*                                                                     *
***********************************************************************
CLOS0240 EQU   *
         MODESET  EXTKEY=DATAMGT
         LM    RCORE,RWTGC,R4        RESTORE INPUT REGS
         XC    ZERO(TWO,RWTGC),ZERO(RWTGC)   CLEAR ID IND COMPLETION
CLOS0250 EQU   *
         LA    RWTGC,WGOFF(RWTGC)      STEP TO NEXT ENTRY
         LA    RPARC,ADR(RPARC)        STEP TO NEXT ENTRY
         CLC   ZERO(TWO,RWTGC),GIDCNST1 CHECK FOR ID MATCH
         BE    CLOS0010                BRANCH IF HIT
         CLC   ZERO(TWO,RWTGC),CLLDB    CHECK FOR ID MATCH
         BL    CLOS0250                BRANCH IF NO HIT
         CLC   ZERO(TWO,RWTGC),CLLDG    CHECK FOR ID MATCH
         BH    CLOS0250                BACK TO LOOP IF HIGH
         LR    RPARC,RPAR              INITIALIZE REGISTER
         LA    RWTGC,WAOFF(RWTG)     INITIALIZE REGISTER
CLOS0260 EQU   *
         CLI   ZERO(RWTGC),ZERO      IS THIS ENTRY ZERO?
         BNE   CLOS0270                BRANCH IF NO
         LA    RWTGC,WGOFF(RWTGC)    INCREMENT POINTER
         LA    RPARC,ADR(RPARC)    INCREMENT POINTER
         B     CLOS0260                BRANCH UNCONDITIONALLY
CLOS0270 EQU   *
         LR    RADR,RSAVE         PRIME ADDR REG
         L     RSIZE,SAVSPSIZ     GET SAVE/WORK AREA DESCRIPTION
         FREEMAIN R,LV=(0),A=(1)
         MVC   SIX(TWO,RWTG),ZERO(RWTGC)  SET UP ID FOR XCTL
         MVC   TTROFF(THREE,RWTG),TWO(RWTGC)    ID TO TTR
         LA    RLINK,DXCCW12              POINT TO XCTL PARAMETER
         XCTL  DE=(RWTG),SF=(E,(RLINK))
         EJECT
***********************************************************************
*                                                                     *
*                  EXPRESS ATTENTION HANDLING SHUTDOWN                *
*                                                                     *
*   ABEND CHECK: SKIP POLST PROCESSING IF TASK ABENDING SINCE         *
*                POLST IS NO LONGER FUNCTIONAL                        *
*                                                                     *
*       1.     FREE/COMPRESS POLLING LIST (USER KEY)                  *
*                                                                     *
***********************************************************************
EAH0280  TM    TCBFLGS1,TCBFA      IS ABEND IN PROGRESS
         BO    CLOS0240               IF YES, NO PROCESSING REQUIRED
         L     RADR,DCBPOLST   GET POLL LIST ADDR FROM COPY DCB
         LA    RADR,ZERO(RADR)    CLEAR HIGH ORDER BYTE
         LTR   RADR,RADR           DOES A LIST EXIST
         BZ    CLOS0220              IF NO, CK FOR IOB PURGE
         CLI   ZERO(RADR),ZERO     IF YES, CK IF LIST IS ACTIVE
         BE    CLOS0220                IF NO, SKIP PROCESSING
         L     RLAST,ZERO(RADR)       IF YES, GET LAST ENTRY ADDR
         LA    RLAST,ZERO(RLAST)        CLEAR FLAG BYTE
         LA    RLOC,FOUR(RADR)           GET LOCATION FIRST ENTRY
         L     RCORE,R4                  GET WORK AREA
         L     RSCB1,DECBDCB             GET USER DCB FOR CHECK
         MODESET  KEYADDR=DXUKEY,WORKREG=7
EAH0290  EQU   *
         L     RSCB2,ZERO(RLOC)         GET DCB ADDR FROM ENTRY
         LA    RSCB2,ZERO(RSCB2)        CLEAR INDEX BYTE
         CLR   RSCB1,RSCB2               CLOSING DCB MATCH THIS ENTRY
         BE    EAH0300                     IF YES, REMOVE ENTRY
         CLR   RLOC,RLAST            IS THIS END OF LIST
         BE    CLOS0220                IF YES, PROCESSING DONE
         LA    RLOC,FOUR(RLOC)         IF NO, GET ADDR NEXT ENTRY
         B     EAH0290                        AND REPEAT PROCESSING
         EJECT
***********************************************************************
*                                                                     *
*                  EXPRESS ATTENTION HANDLING SHUTDOWN                *
*                                                                     *
*         1.    DCB MATCH FOUND (CLOSING DCB & POLL LIST ENTRY)       *
*                                                                     *
*                    RLOC  HAS ENTRY ADDR WITH MATCH                  *
*                    RLAST     ADDR OF LAST ENTRY IN POLL LIST        *
*                    RSCB2     ENTRY CONTENTS; I.E., DCB ADDR         *
*                                                                     *
***********************************************************************
EAH0300  EQU   *
         SR    RE,RE                 GET SOURCE OF ZEROS
         LA    RNEXT,FOUR(RLOC)      GET NEXT ENTRY LOCATION
EAH0310  EQU   *
         CLR   RNEXT,RLAST           IS NEXT ENTRY LOC INSIDE LIST
         BH    EAH0330                 IF NO, REMOVE LAST ENTRY
         L     RSCB2,ZERO(RNEXT)       IF YES, GET NEXT ENTRY
         LA    RSCB2,ZERO(RSCB2)         CLEAR INDEX BYTE
         CLR   RSCB1,RSCB2               CLOSING DCB MATCH NEXT ENTRY
         BE    EAH0320                     IF YES, REMOVE ENTRY
         MVC   ZERO(FOUR,RLOC),ZERO(RNEXT)    IF NO, COMPRESS ENTRY
         LA    RLOC,FOUR(RLOC)        ADJUST LOCATION TO NEXT ENTRY
EAH0320  EQU   *
         LA    RNEXT,FOUR(RNEXT)      GET NEW NEXT ENTRY LOCATION
         B     EAH0310                 AND REPEAT PROCESSING
*      DCB MATCH FOUND
EAH0330  EQU   *
         LR    RNEXT,RLOC                GET PREVIOUS ENTRY LOCATION
EAH0340  EQU   *
         ST    RE,ZERO(RNEXT)            REMOVE ENTRY FROM LIST
         LA    RNEXT,ADR(RNEXT)          ADJUST POINTER TO NEXT ENTRY
         CLR   RNEXT,RLAST           IS NEXT ENTRY INSIDE LIST
         BNH   EAH0340                 IF YES, CONTINUE TO PROCESS
         S     RLOC,ENTRY              IF NO, BACK UP TO LAST ENTRY
         CLR   RADR,RLOC             ANY ENTRIES IN POLL LIST
         BE    EAH0350                 IF NO, RESET ACTIVE FLAG
         STCM  RLOC,SEVEN,ONE(RADR)    IF YES, INSERT NEW EOL PTR
         B     CLOS0220              CHECK FOR IOBS TO PURGE
EAH0350  EQU   *
         STC   RE,ZERO(RADR)        RESET TO INACTIVE LIST (NO ENTRIES)
         B     CLOS0220             CHECK FOR IOBS TO PURGE I/O
         EJECT
***********************************************************************
*                                                                     *
*                      CONSTANTS                                      *
*                                                                     *
***********************************************************************
ZEROS    DC    F'0'                MASK TO TURN OFF PFK LIGHTS  SA42814
HLTCCW   DC    X'0700000040000002'
         DC    X'0B00000060000002'                              SA42814
PFKCCW   DC    X'1B00000020000004'  CCW TO TURN OFF PFK'S       SA42814
IOBSPOOL DC    X'FA000000'             SP 250 FOR IOB
IQESIZE  DC    X'E9000010'             SP 233     16 BYTE IQE     AOS/1
IRBSIZE  DC    X'E9000068'             SP 233     104 BYTE IRB    AOS/1
TEBSIZE  DC    X'EB000024'             SP 235    36 BYTE TEB LG @ZM2360
REQUEST2 DC    X'FA000048'             SP 250     72 BYTE PP SAVE AREA
SAVSPSIZ DC    X'E6000078'             SP 230     120 BYTE WORK AREA
ENTRY    DC    F'4'                    POLL LIST ENTRY SIZE IN BYTES
IOBECB   DC    F'8'                    IOB ECB SIZE IN BYTES
GIDCNST1 DC    C'3Y'
CLLDB    DC    C'0B'
CLLDG    DC    C'0G'
         SPACE 5
**********************************************************************
*                                                                    *
*                SPECIAL MAINTENANCE PATCH AREA FOR FIELD USE        *
*                                                                    *
**********************************************************************
PATCH    DC    C'IGG0203Y 50 BYTE PATCH AREA.'
         DC    C'50 BYTE AREA ENDS HERE'
         EJECT
***********************************************************************
*                                                                     *
*              DSECTS                                                 *
*                                                                     *
***********************************************************************
         CVT   DSECT=YES
         EJECT
         DCBD  DSORG=GS
**********************************************************************
*                                                                     *
*                    ADDITIONAL DCB EQUATES FOR DSECT                 *
*                                                                     *
***********************************************************************
DCBGNCPL EQU   X'00'   LOW END OF RANGE FOR CHANNEL PROGRAMS IS 1
DCBGNCPH EQU   X'63'   HIGH END OF RANGE FOR CHANNEL PROGRAMS IS 99
         EJECT
         IEZDEB
         IECDSECS MAIN,EXPAND=YES
*
*        ADDITIONAL IOB LABELS FOR GAM EXTENSION
*
         ORG   DXIOB
IOBDX    DS    8F          USE WORK AREA LABELS FOR STD FIELDS
*       GAM EXTENSION
IOBUCBX  DS    0CL1               UCB INDEX
IOBADRPT DS    F                  ADDRESSING LIST POINTER
IOBAVAIL DS    0CL1    BIT 0:  ON IF IOB NOT AVAILABLE FOR USE
IOBNXTPT DS    F       NEXT IOB POINTER
IOBCCW1  DS    2F      MAX NBR OF CCWS FOR A GRAPHIC CHANNEL PGM IS 4
IOBCCW2  DS    2F
IOBCCW3  DS    2F
IOBCCW4  DS    2F      * USED AS ECB FOR SHUTDOWN I/O *
         EJECT
GACB     DSECT
GACBCOM  DS    F        ADDR OF:     USER COMMUNICATION AREA FOR ATNS
GACBDCB  DS    F                     DCB FOR DEVICE WITH ATTENTIONS
GACBPFK  DS    F        MASKS:       PFK ATTENTION SOURCES PERMITTED
GACBATYP DS    F                     ATTENTION TYPES PERMITTED
GACBEP1  DS    F        ADDR OF:     ENTRY POINT FOR USER'S ATN RTN
GACBEP2  DS    F                     ENTRY POINT FOR ATNQ, MODE=R
GACBSA   DS    F                     SAVE AREA FOR ATNQ
GACBPKSA DS    F        MASKS:       SAVE AREA FOR PFK MASK
GACBATSA DS    F                     SAVE AREA FOR ATTENTION TYPES
GACBECB  DS    F                     ECB USED FOR ATNQ, MODE=W
GACBREB  DS    F        ADDR OF:     REB FOR THIS ATTENTION ROUTINE
GACBIDX  DS    C        INFO:        INDEX VALUE TO FIND 2260 UCBS
GACBLPR  DS    C                     BUFFER RESTART LOCATION FOR LP
GACBFLGS DS    H        FLAGS:
GACBCLOS EQU   X'01'                 CLOSE HAS ISSUED DAR
GACBATNQ DS    F        ADDR OF:     ATTENTION INQUIRY RTN (IGG019OK)
GACBRES  DS    F                     RESERVED
         EJECT
         IHAPSA
         EJECT
*             ROUTINE ENTRY BLOCK (REB)
*
*       1.  CREATED BY USER WITH GRAPHICS SPAR MACRO
*       2.  SEE SVC MODULE, IGC0007C FOR FURTHER DETAILS
*
REB      DSECT
REBL     DS    F        ADDR OF:     NEXT LOWER PRIORITY REB, OR ZEROS
REBH     DS    F                     NEXT HIGHER PRIORITY REB, OR TEB
REBUCB   DS    F                     UCB, OR LIST OF UCBS
REBGACB  DS    F                     GACB
REBGEIRB DS    F                     GEIR'S IRB
REBFLGS  DS    H       FLAGS:
REBPRTY  DS    H                     PRIORTY INDICATED BY USER IN SPAR
REBQ1    DS    F       ADDR OF:      IQE TO BE PROCESSED IF EP=0
REBQ2    DS    F                     IQE NEXT TO BE PROCESSED
REBTCB   DS    F                     TCB
REBRES   DS    F                     RESERVED
         EJECT
         IHARB
         EJECT
         IKJTCB
         EJECT
TEB      DSECT
TEBCKRB  DS    F       ADDR OF:      IRB FOR CANCEL KEY (IFFCAN03)
TEBREB   DS    F                     GAM ROUTINE ENTRY BLOCK
TEBTCB   DS    F                     TCB
TEBCKQE  DS    F                     LIST OF CANCEL KEY IQES
TEBUCBCT DS    F       USE COUNT:    NBR OF UCBS USING THIS TEB
TEBFLGS  DS    F       FLAG BYTES:
TEBFLAG  EQU   X'80'                 TEB ID FLAG
TEBGARB  DS    F       ADDR OF:      IRB FOR GAR (IGG019OE)
TEBGEIR  DS    F                     GEIR (IGG019OJ) ENTRY POINT
TEBGIOCR DS    F              GIOCR(IGG0190A)ENTRY POINT     LG @ZM2360
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
**********************************************************************
*                                                                     *
*                    ADDITIONAL UCB EQUATES FOR DSECT                 *
*                                                                     *
***********************************************************************
         SPACE 3
*          UCBTYP, BYTE 2, BIT SETTINGS
         SPACE
UCBTPFK  EQU   X'10'           UNIT TYPE BIT 3 FOR PFK FEATURE
         SPACE 3
*          UCBTYP, BYTE 4, BIT SETTINGS
         SPACE
UCBT2250 EQU   X'02'           UNIT TYPE ID FOR 2250 DEVICE
UCBT2260 EQU   X'03'                        FOR 2260
UCBT1053 EQU   X'04'                        FOR 1053
         SPACE 2
*          UCBGCB, BIT SETTINGS
         SPACE
UCBGINIT EQU   X'40'                   OK TO ACCEPT ATTNS NOW    A32068
UCBGBAS  EQU   X'08'
UCBGSET  EQU   X'B7'                   RESET ABOVE BITS TO ZERO
         EJECT
***********************************************************************
*                                                                     *
*                        SAVE AREA DSECT                              *
*                                                                     *
***********************************************************************
SAVEAREA DSECT
WKSA     DS    5F             WORK WORDS FOR LOCAL LOCK WHEN REGS=SAV
R0       DS    F                       REG ZERO    *  REGS MAP INTO  *
R1       DS    F                       REG ONE      *  STANDARD OS  *
R2       DS    F                       REG TWO     *  DISPLACEMENTS  *
R3       DS    F                       REG THREE        *  FOR  *
R4       DS    F                       REG FOUR      *  SAVE AREA  *
R5       DS    F                       REG FIVE       *  CHAINING *
R6       DS    F                       REG SIX
R7       DS    F                       REG SEVEN
R8       DS    F                       REG EIGHT
R9       DS    F                       REG NINE
R10      DS    F                       REG TEN
R11      DS    F                       REG ELEVEN
R12      DS    F                       REG TWELVE
EPCAN03  DS    F                       IFFCAN03 ADDRESS SAVE
EPGAR    DS    F                       GAR ADDRESS SAVE
EPGEIR   DS    F                       GEIR ADDRESS SAVE
ASCBADR  DS    F                       ADDR OF CURRENT ASCB
*    THE FOLLOWING SECTION OF THE DSECT IS USED FOR THE GRAPHICS      *
*    SHUTDOWN I/O AND EXTERNAL LINKAGES.  THE GETMAIN AREA IS USED    *
*    TO MAKE THE MODULE REENTRANT.                                    *
*                                                                     *
*                        DECB                                         *
*                                                                     *
DECBECB  DS    F                       ECB WORD
DECBTYPE DS    F                       TYPE OF I/O OPERATION
RMICODE  EQU   X'08'                   TYPE CODE FOR RMI
DECBDCB  DS    F                       POINTER TO DCB
DECBADDR DS    F                       I/O BUFFER-DATA TO BE
*                                      TRANSFERRED
DECBHEX  DS    C                       ECB POSTING CODE
DECBCNT  DS    3C                      RESIDUAL COUNT FROM CSW AFTER
*                                      I/O COMPLETED
DECBOCBP DS    F                       O/P CONTROL BLOCK POINTER
DECBSTRT DS    F                       START ADDR OF CONTROL ORDERS
DECBUNIT DS    C                       UNIT INDEX
DECBBUFF DS    3C                      BUFFER ADDRESS
         ORG   DECBECB
DARPLGH  DS    F          LENGTH OF DAR'S PARAMETER LIST
DARGACB  DS    F          ADDRESS OF GACB TO BE DELETED
         END
