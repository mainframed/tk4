         TITLE 'GRAPHIC ATTENTION ROUTING ROUTINE'
**********************************************************************
*
* MODULE NAME:            IGG019OE (OS/VS2)
*
* DESCRIPTIVE NAME:       GRAPHIC ATTENTION ROUTING ROUTINE
*
* COPYRIGHT:              NONE
*
* STATUS:                 RELEASE 2.0
*
*
*******FILL IN FROM SPECS*****CONTINUED* NEXT*PAGE********************
         EJECT
*FUNCTION:SECOND LEVEL GRAPHIC ATTENTION HANDLING MODULE.
*        1.DECODE ATTENTION TYPE.
*        2.PROVIDE MI DATA ON 2250 KEYBOARD(A/N OR PF)ATTENTIONS.
*        3.PROVIDE XY BEAN POSITION REGISTER READOUT ON LP ATTENTIONS.
*        4.PROVIDE BUFFER RESTART ON LP ATTENTION IF USER REQUESTS IT.
*        5.QUEUE DATA ON ATTENTION ROUTINE THAT IS:
*         A.ACTIVE FOR THAT DEVICE.(ROUTINE MAY BE IN WAIT STATE.IF THE
*           ROUTINE DESIRES A PARTICULAR ATTENTION IT WILL BE POSTED.)
*         B.WILL BECOME ACTIVE SOONEST FOR THAT DEVICE IF:ALREADY HAS
*           DATA ON ITS INTERNAL QUEUE WAITING TO BE DISPATCHED.
*         C.DESIRES THE ATTENTION TYPE.
*         D.IF THE ATTENTION IS UNABLE TO SATISFY A,B OR C IT IS
*           DISCARDED.
*        6.SCHEDULE THIRD LEVEL GRAPHIC CONTROL PROGRAM.
*ENTRY POINT:SCHEDULED BY PRIMARY GRAPHIC CONTROL PROGRAM VIA AN
*        IQE TO AEE STAGE II.
*INPUT:1.GR#0=IQE ADDRESS
*      2.GR#1=UCB ADDRESS AND STATUS BITS 32-39.
*      3.GR#S13,14,15 AS PER CONVENTIONS.
*OUTPUT:IQE TO AEE STAGE II TO SCHEDULE THIRD LEVEL CONTROL PROGRAM.
*       IQE CONTAINS THE DATA THAT WILL BE PLACED IN USER COMAREA.
*EXTERNAL ROUTINES:ASYNCHRONOUS EXIT EFFECTOR(AEE)STAGE TWO AND IOS.
*EXIT:VIA RETURN MACRO
*TABLES/WORK AREAS:N/A
*ATTRIBUTES:1.SUPERVISOR STATE
*           2.PRIVILEGED
*           3.RE-ENTRANT
*           4.OPERATES WITH AND WITHOUT LOCAL LOCK          D11
*NOTES:N/A
IGG019OE CSECT
*    SEE LABEL 'MODID' FOR LAST CHANGE DATE; FORMAT:  NAME.SYSREL.DATE
*C025400                                                      LF YM4067
* SEQ NBRS MUST BE RESPECIFIED BECAUSE OF RESEQ FOR VS
*A27115,A61060-61120,A64260-64320,A64630-64750,A75500,A89660  LI F40815
*C48000-48400,C52000,C61060,C64260                            LI A42794
*1708,065600-066000,084800,088000,089000-0892000                   0803
*D29665                                                       LI A39419
*C89720                                                       LI A39419
*1190,084700                                                   PTM 2173
* 087404                                                         A27773
*A16630-16660,A29710-30510,A65490-65510,A92810-92900             A34795
*C88000                                                          A34795
*D29695-30400                                                    A34795
*C91900,C92100-92300                                             A35022
*A29400                                                          A35022
* 042800-043200,065510,076800,079410,080040                   LB  AOS/1
*A068700,D087539,D087548                                        YM1901
*A051700                                                     LD YA00804
* A066900-066960,C067000,D067800-068000,A068500,D077000,     LD YA01555
* C078200,D079420-079440                                     LD YA01555
* A7900,A84490-84550,C84554                                      YM5683
*A27190,D30000,A30004-30008                                  LG Z30AALG
*A26500-26520,A26909-29627,A37500-37588                      LG @ZM2360
*A86900-86984,A87708-87749,A88000,A93020                     LG @ZM2360
*C30120,C43600,C55200,C57200,C58400,C68500                  LG @ZA00524
*C87408,C87416,C87432,C87448,C87498,C87548                  LG @ZA00524
*D68600-71000,D76000                                        LG @ZA00524
*A006700,037500-037720,086900-087130,087708-087740          D11 ZA13194
*A074760-074794,075300,093195-093197                        D11 ZA13195
*A087140-087150                                             D11 ZA13196
*
         EJECT
PAR0     EQU   0                       PARAM REG 0
PAR1     EQU   1                       PARAM REG 1
R1       EQU   1  REFERENCE REG FOR GIOCR HAND-EXPANSIONS   D11 ZA13194
BASERG   EQU   2                       BASE REG
MAINREG  EQU   3                       REG FOR WORK AREA DSECT
RIQE     EQU   4                       IQE ADR REG
RTCB     EQU   4                       TCB REG
RUCB     EQU   5                       UCB ADR REG
REBRG    EQU   6                       REB ADR REG
R7       EQU   7                       WORK REG
RASCB    EQU   7                       ASCB REG                  YM5683
R8       EQU   8                       WORK REG
R9       EQU   9                       WORK REG
R10      EQU   10                      WORK REG
R11      EQU   11                      WORK REG
R12      EQU   12                      WORK REG
SAVEREG  EQU   13                      SAVE AREA REG
R14      EQU   14                      RTN REG
R15      EQU   15                      RTN CODE REG
         EJECT
*
*        GACB CONSTRUCTION
*
COMAREA  EQU   0                       OFFSET TO COMAREA ADR
DCBADR   EQU   4                       OFFSET TO DCB ADR
PFKMSK   EQU   8                       OFFSET TO PFK MASK
ATNTYP   EQU   15                      OFFSET TO ATTENTION TYPE
EP1      EQU   16                      OFFSET TO ENTRY POINT ONE ADR
EP2      EQU   20                      OFFSET TO ENTRY POINT TWO ADR
SAVE13   EQU   24                      REG 13 SAVE AREA FOR R MODE
PFKEY    EQU   28                      FUNCTION KEY SAVE AREA
SAVETYP  EQU   32                      ATTN TYPE SAVE AREA
ECB      EQU   36                      EVENT CONTROL BLOCK FOR MODE W
REBADR   EQU   40                      ROUTINE ENTRY BLOCK ADR
GACBRSVD EQU   44                      RESERVED AREA
*
*        COMAREA CONTRUCTION
*
COMRSVD1 EQU   0                       RESERVED
COMOVCOD EQU   1                       OVERLAY CODE
COMKEY   EQU   2                       PF KEY NUMBER
COMTYPE  EQU   3                       ATTENTION TYPE
SENSE    EQU   4                       SENSE AREA
COMMI    EQU   4                       MANUAL INPUT AREA
XYPOS    EQU   8                       XY POSITION REGISTERS
COMRSVD3 EQU   12                      RESERVED
*
*        COMAREA TYPE FIELD
*
ENDORANK EQU   X'01'                   END OR A/N KEYBOARD ATTENTION
PFKB     EQU   X'02'                   PF KEYBOARD ATTENTION
LP       EQU   X'03'                   LIGHT PEN ATTENTION
EOS      EQU   X'04'                   END ORDER SEQUENCE ATTENTION
CANCEL   EQU   X'05'                   CANCEL KEY ATTENTION
AE       EQU   X'06'                   ASYNCHRONOUS ERROR ATTENTION
A2260    EQU   X'07'                   2260 ATTENTION
HEX82    EQU   X'82'              CSW STATUS BITS INDICATING A LIGHT
*                            PEN  ATTENTION
         EJECT
*
*        GACB ATTNTYP FIELD
*
GENAN    EQU   X'01'                   END OR A/N KEYBOARD ATTENTION
GLP      EQU   X'02'                   LIGHT PEN ATTENTION
GEOS     EQU   X'04'                   END ORDER SEQUENCE ATTENTION
GCANCEL  EQU   X'08'                   CANCEL KEY ATTENTION
GAE      EQU   X'10'                   ASYNCHRONOUS ERROR ATTENTION
T2260    EQU   X'20'                   2260 KEYBOARD ATTENTION
*
*
ANKBD    EQU   X'80'                   A/N BIT
AT2260   EQU   X'03'                   TYPE=2260
CANCELD  EQU   X'10'                   CANCEL KEY BIT
COMPLETE EQU   X'7F'                   POST CODE
CVTADR   EQU   16                      CVT  ADR
ENDD     EQU   X'20'                   END KEY BIT
EOSDET   EQU   X'40'                   END ORDER SEQUENCE BIT
FLAGS    EQU   X'40'                   TE PTR FLAG
GACBADR  EQU   12                      GACB ADR
IQELNK   EQU   0                       LINK FLD
IQEPAR   EQU   4                       PARAM FLD
IQEIRB   EQU   8                       IRD ADR FLD
IQETCB   EQU   12                      TCB ADR FLD
IQEDATA  EQU   16                      DATA FLD
LPDET    EQU   X'80'                   LIGHT PEN BIT
NEXAVL   EQU   96                      NEXT AVAILABLE  IQE  PTR
ONE      EQU   1                       ONE
PFKBD    EQU   X'40'                   PF BIT
RBIQE    EQU   24                      ADR OF FIRST INPUT IQE
RTNB     EQU   4                       HIGHER REB PTR
RTNF     EQU   0                       PTR TO NEXT REB
RTNFLG   EQU   20                      FLAG FLD
RTNGACB  EQU   12                      GACB ADR
RTNIRB   EQU   16                      IRB ADR
RTNQ1    EQU   24                      EP=0  QUEUE FIELD           2575
RTNQ2    EQU   28                      Q2 LIST PTR
REBTCB   EQU   33                      OFFSET TO TCB ADR IN REB
RTNUCB   EQU   8                       UCB PTR
STAGE2   EQU   4                       STAGE 2 ADR
TEREB    EQU   4                       REB PTR
TETCB    EQU   8                       TE TCB ADR FLD
TWO      EQU   2                       TWO
TYPE4    EQU   19                      UCB TYPE FLD
UCBSEN   EQU   32                      START OF UCB SENSE     LF YM4067
UCBTE    EQU   28                      USER TE ADR
WAITST   EQU   X'80'                   WAIT BIT
ZERO     EQU   0                       ZERO
MULTI    EQU   X'20'                   INDICATES MULTIPLE 2260
NEXT     EQU   4                       CONSTANTS TO STEP ADR SEARCH
TEBGIOCR EQU   32                    GIOCR ADDRESS IN TEB    LG @ZM2360
GIOCR    EQU   49                    GIOCR ADDRESS IN DCB    LG @ZM2360
ALL      EQU   X'FF'                   ACCEPT ANY 2260
RSTRT    EQU   45                      GACB LP RESTART FLAG
RESTART  EQU   X'01'                   AUTO RESTART BIT IN GFLGS
DEBDEB   EQU   4                   DEB CHAIN IN DEB                RSTD
DEBDCB   EQU   24                  DCB IN DEB                      RSTD
DEBUCB   EQU   32                  UCB IN DEB                      RSTD
CAN      EQU   12                  CANCEL KEY RTN ADDR/SW          RSTD
T2250    EQU   X'02'               2250 BIT IN UCB TYPE FIELD      RSTD
GCB      EQU   27                                                  RSTD
*GJPBIT  EQU   X'80'               GJP NO LONGER SPTD       D11
WODMP    EQU   X'00'                                               RSTD
RTNCD    EQU   X'40'                                               RSTD
ABND     EQU   X'80'                                               RSTD
ATTNERR  EQU   X'41'                                             F40815
FOUR     EQU   4
FIVE     EQU   5
SIX      EQU   6
ECBLN    EQU   32
SIXTN    EQU   16
INDGAR   EQU   X'04'
REBUCB   EQU   8                       UCB ADDR
DQCKQE   EQU   X'02'                   DEQUEUE CKQE
EIGHT    EQU   8                       DISPLACEMENT
THREE    EQU   3                       MASK OF 3             LG Z30AALG
         EJECT
         SAVE  (14,12)
         BALR  BASERG,PAR0             SET UP BASE REG
         USING *,BASERG                DEFINE BASE REG
         USING WORKAREA,MAINREG        DEFINE CSECT REG
         B     MODID1                                       D11
MODID    DC    C'IGG019OE.VS2R2.&SYSDATE' EYECATCHER        D11
MODID1   LR    RIQE,PAR0               SAVE PARAMETER
         LR    RUCB,PAR1               SAVE PARAMETER
         L     PAR0,REQUEST            LOAD BYTE REQUEST
         GETMAIN R,LV=(0)
         ST    PAR1,EIGHT(SAVEREG)     BACK CHAIN SAVE AREAS
         LR    MAINREG,PAR1            LOAD DSECT REG
         XC    RSVD(120),RSVD          CLEAR CORE
         ST    SAVEREG,CHAIN           CHAIN SAVE AREA
         LA    SAVEREG,SAVE             ESTABLISH NEW SAVE AREA
         L     R11,UCBTE(ZERO,RUCB)     LOAD TE ADDRESS FROM UCB
         L     R10,TEBGIOCR(R11)     GIOCR ADDR FROM TEB     LG @ZM2360
         ST    R10,SAVGIOCR          SAVE IN WORK AREA       LG @ZM2360
         L     R10,CAN(R11)            LOAD CKQE POINTER
DEQLOOP  LA    R10,ZERO(R10)           CLEAR HIGH ORDER BYTE
         LTR   R10,R10                 CKQE POINTER ZERO
         BZ    GASBACK                 YES TEST FOR ABEND
         EJECT
*    CANCEL KEY PROCESSING
         USING CKQE,R10                BASE REG FOR CKQE DSECT
         TM    CKQEFLGA,CKQEDEQ        SHOULD CKQE BE DEQUEUED
         BZ    CKCHECK                 NO
*                                      YES REBUILD CHAIN
         L     R12,CKQENEXT            GET NEXT CKQE ADDRESS
         ST    R12,CAN(R11)            STORE NEW CKQE ADDRESS IN TE
         LR    PAR1,R10                LOAD CKQE ADDRESS FOR FREEMAIN
         L     PAR0,CKQECORE           LOAD SUBPOOL AND BYTE REQUEST
         FREEMAIN  R,LV=(0),A=(1)      FREE CKQE
         LR    R10,R12                 PUT NEW CKQE PTR IN REG 10 AND
         B     DEQLOOP                 TEST FOR DEQUEUE NECESSARY
CKCHECK  ST    RUCB,IOSTAT         SAVE IO STATUS                   CAN
         LA    RUCB,ZERO(RUCB)     CLEAR HIGH ORDER BYTE         CAN01
         SR    R12,R12                 CLEAR REGISTER        LG Z30AALG
         ICM   R12,THREE,CKQEUCB       GET UCB ADDRESS       LG Z30AALG
         CR    RUCB,R12                COMPARE UCB ADDRS
         BE    CHKATTN                 MATCH FOUND-CAN KEY IN CONTROL
         L     R10,ZERO(R10)           GET NEXT CKQE
         B     DEQLOOP                 CHECK FOR MORE CKQE'S
         EJECT
CHKATTN  TM    CKQEFLGA,CKQEATTN       IS ATTN EXPECTED FROM 2250
         BZ    SSM6A                 NO                    D11 @ZA00524
         TM    IOSTAT,HEX82        IS IT A LIGHT PEN ATTENTION   CAN01
         BNO   GASRMI             BRANCH TO UNLOCK KYBD          CAN01
         L     PAR1,UCBTE(RUCB)   GET TEB ADDRESS                CAN01
         MVC   SENSED(FOUR),UCBSEN(RUCB)    MOVE SENSE DATA TO WORKAREA
         TM    SENSED+ONE,LPDET        TEST FOR LIGHT PEN DETECT
         BZ    GASRMI                  NOT LP - FREE KEYBOARD
         OI    FIVE(R10),INDGAR        IND ENTRY FROM GAR W/LP ATTN
         B     GAS60                   PLACE CKQE ON TEB CKQE LIST
GASRMI   L     REBRG,TEREB(PAR1)       LOAD REB ADDRESS
REBLOOPA LA    REBRG,ZERO(REBRG)       CLEAR HIGH ORDER BYTE
         LTR   REBRG,REBRG             DOES REB EXIST
         BNZ   REBGOOD            BRANCH IF REB ADDR IS GOOD
         L     R8,CKQEDCB         GET DCB ADDRESS
         B     NOREBRMI           BRANCH IF REB IS NOT FOUND
         DROP  R10
REBGOOD  L     R10,REBUCB(REBRG)  GET UCB FROM REB
         CR    R10,R12                 RIGHT REB
         BE    DCBRMI                  YES
         L     REBRG,ZERO(REBRG)       NO TRY ANOTHER
         B     REBLOOPA                REB
DCBRMI   L     R8,GACBADR(REBRG)       GET GACB ADDRESS
         L     R8,DCBADR(R8)           GET DCB ADDRESS
         LA    R8,ZERO(R8)             CLEAR HIGH ORDER BYTE
NOREBRMI XC    IOREQ(ECBLN),IOREQ CLEAR ECB
         LA    PAR1,IOREQ              LOAD DECB ADDRESS
         LA    R9,MI                   INPUT AREA ADDRESS
         BAL   R7,COMNREAD            GO EXECUTE GREADR FOR RMI
         B     GASHSKP
         EJECT
GASBACK  L     R11,UCBTE(0,RUCB)       LOAD TE ADR FROM UCB
         L     REBRG,TEREB(0,R11)      LOAD PTR TO FIRST REB
         LR    R10,RUCB                PARAM TO WORK REG(UCB ADR)
         LA    R10,0(0,R10)            ZERO HI ORDER BYTE
GAS02    LTR   REBRG,REBRG             PTR 0
         BZ    GASCHAIN            BRANCH IF YES            D11    RSTD
         TM    RTNFLG(REBRG),MULTI     REB POINT TO A LIST OF
*                                      UCB ADRS
         BNO   GAS24                   BRANCH IF NO
         SR    R15,R15                 CLEAR REG
         IC    R15,RTNFLG+1(REBRG)     LOAD NO OF UCB ADRS
         L     R14,RTNUCB(REBRG)       LOAD PTR TO LIST
GAS25    L     R12,ZERO(0,R14)         LOAD UCB ADR
         LA    R12,ZERO(0,R12)         ZERO HI ORDER BYTE
         CLR   R10,R12                 UCB ADRS EQUAL
         BE    GAS01                   BRANCH IF YES        D11
         LA    R14,NEXT(0,R14)         UPDATE PTR
         BCT   R15,GAS25               BRANCH IF MORE UCB ADRS
         B     GAS05                   BRANCH
GAS24    L     R12,RTNUCB(0,REBRG)     LOAD REB UCB ADR
         CLR   R10,R12                 ADDRESSES COMPARE
         BE    GAS01                   BRANCH IF YES        D11
GAS05    L     REBRG,RTNF(0,REBRG)     LOAD PTR TO NEXT REB
         B     GAS02
         EJECT
*     UCB MATCH FOUND
GAS01    L     R12,CODETYP             LOAD MASK
         NR    R12,RUCB                LOAD R12 WITH ATN TYPE
         LTR   R12,R12                 TEST FOR KYBD ATTN
         BZ    GAS03                   BRANCH IF KYBD ATTN  D11
         MVC   SENSED(4),UCBSEN(RUCB)  SENSE DATA TO WORK AREA
         TM    SENSED+1,LPDET          TEST FOR LIGHT PEN
         BNO   GAS04                   BRANCH IF NOT LP     D11
         LA    R10,LP                  LOAD LP CODE
         L     R8,GACBADR(0,REBRG)     LOAD GACB ADR
         L     R8,DCBADR(0,R8)         LOAD DCB ADR
         LA    R8,ZERO(0,R8)           ZERO HI ORDER BYTE
         LTR   R8,R8                   DCB ADR=0
         BZ    GAS05                   BRANCH IF YES        D11
         XC    IOREQ(32),IOREQ              CLEAR ECB              AHD6
         LA    PAR1,IOREQ              LOAD DECB ADR
         LA    R9,XYP                  LOAD XYP ADR
         SPACE 3                                            D11 ZA13194
* THERE FOLLOWS A HAND-CODED EXPANSION OF THE GREADR MACRO  D11 ZA13194
*    IN ORDER TO ACCESS THE ADDRESS OF GIOCR FROM A PRO-    D11 ZA13194
*    TECTED AREA (SAVGIOCR IN TEB) IN ORDER TO AVOID A      D11 ZA13194
*    SYSTEM INTEGRITY EXPOSURE.                             D11 ZA13194
*        GREADR (1),XYP,(8),(9),MF=E                        D11 ZA13194
         SR    R15,R15             CLEAR REG                D11 ZA13194
         ST    R15,4(R1)           CLEAR TYPE AREA IN DECB  D11 ZA13194
         MVI   4(R1),X'10'         XYP TYPE TO DECB         D11 ZA13194
         LA    R15,0(R8)           DCB ADDRESS              D11 ZA13194
         ST    R15,8(0,R1)         DCB ADDRESS IN DECB      D11 ZA13194
         LA    R15,0(R9)           AREA ADDRESS             D11 ZA13194
         ST    R15,12(0,R1)        AREA ADDRESS IN DECB     D11 ZA13194
         XC    0(4,R1),0(R1)       CLEAR ECB                D11 ZA13194
         L     R15,SAVGIOCR        SAFE GIOCR ADDR FROM TEB D11 ZA13194
         BALR  R14,R15             BRANCH TO GIOCR          D11 ZA13194
*  END OF HAND-CODED EXPANSION OF GREADR MACRO              D11 ZA13194
         SPACE 3                                            D11 ZA13194
         BAL   R7,COMNWAIT             TEST FOR GOOD I/O AND WAIT
         B     TRANSLAT                BRANCH
GAS04    TM    SENSED+1,EOSDET         TEST FOR END ORDER SEQUENCE
         BNO   GAS06                   BRANCH IF NOT EOS    D11
         LA    R10,EOS                 LOAD EOS CODE
         B     TRANSLAT                BRANCH
GAS06    LA    R10,AE                  LOAD AE CODE
         B     TRANSLAT                BRANCH
         EJECT
*     KEYBOARD ATTENTION, SO CHECK DEVICE TYPE
GAS03    CLI   TYPE4(RUCB),AT2260      IS THIS A 2260
         BNE   GAS08                   BRANCH IF NO         D11
         LA    R10,A2260               LOAD 2260 CODE
         B     TRANSLAT                BRANCH
*   FOR 2250 KEYBOARD ATTENTION
GAS08    L     R8,GACBADR(0,REBRG)     LOAD GACB ADR
         L     R8,DCBADR(0,R8)         LOAD DCB ADR
         LA    R8,ZERO(0,R8)           ZERO HI ORDER BYTE
         LTR   R8,R8                   DCB ADR=0
         BZ    GAS05                   BRANCH IF YES TO TRY D11
*                                      ANOTHER UCB ADR MATCH
         XC    IOREQ(32),IOREQ              CLEAR ECB              AHD6
         LA    PAR1,IOREQ              LOAD DECB ADR
         LA    R9,MI                   LOAD INPUT AREA ADR
         BAL   R7,COMNREAD            GO EXECUTE GREADR FOR RMI
         TM    MI,PFKBD                TEST FOR PF KYBD ATTN
         BO    GASPF                   BRANCH IF YES        D11
GAS10    TM    MI,CANCELD              TEST FOR CANCEL KEY
         BO    GAS132                  BR IF CANCEL KEY       LB  AOS/1
GAS22    TM    MI,ANKBD+ENDD           TEST FOR AN OR END KEY ATTENTIOM
         BZ    SSM6A             BRANCH IF NOT A/N OR END  D11 @ZA00524
         LA    R10,ENDORANK            LOAD END OR A/N CODE
         EJECT
TRANSLAT STC   R10,TYPE                PUT TYPE IN DATA AREA
         LA    R7,ONE                  LOAD TYPE MASK
         LA    R8,ZERO                 ZERO PF MASK
         CLI   TYPE,ENDORANK           TEST FOR END OR A/N
         BE    SEARCH              YES,GO SEARCH            D11
         LA    R9,TWO                  LOAD CONSTANT
         SR    R10,R9                  DETERMINE SHIFT COUNT
         SLL   R7,ZERO(R10)            DETERMINE SEARCH ARGUMENT
         B     SEARCH                  BRANCH
GASPF    MVC   KEY(1),MI+1             KEY NUMBER TO WORH AREA
         MVC   OV(1),MI+2              OVERLAY COSE TO WORK AREA
         MVI   TYPE,X'02'              PF CODE TO TYPE FIELD
         SR    R10,R10                 CLEAR REG
         IC    R10,KEY                 INSERT KEY NUMBER
         LA    R7,ZERO                 ZERO TYPE REG
         L     R8,PFKCODE              LOAD PF MASK
         SRL   R8,ZERO(R10)            DETERMINE PF SEARCH ARGUMENT
SEARCH   L     REBRG,UCBTE(0,RUCB)     LOAD TE ADR FROM UCB
         L     REBRG,TEREB(0,REBRG)    LOAD PTR TO FIRST REB
         LA    RUCB,ZERO(0,RUCB)       ZERO HIGH ORDER BYTE
GAS28    SR    R15,R15             CLEAR REG                  LI A42794
         TM    RTNFLG(REBRG),MULTI  REB POINT TO MULTI UCBS   LI A42794
         BNO   GAS12               BRANCH IF NO             D11  A42794
         IC    R15,RTNFLG+1(REBRG)     LOAD # OF UCB ADRS
         L     R14,RTNUCB(0,REBRG)     LOAD PTR
GAS27    L     R9,ZERO(0,R14)          LOAD UCB ADR
         LA    R9,ZERO(0,R9)           ZERO HI ORDER BYTE
         CLR   R9,RUCB                 UCB ADRS COMPARE
         BE    GAS26                   BRANCH IF YES        D11
         LA    R14,NEXT(0,R14)         LOAD PTR TO NEXT UCB ADR
         BCT   R15,GAS27               BRANCH IF MORE
         B     GAS13                   BRANCH
GAS26    SR    R14,R14                 CLEAR REG
         IC    R14,RTNFLG+1(REBRG)     INSERT # OF EXTENTS
         LA    R14,ONE(0,R14)          INCREMENT COUNT BY ONE
         SR    R14,R15                 DETERMINE OFFSET TO UCB ADR
         STC   R14,RSVD                2260 OFFSET TO DATA AREA
         B     GAS11                   BRANCH
GAS12    L     R9,RTNUCB(0,REBRG)      LOAD UCB ADR
         LA    R15,ONE(R15)            INCREMENT DEV INDEX   LD YA00804
         CLR   R9,RUCB                 ADRS COMPARE
         BE    GAS26               BRANCH IF YES            D11  A42794
GAS13    L     REBRG,RTNF(0,REBRG)     LOAD NEXT REB PTR
         LTR   REBRG,REBRG             PTR = 0
         BNZ   GAS28                   BRANCH IF PTR NOT 0  D11
         L     REBRG,UCBTE(0,RUCB)     LOAD TE ADR
         L     REBRG,TEREB(0,REBRG)    LOAD PTR TO FIRST REB
         LA    RUCB,0(0,RUCB)          ZERO HI ORDER BYTE
GAS56    TM    RTNFLG(REBRG),MULTI     REB POINT TO LIST OF UCB ADRS
         BNO   GAS53                   BRANCH IF NO
         SR    R9,R9                   CLEAR REG
         IC    R9,RTNFLG+1(REBRG)      LOAD # OF UCB ADRS
         L     R11,RTNUCB(REBRG)       LOAD PTR TO LIST
GAS55    L     R12,ZERO(0,R11)         LOAD UCB ADR
         LA    R12,ZERO(0,R12)         ZERO HI ORDER BYTE
         CLR   RUCB,R12                UCB ADRS COMPARE
         BNE   GAS54                   BRANCH IF NO         D11
         LA    R12,SSM6A             LOAD RETURN ADR       D11 @ZA00524
         CLI   TYPE,LP                 IS THIS A LP ATTN
         BNER  R12                     BRANCH IF NO         D11
         L     R9,RTNGACB(0,REBRG)     LOAD GACB ADRESS
         B     LPSTART1                BRANCH
GAS54    LA    R11,4(0,R11)            INCREMENT PTR
         BCT   R9,GAS55                BRANCH IF MORE UCBS
GAS57    L     REBRG,RTNF(0,REBRG)     LOAD NEXT REB PTR
         LA    REBRG,ZERO(0,REBRG)     ZERO HI ORDER BYTE
         LTR   REBRG,REBRG             PTR=0
         BZ    SSM6A                   BRANCH IF YES       D11 @ZA00524
         B     GAS56                   BRANCH
GAS53    L     R12,RTNUCB(0,REBRG)     LOAD UCB ADR
         LA    R12,ZERO(0,R12)         ZERO HI ORDER BYTE
         CLR   R12,RUCB                UCB ADRS COMPARE
         BNE   GAS57                   BRANCH IF NO         D11
         LA    R12,SSM6A               LOAD RETURN ADR     D11 @ZA00524
         CLI   TYPE,LP                 IS THIS A LP ATTN
         BNER  R12                     BRANCH IF NO         D11
         L     R9,RTNGACB(0,REBRG)     LOAD GACB ADRESS
         B     LPSTART1                BRANCH
GAS11    L     R10,RTNIRB(0,REBRG)     LOAD IRB ADR
         L     R9,RBIQE(0,R10)         LOAD RB IQE PTR
         LA    R9,ZERO(0,R9)           ZERO HI ORDER BYTE
         LTR   R9,R9                   PTR=0
         BNZ   GAS14                   BRANCH IF NOT 0      D11
         L     R9,RTNGACB(0,REBRG)     LOAD GACB ADR
         LA    R9,ZERO(0,R9)           ZERO HIGH ORDER BYTE
         LTR   R9,R9                   GACB ADR=0
         BZ    GAS58                   BRANCH IF YES        D11
         CLC   ECB(FOUR,R9),INIT   WAS ECB INITALIZED         LI A42794
         BE    POSTERR             YES, BRANCH                   F40815
         TM    ECB(R9),X'80'           ECB=WAIT
         BO    GAS16                   BRANCH IF YES        D11
GAS58    L     R9,RTNQ2(0,REBRG)       LOAD QZ PTR
         LTR   R9,R9                   PTR=0
         BNZ   GAS15                   BRANCH IF NO         D11
         L     R9,RTNGACB(0,REBRG)     LOAD GACB ADR
         L     R11,PFKMSK(0,R9)        LOAD PFKMSK MASK FROM GACB
         L     R12,PFKMSK+4(0,R9)      LOAD ATTNTYP MASK FROM GACB
         NR    R12,R7                  DETERMINE IF MATCH
         NR    R11,R8                  DETERMINE IF MATCH
         LTR   R12,R12                 REG=0
         BNZ   GAS15                   NOT 0-ATTN TYP MATCH D11
         LTR   R11,R11                 REG=0
         BNZ   GAS15                   NOT 0-PF KEY MATCH   D11
         B     GAS13                   BRANCH
GAS14    L     R9,RTNGACB(0,REBRG)     LOAD GACB ADR
         CLC   ECB(FOUR,R9),INIT        WAS ECB INITALIZED    LI A42794
         BE    POSTERR             YES, BRANCH                   F40815
         TM    ECB(R9),WAITST          ROUTINE IN WAIT STATE
         BO    GAS16                   BRANCH IF YES        D11
         B     GAS15               NO, BRANCH TO LP RESTART      F40815
POSTERR  LA    PAR0,ATTNERR        LOAD ERR CODE                 F40815
         SLL   PAR0,24             SHIFT TO HIGH ORDER BYTE      F40815
         LA    PAR1,ECB(R9)        LOAD ECB ADDR                 F40815
         POST  (1),(0)             POST ECB WITH ERROR           F40815
GAS15    BAL   R12,LPSTART             BRANCH TO LP RESTART CYCLE
GAS15A   L     R9,NEXAVL(0,R10)        LOAD NEXT AVAIL IQE FLD
         LTR   R9,R9                   PTR=0
         BNZ   GAS17                   BRANCH IF NO         D11
         LA    R9,GMAINADR             SET UP RE-ENTRANT ADR LOC   0803
         LA    PAR1,GMAINLST       POINT TO LIST                 A34795
         GETMAIN EC,LV=32,A=(9),SP=234,MF=(E,(1))             LB  AOS/1
         LTR   R15,R15               IS CORE AVAILABLE             0803
         BZ    GAS15B                  IF YES, CONTINUE PROCESSING 0803
         BNZ   GASHSKP
GAS15B   EQU   *                                             LG ZA00524
         LA    SAVEREG,LOCKSAVE        GET SAVE AREA ADDR    LG ZA00524
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,RELATED=(LOCAL,IGG019OE(X
               SSM3)),REGS=SAVE                              LG ZA00524
         LA    SAVEREG,SAVE            RESTORE SVAREA ADDR   LG ZA00524
         L     R9,GMAINADR           CLEAR IQE                     0803
         XC    ZERO(32,R9),ZERO(R9)    CLEAR CORE
         ST    R10,IQEIRB(0,R9)        IRB ADR TO IQE
         L     PAR1,UCBTE(0,RUCB)      LOAD TE ADR
         MVC   IQETCB+1(3,R9),REBTCB(REBRG)    SPAR TCB ADR TO IQE
GAS17    EQU   *                                             LD YA01555
         CLI   ENABL1,ALL              FORCE PAGE-IN         LD YA01555
         CLI   EP1(R7),ALL             FORCE PAGE-IN OF GACB LD YA01555
         LA    SAVEREG,LOCKSAVE        POINT TO SETLOCK SAVE AREA
SSM1     SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,RELATED=(LOCAL,IGG019OE(X
               SSM3)),REGS=SAVE
         LA    SAVEREG,SAVE            RESTORE SAVE AREA ADDRESS
         MVC   NEXAVL(4,R10),IQELNK(R9) LINK ADR TO IRB NEXAVL FLD
         L     R11,RTNGACB(0,REBRG)    LOAD GACB ADR
         ST    R11,IQEPAR(R9)          GACB ADR TO IQE
         MVC   IQEDATA(12,R9),RSVD     DATA TO IQE
         MVC   IQELNK(4,R9),RTNQ2(REBRG) QZ PTR TO IQE LNK FIELD
         ST    R9,RTNQ2(REBRG)         IQE ADR TO Q2 PTR FLD
         B     SSM6A                                       D11 @ZA00524
GAS16    L     R12,RTNGACB(0,REBRG)    LOAD GACB ADR
         L     R11,PFKMSK(0,R12)       LOAD PFKMSK
         L     R14,PFKMSK+4(0,R12)     LOAD ATTNTYP MASK
         NR    R11,R8                  DETERMINE IF MATCH
         NR    R14,R7                  DETERMINE IF MATCH
         LTR   R11,R11                 REG=0
         BNZ   GAS19               BRANCH NOT 0-HAD A MATCH D11
         LTR   R14,R14                 REG=0
         BZ    GAS15
GAS19    BAL   R12,LPSTART             BRANCH TO LP RESTART CYCLE
         L     R12,RTNGACB(0,REBRG)    LOAD GACB ADR
         CLI   TYPE,A2260              IS THIS A 2260 ATTN
         BNE   GAS19A                  BRANCH IF NOT 2260   D11
         CLI   GACBRSVD(R12),ALL       ACCEPT ANY 2260 ATN
         BE    GAS19A                  BRANCH IF YES        D11
         CLC   GACBRSVD(1,R12),RSVD    IS THIS THE 2260 RTN WAITING FOR
         BNZ   GAS15A                  NO
GAS19A   L     R11,REBTCB-1(0,REBRG)   LOAD TCB ADDRESS     D11 ZA13195
         USING TCB,R11                                      D11 ZA13195
         MODESET EXTKEY=TCB,WORKREG=1   GO TO USERKEY       D11 ZA13195
         DROP  R11                                          D11 ZA13195
         L     R11,COMAREA(0,R12)      LOAD COMAREA ADR
         MVC   ZERO(12,R11),RSVD       DATA TO COMAREA
         MODESET EXTKEY=SUPR           BACK TO SUPERKEY     D11 ZA13195
         LA    PAR0,COMPLETE           LOAD POST CODE
         SLL   PAR0,24             SHIFT TO HIGH ORDER BYTE      F40815
         LA    PAR1,ECB(0,R12)         LOAD ECB ADR
         POST  (1),(0)
GAS18    EQU   *                                              LB  AOS/1
         LA    SAVEREG,LOCKSAVE        POINT TO SETLOCK SAVE AREA
SSM6     SETLOCK RELEASE,TYPE=LOCAL,RELATED=(LOCAL,IGG019OE(SSM7)),REGSX
               =SAVE
         LA    SAVEREG,SAVE            RESTORE SAVE AREA ADDRESS
SSM6A    L     REBRG,UCBTE(RUCB)       LOAD TE ADDRESS      D11   AOS/1
         L     REBRG,TEREB(0,REBRG)    LOAD PTR TO FIRST REB
         LA    REBRG,0(0,REBRG)        ZERO HI ORDER BYTE
         LTR   REBRG,REBRG
         BZ    ENABL1                  BRANCH IF YES        D11 YA01555
GAS23    L     R7,RTNF(0,REBRG)        LOAD PTR TO NEXT REB
         LTR   R7,R7                   REG 0
         BZ    GAS20                   BRANCH IF YES        D11
         LR    REBRG,R7                SAVE ADR
         B     GAS23                   BRANCH
* REBR6 CONTAINS ADR OF LOWEST PRIORITY REB                        2575
* R7,R8 AND PAR1 REGS AVAILABLE AS WORK REGS                       2575
GAS20    EQU   *                                              LB  AOS/1
         LA    SAVEREG,LOCKSAVE        POINT TO SETLOCK SAVE AREA
SSM7     SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,RELATED=(LOCAL,IGG019OE(X
               SSM6)),REGS=SAVE
         LA    SAVEREG,SAVE            RESTORE SAVE AREA ADDRESS
GAS103   L     R7,RTNGACB(REBRG)       LOAD GACB ADR FROM REB      2575
         CLC   ZERO1(4),EP1(R7)        ENTRY POINT=0               2575
         BE    GAS100                  BRANCH-EP=0                 2575
GAS110   CLC   ZERO1(4),RTNQ1(REBRG)   ANY IQES ON Q1 FIELD        2575
         BNE   GAS101                  BRANCH-Q1 HAS IQES          2575
GAS104   CLC   ZERO1(4),RTNQ2(REBRG)   ANY IQES ON Q2 FIELD        2575
         BNE   GAS102                  BRANCH-Q2 HAS IQES          2575
GAS113   TM    RTNFLG(REBRG),FLAGS     REB POINT TO A TE           2575
         BO    ENABL1                  BR IF REB POINTS TO TE LB  AOS/1
         L     REBRG,RTNB(REBRG)       LOAD NEXT REB POINTER       2575
         B     GAS103                  BRANCH TO CHECK NEXT REB    2575
GAS102   L     R7,RTNQ2(REBRG)         LOAD IQE ADR                2575
         MVC   RTNQ2(4,REBRG),IQELNK(R7)  UPDATE CHAIN             2575
         LNR   PAR1,R7                 COMPLEMENT IQE ADR TO PARAM 2575
         L     R15,CVTADR              GET CVT TABLE ADR           2575
         L     R15,STAGE2(R15)         GET ADR OF AEE STAGE 2      2575
         BALR  R14,R15                 LINK TO AEE STAGE 2         2575
         B     GAS104                  BRANCH TO TEST FOR MORE IQE 2575
GAS101   L     PAR1,RTNQ1(REBRG)       LOAD ADR OF FIR0T IQE       2575
         SR    R7,R7                   ZERO PREVIOUS IQE REG       2575
GAS106   CLC   IQELNK(4,PAR1),ZERO1    LAST IQE ON CHAIN           2575
         BE    GAS105                  BRANCH-LAST IQE ON CHAIN    2575
         LR    R7,PAR1                 SAVE ADR THIS IQE           2575
         L     PAR1,IQELNK(PAR1)       LOAD NEXT IQE ADR ONCHAIN   2575
         B     GAS106                  BRANCH TO TEST FOR END OF C 2575
GAS105   LTR   R7,R7                   PREVIOUS IQE REG = 0        2575
         BZ    GAS107                  BRANCH-YES           D11    2575
         MVC   IQELNK(4,R7),IQELNK(PAR1) REMOVE IQE FROM END OF 01 2575
         B     GAS108                  BRANCH                      2575
GAS107   MVC   RTNQ1(4,REBRG),IQELNK(PAR1)   UPDATE 01 CHAIN       2575
GAS108   XC    IQELNK(4,PAR1),IQELNK(PAR1)   ZERO IQE LINK FIELD   2575
         CLC   ZERO1(4),RTNQ2(REBRG)   02 FIE TO ZERO              2575
         BNE   GAS109                  BRANCH-02 NOT ZERO          2575
         ST    PAR1,RTNQ2(REBRG)       ADD IQE TO Q2 FIELD         2575
         B     GAS110                  BRANCH TO TEST 01 FIELD     2575
GAS109   L     R7,RTNQ2(REBRG)         LOAD 1ST IQE ADR            2575
GAS112   CLC   ZERO1(4),IQELNK(R7)     LAST IQE ON CHAIN           2575
         BE    GAS111                  BRANCH-LAST IQE ON CHAIN    2575
         L     R7,IQELNK(R7)           LOAD ADR OF NEXT IQE        2575
         B     GAS112                  BRANCH TO FIND END OF Q2 CH 2575
GAS111   ST    PAR1,IQELNK(R7)         ADD IQE TO END OF CHAIN     2575
         B     GAS110                  BRANCH TO TEST Q1 FIELD     2575
GAS100   CLC   ZERO1(4),RTNQ2(REBRG)   ANU IQES ON Q2 FIELD        2575
         BE    GAS113                  BRANCH-NO IQES ON Q2 FIELD  2575
         L     PAR1,RTNQ2(REBRG)       LOAD ADR OF FIRST IQE       2575
         SR    R7,R7                   ZERO PREVIOUS IQE REG       2575
GAS114   CLC   ZERO1(4),IQELNK(PAR1)   LAST IQE ON CHAIN           2575
         BE    GAS115                  BRANCH LAST IQE ON CH       2575
         LR    R7,PAR1                 SAVE ADR THIS IQE           2575
         L     PAR1,IQELNK(PAR1)       LOAD NEXT IQE ADR ON CHAIN  2575
         B     GAS114                  BRANCH TO TEST FOR END OF   2575
GAS115   LTR   R7,R7                   PREVIOUS IQE REG= 0         2575
         BZ    GAS116                  BRANCH-YES           D11    2575
         MVC   IQELNK(4,R7),IQELNK(PAR1) REMOVE IQE FROM END OF Q  2575
         B     GAS117                  BRANCH                      2575
GAS116   MVC   RTNQ2(4,REBRG),IQELNK(PAR1) UPDATE Q2 CHAIN         2575
GAS117   XC    IQELNK(4,PAR1),IQELNK(PAR1) ZERO IQE LINK FIELD     2575
         CLC   ZERO1(4),RTNQ1(REBRG)   Q1 FIELD ZERO               2575
         BNE   GAS118                  BRANCH-Q1 NOT ZERO          2575
         ST    PAR1,RTNQ1(REBRG)       ADD IQE TO Q1 FIELD         2575
         B     GAS100                  BRANCH TO TEST 02 FIELD     2575
GAS118   L     R7,RTNQ1(REBRG)         LOAD 1SR IQE ADR            2575
GAS119   CLC   ZERO1(4),IQELNK(R7)     LAST IQE ON CHAIN           2575
         BE    GAS120                  BRANCH LAST IQE ON CHAI     2575
         L     R7,IQELNK(R7)           LOAD ADR OF NEXT IQE        2575
         B     GAS119                  BRANCH TO FIND END OF CHA   2575
GAS120   ST    PAR1,IQELNK(R7)         ADD IQE TO END OF CHAIN     2575
         B     GAS100                  BRANCH                      2575
ENABL1   EQU   *                       LIMIT OF DISABLED CODE LB  AOS/1
GASHSKP  L     SAVEREG,CHAIN            RESTORE SAVE AREA POINTER
         LR    PAR1,MAINREG            LOAD ADDRESS TO FREE
         L     PAR0,REQUEST            LOAD BYTE COUNT
         L     RTCB,CVTADR          GET CVT ADDRESS              YM5683
         L     RTCB,0(RTCB)         ADDRESS OF TCB TABLE         YM5683
         L     RASCB,12(RTCB)       ADDRESS OF CURRENT ASCB      YM5683
         L     RTCB,4(RTCB)         ADDRESS OF CURRENT TCB       YM5683
         FREEMAIN R,LV=(0),A=(1),BRANCH=YES                      YM5683
SSM4     SETLOCK RELEASE,TYPE=LOCAL,RELATED=(LOCAL,IGG019OE(SSM5)),REGSX
               =USE
GASHSKP1 RETURN (14,12)                                            RSTD
         EJECT
LPSTART  CLI   TYPE,LP                 IS THIS A LP ATTN
         BNER  R12                     BRANCH IF NOT LP     D11
         L     R9,RTNGACB(0,REBRG)     LOAD GACB ADR
         TM    RSTRT(R9),RESTART       AUTO RESTART REQUESTED
         BNOR  R12                     BRANCH IF NO         D11
LPSTART1 L     R11,DCBADR(0,R9)        LOAD DCB ADDRESS
         LA    R9,SENSED+TWO           LOAD RESTART ADR
         XC    IOREQ(32),IOREQ         CLEAR DECB
         LA    PAR1,IOREQ              LOAD DECB ADR
*        SPACE 3                                            D11 ZA13194
*   THERE FOLLOWS A HAND-CODED EXPANSION OF THIS GCNTRL     D11 ZA13194
*   MACRO CALL IN ORDER TO ACCESS A SAFE GIOCR ADDR(TEB)    D11 ZA13194
*        GCNTRL (1),STR,(11),(9),MF=E                       D11 ZA13194
         SR    R15,R15             CLEAR REG                D11 ZA13194
         ST    R15,4(R1)           CLEAR TYPE AREA IN DECB  D11 ZA13194
         MVI   4(R1),X'6C'         STR TYPE CODE TO DECB    D11 ZA13194
         LA    R15,0(R11)          DCB ADDRESS              D11 ZA13194
         ST    R15,8(R1)           DCB ADDRESS IN DECB      D11 ZA13194
         LR    R15,R9              AREA ADDRESS             D11 ZA13194
         ST    R15,24(0,R1)        START ADDRESS IN DECB    D11 ZA13194
         XC    0(4,R1),0(R1)       CLEAR ECB                D11 ZA13194
         L     R15,SAVGIOCR        SAFE GIOCR ADDR FROM TEB D11 ZA13194
         BALR  R14,R15             BRANCH TO GIOCR          D11 ZA13194
*  END OF HAND-CODED EXPANSION OF GCNTRL MACRO CALL         D11 ZA13194
         SPACE 3                                            D11 ZA13194
         LTR   R15,R15             STARTED OK?              D11 ZA13196
         BNZ   GAS06               NO,SHOW ASYNCH ERR       D11 ZA13196
         WAIT  ECB=(1)
         CLI   IOREQ,COMPLETE               I/O COMPLETED          AHD6
         BNE   GAS06                        BRANCH IF NO           AHD6
         BR    R12
GASCHAIN CLI   TYPE4(RUCB),T2250       IS THIS A 2250            A27773
         BNE   SSM6A                 NO                    D11 @ZA00524
         L     R12,CODETYP         LOAD MASK                       RSTD
         NR    R12,RUCB                                            RSTD
         LTR   R12,R12                                             RSTD
         BNZ   SSM6A                 IS THIS A KYBD ATTN?  D11 @ZA00524
         L     REBRG,TETCB(R11)    LOAD TCB ADDR FIELD             RSTD
         USING TCB,REBRG                                    D11
         L     REBRG,TCBDEB        LOAD DEB QUEUE                  RSTD
         DROP  REBRG                                        D11
GAS130   LTR   REBRG,REBRG         ANOTHER DEB                     RSTD
         BZ    SSM6A                 NO...RETURN           D11 @ZA00524
         L     R15,DEBUCB(REBRG)   LOAD UCB                        RSTD
         LA    R15,0(R15)          ZERO HI ORDER BYTE              RSTD
         CR    R10,R15             SAME UCB                        RSTD
         BE    GAS131              YES                             RSTD
         L     REBRG,DEBDEB(REBRG) LOAD NEXT DECB                  RSTD
         LA    REBRG,0(REBRG)      ZERO HI ORDER BYTE              RSTD
         B     GAS130              CONTINUE SEARCH                 RSTD
GAS131   L     R8,DEBDCB(REBRG)
         LA    R8,0(R8)            ZERO HI ORDER BYTE              RSTD
         XC    IOREQ(32),IOREQ              CLEAR ECB              AHD6
         LA    PAR1,IOREQ          LOAD DECB ADDR                  RSTD
         LA    R9,MI               INPUT AREA ADDR                 RSTD
         BAL   R7,COMNREAD            GO TO EXECUTE GREADR
         TM    MI,CANCELD              TEST FOR CANCEL KEY     RSTD
         BNO   SSM6A                 NO...RETURN           D11 @ZA00524
GAS132   L     PAR0,CKQECORE           LOAD REQUEST FOR GETMAIN
         GETMAIN  R,LV=(0)      GET CORE FOR CKQE
         LR    R10,PAR1                 LOAD ADDR OF CORE FOR CKQE
         XC    ZERO(SIXTN,R10),ZERO(R10)       ZERO CKQE
         STH   RUCB,SIX(R10)            PUT UCB PTR IN CKQE
         LA    SAVEREG,LOCKSAVE       POINT TO SETLOCK SAVE AREA
SSM5     SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,RELATED=(LOCAL,IGG019OE(X
               SSM4)),REGS=SAVE
         LA    SAVEREG,SAVE           RESTORE SAVE AREA POINTER
         L     PAR1,UCBTE(RUCB)        LOAD TE ADDRESS
         ST    R8,CAN(R10)              STORE DCB ADDR IN CKQE
         MVC   ZERO(FOUR,R10),CAN(PAR1)    ADD NEW CKQE TO TOP OF
         ST    R10,CAN(PAR1)              TE CKQE LIST
GAS60    L     R9,ZERO(PAR1)           LOAD CANCEL KEY IRB ADDR
         L     R7,NEXAVL(R9)          GET NEXT AVAILABLE CKIQE
         LA    R7,ZERO(R7)           CLEAR HIGH ORDER BYTE
         LTR   R7,R7                 CKIQE AVAILABLE
         BNE   UPDATE
         OI    FOUR(R10),DQCKQE        CKQE NO LONGER NEEDED
         B     GASHSKP
UPDATE   MVC   NEXAVL(FOUR,R9),ZERO(R7)    UPDATE NEXT AVAIL CKIQE PTR
         ST    R10,FOUR(R7)            PUT CKQE PTR IN CKIQE
         LNR   PAR1,R7                COMPLEMENT IQE ADDR TO PARM LIST
         L     R15,CVTADR              GET CVT TABLE ADDRESS
         L     R15,STAGE2(R15)         GET ADDR OF AEE STAGE 2
         BALR  R14,R15                 LINK TO AEE STAGE 2
         B     GAS18                                        LG @ZA00524
         EJECT
***
*
*              COMMON GREADR FOR READ MANUAL INPUT
*
***
COMNREAD EQU   *
         SPACE 3                                            D11 ZA13194
*  THERE FOLLOWS A HAND-CODED EXPANSION OF THIS CALL OF THE D11 ZA13194
*    GREADR MACRO IN ORDER TO ACCESS A SAFE GIOCR ADDR      D11 ZA13194
*    FROM THE TASK ENTRY BLOCK IN PROTECTED STORAGE.        D11 ZA13194
*        GREADR (1),MIP,(8),(9),MF=E                        D11 ZA13194
         SR    R15,R15             CLEAR REGISTER           D11 ZA13194
         ST    R15,4(0,R1)         CLEAR TYPE AREA IN DECB  D11 ZA13194
         MVI   4(R1),X'08'         MIP TYPE TO DECB         D11 ZA13194
         LA    R15,0(R8)           DCB ADDRESS              D11 ZA13194
         ST    R15,8(0,R1)         DCB ADDRESS IN DECB      D11 ZA13194
         LA    R15,0(R9)           AREA ADDRESS             D11 ZA13194
         ST    R15,12(0,R1)        AREA ADDRESS IN DECB     D11 ZA13194
         XC    0(4,R1),0(R1)       CLEAR ECB                D11 ZA13194
         L     R15,SAVGIOCR        SAFE GIOCR ADDR FROM TEB D11 ZA13194
         BALR  R14,R15             BRANCH TO GIOCR          D11 ZA13194
*  END OF HAND-EXPANSION OF GREADR MACRO CALL               D11 ZA13194
         SPACE 3                                            D11 ZA13194
COMNWAIT LTR   R15,R15                 I/O STARTED
         BNZ   GASHSKP                 NO -- LEAVE
         LA    PAR1,IOREQ
         WAIT  ECB=(1)
         CLI   IOREQ,COMPLETE          I/O GOOD
         BNE   GAS06                   NO -- BRANCH
         BR    R7                     RETURN
         EJECT
ZERO1    DC    1F'0'                   ZERO                        2575
REQUEST  DC    A(164)              WORK AREA DSECT SPACE     LG @ZM2360
CODETYP  DC    X'02'
         DC    AL3(0)
PFKCODE  DC    X'80'
         DC    AL3(0)
DISABLE  DC    X'00'
ENABLE   DC    X'FF'
         DS    0F
INIT     DC    X'00FFFFFF'                                       F40815
CKQECORE DC    X'E9'                   SP 233 FOR CKQE        LB  AOS/1
         DC    AL3(26)                 LENGTH OF CKQE         LI A39419
HOLDREG0 DC    F'0'
         DS    0F
         CNOP  0,8
         EJECT
WORKAREA DSECT
         ORG   WORKAREA+0
RSVD     DS    BL1
OV       DS    BL1
KEY      DS    BL1
TYPE     DS    BL1
MI       DS    0BL3
SENSED   DS    F
XYP      DS    F
         ORG   WORKAREA+16                                       A35022
IOREQ    DS    4D
         ORG   WORKAREA+48                                       A35022
SAVE     DS    F                                                 A35022
CHAIN    DS    F                                                 A35022
         DS    16F
         ORG   WORKAREA+120                                        0803
GMAINADR DS    F                       CORE ADR FOR IQE            0803
         ORG   WORKAREA+124                                      A34795
GMAINLST DS    F                   LENGTH FOR E-TYPE GETMAIN     A34795
         DS    F                   ADDR OF ADDR LIST             A34795
         DS    C                   EC MODE GETMAIN               A34795
         DS    C                   SUB POOL VALUE                A34795
         ORG   WORKAREA+136
IOSTAT   DS    F
LOCKSAVE DS    5F
SAVGIOCR DS    F                    SAVEAREA FOR GIOCR ADDR  LG @ZM2360
         CVT   DSECT=YES
         SPACE 3
CKQE     DSECT
CKQENEXT DS    F                       CHAIN POINTER TO NEXT CKQE
CKQEFLGA DS    XL1                     TWO BYTE SWITCHES USED TO
CKQEGJP  EQU   X'80'                   CALLER IS GJP
CKQEDUMP EQU   X'40'                   CORE DUMP REQUESTED BY USER
CKQEATTN EQU   X'20'                   SYS CAN KEY RTN EXPECTING ATTN
CKQECKPT EQU   X'10'                   CKCB POINTER VALID
CKQERESV EQU   X'08'                   RESERVED
CKQEGJPE EQU   X'04'                   GJP ECB ADDR VALID-GJP WAITING
CKQEDEQ  EQU   X'02'                   DEQUEUE CKQE
CKQEBUF  EQU   X'01'                   2250 BUFFER RUNNING
CKQEFLGB DS    XL1                     COMMUNICATE WITH GAR,GJP ABEND
*                                      HOOK,IFFCAN01 AND IFFCAN02.
CKQEGETA EQU   X'80'                   GETMAIN FOR USER BUFFER
CKQERLOT EQU   X'40'                   USER BUFFER FOLLED OUT
CKQEGETB EQU   X'20'                   GETMAIN 2250 I/P BUFFER AND DECB
CKQEGETC EQU   X'10'                   GETMAIN OPCB,DECB AND O/P LINES
CKQESBD  EQU   X'08'                   SYSBFDMP O/P DCB IS OPEN
CKQEGAR  EQU   X'04'                   ENTRY FROM GAR WITH LP ATTN
CKQEUCB  DS    H                       POINTER TO ASSOCIATED UCB
CKQECKCB DS    F                       POINTER TO CAN KEY CONTROL BLOCK
CKQEDCB  DS    F                       DCB ADDR FOR ASSOCIATED DEVICE
         EJECT
         IHAPSA
         EJECT                                              D11 ZA13195
         IKJTCB                                             D11 ZA13195
         END
