         TITLE 'IGE0010D - LOCAL 5450 ERROR RECOVERY PROCEDURE'
IGE0010D CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0010D                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    LOCAL 5450 ERROR RECOVERY PROCEDURE                              *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS MODULE IS THE ERP FOR LOCALLY ATTACHED DEVICES OF THE       *
*    5450 DISPLAY SYSTEM OR 3066 CONSOLE. IT IS SCHEDULED BY IOS.     *
*    THE FOLLOWING FUNCTIONS ARE PERFORMED BY THIS MODULE:            *
*        * DETERMINES IF AN ERROR IS RETRYABLE BY THE ERP AND RETRIES *
*          THOSE THAT ARE.                                            *
*        * DETERMINES IF ERP RETRIES ARE SUCCESSFUL.                  *
*        * MAINTAINS THE STATISTICS TABLE FOR THE DEVICE.             *
*        * REQUESTS OBR LOGOUT WHEN REQUIRED.                         *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS FOR LOCALLY ATTACHED DEVICES ONLY USING EXCP     *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITIONS' BELOW                             *
*                                                                     *
* PATCH LABEL:                                                        *
*    PACHAREA (40 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        378 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD   *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0010D                                                         *
*    PURPOSE:                                                         *
*        THIS MODULE IS ENTERED WHEN ONE OR MORE OF THE FOLLOWING     *
*        CONDITIONS IS ENCOUNTERED:                                   *
*            UNIT CHECK                                               *
*            UNIT EXCEPTION                                           *
*            INVALID COMMAND SEQUENCE                                 *
*            INCORRECT LENGTH                                         *
*            PROGRAM CHECK                                            *
*            PROTECTION CHECK                                         *
*            CHAINING CHECK                                           *
*            CHANNEL DATA CHECK                                       *
*            INTERFACE CONTROL CHECK                                  *
*            CHANNEL CONTROL CHECK                                    *
*            COMMAND REJECT                                           *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        RECOVERABLE ERRORS:                                          *
*            IOSERR IS SET ON FOR RETRY                               *
*            SVC 15 IS ISSUED                                         *
*            RETURN VIA SVC 3                                         *
*        CORRECTED ERRORS:                                            *
*            IOSEX AND IOSERR ARE SET OFF                             *
*            IOSB IS CLEANED UP                                       *
*            EXIT IS TO IGE0025D TO UPDATE STATISTICS TABLE           *
*        PERMANENT ERRORS:                                            *
*            AN OBR LOGOUT IS SCHEDULED                               *
*            A STATISTIC TABLE UPDATE IS SCHEDULED                    *
*            IOSEX IS SET ON                                          *
*            IOSERR IS SET OFF                                        *
*            EXIT IS TO IGE0025D TO UPDATE STATISTICS TABLE           *
*        PROGRAMMING ERRORS:                                          *
*            SVC 15 IS ISSUED                                         *
*            IOSEX IS SET ON                                          *
*            IOSERR IS SET OFF                                        *
*            EXIT IS VIA SVC 3                                        *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    SUPERVISOR VIA SVC 3                                             *
*    STATISTICS UPDATE ROUTINE (IGE0025D) VIA BRANCH ENTRY TO XCTL    *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        SVC 15 ERROR EXCP                                            *
*    DATA AREAS                                                       *
*        CHANNEL PROGRAM                                              *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB          (MAPPED BY IEFUCBOB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        RQE          (MAPPED BY IECDRQE)                             *
*        IOB          (MAPPED BY IEZIOB)                              *
*        DCB          (MAPPED BY DCBD)                                *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02594K (11/6/73)                                               *
*    OZ25331 (09/15/77)           OZ26007 (09/15/77)                  *
*    OZ29608 (02/15/78)           OZ33003 (06/05/78)                  *
***********************************************************************
         EJECT
*/*IGE0010D: CHART */
*/* E IGE0010D */
*/* D (YES,,NO,ERR603) EXCP */
*/* D (YES,,NO,START) FIRST ENTRY */
*/* P CLEAR EWASTUP */
*/*START: P SET ERP IN CONTROL */
*/* D (YES,ERR670,NO,) IOSB INTERCEPT */
*/* D (YES,ERR630,NO,) ICC OR CCC */
*/* D (YES,,NO,ERR600) CDC */
*/* D (YES,ERR603,NO,) CDC RETRIED */
*/* P (,ERR650) SET CDC RETRIED */
*/*ERR600: D (YES,,NO,ERR618) UNIT CHECK */
*/*ERR601: S INTERPRETER RTN: CHECK SENSE AND BRANCH */
*/* D (YES,ERR606,NO,) BUS OUT CHECK */
*/* D (YES,ERR605,NO,) DATA CHECK */
*/* D (YES,ERR607,NO,) BUF ADDR PARITY */
*/* D (YES,ERR608,NO,) INVALID NUF ADDR */
*/* D (YES,ERR609,NO,) COMMAND REJECT */
*/* P (,ERR603) TEST END, PERM ERROR */
*/*ERR603: P INDICATE LOGOUT REQUIRED */
*/* P INDICATE PERMANENT ERROR */
*/* P (,ERR626) RESET RETRY FLAGS */
*/*ERR626: P MOVE SENSE TO STAT TABLE ROUTINE COMM AREA */
*/* P LOAD STAT UPDATE ROUTINE ID */
*/* R BRANCH REG 14 */
*/*ERR606: D (YES,ERR603,NO,) BUS OUT RETRIED */
*/* P (,ERR650) SET BUS OUT RETRIED */
*/*ERR650: S SVC 15: ERROR EXCP */
*/* R SVC 3 */
*/*ERR605: D (YES,ERR603,NO,) DATA CHK RETRIED */
*/* P (,ERR650) SET DATA CHECK RETRIED */
*/*ERR607: D (YES,ERR603,NO,) ADDR PARITY RETRIED */
*/* P (,ERR650) SET ADDR PARITY RETRIED */
*/*ERR608: D (YES,ERR603,NO,) BUF ADDR RETRIED */
*/* P (,ERR650) SET BUFFER ADDRESS RETRIED */
*/*ERR609: D (YES,ERR603,NO,) COMM REJ RETRIED */
*/* P (,ERR650) SET COMMAND REJECT RETRIED */
*/*ERR618: D (YES,,NO,ERR603) UNIT EXCEPTION? */
*/* D (YES,,NO,ERR621) PROT OR PROG CHK */
*/* P (,ERR650) INDICATE PERMANENT ERROR */
*/*ERR670: D (YES,,NO,ERR603) UNIT CHECK */
*/* D (YES,,NO,ERR603) BAD SENSE */
*/* D (YES,,NO,ERR603) EQUIPT CHECK */
*/* P SET EQUIPT CHECK IN STAT TBL */
*/* P CLEAR INTERCEPT INDICATOR */
*/* P CLEAR PERM ERROR IN IOSB */
*/* P CLEAR SENSE IN IOSB */
*/* P MODESET TCB KEY */
*/* P CLEAR DCB ERROR FLAGS */
*/* P (,ERR650) MODESET SUPVR KEY */
*/*ERR621: P MODESET TCB KEY */
*/* P RESET ERROR FLAGS IN DCB */
*/* P MODESET SUPVR KEY */
*/* P RESET ERROR FLAGS IN IOB AND IOSB */
*/* P CLEAR RETRY FLAGS */
*/* P (,ERR626) RESET UNIT CHECK IN CSW */
*/*ERR630: D (YES,ERR685,NO,) ICC OR CCC RETRIED */
*/* D (YES,ERR685,NO,) IOSB INTERCEPT */
*/* D (YES,ERR685,NO,) UNCOND NO RETRY */
*/* P SET ICC OR CCC RETRIED */
*/* P (,ERR650) ZERO ERPIB */
*/*ERR685: P (,ERR603) ZERO ERPIB */
         EJECT
         USING UCBSET,XUCB
         USING CVTMAP,XVEC
         USING IOSB,XIOSB
         USING EWA,XERP
         USING *,PBASE
         LR    XIOSB,XPARM         BASE IOSB
         L     XERP,IOSERP         BASE ERP WORKAREA
         L     XUCB,IOSUCB         BASE UCB
         L     XVEC,CVTPTR         BASE CVT
         L     XER11,IOSUSE        BASE RQE                 E12 ZA29608
         USING RQE,XER11                                    E12 ZA29608
         L     XER4,RQEIOB         BASE IOB
         USING IOBSTDRD,XER4
         CLI   IOSDVRID,IOSXCPID   EXCP I/O
         BNE   ERR603              NO, PERM ERROR
         L     XER2,IOBDCBPT       BASE DCB                 E12 ZA26007
         USING IHADCB,XER2                                  E12 ZA26007
         TM    IOSFLA,IOSERR       FIRST ENTRY
         BO    START               NO
         XC    EWASTUP(N2),EWASTUP   CLEAR STAT UPDATE AREA
START    EQU   *
         OI    IOSFLA,IOSEX+IOSERR   INDICATE ERP IN CONTROL
         OI    IOBFLAG1,IOBERRTN+IOBIOERR   ALSO IN IOB
         LA    XLNK,ERR618         SET ADDR TO SVC 15/SVC3 RETURN
         CLI   IOSCOD,IOSFINTC     INTERCEPT CONDITION
         BE    ERR670              YES, ANALYZE             E12 ZA26007
         TM    IOSTSB,CSWCCC+CSWICC   ICC OR CCC
         BNZ   ERR630              YES, CHECK CHANNEL ERROR
         TM    IOSTSB,CSWCDC       CHANNEL DATA CHECK
         BNO   ERR600              NO, CHECK FOR UNIT CHECK
         OI    EWASTUP+N1,CDCBIT   SET CDC FOR STAT UPDATE
         TM    EWACNTR1,IOBRTCDC   HAS CHAN DATA CHECK BEEN RETRIED
         BO    ERR603              YES, EXIT
         OI    EWACNTR1,IOBRTCDC   TURN ON RETRY FLAG
         B     ERR650              GO RETRY
ERR600   EQU   *
         TM    IOSTSA,CSWUCK       UNIT CHECK
         BNO   ERR618              NO, CHECK STATUS INFO
         LR    XPARM,XIOSB         SET UP IOSB POINTER
         LR    XSAV,PBASE          YES, CHECK SENSE
         L     PBASE,CVTXITP       POINT TO RESIDENT INTERPRETER RTN
ERR601   EQU   *
         BALR  XCTL,PBASE          LINK TO INTERPRET SENSE INFO
         DC    X'02'               BUS OUT CHECK
         DC    AL1(ERR606-ERR601-N2)   ADDRESS TO BRANCH TO
         DC    X'04'               DATA CHECK
         DC    AL1(ERR605-ERR601-N4)   ADDRESS TO BRANCH TO
         DC    X'07'               BUFFER ADDRESS PARITY CHECK
         DC    AL1(ERR607-ERR601-N6)   ADDRESS TO BRANCH TO
         DC    X'06'               INVALID BUFFER ADDRESS
         DC    AL1(ERR608-ERR601-N8)   ADDRESS TO BRANCH TO
         DC    X'00'               COMMAND REJECT
         DC    AL1(ERR609-ERR601-N10)   ADDRESS TO BRANCH TO
         DC    X'2F'                TEST END - PERMANENT ERROR
         DC    AL1(ERR603-ERR601-N12)   ADDRESS TO BRANCH TO
ERR603   EQU   *
         OI    IOSFLB,IOSLOG        SET LOG OUT FLAG
         NI    IOSFLA,N255-IOSERR   MARK AS PERMANENT ERROR
         NI    IOBFLAG1,N255-IOBERRTN   ALSO IN IOB
         NI    EWACNTR1,N255-IOBCHN-IOBRTCDC   RESET CHAN RETRY FLAGS
ERR626   EQU   *
         OC    EWASTUP(N1),IOSSNS   SET SENSE IN COMMUNICATIONS AREA
         L     XCTL,CVTXTLER       GET BRANCH ENTRY ADDRESS
         LA    XNAME,STATUP        ADDRESS OF STAT ROUTINE
         BR    XCTL                XCTL TO STATISTICS UPDATE ROUTINE
ERR606   EQU   *
         TM    EWACNTR1,IOBBOE     BUS OUT RETRIED
         BO    ERR603              YES, CONSIDER PERM ERROR
         OI    EWACNTR1,IOBBOE     INDICATE BUS OUT RETRY
ERR650   EQU   *
         SVC   EXCPER              ERROR EXCP
         SVC   ERRETURN            RETURN
ERR605   EQU   *
         TM    EWACNTR1,IOBDCE     DATA CHECK RETRIED
         BO    ERR603              YES, PERM ERROR
         OI    EWACNTR1,IOBDCE     INDICATE DATA CHECK RETRY
         B     ERR650              GO RETRY
ERR607   EQU   *
         TM    EWACNTR1,IOBBPC     BAD ADDRESS PARITY RETRIED
         BO    ERR603              YES, PERM ERROR
         OI    EWACNTR1,IOBBPC     INDICATE RETRY
         B     ERR650              GO RETRY
ERR608   EQU   *
         TM    EWACNTR1,IOBIBA     BAD BUFFER ADDRESS RETRIED
         BO    ERR603              YES, PERM ERROR
         OI    EWACNTR1,IOBIBA     INDICATE RETRY
         B     ERR650              GO RETRY
ERR609   EQU   *
         TM    EWACNTR1,IOBCRJ     COMMAND REJECT RETRIED
         BO    ERR603              YES, PERM ERROR
         OI    EWACNTR1,IOBCRJ     INDICATE RETRY
         B     ERR650              GO RETRY
ERR618   EQU   *
         TM    IOSTSB,CSWUEX       UNIT EXCEPTION           E12 ZA25331
         BO    ERR603              YES, NO RETRY            E12 ZA25331
         TM    IOSTSB,CSWPC+CSWPCK   PROTECTION OR PROGRAM CHECK
         BZ    ERR621              NO, THEN ENTERED FOR NORMAL END
         NI    IOSFLA,N255-IOSERR   SET  PERM ERROR
         NI    IOBFLAG1,N255-IOBERRTN   ALSO IN IOB
         B     ERR650              ISSUE SVC 15 AND SVC 3
ERR670   EQU   *                   INTERCEPT RETRY          E12 ZA26007
         TM    IOSTSA,CSWUCK       UNIT CHECK SENSE         E12 ZA26007
         BNO   ERR603              NO, PERM ERROR           E12 ZA26007
         TM    EWAFLG1,EWABDSNS    BAD SENSE                E12 ZA26007
         BO    ERR603              YES, CAN'T RETRY         E12 ZA26007
         CLI   IOSSNS,SENEC        EQUIPT CHECK             E12 ZA26007
         BNE   ERR603              NO, PERM. ERROR          E12 ZA26007
         OC    EWASTUP(N1),IOSSNS  SET SENSE IN COM AREA    E12 ZA26007
         MVI   IOSCOD,IOSNRMC      CLEAR INTCPT INDICATOR   E12 ZA26007
         NI    IOSFLA,N255-IOSEX   CLR PERM FLAG            E12 ZA26007
         NI    IOBFLAG1,N255-IOBIOERR  ALSO IN IOB          E12 ZA26007
         MVI   IOSSNS,ZERO         CLEAR SENSE              E12 ZA26007
         L     XER12,RQETCB         TCB ADDR IN RQE         E12 ZA29608
         DROP  XER11                                        E12 ZA29608
         USING TCB,XER12                                    E12 ZA29608
         MODESET EXTKEY=TCB,WORKREG=11                      E12 ZA29608
         MVI   IOBSENS0,ZERO       CLEAR SENSE IN IOB       E12 ZA33003
         NI    DCBIFLGS,N255-DCBIFEC CLEAR DCB ERR FLAGS    E12 ZA26007
         DROP  XER2                DCB NO LONGER NEEDED     E12 ZA26007
         MODESET EXTKEY=SUPR                                E12 ZA29608
         B     ERR650              EXIT TO RETRY            E12 ZA26007
ERR621   EQU   *
         USING RQE,XER11                                    E12 ZA29608
         L     XER12,RQETCB             TCB FROM RQE        E12 ZA29608
         DROP  XER11                                        E12 ZA29608
         USING TCB,XER12                                    E12 ZA29608
         MODESET EXTKEY=TCB,WORKREG=11                      E12 ZA29608
         USING IHADCB,XER2
         NI    DCBIFLGS,N255-DCBIFEC   RESET ERROR INDICATORS
         DROP  XER2
         MODESET EXTKEY=SUPR                                E12 ZA29608
         NI    IOSFLA,N255-IOSERR-IOSEX   RESET ERROR AND RESTART
         NI    IOBFLAG1,N255-IOBERRTN-IOBIOERR   ALSO IN IOB
         MVI   EWACNTR1,N0         CLEAR ERROR COUNTS
         NI    IOSTSA,N255-CSWUCK   RESET CSW UNIT CHECK
         B     ERR626              GO TO STATISTIC UPDATE ROUTINE
ERR630   EQU   *
         TM    EWACNTR1,IOBCHN     ICC OR CCC RETRIED
         BO    ERR685              YES
         CLI   IOSCOD,IOSFINTC     IOSB INTERCEPTED
         BE    ERR685              YES
         TM    EWARGFG1,EWANORTY   UNCONDITIONAL NO RETRY
         BO    ERR685              YES
         OI    EWACNTR1,IOBCHN     SET RETRY
         XC    EWAERPIB(N8),EWAERPIB   CLEAR ERPIB
         B     ERR650              GO ISSUE RETRY AND RETURN
ERR685   EQU   *
         XC    EWAERPIB(N8),EWAERPIB   CLEAR ERPIB
         B     ERR603              GO LOG OUT AND EXIT
         EJECT
**********
*
*        CONSTANTS
*
**********
PACHAREA EQU   *
         DC    35C'010D'           PATCH AREA FOR MAINT     E12 ZA26007
         SPACE 2
**********
*
*        REGISTER DEFINITIONS
*
**********
XER0     EQU   0                   WORK REG
XPARM    EQU   1                   PARAMETER REG
XER2     EQU   2                   WORK REG
XERP     EQU   3                   ERP WORKAREA BASE
XER4     EQU   4                   WORK REG
XER5     EQU   5                   WORK REG
XUCB     EQU   6                   UCB BASE
XLNK     EQU   7                   LINK REG
XVEC     EQU   8                   CVT BASE
XSAV     EQU   9                   SAVE REG
XIOSB    EQU   10                  IOSB BASE
XER11    EQU   11                  WORK REG
XER12    EQU   12                  WORK REG
XNAME    EQU   13                  XCTL NAME
XCTL     EQU   14                  XCTL ENTRY POINT
PBASE    EQU   15                  PROGRAM BASE
         SPACE 2
**********
*
*        MISCELLANEOUS EQUATES
*
TCBADR   EQU   0                   CVT DOUBLE WORD          E12 ZA29608
CURRTCB  EQU   4                   ADDRESS CURRENT TCB      E12 ZA29608
**********
CDCBIT   EQU   X'01'               STAT TABLE CDC ERROR BIT
EXCPER   EQU   15                  ERROR EXCP (SVC 15)
ERRETURN EQU   3                   RETURN TO SUPERVISOR (SVC 3)
N0       EQU   0                   ZERO
N1       EQU   1                   ONE
N2       EQU   2                   TWO
N4       EQU   4                   FOUR
N5       EQU   5                   FIVE
N6       EQU   6                   SIX
N8       EQU   8                   EIGHT
N10      EQU   10                  TEN
N12      EQU   12                  TWELVE
N255     EQU   255                 AND MASK
SENEC    EQU   X'10'               EQUIPTMENT CHECK SENSE   E12 ZA26007
STATUP   EQU   254                 STATISTIC UPDATE ROUTINE
ZERO     EQU   X'00'               ONE BYTE OF ZEROS        E12 ZA26007
         SPACE 2
**********
*
*        WORK AREA RETRY FLAGS
*
**********
IOBCHN   EQU   X'80'               ICC/CCC
IOBRTCDC EQU   X'40'               CHANNEL DATA CHECK
IOBBOE   EQU   X'20'               BUSOUT ERROR
IOBDCE   EQU   X'10'               DATA CHECK
IOBBPC   EQU   X'08'               BUFFER ADDRESS PARITY
IOBIBA   EQU   X'04'               INVALID BUFFER ADDRESS
IOBCRJ   EQU   X'02'               COMMAND REJECT
         SPACE 2
**********
*
*        CSW STATUS INDICATORS
*
**********
CSWPC    EQU   X'20'               PROGRAM CHECK
CSWPCK   EQU   X'10'               PROTECTION CHECK
CSWUCK   EQU   X'02'               UNIT CHECK
CSWCCC   EQU   X'04'               CHANNEL CONTROL CHECK
CSWICC   EQU   X'02'               INTERFACE CONTROL CHECK
CSWCDC   EQU   X'08'               CHANNEL DATA CHECK
CSWUEX   EQU   X'01'               UNIT EXCEPTION
         EJECT
UCBSET   DSECT
         IEFUCBOB
         EJECT
         IECDERWA
         EJECT
CVTSET   DSECT
         CVT
         EJECT
         IECDIOSB
         EJECT
         IEZIOB
         EJECT
         IECDRQE
         EJECT
         DCBD
         EJECT                                              E12 ZA29608
         IKJTCB                                             E12 ZA29608
         END
