         TITLE 'IGG019OC - GRAPHICS PAGE FIX APPENDAGE'
***********************************************************************
*                                                                     *
*                   GRAPHIC PAGE FIX APPENDAGE                        *
*                                                                     *
* STATUS CHANGE LEVEL: 0                                              *
*                                                                     *
* FUNCTION: RETURN THE VIRTUAL BOUNDS OF THE 'DECB' IN THE IOS PAGE   *
*           FIX WORK AREA TO BE FIXED BY IOS IN ORDER TO PREVENT      *
*           PAGING EXCEPTIONS FROM OCCURRING IN THE CE-XCE APPENDAGE  *
*           AND IN THE 2250/2260 ERP.                                 *
*                                                                     *
* ENTRY POINT: IGG019OC VIA BALR 14,15+4 IN IOS                       *
*                                                                     *
* INPUT: REG2:  ADDRESS OF THE INPUT/OUTPUT BLOCK (IOB)               *
*        REG10: IOS SEVEN DOUBLEWORDS WORK AREA.
*                                                                     *
* OUTPUT:REG10: ADDRESS OF THE FIRST ENTRY IN THE IOS WORK AREA       *
*        REG11: NUMBER OF ENTRIES PUT INTO THE WORK AREA              *
*                                                                     *
*        EACH ENTRY IN THE IOS PAGE FIX WORK AREA IS EIGHT BYTES IN   *
*        LENGTH:                                                      *
*                                                                     *
*              BYTE   BIT     MEANING                                 *
*                                                                     *
*              0              MUST BE ZERO                            *
*              1-3            START VIRTUAL ADDR OF AREA TO BE FIXED  *
*              4      0       IF ON INDICATES LAST ENTRY              *
*              5-7            END VIRTUAL ADDR OF AREA TO BE FIXED + 1*
*                                                                     *
* EXTERNAL ROUTINES: N/A                                              *
*                                                                     *
* EXITS: NORMAL                                                       *
*                                                                     *
* TABLES: N/A                                                         *
*                                                                     *
* WORK AREAS: N/A                                                     *
*                                                                     *
* ATTRIBUTES: PROBLEM PROGRAM RESIDENT, SUPERVISOR ENTERED            *
*                                                                     *
* NOTES: 20 BYTES X'FF' LABELLED PATCH  RESERVED FOR MAINTENANCE      *
*                                                                     *
***********************************************************************
         SPACE 3
*A622000-628000,A901000-908000                              D11 ZA10012
         SPACE 3
         EJECT
*
*
*              REGISTERS USED IN THIS ROUTINE
*
*
RIOB     EQU   2                        ADDRESS OF IOB
RWRKAREA EQU   10                       ADDRESS OF IOS WORK AREA
RENTRIES EQU   11                       NUMBER OF ENTRIES IN WORK AREA
RWORK    EQU   11                       WORK REGISTER
RRETURN  EQU   14                       RETURN REGISTER
*
*
*              EQUATES
*
*
ZERO     EQU   0
ONE      EQU   1
TWO      EQU   2                                            D11 ZA10012
FOUR     EQU   4
EIGHT    EQU   8                                            D11 ZA10012
TWELVE   EQU   12                                           D11 ZA10012
TWENTY   EQU   20                                           D11 ZA10012
TWENTY8  EQU   28                                           D11 ZA10012
THIRTWO  EQU   32
X80      EQU   X'80'
*
*
IGG019OC CSECT
*
*        SINCE GAM DOES NOT HAVE AN SIO APPENDAGE, THE FIRST 4 BYTES
*        OF THE PAGE FIX APPENDAGE WILL CONTAIN A BR 14 TO CAUSE NORMAL
*        PROCESSING OF THE I/O REQUEST TO CONTINUE
*
         BR    RRETURN                  RETURN TO IOS
         DC    X'0000'
*
*        PAGE FIX APPENDAGE ROUTINE
*
         L     RWORK,FOUR(RIOB)         ADDRESS OF ECB IN IOB
         LA    RWORK,ZERO(RWORK)        CLEAR HIGH ORDER BYTE
*
*        BECAUSE THE ECB IS THE FIRST WORD OF THE DECB, THIS IS THE
*        STARTING VIRTUAL ADDRESS OF THE AREA TO BE FIXED
*
         ST    RWORK,ZERO(RWRKAREA)     STORE IN IOS WORK AREA
*
*        THE DECB IS 32 BYTES LONG. DECB+32 SHOULD BE THE ENDING
*        VIRTUAL ADDRESS OF THE AREA TO BE FIXED PLUS ONE
*
         LA    RWORK,THIRTWO(RWORK)     DECB + 32
         ST    RWORK,FOUR(RWRKAREA)     STORE IN IOS WORK AREA
         L     RWORK,TWENTY(RIOB)  POINT TO DCB             D11 ZA10012
         L     RWORK,TWENTY8(RWORK)  POINT BACK TO 1ST IOB  D11 ZA10012
         BCTR  RWORK,0             MAKE SURE PAGE FIXED     D11 ZA10012
         ST    RWORK,EIGHT(RWRKAREA) SAVE START 1STIOB      D11 ZA10012
         LA    RWORK,47(RWORK)     BUMP TO END ADDR         D11 ZA10012
         ST    RWORK,TWELVE(RWRKAREA) SAVE END 1STIOB       D11 ZA10012
         OI    TWELVE(RWRKAREA),X80     INDICATE LAST ENTRY D11 ZA10012
         LA    RENTRIES,TWO        SET R11 W NUMENTRIES     D11 ZA10012
         BR    RRETURN                  RETURN TO IOS
PATCH    DS    0F                                           D11
         DC    20X'FF'                 MAINTENANCE SPACE    D11
         END
