         TITLE 'DEQUEUE ROUTINE'                            D11
*TITLE 'DEQUEUE ROUTINE'
*STATUS:CHANGE LEVEL 0
*FUNCTION:PERFORM MODIFICATIONS TO PROTECTED CORE FOR ATTENTION
*   INQUIRY ROUTINE.
*        1.CLEAR ALL DATA FROM REQUESTING ROUTINE'S         D11
*        INTERNAL QUEUE.(NOTE: ONE IQE MUST BE LEFT         D11
*        FOR SYSTEM TO REMOVE)
*        2.SELECTIVELY DEQUEUE DATA FROM REQUESTOR'S IRB OR INTERNAL
*        QUEUE AND MAKE DATA AVAILABLE IN COMAREA.
*        3.UPDATE ALL MANIPULATED QUEUES.
*        4.NOTIFY ATTENTION INQUIRY ROUTINE AS TO WHETHER DATA WAS
*        FOUND.
*ENTRY:VIA SVC 75 FROM ATTENTION INQUIRY ROUTINE
*INPUT:MODE=CLEAR:
*       1.GR#0=RBIQE ADR
*       2.GR#1=GACB  ADR
*      ALL OTHER MODES:
*       1.GR0=0
*       2.GR1-HIORDER BYTE-INDEX TO 2260 FROM WHICH         D11
*                          DATA IS TO BE DEQUEUED.          D11
*            -LO-ORDER BYTES=GACB ADDRESS                   D11
*OUTPUT:MODE CLEAR:
*       1.DATA REMOVED
*         INVALID DCB,DEB,GACB,IQE RC=8                     D11 ZA04885
*       ALL OTHER MODES:
*       1.DATA TO COMAREA AND RC=X'04'
*       2.RC=X'00'
*       ALL MODES; INV DCB,DEB,GACB ADDR RC X'08'           D11 ZA04885
*EXTERNAL ROUTINES:N/A
*EXIT:VIA SVC 3
*TABLES/WORK AREAS:N/A
*ATTRIBUTES:1.PRIVILEDGED
*           2.RE-ENTRANT
*           3.SUPERVISOR STATE
*           4.OPERATES BOTH WITH/WITHOUT LOCAL LOCK HELD.   D11
*NOTE:N/A
           EJECT                                            E12 ZA20103
IGC0007E CSECT
*
* 2342,0390-0392,0400                                              6999
* 036600,038600
* C016720,A017620-017712,A021104,D026450-026600,D028400,     LD YA01241
* A028540,D034450-034600,D036600-036660,D036860-036920,      LD YA01241
* C043400,A043520,D044570,A049900                            LD YA01241
* A017680,A017720                                             LD YM5168
* D27000-28200                                              LG @ZA00524
*A017705-017849,A034500-034720,A026804                       LG @ZM2361
*A026500-026740,A034900,A054720,A004900                      LG @ZM2361
*A016860-016920                                              LG @ZM2361
*A004300,C004900,A008700,016962-016990,C017789,017809,      D11 ZA04885
*A017810-017819,C017829,A036808-036976,C037000              D11 ZA04885
*A000100,050000-050100                                      D11
*C016985,017818,036920                                      D11 ZA10042
*C017813                                                    D11 ZA13202
*A006404,A006780,A016994,C036864                            E12 ZA20103
*A010250,C017804-017807,C017813,C017819,C019600-0200000     E12 ZA26486
*D020960,C021000,A028850,D029000,C029200,C036816,C0378-0382 E12 ZA26486
*C040000,C041200,D042280,C042300-042320,D042400,C0426-0430  E12 ZA26486
*C043800-044200,C044450-044500                              E12 ZA26486
*C017794                                                    E12 ZA27737
*C17789-17792,C17796-17800                                  E12 ZA27738
*C018400,A018402-018414,C018600,C036808,D036816-036984      E12 ZA30227
*C037200                                                    E12 ZA30227
*
         EJECT                                              E12 ZA20103
*        REGISTER DEFINITION
*
PAR0     EQU   0                       PARAM  REG 0
PAR1     EQU   1                       PARAM  REG 1
RBASE    EQU   2                       BASE REG
RBIQE    EQU   3                       RB IQE ADR REG
RIQE     EQU   4                       IQE ADR REG
RGACB    EQU   5                       GACB ADR REG
RPIQE    EQU   6                       PREVIOUS IQE ADR
BACKREG  EQU   7                       RETURN INTERNAL
R1       EQU   1                       USED TO VALCHK PARMS D11 ZA04885
R8       EQU   8                       WORK REG
R9       EQU   9                       WORK REG
R10      EQU   10                      WORK REG
R11      EQU   11                      WORK REG
R12      EQU   12                      WORK REG
SAVRG    EQU   13                      SAVE AREA PTR REG
RTNRG    EQU   14                      RETURN REG
RCODE    EQU   15                      RETURN CODE REG
REBREG   EQU   8                       REB ADDRESS REG      E12 ZA26486
*
*        TYPE FIELD OF IQE
*
END      EQU   X'01'                   END CODE
PFKB     EQU   X'02'                   PF KEY CODE
LP       EQU   X'03'                   LP CODE
EOS      EQU   X'04'                   END ORDER SEQUENCE CODE
CANCEL   EQU   X'05'                   CANCEL KEY CODE
AE       EQU   X'06'                   ASYM ERROR CODE
A2260    EQU   X'07'                   2260 CODE
*
*        GACB ATNTYP FIELD
*
GEND     EQU   X'01'                   END BIT
GLP      EQU   X'02'                   LIGHT PEN BIT
GEOS     EQU   X'04'                   END ORDER SEQUENCE BIT
GCANCEL  EQU   X'08'                   CANCEL BIT
GAE      EQU   X'10'                   ASYM ERROR BIT
T2260    EQU   X'20'                   2260 BIT
*
*
*
ZERO     EQU   0                       ZERO
RTNIRB   EQU   16                      IRB ADR
RBIQES   EQU   24                      IQE ADR CHAIN
RTNQ1    EQU   24                      REB Q1 FIELD                2575
EXIT     EQU   3                       EXIT SVC NO.
ONE      EQU   1                       ONE
TWO      EQU   2                       TWO
FOUR     EQU   4                       FOUR
NEXAVL   EQU   96                      NEXT FREE IQE FLD
RTNQ2    EQU   28                      ADR OF FIRST IQE
ALL      EQU   X'FF'                   ALL 2260 OPTION
XDUMMY   EQU   X'00'                   IMMED. OPERAND         LB  AOS/1
COMEND   EQU   11                      COMAREA DISP(END)      LB  AOS/1
CVTPTR   EQU   16                    CVT ADDRESS             LG @ZM2361
TCBPTR   EQU   0                     CVT DLBLWD              LG @ZM2361
CURRTCB  EQU   4                     ADDR CURR TCB           LG @ZM2361
RC8      EQU   8                     RETCD OF 8              LG @ZM2361
TEREB    EQU   4                   REBPTR FROM TEB          D11 ZA04885
RTNGACB  EQU   12                  GACBPTR FROM REB         D11 ZA04885
DCBDEB   EQU   45                  DEBPTR FROM DCB          D11 ZA04885
UCBTEB   EQU   28                  TEBPTR FROM UCB          D11 ZA04885
DEBUCB   EQU   33                  UCBPTR FROM DEB          D11 ZA04885
REBGACB  EQU   40                  REBPTR FROM GACB         D11 ZA04885
RTNF     EQU   0                   NXTREB PTR FROM REB      D11 ZA10042
RWORK    EQU   15                  VALIDATE WORKREG         D11 ZA04885
M7       EQU   7                   MASK VALUE 7             E12 ZA20103
*
         BALR  RBASE,0                 SET UP BASE REG      D11
         USING *,RBASE                 DEFINE BASE REG
         USING GACB,RGACB              DEFINE DSECT REG
         LR    RGACB,PAR1         INIT GACB REG             D11  YM5168
         LR    RBIQE,PAR0          PARAM TO WORK REGISTER      YA01241
         USING IQEFLDS,RIQE            DEFINE DSECT REG     D11
         L     R8,DCB              LOAD DCB ADDRESS         D11 ZA13202
         LA    R8,ZERO(R8)           CLEAR HI ORDER BYTE     LG @ZM2361
         DEBCHK  (R8),TYPE=VERIFY,AM=GAM                     LG @ZM2361
         LTR   RWORK,RWORK         RC= ZERO                 E12 ZA27738
         BNZ   VALERR              DEB IS INVALID           E12 ZA27738
         ICM   RWORK,M7,DEBUCB(PAR1) GET UCB FROM DEB       E12 ZA27737
         L     RWORK,UCBTEB(,RWORK) POINT TO TEB            E12 ZA27738
         LTR   RWORK,RWORK             IS THERE ONE?        E12 ZA27738
         BZ    VALERR                NO, UCB INVALID        E12 ZA27738
         L     REBREG,TEREB(,RWORK)  POINT TO FIRST REB     E12 ZA26486
NREB1    LTR   REBREG,REBREG       IS THERE ONE?            E12 ZA26486
         BZ    VALERR              PASSED GACB NOT FOUND    D11 ZA04885
         CLM   REBREG,7,REBADR+1   THIS PASSED GACB?        E12 ZA26486
         BE    SSM1OFF             YES,GO SET LOCK          D11 ZA04885
         L     REBREG,RTNF(,REBREG)   POINT TO NEXT REB     E12 ZA26486
         B     NREB1               AND GO CHECK IT          D11 ZA04885
VALERR   LA    RCODE,RC8             LD RETCD OF 8          D11 ZA04885
         SVC   EXIT                  RETURN                  LG @ZM2361
SSM1OFF  SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,RELATED=(LOCAL,IGC0007E(X
               SSM1ON)),REGS=USE                                YA01241
         LR    PAR1,RGACB              RESTORE REG1         D11  YM5168
         LTR   RBIQE,RBIQE             RBIQE REG=0?
         BZ    NOTCLEAR                BRANCH IF ZERO NO RB E12 ZA30227
         L     RPIQE,RTNIRB(0,REBREG)  GET IRB ADDR FR REB  E12 ZA30227
         LA    RPIQE,RBIQES(0,RPIQE)   LOAD PTR TO RBIQEFLD E12 ZA30227
         CLR   RPIQE,RBIQE             VALID PARM PASSED    E12 ZA30227
         BE    DECLEAR                 YES GO TO CLEAR FUNC E12 ZA30227
         BAL   BACKREG,SSM1ON          RELEASE LOCK         E12 ZA30227
         LA    RCODE,RC8               PASS BACK RET CODE=8 E12 ZA30227
         SVC   EXIT                    RETURN               E12 ZA30227
NOTCLEAR LR    SAVRG,PAR1              OFFSET TO SAVE REG   E12 ZA30227
         SRL   SAVRG,24                ZERO
         LA    RTNRG,ZERO              CLEAR REG
*                                      CLEAR FUNCTION
         LA    RPIQE,ZERO              INITIALIZE REG
CHECK    EQU   *                                            E12 ZA26486
         L     RIQE,RTNIRB(0,REBREG)   LOAD IRB ADR         E12 ZA26486
         L     RIQE,RBIQES(0,RIQE)     LOAD PTR TO RBIQEFLD E12 ZA26486
DE04     LA    RIQE,ZERO(0,RIQE)       ZERO HI ORDER BYTE
         LTR   RIQE,RIQE               PTR=0
         BNZ   DE02                    BRANCH IF NO
         B     DE01                BRANCH TO INT. QUEUE TEST
NODATA   LTR   PAR1,PAR1               HAS Q1 FIELD BEEN TESTED    2575
         BZ    DE13                    BRANCH-Q1 HAS BEEN TESTED   2575
         SR    PAR1,PAR1               CLEAR REGISTER SWITCH       2575
*        L     R8,REBADR   LOAD REB ADR LINE DELETED BY---> E12 ZA26486
         L     RIQE,RTNQ1(REBREG)      LOAD PTR TO FST IQE  E12 ZA26486
         B     DE08                    BRANCH TO TEST FOR DATA     2575
DE13     EQU   *                                                   2575
         BAL   BACKREG,SSM1ON      RELEASE LOCAL LOCK           YA01241
         LA    RCODE,ZERO              RETURN CODE=ZERO            2575
         SVC   EXIT                    RETURN TO IOS
DE02     LR    RPIQE,RIQE              SAVE PRESENT IQE ADR
         L     RIQE,IQELNK             LOAD NEXT IQE ADR
         LA    RIQE,ZERO(0,RIQE)       ZERO HI ORDER BYTE
         LTR   RIQE,RIQE               PTR=0
         BZ    DE01                BRANCH IF YES
         BAL   BACKREG,CONVERT         TO TYPE CONVERSION
         L     R11,PFKMSK              LOAD PF MASK FROM GACB
         L     R12,ATNTYP              LOAD ATNTYP MASK FROM GACB
         NR    R11,R9                  CHECK FOR MATCH
         LTR   R11,R11                 WAS THERE A MATCH
         BNZ   DE03                    BRANCH IF YES
         NR    R12,R10                 CHECK FOR MATCH
         LTR   R12,R12                 WAS THERE A MATCH
         BNZ   DE03                    BRANCH IF YES
         B     DE02                    BRANCH
DE03     CLI   TYPE,A2260              IS THIS A 2260 ATTN
         BNE   DE03A                   BRANCH IF NO
         CLR   RTNRG,SAVRG             OFFSETS MATCH
         BE    DE03A                   BRANCH IF YES
         LA    RTNRG,ALL               LOAD ALL TYPE
         CLR   RTNRG,SAVRG             OFFSETS MATCH
         BNZ   DE02                    BRANCH IF NO
DE03A    L     R9,COMARA               LOAD COMAREA ADR
         LA    R9,ZERO(0,R9)           ZERO HI ORDER BYTE
         LTR   R9,R9                   PTR=0
         BZ    NODATA                  BRANCH IF YES
         L     R12,CVTPTR            CVT ADDRESS             LG @ZM2361
         L     R12,TCBPTR(R12)       TCB DLBLWORD            LG @ZM2361
         L     R12,CURRTCB(R12)      CURRENT TCB ADDRESS     LG @ZM2361
         USING TCB,R12                                       LG @ZM2361
         MODESET  EXTKEY=TCB,WORKREG=11                      LG @ZM2361
         MVC   ZERO(12,R9),RSVD        DATA TO COMAREA
         MODESET  EXTKEY=SUPR                                LG @ZM2361
*
*
* THE NEXT 7 INSTRUCTIONS WERE CHANGED TO COMMENTS TO       LG @ZA00524
* AVIOD DEQUEUEING IQES FROM THE RBIQE FIELD OF THE IRB;    LG @ZA00524
* THIS FIELD NO LONGER POINTS TO A LIST OF IQES THAT HAVE   LG @ZA00524
* BEEN SCHEDULED,BUT RATHER A SINGLE IQE.                   LG @ZA00524
*                                                           LG @ZA00524
*        LTR   RPIQE,RPIQE             PREVIOUS IQE ADR=0   LG @ZA00524
*        BZ    DE05                    BRANCH IF YES        LG @ZA00524
*        MVC   ZERO(4,RPIQE),IQELNK  REMOVE IQE FROM CHAIN  LG @ZA00524
*        B     DE06                    BRANCH               LG @ZA00524
*DE05    MVC   RBIQES(4,R8),IQELNK     NEW IQE LINK TO RBIQE FLD  ZA524
*DE06    MVC   IQELNK(4),NEXAVL(R8)    NEXT AVAIL FLD TO IQE LNK  ZA524
*        ST    RIQE,NEXAVL(R8)         IQE ADR TO NEXAVL FLD      ZA524
RCODE4   EQU   *                                              LB  AOS/1
         BAL   BACKREG,SSM1ON      RELEASE LOCAL LOCK           YA01241
         LA    RCODE,FOUR              RC=4
         SVC   EXIT                    RETURN TO IOS
DE01     EQU   *                                            E12 ZA26486
*DE01    L     R8,REBADR LOAD REB ADR LINE DELETED BY APAR> E12 ZA26486
         L     RIQE,RTNQ2(0,REBREG)    PTR TO FIRST REB IQE E12 ZA26486
DE08     LA    RIQE,ZERO(0,RIQE)       ZERO HI ORDER BYTE
         LTR   RIQE,RIQE               PTR=0
         BZ    NODATA              BRANCH-NO(MORE)IQES
         BAL   BACKREG,CONVERT         BRANCH TO CONVERT ROUTINE
         L     R11,PFKMSK              LOAD PF MASK
         L     R12,ATNTYP              LOAD ATTNTYP
         NR    R11,R9                  CHECK MASK
         NR    R12,R10                 CHECK MASK
         LTR   R11,R11                 WAS THERE A MATCH
         BNZ   DE07                    BRANCH IF YES
         LTR   R12,R12                 WAS THERE A MATCH
         BNZ   DE07                    BRANCH IF YES
DE11     LR    RPIQE,RIQE              SAVE ADR
         L     RIQE,IQELNK             LOAD ADR OF NEXT IQE
         B     DE08                    BRANCH
DE07     CLI   TYPE,A2260              IS THIS A 2260 ATTN
         BNE   DE07A                   BRANCH IF NO
         CLR   RTNRG,SAVRG             OFFSETS MATCH
         BZ    DE07A                   BRANCH IF YES
         LA    RTNRG,ALL               LOAD ALL TYPE
         CLR   RTNRG,SAVRG             OFFSETS MATCH
         BNE   DE11                    BRANCH IF NO
DE07A    L     R9,COMARA               LOAD COM AREA ADR
         LA    R9,ZERO(0,R9)           ZERO HI ORDER BYTE
         LTR   R9,R9                   PTR=0
         BZ    NODATA                  BRANCH IF YES
         L     R12,CVTPTR            LD ADDR CVT             LG @ZM2361
         L     R12,TCBPTR(R12)       LD ADDR DLBLWD          LG @ZM2361
         L     R12,CURRTCB(R12)      LD ADR CURR TCB         LG @ZM2361
         MODESET  EXTKEY=TCB,WORKREG=11                      LG @ZM2361
         MVC   ZERO(12,R9),RSVD        DATA TO COMAREA
         MODESET  EXTKEY=SUPR                                LG @ZM2361
         LTR   RPIQE,RPIQE             PREVIOUS IQE ADR=0
         BNZ   DE09                    BRANCH IF NO
         LTR   PAR1,PAR1               IS THIS A Q1 SEARCH         2575
         BNZ   DE07B                   BRANCH-NOT A Q1 SEARCH      2575
         MVC   RTNQ1(4,R8),IQELNK      NEW IQE ADR TO Q1 FIELD     2575
         B     DE10                    BRANCH                      2575
DE07B    EQU   *                                                   2575
         MVC   RTNQ2(4,R8),IQELNK      NEW IQE ADR TO Q2 FLD
         B     DE10                    BRANCH
DE09     MVC   ZERO(4,RPIQE),IQELNK    REMOVE IQE FROM CHAIN
DE10     L     R8,IQEIRB               LOAD IRB ADR
         MVC   IQELNK,NEXAVL(R8)       NEXT AVAIL TO IQE LINK
         ST    RIQE,NEXAVL(R8)         IQE ADR TO NEXT AVL
         B     RCODE4                  GO SET RETURN CODE     LB  AOS/1
DECLEAR  EQU   *                                              LB  AOS/1
*   22 LINES FOR APAR ZA04885,10042,20103,26486 DELETED BY-->   ZA30227
         L     RIQE,ZERO(0,RBIQE)      LOAD IQE ADR         E12 ZA30227
         LA    RIQE,ZERO(0,RIQE)       ZERO HI ORDER BYTE
         LTR   RIQE,RIQE               PTR=0
         BZ    Q2                      BRANCH IF YES
         L     R9,IQELNK               LOAD PTR TO NEXT IQE E12 ZA26486
         LA    R9,ZERO(0,R9)           ZERO HI ORDER BYTE   E12 ZA26486
         LTR   R9,R9                   PTR=0                E12 ZA26486
         BZ    Q2                      BRANCH IF YES
         LA    R10,ZERO                LOAD ZERO
         ST    R10,IQELNK              ZERO FIRST IQE LINK FLD
DE12     L     R11,IQEIRB              LOAD IRB ADR                6999
         L     R10,NEXAVL(0,R11)       LOAD NEXT FREE IQE PTR      6999
         LA    R10,ZERO(0,R10)         ZERO HI ORDER BYTE
         LTR   R10,R10                 PTR=0
         BNZ   NOT                     BRANCH IF NO
         ST    R9,NEXAVL(0,R11)        IQES TO FREE CHAIN   E12 ZA26486
         B     Q2                      BRANCH
NOT      L     R11,ZERO(0,R10)         LOAD NEXT IQE ADR
         LA    R11,ZERO(0,R11)         ZERO HI ORDER BYTE
         LTR   R11,R11                 PTR=0
         BNZ   NOT1                    BRANCH IF NOT 0
         ST    R9,ZERO(0,R10)          IQES TO FREE CHAIN   E12 ZA26486
         B     Q2                      BRANCH
NOT1     LR    R10,R11                 SAVE ADR
         B     NOT                     BRANCH
Q2       LTR   RGACB,RGACB             PTR=0
         BZ    YES                     BRANCH IF YES
         LTR   PAR1,PAR1               REGISTER SWITCH=ZERO        2575
         BZ    Q2A                     BRANCH=0                    2575
         SR    PAR1,PAR1               CLEAR REGISTER SWITCH       2575
*        L     R10,REBADR   LINE DELETED BY APAR--------->  E12 ZA26486
         L     R9,RTNQ1(REBREG)        LOAD Q1 FIELD        E12 ZA26486
         LTR   R9,R9                   ANY IQES ON Q1 FIELD E12 ZA26486
         BNZ   DENOA                   BRANCH IF IQES PRESENT      2575
Q2A      EQU   *                                                   2575
*        L     R10,REBADR   LINE DELETED BY APAR----------> E12 ZA26486
         L     R9,RTNQ2(0,REBREG)      LOAD IQE ADR         E12 ZA26486
         LA    R9,ZERO(0,R9)           ZERO HI ORDER BYTE   E12 ZA26486
         LTR   R9,R9                   PTR=0                E12 ZA26486
         BNZ   DENO                    BRANCH IF NO
YES      EQU   *                                             LD YA01241
         BAL   BACKREG,SSM1ON      RELEASE LOCAL LOCK           YA01241
         SVC   EXIT
DENO     LR    RIQE,R9                 LOAD NEW IQE ADR     E12 ZA26486
         SR    RGACB,RGACB             ZERO REG             E12 ZA26486
         ST    RGACB,RTNQ2(REBREG)     ZERO REB IQE PTR     E12 ZA26486
         B     DE12
DENOA    LR    RIQE,R9                 LOAD IQE ADR         E12 ZA26486
         SR    RPIQE,RPIQE             ZERO REG             E12 ZA26486
         ST    RPIQE,RTNQ1(REBREG)     ZERO Q1 FIELD        E12 ZA26486
         B     DE12                    BRANCH                      2575
CONVERT  CLI   TYPE,PFKB               TEST FOR PF KEY ATTN
         BE    DEPFKEY                 BRANCH IF YES
         LA    R9,ZERO                 ZERO PF ARGUMENT REG
         LA    R10,ONE                 LOAD INITIAL ATNTYP SEARCH CODE
         CLI   TYPE,END                END KEY TYPE
         BNE   C01                     BRANCH IF NO
         BR    BACKREG                 RETURN TO NSI FROM CALLER
C01      SR    R11,R11                 CLEAR REG
         IC    R11,TYPE                INSERT TYPE CODE
         LA    R12,TWO                 LOAD CONSTANT
         SR    R11,R12                 DETERMINE SHIFT COUNT
         SLL   R10,ZERO(R11)           DETERMINE SHIFT COUNT
         CLI   TYPE,A2260              IS THIS A 2260 ATTN
         BNE   C02                     BRANCH IF NO
         IC    RTNRG,RSVD              INSERT OFFSET
C02      BR    BACKREG                 RETURN TO NSI FROM CALLER
DEPFKEY  LA    R10,ZERO                ZERO TYPE ARGUMENT
         L     R9,PFCODE               LOAD INITIAL PF KEY SEARCH
         SR    R11,R11                 CLEAR REG
         IC    R11,KEY                 INSERT KEY #
         SRL   R9,ZERO(R11)            DETERMINE PF KEY SEARCH
         BR    BACKREG                 RETURN TO NSI AFTER CALLER
SSM1ON   SETLOCK RELEASE,TYPE=LOCAL,RELATED=(LOCAL,IGC0007E(SSM1OFF)), *
               REGS=USE                                         YA01241
         BR    BACKREG             RETURN TO NSI AFTER CALLER   YA01241
         DS    0F
DISABLE  DC    2XL1'00'
ENABLE   DC    2XL1'FF'
PFCODE   DC    X'80'
         DC    AL3(0)
MODEND   EQU   *                                             LD YA01241
PATCH    DS    0F                  PATCH AREA               D11
         DC    40AL1(255)          = ALL FOXES              D11
IQEFLDS  DSECT
IQELNK   DS    1F
IQEPARM  DS    1F
IQEIRB   DS    1F
IQETCB   DS    1F
RSVD     DS    BL1
OV       DS    BL1
KEY      DS    BL1
TYPE     DS    BL1
SENSE    DS    1F
GACB     DSECT
COMARA   DS    1F
DCB      DS    1F
PFKMSK   DS    1F
ATNTYP   DS    1F
EP1      DS    1F
EP2      DS    1F
REG13    DS    1F
PFKEY    DS    1F
ATNTYPS  DS    1F
ECB      DS    1F
REBADR   DS    1F
RESERVED DS    1F
         IHAPSA                                                 YA01241
         IKJTCB                                              LG @ZM2361
         END
