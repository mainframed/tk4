***********************************************************************
* MODULE NAME: IGC0207A (BUFFER MANAGEMENT MESSAGE MODULE)
*
* STATUS: CHANGE LEVEL 0
*
* FUNCTION: ISSUE WRITE-TO-PROGRAMMER MESSAGES FOR ERRORS DISCOVERED BY
* IGC0007A (BUFFER MNGT-LOAD 1)
*
* ENTRY POINT: IGC0207A VIA XCTL FROM IGC0007A
*
* INPUT: REG7 - RETURN CODE SET BY IGC0007A; INDEXES MSGLIST
*
* OUTPUT: WRITE-TO-PROGRAMMER MESSAGE
*         REGISTER 15 - RETURN CODE 04 REQUEST EXCEEDS AVAILABLE
*                                   08 REQUEST EXCEEDS ZONE
*                                   0C DCB NOT OPEN
*                                   10 RELEASE EXCEEDS ASSIGNED
*                                   18 ASSIGN BYTE COUNT BAD
*                                   1C INVALID REQUEST CODE
*                                   20 RELEASE BYTE COUNT BAD
*                                   24 CONTIG SECT NOT AVAILABLE
*                                  28 DCB-IOB LINK BAD
*                                   2C NON-GRAPHIC DCB
*                                   30 RELEASE FOR UNASSIGNED SECTION
*
*         REGISTER 1 - PARAMETER LIST ADDRESS RESTORED EXCEPT FOR
*                      RETURN CODES 1C AND 24
*
* EXIT: VIA SVC 3
*
* TABLES: MSGLIST - DISPLACEMENTS TO LIST FORMS OF WTO FOR MESSAGES
*
* ATTRIBUTES: SUPERVISOR STATE,REENTRANT,TRANSIENT
*
***********************************************************************
IGC0207A CSECT
*A745500,900500-901000                                        LG YM5684
* REGISTER EQUATES
R0       EQU   0
R1       EQU   1
R2       EQU   2                       POINTER TO SVRB EXT
RCODE    EQU   7                       MSGLIST OFFSET(INPUT)
RWORK    EQU   8
RMSG     EQU   9                       ADDR OF MSG
RBUF     EQU   10                      POINTER TO MSG BUFFER
RLNGTH   EQU   11                      FOR LENGTH OF MSG
RBASE    EQU   12                      BASE REGISTER
RSAVE    EQU   13
R15      EQU   15
* MISC. EQUATES
EXIT     EQU   3                       SVC NUMBER FOR EXIT
X1C      EQU   X'1C'                   RETURN CODE COMPARAND
X24      EQU   X'24'                   RETURN CODE COMPARAND
CVTPTR   EQU   16                      CVT POINTER
D0       EQU   0                       DISPLACEMENT
D4       EQU   4                       DISPLACEMENT
D96      EQU   96                      DISPLACEMENT
         BALR  RBASE,R0                LOAD BASE REG
ORG      EQU   *
         USING *,RBASE
         L     RMSG,MSGLIST(RCODE)     ADDRESS OF MESSAGE
         LA    RMSG,R0(RMSG,RBASE)     **IS DISP + BASE
         L     R2,CVTPTR               CVT ADDRESS
         L     R2,D0(R2)               ADDRRSS OF TCB POINTERS
         L     R2,D4(R2)               CURRENT TCB ADDR
         L     R2,D0(R2)               CURRENT RB ADDR
         LA    R2,D96(R2)              ADDR OF SVRB EXT
         MVC   D4(GMLNG,R2),GMLIST     MOVE GM LIST TO SVRB EXT
         LA    RWORK,D4(R2)            ADDR OF GM PARM LIST
         GETMAIN A=(R2),MF=(E,(RWORK))  GET MSG BUFFER
         LTR   R15,R15                 DID GM SUCCEED
         BNZ   SKIPWTO                 BRANCH IF GM FAILED
         L     RBUF,D0(R2)             ADDR OF MSG BUFFER
         LH    RLNGTH,D0(RMSG)         LENGTH OF MSG
         LA    RLNGTH,D4(RLNGTH)       +4 FOR DESC,RTG CODES
         EX    RLNGTH,MOVEMSG          MOVE MSG TO BUFFER
         WTO   MF=(E,(RBUF))           ISSUE MSG
         FREEMAIN A=(R2),MF=(E,(RWORK))  FREE BUFFER
SKIPWTO  EQU   *
         LA    R15,X1C
         CR    RCODE,R15
         BE    LEAVE                   GO TO EXIT
         LA    R15,X24
         CR    RCODE,R15
         BE    LEAVE                   GO TO EXIT
         LR    R1,RSAVE                RESTORE PARMLIST ADDR FOR OPEN
LEAVE    EQU   *
         LR    R15,RCODE               SET RETURN CODE
         SVC   EXIT
MOVEMSG  MVC   D0(D0,RBUF),D0(RMSG)
GMLIST   GETMAIN EC,LV=80,MF=L         GM PARM LIST
GMLNG    EQU   *-GMLIST                LENGTH OF GMLIST
         DS    0F
MSGLIST  EQU   *-4                     MESSAGE SELECTION TABLE
* RCODE PASSED AS INPUT IS DISPLACEMENT FROM MSGLIST ORIGIN
         DC    A(MSG1-ORG)
         DC    A(MSG2-ORG)
         DC    A(MSG3-ORG)
         DC    A(MSG4-ORG)
         DC    1F'0'                   RCODE X'14' SHOULD NOT OCCUR
         DC    A(MSG5-ORG)
         DC    A(MSG6-ORG)
         DC    A(MSG7-ORG)
         DC    A(MSG8-ORG)
         DC    A(MSG9-ORG)
         DC    A(MSG10-ORG)
         DC    A(MSG11-ORG)
         DC    A(MSG12-ORG)
         DC    A(MSG13-ORG)                                   LG YM5684
MSG1     WTO   'IFF512I ASGNBFR FOUND BUFFER STORAGE REQUESTED EXCEEDS X
               FREE SECTIONS',ROUTCDE=11,DESC=7,MF=L             S21016
MSG2     WTO   'IFF513I ASGNBFR FOUND BUFFER STORAGE REQUESTED EXCEEDS X
               DEVICE''S ZONE',ROUTCDE=11,DESC=7,MF=L            S21016
MSG3     WTO   'IFF501I IGC0007A FOUND DCB NOT OPEN',ROUTCDE=11,DESC=7,X
               MF=L
MSG4     WTO   'IFF522I RLSEBFR REQUEST BYTE COUNT EXCEEDS BUFFER ASSIGX
               NED TO DEVICE',ROUTCDE=11,DESC=7,MF=L             S21016
MSG5     WTO   'IFF511I ASGNBFR REQUESTED WITH NON-POSITIVE BYTE COUNT'X
               ,ROUTCDE=11,DESC=7,MF=L                           S21016
MSG6     WTO   'IFF504I IGC0007A FOUND INVALID REQUEST CODE IN PARAMETEX
               R LIST',ROUTCDE=11,DESC=7,MF=L                    S21016
MSG7     WTO   'IFF521I RLSEBFR REQUESTED WITH NON-POSITIVE BYTE COUNT'X
               ,ROUTCDE=11,DESC=7,MF=L                           S21016
MSG8     WTO   'IFF514I ASGNBFR FOUND NO CONTIGUOUS SECTIONS TO SATISFYX
                REQUEST',ROUTCDE=11,DESC=7,MF=L                  S21016
MSG9     WTO   'IFF502I IGC0007A FOUND DCB UNEQUAL TO DCB ADDRESS IN ASX
               SOCIATED IOB',ROUTCDE=11,DESC=7,MF=L              S21016
MSG10    WTO   'IFF503I IGC0007A FOUND DCB NOT GRAPHIC',ROUTCDE=11,DESCX
               =7,MF=L                                           S21016
MSG11    WTO   'IFF523I RLSEBFR REQUEST SPECIFIES BUFFER SECTION NOT ASX
               SIGNED TO DEVICE',ROUTCDE=11,DESC=7,MF=L          S21016
MSG12    WTO   'IFF515I ASGNBFR UNABLE TO CLEAR REQUESTED BUFFER STORAGX
               E',ROUTCDE=11,DESC=7,MF=L
MSG13    WTO   'IFF505I IGC0007A FOUND NO BUFFER TABLE ADDRESS IN UCB',X
               ROUTCDE=11,DESC=7,MF=L                         LG YM5684
         END
