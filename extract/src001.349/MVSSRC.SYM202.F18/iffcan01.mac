   TITLE  'IFFCAN01    CANCEL KEY ROUTINE'
**********************************************************************
*
* MODULE NAME:            IFFCAN01 (OS/VS2)
*
* DESCRIPTIVE NAME:       CANCEL KEY ROUTINE
*
* COPYRIGHT:              NONE
*
* STATUS:                 RELEASE 2.0
*
*
******************************CONTINUED* NEXT*PAGE********************
         EJECT
*  FUNCTION :
*         PROVIDE 2250 DISPLAY UNIT CANCEL KEY FUNCTIONS TO THE
*   TERMINAL USER.  THESE FUNCTIONS INCLUDE TERMINATING HIS PROGRAM,
*   TERMINATING HIS PROGRAM WITH A SYSTEM/360 CORE DUMP, RESUMING HIS
*   PROGRAM OR REQUESTING A 2250 BUFFER DUMP
*   UPON BEING ENTERED, IFFCAN01 DETERMINES IF A CKCB ALREADY EXISTS.
*   IF NOT, A GETMAIN IS ISSUED AND THE CKCB INITIALIZED.  A GREADR
*   MACRO IS ISSUED TO DETERMINE IF THE 2250 BUFFER FOR THIS DEVICE IS
*   RUNNING.  IF SO, THE RESTART ADDRESS IS SAVED.  AN ASGNBFR IS
*   ISSUED TO ACQUIRE 2250 BUFFER FOR THE CANCEL KEY MAIN PANEL.  IF
*   NO BUFFER EXISTS, A GETMAIN IS ISSUED FOR MAIN STORAGE, AND A
*   PORTION OF THE USER'S BUFFER IS READ INTO THIS AREA WHILE IFFCAN01
*   IN TURN USES THE NOW AVAILABLE 2250 BUFFER.  A GWRITE IS ISSUED
*   TO STORE THE MAIN PANEL INTO THE BUFFER.  A SEARCH OF THE TIOT IS
*   THEN MADE TO DETERMINE IF A SYSBFDMP DD STATEMENT WAS SUPPLIED.  IF
*   NOT, THE 2250 BUFFER DUMP OPTION IS NOT MADE AVAILABLE TO THE
*   USER.  IF GJP HAD INITIATED THE PROBLEM PROGRAM, THE RESUME OPTION
*   IS NOT MADE AVAILABLE TO THE USER.  A GWRITE IS THEN ISSUED AND
*   BUFFER GENERATION IS STARTED.  IFFCAN01 THEN RETURNS TO ITS CALLER
*
*   IF IFFCAN01 DETERMINES THE CKCB VALID, A LIGHT PEN ATTENTION HAS
*   BEEN RECEIVED, AND IT SCANS THE UCB SENSE DATA TO DETERMINE WHICH
*   OPTION HAD BEEN SELECTED BY THE USER.  IF THE BUFFER DUMP OPTION
*   WAS SELECTED, IFFCAN01 LINKS TO IFFCAN03.  AT THIS TIME, IF THE
*   USER WAS TAKEN FROM THE BUFFER, HE IS REINSTATED.  A RLSEBUF IS
*   ISSUED FOR ANY ACQUIRED 2250 BUFFER.  IF THE RESUME OPTION WAS
*   SELECTED, IFFCAN01 RETURNS TO THE CALLING ROUTINE, RESTARTING THE
*   BUFFER GENERATION, IF NECESSARY.  ALSO, IF GJP IS IN CONTROL, A
*   RETURN IS AFFECTED.  IF THE USER REQUESTED TERMINATE, AN ABEND IS
*   ISSUED.  IF DUMP WAS SELECTED AN ABEND/DUMP IS ISSUED.  IN BOTH
*   CASES THE ABEND CODE IS 063.
*
*ENTRY POINTS ENTERED VIA LINK FROM IFFCAN03
*
*INPUT REGISTER ONE POINTS TO CKQE
*
*EXIT,ERROR IF NO CORE AVAILABLE AFTER GETMAIN, RETURN TO CALLER, RC=4
*           IF I/O ERROR ON 2250, RETURN TO CALLER, RC=4
*           IF NO 2250 BUFFER AVAILABLE, RETURN TO CALLER, RC=4
*ATTRIBUTES TRANSIENT, REENTRANT, PRIVILIGED
*INPUT
*
*OUTPUT DISPLAY ON 2250
*                        * TERMINATE
*                        * DUMP
*                        * RESUME
*                        * BUFFER DUMP
*
         EJECT
*EXTERNAL REFERENCES 1-GWRITE VIA BALR       7-BUFINQ VIA SVC 71
*                    2-GCNTRL VIA BALR       8-POST VIA SVC 2
*                    3-GETMAIN VIA SVC 4     9-FREEMAIN VIA SVC 10
*                    4-WAIT VIA SVC 1       10-RLSEBFR VIA SVC 71
*                    5-ASGNBFR VIA SVC 71   11-WTO VIA SVC 35
*                    6-IFFCAN02 VIA LINK
*
*EXIT,NORMAL RETURN TO CALLER VIA REGISTER 14
*
*
*TABLES/WORKAREAS CANCEL KEY QUEUE ELEMENT
*                 CANCEL KEY CONTROL BLOCK
*
*
*NOTES N/A
*
IFFCAN01 CSECT
*   SEE LABEL 'MODID' FOR LAST CHANGE DATE; FORMAT:  NAME.SYSREL.DATE
*C108000,111000,164000                                        LF YM4067
* SEQ NBRS MUST BE RESPECIFIED BECAUSE OF RESEQ FOR VS
* 522300-522600                                                  A30600
*0831,132000,135000,363000,366000,369000,375000,543000             3112
* 128000,144000                                                  A25397
* 085000,216000-268400,328000,414000-421000,519000                M2878
* 271000,447000                                                   M2878
* THIS MODULE RE-WRITTEN FOR RELEASE 20
* C375000,C377000                                                A38916
* A376500,A376600                                                A38916
* 231000-236000,274000-275000,417000-418000,462000-467000     LB  AOS/1
* C291000                                                     LI  AOS/1
* A291500                                                     LI  AOS/1
* C383000                                                        A42796
* A157000,A443000-444000,A707000                                  M6619
* A384000-385000                                                 A44019
*D145000,D295000,D458000                                      LI A39419
*A144600-145200,A294600-295200,A383500-422910,A455500         LI A39419
*A457600-458200,A707200-707800                                LI A39419
*C147000,C423410                                              LI A39419
*A279500-279600,A284500-284920,A401500-401600,A405500-405920    OY01294
*A422370-422390,A422650-422699                                  OY01294
*D163000,A163500-163600,D190000,A190500-190600               LG Z30AALG
*D278000,A278500-278600,D242000,A342500-342600               LG Z30AALG
*D400000,A400500-400600,D422280,A422300-422320               LG Z30AALG
*D502000,A502500-502600                                      LG Z30AALG
*A270500,A110500,A157500,A310500,A394500,A454500             LG @ZM2360
*A476500,A517500,A536500,A536700,A540500,A536600             LG @ZM2360
*A489500-489997,A499500-499700                               LG @ZM2360
*C215000-216000                                             D11 ZA06390
*C002194,C422000,C445000,C510000                            E12 ZA24234
*C42900,C43700                                              E12 ZA26469
*A156050-156300,A157100-158060,A270010-271060,A310020-310300E12 ZA26368
*A394010-394180,A455000-455055,A477000-477065,D48950-489997 E12 ZA26368
*
*
*
         EJECT
*
*        GENERAL REGISTERS
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
BASE     EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
*
*        EQUATES
*
HEX04    EQU   X'04'
HEX7F    EQU   X'7F'
BUFRUN   EQU   X'02'                    SENSE BIT FOR BUFFER     A25397
*                                            RUNNING             A25397
ZERO     EQU   0
WON      EQU   1
TWO      EQU   2
THREE    EQU   3
FOUR     EQU   4
FIVE     EQU   5
EIGHT    EQU   8
TCB      EQU   8
TIOT     EQU   12
TWELVE   EQU   12
THIRTEEN EQU   13
SIXTEEN  EQU   16
TWOFOUR  EQU   24
UCBSENSE EQU   34                                             LF YM4067
TEB      EQU   28
RB       EQU   28
GIOCR    EQU   33             DISP OF TEB GIOCR ADDRESS      LG @ZM2360
UCBRST   EQU   24                                             LF YM4067
FULLMASK EQU   X'FF'              FF MASK USED TO TURN FLAG BITS A34790
         EJECT
*        ON ENTRY IT IS DETERMINED IF THIS IS THE FIRST ENTRY OR SECOND
*        FOR SECOND ENTRY THE TE WILL CONTAIN MY WORKAREA WITH AN
*        ID IN THE FIRST WORD.
*        ON FIRST ENTRY A GETMAIN IS DONE AND THE WORK AREA IS INITIAL-
*        IZED.  A GREADR XYP IS USED TO SEE IF THE BUFFER IS RUNNING
*        BECAUSE IT DOES NOT STOP THE BUFFER FIRST.  THEREFORE THE
*        SUCCESSFUL COMPLETION FLAG OF THE ECB WILL TELL IF THE BUFFER
*        IS RUNNING.  IF NOT SUCCESSFUL THE BUFFER IS.
*
         SAVE  (14,12)
         BALR  BASE,0                   ESTABLISH ADDRESSABILITY
         USING *,BASE
         B     *+24
MODID    DC    C'IFFCAN01.VS2R2.77275'          MODULE EYECATCHER ID
*
* REGISTER 1 CONTAINS POINTER TO CKQE
*
         TM    FIVE(R1),HEX04      TEST FLAG BITS OF CKQE TO DETERMINE
         BO    CAN040                   IF FIRST ENTRY. NO BRANCH M2878
         LA    R2,EIGHT(R1)        SET POINTER TO AREA ADDRESS
         LR    R11,R1              SET UP BASE FOR CKQE
         USING CKQE,R11
         LA    R1,16(R1)               PT TO LIST IN CKQE     LI A39419
         GETMAIN  EC,LV=CKBLGTH,A=(2),SP=250,MF=(E,(1))    E12 @ZA24234
         LTR   R15,R15                  ANY SPACE AVAILABLE        3112
         BNZ   CANERR1                 GO ISSUE ERROR MSG     LI A39419
         L     R9,ZERO(R2)
         USING CKCB,R9             SET UP ADDRESSABILITY
         XC    CKCB(CKBLGTH),CKCB  CLEAR GETMAIN AREA
         LA    R6,CKCBSVAR        LOAD SAVEAREA ADDRESS          A34790
         ST    R6,8(R13)           CHAIN SAVE AREAS
         ST    R13,4(R6)
         LR    R13,R6
         OI    CKQEFLG1,CKFCKCB   SET CKCB INVALID FLAG          A34790
         L     R3,CKQEDCB         GET DCB ADDRESS                A34790
         SR    R15,R15                 CLR REGISTER         E12 ZA26368
         ICM   R15,THREE,CKQEUCB   GET UCB ADDRESS          E12 ZA26368
         L     R15,TEB(R15)   GET TEB ADDRESS               E12 ZA26368
         L     R1,CKQEDCB     GET DCB ADDR                  E12 ZA26368
         CLC   GIOCR(3,R15),49(R1)  ADDR IN DCB CORRECT     E12 ZA26368
         BNE   CANERR9        NO,ISSUE ERROR MSG            E12 ZA26368
         MVC   CKDCBSTR(4),SIXTEEN(R3)   SAVE DCB BFR START ADDR  M6619
         MVC   CKCBECB+1(3),GIOCR(R15)  ADDRESS OF GIOCR    E12 ZA26368
*        GREADR     CKCBDECB,SEN,(3),CKCBWRKA,MF=E          E12 ZA26368
         SR    15,15              CLEAR REGISTER            E12 ZA26368
         LA    1,CKCBDECB         PARAMETER LIST            E12 ZA26368
         ST    15,4(0,1)          CLEAR TYPE AREA IN DECB   E12 ZA26368
         MVI   4(1),X'18'         SENSE TYPE TO DECB        E12 ZA26368
         LA    15,0(3)            DCB ADDRESS               E12 ZA26368
         ST    15,8(0,1)          DCB ADDRESS IN DECB       E12 ZA26368
         LA    15,CKCBWRKA        AREA ADDRESS              E12 ZA26368
         ST    15,12(0,1)         AREA ADDRESS IN DECB      E12 ZA26368
         XC    0(4,1),0(1)        CLEAR ECB                 E12 ZA26368
         L     15,CKCBECB         GET SAFE GIOCR ADDRESS    E12 ZA26368
         DS    0H                                           E12 ZA26368
         BALR  14,15              BRANCH TO GIOCR           E12 ZA26368
         BAL   R12,CANIO          GO CHECK I/O                   A34790
         TM    CKCBWRKA,BUFRUN    IS BUFFER RUNNING              A34790
         BNO   CAN010                   IF NOT TAKE BRANCH       A25397
         OI    CKQEFLG1,CKFBFRUN  SET BUFFER RUNNING FLAG        A34790
         SR    R7,R7                   CLEAR REGISTER        LG Z30AALG
         ICM   R7,THREE,CKQEUCB        GET UCB ADDRESS       LG Z30AALG
         MVC   CKCBREST+TWO(TWO),UCBRST(R7)  RESTART IN CKCB  LF YM4067
*                                                                A34790
         EJECT
*        BUFFER IS OBTAINED EITHER BY AN ASGNBFR OR A BUFFER INQ
*        FOLLOWED BY ROLLING OUT THE USER PICTURE.
*        THE DISPLAY IS WRITTEN OUT.  IF THE CALL WAS FROM GJP THEN
*        THE RESUME OPTION IS BLANKED BY WRITING OVER THE SET MODE.
*        THEN IF NOT GJP THE PROBLEM PROGRAM IS SET INTO A WAIT
*        STATE AND THE ROUTINE EXITS.
*
CAN010   LA    R5,LEN
         STH   R5,CKCBBFLG        SET UP BUFFER LENGTH           A34790
         ASGNBFR (R3),CKCBBFLG,MF=(E,CKCBWRKA)                   A34790
         LTR   R15,R15             AVAILABLE
         BNE   CAN030              NO-GO ROLL OUT OF USER OWNED BUFFER
         LH    R5,SIXTEEN(R3)   GET BUFFER ADRESS
         STH   R5,CKCBBFAD        SAVE IT                        A34790
CAN020   LA    R4,ODERLNTH    GET LENGTH
         LA    R5,ORDERS      AREA ADDRESS
         LA    R6,CKCBBFAD        BUFADR IN DECB                 A34790
         BAL   R7,CANWRITE    GO WRITE TO BUFFER
         EJECT
*
* CHECK TIOT TO DETERMINE IF SYSBFDMP DD CARD AVAILABLE. IF NOT, DO
* NOT ALLOW 2250 BUFFER DUMP OPTION.
*
         SR    R7,R7                   CLEAR REGISTER        LG Z30AALG
         ICM   R7,THREE,CKQEUCB        GET UCB ADDRESS       LG Z30AALG
         L     R7,TEB(R7)    CONTROL BLOCK SEARCHING TCB PTR
         L     R7,TCB(R7)      TCB POINTER
         L     R7,TIOT(R7)     TIOT ADDRESS
*
* REGISTER SEVEN POINTS TO TIOT. FIRST SIX WORDS ARE CONTROL INFOR-
* MATION. SET POINTER (R7) TO BYPASS THIS AND BEGIN SEARCH OF DD
* NAMES FOR SYSBFDMP.
*
         LA    R7,TWOFOUR(R7)      BYPASS JOB AND STEP NAMES
CAN021D3 EQU   *
         CLC   ZERO(FOUR,R7),ZEROS   IS THIS END OF TIOT
         BNE   CAN021               YES, SET NOP BLANK 2250 DUMP OPTION
*
* BUFFER DUMP DD CARD NOT FOUND. COMPUTE ADDRESS WHER NOP ORDER WILL
* BE STORED TO BLANK BUFFER DUMP OPTION.
*
         LH    R7,CKCBBFAD        GET BUFFER ADDRESS             A34790
         LA    R7,D2250(R7)   ADD DISPLACEMENT
         STH   R7,CKCBWRKA        SET UP FOR GWRITE              A34790
         LA    R4,TWO         GET LENGTH
         LA    R5,GNOP        AREA ADDRESS
         LA    R6,CKCBWRKA        ADDRESS ON DECB                A34790
         BAL   R7,CANWRITE    GO WRITE TO BUFFER
         B     CAN021D5   BRANCH TO CHECK FOR RESUME OPTION
CAN021   SR    R6,R6               CLEAR FOR IC             D11 ZA06390
         IC    R6,0(R7)            GET NXTENTRY LEN         D11 ZA06390
         CLC   FOUR(EIGHT,R7),SYSBFDMP   CHECK FOR DD CARD
         BE    CAN021D5            IF FOUND, BRANCH
         AR    R7,R6          SET POINTER TO NEXT ENTRY
         B     CAN021D3       BRANCH TO CONTINUE SEARCH
         EJECT
*
* NOW DETERMINE IF RESUME OPTION IS TO BE DISPLAYED
*
CAN021D5 LH    R6,CKCBBFAD        GET BUFFER ADDRESS             A34790
         N     R6,CLEAR           ZERO HIGH ORDER BYTES          A34790
         LA    R7,REP(R6)     COMPUTE AREA FOR NOP IN GR ORDERS
         STH   R7,CKCBWRKA                                       A34790
         LA    R6,LEN(R6)     COMPUTE STARTING ADDRESS
         STH   R6,CKCBWRKA+TWO                                   A34790
*
* BLANK BUFFER DUMP IN PROGRESS MESSAGE
*
CAN022   LA    R4,TWO         GET LENGTH
         LA    R5,GNOP        AREA ADDRESS
         LH    R6,CKCBBFAD        GET BUFFER ADDRESS             A34790
         N     R6,CLEAR           ZERO HIGH ORDER BYTES          A34790
         LA    R6,BUFFGO(R6)  COMPUTE ADDRESS OF THIS MESSAGE
         STH   R6,CKCBWRKA        STORE IN AREA                  A34790
         LA    R6,CKCBWRKA        ADDRESS IN DECB                A34790
         BAL   R7,CANWRITE    GO WRITE TO BUFFER
*
* CHECK IF BUFFER DUMP UNSUCCESSFUL SHOULD BE DISPLAYED
*
         LH    R6,CKCBBFAD        GET BUFFER   ADDRESS           A34790
         N     R6,CLEAR           ZERO HIGH ORDER BYTES          A34790
         LA    R6,BUFFNOGO(R6) COMPUTE ADDRESS OF THIS MESSAGE
         STH   R6,CKCBWRKA        STORE IN AREA                  A34790
         LA    R6,CKCBWRKA        ADDRESS IN DECB                A34790
         TM    CKQEFLG2,CKFBDFAL  ERROR ON BUFFER DUMP           A34790
         BO    CAN0221            IF YES BRANCH AND DISP MSG     A34790
         BAL   R7,CANWRITE        GO TO WRITE BUFFER             A34790
         B     CAN023             BRANCH AROUND ERROR MSG        A34790
CAN0221  LA    R5,GECP            UNBLANK BUFF DUMP ERR MSG      A34790
         BAL   R7,CANWRITE     GO TO WRITE TO BUFFER
         EJECT
* THIS WRITE UPDATES THE GTRU BRANCH ADDRESS TO POINT TO THE START OF
* THE DISPLAY AND STARTS BUFFER GENERATION.
CAN023   NI    CKQEFLG2,FULLMASK-CKFBDFAL  TURN ERR IND OFF      A34790
         XC    CKCBDECB(FOUR),CKCBDECB  CLEAR ECB                A34790
         LH    R2,CKCBBFAD        GRET BUFFER ADDRESS            A34790
         N     R2,CLEAR           ZERO HIGH ORDER BUTES          A34790
         LA    R2,RETURN-ORDERS+2(R2) GET ORDER PROG START ADDR  A34790
         STH   R2,CKCBWRKA+2      STORE GTRU LOC FOR RESTART     A34790
*        GWRITE CKCBDECB,STR,,2,CKCBBFAD,CKCBWRKA+TWO,CKCBBFAD,MF=E
         SR    15,15               CLEAR REGISTER           E12 ZA26368
         LA    1,CKCBDECB          PARAMETER LIST           E12 ZA26368
         MVI   4(1),X'A0'          BUF TYPE CODE IN DECB    E12 ZA26368
         MVI   5(1),X'6C'          STR TYPE CODE IN DECB    E12 ZA26368
         L     15,=A(2)                                     E12 ZA26368
         ST    15,20(0,1)          LENGTH IN DECB           E12 ZA26368
         LA    15,CKCBBFAD         AREA ADDRESS             E12 ZA26368
         ST    15,12(0,1)          AREA ADDRESS IN DECB     E12 ZA26368
         LA    15,CKCBWRKA+TWO     BUFFER ADDRESS           E12 ZA26368
         ST    15,28(0,1)          BUFFER ADDRESS IN DECB   E12 ZA26368
         LA    15,CKCBBFAD         START ADDRESS            E12 ZA26368
         ST    15,24(0,1)          START ADDRESS IN DECB    E12 ZA26368
         XC    0(4,1),0(1)         CLEAR ECB                E12 ZA26368
         L     15,CKCBECB          GET GIOCR ADDRESS        E12 ZA26368
         BALR  14,15               BRANCH TO ENTRY          E12 ZA26368
*                                                                A34790
         EJECT
         BAL   R12,CANIO          GO CHECK I/O
         TM    CKQEFLG2,CKFWTCNT  HAS WAIT CNT BEEN INCR BEFORE  A34790
         BO    CAN024             DO NOT INCR WAIT COUNT         A34790
         SR    R7,R7                   CLEAR REGISTER        LG Z30AALG
         ICM   R7,THREE,CKQEUCB        GET UCB ADDRESS       LG Z30AALG
         L     R7,TEB(R7)               GET TEB POINTER           M2878
         L     R6,TWOFOUR(R7)      GET GAR IRB ADDRESS          OY01294
         LA    R6,0(R6)            CLEAR HI-ORDER BYTE          OY01294
         L     R7,TCB(R7)               GET TCB POINTER           M2878
         L     R7,ZERO(R7)              GET ADDRESS OF OUR RB     M2878
         L     R7,RB(R7)                GET ADDRESS OF IFFCAN03'S M2878
*                                            RB                   M2878
         L     R7,RB(R7)                GET ADDRESS OF USER'S RB  M2878
         LA    R7,0(R7)            CLEAR HI-ORDER BYTE          OY01294
         CLR   R6,R7               IS THIS GAR'S IRB?           OY01294
         BNE   CAN0231             IF NO,INCREMENT...           OY01294
*                                  ...WAIT COUNT.               OY01294
         L     R7,RB(R7)           GET NEXT RB ADDRESS.         OY01294
CAN0231  EQU   *                                                OY01294
         SR    R6,R6                    CLEAR REGISTER            M2878
         IC    R6,RB(R7)                GET WAIT COUNT            M2878
         LA    R6,WON(R6)               INCREMENT BY ONE          M2878
         STC   R6,RB(R7)                RESTORE IN RB             M2878
         OI    CKQEFLG2,CKFWTCNT   SET WAIT CNT INCREMENTED FLAG A34790
CAN024   OI    CKQEFLG1,CKFATTWT  SET L.P. ATTN EXPECTED FLAG    A34790
         L     R13,CKCBSVAR+FOUR  RESTORE CALLERS SAVE AREA   LI  AOS/1
CAN025   EQU   *                                              LI  AOS/1
         RETURN (14,12),RC=0      RESTORE CALLERS SAVE AREA      A34790
         EJECT
CAN030   LA    R7,CKCBROLL     SET PTR TO AREA ADDR FOR R/O      A34790
         LA    R1,CKCBWORK             PT TO LIST IN CKCB     LI A39419
         GETMAIN  EC,LV=256,A=(7),SP=0,MF=(E,(1))             LI A39419
         LTR   R15,R15                  ANY SPACE AVAILABLE        3112
         BNZ   CANERR1     NO, BRANCH FOR WTO
         OI    CKQEFLG2,CKFROLBF  SET GETMAIN INDICATOR          A34790
         L     R7,CKCBROLL     GET BUFFER ADDRESS                A34790
         BUFINQ (R3),CKCBWRKA,8,MF=(E,(7))                       A34790
         C     R15,HEX20           SHORT LIST RETURN CODE
         BE    TSTENTRY            YES BRANCH AROUND ZERO TEST
         LTR   R15,R15                  RETURN CODE FROM BUFINQPTM 2320
*                                       ZERO FOR VALID REQUEST PTM 2320
         BNZ   CANERR3     IF NOT, BRANCH
         EJECT
TSTENTRY CLC   CKCBWRKA+FOUR(FOUR),FFS   IS THERE AN ENTRY       A34790
         BE    CANERR3     NO, WE'RE STUCK
         LH    R6,CKCBWRKA+FOUR   FET BUFFER IN USE              A34790
         STH   R6,CKCBBFAD        SET AS OUR BUFFER ADDRESS      A34790
         XC    CKCBDECB(FOUR),CKCBDECB  CLEAR ECB                A34790
*        GREAD CKCBDECB,BUF,,256,(R7),CKCBBFAD,MF=E READ    E12 ZA26368
*                                        USER  FROM BUFFER  E12 ZA26368
         SR    15,15              CLEAR REGISTER            E12 ZA26368
         LA    1,CKCBDECB         PARAMETER LIST            E12 ZA26368
         ST    15,4(0,1)          CLEAR TYPE AREA IN DECB   E12 ZA26368
         MVI   4(1),X'80'           BUF TYPE CODE TO DECB   E12 ZA26368
         L     15,=A(256)         GET LENGTH                E12 ZA26368
         ST    15,20(0,1)         STORE LENGTH IN DECB      E12 ZA26368
         LA    15,0(R7)           GET AREA ADDRESS          E12 ZA26368
         ST    15,12(0,1)         STORE IN DECB             E12 ZA26368
         LA    15,CKCBBFAD        GET BUFFER ADDRESS        E12 ZA26368
         ST    15,28(0,1)         STORE IN DECB             E12 ZA26368
         XC    0(4,1),0(1)        CLEAR ECB                 E12 ZA26368
         L     15,CKCBECB         GET GIOCR ADDRESS         E12 ZA26368
         BALR  14,15              BRANCH TO ENTRY POINT     E12 ZA26368
         BAL   R12,CANIO          GO CHECK I/O
         OI    CKQEFLG2,CKFROLUT  SET BUFFER R/O FLAG            A34790
         B     CAN020                   GO DO THE REST
         EJECT
*        SECOND ENTRY DETERMINES WHAT WAS DESIRED.
*        IF TERMINATE OR DUMP THE BUFFER IS FREED AND
*        IF TERMINATE THE BUFFER IS FREED OR ROLLED BACK OUT.
*        THEN THE RETURN CODE IS SET AND RETURN
*        IF RESUME THE PICTURE IS RESTARTED, AND THE PROBLEM PROGRAM
*        IS TAKEN OUT OF THE WAIT STATE.
CAN040   LR    R11,R1         SET UP ADDRESSABILITY FOR CKQE
         USING CKQE,R11
         L     R9,CKQECKCB    SET UP ADDRESSABILITY FOR CKCB     A34790
         USING CKCB,R9
         LA    R6,CKCBSVAR        CHAIN SAVEAREAS                A34790
         ST    R6,EIGHT(R13)  SAVE
         ST    R13,FOUR(R6)    AREAS
         LR    R13,R6
         L     R3,CKQEDCB         GET DCB ADDRESS                A34790
*
* NOW DETERMINE WHICH OPTION WAS SELECTED BY THE USER.  ONE OF THE
* FOLLOWING ACTIONS IS TAKEN DEPENDENT UPON THE USER'S SELECTION:
*                   1-TERMINATE-ISSUE ABEND, CODE 063, NO DUMP
*                   2-DUMP-ISSUE ABEND, CODE 063, DUMP
*                   3-RESUME-RESTART USER DISPLAY
*                   4-BUFFER DUMP-LINK TO IFFCAN02; UPON RETURN
*                     REDISPLAY OUR MENU
         NI    CKQEFLG1,FULLMASK-CKFATTWT  TURN OFF LIGHT PEN
*                                        EXPECTED FLAG           A34790
         SR    R10,R10                 CLEAR REGISTER        LG Z30AALG
         ICM   R10,THREE,CKQEUCB       GET UCB ADDRESS       LG Z30AALG
         LH    R6,CKCBBFAD        GET BUFFER ADDREESS            A34790
         N     R6,CLEAR           ZERO TWO LOW ORDER BYTES OF REGA34790
         LA    R7,D2250(R6)   COMPUTE MIN BFR LOC FOR BUFF DUMP
         MVC   CKCBBFDP(TWO),UCBSENSE(R10)  STORE SENSE IN CKCB  A34790
         CH    R7,CKCBBFDP        IS BUFF DUMP OPTION SELECTED   A34790
         BH    CAN044             NO,BRANCH TO CONTINUE SEARCH   A34790
         EJECT
*
* BUFFER DUMP OPTION SELECTED.  PREARE MESSAGE AND GO TO IFFCAN02
*
         LA    R4,TWO          GET LENGTH
         LA    R5,GECP          AREA ADDRESS
         LH    R6,CKCBBFAD        GET BUFFER   ADDRESS           A34790
         N     R6,CLEAR           CLEAR HIGH ORDER BYTES         A34790
         LA    R6,BUFFGO(R6)   COMPUTE ADRESS OF THIS MESSAGE
         STH   R6,CKCBWRKA        STORE IN AREA                  A34790
         LA    R6,CKCBWRKA        ADDRESS IN DECB                A34790
         BAL   R7,CANWRITE     GO TO WRITE TO BUFFER
         LA    R1,CANMSGS     GET POINTER TO ERROR MESSAGES
         ST    R1,CKCBERMT        PUT IN WORKAREA                A34790
         LR    R1,R11         PREPARE CKQE PTR
         LINK  EP=IFFCAN02,MF=(E,(1))    GO TO DUMP PROGRAM
         LTR   R15,R15         IS RETURN CODE ZERO
         BZ    CAN022             IF YES BRANCH                  A34790
         OI    CKQEFLG2,CKFBDFAL  ERROR ON BUFFER DUMP           A34790
         B     CAN022             BRANCH TO MSG ROUTINR          A34790
         EJECT
CAN044   OI    CKQEFLG1,CKFEXIT    TURN ON EXIT IN PROCESS BIT   A34790
CAN045   TM    CKQEFLG2,CKFROLUT  CHECK IF BUFFER ROOLED OUT     A34790
         BO    CAN060         IF YES, BRANCH TO BRING IT BACK
         TM    CKQEFLG2,CKFROLBF  HAS BUFFER BEEN GOTTEN         A34790
         BO    CAN061             IF YES, GO FREE IT             A34790
         CLC   CKCBBFAD(FOUR),ZEROS   HAS BUFFER BEEN GOTTEN      CRB
         BE    CAN047                 NO DON'T RELEASE BUFFER     CRB
         EJECT
*
* BUFFER HAS NOT BEEN ROLLED OUT.  RELEASE ANY ACQUIRED BUFFER BEFORE
* ANYTHING ELSE.
*
         L     R3,CKQEDCB             GET DCB ADDRESS             CRB
         RLSEBFR  (R3),CKCBBFAD,MF=(E,CKCBWRKA)  FREE BUFFER     A42796
         MVC   CKCBBFAD(FOUR),ZEROS    CLEAR CKCBBFAD FIELD   LI A39419
         C     R15,HEX14                                         A44019
         BE    CAN047                   BRANCH IF GOOD RELEASE   A44019
         LTR   R15,R15                  RELEASE BUFFER GOOD      A38916
         BNZ   CANERR8                NO, BRANCH TO ERROR MSG     CRB
CAN047   LA    R7,REP(R6)    SET MIN BFR ADDRESS FOR RESUME       CRB
         CH    R7,CKCBBFDP        IS RESUME OPTION SELECTED      A34790
         BH    CAN048      IF NO, BRANCH
         TM    CKQEFLG1,CKFBFRUN   WAS FBUFFER RUNNING           A34790
         BZ    CAN047D1       IF NOT, BRANCH
         NI    CKQEFLG1,FULLMASK-CKFBFRUN   ZERO THIS BIT        A34790
         XC    CKCBDECB(FOUR),CKCBDECB   CLEAR ECB               A34790
*        GCNTRL  CKCBDECB,STR,,CKCBREST+TWO,MF=E RSTRT USER E12 ZA26368
         SR    15,15                 CLEAR REGISTER         E12 ZA26368
         LA    1,CKCBDECB             PARAMETER LIST        E12 ZA26368
         ST    15,4(0,1)             CLEAR TYPE IN DECB     E12 ZA26368
         MVI   4(1),X'6C'            STR TYPE CODE IN DECB  E12 ZA26368
         LA    15,CKCBREST+TWO       AREA ADDRESS           E12 ZA26368
         ST    15,24(0,1)            STORE IN DECB          E12 ZA26368
         XC    0(4,1),0(1)           CLEAR ECB              E12 ZA26368
         L     15,CKCBECB            GET GIOCR ADDRESS      E12 ZA26368
         BALR  14,15                 BRANCH TO ENTRY POINT  E12 ZA26368
         BAL   R12,CANIO          CHECK FOR GOOD I/O             A34790
         EJECT
CAN047D1 TM    CKQEFLG2,CKFWTCNT   HAS WAIT COUNT BEEN MODIFIED  A34790
         BNO   CAN048D3           IF NO, DON'T DECREMENT         A34790
         NI    CKQEFLG2,FULLMASK-CKFWTCNT  TURN OFF FLAG         A34790
         SR    R7,R7                   CLEAR REGISTER        LG Z30AALG
         ICM   R7,THREE,CKQEUCB        GET UCB ADDRESS       LG Z30AALG
         L     R7,TEB(R7)               GET TEB ADDRESS           M2878
         L     R6,TWOFOUR(R7)      GET GAR'S IRB ADDRESS        OY01294
         LA    R6,0(R6)            CLEAR HI-ORDER BYTE          OY01294
         L     R7,TCB(R7)               GET TCB ADDRESS           M2878
         L     R7,ZERO(R7)              GET OUR RB ADDRESS        M2878
         L     R7,RB(R7)                GET IFFCAN03'S RB ADDRESS M2878
         L     R7,RB(R7)                GET USER'S RB ADDRESS     M2878
         LA    R7,0(R7)            CLEAR HI-ORDER BYTE          OY01294
         CLR   R6,R7               IS THIS GAR'S IRB?           OY01294
         BNE   CAN047D2            IF NO,DECREMENT IRB....      OY01294
*                                  ...WAIT COUNT.               OY01294
         L     R7,RB(R7)           GET NEXT RB ADDRESS.         OY01294
CAN047D2 EQU   *                                                OY01294
         SR    R6,R6                    CLEAR REGISTER            M2878
         IC    R6,RB(R7)                GET USERS WAIT COUNT      M2878
         BCTR  R6,0                     DECREMENT IT BY ONE       M2878
         STC   R6,RB(R7)          RESTORE USERS WAIT COUNT       A34790
         B     CAN048D3       BRANCH TO FREE STORAGE
CAN048   TM    CKQEFLG1,CKFEXIT   OPTIN SELECTED                 A34790
         BNO   CANEXIT            NO, GO FREE CKCB               A34790
         LA    R7,DMP(R6)  COMP MIN BUFFER ADDRESS FOR DUMP OPT. A34790
         CH    R7,CKCBBFDP        IS TERM OPTION SELECTED        A34790
         BH    CAN048D1       NO, BRANCH
         OI    CKQEFLG1,CKFABDMP  SET DUMP BIT                   A34790
CAN048D1 EQU   *                                              LB  AOS/1
         OI    CKQEFLG1,CKFDEQUE  SET DE-QUE BIT                 A34790
         NI    CKQEFLG1,FULLMASK-CKFCKCB TURN OFF CKCB VALID FLG A34790
         L     R13,FOUR(R13)  SET FREEMAIN AREA
         FREEMAIN R,LV=CKBLGTH,A=(9),SP=250                E12 @ZA24234
         EJECT
         TM    CKQEFLG2,CKFWTCNT       HAS WAIT COUNT UPED    LI A39419
         BNO   CAN048D4                IF NO DON'T DECREMENT  LI A39419
         NI    CKQEFLG2,FULLMASK-CKFWTCNT  TURN OFF FLAG      LI A39419
         SR    R7,R7                   CLEAR REGISTER        LG Z30AALG
         ICM   R7,THREE,CKQEUCB        GET UCB ADDRESS       LG Z30AALG
         L     R7,TEB(R7)              GET TEB ADDRESS        LI A39419
         L     R6,TWOFOUR(R7)      GET GAR'S IRB ADDRESS        OY01294
         LA    R6,0(R6)            CLEAR HI-ORDER BYTE          OY01294
         L     R7,TCB(R7)              GET TCB ADDRESS        LI A39419
         L     R7,ZERO(R7)             GET OUR RB ADDRESS     LI A39419
         L     R7,RB(R7)               GET IFFCAN03 RB ADDRESSLI A39419
         L     R7,RB(R7)               GET USER RB ADDRESS    LI A39419
         LA    R7,0(R7)            CLEAR HI-ORDER BYTE          OY01294
         CLR   R6,R7               IS THIS GAR'S IRB?           OY01294
         BNE   CAN048D5            IF NO,DECREMENT IRB...       OY01294
*                                  ... WAIT COUNT.              OY01294
         L     R7,RB(R7)           GET NEXT RB ADDRESS          OY01294
CAN048D5 EQU   *                                                OY01294
         SR    R6,R6                   CLEAR REGISTER         LI A39419
         IC    R6,RB(R7)               GET USER WAIT COUNT    LI A39419
         BCTR  R6,0                    DECREMENT BY 1         LI A39419
         STC   R6,RB(R7)               RESTORE USER WAIT COUNTLI A39419
CAN048D4 TM    CKQEFLG1,CKFABDMP       IS THIS DUMP           LI A39419
         BO    CAN048D2       IF YES, BRANCH
         WTO   'IFF006I JOB WAS TERMINATED WITH CANCEL KEY TERMINATE OPX
               TION',ROUTCDE=11,DESC=7                           S21016
         ABEND 099,,,SYSTEM   NO, SET UP ABEND WITHOUT DUMP E12 ZA26469
         EJECT
CAN048D2 EQU   *
         WTO   'IFF007I JOB WAS TERMINATED WITH CANCEL KEY DUMP OPTION'X
               ,ROUTCDE=11,DESC=7                                S21016
         ABEND 099,DUMP,,SYSTEM        SET UP ABEND W/DUMP  E12 ZA26469
*
*
CAN048D3 NI    CKQEFLG1,FULLMASK-CKFCKCB  SET CKCB VALID BIT OFF A34790
         OI    CKQEFLG1,CKFDEQUE  SET DEQUE BIT AFTER RESUME     A34790
         L     R13,FOUR(R13)  RESET SAVE ADDRESS
         L     R3,CKQEDCB          LOAD DCB ADDRESS               M6619
         MVC   SIXTEEN(4,R3),CKDCBSTR RESTORE DCB BFR STRT ADDR   M6619
         FREEMAIN  R,LV=CKBLGTH,A=(9),SP=250               E12 @ZA24234
         B     CAN025                   BRANCH TO RETURN          M2878
         EJECT
*
* USER BUFFER WAS ROLLED OUT.  BRING HIS BUFFER BACK AND FREE AQUIRED
* STORAGE
*
CAN060   NI    CKQEFLG2,FULLMASK-CKFROLUT  TURN OFF ROLLOUT FLG  A34790
         L     R7,CKCBROLL        GET PTR TO BUFFER              A34790
         XC    CKCBDECB(FOUR),CKCBDECB  CLEAR ECB                A34790
*        GWRITE  CKCBDECB,BUF,,256,(7),CKCBBFAD,MF=E        E12 ZA26368
         LA    1,CKCBDECB          PARAMETER LIST           E12 ZA26368
         SR    15,15             CLEAR REGISTER             E12 ZA26268
         MVI   4(1),X'A0'        BUF TYPE CODE TO DECB      E12 ZA26368
         L     15,=A(256)        GET LENGTH                 E12 ZA26368
         ST    15,20(0,1)        STORE IN DECB              E12 ZA26368
         LA    15,0(7)           GET AREA ADDRESS           E12 ZA26368
         ST    15,12(0,1)        STORE IN DECB              E12 ZA26368
         LA    15,CKCBBFAD       GOT BUFFER ADDRESS         E12 ZA26368
         ST    15,28(0,1)        STORE IN DECB              E12 ZA26368
         L     15,CKCBECB        GET GIOCR ADDRESS          E12 ZA26368
         BALR  14,15             BRANCH TO ENTRY POINT      E12 ZA26368
         MVC   CKCBBFAD(FOUR),ZEROS    CLEAR CKCBBFAD FIELD   LI A39419
         BAL   R12,CANIO          GO TO CHECK IO                 A34790
CAN061   LA    R7,CKCBROLL     SET ADDRESS OF AREA TO BR FREED   A34790
         LA    R1,CKCBWORK             PT TO LIST IN CKCB     LI A39419
         FREEMAIN  E,LV=256,A=(7),SP=0,MF=(E,(1))             LI A39419
         NI    CKQEFLG2,FULLMASK-CKFROLBF  TURN OFF R/O GETMN FLGA34790
         B     CAN047             BRANCH TO TEST SELECTIONS
         EJECT
*
* THIS WRITE SUBROUTINE IS USED THROUGHOUT PROGRAM TO PUT DATA TO THE
* BUFFER WITHOUT STARTING GENERATION. INPUT IS AS FOLLOWS:
*  REGISTER 4: LENGTH OF DATA TO BE WRITTEN
*    REGISTER 5; POINTER TO DATA TO BE WRITTEN
*   REGISTER 6: BUFFER ADDRESS IN DECB
*
CANWRITE XC    CKCBDECB(FOUR),CKCBDECB   CLEAR DECB              A34790
*        GWRITE  CKCBDECB,BUF,,(4),(5),(6),MF=E  WRT TO BFR E12 ZA26368
         LA    1,CKCBDECB        PARAMETER LIST             E12 ZA26368
         SR    15,15             CLEAR REGISTER             E12 ZA26368
         ST    15,4(0,1)         CLEAR TYPE IN DECB         E12 ZA26368
         MVI   4(1),X'A0'        BUF TYPE CODE TO DECB      E12 ZA26368
         LA    15,0(4)           GET LENGTH                 E12 ZA26368
         ST    15,20(0,1)        STORE IN DECB              E12 ZA26368
         LA    15,0(5)           GET AREA ADDRESS           E12 ZA26368
         ST    15,12(0,1)        STORE IN DECB              E12 ZA26368
         LA    15,0(6)           GET BUFFER ADDRESS         E12 ZA26368
         ST    15,28(0,1)        STORE IN DECB              E12 ZA26368
         L     15,CKCBECB        GET GIOCR ADDRESS          E12 ZA26368
         BALR  14,15             BRANCH TO ENTRY POINT      E12 ZA26368
         LR    R12,R7      SET RETURN ADDRESS
*
* CHECK ERROR CODE FROM GIOCR FOR I/O STARTED.  WAIT FOR I/O
* COMPLETION, CHECK POST CODE, BRANCH ON ERRORS OR RETURN TO CALLER
*
CANIO    LTR   R15,15        CHECK FOR ZERO RETURN CODE
         BNZ   CANERR2         IF NOT, BRANCH
         LA    R1,CKCBDECB        GET ECB POINTER                A34790
         WAIT  ECB=(1)       WAIT FOR I/O COMPLETION
         CLI   CKCBDECB,HEX7F     I/O GOOD                       A34790
         BNE   CANERR2           NO, BRANCH                      A34790
         BR    R12           RETURN TO CALLER
*
* ERROR HAS BEEN DETECTED.  PUT MESSAGE IN WORKAREA AND ISSUE WTO.
*
CANERR1  MVC   CKCBWRKA(CAN1LN),CANMSG1  PUT MSG IN WKAREA       A34790
         B     CANSVC               BRANCH TO MOVE UNIT ADDRESS
CANERR2  MVC   CKCBWRKA(CAN2LN),CANMSG2  PUT MSG IN WKAREA       A34790
         B     CANSVC               BRANCH TO MOVE UNIT ADDRESS
CANERR8  MVC   CKCBWRKA(CAN8LN),CANMSG8   PUT MSG IN WKAREA       CRB
         B     CANSVC               GO GET UNIT ADDRESS
CANERR9  EQU   *                                             LG @ZM2360
         MVC   CKCBWRKA(CAN9LN),CANMSG9 MSG IN WRKAREA       LG @ZM2360
         B     CANSVC         BRANCH TO SET UNIT ADDRESS     LG @ZM2360
CANERR3  MVC   CKCBWRKA(CAN3LN),CANMSG3  PUT MSG IN WKAREA       A34790
CANSVC   LA    R1,CKCBWRKA        GET PARAMETER LIST             A34790
         SR    R7,R7                   CLEAR REGISTER        LG Z30AALG
         ICM   R7,THREE,CKQEUCB        GET UCB ADDRESS       LG Z30AALG
         MVC   TWELVE(THREE,R1),THIRTEEN(R7)  PUT UNIT INTO LIST
         SVC   35      ISSUE WTO
         B     CAN045             GO TO BUFFER R/O CHECK         A34790
CANEXIT  TM    CKQEFLG1,CKFCKCB   IS CKCB VALID                  A34790
         BZ    CANRTN              IF NOT, BRANCH TO RETURN
         NI    CKQEFLG1,FULLMASK-CKFCKCB  SET VALID CKCB FLAG    A34790
         L     R13,FOUR(R13)       GET SAVE AREA
         FREEMAIN  R,LV=CKBLGTH,A=(R9),SP=250              E12 @ZA24234
CANRTN   OI    CKQEFLG1,CKFDEQUE   MARK CKQE TO BE FREED         A34790
         B     CAN025             GO TO RETURN TO USER           A34790
CANMSGS  DC    A(CANMSG1)
         DC    A(CANMSG2)
         DC    A(CANMSG3)
         DC    A(CANMSG4)
         DC    A(CANMSG5)
         DC    A(CANMSG9)                                    LG @ZM2360
         EJECT
CANMSG1  WTO   'IFF001I XXX REQUEST IGNORED; LACK OF CORE',MF=L,       ,
               ROUTCDE=(2,11),DESC=4
CANMSG1E EQU   *
CANMSG2  WTO   '1FF002I XXX REQUEST IGNORED; PERMANENT I/O ERROR',     ,
               MF=L,ROUTCDE=(2,11),DESC=4
CANMSG2E EQU   *
CANMSG3  WTO   'IFF003I XXX REQUEST IGNORED; NO 2250 BUFFER AVAILABLE',,
               MF=L,ROUTCDE=(2,11),DESC=4
CANMSG3E EQU   *
CANMSG4  WTO   'IFF004I XXX REQUEST IGNORED; OPEN FAILURE ON SYSBFDMP',,
               MF=L,ROUTCDE=(2,11),DESC=4
CANMSG4E EQU   *
         EJECT
CANMSG5  WTO   'IFF005I                                              ',,
               MF=L,ROUTCDE=(2,11),DESC=4
CANMSG5E EQU   *
CANMSG8  WTO   'IFF008I XXX RLSEBFR FAILED IN CANCEL KEY RTN',MF=L,    X
               ROUTCDE=11,DESC=7
CANMSG8E EQU   *
CANMSG9  WTO   'IFF0009I XXX REQUEST IGNORED;GIOCR ADDRESS NOT IN DCB',X
               MF=L,ROUTCDE=11,DESC=7                        LG @ZM2360
CANMSG9E EQU   *                                             LG @ZM2360
CAN1LN   EQU   CANMSG1E-CANMSG1
CAN2LN   EQU   CANMSG2E-CANMSG2
CAN3LN   EQU   CANMSG3E-CANMSG3
CAN8LN   EQU   CANMSG8E-CANMSG8
CAN9LN   EQU   CANMSG9E-CANMSG9  LENGTH OF MESSAGE 9         LG @ZM2360
         EJECT
         GINIT
ORDERS   GSRT
         GEVM
         GDV   0,3900,B
         GECP  L
         GTXT  '* TERMINATE'
         GCNOP C
         GEVM
         GDV   0,3600,B
DUMP     GECP  L
         GTXT  '* DUMP'
         GCNOP C
         GEVM
         GDV   0,3300,B
REPRESS  GECP  L
         GTXT  '* RESUME'
         GCNOP C
         GEVM
         GDV   0,3000,B
DUMP2250 GECP  L
         GTXT  '* BUFFER DUMP'
         GCNOP C
         GEVM
         GDV   0,2400,B
DUMPGO   GECP  B
         GTXT  'IFF101I BUFFER DUMP IN PROGRESS'
         GCNOP C
         GEVM
         GDV   0,2400,B
DUMPNOGO GECP  B
         GTXT  'IFF105I BUFFER DUMP UNSUCCESSFUL'
         GCNOP C
RETURN   GTRU  0
LEN      EQU   *-ORDERS-2
DMP      EQU   DUMP-ORDERS
REP      EQU   REPRESS-ORDERS
D2250    EQU   DUMP2250-ORDERS
BUFFGO   EQU   DUMPGO-ORDERS
BUFFNOGO EQU   DUMPNOGO-ORDERS
ORDERSE  EQU   *
ODERLNTH EQU   ORDERSE-ORDERS
GECP     GECP  B
GNOP     GNOP2
         EJECT
*                                                                 M2878
*                                                                 M2878
*                        STORAGE CONSTANTS                        M2878
*                                                                 M2878
*                                                                 M2878
         SPACE 2                                                  M2878
DISABLE  DC    X'00'                    SYSTEM MASK TO REJECT     M2878
*                                            INTERRUPTS           M2878
ENABLE   DC    X'FF'                    SYSTEM MASK TO EXCEPT     M2878
*                                            INTERRUPTS           M2878
SYSBFDMP DC    C'SYSBFDMP'
HEX14    DC    F'20'
HEX20    DC    F'32'
ZEROS    DC    F'0'
ONE      DC    F'1'
FFS      DC    X'FFFFFFFF'
CLEAR    DC    X'0000FFFF'        ZERO LOW ORDER BYTES OF REG    A34790
         LTORG
          EJECT
CKQE     DSECT         SYSTEM CANCEL KEY QUEUE ELEMENT
         SPACE 2
CKQECKQE DS    F                        POINTER TO NEXT CKQE ON
*                                            ACTIVE LIST
CKQEFLG1 DS    X                        FLAG FIELD BYTE ONE
CKFGJP   EQU   X'80'                    IS THIS CALL TO SYSTEM CANCEL
*                                            KEY ROUTINES FROM GJP
*                                            ABEND HOOK
CKFABDMP EQU   X'40'                    A ABEND DUMP IS REQUESTED BY
*                                            THE 2250 OPERATOR
CKFATTWT EQU   X'20'                    SYSTEM CANCEL KEY ROUTINES
*                                            ARE EXPECTING AN ATTENTION
*                                            FROM DEVICE ASSOCIATED
*                                            WITH THIS CKQE
CKFCKCB  EQU   X'10'                    CANCEL KEY CONTROL BLOCK
*                                            POINTER IS VALID IN CKQE
CKFEXIT  EQU   X'08'                    EXIT IN PROCESS
CKFECB   EQU   X'04'                    GJP ECB POINTER IS VALID IN
*                                            CKQE AND GJP IS WAITING
*                                            TO BE POSTED
CKFDEQUE EQU   X'02'                    SYSTEM CANCEL KEY ROUTINE HAS
*                                            PERFORMED ALL REQUESTS
*                                            FOR DEVICE ASSOCIATED
*                                            WITH THIS CKQE AND THE
*                                            CKQE SHOULD BE DEQUEUED
CKFBFRUN EQU   X'01'                    THE 2250 BUFFER WAS RUNNING
*                                            AT THE TIME THE SYSTEM
*                                            CANCEL KEY FUNCTION WAS
*                                            REQUESTED
CKQEFLG2 DS    X                        SECOND BYTE OF FLAG FIELD
CKFROLBF EQU   X'80'                    GETMAIN HAS BEEN ISSUED TO
*                                            ROLL OUT THE USERS
*                                            BUFFER SECTION
CKFROLUT EQU   X'40'                    USERS BUFFER HAS BEEN ROLLED
*                                            OUT
CKFINBUF EQU   X'20'                    GETMAIN FOR INPUT BUFFER FOR
*                                            BUFFER DUMP REQUEST HAS
*                                            BEEN ISSUED SUCCESSFULLY
CKFOPCB  EQU   X'10'                    GETMAIN FOR OUTPUT CONTROL
*                                            BLOCK HAS BEEN ISSUED
CKFOPEN  EQU   X'08'                    SYSBFDMP OUTPUT DCB IS OPEN
CKFLPATN EQU   X'04'                    THIS IS AN ENTRY SCHEDULED
*                                            BY GAR WITH A LIGHTPEN
*                                            ATTENTION.
CKFWTCNT EQU   X'02'                    THE REQUESTOR OF A        M2878
*                                            SYSTEM CANCEL KEY    M2878
*                                            FUNCTION REQUEST     M2878
*                                            BLOCK'S WAIT COUNT   M2878
*                                            HAS BEEN INCREMENTED M2878
*                                            BY ONE               M2878
CKFBDFAL EQU   X'01'              SET TO ONE IF IFFCAN02 GAVE    A34790
*                                    A NONZERO RC TO IFFCAN01    A34790
*                                     INDICATING AN ERROR WHILE  A34790
*                                    DUMPING THE BUFFER          A34790
CKQEUCB  DS    H                        UNIT CONTROL BLOCK THAT IS NOW
*                                            UNDER CONTROL OF THE
*                                            SYSTEM CANCEL KEY ROUTINES
CKQECKCB DS    F                        POINTER TO CANCEL KEY CONTROL
*                                            BLOCK ASSOCIATED WITH THIS
*                                            CKQE
CKQEDCB  DS    F                        POINTER TO THE OPEN DATA
*                                            CONTROL BLOCK FOR THE
*                                            DEVICE REQUESTING THE
*                                            SYSTEM CANCEL KEY FUNCTION
         EJECT
CKCB     DSECT         SYSTEM CANCEL KEY CONTROL BLOCK
         SPACE 2
CKCBOPCB DS    F                        ADDRESS OF OUTPUT CONTROL BLOCK
*                                            ASSOCIATED WITH THIS CKCB
CKCBECB  DS    F                        ADDRESS OF GJP'S ECB TO BE
*                                            POSTED WHEN SYSTEM CANCEL
*                                            KEY FUNCTION REQUESTED BY
*                                            GJP IS COMPLETE
CKCBSVAR DS    18F                      SAVE AREA FOR IFFCAN01
CKCBBFAD DS    H                        BUFFER ADDRESS WHERE SYSTEM
*                                            CANCEL KEY 256 BYTE BUFFER
*                                            SECTION STARTS.  OPTION
*                                            PANEL WILL BE PLACED INTO
*                                            THIS SECTION
CKCBBFLG DS    H                        BUFFER LENGTH OF THE DATA
*                                            WRITTEN TO THE 2250 FOR
*                                            THE OPTIONS PANEL
CKCBWRKA DS    CL64                    WORK AREA FOR:
*                                            READ MI INPUT
*                                            READ SENSE INPUT
*                                            BUFFER INQUIRY TABLE
*                                            WRITE TO OPERATOR MESSAGES
CKCBDECB DS    8F                       DATA EVENT CONTROL BLOCK
*                                            USED IN I/O OPERATIONS
*                                            TO THE 2250
CKCBREST DS    F                        ADDRESS OF 2250 RESTART
*                                            GNERATION OF THE USERS
*                                            BUFFER PROGRAM
CKCBROLL DS    F                        ADDRESS OF 256 BYTE AREA USED
*                                            TO ROLL OUT USERS BUFFER
*                                            IF NO OTHER BUFFER SPACE
*                                            AVAILABLE FOR THE DEVICE
CKCBERMT DS    F                        POINTER TO TABLE OF ERROR
*                                            MESSAGES TO BE SENT TO
*                                            THE SYSTEM OPERATOR UPON
*                                            ERROR CONDITION.
*
CKDCBSTR DS    F                  SAVE AREA FOR DCB BFR STRT ADDR M6619
CKCBWORK DS    F                       LENGTH FOR E GETMAIN   LI A39419
         DS    F                       ADDR OF ADDR LIST      LI A39419
         DS    C                       EC MODE                LI A39419
         DS    C                       SP VALUE               LI A39419
CKCBBFDP DS    H                  STOREAGE AREA FOR SENSE DATA   A34790
*                                    FROM THE UCB ON A LIGHT     A34790
*                                    PEN ATTENTION               A34790
CKCBE    EQU   *
CKBLGTH  EQU   CKCBE-CKCB
         END
UCBBUF   EQU   24
UCBTE    EQU   28                  TE PTR IN UCB
CAN      EQU   12                  CANCEL WORD IN TE
UCBRST   EQU   32                  BUFFER RESTART ADDR IN UCB
RUN      EQU   X'01'               BUFFER RUN BIT IN SW
ROLL     EQU   X'02'               ROLL OUT BIT IN SW
IOERR    EQU   X'04'
GJP      EQU   X'08'
ENDKEY   EQU   X'20'               END KEY IN READ MIP
RUNOFF   EQU   X'FE'               TURN OFF RUN SW
RTNCDE   EQU   X'80'
ABND     EQU   X'40'
TETCB    EQU   8
TCBRB    EQU   0
RBLNK    EQU   28
CONST    DC    C'CANC'
FFS      DC    X'FFFFFFFF'
ONE      DC    F'1'
F8       DC    F'8'
COREADR  DC    F'0'                                                3112
COMCODE  DS    0F
         DC    X'00000222'
WTOPARM  DC    H'43'
         DC    H'0'
WTOMSG   DC    C'PERMANENT ERROR ON    .  ATTENTION IGNORED.'
         EJECT
         GINIT
ORDERS   GSRT
         GEVM
         GDV   0,3900,B
         GECP  L
         GTXT  '* TERMINATE'
         GCNOP C
         GEVM
         GDV   0,3600,B
DUMP     GECP  L
         GTXT  '* DUMP'
         GCNOP C
         GEVM
         GDV   0,3300,B
REPRESS  GECP  L
         GTXT  '* RESUME'
         GCNOP C
RETURN   GTRU  0
LEN      EQU   *-ORDERS-2
DMP      EQU   DUMP-ORDERS
REP      EQU   REPRESS-ORDERS
GNOP     GNOP2
         EJECT
STORAG   DSECT
CONID    DS    F
GJPWAIT  DS    F
SAVE     DS    18F
READIN   DS    3F
BUFAD    DS    H
BUFLEN   DS    H
DECB1    DS    8F
SW       DS    X
WAIT     DS    X
RESTRT   DS    F
SAV256   DS    F
DCBAD    DS    F
         END
