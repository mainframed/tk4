IFFPAAST CSECT                          GSTOR
         TITLE 'GSTOR' STORE GRAPHIC DATA
*TITLE. STORE GRAPHIC ORDERS ROUTINE
*
*STATUS. CHANGE LEVEL 0
*
*FUNCTION/OPERATION. GSTOR STORES GRAPHIC DATA IN THE GDOA
*    (GRAPHIC DATA OUTPUT AREA)
*
*ENTRY POINTS. GSTOR VIA CALL OR LINK MACRO  ALIAS NAME IFFPAA
*
*INPUT.OUTPUT CONTROL BLOCK POINTER (OCBP) AND A PARAMETER ADDRESS
*   WHICH CONTAINS THE ADDRESS OF THE DATA TO STORE AND THE 'N'
*   NUMBER OF BYTES TO STORE
*
*OUTPUT. GRAPHIC DATA STORED IN THE G D O A (GRAPHIC DATA OUTPUT AREA)
*
*EXTERNAL ROUTINES. NONE
*
*EXITS-NORMAL. COMPLETION OF TASK EXIT VIA RETURN MACRO
*    -ERROR. HEXADECIMAL 14 OR 18 IN REG. 15
*  IMMEDIATE RETURN. EXIT VIA RETURN MACRO
*
*TABLES/WORK AREAS. USER SPECIFIED PARAMETER TABLE AND USER
*    SUPPLIED WORK AREA
*
*ATTRIBUTES. READ ONLY, REENTRANT
* STORE GRAPHIC DATA IN USER PROVIDED GRAPHIC DATA OUTPUT AREA-N BYTES
         ENTRY GSTOR
GSTOR    SAVE  (14,12),T,*              SYSTEM SAVE 14,15,0 THRU 12
         BALR  9,0                      SET UP BASE ADDRESS
         USING *,9
         SR    15,15                    CLEAR RETURN CODE
         SR    PASREG,PASREG            CLEAR OVERFLOW PASS REGISTER
         LM    OBPREG,PATREG,0(1)       LOAD OCBP AND PARTAB ADDRESS
         LM    ADDREG,CNTREG,0(PATREG)  LOAD INPUT DATA ADDRESS AND N
GSTOR1   L     OABREG,0(0,OBPREG)       ADDRESS OF OACB
         L     OLPREG,16(0,OABREG)      ADDRESS OF OLP FROM OACB
         LTR   WRKREG,CNTREG            LOAD N AND TEST IF ZERO OR LESS
         BC    2,GSTOR2                 GREATER THAN ZERO
         WTO   'IFF541I GSTOR FOUND NON-POSITIVE BYTE COUNT IN PARTAB',X
               ROUTCDE=11,DESC=7                                 S21016
         LA    15,20                    LOAD RETURN CODE
         B     EXIT                     RETURN
*
* CALCULATE ADDRESSES BASED ON N VALUE AND SIZE OF GDOA
*
GSTOR2   AR    OLPREG,WRKREG            OLP ADDRESS PLUS N
         LA    TEMREG,4
         AR    TEMREG,OLPREG            OLP ADDRESS PLUS N PLUS 4
         L     SLOREG,0(OABREG)         GET STARTING ADDRESS
         A     SLOREG,4(OABREG)         LAST ADDRESS OF GDOA
         L     OLPREG,16(0,OABREG)      RESET OLP ADDRESS
         CR    TEMREG,SLOREG            COMPARE LAST TO TOTAL AVAILABLE
         BNH   OK                       ROOM FOR NEW DATA
*
* NOT ENOUGH ROOM TO STORE ALL OF THE DATA-DETERMINE AMOUNT TO STORE
*
         SR    TEMREG,SLOREG            EQUALS BYTES-CAN'T STORE
         SR    WRKREG,TEMREG            NUMBER OF BYTES-CAN STORE
         C     WRKREG,ZERO
         BNH   GSTOR4                   NO STORAGE AVAILABLE
         SR    CNTREG,WRKREG            BYTES REMAINING
         BAL   LINREG,GSTOR6            MOVE GRAPHIC DATA
GSTOR3   LA    PASREG,1                 SET PASS INDICATOR
         L     WKAREG,4(0,OBPREG)       ADDRESS OF WORKAREA
         STM   0,15,0(WKAREG)           SAVE REGISTERS
         L     15,8(0,OABREG)           OVERFLOW ADDRESS
         LM    2,12,28(13)              RELOAD USER'S REGISTERS
*   SAVE AREA. THIS GIVES HIS BASE REGISTER THE RIGHT VALUE
         BALR  14,15                    LINK TO ROUTINE-SET RETURN
         LM    0,15,0(WKAREG)           RELOAD REGISTERS
         B     GSTOR1                   STORE REMAINING DATA
*
*
*
*
* NO DATA TO MOVE-NO SPACE AVAILABLE
*
GSTOR4   C     PASREG,ONE               WAS A TRANSFER MADE TO OVERFLOW
         BL    GSTOR3                   NO
         WTO   'IFF542I GSTOR, AFTER RETURN FROM OVERFLOW ROUTINE, FOUNX
               D GDOA STILL FULL',ROUTCDE=11,DESC=7              S21016
         LA    15,24                    SET ERROR RETURN CODE
         B     EXIT                     RETURN WITH ERROR INDICATION
*
*
*
* STORE GRAPHIC DATA IN GDOA
*
GSTOR6   LA    TEMREG,255               SET REG EQUAL TO 255
GSTOR6A  CR    WRKREG,TEMREG            IS NO. TO MOVE OVER 255
         BH    GSTOR7                   YES
         LR    TEMREG,WRKREG            LOAD NO. TO MOVE
         S     TEMREG,ONE               COMPENSATE FOR BYTE OVER
         EX    TEMREG,MOVE              MOVE DATA
         AR    OLPREG,WRKREG            UPDATE OLP ADDRESS
         AR    ADDREG,WRKREG            UPDATE DATA ADDR
         ST    OLPREG,16(0,OABREG)      STORE NEW OLP ADDRESS
         BR    LINREG                   RETURN INTERNAL
GSTOR7   LR    SLOREG,TEMREG            SET REG TO 255
         S     SLOREG,ONE               REDUCE BY ONE
         EX    SLOREG,MOVE              MOVE DATA
         AR    OLPREG,TEMREG            UPDATE OLP ADDRESS
         AR    ADDREG,TEMREG            UPDATE DATA ADDRESS
         SR    WRKREG,TEMREG            REDUCE AMOUNT TO MOVE
         B     GSTOR6A                  DO NEXT GROUP
OK       BAL   LINREG,GSTOR6            MOVE GRAPHIC DATA
EXIT     RETURN (14,12),T,RC=(15)       RETURN -CODE SET IN RET CODE
MOVE     MVC   0(0,OLPREG),0(ADDREG)
ZERO     DC    F'0'
ONE      DC    F'1'
FOUR     DC    F'4'
PASREG   EQU   0
WKAREG   EQU   1
OBPREG   EQU   2
PATREG   EQU   3
OABREG   EQU   4
OLPREG   EQU   5
ADDREG   EQU   6
CNTREG   EQU   7
WRKREG   EQU   8
TEMREG   EQU   10
LOAREG   EQU   12
SLOREG   EQU   11
LINREG   EQU   14
         DS    0D
         END
