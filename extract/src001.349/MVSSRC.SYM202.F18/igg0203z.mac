         TITLE 'GRAPHICS TASK TERMINATION SECTION'
***********************************************************************
*
* MODULE NAME:            IGG0203Z (OS/VS2)
*
* DESCRIPTIVE NAME:       GRAPHICS TASK TERMINATION
*
* COPYRIGHT:              NONE
*
* STATUS:                 RELEASE 2.0
*
* FUNCTION:              THIS MODULE PERFORMS BOTH NORMAL AND ABNORMAL
*                        TASK TERMINATION FUNCTIONS FOR THE 2250S AND
*                        2260S SUPPORTED BY GPS.
*
*                        THIS INCLUDES:
*                          1. NORMAL TERMINATION: DELETE ANY ATTENTION
*                             ROUTINES STILL ACTIVE BY ISSUING GPS
*                             MACRO DAR FOR ACTIVE TEB/REB CHAINS (DONE
*                             IN EARLIER RELEASES BY MODULE IFFGRTTR)
*
*
*                          2. ABNORMAL TERMINATION:
*
*                            A. RECOVER THE UCB SO IT IS AVAILABLE FOR
*                               REALLOCATION (DONE IN EARLIER RELEASES
*                               BY THE FORCED CLOSE ABEND MACRO
*                               IGGIFF01) THE FIELDS RESET ARE:
*                                GCB   ZEROED SO ATTENTIONS ARE IGNORED
*                                OPEN  ZEROED TO INDICATE NO DCBS OPEN
*                                TEB   ZEROED TO PREVENT USING BAD CBS
*
*                            B. RELEASE 2250 ALLOCATED BUFFER FOR
*                               DEVICES ASSIGNED TO THE ABENDING MEMORY
*
******************* PROLOGUE*CONTINUED*NEXT*PAGE **********************
         EJECT
***********************************************************************
*
* NOTES:
*
*     DEPENDENCIES:      NORMAL TERMINATION: USER MEMORY ADDRESSABILITY
*                        ABNORMAL TERMINATIONS:
*                          GRAPHIC DEVICES ARE NOT SHARABLE; THUS:
*                          1. TERMINATION ASSUMES THE PRIMARY GOAL IS
*                             SUCCESSFUL REALLOCATION OF THE DEVICES
*                          2. LOCAL LOCK IS NOT NEEDED FOR UCB RESETS
*
*     RESTRICTION:       NONE
*
*     RESISTERS:         SEE EQUATE DEFINITIONS FOLLOWING PROLOG
*
*     PATCH LABEL:       PATCH, A 50 BYTE DC AREA AT END OF MODULE
*
* MODULE TYPE:           EXECUTABLE CODE
*
*    PROCESSOR:          ASSEMBLER XF
*
*    MODULE SIZE:        SEE PAGE 1 OF THIS LISTING
*
*    ATTRIBUTES:
*       AT ENTRY:        REENTRANT  ENABLED  SUPERVISOR STATE IN KEY 0
*
* ENTRY POINT:           IGG0203Z
*
*    PURPOSE:            SEE 'FUNCTION' HEADING ABOVE
*
*    LINKAGE:            SEE 'INPUT', NEXT HEADING
*
* INPUT:                 REGISTERS AS FOLLOWS:
*                          1  ADDR OF: RMPL ADDRESS
*                         13           SAVE AREA OF 18 WORDS
*                         14           RETURN ADDRESS TO TASK CLOSE
*                         15           IGG0203Z ENTRY POINT
*
* OUTPUT:                SEE 'FUNCTION' HEADING ABOVE
*
* EXITS:
*
*    NORMAL:             RETURN MACRO, RC=0
*
*    ERROR:              NOT USED
*
******************* PROLOGUE*CONTINUED*NEXT*PAGE **********************
         EJECT
***********************************************************************
*
* EXTERNAL REFERENCES:
*
*     ROUTINES:          NORMAL TERMINATION:  IGC0007D
*
*     DATA AREAS:        SEE DSECT SECTION AT END OF LISTING
*
*     CONTROL BLOCKS:    SEE DSECT SECTION AT END OF LISTING
*
* TABLES:                NONE
*
* MACROS:                FREEMAIN  GETMAIN   SAVE   RETURN
*
* CHANGE ACTIVITY:       MODULE IS NEW FOR VS2/R2
*
***********************************************************************
         SPACE 2
IGG0203Z CSECT
*   SEE LABEL 'MODID' FOR LAST CHANGE DATE; FORMAT:  NAME.SYSREL.DATE
*C106000-122000                                               LF YM8208
*D160000,A160100-160200                                      LG Z30AALG
*C160500-161500,A196096                                     LG @ZM30151
*C167000,C168000,A195500-195920                             L5 @ZA02832
*A161100-161200                                             L5 @ZA03950
*A114700-114900,A373400                                     D11 ZA11330
*A283700-283800                                             D11 ZA10028
*
         EJECT
***********************************************************************
*                                                                     *
*                        REGISTER ASSIGNMENTS                         *
*                                                                     *
***********************************************************************
         SPACE 2
RSIZE    EQU   0     TEMPORARY:         GETMAIN SIZES
RADR     EQU   1                        ADDRESS WORK REGISTER
RCALC    EQU   1                        CALCULATIONS
RCODE    EQU   15                       RETURN CODE
         SPACE
RBASE    EQU   2     CONVENTIONS:       BASE REGISTER
         SPACE
RDEV     EQU   4     CALCULATIONS:      DEVICE INDEX NUMBER FROM UCB
RLOC     EQU   7                        LOCATION REG FOR INDEXING
         SPACE
RRMPL    EQU   8     INPUT ADDRESS:     RESOURCE MGR PARAMETER LIST
         SPACE
RBTAB    EQU   3     ADDRESSES:         BUFFER TABLE (FROM 2250 UCBS)
RDSAB    EQU   4                        DSAB  (FROM QDB & ACTIVE JSCB)
RENTRY   EQU   5                        CURRENT ENTRY IN TABLE
RLAST    EQU   6                        LAST ENTRY IN TABLE
RLIST    EQU   7                        INPUT PARAMETER LIST FOR DAR
RREB     EQU   9                        ROUTINE ENTRY BLOCK (UCB & TEB)
RTEB     EQU   10                       TASK ENTRY BLOCK (FROM UCB)
RTCB     EQU   11                       TCB IN CONTROL (CURRENT)
RUCB     EQU   12                       UCB
RSAVE    EQU   13                       SAVE AREA OF CALLER
RSCB1    EQU   14                       TEMP SYS CB ADDRESSES
RSCB2    EQU   15                       TEMP SYS CB ADDRESSES
         EJECT
***********************************************************************
*                                                                     *
*                        EQUATES                                      *
*                                                                     *
***********************************************************************
ZERO     EQU   0        DECIMAL/HEX NUMBERS FOR GENERAL USE
ONE      EQU   1
TWO      EQU   2
THREE    EQU   3
SEVEN    EQU   7
         SPACE
DAR      EQU   74       SVC NUMBERS
         EJECT
***********************************************************************
*                                                                     *
*                        INITIALIZATION                               *
*                                                                     *
*         1. USING STATEMENTS IDENTIFY DSECT BASE REGISTERS           *
*         2. REGISTERS SAVED IN CALLER'S AREA                         *
*         3. BASE ESTABLISHED FOR ADDRESSABILITY OF MODULE            *
*         4. MODULE EYECATCHER ID FOR RELEASE LEVEL VERIFICATION      *
*                                                                     *
***********************************************************************
         SPACE 2
         USING CVT,RSCB1         CVT DSECT INITZ FROM LOC 16
         USING REB,RREB          STD DSECTS, KEY 0:  REB (SEE IGC0007C)
         USING RMPL,RRMPL                            RMPL (INPUT LIST)
         USING TEB,RTEB                              TEB
         USING TCB,RTCB                              TCB
         USING TIOENTRY,RENTRY                       TIOT
         USING UCB,RUCB                              UCB
         USING UCBCMEXT,RSCB2                        UCB EXTENSION
         SPACE
         SAVE  (14,12)
         BALR  RBASE,0
         USING *,RBASE
         B     *+24
MODID    DC    C'IGG0203Z.VS2R2.75092'          MODULE EYECATCHER ID
         EJECT
***********************************************************************
*                                                                     *
*                        INITIALIZATION                               *
*                                                                     *
*         5. DETERMINE TYPE OF PROCESSING REQUIRED; THEN IF:          *
*                                                                     *
*              NORMAL TERMINATION:                                    *
*                1.  DAR ANY REBS ASSOCIATED WITH CURRENT TCB.        *
*                                                                     *
*              ABNORMAL TERMINATION:                                  *
*                1.  RESET 2250 BUFFER TABLE FOR REALLOCATION         *
*                2.  RESET UCB FIELDS  (OPEN, GCB, TEB)               *
*                                                                     *
***********************************************************************
         L     RRMPL,ZERO(RADR)   GET RESOURCE MANAGER'S INPUT LIST
         TM    RMPLFLG1,RMPLTYPE    CHECK IF ABNORMAL TERMINATION
         BO    GTT0080                IF YES, RECOVER UCB AND BFR TAB
         CLI   RMPLASID+1,ONE     CHECK IF MASTER SCHEDULER MEMORY
         BE    GTT0010              IF YES, NOTHING TO DO
         L     RTCB,RMPLTCBA        IF NO, GET TCB ADR
         LTR   RTCB,RTCB              IS A TCB AVAILABLE
         BZ    GTT0010                  IF NO, NOTHING TO DO
         SR    RSCB1,RSCB1                CLEAR FOR ICM INSTRUCTION
         ICM   RSCB1,SEVEN,TCBJSTCB+1   IF YES, SEE IF P/P TCB AVAIL:
         CR    RTCB,RSCB1             DOES CURRENT TCB = JOBSTEP TCB
         BNE   GTT0020                  IF NO, OK TO PROCESS
GTT0010  SR    RCODE,RCODE              IF YES, NOTHING TO PROCESS
         RETURN (14,12),T,RC=(15)
         EJECT
***********************************************************************
*                                                                     *
*                    NORMAL TASK TERMINATION                          *
*                                                                     *
*        1. SCAN DSAB CHAIN FOR GRAPHIC DD ENTRIES IN TIOT   LF YM8202*
*        2. VALIDATE TIOT UCBS ARE FOR GAM SUPPORTED DEVICES          *
*                                                                     *
***********************************************************************
GTT0020  SR    RENTRY,RENTRY           CLEAR REGS FOR ICM INSTRUCTIONS
         SR    RDSAB,RDSAB
         SR    RUCB,RUCB
         ICM   RSCB1,SEVEN,TCBJSCBB    IS JSCB AVAILABLE
         BZ    GTT0010                    IF NO, NOTHING TO PROCESS
         USING IEZJSCB,RSCB1
         ICM   RSCB1,SEVEN,JSCBACT+1   IS ACTIVE JSCB AVAILABLE
         BZ    GTT0010                    IF NO, NOTHING TO PROCESS
         ICM   RSCB1,SEVEN,JSCDSABQ+1  IS QDB AVAILABLE
         BZ    GTT0010                    IF NO, NOTHING TO PROCESS
         USING QDB,RSCB1
         ICM   RDSAB,SEVEN,QDBFELMP+1  IS DSAB AVAILABLE
         BZ    GTT0010                    IF NO, NOTHING TO PROCESS
         USING DSAB,RDSAB
GTT0030  ICM   RENTRY,SEVEN,DSABTIOT+1 IS TIOT AVAILABLE
         BZ    GTT0040                    IF NO, CHECK IF MORE DSABS
         TM    TIOESTTA,DYNALLOC   IS/WAS IT DYNAMICALLOC?  D11 ZA11330
         BO    GTT0040             YES,GO SEE IF MORE DSABS D11 ZA11330
         ICM   RUCB,SEVEN,TIOESTTB+1   IS UCB AVAILABLE
         BZ    GTT0040                    IF NO, CHECK IF MORE DSABS
         CLI   UCBID,UCBSTND           CHECK IF SUBUCB          SA15459
         BNE   GTT0040                    IF YES, NOT GRAPHIC   SA15459
         CLI   UCBTBYT3,UCB3DISP       CHECK IF A GRAPHICS DEVICE
         BNE   GTT0040                    IF NO, CHECK IF MORE DSABS
         CLI   UCBTBYT4,UCBT2250       IS DEVICE 2250 TYPE
         BE    GTT0050                    IF YES,  OK TO PROCESS
         CLI   UCBTBYT4,UCBT2260       IS DEVICE 2260 TYPE
         BE    GTT0050                    IF YES, OK TO PROCESS
         CLI   UCBTBYT4,UCBT1053       IS DEVICE 1053
         BE    GTT0050                    IF YES, OK TO PROCESS
GTT0040  ICM   RDSAB,SEVEN,DSABFCHN+1  CHECK IF MORE DSABS
         BZ    GTT0010                    IF NO, NOTHING TO PROCESS
         B     GTT0030                    IF YES, CHECK FOR TIOT
         EJECT
***********************************************************************
*                                                                     *
*                    NORMAL TASK TERMINATION                          *
*                                                                     *
*         3. DAR ANY REB ASSOCIATED WITH CURRENT TCB                  *
*                                                                     *
***********************************************************************
GTT0050  SR    RTEB,RTEB                    (CLEAR FOR ICM INSTRUCTION)
         ICM   RTEB,SEVEN,UCBTEB+1    IS TEB AVAILABLE
         BZ    GTT0040                 IF NO, CHECK IF MORE DSABS
         SR    RREB,RREB                    (CLEAR FOR ICM INSTRUCTION)
         ICM   RREB,SEVEN,TEBREB+1    IS REB AVAILABLE
         BZ    GTT0040                 IF NO, CHECK IF MORE DSABS
         SR    RADR,RADR                    (CLEAR FOR ICM INSTRUCTION)
GTT0060  ICM   RADR,SEVEN,REBTCB+1
         CR    RADR,RTCB               IF YES, IS REB TCB = CURRENT TCB
         BE    GTT0070                   IF YES, OK TO DEACTIVATE
         ICM   RREB,SEVEN,REBL+1         IF NO, ARE MORE REBS AVAILABLE
         BZ    GTT0040                     IF NO, CHECK IF MORE DSABS
         B     GTT0060                     IF YES, CHECK FOR TCB MATCH
GTT0070  L     RSIZE,DARPLIST         GET SAVE/WORK AREA FOR DAR USE
         GETMAIN R,LV=(0)
         L     RLIST,REBGACB          SET UP PLIST FOR DAR
         ST    RLIST,ADR(RADR)          1. ADDRESS OF GACB
         LA    RLIST,TWO
         ST    RLIST,ZERO(RADR)         2. LENGTH OF PLIST IN WORDS
         L     RREB,REBL              KEEP NEXT REB ADR (IF ANY)
         LR    RLIST,RADR             SAVE ADDR FOR FREEMAIN
         SVC   DAR                    ISSUE DAR  SVC
         LR    RADR,RLIST             FREE PLIST AREA
         L     RSIZE,DARPLIST
         FREEMAIN R,LV=(0),A=(1)
         LTR   RREB,RREB              ANY MORE REBS ON THIS TEB
         BZ    GTT0040                 IF NO, CHECK IF MORE DSABS
         B     GTT0060                 IF YES, CHECK FOR TCB MATCH
         EJECT
***********************************************************************
*                                                                     *
*                  ABNORMAL TASK TERMINATION                          *
*                                                                     *
*         1. SCAN LOOKUP TABLE UCBS FOR GAM SUPPORTED DEVICES         *
*                                                                     *
***********************************************************************
GTT0080  EQU   *
         USING CVT,RSCB1
         L     RSCB1,CVTPTR           GET CVT ADDR: FIXED LOCATION 16
         L     RSCB1,CVTILK2          GET UCB LOOKUP TABLE ADDR
         DROP  RSCB1
GTT0090  EQU   *
         SR    RUCB,RUCB               CLEAR REGISTER        LG Z30AALG
         ICM   RUCB,THREE,ZERO(RSCB1)  GET 1'ST ENTRY        LG Z30AALG
         C     RUCB,TBLEND            END OF LOOKUP TABLE?  LG @ZM30151
         BE    GTT0010                YES,NOTHING TO DO.    LG @ZM30151
         LTR   RUCB,RUCB           IS ENTRY 0?              L5 @ZA03950
         BZ    GTT0100             YES,GET NEXT ENTRY       L5 @ZA03950
         B     GTT0110                CK IF GRAPHICS DEVICE LG @ZM30151
GTT0100  EQU   *
         LA    RSCB1,TWO(RSCB1)       ADJUST TO NEXT ENTRY
         B     GTT0090                   CONTINUE SCANNING
GTT0110  EQU   *
         L     RSCB2,UCBEXTPT         GET UCB EXTENTION ADDRESS
         CLC   UCBASID,RMPLASID       DO MEMORIES MATCH
         BNE   GTT0100                   IF NO, CONTINUE SEARCH
         CLI   UCBTBYT3,UCB3DISP      CHECK IF GRAPHIC UCB
         BNE   GTT0100                   IF NO, CONTINUE SEARCH
         CLI   UCBTBYT4,UCBT1053         IF YES, CK IF GRAPHIC DEVICE
         BE    GTT0113                 IF YES, RESET UCB    L5 @ZA02832
         CLI   UCBTBYT4,UCBT2260
         BE    GTT0113                                      L5 @ZA02832
         CLI   UCBTBYT4,UCBT2250
         BNE   GTT0100                     IF NO, CONTINUE SEARCH
         EJECT
***********************************************************************
*                                                                     *
*                  ABNORMAL TASK TERMINATION                          *
*                                                                     *
*         2. FOR 2250S, RESET BUFFER TABLE FOR REALLOCATION           *
*                                                                     *
***********************************************************************
         USING BUFTAB,RBTAB       ESTABLISH UCB BUFFER TABLE ADDRESSING
GTT0113  TM    RMPLFLG1,RMPLTERM   MEM TERM?                L5 @ZA02832
         BZ    GTT0165             NO,CHK TCB TERM          L5 @ZA02832
GTT0115  CLI   UCBTBYT4,UCBT2250   2250?                    L5 @ZA02832
         BNE   GTT0160             NO,PURGE UCB             L5 @ZA02832
*                                  YES,RESET BUFFER TABLE   L5 @ZA02832
*                                  BEFORE PURGE             L5 @ZA02832
         SR    RBTAB,RBTAB        CLEAR FOR ICM INSTRUCTION
         ICM   RBTAB,SEVEN,UCBBTB      IS BUFFER TABLE AVAILABLE
         BZ    GTT0160                   IF NO, SKIP BUFFER RESET CODE
         SR    RDEV,RDEV                 IF YES, RESET FOR THIS UCB
         IC    RDEV,UCBDI              GET DEVICE INDEX NUMBER FOR BT
         LR    RLOC,RDEV               CALC DEVICE DISPLACEMENT INTO BT
         BCTR  RLOC,ZERO                 REDUCE DEVICE NBR BY ONE &
         SLL   RLOC,THREE                MULTIPLY BY EIGHT BYTES
         LH    RCALC,BTDVAS(RLOC)      GET SECTIONS ASSIGNED TO DEVICE
         AH    RCALC,BTTOTAVL            MAKE SECTIONS AVAIL FOR REUSE
         STH   RCALC,BTTOTAVL
         SR    RCALC,RCALC             INDICATE NO SECTIONS ASSIGNED TO
         STH   RCALC,BTDVAS(RLOC)        THIS DEVICE
         LH    RLAST,BTTOTSEC          GET TOTAL NUMBER OF SECTIONS
         TM    UCBTBYT1,UCBTMOD3       IS DEVICE A 2250-3
         BO    GTT0120                   IF YES, BT HAS 40 BYTE HEADER
         LA    RENTRY,BTASGMT1           IF NO,  BT HAS 16 BYTE HEADER
         B     GTT0130                 PREPARE FOR BT SCAN OF ASGMENTS
GTT0120  LA    RENTRY,BTASGMT3         GET ADDR OF FIRST ENTRY, 2250-3
GTT0130  AR    RLAST,RENTRY            CALC LAST ENTRY ADDR
GTT0140  EX    RDEV,ASSIGNED           IS SECTION ASSIGNED TO THIS UCB
         BNE   GTT0150                   IF NO, DO NOT FREE SECTION
         MVI   ZERO(RENTRY),ZERO         IF YES, FREE BY RESET TO ZERO
GTT0150  LA    RENTRY,ONE(RENTRY)      ADJUST TO NEXT ENTRY ADDRESS
         CR    RENTRY,RLAST            IS LAST ENTRY PROCESSED
         BNH   GTT0140                   IF NO, REPEAT SCANNING
         DROP  RBTAB
         EJECT
***********************************************************************
*                                                                     *
*                  ABNORMAL TASK TERMINATION                          *
*                                                                     *
*          3.  RESET UCB FIELDS:  OPEN, GCB, TEB                      *
*                                                                     *
***********************************************************************
GTT0160  EQU   *
         SR    RADR,RADR       GET ZEROS TO REINITIALIZE:
         STH   RADR,UCBOPEN       DCBS OPEN COUNT AND GCB
         ST    RADR,UCBTEB        TEB TO DEACTIVATE ATTN HANDLING
         B     GTT0100         GET NEXT UCB FOR PROCESSING
GTT0165  SR    RTEB,RTEB           CLR TEB REG              L5 @ZA02832
         ICM   RTEB,SEVEN,UCBTEB+1  GET TEB ADDR            L5 @ZA02832
         BZ    GTT0100              NO TEB, GET NEXT UCB    L5 @ZA02832
         CLC   TEBTCB+1(THREE),RMPLTCBA+1  TEBTCB=ABEND TCB?   @ZA02832
         BNE   GTT0100             NO,GET NEXT UCB          L5 @ZA02832
         B     GTT0115                                      L5 @ZA02832
         EJECT
**********************************************************************
*                                                                    *
*                           CONSTANTS                                *
*                                                                    *
**********************************************************************
         DS    0F
DARPLIST DC    X'FF000040'        SUBPOOL/SIZE FOR GETMAIN    LI SM1709
TBLEND   DC    X'0000FFFF'        LOOKUP TBL END INDICATOR  LG @ZM30151
ASSIGNED CLI   ZERO(RENTRY),ZERO  COMPARE UCBDI WITH BT ENTRY
         SPACE 5
**********************************************************************
*                                                                    *
*                SPECIAL MAINTENANCE PATCH AREA FOR FIELD USE        *
*                                                                    *
**********************************************************************
PATCH    DS    0F                  50 BYTE PATCH AREA       D11
         DC    50X'FF'                                      D11
         TITLE 'GRAPHICS TASK RECOVERY SECTION'
***********************************************************************
*
* MODULE NAME:           IGG0213Z (OS/VS2)
*
* DESCRIPTIVE NAME:      GRAPHICS TASK RECOVERY
*
* COPYRIGHT:             NONE
*
* STATUS:                RELEASE 2.0
*
* FUNCTION:              THIS MODULE PERFORMS TASK RECOVERY FUNCTIONS
*                        FOR THE 2250S AND 2260S SUPPORTED BY GPS WHEN
*                        OPEN/CLOSE ABENDS.
*
*                        THIS INCLUDES:
*                          1. RECOVER THE UCB SO IT IS AVAILABLE FOR
*                             REALLOCATION (DONE IN EARLIER RELEASES
*                             BY THE FORCED CLOSE ABEND MACRO IGGIFF01)
*                             THE FIELDS RESET ARE:
*                                GCB   ZEROED SO ATTENTIONS ARE IGNORED
*                                OPEN  ZEROED TO INDICATE NO DCBS OPEN
*                                TEB   ZEROED TO PREVENT USING BAD CBS
*
*                          2. RESTORE CONTROL BLOCKS TO PREOPEN STATUS
*                             IF POSSIBLE, USING INFO ONLY IN PROTECTED
*                             STORAGE TO HELP SUCCESSFUL RECOVERY.
*                                A. RELEASE 2250 ALLOCATED BUFFER
*                                B. DEACTIVATE EXPRESS USER POLL LIST
*
******************* PROLOGUE*CONTINUED*NEXT*PAGE **********************
         EJECT
***********************************************************************
* NOTES:
*
*     DEPENDENCIES:      KEY 5 CONTROL IS RECEIVED FROM SYSTEM OPEN.
*                        USER DCB ADDRESS, KEY AVAIL FROM SYS WORKAREA.
*                        GRAPHIC DEVICES ARE NOT SHARABLE; THUS:
*                          1. RECOVERY ASSUMES SUCCESSFUL REALLOCATION
*                             IS THE PRIMARY GOAL
*                          2. LOCAL LOCK IS NOT NEEDED FOR UCB RESETS
*                        SYSTEM CLOSE RESTORES DCB TO PREOPEN STATUS
*                          AND FREES DEB.
*
*     RESTRICTION:       NONE
*
*     RESISTERS:         SEE EQUATE DEFINITIONS FOLLOWING PROLOG
*
*     PATCH LABEL:       PATCH1, A 50 BYTE DC AREA AT END OF MODULE
*
* MODULE TYPE:           EXECUTABLE CODE
*
*    PROCESSOR:          ASSEMBLER XF
*
*    MODULE SIZE:        SEE PAGE 1 OF THIS LISTING
*
*    ATTRIBUTES:
*       AT ENTRY:        REENTRANT  ENABLED  SUPERVISOR STATE IN KEY 5
*       ASSUMED KEYS:    0, USER (AS RECORDED IN SYSTEM OPEN WORKAREA)
*
* ENTRY POINT:           IGG0213Z
*
*    PURPOSE:            SEE 'FUNCTION' HEADING ABOVE
*
*    LINKAGE:            SEE 'INPUT', NEXT HEADING
*
* INPUT:                 REGISTERS AS FOLLOWS:
*                          2 ADDR OF: COPIED DCB IN WORK AREA
*                          4          WORK AREA
*                          9          TIOT DD ENTRY, OR ZEROS
*                         11          DEB FOR DCB, OR ZEROS
*                         13          SAVE AREA OF 18 WORDS
*                         14          RETURN ADDRESS TO TASK RECOVERY
*                         15          IGG0213Z ENTRY POINT
*
* OUTPUT:                SEE 'FUNCTION' HEADING ABOVE
*
******************* PROLOGUE*CONTINUED*NEXT*PAGE **********************
         EJECT
***********************************************************************
* EXITS:
*
*    NORMAL:             RETURN MACRO, RC=0
*
*    ERROR:              RETURN MACRO, RC=4  NO RETRY
*
*
* EXTERNAL REFERENCES:
*
*     ROUTINES:          NONE
*
*     DATA AREAS:        SEE DSECT SECTION AT END OF LISTING
*
*     CONTROL BLOCKS:    SEE DSECT SECTION AT END OF LISTING
*
* TABLES:                NONE
*
* MACROS:                MODESET     SAVE       RESTORE
*
* CHANGE ACTIVITY:       MODULE IS NEW FOR VS2/R2
*
***********************************************************************
         SPACE 3
IGG0213Z CSECT
         EJECT
***********************************************************************
*                                                                     *
*                 ADDITIONAL REGISTERS FOR IGG0213Z                   *
*                                                                     *
***********************************************************************
RDCB     EQU   3          ADDR OF:     DCB BEING FORCED CLOSED
RCORE    EQU   4                       WORK AREA USED BY OPEN/CLOSE
REXT     EQU   5                       NUMBER OF EXTENTS (UCBS)
RRRPL    EQU   8                       RECOVERY ROUTINE PARM LIST
RTIOT    EQU   9                       DD ENTRY IN TIOT BEING PROCESSED
RDEB     EQU   11                      DEB
RBACK    EQU   15                      NSI FOR SUBRTN RETURN
         SPACE 3
***********************************************************************
*                                                                     *
*                        EQUATES                                      *
*                                                                     *
***********************************************************************
ADR      EQU   4                       INDEX FACTOR FOR ENTRIES
RC4      EQU   4                       RETURN CODE TO PREVENT RETRY
         EJECT
***********************************************************************
*                                                                     *
*                        INITIALIZATION                               *
*                                                                     *
*         1. USING STATEMENTS IDENTIFY DSECT BASE REGISTERS           *
*         2. REGISTERS SAVED IN CALLER'S AREA                         *
*         3. BASE ESTABLISHED FOR ADDRESSABILITY OF MODULE            *
*                                                                     *
***********************************************************************
         USING IHADCB,RDCB
         USING FORCORE,RCORE
         USING IECRRPL,RRRPL
         USING DEBBASIC,RDEB
         USING TIOENTRY,RTIOT
         SAVE  (14,12)
         LR    RDCB,RBASE     KEEP GAM OPEN/CLOSE CONVENTIONAL SETUPS
         BALR  RBASE,0
         USING *,RBASE
         EJECT
***********************************************************************
*                                                                     *
*                  CHECK FOR VALID INPUT TO PROCESS                   *
*                                                                     *
*          1.  TRY TO VERIFY UCBS ARE FOR 2250, 2260 OR 1053          *
*               A. USE TIOT AS SAFEST SOURCE OF INFO SINCE            *
*                  OPEN MAY HAVE ABENDED BEFORE DEB COMPLETED         *
*               B. USE DEB AS SECOND CHOICE FOR UCB INFO              *
*           2. IF GRAPHICS DEVICES CAN NOT BE VALIDATED:              *
*               A. USE RC=4+ TO PREVENT RETRIES FOR GAM               *
*               B. CK DCB AND DEACTIVATE POLL LIST IF A GAM           *
*                  DCB CAN BE VALIDATED.                              *
*                                                                     *
***********************************************************************
         MODESET EXTKEY=ZERO
         LTR   RTIOT,RTIOT       IS A TIOT DD ENTRY AVAILABLE
         BZ    DEBCK               IF NO, CHECK FOR A DEB
         SR    REXT,REXT             CLEAR REG FOR LGH INSERT
TIOTOK   EQU   *
         IC    REXT,TIOELNGH         GET LGH OF ENTRY TO CALC # OF UCBS
         LTR   REXT,REXT                IS THERE A DD ENTRY
         BZ    DEBCK                     IF NO, TRY TO USE DEB
         TM    TIOESTTA,DYNALLOC   THIS A DYNAMIC ENTRY?    D11 ZA10028
         BO    DEBCK               YES,NOGOOD,USE DEB       D11 ZA10028
         LA    RLOC,TIOESTTB             GET ADDR OF UCB DEVICE ENTRIES
         AR    RTIOT,REXT            ADJUST TO NEXT DD ENTRY
         SH    REXT,SIXTEEN              IF YES, CALC # ENTRIES
         BNP   DEBCK                        IF NEGATIVE, NO ENTRIES
         SRL   REXT,TWO                     IF POSITIVE, 4 BYTES/ENTRY
         BAL   RBACK,RESETUCB            CLEAR UCB FIELDS
         B     TIOTOK            CONTINUE WITH NEXT DD ENTRY
         EJECT
***********************************************************************
*                                                                     *
*                DEB USED AS SECOND CHOICE FOR UCB RESET INFO         *
*                                                                     *
***********************************************************************
DEBCK    EQU   *
         LTR   RDEB,RDEB       IS THERE A DEB
         BZ    DCBCK             IF NO, CHECK IF A GAM DCB IS AVAILABLE
         SR    REXT,REXT         IF YES, CLEAR FOR NBR OF UCBS
         LA    RLOC,DEBSUCBA       GET ADDR OF UCB ENTRIES
         IC    REXT,DEBNMEXT       GET NBR OF UCBS
         BAL   RBACK,RESETUCB     CLEAR UCB FIELDS
         EJECT
***********************************************************************
*                                                                     *
*               DEACTIVATE POLL LIST OF DCBS IF:                      *
*                 1. DCB BEING RECOVERED IS FOR GRAPHICS              *
*                 2. EXPRESS ATTENTION HANDLING BEING USED            *
*                                                                     *
*           NOTE:  POLL LIST PROCESSING DONE IN USER'S KEY            *
*                                                                     *
***********************************************************************
DCBCK    EQU   *
         CLI   DCBDSRG2,DCBDSGGS     CK IF DCB IS GRAPHICS
         BE    DCBOK                    IF YES, OK TO CHECK POLL LIST
         LA    RCODE,RC4                IF NO, RETURN WITH RC, NO RETRY
         B     RETURN
DCBOK    EQU   *
         L     RADR,DCBPOLST         CK IF POLL LIST IS AVAILABLE
         LTR   RADR,RADR                IF NO, PROCESSING DONE
         BZ    DONE
         CLI   DCBGTYPE,DCBGTEXP        IF YES, MUST BE EXPRESS
         BNE   DONE                        IF NOT, DON'T ZERO
         MODESET  KEYADDR=DXUKEY,WORKREG=15
         SR    RLIST,RLIST              GET SOURCE OF ZEROS
         ST    RLIST,ZERO(RADR)         ZERO POLL LIST ACTIVE ENTRY
         MODESET  EXTKEY=DATAMGT
DONE     EQU   *
         SR    RCODE,RCODE             INDICATE PROCESSING COMPLETED
RETURN   EQU   *
         MODESET EXTKEY=DATAMGT
         RETURN (14,12),T,RC=(15)
         EJECT
***********************************************************************
*                                                                     *
*            RESET UCB FIELDS    (INTERNAL SUBROUTINE)                *
*                                                                     *
*        SEE PROLOG FUNCTION DESCRIPTION FOR DETAILS OF FIELDS        *
*                                                                     *
***********************************************************************
RESETUCB EQU   *
         L     RUCB,ZERO(RLOC)    LOAD FIRST/NEXT UCB ADDRESS
         CLI   UCBID,UCBSTND      CHECK FOR UCB VALIDITY
         BNE   NEXTUCB               IF INVALID, GET NEXT UCB
         CLI   UCBTBYT3,UCB3DISP     IS DEVICE GRAPHICS TYPE
         BNE   NEXTUCB                 IF NO, GET NEXT UCB
         CLI   UCBTBYT4,UCBT1053     IS DEVICE 1053 TYPE
         BE    UCBOK                   IF YES,  OK TO PROCESS
         CLI   UCBTBYT4,UCBT2260     IS DEVICE 2260 TYPE
         BE    UCBOK                   IF YES,  OK TO PROCESS
         CLI   UCBTBYT4,UCBT2250     IS DEVICE 2250 TYPE
         BNE   NEXTUCB                 IF NO, GET NEXT UCB
UCBOK    EQU   *
         SR    RADR,RADR          GET SOURCE OF ZEROS
         STH   RADR,UCBOPEN        RESET COUNTER AND CLEAR GCB
         ST    RADR,UCBTEB         PREVENT USE OF BAD CBS
NEXTUCB  EQU   *
         LA    RLOC,ADR(RLOC)     ADJUST ADDR TO NEXT ENTRY
         BCT   REXT,RESETUCB       GET NEXT UCB ENTRY
         BR    RBACK               RETURN FOR CONTINUED PROCESSING
SIXTEEN  DC    H'16'
         SPACE 3
**********************************************************************
*                                                                    *
*                SPECIAL MAINTENANCE PATCH AREA FOR FIELD USE        *
*                                                                    *
**********************************************************************
PATCH1   DS    0F         50 BYTE PATCH AREA.'              D11
         DC    50X'FF'                                      D11
         EJECT
BUFTAB   DSECT
*
BTTOTSEC DS    CL2  HEADER:  TOTAL NUMBER OF 256 BYTE SECTIONS
BTNUMDEV DS    CL2                           DEVICES SHARING BUFFER
BTEXPSIZ DS    CL2                           SECTIONS FOR EXPRESS
BTTOTAVL DS    CL2                           SECTIONS STILL AVAIL
*
BTDVAS   DS    CL2  DEVICES: CURRENT NBR OF SECTIONS ASGNED TO DEVICE
BTDVDZ   DS    CL2           DISPL INTO BT FOR THIS DEVICE'S ZONE
BTDVSZ   DS    CL2           NBR OF SECTIONS IN THIS DEVICE'S ZONE
BTDVGS   DS    CL2           NBR OF GUARANTEED SECTIONS FOR THIS DEVICE
*            START OF BUFFER ASSIGNMENTS FOR 2250-1S
BTASGMT1 DS    CL56 ASGMT:   ASSIGNMENTS WILL CONTAIN DEVICE IDS
         SPACE 3
         ORG   BTASGMT1
*
BTDV2AS  DS    CL2  DEVICE2: CURRENT NBR OF SECTIONS ASGNED TO DEVICE
BTDV2DZ  DS    CL2           DISPL INTO BT FOR THIS DEVICE'S ZONE
BTDV2SZ  DS    CL2           NBR OF SECTIONS IN THIS DEVICE'S ZONE
BTDV2GS  DS    CL2           NBR OF GUARANTEED SECTIONS FOR THIS DEVICE
*
BTDV3AS  DS    CL2  DEVICE3: CURRENT NBR OF SECTIONS ASGNED TO DEVICE
BTDV3DZ  DS    CL2           DISPL INTO BT FOR THIS DEVICE'S ZONE
BTDV3SZ  DS    CL2           NBR OF SECTIONS IN THIS DEVICE'S ZONE
BTDV3GS  DS    CL2           NBR OF GUARANTEED SECTIONS FOR THIS DEVICE
*
BTDV4AS  DS    CL2  DEVICE4: CURRENT NBR OF SECTIONS ASGNED TO DEVICE
BTDV4DZ  DS    CL2           DISPL INTO BT FOR THIS DEVICE'S ZONE
BTDV4SZ  DS    CL2           NBR OF SECTIONS IN THIS DEVICE'S ZONE
BTDV4GS  DS    CL2           NBR OF GUARANTEED SECTIONS FOR THIS DEVICE
*            START OF BUFFER ASSIGNMENTS FOR 2250-3S
BTASGMT3 DS    CL56 ASGMT:   ASSIGNMENTS WILL CONTAIN DEVICE IDS
         EJECT
         CVT   DSECT=YES
         EJECT
         DCBD  DSORG=GS
         EJECT
         IEZDEB
         EJECT
         IHADSAB
         IECDSECS MAIN,EXPAND=YES
         IEZJSCB
         EJECT
         IHAQDB
         EJECT
*                  ROUTINE ENTRY BLOCK (REB)
*
*       1.  CREATED BY USER WITH GRAPHICS SPAR MACRO
*       2.  SEE SVC MODULE, IGC0007C FOR FURTHER DETAILS
*
REB      DSECT
REBL     DS    F       ADDR OF:     NEXT LOWER PRIORITY REB, OR ZEROS
REBH     DS    F                     NEXT HIGHER PRIORITY REB, OR TEB
REBUCB   DS    F                     UCB, OR LIST OF UCBS
REBGACB  DS    F                     GACB
REBGEIRB DS    F                     GEIR'S IRB
REBFLGS  DS    H       FLAGS:
REBPRTY  DS    H                     PRIORTY INDICATED BY USER IN SPAR
REBQ1    DS    F       ADDR OF:      IQE TO BE PROCESSED IF EP=0
REBQ2    DS    F                     IQE NEXT TO BE PROCESSED
REBTCB   DS    F                     TCB
REBRES   DS    F                     RESERVED
         EJECT
         IHARB
         EJECT
         IHARMPL
         IECDSECS RRPL,EXPAND=YES
         IKJTCB DSECT=YES
         EJECT
TEB      DSECT
TEBCKRB  DS    F       ADDR OF:      IRB FOR CANCEL KEY (IFFCAN03)
TEBREB   DS    F                     GAM ROUTINE ENTRY BLOCK
TEBTCB   DS    F                     TCB
TEBCKQE  DS    F                     LIST OF CANCEL KEY IQES
TEBUCBCT DS    F       USE COUNT:    NBR OF UCBS USING THIS TEB
TEBFLGS  DS    F       FLAG BYTES:
TEBFLAG  EQU   X'80'                 TEB ID FLAG
TEBGARB  DS    F       ADDR OF:      IRB FOR GAR (IGG019OE)
TEBGEIR  DS    F                     ENTRY POINT FOR GEIR (IGG019OJ)
         EJECT
TIOT     DSECT
         IEFTIOT1
         EJECT
UCB      DSECT
         IEFUCBOB
**********************************************************************
*                                                                     *
*                    ADDITIONAL UCB EQUATES FOR DSECT                 *
*                                                                     *
***********************************************************************
         SPACE 3
*          UCBTYP, BYTE 1, BIT SETTINGS
         SPACE
UCBTMOD3 EQU   X'33'           UNIT TYPE SETTING FOR 2250-3 MODEL
         SPACE 3
*          UCBTYP, BYTE 2, BIT SETTINGS
         SPACE
UCBTPFK  EQU   X'10'           UNIT TYPE BIT 3 FOR PFK FEATURE
         SPACE 3
*          UCBTYP, BYTE 4, BIT SETTINGS
         SPACE
UCBT2250 EQU   X'02'           UNIT TYPE ID FOR 2250 DEVICE
UCBT2260 EQU   X'03'                        FOR 2260
UCBT1053 EQU   X'04'                        FOR 1053
         SPACE 2
*          UCBGCB, BIT SETTINGS
         SPACE
UCBGINIT EQU   X'40'                   OK TO ACCEPT ATTNS NOW    A32068
UCBGBAS  EQU   X'08'
UCBGSET  EQU   X'B7'                   RESET ABOVE BITS TO ZERO
*   END OF ADDITIONAL UCB BIT SETTINGS                      D11
DYNALLOC EQU   X'80'               TO TEST TIOTENTRY        D11 ZA11330
         END
