         TITLE 'IFCEXXX4 - EDIT TABLE - LINKNAME=IFCE2311, ALIASES=IFCEC
               2302,IFCE2303,IFCE2321'
         SPACE 2
*  LINE MACRO     LINE MACRO     LINE MACRO     LINE MACRO            *
         MACRO
&NAME    LINE  &DATA,&LABEL,&SKIP=
.*                            THIS MACRO ESTABLISHES THE MARGINS OR
.*                                 COLUMN SETTINGS FOR EACH LINE. IT
.*                                 GENERATES NECESSARY CARRIAGE CONTROL
.*                                 ENTRIES. LASTLY IT DETERMINES THE
.*                                 NUMBER OF ENTRIES ASSOCIATED WITH
.*                                 THE PREVIOUS LINE AND STORES THAT #
.*                                 INTO THE APPROPRIATE BYTE.
.*                                 &DATA(1) - CHARACTER POSITION TO
.*                                 BEGIN DATA.
.*
.*                                 &DATA(2) - # OF SPACES BETWEEN
.*                                 SUCCESSIVE DATA COLUMN STARTING
.*                                 CHARACTERS.
.*
.*                                 &LABEL(1) - CHARACTER POSITION
.*                                 TO BEGIN LABELS.
.*
.*                                 &LABEL(2) - # OF SPACES BETWEEN
.*                                 LINE'S LABEL STARTING CHARACTERS.
.*
.*                                 &SKIP= - # OF LINES TO ADVANCE
.*                                 CARRIAGE TO BEGIN NEXT PRINTED LINE.
.*
.*                                 NEW ENTRIES ARE GENERATED ONLY WHEN
.*                                 NECESSARY. OMISSION OF ITEMS MEANS
.*                                 THEY HAVE NOT CHANGED SINCE PREVIOUS
.*                                 SETTINGS. A LINE MACRO IS REQUIRED
.*                                 FOR EACH PRINTED LINE. IF NO SETTING
.*                                 CHANGES ARE REQUIRED ALL OPERANDS
.*                                 ARE OMITTED IN WHICH CASE ONLY THE
.*                                 # OF ENTRIES FOR THE PREVIOUS LINE
.*                                 FIRST MACRO INSTRUCTION ISSUED FOR
.*                                 THE SUBJECT LINE.
         GBLA  &DB,&DT,&LB,&LT,&NRENTS
         GBLA  &LASTCC,&INITIAL
         GBLC  &NRENTSX
         GBLC  &CARRCON
         GBLB  &TEST
         LCLA  &S
&NAME    DS    0C
         AIF   (&INITIAL EQ 0).FIRSTLN
         LINEND
         AGO   .NTFIRST
.FIRSTLN ANOP
&NRENTSX SETC  'N&SYSNDX'
&INITIAL SETA  1
&LASTCC  SETA  1
N&SYSNDX EQU   *
.NTFIRST ANOP
         DC    AL1(0)
         AIF   ('&DATA(1)' EQ '').CKDTABS
         AIF   ('&DATA'(1,1) NE '(').SLABEL
         AIF   (&DATA(1) EQ &DB).CKDTABS
&DB      SETA  &DATA(1)
         AGO   .AA
.CKDTABS AIF   ('&DATA(2)' EQ '').NCDATA
         AIF   (&DATA(2) EQ &DT).NCDATA
         AGO   .CC
.AA      AIF   ('&DATA(2)' EQ '').BB
         AIF   (&DATA(2) EQ &DT).BB
.CC      ANOP
&DT      SETA  &DATA(2)
.BB      DC    XL2'FFFE'
         DC    FL1'&DB'
         DC    FL1'&DT'
&NRENTS  SETA  &NRENTS+1
.NCDATA  ANOP
         AIF   ('&LABEL(1)' EQ '').CKLTABS
         AIF   ('&LABEL'(1,1) NE '(').SLABEL
         AIF   (&LABEL(1) EQ &LB).CKLTABS
&LB      SETA  &LABEL(1)
         AGO   .AAA
.CKLTABS AIF   ('&LABEL(2)' EQ '').NCLABEL
         AIF   (&LABEL(2) EQ &LT).NCLABEL
         AGO   .CCC
.AAA     AIF   ('&LABEL(2)' EQ '').BBB
         AIF   (&LABEL(2) EQ &LT).BBB
.CCC     ANOP
&LT      SETA  &LABEL(2)
.BBB     DC    XL2'FFFF'
         DC    FL1'&LB'
         DC    FL1'&LT'
&NRENTS  SETA  &NRENTS+1
.NCLABEL ANOP
         AIF   ('&SKIP' EQ 'E').EJECT
         AIF   ('&SKIP' EQ '').TESTOFF
.CONT    ANOP
         AIF   (&TEST EQ 0).AB
         AIF   (&SKIP EQ &LASTCC).TESTOFF
&S       SETA  &SKIP
         ORG   &CARRCON+3
         DC    AL.3(0),AL.2(&S),AL.3(1)
         ORG
&LASTCC  SETA  &S
.SLABEL  ANOP
.TESTOFF ANOP
&TEST    SETB  0
         AGO   .DD
.EJECT   DC    XL2'FFFB'
&NRENTS  SETA  &NRENTS+1
         DC    X'8B'              SKIP TO CHANNEL 1
         DC    AL.3(0),AL.2(&LASTCC),AL.3(1)
&TEST    SETB  1
         AGO   .DD
.AB      AIF   (&SKIP EQ &LASTCC).NOCHANG
A&SYSNDX DC    XL2'FFFB'
         DC    AL.3(0),AL.2(&SKIP),AL.3(1)
         DC    AL.3(0),AL.2(&SKIP),AL.3(1)
&LASTCC  SETA  &SKIP
&TEST    SETB  1
&CARRCON SETC  'A&SYSNDX'
&NRENTS  SETA  &NRENTS+1
.DD      ANOP
.NOCHANG ANOP
         MEND
         SPACE 2
*  ROUTINE MACRO     ROUTINE MACRO     ROUTINE MACRO     ROUTINE MACRO*
         MACRO
&NAME    ROUTINE &LOGITEM,&RNAME
         GBLC  &DSECT,&CSECT
         GBLC  &ITEM(1000)
         GBLA  &SHIFT(1000),&ITEMNBR
         GBLA  &NRENTS
         LCLA  &ISHIFT,&NDX
.LOOP    ANOP
&NDX     SETA  &NDX+1
         AIF   ('&ITEM(&NDX)' EQ '&LOGITEM').FOUND
         AIF   (&NDX LT &ITEMNBR).LOOP
         MNOTE *,'LOG ITEM NOT DEFINED'
         MEXIT
.FOUND   ANOP
&ISHIFT  SETA  &SHIFT(&NDX)
&NAME    DC    AL.12(&LOGITEM-&DSECT),XL.4'D'
         DC    AL.4(&ISHIFT),AL.12(&RNAME-&CSECT)
&NRENTS  SETA  &NRENTS+1
         MEND
*  DSGEN MACRO     DSGEN MACRO     DSGEN MACRO     DSGEN MACRO        *
         SPACE 2
         MACRO
&NAME    DSGEN &X1,&X2,&X3,&X4,&X5,&X6,&X7,&X8,&X9,&X10,&X11,&X12,&X13,X
               &X14,&X15,&X16,&X17,&X18,&X19,&X20,&X21,&X22,&X23,&X24, X
               &X25,&X26,&X27,&X28,&X29,&X30,&X31,&X32
.*                            THIS MACRO GENERATES ONE DSECT WORD
.*                                 FORMATTED FOR THE SUBJECT LOG RECORD
.*                                 EACH OPERAND HAS TWO SUB LISTS. THE
.*                                 FIRST IS THE NAME OF THE LOG ITEM
.*                                 WHILE THE SECOND IS THE # OF BITS TO
.*                                 ALLOCATE FOR THIS ITEM. ITEMS MUST
.*                                 BE DEFINED CONTIGUOUSLY WITHOUT GAPS
.*                                 UNUSED BIT GROUPS MUST BE GIVEN
.*                                 DUMMY NAMES. THE NAMES PROVIDED FOR
.*                                 THE LOG ITEMS MAY THEN BE USED IN
.*                                 THE CONVERSION MACRO INSTRUCTIONS.
         GBLC &ITEM(1000)
         GBLA &ITEMNBR,&BITS(1000),&SHIFT(1000)
         LCLC  &SYMBOL
         LCLA  &OP,&COUNT,&BYTE
.LOOP    ANOP
&OP      SETA  &OP+1
&ITEMNBR SETA  &ITEMNBR+1
&SYMBOL  SETC  '&SYSLIST(&OP,1)'
         AIF   ('&SYSLIST(&OP,2)' NE '').OK1
         MNOTE *,'NULL BIT COUNT FIELD'
         MEXIT
.OK1     ANOP
         AIF   ('&SYMBOL' NE '').OK
         MNOTE 'LOG ITEM SYMBOL NOT PROVIDED'
         AGO   .CNT
.OK      ANOP
         AIF   ('&SYMBOL'(1,1) EQ '''').CNT                *TEMP*
&SYMBOL  EQU   *+&BYTE
&ITEM(&ITEMNBR)  SETC '&SYSLIST(&OP,1)'
&BITS(&ITEMNBR) SETA &SYSLIST(&OP,2)
&SHIFT(&ITEMNBR) SETA &COUNT-&BYTE*8
.CNT     ANOP
&COUNT   SETA  &COUNT+&SYSLIST(&OP,2)
&BYTE    SETA  &COUNT/8
         AIF (&OP LT N'&SYSLIST).LOOP
         AIF   (&COUNT NE 32).NOT32
&NAME    DS    4C
         MEXIT
.NOT32   AIF   (&COUNT NE 64).ERR
&NAME    DS    8C
         MEXIT
.ERR     MNOTE '**BIT COUNT FIELDS DO NOT TOTAL 32 OR 64**'
         MEND
         MACRO
         LINEND
.*                            THIS MACRO IS USED ONLY BY THE LINE
.*                                 MACRO TO SET THE NUMBER OF ENTRIES
.*                                 ASSOCIATED WITH EACH LINE.
         GBLA  &NRENTS
         GBLC &NRENTSX
         ORG   &NRENTSX
         DC    FL1'&NRENTS'
         ORG
&NRENTS  SETA  0
B&SYSNDX EQU   *
&NRENTSX SETC  'B&SYSNDX'
         MEND
         SPACE 2
         EJECT
*  LABEL MACRO     LABEL MACRO     LABEL MACRO     LABEL MACRO        *
         SPACE 2
         MACRO
&NAME    LABEL &LABEL1,&LABEL2,&LABEL3,&LABEL4,&LABEL5,&LABEL6,        C
               &LABEL7,&LABEL8
.*                            THIS MACRO ALLOWS UP TO ACHT LABELS TO
.*                                 BE SPECIFIED FOR A GIVEN LINE. THE
.*                                 LABELS SHOULD BE ENCLOSED IN QUOTA-
.*                                 TION MARKS. THE COLUMN AND LINE END
.*                                 CONTROLS WILL BE INSERTED BY THE
.*                                 MACRO. OMITTED OPERANDS MEAN NO
.*                                 LABEL REQUIRED FOR RESPECTIVE
.*                                 COLUMN. THIS MACRO WILL ALSO ALERT
.*                                 THE PROGRAMMER TO EXACTLY WHERE THE
.*                                 TABLE MUST BE BROKEN IF NECESSARY
.*                                 TO AVOID EXCEEDING THE MAXIMUM LIMIT
         GBLC &LABELS(2000)
         GBLC  &LBL(100)
         GBLA  &LBLCTR(100)
         GBLA  &LABELNR
         GBLA  &NRENTS,&LPTR
         LCLC  &MARGIN,&TACK
         LCLC  &TEXT
         LCLA  &OP,&K,&START,&NRCHAR,&REMAIN
&MARGIN  SETC  '*'
         AIF   ('&NAME' EQ '').LOOPOUT
&LPTR    SETA  &LPTR+1
&LBL(&LPTR) SETC '&NAME'
&LBLCTR(&LPTR) SETA &LABELNR+1
.LOOPOUT ANOP
&OP      SETA  &OP+1
&K       SETA  K'&SYSLIST(&OP)
         AIF (&K GE 2).NORM
&K       SETA  2
.NORM    ANOP
&START   SETA  2
         AIF (&OP LT N'&SYSLIST).NOTLAST
&MARGIN  SETC  '$'
.NOTLAST ANOP
&NRCHAR  SETA  8
&TACK    SETC  ''
.LOOPIN  ANOP
&REMAIN  SETA  &K-&START
         AIF (&REMAIN GE 8).MORE
&TACK    SETC  '&MARGIN'
&NRCHAR  SETA  &REMAIN
.MORE    ANOP
&TEXT    SETC  ''
         AIF (&REMAIN EQ 0).ENDTEXT
&TEXT    SETC  '&SYSLIST(&OP)'(&START,&NRCHAR)
.ENDTEXT ANOP
&LABELNR SETA  &LABELNR+1
&LABELS(&LABELNR) SETC '&TEXT'.'&TACK'
&START   SETA  &START+8
         AIF ('&TACK' EQ '').LOOPIN
         AIF ('&TACK' EQ '*').LOOPOUT
         MEND
         SPACE 2
         MACRO
         LSTART
.*                            THIS MACRO IS ISSUED WHEN THE LABEL AREA
.*                                 OF THE TABLE IS TO BEGIN.
         GBLC &LABELS(2000)
         GBLA  &LABELNR
         GBLC  &LBL(100)
         GBLA  &LBLCTR(100)
         LCLA  &NDX,&CTR
         LINEND
&CTR     SETA  1
.LOOP    ANOP
&NDX     SETA  &NDX+1
         AIF   (&LBLCTR(&CTR) NE &NDX).NOLBL
&LBL(&CTR) DC  C'&LABELS(&NDX)'
&CTR     SETA  &CTR+1
         AGO   .LOOPCTL
.NOLBL   ANOP
         DC    C'&LABELS(&NDX)'
.LOOPCTL AIF   (&NDX LT &LABELNR).LOOP
         MEND
         SPACE 2
*  BIN MACRO     BIN MACRO     BIN MACRO     BIN MACRO     BIN MACRO  *
         SPACE 2
         MACRO
         BIN   &LOGITEM,&SKIP$OR
.*                                 &LOGITEM - NAME OF ITEM IN LOG
.*                                      RECORD DSECT
.*                                 &SKIP$OR - NBR OF SPACES TO SKIP ON
.*                                 MULTIPLE PRINT OR NBR OF SPACES
.*                                      TO BACK UP FROM COLUMN FOR
.*                                      START OF PRINTING. THE LETTER
.*                                      B PRECEEDS THE NUMBER (B5) IF
.*                                      THE LATTER IS DESIRED. OMISSION
.*                                      OF THIS OPERAND FORCES OPTION 2
.*                                      WITH BACKUP SPACING EQUAL TO
.*                                 THE NUMBER OF BITS IN THE ITEM
.*                                 DEFINITION (VIA MACRO 'DSGEN').
.*                            A LIMIT OF 15 BITS IS IMPOSED FOR THE
.*                                 TOTAL NUMBER OF BITS TO PRINT.
         GBLC &ITEM(1000)
         GBLA &ITEMNBR,&BITS(1000),&SHIFT(1000),&NRENTS
         GBLC  &DSECT
         LCLB  &B
         LCLA  &IBITS,&NDX,&ISHIFT,&K
         LCLA  &TEMP,&T
         LCLC  &S
.LOOP    ANOP
&NDX     SETA  &NDX+1
         AIF   ('&ITEM(&NDX)' EQ '&LOGITEM').FOUND
         AIF   (&NDX LT &ITEMNBR).LOOP
         MNOTE *,'YOU BLEW IT'
         ROUTINE DUM,SKIPPER
         MEXIT
.FOUND   ANOP
&ISHIFT  SETA  &SHIFT(&NDX)
&IBITS   SETA  &BITS(&NDX)
&T       SETA  &IBITS-1
&B       SETB  0
         AIF   ('&SKIP$OR' EQ '').BACKUP
&K       SETA  K'&SKIP$OR
&B       SETB  ('&SKIP$OR'(1,1) EQ 'B')
&S       SETC  '&SKIP$OR'(&B+1,&K-&B)
&T       SETA  &S
.BACKUP  ANOP
&NRENTS  SETA  &NRENTS+1
&TEMP    SETA  &ISHIFT
A&SYSNDX DC    AL.12(&LOGITEM-&DSECT),XL.4'&TEMP'
         AIF (&IBITS EQ 1 AND &B EQ 0).OUT
         ORG   A&SYSNDX
&B       SETB  1
         DC    AL.12(&LOGITEM-&DSECT),XL.4'C'
         DC    BL.1'&B',AL.7(&T),AL.4(&IBITS),FL.4'&ISHIFT'
.OUT     ANOP
         MEND
         EJECT
*  HEX MACRO     HEX MACRO     HEX MACRO    HEXN MACRO     HEX MACRO  *
         SPACE 2
         MACRO
         HEX   &LOGITEM,&SKIP$OR,&BYTES
.*                            THIS MACRO FACILITATES GENERATION OF THE
.*                                 TABLE ENTRIES SPECIFYING HEX TYPE
.*                                 CONVERSIONS.
.*                            &LOGITEM - NAME OF DATA ITEM TO BE
.*                                 CONVERTED
.*                            &SKIP$OR - NUMBER OF SPACES TO SKIP ON
.*                                 MULTIPLE PRINT OR NBR OF SPACES TO
.*                                 BACK UP FROM COLUMN FOR START OF
.*                                 PRINTING. THE LETTER B PRECEEDS THE
.*                                 NUMBER (B8) IF THE LATTER OPTION IS
.*                                 DESIRED. OMISSION OF THIS OPERAND
.*                                 FORCES OPTION 2 WITH BACKUP SPACING
.*                                 EQUAL TO THE NUMBER OF BYTES SPECI-
.*                                 FIED BY THE NEXT OPERAND (TWICE THIS
.*                                 NUMBER IF SUFFIX IS EXPLICITLY OR
.*                                 IMPLICITLY B).
.*                            &BYTES - NUMBER OF TOTAL INPUT BYTES TO
.*                                 PROCESS. THIS NUMBER IS SUFFIXED
.*                                 WITH AN L,R, OR B TO STIPULATE
.*                                 PRINTING OF THE LEFT HALF, RIGHT
.*                                 HALF, OR BOTH HALVES OF THE BYTE
.*                                 RESPECTIVELY. OMISSION OF THIS
.*                                 OPERAND DEFAULTS TO B WITH THE NUM-
.*                                 BER OF BYTES EQUAL TO THE ITEM LNGTH
.*                            A NEW ENTRY IS GENERATED ONLY WHEN NECES-
.*                                 SARY. THIS MACRO MUST BE THE FIRST
.*                                 MACRO INSTRUCTION ISSUED FOR THE
.*                                 SUBJECT LINE(S).
         GBLC &ITEM(1000),&DSECT
         GBLA &ITEMNBR,&BITS(1000),&SHIFT(1000),&NRENTS
         LCLB  &B
         LCLA  &IBITS,&NDX,&ISHIFT,&K,&T,&CODE,&BYTESP
         LCLC  &S,&LETTER
.LOOP    ANOP
&NDX     SETA  &NDX+1
         AIF   ('&ITEM(&NDX)' EQ '&LOGITEM').FOUND
         AIF   (&NDX LT &ITEMNBR).LOOP
         MNOTE *,'LOG ITEM NOT DEFINED'
         MEXIT
.FOUND   ANOP
&ISHIFT  SETA  &SHIFT(&NDX)
&IBITS   SETA  &BITS(&NDX)
&CODE    SETA  1
&K       SETA  K'&BYTES
         AIF   (&IBITS-(&IBITS/4)*4 EQ 0).OK
         MNOTE *,'BIT LENGTH SPECIFICATION INCONSISTENT WITH CONTROL'
         MEXIT
.OK      AIF   ('&BYTES' EQ '').USEDEFN
&LETTER  SETC  '&BYTES'(&K,1)
&S       SETC  '&BYTES'(1,&K-1)
&BYTESP  SETA  &S
         AIF   ('&LETTER' EQ 'B').BOTH
         AIF   (&ISHIFT EQ 0 OR &ISHIFT EQ 4).OK2
.ERR     MNOTE *,'DATA NOT ALLIGNED ON HEX OR BYTE BOUNDARY'
         MEXIT
.USEDEFN ANOP
&BYTESP  SETA  &IBITS/8
.BOTH    AIF   (&ISHIFT NE 0).ERR
&CODE    SETA  3
&T       SETA  &BYTESP*2-1
&S       SETC  '&T'
.OK2     AIF   (&ISHIFT EQ 0).L
&CODE    SETA  2
.L       ANOP
&B       SETB  1
         AIF   ('&SKIP$OR' EQ '').BACKUP
&K       SETA  K'&SKIP$OR
&B       SETB  ('&SKIP$OR'(1,1) EQ 'B')
&S       SETC  '&SKIP$OR'(&B+1,&K-&B)
.BACKUP  ANOP
&NRENTS  SETA  &NRENTS+1
         DC    AL.12(&LOGITEM-&DSECT),XL.4'A'
         DC    BL.1'&B',FL.7'&S',FL.4'&BYTESP',FL.4'&CODE'
.OUT     ANOP
         MEND
         MACRO
         SPECIAL  &ROUTINE,&PARAM,&SPACE=
.*                            THIS MACRO GENERATES A SPECIAL ROUTINE
.*                                 ENTRY TO BE PROCESSED BY THE SUM. M.
.*                            &ROUTINE - SYMBOL ON SPECIAL ROUTINE EWT.
.*                            &PARAM - AN 8 BIT BINARY REPRESENTATION
.*                                 TO OCCUPY THE 'MASK' FIELD BUT TO BE
.*                                 INTERPRETED AS AN ARGUMENT OF THE
.*                                 SPECIAL ROUTINE.
.*                            &SPACE - THE NUMBER OF 2BYTE COUNTERS TO
.*                                 ALLOCATE FOR REQUIRED PROCESSING.
         GBLC  &CSECT
         GBLA  &COUNTR
         LCLA  &ADD
         DC    AL1(128)
         DC    BL.8'&PARAM'
         DC    AL2(&ROUTINE-&CSECT)
         DC    AL2(&COUNTR)
&ADD     SETA  1
         AIF   ('&SPACE' EQ '').NORM
&ADD     SETA  &SPACE
.NORM    ANOP
&COUNTR  SETA  &COUNTR+&ADD
         MEND
         MACRO
         SUM   &LOGITEM,&MASK
.*                            THIS MACRO GENERATES A NORMAL EDIT ENTRY
.*                                 FOR PROCESSING BY THE SUMMARY MODULE
.*                            &LOGITEM - NAME OF DATA ITEM TO BE SUMMED
.*                            &MASK - BYTE MASK FOR BITS TO BE TESTED.
.*                                 THIS OPERAND SHOULD BE OMITTED FOR
.*                                 SINGLE BIT ITEMS IN WHICH CASE THE
.*                                 MASK WILL BE SET ACCORDING TO THE
.*                                 OFF BYTE BIT DISPLACEMENT OF THE
.*                                ITEM AS DEFINED BY THE MACRO 'DSGEN'
         GBLA  &COUNTR,&ITEMNBR,&BITS(1000),&SHIFT(1000)
         GBLC  &ITEM(1000),&DSECT
         LCLA  &IBITS,&NDX,&ISHIFT,&M,&BITNBR
.LOOP    ANOP
&NDX     SETA  &NDX+1
         AIF   ('&ITEM(&NDX)' EQ '&LOGITEM').FOUND
         AIF   (&NDX LT &ITEMNBR).LOOP
         MNOTE *,'LOGITEM NOT FOUND'
         MEXIT
.FOUND   ANOP
         DC    AL1(0)
&ISHIFT  SETA  &SHIFT(&NDX)
&IBITS   SETA  &BITS(&NDX)
         AIF   ('&MASK' EQ '').FORM
         DC    B'&MASK'
         AGO   .ITEM
.FORM    ANOP
&BITNBR  SETA  &ISHIFT-7
&BITNBR  SETA  0-&BITNBR
&M       SETA  1
.LOOP2   ANOP
         AIF   (&BITNBR EQ 0).GOTIT
&M       SETA  &M*2
&BITNBR  SETA  &BITNBR-1
         AGO   .LOOP2
.GOTIT   ANOP
         DC    AL1(&M)
.ITEM    DC    AL2(&LOGITEM-&DSECT)
         DC    AL2(&COUNTR)
&COUNTR  SETA  &COUNTR+1
         MEND
         GBLC  &DSECT,&CSECT
IFCEXXX4 CSECT
*
*        OBR EDIT TABLE     2311,2302,2303,2321
*
&CSECT   SETC  'IFCEXXX4'
&DSECT   SETC  'XXX4'
*
XXX4     DSECT
         DSGEN (XDUM1,8),(XRELN,8),(XDUM2,8),                    S21021X
               (SW2B0,1),(SW2B1,1),(SW2B2,1),(SW2B3,1),          S21021X
               (SW2B4,1),(SW2B5,1),(SW2B6,1),(SW2B7,1)           S21021
         DSGEN (XDUM2A,32)              SPARES                   S21021
         DSGEN (XDATE,32)                    DATE
         DSGEN (XTIME,32)                    TIME
         DSGEN (XDUM3,8),(XSER,24)           SPARE AND SERIAL NO
         DSGEN (XMODEL,16),(XMCEL,16)        MODEL #  AND MCEL
         DSGEN (XPGN1,32)                    PROGRAM
         DSGEN (XPGN2,32)                           NAME
         DSGEN (XFCCWCC,8),(XFCCWDA,24)      FAILING   COMM & ADDR
         DSGEN (XFCCWFL,8),(XZERO,8),(XFCCWCT,16)  CCW  CHAIN & COUNT
         DSGEN (XCSWK,8),(XCSWCA,24)               CSW  COMM & ADDR
*
         DSGEN (XCSWATTN,1),(XCSWSM,1),(XCSWCUE,1),(XCSWBSY,1),        C
               (XCSWCE,1),(XCSWDE,1),(XCSWUC,1),(XCSWUE,1),            C
               (XCSWPCI,1),(XCSWSILI,1),(XCSWPC,1),(XCSWPCHK,1),       C
               (XCSWCDC,1),(XCSWCCC,1),(XCSWICC,1),(XCSWCH,1),         C
               (XCSWCT,16)
         DSGEN (XDUMA,8),(XCUA2,24)                              S21021
         DSGEN (XDEVT1,16),(XDEVT2,16)       DEVICE TYPE
         DSGEN (XDUM4,8),(XCUA,24)           SPARE & CUA
         DSGEN (XDUM5,32)                    SPARES
         DSGEN (XVOL1,32)                    VOLUME
         DSGEN (XVOL2,16),(XDUM6,16)               SERIAL & SPARE
         DSGEN (XM,8),(XB1,8),(XB2,8),(XC1,8)
         DSGEN (XC2,8),(XH1,8),(XH2,8),(XR,8)      SEEK
         DSGEN (XDUM7,32)                         HOME
         DSGEN (XDUM8,32)                         ADDR
         DSGEN (SDR1,8),(SDR2,8),(SDR3,8),(SDR4,8),(SDR5,8),     S21021X
               (SDR6,8),(SDR7,8),(SDR8,8)                        S21021
         DSGEN (SDR9,8),(SDRA,8),                                S21021X
               (XB0CMDR,1),(XB0INTRQ,1),(XB0BUSC,1),(XB0ECHK,1),       C
               (XB0DCHK,1),(XB0OVRN,1),(XB0TRKC,1),(XB0SKCK,1),        C
               (XB1DCHK,1),(XB1TRK0,1),(XB1CYLE,1),(XB1INVS,1),        C
               (XB1RECU,1),(XB1FILP,1),(XB1MAM,1),(XB1OVFL,1)
         DSGEN (XB2UNSF,1),(XB2BIT1,1),(XB2SERC,1),(XB2TAGL,1),        C
               (XB2ALUC,1),(XB2UNSEL,1),(XB2BIT6,1),(XB2BIT7,1),       C
               (XB3RDY,1),(XB3ONLN,1),(XB3UNSF,1),(XB3BIT3,1),         C
               (XB3OLN,1),(XB3CYLE,1),(XB3BIT6,1),(XB3SEEKI,1),        C
               (XB4BIT0,1),(XB4BIT1,1),(XB4BIT2,1),(XB4BIT3,1),        C
               (XB4BIT4,1),(XB4BIT5,1),(XB4BIT6,1),(XB4BIT7,1),        C
               (XB5BIT0,1),(XB5BIT1,1),(XB5BIT2,1),(XB5BIT3,1),        V
               (XB5BIT4,1),(XB5BIT5,1),(XB5BIT6,1),(XB5BIT7,1)
EXPNOBR  DSECT                                                   S21021
EXPNHDRA DS CL52                                                 S21021
EXPNDEVA DS CL36                                                 S21021
EXPNSDRA DS CL10                                                 S21021
EXPNSNSA DS CL6                                                  S21021
*                                                                S21021
NORMOBR DSECT                                                    S21021
NORMHDRA DS CL64                                                 S21021
NORMDA   DS    CL24                                              S21021
NORMSDRA DS CL10                                                 S21021
NORMSNSA DS CL6                                                  S21021
*                                                                S21021
SHRTOBR DSECT                                                    S21021
SHRTHDRA DS CL24                                                 S21021
SHRTDEVA DS CL8                                                  S21021
SHRTSDRA DS CL10                                                 S21021
*                                                                S21021
*
*
IFCEXXX4 CSECT
         USING *,R9
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
L0       EQU   0
L1       EQU   1
L2       EQU   2
L3       EQU   3
L4       EQU   4
L5       EQU   5
L6       EQU   6
L7       EQU   7
L8       EQU   8
L9       EQU   9
L10      EQU   10
L11      EQU   11
L12      EQU   12
L13      EQU   13
L14      EQU   14
L15      EQU   15
L16      EQU   16
L17      EQU   17
L18      EQU   18
L19      EQU   19
L20      EQU   20
L21      EQU   21                                                S21021
L24      EQU   24
L26      EQU   26
L28      EQU   28
L29      EQU   29                                                S21021
L30      EQU   30
L31      EQU   31                                                S21021
L36      EQU   36                                                S21021
L44      EQU   44                                                S21021
L46      EQU   46                                                S21021
L50      EQU   50
L52      EQU   52
L60      EQU   60                                                S21021
L104     EQU   104                                               S21021
HEX08    EQU   X'08'                                             S21021
HEX10    EQU   X'10'                                             S21021
HEX20    EQU   X'20'                                             S21021
A        EQU   C'A'                                              S21021
B        EQU   C'B'                                              S21021
         USING XXX4,R2                                           S21021
*
         DC    A(ENTRIES)               PTR TO EDIT ENTRIES
         DC    CL8'IFCEXXX4'            TABLE NAME
XR9PTR   DC    F'0'                                              S21021
XLABELP  DC    F'0'                     LABEL PTR SAVE
*
*              SPECIAL  ROUTINE SECTION
*
SKIPPER  BR    R14                      RETURN
*
*
SPACER1  LA    R3,L1(R3)                SPACE ONE BLANK          S21021
         BR    R14
*
*        SET BINARY      R4 PTS TO 1 BYTE TO STRING OUT
*
SETBIN   STM   R5,R8,XSAVE              SAVE REGS
         IC    R7,L0(R4)                PICK UP CHAR
         SLL   R7,L24                   MOVE IT TO HI OF R7
         LR    R8,R3                    PRINT POS PTR
         LA    R5,L8                    COUNTER(8)
SETBINA  SR    R6,R6                    MOVE BIT TO R6
         SLDL  R6,R1                    STORE IN PRINT LINE
         STC   R6,L0(R8)                PUT ON ZONE
         MVZ   L0(L1,R8),CHAR0          UP PTR
         LA    R8,L1(R8)
         BCT   R5,SETBINA               LOOP
         B     RETURN
*                   HEX (SAME AS BIN BUT PRINTS 4 BITS)
SETHEX   STM   R5,R8,XSAVE              SAVE REGS
         LA    R8,CHARS                 PTR TO CHARS
         IC    R7,L0(R4)                PICK UP CHAR
         SLL   R7,L24                   MOVE IT TO HI OF R7
         SR    R6,R6                    CLEAR REG
         SLDL  R6,L4                    MOVE 4 BITS TO R6
         AR    R6,R8                    COMPUTE OFFSET
         MVC   L0(L1,R3),L0(R6)         MOVE CHAR TO LINE
         SR    R6,R6                    CLEAR
         SLDL  R6,L4                    MOVE LAST 4 BITS TO R6
         AR    R6,R8                    COMPUTE OFFSET
         MVC   L1(L1,R3),L0(R6)         MOVE CHAR TO LINE + 1
         B     RETURN
*
*        SET PROGRAM NAME
*
SETPGMN  DS    0H                                                S21021
         TM    SW2B0,HEX20              SHORT RECORD             S21021
         BCR   1,R14                    YES, NO JOB ID           S21021
         MVC   0(L12,R3),JOBID          MOVE IN LABEL            S21021
         MVC   L13(L8,R3),XPGN1         MOVE IN NAME             S21021
         BR    R14                                               S21021
NONAME   TM    L0(R4),HEX20             SHORT RECORD?            S21021
         BCR   14,R14                   NO, LET HEX JOBNAME PRNT S21021
         ST    R5,XSAVE                 SAVE REG                 S21021
         LA    R5,L44                   BACK UP BUFFER PNTR      S21021
         SR    R3,R5                                             S21021
         MVC   0(L17,R3),BLANKS         CLEAR OUT HEX JOBNAME    S21021
         L     R5,XSAVE                 RESTORE REG              S21021
         BR    R14                                               S21021
*
*
*                 SET VOLUME LABEL  6 CHARS
*
SETVOL   MVC   L0(L6,R3),L0(R4)         MOVE LABEL TO BUFF
         BR    R14                      RETURN
*
*
*        TEST FOR CONVERTED RECORD KEY 32
*
SETCONV  TM    L0(R4),HEX02             CONVERT SW ON
         BCR   ON,R14                   YES RETURN
         SR    R15,R15                  ZERO IS TO STOP PRINT
         BR    R14
ON       EQU   1
HEX02    EQU   X'02'
*
*        SET DEV TYPE
*
*
SETDEVTY STM   R5,R8,XSAVE              SAVE REGS
         LA    R6,DEVTABLE              DEVICE TABLE
         LA    R5,L4                    LOOP CTR
SETDEVLP CLC   L0(L2,R4),L0(R6)         CHECK TYPE
         BE    SDEVFND                  BR-EQUAL
         LA    R6,L6(R6)                BUMP TO NXT ENTRY
         BCT   R5,SETDEVLP              LOOP
         MVC   L0(L4,R3),NONE           NONE FOUND
         B     RETURN                   RETURN
SDEVFND  MVC   L0(L4,R3),L2(R6)         MOVE TYPE TO PRINT LINE
         B     RETURN                   RETURN
*
*
*        SETCPU NO AND SERIAL NO.
*
SETCPUNO STM   R5,R8,XSAVE              SAVE REGS
         SR    R5,R5
         IC    R5,L0(R4)                PICK UP CPU MODEL
         SLL   R5,L4                    NUMBER IN R5
         IC    R5,L1(R4)
         LTR   R5,R5                    TEST FOR ZERO MODEL      S21021
         LR    R5,R3                    POINT TO DATA FIELD      S21021
         BNZ   SETSER                   BRANCH IF MODEL NOT 0    S21021
         LA    R6,L46                   BACK UP TO SAY 'NONE'    S21021
         SR    R5,R6                    BACKUP BUFFER            S21021
         MVC   L0(L29,R5),UNIVERS       SAY NONE, MODEL UNIVERSALS21021
         B     RETURN
SETSER   LA    R6,L36                   SET UP TO ADD 'SERIAL NO.S21021
         SR    R5,R6                                             S21021
         MVC   0(L10,R5),SERNO                                   S21021
         B     RETURN                                            S21021
*
*
*        SET   DATE
*
SETDATE  CLC   L1(L3,R4),ZERO           IS DATE ZERO
         BE    SETDTE                   BR-YES
         UNPK  XSAVE(L8),L1(L3,R4)      UNPACK DATE
         MVC  L5(L2,R3),XSAVE+L3  MOVE YEAR
         MVC   L0(L3,R3),XSAVE+L5
         BR    R14
SETDTE   MVC   L0(L4,R3),NONE           SET DATE TO NONE
         BR    R14
ZERO     DC    XL4'00'
*
*        SET LABELS  BYTE 3 IS DEFFERENT LABELS ARE ALL 9 CHARS LONG
*                                                  + 1 FOR */$
*
SETLABEL STM   R5,R8,XSAVE              SAVE REGS
         L     R5,XLABELP               PICK UP CURRENT LABEL PTR
         CLI   L1(R4),HEX01             IS IT 2311
         BE    RETURN                   BR-YES NO SUBS
         CLI   L1(R4),HEX04
         BL    SET2303                  BR-03 2303
         BE    SET2302                  BR-04 2302
         BH    SET2321                  BR-05 2321
SET2302  LA    R6,C2302                 SET PTR TO LABELS TO BE
         B     SETLBLP                  SUBSTITUTED
SET2303  LA    R6,C2303
         B     SETLBLP
SET2321  LA    R6,C2321
SETLBLP  LA    R7,L8                    LOOP COUNT
SETLBLP1 MVC  L30(L10,R5),L0(R6)
         LA    R5,L60(R5)               BUMP TO NXT LINE OF LABELS
         LA    R6,L10(R6)               BUMP TO NXT LABEL
         BCT   R7,SETLBLP1              LOOP TILL ALL 8 SUBBED
*
RETURN   LM    R5,R8,XSAVE
         BR    R14
BKSPC1   BCTR  R3,R0                    BACKSPACE BUFFER 1 SPACE S21021
         BR    R14                      RETURN                   S21021
SPACER16 LA    R3,L16(R3)                                        S21021
         BR    R14                                               S21021
SDRCONVL STM   R5,R8,XSAVE              SAVE REGS                S21021
         SR    R6,R6                    CLEAR REG                S21021
         IC    R6,L0(R4)                PICK UP COUNTER IN QUEST S21021
         SRL   R6,L4                    DROP OFF 4 LOW BITS      S21021
         B     SDRCONVT                                          S21021
SDRCONVR STM   R5,R8,XSAVE              SAVE REGS                S21021
         IC    R6,L0(R4)                PICK UP CHAR             S21021
         SLL   R6,L28                                            S21021
         SRL   R6,L28                   DROP OFF HI ORDER BITS   S21021
SDRCONVT CVD   R6,WORK                  CONVERT IT               S21021
         UNPK  L0(L3,R3),WORK+L5(L3)    TO DECIMAL               S21021
         MVZ   L2(L1,R3),L0(R3)         PUT ON ZONE              S21021
         B     RETURN                                            S21021
*                                                                S21021
RESETR2  L     R2,SAVE2                 PICK UP SAVED REC POINTERS21021
         BR    R14                                               S21021
*                                                                S21021
TESTMP   TM    L0(R4),HEX10             IS IT A MP RECODR        S21021
         BZ    NOMP                                              S21021
         MVC   0(L20,R3),MPCPU          SHOW MULTIPROC           S21021
         TM    L0(R4),HEX08             IS IT CPUA / CPUB        S21021
         BO    MPB                      BR IT IS B               S21021
         MVI   L21(R3),A                MOVE 'A' TO PRINT LINE   S21021
NOMP     BR    R14                                               S21021
MPB      MVI   L21(R3),B                MOVE 'B' TO PRINT LINE   S21021
         BR    R14                                               S21021
NOPRT    SR    R15,R15                  STOP PRINTING            S21021
         BR    R14                                               S21021
*                                                                S21021
CONVREL  STM   R5,R8,XSAVE              SAVE WORK REGS
         SR    R6,R6                    CLEAR R6
         IC    R6,L0(R4)                PICK UP REL & SYS VALUES
         SRDL  R6,L5                    SAVE REL NO IN R7
        SLL   R6,L2                    MULTIPLY SYS VALUE BY 4
         L     R8,XLABELP               PICK UP LABEL POINTER
         LA    R6,RELTAB(R6)            INDEX INTO TABLE OF SYSTEMS
         MVC   L0(L4,R8),L0(R6)         MOVE SYSTEM NAME INTO LABEL
         SR    R6,R6                    CLEAR R6
         SLDL  R6,L5                    RETREIVE REL NO.
         LTR   R6,R6                    IS IT ZERO
         BZ    NOREL                    BR-YES
         CVD   R6,WORK                  CONVERT IT
         UNPK  L0(L2,R3),WORK+L6(L2)    UNPACK IT INTO PRINT BUFF
         MVZ   L1(L1,R3),L0(R3)         MOVE ON PROPER ZONE
         B     RETURN                   EXIT
NOREL    MVC   L0(L4,R3),NONE           MOVE IN NONE FOR REL
         B     RETURN
*                                                                S21021
TESTSHRT TM    SW2B0,HEX20              IS IT A SHORT REC        S21021
         BZ    SKIPPER                  BR-NO                    S21021
         BCTR  R3,R0                    BACKUP ONE DATA SPACE    S21021
         MVC   0(L6,R3),BLANKS          CLEAR OUT HEX 2NDARY CUA S21021
         MVC   XR9PTR(L8),SHRTADCN      MOVE IN NEW A POINTERS   S21021
         BR    R14                                               S21021
*                                                                S21021
EXPNDRTN STM   R5,R8,XSAVE                                       S21021
         ST    R2,SAVE2                 SAVE R2 FOR LATER        S21021
         LA    R5,OBRAREA               PICK UP EXPANDED AREA    S21021
         USING EXPNOBR,R5                                        S21021
         USING NORMOBR,R4                                        S21021
         XC    EXPNOBR(L104),EXPNOBR    CLEAR AREA               S21021
         TM    SW2B0,HEX20              IS IT A SHORT REC        S21021
         BO    MOVESHRT                 BR-YES                   S21021
MOVEALL  MVC   EXPNOBR(L104),NORMOBR    MOVE ENTIRE RECORD       S21021
EXPNDRET LR    R2,R5                    SWAP POINTERS            S21021
         B     RETURN                                            S21021
         USING SHRTOBR,R4                                        S21021
MOVESHRT MVC   EXPNHDRA(L24),SHRTHDRA   MOVE HEADER              S21021
         MVC   EXPNDEVA(L8),SHRTDEVA                             S21021
         MVC   EXPNSDRA(L10),SHRTSDRA                            S21021
         B     EXPNDRET                                          S21021
         DROP  R4,R5                                             S21021
*                                                                S21021
RECTYPE  STM   R5,R8,XSAVE                                       S21021
         LR    R5,R3                    SET UP DATA SLOT FOR TYPES21021
         LA    R6,L31                                            S21021
         SR     R5,R6                                            S21021
         TM    0(R4),HEX20              SHORT RECORD?            S21021
         BO    RECSHORT                                          S21021
         MVC   0(L10,R5),UNITCK         INSERT DATA 'UNIT CHECK' S21021
         B     RETURN                                            S21021
RECSHORT MVC   0(L10,R5),OVFL           DATA = 'OVERFLOW'        S21021
         B     RETURN                                            S21021
WORK     DS    D                                                 S21021
SAVE2    DS    F                                                 S21021
SHRTADCN DC    A(SHRTOBRS-L4)                                    S21021
         DC    A(SHRTOBRL)                                       S21021
OBRAREA  DS    CL104                                             S21021
BLANKS   DC    CL25' '                                           S21021
HW21     DC    H'21'                                             S21021
HW13     DC    H'13'                                             S21021
UNITCK   DC    C'UNIT CHECK'                                     S21021
OVFL     DC    C'OVERFLOW  '                                     S21021
SERNO    DC    C'SERIAL NO.'                                     S21021
MPCPU    DC    C'MULTIPROCESSOR - CPU'                           S21021
UNIVERS  DC    C'UNIVERSAL   SERIAL NO. NONE  '                  S21021
JOBID    DC    C'JOB IDENTITY'                                   S21021
*
HEX01    EQU   X'01'
HEX04    EQU   X'04'
*
RELTAB   DS    0C                                              @L03976
         DC    C'OS  '                                         @L03976
           DC    C'DOS '                                       @L03976
         DC    C'VS 1'                                         @L03976
         DC    C'V370'                                         @L03976
         DC    C'VS 2'                                         @L03976
         DC    C'    '                                         @L03976
         DC    C'    '                                         @L03976
         DC    C'    '                                         @L03976
*
*
         DS    0F
XSAVE    DS    4F
NONE     DC    C'NONE  '
CHARS    DC    C'0123456789ABCDEF'
CHAR0    DC    C'0'
C2302    DC    CL10'ACC RDY  *'
         DC    CL10'ACC OPER *'
         DC    CL10'RD SAFETY*'
         DC    CL10'WT SAFETY*'
         DC    CL10'ON LINE  *'
         DC    CL10'BIT 5    *'
         DC    CL10'BIT 6    *'
         DC    CL10'BIT 7    *'
C2303    DS    0CL80
         DC    CL10'DRUM RDY *'
         DC    CL10'DRUM OPER*'
         DC    CL10'RD SAFETY*'
         DC    CL10'WT SAFETY*'
         DC    CL10'READY    *'
         DC    CL10'BIT 5    *'
         DC    CL10'BIT 6    *'
         DC    CL10'BIT 7    *'
C2321    DS    0CL80
         DC    CL10'RDY DRIVE*'
         DC    CL10'DR OPER  *'
         DC    CL10'RD SAFETY*'
         DC    CL10'WT SAFETY*'
         DC    CL10'STRIP RDY*'
         DC    CL10'INVAL ADD*'
         DC    CL10'AUTO REST*'
         DC    CL10'CE CELL  *'
         DS    0F
DEVTABLE DC    X'2001'
         DC              C'2311'
         DC    X'2003'
         DC              C'2303'
         DC    X'2004'
         DC              C'2302'
         DC    X'2005'
         DC              C'2321'
*
*
*
ENTRIES  DS    0F
         DC    XL4'00'        FORMAT 1
         DC    XL4'00'             FORMAT 2
         DC    A(LABELS)           LABEL PTR
         DC    H'0'                DISP
*
*
*        SKIP TO CHANNEL 1
*
         DC    X'02'                                             S21021
         DC         X'000B8B00'
         DC    AL.12(XDUM1-XXX4),XL.4'D'  = ROUTINE XDUM1,EXPNDRTS21021
         DC    AL.4(0),AL.12(EXPNDRTN-IFCEXXX4)       TLB        S21021
*
*
*
         LINE  (55,23),(1,35),SKIP=1                             S21021
         ROUTINE  SW2B0,RECTYPE                                  S21021
         HEX   XMODEL,0,2B                                       S21021
         HEX   XSER,0,3B                                         S21021
         ROUTINE XMODEL,SETCPUNO                                 S21021
         LABEL '---RECORD ENTRY TYPE -','   SOURCE - OUTBOARD',  S21021X
               'MODEL-'                                          S21021
         LINE  (16,1),(4,1)                                      S21021
         ROUTINE    XRELN,CONVREL
         LABEL      'XXXX REL.'
         LINE  (69,1),(31,19)                                    S21021
         ROUTINE XPGN1,SETPGMN                                   S21021
         LABEL 'DAY YEAR','HH MM SS.TH'                          S21021
         LINE  (31,19),(25,19)                                   S21021
         ROUTINE XDATE,SETDATE                                   S21021
         HEX   XTIME,1,4B                                        S21021
         ROUTINE  XDUM1,SKIPPER                                  S21021
         HEX   XPGN1,B6,4B                                       S21021
         HEX   XPGN2,B16,4B                                      S21021
         ROUTINE  SW2B0,NONAME                                   S21021
         LABEL 'DATE-','TIME-'                                   S21021
         LINE  (1,1),(1,1)                                       S21021
         ROUTINE SW2B0,TESTMP                                    S21021
         LABEL ''                                                S21021
         LINE  ,(1,1),SKIP=2                                     S21021
         ROUTINE XDUM1,SETCONV                                   S21021
         LABEL 'OBR RECORD CONVERTED TO THE STANDARD FORMAT'     S21021
         LINE  (34,1),(2,1),SKIP=1                               S21021
         ROUTINE XDEVT2,SETDEVTY                                 S21021
         LABEL 'DEVICE TYPE'                                     S21021
         LINE                                                    S21021
         HEX   XCUA,B0,3B                                        S21021
         LABEL 'PRIMARY   CHANNEL UNIT ADDRESS  '                S21021
         LINE                                                    S21021
         HEX   XCUA2,B0,3B                                       S21021
         ROUTINE XDUM1,TESTSHRT         IF SHORT REC, GO TO STATSS21021
         LABEL 'ALTERNATE CHANNEL UNIT ADDRESS  '                S21021
         LINE                                                    S21021
         ROUTINE XVOL1,SETVOL                                    S21021
         LABEL 'VOLUME LABEL'                                    S21021
         LINE                                                    S21021
         LABEL ' '                                               S21021
         LINE  (1,1),(23,38)                                     S21021
         LABEL 'CC   CA   FL     CT','K    CA   US CS  CT'       S21021
         LINE  (23,3),(5,51),SKIP=2                              S21021
         HEX   XFCCWCC,0,1B                                      S21021
         HEX   XFCCWDA,0,3B                                      S21021
         ROUTINE  XDUM1,SPACER1                                  S21021
         HEX   XFCCWFL,0,1B                                      S21021
         HEX   XZERO,0,1B                                        S21021
         HEX   XFCCWCT,0,2B                                      S21021
         ROUTINE  XDUM1,SPACER16                                 S21021
         HEX   XCSWK,0,1B                                        S21021
         HEX   XCSWCA,0,3B                                       S21021
         ROUTINE  XDUM1,SPACER1                                  S21021
         ROUTINE XCSWATTN,SETHEX                                 S21021
         ROUTINE XCSWPCI,SETHEX                                  S21021
         HEX   XCSWCT,0,2B                                       S21021
         LABEL 'FAILING CCW','CSW'                               S21021
         LINE  (1,1),(23,38),SKIP=1                              S21021
         ROUTINE  XDUM1,SKIPPER                                  S21021
         LABEL 'M  B B  C C  H H  R'                             S21021
         LINE  (23,3),(5,51),SKIP=2                              S21021
         HEX   XM,0,1B                                           S21021
         HEX   XB1,0,2B                                          S21021
         ROUTINE  XDUM1,BKSPC1                                   S21021
         HEX   XC1,0,2B                                          S21021
         ROUTINE  XDUM1,BKSPC1                                   S21021
         HEX   XH1,0,2B                                          S21021
         ROUTINE  XDUM1,BKSPC1                                   S21021
         HEX   XR,0,1B                                           S21021
         LABEL 'LAST SEEK ADDRESS'                               S21021
*
         LINE  ,(5,28)                                           S21021
         ROUTINE    XDUM1,SKIPPER
         LABEL 'UNIT STATUS','CHANNEL STATUS',                   S21021X
               'STATISTICAL DATA','STATISTICAL DATA'             S21021
*
         LINE  (20,28),(3,28),SKIP=1                             S21021
         BIN        XCSWATTN
         BIN   XCSWPCI                              DETAIL OF
         ROUTINE    SDR1,SDRCONVL                                S21021
         ROUTINE    SDR1,SDRCONVR                                S21021
         LABEL 'ATTENTION','PRGM-CTLD TRPT',                     S21021X
               'TEMPY READS  ','TEMPY WRITES '                   S21021
         LINE
         BIN        XCSWSM
         BIN        XCSWSILI
         ROUTINE    SDR2,SDRCONVL                                S21021
         ROUTINE    SDR2,SDRCONVR                                S21021
         LABEL 'STATUS MODIFIER','INCORRECT LENGTH',             S21021X
               'INTRVN REQD  ','BUS OUT CHK  '                   S21021
         LINE
         BIN        XCSWUE
         BIN        XCSWPC
         ROUTINE    SDR3,SDRCONVL                                S21021
         ROUTINE SDR3,SDRCONVR                                   S21021
         LABEL 'CONTROL UNIT END','PROGRAM CHECK',               S21021X
               'EQUIP CHK    ','OVERRUN      '                   S21021
         LINE
         BIN        XCSWBSY
         BIN        XCSWPCHK
         ROUTINE SDR4,SDRCONVL                                   S21021
         ROUTINE SDR4,SDRCONVR                                   S21021
         LABEL 'BUSY','PROTECTION CHECK',                        S21021X
               'TRACK COND   ','SEEK CHECK   '                   S21021
         LINE
         BIN        XCSWCE
         BIN        XCSWCDC
         ROUTINE SDR5,SDRCONVL                                   S21021
         ROUTINE SDR5,SDRCONVR                                   S21021
         LABEL 'CHANNEL END','CHAN DATA CHECK',                  S21021X
               'UNSAFE       ','NOT USED     '                   S21021
         LINE
         BIN        XCSWDE
         BIN        XCSWCCC
         ROUTINE SDR6,SDRCONVL                                   S21021
         ROUTINE SDR6,SDRCONVR                                   S21021
         LABEL 'DEVICE END','CHAN CTL CHECK',                    S21021X
               'SER/DESER    ','CHAN TAG LINE'                   S21021
         LINE
         BIN        XCSWUC
         BIN        XCSWICC
         ROUTINE SDR7,SDRCONVL                                   S21021
         ROUTINE SDR7,SDRCONVR                                   S21021
         LABEL 'UNIT CHECK','I/F CTL CHECK',                     S21021X
               'ALU          ','NOT USED     '                   S21021
         LINE       ,,SKIP=2
         BIN        XCSWUE
         BIN        XCSWCH
         ROUTINE SDR8,SDRCONVL                                   S21021
         ROUTINE SDR8,SDRCONVR                                   S21021
         LABEL 'UNIT EXCEPTION','CHAINING CHECK',                S21021X
               'MISG ADDR MKR','CHAN DATA CHK'                   S21021
*
         LINE  ,(1,1),SKIP=2                                     S21021
         ROUTINE  XDUM1,SKIPPER                                  S21021
         LABEL 'SENSE BYTE DATA'                                 S21021
         LINE  (11,19),(1,19),SKIP=2                             S21021
         ROUTINE  XB0CMDR,SETHEX                                 S21021
         ROUTINE  XB1DCHK,SETHEX                                 S21021
         ROUTINE  XB2UNSF,SETHEX                                 S21021
         ROUTINE  XB3RDY,SETHEX                                  S21021
         ROUTINE  XB4BIT0,SETHEX                                 S21021
         ROUTINE  XB5BIT0,SETHEX                                 S21021
         LABEL      'BYTE 0','BYTE 1','BYTE 2','BYTE 3','BYTE 4',      C
               'BYTE 5'
*                                            INDIVIDUAL LIST OF SENSE
         LINE       (11,19),(1,19),SKIP=1
         BIN        XB0CMDR
         BIN        XB1DCHK
         BIN        XB2UNSF
         BIN        XB3RDY
         BIN        XB4BIT0
         BIN   XB5BIT0
         ROUTINE    XDEVT2,SETLABEL
*              NOTE  LABELS MUST BE  9 CHAR LONG TO ALLOW SUBSTITUTION
*
         LABEL 'CMND REJ ','DATA CHK ','UNSAFE   ','READY    ',  S21021X
               'BIT 0    ','COMMAND  '
*
         LINE
         BIN        XB0INTRQ
         BIN        XB1TRK0
         BIN        XB2BIT1
         BIN        XB3ONLN
         BIN        XB4BIT1
         BIN        XB5BIT1
         LABEL 'INTV REQ ','TRK OVERF','BIT 1    ','ON LINE  ',        C
               'BIT 1    ','IN       '
*
         LINE
         BIN        XB0BUSC
         BIN        XB1CYLE
         BIN        XB2SERC
         BIN        XB3UNSF
         BIN        XB4BIT2
         BIN        XB5BIT2
         LABEL 'BUS OUT  ','CYL END  ','SERIAL CH','UNSAFE   ',        C
               'BIT 2    ','PROGRESS '
*
         LINE
         BIN        XB0ECHK
         BIN        XB1INVS
         BIN        XB2TAGL
         BIN        XB3BIT3
         BIN        XB4BIT3
         BIN        XB5BIT3
         LABEL 'EQUIP CHK','INV SEQ  ','TAG LINE ','BIT 3    ',        C
               'BIT 3    ','WHEN     '
*
         LINE
         BIN        XB0DCHK
         BIN        XB1RECU
         BIN        XB2ALUC
         BIN   XB3OLN
         BIN        XB4BIT4
         BIN        XB5BIT4
         LABEL 'DATA CHK ','REC UNFNO','ALU CHK  ','ON LINE  ',        C
               'BIT 4    ','OVERFLOW '
*
         LINE
         BIN        XB0OVRN
         BIN        XB1FILP
         BIN        XB2UNSEL
         BIN        XB3CYLE
         BIN        XB4BIT5
         BIN        XB5BIT5
         LABEL 'OVERRUN  ','FILE PROT','UNSEL STA','CYL END  ',        C
               'BIT 5    ','INCMPLETE'
*
         LINE
         BIN        XB0TRKC
         BIN        XB1MAM
         BIN        XB2BIT6
         BIN        XB3BIT6
         BIN        XB4BIT6
         BIN        XB5BIT6
         LABEL 'TRK COND ','MISG A MK','BIT 6    ','BIT 6    ',        C
               'BIT 6    ','OCCURS   '
*
         LINE  ,,SKIP=2                                          S21021
         BIN        XB0SKCK
         BIN        XB1OVFL
         BIN   XB2BIT7
         BIN        XB3SEEKI
         BIN        XB4BIT7
         BIN        XB5BIT7
         ROUTINE    XDUM1,RESETR2                                S21021
         LABEL 'SEEK CHK ','OVFL INC ','BIT 7    ','SEEK INCP',        C
               'BIT 7    '
*
         LINE  ,,SKIP=1                                          S21021
         LABEL ''                                                S21021
*
*
         DC    X'FF'
         DC    XL2'0068'
         DC    XL2'0018'
         DC    X'0018'                                           S21021
SHRTOBRL LABEL      ''                                           S21021
SHRTOBRS LINE  ,(1,1),SKIP=2                                     S21021
         ROUTINE    XDUM1,SKIPPER                                S21021
         LABEL      'STATISTICAL DATA'                           S21021
*                                                                S21021
         LINE       (21,30),(1,30),SKIP=1                        S21021
         ROUTINE    SDR1,SDRCONVL                                S21021
         ROUTINE    SDR1,SDRCONVR                                S21021
         LABEL 'TEMPY READS  ','TEMPY WRITES '                   S21021
*                                                                S21021
         LINE                                                    S21021
         ROUTINE    SDR2,SDRCONVL                                S21021
         ROUTINE    SDR2,SDRCONVR                                S21021
         LABEL 'INTRVN REQD  ','BUS OUT CHK  '                   S21021
         LINE                                                    S21021
         ROUTINE    SDR3,SDRCONVL                                S21021
         ROUTINE SDR3,SDRCONVR                                   S21021
         LABEL 'EQUIP CHK    ','OVERRUN      '                   S21021
         LINE                                                    S21021
*                                                                S21021
         ROUTINE SDR4,SDRCONVL                                   S21021
         ROUTINE SDR4,SDRCONVR                                   S21021
         LABEL 'TRACK COND   ','SEEK CHECK   '                   S21021
         LINE                                                    S21021
         ROUTINE SDR5,SDRCONVL                                   S21021
         ROUTINE SDR5,SDRCONVR                                   S21021
         LABEL 'UNSAFE       ','NOT USED     '                   S21021
         LINE                                                    S21021
         ROUTINE SDR6,SDRCONVL                                   S21021
         ROUTINE SDR6,SDRCONVR                                   S21021
         LABEL 'SER/DESER    ','CHAN TAG LINE'                   S21021
         LINE                                                    S21021
         ROUTINE SDR7,SDRCONVL                                   S21021
         ROUTINE SDR7,SDRCONVR                                   S21021
         LABEL 'ALU          ','NOT USED     '                   S21021
         LINE                                                    S21021
         ROUTINE SDR8,SDRCONVL                                   S21021
         ROUTINE SDR8,SDRCONVR                                   S21021
         LABEL 'MISG ADDR MKR','CHAN DATA CHK'                   S21021
         LINE                                                    S21021
         ROUTINE    XDUM1,RESETR2                                S21021
         LABEL      ''                                           S21021
*                                                                S21021
         DC         X'FF'                                        S21021
         DC         X'0028'                                      S21021
         DC         X'0018'                                      S21021
         DC    X'0018'                                           S21021
LABELS   DS    0F
         DC    C'$'
         LSTART
         END
