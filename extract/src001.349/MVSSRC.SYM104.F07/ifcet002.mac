         TITLE 'IFCET002 - EDIT TABLE - LINKNAME=IFCET002, ALIASES=IFCEC
               T012'
*  DSGEN MACRO     DSGEN MACRO     DSGEN MACRO     DSGEN MACRO        *
         SPACE 2
         MACRO
&NAME    DSGEN &X1,&X2,&X3,&X4,&X5,&X6,&X7,&X8,&X9,&X10,&X11,&X12,&X13,X
               &X14,&X15,&X16,&X17,&X18,&X19,&X20,&X21,&X22,&X23,&X24, X
               &X25,&X26,&X27,&X28,&X29,&X30,&X31,&X32
.*                            THIS MACRO GENERATES ONE DSECT WORD
.*                                 FORMATTED FOR THE SUBJECT LOG RECORD
.*                                 EACH OPERAND HAS TWO SUB LISTS. THE
.*                                 FIRST IS THE NAME OF THE LOG ITEM
.*                                 WHILE THE SECOND IS THE # OF BITS TO
.*                                 ALLOCATE FOR THIS ITEM. ITEMS MUST
.*                                 BE DEFINED CONTIGUOUSLY WITHOUT GAPS
.*                                 UNUSED BIT GROUPS MUST BE GIVEN
.*                                 DUMMY NAMES. THE NAMES PROVIDED FOR
.*                                 THE LOG ITEMS MAY THEN BE USED IN
.*                                 THE CONVERSION MACRO INSTRUCTIONS.
         GBLC &ITEM(1000)
         GBLA &ITEMNBR,&BITS(1000),&SHIFT(1000)
         LCLC  &SYMBOL
         LCLA  &OP,&COUNT,&BYTE
.LOOP    ANOP
&OP      SETA  &OP+1
&ITEMNBR SETA  &ITEMNBR+1
&SYMBOL  SETC  '&SYSLIST(&OP,1)'
         AIF   ('&SYSLIST(&OP,2)' NE '').OK1
         MNOTE *,'NULL BIT COUNT FIELD'
         MEXIT
.OK1     ANOP
         AIF   ('&SYMBOL' NE '').OK
         MNOTE 'LOG ITEM SYMBOL NOT PROVIDED'
         AGO   .CNT
.OK      ANOP
         AIF   ('&SYMBOL'(1,1) EQ '''').CNT                *TEMP*
&SYMBOL  EQU   *+&BYTE
&ITEM(&ITEMNBR)  SETC '&SYSLIST(&OP,1)'
&BITS(&ITEMNBR) SETA &SYSLIST(&OP,2)
&SHIFT(&ITEMNBR) SETA &COUNT-&BYTE*8
.CNT     ANOP
&COUNT   SETA  &COUNT+&SYSLIST(&OP,2)
&BYTE    SETA  &COUNT/8
         AIF (&OP LT N'&SYSLIST).LOOP
         AIF   (&COUNT NE 32).NOT32
&NAME    DS    4C
         MEXIT
.NOT32   AIF   (&COUNT NE 64).ERR
&NAME    DS    8C
         MEXIT
.ERR     MNOTE '**BIT COUNT FIELDS DO NOT TOTAL 32 OR 64**'
         MEND
         EJECT
*  LINE MACRO     LINE MACRO     LINE MACRO     LINE MACRO            *
         SPACE 2
         MACRO
&NAME    LINE  &DATA,&LABEL,&SKIP=
.*                            THIS MACRO ESTABLISHES THE MARGINS OR
.*                                 COLUMN SETTINGS FOR EACH LINE. IT
.*                                 GENERATES NECESSARY CARRIAGE CONTROL
.*                                 ENTRIES. LASTLY IT DETERMINES THE
.*                                 NUMBER OF ENTRIES ASSOCIATED WITH
.*                                 THE PREVIOUS LINE AND STORES THAT #
.*                                 INTO THE APPROPRIATE BYTE.
.*                            &DATA(1) - CHARACTER POSITION TO BEGIN
.*                                 DATA.
.*                            &DATA(2) - # OF SPACES BETWEEN SUCCESSIVE
.*                                 DATA COLUMN STARTING CHARACTERS.
.*                            &LABEL(1) - CHAR. POS. TO BEGIN LABELS.
.*                            &LABEL(2) - # SPACES BETWEEN LINE'S LABEL
.*                                 STARTING CHARACTERS.
.*                            &SKIP= - # OF LINES TO ADVANCE CARRIAGE
.*                                 TO BEGIN NEXT PRINTED LINE.
.*                            NEW ENTRIES ARE GENERATED ONLY WHEN
.*                                 NECESSARY. OMISSION OF ITEMS MEANS
.*                                 THEY HAVE NOT CHANGED SINCE PREVIOUS
.*                                 SETTINGS. A LINE MACRO IS REQUIRED
.*                                 FOR EACH PRINTED LINE. IF NO SETTING
.*                                 CHANGES ARE REQUIRED ALL OPERANDS
.*                                 ARE OMITTED IN WHICH CASE ONLY THE
.*                                 # OF ENTRIES FOR THE PREVIOUS LINE
.*                                 IS COMPUTED. THIS MACRO MUST BE THE
.*                                 FIRST MACRO INSTRUCTION ISSUED FOR
.*                                 THE SUBJECT LINE
         GBLA  &DB,&DT,&LB,&LT,&NRENTS
         GBLA  &LASTCC,&INITIAL
         GBLC  &NRENTSX
         GBLC  &CARRCON
         GBLB  &TEST
         LCLA  &S
&NAME    DS    0C
         AIF   (&INITIAL EQ 0).FIRSTLN
         LINEND
         AGO   .NTFIRST
.FIRSTLN ANOP
&NRENTSX SETC  'N&SYSNDX'
&INITIAL SETA  1
N&SYSNDX EQU   *
.NTFIRST ANOP
         DC    AL1(0)
         AIF   ('&DATA(1)' EQ '').CKDTABS
         AIF   (&DATA(1) EQ &DB).CKDTABS
&DB      SETA  &DATA(1)
         AGO   .AA
.CKDTABS AIF   ('&DATA(2)' EQ '').NCDATA
         AIF   (&DATA(2) EQ &DT).NCDATA
         AGO   .CC
.AA      AIF   ('&DATA(2)' EQ '').BB
         AIF   (&DATA(2) EQ &DT).BB
.CC      ANOP
&DT      SETA  &DATA(2)
.BB      DC    XL2'FFFE'
         DC    FL1'&DB'
         DC    FL1'&DT'
&NRENTS  SETA  &NRENTS+1
.NCDATA  ANOP
         AIF   ('&LABEL(1)' EQ '').CKLTABS
         AIF   (&LABEL(1) EQ &LB).CKLTABS
&LB      SETA  &LABEL(1)
         AGO   .AAA
.CKLTABS AIF   ('&LABEL(2)' EQ '').NCLABEL
         AIF   (&LABEL(2) EQ &LT).NCLABEL
         AGO   .CCC
.AAA     AIF   ('&LABEL(2)' EQ '').BBB
         AIF   (&LABEL(2) EQ &LT).BBB
.CCC     ANOP
&LT      SETA  &LABEL(2)
.BBB     DC    XL2'FFFF'
         DC    FL1'&LB'
         DC    FL1'&LT'
&NRENTS  SETA  &NRENTS+1
.NCLABEL ANOP
         AIF   ('&SKIP' NE '').CONT
         AIF   (&TEST EQ 0).NOCHANG
&S       SETA  &LASTCC
         AGO   .DEFAULT
.CONT    ANOP
         AIF   (&TEST EQ 0).AB
&S       SETA  &SKIP
.DEFAULT ANOP
         ORG   &CARRCON+3
         DC    AL.3(0),AL.2(&S),AL.3(1)
         ORG
&LASTCC  SETA  &S
&TEST    SETB  0
         AGO   .DD
.AB      AIF   (&SKIP EQ &LASTCC).NOCHANG
A&SYSNDX DC    XL2'FFFB'
         DC    AL.3(0),AL.2(&SKIP),AL.3(1)
         DC    FL1'9'
&LASTCC  SETA  &SKIP
&TEST    SETB  1
&CARRCON SETC  'A&SYSNDX'
&NRENTS  SETA  &NRENTS+1
.DD      ANOP
.NOCHANG ANOP
         MEND
         EJECT
*  BIN MACRO     BIN MACRO     BIN MACRO     BIN MACRO     BIN MACRO  *
         SPACE 2
         MACRO
         BIN   &LOGITEM,&SKIP$OR
.*                                 &LOGITEM - NAME OF ITEM IN LOG
.*                                      RECORD DSECT
.*                                 &SKIP$OR - NBR OF SPACES TO SKIP ON
.*                                 MULTIPLE PRINT OR NBR OF SPACES
.*                                      TO BACK UP FROM COLUMN FOR
.*                                      START OF PRINTING. THE LETTER
.*                                      B PRECEEDS THE NUMBER (B5) IF
.*                                      THE LATTER IS DESIRED. OMISSION
.*                                      OF THIS OPERAND FORCES OPTION 2
.*                                      WITH BACKUP SPACING EQUAL TO
.*                                 THE NUMBER OF BITS IN THE ITEM
.*                                 DEFINITION (VIA MACRO 'DSGEN').
.*                            A LIMIT OF 15 BITS IS IMPOSED FOR THE
.*                                 TOTAL NUMBER OF BITS TO PRINT.
         GBLC &ITEM(1000)
         GBLA &ITEMNBR,&BITS(1000),&SHIFT(1000),&NRENTS
         GBLC  &DSECT
         LCLB  &B
         LCLA  &IBITS,&NDX,&ISHIFT,&K
         LCLA  &TEMP,&T
         LCLC  &S
.LOOP    ANOP
&NDX     SETA  &NDX+1
         AIF   ('&ITEM(&NDX)' EQ '&LOGITEM').FOUND
         AIF   (&NDX LT &ITEMNBR).LOOP
         MNOTE *,'YOU BLEW IT'
         ROUTINE DUM,SKIPPER
         MEXIT
.FOUND   ANOP
&ISHIFT  SETA  &SHIFT(&NDX)
&IBITS   SETA  &BITS(&NDX)
&T       SETA  &IBITS-1
&B       SETB  0
         AIF   ('&SKIP$OR' EQ '').BACKUP
&K       SETA  K'&SKIP$OR
&B       SETB  ('&SKIP$OR'(1,1) EQ 'B')
&S       SETC  '&SKIP$OR'(&B+1,&K-&B)
&T       SETA  &S
.BACKUP  ANOP
&NRENTS  SETA  &NRENTS+1
&TEMP    SETA  &ISHIFT
A&SYSNDX DC    AL.12(&LOGITEM-&DSECT),XL.4'&TEMP'
         AIF (&IBITS EQ 1 AND &B EQ 0).OUT
         ORG   A&SYSNDX
&B       SETB  1
         DC    AL.12(&LOGITEM-&DSECT),XL.4'C'
         DC    BL.1'&B',AL.7(&T),AL.4(&IBITS),FL.4'&ISHIFT'
.OUT     ANOP
         MEND
         EJECT
*  HEX MACRO     HEX MACRO     HEX MACRO    HEXN MACRO     HEX MACRO  *
         SPACE 2
         MACRO
         HEX   &LOGITEM,&SKIP$OR,&BYTES
.*                            THIS MACRO FACILITATES GENERATION OF THE
.*                                 TABLE ENTRIES SPECIFYING HEX TYPE
.*                                 CONVERSIONS.
.*                            &LOGITEM - NAME OF DATA ITEM TO BE
.*                                 CONVERTED
.*                            &SKIP$OR - NUMBER OF SPACES TO SKIP ON
.*                                 MULTIPLE PRINT OR NBR OF SPACES TO
.*                                 BACK UP FROM COLUMN FOR START OF
.*                                 PRINTING. THE LETTER B PRECEEDS THE
.*                                 NUMBER (B8) IF THE LATTER OPTION IS
.*                                 DESIRED. OMISSION OF THIS OPERAND
.*                                 FORCES OPTION 2 WITH BACKUP SPACING
.*                                 EQUAL TO THE NUMBER OF BYTES SPECI-
.*                                 FIED BY THE NEXT OPERAND (TWICE THIS
.*                                 NUMBER IF SUFFIX IS EXPLICITLY OR
.*                                 IMPLICITLY B).
.*                            &BYTES - NUMBER OF TOTAL INPUT BYTES TO
.*                                 PROCESS. THIS NUMBER IS SUFFIXED
.*                                 WITH AN L,R, OR B TO STIPULATE
.*                                 PRINTING OF THE LEFT HALF, RIGHT
.*                                 HALF, OR BOTH HALVES OF THE BYTE
.*                                 RESPECTIVELY. OMISSION OF THIS
.*                                 OPERAND DEFAULTS TO B WITH THE NUM-
.*                                 BER OF BYTES EQUAL TO THE ITEM LNGTH
.*                            A NEW ENTRY IS GENERATED ONLY WHEN NECES-
.*                                 SARY. THIS MACRO MUST BE THE FIRST
.*                                 MACRO INSTRUCTION ISSUED FOR THE
.*                                 SUBJECT LINE(S).
         GBLC &ITEM(1000),&DSECT
         GBLA &ITEMNBR,&BITS(1000),&SHIFT(1000),&NRENTS
         LCLB  &B
         LCLA  &IBITS,&NDX,&ISHIFT,&K,&T,&CODE,&BYTESP
         LCLC  &S,&LETTER
.LOOP    ANOP
&NDX     SETA  &NDX+1
         AIF   ('&ITEM(&NDX)' EQ '&LOGITEM').FOUND
         AIF   (&NDX LT &ITEMNBR).LOOP
         MNOTE *,'LOG ITEM NOT DEFINED'
         MEXIT
.FOUND   ANOP
&ISHIFT  SETA  &SHIFT(&NDX)
&IBITS   SETA  &BITS(&NDX)
&CODE    SETA  1
&K       SETA  K'&BYTES
         AIF   (&IBITS-(&IBITS/4)*4 EQ 0).OK
         MNOTE *,'BIT LENGTH SPECIFICATION INCONSISTENT WITH CONTROL'
         MEXIT
.OK      AIF   ('&BYTES' EQ '').USEDEFN
&LETTER  SETC  '&BYTES'(&K,1)
&S       SETC  '&BYTES'(1,&K-1)
&BYTESP  SETA  &S
         AIF   ('&LETTER' EQ 'B').BOTH
         AIF   (&ISHIFT EQ 0 OR &ISHIFT EQ 4).OK2
.ERR     MNOTE *,'DATA NOT ALLIGNED ON HEX OR BYTE BOUNDARY'
         MEXIT
.USEDEFN ANOP
&BYTESP  SETA  &IBITS/8
.BOTH    AIF   (&ISHIFT NE 0).ERR
&CODE    SETA  3
&T       SETA  &BYTESP*2-1
&S       SETC  '&T'
.OK2     AIF   (&ISHIFT EQ 0).L
&CODE    SETA  2
.L       ANOP
&B       SETB  1
         AIF   ('&SKIP$OR' EQ '').BACKUP
&K       SETA  K'&SKIP$OR
&B       SETB  ('&SKIP$OR'(1,1) EQ 'B')
&S       SETC  '&SKIP$OR'(&B+1,&K-&B)
.BACKUP  ANOP
&NRENTS  SETA  &NRENTS+1
         DC    AL.12(&LOGITEM-&DSECT),XL.4'A'
         DC    BL.1'&B',FL.7'&S',FL.4'&BYTESP',FL.4'&CODE'
.OUT     ANOP
         MEND
         SPACE 2
*  ROUTINE MACRO     ROUTINE MACRO     ROUTINE MACRO     ROUTINE MACRO*
         SPACE 2
         MACRO
&NAME    ROUTINE &LOGITEM,&RNAME
         GBLC  &DSECT,&CSECT
         GBLC  &ITEM(1000)
         GBLA  &SHIFT(1000),&ITEMNBR
         GBLA  &NRENTS
         LCLA  &ISHIFT,&NDX
.LOOP    ANOP
&NDX     SETA  &NDX+1
         AIF   ('&ITEM(&NDX)' EQ '&LOGITEM').FOUND
         AIF   (&NDX LT &ITEMNBR).LOOP
         MNOTE *,'LOG ITEM NOT DEFINED'
         MEXIT
.FOUND   ANOP
&ISHIFT  SETA  &SHIFT(&NDX)
&NAME    DC    AL.12(&LOGITEM-&DSECT),XL.4'D'
         DC    AL.4(&ISHIFT),AL.12(&RNAME-&CSECT)
&NRENTS  SETA  &NRENTS+1
         MEND
         EJECT
*  LABEL MACRO     LABEL MACRO     LABEL MACRO     LABEL MACRO        *
         SPACE 2
         MACRO
&NAME    LABEL &LABEL1,&LABEL2,&LABEL3,&LABEL4,&LABEL5,&LABEL6,        C
               &LABEL7,&LABEL8
.*                            THIS MACRO ALLOWS UP TO ACHT LABELS TO
.*                                 BE SPECIFIED FOR A GIVEN LINE. THE
.*                                 LABELS SHOULD BE ENCLOSED IN QUOTA-
.*                                 TION MARKS. THE COLUMN AND LINE END
.*                                 CONTROLS WILL BE INSERTED BY THE
.*                                 MACRO. OMITTED OPERANDS MEAN NO
.*                                 LABEL REQUIRED FOR RESPECTIVE
.*                                 COLUMN. THIS MACRO WILL ALSO ALERT
.*                                 THE PROGRAMMER TO EXACTLY WHERE THE
.*                                 TABLE MUST BE BROKEN IF NECESSARY
.*                                 TO AVOID EXCEEDING THE MAXIMUM LIMIT
         GBLC &LABELS(2000)
         GBLC  &LBL(100)
         GBLA  &LBLCTR(100)
         GBLA  &LABELNR
         GBLA  &NRENTS,&LPTR
         LCLC  &MARGIN,&TACK
         LCLC  &TEXT
         LCLA  &OP,&K,&START,&NRCHAR,&REMAIN
&MARGIN  SETC  '*'
         AIF   ('&NAME' EQ '').LOOPOUT
&LPTR    SETA  &LPTR+1
&LBL(&LPTR) SETC '&NAME'
&LBLCTR(&LPTR) SETA &LABELNR+1
.LOOPOUT ANOP
&OP      SETA  &OP+1
&K       SETA  K'&SYSLIST(&OP)
         AIF (&K GE 2).NORM
&K       SETA  2
.NORM    ANOP
&START   SETA  2
         AIF (&OP LT N'&SYSLIST).NOTLAST
&MARGIN  SETC  '$'
.NOTLAST ANOP
&NRCHAR  SETA  8
&TACK    SETC  ''
.LOOPIN  ANOP
&REMAIN  SETA  &K-&START
         AIF (&REMAIN GE 8).MORE
&TACK    SETC  '&MARGIN'
&NRCHAR  SETA  &REMAIN
.MORE    ANOP
&TEXT    SETC  ''
         AIF (&REMAIN EQ 0).ENDTEXT
&TEXT    SETC  '&SYSLIST(&OP)'(&START,&NRCHAR)
.ENDTEXT ANOP
&LABELNR SETA  &LABELNR+1
&LABELS(&LABELNR) SETC '&TEXT'.'&TACK'
&START   SETA  &START+8
         AIF ('&TACK' EQ '').LOOPIN
         AIF ('&TACK' EQ '*').LOOPOUT
         MEND
         SPACE 2
         MACRO
         LSTART
.*                            THIS MACRO IS ISSUED WHEN THE LABEL AREA
.*                                 OF THE TABLE IS TO BEGIN.
         GBLC &LABELS(2000)
         GBLA  &LABELNR
         GBLC  &LBL(100)
         GBLA  &LBLCTR(100)
         LCLA  &NDX,&CTR
         LINEND
&CTR     SETA  1
.LOOP    ANOP
&NDX     SETA  &NDX+1
         AIF   (&LBLCTR(&CTR) NE &NDX).NOLBL
&LBL(&CTR) DC  C'&LABELS(&NDX)'
&CTR     SETA  &CTR+1
         AGO   .LOOPCTL
.NOLBL   ANOP
         DC    C'&LABELS(&NDX)'
.LOOPCTL AIF   (&NDX LT &LABELNR).LOOP
         MEND
         SPACE 2
         MACRO
         LINEND
.*                            THIS MACRO IS USED ONLY BY THE LINE
.*                                 MACRO TO SET THE NUMBER OF ENTRIES
.*                                 ASSOCIATED WITH EACH LINE.
         GBLA  &NRENTS
         GBLC &NRENTSX
         ORG   &NRENTSX
         DC    FL1'&NRENTS'
         ORG
&NRENTS  SETA  0
B&SYSNDX EQU   *
&NRENTSX SETC  'B&SYSNDX'
         MEND
         GBLC  &DSECT,&CSECT
&CSECT   SETC  'IFCET002'
&DSECT   SETC  'M2305'
***********************************************************************
*                                                                     *
*              THIS ENTIRE MODULE IS IN SUPPORT OF  S20201            *
*                                                                     *
***********************************************************************
IFCET002 CSECT
*
M2305    DSECT
         DSGEN (CLASRC,8),(SYSREL,8),(SWITCHES,16)
         DSGEN (SPARE1,32)
         DSGEN (DAY,32)
         DSGEN (HOUR,32)
         DSGEN (SPARE2,8),(CPUSERL,24)
         DSGEN (CPUID,16),(SPARE3,16)
DUM2     DS    H
         DSGEN (SNB00,8),(SNB01,8),(SNB02,8),(SNB03,8),                X
               (SNB04,8),(SNB05,8),(SNB06,8),(SNB07,8)
         DSGEN (SNB10,8),(SNB11,8),(SNB12,8),(SNB13,8),                X
               (SNB14,8),(SNB15,8),(SNB16,8),(SNB17,8)
         DSGEN (SNB20,8),(SNB21,8),(SNB22,8),(SNB23,8),                X
               (SNB24,8),(SNB25,8),(SNB26,8),(SNB27,8)
         DSGEN (SNB30,8),(SNB31,8),(SNB32,8),(SNB33,8),                X
               (SNB34,8),(SNB35,8),(SNB36,8),(SNB37,8)
         DSGEN (SNB40,8),(SNB41,8),(SNB42,8),(SNB43,8),                X
               (SNB44,8),(SNB45,8),(SNB46,8),(SNB47,8)
         DSGEN (SNB50,8),(SNB51,8),(SNB52,8),(SNB53,8),                X
               (SNB54,8),(SNB55,8),(SNB56,8),(SNB57,8)
         DSGEN (SNB60,8),(SNB61,8),(SNB62,8),(SNB63,8),                X
               (SNB64,8),(SNB65,8),(SNB66,8),(SNB67,8)
         DSGEN (SNB70,8),(SNB71,8),(SNB72,8),(SNB73,8),                X
               (SNB74,8),(SNB75,8),(SNB76,8),(SNB77,8)
         DSGEN (SNB80,8),(SNB81,8),(SNB82,8),(SNB83,8),                X
               (SNB84,8),(SNB85,8),(SNB86,8),(SNB87,8)
         DSGEN (SNB90,8),(SNB91,8),(SNB92,8),(SNB93,8),                X
               (SNB94,8),(SNB95,8),(SNB96,8),(SNB97,8)
         DSGEN (SNB100,8),(SNB101,8),(SNB102,8),(SNB103,8),            X
               (SNB104,8),(SNB105,8),(SNB106,8),(SNB107,8)
         DSGEN (SNB110,8),(SNB111,8),(SNB112,8),(SNB113,8),            X
               (SNB114,8),(SNB115,8),(SNB116,8),(SNB117,8)
         DSGEN (SNB120,8),(SNB121,8),(SNB122,8),(SNB123,8),            X
               (SNB124,8),(SNB125,8),(SNB126,8),(SNB127,8)
         DSGEN (SNB130,8),(SNB131,8),(SNB132,8),(SNB133,8),            X
               (SNB134,8),(SNB135,8),(SNB136,8),(SNB137,8)
         DSGEN (SNB140,8),(SNB141,8),(SNB142,8),(SNB143,8),            X
               (SNB144,8),(SNB145,8),(SNB146,8),(SNB147,8)
         DSGEN (SNB150,8),(SNB151,8),(SNB152,8),(SNB153,8),            X
               (SNB154,8),(SNB155,8),(SNB156,8),(SNB157,8)
         DSGEN (SNB160,8),(SNB161,8),(SNB162,8),(SNB163,8),            X
               (SNB164,8),(SNB165,8),(SNB166,8),(SNB167,8)
IFCET002 CSECT
         USING *,R9
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
TEST     EQU   4
H10      EQU   X'10'
H20      EQU   X'20'
H30      EQU   X'30'
H60      EQU   X'60'
HC0      EQU   X'C0'
HE0      EQU   X'E0'
H80      EQU   X'80'
L0       EQU   0
L1       EQU   1
L2       EQU   2
L3       EQU   3
L4       EQU   4
L5       EQU   5
L6       EQU   6
L7       EQU   7
L8       EQU   8
L9       EQU   9
L10      EQU   10
L11      EQU    11
L12      EQU   12
L22      EQU   22
L24      EQU    24
L44      EQU   44
         DC    A(ENTRIES)
         DC    CL8'IFCET002'
XR9PTR   DC    F'0'
XLABELP  DC    F'0'
OBRPTR   DC    X'FE'
         DC    C'IFCE2006'                                     @XA17529
NONE     DC    C'NONE  '
UNIVERS  DC    C'UNIVERSAL  '
CHARS    DC    C'0123456789ABCDEF'
ONE      DC    C'1'
TWO      DC    C'2'
L99      DC    F'99'
L109     DC    F'109'
BLANKS   DC    C'    '
XSAVE    DS    5F
READYP   DS    D
*
*        RESTORE REGISTERS USED BY ROUTINES IN THIS MODULE
*
RETURN   LM    R5,R8,XSAVE              RESTORE REGS
         BR    R14                      RETURN
*
TESTMOD  STM   R5,R8,XSAVE
         TM    L0(R4),H30
         BO    ITISOBR
         B     RETURN
ITISOBR  LA    R5,OBRPTR-L4
         ST    R5,XR9PTR
         B     RETURN
*        SET DATE READY FOR PRINT
*
SETDATE  UNPK  READYP(L8),L1(L3,R4)     UNPACK THE DATE
         MVC   L0(L3,R3),READYP+L5     MOVE DAY
         MVC   L6(L2,R3),READYP+L3      MOVE YEAR
         BR    R14
*
*        CHECK FOR CPU AND SERIAL NUMBER
*
SETCPUNO STM   R5,R8,XSAVE             SAVE REGS
         SR    R5,R5                   CLEAR R5
         IC    R5,L0(R4)               PICK UP MODEL NO
         SLL   R5,L8                   IN TO
         IC    R5,L1(R4)               R5
         LTR   R5,R5                   TEST FOR ZERO
         BNZ   RETURN                  NONE ZERO BR
         LR    R5,R3                   PUT LINE ADDRESS IN R5
         LA    R6,L44
         SR    R5,R6
         MVC   L0(L9,R5),UNIVERS       PUT IN UNIVERSAL
         MVC   L22(L6,R5),NONE         PUT IN NONE
         B     RETURN
*
*        SET PROGRAM NAME TO PRINT LINE
*
SETPGMN  MVC   L0(L8,R3),L0(R4)        MOVE 8 CHARS TO LINE
         BR    R14                     RETURN
BITE     SR    R15,R15
         BR    R14
SETCUA   STM   R5,R8,XSAVE
         LA    R5,L2
         LA    R8,CHARS                CHARACTER TABLE POINTER
         LA    R4,L2(R4)
         B     JUMP
AGAIN    LA    R3,L2(R3)
         LA    R4,L1(R4)
JUMP     IC    R7,L0(R4)               PICK UP CHARACTER
         SLL   R7,L24                  MOVE TO HI R7
         SR    R6,R6                   CLEAR REGS
         SLDL  R6,L4                   MOVE FOUR BITS
         AR    R6,R8                   ADD OFFSET AND POINTER
         MVC   L0(L1,R3),L0(R6)        MOVE CHARACTER TO LINE
         SR    R6,R6                   CLEAR AGAIN
         SLDL  R6,L4                   MOVE LAST 4 BITS
         AR    R6,R8                   COMPUTE OFFSET
         MVC   L1(L1,R3),L0(R6)        MOVE LAST CHARACTER
         BCT   R5,AGAIN
         B     RETURN
MODCHK   TM    L0(TEST),H10
         BO    MOD1
         MVC   L0(L1,R3),TWO
         BR    R14
MOD1     MVC   L0(L1,R3),ONE
         BR    R14
CHECK1   STM   R5,R8,XSAVE              STORE REGS
         TM  L0(TEST),HE0               TEST FIRST THREE BITS
         BO    UDC1                     BRANCH IF  111
         TM  L0(TEST),HC0               TEST FIRST THREE BITS
         BO    CDC1                     BRANCH IF  110
         TM  L0(TEST),H60               TEST FIRST THREE BITS
         BO    MFC1                     BRANCH IF  011
         TM  L0(TEST),H80               TEST FIRST THREE BITS
         BO    UOC1                     BRANCH IF  100
         TM  L0(TEST),H20               TEST FIRST THREE BITS
         BO    CUSR1                    BRANCH IF  001
         B     DUMMY                                           @08969FR
CHECK2   STM   R5,R8,XSAVE              STORE REGS
         TM  L0(TEST),HE0               TEST FIRST THREE BITS
         BO    UDC2                     BRANCH IF 111
         TM  L0(TEST),HC0               TEST FIRST THREE BITS
         BO    CDC2                     BRANCH IF 110
         TM  L0(TEST),H60               TEST FIRST THREE BITS
         BO    MFC2                     BRANCH IF 011
         TM  L0(TEST),H80               TEST FIRST THREE BITS
         BO    UOC2                     BRANCH IF 100
         TM  L0(TEST),H20               TEST FIRST THREE BITS
         BO    CUSR2                    BRANCH IF 001
DUMMY    LA    R5,DUMLAB                                       @08969FR
         ST    R5,XLABELP                                      @08969FR
         SR    R15,R15                  PRINT NOTHING IF IT GETS HERE
*      FALL THRU TO MAINTAIN GOOD LABEL ADDR         @OY04577  @OX04774
CUSR1    LA    R5,CULABE1               GET ADDR OF CON/UNIT LABELS
         ST    R5,XLABELP               PUT IT IN POINTER TO LABELS
         B     RETURN                   RETURN
MFC1     LA    R5,MFCLABE1              GET ADDR OF MFP FILE LABELS
         ST    R5,XLABELP               PUT IT IN POINTER TO LABELS
         B     RETURN                   RETURN
CDC1     LA    R5,CDCLABE1              GET ADDR OF COR/DATA LABELS
         ST    R5,XLABELP               PUT IT IN POINTER TO LABELS
         B     RETURN                   RETURN
UDC1     LA    R5,UDCLABE1              GET ADDR OF UCOR/DATA LABELS
         ST    R5,XLABELP               PUT IT IN POINTER TO LABELS
         B     RETURN                   RETURN
UOC1     LA    R5,UOCLABE1              GET ADDR OF US/ORUN LABELS
         ST    R5,XLABELP               PUT IT IN POINTER TO LABELS
         B     RETURN                   RETURN
CUSR2    LA    R5,CULABE2
         ST    R5,XLABELP
         B     RETURN
MFC2     LA    R5,MFCLABE2
         ST    R5,XLABELP
         S     R3,L99
         MVC   L0(L4,R3),L1(R3)
         MVC   L4(L1,R3),BLANKS
         B     RETURN
CDC2     LA    R5,CDCLABE2
         ST    R5,XLABELP
         B     RETURN
UDC2     LA    R5,UDCLABE2
         ST    R5,XLABELP
         B     RETURN
UOC2     LA    R5,UOCLABE2
         ST    R5,XLABELP
         S     R3,L109
         MVC   L0(L1,R3),L2(R3)
         MVC   L5(L1,R3),L4(R3)
         MVC   L1(L4,R3),BLANKS
         B     RETURN
CONVREL  STM   R5,R8,XSAVE              SAVE WORK REGS
         SR    R6,R6                    CLEAR R6
         IC    R6,L0(R4)                PICK UP REL & SYS VALUES
         SRDL  R6,L5                    SAVE REL NO IN R7
        SLL   R6,L2                    MULTIPLY SYS VALUE BY 4
         L     R8,XLABELP               PICK UP LABEL POINTER
         LA    R6,RELTAB(R6)            INDEX INTO TABLE OF SYSTEMS
         MVC   L0(L4,R8),L0(R6)         MOVE SYSTEM NAME INTO LABEL
         SR    R6,R6                    CLEAR R6
         SLDL  R6,L5                    RETREIVE REL NO.
         LTR   R6,R6                    IS IT ZERO
         BZ    NOREL                    BR-YES
         CVD   R6,WORK                  CONVERT IT
         UNPK  L0(L2,R3),WORK+L6(L2)    UNPACK IT INTO PRINT BUFF
         MVZ   L1(L1,R3),L0(R3)         MOVE ON PROPER ZONE
         B     RETURN                   EXIT
NOREL    MVC   L0(L4,R3),NONE           MOVE IN NONE FOR REL
         B     RETURN
RELTAB   DS    0C                                              @L03976
         DC    C'OS  '                                         @L03976
         DC    C'DOS '                                         @L03976
         DC    C'VS 1'                                         @L03976
         DC    C'V370'                                         @L03976
         DC    C'VS 2'                                         @L03976
         DC    C'    '                                         @L03976
         DC    C'    '                                         @L03976
         DC    C'    '                                         @L03976
XRELN    EQU   SYSREL
WORK     DC    D'0'
LASTLAB  STM   R5,R8,XSAVE              SAVE REGS
         LA    R5,X                     POINT TO LAST LABEL
         ST    R5,XLABELP               POINTER IN AREA
         B     RETURN
ENTRIES  DS    0F
         DC    XL4'00010001'
         DC    XL4'00010001'
         DC    A(LABELS)
         DC    H'0'
         LINE
         ROUTINE CLASRC,TESTMOD
         LABEL ' '
*
*        SKIP TO CHANNEL 1
*
         DC    X'01'
         DC    X'000B8B00'
         LINE  (83,1),(1,93),SKIP=1
               ROUTINE  SPARE1,MODCHK
*                                                                XM0793
         LABEL '---RECORD ENTRY SOURCE -MDR-           TYPE- OUTBOARD  C
                         DEVICE TYPE 2305-'
*
         LINE       (16,1),(4,1)
         ROUTINE    SYSREL,CONVREL
         LABEL      'XXXX REL.'
         LINE  (86,22),(27,70)
         HEX   CPUID,0,2B
         HEX   CPUSERL,0,3B
         ROUTINE   CPUID,SETCPUNO
         LABEL 'DAY  YEAR                    HH MM SS.TH            MODC
               EL-','SERIAL NO.'
         LINE  (27,29),(21,27),SKIP=2
         ROUTINE   DAY,SETDATE
         HEX   HOUR,1,4B
         LABEL 'DATE-','TIME-'
*
         LINE  (20,13),(1,22),SKIP=1
         ROUTINE SPARE3,SETCUA
         LABEL 'CHANNEL/UNIT ADDR--','X    ---------BUFFERED LOG DATA--X
               ----------'
         LINE ,,SKIP=2
         LABEL  '(INCLUDES ALT PATH RECORDS)'
         LINE  ,(3,13)
         LABEL '  NAME         --BYTE 0--   --BYTE 1--   --BYTE 2--   -X
               -BYTE 3--   --BYTE 4--   --BYTE 5--   --BYTE 6--   -BYTEX
                7--'
         LINE  (26,13),(1,13),SKIP=1
         ROUTINE  SNB00,CHECK1
DUMLAB   LABEL ' '                                             @08969FR
         LINE  (26,13),(1,13),SKIP=2
         BIN   SNB00
         BIN   SNB01
         BIN   SNB02
         BIN   SNB03
         BIN   SNB04
         BIN   SNB05
         BIN   SNB06
         BIN   SNB07
         ROUTINE  SNB00,CHECK2
         LABEL ' ',' '
         LINE  ,,SKIP=1
         ROUTINE  SNB10,CHECK1
         LABEL ' '
         LINE  ,,SKIP=2
         BIN   SNB10
         BIN   SNB11
         BIN   SNB12
         BIN   SNB13
         BIN   SNB14
         BIN   SNB15
         BIN   SNB16
         BIN   SNB17
         ROUTINE  SNB10,CHECK2
         LABEL ' '
         LINE  ,,SKIP=1
         ROUTINE  SNB20,CHECK1
         LABEL ' '
         LINE  ,,SKIP=2
         BIN   SNB20
         BIN   SNB21
         BIN   SNB22
         BIN   SNB23
         BIN   SNB24
         BIN   SNB25
         BIN   SNB26
         BIN   SNB27
         ROUTINE  SNB20,CHECK2
         LABEL ' '
         LINE  ,,SKIP=1
         ROUTINE  SNB30,CHECK1
         LABEL ' '
         LINE  ,,SKIP=2
         BIN   SNB30
         BIN   SNB31
         BIN   SNB32
         BIN   SNB33
         BIN   SNB34
         BIN   SNB35
         BIN   SNB36
         BIN   SNB37
         ROUTINE  SNB30,CHECK2
         LABEL ' '
         LINE  ,,SKIP=1
         ROUTINE  SNB40,CHECK1
         LABEL ' '
         LINE  ,,SKIP=2
         BIN   SNB40
         BIN   SNB41
         BIN   SNB42
         BIN   SNB43
         BIN   SNB44
         BIN   SNB45
         BIN   SNB46
         BIN   SNB47
         LABEL ' '
         ROUTINE  SNB40,CHECK2
         LINE  ,,SKIP=1
         ROUTINE  SNB50,CHECK1
         LABEL ' '
         LINE  ,,SKIP=2
         BIN   SNB50
         BIN   SNB51
         BIN   SNB52
         BIN   SNB53
         BIN   SNB54
         BIN   SNB55
         BIN   SNB56
         BIN   SNB57
         ROUTINE  SNB50,CHECK2
         LABEL ' '
         LINE  ,,SKIP=1
         ROUTINE  SNB60,CHECK1
         LABEL ' '
         LINE  ,,SKIP=2
         BIN   SNB60
         BIN   SNB61
         BIN   SNB62
         BIN   SNB63
         BIN   SNB64
         BIN   SNB65
         BIN   SNB66
         BIN   SNB67
         ROUTINE  SNB60,CHECK2
         LABEL ' '
         LINE  ,,SKIP=1
         ROUTINE  SNB70,CHECK1
         LABEL ' '
         LINE  ,,SKIP=2
         BIN   SNB70
         BIN   SNB71
         BIN   SNB72
         BIN   SNB73
         BIN   SNB74
         BIN   SNB75
         BIN   SNB76
         BIN   SNB77
         ROUTINE  SNB70,CHECK2
         LABEL ' '
         LINE  ,,SKIP=1
         ROUTINE  SNB80,CHECK1
         LABEL ' '
         LINE  ,,SKIP=2
         BIN   SNB80
         BIN   SNB81
         BIN   SNB82
         BIN   SNB83
         BIN   SNB84
         BIN   SNB85
         BIN   SNB86
         BIN   SNB87
         ROUTINE  SNB80,CHECK2
         LABEL ' '
         LINE  ,,SKIP=1
         ROUTINE  SNB90,CHECK1
         LABEL ' '
         LINE  ,,SKIP=2
         BIN   SNB90
         BIN   SNB91
         BIN   SNB92
         BIN   SNB93
         BIN   SNB94
         BIN   SNB95
         BIN   SNB96
         BIN   SNB97
         ROUTINE  SNB90,CHECK2
         LABEL ' '
         LINE  ,,SKIP=1
         ROUTINE  SNB100,CHECK1
         LABEL ' '
         LINE  ,,SKIP=2
         BIN   SNB100
         BIN   SNB101
         BIN   SNB102
         BIN   SNB103
         BIN   SNB104
         BIN   SNB105
         BIN   SNB106
         BIN   SNB107
         ROUTINE  SNB100,CHECK2
         LABEL ' '
         LINE  ,,SKIP=1
         ROUTINE  SNB110,CHECK1
         LABEL ' '
         LINE  ,,SKIP=2
         BIN   SNB110
         BIN   SNB111
         BIN   SNB112
         BIN   SNB113
         BIN   SNB114
         BIN   SNB115
         BIN   SNB116
         BIN   SNB117
         ROUTINE SNB110,CHECK2
         LABEL ' '
         LINE  ,,SKIP=1
         ROUTINE  SNB120,CHECK1
         LABEL ' '
         LINE  ,,SKIP=2
         BIN   SNB120
         BIN   SNB121
         BIN   SNB122
         BIN   SNB123
         BIN   SNB124
         BIN   SNB125
         BIN   SNB126
         BIN   SNB127
         ROUTINE SNB120,CHECK2
         LABEL ' '
         LINE  ,,SKIP=1
         ROUTINE  SNB130,CHECK1
         LABEL ' '
         LINE  ,,SKIP=2
         BIN   SNB130
         BIN   SNB131
         BIN   SNB132
         BIN   SNB133
         BIN   SNB134
         BIN   SNB135
         BIN   SNB136
         BIN   SNB137
         ROUTINE SNB130,CHECK2
         LABEL ' '
         LINE  ,,SKIP=1
         ROUTINE  SNB140,CHECK1
         LABEL ' '
         LINE  ,,SKIP=2
         BIN   SNB140
         BIN   SNB141
         BIN   SNB142
         BIN   SNB143
         BIN   SNB144
         BIN   SNB145
         BIN   SNB146
         BIN   SNB147
         ROUTINE SNB140,CHECK2
         LABEL ' '
         LINE  ,,SKIP=1
         ROUTINE  SNB150,CHECK1
         LABEL ' '
         LINE  ,,SKIP=2
         BIN   SNB150
         BIN   SNB151
         BIN   SNB152
         BIN   SNB153
         BIN   SNB154
         BIN   SNB155
         BIN   SNB156
         BIN   SNB157
         ROUTINE SNB150,CHECK2
         LABEL ' '
         LINE  ,(12,),SKIP=1
         ROUTINE SNB00,LASTLAB
X        LABEL 'X REGISTERS WILL BE ZERO IF SELECTIVE RESET CAUSED BY TX
               YPE 1 ERROR.'
         LINE  ,,SKIP=1
         ROUTINE SNB00,BITE
         LABEL ' '
CULABE1  LABEL 'CU SEL RESET                   ERROR PATT    TC REG X  X
                  TG REG X    HIGH ADD     LOW ADD     CK 1   0-7   CK X
               1  8-15'
CULABE2  LABEL '                            '
MFCLABE1 LABEL 'MPL FILE CHK                  READ/SEEK'
MFCLABE2 LABEL '                            ',' ',' ','                X
                                                                       X
                       '                                         S21021
CDCLABE1 LABEL 'CORRECTABLE        RETRY CT    COMMAND      UNIT ADD   X
               ADD MODIFER    CYLINDER       HEAD        RECORD      MSX
               G CODE'
CDCLABE2  LABEL 'DATA CHECK','        '
UDCLABE1 LABEL 'UNCORRECTABLE      RETRY CT    COMMAND      UNIT ADD   X
               ADD MODIFER    CYLINDER       HEAD        RECORD      MSX
               G CODE'
UDCLABE2  LABEL 'DATA CHECK','        '
UOCLABE1 LABEL 'USAGE/OVERRUN    MODULE/OVFL   OVERRUN A    OVERRUN B  X
                 AM CHECK     USE 0-7      USE 8-15     USE 16-23    USX
               E 24-31'
UOCLABE2 LABEL 'COUNTERS','       '
         DC    X'FF'
         DC    XL2'009A'
         DC    XL2'001A'
         DC    XL2'0000'
LABELS   DS    0F
         DC    C'$'
         LSTART
         END
