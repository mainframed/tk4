         TITLE 'I F C E T 0 0 C --- ARGONAUT MDR RECORD SUPPORT'
         MACRO
         BIN   ,
.*                                 &SYSLIST(&OP,1) - NAME OF LOG ITEM
.*                                      RECORD DSECT
.*                                 $SYSLIST(&OP,2) - NBR OF SPACES
.*                                      TO SKIP ON MULTIPLE
.*                                      PRINT OR NBR OF SPACES
.*                                      TO BACK UP FROM COLUMN FOR
.*                                      START OF PRINTING. THE LETTER
.*                                      B PRECEEDS THE NUMBER (B5) IF
.*                                      THE LATTER IS DESIRED. OMISSION
.*                                      OF THIS OPERAND FORCES OPTION 2
.*                                      WITH BACKUP SPACING EQUAL TO
.*                                 THE NUMBER OF BITS IN THE ITEM
.*                                 DEFINITION (VIA MACRO 'DSGEN').
.*                            A LIMIT OF 15 BITS IS IMPOSED FOR THE
.*                                 TOTAL NUMBER OF BITS TO PRINT.
         GBLA  &BITS(3000),&DB,&DT,&INITIAL
         GBLA  &ITEMITR,&ITEMFST(20),&ITEMNBR,&ITEMMDX
         GBLA  &ITEMNDX(3000),&ITEMXPT
         GBLA  &LABELNR,&LASTCC,&LB,&LBLCTR(100),&LPTR,&LT
         GBLA  &NRENTS,&SHIFT(3000)
         GBLB  &DIAGITF,&DIAGLIN,&DIAGSET,&DIAGTST,&FSW,&TEST
         GBLC  &CARRCON,&CSECT,&DSECT,&ITEM(3000)
         GBLC  &LABELS(3000),&LBL(100),&NRENTSX
         LCLB  &B,&SUB
         LCLA  &IBITS,&ISHIFT,&K
         LCLA  &TEMP,&T,&OP
         LCLC  &S,&ARG
          ACTR  500000
         AIF   (&DIAGTST EQ 0).NOMSG
         AIF   (&INITIAL EQ 0).NOMSG
         AIF   (&DIAGLIN EQ 1).NOMSG
         MNOTE '**NO LINE ASSOCIATED WITH THIS DATA**'
*****THE FOLLOWING GENERATED FOR TEST PURPOSES*****
*        LINE
         LINE
.NOMSG   ANOP
         AIF   (K'&SYSLIST(1) EQ K'&SYSLIST(1,1)).LOOP
&SUB     SETB  1
.LOOP    ANOP
&OP      SETA  &OP+1
&ARG     SETC  '&SYSLIST(&OP,1)'
         ITEMFIND &ARG
         AIF   (&ITEMXPT EQ 0).MEXIT
&ISHIFT  SETA  &SHIFT(&ITEMXPT)
&IBITS   SETA  &BITS(&ITEMXPT)
         AIF   (&SUB EQ 1).SUB00
         AIF   ('&SYSLIST(3)' EQ '').SETT
&IBITS   SETA  &SYSLIST(3)
         AGO   .SETT
.SUB00   AIF   ('&SYSLIST(&OP,3)' EQ '').SETT
&IBITS   SETA  &SYSLIST(&OP,3)
.SETT    ANOP
&T       SETA  &IBITS-1
&B       SETB  0
         AIF   (&IBITS GT 0 AND &IBITS LT 16).SUB0
         MNOTE '**NO. OF BITS OUTSIDE RANGE OF 1-15**'
.MEXIT   MEXIT
.SUB0    AIF   (&SUB EQ 1).SUBA
         AIF   ('&SYSLIST(2)' EQ '').BACKUP
&K       SETA  K'&SYSLIST(2)
&B       SETB  ('&SYSLIST(2)'(1,1) EQ 'B')
&S       SETC  '&SYSLIST(2)'(&B+1,&K-&B)
         AGO   .SUBB
.SUBA    ANOP
         AIF   ('&SYSLIST(&OP,2)' EQ '').BACKUP
&K       SETA  K'&SYSLIST(&OP,2)
&B       SETB  ('&SYSLIST(&OP,2)'(1,1) EQ 'B')
&S       SETC  '&SYSLIST(&OP,2)'(&B+1,&K-&B)
.SUBB    ANOP
&T       SETA  &S
.BACKUP  ANOP
&NRENTS  SETA  &NRENTS+1
         AIF   (&IBITS NE 1 OR &B NE 0).BIN2
&TEMP    SETA  &ISHIFT
         DC    AL.12(&SYSLIST(&OP,1)-&DSECT),XL.4'&TEMP'
         AGO   .OUT
         AIF   (&SUB EQ 1).BIN2
         AIF   ('&SYSLIST(2)' NE '').BIN3
         AGO   .BIN2A
.BIN2    AIF   ('&SYSLIST(&OP,2)' NE '').BIN3
.BIN2A   ANOP
&B       SETB  1
.BIN3    ANOP
         DC    AL.12(&SYSLIST(&OP,1)-&DSECT),XL.4'C'
         DC    BL.1'&B',AL.7(&T),AL.4(&IBITS),FL.4'&ISHIFT'
.OUT     ANOP
&ITEMXPT     SETA  0
         AIF   (&SUB EQ 0).OUTA
         AIF   (&OP LT N'&SYSLIST).LOOP
.OUTA    ANOP
         MEND
         MACRO
&NAME    DSGEN ,
.*                            THIS MACRO GENERATES ONE DSECT WORD
.*                                 FORMATTED FOR THE SUBJECT LOG RECORD
.*                                 EACH OPERAND HAS TWO SUB LISTS. THE
.*                                 FIRST IS THE NAME OF THE LOG ITEM
.*                                 WHILE THE SECOND IS THE # OF BITS TO
.*                                 ALLOCATE FOR THIS ITEM. ITEMS MUST
.*                                 BE DEFINED CONTIGUOUSLY WITHOUT GAPS
.*                                 UNUSED BIT GROUPS MUST BE GIVEN
.*                                 DUMMY NAMES. THE NAMES PROVIDED FOR
.*                                 THE LOG ITEMS MAY THEN BE USED IN
.*                                 THE CONVERSION MACRO INSTRUCTIONS.
         GBLA  &BITS(3000),&DB,&DT,&INITIAL
         GBLA  &ITEMITR,&ITEMFST(20),&ITEMNBR,&ITEMMDX
         GBLA  &ITEMNDX(3000),&ITEMXPT
         GBLA  &LABELNR,&LASTCC,&LB,&LBLCTR(100),&LPTR,&LT
         GBLA  &NRENTS,&SHIFT(3000)
         GBLB  &DIAGITF,&DIAGLIN,&DIAGSET,&DIAGTST,&FSW,&TEST
         GBLC  &CARRCON,&CSECT,&DSECT,&ITEM(3000)
         GBLC  &LABELS(3000),&LBL(100),&NRENTSX
         LCLB  &ONLY1,&CTIND
         LCLA  &START1,&START2,&END1,&END2
         LCLC  &SYMBOL,&EQUSYM
         LCLA  &OP,&COUNT,&BYTE,&EQUCNT
         ACTR  100000
         AIF   (&ITEMITR EQ 0).START
 MNOTE   20,'DSGEN MACRO OCCURRS AFTER ITEMSORT MACRO HAS BEEN ISSUED.'
         MEXIT
.START   AIF   (N'&SYSLIST(&OP+1) LT 3).LOOP
         AIF   ('&SYSLIST(&OP+1,3)' EQ 'EQU').LOOP     TEST FOR EQU
  AIF   ('&SYSLIST(&OP+1,3)' EQ '' OR '&SYSLIST(&OP+1,4)' EQ '').ERR1
         AIF   ('&SYSLIST(&OP+1,3)' GT '&SYSLIST(&OP+1,4)').ERR3
&START1  SETA  &SYSLIST(&OP+1,3)        GET START COUNT OF 1
&START2  SETA  &SYSLIST(&OP+1,4)        GET FIRST END COUNT
         AGO   .SET2A                   BRANCH AROUND
         AIF   ('&SYSLIST(&OP+1,5)' GT '&SYSLIST(&OP+1,6)').ERR3
.ERR1    MNOTE '**FIRST SYMBOL CONCATENATION INCOMPLETE**'
         MEXIT
.SET2A   AIF   ('&SYSLIST(&OP+1,5)' EQ ''AND '&SYSLIST(&OP+1,6)' EQ '' X
               ).SET2                   TEST FOR NO SECOND
  AIF   ('&SYSLIST(&OP+1,5)' EQ '' OR '&SYSLIST(&OP+1,6)' EQ '').ERR2
&END1    SETA  &SYSLIST(&OP+1,5)        GET START COUNT OF 2
&END2    SETA  &SYSLIST(&OP+1,6)        GET END COUNT OF 2
         AGO   .LOOP
.SET2    ANOP
&ONLY1   SETB  1                        INDICATE ONLY ONE CONCATENATION
         AGO   .LOOP
.ERR2    MNOTE '**SECOND SYMBOL CONCATENATION INCOMPLETE**'
         MEXIT
.ERR3    MNOTE '**START GREATER THAN END CONCATENATION**'
         MEXIT
.LOOP    ANOP
&OP      SETA  &OP+1
         AIF   (&OP GT N'&SYSLIST).DONE      TEST IF ALL DONE
&ITEMNBR SETA  &ITEMNBR+1
         AIF   ('&SYSLIST(&OP,2)' NE '').OK1
         MNOTE '**NULL BIT COUNT FIELD**'
         MEXIT
.OK1     ANOP
&SYMBOL  SETC  '&SYSLIST(&OP,1)'
         AIF   ('&SYMBOL' NE '').OK
*              *****     LOG ITEM SYMBOL NOT PROVIDED     *****
         AIF   ('&SYSLIST(&OP,3)' NE 'EQU').CNT
         AGO   .START                   GET NEXT
.OK      AIF   (N'&SYSLIST(&OP) LT 3).NO3    TEST FOR OLD FORMAT
         AIF   ('&SYSLIST(&OP,3)' EQ 'EQU').EQU   TEST FOR EQU
         AIF   (&ONLY1).OK2             TEST FOR ONLY ONE CONCAT
&SYMBOL  SETC  '&SYMBOL.&START1.&END1'       GET 1 AND 2 CONCAT
         AGO   .NO3                     BRANCH AROUND
.OK2     ANOP
&SYMBOL  SETC  '&SYMBOL.&START1'        GET 1 CONCATENATED
.NO3     ANOP
&SYMBOL  EQU   *+&BYTE
&ITEM(&ITEMNBR) SETC '&SYMBOL'          GET LABEL IN GLOBAL
&BITS(&ITEMNBR) SETA &SYSLIST(&OP,2)
&SHIFT(&ITEMNBR) SETA &COUNT-&BYTE*8
.CNT     ANOP
&CTIND   SETB  1                        SET COUNT INDICATOR
&COUNT   SETA  &COUNT+&SYSLIST(&OP,2)
&BYTE    SETA  &COUNT/8
         AIF   (N'&SYSLIST(&OP) LT 3).START  TEST FOR OLD FORMAT
         AIF   (&ONLY1).TSTST           TEST FOR ONLY ONE CONCAT
         AIF   (&END1 EQ &END2).TSTST   TEST FOR SECOND END
&END1    SETA  &END1+1                  INCREMENT COUNT
&OP      SETA  &OP-1                    DECREMENT INCREMENT
         AGO   .LOOP                    GENERATE NEXT
.TSTST   ANOP
         AIF   (&START1 EQ &START2).LPDONE   TEST FOR END
         AIF   (&ONLY1).NSET1           TEST FOR ONLY ONE CONCAT
&END1    SETA  &SYSLIST(&OP,5)          RE-INIT SECOND START COUNT
.NSET1   ANOP
&OP      SETA  &OP-1                    DECREMENT INDEX
&START1  SETA  &START1+1                INCREMENT FIRST COUNT
         AGO   .LOOP                    GO DO NEXT
.LPDONE  ANOP
&ONLY1   SETB  0                        CLEAR INDICATOR
         AGO   .START                   GO TEST FOR MORE SUB-LISTS
.DONE    AIF   (&COUNT-&COUNT/8*8 NE 0).ERR  TEST FOR MULTIPLE OF EIGHT
         AIF   (&CTIND EQ 0).END        NO DS IF NO COUNT
&COUNT   SETA  &COUNT/8                 GET NUMBER OF BYTES
&NAME    DS    &COUNT.C
.END     MEXIT
.ERR     MNOTE '**BIT COUNT FLDS DO NOT TOTAL MULTIPLE OF 8**'
         MEXIT
.EQU     AIF   ('&SYSLIST(&OP,4)' NE '').EQU1     TEST FOR NULL LABEL
.ERR4    MNOTE '**EQU OPERAND OMITTED OR NOT FOUND**'
         MEXIT
.EQU1    AIF   (&SYSLIST(&OP,2) LT 16).EQU2  COUNT OVER FIFTEEN
*****               THE FOLLOWING STATEMENT                *****
*****     NUMBER OF BITS EXCEED 15 - DO NOT USE AS BIN     *****
.EQU2    ANOP
&EQUCNT  SETA  0                        CLEAR COUNTER
&EQUSYM  SETC  '&SYSLIST(&OP,4)'        GET EQU OPERAND
.EQULOP  ANOP
&EQUCNT  SETA  &EQUCNT+1                INCREMENT INDEX
         AIF   ('&ITEM(&EQUCNT)' EQ '&EQUSYM').EQUOUT  LABEL FOUND?
         AIF   (&EQUCNT EQ &ITEMNBR).ERR4    ALL TESTED?
         AGO   .EQULOP                  NO-GET NEXT
.EQUOUT  ANOP
&ITEM(&ITEMNBR) SETC '&SYMBOL'          GET LABEL NAME
&BITS(&ITEMNBR) SETA &SYSLIST(&OP,2)    GET NUMBER OF BITS
&SHIFT(&ITEMNBR) SETA &SHIFT(&EQUCNT)   GET SHIFT COUNT
&SYMBOL  EQU   &EQUSYM                  THIS SYMBOL EQUATED
         AGO   .START                   RETURN FOR NEXT
         MEND
         MACRO
         CONVT ,
         GBLA  &BITS(3000),&DB,&DT,&INITIAL
         GBLA  &ITEMITR,&ITEMFST(20),&ITEMNBR,&ITEMMDX
         GBLA  &ITEMNDX(3000),&ITEMXPT
         GBLA  &LABELNR,&LASTCC,&LB,&LBLCTR(100),&LPTR,&LT
         GBLA  &NRENTS,&SHIFT(3000)
         GBLB  &DIAGITF,&DIAGLIN,&DIAGSET,&DIAGTST,&FSW,&TEST
         GBLC  &CARRCON,&CSECT,&DSECT,&ITEM(3000)
         GBLC  &LABELS(3000),&LBL(100),&NRENTSX
         LCLB  &SUB
         LCLA  &OP
         LCLA  &OFFSET,&OUT,&IN,&IND,&IBITS,&ISHIFT
.*
.*       CONVERT BINARY TO DECIMAL
.*             &NAME IS THE REFERENCE INTO THE DSECT RECORD
.*             &OUT    IS THE NUMBER OF CHARS TO BE PUT ONTO PRINT LINE
.*                                      DEFAULT IS FOUR MAXIMUM IS 8
.*             &IN       IS THE NUMBER OF BYTES AS INPUT
.*                                      ERROR DEFAULT IS 2 MAXIMUM IS 4
.*
         AIF   (&DIAGTST EQ 0).NOMSG
         AIF   (&INITIAL EQ 0).NOMSG
         AIF   (&DIAGLIN EQ 1).NOMSG
         MNOTE '**NO LINE ASSOCIATED WITH THIS DATA**'
*****THE FOLLOWING GENERATED FOR TEST PURPOSES*****
*        LINE
         LINE
.NOMSG   ANOP
         AIF   (K'&SYSLIST(1) EQ K'&SYSLIST(1,1)).LLOP
&SUB     SETB  1
.LLOP    ANOP
&OP      SETA  &OP+1
&OUT     SETA  4                        .BR NOT NULL ELSE SET TO 4
         AIF   (&SUB EQ 1).SUBA
         AIF   ('&SYSLIST(3)' EQ '').B  TEST FOR NULL INPUT
&OUT     SETA  &SYSLIST(3)             SET &OUT FROM INPUT
         AGO   .A
.SUBA    AIF   ('&SYSLIST(&OP,3)' EQ '').B
&OUT     SETA  &SYSLIST(&OP,3)          SET &OUT FROM INPUT
.A       AIF   (&OUT GT 0 AND &OUT LT 9).B   TEST RANGE
.AERR    MNOTE '*** BYTES OUT INCORRECTLY SPECIFIED DEFAULT USED ***'
&OUT     SETA  4
.B       AIF   (&SUB EQ 1).SUBB
         AIF   ('&SYSLIST(2)' EQ '').LOOP    BRANCH IF NULL
&IN      SETA  &SYSLIST(2)              .SET &IN FROM INPUT
         AGO   .BB
.SUBB    AIF   ('&SYSLIST(&OP,2)' EQ '').LOOP
&IN      SETA  &SYSLIST(&OP,2)          .SET &IN FROM INPUT
.BB      AIF   (&IN GT 0 AND &IN LT 5).D     TEST RANGE
.BERR    MNOTE '*** BYTES IN INCORRECTLY SPECIFIED DEFAULT USED ***'
&IN      SETA  2
         AGO   .D
.LOOP    ANOP                           .LOOP TO FIND &NAME IN DSECT
&IND     SETA  &IND+1                   .SET UP LOOP COUNTER
         AIF   ('&ITEM(&IND)' EQ '&SYSLIST(&OP,1)').FOUND
         AIF   (&IND LT &ITEMNBR).LOOP  .TEST FOR END OF DSECT
         MNOTE '*** LOG ITEM NOT DEFINED ***'
         MEXIT
.FOUND   ANOP
&IBITS   SETA  &BITS(&IND)              .SET UP NUMBER OF BITS SPEC
         AIF   (&IBITS-((&IBITS/8)*8) EQ 0).C  .TEST FOR MULTIPLE OF 8
         MNOTE '*** DATA NOT DEFINED A MULTIPLE OF 8 BITS ***'
         MEXIT
.C       ANOP
&IN      SETA  &IBITS/8                 .COMPUTE NUMBER OF BYTES
         AIF   (&IN LT 1 OR &IN GT 4).BERR  .TEST RANGE B IF IN ERROR
&ISHIFT  SETA  &SHIFT(&IND)             .SET UP FOR TEST OF BYTE BNDR
         AIF   (&ISHIFT EQ 0).D         .TEST FOR BYTE BNDRY
         MNOTE '*** DATA NOT ALIGNED ON A BYTE BOUNDARY ***'
         MEXIT
.*
.D       ANOP
&NRENTS  SETA  &NRENTS+1
         DC    AL.12(&SYSLIST(&OP,1)-&DSECT),AL.4(9)
         DC    X'&IN',X'&OUT'
&IND     SETA  0
         AIF   (&SUB EQ 0).OUTA
         AIF   (&OP LT N'&SYSLIST).LLOP
.OUTA    ANOP
         MEND
         MACRO
         ITEMFIND &ARG,&ITFDIAG=
         GBLA  &BITS(3000),&DB,&DT,&INITIAL
         GBLA  &ITEMITR,&ITEMFST(20),&ITEMNBR,&ITEMMDX
         GBLA  &ITEMNDX(3000),&ITEMXPT
         GBLA  &LABELNR,&LASTCC,&LB,&LBLCTR(100),&LPTR,&LT
         GBLA  &NRENTS,&SHIFT(3000)
         GBLB  &DIAGITF,&DIAGLIN,&DIAGSET,&DIAGTST,&FSW,&TEST
         GBLC  &CARRCON,&CSECT,&DSECT,&ITEM(3000)
         GBLC  &LABELS(3000),&LBL(100),&NRENTSX
         LCLA  &A,&B,&X,&I
         LCLC  &C1,&C2,&S
         ACTR  100000
&ITEMXPT SETA  0                  INDICATE ITEM NOT FOUND.
         AIF   ('&ARG' NE '').NORM
         AIF   ('&ITFDIAG' EQ 'OFF'  OR  '&ITFDIAG' EQ 'ON').DIAGSET
  MNOTE 0,'ITEMFIND - OPERAND (&ITFDIAG) INVALID FOR ITFDIAG KEYWORD.'
         MEXIT
.DIAGSET ANOP
&DIAGITF SETB  ('&ITFDIAG' EQ 'ON')
         MEXIT
.NORM    AIF   (&ITEMITR GT 0).SORTED
         ITEMSORT
.SORTED  ANOP
&I       SETA  &ITEMITR           COMPUTED ITTERATION COUNT.
&X       SETA  &ITEMMDX           COMPUTED MAXIMUM INDEX.
&C1      SETC  '&ARG'             ISOLATE SEARCH ARGUMENT.
&C1      SETC  '&C1'.'       '(1,8-K'&C1) PAD TO FULL 8 CHARACTERS.
.SRCHUP  ANOP
&A       SETA  &A+&X              CURRENT TABLE SUBSCRIPT + INDEX.
&S       SETC  '+'
         AIF   (&A  GT &ITEMNBR  AND  NOT &DIAGITF).SRCHDN1
         AIF   (&A  LE &ITEMNBR).SRCH00
&C2      SETC  '*-OVER-*'
&I       SETA  &I-1               DECREMENT ITTERATION COUNTER.
&X       SETA  &X/2               HALVE INDEX VALUE.
 MNOTE *,'&ARG - &C2   ITRCTR = &I   PTR = &A&S &X'
         AIF   (&I GE 0).SRCHDWN  IF ITTERATION COUNT NOT EXPIRED.
.NOFIND  MNOTE  20,' ITEMFIND FAILED FOR NAME  &C1  '
         MEXIT
.SRCH00  ANOP
&I       SETA  &I-1               DECREMENT ITTERATION COUNTER.
         AIF   (&I LT 0).NOFIND   IF COUNT HAS GONE TO ZERO.
&X       SETA  &X/2               HALVE INDEX VALUE.
&B       SETA  &ITEMNDX(&A)       GET SUBSCRIPT FROM INDEX TABLE.
&C2      SETC  '&ITEM(&B)'.'       '(1,8-K'&ITEM(&B))  PAD COMPARAND.
         AIF   (NOT &DIAGITF).COMPARE
 MNOTE *,'&ARG - &C2   ITRCTR = &I   PTR = &A&S &X   POS = &B'
.COMPARE AIF   ('&C1' LT '&C2').SRCHDWN   ARGUMENT LOWER, MOVE DOWN.
         AIF   ('&C1' GT '&C2').SRCHUP    ARGUMENT HIGHER, MOVE UP.
&ITEMXPT SETA  &B                 FOUND. SET INDEX POINTER FOR RETURN.
&ITEMFST(&I+1)  SETA  &ITEMFST(&I+1)+1
         MEXIT
.SRCHDWN ANOP
&A       SETA  &A-&X              CURRENT POINTER MINUS INDEX VALUE.
&S       SETC  '-'
         AIF   (&A LE &ITEMNBR).SRCH00    IF WITHIN BOUNDS OF TABLE.
.SRCHDN1 ANOP
&I       SETA  &I-1               DECREMENT ITTERATION COUNTER.
&X       SETA  &X/2               HALVE INDEX VALUE.
         AIF   (&I GE 0).SRCHDWN  IF ITTERATION COUNT NOT EXPIRED.
         MNOTE  20,' ITEMFIND FAILED FOR NAME  &C1  '
         MEND
         MACRO
         ITEMSORT  &DUMMY
         GBLA  &BITS(3000),&DB,&DT,&INITIAL
         GBLA  &ITEMITR,&ITEMFST(20),&ITEMNBR,&ITEMMDX
         GBLA  &ITEMNDX(3000),&ITEMXPT
         GBLA  &LABELNR,&LASTCC,&LB,&LBLCTR(100),&LPTR,&LT
         GBLA  &NRENTS,&SHIFT(3000)
         GBLB  &DIAGITF,&DIAGLIN,&DIAGSET,&DIAGTST,&FSW,&TEST
         GBLC  &CARRCON,&CSECT,&DSECT,&ITEM(3000)
         GBLC  &LABELS(3000),&LBL(100),&NRENTSX
         LCLA  &R,&ZI,&I,&A1,&A2,&TEMP
         LCLB  &SR,&GOTO,&TB
         LCLB   &SW(4000)
         LCLC  &C1,&C2
         ACTR   200000
&A1      SETA  0
         AIF   (&ITEMITR EQ 0).LOOP0   TEST FOR RE-ENTRY.
 MNOTE   0,'ITEMSORT MACRO OCCURRS MORE THAN ONCE. TABLE NOT RESORTED.'
         MEXIT
.LOOP0   ANOP
&A1      SETA  &A1+1
&ITEMNDX(&A1) SETA &A1
         AIF   (&A1 LT &ITEMNBR).LOOP0
         AIF   (&ITEMNBR LT 2).MEXIT
&A1      SETA  &ITEMNBR/2         HALF OF TOTAL ENTRIES.
&A2      SETA  0
.LOOP1   ANOP
&A2      SETA  &A2+1
&SW(&A2) SETB  1                  SET FIRST HALF OF SWITCHES TO 1.
         AIF   (&A2 LT &A1).LOOP1
.LOOP2   ANOP
&A2      SETA  &A2+1
&SW(&A2) SETB  0                  SET SECOND HALF TO ZERO.
         AIF   (&A2 LT &ITEMNBR).LOOP2
&R       SETA  0                  SET R TO FIRST TRIAL ITEM.
&ZI      SETA  &ITEMNBR-1         SET ZI TO LAST ITEM.
&I       SETA  &ZI-1
.T3      ANOP
&SR      SETB  (&SW(&R+1))        GET FLAG, WHICH WAY TO GO?
&R       SETA  (&R*2)+1+&SR       R=2R+(1 OR 2)
         AIF   (&R LE &I).T3
.T4      ANOP
&R       SETA  (&R-1)/2           R=(R-1)/2
&SR      SETB  (&SW(&R+1))        GET SUBTREE SELECT FLAG.
         AIF   (&SR EQ 0).T7
         AIF   (&GOTO EQ 0).T5
.T7      ANOP
&A1      SETA  &ITEMNDX(&R+1)
&A2      SETA  &ITEMNDX(&ZI+1)
&C1      SETC  '&ITEM(&A1)'.'       '(1,8-K'&ITEM(&A1))
&C2      SETC  '&ITEM(&A2)'.'       '(1,8-K'&ITEM(&A2))
         AIF   ('&C1' LT '&C2').T8
.T9      ANOP
&TEMP    SETA  &ITEMNDX(&R+1)     Z(R) TO TEMP.
&TB      SETB  (&SW(&R+1))
&ITEMNDX(&R+1) SETA &ITEMNDX(&ZI+1)    Z(I) TO Z(R)
&SW(&R+1) SETB (&SW(&ZI+1))
&ITEMNDX(&ZI+1) SETA &TEMP        TEMP TO Z(I)
&SW(&ZI+1) SETB (&TB)
&SR      SETB  (&SR EQ 0)
&SW(&R+1) SETB (&SR)
.T8      AIF   (&R GT 0).T4
&GOTO    SETB  1                  SET 'GO TO' T7
&ZI      SETA  &ZI-1
&I       SETA  &I-1
         AIF   (&I GE 0).T3
.MEXIT   ANOP
&ITEMITR SETA 1
&ITEMMDX SETA 1
.ITRLOOP ANOP
&ITEMITR SETA &ITEMITR+1
&ITEMMDX SETA &ITEMMDX+&ITEMMDX
         AIF  (&ITEMMDX LT &ITEMNBR).ITRLOOP
&ITEMITR SETA &ITEMITR-1
&ITEMMDX SETA &ITEMMDX/2
&A1      SETA &ITEMNBR
&A2      SETA &ITEMMDX
&TEMP    SETA &ITEMITR
 MNOTE *,' ITEMS = &A1, MAX INDEX = &A2, ITTERATION COUNT = &TEMP'
         MEXIT
.T5      ANOP
&TEMP    SETA  &ITEMNDX(&R+1)     Z(R) TO TEMP.
&TB      SETB  (&SW(&R+1))
&ITEMNDX(&R+1) SETA &ITEMNDX(&ZI+1)    Z(I) TO Z(R)
&SW(&R+1) SETB (&SW(&ZI+1))
&ITEMNDX(&ZI+1) SETA &TEMP        TEMP TO Z(I)
&SW(&ZI+1) SETB (&TB)
&SR      SETB  (&SR EQ 0)
&SW(&R+1) SETB (&SR)
         AGO   .T3
         MEND
           MACRO
&NAME    LABEL &LABEL1,&LABEL2,&LABEL3,&LABEL4,&LABEL5,&LABEL6,        C
               &LABEL7,&LABEL8
.*                                 THIS MACRO ALLOWS LABELS TO
.*                                 BE SPECIFIED FOR A GIVEN LINE. THE
.*                                 LABELS SHOULD BE ENCLOSED IN QUOTA-
.*                                 TION MARKS. THE COLUMN AND LINE END
.*                                 CONTROLS WILL BE INSERTED BY THE
.*                                 MACRO. OMITTED OPERANDS MEAN NO
.*                                 LABEL REQUIRED FOR RESPECTIVE
.*                                 COLUMN.
         GBLA  &BITS(3000),&DB,&DT,&INITIAL
         GBLA  &ITEMITR,&ITEMFST(20),&ITEMNBR,&ITEMMDX
         GBLA  &ITEMNDX(3000),&ITEMXPT
         GBLA  &LABELNR,&LASTCC,&LB,&LBLCTR(100),&LPTR,&LT
         GBLA  &NRENTS,&SHIFT(3000)
         GBLB  &DIAGITF,&DIAGLIN,&DIAGSET,&DIAGTST,&FSW,&TEST
         GBLC  &CARRCON,&CSECT,&DSECT,&ITEM(3000)
         GBLC  &LABELS(3000),&LBL(100),&NRENTSX
         LCLC  &MARGIN,&TACK
         LCLC  &TEXT
         LCLA  &OP,&K,&START,&NRCHAR,&REMAIN
         AIF   (&INITIAL EQ 0).DIAGCLR
         AIF   (&DIAGTST EQ 0).DIAGCLR
         AIF   (&DIAGLIN EQ 1).DIAGCLR
         MNOTE '**NO LINE ASSOCIATED WITH PREVIOUS LABEL**'
***** THE FOLLOWING GENERATED FOR TEST PURPOSES *****
*        LINE
         LINE
.DIAGCLR ANOP
&DIAGSET SETB  0
&DIAGLIN SETB  0
&MARGIN  SETC  '*'
         AIF   ('&NAME' EQ '').LOOPOUT
&LPTR    SETA  &LPTR+1
&LBL(&LPTR) SETC '&NAME'
&LBLCTR(&LPTR) SETA &LABELNR+1
.LOOPOUT ANOP
&OP      SETA  &OP+1
&K       SETA  K'&SYSLIST(&OP)
         AIF (&K GE 2).NORM
&K       SETA  2
.NORM    ANOP
&START   SETA  2
         AIF (&OP LT N'&SYSLIST).NOTLAST
&MARGIN  SETC  '$'
.NOTLAST ANOP
&NRCHAR  SETA  8
&TACK    SETC  ''
.LOOPIN  ANOP
&REMAIN  SETA  &K-&START
         AIF (&REMAIN GE 8).MORE
&TACK    SETC  '&MARGIN'
&NRCHAR  SETA  &REMAIN
.MORE    ANOP
&TEXT    SETC  ''
         AIF (&REMAIN EQ 0).ENDTEXT
&TEXT    SETC  '&SYSLIST(&OP)'(&START,&NRCHAR)
.ENDTEXT ANOP
&LABELNR SETA  &LABELNR+1
&LABELS(&LABELNR) SETC '&TEXT'.'&TACK'
&START   SETA  &START+8
         AIF ('&TACK' EQ '').LOOPIN
         AIF ('&TACK' EQ '*').LOOPOUT
         MEND
         MACRO
&NAME    LINE  &DATA,&LABEL,&SKIP=
.*                            THIS MACRO ESTABLISHES THE MARGINS OR
.*                                 COLUMN SETTINGS FOR EACH LINE. IT
.*                                 GENERATES NECESSARY CARRIAGE CONTROL
.*                                 ENTRIES. LASTLY IT DETERMINES THE
.*                                 NUMBER OF ENTRIES ASSOCIATED WITH
.*                                 THE PREVIOUS LINE AND STORES THAT #
.*                                 INTO THE APPROPRIATE BYTE.
.*                            &DATA(1) - CHARACTER POSITION TO BEGIN
.*                                 DATA.
.*                            &DATA(2) - # OF SPACES BETWEEN SUCCESSIVE
.*                                 DATA COLUMN STARTING CHARACTERS.
.*                            &LABEL(1) - CHAR. POS. TO BEGIN LABELS.
.*                            &LABEL(2) - # SPACES BETWEEN LINE'S LABEL
.*                                 STARTING CHARACTERS.
.*                            &SKIP= - # OF LINES TO ADVANCE CARRIAGE
.*                                 TO BEGIN NEXT PRINTED LINE.
.*                            NEW ENTRIES ARE GENERATED ONLY WHEN
.*                                 NECESSARY. OMISSION OF ITEMS MEANS
.*                                 THEY HAVE NOT CHANGED SINCE PREVIOUS
.*                                 SETTINGS. A LINE MACRO IS REQUIRED
.*                                 FOR EACH PRINTED LINE. IF NO SETTING
.*                                 CHANGES ARE REQUIRED ALL OPERANDS
.*                                 ARE OMITTED IN WHICH CASE ONLY THE
.*                                 # OF ENTRIES FOR THE PREVIOUS LINE
.*                                 IS COMPUTED. THIS MACRO MUST BE THE
.*                                 FIRST MACRO INSTRUCTION ISSUED FOR
.*                                 THE SUBJECT LINE
         GBLA  &BITS(3000),&DB,&DT,&INITIAL
         GBLA  &ITEMITR,&ITEMFST(20),&ITEMNBR,&ITEMMDX
         GBLA  &ITEMNDX(3000),&ITEMXPT
         GBLA  &LABELNR,&LASTCC,&LB,&LBLCTR(100),&LPTR,&LT
         GBLA  &NRENTS,&SHIFT(3000)
         GBLB  &DIAGITF,&DIAGLIN,&DIAGSET,&DIAGTST,&FSW,&TEST
         GBLC  &CARRCON,&CSECT,&DSECT,&ITEM(3000)
         GBLC  &LABELS(3000),&LBL(100),&NRENTSX
         LCLA  &S
         AIF   (&INITIAL EQ 0 OR &DIAGTST EQ 0).DIAGSET
         AIF   (&DIAGSET EQ 0).DIAGSET
         MNOTE '**NO LABEL ASSOCIATED WITH LAST SET**'
***** THE FOLLOWING GENERATED FOR TEST PURPOSES *****
*        LABEL '%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%'
               LABEL '%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%'
.DIAGSET ANOP
&DIAGSET SETB  1
&DIAGLIN SETB  1
&NAME    DS    0C
         AIF   (&INITIAL EQ 0).FIRSTLN
         LINEND
         AGO   .NTFIRST
.FIRSTLN ANOP
&NRENTSX SETC  'N&SYSNDX'
&INITIAL SETA  1
N&SYSNDX EQU   *
.NTFIRST ANOP
         DC    AL1(0)
         AIF   ('&DATA(1)' EQ '').CKDTABS
         AIF   (&DATA(1) EQ &DB).CKDTABS
&DB      SETA  &DATA(1)
         AGO   .AA
.CKDTABS AIF   ('&DATA(2)' EQ '').NCDATA
         AIF   (&DATA(2) EQ &DT).NCDATA
         AGO   .CC
.AA      AIF   ('&DATA(2)' EQ '').BB
         AIF   (&DATA(2) EQ &DT).BB
.CC      ANOP
&DT      SETA  &DATA(2)
.BB      DC    XL2'FFFE'
         DC    FL1'&DB'
         DC    FL1'&DT'
&NRENTS  SETA  &NRENTS+1
.NCDATA  ANOP
         AIF   ('&LABEL(1)' EQ '').CKLTABS
         AIF   (&LABEL(1) EQ &LB).CKLTABS
&LB      SETA  &LABEL(1)
         AGO   .AAA
.CKLTABS AIF   ('&LABEL(2)' EQ '').NCLABEL
         AIF   (&LABEL(2) EQ &LT).NCLABEL
         AGO   .CCC
.AAA     AIF   ('&LABEL(2)' EQ '').BBB
         AIF   (&LABEL(2) EQ &LT).BBB
.CCC     ANOP
&LT      SETA  &LABEL(2)
.BBB     DC    XL2'FFFF'
         DC    FL1'&LB'
         DC    FL1'&LT'
&NRENTS  SETA  &NRENTS+1
.NCLABEL ANOP
         AIF   ('&SKIP' NE '').CONT
         AIF   (&TEST EQ 0).NOCHANG
         AGO   .DEFALTA
.CONT    ANOP
         AIF   (&TEST EQ 0).AB
         AIF   ('&SKIP' EQ 'E').AB
&S       SETA  &SKIP
.DEFAULT ANOP
         AIF   (&SKIP EQ &LASTCC).DEFALTA
         ORG   &CARRCON+3
         DC    AL.3(0),AL.2(&S),AL.3(1)
         ORG
&LASTCC  SETA  &S
.DEFALTA ANOP
&TEST    SETB  0
         AGO   .DD
.AB      AIF   ('&SKIP' NE 'E').ABB
A&SYSNDX DC    X'FFFB8B00'              EJECT
&LASTCC  SETA  184
&TEST    SETB  1
         AGO   .ABC
.ABB     AIF   (&SKIP EQ &LASTCC).NOCHANG
A&SYSNDX DC    XL2'FFFB'
         DC    AL.3(0),AL.2(&SKIP),AL.3(1)
         DC    AL.3(0),AL.2(&SKIP),AL.3(1)
&LASTCC  SETA  &SKIP
&TEST    SETB  1
.ABC     ANOP
&CARRCON SETC  'A&SYSNDX'
&NRENTS  SETA  &NRENTS+1
.DD      ANOP
.NOCHANG ANOP
         MEND
         MACRO
         LINEND ,
.*                                 THIS MACRO IS USED ONLY BY THE LINE
.*                                 MACRO TO SET THE NUMBER OF ENTRIES
.*                                 ASSOCIATED WITH EACH LINE.
         GBLA  &BITS(3000),&DB,&DT,&INITIAL
         GBLA  &ITEMITR,&ITEMFST(20),&ITEMNBR,&ITEMMDX
         GBLA  &ITEMNDX(3000),&ITEMXPT
         GBLA  &LABELNR,&LASTCC,&LB,&LBLCTR(100),&LPTR,&LT
         GBLA  &NRENTS,&SHIFT(3000)
         GBLB  &DIAGITF,&DIAGLIN,&DIAGSET,&DIAGTST,&FSW,&TEST
         GBLC  &CARRCON,&CSECT,&DSECT,&ITEM(3000)
         GBLC  &LABELS(3000),&LBL(100),&NRENTSX
         ORG   &NRENTSX
         DC    FL1'&NRENTS'
         ORG
&NRENTS  SETA  0
B&SYSNDX EQU   *
&NRENTSX SETC  'B&SYSNDX'
         MEND
         MACRO
         LSTART ,
.*                            THIS MACRO IS ISSUED WHEN THE LABEL AREA
.*                                 OF THE TABLE IS TO BEGIN.
         GBLA  &BITS(3000),&DB,&DT,&INITIAL
         GBLA  &ITEMITR,&ITEMFST(20),&ITEMNBR,&ITEMMDX
         GBLA  &ITEMNDX(3000),&ITEMXPT
         GBLA  &LABELNR,&LASTCC,&LB,&LBLCTR(100),&LPTR,&LT
         GBLA  &NRENTS,&SHIFT(3000)
         GBLB  &DIAGITF,&DIAGLIN,&DIAGSET,&DIAGTST,&FSW,&TEST
         GBLC  &CARRCON,&CSECT,&DSECT,&ITEM(3000)
         GBLC  &LABELS(3000),&LBL(100),&NRENTSX
         LCLA  &NDX,&CTR
         ACTR  500000
         AIF   (&DIAGTST EQ 0).NODIAG
         AIF   (&DIAGSET EQ 0).NODIAG
         MNOTE '**NO LABEL ASSOCIATED WITH LAST SET**'
***** THE FOLLOWING GENERATED FOR TEST PURPOSES *****
*        LABEL '%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%'
               LABEL '%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%'
.NODIAG  ANOP
         LINEND
&CTR     SETA  1
.LOOP    ANOP
&NDX     SETA  &NDX+1
         AIF   (&LBLCTR(&CTR) NE &NDX).NOLBL
&LBL(&CTR) DC  C'&LABELS(&NDX)'
&CTR     SETA  &CTR+1
         AGO   .LOOPCTL
.NOLBL   ANOP
         DC    C'&LABELS(&NDX)'
.LOOPCTL AIF   (&NDX LT &LABELNR).LOOP
         MEND
         MACRO
&NAME    ROUTINE &LOGITEM,&RNAME
         GBLA  &BITS(3000),&DB,&DT,&INITIAL
         GBLA  &ITEMITR,&ITEMFST(20),&ITEMNBR,&ITEMMDX
         GBLA  &ITEMNDX(3000),&ITEMXPT
         GBLA  &LABELNR,&LASTCC,&LB,&LBLCTR(100),&LPTR,&LT
         GBLA  &NRENTS,&SHIFT(3000)
         GBLB  &DIAGITF,&DIAGLIN,&DIAGSET,&DIAGTST,&FSW,&TEST
         GBLC  &CARRCON,&CSECT,&DSECT,&ITEM(3000)
         GBLC  &LABELS(3000),&LBL(100),&NRENTSX
         AIF   (&DIAGTST EQ 0).NOMSG
         AIF   (&INITIAL EQ 0).NOMSG
         AIF   (&DIAGLIN EQ 1).NOMSG
         MNOTE '**NO LINE ASSOCIATED WITH THIS ROUTINE**'
*****THE FOLLOWING GENERATED FOR TEST PURPOSES*****
*        LINE
         LINE
.NOMSG   ANOP
&NAME    DC    AL.12(&LOGITEM-&DSECT),XL.4'D'
         DC    AL2(&RNAME-&CSECT)
&NRENTS  SETA  &NRENTS+1
         MEND
         MACRO
         SPECIAL &ROUTINE,&PARAM,&SPACE=
.*                            THIS MACRO GENERATES A SPECIAL ROUTINE
.*                                 ENTRY TO BE PROCESSED BY THE SUM. M.
.*                            &ROUTINE - SYMBOL ON SPECIAL ROUTINE EWT.
.*                            &PARAM - AN 8 BIT BINARY REPRESENTATION
.*                                 TO OCCUPY THE 'MASK' FIELD BUT TO BE
.*                                 INTERPRETED AS AN ARGUMENT OF THE
.*                                 SPECIAL ROUTINE.
.*                            &SPACE - THE NUMBER OF 2BYTE COUNTERS TO
.*                                 ALLOCATE FOR REQUIRED PROCESSING.
         GBLC  &CSECT
         GBLA  &COUNTR
         LCLA  &ADD
         DC    AL1(128)
         DC    BL.8'&PARAM'
         DC    AL2(&ROUTINE-&CSECT)
         DC    AL2(&COUNTR)
&ADD     SETA  1
         AIF   ('&SPACE' EQ '').NORM
&ADD     SETA  &SPACE
.NORM    ANOP
&COUNTR  SETA  &COUNTR+&ADD
         MEND
         MACRO
         HEX   ,
.*                            THIS MACRO FACILITATES GENERATION OF THE
.*                                 TABLE ENTRIES SPECIFYING HEX TYPE
.*                                 CONVERSIONS.
.*                            &SYSLIST(&OP,1) - NAME OF DATA ITEM TO BE
.*                                 CONVERTED
.*                            &SYSLIST(&OP,2) - # OF SPACES TO SKIP ON
.*                                 MULTIPLE PRINT OR NBR OF SPACES TO
.*                                 BACK UP FROM COLUMN FOR START OF
.*                                 PRINTING. THE LETTER B PRECEEDS THE
.*                                 NUMBER (B8) IF THE LATTER OPTION IS
.*                                 DESIRED. OMISSION OF THIS OPERAND
.*                                 FORCES OPTION 2 WITH BACKUP SPACING
.*                                 EQUAL TO THE NUMBER OF BYTES SPECI-
.*                                 FIED BY THE NEXT OPERAND (TWICE THIS
.*                                 NUMBER IF SUFFIX IS EXPLICITLY OR
.*                                 IMPLICITLY B).
.*                            &SYSLIST(&OP,3) - NUMBER OF BYTES TO
.*                                 PROCESS. THIS NUMBER IS SUFFIXED
.*                                 WITH AN L,R, OR B TO STIPULATE
.*                                 PRINTING OF THE LEFT HALF, RIGHT
.*                                 HALF, OR BOTH HALVES OF THE BYTE
.*                                 RESPECTIVELY. OMISSION OF THIS
.*                                 OPERAND DEFAULTS TO B WITH THE NUM-
.*                                 BER OF BYTES EQUAL TO THE ITEM LNGTH
.*                            A NEW ENTRY IS GENERATED ONLY WHEN NECES-
.*                                 SARY. THIS MACRO MUST BE THE FIRST
.*                                 MACRO INSTRUCTION ISSUED FOR THE
.*                                 SUBJECT LINE(S).
         GBLA  &BITS(3000),&DB,&DT,&INITIAL
         GBLA  &ITEMITR,&ITEMFST(20),&ITEMNBR,&ITEMMDX
         GBLA  &ITEMNDX(3000),&ITEMXPT
         GBLA  &LABELNR,&LASTCC,&LB,&LBLCTR(100),&LPTR,&LT
         GBLA  &NRENTS,&SHIFT(3000)
         GBLB  &DIAGITF,&DIAGLIN,&DIAGSET,&DIAGTST,&FSW,&TEST
         GBLC  &CARRCON,&CSECT,&DSECT,&ITEM(3000)
         GBLC  &LABELS(3000),&LBL(100),&NRENTSX
         LCLB  &B,&SUB
         LCLA  &IBITS,&ISHIFT,&K,&T,&CODE,&BYTESP,&OP
         LCLC  &S,&LETTER,&ARG
       ACTR  500000
         AIF   (&DIAGTST EQ 0).NOMSG
         AIF   (&INITIAL EQ 0).NOMSG
         AIF   (&DIAGLIN EQ 1).NOMSG
         MNOTE '**NO LINE ASSOCIATED WITH THIS DATA**'
*****THE FOLLOWING GENERATED FOR TEST PURPOSES*****
*        LINE
         LINE
.NOMSG   ANOP
         AIF   (K'&SYSLIST(1) EQ K'&SYSLIST(1,1)).LOOP
&SUB     SETB  1
.LOOP    ANOP
&OP      SETA  &OP+1
&ARG     SETC  '&SYSLIST(&OP,1)'
         ITEMFIND  &ARG
         AIF   (&ITEMXPT EQ 0).MEXIT
.FOUND   ANOP
&ISHIFT  SETA  &SHIFT(&ITEMXPT)
&IBITS   SETA  &BITS(&ITEMXPT)
         AIF   (&ISHIFT EQ 0 OR &ISHIFT EQ 4).ALIGN
.ERR     MNOTE '**DATA NOT ALLIGNED ON HEX OR BYTE BOUNDARY**'
.MEXIT   MEXIT
.ALIGN   ANOP
&CODE    SETA  1
         AIF   (&SUB EQ 1).SUBA
&K       SETA  K'&SYSLIST(3)
         AGO   .SUBB
.SUBA    ANOP
&K       SETA  K'&SYSLIST(&OP,3)
.SUBB    ANOP
         AIF   (&K EQ 1).ERR3
         AIF   (&IBITS-(&IBITS/4)*4 EQ 0).OK
         MNOTE '**BIT LENGTH SPECIFICATION INCONSISTENT WITH CONTROL**'
         MEXIT
.OK      AIF   (&SUB EQ 1).SUBC
         AIF   ('&SYSLIST(3)' EQ '').USEDEFN
&LETTER  SETC  '&SYSLIST(3)'(&K,1)
&S       SETC  '&SYSLIST(3)'(1,&K-1)
         AGO   .SUBD
.SUBC    ANOP
         AIF   ('&SYSLIST(&OP,3)' EQ '').USEDEFN
&LETTER  SETC  '&SYSLIST(&OP,3)'(&K,1)
&S       SETC  '&SYSLIST(&OP,3)'(1,&K-1)
.SUBD    ANOP
&BYTESP  SETA  &S
         AIF   ('&LETTER' EQ 'B').BOTH
         AIF   ('&LETTER' EQ 'L').LEFT
         AIF   ('&LETTER' EQ 'R').RIGHT
.ERR3    MNOTE '**PARAMETER 3 INCOMPLETE-DSGEN DEFINITION USED**'
         AGO   .USEDEFN
.RIGHT   ANOP
&CODE    SETA  2
         AGO   .L
.LEFT    ANOP
&CODE    SETA  1
         AGO   .L
.USEDEFN ANOP
&BYTESP  SETA  &IBITS/8
.BOTH    AIF   (&ISHIFT NE 0).ERR
&CODE    SETA  3
&T       SETA  &BYTESP*2-1
&S       SETC  '&T'
.OK2     AIF   (&ISHIFT EQ 0).L
&CODE    SETA  2
.L       ANOP
&B       SETB  1
         AIF   (&SUB EQ 1).SUBE
         AIF   ('&SYSLIST(2)' EQ '').BACKUP
&K       SETA  K'&SYSLIST(2)
&B       SETB  ('&SYSLIST(2)'(1,1) EQ 'B')
&S       SETC  '&SYSLIST(2)'(&B+1,&K-&B)
         AGO   .SUBF
.SUBE    ANOP
         AIF   ('&SYSLIST(&OP,2)' EQ '').BACKUP
&K       SETA  K'&SYSLIST(&OP,2)
&B       SETB  ('&SYSLIST(&OP,2)'(1,1) EQ 'B')
&S       SETC  '&SYSLIST(&OP,2)'(&B+1,&K-&B)
.SUBF    ANOP
.BACKUP  ANOP
&NRENTS  SETA  &NRENTS+1
         AIF   (&BYTESP GT 0 AND &BYTESP LT 16).SUB0
         MNOTE '**NO. OF BYTES OUTSIDE RANGE OF 1-15**'
         MEXIT
.SUB0    AIF   (&SUB EQ 1).SUBG
         DC    AL.12(&SYSLIST(1)-&DSECT),XL.4'A'
         AGO   .SUBH
.SUBG    ANOP
         DC    AL.12(&SYSLIST(&OP,1)-&DSECT),XL.4'A'
.SUBH    ANOP
         DC    BL.1'&B',FL.7'&S',AL.4(&BYTESP),FL.4'&CODE'
.OUT     ANOP
&ITEMXPT     SETA  0
         AIF   (&SUB EQ 0).OUTA
         AIF   (&OP LT N'&SYSLIST).LOOP
.OUTA    ANOP
         MEND
         MACRO
         SUM   &LOGITEM,&MASK
.*                            THIS MACRO GENERATES A NORMAL EDIT ENTRY
.*                                 FOR PROCESSING BY THE SUMMARY MODULE
.*                            &LOGITEM - NAME OF DATA ITEM TO BE SUMMED
.*                            &MASK - BYTE MASK FOR BITS TO BE TESTED.
.*                                 THIS OPERAND SHOULD BE OMITTED FOR
.*                                 SINGLE BIT ITEMS IN WHICH CASE THE
.*                                 MASK WILL BE SET ACCORDING TO THE
.*                                 OFF BYTE BIT DISPLACEMENT OF THE
.*                                 ITEM AS DEFINED BY THE MACRO 'DSGEN'
         GBLA  &COUNTR,&ITEMNBR,&BITS(3000),&SHIFT(3000)
         GBLC  &ITEM(3000),&DSECT
         LCLA  &IBITS,&NDX,&ISHIFT,&M,&BITNBR
.LOOP    ANOP
&NDX     SETA  &NDX+1
         AIF   ('&ITEM(&NDX)' EQ '&LOGITEM').FOUND
         AIF   (&NDX LT &ITEMNBR).LOOP
         MNOTE *,'LOGITEM NOT FOUND'
         MEXIT
.FOUND   ANOP
&ISHIFT  SETA  &SHIFT(&NDX)
&IBITS   SETA  &BITS(&NDX)
         AIF   (&IBITS EQ 1).FORM1      TEST FOR OLD FORM
         AIF   ('&MASK' NE '').FORM1    TEST FOR MASK
         AIF   (&IBITS+&ISHIFT GT 8).MSG     TEST FOR MORE THAN EIGHT
         DC    AL.1(0),AL.3(&ISHIFT),AL.4(&IBITS)
         DC    AL1(0)
         AGO   .ITEM
.MSG     MNOTE '**BITS GREATER THAN 8**'
         MEXIT
.FORM1   ANOP
         DC    AL1(0)
         AIF   ('&MASK' EQ '').FORM
         DC    B'&MASK'
         AGO   .ITEM
.FORM    ANOP
&BITNBR  SETA  &ISHIFT-7
&BITNBR  SETA  0-&BITNBR
&M       SETA  1
.LOOP2   ANOP
         AIF   (&BITNBR EQ 0).GOTIT
&M       SETA  &M*2
&BITNBR  SETA  &BITNBR-1
         AGO   .LOOP2
.GOTIT   ANOP
         DC    AL1(&M)
.ITEM    DC    AL2(&LOGITEM-&DSECT)
         DC    AL2(&COUNTR)
&COUNTR  SETA  &COUNTR+1
         MEND
* THIS MODULE SUPPORTS ARGONAUT DEVICE --- SUPPORT CODE =
         GBLC  &DSECT,&CSECT
&DSECT   SETC  'RECORD'
&CSECT   SETC  'IFCET00C'
IFCET00C CSECT
*****       D S E C T   G E N E R A T I O N  E N T R I E S        *****
RECORD   DSECT
         DS    0F
         DSGEN (CLASRC,8),              RECORD TYPE                    X
               (SYSREL,8),              SYSTEM & RELEASE LEVEL         X
               (SWITCH,8,0,3),          SWITCHES (4 BYTES OF EM)       X
               (RCDCNT,8),              COUNT & MULTI PART INDICATION  X
               (SPARE1,8),              SPACE (1 BYTE)                 X
               (DATE,32),               DATE                           X
               (TIME,32),               TIME                           X
               (VERNO,8),               MACHINE VERSION                X
               (CPUSER,24),             C P U SERIAL NO.               X
               (CPUID,16),              C P U IDENTIFICATION           X
               (SPACE2,16),             SPARE (2 BYTES)                X
               (DEVCUA,16),             DEVICE CHANNEL UNIT ADDR       X
               (CNT,8),                 COUNT                          X
               (SPARE,8),               SPACE (1 BYTE)                 X
               (SENSE01,64),            SENSE AREA  1                  X
               (SENSE02,64),            SENSE AREA  2                  X
               (SENSE03,64),            SENSE AREA  3                  X
               (SENSE04,64),            SENSE AREA  4                  X
               (SENSE05,64),            SENSE AREA  5                  X
               (SENSE06,64),            SENSE AREA  6                  X
               (SENSE07,64),            SENSE AREA  7                  X
               (SENSE08,64),            SENSE AREA  8                  X
               (SENSE09,64),            SENSE AREA  9                  X
               (SENSE10,64),            SENSE AREA 10                  X
               (SENSE11,64),            SENSE AREA 11                  X
               (SENSE12,64),            SENSE AREA 12                  X
               (SENSE13,64),            SENSE AREA 13                  X
               (SENSE14,64),            SENSE AREA 14                  X
               (SENSE15,64),            SENSE AREA 15                  X
               (SENSE16,64)             SENSE AREA 16
         DSGEN (BTSSHTS,16),            BTSS SHEETS                    X
               (CFSFOLDS,16),           CFS FOLDS                      X
               (PAPERPRS,16),           PAPER PROCESSED                X
               (SERIALNO,16),           SERIAL NUMBER          @Y17CSWTX
               (RECORDWK,72)            RECORD WORK AREA
         ORG   RECORDWK
         DSGEN (ERRCODE,8)              SENSE BYTE 4
         DSGEN (SNS05,1,0,7),           SENSE BYTE 5                   X
               (SNS06,1,0,7),           SENSE BYTE 6                   X
               (SNS07,1,0,7),           SENSE BYTE 7                   X
               (SNS08,1,0,7),           SENSE BYTE 8                   X
               (SNS09,1,0,7),           SENSE BYTE 9                   X
               (SNS10,1,0,7),           SENSE BYTE 10                  X
               (SNS11,1,0,7),           SENSE BYTE 11                  X
               (SENSECNT,8)             COUNT OF SENSE AREAS
         DSGEN (SNS05,8,EQU,SNS050),                                   X
               (SNS06,8,EQU,SNS060),                                   X
               (SNS07,8,EQU,SNS070),                                   X
               (SNS08,8,EQU,SNS080),                                   X
               (SNS09,8,EQU,SNS090),                                   X
               (SNS10,8,EQU,SNS100),                                   X
               (SNS11,8,EQU,SNS110)
IFCET00C CSECT
         USING *,R9                     SET UP BASE REG
         USING RECORD,R2                SET UP DSECT PTR
         DC    A(ENTRY)                 LINE/LABEL SET ADDR
         DC    CL8'IFCET00C'            MODULE NAME
R9PTR    DC    F'0'
LABELPTR DC    F'0'
         DS    0F
CNTSAVE  DC    F'0'                     ENTRY COUNT SAVE AREA
ENTRYNO  DC    F'0'                     COUNTER SAVE
WORK     DC    D'0'                     WORK SPACE
ENTRYCNT DC    F'0'
REGSAVE  DC    4F'0'
LOOP21   DC    A(LOOP2-4)               LINE
ENTRYLB2 DC    A(ENTRYLB-1)             LABEL
LINEXIT1 DC    A(LINEXIT-4)             LINE FOR EXIT
LBLEXIT1 DC    A(LBLEXIT-1)             LABEL FOR EXIT
FMT5ALN1 DC    A(FMT5ALN-4)             LINE FOR FORMAT 5A
FMT6ALN1 DC    A(FMT6ALN-4)             LINE FOR FORMAT 6A
FMT3ALN1 DC    A(FMT3ALN-4)             LINE FOR FORMAT 3A
FMT7ALN1 DC    A(FMT7ALN-4)             LINE FOR FORMAT 7A
FMT5ALB1 DC    A(FMT5ALB-1)             LABEL FOR FORMAT 5A
FMT6ALB1 DC    A(FMT6ALB-1)             LABEL FOR FORMAT 6A
FMT3ALB1 DC    A(FMT3ALB-1)             LABEL FOR FORMAT 3A
FMT7ALB1 DC    A(FMT7ALB-1)             LABEL FOR FORMAT 7A
GETOUTLN DC    A(EXITLN-4)              TO MDR MODULE
GETOUTLB DC    A(EXITLB-1)              TO MDR MODULE
DEVTYPE  EQU   SWITCH2
SUPPORT  EQU   1                        COUNT OF XDEVTBL ENTRIES
XDEVTBL  EQU   *
         DC    X'0C',C'3800'       ARGONAUT CLASS/TYPE CVT
RELTAB   EQU   *
         DC    CL4'  OS'                SYSTEM/RELEASE CONV TBL
         DC    CL4' DOS'
         DC    CL4' VS1'
         DC    CL4'  VM'
         DC    CL4' VS2'
         DC   3CL4'    '
R0       EQU   0
R1       EQU   1
R2       EQU   2                        DSECT PTR
R3       EQU   3                        PRINT BUFFR PTR
R4       EQU   4                        RECORD POINTER
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9                        MODULE BASE REG
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
L0       EQU   0                        ZERO
L1       EQU   1                        ONE
L2       EQU   2                        TWO
L3       EQU   3                        THREE
L4       EQU   4                        FOUR
L5       EQU   5                        FIVE
L6       EQU   6                        SIX
L7       EQU   7                        SEVEN
L8       EQU   8                        EIGHT
L9       EQU   9
L23      EQU   23                       TWENTY THREE
L46      EQU   46                       FOURTY SIX
ZERO     EQU   0
HW01     DC    H'1'
EIGHT    DC    H'8'                     EIGHT
HW16     DC    H'16'
NONE     DC    C'NONE  '
UNIV     DC    C'UNIVERSAL'
F3       DC    C'FORMAT 3A'
F5       DC    C'FORMAT 5A'
F6       DC    C'FORMAT 6A'
F7       DC    C'FORMAT 7A'
X80      EQU   X'80'
X81      EQU   X'81'
X89      EQU   X'89'
X8A      EQU   X'8A'
X8B      EQU   X'8B'
X8C      EQU   X'8C'
X90      EQU   X'90'
X9F      EQU   X'9F'
X50      EQU   X'50'
ENTRYNO1 EQU   ENTRYNO+2
         EJECT
         DS    0F
COUNTEM  EQU   *
         STM   R5,R8,REGSAVE            SAVE REGS
         XC    ENTRYNO(4),ENTRYNO       INITIALIZE ENTRY NO.
         XC    SENSECNT(L8),SENSECNT    INTLZE SENSE COUNT AREA
         LR    R6,R4                    GET RECORD POINTER
         SR    R5,R5                    ZERO R5
COUNTEM1 EQU   *
         OC    L0(L8,R6),L0(R6)         ANY SENSE INFO?
         BZ    RETURN                   NO, BRANCH
         LA    R5,L1(R5)                YES, BUMP COUNTER
         STC   R5,SENSECNT              STORE COUNTER
         CH    R5,HW16                  REACHED MAX ?
         BE    RETURN                   YES, BRANCH
         LA    R6,L8(R6)                NO, BUMP POINTER
         B     COUNTEM1                 AND GO BACK TO TOP
SETCPUNO EQU   *
         STM   R5,R8,REGSAVE            SAVE REGS
         SR    R5,R5                    CLEAR R5
         IC    R5,L0(R4)                GET CPU TYPE
         SLL   R5,L8
         IC    R5,L1(R4)
         LTR   R5,R5                    IS IT ZERO ?
         BNZ   RETURN                   NO, RETURN
         LA    R6,L46                   BACK UP POINTER 46 BYTES
         SR    R3,R6
         MVC   L0(L9,R3),UNIV           SAY MODEL=UNIVERSAL
         MVC   L23(L6,R3),NONE          SAY SERIAL=NONE
         B     RETURN
NEXT     EQU   *
         STM   R5,R8,REGSAVE            SAVE REGS
         L     R5,LOOP21                SET UP POINTERS
         ST    R5,R9PTR                  V
         L     R5,ENTRYLB2               V
         ST    R5,LABELPTR               V
         B     RETURN                   RETURN
SKIP     SR    R15,R15                  SUPRESS PRINT OF LINE
RETURN   LM    R5,R8,REGSAVE            RESTORE REGS
SKIPIT   BR    R14                      RETURN
TOPOLOOP EQU   *
         STM   R5,R8,REGSAVE            SAVE REGS
         L     R6,ENTRYNO               GET ENTRY NO.
         LA    R6,L1(R6)                INCREMENT BY 1
         ST    R6,ENTRYNO               SAVE ENTRY NO.
         CH    R6,HW16                  FINISHED W/SENSE INFO ?
         BH    EXIT                     YES, BRANCH
         MH    R6,EIGHT                 CALC OFFSET(ENTRYNO X 8)
         SH    R6,EIGHT                 AND BACK-UP EIGHT
         AR    R6,R4                    ADD RECORD POINTER TO IT
         MVC   RECORDWK(L8),L0(R6)      MOVE SENSE INFO TO WORK
         OC    RECORDWK(L8),RECORDWK    ANY SENSE INFO ?
         BZ    EXIT                     NO, BRANCH
         L     R6,ENTRYNO               GET ENTRY NO.
         CVD   R6,WORK                  CONVERT ENTRYNO TO DEC
         UNPK  WORK(L4),WORK+6(L2)      UNPACK IT
         MVZ   WORK+3(L1),WORK+2         FIX ZONE BITS
         MVC   ENTRYLB1(L2),WORK+2      MOVE TO PRINT LINE
         B     RETURN
EXIT     EQU   *
         L     R5,LINEXIT1              NO, SET UP POINTERS
         ST    R5,R9PTR                  V
         L     R5,LBLEXIT1                   V
         ST    R5,LABELPTR               V
         B     RETURN                   BRANCH
TYPE1    EQU   *
         SR    R15,R15                  INDICATE NO PRINT
         STM   R5,R8,REGSAVE            SAVE REGISTERS
         TM    L0(R2),X90               RECORD TYPE = MDR ?
         BO    RETURN                   YES, BRANCH
*                                       NO
         L     R5,GETOUTLN              SET UP POINTERS
         ST    R5,R9PTR                      V
         L     R5,GETOUTLB               V
         ST    R5,LABELPTR                   V
         B     RETURN
TYPELBL  EQU   *
         STM   R5,R8,REGSAVE            SAVE REGS
         L     R6,ENTRYNO               ENTRY NUMBER IN R6
         MH    R6,EIGHT                 MULT ENTRY NO BY 8
         AR    R6,R4                    ADD DSECT PTR TO R6
         SH    R6,EIGHT                 R6 NOW POINTS TO CURRENT SNS
         CLI   L0(R6),X81               FMT 5 REQD ?
         BL    TLBL2                    NO, GO TO NEXT CHECK
         CLI   L0(R6),X8A               FMT 6A REQD ?
         BE    MVCLBL6                  YES, BRANCH
         CLI   L0(R6),X89               FMT 5A REQD ?
         BNH   MVCLBL5                  YES, BRANCH
TLBL2    EQU   *
         CLI   L0(R6),X50               FMT 3A REQD ?
         BL    MVCLBL7                  NO, BRANCH
         CLI   L0(R6),X9F               FMT 3A REQD ?
         BH    MVCLBL7                  NO, BRANCH
         MVC   ERRLBL+33(L8),F3         YES, MOVE IN FORMAT 3
         B     RETURN                   RETURN
MVCLBL6  EQU   *
         MVC   ERRLBL+33(L8),F6         MOVE IN FORMAT 6
         B     RETURN                   RETURN
MVCLBL5  EQU   *
         MVC   ERRLBL+33(L8),F5         MOVE IN FORMAT 5
         B     RETURN                   RETURN
MVCLBL7  EQU   *
         MVC   ERRLBL+33(L8),F7         MOVE IN UNKNOWN(FORMAT 7A)
         B     RETURN                   RETURN
*        THE FOLLOWING CHECKS ARE MADE TO DETERMINE THE FORMATS
*          TO USE WHEN PRINTING MDR RECORDS. THE FOLLOWING LIST
*          GIVES THE FORMAT USED FOR SPECIFIC ERROR CODES:
*
*              FORMAT    ERROR CODES
*
*                3A        5X-7F WHERE X = 0-F
*                3A        8B
*                3A        9X
*                5A        81-89
*                6A        8A
********
TYPE     EQU   *
         STM   R5,R8,REGSAVE            SAVE REGISTERS
         L     R6,ENTRYNO               ENTRY NUM IN R6
         MH    R6,EIGHT                 MULT ENTRY NUM BY 8
         AR    R6,R4                    ADD DSECT PTR TO R6
         SH    R6,EIGHT                 R6 NOW POINTS TO CURRENT SNS
         CLI   L0(R6),X81               FMT 5A REQD ?
         BL    TYPE2                    NO, BRANCH TO NEXT CHECK
         CLI   L0(R6),X8A               FMT 6A REQD ?
         BE    FMT6A                    YES, BRANCH TO SET-UP
         CLI   L0(R6),X89               FMT 5A REQD ?
         BNH   FMT5A                    YES, BRANCH TO SET-UP
TYPE2    EQU   *
         CLI   L0(R6),X50               FMT 3A REQD ?
         BL    FMT7A                    NO, UNKNOWN ERR CODE, BR
         CLI   L0(R6),X9F               FMT 3A REQD ?
         BH    FMT7A                    NO, UNKNOWN ERR CODE, BR
*                                       YES, DROP THRU & GO
FMT3A    EQU   *
         L     R5,FMT3ALN1              SET UP POINTERS
         ST    R5,R9PTR                   V
         L     R5,FMT3ALB1               V
         ST    R5,LABELPTR                V
         B     RETURN                    RETURN
FMT5A    EQU   *                        ASSUME FORMAT 5A REQD
         L     R5,FMT5ALN1              SET UP POINTERS
         ST    R5,R9PTR                   V
         L     R5,FMT5ALB1               V
         ST    R5,LABELPTR                V
         B     RETURN                    RETURN
FMT6A    EQU   *
         L     R5,FMT6ALN1              SET UP POINTERS
         ST    R5,R9PTR                   V
         L     R5,FMT6ALB1               V
         ST    R5,LABELPTR                V
         B     RETURN                    RETURN
FMT7A    EQU   *
         L     R5,FMT7ALN1              SET UP POINTERS
         ST    R5,R9PTR                  V
         L     R5,FMT7ALB1               V
         ST    R5,LABELPTR               V
         B     RETURN                   RETURN
CONVTYPE EQU   *
         STM   R5,R8,REGSAVE            SAVE REGS
         LA    R5,SUPPORT               BCT COUNT, CONV DEV TYPE TBL
*        LA    R4,L2(R4)                R4 POINTS TO CLASS/TYPE 2 BYTES
         LA    R6,XDEVTBL               ADDR OF CLASS/TYPE XLATE TABLE
NEXT2     EQU   *
         CLC   L0(L1,R4),L0(R6)         IS THIS CLASS/TYPE WE LOOKIN FO
         BNE   BUMP                     NO, BRANCH
         MVC   L0(L4,R3),L1(R6)         YES, MOVE CLASS/TYPE TO BUFF
         B     RETURN                   RETURN
BUMP     DS    0H
         LA    R6,L5(R6)                BUMP CONVERT TBL PTR
         BCT   R5,NEXT2                KEEP LOOKING
         MVC   L0(L7,R3),F7
         B     RETURN                  RETURN FROM WENCE YOU CAME
CONVREL  EQU   *
         STM   R5,R8,REGSAVE            SAVE REGS
         SR    R6,R6                    ZERO R6
         IC    R6,L0(R4)                GET REL & SYS VALUES
         SRDL  R6,L5                    PUT REL NO. INTO R7
         SLL   R6,L2                    MULT SYS VALUE BY 4
         L     R8,LABELPTR              GET LABEL PTR
         LA    R6,RELTAB(R6)            INDEX TO SYS TBL
         MVC   L0(L4,R8),L0(R6)         MOVE SYS NAME TO LABEL
         SR    R6,R6                    ZERO R6
         SLDL  R6,L5                    RETRIEVE REL NO.
         LTR   R6,R6                    IS IT ZERO ?
         BZ    NOREL                    YES, BRANCH
         CVD   R6,WORK                  NO, CONVERT IT
         UNPK  L0(L2,R3),WORK+L6(R2)    UNPACK IT INTO PRT BUFF
         MVZ   L1(L1,R3),L0(R3)         FIX THE ZONE
         B     RETURN                   RETURN
NOREL    EQU   *
         MVC   L0(L4,R3),NONE           MOVE 'NONE' TO REL MSG
         B     RETURN                   RETURN
DATESET  EQU   *
         UNPK  WORK(L8),L1(L3,R4)       UNPACK THE DATE
         MVC   L0(L3,R3),WORK+L5        MOVE DAY
         MVC   L5(L2,R3),WORK+L3        MOVE YEAR
         B     RETURN                   RETURN
         EJECT
         DS    0F
ENTRY    EQU   *
         DC    XL4'0'
         DC    XL4'0'
         DC    A(LABELS)
         DC    H'0'
         LINE  (1,1),(1,1),SKIP=1
         ROUTINE SPARE1,TYPE1
         LABEL ''
         LINE  SKIP=E                   EJECT
         LABEL ''
         LINE  (78,23),(1,89),SKIP=1
         HEX   CPUID,0,2B
         HEX   CPUSER,0,3B
         ROUTINE CPUID,SETCPUNO
         LABEL '---RECORD ENTRY TYPE - MDR              SOURCE - MISC  X
                         CPU MODEL','SERIAL NO.'
         LINE  (17,1),(4,1),SKIP=1
         ROUTINE SYSREL,CONVREL
         LABEL 'XXXX RELEASE'
         LINE  ,(25,23),SKIP=1
         LABEL 'DAY YEAR','HH.MM.SS.TH'
         LINE  (25,23),(18,23),SKIP=2
         ROUTINE DATE,DATESET
         HEX   TIME,1,4B
         LABEL 'DATE -','TIME -'
         LINE  (24),(2),SKIP=1
         ROUTINE DEVTYPE,CONVTYPE
         LABEL 'DEVICE TYPE'
         LINE  (24),(2),SKIP=2
         HEX   DEVCUA,0,2B
         LABEL 'CHANNEL UNIT ADDRESS'
         LINE  (45,1),(1),SKIP=1
         ROUTINE SENSE01,COUNTEM
         CONVT SENSECNT,1,2
         LABEL 'NUMBER OF ENTRIES IN THIS RECORD (DECIMAL) -'
         LINE  (45,1),(1),SKIP=2
         LABEL 'INTERNAL LOG ENTRY BYTES 0-7 ARE SAME AS SENSE BYTES 4-X
               11'
LOOP2    LINE  (11),(1),SKIP=1
         ROUTINE SENSE01,TOPOLOOP
ENTRYLB  LABEL 'ENTRY NO.    '
         LINE  ,(1),SKIP=1
         LABEL '---------------------------------------'
         LINE  (31),(1,40),SKIP=1
         HEX   ERRCODE,0,1B
         ROUTINE SENSE01,TYPELBL
ERRLBL   LABEL ' STATUS CODE (SENSE BYTE 4) -',' XXXXXXXX'
         LINE  ,(1),SKIP=2
         LABEL '---------------------------------------'
         LINE  ,(3,17),SKIP=1
         LABEL 'SENSE','SENSE','SENSE','SENSE','SENSE','SENSE','SENSE'
         LINE  (12,17),(3,17),SKIP=0
         HEX   SNS05,0,1B
         HEX   SNS06,0,1B
         HEX   SNS07,0,1B
         HEX   SNS08,0,1B
         HEX   SNS09,0,1B
         HEX   SNS10,0,1B
         HEX   SNS11,0,1B
         LABEL 'BYTE 5 -','BYTE 6 -','BYTE 7 -','BYTE 8 -','BYTE 9 -','X
               BYTE 10 -','BYTE 11 -'
         LINE  (14,17),(1),SKIP=1
         ROUTINE SENSE01,TYPE
         LABEL ''
FMT3ALN  LINE  ,(2,17),SKIP=2
FMT3ALB  LABEL 'RETRY COUNT','FO STAT(6-3)','CORNA STAT(6-8)','PRCS STAX
               T(6-9)','CGEN STAT1(4-0)','CGEN STAT2(4-1)','CGEN STAT3(X
               4-2)'
         LINE  (14,17),(1,17),SKIP=1
         BIN   (SNS050,0,1),                                           X
               (SNS060,0,1),                                           X
               (SNS070,0,1),                                           X
               (SNS080,0,1),                                           X
               (SNS090,0,1),                                           X
               (SNS100,0,1),                                           X
               (SNS110,0,1)
         LABEL 'RETRY CT 8','FOF OCCURRED','ERAS LMP OFF',             X
               'CONTR BIT 4','DRUM/MIRR TH','SSYS CLK ER','UPGM AHEAD'
         LINE
         BIN   (SNS051,0,1),                                           X
               (SNS061,0,1),                                           X
               (SNS071,0,1),                                           X
               (SNS081,0,1),                                           X
               (SNS091,0,1),                                           X
               (SNS101,0,1),                                           X
               (SNS111,0,1)
         LABEL 'RETRY CT 4','FO INT HI','CLNR NOT RUN','CONTR BIT 2',  X
               'MIRR MTR TH','BIT TIME CLK','PRT LIN SYNC'
         LINE
         BIN   (SNS052,0,1),                                           X
               (SNS062,0,1),                                           X
               (SNS072,0,1),                                           X
               (SNS082,0,1),                                           X
               (SNS092,0,1),                                           X
               (SNS102,0,1),                                           X
               (SNS112,0,1)
         LABEL 'RETRY CT 2','FO INT LO','PRECLN COR','CONTR BIT 1',    X
               'MIRR DRV AMP','WCGM INP PE','LIN BUF INTF'
         LINE
         BIN   (SNS053,0,1),                                           X
               (SNS063,0,1),                                           X
               (SNS073,0,1),                                           X
               (SNS083,0,1),                                           X
               (SNS093,0,1),                                           X
               (SNS103,0,1),                                           X
               (SNS113,0,1)
         LABEL 'RETRY CT 1','FO HLD INTLK','CHG COR CK','DVLPR INTLK', X
               'MIRR CURR HI','LIN BUF PE','LIN BUF ORUN'
         LINE
         BIN   (SNS054,0,1),                                           X
               (SNS064,0,1),                                           X
               (SNS074,0,1),                                           X
               (SNS084,0,1),                                           X
               (SNS094,0,1),                                           X
               (SNS104,0,1),                                           X
               (SNS114,0,1)
         LABEL 'RESERVED','RESERVED','XFR COR CK','TNR HPR EMPT',      X
               'MIRR SYNC','SERIALZR PE','UNDSCORE PE'
         LINE
         BIN   (SNS055,0,1),                                           X
               (SNS065,0,1),                                           X
               (SNS075,0,1),                                           X
               (SNS085,0,1),                                           X
               (SNS095,0,1),                                           X
               (SNS105,0,1),                                           X
               (SNS115,0,1)
         LABEL 'RESERVED','RESERVED','POSXFR COR ','TNR COL FULL','LASRX
                SYNC','ANY WCGM PE','UNDSCORE BIT'
         LINE
         BIN   (SNS056,0,1),                                           X
               (SNS066,0,1),                                           X
               (SNS076,0,1),                                           X
               (SNS086,0,1),                                           X
               (SNS096,0,1),                                           X
               (SNS106,0,1),                                           X
               (SNS116,0,1)
         LABEL 'RESERVED','RESERVED','DRUM SVO ER','CYCLONE VAC','LASR X
               LOW PWR','CAR BIT 0','BIT CTR OFLO'
         LINE
         BIN   (SNS057,0,1),                                           X
               (SNS067,0,1),                                           X
               (SNS077,0,1),                                           X
               (SNS087,0,1),                                           X
               (SNS097,0,1),                                           X
               (SNS107,0,1),                                           X
               (SNS117,0,1)
         LABEL 'RESERVED','RESERVED','DVLPR BIAS','COL HANDL CK',      X
               'RESERVED','CAR BIT 1','PRT/HOR CT=0'
******                                  FORMAT 3A COMPLETE ******
FMT3AEND LINE  (11),(1),SKIP=1
         ROUTINE SPARE1,NEXT
         LABEL ''
FMT5ALN  LINE  (14,17),(2,17),SKIP=2
FMT5ALB  LABEL 'INTRPT REQ','MAR HI','MAR LO','IEU REG 1(0-2)','IEU REGX
                2(0-3)','INST/SOREG HI','INST/SOREG LO'
         LINE  (14,17),(1,17),SKIP=1
         BIN   (SNS050,0,1)
         ROUTINE SPARE1,SKIPIT
         ROUTINE SPARE1,SKIPIT
         BIN   (SNS080,0,1),                                           X
               (SNS090,0,1)
         LABEL 'SO REG',' ',' ','MIRH PE','SAR PE'
         LINE
         BIN   (SNS051,0,1)
         ROUTINE SPARE1,SKIPIT
         ROUTINE SPARE1,SKIPIT
         BIN   (SNS081,0,1),                                           X
               (SNS091,0,1)
         LABEL 'LEVEL 1',' ',' ','MIRL PE','LSR PE'
         LINE
         BIN   (SNS052,0,1)
         ROUTINE SPARE1,SKIPIT
         ROUTINE SPARE1,SKIPIT
         BIN   (SNS082,0,1),                                           X
               (SNS092,0,1)
         LABEL 'LEVEL 2',' ',' ','MDR PE','INBUS PE'
               LINE
         BIN   (SNS053,0,1)
         ROUTINE SPARE1,SKIPIT
         ROUTINE SPARE1,SKIPIT
         BIN   (SNS083,0,1),                                           X
               (SNS093,0,1)
         LABEL 'LEVEL 3',' ',' ','WCS DBL BIT','NO DEV RSP'
         LINE
         BIN   (SNS054,0,1)
         ROUTINE SPARE1,SKIPIT
         ROUTINE SPARE1,SKIPIT
         BIN   (SNS084,0,1),                                           X
               (SNS094,0,1)
         LABEL 'LEVEL 4',' ',' ','ALUA PE','I-CYCLE'
         LINE
         BIN   (SNS055,0,1)
         ROUTINE SPARE1,SKIPIT
         ROUTINE SPARE1,SKIPIT
         BIN   (SNS085,0,1),                                           X
               (SNS095,0,1)
         LABEL 'LEVEL 5',' ',' ','ALUB PE','E-CYCLE'
         LINE
         BIN   (SNS056,0,1)
         ROUTINE SPARE1,SKIPIT
         ROUTINE SPARE1,SKIPIT
         BIN   (SNS086,0,1),                                           X
               (SNS096,0,1)
         LABEL 'LEVEL 6',' ',' ','SDRH PE','ROS HSL'
         LINE
         BIN   (SNS057,0,1)
         ROUTINE SPARE1,SKIPIT
         ROUTINE SPARE1,SKIPIT
         BIN   (SNS087,0,1),                                           X
               (SNS097,0,1)
         LABEL 'RESERVED',' ',' ','SDRL PE','ANY ERROR'
******                                  FORMAT 5A COMPLETE ******
FMT5AEND LINE
         ROUTINE SPARE1,NEXT
         LABEL ''
FMT6ALN  LINE  ,(2,17),SKIP=2
FMT6ALB  LABEL 'RETRY COUNT','CNTRL FLAGS','TRACK ADDR','RESERVED','RESX
               ERVED','RESERVED','RESERVED'
         LINE  (31),(18),SKIP=1
         BIN   (SNS070,0,1)
         LABEL 'INTF RTN RQD'
         LINE
         BIN   (SNS071,0,1)
         LABEL 'RESERVED'
         LINE
         BIN   (SNS072,0,1)
         LABEL 'ALT TRK USED'
         LINE
         BIN   (SNS073,0,1)
         LABEL 'PERMANENT'
         LINE
         BIN   (SNS074,0,1)
         LABEL 'NO ALT AVAIL'
         LINE
         BIN   (SNS075,0,1)
         LABEL 'RETAIN PWR'
         LINE
         BIN   (SNS076,0,1)
         LABEL 'DIAG LD MODE'
         LINE
         BIN   (SNS077,0,1)
         LABEL 'IMPL MODE'
******                                  FORMAT 6A COMPLETE ******
FMT6AEND LINE  (11),(1),SKIP=1
         ROUTINE SPARE1,NEXT
         LABEL ''
FMT7ALN  LINE  (14,17),(2,17),SKIP=2
FMT7ALB  LABEL 'RESERVED','RESERVED','RESERVED','RESERVED','RESERVED','X
               RESERVED','RESERVED'
FMT7AEND LINE  (11),(1),SKIP=1
         ROUTINE SPARE1,NEXT
         LABEL ''
LINEXIT  LINE  (40,1),(1,45),SKIP=1
         CONVT BTSSHTS,2,6
LBLEXIT  LABEL 'BTS COUNT                    (DECIMAL)','00'
         LINE  (40,1),(1,45),SKIP=1
         CONVT CFSFOLDS,2,6
         LABEL 'CFS COUNT                    (DECIMAL)','00'
         LINE  (40,1),(1,45),SKIP=2
         CONVT PAPERPRS,2,6
         LABEL 'PAPER COUNT                  (DECIMAL)','00'
         LINE  (40,1),(1,1),SKIP=3
         CONVT SERIALNO,2,4
         LABEL 'DEVICE SERIAL NO.'
         LINE  SKIP=1
         LABEL ''
         DC    X'FF'                    BAIL OUT
         DC    X'00A4'
         DC    X'0018'                  OFFSET IN THE RECORD
         DC    X'0018'
EXITLN   LINE
EXITLB   LABEL ''
         DC    X'FE'
         DC    C'IFCEXXXF'
LABELS   DS    0F
         LSTART
ENTRYLB1 EQU   ENTRYLB+10               LABEL FOR MOVE (MVC)
         END
