         TITLE 'IGC0308B --- ALTERNATE TRACK ASSIGNMENT FOR 2305'
         COPY  LCGASMSW                                          SM4351
IGC0308B CSECT
*
*C 228000                           @SA74452=@YA09634=@XA09732=@ZA04363
*A 226500-227000,708500             @SA74452=@YA09634=@XA09732=@ZA04363
*
* STATUS - CHANGE LEVEL 000                                           *
*                                                                     *
* FUNCTION                                                            *
*     THIS LOAD OF SVC 82 PERFORMS ALTERNATE TRACK ASSIGNMENT FOR     *
*     EITHER OF TWO CASES ON A 2305 DEVICE                            *
*                                                                     *
*       1)VOLUMES IN THE INITIALIZATION PROCESS.                      *
*       2)PRE-INITIALIZED VOLUMES(THEY HAVE A FORMAT 4 DSCB).         *
*                                                                     *
*     IF THE SPECIFIED DEFECTIVE TRACK IS A PRIMARY TRACK, THE        *
*     DYNAMIC ALTERNATE IS ASSIGNED TO THE ORIGINAL PRIMARY TRACK.    *
*                                                                     *
*     IF THE SPECIFIED DEFECTIVE TRACK IS THE UNASSIGNED DYNAMIC      *
*     ALTERNATE, NO TRACK ASSIGNMENTS ARE MADE AND A RETURN CODE      *
*     OF 16 IS PASSED TO CALLING ROUTINE.                             *
*                                                                     *
* ENTRY POINT - IGC0308B IS THE ONLY ENTRY POINT.                     *
*                                                                     *
* INPUT - REGISTER 1 POINTS TO 3-FULL WORDS AS FOLLOWS:               *
*         WORD1 - INPUT PARM LIST POINTER PASSED TO IGC0008B.         *
*         WORD2 - WORK AREA ADDRESS(DESCRIBED BY IOBLOCKS DESECT).    *
*         WORD3 - POINTER TO SVRB EXTENDED SAVE AREA.                 *
*                                                                     *
* EXITS                                                               *
*     NORMAL - THE SECOND WORD OF THE INPUT PARM LIST CONTAINS THE    *
*              CCHH OF THE ASSIGNED ALTERNATE TRACK, IF ANY.  IF NO   *
*              ALTERNATE WAS ASSIGNED, THE SECOND WORD CONTAINS ZEROES*
*              WITH A RETURN CODE OF ZERO(IN EITHER CASE).            *
*              SIX BYTES OF THE ALTERNATE TRACK INFORMATION ARE       *
*              UPDATED(EITHER IN FORMAT 4 DSCB OR POINTED TO BY THE   *
*              PARM LIST).                                            *
*                                                                     *
*     ERROR - RETURN CODE OF 12 IF I/O ERROR WAS ENCOUNTERED.         *
*             RETURN COE OF 16 IF DYNMAIC ALTERNATE HAS BEEN          *
*             USED OR IS DEFECTED.                                    *
*                                                                     *
* SUPERVISOR MACROS - EXCP,WAIT,FREEMAIN.                             *
*                                                                     *
* WORKAREA/TABLES - USES THE WORK AREA DESCRIBED BY DSECT IOBLOCKS.   *
*                                                                     *
* OUTPUT - SEE EXITS DESCRIPTION.                                     *
*                                                                     *
* ATTRIBUTES - REENTRANT, RELOCATABLE, PRIVILEGED,ENABLED.            *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*     REGISTER ASSIGNMENTS.                                           *
*                                                                     *
***********************************************************************
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   7
R7       EQU   8
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R14      EQU   14
R15     EQU   15
PARMPTR  EQU   1
BASEREG  EQU   12
         SPACE 1
***********************************************************************
*                                                                     *
*     EQUATE DEFINITIONS.                                             *
*                                                                     *
***********************************************************************
         SPACE 1
K0       EQU   0                       DISPLACEMENT CONSTANT.
K1       EQU   1                       DISPLACEMENT CONSTANT.
K2       EQU   2                       DISPLACEMENT CONSTANT.
K4       EQU   4                       DISPLACEMENT CONSTANT.
K5       EQU   5                       DISPLACEMENT
K6       EQU   6
K7       EQU   7
K8       EQU   8
K12      EQU   12
K16      EQU   16
K36      EQU   36                      DISPLACEMENT CONSTANT.   YL02919
K44      EQU   44
ONE      EQU   1
CODERO   EQU   X'16'                   READ R0  OP CODE
FLAGS    EQU   X'42'                   FLAGS FOR IOBLOCK.
NULL     EQU   X'00'                   CODE FOR ANALYZE/FORMAT FUNCTION
OPCODE   EQU   X'06'                   READ DATA OP CODE.
SOPCODE  EQU   X'31'                   SEARCH ID EQU OP CODE.
ROPCODE  EQU   X'1A'                   READ HA OP CODE.
WOPCODE  EQU   X'15'                   WRITE RECORD R0 OP CODE.
WRTCD    EQU   X'05'                   WRITE DATA OP CODE.
RDCD     EQU   X'19'                   WRITE HOME ADDRESS OP CODE.
RBEXSAVE EQU   96                      EXTENTED SAVE AREA OF SVRB.
LAST     EQU   X'80'                   LAST PARAMETER INDICATOR.
GOOD     EQU   X'7F'                   I/O GOOD COMPLETION BIT.
COMPLETE EQU   X'40'                   I/O COMPLETION BIT.
SKIP     EQU   X'10'                   SKIP DATA TRANSFER.
CHAIN    EQU   X'40'                   COMMAND CHAIN CCWS.
SILI     EQU   X'20'                   SUPPRESS INCORRECT LENGTH.
ALT      EQU   X'01'                   ALTERNATE TRACK FLAG.
DEFECT   EQU   X'02'                   DEFECTIVE TRACK FLAG.
MOVEHA   EQU    X'40'                   MOVE HA DOWN THE TRACK.
UE       EQU   X'01'                   UNIT EXCEPTION.
XFF      EQU   X'FF'                   HEX FF
FMT4LNTH EQU   96                      FORMAT 4 DATA LENGTH.     YM4604
         EJECT
         LR    BASEREG,R15             ESTABLISH ADDRESSING.
         USING IGC0308B,BASEREG
         USING PARMLIST,R11                                     YL02912
         SPACE
         B     APARNO                  BRANCH AROUND APAR NO   @ZA04363
         DC    C'IGC0308B OZ04363'     LAST FIX THIS MOD       @ZA04363
APARNO   L     R11,K0(R1)              PARM LIST POINTER.      @ZA04363
         L     R2,K4(R1)               WORKAREA ADDRESS.
         L     R5,K8(R1)               SVRB EXTENDED AREA.
         SPACE 2
***********************************************************************
*                                                                     *
*   INITIALIZE THE IOB.                                               *
*                                                                     *
***********************************************************************
         SPACE 2
         USING IOBLOCKS,R2
INITIOB  EQU   *
         MVI   IOBFLAG1,FLAGS          SET PROPER FLAGS.
         LA    R3,ECB                  ECB ADDRESS.
         ST    R3,IOBECBAD             STORE IN IOB.
         MVI   IOBINCAM+ONE,ONE        BLOCK COUNT.
         LA    R3,DCB-K44              DCB ADDRESS.
         ST    R3,IOBDCBPT             STORE IN IOB.
FUNCTEST EQU   *
         CLI   FUNCTION,NULL            ANALYZE/FORMAT REQUEST? YL02912
         BE    ASSGN                   YES--GO HANDLE.
         SPACE 1
*   BUILD CCWS FOR READING FORMAT 4 DSCB HERE.
         USING IOBLOCKS,R2
FORM4    EQU   *
         LA    R3,FORMAT4              FORMAT 4 READ-IN AREA.
         ST    R3,WRTRD                STORE IN READ-DATA CCW.
         MVI   WRTRD,OPCODE             SET OP CODE.
         MVI   WRTRD+K7,FMT4LNTH       SET COUNT.                YM4604
         LA    R3,SEARCH               SEARCH CCW ADDRESS.
         ST    R3,TIC                  STORE IN TIC.
         MVI   TIC,K8                  SET TIC OP CODE.
         LA    R3,CCHHR                SEARCH ADDRESS.
         ST    R3,SEARCH               STORE IN SEARCH CCW.
         MVI   SEARCH+K4,COMPLETE      COMMAND CHAIN ON.
         MVI   SEARCH+K7,K5            SET COUNT.
         MVI   SEARCH,SOPCODE          SET SEARCH OP CODE.
         LA    R3,SEARCH               SEARCH CCW ADDRESS.
         ST    R3,IOBSTART             SET IN IOB.
         SPACE
*   STORE CCHHR OF VTOC IN IOB HERE.
         L     R3,UCBPTR                GET UCB ADDRESS.        YL02912
         USING UCB,R3
         L     R0,SRTEFSCT             TTR OF VTOC.
         SPACE
FINDCCHH EQU   *
         LA    R3,RBEXSAVE(R5)         EXTENTED SVRB SAVE AREA.
         L     R10,K36(R3)             SAVE RETURN ADDRESS.     YL02912
         MODESET   EXTKEY=SUPR                                  YL02912
         STM   R1,BASEREG,K0(R3)       SAVE THE REGISTERS.
         MODESET   EXTKEY=DATAMGT                               YL02912
         USING CVT,R15
         L     R15,CVTPTR              CVT ADDRESS.
         L     R15,CVTPCNVT            CONVERT ROUTINE ADDRESS.
         DROP  R15
         LA    R1,DEB                  ADDDRESS OF DEB.
         LA    R2,IOBSEEK              TARGET AREA FOR MBBCCHHR.
         BALR  R14,R15                 CONVERT TTR TO MBBCCHHR.
         LM    R1,BASEREG,K0(R3)       RESTORE THE REGISTERS.
         SPACE
*   READ IN THE FORMAT 4 DSCB HERE.                                   *
         SPACE
         BAL   R9,WRTREAD              READ IN FORMAT4 DSCB DATA FIELD.
         BAL   R9,WAIT                 AWAIT COMPLETION.
         MVC   ALTINFO(K6),ALTDATA     CCHH NEXT ALT. AND COUNT LEFT.
         SPACE
ASSG1    EQU   *
         CLC   ALTINFO+K4(K2),ZERO     ANY ALTERNATES LEFT.
         BNE   WRTPRIM                 YES-
EXIT16   EQU   *
         BAL   R9,FREECORE             FREE THE WORK AREA.
         LA    R15,K16                 SET RETURN CODE.
         B     RETURN                  RETURN TO CALLER.        YL02912
         EJECT
***********************************************************************
*                                                                     *
*   WRITE HOME ADDRESS/RECORD ZERO ON THE PRIMARY TRACK HERE.         *
*                                                                     *
***********************************************************************
WRTPRIM  EQU   *
         L     R3,CCHHPARM              CCHH OF BAD PRIMARY.    YL02912
         ST    R3,R0COUNT              TEMPORARY STORAGE
         MVC   CCHHR(K4),R0COUNT       PRIMARY CCHH TO IOB.
         MVC   HA+K1(K4),R0COUNT       SET IN CCHH OF HA.
         MVI   HA,DEFECT               SET FLAG IN HA.
         LA    R4,HA                   WRITE-OUT AREA.
         ST    R4,WRTHA                STORE IN CCW.
         MVI   WRTHA,RDCD              SET OP CODE.
         NI    WRTHA+K4,XFF-CHAIN      SET CHAIN BIT OFF
         MVI   WRTHA+K7,K5             SET COUNT IN CCW.
         LA    R4,R0COUNT              COUNT FIELD ADDRESS.
         ST    R4,WRTR0                STORE IN CCW.
         MVI   WRTR0,WOPCODE           RESET OP CODE.
         NI    WRTR0+K4,XFF-CHAIN      SET CHAIN BIT OFF
         LA    R4,WRTHA                CHANNEL PROGRAM START.
         ST    R4,IOBSTART             STORE IN IOB.
         BAL   R9,WRTREAD              WRITE HA/R0 ON PRIMARY.
         BAL   R9,WAIT                 AWAIT COMPLETION.
         L     R4,PRMPTR                GET CALLER'S PARM PTR.  YL02912
         L     R9,ALTINFO              GET ALT TRK INFO.         YM4604
         MODESET KEYADDR=KEY,WORKREG=15 GET CALLER'S KEY.        YM4604
         ST    R9,K4(R4)               PUT ALT INFO IN USER LIST.YM4604
         MODESET EXTKEY=DATAMGT                                  YM4604
         EJECT
***********************************************************************
*                                                                     *
*   WRITE HOME ADDRESS/RECORD ZERO ON THE ALTERNATE TRACK HERE.       *
*                                                                     *
***********************************************************************
         SPACE 1
WRTALT   EQU   *
         LA    R4,WRTR0                ADDR OF START OF CHAN PROG.
         ST    R4,IOBSTART             STORE IN IOB
         ST    R3,R0COUNT              PRIMARY CCHH TO RECORD ZERO.
         MVI   R0COUNT+K7,K8           SET DATA LENGTH FOR EIGHT.
         MVI   WRTR0+K7,K16            SET CCW COUNT FOR 16.
         BAL   R9,WRTREAD              WRITE R0 ON ALTERNATE.
         BAL   R9,WAIT                 AWAIT COMPLETION.
         SPACE
         XC    ALTINFO+K4(K2),ALTINFO+K4 CLEAR NO. OF ALT. TO ZERO.
         CLI   FUNCTION,NULL            FORMAT-ANALYZE REQUEST. YL02912
         BNE   UPFORMAT                NO--NEED TO UPDATE FORMAT4 DSCB.
         L     R4,ALTPTR                ADDR OF ALT INFO.        YM4604
         MODESET EXTKEY=SUPR                                     YM4604
         MVC   K0(K6,R4),ALTINFO       UPDATE ALTERNATE TRACK INFO.
         MODESET EXTKEY=DATAMGT                                  YM4604
         SPACE 3
EXIT0    BAL   R9,FREECORE             FREE THE WORKAREA.
         LA    R15,0                   SET RETURN CODE.
RETURN   EQU   *                                                YL02912
         L    R14,RBEXSAVE+K36(R5)     GET RETURN ADDRESS.      YL02912
         MODESET EXTKEY=SUPR                                    YM1315
         BR   R14                      RETURN TO CALLER.        YL02912
         SPACE
ASSGN    EQU   *                       SET UP ALTERNATE INFORMATION.
         L     R4,ALTPTR                POINTER TO ALT INFO.     YM4604
         MODESET EXTKEY=SUPR                                     YM4604
         MVC   ALTINFO(K6),K0(R4)      SET UP ALTERNATE TRACK INFO.
         MODESET EXTKEY=DATAMGT                                  YM4604
         B     ASSG1                   GO PROCESS THIS TRACK.
         EJECT
***********************************************************************
*                                                                     *
*   UPDATE THE FORMAT 4 DSCB HERE.                                    *
*                                                                     *
***********************************************************************
         SPACE 3
UPFORMAT EQU   *                       UPDAYE THE FORMAT 4 DSCB HERE.
         MVC   ALTDATA(K6),ALTINFO     UPDATE ALTERNATE INFORMATION.
         LA    R3,SEARCH               CHANNEL PROGRAM ADDRESS.
         ST    R3,IOBSTART             STORE IN IOB.
         MVI   WRTRD,WRTCD             SET OP CODE FOR WRITE.
         MVC   CCHHR(K4),VTOCCCHH      SEEK//SEARCH ADDRESS.
         BAL   R9,WRTREAD              WRITE OUT NEW FORMAT 4 DSCB DATA
         BAL   R9,WAIT                 AWAIT COMPLETION.
         MVI   WRTRD,OPCODE            SET OP CODE FOR READ.
         OI    WRTRD+K4,SKIP           SKIP DATA TRANSFER.
         BAL   R9,WRTREAD              READ BACK CHECK DATA.
         BAL   R9,WAIT                 AWAIT COMPLETION.
         B     EXIT0                   GO RETURN TO CALLER.
         SPACE
***********************************************************************
*                                                                     *
*     EXECUTE CHANNEL PROGRAM                                         *
*                                                                     *
***********************************************************************
         SPACE
WRTREAD  EXCP  IOB                     START THE I/O OPERATION.
         BR    R9                      RETURN.
WAIT     TM    ECB,COMPLETE            THIS OPERATION COMPLETE.
         BO    TEST                    YES-TEST ITS STATUS.
         WAIT  ECB=ECB                 NO--AWAIT COMPLETION.
TEST     CLI   ECB,GOOD                EVERYTHING A-OK.
         BCR   K8,R9                   YES-RETURN.
IOERR    EQU   *
         BAL   R9,FREECORE             GO FREE STORAGE.         YL02912
         LA    R15,K12                 SET RETURN CODE.
         B     RETURN                  EXIT.*
         SPACE 3
FREECORE EQU   *                       RELEASE THE CORE HERE.
         LH    R3,SIZE                 SIZE OF AREA TO RELEASE.
         FREEMAIN R,LV=(3),A=(2),SP=229                         YL02912
         LA    R2,ENDLIST-PARMLIST      GET PARM LIST SIZE      YL02912
         FREEMAIN R,LV=(2),A=(11)       FREE PARM LIST          YL02912
         BR    R9                      RETURN.
         SPACE
         SPACE 3
F1       DC    F'1'                    TRACK INCREMENT CONSTANT.
ZERO     DC    1H'0'                   COMPARE CONSTANT.
MAINT    DS    40X                     ZAP FIX AREA.            YL02912
         EJECT
***********************************************************************
*                                                                     *
*   PARAMETER LIST DSECT                                              *
*                                                                     *
***********************************************************************
         SPACE 2
PARMLIST DSECT                                                  YL02912
FUNCTION DS    0F                       CALLER'S FUNCTION.      YL02912
UCBPTR   DS    F                        UCB POINTER.            YL02912
DCBPTR   DS    F                        DCB POINTER OR          YL02912
CCHHPARM EQU   DCBPTR                   ADDR OF TRACK OR        YL02912
SERPTR   EQU   DCBPTR                   ADDR OF VOL SER.        YL02912
ALTPTR   DS    F                        ADDR OF ALTINFO OR      YL02912
NEWCCHH  EQU   ALTPTR                   NEW TRACK ADDRESS.      YL02912
DEBPTR   DS    F                        DEB POINTER OR          YL02912
DEVMODP  EQU   DEBPTR                   ADDR OF 3340 MODEL NO.  YL02912
PRMPTR   DS    F                        ADDR OF CALLER'S LIST.  YL02912
KEY      EQU   PRMPTR                   KEY OF CALLER.          YL02912
IGCRESV  DS    4F                      RESERVED.                 YM1129
ENDLIST  EQU   *                        PARAMETER LIST END.     YL02912
         SPACE 2
         EJECT
IOBLOCKS DSECT
MYDEB    EQU   *                       DEB DEFINITION.
DEBEOEA  DS    1F                      END-OF-EXTENT.
DEBSIOA  DS    1F                      START I/O.
DEBPCIA  DS    1F                      PCI.
DEBCEA   DS    1F                      CHANNEL END.
DEBXCEA  DS    1F                      ABNORMAL END.
         DS    CL12
DEBLNGTH DS    CL1                     DEB LENGTH(DOUBLE WORDS)
         DS    CL3
DEB      EQU   *                       DEB PROPER
DEBNMSUB DS    CL1                     OPEN SUBROUTINES.
DEBTCBAD DS    CL3                     TCB ADDRESS.
DEBAMLNG DS    CL1                     LENGTH ACCESS METHOD.
DEBDEBAD DS    CL3                     NEXT DEB ADDRESS.
DEBOFLGS DS    CL1                     DATA SET FLAGS.
DEBIRBAD DS    CL3                     IRB ADDRESS.
DEBOPATB DS    CL1                     TYPE OF I/O.
DEBSYSPG DS    CL3                     IOB PURGE ADDRESS.(SYSTEM)
DEBNMEXT DS    CL1                     NO. OF EXTENTS.
DEBUSRPG DS    CL3                     IOB USER PURGE ADDRESS.
DEBPRIOR DS    CL1                     ZERO
DEBECBAD DS    CL3                     PURGE ECB.
DEBDEBID DS    CL1                     PROTECT KEY//ID.
DEBDCBAD DS    CL3                     DCB ADDRESS.
DEBEXSCL DS    CL1                     EXTENT SCALE.
DEBAPPAD DS    CL3                     APPENDAGE VECTOR TABLE.
DEBDVMOD DS    CL1                     FILE MASK.
DEBUCBAD DS    CL3                     UCB ADDRESS.
DEBBINNO DS    CL2                     BIN NO. FOR 2321.
DEBSTRCC DS    CL2                     CYLINDER START.
DEBSTRHH DS    CL2                     HEAD START.
DEBENDCC DS    CL2                     CYLINDER END.
DEBENDHH DS    CL2                     HEAD END.
DEBNMTRK DS    CL2                     NUMBER OF TRACKS.
         DS    CL20
DEBEND   DS    0C                      END OF DEB.
         EJECT
ECB      DS    1F                      ECB HERE.
IOB      EQU   *                       IOB HERE.
IOBFLAG1 DS    CL1                     FLAG1
IOBFLAG2 DS    CL1                     FLAG2.
IOBSENS0 DS    CL1                     SENSE BYTE ZERO.
IOBSENS1 DS    CL1                     SENSE BYTE ONE.
IOBECBAD DS    CL4                     ADDRESS OF ECB.
IOBCSW   DS    CL8                     STATUS WORDS.
IOBSTART DS    CL4                     CHANNEL PROGRAM ADDRESS.
IOBDCBPT DS    CL4                     ADDRESS OF DCB.
IOBRESTR DS    CL4                     RESTART ADDRESS.
IOBINCAM DS    CL2                     BLOCK COUNT.
IOBERRCT DS    CL2                     ERROR COUNT.
IOBSEEK  DS    CL8                     SEEK ADDRESS.
CCHHR    EQU   IOBSEEK+3               SEARCH ADDRESS.
DCB      DS    1F                      DEB ADDRESS.
ALTINFO  DS    1D                      KEEP ALTERNATE TRACK INFO HERE.
WRTHA    DS    1D                      WRITE HOME ADDRESS.
WRTR0    DS    1D                      WRITE RECORD ZERO.
RDHA     DS    1D                      READ HOME ADDRESS.
READR0   DS    1D                      READ RECORD ZERO.
         AIF   ('&LIB' EQ 'LIB1').X227800                       XL03130
         DS    3D                       RESERVED.                YM6524
.X227800 ANOP                                                   XL03130
HA       DS    CL5                     HOME ADDRESS.
SW       DS    CL1                     SWITCH.
D2314    EQU   X'80'                   2314 DEVICE.
D2321    EQU   X'40'                   2321 DEVICE.
DEFALT   EQU   X'02'                                               1057
RETRY    EQU   X'01'                   GIVE IT ONE MORE TRY.
SIZE     DS    CL2                     SIZE OF GETMAIN AREA.
R0COUNT  DS    1D                      RECORD ZERO COUNT FIELD.
R0DATA   DS    1D                      RECORD ZERO DATA  FIELD.
RETSW    DS    CL1                     RETRY SW USED IN 108B   @ZA04363
IOEND1   DS    0C                      END OF BASIC I/O BLOCKS.
*   EXTENSION FOR GETALT//DYNAMIC TRACK REQUEST.
SEARCH   DS    1D                      SEARCH CCW.
TIC      DS    1D                      REPEAT UNTIL FOUND.
WRTRD    DS    1D                      WRITE OR READ DATA.
FORMAT4  DS    CL96                    FORMAT4 DSCB READ IN AREA.
ALTDATA  EQU   FORMAT4+8               ALTERNATE TRACK INFORMATION.
VTOCCCHH EQU   FORMAT4+63              CCHH START OF VTOC.
IOEND    DS    0C
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
CVT      DSECT
         CVT   SYS=MIN
         END
