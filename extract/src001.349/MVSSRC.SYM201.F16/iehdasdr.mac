         COPY  LCGASMSW                                          SM4351
IEHDASDR CSECT
*                                                                     *
*0000   RE-ASSEMBLE FOR CHANGE TO IEHDWORK                     @ZA01201
*        RELEASE 23 DELETIONS                                         *
*        RELEASE 22 DELETIONS                                         *
*        RELEASE 21 DELETIONS                                         *
*        RELEASE 20 DELETIONS                                         *
*1418591000,825000                                               A32161
*1418562000,589000,590000-591000,887000                          M2680
*1418887000                                                      M2683
*1418887000                                                      S20201
*STATUS CHANGE LEVEL 003
*                                                                     *
*FUNCTION/OPERATION- THE ENTRY POINT FOR THE IEHDASDR SYSTEM UTILITY  *
*   PROGRAM IS THIS ROUTINE. CONTROL IS RECEIVED VIA A LINK OR        *
*   AN ATTACH MACRO ISSUED BY THE CALLING ROUTINE. -IEHDASDR- WILL    *
*   DO A GETMAIN FOR THE WORK AREA WHICH IS THEN ZEROED OUT,          *
*   PROCESSES ALL PARAMETERS POINTED TO BY REGISTER 1 (LINECNT,N,     *
*   INPUT FOR INVOCATION), AND THEN VIA THE XCTL MACRO, WILL PASS     *
*   CONTROL TO THE -IEHDASDS- ROUTINE.                                *
*                                                                     *
*ENTRY POINTS- THE ONLY ENTRY POINT IS -IEHDASDR-.                    *
*                                                                     *
*INPUT- REGISTER ONE WILL POINT TO A THREE WORD PARAMETER LIST        *
*   CONSTRUCTED AS FOLLOWS-                                           *
*                                                                     *
*                  **************************                         *
*                  * PTR TO PARM FIELD INFO *                         *
*                  *     (IF SUPPLIED)      *                         *
*                  **************************                         *
*                  * PTR TO INVOCATION      *                         *
*                  *     PARAMETERS         *                         *
*                  **************************                         *
*                  * PTR TO INVOCATION      *                         *
*                  *     PARAMETERS         *                         *
*                  **************************                         *
*                                                                     *
*EXTIS-NORMAL- CONTROL IS PASSED TO THE -IEHDASDS- ROUTINE VIA THE    *
*   XCTL MACRO.                                                       *
*                                                                     *
*EXITS-ERROR- CONTROL WILL BE RETURNED TO THE CALLINE ROUTINE WITH    *
*   A RETURN CODE OF 16 FOR ANY ERROR DETECTED IN THE PASSED          *
*   PARAMETERS.                                                       *
*                                                                     *
*EXTERNAL ROUTINES- NONE.                                             *
*                                                                     *
*TABLES/WORK AREAS- THIS ROUTINE DOES A GETMAIN FOR THE WORK AREA,    *
*   DESCRIBED BY THE DSECT -WORK-.                                    *
*                                                                     *
*ATTRIBUTES- SERIALLY REUSABLE, RELOCATABLE.                          *
*                                                                     *
 TITLE   'IEHDASDR --- INVOCATION AND PARM INFO HANDLER'
*   THE FOLLOWING ARE REGISTER ASSIGNMENTS.
GR0      EQU   0
GR1      EQU   1
GR2      EQU   2
GR3      EQU   3
GR4      EQU   4
GR5      EQU   5
GR6      EQU   6
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10
GR11     EQU   11
GR12     EQU   12
GR13     EQU   13
GR14     EQU   14
GR15     EQU   15
ROOTBASE EQU   11
WORKBASE EQU   12
M39      EQU   39                      MESSAGE 839.              A32161
M47      EQU   47                      MESSAGE 847.              A32161
C16      EQU   16                      RETURN CODE.              A32161
K1       EQU   1                       DISPLACEMENT CONSTANT     A32161
K2       EQU   2                       DISPLACEMENT CONSTANT     A32161
K6       EQU   6                       DISPLACEMENT CONSTANT     A32161
NUM      EQU   X'F0'                   DDEGITAL TEST VALUE.      A32161
         SPACE 1
DELIM    EQU   X'7E'                   PARM FIELD DELIMITER TEST.
         SPACE 1
         USING WORK,12                 SET UP BASE FOR WORK AREA
         USING IEHROOT,ROOTBASE
         SPACE
IEHROOT  SAVE  (14,12),T               SAVE THE REGISTERS.
         LR    ROOTBASE,GR15           LOAD THE BASE REGISTER.
         LR    GR7,GR1                 SAVE PASSED POINTER, IF ANY.
         SPACE 3
*   OBTAIN CORE FOR AND ZERO OUT WORKAREA.
         SPACE 1
GETCORE  GETMAIN R,LV=0+WORKSIZE
         SPACE 1
         LR    WORKBASE,GR1            SAVE ADDRESS OF WORKAREA.
         LA    GR3,WORKSIZE            LENGTH OF WORKAREA.
         BAL   GR14,CLEAR              CLEAR WORKAREA TO ZERO.
         EJECT
*   SET DEFAULT OPTIONS HERE.
*   THESE OPTIONS WILL BE CHANGED BY PARM FIELD INFO, IF SUPPLIED.
         MVI   PAGENO+1,1              DEFAULT IS PAGE ONE.
         MVI   LINECNT+1,58            FIXED LINE COUNT.
         MVI   LINECNT1+1,58           CURRENT LINE COUNT.
         LA    GR4,ENDCLIST-CSIZE      END OF QUEUE POINTER.
         ST    GR4,ENDCLIST            SET UP END OF QUEUE POINTER.
         MVC   SYSINDD(8),CONSYSIN     DEFAULT DDNAME TO WORK AREA.
         MVC   SYSOUTDD(8),CONSPRIN    DEFAULT DDNAME TO WORK AREA.
         SPACE 1
* THIS ROUTINE CHECKS THE PARAMETER LISTS PASSED WHEN THE PROGRAM IS
*  INVOKED.  REGISTER 1 POINTS TO A THREE FULL WORD AREA IN CORE WHICH
*  CONTAINS ADDRESSES OF THE THREE POSSIBLE PARAMETER LISTS.  THE HIGH
*  ORDER BYTE OF EACH OF THE FULL WORDS MUST BE EITHER HEX 00 OR HEX 80
*  THE 80 SIGNIFIES THAT THIS IS THE LAST PARAMETER LIST POINTER.  THE
*  FIRST POINTER IS TO THE LIST THAT EQUALS THE PARM VALUES FOUND ON
*  THE EXECUTE CARD. TWO PARM VALUES ARE POSSIBLE, A LINE COUNT
*  PARAMETER AND A POWER FACTOR WHICH CONTROLS THE NUMBER OF
*  FUNCTIONS WHICH CAN BE EXECUTING CONCURRENTLY.
*  THE SECOND POINTER IS TO THE DDNAME LIST WHICH MUST
*  BEGIN ON A HALF WORD BOUNDARY THAT IS ALSO NOT A FULL WORD
*  BOUNDARY.  THE TWO HIGH ORDER BYTES CONTAIN A COUNT OF THE NUMBER
*  OF BYTES IN THE REMAINDER OF THE LIST.  ALL ENTRIES IN THE LIST HAVE
*  A MAXIMUM LENGTH OF EIGHT BYTES AND ARE LEFT-JUSTIFIED AND PADDED
*  WITH BLANKS.  IF A NAME IS OMITTED WITHIN THE LIST, THE EIGHT-BYTE
*  ENTRY MUST CONTAIN BINARY ZEROES.  NAMES ARE OMITTED FROM THE END BY
*  SHORTENING THE LIST.  THE SEQUENCE OF THE ENTRIES IS AS FOLLOWS:
*
*                  ENTRY 1   00000000
*                        2   00000000
*                        3   00000000
*                        4   00000000
*                        5   SYSIN
*                        6   SYSPRINT
*                        7   00000000
*                        8   00000000
*                        9   00000000
*
*  THE THIRD PARAMETER LIST PERTAINS TO HEADER INFORMATION.
*  THE THIRD POINTER IS TO THE SIX BYTE LIST CONTAINING A PAGE COUNT
*  FOR THE OUTPUT DEVICE.  THE FIRST TWO BYTES OF THIS LIST CONTAIN THE
*  LENGTH IN BYTES OF THE HEADING LIST.  THE REMAINING FOUR BYTES
*  CONTAIN A PAGE NUMBER THAT THE UTILITY PROGRAM IS TO PLACE ON THE
*  FIRST PAGE OF PRINTED OUTPUT.
         EJECT
* THIS SECTION GETS THE ADDRESS OF THE DDNAMES IF ANY ARE PROVIDED,
*  OTHERWISE STANDARD DDNAMES ARE PROVIDED
SETOPT   LR    GR1,GR7                 RESTORE POINTER TO PARMS, IF ANY
         TM    0(GR1),X'80'            ONE PARAMETER ONLY.
         BO    TEST1                   YES-GO PROCESS.
         TM    4(GR1),X'80'            TWO PARAMETERS ONLY.
         BO    TEST2                   YES-GO PROCESS
         TM    8(GR1),X'80'            THREE PARAMETERS ONLY
         BZ    SETCODE                 NO--MUST BE INVOCATION ERROR.
TEST3    OC    9(3,GR1),9(GR1)         IS THE THIRD POINTER VALID.
         BZ    TEST2                   NO--GO PROCESS 2ND PARAMETER.
         L     GR2,8(GR1)              POINTER TO PAGE NUMBER.
         OC    0(2,GR2),0(GR2)         IS LENGTH ZERO.
         BZ    TEST2                   YES-GO PROCESS 2ND PARAMETER.
*   PROCESS PASSED PAGE NUMBER HERE.
         MVI   PAGESW,X'FF'            INDICATE PAGE NO. PASSED.
         XC    SARG(8),SARG            CLEAR.
         PACK  SARG+5(3),2(4,GR2)      PACK GRAPHICS.
         CVB   GR3,SARG                CHANGE TO BINARY.
         STH   GR3,PAGENO              SET UP PAGE NUMBER.
TEST2    OC    5(3,GR1),5(GR1)         IS THE 2ND POINTER VALID.
         BZ    TEST1                   NO--GO PROCESS 1ST PARAMETER.
         L     GR2,4(GR1)              POINTER TO DDNAME LIST.
         OC    0(2,GR2),0(GR2)         IS LIST LENGTH ZERO.
         BZ    TEST1                   YES-GO PROCESS 1ST PARAMETER.
*   PROCESS DDNAME LIST HERE.
         CLI   1(GR2),48               DDNAME LIST LONG ENOUGH/SYSPRINT
         BNL   TESTTWO                 YES-GO PROCESS SYSPRINT/SYSIN
         CLI   1(GR2),40               DDNAME LIST LONG ENOUGH/SYSIN.
         BNL   TESTONE                 YES-GO PROCESS SYSIN ONLY.
         B     TEST1                   GO-PROCESS FIRST PARAMETER.
TESTTWO  OC    42(8,GR2),42(GR2)       CHECK FOR SYSPRINT DDNAME.
         BZ    TESTONE                 NONE-GO TEST FOR SYSIN.
         MVC   SYSOUTDD(8),42(GR2)     MOVE IN USERS NAME FOR SYSPRINT.
TESTONE  OC    34(8,GR2),34(GR2)       CHECK FOR SYSIN DDNAME.
         BZ    TEST1                   NONE-GO PROCESS 1ST PARAMETER.
         MVC   SYSINDD(8),34(GR2)      MOVE IN USERS NAME FOR SYSIN.
TEST1    OC    1(3,GR1),1(GR1)         IS FIRST PARAMETER VALID.
         BZ    HEADPRIN                NO--NO PARM OPTIONS TODAY.
         L     GR2,0(GR1)              POINTER TO PARM LIST.
         OC    0(2,GR2),0(GR2)         IS LIST LENGTH ZERO.
         BZ    HEADPRIN                YES
         LH    GR3,0(GR2)              LENGTH OF PARAMETER LIST.
         LA    GR3,1(GR3)
         LA    GR5,1(GR2)              POINTER TO BYTE BEFORE OPTION.
         LA    GR2,1(GR2)              START OF FIELD TO SCAN.
         SPACE 1
SEEK     BCTR  GR3,0                   MINUS ONE FROM BYTE COUNT.
         LTR   GR3,GR3                 END OF TEST.
         BE    HEADPRIN                YES-NO MORE OPTIONS
         LA    GR5,1(GR5)              INCREMENT SCAN POINTER.
         CLI   0(GR5),DELIM            IS THIS A DELIMITER.
         BNH   TESTCNT2                YES-GO TEST FOR LINE COUNT.
         SPACE 1
         LTR   GR3,GR3                 END OF TEST.
         BNE   SEEK                    NO--LOOP UNTIL DELIMITER.
         SPACE 1
TESTCNT2 CLC   1(8,GR2),LINCT          IS PARAMETER LINECOUNT.
         BE    FOUNDL                  YES-GO PROCESS.
         SPACE 1
         CLC   1(2,GR2),NEQUAL         IS PARAMETER A 'N='.
         BE    FOUNDN                  YES-GO PROCESS.
         SPACE 1
         LR    GR2,GR5                 RESET START POINTER.
TESTCNT1 LTR   GR3,GR3                 IS LIST EXAUSTED
         BE    HEADPRIN                YES-NO MORE OPTIONS.
         B     SEEK                    NO--CONTINUE SCAN.
         SPACE 1
FOUNDL   EQU   *                       HANDLE THE LINE COUNT HERE.
         BCTR  GR3,0                   ACCOUNT FOR '=' SIGN.
         CH    GR3,TWO                 AT LEAST TWO CHARACTER PARAMETER
         BL    SETCODE                  NO - INVOCATION ERROR    M2680
         TM    9(GR2),X'F0'            1ST DIGIT A DECIMAL DIGIT.
         BC    12,SETCODE              NO--INVOCATION ERROR.
         TM    10(GR2),X'F0'           2ND DIGIT A DECIMAL DIGIT.
         BC    12,SETCODE              NO--INVOCATION ERROR.
         XC    SARG(8),SARG            CLEAR.
         PACK  SARG+6(2),9(2,GR2)      PACK GRAPHICS.
         BCTR  GR3,0                   ACCOUNT FOR ONE DIGIT.
         LA    GR2,10(GR2)             POINT TO LAST DIGIT.
         CH    GR3,ONE                 MORE THAN 2 DIGITS LEFT IN LIST.
         BNH   CONT                    NO--PROCESS THIS LINE COUNT.
         CLI   1(GR2),DELIM            SOME DELIMITER FOLLOW 2ND DIGIT.
         BH    SETCODE                 NO--INVOCATION ERROR.
CONT     LR    GR5,GR2                 RESET POINTER.
         CVB   GR9,SARG                CHANGE TO BINARY.
         STC   GR9,SARG                TEMPORARY STORAGE.
         CLI   SARG,0                  DOES THE LINE COUNT EQUAL ZERO.
         BNH   SETCODE                 YES-INVOCATION ERROR.
         CLI   SARG,99                 LINE COUNT MORE THAN 99.
         BH    SETCODE                 YES-INVOCATION ERROR.
         STH   GR9,LINECNT             FIXED LINE COUNT.
         STH   GR9,LINECNT1            CURRENT LINE COUNT.
         B     TESTCNT1
         EJECT
*   SET UP THE QUEUE SIZE HERE, IF SPECIFIED.
FOUNDN   XC    SARG(8),SARG            ZERO.
         BCTR  GR3,0                   ACCOUNT FOR '=' SIGN.
         CH    GR3,ONE                 MORE THAN 1 BYTE LEFT IN LIST.
         BNH   CONT1                   NO--GO PROCESS 'N'.
         CLI   2(GR5),DELIM            DELIMITER FOLLOW THE 1 DIGIT.
         BH    SETCODE                 NO--INVOCATION ERROR.
CONT1    EQU   *
         LA    GR2,1(GR5)              RESET POINTER.
         LR    GR5,GR2                 RESET POINTER -LAST DIGIT.
         CLI   0(GR2),C'0'             N GREATER THAN 0.
         BNH   SETCODE                 NO--INVOCATION ERROR.
         CLI   0(GR2),C'6'             N GREATER THAN 6.
         BH    SETCODE                 YES-INVOCATION ERROR.
         PACK  SARG+7(1),0(1,GR2)      PACK GRAPHICS.
         CVB   GR9,SARG                'N' VALUE IN BINARY.
         SPACE 1
FOUND1   SLA   GR9,2                   N TIMES BYTES/ENTRY.
         LA    GR4,PTRFUNC1-CSIZE      ADDRESS OF CONTROL LIST.
         AR    GR4,GR9                 PLUS N TIMES BYTES PER ENTRY.
         ST    GR4,ENDCLIST            POINTS TO LAST USEABLE SLOT.
         B     TESTCNT1
         SPACE
***********************************************************************
*                                                                     *
*   THIS ROUTINE WILL CLEAR ANY BLOCK OF CORE TO ZERO.                *
*                                                                     *
*     REGISTER 1 MUST CONTAIN THE STARTING ADDRESS UPON ENTRY.        *
*     REGISTER 2 IS A WORK REGISTER.                                  *
*     REGISTER 3 MUST CONTAIN THE SIZE UPON ENTRY.                    *
*     REGISTER 14 IS THE RETURN REGISTER.                             *
*                                                                     *
***********************************************************************
         SPACE
CLEAR    LA    GR2,255                 SET FOR EXECUTE.
         MVI   0(GR1),X'00'            PRIME FIRST BYTE FOR EXEC.
EXEC     CH    GR3,TWO56               WILL THIS BE THE LAST PASS.
         BNH   LASTTIME                YES-DO ONE MORE MOVE.
         EX    GR2,MVZO                CLEAR 256 BYTES.
         LA    GR1,256(GR1)            INCREMENT ADDRESS.
         SH    GR3,TWO56               UPDATE BYTE COUNT.
         B     EXEC                    RETURN TO CHECKPOINT.
         SPACE 1
LASTTIME BCTR  GR3,0                   DECREMENT FOR MVC.
         EX    GR3,MVZO                CLEAR REMAINING PORTION.
         BR    GR14                    RETURN.
         EJECT
HEADPRIN EQU   *                       PASS CONTROL TO 2ND LOAD/MONITOR
         ST    GR1,PRINT1              PASS PARM POINTER TO 2ND LOAD.
         LR    GR1,WORKBASE            PASS WORK AREA ADDRESS.
         L     GR14,12(GR13)           RESTORE REGISTER 14.
         LA    GR15,XCTL               XCTL LIST.
         XCTL  (2,12),MF=(E,(1)),SF=(E,(15))
         SPACE 3
SETCODE  EQU   *                                                 A32161
         USING IHADCB,GR4                                        A32161
         LA    GR4,WRIT                GET DCB ADDRESS.          A32161
         ST    GR4,PRINT1              STORE IN WORK AREA.       A32161
         MVC   DCBDDNAM(8),CONSPRIN  PUT DDNAME IN DCB.          A32161
         MVC   PRNTSW,0                CLEAR SWITCH.             A32161
         LA    GR1,OPENLST             GET OPEN LST ADDR.        A32161
         OPEN  MF=(E,(1))              OPEN PRINTER DCB.         A32161
         TM    DCBOFLGS,WAITING        SUCCESSFUL OPEN.          A32161
         BZ    SETCODE                 NO-TRY AGAIN.             A32161
         BAL   GR8,WRITMSG             PRINT HEADING.            A32161
         LA    GR1,M47                 GET MESSAGE 847.          A32161
         BAL   GR8,MSGSETUP            PLACE MSG BUFFER.         A32161
         BAL   GR8,WRITMSG             PRINT MESSAGE.            A32161
         LA    GR1,M39                 GET RETURN CODE MSG.      A32161
         BAL   GR8,MSGSETUP            PLACE IN MSG BUFFER.      A32161
         LA    GR6,C16                 RETURN CODE.              A32161
         CVD   GR6,DECODE              CHANGE TO DECIMAL.        A32161
         UNPK  K1(K2,GR1),DECODE+K6(K2)                          A32161
         OI    K2(GR1),NUM                                       A32161
         BAL   GR8,WRITMSG             PRINT MESSAGE.            A32161
         LA    GR1,CLOSLST             GET ADDR OF CLOSE.        A32161
         CLOSE MF=(E,(1))              CLOSE PRINTER DCB.        A32161
         LR    GR1,WORKBASE          ADDRESS OF WORK AREA.       A32161
         FREEMAIN R,LV=0+WORKSIZE,A=(1)
         SPACE
         LA    GR15,16
         RETURN (14,12),T,RC=(15)      RESTORE/RETURN W/PROPER CODE.
MSGSETUP EQU   *                                                 A32161
         LINK  EP=IEHDMSGB             OBTAIN PROPER MSG.        A32161
         BR    GR8                     RETURN TO CALLER.         A32161
WRITMSG  EQU   *                                                 A32161
         LINK  EP=IEHDPRNT             PRINT PROPER MSG.         A32161
         BR    GR8                     RETURN TO CALLER.         A32161
FILIN    MVC   0(0,GR1),0(GR3)         PARM INTO MSG.            A32161
         DC    C'MAINTENANCE AREA'                              XL02912
MAINT    DS    50F                                              XL02912
OPENLST  OPEN  (WRIT,(OUTPUT)),MF=L                              A32161
CLOSLST  CLOSE (WRIT),MF=L                                       A32161
         SPACE 1
*ASSUMED DDNAME CONSTANTS
*
CONSYSIN DC    CL8'SYSIN   '
CONSPRIN DC    CL8'SYSPRINT'
         SPACE 1
SARG     DS    1D                      WORK AREA.
TWO56    DC    H'256'                  COMPARE CONSTANT.
ONE      DC    H'1'                    COMPARE CONSTANT.
TWO      DC    H'2'                    COMPARE CONSTANT.
MVZO     XC    0(1,GR1),0(GR1)         EXECUTED TO CLEAR CORE.
         SPACE
XCTL     XCTL  EP=IEHDASDS,SF=L
         SPACE
NEQUAL   DC    C'N='                   PARM FIELD KEYWORD.
LINCT    DC    C'LINECNT='             PARM FIELD KEYWORD.
ZEROES   DC    CL2'0'                  LINECNT TEST VALUE.       A32161
DECODE   DS    1D            USE FOR PACK INSTRUCTION            A32161
LIST     DC    F'0'                                              A32161
WRIT     DCB   DSORG=PS,MACRF=PM,RECFM=FBSA,BLKSIZE=121,         M2680 X
               LRECL=121,DDNAME=SYSPRINT,EODAD=SETCODE           A32161
         EJECT
         IEHDWORK
         DCBD  DSORG=PS                                          A32161
         END
