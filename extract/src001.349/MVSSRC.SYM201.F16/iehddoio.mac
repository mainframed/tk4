         COPY  LCGASMSW
IEHDDOIO CSECT
***********************************************************************
*                  FIXES THIS MODULE                                  *
*                     LATEST FIRST                                    *
***********************************************************************
*
*C 55700,541000                              @XA21434=@ZA30990=@YA20534
*
*C 055700,437000                             @YA20010=@XA20758=@ZA26845
*
*C 542000,55700                              @ZA27730=@XA20309=@YA19677
*
*D 528100                                    @ZA13746=@XA15446=@YA14025
*C 55700,173800                              @ZA13746=@XA15446=@YA14025
*A 80600-80940,549100                        @ZA13746=@XA15446=@YA14025
*
*C 55700,445500                              @YA12939=@XA14175=@ZA10908
*A 445100-445200                             @YA12939=@XA14175=@ZA10908
*
*                                   3350 DEVICE SUPPORT        @ZA06534
*
*D 62400-62460                      @SA75996=@XA10959=@YA11189=@ZA06532
*C 55700,62500                      @SA75996=@XA10959=@YA11189=@ZA06532
*
*C 56000,62500                      @YA09679=@SA74485=@XA10384=@ZA04411
*A 55600-55700,62400-62460          @YA09679=@SA74485=@XA10384=@ZA04411
*
*C 77000                            @SA69574=@YA04867=@XA05836=@ZA01207
*
*A 516100,516200                    @SA69914=@XA05792=@YA04806=@ZA01201
*
*                        PTM XM0208 IS FIXED UNDER OS APAR NO. SA53223
*                                                XM4417=YA03005=SA67332
*  PTM XM5476 DELETES CHANGES FOR SA67332                        XM5476
*STATUS CHANGE LEVEL 000
***********************************************************************
*                                                                     *
*FUNCTION/OPERATION- THIS ROUTINE HANDLES MOST OF THE I/O FOR THE     *
*   DUMP FUNCTION OF THE IEHDASDR UTILITY PROGRAM. ALL I/O INVOLVED   *
*   WITH DA AND/OR TAPE DEVICES IS AT THE EXCP LEVEL. THE FIRST       *
*   I/O OPERATION ON THE DIRECT ACCESS DEVICE RESULTS IN THE END OF   *
*   EXTENT APPENDAGE(IGG019P8) BEING ENTERED FROM IOS. THIS ROUTINE   *
*   WILL MODIFY THE DEB EXTENTS AND SET THE CORRECT FILE MASK.        *
*   PROCESSING WILL BE DONE ONE TRACK AT A TIME UNTIL EITHER THE      *
*   DUMP LIMITS OR THE VOLUME LIMITS ARE EXHAUSTED. CONTROL WILL BE   *
*   RETURNED TO -IEHDEXCP- AT THIS TIME.                              *
*                                                                     *
*ENTRY POINTS- THE ONLY ENTRY POINT IS -IEHDDOIO-, AND CONTROL IS     *
*   ALWAYS RECEIVED FROM THE -IEHDEXCP- ROUTINE.                      *
*                                                                     *
*INPUT- POINTERS TO A WORKAREA(REGISTER 12) AND TO A CONTROL BLOCK    *
*   (REGISTER 10).                                                    *
*                                                                     *
*OUTPUT TO DIRECT ACCESS- A COPY OF THE -FROM- DIRECT ACCESS          *
*   DEVICE WITH PREVIOUSLY UNOWNED TRACKS CLEANED UP.                 *
*                                                                     *
*OUTPUT TO TAPE-                                                      *
*   LIMITS RECORD- ALWAYS THE FIRST RECORD AFTER A TAPE MARK ON       *
*   EACH VOLUME OF TAPE. THIS RECORD IS 24 BYTES IN LENGTH AND        *
*   CONTAINS THE FOLLOWING INFORMATION.                               *
*                                                                     *
*        BYTES 0-3    CCHH OF THE FIRST TRACK DUMPED.                 *
*        BYTES 4-7    CCHH+1 OF THE LAST TRACK DUMPED.                *
*        BYTES 8-11   CCHH OF THE FIRST TRACK THIS VOLUME.            *
*        BYTES 12-19  CODE THAT IDENTIFIES THIS AS BEING A            *
*                     RESTORE TAPE.                                   *
*        BYTE  20     FULL DUMP SWITCH -X'F0'=FULL,X'00'=PARTIAL.     *
*        BYTE  21     DEVICE TYPE CODE AS FOLLOWS.                    *
*                        CODE 0    2321                               *
*                             1    2311                               *
*                             2    2314                               *
*                             3    2302                               *
*                             4    2303                               *
*                             5    2301                               *
*        BYTES 22-23  UNUSED.                                         *
*                                                                     *
*   CONTROL RECORD- THIS RECORD IS WRITTEN FOR EACH TRACK DUMPED      *
*   AND IMMEDIATELY PRECEDES THE DATA RECORD FOR EACH TRACK. IT       *
*   CONTAINS A CHANNEL PROGRAM USED BY RESTORE TO WRITE ONE TRACK.    *
*   THE LENGTH OF THIS RECORD DEPENDS ON THE NUMBER OF RECORDS ON     *
*   THE DUMPED TRACK.                                                 *
*                                                                     *
*   TRACK IMAGE RECORD- THIS RECORD CONTAINS THE DATA DUMPED FROM     *
*   A TRACK AND ALWAYS FOLLOWS A CONTROL RECORD.  IT IS WRITTEN AS    *
*   ONE MAXIMUM LENGTH PHYSICAL RECORD ON TAPE. IT CONTAINS THE       *
*   DATA FIELD OF RECORD ZERO AND ALL FIELDS OF OTHER RECORDS THAT    *
*   WERE ON THE TRACK THAT WAS DUMPED.                                *
*                                                                     *
*   TRAILER RECORD- THIS ALWAYS FOLLOWS THE TAPE MARK THAT WAS AT     *
*   THE END OF THE LAST TRACK IMAGE RECORD. IT IS 24 BYTES IN         *
*   LENGTH, OF WHICH ONLY THE FIRST 4 BYTES ARE USED. IT INDICATES    *
*   WHETHER THE RESTORE IS COMPLETE WHEN THE END OF THIS VOLUME       *
*   IS REACHED.                                                       *
*                                                                     *
*EXITS-NORMAL- SUCCESSFUL COMPLETION OF ALL I/O OPERATIONS RESULTS    *
*   IN A RETURN TO -IEHDEXCP- WITH A RETURN CODE OF ZERO.             *
*                                                                     *
*EXITS-ERROR-  ANY ERROR ENCOUNTERED WILL BE DESCRIBED BY AN          *
*   APPROPRIATE MESSAGE TO THE MESSAGE DATA SET. A RETURN CODE        *
*   GREATER THAN ZERO WILL ALSO BE PASSED TO -IEHDEXCP-.              *
*   ERROR MESSAGES WILL BE PRINTED BY IEHDEXCP VIA THE BRANCH   SA53223
*   TABLE EXIT LABELED 'MSGWRT'.                                SA53223
*                                                                     *
*EXTERNAL ROUTINES- THIS ROUTINE IS ALWAYS ENTERED FROM -IEHDEXCP-.   *
*   FIVE OTHER CSECTS ARE USED BY THE ROUTINE. ANY MESSAGES WILL      *
*   BE WRITTEN OUT BY THE MESSAGE WRITER ROUTINE(IEHDPRNT) AFTER      *
*   BEING SET UP BY THE MESSAGE ROUTINE(IEHDMSGB). THE END OF         *
*   EXTENT APPENDAGE(IGG019P8) HANDLES THE LIMITS AND EXTENTS IN      *
*   THE DEB FOR ALL DA DEVICES INVOLVED. ALL DEVICE DEPENDENT         *
*   CONSTANTS WILL BE CONTAINED IN THE -IEHDCONS- CSECT. IEHDEXCP     *
*   WILL LOAD BOTH -IEHDAOUT- AND -IEHDPRNT- WHEN DUMPING TO A        *
*  SYSTEM OUTPUT DEVICE.  IEHDRCVR WILL BE LINKED TO WHEN AN I/O   O122
*  ERROR (MISSING ADDRESS MARKER OR DATA CHECK) ARE ENCOUNTERED    O122
*  WHEN DUMPING TO SYSOUT.                                         O122
*  THIS ROUTINE WILL CLOSELY INTERFACE WITH IEHDEXCP VIA A      SA53223
*  BRANCH TABLE.                                                SA53223
*  BRANCHING FROM ONE MODULE TO ANOTHER WILL BE FACILITATED VIA SA53223
*  A BRANCHING VALUE IN A PREVIOUSLY UNUSED BYTE, BRCHVAL, IN   SA53223
*  THE FUNCTION BLOCK, IEHDBLKS.                                SA53223
*                                                                     *
*ATTRIBUTES- SERIALLY REUSABLE, RELOCATABLE, NON-PRIVILEGED.          *
*                                                                     *
 TITLE 'IEHDDOIO-3RD CSECT OF IEHDDUMP-IEHDASDR SYSTEM UTILITY PROGRAM'
*   THE FOLLOWING ARE REGISTER ASSIGNMENTS.
GR0      EQU   0
GR1      EQU   1
GR2      EQU   2
GR3      EQU   3
GR4      EQU   4
GR5      EQU   5
GR6      EQU   6
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10
GR11     EQU   11
GR12     EQU   12
GR13     EQU   13
GR14     EQU   14
GR15     EQU   15
RCVT     EQU   9
UCBPTR   EQU   9
BASEREG  EQU   11
WORKBASE EQU   12
         SPACE 2
*    MISCELLANEOUS EQUATES
BIT7     EQU   1
         SPACE 2
         USING FUNCBLK,10
         USING IEHDDOIO,BASEREG
         USING WORK,12
         SAVE  (14,12),T,*             SAVE THE REGISTERS.
         LR    BASEREG,GR15            LOAD THE BASE REGISTER.
         B     APARNO                  BRANCH AROUND APAR NO   @ZA04411
         DC    C'ZA30990'              LAST FIX THIS MOD       @ZA30990
APARNO   LA    GR7,EXCPSAVE            SAVE AREA ADDRESS.      @ZA04411
         ST    GR7,8(GR13)             ADDRESS OF NEW AREA TO OLD.
         ST    GR13,4(GR7)             ADDRESS OF OLD AREA TO NEW.
         LR    GR13,GR7                SAVE AREA FOR CALLED ROUTINES.
         USING IOBLOCKS,9
         LA    GR9,BLOCKS              ADDRESS PRIMARY I/O BLOCKS.
         L     GR14,FRTNPTR            LOAD RETURN ADDRESS.
         LTR   GR14,GR14                WILL BE ZERO 1ST TIME   SA53223
         BZ    START                    BRCH IF ZERO            SA53223
         XC    FRTNPTR,FRTNPTR
         BR    GR14                    TAKE UP WHERE WE LEFT OFF.
START    EQU   *                                                SA53223
         SR    GR3,GR3                  ZERO WORK REG           SA53223
         IC    GR3,BRCHVAL              GET BRANCH TABLE VALUE  SA53223
         XC    BRCHVAL,BRCHVAL
         LA    GR2,BRCHTABL             LOAD BRCH TABLE ADDR    SA53223
         AR    GR2,GR3                  COMPUTE TABLE ENTRY     SA53223
         BR    GR2                      BRCH TO ENTRY           SA53223
BRCHTABL EQU   *                                                SA53223
         B     RDCNTS                   GO READ COUNT FLDS      SA53223
         B     WRTHAR0                  GO BUILD/EXECUTE        SA53223
*                                       WRITE HA, WRITE R0.     SA53223
RDCNTS   TM    SEQSW,RPSFEAT1           RPS ON FROM DEV        @ZA06532
         BZ    RDCNTS1                  NO - CONTINUE            A42498
         L     GR2,RPSBUFPT             SET PROPPER ADDRESS IN   A42498
*                                       TIC                      A42498
         MVC   9(3,GR2),CCWBUFPT+1                               A42498
RDCNTS1  EXCP  OUTIOB                                            A42498
         CLI   DEVSW,X'F0'             DUMPING TO SYSOUT.
         BE    WAITOUT                 YES--DO NOT RETURN TO MONITOR.
*  LINK BACK TO MONITOR HERE.
         TM    SW1,MULTIPLE            ANY REASON TO GO BACK TO MONITOR
         BZ    WAITOUT                 NO--GO WAIT.
         LA    GR3,WAITOUT             RETURN ADDRESS.
         ST    GR3,FRTNPTR             SAVE IN RETURN POINTER.
         LA    GR15,0                  RETURN CODE FOR MONITOR.
         B     MULTPROC                RETURN.                  SA53223
WAITOUT  TM    OUTECB,COMPLETE         IS THE OPERATION COMPLETE.
         BO    WAITOUTA                YES--GO TEST THE STATUS.
         WAIT  ECB=OUTECB              NO---AWAIT COMPLETION OF I/O.
WAITOUTA EQU   *                                                   7343
         LA    GR2,RDCNTS              RETURN ADDRESS IF RECOVER   O122
         TM    OUTSTAT+4,X'02'         UNIT CHECK.                 7343
         BZ    WAITOUTX                NO-BRANCH.                  7343
         TM    OUTSENSE,DC             DATA CHECK.                 7343
         BO    IOERR                   YES-I/O ERROR.              7343
WAITOUTB TM    OUTSENSE+1,X'04'        CHECK FOR FILE PROTECT.
         BO    WAITOUTC                YES-GO CHECK FOR TRK OVERFLOW.
         TM    OUTSENSE,X'02'          CHECK FOR TRACK CONDITION.
         BZ    IOERR                   NO--MUST BE I/O ERROR.
WAITOUTC TM    OUTSENSE+1,X'01'        CHECK FOR OVERFLOW INCOMPLETE.
         BZ    WAITOUTX                NO                      @ZA01207
         CLI   OUTECB,X'41'             PERMANENT I/O ERROR      A45281
         BE    CHEKSTAT                 YES, ACCEPT ERROR        A45281
         OI    SWITCH,TRKOVFL          SET TRACK OVERFLOW INDICATOR.
         B     HAR0R1                  R1 IS TRACK OVERFLOW.      0811
WAITOUTX EQU   *                                                   7343
         TM    OUTSTAT+4,X'01'         EOF ON RECORD ONE.          7343
         BZ    WAITOUTD                NO-CHECK IF ALL OK.         7343
         L     GR1,OUTSTAT             GET CCW ADR FROM CSW    @ZA13746
         LA    GR1,0(GR1)              CLEAR FIRST BYTE        @ZA13746
         S     GR1,EIGHT               GET FAILING CCW         @ZA13746
         CLI   0(GR1),X'06'            READ R0 DATA ?          @ZA13746
         BE    IOERR                   YES THEN IO ERROR TERM. @ZA13746
         L     GR3,CCWBUFPT            YES-R1 IS EOF REC.          7343
         MVI   24(GR3),X'12'           SET OP CODE FOR READ COUNT. 7343
         B     RDCNTS                  RESTART CHANNEL PROGRAM.    7343
WAITOUTD EQU   *                                                   7343
         CLI   OUTECB,GOOD             ALL OK.                     7343
         BC    7,IOERR                 NO-I/O ERROR.               7343
         EJECT
***********************************************************************
*                                                                     *
*  TEST HERE TO SEE IF ONLY HOME ADDRESS,RECORD ZERO, OF HOME ADDRESS *
*  RECORD ZERO,RECORD ONE, OR RECORDS GREATER THAN RECORD ONE.        *
*                                                                     *
***********************************************************************
         USING CCWS,2
CHEKSTAT L     GR2,CCWBUFPT            RESET POINTER TO CCW BUFFER.
         LA    GR6,RDWRTCNT            COMPARE ADDR IF HA-R0 ONLY.
         ST    GR6,AREA                STORE FOR COMPARE.         0811
         CLC   AREA+1(3),OUTSTAT+1     IS CSW 8 BEYOND READ R0.   0811
         BE    HAR0                    YES--HAVE HA,R0 ONLY
         LA    GR6,8(GR6)              COMPARE ADDR. IF HA,R0,R1 ONLY.
         ST    GR6,AREA                STORE FOR COMPARE.         0811
         BAL   GR15,CHKCNTS             CHECK FOR BAD COUNT FLD SA53223
         CLC   AREA+1(3),OUTSTAT+1     IS CSW 8 BEYOND READ R1.   0811
         BE    HAR0R1                  YES--HAVE HA,R0-R1.
         B     CNGRDCKD                NO--HAVE HA,R0-RN.
WRTHAR0  L     GR6,DATBUFPT            ADDRESS OF DATA BUFFER.
         MVC   0(8,GR6),ZERO           ZERO FOR R0 DATA IF TRK NOT OWND
HAR0     CLI   DEVSW,X'FF'             DUMPING TO DIRECT ACCESS.
         BNE   ONTAPE                  NO--BRANCH.
         LA    GR6,HAR0CCW1            GET ADDRESS OF HA-R0 CCWS
         LA    GR7,56                  SET NUMBER OF CCWS TO MOVE.
         BAL   GR14,SETUPCCW           GO TO PUT CCWS IN BUFFER.
         USING R0CCWS,8
         MVC   WRITER0+1(3),DATBUFPT+1 RESET BUFFER ADDRESS
         MVC   READR0+1(3),DATBUFPT+1  RESET BUFFER ADDRESS
         LA    GR3,ZERO                SET ADDRESS OF ZEROES IN CCW
         ST    GR3,ERASEALL            TO ERASE REST OF TRACK.
         MVI   ERASEALL,X'11'          RESET OP CODE
         B     CCWOUT                  GO TO WRITE R0-ERASE ON DA.
ONTAPE   LA    GR3,TAPECCW3
         ST    GR3,TOCCWAD             STORE CCW ADDRESS IN IOB.
         L     GR4,DATBUFPT            ADDRESS R0 DATA.
         ST    GR4,8(GR3)              STORE INTO CCW.
         MVI   8(GR3),X'01'            RESET OP CODE.
         LA    GR4,8(GR4)              POINT TO END OF DATA.
         B     CCWOUT1                 GO TO PROCESS.
HAR0R1   CLI   DEVSW,X'FF'             DUMPING TO DIRECT ACCESS.
         BNE   TOTAPE                  NO--BRANCH.
         LA    GR6,CCWA                HA,R0,R1 CCWS.
         LA    GR7,64                  LENGTH OF CCWS TO MOVE
         BAL   GR14,SETUPCCW           GO TO PUT CCWS IN BUFFER.
         USING R0CCWS,8
         L     GR3,DATBUFPT            GET POINTER TO DATA BUFFER.
         LA    GR3,8(GR3)              POINT TO R1 DATA.
         ST    GR3,WRITER0             RESET ADDRESS IN WRT CKD.
         MVI   WRITER0,X'1D'           RESET WRT CKD OP CODE.
         TM    SWITCH,TRKOVFL          TRACK OVERFLOW.
         BZ    HAR0R1A                 NO-DO NOT RESET CCW OP CODE.
         MVI   WRITER0,X'01'           RESET OP CODE FOR WRT SPECIAL.
HAR0R1A  SR    GR5,GR5                 CLEAR WORK REGISTER.
         IC    GR5,5(GR3)              KEY LENGTH FOR R1.
         AH    GR5,6(GR3)              ADD DATA LENGTH FOR R1.
         LA    GR5,8(GR5)              ADD COUNT FOR R1.
         STH   GR5,WRITER0+6           STORE TOTAL COUNT IN CCW.
         ST    GR3,ERASEALL            RESET ADDRESS IN READ BACK CHECK
         MVI   ERASEALL,X'1E'          RESET RD CKD OP CODE.
         LA    GR6,8                   SET UP COMPARE FOR EOF ON R1.
         CR    GR5,GR6                 COUNT FOR R1 EIGHT FOR EOF REC.
         BNE   HAR0R1B                 NO--DO NOT RESET OP CODE.
         MVI   ERASEALL,X'12'          YES--R1 IS EOF.RESET OP CODE.
HAR0R1B  MVC   READR0+1(3),DATBUFPT+1  RESET BUFFER ADDRESS.
         MVC   READREC0+1(3),DATBUFPT+1 RESET BUFFER ADDRESS.
         B     CCWOUT                  GO TO WRITE R0-R1 ON DA.
TOTAPE   LA    GR3,TAPECCW4            GET ADDRESS OF CCWS.
         ST    GR3,TOCCWAD             STORE CCW ADDRESS IN IOB.
         L     GR4,DATBUFPT            ADDRESS R0-R1 DATA.
         ST    GR4,8(GR3)              STORE INTO CCW.
         MVI   8(GR3),X'01'            RESET OP CODE.
         SR    GR5,GR5                 CLEAR OUT THE WORK REGISTER.
         IC    GR5,13(GR4)             KEY LENGTH FOR R1.
         AH    GR5,14(GR4)             ADD DATA LENGTH FOR R1.
         LA    GR5,16(GR5)             ADD DATA LENGTH R0, COUNT R1.
         STH   GR5,14(GR3)             STORE TOTAL COUNT IN CCW.
         AR    GR4,GR5                 POINT TO END OF DATA.
CCWOUT1  CLI   DEVSW,X'F0'             DUMPING TO SYSOUT.
         BNE   CCWOUT1A                NO-GO WRITE CCWS AND DATA. 0811
         BAL   GR8,LINKDOUT            YES--GO LINK TO IEHDAOUT.
         B     OUT4                    CONTINUE WITH NEXT TRACK.
CCWOUT1A EQU   *                                                  0811
         TM    SWITCH,TRKOVFL          TRACK OVERFLOW.            0811
         BZ    CCWOUT                  NO-DO NOT RESET OP CODE.   0811
         L     GR1,TAPECCW4            POINT TO CCWS TO BE WRITTEN.0811
         MVI   48(GR1),X'01'           WRITE SPECIAL OP CODE.     0811
CCWOUT   EXCP  TOIOB                   EXECUTE CHANNEL PROGRAM.
         OC    COPYPTR(4),COPYPTR      ARE THERE COPIES.
         BC    8,OUT3                  NO--GO TO AWAIT COMPLETION.
         L     GR3,TOCCWAD             ADDRESS OF CCWS.
         L     GR8,COPYPTR             POINTER TO FIRST COPY BLOCK.
         USING COPYBLK,GR8
OUT1     LA    GR9,CIOBLOCK            ADDRESS THIS COPY I/O BLK.
         ST    GR3,TOCCWAD             CCW ADDR TO THIS COPY IOB.
         EXCP  TOIOB                   WRITE CCWS AND DATA ON TAPE.
         OC    CCOPYPTR(4),CCOPYPTR    ARE THERE MORE COPIES.
         BZ    OUT2                    NO--GO TO AWAIT COMPLETION.
         L     GR8,CCOPYPTR            YES-POINTER TO NEXT COPY BLOCK.
         B     OUT1                    GO WRITE ON THE NEXT DEVICE.
OUT2     LA    GR9,BLOCKS              ADDRESSING FOR PRIMARY DEVICES.
OUT3     BAL   GR14,WAITTO             GO TO AWAIT COMPLETION.
         B     OUT4                     RETURN TO IEHDEXCP
         EJECT
***********************************************************************
*   THIS SECTION MOVES EITHER THE HA,R0 ONLY CCWS OR THE HA,R0,R1     *
*   CCWS TO A BUFFER TO BE EXECUTED ON DIRECT ACCESS DEVICES.         *
*   ADDRESSES ARE RESET WHERE NECESSARY                               *
***********************************************************************
SETUPCCW L     GR8,WRTBUFPT            ADDRESS OF CCW BUFFER.
         EX    GR7,MOVECCWS            MOVE NEEDED CCWS TO BUFFER.
         ST    GR8,TOCCWAD             STORE CCW ADDRESS IN IOB.
         TM    SEQSW+D1,RPSFEAT    RPS ON TODD                   S20201
         BZ    SETUP               NO                            S20201
         L     GR3,CCWBUFPT        PT TO CCW BUFR                S20201
         SH    GR3,SIXTEEN         PT TO RPS BUFR                S20201
         ST    GR8,D8(GR3)         RESET TIC ADDR                S20201
         MVI   D8(GR3),TICCODE     RESET TIC OP                  S20201
         ST    GR3,TOCCWAD         STORE RPS PTR IN IOB          S20201
SETUP    EQU   *                        *                        S20201
         USING R0CCWS,8
         LA    GR3,TOSEEK+3            ADDRESS FOR SEARCH.
         ST    GR3,SEARCHR0            STORE INTO CCW.
         LA    GR3,SEARCHR0            POINTER TO THIS CCW.
         MVI   0(GR3),X'31'            RESET OP CODE.
         ST    GR3,TICBACK             STORE ADDRESS IN TIC.
         MVI   TICBACK,X'08'           RESET TIC OP CODE.
         LA    GR3,TOSEEK+3            ADDRESSS SEARCH ARGUMENT.
         ST    GR3,SEARCH1             STORE IN CCW.
         LA    GR3,SEARCH1             ADDRESS SEARCH CCW.
         MVI   0(GR3),X'31'            RESET OP CODE.
         ST    GR3,TICAGAIN            STORE ADDRESS IN TIC.
         MVI   TICAGAIN,X'08'          RESET TIC OP CODE.
         BR    GR14                    RETURN
***********************************************************************
* THIS SECTION BUILDS THE READ CKD CCWS WHEN HAVE MORE THAN R1 ON TRACK
***********************************************************************
CNGRDCKD L     GR2,DATBUFPT            SET UP TO COMPUTE R1 TOTAL
         LA    GR2,8(GR2)              BUMP POINTER TO R1.
         LR    GR3,GR2                 SET 2ND REGISTER FOR ALIGN RTN.
         BAL   GR15,ALIGN              GO TO ALIGN ON DBLE WORD BDRY.
         L     GR3,CNTBUFPT            ADDRESS COUNT BUFFER.
         L     GR2,SAVESLOT            RESTORE ALIGNED POINTER.
         L     GR8,CCWBUFPT            CCW BUFF ADDRESS.
         MVC   0(8,GR2),0(GR3)         MOVE R2 COUNT FIELD TO DATA BUFF
         MVC   OUTSEEK+3(5),0(GR3) GET NEXT COUNT                A29477
         LA    GR2,8(GR2)              INCREMENT DATA BUFF PTR.
         OC    5(3,GR3),5(GR3)         IS R2 COUNT EQUAL TO ZERO.
         BC    7,NOTEOF                NO--R2 IS NOT AN EOF RECORD.
         MVC   16(8,GR8),8(GR8)        YES--MOVE IN TIC CCW.
         LA    GR6,24(GR8)             INCREMENT TO PTR TO 4TH CCW.
         ST    GR6,16(GR8)             STORE FORWARD TIC ADDRESS.
         MVI   16(GR8),X'08'           RESET TIC OP CODE.
         OI    SWITCH,R2EOF            SET R2=EOF SWITCH FOR LATER.
         LA    GR8,16(GR8)             POINT TO LAST CCW SET UP.
         LM    GR4,GR5,DMMYRCCW        DUMMY READ CCW.
         LR    GR4,GR2                 SET DATA BUFF PTR IN CCW.
         B     CONTINUE                GO TO CHECK IF R3 THIS TRACK.
         EJECT
**********************************************************************
*        FUNCTION: TEST COUNT FIELDS OF ALL RECORDS READ, TO DETERMINE
*              IF SAID COUNT FIELDS ARE IN ERROR. THAT IS, WILL THE
*              COUNT FIELDS CAUSE MORE DATA TO BE READ INTO CORE THAN
*              THE IEHDASDR BUFFER (DATBUFPT) WILL HOLD.
*              THE 'CCHH' PORTION OF COUNT FIELD WILL ALSO BE
*              CHECKED AGAINST THE INITIAL SEEK FIELD ARGUMENT, THEY
*              MUST MATCH OR IEH869I ERROR MESSAGE WILL BE ISSUED.
*              WORK REGISTERS: 1,2,3,5,6,8
*              RETURN ON REG 15
**********************************************************************
         USING TESTCNT,GR3                                      SA53223
         USING CONSTANT,GR5                                     SA53223
CHKCNTS  EQU   *                                                SA53223
         SR    GR6,GR6                  ZERO WORK REG           SA53223
         L     GR3,DATBUFPT             LOAD ADDR R1 CNT        SA53223
         LA    GR3,8(GR3)               INCRE PAST R0 DATA      SA53223
         L     GR5,IODEVCON             LOAD DEVICE CONSTNT PTR SA53223
         L     GR1,OUTSTAT              GET IOBCSW CCW PTR      SA53223
         LA    GR1,0(GR1)               ZERO HIGH BYTE          SA53223
         S     GR1,EIGHT                BACKUP TO FAILING CCW  @ZA13746
         L     GR1,0(GR1)               LOAD END OF CNT ADDR    SA53223
         LA    GR1,0(GR1)               ZERO HIGH BYTE          SA53223
         SR    GR8,GR8                  ZERO TRACK TOTAL REG    SA53223
COMPSK   EQU   *                                                SA53223
         SR    GR2,GR2                  ZERO RECORD-TOTAL REG   SA53223
         IC    GR2,TESTKEY              GET KEY LENGTH          SA53223
         LA    GR2,L8(GR2)              ADD CNT FLD LENGTH      SA53223
         MVC   HAFWD(L2),TESTDL         MOVE DATA-LENGTH FIELD  SA53223
         A     GR2,FULWD                ADD DATA LGTH TO        SA53223
*                                       KEY LGTH + CNT FLD LGTH SA53223
         AR    GR8,GR2                  ADD THIS RECORDS' LGTH  SA53223
*                                       TO ALL OTHER RECS ON    SA53223
*                                       THIS TRK .              SA53223
         CH    GR8,DATASIZE             IS TOTAL GT BUFFER SIZE SA53223
         BH    BADCNT                   IF HIGH, ISSUE MSG      SA53223
         CLC   CCHHONE(L4),TESTCNT      DOES THIS RECORDS'      SA53223
*                                       'CCHH' MATCH THIS TRKS' SA53223
*                                       'CCHH'  ?               SA53223
         BNE   BADCNT                   IF NOT, ISSUE MSG69     SA53223
         LTR   GR6,GR6                  FRIST TIME THRU ?       SA53223
         BNZ   CHKCNT                   NO, NOT 1ST TIME THRU   SA53223
         CLC   AREA+1(3),OUTSTAT+1      ONLY R1 THIS TRK ?      SA53223
         BE    LEAVE                    YES, EXIT               SA53223
         L     GR3,CNTBUFPT             LOAD COUNT FLD BUFF PTR SA53223
         LA    GR6,LEAVE                SET FIRST TIME SWT      SA53223
         B     COMPSK                   GO TOTAL R2-RN CNT FLDS SA53223
CHKCNT   EQU   *                                                SA53223
         LA    GR3,L8(GR3)              INCRE COUNT FIELD PTR   SA53223
         CR    GR3,GR1                  REACHED END OF COUNT    SA53223
*                                       FIELD BUFFER YET ?      SA53223
         BL    COMPSK                   IF NOT, CHK NEXT COUNT  SA53223
LEAVE    EQU   *                                                SA53223
         L     GR2,CCWBUFPT             RELOAD WORK REG         SA53223
         L     GR6,AREA                 RELOAD WORK REG         SA53223
         BR    GR15                     RETURN                  SA53223
BADCNT   EQU   *                                                SA53223
         LA    GR1,MSG69                SET MESSAGE NO.         SA53223
         BAL   GR8,SETUPMSG             GO LINK TO IEHDMSGB     SA53223
         L     GR8,CCHHONE              LOAD CURRENT TRK ADDR   SA53223
         ST    GR8,CONVAD               STORE CURRENT TRK ADD  @ZA06534
         UNPK  CONVAD(9),CONVAD(5)      UNPK CCHH IN ERROR     @ZA06534
         TR    CONVAD(L8),TRT-240       TRANSLATE CCHH         @ZA06534
         SH    GR1,OFFSET               ADJUST LINE PTR ADDR    SA53223
         MVC   D0(L8,GR1),CONVAD        MOVE CCHH TO MSG       @ZA06534
         MVC   18(L8,GR1),DDNAME1      MOVE DDNAME TO MSG       SA53223
         B     MSGWRT                   GO SET BRCH TABLE VALUE SA53223
*                                       FOR IEHDEXCP.           SA53223
TRT      DC    C'0123456789ABCDEF'      TRANSLATE TABLE         SA53223
SETUPMSG EQU   *                                                SA53223
         LINK  EP=IEHDMSGB                                      SA53223
         BR    GR8                      RETURN                  SA53223
         DROP  GR3                                              SA53223
         DROP  GR5                                              SA53223
         EJECT
NOTEOF   LM    GR4,GR5,DMMYRCCW        DUMMY READ CCW.
         IC    GR5,5(GR3)              R2 KEY LENGTH.
         AH    GR5,6(GR3)              R2 DATA LENGTH.
         LR    GR4,GR2                 SET DATA BUFF PTR IN CCW.
         LA    GR8,16(GR8)             POINT TO FIRST READ CCW.
         STM   GR4,GR5,0(GR8)          STORE RD KEY/DATA R2 CCW.
         MVI   0(GR8),X'0E'            RESET OP CODE.
         MVI   4(GR8),X'40'            RESET FLAGS.
CONTINUE LA    GR6,8                   SET UP INCREMENTING FACTOR.
         AR    GR3,GR6                 POINT TO NEXT COUNT FIELD.
         LA    GR5,0(GR5)              CLEAR HIGH ORDER BYTE.
         AR    GR4,GR5                 ADD R2 COUNT TO DATA PTR.
         L     GR7,OUTSTAT             LOAD ADDRESS LAST RD CNT CCW.
         LA    GR7,0(GR7)              CLEAR HIGH ORDER BYTE.
         SH    GR7,THIRTY2             POINT TO LAST CCW EXECUTED.
         CR    GR7,GR8                 JUST HA THRU R2 THIS TRACK.
         BE    CCOFF                   YES-GO TO READ IN R2 KEY/DATA.
         NI    SWITCH,X'BF'            SET R2=EOF SWITCH OFF.
         AR    GR2,GR5                 ADD R2 KEY/DATA LENGTH TO PTR.
         BAL   GR15,ALIGNA             PROCESS FOR DBLE WORD BOUNDARY.
         AR    GR8,GR6                 INCREMENT CCW POINTER.
NEWRDCKD L     GR4,SAVESLOT            POINTER INTO CCW.
         SR    GR5,GR5                 CLEAR WORK REGISTER.
         IC    GR5,5(GR3)              GET THE KEY LENGTH FROM CNT BUF.
         AH    GR5,6(GR3)              ADD THE DATA LENGTH.
         AR    GR5,GR6                 ADD COUNT LENGTH.
         O     GR5,MSKFLGON            SET THE FLAGS.
         STM   GR4,GR5,0(GR8)          STORE CCW IN LIST.
         MVI   0(GR8),X'1E'            RESET OP CODE.
         CLC   6(2,GR3),ZERO           IS DATA LENGTH = 0.
         BNE   ZEROFLG                 NO,NOT EOF RECORD.
         CLI   5(GR3),X'00'            IS KEY LENGTH = ZERO.
         BNE   ZEROFLG                 NO,NOT EOF RECORD.
         MVI   0(GR8),X'12'            EOF RECORD, CHANGE OP CODE.
ZEROFLG  LA    GR4,0(GR4)              CLEAR OUT OP CODE.
         LA    GR5,0(GR5)              CLEAR OUT FLAGS.
         AR    GR4,GR5                 ADD COUNT TO ADDRESS.
         ST    GR4,SAVESLOT            STORE FOR ALIGNMENT ROUTINE.
         BAL   GR15,ALIGNB             GO TO CHECK DOUBLE WORD BNDRY.
         AR    GR3,GR6                 BUMP COUNT FIELD POINTER.
         BXLE  GR8,GR6,NEWRDCKD        UPDATE CCW ADDRESS POINTER.
CCOFF    ST    GR4,TOTAL               STORE END OF DATA POINTER.
         TM    SWITCH,R2EOF            IS R2=EOF SWITCH STILL ON.
         BO    EOFONR2                 YES--NO EXCP THIS TRACK.   0811
*        L     GR5,4(GR7)              COUNT FIELD OF LAST CCW. SA51474
*        LA    GR5,1(GR5)              INCREMENT COUNT TO FORCE SA51474
*                                      TRACK OVERFLOW IF TRUE.  SA51474
*        ST    GR5,4(GR7)              COUNT TO LAST RD CCW.    SA51474
CCOFF1   MVI   4(GR7),X'20'            SET CC OFF IN LAST CCW.
READCKD  EXCP  OUTIOB                  2ND PASS-READ IN CNT,KEY,DATA.
EOFONR2  CLI   DEVSW,X'FF'             DUMPING TO DIRECT ACCESS.
         L     GR1,WRTBUFPT            ADDR FOR WRT/RD CKD CCWS.
         L     GR6,CCWBUFPT            ADDR RD CKD CCWS.
         L     GR3,DATBUFPT            ADDRESS OF DATA BUFFER.
         BNE   TAPEOUT                 NO--TAPE--BRANCH.
         L     GR2,DATBUFPT            ADDR DATA BUFFER.
         BAL   GR14,RESET              GO TO SET UP WRT CKD CCWS.
         L     GR6,WRTBUFPT            ADDR. BEGINNING WRT CKD CCWS.
         L     GR3,DATBUFPT            INITIALIZE DATA POINTER.
         BAL   GR14,SKIPON             GO TO SET SKIP BIT ON.
         TM    SWITCH,R2EOF            WAS R2 EOF AND LAST REC THIS TRK
         BO    NOWAIT                  YES--NO WAIT TODAY.
         BAL   GR14,WAITDISK           GO TO WAIT COMPLETION ON RDCKD.
NOWAIT   L     GR3,WRTBUFPT            ADDR WRT/RD CKD CCWS.
         ST    GR3,TOCCWAD             STORE CCW ADD IN IOB.
         TM    SEQSW+D1,RPSFEAT    RPS ON TODD                   S20201
         BZ    NOWAIT1             NO                            S20201
         L     GR14,CCWBUFPT       PT TO CCW BUFR                S20201
         SH    GR14,SIXTEEN        PT TO RPS BUFR                S20201
         ST    GR14,TOCCWAD        PTR TO IOB                    S20201
         ST    GR3,D8(GR14)        RESET TIC ADDR                S20201
         MVI   D8(GR14),TICCODE    RESET TIC OP                  S20201
NOWAIT1  EQU   *                        *                        S20201
         TM    SWITCH,TRKOVFL          TRACK OVERFLOW.            0811
         BC    8,CKDOUT                NO-GO WRITE ON TO DEVICE.  0811
         BAL   GR15,SETWRTSP           YES-SET UP WRT SPECIAL CCW.0811
         B     CKDOUT                  GO EXEC WRT/RD CKD CCWS.
TAPEOUT  CLI   DEVSW,X'F0'             DUMPING TO SYSOUT.
         BNE   TAPEOUT1                NO--GO TO PROCESS FOR TAPE.
         TM    SWITCH,R2EOF            WAS SECOND PASS NECESSARY.
         BO    TAPEOUT2                NO---NO WAIT TODAY.
         BAL   GR14,WAITDISK           GO TO AWAIT COMPLETION.
TAPEOUT2 L     GR4,TOTAL               END OF DATA POINTER.
         BAL   GR8,LINKDOUT            LINK TO IEHDAOUT.
         B     PROC4                   CONTINUE WITH NEXT TRACK.
TAPEOUT1 LA    GR2,R0R1CCWS            ADDR. OF SEEK AND SFM CCWS.
         MVC   0(16,GR1),0(GR2)        MOVE INTO WRT BUFFER.
         LA    GR1,16(GR1)             RESET BUFFER POINTER.
         LH    GR2,BUFFADDR            S/A D/R BUFFER ADDRESS.
         BAL   GR14,RESET              GO TO RESET ADDR. AND OP CODES.
         L     GR6,WRTBUFPT            ADDR. BEGINNING WRT CKD CCWS.
         LA    GR6,16(GR6)             POINT TO SEARCH,TIC CCWS.
         LH    GR2,BUFFADDR            INITIALIZE S/A D/R BUFF ADDR.
         BAL   GR14,SKIPON             GO TO SET SKIP BIT ON.
         LA    GR3,TAPECCW2            ADDRESS OF WRITE CCW.
         L     GR2,WRTBUFPT            ADDR WRT/RD CKD CCWS.
         SH    GR2,SIXTEEN             PROVIDE PADDING FOR CCWS.
         SR    GR1,GR2                 GR1 HAS LENGTH WRT/RD CKD CCWS.
         ST    GR1,4(GR3)              STORE LENGTH OF CCW REC. IN CCW
         MVI   4(GR3),X'20'            RESET THE FLAGS.
         ST    GR2,0(GR3)              STORE BUFF ADDR. IN CCW.
         MVI   0(GR3),X'01'            RESET WRITE OP CODE.
         ST    GR3,TOCCWAD             STORE ADDRESS IN IOB.
         NI    TOFLGS,X'02'            INDICATE NO CMD CHAINING.
         EXCP  TOIOB                   WRITE OUT CCW REC ON TAPE.
         OC    COPYPTR(4),COPYPTR      ARE THERE COPIES.
         BC    8,SETSW                 NO--GO TO AWAIT COMPLETION.
         L     GR8,COPYPTR             POINTER TO FIRST COPY BLOCK.
         USING COPYBLK,8
TAPEOUTA LA    GR9,CIOBLOCK
         ST    GR3,TOCCWAD             CCW ADDRESS TO COPY IOB.
         EXCP  TOIOB                   WRITE CCWS TO TAPE COPY.
         OC    CCOPYPTR(4),CCOPYPTR    ARE THERE MORE COPIES.
         BZ    TAPEOUTB                NO--GO TO AWAIT COMPLETION.
         L     GR8,CCOPYPTR            POINTER TO NEXT COPY BLOCK.
         B     TAPEOUTA                GO WRITE ON THE NEXT DEVICE.
TAPEOUTB LA    GR9,BLOCKS              ADDRESSING FOR PRIMARY DEVICES.
SETSW    BAL   GR14,WAITTO             GO TO AWAIT COMPLETION.
         TM    SWITCH,R2EOF            WAS R2 EOF AND LAST REC THIS TRK
         BO    NOWAIT2                 YES--NO WAIT TODAY.
         BAL   GR14,WAITDISK           WAIT ON READ CKD CCWS.
NOWAIT2  L     GR1,TOTAL               END OF DATA POINTER.
         L     GR2,DATBUFPT            BEGINNING OF DATA BUFFER.
         SR    GR1,GR2                 GR1 HAS LENGTH OF DATA.
         LA    GR3,TAPECCW2        ADDRESS OF WRITE CCW.           0161
         TM    SWITCH,TRKOVFL          TRACK OVERFLOW.            0811
         BC    8,NOWAIT3               NO-CONTINUE.               0811
         L     GR3,WRTBUFPT            POINT TO WRT/RD CCWS.      0811
         LA    GR3,16(GR3)             INDEX PAST SEEK,SFM CCWS.  0811
         BAL   GR15,SETWRTSP           YES-SET UP WRT SPECIAL CCW.0811
         OI    TOFLGS,X'42'            SET CMD. CHAINING IN IOB.  0811
         L     GR3,CNTBUFPT            BUFFER POINTER.            0811
         ST    GR3,TOCCWAD             STORE ADDRESS IN IOB.      0811
         MVI   0(GR3),X'0C'            SET OP CODE IN CCW.        0811
         MVC   1(3,GR3),CCWBUFPT+1     ADDRESS FOR READ BACKWARD. 0811
         MVI   4(GR3),X'70'            SET FLAGS IN CCW.           2920
         MVC   5(3,GR3),TAPECCW2+5     LENGTH OF CCW RECORD.      0811
         MVC   8(8,GR3),TAPECCW2       WRITE CCW RECORD CCW.      0811
         MVI   12(GR3),X'60'           CHAINING.                  0811
         MVC   16(8,GR3),TAPECCW2      WRITE DATA RECORD CCW.     0811
         LA    GR3,16(GR3)             POINT TO WRT DATA CCW.     0811
NOWAIT3  ST    GR1,4(GR3)              DATA LENGTH TO CCW.        0811
         MVI   4(GR3),X'20'            RESET THE FLAGS.
         ST    GR2,0(GR3)              STORE BUFFER ADDR IN CCW.
         AIF   ('&LIB' EQ 'LIB1').OSASSEM  BRCH IF OS ASSSEMBLY Y02113
*        TIMESTAMP WILL BE PLACED INTO FORMAT 4 DSCB HERE.      Y02113
         TM    TIMESET,TIMECHK          HAS F4 DSCB BEEN UPDATED Y02113
         BO    TIMEOK                   YES, DON'T CHECK        Y02113
         CLC   OUTSEEK+D3(L4),TIMESTMP  IS THIS THE F4 DSCB     Y02113
         BNE   TIMEOK                   NO, DON'T STORE CLOCK   Y02113
         STCK  CLOCK(GR2)               PUT TIMESTAMP IN DSCB   Y02113
         OI    TIMESET,TIMECHK          SET TIME CHECK SWT      Y02113
TIMEOK   EQU   *                                                Y02113
.OSASSEM ANOP  BRCH POINT FOR OS ASSEM.                         Y02113
         MVI   0(GR3),X'01'            RESET OP CODE.
         MVI   R,0                     SET SEARCH ARG. FOR REC ZERO.
CKDOUT   EXCP  TOIOB                   EXEC OR WRITE WRT/RD CKD CCWS.
         OC    COPYPTR(4),COPYPTR      ARE THERE COPIES.
         BC    8,PROC3                 NO--GO TO AWAIT COMPLETION.
         L     GR3,TOCCWAD             POINTER TO CCWS.
         L     GR8,COPYPTR             POINTER TO FIRST COPY BLOCK.
         USING COPYBLK,GR8
PROC1    LA    GR9,CIOBLOCK
         ST    GR3,TOCCWAD             STORE CCW PTR. IN COPY IOB.
         EXCP  TOIOB                   EXEC OR WRT CCWS.
         OC    CCOPYPTR(4),CCOPYPTR    ARE THERE MORE COPIES.
         BZ    PROC2                   NO--GO TO AWAIT COMPLETION.
         L     GR8,CCOPYPTR            YES-POINTER TO NEXT COPY BLOCK.
         B     PROC1                   GO WRITE ON NEXT DEVICE.
PROC2    LA   GR9,BLOCKS               ADDRESSING FOR PRIMARY DEVICES.
         TM    SW1,MULTIPLE            ANY REASON TO GO BACK TO MONITOR
         BZ    PROC3                   NO--GO TO WAIT.
         LA    GR3,PROC3               RETURN ADDRESS.
         ST    GR3,FRTNPTR             SAVE IN RETURN POINTER.
         LA    GR15,0                  RETURN CODE FOR MONITOR.
         B     MULTPROC                RETURN.
PROC3    BAL   GR14,WAITTO             GO TO AWAIT COMPLETION.
         CLI   DEVSW,X'FF'              IS THIS A DISK TO DISK   A37209
*                                       DUMP                     A37209
         BNE   PROC4                    NO, MUST BE TAPE         A37209
*        READ IN VOLUME ID IF FIRST ENTRY                             *
         CLI   IODEVCON,X'05'           IS THIS A 2321           A52430
         BE    PROC4                    YES, GO TO PROC4         A52430
         TM    FLGBYT1,BIT7             IS THIS THE FIRST ENTRY XL03130
         BO    PROC4                    YES, CONTINUE PROCESSING A37209
         OI    FLGBYT1,BIT7             SET FIRST ENTRY SWITCH  XL03130
         LA    GR3,SRCHKEYR             ADDR OF CCW TO READ IN   A37209
*                                         RECORD THREE
         ST    GR3,TOCCWAD              SET UP IOB               A37209
         XC    TOSEEK+3(4),TOSEEK+3     CLEAR SEEK ADDR          A37209
         BAL   GR3,VOLIDCP              GO ISSUE CHANNEL PROGRAM A37209
*        REPLACE NEW VOLUME ID WITH THE ORIGINAL                      *
         L     GR3,TUCBPTR              GET PTR TO UCB           A37209
         MVC   RDVOLBUF+4(6),28(GR3)    PUT ORIGINAL IN BUFFER   A37209
         LA    GR3,SRCHKEYW             ADDR OF CCW TO WRITE OUT A37209
*                                         RECORD THREE
         ST    GR3,TOCCWAD              SET UP IOB               A37209
         OC    COPYPTR(4),COPYPTR       ANY COPIES               A37209
         BZ    GOWRT                    NO, CONTINUE             A37209
*        SET UP COPY BLOCKS                                           *
         L     GR8,COPYPTR                                       A37209
         USING COPYBLK,8                                         A37209
LOOPWRT  EQU   *                                                 A37209
         LA    GR9,CIOBLOCK                                      A37209
         ST    GR3,TOCCWAD              POINT TO CHANNEL PROGRAM A37209
         OC    CCOPYPTR(4),CCOPYPTR     ANY COPIES               A37209
         BZ    GOWRT                    NO, GO WRITE COPIES      A37209
         L     GR8,CCOPYPTR             POINT TO NEXT COPY BLOCK A37209
         DROP  8                                                 A37209
         B     LOOPWRT                  GO SET UP NEXT COPY      A37209
*        EXECUTE CHANNEL PROGRAM TO WRITE OUT FIRST VOLUME ID         *
GOWRT    EQU   *                                                 A37209
         LA    GR9,BLOCKS               PRIMARY I/O BLOCKS       A37209
         BAL   GR3,VOLIDCP              GO ISSUE CHANNEL PROGRAM A37209
         MVC   TOSEEK+3(4),CCHHONE      CCHH OF FIRST TRACK      A37209
         OC    COPYPTR(4),COPYPTR       MORE COPIES              A37209
         BZ    ENDVOLID                 NONE LEFT                A37209
*        EXECUTE CHANNEL PROGRAM FOR COPIES                           *
         L     GR8,COPYPTR                                       A37209
         USING COPYBLK,GR8                                       A37209
COPYIT   LA    GR9,CIOBLOCK                                      A37209
         XC    TOSEEK+3(4),TOSEEK+3     CLEAR SEEK ADDR          A37209
         L     GR3,CUCBPTR              GET ADDR OF COPY UCB PTR A37209
         MVC   RDVOLBUF+4(6),28(GR3)    MOVE IN NEXT VOLID       A37209
         BAL   GR3,VOLIDCP              GO ISSUE CHANNEL PROGRAM A37209
         MVC   TOSEEK+3(4),CCHHONE      CCHH OF FIRST TRACK      A37209
         OC    CCOPYPTR(4),CCOPYPTR     MORE COPIES              A37209
         BZ    ENDVOLID                 NONE LEFT                A37209
         L     GR8,CCOPYPTR                                      A37209
         B     COPYIT                   MAKE NEXT COPY           A37209
*        SHARED EXCP, WAIT, AND TEST FOR I/O ERROR                    *
VOLIDCP  EQU   *                                                 A37209
         EXCP  TOIOB                                             A37209
         WAIT  ECB=TOECB                                         A37209
         CLI   TOECB,GOOD               CHECK FOR GOOD I/O       A37209
         BNE   IOTERR                   YES                      A37209
         BR    GR3                      RETURN TO                A37209
ENDVOLID LA    GR9,BLOCKS                                        A37209
         B     PROC4                    RETURN TO IEHDEXCP
         SPACE                                                    0811
SETWRTSP LA    GR3,24(GR3)             POINT TO WRT R1 CCW.       0811
         L     GR0,AREA                LENGTH OF CCW'S > R2.      0811
         AR    GR3,GR0                 POINT TO LAST WRT CCW.     0811
         MVI   0(GR3),X'01'            WRITE SPECIAL OP CODE.     0811
         BR    GR15                    RETURN TO CALLER.          0811
         EJECT
***********************************************************************
*                                                                     *
*   THIS SECTION BUILDS THE WRITE/READ COUNT/KEY/DATA CCWS TO BE      *
*        EXECUTED FOR DIRECT ACCESS OUTPUT OR WRITTEN OUT AS A CCW    *
*        RECORD FOR TAPE OUTPUT.                                      *
*                                                                     *
*        REGISTER 1 MUST CONTAIN POINTER TO THE WRT BUFF.             *
*        REGISTER 3 MUST CONTAIN POINTER TO THE DATA BUFF.            *
*        REGISTER 6 MUST CONTAIN POINTER TO THE CCW BUFF.             *
*        REGISTER 7 WILL CONTAIN ADDRESS OF LAST RD CKD CCW.          *
*                                                                     *
***********************************************************************
RESET    LA    GR5,CCWA                ADDR SEARCH R0,TIC,WRT CKD R1.
         MVC   0(24,GR1),0(GR5)        MOVE THREE CCWS TO WRT BUFF.
         MVC   24(8,GR1),16(GR5)       MOVE IN 2ND WRT CKD CCW FOR R2.
         LA    GR4,2                   SET UP LOOP CONTROL.
         LA    GR3,8(GR3)              BUMP DATA POINTR TO REC ONE.
         CLI   DEVSW,X'FF'             DUMPING TO DIRECT ACCESS.
         BNE   RESET1                  NO-TAPE-DO NOT RESET ADDRESS.
         LA    GR0,TOSEEK+3            ADDRESS FOR SEARCH.
         ST    GR0,0(GR1)              STORE ADDRESS IN SEARCH CCW.
         MVI   0(GR1),X'31'            RESET OP CODE.
         ST    GR1,8(GR1)              RESET TIC ADDRESS.
         MVI   8(GR1),X'08'            RESET TIC OP CODE.
RESET1   LA    GR1,16(GR1)             RESET WRT BUFF PTR.
         SR    GR7,GR6                 GR7 HAS LENGTH OF RD CKD CCWS.
         L     GR5,TICADDR              LOAD TIC ADDRESS         S20201
         AR    GR5,GR7                  ADD RD CKD LENGTH TO TIC S20201
         ST    GR5,CCWB+8               TIC ADDR                 M5599
         MVI   CCWB+8,X08               TIC OP                   M5599
         LA    GR2,8(GR2)              RESET DATA BUFF PTR TO REC ONE.
RESET11  ST    GR2,0(GR1)              ST R1 DATA PTR IN CCW.
         SR    GR5,GR5                 CLEAR WORK REGISTER.
         IC    GR5,5(GR3)              KEY LENGTH.
         AH    GR5,6(GR3)              PLUS DATA LENGTH.
         LA    GR5,8(GR5)              PLUS COUNT LENGTH.
         STH   GR5,6(GR1)              STORE NEW COUNT INTO CCW.
         AR    GR3,GR5                 ADD R1 LENGTH.
         AR    GR2,GR5                 ADD R1 LENGTH TO DATA PTR.
         ST    GR3,SAVESLOT            STORE TO SEE IF DOUBLE WORD.
         BAL   GR15,ALIGNB             GO TO CHECK DOUBLE WORD ALIGNMT.
         L     GR3,SAVESLOT            POINTER BACK INTO REGISTER.
         BAL   GR15,ALIGNA             GO TO CHECK DOUBLE WORD ALIGNMT.
         L     GR2,SAVESLOT            POINTER BACK INTO REGISTER.
         MVI   0(GR1),X'1D'            RESET OP CODE.
         LA    GR1,8(GR1)              RESET WRT BUFF POINTER.
         BCT   GR4,RESET11             LOOP THRU TO PROCESS R2.
RESETA   LA    GR6,24(GR6)             INCREMENT RD CKD PTR TO R3.
         LA    GR8,16                  DECREMENT FACTOR.
         SR    GR7,GR8                 SUBTRACT FROM LENGTH COUNTER.
         ST    GR7,AREA                STORE LENGTH OF CCW'S > R2.0811
*                                      IF ZERO,CLEAR WORK AREA.
         LTR   GR7,GR7                 MORE THAN TWO RECORDS.
         BCR   8,GR14                  NO-2 RECS-RETURN.          0811
         LR    GR8,GR1                 SET UP FOR EXECUTE.
         SPACE
         BAL   GR15,RESETCCW           MOVE CCWS FOR WRITE.
         OI    4(GR7),X'60'            SET FLAGS IN LAST CCW.
RESETB   CLI   DEVSW,X'FF'             DUMPING TO DIRECT ACCESS.
         BE    RESETC                  YES--DO NOT CHANGE DATA PTR.
         ST    GR2,0(GR1)              STORE DATA BUFF PTR INTO CCW.
         LH    GR5,6(GR1)              INSERT COUNT FROM CCW.
         AR    GR2,GR5                 ADD COUNT TO DATA POINTER.
         BAL   GR15,ALIGNA             GO TO CHECK DOUBLE WORD BDRY.
         L     GR2,SAVESLOT            POINTER BACK INTO REGISTER.
RESETC   MVI   0(GR1),X'1D'            SET WRT OP CODE.
         BXLE  GR1,GR6,RESETB          UPDATE CCW ADDRESS POINTER
         BR    GR14                    RETURN.                    0811
SKIPON   LA    GR5,CCWB                ADDR SEARCH,TIC,WRT DATA CCWS.
         MVC   0(24,GR1),0(GR5)        MOVE THREE CCWS TO WRT BUFF.
         CLI   DEVSW,X'FF'             DUMPING TO DIRECT ACCESS.
         BNE   SKIPONN                 NO--TAPE--DO NOT RESET PTRS.
         LA    GR0,TOSEEK+3            ADDRESS FOR SEARCH.
         ST    GR0,0(GR1)              STORE ADDRESS IN SEARCH CCW.
         MVI   0(GR1),X'31'            RESET OP CODE.
         ST    GR1,8(GR1)              RESET TIC ADDRESS.
         MVI   8(GR1),X'08'            RESET TIC OP CODE.
         ST    GR3,16(GR1)             STORE DATA POINTER.
         MVI   16(GR1),X'05'           RESET WRT DATA OP CODE.
SKIPONN  LA    GR1,24(GR1)             BUMP POINTER TO R1,R2 CCWS.
         MVC   0(16,GR1),16(GR6)       MOVE IN R1,R2 CCWS.
         L     GR7,AREA                LENGTH OF CCW'S > R2.      0811
         LTR   GR7,GR7                 GREATER THAN R2 THIS TRK.
         LA    GR15,SKIPONE            RETURN ADDRESS.
         BZ    SETCCWS                 NO--GO SET REGS FOR BXLE.
         LA    GR6,32(GR6)             YES-BUMP PTR TO WRT CKD R3.
         LA    GR8,16(GR1)             POINT TO NEXT AVAILABLE SLOT.
         BAL   GR15,RESETCCW           MOVE CCWS FOR RD BACK CHECK.
SKIPONE  EQU   *                                                   0161
         AH    GR7,SIXTEEN         INCRM FOR BXLE LOOP.            0161
SKIPONE1 EQU   *                                                   0161
         LH    GR5,6(GR1)          LOAD CNT FROM THIS CCW.         0161
         CR    GR5,GR6                 IS COUNT ONLY 8 FOR EOF REC.
         BNE   SKIPONF                 NO--GO SET RD CKD OP CODE.
         MVI   0(GR1),X'12'            YES-SET RD COUNT OP CODE.
         B     SKIPONF1                GO TO SET SKIP BIT ON.
SKIPONF  MVI   0(GR1),X'1E'            SET RD CKD OP CODE FOR NON-EOF.
SKIPONF1 MVI   4(GR1),X'70'            SET SKIP BIT ON FOR RD BACK CHK.
         BXLE  GR1,GR6,SKIPONE1    LOOP THRU UNTIL DONE.           0161
         MVC   0(8,GR1),CCWB+32        MOVE IN READ BACK CHK R0 CCW.
         CLI   DEVSW,X'FF'             DUMPING TO DIRECT ACCESS.
         BNE   SKIPONG                 NO--DO NOT RESET PTRS.
         L     GR0,DATBUFPT            DATA BUFFER ADDRESS.
         ST    GR0,0(GR1)              STORE INTO CCW.
         MVI   0(GR1),X'16'            RESET OP CODE.
SKIPONG  AR    GR1,GR6                 POINT TO END OF WRT/RD CCWS.
         BR    GR14                    RETURN.
         SPACE
RESETCCW LA    GR5,255                 SET NO. OF BYTES FOR EXEC.
CHKMVC   CR    GR7,GR5                 WILL THIS BE THE LAST MOVE.
         BNH   LASTMVC                 YES--BRANCH.
         EX    GR5,MOVECCWS            MOVE 256 CHARACTERS.
         LA    GR8,256(GR8)            INCREMENT MOVE TO POINTER.
         LA    GR6,256(GR6)            INCREMENT MOVE FROM POINTER.
         SR    GR7,GR5                 DECREMENT TOTAL LENGTH.
         B     CHKMVC                  CONTINUE PROCESSING.
LASTMVC  EX    GR7,MOVECCWS            MOVE CCWS TO WRT/RD CKD BUFF.
         L     GR7,AREA                RESTORE TOTAL LENGTH.      0811
SETCCWS  LA    GR6,8                   INCREMENTING FACTOR.
         AR    GR7,GR1                 SET END ADDRESS
         SR    GR7,GR6                    FOR BXLE LOOP.
         BR    GR15                    RETURN.
         SPACE
*
*  LINK TO IEHDAOUT HERE.
*
         SPACE
LINKDOUT EQU   *                                                SA53223
         LOAD  EP=IEHDPRNT                                      SA53223
         LR    GR2,GR0                  SAVE ADDR FOR IEHDAOUT  SA53223
         LR    GR3,GR4                  PASS END OF DATA POINTER.
         LA    GR1,CCHH                PASS PTR. TO CURRENT TRACK ADDR
         LINK  EP=IEHDAOUT                                      OX01980
         DELETE EP=IEHDPRNT             REDUCE CDE USE CNT.     OX01980
         BR    GR8                     RETURN TO CALLER.
         EJECT
*
* WAIT ON COMPLETION OF EXCP AGAINST 'FROM' DEVICE HERE.
*
         SPACE
WAITDISK TM    OUTECB,COMPLETE         IS THE OPERATION COMPLETE
         BO    TESTD0                  YES-GO TEST THE STATUS.     3651
         WAIT  ECB=OUTECB              NO--AWAIT COMPLETION OF DISK.
TESTD0   EQU   *                                                   3651
         LA    GR2,TESTD3              RETURN ADDRESS IF RECOVER   O122
         TM    OUTSTAT+4,X'02'         UNIT CHECK.                 7343
         BZ    TESTD01             NO,BRANCH                     M0025
         TM    OUTSENSE,DC             DATA CHECK.                 3651
         BO    IOERR                   YES-I/O ERROR.              3651
         TM    OUTSENSE+1,X'04'        FILE PROTECT.               7042
         BO    TESTD01                 YES-THAT'S OKAY.            7042
         TM    OUTSENSE,X'02'          TRACK CONDITION.            7042
         BZ    IOERR                   NO-I/O ERROR.               7042
TESTD01  EQU   *                                                   7042
         TM    OUTSENSE+1,X'01'        TRACK OVERFLOW.             7042
         BZ    TESTD                   NO-CHECK FOR EOF.         A31314
         OI    SWITCH,TRKOVFL          NO-MUST BE TRACK OVERFLOW.  3651
         BR    GR14                    RETURN TO CALLER.           3651
         SPACE
TESTD    EQU   *                                                  0811
         TM    OUTSTAT+4,X'01'         EOF INDICATION.
         BZ    TESTD6                  NO-SEE IF ALL OKAY.         3651
         L     GR3,CCWBUFPT            ADDRESS RD CKD CCWS.
         LA    GR3,16(GR3)             POINT TO RD KD R2 CCW.
         L     GR4,OUTSTAT             CSW ADDRESS.
         LA    GR6,8                   SET UP REGISTER FOR SUBTRACTION.
         SR    GR4,GR6                 POINT TO CCW CAUSING THE EOF.
         TM    4(GR4),CCH              COMMAND CHAINING.           3651
         BCR   8,GR14                  NO-RETURN TO CALLER.        3651
         CR    GR3,GR4                 WAS EOF ON R2.
         BE    TESTD2                  YES--SET UP FORWARD TIC.
         MVI   0(GR4),X'12'            NO---CHANGE CCW TO RD CNT.
         B     TESTD3                  GO TO RESTART CHANNEL PROGRAM.
TESTD2   SR    GR3,GR6                 POINT TO FIRST TIC CCW.
         MVC   0(8,GR4),0(GR3)         MOVE IN TIC CCW.
         LA    GR6,8(GR4)              COMPUTE FORWARD TIC ADDRESS.
         ST    GR6,0(GR4)              STORE ADDRESS IN TIC CCW.
         MVI   0(GR4),X'08'            RESET OP CODE.
TESTD3   EXCP  OUTIOB                  RESTART CHANNEL PROGRAM.
         B     WAITDISK                GO TO AWAIT COMPLETION.
TESTD6   EQU   *                                                  0811
         CLI   OUTECB,GOOD             ALL OK.                    0811
         BCR   8,GR14                  YES-RETURN TO CALLER.       7042
TESTD02  EQU   *                                                   7042
         CLI   OUTECB,X'41'            EXPECTED I/O ERROR.         7042
         BNE   IOERR                   NO-NOT EXPECTED.            7042
         TM    OUTSTAT+5,X'40'         INCORRECT LENGTH.           7042
         BZ    IOERR                   NO-I/O ERROR.               7042
         BR    GR14                    RETURN TO CALLER.           7042
         EJECT
*
* WAIT ON COMPLETION OF EXCP AGAINST 'TO' DEVICE HERE.
*
         SPACE
WAITTO   BAL   GR4,WAIT2               WAIT ON PRIMARY 'TO' DEVICE.
* CONTROL RETURNED HERE IF EOV HIT.
         OI    SWITCH,EOV              SET EOV SWITCH.
* CONTROL RETURNED HERE IF WAIT WAS GOOD.
         OC    COPYPTR(4),COPYPTR      ARE COPIES BEING MADE.
         BCR   8,GR14                  NO--RETURN.
         L     GR8,COPYPTR             YES-ADDRESS OF FIRST COPY BLOCK.
         USING COPYBLK,GR8
WAIT1    LA    GR9,CIOBLOCK            I/O BLOCKS FOR COPY.
         BAL   GR4,WAIT2               AWAIT COMPLETION OF THIS COPY.
* CONTROL RETURNED HERE IF EOV HIT.
         MVI   CERRSW,X'FF'            SET EOV SWITCH.
* CONTROL RETURNED HERE IF WAIT WAS GOOD.
         SPACE
         OC    CCOPYPTR(4),CCOPYPTR    ARE THERE MORE COPIES.
         BC    8,WAIT1A                NO--GO TO RETURN.
         L     GR8,CCOPYPTR            YES-ADDRESS NEXT COPY BLOCK.
         B     WAIT1                   GO WAIT ON NEXT COPY.
WAIT1A   LA    GR9,BLOCKS              PRIMARY I/O BLOCKS.
         BR    GR14                    RETURN.
         SPACE
WAIT2    TM    TOECB,COMPLETE          IS THE OPERATION COMPLETE.
         BO    WAIT3                   YES--GO TO TEST THE STATUS.
         WAIT  ECB=TOECB               NO--AWAIT COMPLETION
         SPACE 1
WAIT3    CLI   TOECB,GOOD              ALL OK.
         BNE   WAIT3A                  NO--GO TO SEE IF DA OUTPUT.
         LA    GR4,4(GR4)              YES--BUMP RETURN POINTER.
         BR    GR4                     RETURN.
WAIT3A   CLI   DEVSW,X'FF'             DUMPING TO DIRECT ACCESS.
         BNE   WAIT4                   NO--TAPE--BRANCH.
*
* PROCESS HERE IF DIRECT ACCESS OUTPUT.
*
         TM    TOSTATUS+4,X'01'        EOF ENCOUNTERED.
         BO    REPEAT                  YES--RESTART CCW CHAIN.
         TM    TOSENSE+1,X'08'         NO---TEST FOR NO RECORD FOUND.
         BZ    IOTERR                  NO---MUST BE I/O ERROR.
         L     GR3,TUCBPTR             ADDRESS OF UCB FOR 'TO' DEV.
         USING UCB,GR3
         AIF   ('&LIB' EQ 'LIB2').NO2321                        Y02113
         CLI   UCBID,X'FF'             IS THIS A SUB UCB(2321).
         BE    WAIT3B                  NO--MAIN UCB--
         LH    GR5,0(GR3)              BIN NUMBER.
         SLA   GR5,4                   TIMES SIXTEEN.
         LA    GR5,DATACELL-UCBOB(GR5) ADD LENGTH OF MAIN.
         LCR   GR5,GR5                 NEGATE.
         AR    GR3,GR5                 ADDRESS OF MAIN UCB.
.NO2321  ANOP                           BRCH POINT IF VS ASSEM   Y02113
WAIT3B   CLC   TOSEEK+3(4),UCBSEEK     SWITCHED TO NEXT TRK ?   YL02912
         BE    IOTERR                  NO--LEGITIMATE ERROR.
         DROP  3
         LA    GR3,CCWC                ADDRESS READ R0 CCW.
*
* THIS CODING IS USED ONLY WHEN INTERRUPTED WHILE DOING READ-BACK
* CHECKING AGAINST DA OUTPUT ON TRACK-OVERFLOW OR END-OF-FILE RECORDS.
*
REPEAT1  ST    GR3,TOCCWAD             RESET STARTING POINT IN IOB.
         EXCP  TOIOB                   FINISH THE CCW CHAIN.
         B     WAIT2                   GO TO AWAIT COMPLETION. @ZA26845
* EOF ON DISK ENCOUNTERED--PROCESS HERE.
REPEAT   L     GR3,TOSTATUS            END OF INETERRUPTED CCW.
         LA    GR5,8
         SR    GR3,GR5                 START OF INTERRUPTED CCW.
         TM    4(GR3),X'40'            IS THE CHAIN BIT ON.
         BC    8,REPEATA               NO--THIS CHAIN IS COMPLETE.
         LA    GR3,8(GR3)              YES--RESET THE START POINT.
         B     REPEAT1                 GO RESTART THE CCW CHAIN.
REPEATA  EQU   *                                                   6057
         CLI   DEVSW,X'FF'             DUMPING TO DA.              6057
         BCR   7,GR4                   NO-TAPE-RETURN.             6057
         LA    GR4,4(GR4)              YES-DA-BUMP RETURN POINTER. 6057
         BR    GR4                     RETURN.
*
* PROCESS HERE IF TAPE OUTPUT.
*
WAIT4    TM    TOSTATUS+4,X'02'        UNIT CHECK?             @ZA10908
         BO    IOTERR                  YES-MUST BE I/O ERROR.  @ZA10908
         TM    TOSTATUS+4,X'01'        EOV ENCOUNTERED.        @ZA10908
         BZ    IOTERR                  NO--MUST BE I/O ERROR.
         B     REPEAT                  GO SEE IF RESTART NEEDED.   6057
         SPACE 1                                                   O122
ALIGN    SR    GR5,GR5                 CLEAR WORK REGISTER.
         IC    GR5,5(GR3)              KEY LENGTH.
         AH    GR5,6(GR3)              ADD DATA LENGTH.
         LA    GR5,8(GR5)              ADD COUNT LENGTH.
         AR    GR2,GR5                 ADD RECORD LENGTH TO DATA PTR.
ALIGNA   ST    GR2,SAVESLOT            STORE TO SEE IF DOUBLE WORD.
ALIGNB   TM    SAVESLOT+3,X'07'        IS PTR ON DOUBLE WORD BOUNDARY.
         BCR   8,GR15                  YES--RETURN TO CALLER.
         NI    SAVESLOT+3,X'F8'        NO---FORCE TO DOUBLE WORD.
         L     GR5,SAVESLOT            POINTER BACK INTO REGISTER.
         LA    GR5,8(GR5)              ADD EIGHT AFTER FORCING DOUBLE.
         ST    GR5,SAVESLOT            STORE FOR FUTURE USE.
         BR    GR15                    RETURN.
         EJECT
*
*  RETURN TO IEHDDUMP HERE.
*
         SPACE
OUT4     EQU   *
         MVI   BRCHVAL,OUT04              SET BRCH TABLE VALUE  SA53223
         B     RETEXCP                  RETURN TO EXCP          SA53223
IOTERR   EQU   *                                                SA53223
         LA    GR9,4(GR9)               UPDATE TO IOB ADR      @ZA01201
         ST    GR9,IOBPTR               SAVE ACTIVE IOB ADR    @ZA01201
         MVI   BRCHVAL,TAPERR             SET TAPE ERROR VALUE  SA53223
         B     RETEXCP                  RETURN TO PROC ERR      SA53223
IOERR    EQU   *                                                SA53223
         MVI   BRCHVAL,DISKERR          INDIC DISK ERR OCCURRED SA53223
         B     RETEXCP                  RETURN TO PROC ERR      SA53223
MSGWRT   EQU   *                                                SA53223
         MVI   BRCHVAL,MSG              INDIC MSG TO BE O/P     SA53223
         B     RETEXCP                  RETURN TO IEHDEXCP      SA53223
FINIS    EQU   *                                                SA53223
         MVI   BRCHVAL,FINISH           NOT USED                SA53223
         B     RETEXCP                  RETURN TO IEHDEXCP      SA53223
MULTPROC EQU   *                                                SA53223
         MVI   BRCHVAL,MULTI            INDIC MULTI-PROCESS     SA53223
         B     RETEXCP                  RESTORE REGISTERS       SA53223
PROC4    EQU   *                                                SA53223
         MVI   BRCHVAL,PROC04           PREPARE TO READ NXT TRK SA53223
RETEXCP  L     GR13,4(GR13)            RESTORE REGISTER THIRTEEN A51474
         RETURN (14,12),T,RC=(15)      RESTORE/RETURN WITH PROPER CODE.
         EJECT
MOVECCWS MVC   0(1,GR8),0(GR6)
         SPACE 2
         DS    0F
FULWD    DC    H'0'                     COUNT FIELD ADDEND.     SA53223
HAFWD    DS    H                        COUNT FIELD DATA LENGTH SA53223
BUFFADDR DC    X'3E18'                 DATA BUFFER ADDR FOR S/A D/R.
SIXTEEN  DC    H'16'
THIRTY2  DC    H'32'
OFFSET   DC    H'27'                    OFFSET FOR MSG VALUES   SA53223
*  THE FOLLOWING ARE 'BRCHVAL' BRANCH TABLE VALUES FOR IEHDEXCP SA53223
PROC04   EQU   X'00'                                            SA53223
DISKERR  EQU   X'04'                                            SA53223
TAPERR   EQU   X'08'                                            SA53223
OUT04    EQU   X'0C'                                            SA53223
MSG      EQU   X'10'                                            SA53223
FINISH   EQU   X'14'                                            SA53223
MULTI    EQU   X'18'                                            SA53223
*        END OF 'BRCHVAL' VALUES.                               SA53223
FOX0     EQU   X'0F'                    MASK FOR CHGING ZONE OF SA53223
*                                       CHAR BEFORE UNPKING.    SA53223
TAPMK    EQU   X'80'                                             A21395
GOOD     EQU   X'7F'
DC       EQU   X'08'                   USED FOR DATA CHECKING TEST.3651
CCH      EQU   X'40'                   USED TO CHECK COMMAND CHAIN.3651
L2       EQU   2                        OFFSET CONSTANT         SA53223
L3       EQU   3                        OFFSET CONSTANT         SA53223
L8       EQU   8                        LENGTH CONSTANT         SA53223
MSG69    EQU   69                       ERROR MESSAGE VALUE     SA53223
D0       EQU   0                        OFFSET CONSTANT         SA53223
D12      EQU   12                       OFFSET CONSTANT         SA53223
D8       EQU   8                   DISP OF 8                     S20201
D4       EQU   4                        DISP OF 4               SA53223
X08      EQU   X'08'                   HEX CONSTANT.             S20201
RPSFEAT1 EQU   X'04'               RPS ON FROMDD                 S20201
TICCODE  EQU   X'08'               TIC CCW OP                    S20201
RPSFEAT  EQU   X'40'               RPS ON TODD                   S20201
D1       EQU   1                   DISP OF 1                     S20201
D3       EQU   3                        DISPL OF 3              Y02113
L4       EQU   4                        LENGTH OF 4             Y02113
TIMECHK  EQU   X'01'                    INDIC TIMESTAMP         Y02113
CLOCK    EQU   92                       F4DSCB TIMESTAMP DISPL @ZA30990
*                                       INTO BUFFER             Y02113
CCHHADDR DS    1F                      SAVE AREA FOR CCHH OF IPL TRK.
AREA     DS    1F                      SAVE AREA FOR CCW LENGTH.  0811
SAVESLOT DS    1F                      SAVE AREA FOR ALIGNMENT @ZA06534
CONVAD   DC    D'0'                    USED TO COVERT CCHH     @ZA06534
         DC    X'0'                     USED WHEN UPACKING CCHH SA53223
LABEL1   DC    C'MAINTENANCE'           LABEL OF MAINT. AREA    SA53223
         DC    100F'0'
LABEL2   DC    C'AREA'                  END OF MAINT. AREA      SA53223
         DS    0D                                                A37209
RDVOLBUF DS    CL92                     VOLID BUFFER             A37209
SRKEY    DC    X'0000000003'            RECORD THREE SEARCH      A37209
*                                       ARGUMENT                 A37209
         DS    0H                      HALF WORD ALIGHNMENT      A51474
ZERO     DC    6X'0'                   TWO BYTES OF ZEROS WILL   A51474
*                                      ALSO BE USED FROM THE     A51474
*                                      FOLLOWING CONSTANT.       A51474
F1       DC    F'1'
EIGHT    DC    F'8'                     LENGTH OF CCW          @ZA13746
         DS    0F                                                S20201
TICADDR  DC    X'000032E8'              TIC ADDR WITH NO RD CKD  S20201
         SPACE
MSKFLGON DC    X'60000000'             SET CHAIN COMMAND AND SILI BITS
SCC2321  DC    X'00F60000'             INCREMENT TO NEXT SUBCELL.
STC2321  DC    X'0000FB00'             INCREMENT TO NEXT STRIP.
RPSBUF   DS    1F                                                S20201
TAPECCW5 CCW   X'1F',0,X'40',1         WTM.
         CCW   3,0,X'20',24            NOOP-ALSO WRTS CNTRL REC AT EOV
         CCW   1,0,X'20',24            WRITE TRAILER RECORD.
         SPACE
SEARIDEQ CCW   X'31',0,X'60',5         SEARCH ID EQUAL ON R0
         CCW   8,SEARIDEQ,X'60',5      TIC BACK.
         CCW   6,0,X'60',8             READ R0 DATA.
         CCW   X'9E',0,X'60',X'4000'   READ CKD R1               4074
*                                      WITH MAXIMUM COUNT
DUMMYCCW CCW   X'92',0,X'60',8         DUMMY READ COUNT CCW.
DMMYRCCW CCW   X'1E',0,X'60',0         DUMMY READ COUNT/KEY/DATA CCW
         SPACE 1
HAR0CCW  CCW   7,X'0032AA',X'40',6     SEEK
         CCW   X'1F',X'0031A6',X'40',1 SET FILE MASK
HAR0CCW1 CCW   X'31',X'0032AC',X'60',5 SEARCH FOR R0.
         CCW   8,X'0032D8',0,0         SEARCH UNTIL FOUND.
         CCW   5,X'003E18',X'60',8     WRITE R0 DATA.
HAR0CCW2 CCW   X'31',X'0032AC',X'60',5 SEARCH FOR R0
         CCW   8,X'0032F0',0,0         REPEAT UNTIL FOUND.
         CCW   6,X'003E18',X'70',8     READ BACK CHECK R0 DATA.
         CCW   X'11',X'000BF0',X'20',8 ERASE REST OF TRACK.
         EJECT
R0R1CCWS CCW   7,X'0032AA',X'40',6     SEEK.
         CCW   X'1F',X'0031A6',X'40',1 SET FILE MASK.
CCWA     CCW   X'31',X'0032AC',X'60',5 SEARCH FOR R0
         CCW   8,X'0032D8',0,0         REPEAT UNTIL FOUND.
         CCW   X'1D',X'003E20',X'60',X'FFFF' WRITE COUNT KEY DATA R1.
CCWB     CCW   X'31',X'0032AC',X'60',5 SEARCH FOR R0
         CCW   8,X'0032F0',0,0         REPEAT UNTIL FOUND.
         CCW   5,X'003E18',X'60',8     WRITE DATA R0.
         CCW   X'1E',X'003E20',X'70',X'FFFF' READ BACK CHECK R1.
CCWC     CCW   X'16',X'003E18',X'30',16      READ BACK CHECK R0 DATA.
         SPACE
READVTOC CCW   X'31',0,X'60',5         SEARCH ON RECORD 1 OF VTOC.
         CCW   8,READVTOC,0,0          REPEAT UNTIL FOUND.
RDVDATA  CCW   6,0,X'20',96            READ FORMAT 4 DSCB.
*        CHANNEL PROGRAMS FOR SAVING OLD VOLID
SRCHKEYR CCW   X'31',SRKEY,X'60',5      SEARCH ID EQUAL          A37209
         CCW   8,SRCHKEYR,0,0                                    A37209
         CCW   X'06',RDVOLBUF,X'20',92  READ IN VOLID            A37209
SRCHKEYW CCW   X'31',SRKEY,X'60',5      SEARCH ID EQUAL          A37209
         CCW   8,SRCHKEYW,0,0                                    A37209
         CCW   X'05',RDVOLBUF,X'20',92  WRITE OUT ORIGINAL VOLID A37209
TOTAL    DS    F'0'                                              A51474
EXCPSAVE DS    18F                      SAVE AREA                A37209
         SPACE
* THIS IS USED TO WRITE HA, R0, R1 ONLY.
R0CCWS   DSECT
*   USED WHEN MODIFYING HA, R0 CCWS FOR EXECUTION.
SEARCHR0 DS    2F
TICBACK  DS    2F
WRITER0  DS    2F
SEARCH1  DS    2F
TICAGAIN DS    2F
READR0   DS    2F
ERASEALL DS    2F
READREC0 DS    2F
         SPACE
CCWS     DSECT
SEIDEQR0 DS    CL8
TICBACKA DS    CL8
RDWRDATA DS    CL8
RDWRTCKD DS    CL8
RDWRTCNT DS    CL8
         EJECT
         IEHDWORK
         EJECT
IOBLOCKS DSECT
* DUMP 'TO' ECB.
TOECB    DS    1F                      WAIT//COMPLETE BITS PLUS CODE.
* DUMP 'TO' IOB.
TOIOB    DS    0F
TOFLGS   DS    CL2                     FLAGS ONE AND TWO.
TOSENSE  DS    CL2                     SENSE BYTES.
TOECBAD  DS    1F                      ECB ADDRESS.
TOSTATUS DS    2F                      CHANNEL STATUS.
TOCCWAD  DS    1F                      CCW LIST ADDRESS.
TODCBAD  DS    1F                      DCB ADDRESS.
TORESAD  DS    1F                      RESTART ADDRESS.
TOBLKCNT DS    CL2                     BLOCK COUNT INCREMENT.
TOERROR  DS    CL2                     ERROR COUNT.
TOSEEK   DS    2F                      MBBCCHHR.
* DUMP 'TO' DCB.
TODCB    DS    13F                     DCB.
         DS    0D                      FOR DOUBLE WORD ALIGNMENT.
TAPECCW2 DS    1D                      USED TO PUT OUT CCW RECORD
*                                        AND DATA ON TAPE.
TAPECCW3 DS    2D                      WRITE HA,R0 CCWS ON TAPE.
TAPECCW4 DS    2D                      WRITE HA,R0,R1 CCWS ON TAPE.
         DS    0D                      FOR DOUBLE WORD ALIGNMENT.
* DUMP 'FROM' ECB.
OUTECB   DS    1F                      WAIT//COMPLETE BITS PLUS CODE.
* DUMP 'FROM' IOB.
OUTIOB   DS    0F
OUTFLG   DS    CL2                     FLAGS ONE AND TWO.
OUTSENSE DS    CL2                     SENSE BYTES.
OUTECBAD DS    1F                      ECB ADDRESS.
OUTSTAT  DS    2F                      CHANNEL STATUS.
OUTCCWAD DS    1F                      CCW LIST ADDRESS.
OUTDCBAD DS    1F                      DCB ADDRESS.
OUTRESAD DS    1F                      RESTART ADDRESS.
OUTBLKCT DS    CL2                     BLOCK COUNT INCREMENT.
OUTERROR DS    CL2                     ERROR COUNT.
OUTSEEK  DS    2F                      7BBCCHHR.
CCHH     EQU   OUTSEEK+3               CYLINDER/HEAD ADDRESS.
R        EQU   OUTSEEK+7               RECORD NUMBER.
* DUMP 'FROM' DCB.
OUTDCB   DS    18F                     DCB.
         SPACE
IOBLKEND DS    0H                      END OF IOBLOCKS DSECT.
DBSIZE   EQU   IOBLKEND-IOBLOCKS       SIZE OF IOBLOCKS DSECT.
         EJECT
         SPACE 1
         IEHDBLKS
         EJECT
         SPACE 2
         SPACE
TESTCNT  DSECT                                                  SA53223
         DS    CL5                      'CCHHR' OF COUNT FIELD  SA53223
TESTKEY  DS    CL1                      KEY LENGTH              SA53223
TESTDL   DS    H                        DATA LENGTH             SA53223
         SPACE
RBUFFER  DSECT
ZERO1    DS    CL8                     WILL CONTAIN 8 BYTES OF ZEROS.
CCWBUFF  EQU   ZERO1
TICSW    DS    CL1                     'FF' INDICATES PREVIOUS CCW=TIC.
         DS    CL3                     NOT USED.
ADDRESS  DS    CL4                     TEMPORARY ADDRESS HOLDER.
SEEKCCW  DS    CL8                     SEEK CCW FOR STAND-ALONE.
SETMASK  DS    CL8                     SET FILE MASK FOR STAND-ALONE.
SEARIDE  DS    CL8                     SEARCH EQUAL ID CCW.
         EJECT
CVT      DSECT
         CVT   SYS=MIN
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
JFCB     DSECT
         IEFJFCBN
         EJECT
         DCBD  DSORG=PS
         END
