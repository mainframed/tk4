 TITLE  'IGG019P9 --- ABNORMAL-END APPENDAGE FOR IEHDASDR UTILITY'
         COPY  LCGASMSW
IGG019P9 CSECT
*486100-494000                                                  VS01098
*1033                                                            Y01048
*                                                               XM3786
*A BUNCH                                                       @X50RSAG
*STATUS CHANGE LEVEL 004
*                                                                     *
*FUNCTION/OPERATION- THIS IS THE ABNORMAL END APPENDAGE FOR THE       *
*   IEHDASDR SYSTEM UTILITY PROGRAM. IT INTERCEPTS I/O ERRORS ON      *
*   DIRECT ACCESS DEVICES FOR ALL ANALYZE FUNCTIONS (IEHDANAL AND     *
*   IEHDCELL). IF IT IS A DATA CHECK, THE BIT IN THE DCBIFLGS IS      *
*   SET TO PREVENT USE OF STANDARD ERROR RECOVERY PROCEDURES. FOR     *
*   A DATA CHECK IN THE HOME ADDRESS OR RECORD ZERO COUNT FIELD,      *
*   A SWITCH IS ALSO SET IN THE DCB(DCBIOBAD) FOR LATER INTERROGATION *
*   BY IEHDANAL OR IEHDCELL. FOR ALL OTHER I/O ERROR CONDITIONS, THE  *
*   BIT IN THE DCBIFLGS IS SET TO ENSURE USE OF STANDARD ERROR        *
*   RECOVERY PROCEDURES. CONTROL IS THEN RETURNED TO IOS.             *
*                                                                     *
*ENTRY POINTS- THE ONLY ENTRY POINT IS -IGG019P9-.                    *
*                                                                     *
*INPUT- REGISTER 2 POINTS TO THE IOB.                                 *
*       REGISTER 4 POINTS TO THE DCB.                                 *
*       REGISTER 1 POINTS TO THE RQE.                         SA57293 *
*       REGISTER 7 POINTS TO THE UCB.                         SA57293 *
*        REGISTER 8 POINTS AN IOSB FOR MVM.                     YL02912
*                                                                     *
*EXITS-NORMAL- RETURN TO IOS ON REGISTER 14.                          *
*                                                                     *
*EXITS-ERROR- NONE.                                                   *
*                                                                     *
*EXTERNAL ROUTINES- THE APPENDAGE ROUTINE IS ALWAYS ENTERED FROM      *
*   IOS AND EXITS TO IOS.                                             *
*                                                                     *
*TABLES/WORK AREAS- NONE.                                             *
*                                                                     *
*ATTRIBUTES- REENTRANT, RELOCATABLE, NON-PRIVILEGED.                  *
*                                                                     *
*   THE FOLLOWING ARE REGISTER ASSIGNMENTS.
GR0      EQU   0
GR1      EQU   1
GR2      EQU   2                       IOB POINTER.
GR4      EQU   4                       DCB POINTER.
GR7      EQU   7                        UCB POINTER             SA57293
GR9      EQU   9                        SAVE AREA FOR REG 0      YM3467
GR10     EQU   10                      WORK REGISTER
GR11     EQU   11                      WORK REGISTER.
GR12     EQU   12                      WORK REGISTER.
GR13     EQU   13                      WORK REGISTER.
GR14     EQU   14                      RETURN REGISTER.
GR15     EQU   15                      BASE REGISTER.
         SPACE 3
HEX42    EQU   X'42'                    IOB FLAG               @VS40033
SILI     EQU   X'20'                    SILI BIT               @VS40033
CMDCH    EQU   X'40'                    CCW CMD CHN            @VS40033
CMDRJT   EQU   X'80'                    SENSE BIT              @VS40033
HEXFF    EQU   X'FF'                    'AND' MASK             @VS40033
FTR      EQU   X'5E'                    RD-MCKD CCW OPCODE     @VS40033
OVRFL    EQU   X'01'                    FILE-PROTECT INDICATION SA57293
ZEUS1    EQU   6                        2305-1 UCB TYPE         SA57293
ZEUS2    EQU   7                        2305-2                  SA57293
D4       EQU   4                        DISPL. OF 4             SA57293
UC       EQU   X'02'                   UNIT CHECK.
NOERR    EQU   X'0C'                   USED TO PREVENT USE OF STD. ERPS
DATACK   EQU   X'08'                   DATA CHECK.
CNTCK    EQU   X'80'                   DATA CHECK IN COUNT FIELD.
BADTRK   EQU   X'80'                   DEFECTIVE HA-R0 COUNT FIELD.
READHA   EQU   X'1A'                   READ HA OP CODE.
READR0   EQU   X'16'                   READ R0 OP CODE.
GR3      EQU   3                                                 S20201
MTRD     EQU   X'92'               M-T READ CIUNT                S20201
HEX9E    EQU   X'9E'               READ COUNT,KEY,DATA CCW      SA55588
FILEPRT  EQU   X'04'               FILE PROTECT                  S20201
COMP     EQU   X'7F'               COMP CODE                     S20201
CLR      EQU   X'00'               CLEAR FLAGS                   S20201
E12      EQU   12                       CONSTANT OF 12           YM3012
L8       EQU   8                        SAVE AREA OFFSET        YL02912
GR8      EQU   8                        IOSB PTR                YL02912
IOERROR  EQU   X'41'                    PERMENENT I/O ERROR      YM3012
         SPACE 3
         USING IOB,GR2
         USING UCB,GR7                                          SA57293
         USING IOSB,GR8                                         YL02912
         USING IHADCB,GR4
         USING IGG019P9,GR15
         EJECT
ENTRY    EQU   *                       ONLY ENTRY POINT HERE.
         NI    DCBIOBAD,X'FF'-BADTRK   INSURE BAD HA-R0 SWITCH IS OFF.
         SPACE
         TM    IOBCSW+4,UC             WAS THERE A UNIT CHECK.
         BZ    RETURN                  NO--RETURN TO IOS.
         USING DEB,GR3                                           S20201
         USING FUNCBLK,GR12                                      S20201
         L     GR12,DCBIOBAD       FUNCBLK ADDRESS               S20201
         CLI   UCBTBYT4,ZEUS2           THIS UCB A 2305-2 ?     SA57293
         BE    Z2                       YES, WHICH IOB HAD ERR  SA57293
         CLI   UCBTBYT4,ZEUS1           THIS DEVICE 2305-1 ?    SA57293
         BNE   AROUND                   NO, PROCESS NORMALLY    SA57293
Z2       EQU   *                                                SA57293
         IC    GR10,DEBVMOD             GET FILE MASK           SA57293
         ST    GR7,DEBVMOD              STORE CORRECT UCB ADDR  SA57293
         STC   GR10,DEBVMOD             RESET FILE MASK         SA57293
*                                       IN DEB                  SA57293
AROUND   EQU   *                                                SA57293
         CLI   FUNCSW,DUMP         IS THIS DUMP FUNCTION         S20201
         BNE   NODUMP              NI                            S20201
         MVC   UCBSEEK(D4),IOSSKCC      SAVE UCB SEEK FOR DDUMP  MVMFIX
         L     GR11,IOBCSW         CCW ADDRESS FROM CSW          S20201
         LA    GR11,0(GR11)        ZERO FIRST BYTE               S20201
         LTR   GR11,GR11                IS CCW ZERO              A46105
         BC    13,RETURN                YES,USE STANDARD ERPS    A46105
         SH    GR11,EIGHT          ADDRESS OF FAILING CCW        S20201
         USING IOBLOCKS,GR13                                     S20201
         LA    GR13,BLOCKS         GET PRIMARY                   S20201
         CLI   0(GR11),MTRD        IS IT M-T RD                  S20201
         BE    RANDLE              YES                          SA55588
         CLI   0(GR11),HEX9E       CHECK READ COUNT,KEY,DATA    SA55588
         BNE  RETURN         SA55588
RANDLE   EQU   *                                                SA55588
         TM    OUTSENSE+1,FILEPRT  IS FILE PROT ON               S20201
         BZ    RETURN              NO                            S20201
         DROP  GR13                                              S20201
         USING CONSTANT,GR13       ADDRESSING FOR                S20201
         L     GR13,IODEVCON        DEVICE CONSTANTS             S20201
         CLC   DEBENDCC(4),LASTALT  DOES DEB EQUAL LAST TRK      S20201
         BNE   RETURN                                            S20201
POSTXIT  EQU   *                                               @VS40033
         MVI   IOBECBCC,COMP       POST COMPLETE CODE            S20201
         NI    DCBIFLGS,X'3F'       SET DCB FLAGS                S20201
         NI    IOBFLAG1,X'DA'      SET IOB FLAGS                 S20201
         B     0(GR14)                                           S20201
NODUMP   EQU   *                                                 S20201
         SPACE
         CLI   FUNCSW,RESTORE           RESTORE FUNCTION ?      YL02912
         BNE   NOREST                   NO, DON'T SET SEEK ADDR YL02912
         MVC   UCBSEEK(D4),IOSSKCC       SAVE UCB SEEK FOR DREST MVMFIX
         DROP  GR8
NOREST   EQU   *                                                YL02912
         TM    IOBSENS0,DATACK         WAS THERE A DATA CHECK.
         BZ    RETURN                  NO--RETURN TO IOS.
         CLI   FUNCSW,RESTORE          IS THIS RESTORE FUNCTION  XM3786
         BE    RETURN                  YES, LET ERPS HDL ERR     XM3786
         SPACE
         L     GR11,IOBCSW             CCW ADDRESS FROM CSW.
         LA    GR11,0(GR11)            INSURE HIGH ORDER BYTE IS CLEAR.
         SH    GR11,EIGHT              ADDRESS OF FAILING CCW.
         SPACE
         CLI   0(GR11),READHA          THIS A READ HOME ADDRESS CCW.
         BE    SETFLAG                 YES--SET INDICATOR.
         SPACE
         CLI   0(GR11),READR0          THIS A READ R0 CCW.
         BNE   SETFLAG1                NO--RETURN//BYPASS ERPS.
         SPACE
         TM    IOBSENS1,CNTCK          WAS DATA CHECK IN COUNT FIELD.
         BZ    SETFLAG1                NO--RETURN//BYPASS ERPS.
         SPACE
SETFLAG  OI    DCBIOBAD,BADTRK         INDICATE DEFECTIVE HA-R0 AREA.
SETFLAG1 OI    DCBIFLGS,NOERR          PREVENT USE OF STD. ERPS.
         BR    GR14                    RETURN TO IOS.
         SPACE
RETURN   NI    DCBIFLGS,X'FF'-NOERR    INSURE STD. ERPS ARE USED.
         BR    GR14                    RETURN TO IOS.
         SPACE
ONE      DC    F'1'                     CONSTANT OF ONE          YM3012
EIGHT    DC    H'8'                    CONSTANT FOR SUBTRACT.
         EJECT
IOB      DSECT                         DESCRIPTION OF IOB.
IOBFLAG1 DS    CL1                     FLAG1
IOBFLAG2 DS    CL1                     FLAG2.
IOBSENS0 DS    CL1                     SENSE BYTE ZERO.
IOBSENS1 DS    CL1                     SENSE BYTE ONE.
IOBECBCC DS    CL1                     COMPLETION CODE.
IOBECBPT DS    CL3                     ADDRESS OF ECB.
IOBCSW   DS    CL8                     STATUS WORDS.
IOBSTART DS    CL4                     CHANNEL PROGRAM ADDRESS.
IOBDCBPT DS    CL4                     ADDRESS OF DCB.
IOBRESTR DS    CL4                     RESTART ADDRESS.
IOBINCAM DS    CL2                     BLOCK COUNT.
IOBERRCT DS    CL2                     ERROR COUNT.
IOBSEEK  DS    CL8                     SEEK ADDRESS.
         IEHDBLKS
         IEHDWORK
IOBLOCKS DSECT
* DUMP 'TO' ECB.
TOECB    DS    1F                  WAIT//COMPLETE BITS PLUS      S20201
*                                       CODE.                    S20201
* DUMP 'TO' IOB.
TOIOB    DS    0F                                                S20201
TOFLGS   DS    CL2                 FLAGS ONE AND TWO.            S20201
TOSENSE  DS    CL2                 SENSE BYTES.                  S20201
TOECBAD  DS    1F                  ECB ADDRESS                   S20201
TOSTATUS DS    2F                  CHANNEL STATUS                S20201
TOCCWAD  DS    1F                  CCW LIST ADDRESS.             S20201
TODCBAD  DS    1F                  DCB ADDRESS.                  S20201
TORESAD  DS    1F                  RESTART ADDRESS.              S20201
TOBLKCNT DS    CL2                 BLOCK COUNT INCREMENT.        S20201
TOERROR  DS    CL2                 ERROR COUNT.                  S20201
TOSEEK   DS    2F                  MBBCCHHR                      S20201
* DUMP 'TO' DCB.
TODCB    DS    18F                 DCB.                        @VS40033
         DS    0D                  FOR DOUBLE WORD ALIGNMENT.    S20201
TAPECCW1 DS    1D                  USED TO PUT OUT CCW RECORD  @VS40033
*                                    AND DATA ON TAPE.
         DS    0D                  FOR DOUBLE WORD ALIGNMENT.    S20201
* DUMP 'FROM' ECB.
OUTECB   DS    1F                  WAIT//COMPLETE BITS PLUS      S20201
*                                       CODE.                    S20201
OUTIOB   DS    0F                                                S20201
OUTFLG   DS    CL2                 FLAGS ONE AND TWO.            S20201
OUTSENSE DS    CL2                 SENSE BYTES.                  S20201
OUTECBAD DS    1F                       ECB ADDR               @VS40033
OUTSTAT  DS    2F                  CHANNEL STATUS.               S20201
OUTCCWAD DS    1F                  CCW LIST ADDRESS              S20201
OUTDCBAD DS    1F                  DCB ADDRESS.                  S20201
OUTRESAD DS    1F                  RESTART ADDRESS.              S20201
OUTBLKCT DS    CL2                 BLOCK COUNT INCREMENT.        S20201
OUTERROR DS    CL2                 ERROR COUNT.                  S20201
OUTSEEK  DS    2F                  7BBCCHHR.                     S20201
CCHH     EQU   OUTSEEK+3           CYLINDER/HEAD ADDRESS.        S20201
R        EQU   OUTSEEK+7           RECORD NUMBER.                S20201
* DUMP 'FROM' DCB.
OUTDCB   DS    18F                 DCB.                          S20201
IOBLKEND DS    0H                  END OF IOBLOCKS DSECT.        S20201
DBSIZE   EQU   IOBLKEND-IOBLOCKS   SIZE OF IOBLOCKS DSECT.       S20201
DEB      DSECT
DEBVMOD  EQU   DEB+32              FILE MASK                     S20201
DEBSTRCC EQU   DEB+38              CYLINDER-START OF EXTENT      S20201
*                                       LIMIT                    S20201
DEBSTRHH EQU   DEB+40              TRACK-END OF EXETEN LIMIT     S20201
DEBENDCC EQU   DEB+42              CYL/END OF EXTENT LIMIT       S20201
DEBENDHH EQU   DEB+44              TRACK/END OF EXTENT LIMIT     S20201
DEBNMTRK EQU   DEB+46              NUMBER OF TRACKS THIS EXTENT  S20201
         EJECT
         IECDIOSB                                               YL02912
         EJECT
UCB      DSECT                                                  SA57293
         IEFUCBOB                                               SA57293
         EJECT                                                  SA57293
         DCBD  DSORG=PS
         EJECT
CVT      DSECT
         CVT   SYS=MIN                                           Y01048
         END
