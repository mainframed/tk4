         TITLE 'IGC0108B --- ASSIGNS ALTERNATE TRACKS'
         COPY  LCGASMSW                                          SM4351
IGC0108B CSECT
*C383953                                                      @VS40037
*
*A273500                                                      @VS40433
* FIX 3350 ALT TO ALT ASSIGNMENT.                             @ZM40450
*A383943,384821,822                                           @ZM43013
*C 84000,419000,423000              @SA74452=@YA09634=@XA09732=@ZA04363
*A 83500-83600,95500,505900         @SA74452=@YA09634=@XA09732=@ZA04363
*
*                                                             @Y30LSFY
*STATUS CHANGE LEVEL 004
*                                                                     *
*FUNCTION/OPERATION-  THIS LOAD OF SVC 82 PERFORMS ALTERNATE          *
*   TRACK ASSIGNMENT FOR EITHER OF TWO CASES.                         *
*                                                                     *
*     1) VOLUMES IN A STATE OF BEING INITIALIZED.                     *
*     2) PRE-INITIALIZED VOLUMES(THEY HAVE A FORMAT 4 DSCB).          *
*                                                                     *
*   IF THE SPECIFIED DEFECTIVE TRACK IS AN UNASSIGNED ALTERNATE, IT   *
*     IS FLAGGED TO PREVENT ITS EVER BEING ASSIGNED.                  *
*                                                                     *
*   IF THE SPECIFIED DEFECTIVE TRACK IS AN ASSIGNED ALTERNATE, THE    *
*     NEXT AVAILABLE ALTERNATE IS ASSIGNED TO THE ORIGINAL PRIMARY    *
*     TRACK.                                                          *
*                                                                     *
*   IF THE SPECIFIED DEFECTIVE TRACK IS A PRIMARY TRACK, THE NEXT     *
*     GOOD AVAILABLE ALTERNATE TRACK IS ASSIGNED TO IT.               *
*                                                                     *
*ENTRY POINT-  THE ONLY ENTRY POINT IS -IGC0108B-.                    *
*                                                                     *
*INPUT-  REGISTER 1 POINTS TO 3-FULL WORDS AS FOLLOWS.                *
*        WORD1-  INPUT PARM LIST POINTER PASSED TO IGC0008B.          *
*        WORD2-  WORKAREA ADDRESS- DESCRIBED BY -IOBLOCKS- DSECT.     *
*        WORD3-  POINTER TO SVRB EXTENDED SAVEAREA.                   *
*                                                                     *
*EXITS-NORMAL-  THE 2ND WORD OF THE INPUT PARM LIST CONTAINS THE      *
*   CCHH OF THE ASSIGNED ALTERNATE TRACK, IF ANY. THE 2ND WORD        *
*   CONTAINS ZEROS IF NO ALTERNATE NEED BE ASSIGNED.RC=0.             *
*   -THE 6-BYTES OF ALTERNATE TRACK INFORMATION IS UPDATED(EITHER IN  *
*   THE FORMAT 4 DSCB OR POINTED TO BY THE INPUT PARM LIST.           *
*                                                                     *
*EXITS-ERROR-  RETURN CODE=12//IO ERROR ENCOUNTERED.                  *
*              RETURN CODE=16//NO MORE ALTERNATES AVAILABLE.          *
*   RETURN TO CALLER VIA SVC 3.                                       *
*                                                                     *
*SUPERVISOR MACROS-  EXCP,WAIT,FREEMAIN.                              *
*                                                                     *
*TABLES/WORKAREA-  USES THE WORKAREA DESCRIBED BY DSECT -IOBLOCKS-.   *
*                                                                     *
*OUTPUT-  SEE EXIT DESCRIPTION.                                       *
*                                                                     *
*ATTRIBUTES-  REENTRANT,RELOCATABLE,PRIVILEGED,ENABLED.               *
*                                                                     *
***********************************************************************
         EJECT
*   THE FOLLOWING ARE REGISTER ASSIGNMENTS.
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R14      EQU   14                      LINK REGISTER.
R15      EQU   15
PARMPTR  EQU   1                       POINTS TO INPUT PARAMETERS.
BASEREG  EQU   12                      BASE REGISTER.
         SPACE 3
*   EQUATE DEFINITIONS.
RBEXSAVE EQU   96                      EXTENDED SAVE AREA FOR SVRB.
LAST     EQU   X'80'                   LAST PARAMETER INDICATOR.
GOOD     EQU   X'7F'                   I/O GOOD COMPLETION BIT.
COMPLETE EQU   X'40'                   I/O COMPLETE BIT.
SKIP     EQU   X'10'                   SKIP DATA TRANSFER.
CHAIN    EQU   X'40'                   COMMAND CHAIN CCWS.
CHAINSD  EQU   X'50'                   COMMAND CHAIN +SKIP CCWS  XM4405
SILI     EQU   X'20'                   SUPPRESS INCORRECT LENGTH.
ALT      EQU   X'01'                   ALTERNATE TRACK FLAG.
DEFECT   EQU   X'02'                   DEFECTIVE TRACK FLAG.
MOVEHA   EQU   X'40'                   MOVE HA DOWN THE TRACK.
UE       EQU   X'01'                   UNIT EXCEPTION.
MAINUCB  EQU   X'FF'                   ID FOR PRIMARY UCB.
DA2321   EQU   X'05'                   UCB TYPE CODE FOR 2321.
DA2314   EQU   X'08'                   2314 UCB TYPE CODE.
ANALYZE  EQU   X'00'                   ANALYZE FUNCTION CODE.   YL02912
D0       EQU   0                        ZERO DISPLACEMENT
D4       EQU   4                        DISPLACEMENT OF 4.      YL02912
K36      EQU   36                       CONSTANT OF 36.         YL02912
         AIF   ('&LIB' EQ 'LIB1').X226605                       XL03130
SS1CONV  EQU   X'01'                    CODE FOR SS1 CONV      @Y30LSFY
ICEBERG  EQU   X'0D'                    UCB DEVICE TYPE 3330-1 @Y30LSFY
WIN      EQU   X'0A'                    UCB DEVICE TYPE 3340.   XL03130
D3350    EQU   X'0B'                   UCB DEVICE TYPE 3350    @Z30RSAG
L16      EQU   16                      R0 CCW LENGTH           @Z30RSAG
.X226605 ANOP                                                   XL03130
         EJECT
         LR    BASEREG,R15             ESTABLISH ADDRESSING.
         USING IGC0108B,BASEREG
         USING PARMLIST,R11                                     YL02912
         SPACE
         B     APARNO                  BRANCH AROUND APAR NO   @ZA04363
         DC    C'IGC0108B OZ04363'     LAST FIX THIS MOD       @ZA04363
APARNO   L     R11,0(R1)               PARM LIST POINTER.      @ZA04363
         L     R2,4(R1)                WORKAREA ADDRESS.
         L     R5,8(R1)                SVRB EXTENDED AREA.
         SPACE 2
***********************************************************************
*                                                                     *
*   INITIALIZE THE IOB.                                               *
*                                                                     *
***********************************************************************
         SPACE 2
         USING IOBLOCKS,R2
INITIOB  MVI   IOBFLAG1,X'42'          SET PROPER FLAGS.
         MVI   RETSW,X'00'             CLEAR RETRY SW          @ZA04363
         LA    R3,ECB                  ECB ADDRESS.
         ST    R3,IOBECBAD             STORE IN IOB.
         MVI   IOBINCAM+1,1            BLOCK COUNT.
         LA    R3,DCB-44               DCB ADDRESS.
         ST    R3,IOBDCBPT             STORE IN IOB.
         L     R4,UCBPTR               UCB ADDRESS.             YL02912
         USING UCB,R4
         CLI   UCBID,MAINUCB           THIS A PRIMARY UCB.
         BE    TESTF                   GO TEST THE FUNCTION.
         OI    SW,D2321                SET 2321 SWITCH.
         MVC   IOBSEEK+1(2),0(R4)      BIN NO. TO IOB.
         DROP  4
         SPACE 2
TESTF    EQU   *
         CLI   FUNCTION,X'00'           ANALYZE/FORMAT REQUEST? YL02912
         BE    ASSG2                   YES--GO HANDLE.
         CLI   FUNCTION,SS1CONV        THIS A SS1 CONV REQUEST @Y30LSFY
         BE    ASSG2                   YES--GO HANDLE.         @Y30LSFY
         EJECT
*   BUILD CCWS FOR READING FORMAT 4 DSCB HERE.
         USING IOBLOCKS,R2
FORM4    EQU   *
         LA    R3,FORMAT4              FORMAT 4 READ-IN AREA.
         ST    R3,WRTRD                STORE IN READ-DATA CCW.
         MVI   WRTRD,X'06'             SET OP CODE.
         MVI   WRTRD+7,96              SET COUNT.
         LA    R3,SEARCH               SEARCH CCW ADDRESS.
         ST    R3,TIC                  STORE IN TIC.
         MVI   TIC,8                   SET TIC OP CODE.
         LA    R3,CCHHR                SEARCH ADDRESS.
         ST    R3,SEARCH               STORE IN SEARCH CCW.
         MVI   SEARCH+4,X'40'          COMMAND CHAIN ON.
         MVI   SEARCH+7,5              SET COUNT.
         MVI   SEARCH,X'31'            SET SEARCH OP CODE.
         SPACE 2
         LA    R3,SEARCH               SEARCH CCW ADDRESS.
         ST    R3,IOBSTART             SET IN IOB.
         SPACE
*   STORE CCHHR OF VTOC IN IOB HERE.
         L     R3,UCBPTR                UCB ADDRESS.            YL02912
         USING UCB,R3
         AIF   ('&LIB' EQ 'LIB2').X229300                       XL03912
         CLI   UCBID,MAINUCB           THIS A 2321.
         BNE   P2321                   YES-GO PROCESS.
.X229300 ANOP                                                   XL03912
         L     R0,SRTEFSCT             TTR OF VTOC.
         SPACE
FINDCCHH LA    R3,RBEXSAVE(R5)         EXTENTED SVRB SAVE AREA.
         L     R10,K36(R3)             GET RETURN ADDRESS.      YL02912
         MODESET EXTKEY=SUPR                                     YM1315
         STM   1,12,0(R3)              SAVE THE REGISTERS.
         MODESET EXTKEY=DATAMGT                                  YM1315
         SPACE
         USING CVT,R15
         L     R15,CVTPTR              CVT ADDRESS.
         L     R15,CVTPCNVT            CONVERT ROUTINE ADDRESS.
         DROP  15
         LA    R1,DEB                  ADDDRESS OF DEB.
         LA    R2,IOBSEEK              TARGET AREA FOR MBBCCHHR.
         BALR  R14,R15                 CONVERT TTR TO MBBCCHHR.
         LM    1,12,0(R3)              RESTORE THE REGISTERS.
         SPACE
*   READ IN THE FORMAT 4 DSCB HERE.                                   *
         BAL   R9,WRTREAD              READ IN FORMAT4 DSCB DATA FIELD.
         BAL   R9,WAIT                 AWAIT COMPLETION.
         MVC   ALTINFO(6),ALTDATA      CCHH NEXT ALT. AND COUNT LEFT.
         SPACE
ASSG1    CLC   ALTINFO+4(2),ZERO       ANY ALTERNATES LEFT.
         BNE   TESTALT                 YES-
         SPACE
EXIT16   BAL   R9,FREECORE             FREE THE WORK AREA.
         LA    R15,16                  SET RETURN CODE.
         B     RETURN                  RETURN TO CALLER.        YL02912
         SPACE
         AIF   ('&LIB' EQ 'LIB2').X229301                       XL03912
         USING DATACELL,R3
P2321    L     R0,DCELVTOC             TTR OF VTOC---2321.
         B     FINDCCHH                CONVERT TO CCHH.
         DROP  R3
         SPACE 2
.X229301 ANOP                                                   XL03912
ASSG2    EQU   *                       SET UP ALTERNATE INFORMATION.
         L     R4,ALTPTR                PTR TO ALT INFO.        YL02912
         BAL   R14,CALLKEY              EXEC NEXT INSTR IN      YL02912
*                                        KEY OF CALLER.         YL02912
         LM    R6,R7,D0(R4)             GET ALTERNATE INFO.     YL02912
         STM   R6,R7,ALTINFO            SAVE ALTERNATE INFO.    YL02912
         B     ASSG1                   GO PROCESS THIS TRACK.
         SPACE
TESTALT  EQU   *                                                YL02912
         L     R3,CCHHPARM              SELECTED TRACK ADDRESS. YL02912
         SPACE
         USING UCB,R4
         L     R4,UCBPTR                UCB ADDRESS.            YL02912
         SPACE
         AIF   ('&LIB' EQ 'LIB2').X229303                       XL03912
         CLI   UCBID,MAINUCB           THIS A PRIMARY UCB.
         BE    TEST1                   YES-GET DEVICE TYPE CODE.
         LA    R6,DA2321               NO--MUST BE 2321.
         B     TEST2                   GO INDEX TO DEVICE CONSTANTS.
         SPACE
.X229303 ANOP                                                   XL03912
TEST1    SR    R6,R6                   CLEAR
         IC    R6,UCBTBYT4             DEVICE TYPE CODE.
         AIF   ('&LIB' EQ 'LIB1').X226602                       XL03130
         CLI   UCBTBYT4,WIN             IS THIS 3340?           XL03130
         BNE   TEST3                    NO, GO TEST MORE        XL03130
         L     R7,DEVMODP               ADDRESS OF DEV MOD NO.  YL02912
         BAL   R14,CALLKEY              EXECUTE NEXT INST       YL02912
*                                        IN THE KEY OF CALLER.  YL02912
         IC    R6,0(R7)                 YES, GET 3340 MOD NO.   XL03130
TEST3    EQU   *                                                XL03130
         LR    R7,R4                    SAVE UCB ADDRESS.       XM3801
.X226602 ANOP                                                   XL03130
         CLI   UCBTBYT4,DA2314         THIS A 2314.
         BNE   TEST2                   NO--
         OI    SW,D2314                YES-REMEMBER THIS.
         DROP  R4
         SPACE
TEST2    LA    R4,FIRSTALT-8           TABLE-8.
         SLA   R6,3                    UCB CODE TIME 8.
         LA    R6,0(R6,R4)             ADDRESS OF CONSTANTS/THIS DEVICE
         SPACE
         USING UCB,R7                                          @Y30LSFY
         CLI   FUNCTION,SS1CONV         SS1 CONVERSION ?       @Y30LSFY
         BNE   NOTSSC                   NO, BRCH               @Y30LSFY
         LA    R6,SS1CONM              YES, LOAD SS1 ALT INFO. @Y30LSFY
         CLI   UCBTBYT4,ICEBERG         3330-1 DEVICE          @Y30LSFY
         BNE   NOTSSC                   NO - USE 3330 CONSTANT @Y30LSFY
         LA    R6,SS1CONI               SET ICEBERG ALT INFO   @Y30LSFY
         DROP  R7                                              @Y30LSFY
NOTSSC   EQU   *                                               @Y30LSFY
         CL    R3,0(R6)                INPUT CCHH IN ALTERNATE AREA.
         BNH   NOTALT                  NO--MUST BE PRIMARY TRACK.
         SPACE 2
YESALT   EQU   *                       INPUT TRACK IS IN ALTERNATE AREA
         ST    R3,R0COUNT              INPUT CCHH//TEMPORARY STORAGE.
         MVC   CCHHR(4),R0COUNT        SEEK ADDRESS IN IOB.
         LA    R4,RDHA                 READ HA CCW.
         ST    R4,IOBSTART             STORE ADDRESS IN IOB.
         LA    R4,HA                   READ-IN HA AREA.
         ST    R4,RDHA                 STORE IN CCW.
         MVI   RDHA,X'1A'              RESET OP CODE.
         MVI   RDHA+7,5                SET CORRECT COUNT.
         AIF   ('&LIB' EQ 'LIB1').X322201                        XM4405
         BAL   R9,SAVESD               SEE IF SD BYTES.          XM4405
.X322201 ANOP                                                    XM4405
         MVI   RDHA+4,CHAIN            CHAIN BIT ON.           @ZM40450
         LA    R4,R0COUNT              COUNT READ-IN AREA.
         ST    R4,READR0               STORE IN CCW.
         MVI   READR0,X'16'            RESET OP CODE.
         MVI   READR0+7,4              SET COUNT IN CCW.
         MVI   READR0+4,SILI           SUPPRESS INCORRECT LENGTH.
         BAL   R9,WRTREAD              READ IN HA/R0 RECORDS.
         BAL   R9,WAIT                 AWAIT COMPLETIOM.
         CLC   HA+1(4),R0COUNT         HA AND R0 SAME CCHH.
         L     R3,R0COUNT              PRIMARY TRACK ADDRESS.
         BE    A                       TRACK NOT ASSIGNED.         1057
         OI    SW,DEFALT               SET DEFECTIVE ALTERNATE SW  1057
A        EQU   *                                                   1057
         BAL   R9,DOSA                 CHK TRK VIA S.A.        @ZM40450
         MVC   WRTHA(16),RDHA          REPOSITION THE CCWS.
         MVI   WRTHA,X'19'             SET WRITE HA OP CODE.
         MVI   WRTR0,X'15'             SET WRITE R0 OP CODE.
         MVI   WRTR0+4,CHAIN           SET CHAIN FLAG ON.
         MVI   WRTR0+7,16              SET COUNT IN CCW.
         MVI   R0COUNT+7,8             SET COUNT IN COUNT FIELD.
*
         LA    R4,RDHA                  MODIFY THE UNUSED CCW  @Z30RSAG
         ST    R4,R0DATA                TO A                   @Z30RSAG
         MVI   R0DATA,TICCMD            TIC CCW.               @Z30RSAG
*
         MVI   RDHA+4,CHAIN+SKIP       SKIP DATA TRANSFER.
         MVI   READR0+4,SKIP+SILI      SKIP DATA TRANSFER.
         OI    HA,DEFECT+ALT           INDICATE DEFECTIVE ALT. IN FLAG.
         SPACE
         LA    R4,WRTHA                CHANNEL PROGRAM START.
         ST    R4,IOBSTART             SET CCW ST ART IN IOB.
         AIF   ('&LIB' EQ 'LIB1').X228301                       XL03130
         BAL   R9,WRTWIN                TEST IF 3340.           XL03130
.X228301 ANOP                                                   XL03130
         SPACE
         BAL   R9,WRTREAD              FLAG THIS TRACK DEFECTIVE.
         BAL   R9,WAIT                 AWAIT I/O COMPLETION.
         MVI   RDHA+4,CHAIN            COMMAND CHAIN FLAGS TO CCW  1057
         MVI   READR0+4,SILI           SUPPRESS LENGTH IND. TO CCW 1057
         TM    SW,DEFALT               DEFECTIVE ALTERNATE SW SET  1057
         BO    NOTALT                  YES-GO SEE IF GOOD ALTERNATE1057
         SPACE
         L     R10,PRMPTR               GET CALLER'S PARM PTR.  YL02912
         SR    R8,R8                    SET ZERO                YL02912
         BAL   R14,CALLKEY              EXECUTE NEXT INST       YL02912
*                                        IN THE KEY OF CALLER.  YL02912
         ST    R8,D4(R10)               IND NO ALT. ASSIGNED.   YL02912
         B     EXIT0                   RETURN TO CALLER.
         EJECT
***********************************************************************
*                                                                     *
*   THIS ROUTINE WILL OBTAIN THE KEY OF THE CALLER, EXECUTE THE       *
*   INSTRUCTION AFTER THE BRANCH AND LINK INSTRUCTION TO THIS         *
*   ROUTINE, RETURN TO DATA MANAGEMENT KEY AND FINALLY RETURN TO      *
*   ONE INSTRUCTION PAST THE ONE EXECUTED BY THIS ROUTINE.            *
*                                                                     *
***********************************************************************
         SPACE 3
CALLKEY  EQU   *                                                YL02912
         MODESET KEYADDR=KEY,WORKREG=15 GET CALLER'S KEY.       YL02912
         SPACE
         EX    0,D0(R14)                DO INST PAST BAL.       YL02912
         SPACE
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY.    YL02912
         LA    R14,D4(R14)             SET BR REG TO INST+1.    YL02912
         BR    R14                      GO TO OBJECT INST+1.    YL02912
         EJECT
***********************************************************************
*                                                                     *
*   TEST IF THIS ALTERNATE TRACK IS A GOOD ONE.
*                                                                     *
***********************************************************************
         SPACE 2
NOTALT   MVC   CCHHR(4),ALTINFO        CCHH OF NEXT ALT TO IOB.
         LA    R4,RDHA                 CCW ADDRESS.
         ST    R4,IOBSTART             STORE IN IOB.
         LA    R4,HA                   READ IN AREA.
         ST    R4,RDHA                 STORE IN CCW.
         MVI   RDHA,X'1A'              RESET OP CODE.
         MVI   RDHA+7,5                SET COUNT IN CCW.
         SPACE
         BAL   R9,WRTREAD              READ IN HOME-ADDRESS/ALTERNATE.
         BAL   R9,WAIT                 AWAIT COMPLETION.
         SPACE
         TM    HA,DEFECT               THIS A GOOD ALTERNATE.
         BZ    WRTPRIM                 YES-GO USE IT.
         SPACE 2
***********************************************************************
*                                                                     *
*   THE NEXT AVAILABLE TRACK WAS DEFECTIVE. MUST FIND ANOTHER ONE.    *
*                                                                     *
***********************************************************************
         SPACE 2
GETNEXT  EQU   *                       FIND NEXT ALTERNATE TRACK.
         BAL   R9,UPCCHH               GET ADDRESS OF NEXT ALTERNATE.
         LTR   R4,R4                   BETTER TEST FOR ZERO HERE.
         BZ    EXIT16                  NONE LEFT---BETTER LEAVE.
         B     NOTALT                  GO SEE IF NEXT ALTERNATE IS GOOD
         EJECT
***********************************************************************
*                                                                     *
*   WRITE HOME ADDRESS/RECORD ZERO ON THE PRIMARY TRACK HERE.         *
*                                                                     *
***********************************************************************
WRTPRIM  ST    R3,R0COUNT              TEMPORARY STORAGE.
         MVC   CCHHR(4),R0COUNT        PRIMARY CCHH TO IOB.
         AIF   ('&LIB' EQ 'LIB1').X322200                        XM4405
         BAL   R9,SAVESD               SEE IF SD BYTES.          XM4405
.X322200 ANOP                                                    XM4405
         MVC   HA+1(4),R0COUNT         SET IN CCHH OF HA.
         BAL   R9,DOSA                 GO DO SURFACE ANALYSIS  @Z30RSAG
         MVI   HA,DEFECT               SET FLAG IN HA.
         MVC   R0COUNT(4),ALTINFO      CCHH OF ALT TO PRIMARY R0.
         SPACE
         LA    R4,HA                   WRITE-OUT AREA.
         ST    R4,WRTHA                STORE IN CCW.
         MVI   WRTHA,X'19'             SET OP CODE.
         MVI   WRTHA+4,CHAIN           CHAIN BIT ON.
         MVI   WRTHA+7,5               SET COUNT IN CCW.
         SPACE
         LA    R4,R0COUNT              COUNT FIELD ADDRESS.
         ST    R4,WRTR0                STORE IN CCW.
         MVI   WRTR0,X'15'             RESET OP CODE.
         MVI   WRTR0+4,CHAIN           CHAIN BIT ON.
         MVI   WRTR0+7,16               SET COUNT TO 16          M0492
         MVI   READR0+7,16                IN CCW'S               M0492
         SPACE
         LA    R4,RDHA                 PREPARE TO SETUP TIC    @Z30RSAG
         ST    R4,R0DATA               CHG CCW DATA ADDR       @VS40034
         MVI   R0DATA,TICCMD           RESET OPCODE            @Z30RSAG
         SPACE
         MVC   RDHA(16),WRTHA          MOVE WRITE CCWS OVER READS.
         MVI   RDHA,X'1A'              SET READ HA OP CODE.
         OI    RDHA+4,SKIP             SKIP DATA TRANSFER.
         MVI   READR0,X'16'            SET READ R0 OP CODE.
         MVI   READR0+4,SKIP+SILI      SKIP DATA TRANSFER.
         SPACE
         LA    R4,WRTHA                CHANNEL PROGRAM START.
         ST    R4,IOBSTART             STORE IN IOB.
         MVC   R0COUNT+K4(K4),ROLENGTH  SET R0 COUNT             S21041
         AIF   ('&LIB' EQ 'LIB1').X226606                       XL03130
         BAL   R9,WRTWIN               TEST IF 3340/3350       @Z30RSAG
.X226606 ANOP                                                   XL03130
         SPACE 2
         BAL   R9,WRTREAD              WRITE HA/R0 ON PRIMARY.
         BAL   R9,WAIT                 AWAIT COMPLETION.
         SPACE
         L     R10,PRMPTR               GET CALLER'S PARM PTR   YL02912
         L     R8,ALTINFO               GET CCHH OF ALTERNATE   YL02912
         BAL   R14,CALLKEY              EXECUTE NEXT INST.      YL02912
*                                        IN THE KEY OF CALLER.  YL02912
         ST    R8,D4(R10)               SET CCHH IN PARM LIST.  YL02912
         MVI   HA,ALT                  SET FLAG TO SHOW ALTERNATE.
         EJECT
***********************************************************************
*                                                                     *
*   WRITE RECORD ZERO ON THE ALTERNATE TRACK HERE.                    *
*                                                                     *
***********************************************************************
         SPACE 3
WRTALT   MVC   CCHHR(4),ALTINFO        SEEK ADDRESS OF ALT. TO IOB.
         MVC   HA+1(4),ALTINFO         CCHH OF ALTERNATE.
         SPACE
         ST    R3,R0COUNT              PRIMARY CCHH TO RECORD ZERO.
         MVI   R0COUNT+7,8             SET DATA LENGTH FOR EIGHT.
         MVI   WRTR0+7,16              SET CCW COUNT FOR 16.
         AIF   ('&LIB' EQ 'LIB1').X227910                       XL03130
         SPACE
         LA    R4,HA+1                  GET SEARCH ADDR.        XL03130
         ST    R4,SCHHA                 SEARCH ADDR TO CCW.     XL03130
         MVI   SCHHA,SRCH               SET SEARCH HA.          XL03130
         MVI   SCHHA+4,CHAIN            CHAIN BIT ON.           XL03130
         MVI   SCHHA+7,C4               SET COUNT TO 4.         XL03130
         LA    R4,SCHHA                 SEARCH CCW ADDRESS.     XL03130
         ST    R4,IOBSTART              CCW ADDRESS TO IOB.     XL03130
         ST    R4,TICSRCH               SET TIC ADDRESS.        XL03130
         MVI   TICSRCH,TICCMD           SET TIC COMMAND.        XL03130
         L     R4,D0(R11)               GET UCB                 XL03130
         USING UCB,R4                                           XL03130
         CLI   UCBTBYT4,ICEBERG         IS THIS A 3330-1       @Z30RSAG
         BE    SKIPWIN                  NO, NEED TO FLAG ALT.   XL03130
         CLI   UCBTBYT4,WIN             IS THIS A 3340/3350?    XL03130
         BL    SKIPWIN                  NO, NEED TO FLAG ALT.   XL03130
         LA    R4,WRTR0                 GET WRITE R0 CCW ADDR.  XL03130
         ST    R4,WRTHA                 SET TIC ADDRESS.        XL03130
         MVI   WRTHA,TICCMD             CHANGE WRITE TO TIC.    XL03130
SKIPWIN  EQU   *                                                XL03130
.X227910 ANOP                                                   XL03130
         BAL   R9,WRTREAD              WRITE HA/R0 ON ALTERNATE.
         BAL   R9,WAIT                 AWAIT COMPLETION.
TESTREQ  EQU   *                                               @Y30LSFY
         CLI   FUNCTION,SS1CONV        SSI CONVERSION ?        @Y30LSFY
         BE    NOUPF4                   YES, BRCH              @Y30LSFY
         CLI   FUNCTION,ANALYZE         ANALYZE/FORMAT REQUEST? YL02912
         BNE   UPF4                    NO--NEED TO UPDATE FORMAT4 DSCB.
         SPACE
NOUPF4   EQU   *                                               @Y30LSFY
         BAL   R9,UPCCHH               INCREMENT TO NEXT TRACK.
         L     R4,ALTPTR                GET ADDR OF ALT INFO.   YL02912
         MODESET EXTKEY=SUPR           ALLOW FETCH OF ALT INFO   YM4604
         MVC   0(6,R4),ALTINFO         UPDATE ALTERNATE TRACK INFO.
         MODESET EXTKEY=DATAMGT                                 YL02912
         SPACE
EXIT0    BAL   R9,FREECORE             FREE THE WORKAREA.       YL02912
         SR    R15,R15                 SET RETURN CODE ZERO.    YL02912
RETURN   L     R14,RBEXSAVE+K36(R5)    GET  RETURN ADDRESS.     YL02912
         MODESET EXTKEY=SUPR                                    YM1315
         BR    R14                     RETURN TO CALLER.        YL02912
         EJECT
         AIF   ('&LIB' EQ 'LIB1').X226607                       XL03130
***********************************************************************
*                                                                     *
*   THIS ROUTINE WILL READ THE SKIP DISPLACEMENT BYTES OF A           *
*   DEFECTIVE TRACK ON 3340/3350 DEVICES.                      @Z30RSAG
*                                                                     *
***********************************************************************
         SPACE 2
SAVESD   EQU   *                                                 XM4405
         USING UCB,R7                                            XM4405
         CLI   UCBTBYT4,D3350          IS THIS A 3350 ?        @Z30RSAG
         BE    MADRID                  YES                     @Z30RSAG
         CLI   UCBTBYT4,WIN            IS THIS A 3340?           XM4405
         BCR   NE,R9                   NO, RETURN.               XM4405
         SPACE
MADRID   EQU   *                                               @Z30RSAG
         LA    R4,SNSBYTES             GET ADDR FOR SNS INFO.    XM4405
         ST    R4,READR0               SET IN CCW.               XM4405
         MVI   READR0,SENSE            SET CCW TO SENSE CMD.     XM4405
         MVI   READR0+C7,L24           SET FOR 24 SENSE BYTES.   XM4405
         SPACE
         MVI   RDHA+K4,CHAINSD         CHAIN READ HA TO SENSE.   XM4405
         SPACE
         EXCP  IOB                     GET SKIP DISPLACEMENT.    XM4405
         TM    ECB,COMPLETE            OPERATION COMPLETE?       XM4405
         BO    CKRTN                   YES, CHECK CONDITION.     XM4405
         WAIT  ECB=ECB                 NO, PAUSE.                XM4405
         SPACE
CKRTN    EQU   *                                                 XM4405
         CLI   ECB,GOOD                GOOD COMPLETION?          XM4405
         BCR   EQ,R9                   YES, GOT BYTES, RETURN.   XM4405
         XC    SDBYTES(L2),SDBYTES     NO, INSURE ZERO SD.       XM4405
         BR    R9                      NOW RETURN.               XM4405
         EJECT
***********************************************************************
*                                                                     *
*   WRITE HOME ADDRESS/RECORD ZERO ON A DEFECTIVE TRACK FOR A 3340.   *
*        OR FOR A 3350.                                        @X50RSAG
*                                                                     *
***********************************************************************
         SPACE 2
WRTWIN   EQU   *                                                XL03130
         USING UCB,R7                                           XL03130
         CLI   UCBTBYT4,D3350          IS IT 3350 ?            @X50RSAG
         BE    MAD                     YES                     @X50RSAG
         CLI   UCBTBYT4,WIN             IS THIS A 3340?         XL03130
         BCR   NE,R9                    NO, RETURN.             XL03130
         MVC   WINHA,SDBYTES            SET SKIP DISPLACEMENT.   XM4405
         LA    R4,WINHA                 WRITE HA OUT AREA.      XL03130
         ST    R4,WRTHA                 STORE IN CCW.           XL03130
         MVI   WRTHA+7,C7               SET COUNT IN CCW.       XL03130
         MVI   WRTHA,WRITE              RESTORE WRITE COMMAND.  XL03130
         BR    R9                       RETURN.                 XL03130
MAD      EQU   *                                               @X50RSAG
         LA    R4,MADSD                POINT TO AREA FR 3350   @X50RSAG
         ST    R4,WRTHA                STORE IN WRTHA          @X50RSAG
         MVI   WRTHA+C7,11             SET COUNT FOR 3350      @X50RSPC
         MVI   WRTHA,WRITE             SET COMMAND             @X50RSAG
         BR    R9                      RETURN                  @X50RSAG
         SPACE 2
WINERR   EQU   *                        SKIP DISPLACEMENT IS    XL03130
*                                       DETERMINED HERE.        XL03130
         L     R4,IOBCSW                GET CCW ADDR FROM IOB.  XL03130
         SL    R4,CCWLEN                BACKUP TO LAST CCW.     XL03130
         SPACE 2
         LR    R3,R9                    SAVE R9 CONTENTS       @ZM42054
         BAL   R9,WRTWIN                CHK FOR WRHA IN CHN    @ZM42054
*                                       RETURN HERE IF WRHA EXISTS
         LR    R9,R3                    RELOAD R9 CONTENTS     @ZM42054
         LA    R3,HASD                  HOME ADDR SKIP DISP.    XL03130
         CLI   0(R4),RDHACOM            WAS IT A HA ERROR?      XL03130
         BE    SETSD                    YES, GO SET SKIP DISP.  XL03130
         SPACE 2
         LA    R3,RZCSD                 REC ZERO CNT SKIP DISP. XL03130
         CLI   IOBCSW+7,C8              RESIDUAL COUNT EQ 8?    XL03130
         BE    SETSD                    YES, GO SET SKIP DISP.  XL03130
         SPACE 2
         LA    R3,RZDSD                 NO, SET R0 DATA SKIP.   XL03130
SETSD    EQU   *                                                XL03130
         STH   R3,WINHA                 SET SKIP DISPLACEMENT.  XL03130
         SPACE 2
         B     REWRITE                  WRITE HA/R0 AGAIN.      XL03130
TRKRECL  EQU   *                                               @Z30RSAG
         BAL   R9,FREECORE             FREE IT                 @Z30RSAG
         LA    R15,8                   CODE=RECLAIMED          @Z30RSAG
         B     RETURN                                          @VS40037
         SPACE 2
MADERR   EQU   *                        SKIP DISPLACEMENT IS    XL03130
*                                       DETERMINED HERE.        XL03130
         L     R4,IOBCSW                GET CCW ADDR FROM IOB.  XL03130
         SL    R4,CCWLEN                BACKUP TO LAST CCW.     XL03130
         SPACE 2
         LR    R3,R9                    SAVE R9 CONTENTS       @ZM42054
         BAL   R9,WRTWIN                CHK FOR WRHA IN CHN    @ZM42054
*                                       RETURN HERE IF WRHA EXISTS
         LR    R9,R3                    RELOAD R9 CONTENTS     @ZM42054
         LA    R3,MHASD                 HOME ADDR SKIP DISP.    XL03130
         CLI   0(R4),RDHACOM            WAS IT A HA ERROR?      XL03130
         BE    SETIT                    YES, GO SET SKIP DISP.  XL03130
         SPACE 1
         LA    R3,MRZCSD                REC ZERO CNT SKIP DISP. XL03130
         CLI   IOBCSW+7,C8              RESIDUAL COUNT EQ 8?    XL03130
         BE    SETSD                    YES, GO SET SKIP DISP.  XL03130
         SPACE 2
         LA    R3,MRZDSD                NO, SET R0 DATA SKIP.   XL03130
SETIT    EQU   *                                                XL03130
         STH   R3,WINHA                 SET SKIP DISPLACEMENT.  XL03130
         SPACE 2
         B     REWRITE                  WRITE HA/R0 AGAIN.      XL03130
         EJECT
.X226607 ANOP                                                   XL03130
***********************************************************************
*                                                                     *
*   UPDATE THE FORMAT 4 DSCB HERE.                                    *
*                                                                     *
***********************************************************************
         SPACE 3
UPF4     EQU   *                       UPDAYE THE FORMAT 4 DSCB HERE.
         BAL   R9,UPCCHH               UPDATE TRACK ADDRESS.
         MVC   ALTDATA(6),ALTINFO      UPDATE ALTERNATE INFORMATION.
         LA    R3,SEARCH               CHANNEL PROGRAM ADDRESS.
         ST    R3,IOBSTART             STORE IN IOB.
         MVI   WRTRD,X'05'             SET OP CODE FOR WRITE.
         MVC   CCHHR(4),VTOCCCHH       SEEK//SEARCH ADDRESS.
         BAL   R9,WRTREAD              WRITE OUT NEW FORMAT 4 DSCB DATA
         BAL   R9,WAIT                 AWAIT COMPLETION.
         MVI   WRTRD,X'06'             SET OP CODE FOR READ.
         OI    WRTRD+4,SKIP            SKIP DATA TRANSFER.
         BAL   R9,WRTREAD              READ BACK CHECK DATA.
         BAL   R9,WAIT                 AWAIT COMPLETION.
         B     EXIT0                   GO RETURN TO CALLER.
         SPACE
***********************************************************************
*                                                                     *
*   INCREMENT TO THE NEXT SEQUENTIAL TRACK HERE.                      *
*                                                                     *
***********************************************************************
         SPACE 3
UPCCHH   L     R4,ALTINFO              ALTERNATE TRACK USED.
         CLC   ALTINFO+3(1),3(R6)      THIS LAST TRACK OF CYLINDER.
         BC    4,CEXIT                 NO--
         SPACE
         A     R4,4(R6)                YES-ADD CYLINDER CONVERSION.
         AIF   ('&LIB' EQ 'LIB2').X228300                       XL03912
         CLI   ALTINFO+2,X'04'         2321 AND LAST CYLINDER.
         BC    7,CEXIT2                NO--
         AL    R4,STC2321              YES-INCREMENT STRIP.
         CLI   ALTINFO+1,X'09'         LAST STRIP
         BC    7,CEXIT2                NO--
         AL    R4,SCC2321              YES--INCREMENT SUBCELL.
.X228300 ANOP                                                   XL03912
         B     CEXIT2                  GO STORE VALUE AND RETURN.
CEXIT    AL    R4,F1                   INCREMENT TO NEXT TRACK.
CEXIT2   ST    R4,ALTINFO              SAVE THIS INFORMATION.
         LH    R4,ALTINFO+4            CURRENT ALTERNATE COUNT.
         BCTR  R4,0                    DECREMENT BY ONE.
         STH   R4,ALTINFO+4            SAVE UPDATED COUNT.
         BR    R9                      RETURN.
         EJECT
DATACHN  EQU   X'80'                                           @Z30RSAG
*************************************************************  @Z30RSAG
*                                                              @Z30RSAG
*   THIS SECTION PERFORMS SURFACE ANALYSIS ON 3350 DEVICES     @Z30RSAG
*                                                              @Z30RSAG
*************************************************************  @Z30RSAG
         SPACE 2                                               @Z30RSAG
DOSA     EQU   *                                               @Z30RSAG
         USING UCB,R7                                          @ZM40450
         CLI   UCBTBYT4,D3350           3350 DEVICE ?          @ZM40450
         BNER  R9                       NO, BYPASS S.A.        @ZM40450
         DROP  R7                                              @ZM40450
         CLI   0(R11),X'1F'             GETALT REQUEST?        @ZM42054
         BNER  R9                       NO, EXIT               @ZM42054
         MVC   MADSD(L6),MADSDB        SAVE SD BYTES           @X50RSPC
         MVI   HA,X'00'                CLEAR HA FLAG BYTE      @X50RSAG
*   SET UP SEARCH HA                                           @X50RSAG
         LA    R4,HA+1                 POINT TO HA AREA        @X50RSAG
         ST    R4,SCHHA                STORE ADR IN SRCH       @X50RSAG
         MVI   SCHHA,SRCH              SET SRCH HA CMND        @X50RSAG
         LA    R4,4                                            @X50RSAG
         ST    R4,SCHHA+L4             SET CCW DATA LENGTH     @Z30RSAG
         MVI   SCHHA+L4,CHAIN          SET CMND CHAINING       @Z30RSAG
*   SET UP TIC                                                 @Z30RSAG
         LA    R4,SCHHA                                        @Z30RSAG
         ST    R4,IOBSTART             UPDATE IOBCCW PTR       @Z30RSAG
         ST    R4,TICSRCH               AND TIC ADR            @Z30RSAG
         MVI   TICSRCH,X'08'           SET COMMAND             @Z30RSAG
*   SET UP WRITE HA                                            @Z30RSAG
         LA    R4,MADSD                GET THE ADDRESS         @Z30RSAG
         ST    R4,WRTHA                STORE IN COMMAND        @Z30RSAG
         MVI   WRTHA,X'19'             SET COMMAND             @Z30RSAG
         LA    R4,11                   COUNT FOR 3350          @VS40430
         STH   R4,WRTHA+L6             UPDATE LENGTH           @Z30RSAG
         MVI   WRTHA+L4,CHAIN          SET CMND CHAINING       @Z30RSAG
*   SET UP WRITE R0 MAX                                        @Z30RSAG
         LA    R3,R0COUNT              MOVE IN R0 COUNT FIELD  @Z30RSAG
         ST    R3,WRTR0                                        @Z30RSAG
         MVI   WRTR0,X'15'             SET COMMAND             @Z30RSAG
         LA    R4,8                                            @Z30RSAG
         ST    R4,WRTR0+L4                                     @Z30RSAG
         MVI   WRTR0+L4,DATACHN                                @Z30RSAG
         L     R3,8(R11)               GET BIT PATTERN ADDRESS @Z30RSAG
         ST    R3,R0DATA               SET UP CCW DATA ADDRESS @Z30RSAG
         L     R3,R0COUNT              RELOAD PRIMARY CCHH     @ZM43013
         MVI   R0DATA,X'15'            SET CCW OPCODE          @Z30RSAG
         LH    R4,MADCAP               3350 TRACK SIZE         @Z30RSAG
         ST    R4,R0DATA+L4            SET CCW DATA LENGTH     @Z30RSAG
         ST    R4,R0COUNT+L4           SET CCW DATA LENGTH     @Z30RSAG
         MVI   R0DATA+L4,CHAIN         SET CMND CHAINING       @Z30RSAG
*   SET UP READ HA                                             @Z30RSAG
         MVC   RDHA(L8),WRTHA          COPY CCW                @Z30RSAG
         MVI   RDHA,X'1A'              SET COMMAND             @Z30RSAG
         MVI   RDHA+C7,5                RESET DATA LEN         @Z30RSAG
         MVI   RDHA+L4,CHAINSD+SILI     RESET FLAGS            @Z30RSAG
*   SET UP READ R0 MAX                                         @Z30RSAG
         MVC   READR0(L8),WRTR0        COPY CCW                @Z30RSAG
         MVI   READR0,X'16'            SET COMMAND             @Z30RSAG
         MVI   READR0+L4,SILI+SKIP     STOP COMMAND CHAIN      @Z30RSAG
*   EXECUTE THE CHANNEL PROGRAM                                @Z30RSAG
         MODESET EXTKEY=SUPR
         EXCP  IOB                                             @Z30RSAG
         MODESET EXTKEY=DATAMGT
         WAIT  ECB=ECB                                         @Z30RSAG
         CLI   ECB,GOOD                OK ?                    @Z30RSAG
         BNER  R9                      NO                      @Z30RSAG
         LA    R4,R0COUNT                                      @Z30RSAG
         STCM  R4,R7,WRTR0+1           STORE COUNT IN CMND     @VS40037
         LA    R4,8                    SET DATA LENGTH FOR R0  @ZM43013
         ST    R4,R0COUNT+4            STORE IN COUNT FLD      @ZM43013
         MVI   WRTR0+R7,L16            RESET CCW LENGTH        @Z30RSAG
         MVI   WRTR0+L4,SILI           STOP CHAIN              @Z30RSAG
         XC    ECB,ECB                 CLEAR IT                @Z30RSAG
         MVI   IOB,X'42'                                       @Z30RSAG
         EXCP  IOB                                             @Z30RSAG
         WAIT  ECB=ECB                                         @Z30RSAG
         B     TRKRECL                 EXIT WITH 823I MSG      @Z30RSAG
         EJECT                                                 @Z30RSAG
***********************************************************************
*                                                                     *
*   DEVICE CONSTANTS---ORDERED BY UCB TYPE CODES.
*                                                                     *
***********************************************************************
         SPACE 3
         DS    0F
         AIF   ('&LIB' EQ 'LIB1').X226603                       XL03130
FIRSTALT DC    X'015B000B'              3340 MOD 1 LAST PRIMARY XL03130
         DC    X'0000FFF5'              3340 CYLINDER CONVERT   XL03130
         SPACE
         DC    X'02B7000B'              3340 MOD 2 LAST PRIMARY XL03130
         DC    X'0000FFF5'              3340 CYLINDER CONVERT   XL03130
         SPACE
         DS    10F                      CODES 3 THRU 7 NOT USED.XL03130
*                                       2305 SUPPORT IS 308B.   XL03130
         AGO   .X226604                                         XL03130
.X226603 ANOP                                                   XL03130
FIRSTALT DC    X'00C70009'             2311 LAST PRIMARY.
CONVCYL  DC    X'0000FFF7'             2311 CYLINDER CONVERSION.
         SPACE 1
         DS    4F                      2301/2303 NOT APPLICABLE.
         SPACE
         DC    X'00F5002D'             2302 LAST PRIMARY.
         DC    X'0000FFD3'             2302 CYLINDER CONVERSION.
         SPACE 1
         DC    X'13050413'             2321 LAST PRIMARY.
         DC    X'000000ED'             2321 CYLINDER CONVERSION.
         SPACE
         DS    4F                      CODES 6 AND 7 NOT USED.
*                             2305 SUPPORT IS HANDLED IN 308B    X02114
.X226604 ANOP
         SPACE
         DC    X'00C70013'             2314 LAST PRIMARY.
         DC    X'0000FFED'             2314 CYLINDER CONVERSION.
         SPACE
         DC    X'01930012'              CCHH AT LAST PRIMARY     M5448
         DC    X'0000FFEE'              CYL INCREMENT            M5448
         SPACE
         AIF   ('&LIB' EQ 'LIB1').X227911                       XL03145
         DS    2F                      CODE A NOT USED         @Z30RSAG
         SPACE
         DC    X'022A001D'             3350 LAST PRIMARY       @Z30RSAG
         DC    X'0000FFE3'             3350 CYL CONVERSION     @Z30RSAG
         SPACE 1                                               @Z30RSAG
         DS    2F                      CODE C NOT USED         @Z30RSAG
         DC    X'03270012'             3330-1 LAST PRIMARY.     XL03145
         DC    X'0000FFEE'             3330-1 CYL CONVERSION.   XL03145
         SPACE
*        SS/1 ALTERNATE TRACK INFORMATION                      @Y30LSFY
SS1CONM  DC    X'01980012'              CCHH AT LAST PRIMARY   @Y30LSFY
         DC    X'0000FFEE'              CYL INCREMENT          @Y30LSFY
SS1CONI  DC    X'03280012'              CCHH AT LAST PRIMARY   @Y30LSFY
         DC    X'0000FFEE'              CYL INCREMENT          @Y30LSFY
         AGO   .X227912                                         XL03145
.X227911 ANOP                                                   XL03145
SCC2321  DC    X'00F60000'             INCREMENT TO NEXT SUBCELL.
STC2321  DC    X'0000FB00'             INCREMENT TO NEXT STRIP.
.X227912 ANOP                                                   XL03145
         EJECT
WRTREAD  EXCP  IOB                     START THE I/O OPERATION.
         BR    R9                      RETURN.
         SPACE
WAIT     TM    ECB,COMPLETE            THIS OPERATION COMPLETE.
         BO    TEST                    YES-TEST ITS STATUS.
         WAIT  ECB=ECB                 NO--AWAIT COMPLETION.
TEST     CLI   ECB,GOOD                EVERYTHING A-OK.
         BCR   8,R9                    YES-RETURN.
         TM    IOBCSW+4,UE             THIS A UNIT EXCEPTION.
         BCR   1,R9                    YES-//THIS IS OKAY.
         TM    RETSW,MOVEHA            THIS THE 2ND ATTEMPT.   @ZA04363
         BO    IOERR                   YES--CANNOT TRY AGAIN.
         AIF   ('&LIB' EQ 'LIB1').X227601
         CLI   UCBTBYT4,D3350           IS THIS A 3350?         XL03130
         BE    MADERR                   YES GO FIND SKIP DISP.  XL03130
         CLI   UCBTBYT4,WIN             IS THIS A 3340?         XL03130
         BE    WINERR                   YES GO FIND SKIP DISP.  XL03130
.X227601 ANOP
REWRITE  EQU   *                                               @ZM42054
         OI    RETSW,MOVEHA            SET FLG,MOVE DOWN TRK.  @ZA04363
         EXCP  IOB                     REPEAT THE REQUEST.
         B     WAIT                    AWAIT COMPLETION.
         SPACE
FREECORE EQU   *                       RELEASE THE CORE HERE.
         LH    R3,SIZE                 SIZE OF AREA TO RELEASE.
         FREEMAIN R,LV=(3),A=(2),SP=229                         YL02912
         LA    R3,ENDLIST-PARMLIST      SIZE OF PARM LIST.      YL02912
         FREEMAIN R,LV=(3),A=(11),SP=229 FREE PARM LIST.        YL02912
         BR    R9                      RETURN.
         SPACE
IOERR    EQU   *
         BAL   R9,FREECORE              RELEASE GOTTEN CORE.    YL02912
         LA    R15,12                  SET RETURN CODE.
         B     RETURN                  RETURN TO CALLER.        YL02912
         EJECT
F1       DC    F'1'                    TRACK INCREMENT CONSTANT.
MADCAP   DC    1H'19069'               3350 TRACK SIZE         @X50RSPC
ROLENGTH DC    1F'8'                    R0 KEY DATA L            S21041
ZERO     DC    1H'0'                   COMPARE CONSTANT.        YL02912
MAINT    DS    60X                      ZAP FIX AREA.           YL02912
K4       EQU   4                        DISPLACEMENT CON         S21041
         AIF   ('&LIB' EQ 'LIB1').X227602
C4       EQU   X'04'                    COUNT FOR SEARCH.       XL03130
EQ       EQU   X'08'                    EQUAL CONDITION.         XM4405
NE       EQU   X'07'                    NOT EQUAL CONDITION.    XL03130
C7       EQU   X'07'                    COUNT FOR 3340 WRT HA.  XL03130
C8       EQU   X'08'                    COUNT COMPARAND.        XL03130
L2       EQU   2                        LENGTH CONSTANT.         XM4405
L4       EQU   4                        LENGTH CONSTANT        @Z30RSAG
L6       EQU   6                        LENGTH CONSTANT        @Z30RSAG
L8       EQU   8                        LENGTH CONSTANT        @Z30RSAG
L24      EQU   24                       LENGTH CONSTANT.         XM4405
HASD     EQU   110                      HOME ADDR SKIP DISP.    XL03130
RZCSD    EQU   200                      REC ZERO CNT SKIP DISP. XL03130
RZDSD    EQU   285                      REC ZERO DATA SKIP DISP XL03130
MHASD    EQU   124                      HOME ADDR SKIP DISP.   @Z30RSAG
MRZCSD   EQU   200                      REC ZERO CNT SKIP DISP @Z30RSAG
MRZDSD   EQU   310                      REC ZERO DATA SD       @Z30RSAG
CCWLEN   EQU   ROLENGTH                 CCW LENGTH.             XL03130
SENSE    EQU   X'04'                    SENSE COMMAND.           XM4405
WRITE    EQU   X'19'                    WRITE HA COMMAND.       XL03130
SRCH     EQU   X'39'                    SEARCH HA COMMAND.      XL03130
RDHACOM  EQU   X'1A'                    READ HA COMMAND.        XL03130
TICCMD   EQU   X'08'                    TIC COMMAND.            XL03130
         SPACE 3
***********************************************************************
*                                                                     *
*   PARAMETER LIST DSECT                                              *
*                                                                     *
***********************************************************************
         SPACE 2
PARMLIST DSECT                                                  YL02912
FUNCTION DS    0F                       CALLER'S FUNCTION.      YL02912
UCBPTR   DS    F                        UCB POINTER.            YL02912
DCBPTR   DS    F                        DCB POINTER OR          YL02912
CCHHPARM EQU   DCBPTR                   ADDR OF TRACK OR        YL02912
SERPTR   EQU   DCBPTR                   ADDR OF VOL SER.        YL02912
ALTPTR   DS    F                        ADDR OF ALTINFO OR      YL02912
NEWCCHH  EQU   ALTPTR                   NEW TRACK ADDRESS.      YL02912
DEBPTR   DS    F                        DEB POINTER OR          YL02912
DEVMODP  EQU   DEBPTR                   ADDR OF 3340 MODEL NO.  YL02912
PRMPTR   DS    F                        ADDR OF CALLER'S LIST.  YL02912
KEY      EQU   PRMPTR                   KEY OF CALLER.          YL02912
IGCRESV  DS    4F                       RESERVED                 YM1129
ENDLIST  EQU   *                        PARAMETER LIST END.     YL02912
         EJECT
.X227602 ANOP                                                   XL03130
         EJECT
IOBLOCKS DSECT
MYDEB    EQU   *                       DEB DEFINITION.
DEBEOEA  DS    1F                      END-OF-EXTENT.
DEBSIOA  DS    1F                      START I/O.
DEBPCIA  DS    1F                      PCI.
DEBCEA   DS    1F                      CHANNEL END.
DEBXCEA  DS    1F                      ABNORMAL END.
         DS    CL12
DEBLNGTH DS    CL1                     DEB LENGTH(DOUBLE WORDS)
         DS    CL3
DEB      EQU   *                       DEB PROPER
DEBNMSUB DS    CL1                     OPEN SUBROUTINES.
DEBTCBAD DS    CL3                     TCB ADDRESS.
DEBAMLNG DS    CL1                     LENGTH ACCESS METHOD.
DEBDEBAD DS    CL3                     NEXT DEB ADDRESS.
DEBOFLGS DS    CL1                     DATA SET FLAGS.
DEBIRBAD DS    CL3                     IRB ADDRESS.
DEBOPATB DS    CL1                     TYPE OF I/O.
DEBSYSPG DS    CL3                     IOB PURGE ADDRESS.(SYSTEM)
DEBNMEXT DS    CL1                     NO. OF EXTENTS.
DEBUSRPG DS    CL3                     IOB USER PURGE ADDRESS.
DEBPRIOR DS    CL1                     ZERO
DEBECBAD DS    CL3                     PURGE ECB.
DEBDEBID DS    CL1                     PROTECT KEY//ID.
DEBDCBAD DS    CL3                     DCB ADDRESS.
DEBEXSCL DS    CL1                     EXTENT SCALE.
DEBAPPAD DS    CL3                     APPENDAGE VECTOR TABLE.
DEBDVMOD DS    CL1                     FILE MASK.
DEBUCBAD DS    CL3                     UCB ADDRESS.
DEBBINNO DS    CL2                     BIN NO. FOR 2321.
DEBSTRCC DS    CL2                     CYLINDER START.
DEBSTRHH DS    CL2                     HEAD START.
DEBENDCC DS    CL2                     CYLINDER END.
DEBENDHH DS    CL2                     HEAD END.
DEBNMTRK DS    CL2                     NUMBER OF TRACKS.
         DS    CL20
DEBEND   DS    0C                      END OF DEB.
         EJECT
ECB      DS    1F                      ECB HERE.
IOB      EQU   *                       IOB HERE.
IOBFLAG1 DS    CL1                     FLAG1
IOBFLAG2 DS    CL1                     FLAG2.
IOBSENS0 DS    CL1                     SENSE BYTE ZERO.
IOBSENS1 DS    CL1                     SENSE BYTE ONE.
IOBECBAD DS    CL4                     ADDRESS OF ECB.
IOBCSW   DS    CL8                     STATUS WORDS.
IOBSTART DS    CL4                     CHANNEL PROGRAM ADDRESS.
IOBDCBPT DS    CL4                     ADDRESS OF DCB.
IOBRESTR DS    CL4                     RESTART ADDRESS.
IOBINCAM DS    CL2                     BLOCK COUNT.
IOBERRCT DS    CL2                     ERROR COUNT.
IOBSEEK  DS    CL8                     SEEK ADDRESS.
CCHHR    EQU   IOBSEEK+3               SEARCH ADDRESS.
DCB      DS    1F                      DEB ADDRESS.
ALTINFO  DS    1D                      KEEP ALTERNATE TRACK INFO HERE.
         AIF   ('&LIB' EQ 'LIB1').X302301                       XL03130
SCHHA    DS    1D                      SEARCH HOME ADDRESS.     XL03130
TICSRCH  DS    1D                       TIC CCW.                XL03130
.X302301 ANOP                                                   XL03130
WRTHA    DS    1D                      WRITE HOME ADDRESS.
WRTR0    DS    1D                      WRITE RECORD ZERO.
R0DATA   DS    1D                                              @Z30RSAG
RDHA     DS    1D                      READ HOME ADDRESS.
READR0   DS    1D                      READ RECORD ZERO.
         AIF   ('&LIB' EQ 'LIB1').X227800                       XL03130
         DS    CL6                      RESERVED.               XL03130
MADSD    DS    CL4                     ADTL 3350 SD BYTES      @X50RSPC
WINHA    DS    CL2                      3340 HOME ADDRESS.      XL03130
.X227800 ANOP                                                   XL03130
HA       DS    CL5                     HOME ADDRESS.
SW       DS    CL1                     SWITCH.
D2314    EQU   X'80'                   2314 DEVICE.
D2321    EQU   X'40'                   2321 DEVICE.
DEFALT   EQU   X'02'                                               1057
RETRY    EQU   X'01'                   GIVE IT ONE MORE TRY.
SIZE     DS    CL2                     SIZE OF GETMAIN AREA.
R0COUNT  DS    1D                      RECORD ZERO COUNT FIELD.
         DS    1D                      RECORD ZERO DATA  FIELD.@Z30RSAG
         AIF   ('&LIB' EQ 'LIB1').X322202                        XM4405
SNSBYTES DS    CL24                    SNS BYTE READ IN AREA.    XM4405
MADSDB   EQU   SNSBYTES+18             ADTL 3350 SD BYTES      @X50RSPC
SDBYTES  EQU   SNSBYTES+22             SKIP DISP IN SENSE AREA.  XM4405
.X322202 ANOP                                                    XM4405
RETSW    DS    CL1                     RETRY SW                @ZA04363
IOEND1   DS    0C                      END OF BASIC I/O BLOCKS.
*   EXTENSION FOR GETALT//DYNAMIC TRACK REQUEST.
SEARCH   DS    1D                      SEARCH CCW.
TIC      DS    1D                      REPEAT UNTIL FOUND.
WRTRD    DS    1D                      WRITE OR READ DATA.
FORMAT4  DS    CL96                    FORMAT4 DSCB READ IN AREA.
ALTDATA  EQU   FORMAT4+8               ALTERNATE TRACK INFORMATION.
VTOCCCHH EQU   FORMAT4+63              CCHH START OF VTOC.
IOEND    DS    0C
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
CVT      DSECT
         CVT   SYS=MIN
         END
