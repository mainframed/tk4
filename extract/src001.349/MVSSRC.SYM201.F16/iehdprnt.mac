 TITLE   'OUTPUT MESSAGE WRITER  IEHDPRNT'
         COPY  LCGASMSW
IEHDPRNT CSECT
*
*C 72000,684000                                                @ZA01240
*A 63500-64000                                                 @ZA01240
*
*A 423500-425000,706500             @SA69598=@YA06118=@XA06603=@ZA01240
************************************************************
*                                                                  0811
*1417684000                                                        0811
*                                                                  1352
*3476684000                                                        1352
*                                                                     *
*684000                                                            6057
*3462684000                                                      M0028
*3462                                                            A32533
*3462684000                                                      M0025
*STATUS CHANGE LEVEL 005
*                                                                     *
*FUNCTION/OPERATION- THIS ROUTINE HANDLES ALL OUTPUT TO THE SYSOUT    *
*   DATA SET FOR THE IEHDASDR SYSTEM UTILITY PROGRAM. INITIAL ENTRY   *
*   TO THIS ROUTINE RESULTS IN THE HEADER INFORMATION BEING FORMATED  *
*   (PROGRAM TITLE, DATE, AND PAGE NUMBER) AND WRITTEN OUT USING THE  *
*   QSAM PUT MACRO, MOVE MODE. THE MESSAGE BUFFER (IN IEHDWORK        *
*   DSECT) IS THEN BLANKED OUT AND CONTROL RETURNED TO THE MONITOR.   *
*   SUBSEQUENT ENTRIES WILL WRITE OUT THE CONTENTS OF THIS BUFFER     *
*   (CONTROL CARDS,MESSAGES, ETC.), DECREMENT THE LINE COUNT,         *
*   BLANK THE OUTPUT BUFFER, AND RETURN TO THE CALLER.                *
*                                                                     *
*ENTRY POINTS- THE ONLY ENTRY POINT IS -IEHDPRNT-.                    *
*                                                                     *
*INPUT- A POINTER TO THE WORK AREA IN REGISTER 12.                    *
*                                                                     *
*EXITS-NORMAL- CONTROL IS RETURNED TO THE CALLING ROUTINE BY A        *
*   BRANCH ON REGISTER 14. NO RETURN CODE IS PASSED.                  *
*                                                                     *
*EXITS-ERROR- NONE.                                                   *
*                                                                     *
*EXTERNAL ROUTINES- INITIAL ENTRY TO THIS ROUTINE AND FOR EACH        *
*   NEW PAGE OF OUTPUT, THE ROUTINE WILL CALL -IEHDDATE- TO PROCESS   *
*   MONTH/DAY/YEAR FOR THE HEADING.                                   *
*                                                                     *
*TABLES/WORK AREAS- UPON ENTRY, REGISTER 12 WILL POINT TO THE WORK    *
*   AREA, DESCRIBED BY THE DSECT -IEHDWORK-.                          *
*                                                                     *
*ATTRIBUTES- SERIALLY REUSABLE, RELOCATABLE.                          *
*                                                                     *
*NOTE- THE VERSION LEVEL OF THE IEHDASDR SYSTEM UTILITY PROGRAM IS    *
*   CONTAINED IN THE HEADER INFORMATION LABELED 'TOPLINE'. THIS       *
*   VERSION LEVEL SHOULD BE UPDATED WHEN INCREMENTAL IMPROVEMENTS     *
*   AND/OR RELEASE LEVELS CHANGE.                                     *
*                                                                     *
         EJECT
         SAVE  (14,12),T,*             SAVE THE REGISTERS.
         USING IEHDPRNT,REG11          SET UP ADDRESSIBILITY.
         USING WORK,12
         LR    REG11,REG15             LOAD BASE REGISTER.
         B     APARNO                  BRANCH AROUND APARNO    @ZA01240
         DC    C'IEHDPRNT OZ01240'     LAST FIX THIS MOD       @ZA01240
APARNO   LA    REG1,PRNTSAVE           ADDRESS NEW SAVE AREA.  @ZA01240
         ST    REG1,8(REG13)           SAVE ADDRESS OF NEW AREA IN OLD.
         ST    REG13,4(REG1)           SAVE ADDRESS OF OLD AREA IN NEW.
         LR    REG13,REG1              SAVE AREA ADDRESS.
         MVC   MSGNO(2),MESS+K4        SAVE MSG NUMBER.          A32533
         LA    REG3,MSGWTR             ADDRESS OF MESSAGE BUFFER.
         SPACE 1
         TM    MSGSW1,X'FF'            IS THIS THE INITIAL ENTRY.
         BC    8,FRSTNTRY              YES-GO PRINT HEADER AND PAGE NO.
         BAL   REG9,PRINT              NO--GO PRINT A LINE.
         SPACE 1
RETURN   L     REG13,PRNTSAVE+4        RESET REGISTER 13.
         RETURN (14,12),T              RESTORE REGS AND RETURN.
         SPACE 1
PRINT    L     REG4,PRINT1             DCB ADDRESS.
         PUT   (REG4),(REG3)           WRITE OUT THE MESSAGE.
         MVI   MSGWTR,X'40'            SET FOR SINGLE SPACE.
         MVI   MESS,BLANK              BLANK FIRST BYTE.
         MVC   MESS+1(119),MESS        CLEAR REMAINDER OF BUFFER.
         LH    REG7,LINECNT1           PICK UP THE NUMBER OF LINES.
         BCT   REG7,NOTHDR             REDUCE BY ONE/BRANCH NOT ZERO.
         CLC   MSGNO(2),LASTMSG        LAST LINE OF FUNCTION.    A32533
         BE    K0(0,REG9)              YES-RETURN TO CALLER.     A32533
         MVC   LINECNT1(2),LINECNT     RESET THE LINE COUNT.
         BAL   REG8,HEADER1            BRANCH TO PRINT HEADER.
         BR    REG9                    RETURN.
         SPACE 1
NOTHDR   STH   REG7,LINECNT1           SAVE THE NEW LINE COUNT.
         BR    REG9                    RETURN.
         SPACE 2
HEADER   MVI   MESS,BLANK              BLANK FIRST BYTE.
         MVC   MESS+1(119),MESS        CLEAR REMAINDER OF BUFFER.
HEADER1  MVC   NOTHER+10(4),PAGE       MOVE IN PAGE.
         LH    REG6,PAGENO             PICK UP THE PAGE NUMBER.
         CVD   REG6,DOUBLE             CONVERT TO DECIMAL.
         UNPK  NUMB(3),DOUBLE+6(2)     CONVERT TO GRAPHIC AND MOVE.
         OI    NUMB+2,X'F0'            RESET ZONE.
         LA    REG6,LAST1-TOPLINE-1    LENGTH OF HEADER MINUS ONE.
         L     REG7,HEAD               ADDRESS OF HEADER.
         EX    REG6,MOVER              MOVE IN THE HEADER.
         LA    REG1,MESS+5             TARGET ADDRESS FOR DATE.
         CALL  IEHDDATE
         MVI   CC,X'F1'                CONTROL CHARACTER.
         L     REG4,PRINT1             DCB ADDRESS.
         PUT   (REG4),(REG3)           WRITE OUT THE HEADER.
         LH    REG7,LINECNT1           PICK UP NUMBER OF LINES @ZA01240
         LH    REG5,THREE              LOAD 3 TO REG5          @ZA01240
         SR    REG7,REG5               SUBTRAKT 3 FROM LINECNT @ZA01240
         STH   REG7,LINECNT1           SAVE NEW LINECNT        @ZA01240
         MVI   MESS,BLANK              BLANK FIRST BYTE.
         MVC   MESS+1(119),MESS        CLEAR REMAINDER OF BUFFER.
         MVI   MSGWTR,X'60'            TRIPLE SPACE BEFORE PRINT.
         LH    REG6,PAGENO             PICK UP THE PAGE NUMBER.
         LA    REG6,1(REG6)            ADD ONE TO PAGE NUMBER.
         STH   REG6,PAGENO             SAVE THE NEW PAGE NUMBER.
         BR    REG8                    RETURN.
         SPACE 1
FRSTNTRY BAL   REG8,HEADER             GO WRITE OUT THE HEADER.
         MVI   MSGSW1,X'FF'            INDICATE FIRST ENTRY.
         B     RETURN                  TAKE NORMAL EXIT.
         SPACE 1
DOUBLE   DC    1D'0'                   WORK AREA FOR CONVERT.
PRNTSAVE DS    18F                     SAVE AREA FOR CALLED ROUTINES.
HEAD     DC    A(TOPLINE)              ADDRESS OF HEADER
MOVER    MVC   MESS+35(1),0(REG7)      EXECUTED TO MOVE IN HEADER.
PAGE     DC    CL4'PAGE'               'PAGE' HEADING.
TOPLINE  DC    CL29'SYSTEM SUPPORT UTILITIES --- '
         DC    CL17'IEHDASDR   MVS-3.0'                        @ZA01240
LAST1    DS    0C                      END OF HEADER.
BLANK    EQU   C' '                    BLANK CHARACTER.
M39      EQU   39                      LAST MSG PRINTED.         A32533
K4       EQU   4                       DISPLACEMENT CONSTANT     A32533
K0       EQU   0                       DISPLACEMENT CONSTANT     A32533
MSGNO    DS    1H                      MESSAGE NUMBER AREA       A32533
THREE    DC    H'3'                    NO OF LINES/HEADER      @ZA01240
LASTMSG  DC    CL2'39'                 LAST MESSAGE PRINTED      A32533
*   THE FOLLOWING ARE REGISTER ASSIGNMENTS.
REG0     EQU   0
REG1     EQU   1
REG2     EQU   2
REG3     EQU   3
REG4     EQU   4
REG5     EQU   5
REG6     EQU   6
REG7     EQU   7
REG8     EQU   8
REG9     EQU   9
REG10    EQU   10
REG11    EQU   11
REG12    EQU   12
REG13    EQU   13
REG14    EQU   14
REG15    EQU   15
         SPACE 1
         IEHDWORK
         END
