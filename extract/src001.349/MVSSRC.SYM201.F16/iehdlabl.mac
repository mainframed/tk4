         TITLE 'IEHDLABL - PERFORMS LABEL FUNCTION  IEHDASDR UTILITY'
         COPY  LCGASMSW
IEHDLABL CSECT
***********************************************************************
*
*                     FIXES THIS MODULE
*                     LATEST FIRST.
*
***********************************************************************
*
*D 66000                                  SU32 ONLY            @ZA25515
*C 113000                                 SU32 ONLY            @ZA25515
*A 112000-112500                          SU32 ONLY            @ZA25515
*A 217160-217190,753100                   SU32 ONLY            @ZA25515
*
*  NO DELETION                            SU32                 @G32DSPD
*                                                                     *
*003600,011900,012300,212500,318200                                O122
*                                                                     *
*STATUS- CHANGE LEVEL 001                                          O122
***********************************************************************
      EJECT
*FUNCTION/OPERATION- THIS ROUTINE HANDLES THE -LABEL- FUNCTION        *
*   OF THE IEHDASDR SYSTEM UTILITY PROGRAM. LABEL CONSISTS OF         *
*   WRITING A NEW VOLUME SERIAL NUMBER ON A SPECIFIED DIRECT          *
*   ACCESS DEVICE. IN ADDITION, IT WILL OPTIONALLY ADD AN OWNER       *
*   NAME TO THE VOLUME LABEL RECORD.                                  *
*   RACF AUTHORIZATION IS REQUIRED ON THOSE VOL IDS THAT ARE   @G32DSPD
*   DEFINED TO RACF.                                           @G32DSPD
*                                                                     *
*ENTRY POINTS- THE ONLY ENTRY POINT IS -IEHDLABL-, AND CONTROL        *
*   IS ALWAYS RECEIVED FROM THE MONITOR MODULE IEHDASDS.              *
*                                                                     *
*INPUT- POINTERS TO A WORKAREA(REGISTER 12) AND TO A CONTROL          *
*   BLOCK (REGISTER 10).                                              *
*   DATA IS ON A DIRECT ACCESS DEVICE. ONLY THE VOLUME LABEL          *
*   RECORD (RECORD 3 ON TRACK 0) WILL BE READ.                        *
*                                                                     *
*EXITS- NORMAL- A SUCCESSFUL LABEL OPERATION RESULTS IN A RETURN      *
*   TO THE MONITOR(IEHDASDS) WITH A RETURN CODE OF 0. A LABEL         *
*   COMPLETE MESSAGE IS ALSO PLACED IN THE MESSAGE DATA SET.          *
*                                                                     *
*EXITS- ERROR- ANY ERROR ENCOUNTERED IS DESCRIBED BY AN APPROPRIATE   *
*   MESSAGE TO THE MESSAGE DATA SET. A RETURN CODE GREATER THAN       *
*   ZERO WILL ALSO BE PASSED BACK TO THE MONITOR.                     *
*                                                                     *
*EXTERNAL ROUTINES- THIS ROUTINE IS ALWAYS ENTERED FROM THE           *
*   MONITOR MODULE IEHDASDS. FOUR OTHER CSECTS ARE USED BY THIS       *
*   ROUTINE. ANY MESSAGES ISSUED WILL BE FORMULATED BY THE MESSAGE    *
*   ROUTINE (IEHDMSGB) AND PRINTED BY THE PRINT ROUTINE (IEHDPRNT)    *
*   THE END OF EXTENT APPENDAGE (IGG019P8) HANDLES THE LIMITS AND  O122
*   EXTENTS IN THE DEB FOR ALL DA DEVICES INVOLVED. IGC0008B (SVC 82) *
*   ENTERS THE NEW VOLUME SERIAL NUMBER INTO THE UCB, AND FOR AN   O122
*   OFFLINE LABEL FUNCTION WILL REQUEST OPERATOR CONFIRMATION AND  O122
*   BUILD A DEB IN PROTECTED CORE.                                 O122
*                                                                     *
*TABLES/WORK AREAS- UPON ENTRY REGISTERS 10 AND 12 POINT TO A         *
*   FUNCTION BLOCK AND A WORK AREA RESPECTIVELY. THEY ARE DESCRIBED   *
*   IN THE DSECTS CALLED -FUNCBLK- AND -WORK-.                        *
*                                                                     *
*ATTRIBUTES- SERIALLY REUSABLE, RELOCATABLE.                          *
*                                                                     *
*   THE FOLLOWING ARE REGISTER ASSIGNMENTS.
GR0      EQU   0
GR1      EQU   1
GR2      EQU   2
GR3      EQU   3
GR4      EQU   4
GR5      EQU   5
GR6      EQU   6
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10
GR11     EQU   11
GR12     EQU   12
GR13     EQU   13
GR14     EQU   14
GR15     EQU   15
UCBPTR   EQU   9
BASEREG  EQU   11
WORKBASE EQU   12
         USING FUNCBLK,10
         USING IEHDLABL,BASEREG
         USING WORK,12
         SAVE  (14,12),T,*             SAVE THE REGISTERS
         LR    BASEREG,GR15            LOAD THE BASE REGISTER.
         LA    GR7,LABLSAVE            SAVE AREA ADDRESS.
         ST    GR7,8(GR13)             ADDRESS OF NEW AREA TO OLD.
         ST    GR13,4(GR7)             ADDRESS OF OLD AREA TO NEW.
         LR    GR13,GR7                LOAD SAVE AREA ADDRESS.
         B     APARNO                  BRANCH AROUND APAR NO   @ZA25515
         DC    C'SU32 OZ25515'         LAST FIX THIS MODULE    @ZA25515
APARNO   L     GR8,RDDCBAD             POINTER TO DCB          @ZA25515
         USING IHADCB,8
         L     UCBPTR,TUCBPTR          PICK UP POINTER TO UCB
         USING UCB,9
         LA    GR4,0                   INITIALIZE BIN NO. TO 0
         ST    GR4,RDSEEK
         AIF   ('&LIB' EQ 'LIB2').X234300                       XL03912
         CLI   UCBID,X'FF'             IS THIS MAIN UCB
         BNE   PASSBY                  NO-GO PICK UP BIN NUMBER
.X234300 ANOP                                                   XL03912
         TM    UCBTBYT3,UCB3DACC       IS THIS A DIRECT ACCESS DEVICE
         BZ    BADPARM                 NO--MUST BE BAD PARAMETER.
         TM    SRTESTAT,SRTESYSR       THIS A SYSTEMS RESIDENCE VOLUME
         BO    BADDEV                  YES - NOT ALLOWED
CHKMORE  EQU   *                                                   O122
         CLI   DDNAME2,X'40'           OFFLINE LABEL REQUEST.      O122
         BNE   SKIP                    NO-NO NEED TO CHECK FURTHER.O122
         TM    SRTESTAT-UCBOB(UCBPTR),SRTEONLI  YES-DEVICE ONLINE. O122
         BO    ONLINERR                YES-SET UP ERROR MESSAGE.   O122
         LA    GR1,RDAREA              ADDR. OF AREA FOR LIST.     O122
         ST    UCBPTR,0(GR1)           STORE UCB PTR. IN LIST.     O122
         MVI   0(GR1),X'8F'            SET UP BUILD DEB INDICATOR. O122
         ST    GR8,4(GR1)              STORE DCB PTR. IN LIST.     O122
         MVI   4(GR1),X'88'            SET LAST ENTRY INDICATOR.   O122
         SVC   POST                    CONSTRUCT DEB.              O122
         LTR   GR15,GR15               WAS DEB SUCCESSFULLY BUILT. O122
         BNZ   ONLINERR                NO-SET UP ERROR MESSAGE.    O122
         ST    GR10,DCBIOBAD           FUNCTION BLK PTR TO DCB.    O122
         B     READ                    GO TO READ IN OLD LABEL.    O122
         AIF   ('&LIB' EQ 'LIB2').X234301                       XL03912
         DROP  9
         USING DCELBBNR,9
PASSBY   MVC   RDSEEK+1(2),DCELBBNR    PICK UP BIN NO. FOR 2321
         B     CHKMORE                 CHECK IF OFFLINE REQUEST.   O122
.X234301 ANOP                                                   XL03912
SKIP     EQU   *                                                YL02912
***********************************************************************
*     THIS SECTION OF CODE PERFORMS RACF SECURITY CHECKING.IF THE NEW *
*  VOLID IS RACF DEFINED, THE USER MUST HAVE ALTER ACCESS TO IT. IF   *
*  THE OLD VOLID IS RACF DEFINED THE USER MUST HAVE ALTER ACCESS TO   *
*  IT. NO SECURITY CHECKING WILL TAKE PLACE IT RACF IS NOT ACTIVE IN  *
*  THE SYSTEM.                                                 @G32DSPD
***********************************************************************
         L     GR5,CVTPTR              GET CVTPTR              @G32DSPD
         USING CVT,GR5                                         @G32DSPD
         L     GR5,CVTRAC              GET RACF POINTER        @G32DSPD
         DROP  GR5                                             @G32DSPD
         LTR   GR5,GR5                 IS RACF ACTIVE ?        @G32DSPD
         BZ    NULLRACF                NO,SKIP RACF CHECKS     @G32DSPD
         USING RCVT,GR5                DO CHECK IF RACF IS     @ZA25515
         TM    RCVTSTA1,RCVTDASD       CURRENT.                @ZA25515
         BZ    NULLRACF                NO DONT RACHECK         @ZA25515
         DROP  GR5                     YES DO RACHECK          @ZA25515
*  TEST THE OLD VOLID FOR RACF SECURITY                        @G32DSPD
         MVC   RACVOL,UCBVOLI                                  @G32DSPD
         RACHECK ATTR=ALTER,ENTITY=RACVOL,CLASS='DASDVOL'      @G32DSPD
         CLM   GR15,B'0001',RC8        USER UNAUTHORIZED ?     @G32DSPD
         BE    UNAUTH                  YES,GO TERMINATE        @G32DSPD
*  TEST THE NEW VOLID FOR RACF SECURITY                        @G32DSPD
         MVC   RACVOL(6),SERNO                                 @G32DSPD
         RACHECK ATTR=ALTER,ENTITY=RACVOL,CLASS='DASDVOL'      @G32DSPD
         CLM   GR15,B'0001',RC8        USER UNAUTHORIZED ?     @G32DSPD
         BE    UNAUTH                  YES,GO TERMINATE        @G32DSPD
NULLRACF EQU   *                                               @G32DSPD
*   END RACF AUTHORIZATION CHECKS.                             @G32DSPD
         SPACE 2                                               @G32DSPD
         MVC   REQUEST+VOL(L6),SRTEVOLI SET VOLID TO MSG.       YL02912
         DROP  GR9                                              YL02912
         L     GR9,CVTPTR               GET ADDRESS OF CVT.     YL02912
         USING CVT,GR9                                          YL02912
         L     GR9,CVTTCBP              GET PTR TO TCB ADDRESS. YL02912
         L     GR9,D4(GR9)              GET TCB ADDRESS.        YL02912
         L     GR9,D12(GR9)             GET TIOT ADDRESS.       YL02912
         MVC   REQUEST+JJJ(L8),D0(GR9)  SET JOB NAME TO MSG.    YL02912
         MVC   REQUEST+SSS(L8),D8(GR9)  SET STEP NAME TO MSG.   YL02912
REQUEST  WTOR  'IEH853D CONFIRM REQUEST TO LABEL CURRENT VOLID=      . *
               JJJJJJJJ,SSSSSSSS.',REPLY,1,WTORECB,ROUTCDE=(4),DESC=(2)
         TM    WTORECB,COMPLETE         IS OPERATION COMPLETE?  YL02912
         BO    TESTANSW                 YES,GO CHECK ANSWER.    YL02912
         WAIT  ECB=WTORECB              NO, AWAIT COMPLETION.   YL02912
TESTANSW MVI   WTORECB,ZERO             CLEAR ECB POST BITS.    YL02912
         CLI   REPLY,C'U'               REPLY EQUAL 'U'?        YL02912
         BE    USE                      YES, CONTINUE LABEL.    YL02912
         CLI   REPLY,C'T'               REPLY EQUAL 'T'?        YL02912
         BE    TERM                     YES, TERMINATE.         YL02912
         MVC   ERRANS+ERJJ(L8),D0(GR9)  SET JOB NAME TO MSG.    YL02912
         MVC   ERRANS+ERSS(L8),D8(GR9)  SET STEP NAME TO MSG.   YL02912
ERRANS   WTO   'IEH808I REPLY IN ERROR. REPLY WITH  U  OR  T , JJJJJJJJ*
               ,SSSSSSSS.',ROUTCDE=(4),DESC=(5)                 YL02912
         B     REQUEST                  GO WRITE REQUEST AGAIN. YL02912
         DROP  GR9                                              YL02912
         EJECT
***********************************************************************
*                                                                     *
*  THIS ROUTINE OPENS THE DATA CONTROL BLOCKS FOR ALL DIRECT ACCESS   *
*     DEVICES INVOLVED. THE OPEN IS ALWAYS PERFORMED ON THE FORMAT    *
*     FOUR DSCB OF THE VTOC.                                          *
*                                                                     *
***********************************************************************
USE      EQU   *
         MVC   DCBDDNAM(8),DDNAME2     PUT DDNAME IN DCB
         MVC   OPENLD+1(3),RDDCBAD+1   MOVE DCB ADDRESS TO LIST
OPENJ    LA    GR9,RDAREA    BUFFER ADDRESS TO READ JFCB.
         ST    GR9,LIST
         MVI   LIST,X'87'              EXIT LIST CODE
         RDJFCB MF=(E,OPENLD)
         USING JFCB,9
         MVI   JFCBDSNM,X'04'          SET DSNAME FIELD FOR
         MVC   JFCBDSNM+1(43),JFCBDSNM   FORMAT 4 DSCB.
         MVI   JFCBTSDM,X'08'          STOP WRITE OF JFCB AT OPEN
         OPEN  MF=(E,OPENLD),TYPE=J
         TM    DCBOFLGS,X'10'          WAS THERE A SUCCESSFUL OPEN.
         BZ    NOOPEN                  NO--GIVE AN ERROR MESSAGE
         NI    DCBMACRF+1,X'F3'        SET BITS OFF TO PREVENT CLOSE
*                                        FROM WRITING AN EOF RECORD
*                                        AND UPDATING THE DSCB.
         ST    GR10,DCBIOBAD           FUNCTION BLOCK ADDRESS IN DCB
         EJECT                                                     O122
***********************************************************************
*                                                                     *
*  THIS ROUTINE READS THE VOLUME LABEL, ENTERS THE NEW VOLUME SERIAL  *
*     NUMBER, AND OWNER ID IF ANY, AND WRITES OUT THE NEW LABEL ONTO  *
*     THE VOLUME.
*                                                                     *
***********************************************************************
         DROP  9
READ     EQU   *                                                   O122
         LA    GR5,RDAREA              SET UP READ/WRITE AREA
         ST    GR5,RDWRTCCW                IN CHANNEL PROGRAM
         MVI   RDWRTCCW,6              SRT OP CODE FOR READ
         MVI   RDWRTCCW+4,X'20'
         EXCP  RDIOB                   READ VOLUME LABLE
         BAL   GR4,WAITD               AWAIT COMPLETION OF I/O
         MVI   POSTSW,X'00'            INITIALIZE SWITCH
         CLI   DDNAME2,X'40'           OFFLINE LABEL REQUEST.      O122
         BE    NOSET                   YES-CONTINUE PROCESSING.    O122
         CLC   RDAREA+4(6),SERNO       IS NEW LABEL SAME AS OLD
         BNE   NOSET                   NO-UCB CAN BE POSTED
         MVI   POSTSW,X'01'            YES-SET SWITCH TO PREVENT POST
NOSET    MVC   RDAREA+4(6),SERNO       MOVE IN NEW SERIAL NUMBER
         TM    SEQSW,OWNERID           IS OWNER ID SPECIFIED
         BZ    WRITEOUT                NO - GO WRITE VOLUME LABEL
         MVC   RDAREA+41(10),OWNID     MOVE IN NEW OWNERID
WRITEOUT MVI   RDWRTCCW,5              SET OP CODE FOR WRITE
         MVI   RDWRTCCW+4,X'60'
         EXCP  RDIOB                   WRITE VOLUME LABEL
         BAL   GR4,WAITD               AWAIT COMPLETION OF I/O
         CLI   POSTSW,X'01'            SHOULD NEW SERIAL NO GO IN UCB
         BE    FINISH                  NO-GO RETURN CONTROL
         CLI   DDNAME2,X'40'           OFFLINE LABEL REQUEST.      O122
         BNE   POSTA                   NO-CONTINUE.                O122
         LA    GR1,RDAREA              AREA FOR LIST.              O122
         LA    GR2,SERNO               PTR. TO NEW SERIAL NUMBER.  O122
         MVC   0(4,GR1),TUCBPTR        UCB PTR. TO LIST.           O122
         MVI   0(GR1),X'88'            SET SPECIAL POST INDICATOR. O122
         ST    GR2,4(GR1)              PTR. TO NEW SERIAL NO.      O122
         SR    GR2,GR2                 CLEAR REGISTER.             O122
         ST    GR2,8(GR1)              ZERO VTOR PTR. IN LIST.     O122
         MVC   12(4,GR1),DCBDEBAD      DEB PTR. TO LIST.           O122
         MVI   12(GR1),X'80'           SET LAST ENTRY INDICATOR.   O122
         B     POSTB                   GO POST THE UCB.            O122
POSTA    EQU   *                                                   O122
         MVC   PARMLIST+1(3),TUCBPTR+1 PUT UCB POINTER IN LIST SVC
         LA    GR1,SERNO               PUT ADDRESS OF NEW SERIAL
         ST    GR1,PARMLIST+4              NUMBER IN LIST FOR SVC
         LA    GR1,PARMLIST            PUT ADDRESS OF LIST IN REGISTER
POSTB    EQU   *                                                   O122
         SVC   POST
FINISH   LA    GR1,6                   SET UP SUCCESSFUL COMPLETION
*                                         MESSAGE
         BAL   GR9,MSGBLD
         MVC   0(8,GR1),DDNAME2        PUT DDNAME IN MESSAGE
         MVC   40(6,GR1),SERNO         PUT SERIAL NO IN MESSAGE
         MVC   MESS+8(8),NAME          PUT MODULE NAME IN MESSAGE
         LA    GR9,GOODRTN
         B     MSGPRNT                 GO PRINT MESSAGE
**********************************************************************
*                        WAIT           ROUTINE                       *
**********************************************************************
WAITD    TM    DISKECB,COMPLETE        IS THE OPERATION COMPLETE.
         BO    TESTD                   YES-GO TEST THE STATUS.
         WAIT  ECB=DISKECB             AWAIT COMPLETION OF DISK.
TESTD    CLI   DISKECB,GOOD            ALL OK.
         BNE   IOERR                   NO--I/O ERROR.
         MVI   DISKECB,0               CLEAR POSTED BITS IN THE ECB.
         BR    GR4                     RETURN.
***********************************************************************
*                                                                     *
*  THIS SECTION WRITES OUT MESSAGES, SETS RETURN CODES, AND RETURNS   *
*     CONTROL.                                                        *
*                                                                     *
***********************************************************************
TERM     EQU   *                                                YL02912
         LA    GR1,MESS57               INDICATE LABEL DENIED.  YL02912
         BAL   GR9,MSGBLD               GO BUILD MESSAGE.       YL02912
         MVC   D0(L8,GR1),DDNAME2       PUT DDNAME IN MSG.      YL02912
         B     ERRPRNT                  PRT MSG AND TERMINATE.  YL02912
NOOPEN   LA    GR1,5                   BAD OPEN
         B     ERRSET
BADPARM  LA    GR1,10                  INCORRECT DEVICE
ERRSET   BAL   GR9,MSGBLD              GO BUILD MESSAGE
         MVC   0(8,GR1),DDNAME2        PUT DDNAME IN MESSAGE
         B     ERRPRNT                 GO PRINT MESSAGE
BADDEV   LA    GR1,14                  IS SYSTEM RESIDENCE VOLUME
         BAL   GR9,MSGBLD              GO BUILD MESSAGE
         MVC   0(8,GR1),DDNAME2        PUT DDNAME IN MESSAGE
         MVC   MESS+8(8),NAME          PUT MODULE NAME IN MESSAGE
         B     ERRPRNT                 GO PRINT MESSAGE
ONLINERR EQU   *                                                   O122
         LA    GR1,24                  MESSAGE NUMBER.             O122
         BAL   GR9,MSGBLD              GO BUILD MESSAGE.           O122
         MVC   0(8,GR1),DDNAME2        DDNAME TO MESSAGE.          O122
         MVC   MESS+9(6),NAME          MOVE LABEL KEYWORD          O122
         MVC   MESS+15(1),MESS+14            INTO THE MESSAGE.     O122
         B     ERRPRNT                 PRINT MESSAGE AND TERMINATE.O122
* RACF AUTHORIZATION NOT HELD BY USER PRINT MSG AND TERMINATE  @G32DSPD
UNAUTH   LA    GR1,MESS66             SET FOR MSG IEH866I      @G32DSPD
         BAL   GR9,MSGBLD             GET MESSAGE TEXT         @G32DSPD
         MVC   0(L'LABELNM,GR1),LABELNM INDICATE LABEL FUNCT.  @G32DSPD
         MVC   20(L'DDNAME2,GR1),DDNAME2 INSERT DDNAME         @G32DSPD
         B     ERRPRNT                GO TERMINATE             @G32DSPD
         EJECT                                                     O122
IOERR    LA    GR1,13                  I/O ERROR
         BAL   GR9,MSGBLD              GO BUILD MESSAGE
         SYNADAF ACSMETH=EXCP,PARM1=RDIOB
         MVC   MESS+22(78),49(GR1)     PUT I/O INFO IN BUFFER    SM4350
         SYNADRLS
ERRPRNT  BAL   GR9,MSGPRNT             GO PRINT ERROR MESSAGE
SETCODE  LA    GR2,8                   SET UP RETURN CODE/ERROR  XM1377
ENDUP    EQU   *                                                   O122
         CLI   DDNAME2,X'40'           OFFLINE LABEL REQUEST.      O122
         AIF   ('&LIB' EQ 'LIB1').X227107
         BNE   CKDCB                    NO, TEST DCB.            YM3475
         CLC   DCBDEBAD(4),BLANKS       WAS DEB REMOVED?         YM3475
         BE    AROUND                   YES, GO RETURN.          YM3475
         LA    GR1,RDAREA               AREA FOR LIST.           YM3475
         MVI   0(GR1),DELDEB            DELETE DEB REQUEST.      YM3475
         MVC   12(4,GR1),DCBDEBAD       DEB ADDR INTO LIST       YM3475
         MVI   12(GR1),PARMEND          SET LAST ENTRY IND.      YM3475
         SVC   POST                     GO DELETE DEB.           YM3475
         B     AROUND                   SKIP CLOSE               YM3475
CKDCB    EQU   *                                                 YM3475
         AGO   .X227108                                          YM3475
.X227107 ANOP                                                    YM3475
         BE    AROUND                  YES-NO FURTHER CHECKING.    O122
.X227108 ANOP                                                    YM3475
         TM    DCBOFLGS,X'10'          NO-WAS DATA SET OPENED.     O122
         BZ    AROUND                  NO- SKIP  CLOSE
         CLOSE ((8))                   CLOSE DATA SET
AROUND   EQU   *                                                 YM5579
         LR    GR15,GR2                PUT RETURN CODE BACK IN REG 15
         L     GR13,LABLSAVE+4         RESTORE REGISTER 13       YM5579
         RETURN (14,12),T,RC=(15)      RETURN WITH PROPER CODE
         EJECT                                                     O122
MSGBLD   LINK  EP=IEHDMSGB             GO CONSTRUCT MESSAGE
         BR    GR9
MSGPRNT  LINK  EP=IEHDPRNT             GO PRINT MESSAGE
         BR    GR9
GOODRTN  LA    GR2,0                   SUCCESS COMPLETION CODE   YM5579
         L     GR3,PTRCFUNC            PICK UP POINTER TO CURRENT FUNC.
         MVI   0(GR3),COMPLETE         SET COMPLETE BIT FOR FUNCTION.
         B     ENDUP
LIST     DS    0F            FOR READ IN JFCB
         DC    X'87'
         DC    AL3(0)
OPENLD   RDJFCB (,(OUTPUT)),MF=L       PARAMETER LIST FOR OPEN
PARMLIST DC    X'08'                   PARAMETER LIST FOR SVC
         DC    AL3(0)                     TO POST UCB
         DC    F'0'
         DC    X'80'
         DC    AL3(0)
POSTSW   DC    X'00'                   SWITCH TO INDICATE POST UCB
POST     EQU   82
RACVOL   DS    CL6                      VOLSER ENTITY          @G32DSPD
RC8      DC    AL1(8)                   RETURN CODE 8          @G32DSPD
         AIF   ('&LIB' EQ 'LIB1').X227106                        YM3475
DELDEB   EQU   X'F8'                    DEB DELETE REQUEST.      YM3475
PARMEND  EQU   X'80'                    END OF PARM LIST IND.    YM3475
BLANKS   DC    X'40404040'              OFFLINE DDNAME END.      YM3475
LABELNM  DC    C'LABEL'                 FUNCTION FOR IEH866I   @G32DSPD
.X227106 ANOP                                                    YM3475
D0       EQU   0                        DISPLACEMENT VALUE 0    YL02912
D4       EQU   4                        DISPLACEMENT VALUE 4    YL02912
D8       EQU   8                        DISPLACEMENT VALUE 8    YL02912
D12      EQU   12                       DISPLACEMENT VALUE 12   YL02912
MESS66   EQU   66                       IEH866I OFFSET         @G32DSPD
L6       EQU   6                        LENGTH VALUE OF 6.      YL02912
L8       EQU   8                        LENGTH VALUE OF 8.      YL02912
ZERO     EQU   0                        IMMEDIATE CONSTANT.     YL02912
MESS57   EQU   57                       MESSAGE IEH857I.        YL02912
WTORECB  DC    F'0'                     ECB FOR WTOR INST.      YL02912
LABLSAVE DS    18F
NAME     DC    C'LABEL OF'             MODULE NAME CONSTANT
GOOD     EQU   X'7F'                   NO I/O ERRORS
VOL      EQU   63                       DISPLACEMENT FOR VOLID. YL02912
JJJ      EQU   71                       DISPLACEMENT FOR JOB.   YL02912
SSS      EQU   80                       DISPLACEMENT FOR STEP.  YL02912
ERJJ     EQU   56                       DISPLACEMENT FOR JOB.   YL02912
ERSS     EQU   65                       DISPLACEMENT FOR STEP.  YL02912
         EJECT                                                     O122
CCWLIST  CCW   X'31',RDSEEK+3,X'60',5     SEARCH FOR RECORD 3
         CCW   8,CCWLIST,0,0              REPEAT UNTIL FOUND
RDWRTCCW CCW   0,0,X'20',80               READ/WRITE DATA
CCWLIST1 CCW   X'31',RDSEEK+3,X'60',5  SEARCH FOR RECORD3
         CCW   X'08',CCWLIST1,0,0      REPEAT UNTIL FOUND
RDBKCHK  CCW   X'06',0,X'30',80        READ CHECK
*      LABL DA WRITE-OUT ECB.
         DS    0D                      NECESSARY ALIGNMENT.
DISKECB  DC    F'0'                    WAIT//COMPLETE BITS
*      LABL DA WRITE-OUT IOB.
RDIOB    DS    0F
RDFLAGS  DC    XL2'2000'               FLAG1 AND FLAG2
RDSENSE  DC    H'0'                    SENSE BYTES.
RDECBAD  DC    A(DISKECB)              ECB ADDRESS
RDSTATUS DC    2F'0'                   CHANNEL STATUS.
RDCCWAD  DC    A(CCWLIST)              CCW ADDRESS.
RDDCBAD  DC    A(RDDCB)                DCB ADDRESS.
RDRESAD  DC    F'0'                    RESTART ADDRESS.
RDBLKCT  DC    H'1'                    BLOCK COUNT INCREMENT.
RDERROR  DC    H'0'                    ERROR COUNT.
RDSEEK   DC    1F'0'                   MBCCHHR.
         DC    X'00000003'
         EJECT                                                     O122
*      LABL DA WRITE-OUT DCB.
         AIF   ('&LIB' EQ 'LIB1').OSDCB
RDDCB    DCB   DSORG=PS,MACRF=(E),DEVD=DA,EXLST=LIST,EOEA=P8,    M5431 F
               SIOA=P7,PGFX=YES                                   M5431
         AGO   .C1
.OSDCB   ANOP
RDDCB    DCB   DSORG=PS,MACRF=(E),DEVD=DA,EXLST=LIST,EOEA=P8
.C1      ANOP
REPLY    DS    CL1                      AREA FOR WTOR REPLY.    YL02912
         EJECT
         IEHDWORK
         EJECT
         IEHDBLKS
VLABEL   DSECT
VOLUME   DS    CL3                     LABEL IDENTIFIER.
VOLNO    DS    CL1                     LABEL NUMBER.
SERIAL   DS    CL6                     LABEL SERIAL NUMBER.
SECURITY DS    CL1                     VOLUME SECURITY.
VTOCPTR  DS    CL10                    POINTER TO VTOC
         DS    CL20                    RESERVED.
OWNER    DS    CL10                    OWNER IDENTIFICATION.
         DS    CL29                    BLANK.
         EJECT
         ICHPRCVT                                              @ZA25515
         EJECT
CVT      DSECT
         CVT
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
JFCB     DSECT
         IEFJFCBN
         EJECT
         DCBD  DSORG=PS
         END
