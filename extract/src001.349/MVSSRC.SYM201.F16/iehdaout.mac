         COPY  LCGASMSW                                          SM4351
IEHDAOUT CSECT
*1240484000                                                        3605
*056000-058000,141200,194000-200000,240000,550000,560000           O122
*482000,483000                                                  A38484
*3434                                                            M4970
*STATUS CHANGE LEVEL 004
*                                                                     *
*FUNCTION/OPERATION- THIS ROUTINE HANDLES THE DUMP TO A SYSTEM        *
*   OUTPUT DEVICE FOR THE IEHDDUMP PORTION OF THE IEHDASDR SYSTEM     *
*   UTILITY PROGRAM. HEADER INFORMATION (TRACK ADDRESS, THE DATA      *
*   FIELD FOR RECORD ZERO, THE COUNT FIELD) WILL BE FORMATED AND      *
*   WRITTEN OUT. THE SIZE OF THE INDIVIDUAL RECORD IS COMPUTED        *
*   AND THEN DIVIDED BY THIRTY-TWO TO OBTAIN THE NUMBER               *
*   OF COMPLETE (32-BYTE) LINES, AND THE NUMBER OF BYTES IN THE       *
*   INCOMPLETE LINE IF ANY. AN EBCDIC AND GRAPHIC TRANSLATION IS      *
*   PERFORMED TO ALLOW THE USER TO EASILY LOCATE VARIOUS FIELDS       *
*   IN THE OUTPUT DATASET. ONLY THE FIRST AND LAST LINES OF           *
*   IDENTICAL RECORD DATA WILL BE PLACED IN THE OUTPUT DATASET,       *
*   SEPARATED BY THIS MESSAGE-                                        *
*              ' LINE(S) SAME AS ABOVE '                              *
*                                                                     *
*ENTRY POINTS- THE ONLY ENTRY POINT IS -IEHDAOUT-, AND CONTROL IS     *
*   ALWAYS RECEIVED FROM THE -IEHDEXCP- ROUTINE.                      *
*                                                                     *
*INPUT- REGISTER 1 WILL POINT TO THE CURRENT TRACK ADDRESS(CCHH).     *
*       REGISTER 2 WILL CONTAIN THE ENTRY POINT FOR -IEHDPRNT-.       *
*       REGISTER 3 WILL CONTAIN A POINTER TO THE END OF THE DATA      *
*       IN THE DATA BUFFER.                                           *
*       REGISTER 10 WILL CONTAIN A POINTER TO THE CONTROL BLOCK.      *
*       REGISTER 12 WILL CONTAIN A POINTER TO THE WORK AREA.          *
*       AOUTSW (IN IEHDBLKS) WILL DESCRIBE THE REQUIRED FORMATTING O122
*              X'00' - FORMAT TRACK ADDRESS, R0 DATA AND RECORDS   O122
*              X'01' - FORMAT RECORDS ONLY                         O122
*              X'02' - DO NOT FORMAT                               O122
*       TRACKPT (IN IEHDBLKS) WILL CONTAIN A POINTER TO THE        O122
*              BEGINNING OF THE DATA                               O122
*                                                                     *
*EXITS-NORMAL- UPON COMPLETION, CONTROL IS RETURNED TO IEHDEXCP OR O122
*   IEHDRCVR.  NO RETURN CODE IS PASSED.                           O122
*                                                                     *
*EXITS-ERROR- NONE.                                                   *
*                                                                     *
*EXTERNAL ROUTINES- THE CONVERTED TRACK DATA WILL BE PLACED IN        *
*   THE OUTPUT BUFFER AND THEN WRITTEN OUT BY THE MESSAGE             *
*   WRITER ROUTINE(IEHDPRNT).                                         *
*                                                                     *
*TABLES/WORK AREAS- UPON ENTRY, REGISTERS 10 AND 12 POINT TO A        *
*   FUNCTION BLOCK AND WORK AREA RESPECTIVELY. THEY ARE DESCRIBED     *
*   IN THE DSECTS CALLED -FUNCBLK- AND -WORK-.                        *
*                                                                     *
*ATTRIBUTES- SERIALLY REUSABLE, RELOCATABLE, NON-PRIVILEGED.          *
 TITLE 'IEHDAOUT-DUMP TO SYSTEM OUTPUT DEVICE FOR IEHDASDR UTILITY'
*  THE FOLLOWING ARE REGISTER ASSIGNMENTS.
GR0      EQU   0
GR1      EQU   1
DATAPTR  EQU   2                       POINTER TO DATA IN DATA BUFFER.
SUMTOTAL EQU   3                       TOTAL LENGTH OF DATA.
GR4      EQU   4
GR5      EQU   5
GR6      EQU   6
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10
GR11     EQU   11
GR12     EQU   12
GR13     EQU   13
GR14     EQU   14
GR15     EQU   15
*
         SPACE
         USING FUNCBLK,10
         USING IEHDAOUT,11
         USING WORK,12
         SPACE
         SAVE  (14,12),T               SAVE THE REGISTERS.
         LR    GR11,GR15               LOAD THE BASE REGISTER.
         LA    GR7,OUTSAVE             SAVE AREA ADDRESS.
         ST    GR7,8(GR13)             ADDRESS OF NEW AREA TO OLD.
         ST    GR13,4(GR7)             ADDRESS OF OLD AREA TO NEW.
         LR    GR13,GR7                SAVE AREA FOR CALLED ROUTINES.
         LR    GR15,DATAPTR            ENTRY PT. FOR PRNT TO LINK REG.
         L     DATAPTR,TRACKPT         POINTER TO DATA             O122
         LA    DATAPTR,0(DATAPTR)      CLEAR HIGH ORDER BYTE       O122
         LA    SUMTOTAL,0(SUMTOTAL)    CLEAR HIGH ORDER BYTE       O122
         MVI   1(SUMTOTAL),X'FF'       FLAG END OF BUFFER        A53156
         CLI   AOUTSW,FMTCNT           FORMAT COUNTS ONLY          O122
         BE    COUNTS                  YES                         O122
         BL    TRACKIN                 NO, FORMAT ENTIRE TRACK     O122
*        DO NOT FORMAT DATA  (USED WHEN IEHDRCVR PRINTS THE        O122
*        REMAINDER OF TRACK AS ONE RECORD)                         O122
         LR    GR7,SUMTOTAL            END OF DATA IN BUFFER       O122
         SR    GR7,DATAPTR             LENGTH OF DATA TO BE PRINTEDO122
         B     PRTRCORD                PRINT THE RECORD            O122
         EJECT
***********************************************************************
*                                                                     *
*        MOVE IN 'TRACK',R0 DATA', AND 'COUNT' KEYWORDS               *
*        WITH ASSOCIATED TRACK INFORMATION. COMPUTE THE NUMBER        *
*        OF COMPLETE LINES IF ANY, AND THE LENGTH OF THE              *
*        POSSIBLE INCOMPLETE LINE.                                    *
*                                                                     *
***********************************************************************
         SPACE
TRACKIN  BAL   GR9,OUTPUT              SPACE A LINE.
         MVC   MESS(10),TRACKKEY       MOVE IN 'TRACK' KEYWORD.
         UNPK  MESS+12(9),0(5,GR1)     TRACK ADDR(CCHH) TO OUTPUT BUFF.
         MVI   MESS+20,C' '            CLEAR TRASH FROM UNPACK.
         TR    MESS+12(8),IOTAB-240    GRAPHIC TRACK ADDRESS.
         MVC   MESS+26(7),R0DATA       MOVE IN 'R0 DATA' KEYWORD.
         UNPK  MESS+35(9),0(5,DATAPTR)   UNPACK ACTUAL R0 DATA
         UNPK  MESS+43(9),4(5,DATAPTR)         TO OUTPUT BUFFER.
         MVI   MESS+51,C' '            CLEAR TRASH FROM UNPACK.
         TR    MESS+35(16),IOTAB-240
         LA    DATAPTR,8(DATAPTR)      POINT TO R1 COUNT FIELD.
         BAL   GR9,OUTPUT              PRINT A LINE.
         BAL   GR9,OUTPUT              SPACE A LINE.
COUNTS   CR    DATAPTR,SUMTOTAL        COMPLETED PRINTING.         O122
         BNL   RETURN                  YES--RETURN.                O122
COUNTIN  MVC   MESS+5(5),COUNTKEY      MOVE IN 'COUNT'KEYWORD.     O122
         UNPK  MESS+12(9),0(5,DATAPTR)   UNPACK COUNT FOR
         UNPK  MESS+20(9),4(5,DATAPTR)     PRINT OUT.
         MVI   MESS+28,C' '            CLEAR TRASH FROM UNPACK.
         TR    MESS+12(16),IOTAB-240   GRAPHIC COUNT.
         BAL   GR9,OUTPUT              PRINT A LINE.
         BAL   GR9,OUTPUT              SPACE A LINE.
         EJECT
         SR    GR7,GR7                 CLEAR WORK REGISTER.
         IC    GR7,5(DATAPTR)          KEY LENGTH.
         AH    GR7,6(DATAPTR)          PLUS DATA LENGTH.
         LA    DATAPTR,8(DATAPTR)      POINT TO R1 DATA FIELD.
PRTRCORD MVC   LOCCTR(4),ZERO          CLEAR LOCATION COUNTER.     O122
         SR    GR6,GR6                 CLEAR WORK REGISTER.
         D     GR6,THIRTY2             HOW MANY COMPLETE LINES.
         STH   GR6,INCOMPLT            SAVE LENGTH OF INCOMPL LINE.
         LTR   GR7,GR7                 ANY COMPLETE LINES.
         BP    FORMAT04                YES--GO TO PROCESS.
COUNTIN1 LH    GR7,INCOMPLT            LENGTH OF POSSIBLE INCOMPL LINE.
         LTR   GR7,GR7                 SIZE OF INCOMPLETE LINE=0.
         BP    FORMAT08                NO--GO TO PROCESS.
COUNTIN2 CR    DATAPTR,SUMTOTAL        END OF DATA THIS TRACK.
         BNL   RETURN                  YES--RETURN.                O122
         BAL   GR9,OUTPUT              SPACE A LINE.
         ST    DATAPTR,SAVESLOT        STORE TO SEE IF DOUBLE WORD.
         TM    SAVESLOT+3,X'07'        IS PTR ON DOUBLE WORD BOUNDARY
         BC    8,COUNTIN               YES--GO TO PROCESS NXT RECORD.
         NI    SAVESLOT+3,X'F8'        NO--FORCE TO DOUBLE WORD.
         L     DATAPTR,SAVESLOT        POINTER BACK INTO REGISTER.
         LA    DATAPTR,8(DATAPTR)      ADD 8 AFTER FORCING DOUBLE.
         B     COUNTIN                 GO PROCESS NEXT RECORD.
         EJECT
***********************************************************************
*                                                                     *
*   PROCESS COMPLETE LINES HERE.                                      *
*                                                                     *
*        GR7 HAS NUMBER OF COMPLETE LINES FOR LOOP CONTROL.           *
*                                                                     *
***********************************************************************
         SPACE
FORMAT04 L     GR6,FORM5FPT            COMPARE BUFFER ADDRESS.
         L     GR5,THIRTY2             LENGTH FOR EXEC.
         BCTR  GR5,0                   DECREMENT FOR EXEC.
         EX    GR5,MOVE                DATA TO COMPARE BUFFER.
         MVI   SAMESW,0                SET SWITCH OFF.
         USING PRNTAREA,4
FORMAT05 LA    GR4,MESS                ADDRESS OF OUTPUT BUFFER.
         L     GR5,THIRTY2             LENGTH FOR EXEC.
         BAL   GR9,GRAPHIC             CREATE ENGLISH TRANSLATION.
FORMAT06 CLI   0(GR8),X'FF'            IS THIS A DELIMITER.
         BC    8,FORMAT07              YES--LINE IS COMPLETE.
         SR    GR0,GR0                 CLEAR WORK REGISTER.
         IC    GR0,0(GR8)              GR0=DISPL FROM BEG. OF LINE.
         SR    GR5,GR5                 CLEAR WORK REGISTER.
         IC    GR5,1(GR8)              GR5=SIZE OF FIELD-1.
         AR    GR4,GR0                 ADDRESS OF FIELD W/IN A LINE.
         EX    GR5,MOVE1               MOVE FIELD TO WORK AREA.
         UNPK  DAWORK(9),DAWORK(5)
         TR    DAWORK(8),IOTAB-240
         LA    GR5,1(GR5,GR5)          GR5=SIZE OF UNPACKED FIELD.
         EX    GR5,MOVE2               MOVE UNPACKED FIELD TO BUFFER.
         LA    GR8,2(GR8)              BUMP LAYOUT FIELD PTR.
         LA    DATAPTR,4(DATAPTR)      BUMP DATA FIELD POINTER.
         B     FORMAT06                CONTINUE PROCESSING THIS LINE.
         SPACE
         EJECT
***********************************************************************
*                                                                     *
*        CHECK FOR IDENTICAL LINES HERE.                              *
*                                                                     *
***********************************************************************
         SPACE
FORMAT07 BAL   GR9,OUTPUT              PRINT A LINE.
COMPARE  L     GR5,LOCCTR              LOCATION COUNTER.
         A     GR5,THIRTY2             INCREMENT AND
         ST    GR5,LOCCTR              STORE FOR FUTURE USE.
         CLI   SAMESW,X'F0'            LAST ENTRY.
         BE    COUNTIN1                YES--GO TO SEE IF END OF DATA.
         L     GR6,FORM5FPT            COMPARE BUFFER ADDRESS.
         CLC   0(32,GR6),0(DATAPTR)    TWO LINES ALIKE.
         BE    SAMELINE                YES--GO TO HANDLE.
         BCT   GR7,FORMAT04            PROCESS NEXT LINE IF ANY MORE.
         CLI   SAMESW,X'FF'            LAST LINE PRINTED A MSG.
         BNE   COUNTIN1                NO--GO SEE IF DONE WITH TRACK.
COMPARE1 MVI   SAMESW,X'F0'            SET LAST ENTRY SWITCH.
         S     GR5,THIRTY2             DECREMENT AND
         ST    GR5,LOCCTR              STORE LOCATION COUNTER.
COMPARE2 S     DATAPTR,THIRTY2         DECREMENT DATA PTR.
         B     FORMAT05                GO TO PROCESS THIS LINE.
SAMELINE BCTR  GR7,0                   DECREMENT LINE COUNTER.
         LTR   GR7,GR7                 SEE IF LAST LINE.
         BZ    COMPARE1                YES--GO TO PROCESS AND PRINT IT.
         A     DATAPTR,THIRTY2         NO---BUMP DATA PTR.
         CLI   SAMESW,X'FF'            SAME MSG ALREADY WRITTEN.
         BE    COMPARE                 YES--NO MESSAGE THIS TIME.
         CLC   0(32,GR6),0(DATAPTR)    3 LINES IN A ROW ALIKE.
         BNE   COMPARE2                NO--GO PRINT THE LINE.
         MVI   SAMESW,X'FF'            SET SWITCH TO INDIC. MSG OUT.
         MVC   MESS+10(21),SAME        SET UP MESSAGE.
         B     FORMAT07                GO TO WRITE THE MESSAGE.
         EJECT
***********************************************************************
*                                                                     *
*        HANDLE INCOMPLETE LINE HERE.                                 *
*                                                                     *
***********************************************************************
         SPACE
         USING PRNTAREA,4
FORMAT08 LA    GR4,MESS                ADDRESS OF OUTPUT BUFFER.
         LR    GR5,GR7                 LENGTH FOR EXEC.
         BAL   GR9,GRAPHIC             GO TO TRANSLATE TO GRAPHIC.
FORMAT09 SR    GR0,GR0                 CLEAR WORK REGISTER.
         IC    GR0,0(GR8)              GR0=DISPL. FROM BEG. OF LINE.
         SR    GR5,GR5                 CLEAR WORK REGISTER.
         IC    GR5,1(GR8)              GR5=SIZE OF FIELD-1.
         AR    GR4,GR0                 ADDRESS OF FIELD WITHIN A LINE.
         LA    GR5,1(GR5)              TOTAL SIZE OF FIELD
         CR    GR7,GR5                 INCOMPLETE LINE LESS THAN 4.
         BH    FORMAT10                NO--CONTINUE.
         LR    GR5,GR7                 LENGTH OF REMAINING DATA.
         SR    GR7,GR7                 CLEAR REGISTER.
         B     FORMAT11                GO TO PROCESS.
FORMAT10 SR    GR7,GR5                 DECREMENT LENGTH.
FORMAT11 BCTR  GR5,0                   DECREMENT FOR EXEC.
         EX    GR5,MOVE1               MOVE FIELD TO WORK AREA.
         AR    DATAPTR,GR5             INCREMENT
         LA    DATAPTR,1(DATAPTR)        DATA POINTER.
         UNPK  DAWORK(9),DAWORK(5)     PREP. FOR CHANGE TO GRAPHIC.
         TR    DAWORK(8),IOTAB-240
         LA    GR5,1(GR5,GR5)          GR5=SIZE OF UNPACKED FIELD.
         EX    GR5,MOVE2               MOVE UNPACKED FIELD TO BUFFER.
         LA    GR8,2(GR8)              INCREMENT LAYOUT FIELD PTR.
         LTR   GR7,GR7                 END OF INCOMPLETE LINE.
         BP    FORMAT09                NO--CONTINUE PROCESSING.
         BAL   GR9,OUTPUT              PRINT A LINE.
         B     COUNTIN2                GO SEE IF MORE RECS. THIS TRK.
         EJECT
***********************************************************************
*                                                                     *
*        CONVERT DATA TO GRAPHICS,OR PRINTABLE ENGLISH.               *
*                                                                     *
***********************************************************************
         SPACE
         USING PRNTAREA,4
GRAPHIC  MVI   DELIMITA,C'*'           DELIMIT THE GRAPHIC
         MVI   DELIMITB,C'*'             FIELD IN THE OUTPUT BUFFER.
         LA    GR6,COREBLK             SET FOR EXEC.
         BCTR  GR5,0                   DECREMENT FOR EXEC.
         EX    GR5,MOVE                STORAGE BLK TO PRINT AREA.
         LA    GR5,1(GR5)              INCREMENT LOOP CONTROL.
LOOP     CLI   0(GR6),X'C1'             CHAR MORE THAN C0       A38484
         BNL   CHECK                    YES - CHECK
         CLI   0(GR6),C' '             IS CHAR BLANK.
         BE    EXITOK                  YES--NO MODIFY.
DOT      EQU   *                                                   3605
         MVI   0(GR6),C'.'             UNPRINTABLE CHAR.
EXITOK   LA    GR6,1(GR6)              INCREMENT BUFFER POINTER.
         BCT   GR5,LOOP                LOOP THRU TO END.
         LA    GR8,COREBLK+32          END OF GRAPHIC AREA.
LOOP1    CR    GR8,GR6                 A COMPLETE LINE.
         BC    8,LOOP2                 YES--GO TO TRANSLATE.
         MVI   0(GR6),C'.'             NO--MOVE IN '.'
         LA    GR6,1(GR6)              INCREMENT POINTER.
         B     LOOP1                   GO CHECK IF LINE COMPLETE.
LOOP2    MVC   DAWORK(3),LOCCTR+1      LOCATION CTR TO WORK AREA.
         UNPK  DAWORK(7),DAWORK(4)     UNPACK AND
         TR    DAWORK(6),IOTAB-240       TRANSLATE TO GRAPHIC.
         MVC   CNTAREA(6),DAWORK       MOVE LOCATION CTR. TO BUFFER.
         LA    GR8,LAYOUT              LAYOUT LINE.
         BR    GR9                     RETURN TO CALLER.
CHECK    EQU   *                                                   3605
         CLI   0(GR6),C'I'             IS IT A THRU I.             3605
         BNH   EXITOK                  YES-OKAY.                   3605
         CLI   0(GR6),C'J'             IS IT A J OR ABOVE.         3605
         BL    DOT                     NO-NONPRINTABLE CHARACTER.  3605
         CLI   0(GR6),C'R'             IS IT J THRU R.             3605
         BNH   EXITOK                  YES-OKAY.                   3605
         CLI   0(GR6),C'S'             IS IT AN S OR ABOVE.        3605
         BL    DOT                     NO-NONPRINTABLE CHARACTER.  3605
         CLI   0(GR6),C'Z'             IS IT AN S THRU Z.          3605
         BNH   EXITOK                  YES-OKAY.                   3605
         CLI   0(GR6),C'0'             IS IT GREATER THAN ZERO.    3605
         BL    DOT                     NO-NONPRINTABLE CHARACTER.  3605
         CLI   0(GR6),C'9'             IS IT 0 THRU 9.             3605
         BNH   EXITOK                  YES-OKAY.                   3605
         B     DOT                     NO-NONPRINTABLE CHARACTER.  3605
         EJECT
***********************************************************************
*                                                                     *
*       PRINT A LINE IN THE OUTPUT DATASET.                           *
*                                                                     *
***********************************************************************
         SPACE
OUTPUT   BALR  GR14,GR15               BRANCH TO -IEHDPRNT-.
         BR    GR9                     RETURN TO CALLER
         SPACE
***********************************************************************
*                                                                     *
*        RETURN TO IEHDEXCP OR IEHDRCVR HERE.                      O122
*                                                                     *
***********************************************************************
         SPACE
RETURN   L     GR13,OUTSAVE+4          RESTORE REGISTER 13.
         RETURN (14,12),T              RETURN                      O122
         SPACE
         SPACE
MOVE     MVC   0(1,GR6),0(DATAPTR)       STORAGE BLK TO PRINT AREA.
         SPACE
MOVE1    MVC   DAWORK(1),0(DATAPTR)      DATA TO WORK AREA.
         SPACE
MOVE2    MVC   0(1,GR4),DAWORK         UNPKED DATA TO PRINT BUFFER.
         EJECT
*
*        CONSTANTS USED BY THIS ROUTINE.
*
         SPACE
         DC    C'MAINTENANCE AREA'
MAINT    DS    50F                      MAINT AREA FOR ZAPS     XL02912
DAWORK   DS    3F
OUTSAVE  DS    18F                     SAVE AREA.
SAVESLOT DS    1F
INCOMPLT DS    1H
LOCCTR   DS    1F
ZERO     DC    F'0'
THIRTY2  DC    F'32'
TRACKKEY DC    C'**** TRACK'
R0DATA   DC    C'R0 DATA'
COUNTKEY DC    C'COUNT'
SAMESW   DC    XL1'00'
         DS    0F
         SPACE
IOTAB    DC    C'0123456789ABCDEF'
         SPACE
SAME     DC    C'LINE(S) SAME AS ABOVE'
         SPACE
LAYOUT   DC    X'0903'
         DC    X'0903'
         DC    X'0903'
         DC    X'0903'
         DC    X'0D03'
         DC    X'0903'
         DC    X'0903'
         DC    X'0903'
         DC    X'FF'
         SPACE
*
*   DSECTS USED BY THIS ROUTINE.
*
PRNTAREA DSECT
CNTAREA  DS    CL6                     LOCATION COUNTER.
BLANKA   DS    CL3
EBCBLK   DS    CL74
BLANKB   DS    CL3
DELIMITA DS    CL1
COREBLK  DS    CL32
DELIMITB DS    CL1
         EJECT
         IEHDWORK
         EJECT
         IEHDBLKS
         END
