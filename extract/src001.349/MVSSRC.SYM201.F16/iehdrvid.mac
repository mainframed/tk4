         COPY  LCGASMSW
*                                                               XM05523
         TITLE 'IEHDRVID --- RESTORE VOLID FOR RESTORE'
*
*
IEHDRVID CSECT
*                                                                     *
*FUNCTION/OPERATION- THIS CSECT WAS CREATED BY SPLITTING THE RESTORE  *
*   FUNCTION OF THE IEHDASDR UTILITY PROGRAM. THE FUNCTION OF THIS    *
*   CSECT IS TO READ THE VOLUME LABEL OF THE DIRECT ACCESS DEVICE.    *
*   IF THE OLD SERIAL NUMBER IS TO BE RETAINED AND IT IS DIFFERENT    *
*   FROM THE NEW SERIAL NUMBER, THEN THE VOLUME LABEL IS REWRITTEN    *
*   WITH THE OLD SERIAL NUMBER. THIS ROUTINE ALSO UPDATES THE         *
*   ALTERNATE TRACK INFORMATION FIELDS IN THE FORMAT 4 DSCB. THE      *
*   ACTUAL SERIAL NUMBER OF THE VOLUME BEING RESTORED IS ALSO SET UP  *
*   IN TH ECOMPLETION MESSAGE FOR SUBSEQUENT PRINTING.                *
*                                                                     *
*INPUT-  POINTERS TO A WORKAREA(REGISTER 12) AND TO A CONTROL         *
*   BLOCK(REGISTER 10).REGISTER 7 WILL POINT TO THE DDNAME UPON       *
*   ENTRY AND REGISTER 8 WILL POINT TO THE UCB FOR THE DASD DEVICE.   *
*
*EXITS-NORMAL- A SUCCESSFUL COMPLETION ENDS IN A RETURN TO THE        *
*   MODULE -IEHDREST- WITH A RETURN CODE OF 0.                        *
*                                                                     *
*EXITS-ERROR-  ANY ERROR ENCOUNTERED IS DESCRIBED BY AN APPROP-       *
*   IATE MESSAGE TO THE MESSAGE DATA SET. A RETURN CODE GREATER       *
*   THAN ZERO WILL ALSO BE PASSED BACK TO MODULE -IEHDREST-.          *
*                                                                     *
*EXTERNAL ROUTINES-  THIS ROUTINE ONLY ENTERED FROM THE RESTORE       *
*   ROUTINE (IEHDREST). ALL MESSAGES ARE BUILT BY -IEHDMSGB-          *
*   AND WRITTEN OUT BY -IEHDPRNT-. ALL DEVICE CONSTANTS REFERENCED    *
*   ARE IN -IEHDCONS-.                                                *
*                                                                     *
*TABLES/WORK AREAS-  UPON ENTRY, REGISTERS 10 AND 12 POINT TO A       *
*   FUNCTION BLOCK AND WORK AREA RESPECTIVELY. THEY ARE DESCRIBED     *
*   IN THE DSECTS CALLED -FUNCBLK- AND -WORK-.                        *
*                                                                     *
*ATTRIBUTES- SERIALLY REUSABLE,RELOCATABLE                            *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*   THE FOLLOWING ARE REGISTER ASSIGNMENTS.                           *
*                                                                     *
***********************************************************************
         SPACE
GR0      EQU   0
GR1      EQU   1
GR2      EQU   2
GR3      EQU   3
GR4      EQU   4
GR5      EQU   5
GR6      EQU   6
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10
GR11     EQU   11
GR12     EQU   12
GR13     EQU   13
GR14     EQU   14
GR15     EQU   15
RCVT     EQU   9
UCBPTR   EQU   9
BASEREG  EQU   11
WORKBASE EQU   12
K0       EQU   0                       DISPLACEMENT CONSTANT.    A21395
         SPACE 3
***********************************************************************
*                                                                     *
*   MESSAGE REFERENCES.                                               *
*                                                                     *
***********************************************************************
         SPACE
MESS6    EQU   6                       MESSAGE IEH806
MESS28   EQU   28                      MESSAGE IEH828
         EJECT
         SPACE 2
         USING FUNCBLK,10
         USING IEHRVID,BASEREG
         USING WORK,12
         SPACE 1
IEHRVID  SAVE  (14,12),T,*             SAVE THE REGISTERS.
         LR    BASEREG,GR15            LOAD THE BASE REGISTER.
         LA    GR3,RVIDSAVE            SAVE AREA ADDRESS.
         ST    GR3,8(GR13)             ADDRESS OF NEW AREA TO OLD.
         ST    GR13,4(GR3)             ADDRESS OF OLD AREA TO NEW.
         LR    GR13,GR3                LOAD SAVE AREA ADDRESS.
         MVI   RETCODE,X'00'           INITIALIZE RETURN CODE
         MVC   RETCODE+1(3),RETCODE    SET RETCODE TO ZERO
***********************************************************************
*        REGISTER 8 MUST POINT TO THE UCB FOR THE DA DEVICE.          *
*        REGISTER 9 MUST POINT TO THE DIRECT ACCESS CONTROL BLOCKS    *
*        REGISTER 7 MUST POINT TO THE DDNAME UPON ENTRY.              *
*          UPON ENTRY.                                                *
*        REGISTER 6 IS A WORK REGISTER.                               *
*        REGISTER 3 IS A WORK REGISTER.                               *
*        REGISTER 2 MUST POINT TO THE ALTERNATE TRACK INFORMATION.    *
*        REGISTER 5 IS THE RETURN REGISTER.                           *
*                                                                     *
***********************************************************************
         SPACE
         USING UCB,GR8
         USING IOBLOCKS,9
RDVOLID  EQU   *
         XC    CCHH(5),CCHH            CLEAR CCHHR.
         LA    GR3,CCHH                SEARCH ADDRESS.
         ST    GR3,READVTOC            PLACE IN SEARCH CCW.
         TM    SEQSW+D1,RPSFEAT        TODD
         BZ    RDVOLIDA                NO
         LA    GR4,SETCCW              YES-SETUP ADDR OF SET
*                                       SECTOR CCW FOR IOB
RDVOLIDA EQU   *                                                 S20201
         MVI   READVTOC,X'31'          SET OP CODE.
         LA    GR4,READVTOC            CCWS ADDRESS.
         ST    GR4,DCCWAD              CCW ADDRESS TO IOB.
         L     GR3,BUFFPTR1            READ IN AREA.
         ST    GR3,RDVDATA             STORE IN CCW.
         MVI   RDVDATA,6               SET FOR READ OP CODE.
         MVI   R,3                     SET SEARCH FOR RECORD 3.
         LA    GR3,VOL1L(GR3)           INCRE FOR RD-HA         YL02912
         ST    GR3,RHA                  STORE ADDR IN CCW       YL02912
         L     GR3,BUFFPTR1             RELOAD BUFFER PTR       YL02912
         MVI   RHA,RDHA                 RESET RDHA CMD          YL02912
         MVI   RDVDATA+L4,CCSILI        SET CMD-CHNG AND SILI   YL02912
*                                       BITS ON RD-DATA CCW     YL02912
         EXCP  DISKIOB                 READ IN THE LABEL
         MVI   RDVDATA+L4,SILI          RESET CCW SILI BIT      YL02912
         BAL   GR4,WAITD               AWAIT COMPLETION OFI/O.
         L     GR14,IODEVCON           GET CONSTANTS
         USING CONSTANT,GR14                                     M5861
         USING VLABEL,GR3                                       YL02912
         L     GR3,BUFFPTR1             RELOAD BUFFER PTR
         CLC   VOL1L+1(L4,GR3),LASTORIG IN THE ALT AREA?       @ZA04433
         BH    TRACK0                  YES, PUT OUT IEH828I    @ZA04433
         DROP  14                                                M5861
TRACK0R  EQU   *
         TM    SEQSW,CPYVOLID          MUST WE RETAIN THE OLD SERIAL.
         BNO   UPDATE4                  NO, UPDATE FORMAT 4 DSCB M2888
UPDSERNO EQU   *                       UPDATE VOLSER           @Z40RSRB
         MVC   SERIAL(6),IDSAVE         GET SERIAL               M6350
         MVI   RDVDATA,5               SET CCW OP CODE TO WRITE.
         EXCP  DISKIOB                 REWRITE THE LABEL.
         BAL   GR4,WAITD               AWAIT COMPLETION OFI/O.
UPDATE4  MVC   CCHH(5),VTOCPTR         SET NEW VTOC IN IOB      M2888
         MVC   150(6,GR3),SERIAL       SAVE SERIAL FOR LATER    M6350
         LA    GR6,150(GR3)            ADDRESS OF SERIAL        M6350
         CLC   CCHH(4),CCHHBEG         VTOC ADDRESS GREATER THAN 1ST
         BL    WRTMSG                    TRACK OF THIS RESTORE//NO.
         CLC   CCHH(4),CCHHEND         VTOC ADDRESS LESS THAN UPPER
         BNL   WRTMSG                    LIMIT//NO-GO GIVE FINAL MSG.
         MVI   RDVDATA,6               SET CCW CODE TO READ.
         EXCP  DISKIOB                 READ IN FORMAT 4 DSCB.
         BAL   GR4,WAITD               AWAIT COMPLETION OF I/O.
         MVC   8(6,GR3),0(GR2)         MOVE IN THE SAVED ALTERNATE
*                                        TRACK INFORMATION.
         EJECT
***********************************************************************
*                                                                     *
* THIS SECTION REPAIRS FAULTY 2314 DIRECTORY BLOCKS                   *
*                                                                     *
***********************************************************************
SKPALTR  EQU   *
         SPACE
         CLI   IODEVCON,X'08'          IS THIS A 2314
         BNE   GOODBLKS                NO, GO AROUND
         MVI   31(GR3),X'11'           SET DIRECTORY BLOCKS TO 17
GOODBLKS EQU   *
         L     GR4,IODEVCON             TODD DEVICE CONSTANTS   XL03145
         USING CONSTANT,GR4
         MVC   D18(L2,GR3),CYLNO        MOVE CYL NO. TO F4 DSCB XL03145
         DROP  4                                                XL03145
*                                                                    *
         TM    FIRSTSW,DOSBIT           IS THE DOS-BIT ON       XL03145
         BNO   NODOS                    NO, DONT SET DOS BIT    XL03145
         OI    D14(GR3),DOSCON          SET DOS CONTAMINATION   XL03145
*                                       BIT IN FORMAT 4         XL03145
NODOS    EQU   *                                                XL03145
         MVI   RDVDATA,5               SET CCW OP CODE FOR WRITE.
         EXCP  DISKIOB                 REWRITE FORMAT 4 DSCB.
         BAL   GR4,WAITD               AWAIT COMPLETION OFI/O.
WRTMSG   EQU   *
         LA    GR1,MESS6               MESSAGE 806.
         LR    GR4,GR9                 IOBLOCKS ADDRESS.
         BAL   GR9,BUILD               PLACE MESSAGE IN BUFFER.
         MVC   0(8,GR1),0(GR7)         DDNAME TO MESSAGE.
         MVC   MESS+9(10),FUNCTION     FUNCTION TYPE TO MESSAGE.
         MVC   MESS+67(6),0(GR6)       VOLUME SERIAL TO MESSAGE.
         BAL   GR9,MSGWRT1             GO WRITE OUT THE MESSAGE.
         LR    GR9,GR4                 RESTORE IOBLOCKS ADDRESS.
         EJECT
***********************************************************************
*                                                                     *
*   POST THE UCB HERE.                                                *
*                                                                     *
***********************************************************************
         SPACE
         LA    GR1,DSEEK               VTOC POINTER.
         ST    GR1,POSTLIST+8          STORE IN PARAMETER LIST.
         ST    GR8,POSTLIST            UCB ADDRESS TO PARM LIST.
         ST    GR6,POSTLIST+4          SERIAL ADDRESS TO PARM LIST.
         MVI   POSTLIST,X'08'          'POST' CODE FOR SVC.
         MVI   POSTLIST+8,X'80'        LAST ENTRY BIT.
         CNOP  0,4
         BAL   GR1,POST                SET PARAMETER POINTER.
POSTLIST DC    F'0'                    POST CODE//UCB ADDRESS.
         DC    F'0'                    SERIAL NO. ADDRESS.
         DC    F'0'                    POINTER TO VTOC ADDRESS.
POST     SVC   POSTSER                 GO UPDATE UCBS.
POSTSER  EQU   82                      SVC CODE FOR POSTING UCBS.
         L     GR15,RETCODE            SET RETURN CODE IN GR15 @Z40UAY1
         B     FINAL3                  RETURN TO CALLING RTN   @Z40UAY1
         DROP  3,8
         EJECT
WAITD    TM    DISKECB,COMPLETE        IS THE OPERATION COMPLETE.
         BO    TESTD                   YES-GO TEST THE STATUS.
         WAIT  ECB=DISKECB             AWAIT COMPLETION OF DISK.
TESTD    EQU   *
         CLI   DISKECB,GOOD            ALL OK ?
         BCR   8,GR4                   YES-RETURN.
         LA    GR15,8                  NO, SET RET. CODE TO 8
         B     FINAL3                  RETURN TO CALLING RTN
         EJECT
TRACK0   LA    GR1,MESS28              MESSAGE 828.
         LR    GR4,GR9                 SAVE I/O BLOCKS ADDRESSING.
         BAL   GR9,BUILD               PLACE MESSAGE IN BUFFER.
         MVC   0(8,GR1),0(GR7)         DDNAME TO MESSAGE.
         BAL   GR9,MSGWRT1             WRITE OUT THE MESSAGE.
         LR    GR9,GR4                 I/O BLOCKS ADDRESSING.
         MVC   RETCODE(L4),UNUSRET     SET RETURN CODE=4
         B     TRACK0R
         EJECT
         SPACE 1
BUILD    LINK  EP=IEHDMSGB
         BR    GR9                     RETURN.
         SPACE 2
MSGWRT1  EQU   *
         LINK  EP=IEHDPRNT              WRITE OUT THE MESSAGE    M0543
         BR    GR9 RETURN               RETURN                   M0543
         SPACE
FINAL3   L     GR13,RVIDSAVE+4         RESTORE REGISTER 13.
         RETURN (14,12),T,RC=(15)      RESTORE/RETURN W/PROPER CODE.
         EJECT
***********************************************************************
*                                                                     *
*    DEFINED CONSTANTS.                                               *
*                                                                     *
***********************************************************************
         SPACE
F1       DC    F'1'                    INCREMENT TRACK ADDR
FUNCTION DC    C'RESTORE TO'           MESSAGE FILL IN.
REELMASK DC    X'FFFFFFFE'             USED TO CHECK FOR LAST REEL TAPE
TOBIG    EQU   REELMASK                MAX CCW BYTE CNT         YM2241
UNUSRET  DC    F'4'                    RETURN CODE OF 4.
GOOD     EQU   X'7F'                   NO ERRORS ON I/O.
D5       EQU   5
L3       EQU   3                       LENGTH CONSTANT.
SACODE5  DC    X'63C24D'               CODE THAT IDENTIFIES RESTORE TP.
ALTRK    EQU   X'01'                    HOME ADDR FLAG FOR AN   YL02912
DOSCON   EQU   X'80'                    FMT4 DOS CONTAM BIT
EOF1     EQU   X'01'                    END OF FILE INDICATOR
NRF      EQU   X'08'                    NO RECORD FOUND
D14      EQU   14                       FMT4 DSCB OFFSET
L2       EQU   2                        DISPLACEMENT 2
D1       EQU   1                        DISPLACEMENT 1
D18      EQU   18                       DISPLACEMENT 18
RPSFEAT  EQU   X'40'                    RPS FEATURE
VOL1L    EQU   96                       VOL1 RECORD LENGTH
RDHA     EQU   X'1A'                    READ HOME ADDRESS
L4       EQU   4                        DISPLACEMENT 4
CCSILI   EQU   X'60'                    CCW CMD-CHNG AND SILI
SILI     EQU   X'20'                    CCW SILI BIT MASK
D0       EQU   0                        DISPLACEMENT 0
*                                       ALTERNATE TRACK.        YL02912
         EJECT
***********************************************************************
SETCCW   CCW   X'23',F1,X'40',1        SET SECTOR ZERO
SECTIC   CCW   8,0,0,0                 TIC BACK TO SETCCW
READVTOC CCW   X'31',0,X'60',5         SEARCH ON RECORD 1 OF VTOC.
         CCW   8,READVTOC,0,0          REPEAT UNTIL FOUND.
RDVDATA  CCW   6,0,X'20',96            READ FORMAT 4 DATA.
RHA      CCW   X'1A',0,X'20',5          READ HA OF VOL1 TRK     YL02912
READR0   CCW   X'16',0,X'30',16        READ-BACK CHECK R0 ON
*                                        OVERFLOW TRACKS.
***********************************************************************
*                                                                     *
*   DEFINED STORAGE.                                                  *
*                                                                     *
***********************************************************************
         SPACE
TAPECB   DC    F'0'                    WAIT OR COMPLETION BITS.  A21395
RVIDSAVE DS    18F                     SAVE AREA FOR CALLED ROUTINES.
ADDRBUFF DC    F'0'
CCHHADDR DS    1F                      IPL TRACK ADDRESS.
RETCODE  DC    F'0'                    RETURN CODE SAVE AREA.
ANSW     DS    CL1                     OPERATOR REPLY AREA.      A21395
SRKEY    DC    X'0000000003'           SEARCH ARGUMENT
***********************************************************************
*           MAINTENANCE AREA - PATCH SPACE                            *
*                                                                     *
MAINT    DC    10CL10'PATCH'     EBCDIC FOR EYEBALLING DUMP
*                                                                     *
***********************************************************************
         SPACE  4
*******************  END OF MODULE  ***********************************
         TITLE 'IEHDRVID - RESTORE VOLID FOR RESTORE'
         EJECT
IOBLOCKS DSECT
*   RESTORE DA WRITE-OUT ECB.
DISKECB  DS    1F                      WAIT//COMPLETE BITS PLUS CODE.
         SPACE 1
*   RESTORE DA WRITE-OUT IOB.
DISKIOB  DS    0F
DISKFLG  DS    CL2                     FLAGS 1 AND 2.
DSENSE   DS    CL2                     SENSE BYTES.
DECBAD   DS    1F                      ECB ADDRESS.
DSTATUS  DS    2F                      CHANNEL STATUS.
DCCWAD   DS    1F                      CCW LIST ADDRESS.
DDCBAD   DS    1F                      DCB ADDRESS.
DRESAD   DS    1F                      RESTART ADDRESS.
DBLKCT   DS    CL2                     BLOCK COUNT INCREMENT.
DERROR1  DS    CL2                     ERROR COUNT
DSEEK    DS    2F                      MBBCCHHR.
BB       EQU   DSEEK+1                 BIN ADDRESS.
CCHH     EQU   DSEEK+3                 CYLINDER-HEAD ADDRESS.
R        EQU   DSEEK+7                 RECORD NUMBER.
         SPACE 1
*   RESTORE DA WRITE-OUT DCB.
DISKDCB  DS    18F
         SPACE 2
*   RESTORE TAPE READ-IN ECB.
TAPEECB  DS    1F                      WAIT//COMPLETE BITS PLUS CODE.
         SPACE 1
*   RESTORE TAPE READ-IN IOB.
TAPEIOB  DS    0F
TAPEFLG  DS    CL2                     FLAGS 1 AND 2.
TSENSE   DS    CL2                     SENSE BYTES.
TECBAD   DS    1F                      ECB ADDRESS
TSTATUS  DS    2F                      CHANNEL STATUS.
TCCWAD   DS    1F                      CCW LIST ADDRESS.
TDCBAD   DS    1F                      DCB ADDRESS.
TRESAD   DS    1F                      RESTART ADDRESS.
TBLKCT   DS    CL2                     BLOCK COUNT INCREMENT.
TERROR   DS    CL2                     ERROR COUNT
         SPACE 1
*   RESTORE TAPE READ-IN DCB NUMBER ONE.
TAPEDCB  DS    13F
         SPACE 1
*   CCWS TO READ-IN RESTORE TAPE.
         DS    0D                      ALIGNMENT.
TAPECCW1 DS    CL8                     USED TO READ CCW RECORD.
TAPEDAT1 DS    CL8                     USED TO READ DATA RECORD.
TAPECCW2 DS    CL8                     USED TO READ CCW RECORD.
TAPEDAT2 DS    CL8                     USED TO READ DATA RECORD.
IOBLKEND DS    0H                      END OF IOBLOCKS DSECT.
         SPACE 1
         IEHDBLKS
         EJECT
         IEHDWORK
         SPACE
VLABEL   DSECT
VOLUME   DS    CL3                     LABEL IDENTIFIER.
VOLNO    DS    CL1                     LABEL NUMBER.
SERIAL   DS    CL6                     LABEL SERIAL NUMBER.
SECURITY DS    CL1                     VOLUME SECURITY.
VTOCPTR  DS    CL10                    POINTER TO VTOC
         DS    CL20                    RESERVED.
OWNER    DS    CL10                    OWNER IDENTIFICATION.
         DS    CL29                    BLANK.
         SPACE
RBUFFER  DSECT
ZERO1    DS    CL8                     WILL CONTAIN 8 BYTES OF ZEROS.
CCWBUFF  EQU   ZERO1
L1       EQU   1                       LENGTH ATTRIBUTE
TICSW    DS    CL1                     'FF' INDICATES PREVIOUS CCW=TIC.
         DS    CL3                     NOT USED.
ADDRESS  DS    CL4                     TEMPORARY ADDRESS HOLDER.
SEEKCCW  DS    CL8                     SEEK CCW FOR STAND-ALONE.
SETMASK  DS    CL8                     SET FILE MASK FOR STAND-ALONE.
SEARIDE  DS    CL8                     SEARCH EQUAL ID CCW.
         EJECT
CVT      DSECT
         CVT   SYS=MIN
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
JFCB     DSECT
         IEFJFCBN
         EJECT
         DCBD  DSORG=PS
         END
