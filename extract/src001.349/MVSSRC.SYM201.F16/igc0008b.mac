         TITLE 'IGC0008B --- SVC 82 FOR IEHDASDR UTILITY'
         COPY  LCGASMSW                                          SM4351
IGC0008B CSECT
*
*C 276100                           @SA74452=@YA09634=@XA09732=@ZA04363
*A 276020-276040,901600             @SA74452=@YA09634=@XA09732=@ZA04363
*
*                                                              @Y30LSFY
* ELIMINATE IEH813I MSG FOR SVC 82 FOR EMUL DEVICES.           @ZM40451
*098765,098432                                                VS06562
*                                                               YM04695
*                                                               VS02889
*                                                               YM03870
*                                                               YM03804
*STATUS CHANGE LEVEL 004    VS2/2 (MVM)                         YL02912
*A                                                             @Z30RSAG
*C                                                             @Z30RSAG
*D                                                             @Z30RSAG
*                                                                     *
*FUNCTION/OPERATION-  SVC 82 PERFORMS THE FOLLOWING FUNCTIONS FOR     *
*   THE -IEHDASDR- UTILITY PROGRAM//TYPE 4 SVC WITH EXTENDED SVRB.    *
*                                                                     *
*     1) IN PREPARATION FOR SURFACE ANALYSIS FOR OFF-LINE VOLUMES.    *
*        -ASK FOR OPERATORS PERMISSION TO INITIALIZE A SPECIFIC VOL.  *
*        -IF OPERATOR REPLIES WITH A 'T', RETURN TO CALLER.RC=8.      *
*        -IF OPERATOR REPLIES WITH A 'U', BUILDS A DEB, LOADS IN      *
*         THE ABNORMAL-END APPENDAGE(IGG019P9), AND RETURNS TO        *
*         CALLER.RC=0.                                                *
*                                                                     *
*     2) ALTERNATE TRACK ASSIGNMENT REQUEST FOR GETALT FUNCTION.      *
*        -GETS CORE FOR A WORKAREA, BUILDS A DEB WITHIN THIS CORE,    *
*         AND PASSES CONTROL VIA A XCTL TO -IGC0108B- FOR THE         *
*         ACTUAL ALTERNATE TRACK ASSIGNMENT.                          *
*                                                                     *
*     3) ALTERNATE TRACK REQUEST BY FORMAT/ANALYZE FUNCTION.          *
*        -SAME AS OPERATION 2 ABOVE.                                  *
*                                                                     *
*     4) POST UCB REQUEST.                                            *
*        -PASSES CONTROL VIA A XCTL TO -IGC0208B-.                    *
*                                                                     *
*                                                                     *
*     5) SPECIAL POST UCB REQUEST.                                    *
*        -DELETES THE ABNORMAL-END APPENDAGE.                         *
*        -FREES THE CORE FOR THE DEB.                                 *
*        -PASSES CONTROL TO -IGC0208B-.                               *
*                                                                     *
         AIF   ('&LIB' EQ 'LIB1').X227900                        YM3475
*     6) DELETE DEB REQUEST.                                          *
*        -DELETES THE ABNORMAL-END APPENDAGE.                         *
*        -FREES THE CORE FOR THE DEB.                                 *
*                                                                     *
*     7) ISSUE SENSE FOR WINCHESTER DEVICES.                    XL03130
*        -ISSUE SENSE AND RETURN WINCHESTER MODEL NUMBER IN     XL03130
*         USERS' PARAMETER LIST + 4 :                           XL03130
*              MODEL 1 = F'01'                                  XL03130
*              MODEL 2 = F'02'                                  XL03130
*                                                                     *
*     8) ONLINE ENQ REQUEST.                                          *
*        -ENQ'S ON VOLID OF 'TODD' VOLUME.                      YL02912
*                                                                     *
.X227900 ANOP                                                    YM3475
*ENTRY POINT-  THE ONLY ENTRY POINT IS -IGC0008B-.                    *
*                                                                     *
*INPUT-  A PARAMETER LIST POINTED TO BY REGISTER 1 FOR THE ABOVE      *
*   DESCRIBED FUNCTIONS.                                              *
*                                                                     *
*     1) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=8F.                  *
*        -2ND WORD-   PTR TO DCB,HIGH ORDER BYTE=80.                  *
*                                                                     *
*     2) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=1F.                  *
*        -2ND WORD-   CCHH OF DEFECTIVE TRACK.                        *
*        -3RD WORD-   PTR TO DATA BUFFER                       @Z30RSAG
*                                                                     *
*     3) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=00.                  *
*        -2ND WORD-   CCHH OF DEFECTIVE TRACK.                        *
*        -3RD WORD-   PTR TO 6-BYTES OF ALT. TRACK INFO.              *
*                     HIGH ORDER BYTE WILL CONTAIN A X'01'     @Y30LSFY
*                     FOR SS1 CONVERSION.                      @Y30LSFY
*                                                                     *
*     4) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=08.                  *
*        -2ND WORD-   PTR TO 6-BYTE SERIAL NO, IF OVERWRITTEN.        *
*        -3RD WORD-   PTR TO MBBCCHHR FOR NEW VTOC.                   *
*                                                                     *
*     5) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=88.                  *
*        -2ND WORD-   PTR TO 6-BYTE SERIAL NO, IF OVERWRITTEN.        *
*        -3RD WORD-   PTR TO MBBCCHHR FOR NEW VTOC.                   *
*        -4TH WORD-   PTR TO DEB.                                     *
*                                                                     *
         AIF   ('&LIB' EQ 'LIB1').X227901                        YM3475
*     6) -1ST WORD-   RESERVED, HIGH ORDER BYTE=F8.                   *
*        -2ND WORD-   RESERVED.                                       *
*        -3RD WORD-   RESERVED.                                       *
*        -4TH WORD-   PTR TO DEB.                                     *
*                                                                     *
*     7) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=F1.            XL03130
*        -2ND WORD-   HIGH ORDER BYTE=80.                       XL03130
*                                                                     *
*     8) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=04.            YL02912
*        -2ND WORD-   ADDR OF FUNCBLK, HIGH ORDER BYTE=80.     @Z30RSAG
*     9) -1ST WORD-   PTR TO UCB,HIGH ORDER BYTE=44.            YL02912
*                                                                     *
.X227901 ANOP                                                    YM3475
*EXITS-NORMAL-  RETURN TO CALLER VIA REG 14. RETURN CODE=0.           *
*                                                                     *
*EXITS-ERROR-   RETURN TO CALLER VIA REG 14. RETURN CODE=8.           *
*                                                                     *
*EXTERNAL ROUTINES- SVC IS ISSUED FOR THE FOLLOWING FUNCTIONS FROM    *
*                                                                     *
*   THE FOLLOWING MODULES.                                            *
*                                                                     *
*     1) IHEDANAL.                                                    *
*                                                                     *
*     2) IEHDGETA.                                                    *
*                                                                     *
*     3) IEHDANAL,IEHDCELL.                                           *
*                                                                     *
*     4) IEHDLABL,IEHDANAL,IEHDREST,IEHDDUMP                          *
*                                                                     *
*     5) IEHDANAL                                                     *
*                                                                     *
         AIF   ('&LIB' EQ 'LIB1').X227902                        YM3475
*     6) IEHDANAL,IEHDLABL.                                           *
*                                                                     *
*     7) IEHDASDS                                               XL03130
*                                                                     *
*     8) IEHDASDS, FOR ENQING ON THE TODD UCBS' VOLID.          YL02912
*                                                                     *
.X227902 ANOP                                                    YM3475
*SUPERVISOR MACROS-  GETMAIN,FREEMAIN,WTOR,WTO,LOAD,XCTL.             *
*                                                                     *
*TABLES/WORKAREAS-  OBTAINS A WORKAREA FOR -IGC0108B- WHICH IS        *
*   DESCRIBED BY THE DSECT -IOBLOCKS-.                                *
*                                                                     *
*OUTPUT-                                                              *
*     1) FOR FUNCTION NO. 1,BUILDS A PROPER DEB AND LOADS IN THE      *
*        ABNORMAL-END APPENDAGE.                                      *
*                                                                     *
*     -FOR ALL OTHER FUNCTIONS, SEE  -IGC0108B- OR -IBC0208B.         *
*                                                                     *
*ATTRIBUTES-  REENTRANT,RELOCATABLE,PRIVILEGED,ENABLED.               *
*                                                                     *
***********************************************************************
         EJECT
*   THE FOLLOWING ARE REGISTER ASSIGNMENTS.
PARMPTR  EQU   1                       POINTER TO INPUT PARAMETERS.
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R6       EQU   6
R7       EQU   7                                                 Y01021
R8       EQU   8                       TCB ADDRESS.
R9       EQU   9
R11      EQU   11                      POINTER TO INPUT PARAMETERS.
R14      EQU   14                      LINK REGISTER.
R15      EQU   15
GR0      EQU   0
GR1      EQU   1
GR2      EQU   2                       INDEX TO UCB TABLE ENTRIES.
GR3      EQU   3                       POINTER TO TABLE UCB.
GR4      EQU   4                       POINTER TO NEW SERIAL.
GR5      EQU   5                       POINTER TO TABLE UCB  SERIAL.
GR6      EQU   6                       POINTER TO INPUT UCB.
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10
GR11     EQU   11
GR12     EQU   12
GR14     EQU   14                      LINK REGISTER.
BASEREG  EQU   12                      BASE REGISTER.
GR15     EQU   15
         SPACE
DYNALT   EQU   X'1F'                   GETALT ASSIGNMENT.
SENS     EQU   X'F1'                    SENSE REQUEST FOR WINCH XL03130
*
NEWVOL   EQU   X'8F'                   NEW VOLUME INPUT.
COMMA    EQU   C','                    EBCDIC COMMA.
SLASH    EQU   C'/'                    2321 BIN SEPARATOR.
WSIZE    EQU   52                      CONSOLE MESSAGE SIZE.
DA2321   EQU   X'05'                   UCB DEVICE TYPE CODE FOR 2321.
LASTBIN  EQU   X'09'                   LAST BIN NO. FOR SUB-UCBS.
SUBLNG   EQU   16                      LENGTH OF EACH SUB-UCB.
MAINUCB  EQU   X'FF'                   PRIMARY UCB IDENTIFIER.
NOTREADY EQU   X'40'                   UCB NOT READY BIT.
PGFIXBIT EQU   X'80'                   PAGE FIX APPENDAGE PRESENT
         SPACE
INIT     EQU   X'00'                   ANALYZE/FORMAT REQUEST.
POSTSPEC EQU   X'88'                   POST REQUEST FOR NEW VOLUMES.
POST     EQU   X'08'                   POST INDICATOR.
RETURN   EQU   3                       RETURN SVC CODE.
X06      EQU   X'06'                    HEX CONSTANT             S20201
X07      EQU   X'07'                    HEX CONSTANT             S20201
E0       EQU   0                        CONSTANT OF 0            Y01021
E3       EQU   3                        CONSTANT OF 3            Y01021
E4       EQU   4                        CONSTANT OF 4            Y01021
E5       EQU   5                        CONSTANT OF 5            Y01021
E8       EQU   8                        CONSTANT OF 8            Y01021
E12      EQU   12                       CONSTANT OF 12           Y01021
E44      EQU   44                       CONSTANT OF 44           Y01021
E45      EQU   45                       CONSTANT OF 45           Y01021
D3340    EQU   X'0A'                   3340 DEVICE TYPE        @Z30RSAG
EMUL     EQU   X'08'                   SNS BYTE 2 EMULATE BIT  @Z30RSAG
FOXES    EQU   X'FF'                    END OF P.L. FLAG FOR ENQ SJ0054
D0       EQU   0                        ZERO DISPLACEMENT.      YL02912
D1       EQU   1                        DISPL. OF ONE.          YL02912
D3       EQU   3                        DISPL. OF THREE         YM04695
HEX0     EQU   X'00'                    WILL ZERO ENQ R.C.      YM04695
L3       EQU   3                        LENGTH OF THREE.        YL02912
K36      EQU   36                       CONSTANT OF 36.         YL02912
PTRCVT   EQU   16                      CVT PTR ADDR              Y01021
         AIF   ('&LIB' EQ 'LIB1').X227903                        YM3475
DELDEB   EQU   X'F8'                    DEB DELETE REQUEST.      YM3475
NALOCOFF EQU   X'FB'                    MASK TO RESET UCBNALOC  YL02912
EIGHT    EQU   8                        RETURN CODE FROM ENQ    YL02912
ENQ      EQU   X'04'                    ONLINE ENQ FUNC BYTE    YL02912
DEQ      EQU   X'44'                    ONLINE DEQ FUNC BYTE    YL02912
ONE      EQU   GR1                      CONSTANT                YL02912
OFFLINE  EQU   X'88'                    OFFLINE LABEL IND.      YL02912
SS1LD    EQU   X'20'                   TRANSFER TO SS/1 LOAD   @Y30LSFY
.X227903 ANOP                                                    YM3475
         EJECT
         BALR  BASEREG,0               SET UP ADDRESSING.
         USING *,BASEREG
         SPACE
         B     APARNO                  BRANCH AROUND APAR NO   @ZA04363
         DC    C'IGC0008B OZ04363'     LAST FIX THIS MOD       @ZA04363
APARNO   LR    GR10,PARMPTR            GET PARAMETER POINTER.  @ZA04363
         SPACE
***********************************************************************
*                                                                     *
*  THE CALLERS PARAMETER LIST WILL BE PLACED INTO A FIVE WORD BLOCK   *
*   OF GOTTEN CORE OF THE SVC'S KEY. THE FIRST FOUR WORDS WILL BE A   *
*   COPY OF THE CALLER'S LIST AND THE LAST WORD WILL CONTAIN THE KEY  *
*   OF THE CALLER IN THE FIRST BYTE AND THE ADDRESS OF THE CALLER'S   *
*   LIST IN THE LAST THREE BYTES.                                     *
*                                                                     *
***********************************************************************
         LR    GR8,GR4                 LOAD TCB PTR             XL03130
         SPACE
         USING PARMLIST,R11                                     YL02912
         USING RBBASIC,GR5                                      YL02912
         MODESET EXTKEY=SUPR                                    YL02912
         ST    GR14,RBEXSAVE+K36        SAVE RETURN ADDRESS.    YL02912
         MODESET EXTKEY=DATAMGT                                 YL02912
         LA    R2,ENDLIST-PARMLIST      CORE FOR PARM LIST.     YL02912
         GETMAIN R,LV=(2),SP=229        FOR CALLER'S PARAMETER. YL02912
         LR    R11,R1                   SAVE NEW PARM POINTER.  YL02912
         ST    GR10,PRMPTR              SAVE OLD PARM POINTER.  YL02912
         LR    R8,R4                    GET ADDR OF CURRENT TCB.YL02912
         USING TCB,R8                                           YL02912
         SPACE
         MODESET EXTKEY=RBT234,WORKREG=3  GET CALLER'S KEY.     YL02912
         LM    R1,R4,E0(GR10)           GET CALLER'S PARM LIST. YL02912
         MODESET EXTKEY=DATAMGT,SAVEKEY=KEY,WORKREG=15          YL02912
         STM   R1,R4,PARMLIST           CALLER'S LIST TO MINE.  YL02912
***********************************************************************
*                                                                     *
*   DETERMINE THE FUNCTION TO BE PERFORMED.                           *
*                                                                     *
***********************************************************************
         SPACE
         AIF   ('&LIB' EQ 'LIB1').NOWIN BRCH IF OS ASSEM        XL03130
.NOWIN   ANOP                                                   XL03130
         CLI   FUNCTION,SENS           IS THIS A SENSE REQUEST. XL03130
         BE    SENSIT                  YES, GO ISSUE SENSE CMD  XL03130
         SPACE
         CLI   FUNCTION,NEWVOL          NEW VOLUME REQUEST?     YL02912
         BE    NEWPACK                  YES, GO BUILD DEB.      YL02912
         SPACE
         CLI   FUNCTION,DYNALT          DYNAMIC ALT TRK REQUEST?YL02912
         BE    ASSGALT                  YES, ASSIGN ALTERNATE.  YL02912
         SPACE
         CLI   FUNCTION,INIT            ANALYZE/FORMAT REQUEST? YL02912
         BE    ASGALT1                  YES, PROCESS TRACK.     YL02912
         SPACE
         CLI   FUNCTION,POST            POST UCB REQUEST?       YL02912
         BE    CKUCBS                   YES, UPDATE UCBS.       YL02912
         SPACE
         CLI   FUNCTION,POSTSPEC        OFFLINE POST REQUEST?   YL02912
         BE    SPECPOST                 YES, UPDATE UCB.        YL02912
         SPACE
         CLI   FUNCTION,DELDEB          DEB DELETE REQUEST?     YL02912
         BE    SPECPOST                 YES DELETE DEB.         YL02912
         SPACE
         CLI   FUNCTION,ENQ             ENQUEUE REQUEST?        YL02912
         BE    ENQUE                    YES, UPDATE UCB.        YL02912
         SPACE
         CLI   FUNCTION,SS1CONV         THIS AN ANALYZE REQUEST@Y30LSFY
*                                      TO ASSIGN AN ALTERNATE  @Y30LSFY
*                                      ON AN SS1 SPOOL PACK.   @Y30LSFY
         BE    ASGALT1                 YES-GO PROCESS.         @Y30LSFY
         TM    FUNCTION,SS1LD          TRANSFER TO SS/1 LOAD   @Y30LSFY
         BZ    XIT                     NO                      @Y30LSFY
*        TRANSFER TO THE SS/1 LOAD OF SVC 82                   @Y30LSFY
         LR    GR1,GR11                PARM LIST POINTER       @ZM31081
         LR    GR2,GR5                 RBBASIC POINTER         @ZM31081
         MODESET EXTKEY=SUPR                                   @Y30LSFY
         L     GR3,RBEXSAVE+K36        RETURN ADDRESS          @ZM31081
         STM   GR1,GR3,RBEXSAVE+K16    STORE LOAD LIST         @ZM31081
         LA    GR1,RBEXSAVE+K16        LIST ADDRESS FOR LOAD   @Y30LSFY
         LA    GR4,XCTL3               XCTL FOR NEXT LOAD      @Y30LSFY
         B     NEXTLOAD                GET NEXT LOAD           @Y30LSFY
         SPACE
XIT      EQU   *                                                XL03130
         B     EXIT                     UNSUPPORTED FUNCTION.   YL02912
         EJECT
         AIF   ('&LIB' EQ 'LIB1').NOWIN1 BRCH IF OS ASSEM       XL03130
**********************************************************************
*        THE FOLLOWING ROUTINE WILL ISSUE A 'SENSE' CMD TO THE
*        333X AND 3340 DEVICE BEING PROCESSED BY IEHDASDS. THE
*        SENSE WILL DETERMINE WINCHESTER MODEL BEING USED,     @Z30RSAG
*        AND/OR IF EMULATION IS OCCURRING.                     @Z30RSAG
**********************************************************************
         SPACE
SENSIT   EQU   *                                                XL03130
         GETMAIN R,LV=88,SP=229         CORE FOR EXCP CTL BLKS  YL02130
         USING CTLBLKS,GR2                                      XL03130
         LR    GR2,GR1                  SAVE AREA ADDR          XL03130
         L     GR1,CVTPTR               GET CVT ADDR            XL03130
         USING CVT,GR1
         LA    GR4,BUMP                 DCBORG                  XL03130
         XC    DCBORG(L88),DCBORG       CLEAR CTL BLKS          XL03130
         SR    GR2,GR4                  RESET DSECT BASE        XL03130
         L     GR3,UCBPTR               GET UCB ADDR            XL03130
         USING UCB,GR3                                          XL03130
         MODESET EXTKEY=SUPR                                     YM5769
         NI    UCBFL1,MASK-UCBNOTRD    TURN OFF NOT-READY BIT   XL03130
         MODESET EXTKEY=DATAMGT                                  YM5769
         ST    GR3,DEBUCB               STORE UCB IN DEB        XL03130
         MVI   DEBUCB,INIT              ZERO FILE MASK          XL03130
         LA    GR3,ECB1                 GET ECB ADDR            XL03130
         ST    GR3,IOBECB               STORE IT IN IOB         XL03130
         MVC   CCW,SENSCCW              MOVE SKELTON CCW        XL03130
         LA    GR4,CCW                  GET CCW ADDR            XL03130
         ST    GR4,IOBCCW               STORE IN IOB            XL03130
         LA    GR4,SENSE                GET AREA ADDR           XL03130
         ST    GR4,CCW                  SETUP SENSE CCW         XL03130
         MVI   CCW,SENSEOP              RESET OP CODE           XL03130
         ST    GR2,DEBDCB               PUT DCB ADDR IN DEB     XL03130
         MVC   DEBAPP(L4),CVTXAPG       APPDGE VECTOR TABL ADDR XL03130
         ST    GR2,IOBDCB               DCB ADDR IN IOB         XL03130
         LA    GR4,DEBORG               LOAD DEB ADDR           XL03130
         ST    GR4,DCBDEB               STORE IT IN DCB         XL03130
         MVC   DEBDCB(L1),TCBPKF        MOVE TCB KEY TO DEB     XL03130
         OI    DEBDCB,DEBID             SET DEB ID              XL03130
         EXCP  IOB1                     ISSUE SENSE CMD         XL03130
         WAIT  ECB=ECB1                 WAIT FOR SENSE          XL03130
         L     GR3,UCBPTR              GET UCB ADDR            @Z30RSAG
         CLI   ECB1,GOODRT              SUCCESSFUL SENSE ?      XL03130
         BNE   FREEIT                   NO, DONT SET MODEL NO.  XL03130
         CLI   UCBTBYT4,D3340          THIS A 3340 ?           @Z30RSAG
         BNE   INTSNS                  NO, GO LOOK AT SNS      @Z30RSAG
         TM    SENSE+L2,MOD1            36MEG WINCH. ?          XL03130
         BO    STORE1                   YES, GO STORE X'01'     XL03130
         TM    SENSE+L2,MOD2            72 MEG WINCH            XL03130
         BNO   FREEIT                   NO, EXIT                XL03130
         MVI   WINMOD,MOD2              MOVE MODEL NO TO PL     XL03130
         B     FREEIT                   EXIT                    XL03130
STORE1   EQU   *                                                XL03130
         MVI   WINMOD,MOD1              MOVE MODEL NO TO PL     XL03130
FREEIT   EQU   *                                                XL03130
         LA    GR2,BUMP(GR2)            RESET CORE PTR          XL03130
         FREEMAIN R,LV=88,A=(GR2),SP=229 FREE CTL BLK CORE      YL02130
         L     GR4,PRMPTR               CALLER'S LIST ADDR.     YL02912
         IC    GR9,WINMOD              GET WINCH MODEL NO.      VS02889
         MODESET KEYADDR=KEY,WORKREG=15 GET CALLER'S KEY.       YL02912
         STC   GR9,L7(GR4)             STORE MODEL NO. IN P.L.  VS02889
         MODESET EXTKEY=DATAMGT                                 YL02912
         CLI   WINMOD,INIT              WAS SENSE GOOD ?        XL03130
         BNE   EXIT                     YES, EXIT               XL03130
         BAL   R9,FREEPARM              GO FREE PARM LIST.
         LA    GR15,GR15                NO, SET NON-ZERO R.C.   XL03130
         B     EXIT2                    RETURN TO 'DASDS        XL03130
         DROP GR1
         USING FUNCBLK,GR3                                     @Z30RSAG
INTSNS   EQU   *                                               @Z30RSAG
         L     GR3,SENSFB              LOAD FUNCBLK PTR        @Z30RSAG
         TM    SENSE+L2,EMUL           THIS DEV EMULATED ?     @Z30RSAG
         BNO   STORE1                  NO EXIT THIS RTNE       @ZM40451
         MODESET KEYADDR=KEY,WORKREG=15                        @ZM40430
         OI    FLGBYT1,EMU             YES, TURN ON FLAG       @Z30RSAG
         MODESET EXTKEY=DATAMGT                                @ZM40430
         B     STORE1                  EXIT TO IEHDASDS        @ZM40451
         DROP GR3                                              @Z30RSAG
         EJECT
.NOWIN1  ANOP                                                   XL03130
SPECPOST EQU   *
         DELETE EP=IGG019P9            FREE UP CORE USED BY APPENDAGE.
         AIF   ('&LIB' EQ 'LIB1').WM004
         DELETE EP=IGG019P7            FREE UP CORE USED BY      YM3011
*                                          APPENDAGE.
.WM004   ANOP
         L     GR6,DEBPTR               ADDRESS OF DEB.         YL02912
         LA    GR0,DEB-MYDEB           OFFSET FORM WORKAREA START.
         SR    GR6,GR0                 START OF WORK AREA.      YL02912
         USING MYDEB,GR6                                        YL02912
         AIF   ('&LIB' EQ 'LIB1').WM008
         L     GR4,DEBUCBAD-ONE         GET UCB ADDR            YL02912
         USING UCB,GR4                                          YL02912
         MODESET EXTKEY=SUPR                                    YL02912
         NI    UCBFL5,NALOCOFF          SET BIT OFF             YL02912
         MODESET EXTKEY=DATAMGT                                 YL02912
         L     GR4,DEBDEBID             GET DCB ADDR             Y01021
         LA    GR4,E0(GR4)              CLEAR HIGH ORDER BYTE    Y01021
         DEBCHK (GR4),TYPE=DELETE,AM=EXCP                       YL02912
         EJECT
***********************************************************************
*                                                                     *
*    TAKE DEB OFF TCB DEB CHAIN.                                      *
*                                                                     *
***********************************************************************
         SPACE
         LA    GR9,DEBDEBAD             GET ADDRESS OF NEXT DEB.YL02912
         CLC   TCBDEB+D1(L3),DEBPTR+D1  TCB POINT TO MY DEB?    YL02912
         BNE   GETDEB                   NO,SEARCH DEB CHAIN.    YL02912
         MODESET EXTKEY=SUPR                                    YL02912
         MVC   TCBDEB+D1(L3),D0(GR9)    YES, UNCHAIN DEB.       YL02912
         MODESET EXTKEY=DATAMGT                                 YL02912
         B     FREEDEB                  GO FREE DEB AREA.       YL02912
         SPACE
GETDEB   EQU   *                                                YL02912
         L     GR4,TCBDEB               GET DEB CHAIN POINTER.  YL02912
         USING DEB,GR4                                          YL02912
CHKDEB   EQU   *                                                YL02912
         CLC   DEBDEBAD(L3),DEBPTR+D1   DEB POINT TO MY DEB?    YL02912
         BE    OURDEB                   YES, GO UNCHAIN.        YL02912
         L     GR4,DEBDEBAD-D1          NO, GET NEXT DEB.       YL02912
         B     CHKDEB                   CHECK NEXT DEB.         YL02912
         SPACE
OURDEB   EQU   *                                                YL02912
         MVC   DEBDEBAD(L3),D0(GR9)     UNCHAIN MY DEB.         YL02912
         B     FREEDEB                  GO FREE DEB AREA.       YL02912
         DROP  GR4                                              YL02912
         EJECT
***********************************************************************
*        THE FOLLOWING ROUTINE WILL CAUSE THE DASDI DEVICE TO BE ENQ'ED
*        UPON, VIA ITS' VOLID. THE INITIATORS' TCB WILL BE USED IN
*        THE EVENT IEHDASDR SHOULD ABEND BEFORE DEQ'ING.
***********************************************************************
         SPACE
ENQUE    EQU   *                                                YL02912
         L     GR6,UCBPTR               GET UCB ADDR YL02912
         USING UCB,GR6                                          YL02912
         LA    GR7,UCBVOLI              GET ADDR OF VOLID  YL02912
         L     GR9,TCBOTC               LOAD MOTHER TCB ADDR    YM03804
         MVC   ENQLIST(SYSQ-ENQLISTF),ENQLISTF MOVE LIST TO W/A YM03804
*
         ENQ   (,(GR7),E,6,SYSTEM),RET=USE,TCB=(GR9),           YM03804*
               MF=(E,ENQLIST)                                   YM03804
*
         LTR   GR15,GR15                HAVE WE GOT THE RESR ?  YL02912
         BZ    EXIT                     YES, BRCH               YL02912
         LA    GR15,D3(GR15)            INCRE TO R.C.           YL02912
         CLI   0(GR15),EIGHT            RC = 8 ?                YL02912
         BE    ENQAGN                   YES, RE-ENQ             YL02912
         BAL   R9,FREEPARM              FREE PARAMETER LIST.    YL02912
         LA    GR15,NOGO                SOMEONE ELSE HAS RESRC  YL02912
         B     EXIT2                    RETURN TO 'DASDS        YL02912
ENQAGN   EQU   *                                                YL02912
*
         MVI   ENQLIST+D3,HEX0          ZERO R.C.               YM04695
         ENQ   (,(GR7),E,6,SYSTEM),RET=CHNG,TCB=(GR9),          YM03804*
               MF=(E,ENQLIST)                                   YL02912
*
         LTR   GR15,GR15                HAVE WE GOT THE RESRC?  YL02912
         BZ    EXIT                     YES, RETURN TO 'DASDS   YL02912
         LA    GR15,D3(GR15)            INCRE TO R.C.           YL02912
         CLI   0(GR15),EIGHT            RC = 8 ?                YL02912
         BE    ENQUE                    ENQ WITH RET=USE        YL02912
         BAL   R9,FREEPARM              FREE PARAMETER LIST.    YL02912
         LA    GR15,NOGO                SOMEONE ELSE HAS RESRC  YL02912
         B     EXIT2                                            YL02912
FREEDEB  EQU   *                        CONTINUE                 YM2646
         LR    GR1,GR6                  GET DEB ADDRESS.        YL02912
         USING MYDEB,GR1                                        YL02912
         L     GR4,DEBDEBID             GET DCB ADDRESS.         YM3475
         L     GR6,BLANKS               GET BLANK DDNAME.        YM3475
         MODESET KEYADDR=KEY,WORKREG=2  GET CALLER'S KEY.       YL02912
         ST    GR6,E44(GR4)             SET BLANKS IN DCB DDNAME.YM3475
         MODESET EXTKEY=DATAMGT                                 YL02912
.WM008   ANOP
         LA    R3,DEBEND-MYDEB          SIZE OF DEB WORKAREA.   YL02912
         FREEMAIN R,LV=(3),A=(1),SP=230                          YM4604
         LR    GR1,GR11                RESTORE PARM POINTER.
         AIF   ('&LIB' EQ 'LIB1').X227102                        YM3475
         CLI   FUNCTION,DELDEB          DEB DELETE REQUEST?     YL02912
         BNE   CKUCBS                   NO, GO POST UCBS.        YM3475
EXIT     EQU   *                                                YL02912
         BAL   R9,FREEPARM              GO FREE PARM LIST.      YL02912
         SR    R15,R15                  SET RETURN CODE ZERO.   YL02912
EXIT2    EQU   *                                                YL02912
         L     R14,RBEXSAVE+K36         GET RETURN ADDRESS.     YL02912
         MODESET EXTKEY=SUPR                                    SJ0054
         BR    R14                      RETURN.                 YL02912
.X227102 ANOP                                                    YM3475
         SPACE
CKUCBS   EQU   *
         MODESET EXTKEY=SUPR                                    YL02912
         ST    GR11,RBEXSAVE+K16       SAVE PARM POINTER.       YL02912
         LR    GR1,GR5                 ADDRESS OF EXTENTED SAVE AREA.
         LA    GR4,XCTL1               ADDRESS OF XCTL LIST.
         B     NEXTLOAD                GO BRING IN NEXT LOAD.
         SPACE 2
         USING IOBLOCKS,R2
FREEPARM EQU   *                                                YL02912
         LA    R3,ENDLIST-PARMLIST      SIZE OF PARM LIST.      YL02912
         LR    R1,R11                   ADDRESS OF PARM LIST.   YL02912
FREECORE EQU   *                       RELEASE THE CORE HERE.
         FREEMAIN R,LV=(3),A=(1),SP=229 FREE THE WORK AREA.     YL02912
         BR    R9                      RETURN.
         EJECT
***********************************************************************
*                                                                     *
*   ASK FOR PERMISSION TO INITIALIZE THIS VOLUME HERE.
*                                                                     *
***********************************************************************
NEWPACK  EQU   *                       BUILD A DEB FOR A NEW VOLUME.
         LA    R2,WTORP1-MSG           SIZE OF GETMAIN AREA.
         GETMAIN R,LV=(2),SP=229       CORE FOR MESSAGE AREA.   YL02912
         LR    R2,R1                   SAVE ADDRESS OF WORKAREA.
         USING MSG,R2
TRYAGAIN EQU   *
         XC    MSG(WTORP1-MSG),MSG     CLEAR AREA TO ZERO.
         MVC   MSG(WTOREND-WTORP),WTORP PLACE MESSAGE IN WORKAREA.
         SPACE
         USING UCB,R6
         L     R6,UCBPTR                UCB ADDRESS.            YL02912
         AIF  ('&LIB' EQ 'LIB2').X301001                        XL03912
         CLI   UCBID,MAINUCB           THIS A 2321.
         BNE   W2321                   YES-GO PROCESS.
         SPACE
FIXMSG   EQU   *
.X301001 ANOP                                                   XL03912
         MODESET EXTKEY=SUPR                                    YL02912
         NI    UCBFL2,X'FF'-X'40'  *** TURN-OFF NOT READY BUT.  *******
         MODESET EXTKEY=DATAMGT                                 YL02912
         MVC   MSG6(3),UCBNAME         EBCDIC NAME TO MESSAGE.
         LA    GR4,INITIAL                                         O122
         CLI   DCBPTR,OFFLINE          OFFLINE LABEL REQUEST?   YL02912
         BNE   FIXMSGA                 NO-ANALYZE-ALL SET.         O122
         LA    GR4,LABELL              YES-POINT TO LABEL WORD.@Z30RSAG
FIXMSGA  MVC   MSG5+34(10),0(GR4)      FINISH SETTING UP MESSAGE.  O122
         SPACE
         LA    R4,REPLY                REPLY ADDRESS.
         LA    R3,WECB                 ECB ADDRESS FOR WTOR.
         LR    R1,R2                   INSURE ADDRESS IS SET FOR LIST.
         WTOR  ,(4),1,(3),MF=(E,(1))   ASK FOR PERMISSION TO INITIALIZE
*                                                                     *
         WAIT  ECB=WECB                AWAIT FOR REPLY.
         CLI   REPLY,C'U'              DOES REPLY INDICATE 'USE'.
         BE    USE                     YES-GO BUILD A DEB.
         CLI   REPLY,C'T'              DOES REPLY INDICATE TERMINATE.
         BE    NOUSE                   YES--EXIT NOW.
         MVC   MSG(WTOEND-WTO),WTO     MESSAGE TO BUFFER.
         LR    R1,R2                   ISURURE LIST ADDRESS IS SET.
         WTO   ,MF=(E,(1))             TELL OPERATOR ABUOT ERROR.
         B     TRYAGAIN                REPEAT THE REQUEST.
*                                                                     *
NOUSE    EQU   *                       DEB BUILDING TERMINATED.
         LA    R3,WTORP1-MSG           LENGTH OF WORK AREA.
         LR    R1,R2                   ADDRESS OF WORKAREA.
         BAL   GR9,FREECORE            GO FREE THE WORKAREA.
         BAL   R9,FREEPARM              GO FREE PARM LIST.      YL02912
         LA    R15,8                   SET RETURN CODE.
         B     EXIT2                    RETURN TO CALLER.       YL02912
         EJECT
OFFLENQ  EQU   *                                                YL02912
         LA    GR4,SYSQ                 ADDR OF QNAME HDR       YL02912
         LA    GR6,Q4                   ADDR OF RNAME ID        YL02912
         L     GR7,TCBOTC               GET MOTHER' TCB ADDR    YM03804
         MVC   ENQLIST(SYSQ-ENQLISTF),ENQLISTF MOVE LIST TO W/A YM03804
*
         ENQ   ((GR4),(GR6),E,2,SYSTEM),RET=USE,TCB=(GR7),             *
               MF=(E,ENQLIST)
*
         L     GR1,UCBPTR               DEQ'ED UCB ADDR         YL02912
         USING UCB,GR1
         TM    UCBSTAT,UCBONLI          THIS UCB STILL OFFLINE? YL02912
         BO    ERR                      NO, DEQ AND LEAVE.      YL02912
         MODESET EXTKEY=SUPR                                    YL02912
         OI    UCBFL5,UCBNALOC          SET FLAG FOR DEV ALLOC  YL02912
*                                       AND VARY PROCESSOR.     YL02912
         MODESET EXTKEY=DATAMGT                                 YL02912
DEQU     EQU   *                                                YL02912
         DEQ   ((GR4),(GR6),2,SYSTEM),RET=HAVE,TCB=(GR7),       YL02912*
               MF=(E,ENQLIST)
*
         BR    GR9                      EXIT                    YL02912
ERR      EQU   *                                                YL02912
         LA    GR9,ERREXIT              SVC RETURN CODE SET     YL02912
         B     DEQU                     DEQUEUE FROM SYSQ HDR   YL02912
ERREXIT  EQU   *                                                YL02912
         BAL   R9,FREEPARM              GO FREE PARM LIST.      YL02912
         LA    GR15,NOGO                SETUP SVC RETURN CODE   YL02912
         B     EXIT2                    EXIT TO DASDS           YL02912
         EJECT                                                  YL02912
         AIF  ('&LIB' EQ 'LIB2').X301002                        XL03912
         USING DATACELL,GR6
W2321    MVC   MSG6+4(1),DCELBBNR+1    BIN NO. TO MESSAGE.
         OI    MSG6+4,X'F0'            SET ZONE BITS.
         MVI   MSG6+3,SLASH            BIN SEPARATOR.
         BAL   GR14,CONV2321           FIND MAIN UCB ADDRESS.
         B     FIXMSG                  GO COMPLETE THE MESSAGE.
.X301002 ANOP                                                   XL03912
***********************************************************************
*                                                                     *
*   BUILD A DEB FOR A NEW VOLUME HERE.                                *
*                                                                     *
***********************************************************************
USE      EQU   *                       OKAY TO BUILD A DEB.
         LA    R3,WTORP1-MSG           LENGTH OF WORK AREA.
         LR    R1,R2                   ADDRESS OF WORKAREA.
         BAL   GR9,FREECORE            GO FREE THE WORKAREA.
         L     R3,DCBPTR                DCB ADDRESS.            YL02912
         LA    R2,DEBEND-MYDEB         SIZE OF DEB.
         USING MYDEB,GR1                                         YM4604
         GETMAIN R,LV=(2),SP=230                                 YM4604
         LR    R2,R1                   SAVE AREA ADDRESS.        YM4604
         XC    MYDEB(DEBEND-MYDEB),MYDEB  CLEAR DEB TO ZER.      YM4604
         BAL   GR9,OFFLENQ              ENQ FOR OFFLINE UCB     YL02912
         BAL   R9,BUILDDEB             GO BUILD A DEB.
         AIF   ('&LIB' EQ 'LIB2').WM007
         ST    R4,44(R3)                DEB ADDRESS TO DCB
.WM007   ANOP
         USING IOBLOCKS,GR2
         L     R4,DEBAPPAD-1           ADDRESS OF APPENDAGE TABL5#
         MVC   DEBEOEA(20),0(R4)       PLACE IN DEB.
         LA    R4,DEBEOEA              ADDRESS OF NEW TABLE.
         ST    R4,DEBAPPAD-1           SAVE ADDRESS IN DEB.
         MVZ   DEBDEBID(L1),TCBPKF      PROTECT KEY TO DEB.     YL02912
         CLI   FUNCTION,POSTSPEC        OFFLINE LABEL REQUEST?  YL02912
         BE    EXIT                     YES, NOT NEED APPENDAGE.YL02912
         EJECT
***********************************************************************
*                                                                     *
*   LOAD IN THE ABNORMAL END APPENDAGE HERE.                          *
*                                                                     *
***********************************************************************
         USING CVT,1
         L     GR1,CVTPTR              CVT ADDRESS.
         L     GR4,CVTSVDCB            DCB ADDRESS.
         LOAD  EP=IGG019P9,DCB=(4)
         ST    R0,DEBXCEA              PLACE ADDRESS IN DEB.
         AIF   ('&LIB' EQ 'LIB1').WM001
         SRL   GR1,7(0)                SAVE SIZE OF MODULE       M4873
         LA    GR1,1(GR1)              IN 2K BYTES IN            M4873
         STC   GR1,DEBXCEA             AVT TABLE                 M4873
         LOAD  EP=IGG019P7,DCB=(4)     LOAD PAGE FIX APPENDAGE   M5431
         ST    R0,DEBSIOA              PLACE ADDRESS IN DEB       M5431
         SRL   GR1,7(0)                SAVE SIZE OF MODULE        M5431
         LA    GR1,1(GR1)              IN 2K BYTES IN             M5431
         STC   GR1,DEBSIOA             AVT TABLE                  M5431
         OI    DEBSIOA,PGFIXBIT        SHOW APPENDAGE IS PAGE FIX M5431
.WM001   ANOP
         B     EXIT                     RETURN TO CALLER.       YL02912
         DROP  2
         EJECT
***********************************************************************
*                                                                     *
*   ASSIGN ALTERNATE TRACKS HERE.                                     *
*        1)    GETALT REQUEST(MUST READ IN FORMAT4 DSCB)              *
*        2)    ANALYZE/FORMAT REQUEST.                                *
*                                                                     *
***********************************************************************
         SPACE 3
ASSGALT  EQU   *                       GETALT/DYNAMIC TRACK ASSIGN.
         LA    R2,IOEND-MYDEB          SIZE OF GETMAIN AREA.
         LR    R4,R2                   SIZE OF AREA.             YM6524
         BAL   R9,GETMAIN              OBTAIN A WORK AREA.       YM6524
         USING IOBLOCKS,R2                                       YM6524
         XC    IOEND1(IOEND-IOEND1),IOEND1    CLEAR EXTENSION.   YM6524
         B     CLEAR                   GO TO CLEAR REST.         YM6524
         SPACE 3
         USING MYDEB,GR1                                         YM6524
GETMAIN  EQU   *                                                 YM6524
         GETMAIN R,LV=(2),SP=229                                 YM6524
         LR    R2,R1                   SAVE AREA ADDRESS.        YM6524
         XC    MYDEB(DEBEND-MYDEB),MYDEB  CLEAR DEB TO ZER.      YM6524
         BR    R9                      RETURN.                   YM6524
         SPACE 3                                                 YM6524
ASGALT1  EQU   *
         LA    R2,IOEND1-MYDEB         SIZE OF GETMAIN AREA.
         LR    R4,R2                   SIZE OF AREA.
         BAL   R9,GETMAIN              OBTAIN A WORK AREA.       YM6524
CLEAR    STH   R4,SIZE                 REMEMBER THE SIZE.        YM6524
         XC    ECB(IOEND1-ECB),ECB     CLEAR PART OF WORK AREA.  YM6524
         LA    GR3,DCB-44              DCB ADDRESS.
         BAL   R9,BUILDDEB             GO BUILD A DEB.
         ST    R4,DCB                  DEB ADDRESS TO DCB.
         LR    GR3,GR11                PARM LIST POINTER.
         LR    GR4,GR2                 WORKAREA POINTER.
         L     GR7,UCBPTR               ADDRESS OF UCB.         YL02912
         USING UCB,GR7                                           S20201
         MODESET EXTKEY=SUPR                                    YL02912
         CLI   UCBTBYT4,X06             IS THIS A 2305 MOD 1.    S20201
         BE    ASGALT3                  YES                      S20201
         CLI   UCBTBYT4,X07             IS THIS A 2305 MOD 2.    S20201
         BE    ASGALT3                                           S20201
         STM   GR3,GR5,RBEXSAVE+K16     STORE 2ND LOAD LIST.    YL02912
         LA    GR1,RBEXSAVE+K16         LIST ADDR FOR 2ND LOAD. YL02912
         LA    GR4,XCTL                XCTL LIST FOR NEXT LOAD.
         B     NEXTLOAD                                          S20201
ASGALT3  EQU   *                                                 S20201
         DROP  GR7                                               S20201
         STM   GR3,GR5,RBEXSAVE+K16     STORE 4TH LOAD LIST.    YL02912
         LA    GR1,RBEXSAVE+K16         LIST ADDR FOR 4TH LOAD. YL02912
         LA    GR4,XCTL2                                         S20201
         SPACE 2
NEXTLOAD EQU   *                       MUST BRING IN THE NEXT LOAD.
         MVC   RBEXSAVE(XCTLEND-XCTL),E0(GR4)  GET XCTL LIST.   YL02912
         LA    GR15,RBEXSAVE            ADDR OF EXTENDED AREA.  YL02912
         LA    GR6,8(GR15)             ADDRESS OF ENTRY POINT.
         ST    GR6,0(GR15)             STORE IN LIST LIST.
         MODESET EXTKEY=DATAMGT                                  YM1315
         XCTL  ,MF=(E,(1)),SF=(E,(15))                          YM03870
         EJECT
***********************************************************************
*                                                                     *
*   BUILD A DEB HERE.                                                 *
*        R3 CONTAINS THE DCB ADDRESS UPON INPUT.
*        R4 CONTAINS THE DEB ADDRESS UPON EXIT.
*        R9 IS THE RETURN REGISTER.
*                                                                     *
***********************************************************************
BUILDDEB EQU   *
         MVI   DEBNMEXT,1              SET NO. OF EXTENTS.
         MVI   DEBENDCC,X'7F'          SET UPPER EXTENT LIMIT.
         MVI   DEBNMTRK,X'7F'          SET NO. OF TRACKS.
         ST    R3,DEBDCBAD-1           DCB ADDRESS TO DEB.
         MVI   DEBDEBID,X'0F'          PROTECT KEY//ID.
         AIF  ('&LIB' EQ 'LIB2').X301003                        XL03912
         L     GR4,UCBPTR               ADDRESS OF UCB.         YL02912
         USING UCB,GR4
         CLI   UCBTBYT4,X06             IS THIS A 2305-1.        S20201
         BE    SW2305                   SET 2305 SWITCH.         S20201
         CLI   UCBTBYT4,X07             IS THIS A 2305-2.        S20201
         BNE   CHK2321                  NO-CHECK FOR 2321        S20201
SW2305   EQU   *                                                 S20201
         OI    DEVTYP,MAINUCB           YES-SET DEVICE BYTE.     S20201
         B     BUILD                    CONTINUE PROCESS         S20201
CHK2321  EQU   *                                                 S20201
         CLI   UCBID,MAINUCB           THIS A 2321.
         DROP  GR4
         BNE   DEB2321                 YES-GO PROCESS
BUILD    EQU   *                        *                        S20201
.X301003 ANOP                                                   XL03912
         MVC   DEBUCBAD(L3),UCBPTR+D1   UCB ADDRESS TO DEB.     YL02912
BUILD1   MVI   DEBDVMOD,X'C0'          SET THE FILE MASK.
         L     R4,CVTPTR               ADDRESS OF CVT.
         USING CVT,R4
         L     R4,CVTXAPG              APPENDAGE TABLE ADDRESS.
         DROP  R4
         ST    R4,DEBAPPAD-1           STORE IN DEB.
         LA    R4,DEB                  DEB ADDRESS.
         AIF   ('&LIB' EQ 'LIB1').WM006
         CLI   FUNCTION,NEWVOL          NEW VOLUME REQUEST?     YL02912
         BNE   NOCHK4                   NO, DEB WILL NOT BE      Y01021
*                                       VALIDATED                Y01021
         MODESET KEYADDR=KEY,WORKREG=15 GET CALLERS KEY.        YL02912
         ST    R4,E44(R3)               DEB ADDR TO DCB          Y01021
         MODESET EXTKEY=DATAMGT                                 YL02912
         EJECT
**********************************************************************
*        PLACE DEB ON TCB DEB CHAIN                                  *
**********************************************************************
         SPACE
         USING DEB,GR4                                          YL02912
         ST    R8,DEBTCBAD-D1           PUT TCB PTR IN DEB      YL02912
         L     R7,TCBDEB                GET DEB PTR FROM TCB    YL02912
         ST    R7,DEBDEBAD-D1           DEB CHAIN PTR TO MY DEB.YL02912
         MODESET EXTKEY=SUPR                                    YL02912
         ST    R4,TCBDEB                CHAIN MY DEB.           YL02912
         MODESET EXTKEY=DATAMGT                                 YL02912
         DEBCHK (R3),TYPE=ADD,AM=EXCP                            YM0960
         LR    GR1,GR11                 RESTORE PARM POINTER     YM2646
NOCHK4   EQU   *                                                 Y01021
.WM006   ANOP
         BR    R9                      RETURN.
         AIF  ('&LIB' EQ 'LIB2').X301004                        XL03912
         SPACE
         USING DATACELL,GR6
DEB2321  LR    GR6,GR4                 SUB-UCB ADDRESS.
         MVC   DEBBINNO(2),DCELBBNR    BIN NO. TO DEB.
         BAL   GR14,CONV2321           CONVERT SUB TO MAIN UCB ADDRESS.
         ST    GR6,DEBUCBAD-1          MAIN UCB ADDRESS TO DEB.
         B     BUILD1                  GO FINISH THE DEB.
         DROP  6
         SPACE 3
CONV2321 LH    GR10,0(GR6)             PICK UP BIN NO.
         SLA   GR10,4                  TIMES SIXTEEN.
         LA    GR10,DATACELL-UCBOB(GR10)  ADD LENGTH OF MAIN.
         LCR   GR10,GR10               NEGATE.
         AR    GR6,GR10                ADDRESS OF MAIN.
         BR    GR14                    RETURN.
.X301004 ANOP                                                   XL03912
         EJECT
         AIF   ('&LIB' EQ 'LIB1').WM005
.WM005   ANOP
         DROP  R2
XCTL     XCTL  EP=IGC0108B,SF=L        LIST FORM .
XCTLEND  DS    0C                      END OF XCTL LIST.
XCTL1    XCTL  EP=IGC0208B,SF=L        LIST FORM.  2ND LOAD.
XCTL2    XCTL  EP=IGC0308B,SF=L         LIST FORM. 3RD LOAD.     S20201
XCTL3    XCTL  EP=IGC0408B,SF=L         LIST FORM. SS/1 LOAD.  @Y30LSFY
DEVTYP   DC    X'00'                    DEVICE TYPE SWITCH.      S20201
K16      EQU   16                       DISPLACEMENT CONSTANT    S20201
         SPACE 1
WTORP    WTOR  'IEH841D        CONFIRM REQUEST TO           ',0,1, O122X
               0,ROUTCDE=(4),DESC=(2),MF=L                         MC0I
WTOREND  DS    0C                      END OF WTOR.
         SPACE
WTO      WTO   'IEH808I REPLY IN ERROR. REPLY WITH U OR T',        MC0IX
               ROUTCDE=(4),DESC=(5),MF=L                           MC0I
WTOEND   DS    0C
DEBLEN   DS    0F                                                A34646
         DC    X'E5'                    SUB POOL NUMBER.        YL02912
         DC    AL3(DEBEND-MYDEB)        LENGTH OF DEB FOR        Y01021
*                                       VALIDATION               Y01021
MAINT    DS    10F                      MAINTENANCE AREA        XL02912
         AIF   ('&LIB' EQ 'LIB1').X227904                        YM3475
BLANKS   DC    X'40404040'              OFFLINE DDNAME END.      YM3475
.X227904 ANOP                                                    YM3475
SS1CONV  EQU   X'01'                   INDIC SS1 CONVERSION    @Y30LSFY
LABELL   DC    C'LABEL     '           USED FRO LABEL OFFLINE.     O122
INITIAL  DC    C'INITIALIZE'           USED FOR OFFLINE DASDI.     O122
ENQLISTF ENQ   (QHDR,,E,6,SYSTEM),TCB=,RET=USE,MF=L             YM03804
SYSQ     DC    C'SYSIEFSD'              ENQ/DEQ QNAME HEADER    YL02912
QHDR     DC    C'SYSZVOLS'             ONLINE ENQ HEADER        YM03804
Q4       DC    C'Q4'                    ENQ/DEQ RNAME HEADER    YL02912
NOGO     EQU   8                        ENQ FAILED RETURN CODE. YL02912
         SPACE
SENSCCW  CCW   4,SENSCCW,X'20',6        SENSE CMD               XL03130
MAINTNC  DS    50D                      MAINTENANCE AREA        XL03130
GOODRT   EQU   X'7F'                    GOOD ECB RETURN CODE    XL03130
MASK     EQU   X'FF'                    'AND' MASK              XL03130
TCBKOFST EQU   28                       OFFSET TO TCB KEY       XL03130
MOD1     EQU   X'01'                    MODEL ONE SENSE VAL     XL03130
MOD2     EQU   X'02'                    MODEL TWO SENSE VAL     XL03130
L1       EQU   1                                                XL03130
L4       EQU   4                                                XL03130
L2       EQU   2                                                XL03130
DEBID    EQU   X'0F'                    DEB ID                  XL03130
L7       EQU   7                        OFFSET TO USERS' R.C.   XL03130
SENSEOP  EQU   X'04'                    SENSE OP CODE           XL03130
L88      EQU   88                       LENGTH OF EXCP CTL BLKS XL03130
BUMP     EQU   X'28'                    ADDR ADJUSTMENT         XL03130
CTLBLKS  DSECT                                                  XL03130
DCBORG   DS    7F                       DCB                     XL03130
DEBORG   DS    3F                       DEB                     XL03130
ECB1     DS    1F                       ECB                     XL03130
DCBDEB   DS    A                        DCB+2C                  XL03130
DEBPURG  DS    F                        DEB+14                  XL03130
DEBDCB   DS    A                        DEB+18                  XL03130
DEBAPP   DS    F                        DEB+1C                  XL03130
DEBUCB   DS    A                        DEB+20                  XL03130
DEBSKA   DS    2F                       DEB                     XL03130
IOB1     DS    F                        IOB                     XL03130
IOBECB   DS    A                        IOB+4                   XL03130
         DS    2F                       FILL                    XL03130
IOBCCW   DS    A                        IOB+10                  XL03130
IOBDCB   DS    A                        IOB+14                  XL03130
SENSE    DS    2F                       INPUT BUFFER            XL03130
IOBSKA   DS    2F                       IOB+20                  XL03130
CCW      DS    D                        SENSE CMD               XL03130
MSG      DSECT
         DS    0D
MSG1     DS    CL1                     REPLY LENGTH.
MSG2     DS    CL3                     REPLY ADDRESS.
MSG3     DS    CL4                     ECD ADDRESS.
MSG4     DS    CL2                     MESSAGE LENGTH.
         DS    CL2
MSG5     DS    CL44                    MESSAGE BODY                MC0I
MSG55    DS    CL4                     MCS FLAGS/CODES.            MC0I
MSG6     EQU   MSG5+8                  CUU OR CUU/B                MC0I
WECB     DS    CL4                     ECB FOR WTOR.
REPLY    DS    CL1                     REPLY AREA.
WTORP1   DS    0C
         EJECT
IOBLOCKS DSECT
MYDEB    EQU   *                       DEB DEFINITION.
DEBEOEA  DS    1F                      END-OF-EXTENT.
DEBSIOA  DS    1F                      START I/O.
DEBPCIA  DS    1F                      PCI.
DEBCEA   DS    1F                      CHANNEL END.
DEBXCEA  DS    1F                      ABNORMAL END.
         DS    CL12
DEBLNGTH DS    CL1                     DEB LENGTH(DOUBLE WORDS)
         DS    CL3
DEB      EQU   *                       DEB PROPER
DEBNMSUB DS    CL1                     OPEN SUBROUTINES.
DEBTCBAD DS    CL3                     TCB ADDRESS.
DEBAMLNG DS    CL1                     LENGTH ACCESS METHOD.
DEBDEBAD DS    CL3                     NEXT DEB ADDRESS.
DEBOFLGS DS    CL1                     DATA SET FLAGS.
DEBIRBAD DS    CL3                     IRB ADDRESS.
DEBOPATB DS    CL1                     TYPE OF I/O.
DEBSYSPG DS    CL3                     IOB PURGE ADDRESS.(SYSTEM)
DEBNMEXT DS    CL1                     NO. OF EXTENTS.
DEBUSRPG DS    CL3                     IOB USER PURGE ADDRESS.
DEBPRIOR DS    CL1                     ZERO
DEBECBAD DS    CL3                     PURGE ECB.
DEBDEBID DS    CL1                     PROTECT KEY//ID.
DEBDCBAD DS    CL3                     DCB ADDRESS.
DEBEXSCL DS    CL1                     EXTENT SCALE.
DEBAPPAD DS    CL3                     APPENDAGE VECTOR TABLE.
DEBDVMOD DS    CL1                     FILE MASK.
DEBUCBAD DS    CL3                     UCB ADDRESS.
DEBBINNO DS    CL2                     BIN NO. FOR 2321.
DEBSTRCC DS    CL2                     CYLINDER START.
DEBSTRHH DS    CL2                     HEAD START.
DEBENDCC DS    CL2                     CYLINDER END.
DEBENDHH DS    CL2                     HEAD END.
DEBNMTRK DS    CL2                     NUMBER OF TRACKS.
         DS    CL20
DEBEND   DS    0C                      END OF DEB.
         EJECT
ECB      DS    1F                      ECB HERE.
IOB      EQU   *                       IOB HERE.
IOBFLAG1 DS    CL1                     FLAG1
IOBFLAG2 DS    CL1                     FLAG2.
IOBSENS0 DS    CL1                     SENSE BYTE ZERO.
IOBSENS1 DS    CL1                     SENSE BYTE ONE.
IOBECBAD DS    CL4                     ADDRESS OF ECB.
IOBCSW   DS    CL8                     STATUS WORDS.
IOBSTART DS    CL4                     CHANNEL PROGRAM ADDRESS.
IOBDCBPT DS    CL4                     ADDRESS OF DCB.
IOBRESTR DS    CL4                     RESTART ADDRESS.
IOBINCAM DS    CL2                     BLOCK COUNT.
IOBERRCT DS    CL2                     ERROR COUNT.
IOBSEEK  DS    CL8                     SEEK ADDRESS.
CCHHR    EQU   IOBSEEK+3               SEARCH ADDRESS.
DCB      DS    1F                      DEB ADDRESS.
ALTINFO  DS    1D                      KEEP ALTERNATE TRACK INFO HERE.
         AIF  ('&LIB' EQ 'LIB1').X302300                        XL03130
SCHHA    DS    1D                      SEARCH HOME ADDR.        XL03130
TICSRCH  DS    1D                      TIC ON SEARCH.           XL03130
.X302300 ANOP                                                   XL03130
WRTHA    DS    1D                      WRITE HOME ADDRESS.
WRTR0    DS    1D                      WRITE RECORD ZERO.
R0DATA   DS    1D                                              @Z30RSAG
RDHA     DS    1D                      READ HOME ADDRESS.
READR0   DS    1D                      READ RECORD ZERO.
         AIF   ('&LIB' EQ 'LIB1').X227800                       XL03130
         DS    CL6                      RESERVED.               XL03130
MADSD    DS    CL4                  3350 ADDITIONAL SD BYTES   @X50RSPC
WINHA    DS    CL2                      3340 HOME ADDRESS.      XL03130
.X227800 ANOP                                                   XL03130
HA       DS    CL5                     HOME ADDRESS.
SW       DS    CL1                     SWITCH.
D2314    EQU   X'80'                   2314 DEVICE.
D2321    EQU   X'40'                   2321 DEVICE.
SIZE     DS    CL2                     SIZE OF GETMAIN AREA.
R0COUNT  DS    1D                      RECORD ZERO COUNT FIELD.
         DS    1D                      RECORD ZERO DATA  FIELD.@X50RSPC
         AIF   ('&LIB' EQ 'LIB1').X322203                        XM4405
SNSBYTES DS    CL24                    3340 SENSE BYTE AREA.     XM4405
.X322203 ANOP                                                    XM4405
RETSW    DS    CL1                     RETRY SW USED IN 108B   @ZA04363
IOEND1   DS    0C                      END OF BASIC I/O BLOCKS.
*   EXTENSION FOR GETALT//DYNAMIC TRACK REQUEST.
SEARCH   DS    1D                      SEARCH CCW.
TIC      DS    1D                      REPEAT UNTIL FOUND.
WRTRD    DS    1D                      WRITE OR READ DATA.
FORMAT4  DS    CL96                    FORMAT4 DSCB READ IN AREA.
VTOCCCHH EQU   FORMAT4+63              CCHH START OF VTOC.
IOEND    DS    0C
         EJECT
***********************************************************************
*                                                                     *
*   PARAMETER LIST DSECT                                              *
*                                                                     *
         IEHDBLKS                                              @Z30RSAG
***********************************************************************
         SPACE 2
PARMLIST DSECT                                                  YL02912
FUNCTION DS    0F                       CALLER'S FUNCTION.      YL02912
UCBPTR   DS    F                        UCB POINTER.            YL02912
DCBPTR   DS    F                        DCB POINTER OR          YL02912
CCHHPARM EQU   DCBPTR                   ADDR OF TRACK OR        YL02912
SERPTR   EQU   DCBPTR                   ADDR OF VOL SER.        YL02912
WINMOD   EQU   DCBPTR+3                 3340 MODEL NO.          YL02912
SENSFB   EQU   DCBPTR                   FUNCTION BLK ADDR      @Z30RSAG
ALTPTR   DS    F                        ADDR OF ALTINFO OR      YL02912
NEWCCHH  EQU   ALTPTR                   NEW TRACK ADDRESS.      YL02912
DEBPTR   DS    F                        DEB POINTER OR          YL02912
DEVMODP  EQU   DEBPTR                   ADDR OF 3340 MODEL NO.  YL02912
PRMPTR   DS    F                        ADDR OF CALLER'S LIST.  YL02912
KEY      EQU   PRMPTR                   KEY OF CALLER.          YL02912
EDQLIST  DS    4F                       ENQ/DEQ PARAM LIST      SJ0054
ENQLIST  EQU   EDQLIST+4                                        SJ0054
ENDLIST  EQU   *                        PARAMETER LIST END.     YL02912
         EJECT
CVT      DSECT
         CVT   SYS=MIN
         IKJTCB LIST=YES
         EJECT
         IHARB DSECT=YES
         EJECT
UCB      DSECT
         IEFUCBOB
         END
