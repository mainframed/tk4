         TITLE 'IGC0208B --- UPDATES UCBS FOR IEHDASDR UTILITY'
         COPY  LCGASMSW                                          SM4351
IGC0208B CSECT
***********************************************************************
*                  FIXES THIS MODULE                                  *
*                    LATEST FIRST                                     *
***********************************************************************
*
*A 240020                     MVS 037 ONLY                     @ZA31271
*
*C 185000                           @YA16250=@XA19110=@ZA24230=@SA79732
*A 534000-534500                    @YA16250=@XA19110=@ZA24230=@SA79732
*
*C 186000,490000,576000,580000               @XA14188=@YA13317=@ZA07401
*A 184100-185000,572500-573700,592100        @XA14188=@YA13317=@ZA07401
*
*C193000                                                        @30AAAG
*A196500,197000,198500,199000,500,600                           @30AAAG
*
*STATUS- CHANGE LEVEL 000                                             *
*                                                                     *
***********************************************************************
         EJECT
*FUNCTION/OPERATION-  THIS LOAD OF SVC 82 -POSTS- THE UCBS FOR        *
*   DIRECT ACCESS VOLUMES THAT ARE WRITTEN ON BY THE -IEHDASDR-       *
*   UTILITY PROGRAM. IT DOES SO AS FOLLOWS.                           *
*                                                                     *
*        -IF DUPLICATE SERIAL NUMBERS ARE ENCOUNTERED ;         SA55451
*         1) SVC 91 IS ISSUED,                                  SA55451
*         2) VOLUME SERIAL NO. IN THE UCB IS CLEARED,           SA55451
*         3) NOT-READY BIT IN UCB IS SET,                       SA55451
*         4) A DEMOUNT MESSAGE IS SENT TO THE OPERATOR.         SA55451
*                                                                     *
*        -IF A NEW/UNIQUE SERIAL NO. RESULTS,ISSUE A -NEW- MESSAGE    *
*         TO THE OPERATOR. PLACE THE SERIAL NO. IN THE UCB.           *
*                                                                     *
*        -IF A NEW VTOC HAS BEEN PLACED ON THIS VOLUME, PUT ITS       *
*         TTR IN THE UCB.                                             *
*                                                                     *
*ENTRY POINTS- THE ONLY ENTRY POINT IS -IGC0208B-.                    *
*                                                                     *
*INPUT-  REGISTER 1 POINTS TO THE SVRB.                               *
*   THE EXTENDED AREA OF THE SVRB CONTAINS THE INPUT PARM POINTER     *
*   THAT WAS PASSED TO -IGC0008B-.                                    *
*                                                                     *
*EXITS-NORMAL- VIA A SVC 3 TO THE CALLING ROUTINE.                    *
*                                                                     *
*EXITS-ERROR-  NONE POSSIBLE.                                         *
*                                                                     *
*SUPERVISOR MACROS-  GETMAIN,FREEMAIN,WTO.                            *
*                                                                     *
*OUTPUT-  IF NEW VTOC, ITS TTR IS PLACED IN THE UCB.                  *
*         IF DUPLICATE SERIAL NO., A RETAIN MESSAGE IS ISSUED.        *
*         IF NEW SERIAL NO., A NEW MESSAGE IS ISSUED.                 *
*                                                                     *
*ATTRIBUTES-  REENTRANT,RELOCATABLE,PRIVILEGED,ENABLED.               *
*                                                                     *
***********************************************************************
         EJECT
*   THE FOLLOWING ARE REGISTER ASSIGNMENTS.
PARMPTR  EQU   1                       POINTER TO INPUT PARAMETERS.
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5                                                 YM1180
R6       EQU   6
R9       EQU   9
R11      EQU   11                      POINTER TO INPUT PARAMETERS.
R14      EQU   14                      LINK REGISTER.
R15      EQU   15
GR1      EQU   1
GR2      EQU   2                       INDEX TO UCB TABLE ENTRIES.
GR3      EQU   3                       POINTER TO TABLE UCB.
GR4      EQU   4                       POINTER TO NEW SERIAL.
GR5      EQU   5                       POINTER TO TABLE UCB  SERIAL.
GR6      EQU   6                       POINTER TO INPUT UCB.
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10
GR11     EQU   11
GR12     EQU   12
GR14     EQU   14                      LINK REGISTER.
BASEREG  EQU   12                      BASE REGISTER.
GR15     EQU   15
         SPACE
DYNALT   EQU   X'1F'                   GETALT ASSIGNMENT.
NEWVOL   EQU   X'8F'                   NEW VOLUME INPUT.
COMMA    EQU   C','                    EBCDIC COMMA.
WSIZE    EQU   64                      SIZE OF MESSAGE AREA.
WSIZE1   EQU   WSIZE-12                MESSAGE LENGTH.
RBEXSAVE EQU   96                      EXTENTED SAVE AREA FOR SVRB.
NOTREADY EQU   X'40'                   UCB NOT READY BIT.
K36      EQU   36                      DISPLACEMENT CONSTANT.   YL02912
D0       EQU   0                       DISPLACEMENT OF ZERO.     YM1129
D1       EQU   1                       DISPLACEMENT OF ONE      YL02912
L3       EQU   3                       LENGTH OF THREE.         YL02912
L6       EQU   6                       LENGTH OF SIX.           YL02912
         SPACE
POST     EQU   X'08'                   POST INDICATOR.
         EJECT
         LR    BASEREG,GR15            SET UP ADDRESSING.
         USING IGC0208B,BASEREG
         SPACE
         SPACE 2
*   POST THE UCBS HERE.
CKUCBS   EQU   *
         L     GR11,RBEXSAVE+16(GR1)   PARM LIST POINTER.
         USING PARMLIST,GR11                                    YL02912
         LR    GR9,GR1                 SVRB ADDRESS.
         USING CVT,GR2
         B     APARNO                  B AROUND APARNO         @ZA07401
         DC    C'IGC0208B'             MODULE IDENTIFIER       @ZA07401
         DC    C'OZ31271'              LAST FIX IN THIS MODULE @ZA31271
APARNO   L     GR2,CVTPTR              ADDRESS OF CVT.         @ZA07401
         L     GR2,CVTILK2             UCB ADDRESS TABLE.
         DROP  GR2
STARTEST EQU   *                                                   O122
         L     GR4,SERPTR               POINTER TO NEW SERIAL.  YL02912
         L     GR6,UCBPTR               POINTER TO UCB.         YL02912
         LA    GR6,0(GR6)              CLEAR HIGH ORDER BYTE.      O122
         SR    GR3,GR3                 CLEAR REGISTER 3.       @ZA31271
         ICM   GR3,3,0(GR2)            GET UCB ADDR            @30AAAG
         LTR   GR3,GR3                 VALID ENTRY.
         BZ    UPUCBPTR                NO--GO SEE IF MORE ENTRIES.
*        THE FOLLOWING CODE HANDLES 16-BIT UCB ADDRESSES.      @30AAAG
         BP    NOT16                   NOT 16-BIT UCB ADDR     @30AAAG
         USING UCB,GR3
         N     GR3,BITMASK             TURN OFF LEFT HALF WD   @30AAAG
         CLM   GR3,3,UCBLAST           LOOK FOR EOT            @30AAAG
         BE    PRETURN                 BRCH IF END OF TABLE    @30AAAG
NOT16    EQU   *                                               @30AAAG
         TM    UCBTBYT3,UCB3DACC       THIS A DIRECT ACCESS DEVICE.
         BZ    UPUCBPTR                NO--GO SEE IF MORE ENTRIES.
         AIF   ('&LIB' NE 'LIB1').X301201                       XL03912
         CLI   UCBTBYT4,DA2321         THIS A 2321.
         BE    PROC2321                YES-SPECIAL CASE.
.X301201 ANOP                                                   XL03912
         LA    GR5,SRTEVOLI            ADDRESS OF SERIAL NO.
         C     GR5,RBEXSAVE+16(GR9)    THIS UCB TESTED          SA53836
         BE    UPUCBPTR                GO TEST NEXT UCB         SA53836
         BAL   GR7,TESTUCB             GO TEST FOR IDENTICAL/NEW.
         B     UPUCBPTR                GO TEST NEXT UCB.
         AIF   ('&LIB' NE 'LIB1').X301202                       XL03912
         SPACE
PROC2321 LA    GR3,DATACELL            ADDRESS OF 1ST SUB-UCB.
CK2321   LA    GR5,DCELVOLI-DATACELL(GR3) SERIAL NO. ADDRESS.
         BAL   GR7,TESTUCB             GO TEST IDENTICAL//NEW.
         CLI   1(GR3),LASTBIN          THIS THE LAST SUB-UCB.
         BE    UPUCBPTR                YES--GO TEST NEXT UCB.
         LA    GR3,SUBLNG(GR3)         STEP TO NEXT SUB-UCB.
         B     CK2321                  GO CHECK ALL SUB-UCBS.
.X301202 ANOP                                                   XL03912
         DROP  3
         SPACE
UPUCBPTR LA    GR2,2(GR2)              STEP TO NEXT TABLE ENTRY.
         CLC   0(2,GR2),UCBLAST        ANY MORE ENTRIES.
         BNE   STARTEST                YES--KEEP LOOKING.
PRETURN  EQU  *                                                 YL02912
         LA    R2,ENDLIST-PARMLIST      PARM LIST SIZE.         YL02912
         FREEMAIN R,LV=(2),A=(11),SP=229 FREE PARM LIST         YL02912
         SR    R15,R15                  SET RETURN CODE.        YL02912
         L     R14,RBEXSAVE+K36(GR9)    GET RETURN ADDRESS.     YL02912
         MODESET EXTKEY=SUPR                                    YM1315
         BR    R14                      RETURN TO CALLER.       YL02912
         EJECT
TESTUCB  EQU   *
         MODESET KEYADDR=KEY,WORKREG=15 GET CALLER'S KEY.       YL02912
         CLC   D0(L6,GR4),D0(GR5)       SERIAL NUMBERS EQUAL?   YL02912
         MODESET EXTKEY=DATAMGT                                 UL02912
         BE    WTOR                    YES-TELL OPERATOR TO REMOVE VOL.
TESTUCB1 CLR  GR6,GR3                 THIS THE SELECTED UCB/SUB UCB.
         BE    WTON                    YES-TELL ABOUT NEW SERIAL.
         BR    GR7                     NO--RETURN.
         SPACE 2
WTON     EQU   *                       UPDATE UCB HERE//TELL OPERATOR.
         LTR   GR4,GR4                 IS THERE A NEW SERIAL NO.
         BZ    TESTTR                  NO--GO SEE IF TTR NEEDS UPDATE.
         MODESET KEYADDR=KEY,WORKREG=15 GET CALLER'S KEY.       YL02912
         CLC   0(6,GR4),0(GR5)         ARE THE SERIAL NOS. THE SAME.
         MODESET EXTKEY=DATAMGT                                 YL02912
         BE    TESTTR                  YES-NO NEED TO TELL THE USER.
         BAL   GR8,SETUPMSG            PLACE MESSAGE IN BUFFER.
         MVI   12(GR10),C'N'           'NEW' INDICATOR TO MESSAGE.
         BAL   GR8,WRITE               WRITE TO OPERATOR//FREECORE.
         MODESET   EXTKEY=SUPR                                  YL02912
         ST    GR5,RBEXSAVE+16(GR9)    INDICATE UCB TESTED      SA53836
         MODESET   EXTKEY=DATAMGT                               YL02912
         TM    SRTESTAT-UCB(GR6),SRTEONLI  DEVICE ONLINE.          X726
         BZ    TESTTR                  NO-GO PROCESS VTOC.         X726
         MODESET EXTKEY=SUPR                                    YL02912
         MVC   0(6,GR5),0(GR4)         YES-NEW SERIAL NO TO UCB.   X726
         MODESET EXTKEY=DATAMGT                                 YL02912
*    UPDATE TTR OF VTOC HERE.
TESTTR   EQU   *                                                YL02912
         OC    NEWCCHH+D1(L3),NEWCCHH+D1 NEED TTR UPDATE?       YL02912
         BCR   8,GR7                   NO--GO CHECK MORE UCBS.     O122
*     BUILD A DUMMY DEB HERE.
         LA    R3,RBEXSAVE(GR9)        SAVE AREA ADDRESS.
         L     GR8,K36(R3)             SAVE RETURN ADDRESS.     YL02912
         MODESET   EXTKEY=SUPR                                  YL02912
         STM   1,12,0(R3)              SAVE THE REGISTERS.
         MODESET   EXTKEY=DATAMGT                               YL02912
         GETMAIN R,LV=0+DEBEND-DEB,SP=229                       YL02912
         USING DEB,1
         XC    DEB(DEBEND-DEB),DEB     CLEAR CORE TO ZERO.
         MVI   DEBNMEXT,1              SET FOR 1 EXTENT.
         MVI   DEBENDCC,X'7F'          SET UPPER EXTENT LIMIT.
         MVI   DEBNMTRK,X'7F'          SET NO. OF TRACKS.
         MVI   DEBDEBID,X'0F'          PROTECT KEY//DEB ID.
         AIF   ('&LIB' NE 'LIB1').X301203                       XL03912
         L     GR4,0(GR11)             UCB ADDRESS.
         USING UCB,4
         CLI   UCBID,MAINUCB           THIS A 2321.
         BNE   DEB2321                 YES--GO HANDLE.
.X301203 ANOP                                                   XL03912
         MVC   DEBUCBAD(L3),UCBPTR+D1   UCB ADDRESS TO DEB.     YL02912
         AIF   ('&LIB' NE 'LIB1').X301204                       XL03912
         DROP  4
.X301204 ANOP                                                   XL03912
         SPACE
BUILD1   EQU   *
         USING CVT,R15
         L     R15,CVTPTR              ADDRESS OF CVT.
         L     R15,CVTPRLTV            CONVERT ADDRESS//MBBCCHHR TO TTR
         DROP  15
         SPACE
         L     R2,NEWCCHH               VTOC PTR FOR UCB.       YL02912
         MODESET KEYADDR=KEY,WORKREG=14  GET CALLERS KEY         YM1384
         LM    R4,R5,D0(R2)            GET VTOC CCHH.            YM1180
         MODESET EXTKEY=DATAMGT                                  YM1384
         STM   R4,R5,VTOCCCHH          PUT CCHH IN SVC AREA.     YM1180
         LA    R2,VTOCCCHH             TTR PTR FOR CONVERT.      YM1129
         SPACE
         BALR  R14,R15                 CONVERT MBBCCHHR TO TTR.
         SPACE
         LR    R15,R1                  SAVE DEB ADDRESS.
         LM    1,12,0(R3)              RESTORE THE REGISTERS.
         MODESET   EXTKEY=SUPR                                  YL02912
         ST    GR8,K36(R3)             RESTORE RETURN ADDRESS.  YL02912
         MODESET   EXTKEY=DATAMGT                               YL02912
         SPACE
         USING UCB,R3
         L     R3,UCBPTR                GET UCB ADDRESS.        YL02912
         MODESET EXTKEY=SUPR                                    YL02912
         AIF   ('&LIB' NE 'LIB1').X301205                       XL03912
         CLI   UCBID,MAINUCB           THIS A 2321.
         BNE   PR2321                  YES--GO HANDLE.
.X301205 ANOP                                                   XL03912
         ST    R0,SRTEFSCT             TTR OF VTOC.
         MODESET EXTKEY=DATAMGT                                 YL02912
TTREND   EQU   *
         LR    R1,R15                  RESTORE DEB ADDRESS.
         FREEMAIN R,LV=0+DEBEND-DEB,A=(1),SP=229                YL02912
         BR    GR7                     GO CHECK MORE UCBS.         O122
         AIF   ('&LIB' NE 'LIB1').X301206                       XL03912
         SPACE
         USING DATACELL,R3
PR2321   ST    R0,DCELVTOC             TTR OF VTOC TO UCB---2321.
         B     TTREND                  GO EXIT.
         DROP  3
         SPACE 2
DEB2321  LR    GR6,GR4                 UCB ADDRESS.
         BAL   GR14,CONV2321           FIND ADDRESS OF MAIN UCB.
         ST    GR6,DEBUCBAD-1          2321 UCB ADDRESS TO DEB.
         B     BUILD1                  EXIT.
.X301206 ANOP                                                   XL03912
         DROP  1
         SPACE
*   TELL OPERATOR TO REMOVE THIS VOLUME//IDENTICAL SERIAL NUMBERS     *
WTOR     EQU   *
         LTR   GR4,GR4                 WAS THERE A NEW SERIAL NO.
         BZ    TESTUCB1                NO--GO MAKE MORE TESTS.
         USING UCB,3
         AIF   ('&LIB' NE 'LIB1').X301207                       XL03912
         CLI   UCBID,MAINUCB           THIS A SUB-UCB.
         BNE   TESTSUB3                YES--GO CHECK STATUS.
.X301207 ANOP                                                   XL03912
         TM    SRTESTAT,SRTEONLI       IS THE TABLE UCB ON-LINE.
         BCR   8,GR7                   NO--RETURN.
         SPACE
         USING UCB,6
         TM    SRTESTAT,SRTEONLI       THE SUBJECT UCB ON-LINE.
         BC    8,TESTUCB1              NO--GO MAKE MORE TESTS.
         DROP  3,6
TESTIDEN CLR   GR6,GR3                 THIS THE SUBJECT UCB.
         BE    TESTUCB1                YES-GO MAKE MORE TESTS.
         BAL   GR8,SETUPMSG            COMPLETE THE MESSAGE.
         MVI   12(GR10),C'R'           'REMOVE' INDICATOR TO MESSAGE.
         USING UCB,GR6
         AIF   ('&LIB' NE 'LIB1').X301208                       XL03912
         CLI   UCBTBYT4,DA2321         THIS A 2321.
         BE    YES2321                 YES-GO CLEAR SERIAL NO.
.X301208 ANOP                                                   XL03912
         TM    SRTESTAT,SRTEPRES       THIS A NON-DEMOUNTABLE DEVICE.
         BO    SETNR2                  YES-BETTER MARK IT OFF-LINE.
         LNR   R0,R6                    COMPLIMENTED UCB ADDR   SA55451
         SVC   91                       ISSUE VOLSTAT SVC       SA55451
         MODESET EXTKEY=SUPR                                    YL02912
         XC    SRTEVOLI(6),SRTEVOLI     CLEAR SERIAL NO.        SA55451
*   TURN ON NOT-READY BIT HERE.
         OI    UCBFL2,NOTREADY         MAKE UCB NOT-READY.
         MODESET EXTKEY=DATAMGT                                 YL02912
SETNR    EQU   *
         AIF   ('&LIB' NE 'LIB1').X301211                       XL03912
         DROP  GR6
.X301211 ANOP                                                   XL03912
         BAL   GR8,WRITE               TELL OPERATOR TO REMOVE VOLUME.
         B     PRETURN                 POSTING COMPLETE.
         AIF   ('&LIB' NE 'LIB1').X301209                       XL03912
YES2321  L     GR11,0(GR11)            INPUT SUB-UCB ADDRESS.
         USING DATACELL,GR11
         NI    DCELSTAT,X'FF'-SRTEONLI  PUT THIS SUB-UCB OFF-LINE.
         XC    DCELVOLI-DATACELL(6,GR11),DCELVOLI-DATACELL(GR11)
*                                      CLEAR OUT NO. IN SUB-UCB.
.X301209 ANOP                                                   XL03912
SETNR1   MVC   42(12,GR1),OFFMSG       TELL ABOUT OFFLINE.
         MVI   1(GR1),WSIZE            SET SIZE IN MESSAGE.
         B     SETNR                   GO MAKE UCB NOT READY.
         AIF   ('&LIB' NE 'LIB1').X301210                       XL03912
         DROP  11
         SPACE
         USING UCB,3
TESTSUB3 TM    DCELSTAT,SRTEONLI       THIS SUB-UCB ONLINE.
         BCR   8,GR7                   NO--GO MAKE MORE TESTS.
         USING UCB,6
TESTSUB6 TM    DCELSTAT,SRTEONLI       THIS SUB-UCB ONLINE.
         BCR   8,GR7                   NO--GO MAKE MORE TESTS.
         B     TESTIDEN                YES-CONTINUE RETAIN TEST.
.X301210 ANOP                                                   XL03912
SETNR2   EQU  *                                                 YL02912
         MODESET EXTKEY=SUPR                                    YL02912
         NI    SRTESTAT,X'FF'-SRTEONLI NON-DEMOUNTABLE DEVICE OFF-LINE.
         MODESET EXTKEY=DATAMGT
         B     SETNR1                  GO TELL THE USER.
         AIF   ('&LIB' NE 'LIB1').X301212                       XL03912
         DROP  3,6
.X301212 ANOP                                                   XL03912
         EJECT
*   COMPLETE MESSAGE IEH809 HERE.
SETUPMSG EQU   *
         GETMAIN R,LV=4+WSIZE,SP=229   GET WORK AREA.          @ZA07401
         XC    0(WSIZE,GR1),0(GR1)     CLEAR MESSAGE AREA.
         MVI   1(GR1),WSIZE1           SET SIZE IN MESSAGE.
         MVC   4(10,GR1),WTOLIST       MESSAGE IDENTIFIER.
         LR    GR15,GR6                SAVE UCB REG.               O122
         USING UCB,GR6
         AIF   ('&LIB' NE 'LIB1').X301213                       XL03912
         CLI   UCBID,MAINUCB           THIS A MAIN UCB.
         BNE   FINDMAIN                NO--MUST BE 2321.
.X301213 ANOP                                                   XL03912
         LR    GR10,GR1                MESSAGE AREA ADDRESS.
SETNAME  MVC   14(3,GR10),UCBNAME      DEVICE ADDRESS TO MESSAGE.
         DROP  GR6
         LR    GR6,GR15                RESTORE UCB REG.            O122
         MVI   17(GR1),COMMA           INSERT COMMA IN MESSAGE.
         MODESET EXTKEY=SUPR           ALLOW FETCH OF VOLSER     YM4604
         MVC   18(6,GR1),0(GR4)        SERIAL NO. TO MESSAGE.
         MODESET EXTKEY=DATAMGT        RESET MODE FOR DATAMGT    YM4604
         MVI   24(GR1),COMMA           INSERT COMMA IN MESSAGE.
         LR    GR15,GR7                SAVE BR AND LINK REG.       O122
         L     GR7,CVTPTR              CVT ADDRESS.
         USING CVT,GR7
         L     GR7,CVTTCBP             CVT DOUBLE WORD ADDRESS.
         DROP  7
         L     GR7,4(GR7)              TCB ADDRESS.
         L     GR7,12(GR7)             TIOT ADDRESS.
         USING TIOT,GR7
         MVC   25(8,GR1),TIOCNJOB      JOB NAME TO MESSAGE.
         MVI   33(GR1),COMMA           INSERT COMMA IN MESSAGE.
         MVC   34(8,GR1),TIOCSTEP      STEP-PROC NAME TO MSG.  @ZA24230
         CLI   TIOCSTEP+8,X'40'        WAS THE PREV STEPNAME.  @ZA24230
         BE    NOPSTEP                 YES SKIP PROCSTEP HANDL.@ZA24230
         MVI   42(GR1),COMMA           INSERT COMMA IN MESSAGE.@ZA24230
         MVC   42(8,GR1),TIOCSTEP+8    INSERT STEPNAME IN MSG. @ZA24230
NOPSTEP  EQU   *                                               ÖZA24230
         LR    GR7,GR15                RESTORE BR AND LINK REG.    O122
         BR    GR8                     RETURN
         DROP  7
         AIF   ('&LIB' NE 'LIB1').X301214                       XL03912
         EJECT
         SPACE
FINDMAIN MVI   17(GR1),SLASH           BIN SEPARATOR.
         MVC   18(1,GR1),1(GR3)        BIN NO. TO MESSAGE.
         OI    18(GR1),X'F0'           SET ZONE BITS.
         BAL   GR14,CONV2321           CONVERT SUB TO MAIN UCB ADDRESS.
         LR    GR10,GR1                MESSAGE AREA ADDRESS.
         LA    GR1,2(GR1)              RESET 'MOVE TO' POINTER.
         B     SETNAME                 COMPLETE THE MESSAGE.
         SPACE
CONV2321 LH    GR10,0(GR6)             PICK UP BIN NO.
         SLA   GR10,4                  TIMES SIXTEEN.
         LA    GR10,DATACELL-UCBOB(GR10) ADD LENGTH OF MAIN.
         LCR   GR10,GR10               NEGATE.
         AR    GR6,GR10                ADDRESS OF MAIN.
         BR    GR14                    RETURN.
.X301214 ANOP                                                   XL03912
         EJECT
WRITE    EQU   *
         SR    GR1,GR1                 CLEAR REG 1             @ZA07401
         IC    GR1,1(GR10)             GET L'MSG FOR WTO       @ZA07401
         AR    GR1,GR10                ADD A(WTOLIST),=END MSG @ZA07401
         MVC   0(4,GR1),ROUTECD        SET ROUTE & DESC CODE=4 @ZA07401
         OI    2(GR10),X'80'           SET MCS BIT ON          @ZA07401
         LR    GR1,GR10                MESSAGE ADDRESS.
         WTO   ,MF=(E,(1))                                     @ZA07401
         LR    GR1,GR10                RESTORE ADDRESS OF GETMAIN AREA.
         FREEMAIN R,LV=4+WSIZE,A=(1),SP=229                    @ZA07401
         BR    GR8                     RETURN.
         SPACE 2
UCBLAST  DC    X'FFFF'                 LAST ENTRY IN UCB POINTER TABLE.
OFFMSG   DC    C',NOW OFFLINE'         2321 MESSAGE COMPLETION.
         SPACE 2
WTOLIST  DC    C'IEH809I   '           MESSAGE IDENTIFIER.
ROUTECD  DC    X'10001000'             ROUTE AND DESC CODE=4   @ZA07401
MAINT    DS    10F                     MAINTENANCE AREA         XL02912
BITMASK  DC    X'0000FFFF'             ZEROS LEFT HALF OF REG  @30AAAG
         EJECT
         SPACE
TIOT     DSECT
TIOCNJOB DS    CL8                     JOB NAME
TIOCSTEP DS    CL16                    STEP OR PROCEDURE NAME.
DEB      DSECT
DEBNMSUB DS    CL1                     OPEN SUBROUTINES.
DEBTCBAD DS    CL3                     TCB ADDRESS.
DEBAMLNG DS    CL1                     LENGTH ACCESS METHOD.
DEBDEBAD DS    CL3                     NEXT DEB ADDRESS.
DEBOFLGS DS    CL1                     DATA SET FLAGS.
DEBIRBAD DS    CL3                     IRB ADDRESS.
DEBOPATB DS    CL1                     TYPE OF I/O.
DEBSYSPG DS    CL3                     IOB PURGE ADDRESS.(SYSTEM)
DEBNMEXT DS    CL1                     NO. OF EXTENTS.
DEBUSRPG DS    CL3                     IOB USER PURGE ADDRESS.
DEBPRIOR DS    CL1                     ZERO
DEBECBAD DS    CL3                     PURGE ECB.
DEBDEBID DS    CL1                     PROTECT KEY//ID.
DEBDCBAD DS    CL3                     DCB ADDRESS.
DEBEXSCL DS    CL1                     EXTENT SCALE.
DEBAPPAD DS    CL3                     APPENDAGE VECTOR TABLE.
DEBDVMOD DS    CL1                     FILE MASK.
DEBUCBAD DS    CL3                     UCB ADDRESS.
DEBBINNO DS    CL2                     BIN NO. FOR 2321.
DEBSTRCC DS    CL2                     CYLINDER START.
DEBSTRHH DS    CL2                     HEAD START.
DEBENDCC DS    CL2                     CYLINDER END.
DEBENDHH DS    CL2                     HEAD END.
DEBNMTRK DS    CL2                     NUMBER OF TRACKS.
         DS    CL20
DEBEND   DS    0C                      END OF DEB.
         EJECT
***********************************************************************
*                                                                     *
*   PARAMETER LIST DSECT                                              *
*                                                                     *
***********************************************************************
         SPACE 2
PARMLIST DSECT                                                  YL02912
FUNCTION DS    0F                       CALLER'S FUNCTION.      YL02912
UCBPTR   DS    F                        UCB POINTER.            YL02912
DCBPTR   DS    F                        DCB POINTER OR          YL02912
CCHHPARM EQU   DCBPTR                   ADDR OF TRACK OR        YL02912
SERPTR   EQU   DCBPTR                   ADDR OF VOL SER.        YL02912
ALTPTR   DS    F                        ADDR OF ALTINFO OR      YL02912
NEWCCHH  EQU   ALTPTR                   NEW TRACK ADDRESS.      YL02912
DEBPTR   DS    F                        DEB POINTER OR          YL02912
DEVMODP  EQU   DEBPTR                   ADDR OF 3340 MODEL NO.  YL02912
PRMPTR   DS    F                        ADDR OF CALLER'S LIST.  YL02912
KEY      EQU   PRMPTR                   KEY OF CALLER.          YL02912
VTOCCCHH DS    2F                      VTOC PTR.                 YM1129
IGCRESV  DS    2F                      RESERVED.                 YM1129
ENDLIST  EQU   *                        PARAMETER LIST END.     YL02912
         SPACE 2
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
CVT      DSECT
         CVT   SYS=MIN
         END
