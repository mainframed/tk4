         COPY  LCGASMSW
IGG019P8 CSECT
*
*C 330000,670000,690000             @YA09679=@SA74485=@XA10384=@ZA04411
*A 325500-326000,666000-669500      @YA09679=@SA74485=@XA10384=@ZA04411
*
*320020-322440                                                  VS01098
*
*D 516500-519000,519700-519900                                 @ZM33043
*D 510000,520000                    @SA69574=@YA04867=@XA05836=@ZA01207
*A 515500-519900                    @SA69574=@YA04867=@XA05836=@ZA01207
*
*0856                                                            Y01048
*                                                             OX02850
*        OY02647 FIX IS FLAGGED AS OX02850                    OY02647
*        SA65713 FIX IS FLAGGED AS OX02850                    SA65713
*                                                                     *
*1063                                                            M4971
*STATUS CHANGE LEVEL 001  VS2/2  (MVM)                          YL02912
*                                                                     *
*FUNCTION/OPERATION-  THIS ROUTINE MODIFIES THE EXTENT LIMITS IN      *
*   THE DATA EXTENT BLOCKS(DEBS) FOR ALL DIRECT ACCESS DEVICES        *
*   INVOLVED. THIS ALLOWS ACCESS TO THE ENTIRE VOLUME BY THE          *
*   NECESSARY I/O OPERATIONS. THE EXTENT LIMITS ARE SET FOR           *
*   MAXIMUM FOR THAT DEVICE FOR ALL FUNCTIONS EXCEPT ONE. THE         *
*   DEVICE THAT IS BEING DUMPED BY THE -DUMP- FUNCTION WILL HAVE      *
*   ITS END EXTENT INCREMENTED BY ONE FOR EACH TRACK. THIS IS         *
*   NECESSARY TO FORCE A FILE-PROTECT INTERRUPT BY THE READ-COUNT     *
*   MULTI-TRACK CHANNEL COMMANDS.  THIS ROUTINE ALSO SETS THE         *
*   PROPER FILE MASK IN THE DEB.                                      *
*                                                                     *
*ENTRY POINTS- THE ONLY ENTRY POINT IS -IGG019P8-.                    *
*                                                                     *
*INPUT-  REGISTER 1 POINTS TO THE REQUEST ELEMENT.                    *
*        REGISTER 2 POINTS TO THE IOB.                                *
*        REGISTER 3 POINTS TO THE DEB.                                *
*        REGISTER 4 POINTS TO THE DCB.                                *
*        REGISTER 6 POINTS TO A UCB-SEEK FIELD EQUIVALENT       YL02912
*        REGISTER 7 POINTS TO THE UCB                                 *
*        THE DCB+28 WILL CONTAIN A POINTER TO THE FUNCTION BLOCK      *
*                                                                     *
*EXITS-  TWO DIFFERENT EXITS ARE POSSIBLE.                            *
*   THE -DUMP- FUNCTION WILL RETURN ON REGISTER 14 PLUS 4. THIS WILL  *
*   POST THE CHANNEL PROGRAM COMPLETE.                                *
*   ALL OTHER FUNCTIONS WILL RETURN ON REGISTER 14 PLUS 8. THE        *
*   REQUEST WILL BE TRIED AGAIN.                                      *
*                                                                     *
*EXITS-ERROR-  NONE                                                   *
*                                                                     *
*EXTERNAL ROUTINE-  THE APPENDAGE ROUTINE IS ALWAYS ENTERED FROM IOS  *
*   AND EXITS TO IOS.                                                 *
*                                                                     *
*TABLES/WORKAREAS-  THIS ROUTINE USES THE DEVICE CONSTANTS FOR        *
*   A PARTICULAR DIRECT ACCESS DEVICE TO SET UP THE EXTENT LIMITS.    *
*   THE DEVICE CONSTANT TABLE IS ACCESSED BY MEANS OF A POINTER       *
*   IN THE FUNCTION BLOCK.                                            *
*                                                                     *
*ATTRIBUTES- REENTRANT, RELOCATABLE, NON-PRIVILEGED.                  *
*                                                                     *
 TITLE 'IGG019P8-END OF EXTENT APPENDAGE-IEHDASDR SYSTEM UTILITY'
*   THE FOLLOWING ARE REGISTER ASSIGNMENTS.
GR0      EQU   0                       REG ZERO
GR1      EQU   1                       REQUEST ELEMENT POINTER.
GR2      EQU   2                       IOB POINTER.
GR3      EQU   3                       DEB POINTER.
GR4      EQU   4                       DCB POINTER.
GR7      EQU   7                       UCB POINTER.
GR9      EQU   9                       SAVE AREA FOR REG 0       YM3467
GR10     EQU   10                      FUNCTION BLOCK POINTER.
GR11     EQU   11                      DEVICE CONSTANT POINTER.
GR12     EQU   12                      WORK REGISTER.
GR13     EQU   13                      WORK REGISTER.
GR14     EQU   14                      RETURN REGISTER.
GR15     EQU   15                      BASE REGISTER.
COMPLT   EQU   X'7F'                   ECB COMPLETE VALUE     OX02850
L8       EQU   8                        OFFSET INTO SAVE AREA   YL02912
OVRFL    EQU   X'01'                   OVERFLOW INCOMPLETE    OX02850
GR6      EQU   6                        UCB SEEK EQUIVALENT     YL02912
         SPACE
         USING IOB,GR2
         USING DEB,GR3
         USING IHADCB,GR4
         USING UCB,GR7
         USING FUNCBLK,GR10
         USING IGG019P8,GR15
         USING CONSTANT,GR11
         EJECT
ENTRY    EQU   *                       ENTRY POINT HERE.
         L     GR10,DCBIOBAD           FUNCTION BLOCK ADDRESS.
         B     APARNO                  BRANCH AROUND APAR NO   @ZA04411
         DC    C'IGG019P8 OZ04411'     LAST FIX THIS MOD       @ZA04411
APARNO   L     GR11,IODEVCON           ADR FOR DEV CONSTANTS.  @ZA04411
         USING CONSTANT,GR11                                     Y01048
         SPACE
         CLI   CCHH,X'FF'              THIS A PARTIAL DUMP FUNCTION.
         BE    EXTENT3A                YES-SPECIAL CASE.
         SPACE
         OC    DEBSTRCC(4),DEBSTRCC    IS THE LOWER DEB LIMIT ZERO.
         BZ    EXTENT0                 YES-THIS IS NOT FIRST ENTRY.
         SPACE
         CLI   FUNCSW,DUMP             THIS A DUMP FUNCTION.
         BNE   EXTENT2                 NO--MUST BE 1 OF OTHER 5.
         SPACE
         AIF   ('&LIB' EQ 'LIB2').NODC1 BRCH IF VS ASSEM        YL02912
         CLI   UCBTBYT4,X'05'          THIS A 2321.
         BE    PROC2321
         SPACE
.NODC1   ANOP                                                   YL02912
         LA    GR7,0(GR7)              CLEAR HIGH ORDER BYTE.
         CL    GR7,FUCBPTR             THIS THE DUMP TO-DEVICE.
         BNE   EXTENT3                 NO--MUST BE DUMP FROM-DEVICE.
         SPACE
EXTENT2  XC    DEBSTRCC(4),DEBSTRCC    SET LOWER LIMIT FOR ZERO.
         MVC   DEBENDCC(4),LASTALT     SET UPPER LIMIT FOR MAXIMUM.
         LH    GR12,TOTALPRM           PRIMARY TRACKS/THIS DEVICE.
         AH    GR12,TOTALALT           PLUS ALTERNATE TRACKS.
         SPACE
EXT3     LA    GR14,8(GR14)            SET RETURN FOR REQUEST RETRY.
EXT4     STH   GR12,DEBNMTRK           TOTAL TRACKS IN EXTENT.
         MVI   DEBVMOD,X'D8'           SET FILE MASK FOR ALL WRITES,
*                                        NO SEEKS.
         CLI   FUNCSW,ANALYSIS         THIS THE ANALYZE FUNCTION.
         BCR   4,GR14                  NO--RETURN TO IOS.
         MVI   DEBVMOD,X'C0'           YES-ALLOW FULL SEEKS.
         BR    GR14                    RETURN
         SPACE
EXTENT0  CLC   CCHH(4),DEBENDCC        DOES IOB TRACK ADDRESS EQUAL
*                                        UPPER LIMIT.
         BNE   EXTENT1                 NO, GO UP EXTENTS, RETRY OX02850
         SPACE
         LA    GR14,4(GR14)            TAKE POST EXIT         OX02850
         TM    IOBSENSE,X'08'          DATA CHECK?             @ZA01207
         BCR   8,GR14                  NO, TAKE POST EXIT      @ZA01207
POEXIT   NI    IOBCSW+4,X'FD'          TURN OF UNIT CHECK      @ZA01207
         BR    GR14                    TAKE POST EXIT          @ZA01207
         SPACE
EXTENT1  MVC   DUMPWORK(4),DEBENDCC    ALIGN TO FULL WORD.
         L     GR12,DUMPWORK           CURRENT UPPER CCHH.
         SPACE
         CLC   DEBENDHH+1(1),LASTORIG+3 THIS LAST TRACK OF CYLINDER.
         BC    4,EXT5
         SPACE
         A     GR12,CONVCYL            STEP TO NEXT CYLINDER.
         SPACE
         CLI   DEBENDHH,X'04'          IS THIS 2321 AND LAST CYLINDER.
         BC    7,EXT6                  NO--GO RETURN.
         SPACE
         AL    GR12,STC2321            INCREMENT STRIP.
         SPACE
         CLI   DEBENDCC+1,X'09'        THIS THE LAST STRIP.
         BC    7,EXT6                  NO--RETURN.
         SPACE
         AL    GR12,SCC2321            INCREMENT SUBCELL.
         B     EXT6                    GO RETURN.
         SPACE
EXT5     AL    GR12,F1                 INCREMENT TO NEXT TRACK.
         SPACE
EXT6     ST    GR12,DUMPWORK           TEMPORARY STORAGE.
         MVC   DEBENDCC(4),DUMPWORK    SET NEW UPPER LIMIT IN DEB.
         LH    GR12,DEBNMTRK           CURRENT TRACK COUNT IN EXTENT.
         LA    GR12,1(GR12)            INCREMENT BY ONE.
         B     EXT3                    GO RETURN.             OX02850
         SPACE
EXTENT3A EQU   *                                                YL02912
         TM    RDVTOC,X'FF'            VTOC JUST READ?         @ZA04411
         BNO   EXTENT3B                NO THEN BRANCH          @ZA04411
         MVC   3(4,GR6),CCHHONE        CLEAR UCB SEEK ADR      @ZA04411
         MVC   CCHH(4),CCHHONE         CLEAR IOB SEEK ADR      @ZA04411
         XC    DEBSTRCC(4),DEBSTRCC    LOWER DEBLIMIT TO ZERO  @ZA04411
         MVC   DEBENDCC(4),CCHHONE     END LIMIT TO CURRENT TRK@ZA04411
         MVI   RDVTOC,X'00'            CLEAR JUST READ VTOC SW @ZA04411
         B     EXTENT3C                GO UPDTE TRK IN EXTENT  @ZA04411
EXTENT3B MVC   3(4,GR6),CCHHBEG        CLEAR UCB SEEK ADDRESS. @ZA04411
         MVC   CCHH(4),CCHHBEG         CLEAR IOB SEEK ADDRESS.
EXTENT3  XC    DEBSTRCC(4),DEBSTRCC    LOWER LIMIT IN DEB/TRACK ZERO.
         MVC   DEBENDCC(4),CCHHBEG     UPPER LIMIT IS FIRST TRACK/DUMP.
EXTENT3C LA    GR12,1                  TRACKS IN EXTENT.       @ZA04411
         B     EXT3                    GO TO RETURN.
         SPACE
         AIF   ('&LIB' EQ 'LIB2').NODC2 BRCH IF VS ASSEM        YL02912
PROC2321 LH    GR12,DCELBBNR           BIN NUMBER.
         SLA   GR12,4                  TIMES SIXTEEN.
         LA    GR12,DATACELL-UCB(GR12,GR7) SUBUCB ADDRESS.
         CL    GR12,FUCBPTR            THIS THE DUMP FROM DEVICE.
         BNE   EXTENT3                 NO-SET LIMITED EXTENTS.
         B     EXTENT2                 SET FULL EXTENTS.
         SPACE
.NODC2   ANOP                                                   YL02912
F1       DC    F'1'                    CONSTANT OF ONE.
ONE      EQU   F1                      ONE USED FOR APF          YM3012
SCC2321  DC    X'00F60000'             INCREMENT TO NEXT SUBCELL.
STC2321  DC    X'0000FB00'             INCREMENT TO NEXT STRIP.
E12      EQU   12                        CONSTANT                YM3012
MAINT    DC    C'P8 MAINT AREA'                               OX02850
         DC    20F'0'                  ZAP AREA               OX02850
         EJECT
         IEHDBLKS
         EJECT
         DCBD  DSORG=PS
         EJECT
CVT      DSECT
         CVT   SYS=MIN                                           Y01048
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
         SPACE
DEB      DSECT
DEBVMOD  EQU   DEB+32                  FILE MASK.
DEBSTRCC EQU   DEB+38                  CYLINDER-START OF EXTENT LIMIT.
DEBSTRHH EQU   DEB+40                  TRACK-START OF EXTENT LIMIT.
DEBENDCC EQU   DEB+42                  CYLINDER/END OF EXTENT LIMIT.
DEBENDHH EQU   DEB+44                  TRACK-END OF EXTENT LIMIT.
DEBNMTRK EQU   DEB+46                  NUMBER OF TRACKS THIS EXTENT.
         SPACE
IOB      DSECT
IOBFLAGS DS    CL2                     FLAG BYTES.
IOBSENSE DS    CL2                     SENSE BYTES.
IOBECBAD DS    1F                      ECB ADDRESS.
IOBCSW   DS    2F                      CHANNEL STATUS WORD.
IOBCAW   DS    1F                      CHANNEL PROGRAM ADDRESS.
IOBDCBAD DS    1F                      DCB ADDRESS.
IOBRST   DS    1F                      RESTART ADDRESS.
IOBBLKER DS    1F                      BLOCK AND ERROR COUNTS.
IOBSEEK  DS    2F                      MBBCCHHR.
CCHH     EQU   IOBSEEK+3               CYLINDER/HEAD ADDRESS.
R        EQU   IOBSEEK+7               RECORD NUMBER.
         END
