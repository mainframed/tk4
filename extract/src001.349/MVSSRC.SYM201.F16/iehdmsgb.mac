         TITLE 'IEHDMSGB --- MESSAGE SELECTION'
         COPY  LCGASMSW                                          SM4351
IEHDMSGB CSECT
*STATUS- CHANGE LEVEL 000                                             *
*                                                                     *
*FUNCTION/OPERATION-  THIS ROUTINE SELECTS THE PROPER MESSAGE FROM    *
*   THE MESSAGE CSECT-IEHDMSGS-. THE SELECTED MESSAGE IS MOVED INTO   *
*   THE OUTPUT BUFFER. THIS ROUTINE ALSO COMPUTES THE DISPLACEMENT    *
*   TO A PORTION OF THE MESSAGE THAT MUST LATER BE FILLED IN.         *
*                                                                     *
*ENTRY POINTS-  THE ONLY ENTRY POINT IS -IEHDMSGB-.                   *
*                                                                     *
*INPUT-  REGISTER 1 MUST CONTAIN THE DESIRED MESSAGE NUMBER.          *
*        REGISTER 12 POINTS TO THE COMMON WORK AREA.                  *
*                                                                     *
*EXITS-NORMAL-  RETURN TO CALLING ROUTINE VIA LINK REGISTER.          *
*                                                                     *
*EXITS-ERROR-  NONE.                                                  *
*                                                                     *
*EXTERNAL ROUTINES-  ENTERED FROM ANY MODULE THAT DESIRES TO          *
*   SELECT AT MESSAGE AND LOAD IT IN THE OUTPUT BUFFER. THIS          *
*   ROUTINE MUST USE THE -IEHDMSGS- CSECT TO PICK UP THE PROPER       *
*   MESSAGE.                                                          *
*                                                                     *
*SUPERVISOR MACROS-  SAVE                                             *
*                                                                     *
*TABLES/WORKAREAS-  USE THE COMMON WORK AREA CALLED -WORK-.           *
*                                                                     *
*OUTPUT-  THE SELECTED MESSAGE IS PLACED IN THE OUTPUT BUFFER.        *
*   REGISTER 1 POINTS TO A FILL-IN SLOT, IF ANY.                      *
*                                                                     *
*ATTRIBUTES-  REENTRANT,RELOCATABLE,NON-PRIVILEGED.                   *
*
         EJECT
*   THE FOLLOWING ARE REGISTER ASSIGNMENTS.
GR0      EQU   0
GR1      EQU   1
GR2      EQU   2
GR3      EQU   3
GR4      EQU   4
GR5      EQU   5
GR6      EQU   6
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10
GR11     EQU   11
GR12     EQU   12
GR13     EQU   13
GR14     EQU   14
GR15     EQU   15
         EJECT
         SAVE  (14,12),T,*             SAVE THE REGISTERS.
         SPACE
         LR    GR11,GR15               LOAD THE BASE REGISTER.
         USING IEHDMSGB,GR11
         USING WORK,GR12
         SPACE
         L     GR2,ADMSGTAB            START OF DISPLACEMENT TABLE.
         SLA   GR1,1                   MESSAGE NO. TIMES TWO.
         LA    GR1,0(GR1,GR2)          ADDRESS OF DISPLACEMENT ENTRY.
         LH    GR5,0(GR1)              CONTENTS THIS DISPLACEMENT ENTRY
         SPACE
         L     GR3,ADMSGTXT            START OF MESSAGES.
         AR    GR3,GR5                 ADDRESS OF DESIRED MESSAGE.
         SPACE
         LH    GR4,2(0,GR1)            CONTENTS OF NEXT ENTRY.
         SR    GR4,GR5                 DIFFERENCE=LENGTH OF MESSAGE.
         SPACE
         BCTR  GR4,0                   DECREMENT FOR EXECUTION.
         BCTR  GR4,0                   ACCOUNT FOR LST BYTE OF MESSAGE.
         EX    GR4,MSGMOVE             MOVE MESSAGE TO OUTPUT BUFFER.
         SPACE
         CLI   0(GR3),0                IS MESSAGE TO BE FILLED IN.
         BE    EXIT                    NO--RETURN.
         SPACE
         SR    GR1,GR1                 CLEAR.
         IC    GR1,0(GR3)              DISPLACEMENT TO SLOT.
         LA    GR1,MESS(GR1)           ADDRESS OF SLOT.
         SPACE
EXIT     LM    14,0,12(13)             RESTORE REGISTERS.
         LM    2,12,28(13)             RESTORE REGISTERS.
         MVI   12(13),X'FF'            SET RETURN INDICATION.
         BR    14                      RETURN.
         SPACE
         SPACE
MSGMOVE  MVC   MESS(1),1(GR3)          MOVES MESSAGE TO BUFFER.
ADMSGTAB DC    V(MSGTABLE)             MESSAGE DISPLACEMENT TABLE.
ADMSGTXT DC    V(MSGSTART)             START OF MESSAGES.
         EJECT
         IEHDWORK
         END
 $ END
