         COPY  LCGASMSW
IEHDDATE CSECT
 TITLE   'IEHDDATE-      DETERMINES CURRENT DATE   IEHDASDR UTILITY'
*                                                                     *
*STATUS- CHANGE LEVEL 000                                             *
*                                                                     *
*FUNCTION/OPERATION- THIS ROUTINE DETERMINES THE CURRENT DATE AS      *
*   TO THE MONTH-DAY-YEAR.                                            *
*                                                                     *
*ENTRY POINTS-  THE ONLY ENTRY POINT IS IEHDDATE.                     *
*                                                                     *
*INPUT-  REGISTER 1 MUST POINT TO AN 8-BYTE AREA TO RECEIVE THE DATE  *
*   ON ANY BOUNDARY ALIGNMENT.                                        *
*                                                                     *
*EXITS-NORMAL-  RETURN TO CALLING ROUTINE VIA LINK REGISTER.          *
*                                                                     *
*EXITS-ERROR-  NONE.                                                  *
*                                                                     *
*EXTERNAL ROUTINES-  ALWAYS ENTERED FROM AND RETURNS TO -IEHDPRNT-.   *
*                                                                     *
*SUPERVISOR MACROS-  TIME.                                            *
*                    GETMAIN.                                         *
*                    FREEMAIN.                                        *
*                                                                     *
*TABLES/WORK AREAS-  XTMN IS A TABLE OF THE NUMBER OF DAYS IN         *
*   EACH MONTH.                                                       *
*                                                                     *
*OUTPUT-  THE CURRENT DATE IN THE FORM OF -MM/DD/YY- IS PLACED IN     *
*   AN 8-BYTE RECEIVING AREA.                                         *
*                                                                     *
*ATTRIBUTES-  REENTRANT,RELOCATABLE,NON-PRIVILEGED.                   *
*
         EJECT
*   THE FOLLOWING ARE REGISTER ASSIGNMENTS.
GR0      EQU   0
GR1      EQU   1
GR2      EQU   2
GR3      EQU   3
GR4      EQU   4
GR5      EQU   5
GR6      EQU   6
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10
GR11     EQU   11
GR12     EQU   12
GR13     EQU   13
GR14     EQU   14
GR15     EQU   15
         EJECT
         USING *,GR11
IEHDPRDA SAVE  (14,12),T               SAVE THE REGISTERS.
         LR    GR11,GR15               LOAD BASE REGISTER.
         LR    GR7,GR1                 SAVE TARGET ADDRESS.
         GETMAIN R,LV=0+ASIZE
         SPACE
         LR    GR8,GR1                 SAVE AREA/WORKAREA ADDRESS.
         USING AREA,GR8
         SPACE
         ST    GR13,4(GR8)             ADDRESS OF NEW AREA TO OLD AREA.
         ST    GR8,8(GR13)             ADDRESS OF OLD AREA TO NEW AREA.
         LR    GR13,GR8                SAVE AREA ADDRESS.
         SPACE
         TIME
         SPACE
         LR    GR0,GR1                 CONTAINS DATE.
         SRDL  GR0,16                  SEPARATE YEARS AND DAYS.
         SRL   GR1,16                  DAYS TO LOW ORDER.
         LR    GR4,GR0                 SAVE YEAR.
         SR    GR0,GR0                 CLEAR.
         LA    GR3,19                  TEST TO SEE IF YEAR(IN DECIMAL)
         NR    GR3,GR4                   IS DIVISIBLE BY FOUR.
         BZ    DA010                   NO--
         LA    GR2,18
         XR    GR3,GR2                 YES-GR3 WILL CONTAIN ZERO.
         BZ    DA010                   IS THIS LEAP YEAR.
         LA    GR3,1                   NO--
         SPACE
DA010    STM   GR0,GR1,WORKAREA        SET UP FOR CONVERSION.
         CVB   GR2,72(GR13)            CONVERT DAYS TO BINARY.
         SR    GR1,GR1                 GR1 WILL COUNT MONTHS.
         LR    GR5,GR1                 CLEAR
         SPACE
DA020    LA    GR1,1(GR1)              INCREMENT MONTH.
         IC    GR5,XTMN-1(GR1)         DAYS IN MONTH.
         SR    GR2,GR5                 SUBTRACT FROM DAYS LEFT.
         LA    GR6,3                   CONSTANT FOR 3RD MONTH
         CLR   GR6,GR1                 IS THIS MARCH.
         BC    7,DA030                 NO--
         SPACE
         LTR   GR3,GR3                 YES-AND IS IT LEAP YEAR.
         BC    2,DA030                 NO--
         BCTR  GR2,0                   YES-REDUCE DAYS BY ONE.
         SPACE
DA030    LTR   GR2,GR2                 ARE DAYS COUNTED DOWN TO ZERO.
         BC    2,DA020                 NO--REPEAT LOOP.
         AR    GR2,GR5                 YES-RESTORE SUBTRACTED VALUE.
         LTR   GR2,GR2                 TEST FOR ZERO(FEBRUARY 29).
         BNZ   DA040                   NO--
         LA    GR2,29                  YES-SET DAY FOR 29TH.
         BCTR  GR1,0                   DECREMENT TO FEBRUARY.
         SPACE
DA040    LA    GR3,2                   LOOP COUNT.
DA050    CVD   GR1,WORKAREA            CHANGE MONTH TO DECIMAL.
         L     GR5,WORKAREA+4          MONTH TO LOWER HALF.
         SLL   GR5,20
         SLDL  GR4,8                   SHIFT IT UP TO YEAR OR MONTH.
         LR    GR1,GR2                 DAY
         BCT   GR3,DA050               REPEAT UNTIL DONE.
         SLL   GR4,8                   POSITION DATE FOR STORING.
         ST    GR4,WORKAREA            STORE YEAR/MONTH/DAY FOR UNPACK.
         UNPK  WORKAREA+2(7),WORKAREA(4)   CHANGE TO GRAPHICS
         SPACE
         USING TARGET,GR7
         MVC   MONTH(2),TMONTH         MONTH TO TARGET.
         MVI   SLASH1,C'/'             SEPARATOR.
         MVC   DAY(2),TDAY             DAY TO TARGET.
         MVI   SLASH2,C'/'             SEPARATOR.
         MVC   YEAR(2),TYEAR           YEAR TO TARGET.
         SPACE
DA060    LR    GR1,GR13                CORE TO BE RELEASED.
         L     GR13,4(GR13)            OLD SAVE AREA.
         SPACE
         FREEMAIN R,LV=0+ASIZE,A=(1)
         SPACE
         RETURN (14,12),T,RC=0         RESTORE REGISTERS/RETURN.
         SPACE
XTMN     DC    X'1F1C1F1E1F1E1F1F1E1F1E1F'  DAYS IN MONTH.
         EJECT
*   DEFINITION OF TARGET AREA (8 BYTES ON ANY BOUNDARY).
TARGET   DSECT
MONTH    DS    CL2                     CURRENT MONTH.
SLASH1   DS    CL1                     MONTH/DAY SEPARATOR.
DAY      DS    CL2                     CURRENT DAY.
SLASH2   DS    CL1                     DAY/YEAR SEPARATOR.
YEAR     DS    CL2                     CURRENT YEAR.
         SPACE 3
*   DEFINITION OF SAVE AND WORK AREAS.
AREA     DSECT
DATESAVE DS    18F                     REGISTER SAVE AREA.
WORKAREA DS    0D                      WORK AREA ROUTINE.
         DS    CL2
TYEAR    DS    CL2                     YEAR ENDS UP HERE.
TMONTH   DS    CL2                     MONTH ENDS UP HERE.
TDAY     DS    CL2                     DAY ENDS UP HERE.
         DS    CL8
AEND     DS    0C                      END OF WORK AREA.
ASIZE    EQU   AEND-AREA               SIZE OF AREA.
         END
