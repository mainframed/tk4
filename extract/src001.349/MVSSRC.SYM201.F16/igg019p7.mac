IGG019P7 CSECT                                                   M5431
*                                                                YM5022
*                                                                M5431*
*        RELEASE 4 DELETIONS                                     M5431*
*        RELEASE 3 DELETIONS                                     M5431*
*        RELEASE 2 DELETIONS                                     M5431*
*        RELEASE 1 DELETIONS                                     M5431*
*                                                                M5431*
*STATUS CHANGE LEVEL 001                                         M5431*
*                                                                M5431*
*FUNCTION/OPERATION- THIS IS THE PAGE FIX APPENDAGE FOR THE      M5431*
*   IEHDASDR SYSTEM UTILITY PROGRAM. IT FIXES THOSE UTILITY DATA M5431*
*   AREAS REFERENCED BY THE OTHER IEHDASDR APPENDAGES NOT        M5431*
*   OTHERWISE FIXED BY IOS UPON ENTRY TO THE APPENDAGES. THIS IS M5431*
*   DONE IN ORDER TO AVOID DISABLED PAGE EXCEPTIONS. THOSE AREAS M5431*
*   FIXED BY THIS APPENDAGE ARE;                                 M5431*
*                  (1) THE -FUNCTION- BLOCK FOR THE PARTICULAR   M5431*
*                      REQUEST IE -ANALYZE-,-DUMP-,-FORMAT-,     M5431*
*                      -LABEL-,-RESTORE-,GETALT-                 M5431*
*                  (2) THE I/O DEVICE CONSTANT BLOCK FOR THE DEVICE   *
*                      UPON WHICH I/O IS CURRENTLY BEING PERFORMED    *
*                                                                M5431*
*ENTRY POINTS- THE ONLY ENTRY POINT IS -IGG019P7-.               M5431*
*                                                                M5431*
*INPUT- REGISTER 4 POINTS TO THE DCB.                            M5431*
*       REGISTER 10 POINTS TO THE WORK AREA IN WHICH THE -FIX-   M5431*
*                   LIST WILL BE PLACED.                         M5431*
*                                                                M5431*
*EXITS-NORMAL- RETURN TO IOS ON REGISTER 14.                     M5431*
*        REGISTER 11 CONTAINS THE NUMBER OF AREAS                M5431*
*                    TO BE FIXED                                 M5431*
*                                                                M5431*
*EXITS-ERROR- NONE.                                              M5431*
*                                                                M5431*
*EXTERNAL ROUTINES- THE APPENDAGE ROUTINE IS ALWAYS ENTERED FROM M5431*
*   IOS AND EXITS TO IOS WHERE THE PAGES ASSOCIATED WITH THE WORK     *
*   AREAS CITED ABOVE ARE FIXED.                                 M5431*
*                                                                M5431*
*TABLES/WORK AREAS- THE FORMAT OF THE LISTS OF ADDRESSES         M5431*
*   TO BE FIXED IS AS FOLLOWS;                                   M5431*
*                   WORD 1-FIRST POSISTION OF THE -FUNCTION-     M5431*
*                          BLOCK FOR THE UTILITY OPERATION       M5431*
*                   WORD 2-LAST POSISTION OF THE -FUNCTION-      M5431*
*                          BLOCK PLUS 1                          M5431*
*                   WORD 3-FIRST POSISTION OF THE I/O DEVICE     M5431*
*                          CONSTANT BLOCK                        M5431*
*                   WORD 4-LAST POSISTION OF THE I/O DEVICE      M5431*
*                          CONSTANT BLOCK PLUS 1                 M5431*
*                                                                M5431*
*ATTRIBUTES- REENTRANT, RELOCATABLE, NON-PRIVILEGED              M5431*
*                                                                M5431*
 TITLE    'IGG019P7-PAGE FIX APPENDAGE-IEHDASDR SYSTEM UTILITY'  M5431*
         EJECT                                                   M5431*
*                                                                M5431*
*     THE FOLLOWING ARE REGISTER ASSIGNMENTS                     M5431*
*                                                                M5431*
DCBREG   EQU   4                        DCB REGISTER             M5431
LISTREG  EQU   10                       POINTER TO AREA WHERE    M5431
*                                       FIX LIST TO BE BUILT     M5431
*                                            FIXED               M5431
GR11     EQU   11                       HOLDS NUMBER OF AREAS TO YM5022
*                                       BE BUILT                 M5431
GR13     EQU   13                       WORK REGISTER            M5431
RETREG   EQU   14                       RETURN REGISTER          M5431
BASEREG  EQU   15                       BASE REGISTER            M5431
*                                                                M5431*
         USING IGG019P7,BASEREG                                  M5431
         USING IHADCB,DCBREG                                     M5431
         EJECT                                                   M5431
*                                                                M5431*
*                                                                M5431*
*    THE FIRST FOUR BYTES OF THIS CSECT ARE NOT A PART OF THE    M5431*
*    PAGE FIX APPENDAGE. INSTEAD THEY CONTAIN A BRANCH INSTRUCTION    *
*    TO THE SIO APPENDAGE IF ONE EXISTS. SINCE THE IEHDASDR PROGRAM   *
*    DOES NOT HAVE AN SIO APPENDAGE A BRANCH INSTRUCTION IS USED M5431*
*    TO RETURN CONTROL BACK TO IOS THROUGH REGISTER 14. THIS     M5431*
*    CONDITION IS BROUGHT ABOUT BY THE FACT THAT THE SAME WORD IN     *
*    THE AVT TABLE IS EMPLOYED AS A POINTER TO BOTH THE SIO AND THE   *
*    PAGE FIX APPENDAGES. THE PAGE FIX APPENDAGE IS ASSUMED TO START  *
*    FOUR BYTES BEYOND THE DESIGNATED ENTRY POINT.               M5431*
*                                                                M5431*
*                                                                M5431*
ENTRY    EQU   *                                                 M5431
         B     0(RETREG)                RETURN TO IOS            M5431
*                                                                M5431*
*                                                                M5431*
         EJECT
*                                                                M5431*
*                                                                M5431*
*    BEGINNING OF PAGE FIX APPENDAGE. THE FIRST AREA TO BE FIXED M5431*
*    IS THAT CONTAINING THE FUNCTION BLOCK FOR THE UTILITY OPERATION. *
*    THE BEGINNING LOCATION OF THE BLOCK IS FOUND IN AN OTHERWISE     *
*    UNUSED FIELD (FOR -EXCP- PROCESSING) IN THE DCB -THE 'DCBIOBAD'  *
*    FIELD. THE LAST-MOST POSISTION OF THE BLOCK IS A FUNCTION OF     *
*    THE TYPE OF UTILITY OPERATION ( IE A -DUMP-,-LABEL-,-FORMAT-     *
*    ETC REQUEST ). THE FUNCTION BLOCK CODE FROM WITHIN THE FUNCTION  *
*    BLOCK IS CONVERTED TO A POINTER TO THE APPROPRIATE ENTRY IN A    *
*    TABLE OF FUNCTION BLOCK SIZES. THE SIZE WHICH IS FOUND IS THEN   *
*    ADDED TO THE BEGINNING LOCATION OF THE BLOCK.               M5431*
*                                                                M5431*
*                                                                M5431*
         L     GR11,DCBIOBAD            LOAD FUNCTION BLOCK      YM5022
*                                       ADDRESS                  M5431
         USING PGFIXLST,LISTREG                                  M5431
         USING FUNCBLK,GR11                                      YM5022
         SR    GR13,GR13                CLEAR A REGISTER AND     M5431
         IC    GR13,FUNCSW              INSERT FUNCTION CODE     M5431
         SRL   GR13,DIVBY8              CONVERT CODE             M5431
*                                       TO POINTER VALUE         M5431
         LH    GR13,FBLKSIZE(GR13)      PICK UP PROPER FUNCT     M5431
*                                       BLK SIZE                 M5431
         ST    GR11,FBEGLOC             PUT 1ST FUNCTION BLOCK   YM5022
*                                       LOCATION IN FIX LIST     M5431
         LA    GR13,0(GR13,GR11)        CALCULATE UPPER FUNCT    YM5022
*                                       BLOCK LOCATION           M5431
         ST    GR13,FENDLOC             AND PUT IN FIX LIST      M5431
*                                                                M5431*
*                                                                M5431*
*    FIX THE AREA OCCUPIED BY THE DEVICE CHARACTERISTIC ENTRY FOR THE *
*    CURRENT I/O REQUEST.THE BEGINNING LOCATION OF THE ENTRY IS       *
*    FOUND WITHIN THE RELATED FUNCTION BLOCK. EACH ENTRY IS           *
*    34 HEXADECIMAL BYTES LONG.                                       *
*                                                                M5431*
*                                                                M5431*
         L     GR13,IODEVCON            GET THE START LOCATION   M5431
*                                       FOR THE ENTRY            M5431
         ST    GR13,IOBEGLOC            SAVE LOCATION IN         M5431
*                                       FIX LIST                 M5431
         LA    GR13,(KDEVSIZE)(GR13)    POINT TO LAST POSITION   M5431
         ST    GR13,IOENDLOC            SAVE IN FIX LIST         M5431
*                                                                M5431*
         DROP  11                                                YM5022
*                                                                M5431*
*    EXIT FROM PAGE FIX ROUTINE                                  M5431*
*                                                                M5431*
*                                                                M5431*
         LA    GR11,LISTSIZE            LOAD NUMB OF AREAS       YM5022
*                                       TO BE FIXED              M5431
         BR    RETREG                   EXIT APPENDAGE           M5431
         EJECT
*                                                                M5431*
*                                                                M5431*
*    THE FOLLOWING TABLE IS USED IN THE CALCULATION OF THE       M5431*
*    FUNCTION BLOCK SIZE.EACH OF THE FOLLOWING                   M5431*
*    HALF-WORDS CONTAINS THE SIZE OF                             M5431*
*    THE INDICATED FUNCTION BLOCK                                M5431*
*                                                                M5431*
*                                                                M5431*
TABLE1   DC    S(DFUNSIZE)              -DUMP- FUNCTIOM          M5431
         DC    S(RFUNSIZE)              -RESTORE- FUNCTION       M5431
         DC    S(GFUNSIZE)              -GETALT- FUNCTION        M5431
         DC    S(LFUNSIZE)              -LABEL- FUNCTION         M5431
         DC    S(AFUNSIZE)              -ANALYZE- FUNCTION       M5431
         DC    S(FFUNSIZE)              -FORMAT- FUNCTION        M5431
         DC    S(IFUNSIZE)              -PUTIPL- FUNCTION        YM3011
FBLKSIZE EQU   TABLE1-L'TABLE1          PTR USED AS BASE FOR     M5431
*                                       FUNCTION TABLE           M5431
*                                                                M5431
*                                                                M5431
DIVBY8   EQU   3                        USED IN FINDING FUNCT    M5431
*                                       BLOCK SIZE               M5431
*                                                                M5431*
*                                                                M5431*
*    THE FOLLOWING DSECT DESCRIBES THE PAGE FIX LIST CONSTRUCTED M5431*
*    BY THIS MODULE                                              M5431*
*                                                                M5431*
*                                                                M5431*
PGFIXLST DSECT                                                   M5431
FBEGLOC  DS    F                        BEG LOC OF FUNC BLK      M5431
FENDLOC  DS    F                        END LOC OF FUNC BLK +1   M5431
IOBEGLOC DS    F                        BEG LOC OF I/O BLOCK     M5431
IOENDLOC DS    F                        END LOC OF I/O BLOCK     M5431
LISTEND  EQU   *                        TBL END MARKER           M5431
LISTSIZE EQU   (LISTEND-PGFIXLST)/8     NUMB OF ELEMENTS IN      M5431
*                                       FIX LIST                 M5431
         EJECT
         DCBD  DSORG=PS                                          M5431
         EJECT
         IEHDBLKS                                                M5431
*                                                                M5431
         END
