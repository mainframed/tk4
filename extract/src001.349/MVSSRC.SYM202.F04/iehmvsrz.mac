 TITLE 'IEHMVSRZ-ROUTING AND ERRORS FOR IEHMOVE LOAD MODULE ESX'
 TITLE 'IEHMVSRZ-ROUTING AND ERRORS FOR IEHMOVE LOAD MODULE ESX'
*TITLE -IEHMVSRZ
*FUNCTION/OPERATION: HANDLES ERRORS AND MESSAGES FOR LOAD MODULE      *
*IEHMVESX
*ENTRY POINTS: IEHMVESZ                                               *
*INPUT: NONE                                                          *
*OUTPUT: MESSAGES RESULTING FROM RETURNS FROM CALLED MODULES          *
*EXTERNAL ROUTINES: IEHMVSRV; IEHMVSRK                                *
*EXITS-NORMAL XCTL TO IEHMVESV                                        *
*ERRORS: NONE                                                         *
*TABLES/WORK AREAS: NONE                                              *
*ATTRIBUTES: REUSEABLE                                                *
*NOTES: PART OF LOAD MODULE IEHMVESX
*
         TITLE 'SINGLE DATA SET CONTROL'
*
*                                  LOAD MODULE  IEHMVL20
*                                               SINGLE DATE SET
*
*
IEHMVSRZ CSECT
*C050860-050930                                                @ZA03378
* 022400                                                         A29052
*
*3050                                                              UL0H
*
*              LOAD MODULE ENTRY POINT  IEHMVESZ
IEHMVESZ SAVE  (14,12),T,ESZ-SEPT20-THERE-CHARLIE
         ENTRY IEHMVESZ
*
    USING  IEHMVV,MCTABLE
*
*              REGISTERS
*
PASSA    EQU   0
PASSB    EQU   1
R2       EQU   2
R3       EQU   3
ACCE     EQU   4
ACCO     EQU   5
BASE     EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
MCTABLE  EQU   12
SAVE     EQU   13
RETURN   EQU   14
LINK     EQU   15
*
*
*
         BALR  BASE,0
         USING *,BASE
SINGLE   EQU   *
    IEHPRE  (14,1),TFC,
*                                       CALL COPY-UNLOAD-LOAD-ABORT
*
         L     LINK,COPYTEST            GET ADDRESS
         BALR  RETURN,LINK         BRANCH TO IEHMESV
         DROP  BASE
COPYRTN  EQU  *
         BALR  LINK,PASSA          SET UP ADDRESSABILITY
         USING *,LINK
         L     BASE,ASIN
         DROP  LINK
         USING SINGLE,BASE
*
     IEHPOST  (14,1),TFC,
*
         TM    IEHMVV20+1,LORUNL   UNLOAD OR LOAD
         BZ    NOLDUNLD            NO CONTINUE
         TM    IEHMVV20+2,INEXSE   YES,INCLUDE,EXCLUDE OR SELECT
         BZ    NOLDUNLD     NO CONTINUE
         MVI   IEHMVV61,ERROR      YES
         MVI   ABSW,ONES
         B     PREABORT                 GO PRINT MESSAGE
ABSW     DC    X'00'
         DS    0F
NOLDUNLD DS    0H
         L     RETURN,12(PASSA,SAVE)
    XCTL   (2,12),EP=IEHMVESV
***** CONSTANTS *****
UNLREC1  DC    FL2'75'
         DC    X'0E'
         DC    CL32'THIS IS AN UNLOADED DATA SET PRO'
         DC    CL8'DUCED BY'
         DC    X'80'
         DC    CL32'THE IBM UTILITY, SYSMOVE.OMMBRLD'
         DC    CL2'WB'
COPYTEST DC    A(IEHMVESV)
         EXTRN IEHMVESV
    ENTRY  UNLREC1
         ENTRY COPYRTN
BUILDREQ EQU   *
LOAD     EQU   X'02'
UNLOAD   EQU   X'01'
EOF      EQU   X'20'
VOLUME   EQU   X'02'
FVOLUME  EQU   X'80'
FOUR     EQU   X'04'
DSGROUP  EQU   X'10'
T        EQU   X'E3'
K        EQU   X'D2'
Z        EQU   X'E9'
H        EQU   X'C8'
I        EQU   X'C9'
S        EQU   X'E2'
ODD      EQU   X'01'
NOTMOV   EQU   X'80'
BLANK    EQU   X'40'
ZERO     EQU   X'00'
LORUNL   EQU   X'03'
INEXSE   EQU   X'0E'
ERROR    EQU   X'19'               CODE FOR MESSAGE IEH433I      A29052
*                                   IN CSECT IEHMVMRZ            A29052
ONES     EQU   X'FF'
UNLOADM  EQU   X'7F'
DEACTIVE EQU   X'00'                                               UL0H
      DS     0H
         TITLE 'PRE ABORT ROUTINE'
PREABORT DS    0H                  ABORT ROUTINE
         ENTRY PREABORT
         BALR  LINK,PASSA
         USING *,LINK
         L     BASE,ASIN
         DROP  LINK
         USING SINGLE,BASE
         TM    IEHMVV10-4,FVOLUME  FROM VOLUME TO BE CLOSED
         BO    CLOSEF              YES,GO TO CLOSE FROM VOLUME
FCLOSE   TM    IEHMVV10-4,BLANK    TO VOLUME TO BE CLOSED
         BO    CLOSET              YES,GO TO CLOSE TO VOL
TCLOSE   DS    0H
*
*
         LR    R9,RETURN
         BAL   RETURN,MSG               GO TO MESSAGE WRITER
         LR    RETURN,R9
         TM    ABSW,ONES
         BO    NOPOST
         IEHPOST (14,1),TFC,
   IEHPOST  (14,1),TFC,
NOPOST   CLI   IEHMVV41+13,X'04'       RETURN CODE EQUAL 8       A25631
         BZ    ABSWOFF                                           A25631
         OI    IEHMVV41+13,X'01'       SET RETURN CODE TO 4      A25631
ABSWOFF  MVI   ABSW,ZERO
         OI    IEHMVV20+2,BLANK    TURN ON MID-ABORT
         TM    IEHMVV20,VOLUME     TEST FOR VOL OPERATION
         MVC   IEHMVV00(6),MODNAME MOVE IN MODULE NAME
         MVI   IEHMVV00+6,S        ADD 'S' TO MODULE NAME
         BO    SINGO
         TM    IEHMVV20,DSGROUP    DSGROUP OPERATION
         BO    GODSG
         TM    IEHMVV20+2,EOF      END OF INPUT
         BO    GOHOME
         MVI   IEHMVV00+7,T        ADD 'T' TO MODULE NAME
ALLDONE  DS    0H                  TRANSFER TO MODULE NAME
         XCTL  (2,12),EPLOC=(12)
*
GOHOME   MVI   IEHMVV00+7,K        ADD 'K' TO MODULE NAME
         B     ALLDONE
SINGO    MVI   IEHMVV00+7,Z        ADD 'Z' TO MODULE NAME
         B     ALLDONE
GODSG    DS    0H
         MVI   IEHMVV00+7,H        ADD 'H' TO MODULE NAME
         B     ALLDONE
CLOSEF   DS    0H                  CLOSE FROM DATASET
         L     ACCE,IEHMVV30       ADDRESS OF FROM DCB
         MVI   UDCBITLE,DEACTIVE       DEACTIVATE INPUT TRLR EXIT  UL0H
         CLOSE ((ACCE))
         B     FCLOSE
CLOSET   DS    0H                  CLOSE TO DATASET
         L     ACCE,IEHMVV31       ADDRESS OF TO DATASET
         MVI   UDCBOTLE,DEACTIVE       DEACTIVATE OUTPUT TRLR EXIT UL0H
         CLOSE ((ACCE))
         B     TCLOSE
MODNAME  DC    CL6'IEHMVE'
         TITLE 'MESSAGE HANDLER'
         DROP  BASE
MSG      DS    0H
         STM   PASSA,LINK,200(MCTABLE)  STORE REGISTERS
         BALR  LINK,PASSA          SET UP ADDRESSABILITY
         USING *,LINK
         L     BASE,ASIN
         DROP  LINK                DROP BASE REGISTERS
      USING    SINGLE,BASE
         LR    R7,RETURN
         MVI   IEHMVV00,ONES
         MVI   IEHMVV00+1,BLANK    BLANK OUT PRINT LINE
         MVC   IEHMVV00+2(120),IEHMVV00+1
         TM    IEHMVV61,ODD        DATA SET MESSAGE OR NORMAL
*                                          MESSAGE
         BO    NORMMES                      NORMAL MESSAGE
*                                           DATA SET MESSAGE
*
         MVC   IEHMVV00+1(4),IEH4       SET UP MSG NUMBER
         LA    ACCE,127                 GET IEH4
         SR    ACCO,ACCO           MAKE HIGH ORDER BIT OF
         IC    ACCO,IEHMVV61       FIRST BYTE ZERO
         NR    ACCO,ACCE
         L     R3,AIEH4DD          ADDRESS OF CODE
         LA    ACCO,0(R3,ACCO)     SET UP MESSAGE DIAGNOSTIC CODE
         MVC   IEHMVV00+5(2),0(ACCO)
         MVI   IEHMVV00+7,I        END CODE WITH 'I'
         L     ACCE,MSGH                SET UP MSG HEADER
         LA    ACCO,7
         BAL   R8,MSGMOVE
*
         L     ACCE,IEHMVV21            SET UP DSNAME
         LA    ACCO,43
         BAL   R8,MSGMOVE
         TM    IEHMVV61,NOTMOV    UNLOAD MSG OR NOT MOVE
         BO    MSGUNLD                       UNLOAD MSG
         LA    ACCE,2                        NOT MOVED
MSGAA    EQU   *
         BAL   R8,MSGDMOVE              SET UP FINAL PART
         SR    ACCE,ACCE
         IC    ACCE,IEHMVV61                 OF MSG
         BAL   R8,MSGDMOVE
PRINTMES SR    PASSA,PASSA         TO PRINT A NORMAL MESSAGE
         LA    R3,IEHMVV00
         LINK  EP=IEHMVESU
*
*
         B     MSGCHECK(LINK)           CHECK RETURNS
MSGCHECK B     GOODMSG                  GOOR WRITE
         XCTL  EP=IEHMVESK              I/O ERROR
*
GOODMSG  LR    RETURN,R7
         MVI   IEHMVV61,ZERO       MOVE ZERO INTO MSG INDICATOR
         LM    PASSA,LINK,200(MCTABLE)
         BR    RETURN              BRANCH BACK TO CALLING ROUTINE
MSGUNLD  DS    0H
         NI    IEHMVV61,UNLOADM   TURN OFF UNLOAD MSG BIT
         LA    ACCE,0
         B     MSGAA
MSGDMOVE DS    0H
         SLL   ACCE,1
         L     R3,ADMSG            LOAD ADDRESS OF MSGSTART
         LH    ACCO,2(R3,ACCE)
         LH    ACCE,0(R3,ACCE)
         A     ACCE,MSGH           GET ADDRESS OF PROPER MSG
MSGMOVE  DS    0H
         LA    R9,IEHMVV00+121
MSGLOOP  CLI   0(R9),BLANK         FIND LAST NON BLANK
         BNE   MSGPL                         CHARACTER IN
         BCT   R9,MSGLOOP                    MSG AREA
MSGPL    DS    0H
         EX    ACCO,MSMOVE         MOVE MESSAGE IN PRINTOUT AREA
         BR    R8                  BRANCH BACK TO FINISH MESSAGE
NORMMES  DS    0H
         SR    ACCE,ACCE
         IC    ACCE,IEHMVV61            GET MESSAGE NUMBER
     LA   ACCO,127
    NR   ACCE,ACCO
         LA    ACCE,1(PASSA,ACCE)
         SLL   ACCE,1              MULTIPLY BY TWO
         L     R3,ANORMALM         LOAD ADDR OF NORMAL MESSAGE
         L     ACCE,0(R3,ACCE)     GET MESSAGE POINTER
         SR    ACCO,ACCO
         IC    ACCO,0(ACCE,PASSA)  GET MESSAGE LENGTH
         LA    ACCE,1(PASSA,ACCE)
         EX    ACCO,NORMM          MOVE MESSAGE TO PRINT AREA
         CLI   IEHMVV00+5,X'F1'        IS MSG IEH413I          @ZA03378
         BE    PSCHK                   YES ISSUE MSG           @ZA03378
         TM    IEHMVV61,FVOLUME    DSNAME INCLUDED IN MSG
         BZ    PRINTMES            NO GO TO PRINT MSG
         LA    ACCO,43             SET UP TO MOVE
         L     ACCE,IEHMVV21       DSNAME TO MSG AREA
     BAL   R8,MSGMOVE
         B     PRINTMES                 GO TO PRINT MESSAGE
PSCHK    L     ACCE,IEHMVV21           MOVE DSNAME               A25631
         LA    ACCO,43                   TO THE PRINT            A25631
         LA    R9,IEHMVV00+17               AREA                 A25631
         EX    ACCO,MSMOVE                                       A25631
         OI    IEHMVV41+13,X'04'       SET RETURN CODE = 8       A25631
         B     PRINTMES                                          A25631
NORMM    MVC   IEHMVV00+1(1),0(ACCE)
IEH4     DC    CL4'IEH4'
MSMOVE   MVC   2(1,R9),0(ACCE)
MSGH     DC    A(MSGHEAD)               POINTER TO MESSAGES
         ENTRY MSG
ADMSG    DC    A(MSGSTART)
ANORMALM DC    A(NORMALM)
AIEH4DD  DC    A(IEH4DD)
         EXTRN MSGSTART
         EXTRN NORMALM
         EXTRN IEH4DD
         EXTRN MSGHEAD
ASIN     DC    A(SINGLE)
         IEHMVV
         ORG   IEHMVV00
         ORG   IEHMVV70                                            UL0H
         IEHDCBXL                                                  UL0H
         END
