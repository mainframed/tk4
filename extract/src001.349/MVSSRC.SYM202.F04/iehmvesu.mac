*TITLE ' IEHMVLSU                                                     *
*FUNCTION/OPERATION: THIS MODULE IS THE MESSAGE WRITER MODULE. IT     *
*PRINTS THE HEADER AND PAGE NUMBER ON EACH PAGE. PRINTS MESSAGE AND   *
*ADJUST LINE COUNT.                                                   *
*ENTRY POINTS: IEHMVESU IS ONLY ENTRY POINT                           *
*INPUT: AT ENTRY REG 13 POINT TO SAVEAREA REG 12 POINTS AT            *
*COMMUNICATION TABLE AND REG 3 POINTS TO START OF THE MESSAGE MINUS   *
*ONE.                                                                 *
*OUTPUT: REG ARE SAME AS AT INPUT                                     *
*EXTERNAL ROUTINES: NONE                                              *
*EXITS - NORMAL RETURNS TO MODULE WHICH PASSED CONTROL.               *
*ERRORS: I/O ERROR CAUSES SYSTEM TO TERMINATE JOB                     *
*TABLES/WORK AREAS: IEHMVV - COMMUNICATION TABLE                      *
IEHMVSSU CSECT
*A010000-01360,014000-014400,017000,018220-018320,020400        YA01194
*A023200-023360,025420-025540                                   YA01194
*C019400,019800                                                 YA01194
*D006600,010000-010200,014000-014600,015600-015800,017000-017600YA01194
*D018400-019200,020400-020600,023200,024200-025000,026200-028400YA01194
*C018000,018200                                                  A48818
* 012200-012400                                                  A26720
* 015000,020400                                                  A26995
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
IEHMVESU SAVE  (14,12),T,ESU-10-27-65-LINK
         USING IEHMVESU,R10            SET
         USING IEHMVV,R12                     ADDRESSABILITY
         ENTRY IEHMVESU
         LR    10,15                    LOAD  BASE
         SR    R15,R15
         IEHPRE (14,1),TF               POINT TO NEW SAVE AREA
         TM    IEHMVV51,INITIAL
         BC    8,FRSTNTRY               YES
         BAL   R9,PRINT
RETURN   IEHPOST (14,1),T               RESTORE TO INIT1
         SR    R15,R15                  SET ERROR CODE TO ZERO
ERRET    RETURN (2,12),T,RC=(15)
SINAD    OI    IEHMVV41+13,X'08'    SET RETURN CODE = 16
         IEHPOST  (14,1),T
         LA    R15,4                    SET RETURN CODE = 4
         B     ERRET
*
PRINT    EQU   *                                                YA01194
         CLI   IEHMVV51,TRISPACE       CHECK NUMBER OF SPACE    YA01194
         BL    A1                      ONE SPACE GO TO A1       YA01194
         BH    A2                      TWO SPACE GO TO A2       YA01194
         CLI   IEHMVV51+11,THREE       CHECK IF IEHMVV51+11 IS  YA01194
*                                       EQUAL OR GREATER THAN 3 YA01194
         BNL   PRINT1                  YES , GO TO PRINT1       YA01194
         B     A3                      NO , GO TO A3            YA01194
A1       CLI   IEHMVV51+11,ONE         CHECK IF IEHMVV51+11 IS  YA01194
*                                       EQUAL OR GREATER THAN 1 YA01194
         BNL   PRINT1                  YES , GO TO PRINT1       YA01194
         B     A3                      NO , GO TO A3            YA01194
A2       CLI   IEHMVV51+11,TWO         CHECK IF IEHMVV51+11 IS  YA01194
*                                       EQUAL OR GREATER THAN 2 YA01194
         BNL   PRINT1                  YES , GO TO PRINT1       YA01194
A3       BAL   R8,HEADER               NO , GO TO HEADER        YA01194
PRINT1   EQU   *                                                YA01194
         IC    R2,IEHMVV51             INSERT CONTROL CHARACTER YA01194
         STC   R2,0(R3)                PLACE CHAR IN MESSAGE    YA01194
         L     R4,IEHMVV33             LOAD ADDR OF SYSPRINT DCB
         LA    R4,0(0,4)               ZERO OUT HIGH ORDER BITS
         CLC   1(3,R3),IEH         CHECK FOR ERROR MSG BEING PRINTED
         BE    ERR                   IF SO---
         CLC   2(3,R3),IEH
         BE    ERR
         CLC   3(3,R3),IEH        CHECK FOR ERROR MSG BEING PRINTED
         BE    ERR
         B     PRI
IEH      DC    CL3'IEH'
ERR      TM    IEHMVV41+13,X'FF'       TEST EXISTING RETURN CODE A26720
         BNE     PRI                   IF CODE IS NOT 0, BRANCH  A26720
         OI    IEHMVV41+13,X'01'       SET RETURN CODE = 4       A26720
PRI      DS    0H
         LA    R1,SINAD            LOAD SYNAD ADDR
         IC    R0,56(R4)
         ST    R1,56(R4)
         STC   R0,56(R4)
         PUT   (R4),(R3)
         LH    R7,IEHMVV51+10          LINES TO GO ON CURRENT PAGE
         BCTR  R7,0                    DECREASE LINE COUNT      YA01194
         CLI   IEHMVV51,X'60'          CHECK NUMBER OF SPACE    YA01194
         BL    SINGLE
         BH    ZIP                    IF HIGH,CAUSE DOUBLE SPACE A26995
         BCTR  R7,0                    DECREASE
ZIP      BCTR  R7,0                             LINE COUNT
SINGLE   STH   R7,IEHMVV51+10          STORE CURRENT LINES TO GO
         MVI   IEHMVV51,X'40'           SET SINGLE SPACE CODE
         BR    R9
*
*
HEADER   LH    R6,IEHMVV51+12          LOAD CURRENT PAGE NUMBER YA01194
         CVD   R6,DOUBLE               CONVERT PAGE NUMBER FROM
         UNPK  NUMB(4),DOUBLE+5(3)       BINARY TO DECIMAL       A48818
         OI    NUMB+3,PLUS             INSURE CORRECT SIGN       A48818
         LA    R11,NUMB                                         YA01194
LOOP     CLI   0(R11),ZERO             CHECK IF ANY LEFT ZEROES YA01194
         BH    PUT                      NO , GO TO PUT          YA01194
         MVI   0(R11),BLANK             YES , MOVE A BLANK      YA01194
         LA    R11,1(R11)              INCREASE R11 WITH ONE    YA01194
         B     LOOP                    GO TO LOOP               YA01194
PUT      L     R4,IEHMVV33             DCB ADDRESS FOR SYSPRINT YA01194
         LA    R4,0(0,R4)              ZERO OUT THE HIGH ORDER BITS
         PUT   (R4),PRINTOUT                                    YA01194
         MVI   IEHMVV51,TRISPACE       TRIPLE SPACE BEFORE PRINT
         LH    R6,IEHMVV51+8           LINES PER PAGE
         BCTR  R6,0                    DECREASE LINE COUNT      YA01194
         STH   R6,IEHMVV51+10          RESTORE CURRENT LINE TO GO
         LH    R6,IEHMVV51+12          *
         LA    R6,1(0,6)                * ADD ONE TO PAGE NUMBER
         STH   R6,IEHMVV51+12          *
         BR    R8
FRSTNTRY DS    0H
         LR    R3,R12
         LH    R5,IEHMVV51+8           LINES PER PAGE
         STH   R5,IEHMVV51+10          INITIALIZE LINES TO GO
         BAL   R8,HEADER               GO TO HEADER PRINT ROUTINE
         B     RETURN
TRISPACE EQU   X'60'
BLANK    EQU   X'40'                                            YA01194
THREE    EQU   X'03'                                            YA01194
TWO      EQU   X'02'                                            YA01194
ONE      EQU   X'01'                                            YA01194
ZERO     EQU   X'F0'                                            YA01194
PLUS     EQU   X'F0'
INITIAL  EQU   X'FF'
*
*
         DS    0D
DOUBLE   DC    2F'0'
PRINTOUT DS    0CL121                                           YA01194
CC       DC    X'F1'                                            YA01194
         DC    CL37'SYSTEM SUPPORT UTILITIES ---- IEHMOVE'      YA01194
         DC    CL63' '                                          YA01194
         DC    CL5'PAGE'                                        YA01194
NUMB     DC    CL15' '                                          YA01194
         SPACE 2                                                YA01194
         IEHMVV
*
*
         END
