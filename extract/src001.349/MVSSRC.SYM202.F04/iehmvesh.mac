*        VSAM CATALOG WORK AREA MACRO                            Y02918
         MACRO                                                   Y02918
         IEZCTGWA
CTGWA    DSECT                                                   Y02918
CTGWAVL  DS    A                                                 Y02918
CTGWALV  DS    H                                                 Y02918
CTGWAVCT DS    H                                                 Y02918
CTGWAUCT DS    H                                                 Y02918
         MEND                                                    Y02918
*        VSAM CATALOG VOLUME LIST MACRO                          Y02918
         MACRO                                                   Y02918
         IEZCTGVL
CTGVL    DSECT                                                   Y02918
CTGVLVOL DS    XL6                                               Y02918
CTGVLDEV DS    XL4                                               Y02918
CTGVLSEQ DS    XL2                                               Y02918
CTGVLTTR DS    XL3                                               Y02918
         MEND                                                    Y02918
         TITLE '  IEHMVESH - DATA SET GROUP MODULE'              Y02918
**************************************************************** Y02918
*                                                                Y02918
*TITLE: 'IEHMVESH'                                               Y02918
*                                                                Y02918
*FUNCTION/OPERATION: DATA SET GROUP MODULE.                      Y02918
*        ON FIRST ENTRY FOR A DSGROUP REQUEST, OBTAINS THE NAMES Y02918
*        OF ALL DATA SETS IN THE GROUP VIA A VSAM GENERIC LOCATE Y02918
*        REQUEST AND WRITES A RECORD ON SYSUT1 DESCRIBING EACH   Y02918
*        NON-VSAM DATA SET IN THE GROUP. THEN SETS UP A SINGLE   Y02918
*        DATA SET MOVE/COPY REQUEST FROM A SYSUT1 RECORD EACH    Y02918
*        TIME IT IS ENTERED. WHEN ALL THE DATA SETS IN THE GROUP Y02918
*        HAVE BEEN PROCESSED, IT IS DONE.                        Y02918
*                                                                Y02918
*ENTRY POINT: IEHMVESH                                           Y02918
*                                                                Y02918
*INPUT: COMMUNICATIONS AREA FIELDS:                              Y02918
*        IEHMVV21 = ADDRESS OF DSGROUP NAME                      Y02918
*        IEHMVV40 = ADDRESS OF SYSUT1 DCB                        Y02918
*        IEHMVV40+4 = ADDRESS OF SYSUT1 DECB                     Y02918
*        IEHMVV10 = ADDRESS OF NEXT AVAILABLE COMM. AREA         Y02918
*        IEHMVV20+2 = END OF INPUT FLAG                          Y02918
*        IEHMVV20 = 'DSNAME' AND 'FROM' FLAGS                    Y02918
*                                                                Y02918
*OUTPUT: SYSUT1 RECORD FOR EACH NON-VSAM DATA SET IN GROUP       Y02918
*        COMMUNICATIONS AREA FIELDS:                             Y02918
*        IEHMVV27+16 = ADDRESS OF SYSUT1 RECORD AREA             Y02918
*        IEHMVV27+12 = DSGROUP CONTINUATION FLAG                 Y02918
*        IEHMVV10 = ADDRESS OF NEXT AVAILABLE COMM. AREA         Y02918
*        IEHMVV21 = ADDRESS OF 'FROM' DATA SET NAME              Y02918
*        IEHMVV82 = DSCB TTR FOR SINGLE VOLUME 'FROM' DATA SET   Y02918
*        IEHMVV22 = VOLUME COUNT AND LIST ADDRESS FOR 'FROM'     Y02918
*              DATA SET                                          Y02918
*                                                                Y02918
*EXTERNAL MODULES: IEHMVESU - TO PRINT ERROR MESSAGES            Y02918
*                                                                Y02918
*EXITS:  IEHMVESU - TO PROCESS A SINGLE DATA SET REQUEST         Y02918
*        IEHMVEST - WHEN A DSGROUP HAS BEEN PROCESSED AND        Y02918
*              ANOTHER REQUEST IS PENDING                        Y02918
*        IEHMVESK - WHEN A DSGROUP HAS BEEN PROCESSED AND        Y02918
*              END OF INPUT WAS ENCOUNTERED                      Y02918
*                                                                Y02918
*TABLES/WORK AREA: PERMANENT COMMUNICATIONS AREA - IEHMVV (R12)  Y02918
*        VSAM CATALOG AREAS - CTGPL (R11), CTGWA (R3),           Y02918
*              CTGVL (R2)                                        Y02918
*        SYSUT1 RECORD AREA - UT1RCD (R10)                       Y02918
*        GENERIC LOCATE WORK AREA - GLWA (R6)                    Y02918
*        GENERIC LOCATE WORK AREA ENTRY - GLNAME (R7)            Y02918
*        MODULE WORK AREA (R11)                                  Y02918
*                                                                Y02918
*ATTRIBUTES: SERIALLY REUSABLE                                   Y02918
*                                                                Y02918
*NOTES: THIS MODULE SUPPORTS DATA SET GROUP OPERATIONS FOR       Y02918
*        VS2 RELEASE 2.                                          Y02918
*                                                                Y02918
**************************************************************** Y02918
         EJECT                                                   Y02918
IEHMVESH CSECT                                                   Y02918
*A069200,095700,096100,096500,112580-113540,118000-118200,     @ZA26030
*A251100-251300                                                @ZA26030
*C095500,112500,112540,113840,118300                           @ZA26030
*D096600                                                       @ZA26030
*A193100-193200,250600                                         @ZA16446
*C193000,196000                                                @ZA16446
*A077100-077200,238008-238016,240705-240745                    @Z40CSJH
*C071500,111000,197500                                         @ZA10928
*                                                                Y02918
*A081700                                                       @ZA07397
*C071500                                                       @ZA07397
*A096500-096700                                                @ZA06561
*D096500                                                       @ZA06561
*
*        REGISTER EQUATES                                        Y02918
R0       EQU   0                                                 Y02918
R1       EQU   1                                                 Y02918
R2       EQU   2                       VOLUME LIST ENTRY BASE    Y02918
R3       EQU   3                       CATLG WORK AREA BASE AND  Y02918
*                                      WORK REG                  Y02918
R4       EQU   4                       WORK REG                  Y02918
R5       EQU   5                       GENERIC LOCATE AREA       Y02918
*                                      * USED LENGTH             Y02918
R6       EQU   6                       GENERIC LOCATE WORK AREA  Y02918
*                                      * BASE                    Y02918
R7       EQU   7                       GENERIC LOCATE NAME ENTRY Y02918
*                                      BASE                      Y02918
R8       EQU   8                       BASE REGISTER             Y02918
R9       EQU   9                       WORK REG                  Y02918
R10      EQU   10                      SYSUT1 RECORD BASE        Y02918
R11      EQU   11                      CATLG PARM LIST BASE      Y02918
R12      EQU   12                      COMM. AREA BASE           Y02918
R13      EQU   13                                                Y02918
R14      EQU   14                                                Y02918
R15      EQU   15                                                Y02918
*                                                                Y02918
*        LENGTH AND DISPLACEMENT EQUATES                         Y02918
L1       EQU   1                       LENGTH OF 1               Y02918
L2       EQU   2                       LENGTH OF 2               Y02918
L3       EQU   3                       LENGTH OF 3               Y02918
L4       EQU   4                       LENGTH OF 4               Y02918
L5       EQU   5                       LENGTH OF 5               Y02918
L6       EQU   6                       LENGTH OF 6               Y02918
L12      EQU   12                      LENGTH OF 12              Y02918
L20      EQU   20                      LENGTH OF 20              Y02918
L43      EQU   43                      LENGTH OF 43              Y02918
L44      EQU   44                      LENGTH OF 44              Y02918
*                                                                Y02918
D0       EQU   0                       DISPLACEMENT OF 0         Y02918
D1       EQU   1                       DISPLACEMENT OF 1         Y02918
D2       EQU   2                       DISPLACEMENT OF 2         Y02918
D3       EQU   3                       DISPLACEMENT OF 3         Y02918
D4       EQU   4                       DISPLACEMENT OF 4         Y02918
D6       EQU   6                       DISPLACEMENT OF 6         Y02918
D7       EQU   7                       DISPLACEMENT OF 7         Y02918
D8       EQU   8                       DISPLACEMENT OF 8         Y02918
D12      EQU   12                      DISPLACEMENT OF 12        Y02918
D16      EQU   16                      DISPLACEMENT OF 16        Y02918
D17      EQU   17                      DISPLACEMENT OF 17        Y02918
*                                                                Y02918
*        FLAG EQUATES                                            Y02918
EIP      EQU   X'20'                   END OF INPUT              Y02918
DSNAME   EQU   X'20'                   DSNAME GIVEN              Y02918
FROM     EQU   X'01'                   FROM DATA SET SET UP      Y02918
GL       EQU   0                       GENERIC LOCATE ISSUED     Y02918
SPL      EQU   4                       SUPERLOCATE ISSUED        Y02918
DSGCONT  EQU   X'80'                   DSGROUP CONTINUATION      Y02918
DUMMY    EQU   X'FF'                   DUMMY RECORD CODE         Y02918
*                                                                Y02918
*        MISCELLANEOUS EQUATES                                   Y02918
B1       EQU   1                       SHIFT 1 BIT               Y02918
B2       EQU   2                       SHIFT 2 BITS              Y02918
B8       EQU   8                       SHIFT 8 BITS              Y02918
HEX0F    EQU   X'0F'                   SIGN FOR CONVERSION       Y02918
BLANK    EQU   X'40'                   EBCDIC BLANK              Y02918
DOT      EQU   X'4B'                   EBCDIC DOT              @ZA26030
NONVSAM  EQU   X'C1'                   NON-VSAM DATA SET         Y02918
M3       EQU   3                       MASK FOR ICM              Y02918
*                                                                Y02918
*        PROLOGUE                                                Y02918
         SAVE  (14,12),T,IEHMVESH-OZ26030                      @ZA26030
         BALR  R8,R0                   ESTABLISH ADDR            Y02918
         USING *,R8                                              Y02918
         IEHPRE (14,1),TF                                        Y02918
*        ASSEMBLER USING STATEMENTS                              Y02918
         USING CTGVL,R2                                          Y02918
         USING CTGWA,R3                                          Y02918
         USING GLWA,R6                                           Y02918
         USING GLNAME,R7                                         Y02918
         USING UT1RCD,R10                                        Y02918
         USING CTGPL,R11                                         Y02918
         USING IEHMVV,R12                                        Y02918
CVOLTEST TM    IEHMVV20+1,X'40'        IS CVOL BIT ON - IF YES @Z40CSJH
         BO    CVOLNTRY                BR TO CVOL PROCESSING   @Z40CSJH
         TITLE ' IEHMVESH - GROUP SET-UP'                        Y02918
*                                                                Y02918
*        ON FIRST ENTRY, OBTAIN LIST OF DATA SETS IN GROUP.      Y02918
*        ON SUBSEQUENT ENTRIES, SET UP NEXT MOVE/COPY REQUEST.   Y02918
*                                                                Y02918
         TM    IEHMVV27+D12,DSGCONT    DSGROUP CONTINUATION      Y02918
         BZ    GLREQ                   NO - NEED LIST OF NAMES   Y02918
         L     R10,IEHMVV27+D16        SET SYSUT1 RECORD ADDRESS Y02918
         LA    R2,UT1VLST              SET VOL LIST ADDRESS      Y02918
         NI    IEHMVV20+2,X'BE'        TURN OFF ABORT BITS     @ZA07397
         B     RDUT1                   READ NEXT SYSUT1 RECORD   Y02918
*                                                                Y02918
*        FIRST ENTRY. OBTAIN THE NAMES OF THE DATA SETS IN THE   Y02918
*        GROUP VIA A VSAM GENERIC LOCATE.                        Y02918
*                                                                Y02918
GLREQ    L     R10,IEHMVV10            USE NEXT AVAILABLE AREA   Y02918
         ST    R10,IEHMVV27+D16        * IN COMM. AREA FOR       Y02918
*                                      * SYSUT1 RECORD AREA      Y02918
         LA    R2,UT1VLST              SET VOL LIST BASE         Y02918
         LA    R11,IEHMVV00            SET MODULE WORK AREA BASE Y02918
         XC    CTGPL(CTGPLLEN),CTGPL   INITIALIZE PARM LIST      Y02918
         XC    UT1OVHD,UT1OVHD         INITIALIZE SYSUT1 RECORD  Y02918
*        SET UP GENERIC LOCATE PARM LIST AND WORK AREA           Y02918
         OI    CTGOPTN1,CTGNAME+CTGGENLD SET GENREIC LOCATE      Y02918
         OI    CTGOPTN3,CTGSUPLT+CTGAM0 * OPTION FLAGS           Y02918
         LA    R9,GLREQNM              SET GENERIC DATA SET      Y02918
         ST    R9,CTGENT               * NAME ADDRESS            Y02918
*        SET UP GENERIC DATA SET NAME ENTRY                      Y02918
         L     R4,IEHMVV21             SET NAME ADDRESS          Y02918
         SR    R3,R3                   INITIALIZE LEGNTH         Y02918
GNL      CLI   D0(R4),BLANK            BLANK AFTER NAME          Y02918
         BE    SETNL                   YES - END OF NAME         Y02918
         LA    R3,D1(R3)               INCREMENT LENGTH BY 1     Y02918
         LA    R4,D1(R4)               INCREMENT NAME POINTER    Y02918
         CH    R3,MAXNL                MAX LENGTH REACHED        Y02918
         BH    SET43                   YES - USE LENGTH          Y02918
         B     GNL                     NO - CONTINUE SCAN        Y02918
SET43    MVC   D0(L1,R9),MAXNL+1       SET LENGTH TO MAX       @ZA26030
         STC   R3,NLENGTH              SET LENGTH DETERMINED   @ZA26030
         B     SETNM                   SET NAME IN ENTRY         Y02918
SETNL    EQU   *                                               @ZA26030
         STC   R3,D0(R9)               SET LENGTH DETERMINED   @ZA06561
         STC   R3,NLENGTH              SET LENGTH DETERMINED   @ZA26030
SETNM    L     R4,IEHMVV21             SET NAME ADDRESS          Y02918
         MVC   D1(L43,R9),D0(R4)       SET NAME IN ENTRY         Y02918
*                                                                Y02918
*        OBTAIN INITIAL AREA FOR GENERIC LOCATE WORK AREA AND    Y02918
*        SAVE ITS SIZE IN CASE IT IS INSUFFICIENT AND MUST BE    Y02918
*        FREED                                                   Y02918
*                                                                Y02918
         L     R9,C904                 SET INITIAL WORK AREA     Y02918
         ST    R9,GLSIZE               * SIZE AND SAVE           Y02918
         GETMAIN R,LV=904              OBTAIN WORK AREA          Y02918
         ST    R1,CTGWKA               SET WORK AREA ADDRESS     Y02918
         LR    R6,R1                   SET WORK AREA BASE        Y02918
*        ENTER HERE TO REISSUE GENERIC LOCATE WITH A LARGER WORK Y02918
*        AREA                                                    Y02918
GLRE     STH   R9,GLWALEN              SET WORK AREA LENGTH      Y02918
         CATLG MF=(E,CTGPL)            ISSUE GENERIC LOCATE      Y02918
*        TEST RETURN CODE FROM CATALOG MANAGEMENT                Y02918
         LA    R1,GL                   INDICATE GENERIC LOCATE   Y02918
         BAL   R14,LOCRCK              ANALIZE RETURN CODE       Y02918
         B     GLOK                    LOCATE SUCCESSFUL         Y02918
         B     GLRE                    REISSUE REQUEST           Y02918
         B     DSGDNE                  ERROR - TERMINATE REQUEST Y02918
         EJECT                                                   Y02918
*                                                                Y02918
*        GENERIC LOCATE WAS SUCCESSFUL. SCAN DATA SET NAME       Y02918
*        ENTRIES FOR NON-VSAM DATA SETS                          Y02918
*                                                                Y02918
GLOK     SR    R5,R5                                             Y02918
         ICM   R5,3,GLWANLEN           GET LENGTH OF NAME AREA @ZA10928
         SH    R5,NMOV                 SUBTRACT OVERHEAD         Y02918
         LA    R7,GLWAFNME             GET FIRST NAME ENTRY ADDR Y02918
TSTNV    CLI   GLENTYP,NONVSAM         DATA SET VSAM           @ZA26030
         BNE   ENTDNE                  YES- CANT HONOR REQUEST @ZA26030
* IF LENGTH IS 44 OR IF BLANK AFTER NAME THEN IT IS A          @ZA26030
* FULLY QUALIFIED NAME AND THE DATA SET SHALL BE COPIED.       @ZA26030
         ST    R3,SAVEREG3             SAVE REG3               @ZA26030
         ST    R7,SAVEREG7             SAVE REG7               @ZA26030
         SR    R3,R3                   CLEAR REG 3             @ZA26030
         IC    R3,NLENGTH              GET LENGTH OF NAME      @ZA26030
         LA    R9,44                   SET MAX NAME EQUAL 44   @ZA26030
         SR    R9,R3       FULLY QUALIFIED NAME OF 44 CHARS ?  @ZA26030
         BNZ   NOT44                   NO - GO ON              @ZA26030
         CLC   D0(L44,R4),D1(R7)       YES - CHECK IF THIS     @ZA26030
*                                      IS THE NAME WE ARE      @ZA26030
*                                      LOOKING FOR.            @ZA26030
         BNE   RESTORE                 NO - CANT HONOR REQUEST @ZA26030
         BE    DSNV                    YES - CAN HONOR REQUEST @ZA26030
NOT44    EQU   *                                               @ZA26030
         LA    R3,D1(R3)          ADD ONE TOO POINT AFTER NAME @ZA26030
         AR    R7,R3                   POINT AFTER NAME        @ZA26030
         CLI   D0(R7),BLANK           IS IT A BLANK AFTER NAME @ZA26030
         BE    DSNV                    YES - CAN HONOR REQUEST @ZA26030
* IF A DOT AFTER NAME THE DATA SET SHALL BE COPIED IN OTHER    @ZA26030
* CASE GET NEXT ENTRY.                                         @ZA26030
         CLI   D0(R7),DOT              IS IT A DOT AFTER NAME  @ZA26030
         BE    DSNV                    YES - CAN HONOR REQUEST @ZA26030
RESTORE  EQU   *                                               @ZA26030
         L     R7,SAVEREG7             RESTORE REG7            @ZA26030
         L     R3,SAVEREG3             RESTORE REG3            @ZA26030
ENTDNE   EQU   *                                               @ZA26030
         SH    R5,GLWAELEN             ALL ENTRIES PROCESSED   @ZA26030
         BNP   DUMWT                   YES - WRITE DUMMY RCD     Y02918
         AH    R7,GLWAELEN             POINT TO NEXT ENTRY       Y02918
         B     TSTNV                   TEST ENTRY TYPE           Y02918
         EJECT                                                   Y02918
*                                                                Y02918
*        DATA SET IS NON-VSAM. ISSUE SUPERLOCATE TO OBTAIN       Y02918
*        IT'S VOLUME LIST                                        Y02918
*                                                                Y02918
DSNV     EQU   *                                               @ZA26030
         L     R7,SAVEREG7             RESTORE REG7            @ZA26030
         L     R3,SAVEREG3             RESTORE REG3            @ZA26030
         XC    CTGPL(CTGPLLEN),CTGPL   INITIALIZE PARM LIST    @ZA26030
         OI    CTGOPTN3,CTGSUPLT+CTGAM0 SET SUPERLOCATE OPTIONS  Y02918
         LA    R3,SPWA                 SET WORK AREA ADDRESS     Y02918
         ST    R3,CTGWKA               * IN PARM LIST            Y02918
         LA    R9,GLDSNM               SET DATA SET NAME ADDRESS Y02918
         ST    R9,CTGENT                                         Y02918
         LA    R9,GLREQNM              SET CVOL RETURN AREA      Y02918
         ST    R9,CTGCVOL              * ADDRESS                 Y02918
         ST    R2,CTGWAVL              SET VOLUME LIST ADDRESS   Y02918
         MVC   CTGWALV,VLLEN           SET VOLUME LIST LENGTH    Y02918
         CATLG MF=(E,CTGPL)            ISSUE SUPERLOCATE         Y02918
*        TEST RETURN CODE FROM CATALOG MANAGEMENT                Y02918
         LA    R1,SPL                  INDICATE SUPERLOCATE      Y02918
         BAL   R14,LOCRCK              ANALIZE RETURN CODE       Y02918
         B     SPLOK                   SUCCESSFUL                Y02918
         B     SPLOK                   SUCCESSFUL                Y02918
         B     ENTDNE                  ERROR - SKIP DATA SET     Y02918
         EJECT                                                   Y02918
*                                                                Y02918
*        SUPERLOCATE WAS SUCCESSFUL. CONSTRUCT DATA SET RECORD.  Y02918
*                                                                Y02918
SPLOK    MVC   UT1NAME(44),GLDSNM      SET DATA SET NAME         Y02918
         SR    R9,R9                                             Y02918
         LH    R9,CTGWAVCT             GET VOLUME COUNT          Y02918
         CH    R9,MAXVC                MORE THAN MAX VOLUMES     Y02918
         BNH   SETVC                   NO - SET VOL COUNT        Y02918
         MVC   UT1VLCT,MAXVC           SET VOL COUNT TO MAX      Y02918
         B     MULTVL                  CONTINUE                  Y02918
SETVC    MVC   UT1VLCT,CTGWAVCT        SET VOL COUNT RETURNED    Y02918
         CH    R9,ONEVOL               ONE VOLUME DATA SET       Y02918
         BNE   MULTVL                  NO - NO TTR               Y02918
         MVC   UT1TTR,CTGVLTTR         SET DSCB TTR              Y02918
         B     SKPTTRC                 CONTINUE                  Y02918
MULTVL   XC    UT1TTR,UT1TTR           SET DSCB TTR TO 0         Y02918
*        SUPERLOCATE RETURNS INFORMATION IN ORDER:               Y02918
*              VOLSER,DEVICE CODE,SEQ NO                         Y02918
*        IEHMOVE REQUIRES INFORMATION IN ORDER:                  Y02918
*              DEVICE CODE,VOLSER,SEQ NO                         Y02918
*        SO EACH ENTRY MUST BE REORDERED                         Y02918
SKPTTRC  LR    R4,R9                   SET VOL COUNT             Y02918
REORDER  MVC   TEMPVL,CTGVLVOL         SAVE VOLSER               Y02918
         MVC   MVEVLDEV(L4),CTGVLDEV   REORDER DEVICE CODE       Y02918
         MVC   MVEVLVOL(L6),TEMPVL     REORDER VOLSER            Y02918
         LA    R2,D12(R2)              GET NEXT ENTRY ADDRESS    Y02918
         BCT   R4,REORDER              LOOP UNTIL DONE           Y02918
         LA    R2,UT1VLST              RESET TO FIRST ENTRY      Y02918
*        COMPUTE LENGTH OF VOLUME LIST PORTION OF RECORD         Y02918
         MH    R9,VLELEN               GET VOLUME LIST LENGTH    Y02918
         AH    R9,VLOVHD               ADD OVERHEAD              Y02918
         STCM  R9,M3,UT1VLLEN          SET LENGTH                Y02918
*                                                                Y02918
*        WRITE DATA SET RECORD TO SYSUT1                         Y02918
*                                                                Y02918
         BAL   R14,RCDWRT              WRITE RECORD              Y02918
         B     ENTDNE                  GET NEXT ENTRY            Y02918
         EJECT                                                   Y02918
*                                                                Y02918
*        ALL NON-VSAM DATA SETS IN THE GROUP ARE NOW             Y02918
*        REPRESENTED ON SYSUT1. WRITE A DUMMY RECORD.            Y02918
*                                                                Y02918
DUMWT    MVI   UT1CDE,DUMMY            SET DUMMY CODE            Y02918
         SR    R9,R9                                             Y02918
         STCM  R9,M3,UT1VLLEN          SET VOLUME LIST LENGTH    Y02918
         BAL   R14,RCDWRT              WRITE RECORD              Y02918
*                                                                Y02918
*        FREE GENERIC LOCATE AREA                                Y02918
*        REPOSITION SYSUT1 TO BEGIN PROCESSING AT FIRST RECORD   Y02918
*                                                                Y02918
RDPT     BAL   R14,GLFREE              FREE GENERIC LOCATE AREA  Y02918
         L     R3,IEHMVV40             SET SYSUT1 DCB ADDRESS    Y02918
         POINT (R3),RCD1               POINT TO FIRST RECORD     Y02918
         TITLE ' IEHMVESH - DATA SET SET-UP'                     Y02918
*                                                                Y02918
*        FIRST OR SUBSEQUENT ENTRIES. READ A RECORD FROM SYSUT1  Y02918
*        AND SET UP FIELDS IN COMMUNICATION AREA TO PERFORM A    Y02918
*        SINGLE DATA SET MOVE/COPY REQUEST FOR THE FROM VOLUME   Y02918
*        REPRESENTED BY THE SYSUT1 RECORD.                       Y02918
*                                                                Y02918
RDUT1    L     R3,IEHMVV40             SET SYSUT1 DCB ADDRESS    Y02918
         L     R4,IEHMVV40+4           SET SYSUT1 DECB ADDRESS   Y02918
         READ  (R4),SF,(R3),UT1RCD,'S',MF=E READ UT1 RECORD      Y02918
         CHECK (R4)                    WAIT FOR COMPLETION       Y02918
         TM    UT1CDE,DUMMY            DUMMY RECORD              Y02918
         BO    DSGDNE                  YES - DSGROUP DONE        Y02918
*                                                                Y02918
*        SET UP ADDRESS OF 'FROM' DATA SET NAME IN IEHMVV21.     Y02918
*        FOR SINGLE VOLUME DATA SETS, IEHMVV82 WILL CONTAIN      Y02918
*        THE DSCB TTR OF THE 'FROM' DATA SET.                    Y02918
         LA    R9,UT1NAME              SET FROM DSNAME ADDRESS   Y02918
         ST    R9,IEHMVV21                                       Y02918
         MVC   IEHMVV82(L3),UT1TTR     SET UP DSCB TTR           Y02918
*        SET UP A LIST OF 17 BYTE 'FROM' VOLUME ENTRIES USING    Y02918
*        THE 12 BYTE ENTRIES IN THE SYSUT1 RECORD. PLACE THE     Y02918
*        VOLUME COUNT IN THE FIRST BYTE OF IEHMVV22 AND THE      Y02918
*        ADDRESS OF THE 'FROM' VOLUME LIST IN BYTES 2-4 OF       Y02918
*        IEHMVV22.                                               Y02918
         SR    R9,R9                   DETERMINE NEXT AVAILABLE  Y02918
         ICM   R9,M3,UT1VLLEN          * ADDRESS IN COMM. AREA   Y02918
*                                      * AFTER SYSUT1 RECORD     Y02918
         LA    R9,UT1NAME(R9)          * AREA - BUILD 'FROM'     Y02918
         LA    R9,D8(R9)               VOL LIST THERE            Y02918
         ST    R9,IEHMVV22             SET 'FROM' VOL LIST ADDR  Y02918
         SR    R4,R4                                             Y02918
         LH    R4,UT1VLCT              SET VOLUME COUNT          Y02918
         LTR   R4,R4                   NO VOLUMES                Y02918
         BZ    RDUT1                   YES - SKIP DATA SET       Y02918
         STC   R4,IEHMVV22             SET FROM VOLUME COUNT     Y02918
LOOP2    MVC   D0(L12,R9),CTGVL        MOVE UT1 ENTRY TO 'FROM'  Y02918
         XC    D12(L5,R9),D12(R9)      * LIST AND CLEAR EXTRA    Y02918
         LA    R9,D17(R9)              SET NEXT 'FROM' ENTRY     Y02918
         LA    R2,D12(R2)              SET NEXT UT1 ENTRY        Y02918
         BCT   R4,LOOP2                LOOP UNTIL VOL CT IS 0    Y02918
*        SET UP A POINTER TO THE NEXT AVAILABLE FULLWORD IN THE  Y02918
*        COMMUNICATIONS AREA                                     Y02918
         LA    R9,D3(R9)                                         Y02918
         SRL   R9,B2                                             Y02918
         SLL   R9,B2                                             Y02918
         ST    R9,IEHMVV10             SET NEXT AVAIL. ADDRESS   Y02918
*                                                                Y02918
*        SET FLAGS FOR A SINGLE DATA SET REQUEST AND DATA SET    Y02918
*        GROUP CONTINUATION.                                     Y02918
*                                                                Y02918
         OI    IEHMVV20,DSNAME+FROM    SET DSNAME AND FROM FLAGS Y02918
         OI    IEHMVV27+D12,DSGCONT    SET DSGROUP CONT. FLAG    Y02918
         B     DSPRCS                  PROCESS THE DATA SET      Y02918
         TITLE ' IEHMVESH - EXITS'
*        EXIT TO IEHMVESZ TO PROCESS THE DATA SET REQUEST        Y02918
DSPRCS   IEHPOST (14,1),T                                        Y02918
         XCTL   (2,12),EP=IEHMVESZ                               Y02918
*                                                                Y02918
*        AN ENTIRE DATA SET GROUP HAS BEEN PROCESSED. IF ANOTHER Y02918
*        REQUEST IS PENDING, EXIT TO IEHMVEST, OTHERWISE EXIT TO Y02918
*        IEHMVESK.                                               Y02918
DSGDNE   TM    IEHMVV20+D2,EIP         END OF INPUT              Y02918
         BO    ALLDNE                  YES - WRAP UP RUN         Y02918
NXTREQ   IEHPOST (14,1),T                                        Y02918
         XCTL (2,12),EP=IEHMVEST                                 Y02918
ALLDNE   IEHPOST (14,1),T                                        Y02918
         XCTL  (2,12),EP=IEHMVESK                                Y02918
         TITLE ' IEHMVESH - SUBROUTINES'                         Y02918
*                                                                Y02918
*        THIS ROUTINE ANALIZES THE RETURN CODE FROM CATALOG      Y02918
*        MANAGEMENT AFTER A GENERIC LOCATE OR SUPERLOCATE        Y02918
*                                                                Y02918
LOCRCK   ST    R14,SVRTN               SAVE RETURN ADDRESS       Y02918
         LTR   R15,R15                 RC=0                      Y02918
         BZ    RET0                    YES - LOCATE SUCCESSFUL   Y02918
         CH    R15,RC8                 RC=8                      Y02918
         BE    ANYNMS                  YES                       Y02918
         CH    R15,RC40                RC=40                     Y02918
         BE    SMALL                   YES                     @ZA16446
         CH    R15,RC44                RC=44                   @ZA16446
         BNE   ERR                     NO - UNUSUAL CONDITION  @ZA16446
         SPACE 2                                                 Y02918
*        RETURN CODE INDICATES THAT THE WORK AREA WAS TOO SMALL  Y02918
*        IF REQUEST WAS SUPERLOCATE, EXTRA ENTRIES ARE NOT       Y02918
*        NEEDED. IF REQUEST WAS GENERIC LOCATE, TRY TO GET CORE  Y02918
*        NEEDED TO RE-ISSUE THE REQUEST.                         Y02918
SMALL    LTR   R1,R1                   SUPERLOCATE             @ZA16446
         BP    RET4                    YES - ACCEPT ERROR        Y02918
         SR    R9,R9                                             Y02918
         ICM   R9,3,GLWANLEN           SET FEEDBACK AREA SIZE  @ZA10928
         ST    R9,GLFDBK               SAVE                      Y02918
         BAL   R14,GLFREE              FREE ORIGINAL WORK AREA   Y02918
         L     R9,GLFDBK               SET FEEDBACK SIZE         Y02918
         ST    R9,GLSIZE               SAVE                      Y02918
         LA    R6,CTGWKA               SET WORK AREA PTR ADDR    Y02918
         GETMAIN EC,LV=(R9),A=(R6)     ISSUE COND. GETMAIN       Y02918
         L     R6,CTGWKA               SET WORK AREA BASE        Y02918
         LTR   R15,R15                 WAS GETMAIN SUCCESSFUL    Y02918
         BNZ   NOSPC                   NO                        Y02918
*        RETURN AT NSI + 4 - SPACE INSUFFICIENT                  Y02918
RET4     LA    R15,D4                  SET RETURN OFFSET         Y02918
         B     RETB                    RETURN                    Y02918
         SPACE 2                                                 Y02918
*        COULD NOT OBTAIN SPACE REQUIRED FOR THE GENERIC LOCATE. Y02918
*        ISSUE MESSAGE IEH351I AND RETURN AT OFFSET +8.          Y02918
NOSPC    L     R3,ADSGM2               SET MESSAGE ADDRESS       Y02918
*        LINK TO IEHMVESU TO PRINT ERROR MESSAGE                 Y02918
MSGLNK   LINK  EP=IEHMVESU             PRINT MESSAGE             Y02918
         LTR   R15,R15                 PERM. I/O ERROR           Y02918
         BP    ALLDNE                  YES - TERMINATE RUN       Y02918
*        RETURN AT NSI + 8 - ERROR MESSAGE PRINTED               Y02918
         LA    R15,D8                  SET RETURN OFFSET         Y02918
         B     RETB                    RETURN                    Y02918
         SPACE 2                                                 Y02918
*        LOCATE WAS SUCCESSFUL. IF NO NAMES WERE RETURNED FOR    Y02918
*        GENERIC LOCATE REQUEST, ISSUE MESSAGE IEH348I AND       Y02918
*        RETURN AT OFFSET +8.                                    Y02918
ANYNMS   LTR   R1,R1                   GENERIC LOCATE            Y02918
         BZ    NONMS                   YES                       Y02918
*        RETURN AT NSI - CONTINUE PROCESSING                     Y02918
RET0     SR    R15,R15                 SET RETURN OFFSET         Y02918
RETB     L     R14,SVRTN               RESTORE RETURN ADDRESS    Y02918
         B     D0(R15,R14)             RETURN                    Y02918
*        NO NAMES WERE FOUND FOR DSGROUP SPECIFIED.              Y02918
NONMS    L     R3,ADSGM1               SET MESSAGE ADDRESS       Y02918
*        FREE GOTTEN CORE - REQUEST WILL BE TERMINATED           Y02918
MSGFRE   BAL   R14,GLFREE              FREE GENERIC LOCATE AREA  Y02918
         B     MSGLNK                  PRINT MESSAGE             Y02918
         SPACE 2                                                 Y02918
*        RETURN CODE INDICATES AN UNUSUAL CONDITION.             Y02918
*        FOR GENERIC LOCATE, ISSUE MESSAGE IEH354I.              Y02918
*        FOR SUPERLOCATE, ISSUE MESSAGE IEH356I. RETURN AT       Y02918
*        OFFSET +8.                                              Y02918
ERR      L     R3,AERRMT(R1)           SET MESSAGE MODEL ADDR    Y02918
         MVC   ERRMSG,D0(R3)           MOVE MESSAGE TO WORK AREA Y02918
         LA    R3,ERRMSG               SET MESSAGE ADDRESS       Y02918
         LR    R9,R1                   SET LOCATE TYPE OFFSET    Y02918
         SRL   R9,B1                   DIVIDE OFFSET BY 2        Y02918
         SR    R4,R4                                             Y02918
         LH    R4,ARCT(R9)             SET RC OFFSET             Y02918
         LA    R4,D0(R4,R3)            SET RC ADDRESS            Y02918
         CVD   R15,CVTAR               CONVERT RC TO EBCDIC      Y02918
         OI    CVTAR+D7,HEX0F                                    Y02918
         UNPK  D0(L3,R4),CVTAR+6(L2)   PLACE RC IN MESSAGE       Y02918
         LTR   R1,R1                   SUPERLOCATE               Y02918
         BZ    MSGFRE                  NO - TERMINATE            Y02918
         SR    R4,R4                                             Y02918
         LH    R4,ADSNM                SET DSNAME OFFSET         Y02918
         LA    R4,D0(R4,R3)            SET DSNAME ADDRESS        Y02918
         MVC   D0(L44,R4),GLDSNM       PUT DSNAME IN MESSAGE     Y02918
         B     MSGLNK                  SKIP DATA SET             Y02918
         EJECT                                                   Y02918
*        THIS ROUTINE FREES THE CORE OBTAINED FOR THE            Y02918
*        GENERIC LOCATE WORK AREA                                Y02918
GLFREE   ST    R14,GLFSV               SAVE RETURN ADDRESS       Y02918
         L     R9,GLSIZE               SET AREA SIZE             Y02918
         FREEMAIN R,LV=(R9),A=(R6)     FREE CORE                 Y02918
         L     R14,GLFSV               RESTORE RETURN ADDRESS    Y02918
         BR    R14                     RETURN                    Y02918
         EJECT                                                   Y02918
*        THIS ROUTINE WRITES A RECORD ON SYSUT1                  Y02918
RCDWRT   ST    R14,SVRTN               SAVE RETURN ADDRESS       Y02918
         SR    R9,R9                                             Y02918
         ICM   R9,M3,UT1VLLEN          SET VOL LIST LENGTH       Y02918
         AH    R9,RCDOVHD              ADD OVERHEAD              Y02918
         L     R4,IEHMVV40             SET SYSUT1 DCB ADDRESS    Y02918
         L     R3,IEHMVV40+4           SET SYSUT1 DECB ADDRESS   Y02918
         WRITE (R3),SF,(R4),UT1RCD,(R9),MF=E                     Y02918
         CHECK (R3)                                              Y02918
         L     R14,SVRTN               RESTORE RETURN ADDRESS    Y02918
         BR    R14                     RETURN                    Y02918
***********************************************************************
*       LINES 238020-241109 WERE REACTIVATED                   @Z40CSJH
*       IN SUPPORT OF THE MVS EXTENDED                         @Z40CSJH
*       CVOL FUNCTION                                          @Z40CSJH
***********************************************************************
CVOLNTRY EQU   *
         TM    XSWITCHS,X'08'          IS CLOSE SW ON
         BO    CLOSE2                  BRANCH IF YES
         L     R4,IEHMVV30+8  ADDR OF BEGINNING OF PERM WORK AREA
*                             USED BY DSGROUP LOAD 1
         ST    R4,IEHMVV10             RECLAIM PERM WORK AREA
         OI    XSWITCHS,X'08'          TURN ON CLOSE SW
CLOSE2   DS    0H
         TM    XSWITCHS,X'80'           IS FIRST IN SW ON
         BO    REQCOMP
         MVC   XPOINTER(4),IEHMVV10
         L     R5,IEHMVV41             LOAD UT2 DCB ADDR
         BAL   R10,POINT                GO TO POINT ROUTINE
READ1    L     R4,IEHMVV40+4            LOAD UT1 DECB ADDR
         L     R5,IEHMVV40              LOAD UT1 DCB ADDR
         L     R6,XPOINTER
*XXXXX   R6 CONTAINS THE ADDRESS OF THE PERM WORK AREA
READ2    READ  (R4),SF,(R5),(R6),'S',MF=E
         CHECK (R4)
         CLI   19(R6),X'FF'
         BE    DWRITE1
         CLI   19(R6),X'00'
         BE    REQSET
****** POINTER TO BLOCK IN R6 REFERS TO DATA *****
****** READ FROM SYSUT1 INTO THE PERM WORK AREA **
WRITE1   L     R6,XPOINTER              POINTER TO BLOCK IN R6
         SR    R3,R3
         IC    R3,17(R6)                FIRST L OF LL
         SLL   R3,8
         IC    R3,18(R6)                SECOND L OF LL
      LA   R3,12(R3)         UP LENGTH
         L     R4,IEHMVV41+4            LOAD DECB ADDR
         L     R5,IEHMVV41              LOAD DCB ADDR
****** R4 HAS THE DECB ADDR OF SYSUT2 *****
****** R5 HAS THE DCB ADDR OF SYSUT2  *****
****** WRITE3 WRITES A BLOCK FROM PERM*****
****** WORK AREA TO SYSUT2            *****
WRITE3   WRITE (R4),SF,(R5),(R6),(R3),MF=E
         CHECK (R4)
         TM    XSWITCHS,X'40'           IS END OF UT1 SW ON
         BO    CATOPT
         B     READ1
DWRITE1  OI    XSWITCHS,X'40'           TURN ON END OF UT1 SW
         B     WRITE1
POINT    LA    R0,256         SET TTR2 OF 0010 IN REG
         ST    R0,IEHMVV00
         LR    R0,R12         PLACE ADDR OF COMM AREA IN R0
         L     R1,IEHMVV40              LET R1 POINT AT UT1 DCB
***** REPOSITION SYSUT1 TO READ THE NEXT BLOCK *****
         POINT (1),(0)                  REPOSITION UT1
         L     R1,IEHMVV41              LET R1 POINT AT UT2 DCB
         LR    R0,R12
***** REPOSITION SYSUT2 TO WRITE THE NEXT BLOCK *****
         POINT (1),(0)                  REPOSITION UT2
         BR    R10            GO TO READ1
REQCOMP  L     R6,XPOINTER
         L     R7,IEHMVV10             R7 NOW CONTAINS A PTR TO A LIST
*                                      OF VOL ENTRIES FOR THE DATA SET
*                                      THAT WAS JUST HANDLED
         ST    R6,IEHMVV10
         NI    IEHMVV20+1,X'FC'         INITIALIZE COMM SWITCHES
         NI    IEHMVV20+2,X'6E'
         NI    IEHMVV20+3,X'77'
         TM    IEHMVV20+2,X'41'         ARE ABORT BITS ON
         BNE   REQCOMP1                 BRANCH IF YES
         SR    R2,R2
         IC    R2,1(R7)                INSERT COUNT OF TO VOL ENTRIES
         LA    R3,12
         MR    R2,R2
         LR    R4,R3                    SAVE LENGTH
         LA    R3,1(R3)
         LR    R2,R7                   LOAD PTR TO TO VOL ENTRIES
         EX    R3,EXCINST
         LA    R4,46(R4)                BUMP LENGTH BY 46
         STC   R4,18(R6)
         SRL   R4,8
         STC   R4,17(R6)                STORE COUNT IN RECORD
         MVC   12(3,6),IEHMVV82+4 PUT NEW TTR IN SYSUT1 RECORD   S21042
         XC    IEHMVV82+4(3),IEHMVV82+4 CLEAR TTR FIELD          S21042
         B     WRITE1
REQCOMP1 NI    IEHMVV20+2,X'BE'         TURN OFF ABORTS BITS
         B     READ1
EXCINST  MVC   64(1,R6),0(R2)
REQSET   LA    R7,20(R6)
***** STORE ADDR OF FROM DSNAME *****
         ST    R7,IEHMVV21
         MVC   IEHMVV82(3),12(6)       SAVE OLD DSCBTTR          S21042
         ST    R6,XPOINTER             SAVE ADDRESS OF RECORD    A27712
         SR    R7,R7                   CLEAR  REGISTER SEVEN     A27712
         IC    R7,17(R6)               IN0ERT EN R8  557 8       A27712
         SLL   R7,8                    COMPUTE LENGTH            A27712
         IC    R7,18(R6)               OF RECORD                 A27712
         AR    R6,R7                                             A27712
         LA    R6,28(R6)          POINTER TO AVAIL FREE AREA     A27712
         ST    R6,IEHMVV22             ADDRESS OF VOLUME ENTRIES A27712
         L     R5,XPOINTER             FIND OLD VOLUME LIST      A27712
         SR    R7,R7                   CLEAR REG 7               A27712
         IC    R7,65(R5)               NUMBER OF ENTRIES IN LIST A27712
         LTR   R7,R7                   IS VOL. COUNT 0?          A30287
         BZ    READ1                   YES-INDEX WITH NO DATA    A30287
*                                      WAS FOUND. IGNORE IT.     A30287
         STC     R7,IEHMVV22           SAVE NUMBER OF ENTRIES    A27712
         LA    R5,66(R5)               POINT TO OLD LIST         A27712
****     R5=ADDRESS OF OLD 12 BYTE ENTRY  R6=ADDRESS OF NEW      A27712
****     17 BYTE ENTRY   R7=NUMBER OF ENTRIES IN LIST            A27712
LOOP     MVC   0(12,R6),0(R5)          MOVE VOLUME LIST          A27712
         XC    12(5,R6),12(R6)         CLEAR AREA FOR VOLMOUNT   A27712
         LA    R6,17(R6)               POINT TO NEXT ENTRY       A27712
         LA    R5,12(R5)               POINT TO NEXT ENTRY       A27712
         BCT   R7,LOOP                 ANY MORE ENTRIES          A27712
         LA    R6,3(R6)                ALIGN FOR NEST FULL WORD  A27712
         SRL   R6,2                    BOUNDRY                   A27712
         SLL   R6,2                                              A27712
         ST    R6,IEHMVV10         SAVE ADDRESS FOR NEXT MODULE  A27712
         OI    IEHMVV20,X'21'          TURN ON DSNAME AND FROM   A27712
         OI    XSWITCHS,X'80'          TURN ON FIRST IN SW
         B     EXIT1
EXIT1  IEHPOST (14,1),T
         XCTL  (2,12),EP=IEHMVESZ
EXIT2  TM  IEHMVV20,X'10'              IS DSGROUP SW ON        @Z40CSJH
         BO    EXIT3                    YES-CATLGING ALL DONE  @Z40CSJH
       IEHPOST (14,1),T
         XCTL  (2,12),EP=IEHMVETA
EXIT3    TM    IEHMVV20+2,X'20'
         BO    EXIT4
       IEHPOST (14,1),T
         XCTL  (2,12),EP=IEHMVEST
EXIT4  IEHPOST (14,1),T
         XCTL  (2,12),EP=IEHMVESK
CATOPT   OI    IEHMVV20,X'04'           TURN ON CATALOG REQ BIT
         TM    IEHMVV20,X'80'           IS MOVE BIT ON
         BZ    CATOPT1
         TM    IEHMVV20+1,X'20'         IS UNCATLG BIT ON
         BZ    CATOPT4
         MVC   IEHMVV23(4),IEHMVV24
         B     EXIT2
CATOPT4  DS    0H
         NI    IEHMVV20,X'7F'           TURN OFF MOVE BIT
         OI    IEHMVV20,X'40'           TURN ON  COPY BIT
         OI    IEHMVV20+1,X'20'         TURN ON  UNCATLG BIT
         MVC   IEHMVV23(4),IEHMVV24    MOVE CVOL PTR TO TO VOL SLOT
         B     EXIT2
CATOPT1  TM    IEHMVV20+1,X'30'         CATLG OR UNCATLG BIT ON
         BZ    EXIT3
         BO    CATOPT2
         TM    IEHMVV20+1,X'20'         IS UNCATLG ON
         BO    CATOPT3
         B     EXIT2
CATOPT2  NI    IEHMVV20,X'BF'           TURN OFF COPY BIT
         OI    IEHMVV20,X'80'           TURN ON  MOVE BIT
         NI    IEHMVV20+1,X'DF'        TURN OFF UNCATLG BIT
         B     EXIT2
CATOPT3  NI    IEHMVV20,X'BF'           TURN OFF COPY BIT
         OI    IEHMVV20,X'80'           TURN ON MOVE BIT
         B     EXIT2
         TITLE ' IEHMVESH - CONSTANTS'                           Y02918
*                                                                Y02918
*        CONSTANTS                                               Y02918
*                                                                Y02918
ADSGM1   DC    A(DSGNONE)              NO D/S IN GROUP MSG       Y02918
ADSGM2   DC    A(DSGNOSPC)             NO SPACE FOR NAMES MSG    Y02918
AERRMT   DC    A(DSGSRFL)              DSGROUP LOCATE FAILED MSG Y02918
         DC    A(DSESRFL)              D/S LOCATE FAILED MSG     Y02918
ARCT     DS    0H                      RC OFFSET TABLE           Y02918
         DC    AL2(DSGSRFLR-DSGSRFL)   DSGROUP MESSAGE           Y02918
         DC    AL2(DSESRFLR-DSESRFL)   DATA SET MESSAGE          Y02918
ADSNM    DC    AL2(DSESRFLN-DSESRFL)   DSNAME OFFSET             Y02918
RCD1     DC    F'256'                  TTRZ = 0010               Y02918
C904     DC    F'904'                  INIT. GENERIC LOCATE      Y02918
*                                      * WORK AREA SIZE          Y02918
NMOV     DC    H'4'                    OVERHEAD - GENERIC NAMES  Y02918
VLLEN    DC    H'600'                  MAX LENGTH FOR VOL LIST   Y02918
MAXVC    DC    H'50'                   MAX VOL COUNT             Y02918
ONEVOL   DC    H'1'                    COUNT OF 1 VOLUME         Y02918
VLELEN   DC    H'12'                   VOLUME ENTRY LENGTH       Y02918
VLOVHD   DC    H'46'                   OVERHEAD FOR VOL LIST     Y02918
RCDOVHD  DC    H'20'                   OVERHEAD FOR UT1 RECORD   Y02918
MAXNL    DC    H'43'                   MAX GENERIC NAME LENGTH   Y02918
RC8      DC    H'8'                    RETURN CODE OF 8          Y02918
RC40     DC    H'40'                   RETURN CODE OF 40         Y02918
RC44     DC    H'44'                   RETURN CODE OF 44       @ZA16446
GLWAELEN DC    H'45'                   GENERIC LOCATE NAME       Y02918
SAVEREG3 DS    F                       SAVE AREA FOR REG3      @ZA26030
SAVEREG7 DS    F                       SAVE AREA FOR REG7      @ZA26030
NLENGTH  DC    H'00'                   SAVE LENGTH OF NAME     @ZA26030
*                                      * ENTRY LENGTH            Y02918
PATCH    DC    20F'0'                  PATCH AREA                Y02918
         TITLE ' IEHMVESH - DSECTS'                              Y02918
*                                                                Y02918
*        DSECTS                                                  Y02918
*                                                                Y02918
GLWA     DSECT                         GENERIC LOCATE WORK AREA  Y02918
GLWALEN  DS    XL2                     SIZE OF AREA PROVIDED     Y02918
GLWANLEN DS    XL2                     SIZE OF AREA USED         Y02918
GLWAFNME EQU   *                       FIRST NAME ENTRY ADDRESS  Y02918
*                                                                Y02918
GLNAME   DSECT                         GENERIC LOCATE NAME ENTRY Y02918
GLENTYP  DS    CL1                     ENTRY TYPE                Y02918
GLDSNM   DS    CL44                    DATA SET NAME             Y02918
*                                                                Y02918
UT1RCD   DSECT                         SYSUT1 RECORD             Y02918
UT1OVHD  DS    0XL20                   OVERHEAD LENGTH           Y02918
UT1KEY   DS    XL12                    KEY - NOT USED            Y02918
UT1TTR   DS    XL3                     DSCB TTR FOR 1 VOL D/S    Y02918
         DS    XL2                     NO USED - ALWAYS 0        Y02918
UT1VLLEN DS    XL2                     LENGTH OF VOL LIST        Y02918
UT1CDE   DS    XL1                     CODE                      Y02918
UT1NAME  DS    CL44                    DATA SET NAME             Y02918
UT1VLCT  DS    XL2                     VOLUME COUNT              Y02918
UT1VLST  EQU   *                       VOLUME LIST ADDRESS       Y02918
         EJECT                                                   Y02918
         IEHMVV                                                  Y02918
XSWITCHS EQU   IEHMVV27+12
*****XSWITCHS IS USED AS THE CLOSE SWITCH
XPOINTER EQU   IEHMVV27+16
         EJECT                                                   Y02918
         IEZCTGWA                                                Y02918
         EJECT                                                   Y02918
         IEZCTGVL                                                Y02918
MVEVLVOL EQU   CTGVL+D4                MOVE VOLSER               Y02918
MVEVLDEV EQU   CTGVL                   MOVE DEVICE CODE          Y02918
         EJECT                                                   Y02918
         IEZCTGPL                                                Y02918
         SPACE 2                                                 Y02918
*                                                                Y02918
*        MODULE WORK AREA                                        Y02918
*                                                                Y02918
GLREQNM  DS    CL100                   GENERIC LOCATE NAME ENTRY Y02918
*                                      * AND CVOL RETURN AREA    Y02918
ERRMSG   DS    CL121                   RC ERROR MSG AREA         Y02918
TEMPVL   DS    CL6                     VOLSER SAVE AREA          Y02918
CVTAR    DS    D                       CONVERSION WORK AREA      Y02918
SVRTN    DS    F                       SUBROUTINE SAVE WORD      Y02918
GLFSV    DS    F                       GLFREE ROUTINE SAVE WORD  Y02918
GLSIZE   DS    F                       GENERIC LOCATE WORK AREA  Y02918
*                                      * SIZE                    Y02918
GLFDBK   DS    F                       GENERIC LOCATE WORK AREA  Y02918
*                                      * FEEDBACK SIZE           Y02918
SPWA     EQU   *                       SUPERLOCATE WORK AREA     Y02918
         TITLE ' IEHMSGSH - MESSAGE CSECT'                       Y02918
IEHMSGSH CSECT                                                   Y02918
*        MESSAGE CSECT FOR IEHMVESH                              Y02918
DSGNONE  DC    CL121' IEH348I NO DATA SETS FOUND FOR DSGROUP SPECIFIED.X
               '                                                 Y02918
DSGNOSPC DC    CL121' IEH351I SUFFICIENT SPACE NOT AVAILABLE FOR ALL DAX
               TA SETS IN DSGROUP.'                              Y02918
DSGSRFL  DC    CL52' IEH354I DSGROUP CATALOG SEARCH FAILED. RETURN CODEX
               '                                                 Y02918
DSGSRFLR DC    CL69'000 .'                                       Y02918
DSESRFL  DC    CL18' IEH356I DATA SET '                          Y02918
DSESRFLN DS    CL44                                              Y02918
         DC    CL36' CATALOG SEARCH FAILED. RETURN CODE '        Y02918
DSESRFLR DC    CL23'000 .'                                       Y02918
         END   IEHMVESH                                          Y02918
