*TITLE 'IEHMVSSS'
*FUNCTION/OPERATION: THIS ROUTINE HANDLES THE ANALYSIS OF INPUT
*CONTROL CARDS.  IT RECEIVES PARAMETERS FROM IEHMVLSU ONE AT A
*TIME AND SETS SWITCHES AND BUILDS TABLES ACCORDINGLY IN THE
*PERMANENT WORK AREA
*ENTRY POINTS: IEHMVESS
*INPUT: PERMANENT WORK AREA, IEHMVV
*OUTPUT: PERMANENT WORK AREA, IEHMVV
*EXTERNAL ROUTINES: IEHMVLSU,IEHMVSSA
*EXITS - NORMAL TO IEHMVSST
*        ERROR TO IEHMVSSA
*TABLES/WORK AREAS: IEHMVV
*AA
*ATTRIBUTES: SERIALLY RE-USEABLE
*NOTES: SEE BELOW FOR SWITCH DESCRIPTION
*
*
*
*BYTES USED AS
*SWITCHES                A ONE EQUALS ON
*
*LABEL OF SWITCH    DBLOOKFO   INDICATES WHAT PARAM CARD SCANNER
*                              IS LOOKING FOR.
*
*               BIT POS 8      KEYWORD DDNAME
*                       4      ANY KEYWORD
*                       2      A TYPE CODE
*                       1      A VOL ID
*                       8      SEQ NUMBER
*                       4      A DATA SET NAME
*                       2      A MEMBER NAME
*                       1      EXPAND DATA
*                 ALL BITS OF  COMMAND
*
*
*                   DBLOOKFO+1
*
*
*               BIT POS 8     I,E,R FILL  FILLING INCLUDE,EXCLUDE
*                             REPLACE BLOCK IN PERM WORK AREA
*                       4     ON WHEN PROCESSING INC EXC OR REP OR SEL
*                             CARDS
*                       2     COMPLEX  MORE THAN ONE TYPE-VOLID-SEQNO
*                             IN TO OR FROM LIST.
*                       1     INCLUDE BLOCK WRITE OUT SW  ON WHEN BLOCK
*                             HAS BEEN WRITTEN ON WORK FILE  UT1
*                       8     EXCLUDE BLOCK WRITE OUT  ON WHEN BLOCK
*                             HAS BEEN WRITTEN ON UT2
*                       4     INCLUDE CARD ENCOUNTERED
*                       2     EXCLUDE ENCOUNTERED
*                       1     SELECT  CARD ENCOUNTERED
*
*
*                   IEHMVV74 + 14  CHECKS THE CONTENTS
*                   OF INCLUDE BLOCK
*
*               BIT POS 8     DSNAME
*                       4     FROM  (TYPE-VOLID-SEQNO)
*                       2     CVOL  (TYPE-VOLID-SEQNO)
*                       1     MEMBER NAME
*                       8     UNUSED
*                       4     UNUSED
*                       2     UNUSED
*                       1     UNUSED
*
*
*                        XSWITCHS
*
*               BIT  POS 8     SELECT CARD HAS BEEN OR IS BEING
*                             PROCESSED
*                       4     COMPLEX MEMBER  (MEMBER NAME LIST)
*
*                       2     MEMBER RENAME
*                       1     UNUSED
*                       8     UNUSED
*                       4     UNUSED
*                       2     UNUSED
*                       1     UNUSED
*
*
*                        SWITCHRD       SET BY CARD SCANNER RTN
*
*               BIT  POS 8     SCAN ERROR     SYNTAX ERROR
*
*                       4     BYPASS   IF ON-NEXT ENTRY TO SCAN WILL
*                             RETURN OPERATOR FLD OF NEXT CARD
*                       2     END OF FILL ON SYSIN
*                       1     SCAN RTN FIRST TIME IN
*
*                       8     OPERATOR
*
*                       4     KEYWORD
*
*                       2     PARAMETER
*
*                       1     RIGHT PARENTHESIS
*
*
*NOTE    ABOVE SWITCHES EXCEPT SWITCHRD ARE RESET TO ZEROS BEFORE
*        PROCESSING EACH IEHMOVE REQUEST
*
*        SWITCHRD IS RESET TO ZEROS PRIOR TO GOING TO CARD SCANNER
*        FOR FIRST TIME ONLY
*
*
*
*
*
*
IEHMVSSS CSECT
*A189224-189242,189260                       @XA19624-@YA19299-@ZA26187
*C189218,189248                              @XA19624-@YA19299-@ZA26187
*A070475-070478                                                @ZA24330
*A070557-070575                                                @ZA18348
*D070503,070521,070539,070557                                  @ZA18348
*               SU32 DELETIONS                                 @G32DSFS
*A066060,125900-126300,222900                                  @G32DSFS
*                                                              @G32DSFS
*A189213-189215,189217                       @XA15455-@YA14038-@ZA13783
*C189208-189212,189216,189219                @XA15455-@YA14038-@ZA13783
*A189204-189253,223540                                         @ZA11956
*A070404-070480,223460-223520                                  @ZA10933
*C023000                                                       @ZA10933
*A189220-189360                                                @ZA07336
*C169030                                                       @ZM30153
*A169034,202700,202720                                         @ZM30153
*A42300-42320,50900,59600-59700,90900-90940,114900-114999       YL026VC
*A223140-223160,234700-234780                                   YL026VC
*C26300,47800,60100,66080,91300-93200,115066-115133,115900      YL026VC
*C116400,116900,117400,117900-1183600,234880-2355330            YL026VC
*D35400-35600,52600,59800,60600-60800,94200-107920,115600       YL026VC
*D116000-116320,116600,117000-117200,117600,235600-129400       YL026VC
*C59700-60100,91400-93200,131400,139800,235300-235320            Y02918
*D90900-90940,140200-140400                                      Y02918
*A91320-91340,214900                                             Y02918
*A107860-107920,116260-116320                                   YA01196
*C047800,A066000,066200,222800                                   A37919
* 032800.033000                                                  A32046
* 161100,168800                                                    I276
* 010800,037200,039600,095800,106900,118000,1330000,137200,      A27307
* 142000,149400                                                  A27307
*3195                                                              7474
* 072200,084000,101600,144800,161200                             A21900
*  PRINT ERROR MESSAGE IF HAVE A COMMA INSTEAD OF AN EQUAL SIGN  A21900
*  AFTER MEMBER,FROMDD,TODD,PDS,EXPAND OR DEVICE KEYWORD         A21900
*3236169200,173400,179000,190400                                   DTOI
*A035700-035720,060900-060920,070500-070560                    @Z40CSJH
*A093700,094500-094520,107900-107920,116200-116400             @Z40CSJH
*A127500-127600,139700-139800,240100,131500-131688             @Z40CSJH
*C235466,091600,140000,131400                                  @Z40CSJH
*D091320-091340,234700-235026,235100-235399                    @Z40CSJH
         ENTRY IEHMVESS
         USING IEHMVV,12
         USING IEHMVESS,DBBASE
         USING IEHMVESS+4096,DBBASE1
IEHMVESS SAVE (14,12),T,IEHMVSSS-OZ26187                       @ZA26187
         LR    DBBASE,15                 *
         LA    DBBASE1,2048(DBBASE)        *****************
         LA    DBBASE1,2048(DBBASE1)          ***************
      IEHPRE   (14,1),TFC
       IEHMVV
IEHMVSSS CSECT
         SR    R1,R1
         IC    R1,ESSENTRY             INSERT REENTRY BYTE
         EX    0,BRANCH(R1)            EXEC BRANCH BASED UPON VALUE
*                                      OF REENTRY BYTE
BRANCH   B     DBTOSCA3
         B     ERRPRNT1
         B     DBSKIP4
         B     DBSKIP2
         B     DBMV3
         B     DBCP3
         B     DBTOSCAN                RETURN FROM CVOL MESSAGE YL026VC
DBTOSCAN DS    0H
         TM    SWITCHRD,BYPASS         IS SCAN BYPASS SW ON
         BZ    DBTOSCA1
         TM    DBLOOKFO+1,IERSW        TEST FOR E.E.R. BIT ON
         BO    DBTEST5
         TM    DBCOMSW,X'3E'           IS SUB OPERATION BIT ON
         BZ    DBERR11
         TM    DBCOMSW+1,TO             IS TO BIT ON
         BZ    DBERR11
         B     DBTEST5
DBTOSCA1 DS    0H
         MVI   ESSENTRY,X'00'          RESET RENTRY SW
       IEHPOST (14),T
         XCTL  EP=IEHMVESJ
DBTOSCA3 DS    0H
         LM    R1,R2,IEHMVV90+136      LOAD SCAN REG POINTERS
         TM    DBFAADD,X'80'           HAS ERROR MESSAGE BEEN SAVED
         BO    ERRPRNT                 BRANCH IF YES
DBTOSCA2 DS    0H
          TM    SWITCHRD,SYSINEOF  END OF FILE ON SYSIN          A32046
          BO    DBEOFRTN           *B IF YES                     A32046
         TM    SWITCHRD,ERRSCAN        IS SCAN ERROR SWITCH ON
         BZ    DBTEST2                   BRANCH IF OFF
         B     DBERR9                    SCAN ERROR MSG
ERRPRNT  NI    DBFAADD,X'00'           RESET SAVED ERR MSG SW
         SR    R3,R3
         IC    R3,IEHMVV00+130         SAVED MESSAGE NUMBER
         MVI   ESSENTRY,X'04'          SET RENTRY TO ERRPRNT1
         LA    R2,8                    CAUSE RETURN TO SSS
         B     ESAEXIT
ERRPRNT1 DS    0H
         MVI   ESSENTRY,X'00'          RESET REENTRY SW
         LM    1,2,IEHMVV90+136
         B     DBTOSCA2
DBTEST2   EQU   *                                                A32046
         BO    DBEOFRTN                  BRANCH TO EOF RTN IF YES
         TM    SWITCHRD,BYPASS         IS SCAN BYPASS SWITCH ON
         BZ    DBTEST3                   BRANCH IF OFF
         LTR   DBLENGTH,DBLENGTH         SCAN LENGTH ZERO ?
         BNE   DBTEST3                   BRANCH IF NO
         LTR   DBSCANAD,DBSCANAD         SCAN ADDRESS ZERO ?
         BNE   DBTEST3                   BRANCH IF NO
DBTEST5  TM    DBLOOKFO,ANYKEY         LOOKING FOR A KEYWORD
         BO    DBSKIP1
         B     DBERR9                    SCAN ERROR MSG
DBSKIP1  TM    DBLOOKFO+1,IERSW        INC,EXC,OR REP SWITCH ON
         BZ    DBSKIP3
         TM    DBCOMSW,CATLOG          TEST CATALOG BIT        @Z40CSJH
         BO    DBSKIP4                                         @Z40CSJH
         TM    DBCOMSW,DSGROUP         TEST FOR DSGROUP BIT
         BZ    DBSKIP5
         L     DBNXRPTR,DBIERPTR
         CLI   0(DBNXRPTR),C'I'
         BNE   DBSKIP4
DBSKIP5  DS    0H
         L     DBNXRPTR,DBNXTPTR         STORE INTO THE REQ BLOCK
         LA    DBNXRPTR,78(0,DBNXRPTR)  INCREMENT TO NEXT IERBLK A27307
         ST    DBNXRPTR,DBNXTPTR         AVAILABLE  ADDRESS IN THE
         L     DBNXRPTR,DBIERPTR         BLOCK  & ADD ONE TO THE
         A     DBNXRPTR,DBCTONE          COUNT OF INC,EXC,REP REQ'S
         ST    DBNXRPTR,DBIERPTR
         SRL   DBNXRPTR,24
         CH    DBNXRPTR,HONE
         BE    FROMTEST
         B     DBSKIP2
FROMTEST TM    DBCOMSW,DSGORPDS        TEST FOR DSGROUP OR PDS BIT
         BZ    WRITEUT1                 BRANCH EITHER
         L     DBNXRPTR,DBIERPTR
         TM    IEHMVV74+14,X'40'       IS IER FROM SWITCH ON     A27307
         BO    WRITEUT1
         B     FINDFROM
DBSKIP2  TM    XSWITCHS,COMPLEXM       IS COMPLEX MEMBER SWITCH ON
         BZ    DBSKIP4
        B     DBMEMRTN
DBSKIP3  TM    DBCOMSW,DSNORPDS        TEST FOR DSNAME OR PDS BIT
         BZ    DBSKIP4
         TM    DBCOMSW,FROM            IS FROM BIT ON
         BZ    FINDFROM
DBSKIP4  MVI   DBLOOKFO,OPERATOR       NOW LOOKING FOR AN OPERATOR
         B     DBTOSCA1
DBTEST3  TM    DBLOOKFO,TYPCODE        LOOKING FOR A TYPE
         BO    DBDEVLUP                  GO TO DEVICE LOOKUP RTN
         TM    DBLOOKFO,OPERATR        LOOKING FOR AN OPERATOR
         BZ    DBOPLUP                   GO TO OPERATOR LOOKUP RTN
         TM    DBLOOKFO,ANYKEY         LOOKING FOR A KEYWORD
         BO    DBKEYLUP                  GO TO KEWORD LOOKUP RTN
         TM    DBLOOKFO,VOLID          LOOKING FOR A VOLUME ID
         BO    DBVIDRTN                  GO TO VOL.ID RTN
         TM    DBLOOKFO,KEYDDN         LOOKING FOR A DDNAME
         BO    DDNAMRTN
         TM    DBLOOKFO,SEQNUMB        LOOKING FOR A SEQUENCE NUMBER
         BO    DBSEQRTN                  GO TO SEQ. NO. RTN
         TM    DBLOOKFO,DSNAME         LOOKING FOR A DATA SET NAME
         BO    DBNAMRTN                  GO TO NAME AND NODE RTN
         TM    DBLOOKFO,MEMNAME        LOOKING FOR A MEMBER NAME
         BO    DBMNMRTN                  GO TO MEMBER NAME RTN
         TM    DBLOOKFO,EXPDAT         LOOKING FOR EXPAND PARAM
         BO    EXPDATA
         B     DBERR9                    SCAN ERROR MSG
***********************************************************************
**                                                                   **
**            K E Y W O R D   L O O K U P   R O U T I N E            **
**                                                                   **
***********************************************************************
DBKEYLUP STC   DBLENGTH,DBBYTE           SCAN LENGTH > EIGHT
         CLI   DBBYTE,8
         BH    DBERR8                    LENGTH ERROR MSG
         LTR   DBLENGTH,DBLENGTH                                 A30933
         BZ    DBERR9              SIGNAL SYNTAX ERROR           A30933
         LA    DBR1,DBKWDTAB             SET UP CONTROL LOOP
         LA    DBR3,12
         LA    DBCOMPR,DBKWDEND-12       TABLE LENGTH - UNIT    YL026VC
         MVI   DBSARG,X'40'
         MVC   DBSARG+1(7),DBSARG
         BCTR  DBLENGTH,0
         EX    DBLENGTH,DBKEY4           MOVE  SCAN KEYWD TO DBSARG
DBKEY3   CLC   DBSARG(8),0(DBR1)         LOOK UP IN TABLE
         BNE   DBKEY2                    FIND ?
         LA    DBR1,8(0,DBR1)            SET UP TO BRANCH
         EX    0,0(DBR1,0)               EXECUTE BRANCH IN TABLE
DBKEY2   BXLE  DBR1,DBR3,DBKEY3          LOOP BACK TO DBKEY3
         B     DBERR7                    INVALID KWD MSG
DBKEY4   MVC   DBSARG(0),0(DBSCANAD)
***********************************************************************
**                                                                   **
**           O P E R A T O R   L O O K U P   R O U T I N E           **
**                                                                   **
***********************************************************************
DBOPLUP  STC   DBLENGTH,DBBYTE           LENGTH > EIGHT ?
         CLI   DBBYTE,8
         BNH   DBOPL1
         B     DBERR8                    WRONG LENGTH MSG
DBOPL1   LA    DBR1,DBOPTAB              SET UP CONTROL LOOP
         LA    DBR3,12
         LA    DBCOMPR,DBOPTAB+60
         MVI   DBSARG,BLANK            CLEAR AREA
         MVC   DBSARG+1(7),DBSARG
         BCTR  DBLENGTH,0
         EX    DBLENGTH,DBOPLOP3         MOVE SCAN OPERATOR TO
DBOPCMP2 CLC   DBSARG(8),0(DBR1)         DBSARG
         BNE   DBOPLOP2                  FIND ?
         LA    DBR1,8(0,DBR1)            SET UP BRANCH IF YES
         EX    0,0(DBR1,0)               EXECUTE BRANCH IN TABLE
DBOPLOP2 BXLE  DBR1,DBR3,DBOPCMP2        LOOP TO CONTINUE LOOKING
         B     DBERR8                    INVALID OPERATOR MSG
DBOPLOP3 MVC   DBSARG(0),0(DBSCANAD)
***********************************************************************
**                                                                   **
**                   O P E R A T O R   T A B L E                     **
**                                                                   **
***********************************************************************
         DS    0F
DBOPTAB  DC    C'MOVE    '
         B     DBMVRTN
         DC    C'COPY    '
         B     DBCPRTN
         DC    C'INCLUDE '
         B     DBINCRTN
         DC    C'EXCLUDE '
         B     DBEXCRTN
         DC    C'REPLACE '
         B     DBREPRTN
         DC    C'SELECT  '
         B     DBSELRTN
***********************************************************************
**                                                                   **
**                    K E Y W O R D   T A B L E                      **
**                                                                   **
***********************************************************************
DBKWDTAB DC    C'DSNAME  '
         B     DBDSNRTN
         DC    C'DSGROUP '                                       Y02918
         B     DBDSGRTN                                          Y02918
         DC    C'PDS     '
         B     DBPDSRTN
         DC    C'CATALOG '                                     @Z40CSJH
         B     DBCATRTN                                        @Z40CSJH
         DC    C'VOLUME  '
         B     DBVOLRTN
         DC    C'FROM    '
         B     DBFRORTN
         DC    C'TO      '
         B     DBTORTN
         DC    C'CVOL    '
         B     DBCVORTN
         DC    C'CATLG   '
         B     DBCLGRTN
         DC    C'UNCATLG '
         B     DBUNCRTN
         DC    C'EXPAND  '
         B     DBEXPRTN
         DC    C'PASSWORD'
         B     DBSECRTN
         DC    C'MEMBER  '
         B     DBMEMRTN
         DC    CL8'RENAME'
         B     DBRENAME
         DC    CL8'DUMP'
         B     DUMPRTN
         DC    CL8'FROMDD'
         B     FROMDD
         DC    CL8'TODD'
         B     TODD
         DC    CL8'UNLOAD'                                       A37919
         B     UNLRTN                                            A37919
         DC    CL8'COPYAUTH'           RACF 2 KEYWORD          @G32DSFS
         B     DBATHRTN                BR TO KEYWORD RTN       @G32DSFS
DBKWDEND DS    0H                      END OF KEYWORD TABLE     YL026VC
UNLRTN   DS    0H                                                A37919
         OI    DBCOMSW+5,UNLOAD  USERS UNLOAD REQUEST            A37919
         B     CONTINUE                USER'S UNLOAD REQUEST     A37919
DUMPRTN  OI    DBCOMSW+3,DUMP          TURN ON DUMP SWITCH
CONTINUE DS    0H                                                A37919
         MVI   DBLOOKFO,ANYKEY         NOW LOOKING FOR A KEYWORD
         B     DBTOSCAN
***********************************************************************
**                                                                   **
**             N A M E   OR   N O D E   R O U T I N E                **
**                                                                   **
***********************************************************************
DBNAMRTN TM    SWITCHRD,PARAM          IS IT A PARAMETER
         BZ    DBERR6                     BRANCH IF NO
         TM    DBLOOKFO+1,IERSW        INC,EXC,OR REP REQUIRED
         BO    DBNAM1                     BRANCH IF YES
         LA    DBR1,44(DBNXRPTR)        UPDATE NEXT AVIL SPACE PTR
         ST    DBR1,DBNXTPTR
DBNAM1   MVI   0(DBNXRPTR),BLANK
         MVC   1(43,DBNXRPTR),0(DBNXRPTR) THE DATA SET NAME
         CH    DBLENGTH,DBFORTY4          LENGTH > FORTY FOUR
         BH    DBERR10                    LENGTH ERROR MSG
         LTR   DBLENGTH,DBLENGTH       IS LENGTH ZERO
         BZ    DBERR10                 BRANCH IF YES
         BCTR  DBLENGTH,0                 REDUCE LENGTH BY ONE
         EX    DBLENGTH,DBNAM2
         ST    DBLENGTH,SAVEREG2       SAVE REG2               @ZA10933
         ST    DBNXRPTR,SAVEREGB       SAVE REG11              @ZA10933
         SR    DBLENGTH,DBLENGTH       CLEAR REG2              @ZA10933
LOOP1    LA    DBNXRPTR,1(0,DBNXRPTR)  POINT TO NEXT CHARACTER @ZA10933
*                                       OF DSNAME              @ZA10933
         LA    DBLENGTH,1(0,DBLENGTH)  ADD ONE TO REG2         @ZA10933
         CLI   0(DBNXRPTR),X'40'       END OF DSNAME           @ZA10933
         BE    END                     YES                     @ZA10933
         CLI   0(DBNXRPTR),X'00'       END OF DSNAME           @ZA24330
         BE    END                     YES                     @ZA24330
         CLI   0(DBNXRPTR),X'4B'       CHECK FOR A PERIOD      @ZA10933
         BNE   LOOP1                   CHECK NEXT CHARACTER    @ZA10933
         CH    DBLENGTH,DBEIGHT        LENGTH > 8              @ZA10933
         BH    DBERR10                 LENGTH ERROR MSG        @ZA10933
         LA    DBNXRPTR,1(0,DBNXRPTR)  POINT TO NEXT CHARACTER @ZA10933
*                                       OF DSNAME              @ZA10933
         SR    DBLENGTH,DBLENGTH       CLEAR REG2              @ZA10933
         B     LOOP1                                           @ZA10933
END      CH    DBLENGTH,DBEIGHT        LENGTH > 8              @ZA10933
         BH    DBERR10                 LENGTH ERROR MSG        @ZA10933
         L     DBLENGTH,SAVEREG2       RELOAD REG2             @ZA10933
         L     DBNXRPTR,SAVEREGB       RELOAD REG11            @ZA10933
         TM    DBCOMSW,DSGROUP         WAS DSGROUP SPECIFIED   @ZA18348
         BZ    DBNAM1A                 NO-BRANCH               @ZA18348
         OI    DBCOMSW+5,X'20'         SET=NAME SWITCH         @ZA18348
DBNAM1A  EQU   *                       NO NAME BRANCH TO       @ZA18348
         MVI   DBLOOKFO,ANYKEY         NOW LOOKING FOR A KEYWORD
         B     DBTOSCAN
DBNAM2   MVC   0(1,DBNXRPTR),0(DBSCANAD)
***********************************************************************
**                                                                   **
**             M E M B E R   K E Y W O R D   R O U T I N E           **
**                                                                   **
***********************************************************************
DBMEMRTN TM    SWITCHRD,PARAM          IS SCAN PARAM SWITCH ON  A21900
*                                       FOR A COMMA              A21900
         BNO   IERCHK                  IF NOT, BRANCH            A21900
         CLI   PARENCTR+1,ZERO         TEST WHETHER CHECKING     A21900
*                                       MEMBERNAMES INSIDE       A21900
*                                       PARENTHESES (CTR NOT 0)  A21900
         BE    DBERR8                  IF NOT, HAVE ERROR -      A21900
*                                       COMMA INSTEAD OF EQUAL   A21900
*                                       SIGN AFTER KEYWORD       A21900
IERCHK   TM    DBLOOKFO+1,IERSW        INC,EXC, OR REP REQUIRED  A21900
         BZ    DBERR7                     KWD NOT ALLOWED MSG
         L     DBNXRPTR,DBNXTPTR
         LA    DBNXRPTR,45(0,DBNXRPTR)    ADJ REG PTR TO MEMBER AREA
         MVI   DBLOOKFO,MEMNAME        NOW LOOKING FOR A MEMBER NAME
         B     DBTOSCAN
DBMNMRTN CH    DBLENGTH,DBEIGHT           LENGTH > EIGHT ?
         BH    DBERR10                    LENGTH ERROR MSG
         LTR   DBLENGTH,DBLENGTH        IS LENGTH ZERO
         BZ    DBMNM7
         BCTR  DBLENGTH,0                 REDUCE LENGTH BY ONE
         TM    XSWITCHS,RENAME         IS MEMBER RENAME SWITCH ON
         BO    DBMNM5
         B     DBMNM6
DBMNM7   TM    XSWITCHS,COMPLEXM       IS COMPLEX MEMBER SWITCH ON
         BO    DBMNM8                   BRANCH IF YES
         B     DBERR8
DBMNM8   TM    SWITCHRD,RTPAREN        IS RIGHT PAREN BIT ON
         BO    DBMNM2                   BRANCH IF YES
         B     DBERR8
DBMNM5   NI    XSWITCHS,OFF            TURN OFF MEMBER  RENAME SWITCH
         B     DBMNM4
DBMNM6   DS    0H
         CLI   IEHMVV90+129,TWO         IS PAREN COUNT TWO
         BNE   DBMNM3                   BRANCH IF NO
         OI    XSWITCHS,RENAME         TURN ON MEMBER RENAME SWITCH
DBMNM3   DS    0H
         MVI   0(DBNXRPTR),BLANK
         MVC   1(7,DBNXRPTR),0(DBNXRPTR)  CLEAR MEMBER NAME AREA
         EX    DBLENGTH,DBMNM1            MOVE MEMBER NAME INTO LIST
DBMNM4   LA    DBNXRPTR,25(DBNXRPTR)   PIONT TO MEMBER RENAME SLOT DT0I
         MVI   0(DBNXRPTR),BLANK       CLEAR MEMBER RENAME SLOT
         MVC   1(7,DBNXRPTR),0(DBNXRPTR)
         EX    DBLENGTH,DBMNM1          MOVE MEMBER NAME
         TM    XSWITCHS,RENAME         IS MEMBER RENAME SWITCH ON
         BO    DBMEMRTN
         CLI   IEHMVV90+129,X'00'      IS PAREN COUNT ZERO
        BE    DBMNM2                   BRANCH IF YES
         OI    XSWITCHS,COMPLEXM       TURN ON COMPLEX MEMBER SWITCH
         LA    DBSCANAD,2(DBLENGTH,DBSCANAD) CHECK FOR TWO RIGHT
         CLI   0(DBSCANAD),C')'              PARENTHISIS
         BE    DBMEMRTN
        B     DBSKIP1
DBMNM2   XC    XSWITCHS(1),XSWITCHS     TURN OFF SEL EXC AND MEMBER SW
         MVI   DBLOOKFO,ANYKEY         NOW LOOKING FOR KEYWORD
         B     DBTOSCAN
DBMNM1   MVC   0(1,DBNXRPTR),0(DBSCANAD) EXECUTED INST
***********************************************************************
**                                                                   **
**       F R O M   A N D   TO   D D N A M E   R O U T I N E S        **
**                                                                   **
***********************************************************************
TODD     L     DBNXRPTR,DBNXTPTR
         ST    DBNXRPTR,IEHMVV28+4      STORE ADDR OF TO DDNAME
         OI    DBCOMSW+3,TODDSW        TURN ON TODD SWITCH
         B     FROMDD1
FROMDD   L     DBNXRPTR,DBNXTPTR
         ST    DBNXRPTR,IEHMVV28        STORE ADDR OF FROM DD NAME
         OI    IEHMVV20+3,X'02'         TURN ON FROM DD SW
FROMDD1  TM    SWITCHRD,PARAM          IS SCAN PARAM SWITCH ON  A21900
         BO    DBERR8                   BRANCH IF ON            A21900
         TM    DBLOOKFO+1,IERSW        INC,EXC, OR REP REQUIRED A21900
         BO    DBERR7                   BRANCH IF YES
         MVI   DBLOOKFO,KEYDDN         NOW LOOKING FOR A DDNAME
         B     DBTOSCAN
DDNAMRTN  CH   DBLENGTH,DBEIGHT         IS LENGTH OVER 8
         BH    DBERR10                  LENGTH ERROR MSG
         LTR   DBLENGTH,DBLENGTH        IS LENGTH OVER ZERO
         BZ    DBERR8                   INVALID PARAM MSG
         BCTR  DBLENGTH,0               REDUCE LENGTH BY ONE
         MVI   0(DBNXRPTR),BLANK       CLEAR DDNAME AREA
         MVC   1(7,DBNXRPTR),0(DBNXRPTR)
         EX    DBLENGTH,DBMNM1          MOVE DDNAME INTO LIST
         LA    DBNXRPTR,44(DBNXRPTR)    BUMP AREA POINTER
         ST    DBNXRPTR,DBNXTPTR        STORE AREA POINTER
         MVI   DBLOOKFO,ANYKEY         NOW LOOKING FOR A KEYWORD
         B     DBTOSCAN
***********************************************************************
**                                                                   **
**             R E N A M E   K E Y W O R D   R O U T I N E           **
**                                                                   **
***********************************************************************
DBRENAME TM    SWITCHRD,PARAM          IS SCAN PARAMETER SWITCH ON
         BO    DBERR8                   BRANCH IF ON
         TM    DBLOOKFO+1,IERSW        INC,EXC,OR REP REQUIRED
         BO    DBERR7                   KWD NOT ALLOWED
         TM    DBCOMSW+3,RENAMESW       IS RENAME SWITCH ON
         BO    DBERR5                   BRANCH IF YES
         OI    DBCOMSW+3,RENAMESW       TURN ON RENAME SWITCH
         L     DBNXRPTR,DBNXTPTR        PUT POINTER IN REG
         ST    DBNXRPTR,IEHMVV21+4      SET UP 'TO' DSNAME POINTER
         MVI   DBLOOKFO,DSNAME         NOW LOOKING FOR A NAME
         B     DBTOSCAN
***********************************************************************
**                                                                   **
**               D S G R O U P   R O U T I N E                       **
**                                                                   **
***********************************************************************
DBDSGRTN EQU   *                                                YL026VC
         TM    DBLOOKFO+1,IERSW         INC,EXC,OR REP REQUIRED  Y02918
         BO    DBDSG1                    BRANCH IF YES         @Z40CSJH
         TM    DBCOMSW,EQUKWD            EQUIVALENT KWD ALREADY  Y02918
         BO    DBERR5                    MULT KWD MSG            Y02918
         L     DBNXRPTR,DBNXTPTR                                 Y02918
         OI    DBCOMSW,DSGROUP         TURN ON DSGROUP SWITCH    Y02918
         TM    SWITCHRD,PARAM           SCAN PARAMETER SWITCH ON Y02918
         BZ    DBDSG2                    BRANCH IF NO            Y02918
         MVI   DBLOOKFO,ANYKEY         NOW LOOKING FOR A KEYWORD Y02918
         B     DBTOSCAN                                          Y02918
DBDSG2   MVI   DBLOOKFO,DSNAME         NOW LOOKING FOR A NAME
         ST    DBNXRPTR,DBNNPTR
         NI    DBCOMSW+5,X'DF'         CLEAR=NAME SWITCH       @Z40CSJH
         B     DBTOSCAN
DBDSG1   L     DBNXRPTR,DBNXTPTR         PUT PTR IN REG
         TM    DBCOMSW,CATLOG          IS CATALOG BIT ON       @Z40CSJH
         BO    DBDSG3                  BRANCH YES              @Z40CSJH
         TM    DBCOMSW,DSGROUP         IS DSGROUP BIT ON
         BZ    DBDSG4                  BRANCH IF NO
         L     DBR1,IEHMVV26           LOAD PTR TO IER BLOCK
         CLI   0(DBR1),C'I'            IS IT AN INCLUDE BLOCK
         BNE   DBDSG3                  BRANCH IF NO
DBDSG4   DS    0H
         OI    IEHMVV74+14,X'80'       DSNAME BIT ON IN IER SW   A27307
         LA    DBNXRPTR,1(0,DBNXRPTR)    ADJ PTR IN REG TO NAME AREA
         MVI   DBLOOKFO,DSNAME         NOW LOOKING FOR A NAME
         B     DBTOSCAN
DBDSG3   LA    DBR1,44(DBNXRPTR)       BUMP PERM WORK AREA PTR
         ST    DBR1,DBNXTPTR
         ST    DBNXRPTR,IEHMVV21+4     STORE TO DSNAME PTR
         MVI   DBLOOKFO,DSNAME         NOW LOOKING FOR A NAME
         B     DBTOSCAN
***********************************************************************
**                                                                   **
**         D S N A M E   K E Y W O R D   R O U T I N E               **
**                                                                   **
***********************************************************************
DBDSNRTN TM    SWITCHRD,PARAM          IS SCAN PARAMETER SWITCH ON
         BO    DBERR8
         TM    DBLOOKFO+1,IERSW        IS INC,EXC,OR REP REQUIRED
         BO    DBDSG1
         TM    DBCOMSW,EQUKWD           IS EQUIVALENT KWD ALREADY IN
         BO    DBERR5                    MULTIPLE KWD MSG
         L     DBNXRPTR,DBNXTPTR
         MVC   DBNNPTR(4),DBNXTPTR       NEXT AVAIL. ADDR. TO DBNNPTR
         OI    DBCOMSW,DSNSW           TURN ON DSNAME SWITCH IN COMSW
         B     DBDSG2
***********************************************************************
**                                                                   **
**              P D S   K E Y W O R D   R O U T I N E                **
**                                                                   **
***********************************************************************
DBPDSRTN TM    SWITCHRD,PARAM          IS SCAN PARAM SWITCH ON  A21900
         BO    DBERR8                   BRANCH IF ON            A21900
         TM    DBLOOKFO+1,IERSW        INC,EXC, OR REP REQUIRED A21900
         BO    DBDSG1
         TM    DBCOMSW,EQUKWD           IS EQUIVALENT KWD ALREADY IN
         BO    DBERR5                    MULTABLE KWD MSG
         OI    DBCOMSW,PDS             TURN ON PDS KEYWORD SWITCH
         L     DBNXRPTR,DBNXTPTR
         MVC   DBNNPTR(4),DBNXTPTR       SET UP NODE/NAME PTR
         B     DBDSG2
***********************************************************************
**                                                                   **
**         V O L U M E   K E Y W O R D   R O U T I N E               **
**                                                                   **
***********************************************************************
DBVOLRTN TM    SWITCHRD,PARAM          IS PARAMETER SWITCH ON ?
         BO    DBERR8                    INVALID PARAM MSG
         TM    DBLOOKFO+1,IERSW        INC,EXC,OR REP REQUIRED ?
         BO    DBERR7                    KWD NOT ALLOWED
         TM    DBCOMSW,EQUKWD     KEYWORD ALREADY IN               7474
         BM    DBERR5             MULTIPLE KEYWORD ERROR           7474
         TM    DBCOMSW,X'03'             EQUIVALENT KWD ALREADY IN ?
         BO    DBERR5                    MULT KED MSG
         OI    DBCOMSW,X'03'             TURN ON VOLUME SW
         B     DBFRO4
***********************************************************************
**                                                                   **
**             F R O M   K E Y W O R D   R O U T I N E               **
**                                                                   **
***********************************************************************
DBFRORTN TM    SWITCHRD,PARAM          IS PARAMETER SWITCH ON ?
         BO    DBERR8                    INVALID PARAM MSG
         TM    DBLOOKFO+1,IERSW        INC,EXC,OR REP REQUIRED ?
         BO    DBFRO1                    BRANCH IF YES
         TM    DBCOMSW,X'03'             EQUIVALENT KWD ALREADY IN ?
         BO    DBERR5                    MULT KWD MSG
         TM    DBCOMSW+1,CVOL          TEST FOR 'CVOL' USED    @Z40CSJH
         BO    DBERR5                    EXCLUDES USE OF 'FROM'@Z40CSJH
         OI    DBCOMSW,FROM            TURN ON FROM SWITCH
DBFRO4   MVI   DBLOOKFO,TYPCODE         NOW LOOKING FOR A TYPE
         L     DBNXRPTR,DBNXTPTR        LOAD NEXT AVILIBLE POINTER
         LA    DBNXRPTR,2(DBNXRPTR)     BUMP IT 2 FOR LATER SCRATCHING
         ST    DBNXRPTR,DBNXTPTR
         ST    DBNXRPTR,DBFLPTR         SET UP FROM L-ST PTR
         B     DBTOSCAN
DBFRO1   L     DBNXRPTR,DBNXTPTR         PUT PTR IN REG
         OI    IEHMVV74+14,X'40'       TURN ON IER FROM SW       A27307
IERVOL   LA    DBNXRPTR,53(0,DBNXRPTR)     ADJUST POINTER TO FROM AREA
         MVI   DBLOOKFO,TYPCODE        NOW LOOKING FOR A TYPE
         B     DBTOSCAN
***********************************************************************
**                                                                   **
**               T O   K E Y W O R D   R O U T I N E                 **
**                                                                   **
***********************************************************************
DBTORTN  TM    SWITCHRD,PARAM          IS SCAN PARAMETER SWITCH ON
         BO    DBERR8                    INVALID PARAMETER MSG
         TM    DBLOOKFO+1,IERSW        INC,EXC,OR REP REQUIRED
         BO    DBERR7                    KWD NOT ALLOWED MSG
         TM    DBCOMSW+1,TO            CHECK IF KEYWORD IS ALREADY IN
         BO    DBERR5                    MULTABLE KWD MSG
         OI    DBCOMSW+1,TO            TURN ON TO KEYWORD SWITCH
         MVI   DBLOOKFO,TYPCODE        NOW LOOKING FOR A TYPE
         L     DBNXRPTR,DBNXTPTR        LOAD NEXT AVILIBLE POINTER
         LA    DBNXRPTR,2(DBNXRPTR)     BUMP IT 2 FOR LATER SCRATCHING
         ST    DBNXRPTR,DBNXTPTR
         ST    DBNXRPTR,DBTLPTR         SET UP TO LIST PTR
         ST    DBNXRPTR,DBTLPTR+4      SET ACTIVE TO VOL PTR
         B     DBTOSCAN
***********************************************************************
**                                                                   **
**             C V O L   K E Y W O R D   R O U T I N E               **
**                                                                   **
** LINES 114500-118466 WERE REACTIVATED IN                     @Z40CSJH
** SUPPORT OF THE EXTENDED MVS CVOL FUNCTION                   @Z40CSJH
***********************************************************************
DBCVORTN TM    SWITCHRD,PARAM          IS SCAN PARAMETER SWITCH ON
         BO    DBERR8                    INVALID PARAMETER MSG
         TM    DBLOOKFO+1,IERSW        INC,EXC,OR REP REQUESTED ?
         BO    DBCVO2                    BRANCH IF YES
         TM    DBCOMSW+1,CVOL          IS KEYWORD ALREADY IN
         BO    DBERR5                    MULTABLE KWD MSG
         TM    DBCOMSW,FROM            TEST FOR 'FROM' USED     XA00425
         BO    DBERR5                    EXCLUDES USE OF 'CVOL' XA00425
         OI    DBCOMSW+1,CVOL          TURN ON CVOL KEYWORD SWITCH
         MVI   DBLOOKFO,TYPCODE        NOW LOOKING FOR A TYPE
         L     DBNXRPTR,DBNXTPTR        LOAD NEXT AVILIBLE POINTER
         LA    DBNXRPTR,2(DBNXRPTR)     BUMP IT 2 FOR LATER SCRATCHING
         ST    DBNXRPTR,DBCVPTR         SET UP CVOL LIST PTR
         ST    DBNXRPTR,DBNXTPTR
         B     DBTOSCAN
DBCVO2   L     DBNXRPTR,DBNXTPTR         PUT PTR IN REG
         OI    IEHMVV74+14,X'20'       TURN ON EIR CVOL SW       A27307
         B      IERVOL         YES BRANCH
***********************************************************************
**                                                                   **
**             C A T L G   K E Y W O R D   R O U T I N E             **
**                                                                   **
***********************************************************************
DBCLGRTN TM    DBLOOKFO+1,IERSW        INC,EXC,OR REP REQUEST
         BO    DBERR7                    KWD NOT ALLOWED MSG
         TM    DBCOMSW+1,CATLG         IS KEYWORD ALREADY IN
         BO    DBERR5                    MULTABLE KWD MSG
         TM    DBCOMSW,MOVE            IS THIS A MOVE REQUESTED ?
         BO    DBERR7
         OI    DBCOMSW+1,CATLG         TURN ON CATLG KEYWORD SWITCH
         B     DBTOSCAN
***********************************************************************
**                                                                   **
**             U N C A T L G   K E Y W O R D   R O U T I N E         **
**                                                                   **
***********************************************************************
DBUNCRTN TM    DBLOOKFO+1,IERSW        INC,EXC,OR REP REQUESTED ?
*
         BO    DBERR7                    KWD NOT ALLOWED MSG
         TM    DBCOMSW+1,UNCATLG       IS KEYWORD ALREADY IN
         BO    DBERR5                    MULTABLE KWD MSG
         OI    DBCOMSW+1,UNCATLG       TURN ON UNCATLG KEYWORD SWITCH
         B     DBTOSCAN
***********************************************************************
**                                                                   **
**         S E C U R I T Y   K E Y W O R D   R O U T I N E           **
**                                                                   **
***********************************************************************
DBSECRTN TM    DBLOOKFO+1,IERSW        INC,EXC,OR REP REQUESTED ?
         BO    DBERR7                    KWD NOT ALLOWED MSG
         TM    DBCOMSW+1,SECURITY      IS KEYWORD ALREADY IN
         BO    DBERR5                    MULTABLE KWD MSG
         OI    DBCOMSW+1,SECURITY      TURN ON SECURITY KEYWORD SWITCH
         B     DBTOSCAN
***************************************************************@G32DSFS
**                                                             @G32DSFS
**         C O P Y A U T H   K E Y W O R D   R O U T I N E     @G32DSFS
**                                                             @G32DSFS
***************************************************************@G32DSFS
DBATHRTN TM    DBCOMSW+5,COPYAUTH      IS KEYWORD ALREADY IN   @G32DSFS
         BO    DBERR5                    MULTIPLE KWD MSG      @G32DSFS
         OI    DBCOMSW+5,COPYAUTH      TURN ON COPYAUTH KEYWORD@G32DSFS
         B     DBTOSCAN                BR TO SCAN FOR PARMS    @G32DSFS
***************************************************************@G32DSFS
**                                                                   **
**         C A T A L O G   K E Y W O R D   R O U T I N E             **
**                                                                   **
** LINES 125900-129599 WERE REACTIVATED                        @Z40CSJH
** IN SUPPORT OF THE EXTENDED CVOL FUNCTION                    @Z40CSJH
***********************************************************************
DBCATRTN TM    DBLOOKFO+1,IERSW        INC,EXC,OR REP REQUESTED ?
         BO    DBDSG1                  BRANCH IF YES
***************************************************************@G32DSFS
         TM    DBCOMSW,EQUKWD           IS EQUIVALENT KWD ALREADY IN
         BO    DBERR5                    MULTABLE KWD MSG
         OI    DBCOMSW,CATLOG          TURN ON CATALOG KEYWORD SWITCH
         TM    SWITCHRD,PARAM          IS SCAN PARAMETER SWITCH ON
         BO    DBCAT2
         L     DBNXRPTR,DBNXTPTR
         ST    DBNXRPTR,DBNNPTR
         MVI   0(DBNXRPTR),BLANK       CLEAR JJ BYTE AREA
         MVC   1(43,DBNXRPTR),0(DBNXRPTR)
         MVI   DBLOOKFO,DSNAME         NOW LOOKING FOR A NAME
         B     DBTOSCAN
DBCAT2   MVI   DBLOOKFO,ANYKEY         NOW LOOKING FOR A KEYWORD
         B     DBTOSCAN
***********************************************************************
**                                                                   **
**               I N C L U D E   O P E R A T O R                     **
**                                                                   **
***********************************************************************
DBINCRTN DS    0C
         OI    DBCOMSW+3,CARDPRNT      TURN ON CARDPRINT CONTROL SWITCH
         TM    DBCOMSW,MOVECOPY        MOVE OR COPY SWITCH ALREADY ON
         BZ    DBERR4                    MUST FOLLOW MOVE OR COPY OP
         TM    DBCOMSW,PDS               INCLUDE PERMITTED ?   @Z40CSJH
         BO    DBINC2                   YES-INCLUDE OK         @Z40CSJH
         TM    DBCOMSW,X'10'            IS IT DSGROUP          @Z40CSJH
         BZ    DBERR1                   NO-INCL NOT OK         @Z40CSJH
         TM    DBCOMSW+1,X'40'          CVOL SPECIFIED         @Z40CSJH
         BZ    DBERR1                   NO-INCLUDE NOT OK      @Z40CSJH
DBINC2   TM    DBLOOKFO+1,FRSTIER       IS FIRST I,E,R SW ON   @Z40CSJH
         BO    DBINC3
         OI    DBLOOKFO+1,FRSTIER      TURN ON FIRST INC,EXC,OR REP SW
         MVC   DBIERPTR(4),DBNXTPTR      PUT IER BLOCK CHAIN ADDR IN
DBINC3   L     DBNXRPTR,DBNXTPTR         REQ BLOCK***J*PUT PTR IN REG
         MVI   0(DBNXRPTR),C'I'          PUT '2' IN FIRST BYTE OF REQBL
         MVI   IEHMVV74+14,X'00'                                 A27307
         MVI   DBLOOKFO,ANYKEY         NOW LOOKING FOR A KEYWORD
         OI    DBLOOKFO+1,INCLUDE      TURN INCLUDE SWITCH ON
         B     DBTOSCAN
***********************************************************************
**                                                                   **
**               R E P L A C E   O P E R A T O R                     **
**                                                                   **
***********************************************************************
DBREPRTN DS    0C
         OI    DBCOMSW+3,CARDPRNT      TURN ON CARDPRINT CONTROL SWITCH
         TM    DBCOMSW,MOVECOPY         IS MOVE COPY SW ALREADY ON
         BZ    DBERR4                    MUST FOLLOW MOVE OR COPY OP
         TM    DBCOMSW,PDS             IS PDS SWITCH ON
         BZ    DBERR3                    REPLACE NOT VALID  IGNORED
         TM    DBLOOKFO+1,FRSTIER      IS FIRST INC,EXC,OR REP SW ON
         BO    DBREP3
         OI    DBLOOKFO+1,FRSTIER      TURN ON FIRST INC,EXC,OR REP SW
         MVC   DBIERPTR(4),DBNXTPTR      PUT IER BLOCK CHAIN ADDR IN
DBREP3   L     DBNXRPTR,DBNXTPTR         REQ BLOCK ***SET UP REG PTR
         MVI   0(DBNXRPTR),C'R'          PUT 'CH IN FIRST BYTE OF IERBL
         MVI   IEHMVV74+14,X'00'                                 A27307
         MVI   DBLOOKFO,ANYKEY         NOW LOOKING FOR A KEYWORD
         OI    DBLOOKFO+1,REPLACE      TURN REPLACE SWITCH ON
         B     DBTOSCAN
***********************************************************************
**                                                                   **
**                E X C L U D E   O P E R A T O R   R O U T I N E    **
**                                                                   **
***********************************************************************
DBEXCRTN DS    0C
         OI    DBCOMSW+3,CARDPRNT      TURN ON CARDPRINT CONTROL SWITCH
         TM    DBCOMSW,MOVECOPY         IS MOVE COPY SW ALREADY ON
         BZ    DBERR4                    MUST FOLLOW MOVE OR COPY OP
         TM    DBCOMSW,X'0C'            PDS OR CATALOG         @Z40CSJH
         BM    DBEXC2                   BRANCH ON EITHER       @Z40CSJH
         TM    DBCOMSW,X'10'            IS IT DSGROUP          @Z40CSJH
         BZ    DBERR2                   NO-INVALID ERROR       @Z40CSJH
         TM    DBCOMSW+1,X'40'          CVOL SPECIFIED         @Z40CSJH
         BO    DBEXC6                   BR IF YES-EXCLUDE OK   @Z40CSJH
         B     DBERR2                   EXCLUDE INVALID        @Z40CSJH
DBEXC2   TM    DBCOMSW,PDS              IS IT PDS              @Z40CSJH
         BZ    DBEXC6                   BR IF NOT              @Z40CSJH
         MVI   XSWITCHS,SELECT         TURN ON SEL EXC MULT MEMBER SW
DBEXC5   TM    DBLOOKFO+1,FRSTIER      IS FIRST INC,EXC,OR REP SW ON
         BO    DBEXC3
         OI    DBLOOKFO+1,FRSTIER      TURN ON FIRST INC,EXC,OR REP SW
         MVC   DBIERPTR(4),DBNXTPTR      PUT ADDR OF IER CHAIN IN REQBL
DBEXC3   L     DBNXRPTR,DBNXTPTR         SET UP REQ PTR
         MVI   0(DBNXRPTR),C'E'          PUT 'E' IN FIRST BYTE OF IERBL
         MVI   IEHMVV74+14,X'C0'       TURN ON FROM ,CVOL SW     A27307
         OI    DBLOOKFO+1,EXCLUDE      TURN EXCLUDE SWITCH ON
         MVI   DBLOOKFO,ANYKEY         NOW LOOKING FOR A KEYWORD
         B     DBTOSCAN
DBEXC6   TM    DBLOOKFO+1,EXC          IS EXC SWITCH ON
         BO    DBERR2                  BRANCH YES
         OI    DBLOOKFO+1,EXCLUDE      TURN EXCLUDE SWITCH ON
         MVI   DBLOOKFO,ANYKEY         NOW LOOKING FOR A KEYWORD
         B     DBTOSCAN
***********************************************************************
**                                                                   **
**          E X P A N D   K E Y W O R D   R O U T I N E              **
**                                                                   **
***********************************************************************
DBEXPRTN TM    SWITCHRD,PARAM          IS SCAN PARAM SWITCH ON  A21900
         BO    DBERR8                   BRANCH IF ON            A21900
         TM    DBCOMSW+1,EXPAND        IS KEYWORD ALREADY IN    A21900
         BO    DBERR5                    MULTABLE KWD MSG
         TM    DBLOOKFO+1,IERSW        WAS INC,EXC,OR REP REQUESTED
         BO    DBERR7                   KWD NOT ALLOWED
         XC    IEHMVV25+2,IEHMVV25+2    CLEAR EXP DATA ARE A
         OI    DBCOMSW+1,EXPAND        TURN ON EXPAND KEYWORD SWITCH
         MVI   DBLOOKFO,EXPDAT         NOW LOOKING FOR EXPAND DATA
         B     DBTOSCAN
DBSELRTN DS    0C
         OI    DBCOMSW+3,CARDPRNT      TURN ON CARDPRINT CONTROL SWITCH
         TM    DBCOMSW,MOVECOPY         IS MOVE COPY SW ALREADY ON
         BZ    DBERR4
         TM    DBCOMSW,PDS             IS PDS SWITCH ON
         BZ    DBERR14
         TM    DBLOOKFO+1,EXC          ARE THERE ANY PRECEDING EXCLUDES
         BO    DBERR14
         MVI   XSWITCHS,SELECT         TURN ON SEL EXC MULT MEMBER SW
         TM    DBLOOKFO+1,INC          ARE THERE ANY PRECEDING INCLUDES
         BO    DBSEL3
         OI    DBLOOKFO+1,X'41'          TURN ON IER AND SELECT SW
         MVC   DBIERPTR(4),DBNXTPTR      SET UP IER BLOCK POINTER
DBSEL3   L     DBNXRPTR,DBNXTPTR
         MVI   0(DBNXRPTR),C'I'          MOVE I INTO THE BLOCK
         MVI   IEHMVV74+14,X'E0'     TURN ON FROM,CVOL,DSN SW    A27307
*                                      IN THE INCLUDE BLOCK
         L     DBR1,IEHMVV21
         MVC   1(44,DBNXRPTR),0(DBR1)    MOVE DS NAME INTO BLOCK
         L     DBR1,IEHMVV22
         MVC   53(12,DBNXRPTR),0(DBR1)  MOVE FROM INEO INTO BLOCK
DBSEL4   TM    DBCOMSW+1,CVOL          IS CVOL SWITCH ON
         BZ    DBSEL5                    BRANCH IF NO
         L     DBR1,IEHMVV24
         MVC   65(12,DBNXRPTR),0(DBR1)   MOVE CVOL INFO INTO BLOCK
DBSEL5   MVI   DBLOOKFO,ANYKEY         NOW LOOKING FOR A KEYWORD
         OI    DBLOOKFO+1,SELENCNT     TURN ON SELECT ENCOUNTERED SW
         B     DBTOSCAN
EXPDATA  TM    SWITCHRD,PARAM          IS SCAN PARAMETER SWITCH ON
         BZ    DBERR8                   BRANCH IF NO
         LTR   DBLENGTH,DBLENGTH        IS LENGTH ZERO
         BC    13,DBERR10               BRANCH IF YES
         CH    DBLENGTH,DBTWO           IS LENGTH TWO
         BH    DBERR10                  LENGTH ERROR
EXPD3    BCTR  DBLENGTH,0               REDUCE LENGTH BY ONE
         EX    DBLENGTH,EXPD4
         CVB   DBR1,IEHMVV00
         STH   DBR1,IEHMVV25+2
         MVI   DBLOOKFO,ANYKEY         NOW LOOKING FOR A KEYWORD
         B     DBTOSCAN
EXPD4    PACK  0(8,12),0(1,DBSCANAD)
***********************************************************************
**                                                                   **
**          C O P Y   O P E R A T O R   R O U T I N E                **
**                                                                   **
***********************************************************************
DBCPRTN  DS    0C
         TM    DBCOMSW,MOVECOPY         IS MOVE COPY SW ALREADY ON
         BNE   DBCP2                     BRANCH IF YES
DBCP3    OI    DBCOMSW,COPY            TURN ON COPY SWITCH
         OI    DBCOMSW+3,CARDPRNT      TURN ON CARDPRINT CONTROL SWITCH
         MVI   DBLOOKFO,ANYKEY         NOW LOOKING FOR A KEYWORD
         B     DBTOSCAN
DBCP2    MVI   ESSENTRY,X'14'
         B     SCAEXIT1
***********************************************************************
**                                                                   **
**             M O V E   O P E R A T O R   R O U T I N E             **
**                                                                   **
***********************************************************************
DBMVRTN  DS    0C
         TM    DBCOMSW,MOVECOPY         IS MOVE COPY SW ALREADY ON
         BNE   DBMV2                     BRANCH IF YES
DBMV3    OI    DBCOMSW,MOVE            TURN ON MOVE SWITCH
         OI    DBCOMSW+3,CARDPRNT      TURN ON CARDPRINT CONTROL SWITCH
         MVI   DBLOOKFO,ANYKEY         NOW LOOKING FOR A KEYWORD
         B     DBTOSCAN
DBMV2    MVI   ESSENTRY,X'10'
         B     SCAEXIT1
***********************************************************************
**                                                                   **
**            D E V I C E   L O O K U P   R O U T I N E              **
**                                                                   **
***********************************************************************
DBDEVLUP L     R0,IEHMVV74+16          ADD OF DEVNAMET             I276
         LTR   R0,R0                   IS DEVNAMET LOADED          I276
         BNZ   LOADED                  YES                         I276
         LOAD  EP=DEVNAMET             LOAD DEVICE NAME TABLE      I276
         ST    R0,IEHMVV74+16          SAVE ADDR OF DEVNAMET       I276
         L     R1,IEHMVV90+136         RESET R1                    I276
LOADED   TM    SWITCHRD,PARAM          IS SCAN PARM SWITCH ON      I276
         BO    DBERR8                   BRANCH IF ON            A21900
         TM    SWITCHRD,RTPAREN        IS RIGHT PAREN BIT ON    A21900
         BO    DBVID4
         TM    DBLOOKFO+1,COMPLEX      IS COMPLEX LIST BIT ON
         BO    DBDEV5                    BRANCH IF YES
         CLI   IEHMVV90+129,X'00'      IS PAREN COUNT ZERO
         BE    DBDEV5                    BRANCH IF YES
         OI    DBLOOKFO+1,COMPLEX      TURN ON COMPLEX BIT
DBDEV5   STC   DBLENGTH,DBBYTE           SAVE THE LENGTH
         CLI   DBBYTE,8                  LENGTH > EIGHT ?
         BNH   DBDEV1                    BRANCH IF NO
         B     DBERR8                    INVALID PARAMETER MSG
DBDEV1   DS    0C
         L     DBAVIL2,IEHMVV74+16     PUT ADDR OF DEVNAMET IN R   I276
         LA    DBAVIL3,12
         SR    DBAVIL0,DBAVIL0
         M     DBAVIL0,0(0,DBAVIL2)     CALC LENGTH OF TABLE
         LA    DBAVIL0,12
         SR    DBAVIL3,DBAVIL0          REDUCE LENGTH BY 12
*                                       LOAD COMPR WITH THE ADDR OF THE
*                                       TABLE PLUS TWELEVE LESS THAN
*                                       THE LENGTH OF THE TABLE
         LA    DBCOMPR,4(DBAVIL3,DBAVIL2)
         LA    DBR3,12                  SET UP TABLE LOOK UP
         LA    DBR1,4(DBAVIL2)          ADDR OF TABLE
         MVI   DBSARG,BLANK            CLEAR AREA
         MVC   DBSARG+1(7),DBSARG
         XC    UNIT(5),UNIT            RESET SPECIFIC UNIT NAME TO DT0I
*                                      ZERO                        DT0I
         BCTR  DBLENGTH,0                REDUCE LENGTH BY ONE
         EX    DBLENGTH,DBDEV4           MOVE TYPE TO SARG
DBDEV2   CLC   DBSARG(8),0(DBR1)         COMPARE TO DEVICE TYPE TABLE
         BNE   DBDEV3
         ST    DBR1,DBREENTR            SAVE TABLE ENTRY ADDRESS
         CLI   10(DBR1),X'80'
         BE    DBDVTAPE
         CLI   10(DBR1),X'20'
         BE    DBDVDISK
         CLI   10(DBR1),X'08'
         BE    DBDVDISK
         B     DBERR13
DBDEV3   BXLE  DBR1,DBR3,DBDEV2          LOOP HERE
         L     DBR1,16                 GET PTR TO UCBLUT           DT0I
         L     DBR1,40(DBR1)                                       DT0I
CHECKUCB ICM   DBR3,3,0(DBR1)           GET PTR TO UCB         @ZM30153
         N     DBR3,FOXES               CLEAR TOP HALF         @ZM30153
         TM    0(DBR1),X'FF'           TEST FOR END OF UCBLUT      DT0I
         BC    14,NOTEND                BRANCH ON NOT END          DT0I
         TM    1(DBR1),X'FF'           CHECK FOR DEFINITE END      DT0I
         BC    1,DBERR8                BRANCH TO INDICATE NO       DT0I
*                                      MATCH FOUND                 DT0I
NOTEND   LTR   DBR3,DBR3               CHECK FOR ZERO ENTRY        DT0I
         BZ    NXTUCB                  BRANCH ON YES               DT0I
         CLC   DBSARG(3),13(DBR3)      CHECK FOR UNIT NAME         DT0I
         BE    FOUND                   BRANCH WHEN FOUND           DT0I
NXTUCB   LA    DBR1,2(DBR1)            BRANCH TO NEXT UCBLUT ENTRY DT0I
         B     CHECKUCB                BRANCH TO CHECK NEXT UCB    DT0I
FOUND    LA    DBR3,8(DBR3)            UPDATE SO DEVICE TYPE IS    DT0I
*                                      SAME OFFSET IN UCB AS IN DNTDT0I
         ST    DBR3,DBREENTR           SAVE IT FOR NEXT RTN        DT0I
         MVC   UNIT(5),DBSARG          SAVE SPECIFIC UNIT          DT0I
         CLI   10(DBR3),X'80'          CHECK IF TAPE               DT0I
         BE    DBDVTAPE                BRANCH ON YES               DT0I
         CLI   10(DBR3),X'20'          TEST FOR DA                 DT0I
         BE    DBDVDISK                BRANCH ON YES               DT0I
         CLI   10(DBR3),X'08'          TEST FOR UNIT RECORD        DT0I
         BE    DBDVDISK                 BRANCH ON YES              DT0I
         B     DBERR13                 IF NONE OF ABOVE CONSIDER ITDT0I
*                                      AN ERROR                    DT0I
DBDEV4   MVC   DBSARG(1),0(DBSCANAD)     EXECUTED INST
***********************************************************************
**                                                                   **
**            V O L U M E   I D   R O U T I N E                      **
**                                                                   **
***********************************************************************
DBVIDRTN MVI 0(DBNXRPTR),BLANK          CLEAR VOL ID AREA
         MVC   1(5,DBNXRPTR),0(DBNXRPTR)
         LTR   DBLENGTH,DBLENGTH         IS LENGTH ZERO
         BZ    DBVID17                   BRANCH IF YES
         LA    DBR1,6                    CAUSE VD  ID TO BE
         SR    DBR1,DBLENGTH             RIGHT JUSTIFIED
         BL    DBERR10                   BRANCH IF LENGTH   SIX
         LR    DBR1,DBNXRPTR
         BCTR  DBLENGTH,0                REDUCE LENGTH BY ONE
         EX    DBLENGTH,DBVID2           MOVE VOL ID INTO LIST
DBVID17  LA    DBNXRPTR,6(DBNXRPTR)      ADJ NXT AREA PTR
         TM    DBACTTYP,TAPE            IS THE TYPE A TAPE
         BO    DBVID3                    BRANCH IF YES
         XC    0(2,DBNXRPTR),0(DBNXRPTR) CLEAR SEQ NO. AREA
         LA    DBNXRPTR,7(DBNXRPTR)       ADJUST TO NEXT TYPE SLOT DT0I
         TM    DBLOOKFO+1,COMPLEX      IS COMPLEX SWITCH ON
         BO    DBVID16                   BRANCH IF YES
         CLI   IEHMVV90+129,X'00'      IS PAREN COUNT ZERO
         BE    DBVID4                    BRANCH IF YES
         B     DBDVD1
DBVID16  LH    DBR1,IEHMVV90+128       PUT PAREN COUNT IN REG
         BCTR  DBR1,0
         LTR   DBR1,DBR1                 IS REG ZERO
         BH    DBDVD1
         BM    DBVID4
         MVI   DBLOOKFO,TYPCODE        NOW LOOKING FOR A TYPE
         B     DBTOSCAN
DBVID11  LH    DBR1,IEHMVV90+128       PUT PAREN COUNT IN REG
         BCTR  DBR1,0                    REDUCE REG BY ONE
         LTR   DBR1,DBR1
         BH    DBVID12
         BM    DBVID4
         XC    0(2,DBNXRPTR),0(DBNXRPTR) CLEAR SEQ NO. AREA
         LA    DBNXRPTR,2(DBNXRPTR)      ADJ PTR TO NEXT TYPE SLOT
         MVI   DBLOOKFO,TYPCODE        NOW LOOKING FOR A TYPE
         B     DBTOSCAN
DBVID2   MVC   0(1,DBR1),0(DBSCANAD)     EXECUTED INST
DBVID3   TM    DBLOOKFO+1,COMPLEX      IS COMPLEX SWITCH ON
         BO    DBVID11                   BRANCH IF YES
         CLI   IEHMVV90+129,X'00'      IS PAREN COUNT ZERO
         BNE   DBVID12                   BRANCH IF NO
         XC    0(2,DBNXRPTR),0(DBNXRPTR) CLEAR SEQ NO. AREA
         LA    DBNXRPTR,7(DBNXRPTR)    ADJUST TO NEXT TYPE SLOT    DT0I
         B     DBVID4                    GO TO END OF LIST RTN
DBVID12  XC    0(2,DBNXRPTR),0(DBNXRPTR) CLEAR SEQ NO. AREA
         MVI   DBLOOKFO,SEQNUMB        NOW LOOKING FOR A SEQ NUMBER
         B     DBTOSCAN
***********************************************************************
**                                                                   **
**            E N D   O F   L I S T   R O U T I N E                  **
**                                                                   **
***********************************************************************
DBVID4   MVI   DBLOOKFO,ANYKEY         NOW LOOKING FOR A KEYWORD
         NI    DBLOOKFO+1,X'DF'          TURN OFF COMPLEX BIT
         TM    DBLOOKFO+1,IERSW        WAS INC,EXC,OR REP REQUESTED
         BO    DBTOSCAN
         CLC   DBNXTPTR(4),IEHMVV22     COMP TO FROM LST PTR
         BNE   DBVID6
         LA    DBR1,IEHMVV22
         B     DBVID7
DBVID6   CLC   DBNXTPTR(4),IEHMVV23     COMP TP TO LST PTR
         BNE   DBVID8
         LA    DBR1,IEHMVV23
         B     DBVID7
DBVID8   CLC   DBNXTPTR(4),IEHMVV24     COMP TO CVOL LST PTR
         BNE   DBVID9
         LA    DBR1,IEHMVV24
         B     DBVID7
DBVID9   B     DBERR9
DBVID7   ST    DBNXRPTR,DBNXTPTR         UP NEXT AVIL PTR
         LH    DBAVIL0,DBENTCT           PLACE ENTRY COUNT
         STC   DBAVIL0,0(0,DBR1)         IN LEFT MOST BYTE OF LIST PTR
         STC   DBAVIL0,4(0,DBR1)         DO SOMETHING
         SR    DBAVIL0,DBAVIL0
         STH   DBAVIL0,DBENTCT
         B     DBTOSCAN
***********************************************************************
**                                                                   **
**          S E Q U E N C E   N U M B E R   R O U T I N E            **
**                                                                   **
***********************************************************************
DBSEQRTN MVI   IEHMVV00,BLANK          CLEAR DOUBLE WORD TO BLANKS
         MVC   IEHMVV00+1(7),IEHMVV00
         LTR   DBLENGTH,DBLENGTH         IS LENGTH ZERO
         BZ    DBSEQ17                   BRANCH IF YES
*                                      CAUSE SEQ FLD TO BE RIGHT
*                                      JUSTIFIED IN DOUBLE WORD
         LA    DBR1,4                  MAX LENGTH OF SEQ FLD
         SR    DBR1,DBLENGTH
         BL    DBERR10                   BRANCH IF LENGTH  2
         LA    DBAVIL0,IEHMVV00+4      RIGHT WORD OF DOUBLE WORD
         AR    DBR1,DBAVIL0
         BCTR  DBLENGTH,0
         EX    DBLENGTH,DBVID2           MOVE SEQ. NO. INTO LIST
         ST    DBSCANAD,SAVEREG3       SAVE REG3               @ZA11956
         AR    DBSCANAD,DBLENGTH       POINT AT SEQ NO         @ZA13783
         CLI   1(DBSCANAD),X'5D'       IS NEXT A RIGHT         @ZA13783
*                                      PARENTHESIS ?           @ZA13783
         BNE   OK                      NO, BRANCH              @ZA13783
         CLI   2(DBSCANAD),X'40'       IS IT A BLANK AFTER     @ZA13783
*                                      RIGHT PARENTHESIS ?     @ZA13783
         BE    OK                      YES, BRANCH             @ZA11956
         CLI   2(DBSCANAD),X'6B'       IS IT A COMMA AFTER     @ZA26187
*                                      RIGHT PARENTHESIS ?     @ZA26187
         BE    OK                      YES, BRANCH             @ZA26187
         TM    IEHMVV20+5,X'04'        IS RIGHT SWITCH ON      @ZA26187
*                                      SET IN IEHMVESJ         @ZA26187
         BNO   DBERR9                  NO, ERROR               @ZA26187
OK       L     DBSCANAD,SAVEREG3       RELOAD REG3             @ZA11956
         NI    IEHMVV20+5,X'FB'        RESET RIGHT SWITCH      @ZA26187
         LA    DBLENGTH,1(0,DBLENGTH)  ADD ONE TO LENGTH       @ZA07336
LOOP     CLI   0(DBR1),X'F0'          CHECK DIGIT RANGE OF 1-9 @ZA07336
         BL    DBERR9                  ERROR                   @ZA07336
         CLI   0(DBR1),X'F9'                                   @ZA07336
         BH    DBERR9                  ERROR                   @ZA07336
         LA    DBR1,1(0,DBR1)          ADD ONE TOO CHECK NEXT  @ZA07336
*                                      DIGIT IF ANY            @ZA07336
         BCT   DBLENGTH,LOOP           END OF SEQ LIST         @ZA07336
         PACK  IEHMVV00(8),IEHMVV00(8)
         CVB   DBAVIL0,IEHMVV00
         STC   DBAVIL0,1(DBNXRPTR)    PUT SEQ NO. IN LIST
         SRL   DBAVIL0,8
         STC   DBAVIL0,0(DBNXRPTR)
DBSEQ17  LA    DBNXRPTR,7(DBNXRPTR)      ADJ PTR IN REG            DTOI
         TM    DBLOOKFO+1,COMPLEX      IS COMPLEX SWITCH ON
         BO    DBSEQ1                   BRANCH IF YES
         CLI   IEHMVV90+129,X'00'      IS PAREN COUNT ZERO
         BE    DBVID4
         B     DBDVT1
DBSEQ1   LH    DBR1,IEHMVV90+128       PUT PAREN COUNT IN REG
         BCTR  DBR1,0                   REDUCE IT BY ONE
         LTR   DBR1,DBR1                TEST CONTENTS OF REG
         BNE   DBDVT1                   BRANCH IF NOT ZERO
         MVI   DBLOOKFO,TYPCODE        NOW LOOKING FOR TYPE
         B     DBTOSCAN
***********************************************************************
**                                                                   **
**             D E V I C E    =   T A P E                            **
**                                                                   **
***********************************************************************
DBDVTAPE MVI   DBACTTYP,TAPE            SET ACTIVE TYPE TO TAPE
DBDVT1   L     DBR1,DBREENTR            LOAD ADDR OF DEVICE TABLE ENTRY
         MVC   0(4,DBNXRPTR),8(DBR1)    MOVE TYPE CODE TO COMM AREA
         MVC   12(5,DBNXRPTR),UNIT     SAVE SPECIFIC NAME          DT0I
         B     DBDVD2
***********************************************************************
**                                                                   **
**             D E V I C E    =   D I S K                            **
**                                                                   **
***********************************************************************
DBDVDISK MVI   DBACTTYP,DISK            SET ACTIVE TYPE TO DISK
DBDVD1   L     DBR1,DBREENTR            LOAD ADDR OF DEVICE TABLE ENTRY
         MVC   0(4,DBNXRPTR),8(DBR1)    MOVE TYPE CODE TO COMM AREA
         MVC   12(5,DBNXRPTR),UNIT     SAVE SPECIFIC NAME          DT0I
DBDVD2   LA    DBNXRPTR,4(0,DBNXRPTR)   ADJ PTR TO VOL.ID. SLOT
         MVI   DBLOOKFO,VOLID           NOW LOOKING FOR VOL ID
         LH    DBAVIL0,DBENTCT           PUT COUNT INTO REG
         LA    DBAVIL0,1(DBAVIL0)        ADD ONE TO COUNT
         STH   DBAVIL0,DBENTCT           SAVE COUNT
         B     DBTOSCAN
CLEAR    LTR   0,0                       IS LEGNTH ZERO
         BC    8,0(14)                   RETURN IF YES
         MVI   0(1),X'00'                SET PROPOEATION BYTE
         BCTR  0,0
         LTR   0,0                       IS LENGTH ZERO
         BC    8,0(14)                   RETURN IF YES
         LA    15,255                    MAX PROPOGATION FACTOR IN R15
CLEAR3   CLR   0,15                      IS MAX LARGER THAN LENGTH
         BNH   CLEAR2                    BRANCH IF NO
         EX    15,CLEAR1                 EXECUTE MVC INSTRUCTION
         SR    0,15
         AR    1,15
         B     CLEAR3
CLEAR2   LTR   0,0
         BC    8,0(14)
         XR    15,0
         XR    0,15
         XR    15,0
         EX    15,CLEAR1
         BR    14
CLEAR1   MVC   1(1,1),0(1)
***********************************************************************
**                                                                   **
**    REQUEST BLOCK --  MISC DC,DS,EQUATE STATEMENTS                 **
**                                                                   **
***********************************************************************
         DS    0F
FOXES    DC    X'0000FFFF'              BIT SETTING TO         @ZM30153
*                                       CLEAR REGISTER         @ZM30153
DBTWELVE DC    H'12'
DBEIGHT  DC    XL2'08'
         DS    0H
HONE     DC    X'0001'
         DS    0H
DBTWO    DC    X'0002'
UNIT     DC    XL5'00'                 SPECIFIC UNIT NAME          DT0I
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
DBAVIL1  EQU   0
DBSCANAD EQU   1                         ADDR RETURNED FROM SCAN PROG
DBLENGTH EQU   2                         LENG RETURNED FROM SCAN PROG
DBR1     EQU   3                         MAINLY FOR CONTROL LOOPS
DBR3     EQU   4                         MAINLY FOR CONTROL LOOPS
DBCOMPR  EQU   5                         MAINLY FOR CONTROL LOOPS
DBAVIL3  EQU   7
DBBASE1  EQU   8
DBAVIL0  EQU   6
DBBASE   EQU   9
DBAVIL2  EQU   10
DBNXRPTR EQU   11
DBAVIL5  EQU   12
DBSAVR   EQU   13                        PTR TO SAVE AREA (LINKAGES)
DBRET    EQU   14                        RETURN REG       (LINKAGES)
DBBRTO   EQU   15                        BRANCH TO ADDR   (LINKAGES)
DBCOMSW  EQU   IEHMVV20
DBNNPTR  EQU   IEHMVV21
DBFLPTR  EQU   IEHMVV22
DBTLPTR  EQU   IEHMVV23
DBCVPTR  EQU   IEHMVV24
DBEXPPTR EQU   IEHMVV25
DBIERPTR EQU   IEHMVV26
DBFAADD  EQU   IEHMVV27
DBNXTPTR EQU   IEHMVV10
DBELPTR  EQU   IEHMVV10+4
DBENTCT  EQU   IEHMVV27+8
DBREENTR EQU   IEHMVV27+4
ESSENTRY EQU   IEHMVV27+4
ESTENTRY EQU   IEHMVV27+5
DBSARG   EQU   IEHMVV27+12
DBACTTYP EQU   IEHMVV27+30
DBBYTE   EQU   IEHMVV27+31
NOTEAREA EQU   IEHMVV27+20
DBLOOKFO EQU   IEHMVV27+24
XSWITCHS EQU   IEHMVV27+28
SWITCHRD EQU   IEHMVV90
DISK     EQU   X'02'
EQUKWD   EQU   X'3C'
RENAMESW EQU   X'04'
TWO      EQU   X'02'
TAPE     EQU   X'01'
BYPASS   EQU   X'40'
IERSW    EQU   X'80'
TO       EQU   X'80'
ERRSCAN  EQU   X'80'
SYSINEOF EQU   X'20'
ANYKEY   EQU   X'40'
DSGROUP  EQU   X'10'
DSGORPDS EQU   X'18'
DSGORVOL EQU   X'12'                                             Y02918
DSNORPDS EQU   X'28'
FROM     EQU   X'01'
OPERATOR EQU   X'00'
TYPCODE  EQU   X'20'
OPERATR  EQU   X'FF'
VOLID    EQU   X'10'
KEYDDN   EQU   X'80'
SEQNUMB  EQU   X'08'
DSNAME   EQU   X'04'
MEMNAME  EQU   X'02'
DUMP     EQU   X'01'
PARAM    EQU   X'02'
RENAME   EQU   X'20'
RTPAREN  EQU   X'01'
OFF      EQU   X'DF'
BLANK    EQU   C' '
TODDSW   EQU   X'40'
DSNSW    EQU   X'20'
PDS      EQU   X'08'
CVOL     EQU   X'40'
CATLG    EQU   X'10'
MOVE     EQU   X'80'
UNCATLG  EQU   X'20'
SECURITY EQU   X'08'
CATLOG   EQU   X'04'
CARDPRNT EQU   X'10'
MOVECOPY EQU   X'C0'
FRSTIER  EQU   X'40'
INCLUDE  EQU   X'84'
REPLACE  EQU   X'86'
SELECT   EQU   X'80'
EXCLUDE  EQU   X'82'
EXPAND   EQU   X'04'
EXPDAT   EQU   X'01'
EXC      EQU   X'02'
INC      EQU   X'40'
SELENCNT EQU   X'81'
COPY     EQU   X'40'
COMPLEX  EQU   X'20'
COMPLEXM EQU   X'40'
UNLOAD   EQU   X'40'                                             A37919
COPYAUTH EQU   X'10'                   COPYAUTH KEYWORD        @G32DSFS
RDDCB    EQU   IEHMVV90+48
PARENCTR EQU   IEHMVV90+X'80'          COUNT OF OPEN PARENTHESES A21900
ZERO     EQU   X'00'                                             A21900
CVOLPASS EQU   X'03'                   CVOL BYPASS SWITCH       YL026VC
CVOLMSG  EQU   X'13'                   CVOL MESSAGE NUMBER      YL026VC
         DS    0F
DBSIX    DC    H'06'
SAVEREGB DS    F                       SAVE AREA FOR REG11     @ZA10933
SAVEREG2 DS    F                       SAVE AREA FOR REG2      @ZA10933
SAVEREG3 DS    F                       SAVE AREA FOR REG3      @ZA11956
         DS    0F
DBCTONE  DC    XL4'01000000'
MASK1    DC    XL4'00000030'
         DS    0F
DBFORTY4 DC    X'002C'
*
*
*
*
*
DBERR1   LA    R3,1
         B     ERRDUMP
DBERR2   LA    R3,2
         B     ERRDUMP
DBERR3   LA    R3,3
         B     ERRDUMP
DBERR4   LA    R3,4
         B     ERRDUMP
DBERR5   LA    R3,5
         B     ERRDUMP
DBERR6   LA    R3,6
         B     ERRDUMP
DBERR7   LA    R3,7
         B     ERRDUMP
DBERR8   LA    R3,8
         B     ERRDUMP
DBERR9   LA    R3,9
         B     ERRDUMP
DBERR10  LA    R3,10
         B     ERRDUMP
DBERR11  LA    R3,11
         B     ERRDUMP
DBERR12  LA    R3,13
         B     ERRDUMP
DBERR13  LA    R3,14
         B     ERRDUMP
DBERR14  LA    R3,15
         B     ERRDUMP
DBERR15  LA    R3,12
         B     ERRDUMP
DBEOFRTN OI    XSWITCHS+1,X'20'         TURN ON END OF INPUT SW
         TM    IEHMVV20,X'C0'
         BZ    RET2
          TM    SWITCHRD,ERRSCAN   SYNTAX ERROR                  A32046
          BZ    SCAEXIT1           *B IF NOT                     A32046
          LA    R2,16              CAUSE MSG MODULE TO GO TO ESK A32046
          LA    R3,9               INDIC MSG # 309               A32046
          B     ESAEXIT                                          A32046
         B     SCAEXIT1
ERRDUMP  DS    0C
         TM    IEHMVV20+3,X'20'        HAS CARD BEEN PRINTED
         BZ    SAVMESS                 BRANCH IF NO
         MVI   ESSENTRY,X'00'          RESET RENTRY SW
         MVI   ESTENTRY,X'00'          RESET RENTRY SW
         OI    SWITCHRD,BYPASS    TURN SCAN BYPASS SWITCH ON       7474
         LA    R2,12                   CAUSE MSG MODULE TO XCTL TO EST
ESAEXIT  EQU   *
       IEHPOST (14),T
        XCTL  EP=IEHMVESA
INIT1    DS    0H
         MVI   ESTENTRY,X'00'          RESET RENTRY SW
         B     ESTEXIT
SCAEXIT1 DS    0H
         MVI ESTENTRY,X'0C'            SET EST ENTRY TO SCAEXIT1
         B     ESTEXIT                 WRITE MESSAGE           @Z40CSJH
FINDFROM DS    0H
         MVI   ESTENTRY,X'04'          SET EST ENTRY TO FINDFROM
         B     ESTEXIT
WRITEUT1 MVI   ESTENTRY,X'08'          SET EST ENTRY TO WRITEUT1
         B     ESTEXIT
ESTEXIT  DS    0H
       IEHPOST (14),T
         XCTL  (4,12),EP=IEHMVEST
RET2     DS    0H
       IEHPOST (14),T
         XCTL  (4,12),EP=IEHMVESK
ERRDUMP1 DS    0H
         MVI   DBREENTR,X'00'
         TM    XSWITCHS+1,X'20'         IS END OF INPUT ON
         BO    RET2
         OI    SWITCHRD,BYPASS         TURN ON SCAN BYPASS SWITCH
         TM    SWITCHRD,SYSINEOF       IS SCAN EOF SWITCH ON
         BO    RET2
         OI    DBCOMSW+3,CARDPRNT      TURN ON CARDPRINT CONTROL SWITCH
         B     INIT1
SAVMESS  OI    DBFAADD,X'80'           TURN ON SAVE MESS SW
        STC   R3,IEHMVV00+130         SAVE MESSAGE NUMBER
         B     ERRDUMP1
         DS    0F
PATCH    DS    10F                      PATCH AREA             @Z40CSJH
         END
