 TITLE 'IEHMVSRV-IEHMOVE COPY-UNLOAD-LOAD-ABORT ROUTINE'          10943
*FUNCTION/OPERATION: THIS MODULE DETERMINES IF THE DATA SET UNDER     *
*CONSIDERATION CAN BE MOVED OR UNLOADED OR NEITHER.                   *
*ENTRY POINTS: IEHMVESV                                               *
*INPUT: NONE                                                          *
*OUTPUT: SWITCH SETTINGS IN MOVE/COPY COMMUNICATIONS TABLE            *
*EXTERNAL ROUTINES: NONE                                              *
*EXITS-NORMAL RETURN TO CALLING ROUTINE-IEHMVSRZ                      *
*ERRORS I/O ERROR; SECURITY DATA SET IN DSGROUP; BDAM UNMOVEABLE      *
*TRACK OVERFLOW ERRORS  MORE THAN 5 VOLUMES
*DATA SET                                                             *
*MESSAGES: MESSAGES WHICH CAN BE PRINTED DUE TO TESTS IN THIS MODULE *
*          ARE THESE:                                                *
*                                                                    *
*   DATA SET -                                                       *
*              UNLOADED BECAUSE -                                    *
*                                 I/O ERROR ON FROM VOLUME           *
*                                                                    *
*                                 SECURITY PROTECTED                 *
*                                                                    *
*                                 DATA SET MARKED UNMOVEABLE         *
*                                                                    *
*                                                                    *
*                                                                    *
*              NOT MOVED/COPIED BECAUSE -                            *
*                                    ACCESS METHOD NOT COMPATIBLE    *
*                                                                    *
*                                    RECORD LENGTH NOT COMPATIBLE    *
*                                                                    *
*                                    DATA SET MARKED UNMOVEABLE      *
*                                                                    *
*                                    SUBORDINATE REQUEST WHILE LOADING
*                                                                    *
*                                                                    *
*                                                                    *
*                                                                    *
*   IEH450I REQUEST TERMINATED BECAUSE DATA SET SPANS MORE THAN 5 VOL.
*                                                                    *
*   TRACK OVERFLOW FEATURE REQUESTED/REQUIRED ON DEVICE THAT DOES    *
*   NOT HAVE TRACK OVERFLOW                                          *
*                                                                    *
****    ****    ****    ****    ****    ****    ****    ****    ****
*TABLES/WORK AREAS: NONE                                              *
*ATTRIBUTES: REUSEABLE                                                *
*  NOTES  THIS MODULE IS PART OF LOAD MODULE  IEHMVESX
*        THIS MODULE TEST FOR TRACK OVERFLOW FEATURE USED/REQUESTED
*        AND IN THE UNIT BEFORE A DECISION IS MADE TO UNLOAD    YA01696
IEHMVSRV CSECT                                                  YA01696
*C040700                                                       @ZA28978
*A100496                                                       @ZA24602
*D027500-027520,027540-027560                                  @ZA13732
*A023100-023120                                                @ZA13711
*A088300-088780                                                @ZA04396
*D088300-088850                                                @ZA04396
*A090460-090520                                                @ZA03378
*C030000                                                       @ZA01733
*D 032930-032960                                               @ZA01656
*A032930-032940                                                @ZA01239
*A027500-027560                                                @ZA01234
*D027500-027600,139700-139720,147500                           @ZA01234
*A028200-028600                                                 YA01696
*D028200-028600
*A032940                                                        YA01671
*C102100,105600,106000                                          YA01218
*
*
*
*
*A083620-083720                                                 OY01193
*D100000-100200,C100220,A100530                                  A50960
*A100244,100248                                                  A48802
*C100300                                                         A48802
*065400                                                          A45188
*A103300-103388,132900                                           A45144
*A080600,D085400,A085500,D085600-085800,086400,A086500           A37919
* 006400-007000,008000-008400                                    A44302
* 027800                                                         A32041
*                                                                A32032
* 112000-112800,119400-120000                                    A30084
*                                                                A29052
*088400,088800-089000,089250-089300                              A25286
* 045250,046000,046200-048400,146250                             A25631
*                                                                A22664
* 114000-116600,117600-119200,120200-120800                      A17911
*                                                                A17911
* 059400,089400                                                  A21645
*                                                                A21645
*  042600-043800                                                 A35460
*A040700,040720,134900                                           A35460
*                                                                  UL0H
*0055 004000,025000,025400-026000,066200,146800                    UL0H
*                                                                  UL0H
*                                                                  0310
*0055  110800-111800                                               0310
*                                                                  0310
*C600,12000,16000                                               YL026VC
*D800,2525-2540,10220,10380,12200-12400,12800-13400,67200-69200 YL026VC
*D70130-70160,123200-131600,132200-132600,133000                YL026VC
*A27500-27580,139700-139720,147500                              YM07033
*C27600                                                         YM07033
*A39100                                                         YM06314
*A010220-010284,013100-013200,067100-067400                    @Z40CSJH
*A070120-070140,083620-083680,123100-123400                    @Z40CSJH
*A130600,131000,132683-132787,133000                           @Z40CSJH
IEHMVESV SAVE  (14,12),T,IEHMVSRV-OZ28978                      @ZA28978
         ENTRY IEHMVESV
         LR    BASE,LINK
         USING IEHMVV,MCTABLE
         USING DSCBDSEC,DSCB
         USING IEHMVESV,BASE
         IEHPRE (14,1),TFC
         USING IEHMVV,MCTABLE
* OBTAIN FROM AND TO DEVICE TYPE CODES                           A44302
          STM  DEV14,DEV1,DEVSAVE
         L     DEV14,IEHMVV30+12   ADDRESS OF FROM DDNAME.
         LA    DEV1,0(0,DEV14)
         DEVTYPE (1),DEVT1         OBTAIN  UCB DEVICE TYPE.   (FROM)
         L     DEV14,IEHMVV31+8    RETRIEVE ADDRESS  OF  TO  DDNAME.
         LA    DEV1,0(0,DEV14)
         DEVTYPE  (1),DEVT2        OBTAIN UCB DEVICE TYPE    (TO)
IEHMVRSA LM    DEV14,DEV1,DEVSAVE
*                                       TEST SECURITY ROUTINE
         B     IEHMVESM
IEHMVRSM DS  0H                   LOAD TEST ROUTINE
         B     IEHMVERH
IEHMVRRH DS  0H                   ACCESS METHOD TEST
**********************************************************************
*
* LINES 010300-010396 WERE REACTIVATED IN                      @Z40CSJH
* SUPPORT OF THE EXTENDED MVS CVOL FUNCTION                    @Z40CSJH
*
**********************************************************************
         TM    IEHMVV20,CATALOG+FROM IS UNLOADED CATALOG EXPECTED 10943
         BC    14,IEHMVESW             NO, BRANCH                 10943
         TM    IEHMVV20+1,LOAD         HAS LOAD CONDITION BEEN SET10943
         BO    IEHMVESW                YES, BRANCH                10943
*                                      NO, WRITE ERROR MSG        10943
         MVI   IEHMVV61,FROMERR        MSG NUMBER                 10943
         L     LINK,PREAB              SET UP TO PREABORT         10943
         BR    LINK                                               10943
*                                                                 10943
         B     IEHMVESW
IEHMVRSW DS  0H
*
         TM    IEHMVV20+1,UNLOAD        UNLOAD CONDITION
         BO    IEHMVRTE           YES,SKIP TEST RECORD LENGTH
*
*                                       RECORD LENGTH TEST
         B     IEHMVETE
IEHMVRTE DS  0H                   CHECK FOR LOAD CONDITION      YL026VC
*
         TM    IEHMVV20+1,CATLG    CATALOG REQUEST             @Z40CSJH
         BO    YESCATLG            YES                         @Z40CSJH
RTNCATLG DS  0H                   CHECK FOR LOAD CONDITION
*
         TM    IEHMVV20+1,LOAD          IS THIS A LOAD CONDITION
         BO    IEHMVESL           YES,GO TO UNMOVEABLE TEST
         L     ACCE,IEHMVV22+4
         CLI   2(ACCE),DASD             FROM DEVICE DASD
         BNE   IEHMVRSL           NO,OMIT UNMOVEABLE TEST
*
*                                       UNMOVEABLE TEST
         B     IEHMVESL
IEHMVRSL DS  0H                   PREPARE TO LEAVE ROUTINE
*
        IEHPOST (14,1),TC
*
         MVC   12(4,SAVE),MNOP    SET RETURN TO COPYRTN IN IEHMVSRZ
         RETURN (14,12),T
GOOUT    L     LINK,PREAB
         BR    LINK
*
*
MNOP     DC    A(COPYRTN)
         EXTRN COPYRTN
         EJECT
*
IEHMVERH DS  0H     THIS IS A LOAD TEST ROUTINE. IF PHYSICAL SEQUENTIAL
*                   ORGANIZATION, A JFCB IS READ AND THE DATA SET IS
*                    OPENED. A TEST IS MADE FOR UNLOADED DATA SET OR
*                   THE DSCB IS FILLED
*                   NORMAL EXIT IS TO IEHVESW
         L     ACCE,IEHMVV22+4          FROM DEVICE DASD
         CLI   2(ACCE),DASD
         BNE   LTOPEN                   NO, GO TO OPEN DATA SET
*
*                                       YES, TEST ACCESS METHOD
         L     DSCB,IEHMVV30+8          GET DSCB POINTER
         CLI   DS1DSORG,BSAM            DSORG EQUAL PHYSICAL SEQ.
         BE    LTOPEN
*
         CLI   DS1DSORG,AZERO     NO,TEST FOR NO ORGANIZATION GIVEN
         BE    LTOPEN             YES,BRANCH TO OPEN
*
*
LTRETURN DS  0H                   BRANCH TO ACCESS METHOD TEST
*
         B     IEHMVRRH
*
*
*
*
LTOPEN   DS  0H                   OPEN J
         TM    IEHMVV20+5,X'40'        UNLOAD REQUEST          @ZA13711
         BO    IEHMVRRH                YES,BRANCH              @ZA13711
         L     ACCE,IEHMVV10            GET PTR TO PER WORK AREA
         LA    ACCO,96(ACCE)            UP PTR
         ST    ACCO,IEHMVV10            YES, CONTINUE, STORE PER WORK
         ST    ACCE,IEHMVV30            STORE DCB PTR
         L     ACCO,LENGTHDC            MOVE DCB
         EX    ACCO,MOVELT
         LR    M7,ACCE
         ST    M7,IEHMVV30              SET UP FROM DCB POINTER
         L     M5,IEHMVV30+12
         LA    ACCE,IEHMVV70                                       UL0H
         ST    ACCE,DCBEXLST-IHADCB(0,M7)    EXIT LIST POINTER
         LA    ACCE,IEHMVV00
         ST    ACCE,UDCBJFCB           SET UP JFCB POINTER         UL0H
         MVI   UDCBJFCB,X'87'          FLAG AS JFCB DCBEXLST ENTRY UL0H
         MVC   UDCBDCBE(4),DCBXX       SET UP DCB ENTRY IN EXLIST  UL0H
         MVC   UDCBIHLE(4),ADIHLR      SET UP INPUT HDR LBL RTN    UL0H
         MVI   UDCBIHLE,ACTIVIHL       ENTRY IN DCB EXLIST         UL0H
         TM    IEHMVV20+3,FRDDKEY FROMDD KEYWORD
         BO    FROMDD              YES
*
*
         MVI   IEHMVV00,AZERO     ZERO AREA FOR JFCB IN WORK AREA
         MVC   IEHMVV00+1(175),IEHMVV00
         MVI   JFCBLTYP,LABEL     INDICATE LABELED TAPE
         MVI   JFCBIND2,X'40'    SET DISP OLD                  @ZA01234
FROMBACK EQU   *
         TM    JFCBLTYP,SL             STANDARD LABEL
         BNO   FDSN               *B IF NO-USE EXISTING RECFM    A32041
*                                                        BLKSIZE A32041
*                                                        LRECL   A32041
         XC    JFCRECFM(1),JFCRECFM  ELSE ZERO JFCB FIELDS SO    A32041
         XC    JFCDSORG(1),JFCDSORG         DCB FIELDS WILL      A32041
         XC    JFCBLKSI(2),JFCBLKSI         BE FILLED FROM THE   A32041
         XC    JFCLRECL(2),JFCLRECL         STANDARD LABEL       A32041
*                                                                A32041
FDSN     L     ACCE,IEHMVV21      RETURN FROM USING FROMDD       A32041
         MVC   JFCBDSNM(44),0(ACCE)     SET UP DSNAME
         TM    IEHMVV20+3,FRDDKEY      FROMDD KEYWORD           YA01696
         BO    FROMDD1                 YES KEEP DDNAME IN DCB   YA01696
         L     ACCE,IEHMVV30+12        FROM DDNAME ADDRESS      YA01696
         MVC   DCBDDNAM-IHADCB(8,M7),0(ACCE) PUT DDNAME IN DCB  YA01696
FROMDD1  MVI   JFCBELNM,BLANK          PUT IN ELEMENT NAME      YA01696
         MVC   JFCBELNM+1(7),JFCBELNM
*
*
         LA    M6,JFCBVOLS         POINTER TO JFCB VOL LIST
         SR    ACCO,ACCO
         IC    ACCO,IEHMVV22       COUNT OF VOL IDS
         LA    M5,6                MAX COUNT OF 5 VOL IDS      @ZA01733
         L     ACCE,IEHMVV22       POIHTER TO VOL LIST
         MVC   JFCBFLSQ(2),10(ACCE)
VOLIDLP  DS  0H
         MVC   0(6,M6),4(ACCE)   MOVE VOL ID
         LA    M6,6(0,M6)          UP JFCB POINTER
         LA    ACCE,17(0,ACCE)     UP FROM VOL POINTER           A32032
         BCT   M5,NVOLIDS          MOVED 5 VOL IDS
         MVI   IEHMVV61,NONTOF    MORE THAN FIVE VOLUMES SET UP MSG
         L     LINK,PREAB
         BR    LINK
NVOLIDS  BCT  ACCO,VOLIDLP         MOVED ALL OF THE  VOL IDS
         MVC   JFCBNVOL(1),IEHMVV22
         MVI   0(M6),BLANK
FDDRTN   DS  0H                   OPEN - FROM DCB
         MVC   JFCBDSNM+53(3),IEHMVV82   PUT TTR IN JFCB         S21042
         XC    IEHMVV82(3),IEHMVV82    AFTER IT, CLEAR           S21042
         OPEN  ((M7),(INPUT)),TYPE=J
         OI    IEHMVV10-4,OK      INDICATE FROM DCB OPENED
         CLI   IEHMVV10-5,AZERO   BRANCH IF OPEN SUCCESSFUL
         BE    NOMSS
         IC    ACCE,IEHMVV61
         MVC   IEHMVV61(1),IEHMVV10-5
         L    LINK,GOTOMESS
         BALR  RETURN,LINK
         STC   ACCE,IEHMVV61
NOMSS    DS    0H                                                  UL0H
         CLI   IEHMVV74+4,X'FF'         I/O ERR IN U.L. EXIT RTN   UL0H
         BE    IOERRMSG                 YES, TELL USER             UL0H
ERMSGRET DS    0H                                                  UL0H
*                                       TEST FOR UNLOADED DATA SET UL0H
*
         L     M5,IEHMVV22
         CLI   2(M5),UNITRCD            IS THIS UNIT RECORD
         BNE   AAAAKK
         LA    ACCO,80
         B     LTCON
AAAAKK   DS  0H
         LA    ACCO,80            INITIALIZE REG. 3 TO DECIMAL 80  8952
         L     ACCE,IEHMVV22      PTR. TO FROM LIST                8952
         TM    2(ACCE),UNITRCD    IS DEVICE UNIT RECORD?           8952
         BO    LTCON              IF YES, SET TO READ FIRST RECORD 8952
         L     ACCE,IEHMVV30
         CH    ACCO,DCBLRECL-IHADCB(0,ACCE)
         BNE   NOLT                     NO, STOP LOAD TEST
         CLI   2(M5),TAPE              TEST FOR FROM DEVICE TAPE
         BE    PATCH                   SKIP KEYLENTGH TEST
         CLI   DCBKEYLE-IHADCB(ACCE),AZERO   TEST KEY LENGTH
         BNE   NOLT                    NO LOAD
PATCH    LA    ACCO,800          GET SET TO TEST FOR BLKSIZ OF 800 8952
         CH    ACCO,DCBBLKSI-IHADCB(0,ACCE)  BLOCKSIZE EQUAL TO 800
         BNE   NOLT
LTCON    DS  0H
         LR    ACCE,ACCO          SET BLOCKSIZE                 YM06314
         LR    ZERO,ACCO          GET BLOCK LENGTH
         GETMAIN R,LV=(0)               GET CORE
         L     M5,IEHMVV30+4            GET DECB POINTER
         OI    IEHMVV30+4,OK      TURN ON BUFFER BIT
*
         L     M7,IEHMVV30              DCB PTR
         LA    ACCO,NOLT
         ST    ACCO,DCBEODAD-IHADCB(0,M7)  ADDRESS OF END OF DATA ROUT.
         MVC   DCBSYNAD+1-IHADCB(3,M7),SYNADRTN                @ZA28978
*                                       SYNADRTN - MSG I/O ERR   A35460
         LR    ACCO,1
         READ  (M5),SF,(M7),(ACCO),(ACCE),MF=E
*                                       READ THE FIRST RECORD
*
         LR    PARAM,M5           GET ADDRESS OF DECB
         CHECK (1)                      CHECK THE READ
         L     PARAM,12(PARAM)    GET IO AREA
*  CHECK FIRST RECORD WRITTEN BY IEHMVSSY FOR AN UNLOADED DATA SET
         L     ACCE,LDRC
         CLC   2(78,PARAM),0(ACCE)   FIRST RECORD OF UNLOADED    A35460
*                                    DATA SET?                   A35460
         BE    UNLDPROC              GO TO READ UNLOADED RECORDS A35460
         TM    IEHMVV20,X'08'        IS KEYWORD 'PDS' SPECIFIED? A35460
         BZ    NOLT                  NO, BRANCH (PS DATA SET)    A35460
         MVI   IEHMVV61,X'22'        PUT ERROR CODE IN WORK AREA A35460
         L     LINK,PREAB            LOAD ADDRESS OF THE ENTRY   A35460
*                                    POINT OF THE ERROR ROUTINE  A35460
         BR    LINK                  GO TO ERROR ROUTINE         A35460
UNLDPROC LNR   ZERO,BASE             CONTINUE TO SET UP FOR      A35460
*                                    LOADING                     A35460
*                                    SET UP TO CALL BREAD TO GET A35460
*                                    DSCB                        A35460
         LA    1,IEHMVV00+40
         L     LINK,BREAD               GO TO BREAD ROUTINE
         BALR  RETURN,LINK
         B     LTBREAD(LINK)            CHECK RETURNS FROM BREAD
LTBREAD  B     LTOK                     BREAD OK
         B     NOLT               END OF DATA
         B     LTPIO                    PERMANT IO ERROR
         B     NOPSLT                  SEQ NUMBERS INCORRECT     A25631
         DC    X'0000'
LTPIO    DS    0H
         MVI   IEHMVV61,ERROR     SET ERROR MESSAGE
EXIT     L     LINK,PREAB                                        A25631
         BR    LINK
NOPSLT   MVI   IEHMVV61,PSERR                                    A25631
         B     EXIT                                              A25631
LTOK     DS  0H
         MVC   DEVCHARU(4),IEHMVV00+236 PICK UP OLD DEVICE TYPE
         L     ACCE,IEHMVV30+8    GET FROM DSCB
         MVC   0(140,ACCE),IEHMVV00+43
         MVC   IEHMVV25(2),IEHMVV00+183     SET UP DIRECTORY QUAN
         OI    IEHMVV20+1,LOAD          TURN ON LOAD BIT
         TM    IEHMVV00+185,MOVE  WAS UNLOAD DS UNMOVEABLE
         BZ    GNNNU              NO NOT UNMOVEABLE
* * * * *                         YES-- UNMOVEABLE
         OI    IEHMVV10-4,ANYINCRP TURN ON OUR UNMOVE INDICATION
GNNNU    DS    0H
         SPACE 1
         STM   DEV14,DEV1,DEVSAVE       SAVE THE REGISTERS TO BE USED
         TM    IEHMVV00+185,X'40'       TEST THE LOAD FLAG
         BO    DEVA                    BRANCH IF NEW UNLOAD
         MVC   IEHMVV00+186(8),DEVCONST  SET UP DEFAULT VALUES
DEVA     L     DEV14,IEHMVV31+8        GET ADDRESS OF THE 'TO'
         LA    DEV1,0(0,DEV14)          DDNAME
         DEVTYPE (1),DEVCHAR,DEVTAB     GET THE 'TO' DEVICE CHARCTER
         TM    DEVCHAR+2,X'20'          TEST DEVICE TYPE OF 'TO' DEV
         BZ    DEVCONT                  BRANCH IF NOT DIRECT ACCESS
         MVC   DEVCHAR(4),IEHMVV00+186  GET THE SIZE OF THE DATASET
         L     DEV14,DEVCHAR            AND PUT INTO REGISTER DEV14
         LH    DEV0,DEVCHAR+12         SET UP TO DIVIDE BY THE
         AR    DEV14,DEV0              TRACK CAPACITY AND ROUND
         BCTR  DEV14,0                 UP TO GET AN ADEQUATE
         SRDA  DEV14,32                NUMBER OF TRACKS FOR THE
         DR    DEV14,DEV0              NEW ALLOCATION
         ST    DEV15,DEVCHAR            STORE RESULT BACK INTO DSCB
         MVC   DADPQ-IECSDSL1(4,ACCE),DEVCHAR   FOR ALLOCATION
         MVC   DEVCHAR(4),IEHMVV00+190  GET THE SECONDARY QUANTITY
         L     DEV14,DEVCHAR            AND TEST FOR DEFAULT
         LTR   DEV14,DEV14              VALUES BY SEEING IF NEGATIVE
         BNL   DEVD                     SECONDARY QUANTITY
         LCR   DEV14,DEV14              MAKE POSITIVE AND SET
         MVI   DS1SCALO-IECSDSL1(ACCE),X'80'  SEC ALLOC CONTROL
DEVD     TM    DS1SCALO-IECSDSL1(ACCE),X'80'  TEST TYPE OF SEC ALLOC
         BZ    DEVCONT                  BRANCH IF ABSOLUTE OR BLOCK
         AR    DEV14,DEV0              CAPACITY AND ROUND UP
         BCTR  DEV14,0                 TO GET AN APPROPRIATE
         SRDA  DEV14,32                VALUE FOR THE SECONDARY
         DR    DEV14,DEV0               ALLOCATION REQUEST
         TM    DS1SCALO-IECSDSL1(ACCE),X'40'  TEST TYPE OF SEC ALLOC
         BZ    DEVB                    BRANCH IF TRACK
         LR    DEV14,DEV15              SET UP TO CONVERT TO CYL
         SR    DEV0,DEV0               ALLOCATION
         IC    DEV0,DEVCHAR+11               REQUEST AND
         AR    DEV14,DEV0              ROUND UP TO THE NEXT
         BCTR  DEV14,0                 HIGHER NUMBER OF
         SRDA  DEV14,32                CYLINDERS ON THE
         DR    DEV14,DEV0              DEVICE
         B     DEVB
         SPACE 1
         SPACE 1
DEVB     ST    DEV15,DEVCHAR            STORE THE PROPER SEC ALLOC
         MVC   DS1SCALO-IECSDSL1+1(3,ACCE),DEVCHAR+1  INTO THE DSCB
         B     DEVCONT
         SPACE 2
DEVCHAR  DS    5F                       DEVICE CHARACTERISTICS AREA
DEVSAVE  DS    4F                       REGISTER SAVE AREA
DEVCONST DC    F'72500'                DEFAULT VALUES GIVE A PRIMARY
         DC    F'-36250'                QUAN OF 20 TRACKS ON 2311
         SPACE 1
DEVT1    DS    2F                                                A21645
DEVT2    DS    2F                      UCB TYPE FOR TO   DEVICE
DEVCHARU DS    1F
         SPACE 1
DEV14    EQU   14                       DEFINE THE REGISTERS USED
DEV15    EQU   15                       IN THIS DEVICE ALLOCATION
DEV0     EQU   0
DEV1     EQU   1
         SPACE 1
DEVCONT  LM    DEV14,DEV1,DEVSAVE       RESTORE REGISTERS
         SPACE 1
         B     LTRETURN
NOLT     DS    0H
         L     ACCE,IEHMVV22+4          IS FROM DEVICE DASD
         CLI   2(ACCE),DASD
         BE    LTRETURN           YES,GO TO RETURN
*                                       NO, SET UP FOR TESTING
*
         L     ACCE,IEHMVV30            GET DCB ADDRESS
         L     ACCO,IEHMVV30+8          GET DSCB ADDRESS
         MVC   DS1RECFM-DSCBDSEC(1,ACCO),DCBRECFM-IHADCB(ACCE)
         MVC   DS1BLKL-DSCBDSEC(2,ACCO),DCBBLKSI-IHADCB(ACCE)
         MVC   DS1LRECL-DSCBDSEC(2,ACCO),DCBLRECL-IHADCB(ACCE)
         L     ACCE,IEHMVV21            DATA SET NAME
         MVC   DS1DSNAM-DSCBDSEC(44,ACCO),0(ACCE)
         MVI   DS1FMTID-DSCBDSEC(ACCO),X'F1'
         MVI   DS1DSORG-DSCBDSEC(ACCO),X'40'
         B     LTRETURN                 RETURN
FROMDD   DS    0H
         L     1,IEHMVV28
         MVC   DCBDDNAM-IHADCB(8,M7),0(1)
         L     M5,16                   FIND ADDR CVT             A45188
         L     M5,0(M5)                FIND TCB POINTERS         A45188
         L     M5,4(M5)                FIND TCB ADDR             A45188
         L     M5,12(M5)               FIND TIOT ADDR            A45188
         LA    M5,24(M5)               FIND FIRST DD ENTRY       A45188
NEXTDD   SR    M6,M6                                             A45188
         IC    M6,0(M5)                FIND ENTRY LENGTH         A45188
         CLC   4(8,M5),0(1)            DDNAME FOUND              A45188
         BE    RDJFCB                  YES CONTINUE              A45188
         AR    M5,M6                   NO GO TO NEXT ENTRY       A45188
         L     M6,0(M5)                                          A45188
         LTR   M6,M6                   END OF TIOT               A45188
         BNZ   NEXTDD                  NO COMPARE NEXT ONE       A45188
         MVI   IEHMVV61,X'26'          YES INDICATE MSG NBR      A45188
         L     LINK,PREAB                                        A45188
         BR    LINK                    TERMINATE THE REQ         A45188
RDJFCB   DS    0H                                                A45188
         RDJFCB  ((M7))
         B     FROMBACK
         DS    0F
DCBXX    DC    X'05'                                               UL0H
         DC    AL3(MYDCBXX)
ZZZ      DC    X'0000'
MYDCBXX  CLC   DCBBLKSI-IHADCB(2,M7),ZZZ
         BE    MYDCBX             YES,BLOCKSIZE EQUAL TO ZERO
**********************************************************************
* LINES 067500-069380 WERE REACTIVATED IN                      @Z40CSJH
* SUPPORT OF THE EXTENDED MVS CVOL FUNCTION                    @Z40CSJH
**********************************************************************
         LA    ACCE,80            NO
         CH    ACCE,DCBLRECL-IHADCB(ZERO,M7)  RECL EQUAL TO 80
         BE    RTNDCBX
CHKCAT   TM    IEHMVV20,CATALOG   CATLG KEYWORD
         BZ    RTNDCBX            RETURN
         MVI   DCBDSORG+1-IHADCB(M7),AZERO
         MVI   DCBDSORG-IHADCB(M7),BSAM
         MVI   DCBBLKSI+1-IHADCB(M7),AZERO
         MVI   DCBBLKSI-IHADCB(M7),ONE
         MVI   DCBKEYLE-IHADCB(M7),EIGHT
         MVI   DCBRECFM-IHADCB(M7),FIXED+KEYL                      1719
RTNDCBX  DS    0H
         RETURN   RC=0
MYDCBX   DS    0H                 TEST FORMAT
         L     M5,IEHMVV22
         CLI   2(M5),UNITRCD      FROM VOLUME UNIT RECORD
         BE    UUWW
         TM    IEHMVV20,CATALOG  CATALOG KEYWORD ?             @Z40CSJH
         BO    CHKCAT                                          @Z40CSJH
         MVI   DCBBLKSI-IHADCB(M7),TEN
         MVI   DCBRECFM-IHADCB(M7),UNDEF
         MVI   IEHMVV10-5,X'09'
         B     RTNDCBX
ADIHLR   DC    X'01'                   INPUT HEADER LABEL ROUTINE  UL0H
         DC    AL3(IHLR)               DCBEXLIST ENTRY             UL0H
*                                                                  UL0H
* USER INPUT HEADER LABEL ROUTINE ENTERED FROM OPENJ OF INPUT DCB  UL0H
*   WITH REGISTER 1 POINTING TO A 5-WORD PARAMETER LIST. WORD 1    UL0H
*   POINTS TO THE LABEL BUFFER AREA.  IF WORD 1 IS ZERO, THERE ARE UL0H
*   NO INPUT HEADER LABELS TO BE PROCESSED.                        UL0H
*                                                                  UL0H
IHLR     DS    0H                                                  UL0H
OFFSET1  EQU   IHLR-IEHMVESV                                       UL0H
         LA    M8,OFFSET1              SET UP                      UL0H
         SR    LINK,M8                 ADDRESSABILITY              UL0H
         LR    BASE,LINK               FOR EXIT ROUTINE            UL0H
         L     M8,0(PARAM)        GET ADDR OF LABEL BUFFER AREA    UL0H
         LTR   M8,M8              ARE USER LABELS PRESENT          UL0H
         BNE   CHKIOERR           YES, IS I/O ERROR INDICATED IN   UL0H
*                                 PARAMETER LIST                   UL0H
NOLABEL  SR    LINK,LINK          NO, RETURN CODE OF ZERO TO OPEN  UL0H
*                                 TO IGNORE ADDITIONAL LABELS      UL0H
         BR    RETURN             RETURN TO OPENJ                  UL0H
*                                                                  UL0H
CHKIOERR DS    0H                                                  UL0H
         TM    8(PARAM),X'80'     PERM ERROR SHOWN IN PARM LIST    UL0H
         BC    14,CORAVAIL        NO, SEE IF CORE GOTTEN YET       UL0H
LABLERR  DS    0H                                                  UL0H
         OI    IEHMVV74+4,X'FF'         YES, SET FLAG TO OUTPUT    UL0H
*                                        MSG AFTER OPEN/CLOSE/EOV  UL0H
         MVI   UDCBIHLE,X'00'     DEACTIVATE HEADER LABEL ENTRIES  UL0H
         MVI   UDCBOHLE,X'00'     IN DCB EXIT LIST                 UL0H
         OI    IEHMVV20+2,X'40'        TELL VESN TO PUT OUT MSG    UL0H
*                                      DATA SET(S) NOT MOVED/COPIEDUL0H
         BR    RETURN             RETURN TO OPENJ ROUTINE          UL0H
CORAVAIL DS    0H                                                  UL0H
         CLC   IEHMVV72(4),ALLZERO     WAS CORE GOTTEN FOR LABELS  UL0H
         BNE   SAVLABEL           ADDR NOT ZERO, CORE GOTTEN       UL0H
         GETMAIN  R,LV=640        GET CORE IF NECESSARY            UL0H
         ST    PARAM,IEHMVV72     SAVE ADDR OF GOTTEN CORE         UL0H
         MVC   IEHMVV72+4(4),IEHMVV72  SAVE POINTER TO CURRENT     UL0H
*                                      LABEL SAVEAREA              UL0H
SAVLABEL DS    0H                                                  UL0H
         L     M9,IEHMVV72+4      GET ADDR CURRENT LBL SAVEAREA    UL0H
         MVC   0(80,M9),0(M8)     MOVE LBL TO AREA FROM LABEL      UL0H
*                                 BUFFER ADDRESSED BY PARAM LIST   UL0H
         LA    M9,80(M9)          UPDATE POINTER TO NEXT AREA      UL0H
         ST    M9,IEHMVV72+4      STORE UPDATED POINTER            UL0H
         SR    M9,M9                                               UL0H
         IC    M9,IEHMVV72+12     PICK UP COUNT OF LABELS          UL0H
         LA    M9,1(M9)           INCREMENT COUNT BY 1             UL0H
         STC   M9,IEHMVV72+12     STORE UPDATED COUNT              UL0H
         LA    LINK,4             SET RETURN CODE TO 4 FOR OPEN    UL0H
*                                 TO GET ADDITIONAL LABELS         UL0H
         BR    RETURN             RETURN TO OPENJ ROUTINE          UL0H
*
IOERRMSG DS    0H                                                  UL0H
         IC    ACCE,IEHMVV61            SAVE CURRENT MSG CODE      UL0H
         LA    M9,17                    LOAD MSG CODE AND STORE    UL0H
         STC   M9,IEHMVV61               FOR MESSAGE WRITER        UL0H
         L     LINK,GOTOMESS            GET ADDR OF MESSAGE WRITER UL0H
         LR    M9,RETURN                SAVE RETURN ADDR FOR OPENJ UL0H
         BALR  RETURN,LINK              BRANCH TO WRITE MESSAGE    UL0H
         STC   ACCE,IEHMVV61            RESTORE PREVIOUS CODE      UL0H
         LR    RETURN,M9                RESTORE RETURN ADDRESS     UL0H
         B     ERMSGRET                 RETURN TO INLINE PROCESSINGUL0H
LOADDCB  DCB  DSORG=PS,MACRF=RP,RECFM=U
LOADDCBE DS    0F
LENGTHDC DC    A(LOADDCBE-LOADDCB)
MOVELT   MVC   0(1,ACCE),LOADDCB
BREAD    DC    A(IEHMVERK)
         EXTRN IEHMVERK
UUWW     DS    0H
         L     ACCE,IEHMVV30
         MVI   DCBRECFM-IHADCB(ACCE),X'80'
         LA    ACCO,80
         STH   ACCO,DCBBLKSI-IHADCB(ACCE)
         STH   ACCO,DCBLRECL-IHADCB(ACCE)
         MVI   DCBDSORG-IHADCB(ACCE),X'40'
         B     RTNDCBX
         EXTRN UNLREC1
LDRC     DC    A(UNLREC1)         IN SRZ,FIRST RECORD OF UNLOAD DATAS.
         EJECT
*
*
IEHMVESM DS    0H                 SECURITY TEST
*
*
         L     DSCB,IEHMVV30+8          SET UP DSCB POINTER
*
         TM    DS1DSIND,SECUR           IS DATA SET SECURITY PROTECTED
         BZ    IEHMVRSM           NO,RETURN
*                                       YES, PROCEED WITH TESTING
         TM    IEHMVV20,DSGROUP         DSGROUP FUNCTION
         BO    CONTEST                  YES, CONTINUE TEST
         TM    IEHMVV20,VOLUME          NO, PROCEED TO TEST FOR VOLUME
         BZ    IEHMVRSM           RETURN
CONTEST  DS    0H
         TM    IEHMVV20+1,SECURITY      SECURITY OPERAND GIVEN
         BO    IEHMVRSM           YES,RETURN
*
*
*
*
         MVI   IEHMVV61,SECER     SET ERROR MESSAGE
         L     LINK,PREAB         PREABORT
         BR    LINK
*
         EJECT
*
IEHMVESW DS    0H                 ACCESS METHOD TEST
         TM    IEHMVV20+5,X'40'  UNLOAD REQUESTED                A37919
         BO    UNLREQMS                YES GIVE MESSAGE          A37919
         L     DSCB,IEHMVV30+8          GET DSCB POINTER
         CLI   DS1DSORG,BDAM            DSORG BDAM
         BE    TSTBDAM                  YES CHECK DEVICE TYPE
PROBDAM  DS    0H                 TEST FOR DA
         L     ACCE,IEHMVV23+4    ACTIVE TO VOL LIST
         CLI   2(ACCE),DASD       DIRECT ACCESS
         BNE   TAPHYSE            NO,CONTINUE
*                                 YES,NO UNLOAD
*
TAGOBACK DS    0H
         B     IEHMVRSW
*
*
*
TAPHYSE  DS    0H                 CONTINUE TEST
**********************************************************************
* LINES 083700-083780 WERE REACTIVATED IN                      @Z40CSJH
* SUPPORT OF THE EXTENDED MVS CVOL FUNCTION                    @Z40CSJH
**********************************************************************
         TM    IEHMVV20,CATALOG        TEST FOR CATALOG FUNCTIONXA00427
         BNO   TAPHYSEQ                IF NOT, TEST KEYLEN      XA00427
         OI    IEHMVV20+1,UNLOAD       IF CATALOG, HAS TO BE    XA00427*
                                         UNLOADED               XA00427
         B     TAGOBACK                                         XA00427
TAPHYSEQ EQU   *
         CLI   DS1KEYL,AZERO
         BNE   TAKEY              UNLOAD CONDITION
         CLI   DS1DSORG,BSAM            IS DSORG PHYSICAL SEQUENTIAL
         BE    TAGOBACK                 YES, NOT AN UNLOAD
*                                          TABLE-PRINT MESSAGE
         CLI   DS1DSORG,AZERO     DEFAULT
         BE    TAGOBACK
TAKEY    DS    0H                 SET UNLOAD CONDITION
         MVI   IEHMVV61,X'94'      ACCESS METHOD NOT COMPAT.     A29052
         B     WRTMSG                  WRITE MESSAGE             A37919
*
TAKEYS   DS    0H
         MVI   IEHMVV61,X'A0'      DEVICE TYPES NOT COMPAT MESS  A29052
         B     WRTMSG                  WRITE MESSAGE             A37919
*
UNLREQMS DS    0H                                                A37919
         MVI   IEHMVV61,X'A4'          UNLOADREQUEST MSG         A37919
*
WRTMSG   DS    0H                                                A37919
         OI    IEHMVV20+1,UNLOAD SET THE UNLOAD-CONDITION        A37919
         L     ACCO,GOTOMESS       GO TO 'MSG' IN SRZ TO HAVE    A29052
         BALR  RETURN,ACCO            MESSAGE WRITTEN.           A29052
         B     TAGOBACK
TSTBDAM  DS    0H                 TEST FOR IDENTICAL DEVICES
         L     ACCE,IEHMVV22      ADDR OF FROM VOL
         L     ACCO,IEHMVV23+4    ADDRESS OF TO VOL
*
*
         TM    IEHMVV20+1,LOAD          TEST FOR BDAM LOAD CONDITION
         BZ    CHEKCUR                  BRANCH IF NOT A LOAD CONDITION
         CLC   DEVCHARU+2(1),DEVT2+2   COMPARE DEV CLASS       @ZA04396
         BNE   TAKEYS                  NOT EQUAL UNLOAD BDAM   @ZA04396
         CLI   DEVCHARU+3,X'09'        FROM 3330-1             @ZA04396
         BE    TESTTO                  YES TEST TO DEVICE      @ZA04396
         CLI   DEVCHARU+3,X'0D'        FROM 3330-11            @ZA04396
         BNE   EQUAL                   NO GO TO EQUAL          @ZA04396
TESTTO   CLI   DEVT2+3,X'09'           TO 3330-1               @ZA04396
         BE    MAINLINE                YES GO ON               @ZA04396
         CLI   DEVT2+3,X'0D'           TO 3330-11              @ZA04396
         BE    MAINLINE                YES GO ON               @ZA04396
EQUAL    CLC   DEVCHARU+3(1),DEVT2+3   COMPARE                 @ZA04396
         BNE   TAKEYS                  UNLOAD BDAM             @ZA04396
MAINLINE CLC   DEVCHARU(1),DEVT2       COMPARE FIRST BYTE OF   @ZA04396
*                                      DEVICE TYPE FIELD         A25286
         BNE   TAKEYS                                            A25286
         B     TESTOVF
CHEKCUR  CLC   DEVT1(4),DEVT2           COMPARE UCB TYPE FIELDS
         BE    TESTOVF             DEVICES ALIKE                 A21645
         CLI   2(ACCO),DASD       IS TO DEVICE DIRECT ACCESS     A21645
         BNE   TAKEYS             NO,UNLOAD BDAM                 A21645
         CLC  DEVT1+4(4),DEVT2+4  IS INPUT DEVICE LARGER THAN    A21645
*                                  MAXIMUM OUTPUT BLKSIZE        A21645
         BH    TAKEYS              YES,UNLOAD DATA SET           A21645
TESTOVF  TM    DS1RECFM,YESOFLOW        WAS FROM WRITTEN WITH TRACK
*                                       OVERFLOW
         BO    OFLOW               BRANCH IF YES
         B     IEHMVRTE           ALLOW BDAM MOVE
OFLOW    DS    0H
         TM    DEVT1+2,TAPE            FROM TAPE               @ZA03378
         BO    IEHMVRTE                YES LOAD                @ZA03378
         TM    DEVT1+1,YAOFLOW     DOES FROM DEVICE HAVE OFLOW FEATURE.
         BZ    MSG1                     IF NOT-DEVICES ARE NOT COMPA-
*                                          TABLE-PRINT MESSAGE
         B     IEHMVRTE           ALLOW MOVE
         EJECT
*
IEHMVETE DS    0H                 TEST RECORD LENGTH
         L     ACCE,IEHMVV22+4     FROM VOLUME LIST
         L     ACCO,IEHMVV23+4     TO VOLUME LIST
         CLI   2(ACCO),TAPE        TO TAPE
         BE    IEHMVRTE            YES
         CLI   2(ACCO),DASD        TO VOLUME DA
         BNE   WOW                 NO
         CLI   2(ACCE),TAPE        FROM TAPE
         BE    TESTDSCB                 OBTAIN DSCB
         L     DSCB,IEHMVV30+8     NO,GET FROM DSCB POINTER
         TM    DS1RECFM,YESOFLOW   WRITTEN IN TRACK OVERFLOW
         BZ    TESTDSCB           OBTAIN DSCB
         TM    DEVT1+1,YAOFLOW       DOES UNIT HAVE OVERFLOW FEATURE.
         BO    TESTDSCB           OBTAIN DSCB
MSG1     MVI   IEHMVV61,NOTOF      SET OP MSG
ABORT    L     LINK,PREAB          SET UP TO PREABORT  IN SRZ
         BR    LINK
TESTDSCB DS    0H
         L     ACCE,IEHMVV23      ADDR OF TO VOL LIST
         CLI   2(ACCE),DASD       TO DEVICE DIRECT ACCESS
         BNE   TRACKCAP
         XC    IEHMVV00(20),IEHMVV00    CLEAR 20 SPACES
         MVI   IEHMVV00,MODE      INDICATE SEARCH MODE
         MVC   IEHMVV00+4(4),IEHMVV21+4   TO DATASET NAME
         LA    ACCE,4(ACCE)         GET ADDRESS OF VOL SERIAL NO.
         ST    ACCE,IEHMVV00+8    SERIAL NUMBER
         LA    ACCE,IEHMVV00+16    GET ADDRESS OF WORK AREA
         ST    ACCE,IEHMVV00+12    STORE ADDRESS OF WORK AREA
         OBTAIN IEHMVV00
         LTR   LINK,LINK          RETURN CODE EQUAL TO ZERO
         BZ    PREAL
* ALL TESTING OF THE DSCB FIELDS AFTER THIS POINT, WITH THE EXCEPTION*
* OF THE TWO REFERENCES IN 'PREAL', PERTAIN TO THE 'FROM' DSCB.      *
NOPREAL  TM    DS1RECFM,YESOFLOW   WRITTEN WITH TRACK OVERFLOW
         BZ    TRACKCAP           TEST RECORD LENGTH
TODEVTO  DS    0H
         TM    DEVT2+1,YAOFLOW     DOES TO DEVICE HAVE TRACK OVERFLOW.
         BO    IEHMVRTE
         B     TAKEYS                  UNLOAD DATA SET
* WHEN ADDRESSING THE 'TO' DSCB AFTER THE OBTAIN MACRO,
*  A DISPLACEMENT OF 44 MUST BE MADE BACK TO THE DSNAME AT THE KEY.
*  SINCE THE DSCB STARTS AT IEHMVV00+16, 28 MUST BE SUBTRACTED
*  FROM THE DSCB DISPLACEMENT.
PREAL    L     M10,IEHMVV22       ADDRESS OF FROM VOLUME LIST    A50960
         L     M11,IEHMVV23+4   ADDRESS OF ACTIVE TO VOLUME      S21042
         TM    IEHMVV20+3,X'04'                                  A48802
         BO    PREAL1                                            A48802
         CLC   4(6,M10),4(M11)  COMPARE VOLID OF T0 & FROM VOL   S21042
         BE    NOPREAL          EQUAL, THEN NO PREALLOCATION     S21042
PREAL1   OI    IEHMVV20+3,X'08'        INDICATE PREALLOCATION    A48802
*                                                                S21042
         L     PARAM,IEHMVV31+8        GET PTR TO 'TO' DDNAME    S21042
         LA    PARAM,0(PARAM)                                    S21042
         DEVTYPE (PARAM),INFO,DEVTAB   GET DEVICE CHARACTERISTICSS21042
         MVC   CCHHR(5),96(ACCE)       MOVE CCHHR IN ABS ADDR LOCS21042
         LH    PARAM,CCHHR             GET CC                    S21042
         MH    PARAM,TRCYL             CONVERT CC TO TT          S21042
         AH    PARAM,CCHHR+2           GET TOTAL TT              S21042
         SLL   PARAM,8                 FREE RIGHTMOST BYTE       S21042
         IC    PARAM,CCHHR+4           INSERT R                  S21042
         SLL   PARAM,8                 MOVE TTR TO LEFT 3 BYTES  S21042
         ST    PARAM,IEHMVV82+4        SAVE TTR                  S21042
*                                                                S21042
         GETMAIN R,LV=120              SAVE AREA FOR 'TO' DSCB   S21042
         OI    IEHMVV20+5,X'01'        INDICATE GETMAIN DONE   @ZA24602
         ST    PARAM,IEHMVV82+12       SAVE PTR TO THIS AREA     S21042
         XC    0(120,PARAM),0(PARAM)   CLEAR AREA                S21042
         MVC   0(117,PARAM),IEHMVV00   MOVE IN OBTAIN'S RESULT   S21042
         CLI   DS1RECFM-28-DS1DSNAM(MCTABLE),AZERO  RECFM GIVEN IN DSCB
         BE    NOPREAL            NO, SAME AS NOT PREALLOCATED   A50960
         TM    DS1RECFM-28-DS1DSNAM(MCTABLE),YESOFLOW T.O. IN TO DSCB
         BO    TODEVTO
TRACKCAP STM   RETURN,PARAM,DEVSAVE   STORE REGISTERS TO BE USED
         L     DEV14,IEHMVV31+8        DDNAME OF THE 'TO' VOLUME   1719
         LA    PARAM,0(DEV0,DEV14)
         DEVTYPE (1),DEVCHAR,DEVTAB
         LH    ACCO,DEVCHAR+12     GET TRACK CAPACITY
         LM    RETURN,PARAM,DEVSAVE   RESTORE REGISTERS
         SR    M5,M5
         L     ACCE,IEHMVV22+4         ADDR OF THE 'FROM' VOLUMEYA01218
         CLI   2(ACCE),TAPE        FROM TAPE
         BE    SIZE                BRANCH AROUND KEYL
         IC    M5,DS1KEYL
SIZE     AH    M5,DS1BLKL          ADD KEVL AND BLOCKSIZE
         CR    M5,ACCO             COMPARE LRECL TO CAPACITY
         BNH   IEHMVRTE
         CLC   DS1EXT1+2(8),ZEROS       CHECK FOR MODEL-DSCB     A45144
         BNE   WOW                                               A45144
         CLC   DS1SCALO+1(3),ZEROS      CHECK FOR MODEL DSCB     A45144
         BNE   WOW                                               A45144
         CLC   DS1DSSN(6),ZEROS         CHECK FOR MODEL-DSCB     A45144
         BE    WOW                                               A45144
         B     IEHMVRTE                 IF MODEL-DSCB, BRANCH    A45144
WOW      DS    0H                 TEST FOR REBLOCKING
         TM    DS1RECFM,BLOCKED   BLOCKED FORMAT
*                                           FORMAT
         BZ    NORB                    NO, GO TO SET UP UNLOAD
*
         TM    DS1RECFM,UNDEF     UNDEFINED FORMAT
         BO    NORB                    YES, NO REBLOCKING
*                                      NO, RECFM IS FB OR VB
         LH    M5,DS1LRECL        LOGICAL REC LENGTH
         CR    M5,ACCO             COMPARE SIZE TOCAPACITY
         BH    NORB                    NO, SET UP UNLOAD
         ST    ACCO,IEHMVV10-12                                 YA01218
         TM    DS1RECFM,FIXED     RECFM FIXED
         BNO   VBLOCKED                NO                       YA01218
*
*                                      FIXED BLOCKED
         OI    IEHMVV10-4,X'20'             INDICATE TO MOVE DS
         MVI   IEHMVV10-1,X'07'             ON LRECL
         B     IEHMVRTE
VBLOCKED EQU   *                       VARIABLE BLOCKED
         OI    IEHMVV10-4,X'10'             INDICATE TO MOVE DS
         MVI   IEHMVV10-1,X'09'
         B     IEHMVRTE                NO UNLOAD RETURN
NORB     DS    0H                 SET UNLOAD CONDITION
*MESSAGE - DATA SET UNLOADED BECAUSE RECORD LENGTH NOT DEVICE
*                 COMPATIBLE
         MVI   IEHMVV61,X'96'
         OI    IEHMVV20+1,UNLOAD
         L     ACCO,GOTOMESS       GO TO SRZ TO HAVE             A29052
         BALR  RETURN,ACCO         MESSAGE WRITTEN.              A29052
         B     IEHMVRTE
         EJECT
*
IEHMVESL DS    0H                 UNMOVEABLE TEST
*
*
*
         TM    IEHMVV10-4,UNMOVE  UNMOVEABLE DATA SET
         BZ    IEHMVRSL           NO,
*                                 YES,CONTINUE WITH TEST
UNYES    DS    0H                                                  0310
*
*             TO VOLUME DIRECT ACCESS
         L     ACCE,IEHMVV23           GET TO VOL LIST
         CLI   2(ACCE),DASD            IS TO VOL DIRECT ACCESS
         BNE   INMUN                   IF NO...... BRANCH
         IC    ACCE,IEHMVV61       SAVE MESSAGE CODE.            A29052
         MVI   IEHMVV61,X'15'      WRITE MESSAGE STATING THAT    A17911
         L     ACCO,GOTOMESS         UNMOVABLE DATA MUST BE      A17911
*                                    UPDATED (IEH452I).          A17911
         BALR  RETURN,ACCO         GO TO SRZ TO WRITE MESSAGE.   A17911
         STC   ACCE,IEHMVV61       RESTORE MESSAGE CODE.         A29052
         B     IEHMVRSL            RETURN TO MAIN FLOW AND EXIT. A17911
INMUN    DS    0H                  UNLOAD UNMOVABLE DATA SETS    A17911
         OI    IEHMVV20+1,UNLOAD     NOT GOING TO DIRECT ACCESS. A17911
         IC    ACCE,IEHMVV61       SAVE  MESSAGE  CODE.          A29052
         MVI   IEHMVV61,DSCBER     SET MESSAGE INDICATOR.        A17911
         L     ACCO,GOTOMESS       GO TO 'MSG' IN SRZ TO HAVE    A29052
         BALR  RETURN,ACCO         RESTORE MESSAGE CODE.         A29052
         B     IEHMVRSL            BRANCH TO MAIN FLOW AND       A17911
*                                    RETURN TO IEHMVSRZ.         A17911
**
*
*******     ******    ** * * * * * * * * * *  * **  * * * * * * *
*
         EJECT
*
BADMTTO  DS    0H                 ERROR CONDITION
         MVI   IEHMVV61,BADMT     SET MESSAGE
         L     LINK,PREAB
         BR    LINK
*
**********************************************************************
* LINES 123500-132361 WERE REACTIVATED IN                      @Z40CSJH
* SUPPORT OF THE MVS EXTENDED CVOL FUNCTION                    @Z40CSJH
**********************************************************************
         USING IEHMVESV,BASE
YESCATLG DS    0H       ALLOCATES CATALOG ON RECEIVING VOLUME
         TM    IEHMVV20+4,OK      FIRST TIME SWITCH FOR SZ
         BO    RTNCATLG
         XC    IEHMVV00(140),IEHMVV00  SET TO ZEROS
         MVI   IEHMVV00,BLANK     PUT BLANKS IN NAME OF JFCB
         MVC   IEHMVV00+1(43),IEHMVV00
         MVC   IEHMVV00(7),SYSCTLG
         MVI   IEHMVV00+44,SET
         MVI   IEHMVV00+94,OK
         MVI   IEHMVV00+97,TEN
         MVI   IEHMVV00+59,ONE
         MVI   IEHMVV00+111,TEN
*        SET PARAMETERS FOR SVC 32
         L     ACCE,IEHMVV31+8    GET POINTER TO 'TO' DDNAME
         SR    PARAM,PARAM        CLEAR REG 1(PARAM)           @Z40CSJH
         ICM   PARAM,3,DDUCB(ACCE)     @ UCB                   @Z40CSJH
         MVC   IEHMVV00+45(6),28(PARAM)
         ST    PARAM,IEHMVV31
         LA    PARAM,IEHMVV31     POINTER TO ADDRESS OF UCB
DDUCB    EQU   14
         LA    ZERO,IEHMVV00      ADDRESS OF JFCB
         LA    ACCE,2048          SET HIGH ORDER BIT TO ONE TO SHOW
         SLL   ACCE,20            FROM UTILITY
         OR    ZERO,ACCE
         L     ACCE,IEHMVV23      GET TO VOL
         CLI   2(ACCE),DASD       IS IT DIRECT ACCESS
         BNE   TONOTDA            NO
         SVC   32                 SPACE ALLOCATION
         LA    RETURN,4
         SR    LINK,RETURN        SUBTRACT 4 FROM RETURN CUDE
         BC    12,BBB1            BRANCH IF SUCCESSFUL ALLOCATION OR
*                                 DUPLICATE NAME DSCB EXISTS
         MVI   IEHMVV61,ALER      SET UP MESSAGE
         L     LINK,PREAB
         BR    LINK
TONOTDA  DS    0H          WRITE ERROR MESSAGE
         IC    ACCE,IEHMVV61      SAVE MSG NUMBER
         MVI   IEHMVV61,CATER     SET UP MESSAGE
         L     LINK,GOTOMESS
         BALR  RETURN,LINK
         STC   ACCE,IEHMVV61      RESTORE MSG NUMBER
         NI    IEHMVV20+1,AND
         B     RTNCATLG
GOTOMESS DC    A(MSG)
         EXTRN MSG
BBB1     DS    0H                                              @Z40CSJH
         OI    IEHMVV20+4,OK      SET FIRST TIME SW FOR SZ     @Z40CSJH
         B     RTNCATLG                                        @Z40CSJH
*
ZEROS    DC    2F'0'                                             A45144
SYSCTLG   DC   C'SYSCTLG'                                      @Z40CSJH
*        DATA SET INDICATORS IN DSCB
*
*
         DS    0F
DOG      DC    C'  SZ'
ABSE     DC    A(IEHMVESV)
LASTVOL  EQU   X'80'
UNMOVE   EQU   X'08'
SECUR    EQU   X'10'
SYNADRTN DC    A(LTPIO)                                          A35460
PREAB    DC    A(PREABORT)
         EXTRN PREABORT
INFO     DS    0CL20                   +                         S21042
         DS    CL10                    +DEVICE TYPE              S21042
TRCYL    DS    CL2                     +CHARACTERISTICS          S21042
         DS    CL8                     +                         S21042
CCHHR    DS    CL5                     ABS ADDR OF 'TO' DSCB     S21042
         EJECT
MOVE     EQU   X'80'
COPY     EQU   X'40'
DSNAME   EQU   X'20'
DSGROUP  EQU   X'10'
PDS      EQU   X'08'
CATALOG  EQU   X'04'
VOLUME   EQU   X'02'
FROM     EQU   X'01'
TO       EQU   X'80'
CVOL     EQU   X'40'
UNCATLG  EQU   X'20'
CATLG    EQU   X'10'
SECURITY EQU   X'08'
EXPAND   EQU   X'04'
LOAD     EQU   X'02'
UNLOAD   EQU   X'01'
MIDABORT EQU   X'40'
ENDINPUT EQU   X'20'
PHONYTO  EQU   X'10'
ANYINCRP EQU   X'08'
ANYEXCRP EQU   X'04'
ZERO     EQU   0
PARAM    EQU   1
ACCE     EQU   2
ACCO     EQU   3
BASE     EQU   4
M5       EQU   5
M6       EQU   6
M7       EQU   7
M8       EQU   8
M9       EQU   9
M10      EQU   10
M11      EQU   11
MCTABLE  EQU   12
SAVE     EQU   13
RETURN   EQU   14
LINK     EQU   15
DSCB     EQU   M7
WORK3   EQU   ACCO
WORK2   EQU   ACCE
DASD    EQU   X'20'
BDAM     EQU   X'20'
ALER     EQU   X'05'
AND      EQU   X'EF'
AZERO    EQU   X'00'
BADMT    EQU   X'1A'
BLANK    EQU   X'40'
BLOCKED  EQU   X'10'
BSAM     EQU   X'40'
CATER    EQU   X'07'
DATAC    EQU   X'02'
DSCBER   EQU   X'98'
EIGHT    EQU   X'08'
ERROR    EQU   X'0E'
PSERR    EQU   X'17'                   MSG-DATA SET HAS          A25631
*                                      INCORRECT FORMAT          A25631
FIXED    EQU   X'80'                   BIT IN RECFM                1719
KEYL     EQU   X'01'
FRDDKEY  EQU   X'02'
LABEL    EQU   X'0A'              STANDARD USER LABELS             UL0H
SL       EQU   X'02'              INDICATES STANDARD LABEL       A32041
DEACTIVE EQU   X'00'              DEACTIVATE DCB EXIT LIST         UL0H
ACTIVIHL EQU   X'01'                                               UL0H
ACTIVOHL EQU   X'02'                                               UL0H
ACTIVITL EQU   X'03'                                               UL0H
ACTIVOTL EQU   X'04'                                               UL0H
ACTIVEND EQU   X'87'              JFCB ACTIVE END OF DCB EXIT LIST UL0H
ALLZERO  DC    F'0'                                                UL0H
MERROR   EQU   X'18'
FROMERR  EQU   X'22'   MSG19, 'FROM' D.S. NOT IN UNLOADED FORMAT  10943
MODE     EQU   X'C1'
NEW      EQU   X'C0'
NONTOF   EQU   X'0D'
NOTOF    EQU   X'0F'
OK       EQU   X'80'
ONE      EQU   X'01'
PREALL   EQU   X'08'
SECER    EQU   X'10'
SET      EQU   X'F1'
TAPE     EQU   X'80'
TEN      EQU   X'0A'
TLOAD    EQU   X'40'
UNDEF    EQU   X'C0'
UNITRCD  EQU   X'08'
YAOFLOW  EQU   X'40'
YESOFLOW EQU   X'20'
         EJECT
         TITLE 'DSCB FORMAT'
DSCBDSEC DSECT
         IECSDSL1 (1)
         ORG   DS1EXT1+3
DADPQ    DS    CL4                      PRIM QUAN FOR MODEL ALLOC
         IEHMVV
         ORG   IEHMVV70                                            UL0H
         IEHDCBXL                                                  UL0H
         ORG   IEHMVV00
         IEFJFCBN
         TITLE 'DCB FORMAT'
         DCBD  DSORG=(PS,IS,DA,PO)
         END
