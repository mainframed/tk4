 TITLE 'IEHMVESA-IEHMOVE MESSAGE MODULE'
*FUNCTION/OPERATION: THIS MODULE CONTAINS A MESSAGE CSECT CONTAINING  *
*ALL MESSAGES USED BY THE FOLLOWING MODULES: IEHMVSSE, IEHMVSST &     *
*IEHMVSSS. THE FUNCTION OF THIS MODULE IS TO SELECT THE PROPER MESSAGE*
*BASED ON THE MESSAGE NUMBER PASSED IN REG 3 AND SET IT UP IN THE     *
*WORKAREA TO BE PRINTED BY THE MESSAGE WRITER (IEHMVSSU).             *
*ENTRY POINTS: IEHMVESA                                               *
*INPUT: MESSAGE NUMBER IN REG 3 MODULE TO RETURN TO NUMBER IN REG 2.  *
*REG 2 = 4 IEHMVSSD REG 2 = 8 IEHMVSSS REG 2 = 12 IEHMVSST REG 2 = 0  *
*IEHMVSSK (FINAL WRAPUP) POINTER TO IEHMOVE1 WORKAREA IN REG 12       *
*EXTERNAL ROUTINES: IEHMVSSU (MESSAGE WRITER)                         *
*EXITS - NORMAL SEE INPUT                                             *
*TITLE ' IEHMVSSD                                                     *
*ENTRY POINTS: IEHMVESD                                               *
*OUTPUT: PASSES IEHMOVE1 SAVE AREA POINTER IN REG 13                  *
*EXITS - NORMAL 'LINK' TO IEHMVSSE (E.P. IEHMVESE)                    *
IEHMVSSA CSECT
*D009000,024950                                                  A48781
*A008300,A009060,009160,024920-024940                            A48781
*2051000200,003200,020000,020400-020600,021000-021200              2752
*A8816-8819,1374-1376,20740-20797,24960-24997                   YL026VC
*C20660                                                         YL026VC
*C024997                                                       @ZA07346
         ENTRY IEHMVESA
         USING IEHMVESA,BASE
         USING MESSAGE,MBASE
         USING IEHMVV,R12
IEHMVESA SAVE  (14,12),T,MESSAGE-MODULE-FOR-ST-SS-SE-10-28-65
         LR    BASE,15
         L     MBASE,AMBASE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
BASE     EQU   8
MBASE    EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
BLANK    EQU   C' '                     TO CLEAR PRINT-BUFFER    A48781
        IEHPRE (14),TFC
         LTR   R3,R3
         BM    LINK
         CH    R3,H12        IS IT MSG IEH315I(12TH MSG)?        A30926
         BE    SETRC4        SET RETURN CODE OF 4                A30926
         CH    R3,H22                  IS IT IEH473I            YL026VC
         BE    SETRC4                  SET RETURN CODE TO 4     YL026VC
         CH    R3,H20                  IS IT IEH471I            YL026VC
         BE    SETRC12                 SET RETURN CODE TO 12    YL026VC
         CH    R3,H16             IS IT MSG IEH388I (16TH MSG)     2752
         BE    SETRC12            YES,BRANCH                       2752
         CH    R3,H17             IS IT MSG IEH461I (17TH MSG)     2752
         BNE   SETRC8             NO, BRANCH                       2752
SETRC12  OI    IEHMVV41+13,X'04'  SET RETURN CODE OF 12            2752
SETRC8   OI    IEHMVV41+13,X'02'     SET RETURN CODE OF AT LEAST 8 2752
SETRC4   OI    IEHMVV41+13,X'01'  SET RETURN CODE OF 4           A30926
         MVI   IEHMVV00,BLANK           TO CLEAR PRINT-BUFFER    A48781
         MVC   IEHMVV00+1(120),IEHMVV00 CLEAR PRINT-BUFFER       A48781
         SLL   R3,2                     MULT MESSAGE NO. BY 4
        LA    R6,MESSTABL-4
         IC    R5,0(R3,R6)              INSERT MESSAGE LENGTH
         L     R4,0(R3,R6)             LOAD MESSAGE ADDR
         BCTR  R5,0                     REDUCE LENGTH BY ONE
         EX    R5,EXINST                MOVE MESSAGE TO PRINT LINE
         LA    R3,IEHMVV00
LINK     LINK  EP=IEHMVESU
       IEHPOST (14),T
         LA    15,0(15)
         LTR   15,15
         BNE   RET1
         B     RET2(R2)
RET2     EQU   *-4
         B     RET3                     R2 RETURN CONTROL 4 IS SSD
         B     RET4                                       8 IS SSS
         B     RET5                                      12 IS SST
        B     RET1
EXINST   MVC   IEHMVV00+1(1),0(R4)
RET1     XCTL  (4,12),EP=IEHMVESK
RET3    XCTL  (4,12),EP=IEHMVESE
RET4     XCTL  (4,12),EP=IEHMVESS
RET5     XCTL  (4,12),EP=IEHMVEST
H12      DC    H'12'
H16      DC    H'16'                                               2752
H17      DC    H'17'                                               2752
H20      DC    H'20'                   NUMBER FOR IEH471I       YL026VC
H22      DC    H'22'                   NUMBER FOR IEH473I       YL026VC
MESSTABL DS    0F
         DC    AL1(B-A)
         DC    AL3(A)
         DC    AL1(C-B)
         DC    AL3(B)
         DC    AL1(D-C)
         DC    AL3(C)
         DC    AL1(E-D)
         DC    AL3(D)
         DC    AL1(F-E)
         DC    AL3(E)
         DC    AL1(G-F)
         DC    AL3(F)
         DC    AL1(H-G)
         DC    AL3(G)
         DC    AL1(I-H)
         DC    AL3(H)
         DC    AL1(J-I)
         DC    AL3(I)
         DC    AL1(K-J)
         DC    AL3(J)
         DC    AL1(L-K)
         DC    AL3(K)
         DC    AL1(M-L)
         DC    AL3(L)
         DC    AL1(N-M)
         DC    AL3(M)
         DC    AL1(P-N)
         DC    AL3(N)
         DC    AL1(Q-P)
         DC    AL3(P)
         DC    AL1(R-Q)                                            2752
         DC    AL3(Q)
         DC    AL1(S-R)                                          A28295
         DC    AL3(R)                                              2752
         DC    AL1(T-S)                                         YL026VC
         DC    AL3(S)                                           YL026VC
         DC    AL1(U-T)                                         YL026VC
         DC    AL3(T)                                           YL026VC
         DC    AL1(V-U)                                         YL026VC
         DC    AL3(U)                                           YL026VC
         DC    AL1(W-V)                                         YL026VC
         DC    AL3(V)                                           YL026VC
         DC    AL1(Z-W)                                         YL026VC
         DC    AL3(W)                                           YL026VC
AMBASE   DC    A(MESSAGE)
MESSAGE  CSECT
A        DC    C'IEH301I INCLUDE OP NOT VALID'
B        DC    C'IEH302I EXCLUDE OP NOT VALID'
C        DC    C'IEH303I REPLACE OP NOT VALID'
D        DC    C'IEH304I SUBORDINATE REQ -SKIPPED'
E        DC    C'IEH305I MULTIPLE KEYWORD  ERROR'
F        DC    C'IEH306I MISPLACED KEYWORD ERROR'
G        DC    C'IEH307I KEYWORD NOT PERMITTED'
H        DC    C'IEH308I INVALID PARAMETER ERROR'
I        DC    C'IEH309I SYNTAX ERROR'
J        DC    C'IEH310I LENGTH ERROR'
K        DC    C'IEH311I INCOMPLETE REQUEST'
L        DC    C'IEH315I UNABLE TO FIND FROM VOL'
M        DC    C'IEH383I INVALID DEVICE NAME'
N        DC    C'IEH384I GENERIC DEVICE NAME ERR'
P        DC    C'IEH385I SELECT OP NOT VALID'
Q        DC    C'IEH388I UNABLE TO ALLOCATE'                       8257
         DC    C' IEHMOVE WORKFILES.'                              8257
R        DC    C'IEH461I UNABLE TO OPEN SYSIN DATA SET'            2752
S        DC    C'IEH390I INVALID DATA SET NAME SPECIFIED '       A48781
         DC    C'IN RENAME-PARAMETER'                            A48781
T        DC    C'IEH470I CVOL NOT PERMITTED.  DATA SET ASSUMED' YL026VC
         DC    C' TO BE CATALOGED IN MASTER CATALOG.'           YL026VC
U        DC    C'IEH471I DATA SET NOT FOUND IN CATALOG.'        YL026VC
V        DC    C'IEH472I CANNOT HONOR CATALOG REQUEST.  DATA'   YL026VC
         DC    C' SET NOT CATALOGED.'                           YL026VC
W        DC    C'IEH473I DATA SET WILL BE CATALOGED IN MASTER'  YL026VC
         DC    C'/USER CATALOG.'                               @ZA07346
Z        DS    0C
         IEHMVV                                                    2752
         END
