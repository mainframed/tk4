*TITLE ' IEHMVSSI                                                     *
*FUNCTION/OPERATION: THIS MODULE IS ENTERED ONLY FOR DSGROUP          *
*OPERATIONS. IT WILL INSURE THAT THE VOLUME, WITH THE CATALOG         *
*SPECIFIED IN THE DSGROUP REQUEST, IS MOUNTED AND WILL OPEN THE DATA  *
*SET 'SYSCTLG' FOR INPUT.                                             *
*ENTRY POINTS: IEHMVESI                                               *
*INPUT: POINTER TO IEHMOVE1 WORKAREA IN REG 12                        *
*EXTERNAL ROUTINES: IEHMVXSF (VOLUME MOUNTING RTN)                    *
*EXITS - NORMAL IEHMVSSC (CATALOG ANALIZER)                           *
*
* THE ENTIRE MODULE WAS REACTIVATED IN SUPPORT                 @Z40CSJH
* OF THE EXTENDED MVS CVOL FUNCTION                            @Z40CSJH
*
*C078500-079100,109600                                         @Z40CSJH
IEHMVSSI CSECT
*A38150                                                         XA01754
* 037800                                                         A27712
*1630002200,031800                                                12490
IEHMVESI SAVE  (14,12),T,DSGROUP-INITIALIZER-DBRUCE-9-20-65
         ENTRY IEHMVESI
         LR    R8,R15
         USING MESS,R4
 USING IEHMVV,12
         USING IEHMVESI,R8
       IEHPRE  (14,1),TFC
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
        IEHMVV
IEHMVSSI CSECT
* 012000-012400,014400-0146000                                     DT0I
         TM    IEHMVV20+4,X'10'        IS THIS RETURN FROM VOL MNT
         BZ    GOON                       IF NOT ---
         NI    IEHMVV20+4,X'EF'          TURN OFF SW
         B     VOLRET
GOON     EQU   *
         MVC   IEHMVV61(8),IEHMVV21     SET UP POINTERS FOR
*                                       C ATALOG DEPHLONKER
*                                     ***FIND OR MOUNT CVOL ***
         TM    IEHMVV20+1,X'40'       CUDL BIT ON
         BZ    FINDPACK                                            8113
VOLMNT   DS    0H
         L     R1,IEHMVV24
*                                     *** CALLING SEQUENCE AND CALL **
*                                      ** TO VOLUME MOUNTING ROUTINE**
*                                     *** VOLUME MOUNTING CALL RETURNS
*                                     '** POINTER TO DDNAME IN REG 1
VMGEN    EQU   0
VMINDICA EQU   4
VMIT     EQU   4
VMVMR    EQU   11
VMITC    EQU   10
VMDEV    EQU   12
VMVSN    EQU   16
VMRTA    EQU   22
VMUNIT   EQU   24                                                  DT0I
*
*                                      TO CALL VOL MOUNT
*                                           NO VALID OLD REQUESTS
*                                      REG 1 POINTS TO
*
*                                      DEVICE TYPE 4 BYTES
*                                      VOL ID 6 BYTES
         MVC   IEHMVV00+VMIT(4),IEHMVV50+4  PTR TO IT TABLE
         MVC  IEHMVV00+VMITC(1),IEHMVV50+4  IT ENTRY COUNT
         MVI   IEHMVV00+VMVMR,X'01'    NUMBER OF REQUESTS
         MVI   IEHMVV00+VMINDICA,X'90' SET UP INDICATOR
         MVC   IEHMVV00+VMDEV(10),0(1) SET UP DEVICE TYPE
         MVC   IEHMVV00+VMUNIT(5),12(R1)                           DT0I
         LA    R3,IEHMVV00
         LH    R4,SI
         IEHPOST  (14),T
         XCTL  EP=IEHMVXSF
VOLRET   EQU   *
         TM    IEHMVV00+VMIT,X'01'    WAS VOLUME MOUNTED
         BZ    NOMOUNT                 NO MOUNTING INITIATED ERROR MSG
         L     1,IEHMVV00+VMIT         YES
         AH    1,IEHMVV00+VMRTA
         L     1,0(0,1)                GET POINTER TO DDNAME
         XC    IEHMVV00(48),IEHMVV00   CLEAR VOLITAL WORK AREA
         MVI   IEHMVV00,X'C1'          CODE FOR OBTAIN DSNAME
         LA    R2,IEHMVV00+16          ADDR OF DSNAME AREA
         ST    R2,IEHMVV00+4           STORE PTR TO DSNAME AREA
         L     R2,IEHMVV24             LOAD CVOL PTR
         LA    R2,4(R2)                BUMP PTR TO VOLID SLOT
         ST    R2,IEHMVV00+8           STORE PTR TO VOL ID
         LA    R2,IEHMVV00+64     LOAD ADDR OF 400 BYTE AREA
         ST    R2,IEHMVV00+12          STORE PTR TO AREA
         MVI   IEHMVV00+16,X'40'       CLEAR DSNAME AREA
         MVC   IEHMVV00+17(43),IEHMVV00+16
         MVC   IEHMVV00+16(7),JDSNAME  MOVE SYSCTLG TO DSNAME AREA
         LR    R4,R1                   SAVE PTR TO DDNAME
        OBTAIN IEHMVV00
         LA    15,0(15)
         LTR   15,15
         BNE   NOFIND
         L     R2,IEHMVV10              ADDR OF PERM WORK AREA
         LA    R2,4(R2)                 ALIGN TO FULL WD BOUNDRY
         SRL   R2,2
         SLL   R2,2
         MVC   0(44,R2),IEHMVV00+16    MOVE DSCB KEY TO WORK AREA
         LA    R3,44(R2)               BUMP PTE TO DATA
         MVC   0(96,R3),IEHMVV00+64    MOVE DSCB DATA TO PERM WORK AREA
         ST    R2,IEHMVV30+8      STORE ADDR OF FROM DSCB
         LA    R2,140(R2)
         ST    R2,IEHMVV30+4            ADDR OF FROM DECB
         LA    R2,24(R2)
         ST    R2,IEHMVV30              ADDR OF FROM DCB
         MVC   0(RENT2-RENT1,R2),RENT1
         LR    R1,R4                   LOAD PTR TO DDNAME
         MVC   40(8,R2),0(R1)           MOVE DD NAME INTO DCB
         LA    R2,RENT2-RENT1(R2)      BUMP AND ALIGN PERM WORK AREA
         LA    R2,4(R2)                 POINTER
         SRL   R2,2
         SLL   R2,2
         ST    R2,IEHMVV10
*                                       R1 IS POINTER TO DDNAME RETURN
*                                       FROM VOL MOUNT
         XC    IEHMVV00(176),IEHMVV00  CLEAR JFCB AREA
         MVC   IEHMVV00(52),JDSNAME     MOVE SYSCTLG INTO JFCB
         MVI   IEHMVV00+87,X'40'        SET STATUS TO OLD
         L     R4,IEHMVV24
         MVC   IEHMVV00+118(6),4(R4)    MOVE VOL SER TO JFCB
         MVI   IEHMVV00+117,X'01'       MOVE NUMBER VOL SER TO JFCB
         MVI   IEHMVV00+130,X'40'      END OF VOL SER LIST
         LA    R3,IEHMVV00              LOAD ADDR OF JFCB
         ST   R3,IEHMVV00+176           STORE IN EXIT LIST
         LA    R3,IEHMVV00+176         LOAD ADDR OF EXIT LIST
         ST    R3,IEHMVV00+180          STORE IT IN WORD PAST EXIT LIST
         MVI   IEHMVV00+176,X'87'       SET CODE IN LEFT BYTE OF LIST
         L     R4,IEHMVV30              LOAD ADDR OF FROM DCB
         LA    R3,IEHMVV00+176         LOAD ADDR OF EXIT LIST
         STH   R3,38(R4)               STORE IN DCB
         SRL   R3,16
         STC   R3,37(R4)
         OPEN  ((R4),(INPUT)),TYPE=J
         B     EXIT1
*                                     ***FIND SYSRES UCB
*                                    ***FIND SYSRES UCB
FINDPACK DS    0H                                                  8113
         L     R1,IEHMVV21        GET PTR. TO DSNAME               8113
         MVC   FQDSNAME(44),0(R1) PUT DSNAME INTO CAMLST AREA      8113
         LOCATE GRPLOC            EXEC. LOCATE FOR DSNAME          8113
         L     R1,16              GET PTR. TO CVT                  8113
         L     R1,0(R1)           GET PTR. TO TCB                  8113
         L     R1,0(R1)                                            8113
         L     R1,12(R1)          GET PTR. TO TIOT                 8113
         LA    R1,24(R1)          GET PTR. TO DD ENTRY             8113
FINDID   ICM   R2,3,16(R1)             GET UCB @               @Z40CSJH
         N     R2,CLEARMSK             SAVE 16 BITS            @Z40CSJH
         TM    16(R1),X'FF'            LAST UCB                @Z40CSJH
  BC  1,NOTSUB
   CLC   RSLTAREA+259(6),4(R2)
   BE   VOLTYPE
   B   CONT
NOTSUB   CLC   RSLTAREA+259(6),28(R2)
         BE    VOLTYPE            GO SET UP DEVTYPE                8113
CONT  SR   R3,R3
         IC    R3,0(R1)                 LENGTH OF DIRECTORY ENTRY  8113
         AR    R1,R3     GET PTR TO LENGTH OF NEXT ENTRY          12490
         IC    R3,0(R1)  GET LENGTH OF NEXT ENTRY                 12490
         LTR   R3,R3                    END OF DIRECTORY?          8113
         BZ    NOFIND                   YES, GO TO NOFIND          8113
         B     FINDID                                              8113
VOLTYPE  TM    2(R2),X'FF'
         BC    1,DEVOK
         SR    R3,R3               BACK UP TO POINT TO MAIN UCB
         IC    R3,1(R2)               TO GET E  DEVICE TYPE
         SLL   R3,4
         LA    R3,56(R3)
         SR    R2,R3
DEVOK    MVC   RSLTAREA+255(4),16(R2)
         LA    R2,RSLTAREA              PTR TO VOLID & DEVTYPE     8113
         L     R1,IEHMVV10              LOAD ADDR OF PERMANENT WRK AREA
         LA    R1,4(R1)
        SRL   R1,2
         SLL   R1,2
         XC    0(17,R1),0(R1)          SET AREA TO ZERO          A27712
         MVC   0(4,R1),255(R2)    MOVE TYPE INTO LIST              8113
         MVC   4(6,R1),259(R2)    MOVE VOLID INTO LIST             8113
         ST   R1,IEHMVV24               STORE POINTER IN CVOL PTR SLOT
         LA    R1,17(R1)                                         A27712
         ST    R1,IEHMVV10              BUMP PERM WORK AREA PTR
         OI    IEHMVV20+1,X'40'        TURN ON CVOL BIT
         B     VOLMNT
NOMOUNT  L     R4,MESSAD
         XC    IEHMVV00(121),IEHMVV00
         MVC   IEHMVV00+1(32),M1
         L     R1,IEHMVV24              PICK YP VOLUME SERIAL
         MVC   IEHMVV00+34(6),4(R1)
NOMOUNT1 LA    R3,IEHMVV00
         LINK  EP=IEHMVESU
         LTR   15,15
         BNE   EXIT2
         TM    IEHMVV20+2,X'20'         TEST FOR END OF FILE
         BO    EXIT2                    BRANCH IF ON
         B     EXIT3
NOFIND   L     R4,MESSAD
         MVI   IEHMVV00,X'40'          CLEAR PRINT AREA          A27712
         MVC   IEHMVV00+1(120),IEHMVV00                          A27712
         MVC  IEHMVV00+1(32),M1
         MVC   IEHMVV00+34(50),M2                                 12490
         OI    IEHMVV41+13,X'02'       SET RETURN CODE = 4      XA01754
         B     NOMOUNT1
EXIT1  IEHPOST (14,1),T
         XC    IEHMVV27+12(8),IEHMVV27+12
         XCTL  (2,12),EP=IEHMVESC
EXIT2  IEHPOST (14,1),T
         XCTL  (2,12),EP=IEHMVESK
EXIT3  IEHPOST (14,1),T
         XCTL  (2,12),EP=IEHMVEST
GRPLOC   CAMLST  NAME,FQDSNAME,,RSLTAREA                           8113
CLEARMSK DC    X'0000FFFF'             UCB MASK                @Z40CSJH
FQDSNAME DC    CL44'0'                                             8113
RSLTAREA DS    0D                                                  8113
         DS    265C                                                8113
JDSNAME  DC    CL32'SYSCTLG'
         DC    20X'40'
         DC    X'40'                    DS STATUS OLD JFCB +87
         DC    X'01'                    COUNT OF VOL SERIAL
RENT1    DS    0F
         DCB   DDNAME=X,                                               C
               RECFM=F,                                                C
               BLKSIZE=256,                                            C
               DSORG=PS,                                               C
               KEYLEN=8,                                               C
               MACRF=RCP
RENT2    DS    0F
MESSAD   DC    A(MESS)
SI       DC    CL2'SI'
MESS     CSECT
M1      DC     CL32'IEH349I UNABLE TO MOUNT VOLUME'
M2  DC  CL50' SYSRES DDCARD OMITTED,CANNOT LOCATE FROM VOLUME'    12490
         END
