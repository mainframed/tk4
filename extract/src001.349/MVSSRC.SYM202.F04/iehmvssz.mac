 TITLE 'IEHMVSSZ-VOLUME OPERATIONS FOR IEHMOVE LOAD MODULE ESZ'
*TITLE -IEHMVSSZ
*FUNCTION/OPERATION: TO HANDLE VOLUME OPERATIONS AND TO HANDLE ERROR  *
*RETURNS FROM CALLED ROUTINES.                                        *
*ENTRY POINTS: IEHMVESZ                                               *
*INPUT: MOVE/COPY COMMUNICATIONS TABLE                                *
*OUTPUT: NONE                                                         *
*EXTERNAL ROUTINES: IEHMVSSVA                                         *
*EXITS-NORMAL XCTL TO IEHMVESX                                        *
*ERRORS: NONE                                                         *
*TABLES/WORK AREAS: MOVE/COPY COMMUNICATIONS TABLE                    *
*ATTRIBUTES: REUSEABLE                                                *
*NOTES: PART OF LOAD MODULE IEHMVESZ                                  *
*
         TITLE 'SINGLE DATA SET CONTROL'
*
*                                  LOAD MODULE  IEHMVL20
*                                               SINGLE DATE SET
*
*
IEHMVSSZ CSECT
*A048300                                                       @ZA26443
*A026100,036440-036560                                         @ZA16493
*A34100-34120,40500,108100                                      YM05783
*C34400                                                         YM05783
*A19404-19435,19448-19514,20100-20180,21300-21340,57900         YL026VC
*A58420-58796,97300-97700,98700                                 YL026VC
*C12500,14400,98200,98800                                       YL026VC
*D100,12200-12400,12600-14200,27800-30800,58000-58400           YL026VC
*D77800-78000,97800-9800,098400-98600                           YL026VC
*C19431,20120-20140                                              Y02918
*A19432-19433                                                    Y02918
*A086820,086920                                                  YM5081
*C084920,086800-090320,095000,096100,100000-100700              YA01220
*A105050-105150                                                 YA01220
*D105800-106000,107400-107600                                   YA01220
*A014300                                                        YA01195
*A085700                                                         A48757
*C085800                                                         A48757
*A014400                                                         A43752
* 090000                                                         A24006
*1980 004200                                                       UL0H
*
*  017600-017800                                                 A17911
* 088400,089200,090400-091800,096200,096600-096800,097400-097600 S21042
* 100400,101400-101600,109200                                    S21042
*D019456-019458                                                @Z40CSJH
*A012100-012200,027800-027820,078100-078120                    @Z40CSJH
*A098820-098860                                                @Z40CSJH
*D034600                                                       @ZA06522
*
*              LOAD MODULE ENTRY POINT  IEHMVESZ               @ZA16493
IEHMVESZ SAVE (14,12),T,IEHMVSSZ-OZ26443
         ENTRY IEHMVESZ
*
    USING  IEHMVV,MCTABLE
*
*              REGISTERS
*
PASSA    EQU   0
PASSB    EQU   1
R2       EQU   2
R3       EQU   3
ACCE     EQU   4
ACCO     EQU   5
BASE     EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
MCTABLE  EQU   12
SAVE     EQU   13
RETURN   EQU   14
LINK     EQU   15
         BALR  BASE,0
         USING *,BASE
SINGLE   DS    0H                  LABEL CARD ONLY
*                       FIRST TIME SWITCH QUESTIONS VOLUME MOUNTING
         TM    IEHMVV20+4,TEN      IS THIS VOLUME MOUNTED
         BZ    ABCB                          RE-ENTER
         NI    IEHMVV20+4,OFF      YES,TURN FIRST TIME SWITCH OFF
         L     RETURN,RRR
         BR    RETURN
ABCB     DS    0H                  LABEL CARD ONLY
    IEHPRE  (14,1),TFC,
         TM    IEHMVV20,VOLUME          VOLUME MOVE
         BO    VOLUMEMV                 YES
         TM    IEHMVV20,CATALOG         MOVE CATALOG FUNCTION  @Z40CSJH
         BO    SETCAT                   YES                    @Z40CSJH
SINGLEOK DS    0H                      LABEL CARD ONLY
         XC    IEHMVV62(4),IEHMVV62    CLEAR PTR                YA01195
         XC   IEHMVV10-12(12),IEHMVV10-12     CLEAR SWITCH AREA
         XC    IEHMVV82+16(12),IEHMVV82+16                       A43752
         L     ACCE,IEHMVV10
         LA    ACCO,28(0,ACCE)
         ST    ACCE,IEHMVV30+4
         LA    ACCE,28(0,ACCO)
         ST    ACCO,IEHMVV31+4
         ST    ACCE,IEHMVV10
         TM    IEHMVV20+3,RENAME   TEST FOR RENAME INDICATION
         BO    NOCH
         MVC   IEHMVV21+4(4),IEHMVV21  CHANGE'TO'DSNAME ADDRESS
NOCH     DS    0H                  LABEL CARD ONLY
         XC    IEHMVV30(4),IEHMVV30    BLANK ADDRESS OF FROM DCB
*
*                                  PROCESS
*                                       CALL COPY-UNLOAD-LOAD-ABORT
*
         TM    IEHMVV20,X'02'      IS THIS A VOLUME MOVE ?       A17911
         BO    COPYRTN             YES--DO NOT GO TO IEHCULAT.   A17911
         L     LINK,COPYTEST       GET ADDRESS OF IEHMVESV (EN-  A17911
*                                    TRY POINT OF CSECT IEHCULAT)A17911
         BALR  RETURN,LINK         BRANCH TO IEHCULAT.           A17911
         DROP  BASE
COPYRTN  DS    0H                      LABEL CARD ONLY
         BALR  LINK,0
         USING *,LINK
         L     BASE,ASIN
         DROP  LINK
         USING SINGLE,BASE
*
**********************************************************************
*                                                                    *
* THIS SECTION TESTS FOR DATA SET ORGANIZATION.  IEHMOVE CANNOT      *
* PROCESS VSAM DATA SET/SPACES OR ISAM DATA SETS                     *
* ALTHOUGH FOR VOLUME OPERATIONS THE DATA SET ORGANIZATION COULD     *
* HAVE BEEN TESTED IN THE XRAYVTOC SECTION, ALL TESTING OF DATA      *
* SET ORGANIZATION IS DONE HERE.                                     *
* IF IEHMOVE CANNOT PROCESS THE DATA ORGANIZATION, MESSAGE IEH474I   *
* IS PRINTED.  FOR A SINGLE DATA SET OPERATION THE RETURN CODE IS 12 *
* AND ALL PROCESSING STOPS FOR THAT REQUEST.  IF IT IS A VOLUME      *
* REQUEST, THE RETURN CODE IS 4 AND IEHMOVE GETS THE NEXT ENTRY      *
* FROM THE VTOC TO BE MOVED/COPIED. IF IT IS A DSGROUP REQUEST,      *
* THE RETURN CODE IS 4 AND IEHMOVE GETS THE NEXT DATA SET OF         *
* THE GROUP.                                                         *
*                                                                    *
**********************************************************************
         L     ACCE,IEHMVV30+8     GET ADDR OF 'FROM' DSCB.      A17911
         TM    83(ACCE),VSAM           IS VSAM ORGANIZATION     YL026VC
         BO    ORGERR                  ERROR IF VSAM            YL026VC
         TM    82(ACCE),ISAM           IS ISAM ORGANIZATION     YL026VC
         BO    ORGERR                  ERROR IF ISAM            YL026VC
         TM    IEHMVV20,VOLORDSG       VOLUME OR DSGROUP        YL026VC
         BZ    TESTMOV                 IF NOT, DO NOT PRINT     YL026VC
         MVI   IEHMVV61,NAME           SET UP TO PRINT NAME     YL026VC
         BAL   RETURN,MSG              PRINT DSNAME             YL026VC
TESTMOV  EQU   *                       TEST FOR UNMOVEABLE DATA YL026VC
         TM    82(ACCE),X'01'      CHECK DSORG FIELD OF 'FROM'   A17911
*                                    DSCB FOR UNMOVABLE DATA.    A17911
         BZ    NOTUNMOV            NOT UNMOVABLE -- EXIT         A17911
         NI    82(ACCE),X'FE'      UNMOVABLE DATA--TURN OFF      A17911
         OI    IEHMVV10-4,X'08'      UNMOVABLE BIT IN DSCB AND   A17911
*                                    TURN ON UNMOVABLE BIT IN    A17911
*                                    IEHMOVE SWITCH AREA.        A17911
NOTUNMOV EQU   *                   LABEL CARD                    A17911
     IEHPOST  (14,1),TFC,
         L     RETURN,12(SAVE)
   XCTL   (2,12),EP=IEHMVESX
ORGERR   MVI   IEHMVV61,BADORG         INDICATOR FOR IEH474I    YL026VC
         TM    IEHMVV20,VOLORDSG       VOLUME OR DSGROUP         Y02918
         BM    PREABORT                RETURN CODE WILL BE 4     Y02918
         OI    IEHMVV41+13,X'10'       SET RETURN CODE TO 12    YL026VC
         B     PREABORT                PRINT MESSAGE            YL026VC
***** CONSTANTS *****
COPYTEST DC    A(IEHMVESV)
         EXTRN IEHMVESV
         ENTRY COPYRTN
RRR      DC    A(RTNVM)
         EXTRN RTNVM
VSAM     EQU   X'08'                  VSAM INDICATOR            YL026VC
ISAM     EQU   X'80'                  ISAM INDICATOR            YL026VC
BADORG   EQU   27                     INDICATOR FOR IEH474I     YL026VC
NINETY   EQU X'90'
TEN      EQU   X'10'
OFF      EQU   X'EF'
CATALOG  EQU   X'04'
VOLORDSG EQU   X'12'
NAME     EQU   X'8B'
RENAME   EQU   X'04'
F1       EQU   X'F1'
DEACTIVE EQU   X'00'                                               UL0H
ZERO     EQU   X'00'
LIST     EQU   X'87'
DASDY    EQU   X'20'
BO       EQU   X'B0'
FOURTEEN EQU   X'14'
EDRETURN EQU   140
FC       EQU   X'FC'
F2       EQU   X'2F'
ONE      EQU   X'01'
EIGHT    EQU   X'08'
IA       EQU   X'1A'
OE       EQU   X'0E'
BLANK    EQU   C' '
FOUR     EQU   X'04'
S        EQU   X'E2'
T        EQU   X'E3'
Y        EQU   X'E8'                                           @ZA16493
K        EQU   X'D2'
H        EQU   X'C8'
FF       EQU   X'FF'
I        EQU   X'C9'
F7       EQU   X'7F'
EOF      EQU   X'20'
VOLUME   EQU   X'02'
FVOLUME  EQU   X'80'
**********************************************************************
* LINES 028100-030996 WERE REACTIVATED IN SUPPORT              @Z40CSJH
* OF THE EXTENDED MVS CVOL FUNCTION                            @Z40CSJH
**********************************************************************
ANDBITS  DC    X'C088207180'
ORBITS   DC    X'0440000000'
CAT4VOL  NC    IEHMVV20(5),ANDBITS      RESET VARIOUS VOLUME OPERATIONS
         OC    IEHMVV20(5),ORBITS       TURN ON SWITCHES FOR CATALOG
         MVC   IEHMVV24(4),IEHMVV22     FROM VOL BECOMES A CVOL
         MVC   IEHMVV10(4),IEHMVV30+8   RESET PERMANENT WORK AREA
SETCAT   MVC   IEHMVV61(8),IEHMVV21     SET UP CAT INCLUDE AND EXCLUDE
         MVC   IEHMVV86(4),IEHMVV62    SAVE EXC ADR FOR ESC    @XA02965
         L     ACCE,IEHMVV10
         LA    ACCO,44(ACCE)            UP WORK AREA POINTER
         ST    ACCO,IEHMVV10
         MVI   0(ACCE),BLANK           BLANK OUT WORKAREA DSNAME FIELD
         MVC   1(43,ACCE),0(ACCE)
         MVC   0(7,ACCE),SYSCTLG        SET UP SYSCTLG DSNAME
         ST    ACCE,IEHMVV21            PLACE ADDRESS IN WORK AREA
         B     SINGLEOK
SYSCTLG  DC    CL7'SYSCTLG'
      DS     0H
         TITLE 'PRE ABORT ROUTINE'
PREABORT DS    0H                      LABEL CARD ONLY
         ENTRY PREABORT
         BALR  LINK,0
         USING *,LINK
    L   BASE,ASIN
         DROP  LINK
    USING   SINGLE,BASE
         TM    IEHMVV10-4,FVOLUME      FROM VOLUME TO BE CLOSED
         BO    CLOSEF                  YES.GO TO CLOSE FROM VOLUME
FCLOSE   TM    IEHMVV10-4,BLANK        TO VOLUME TO BE CLOSED
         BO    CLOSET                  YES.GO TO CLOSE TO VOLUME
TCLOSE   DS    0H                      LABEL CARD ONLY
         BAL   RETURN,MSG               GO TO MESSAGE WRITER
         IEHPOST (14,1),TFC,
         CLI   MV61SV,BADORG           IEH474I ISSUED           YM05783
         BE    NOPOST                  NO                       YM05783
   IEHPOST  (14,1),TFC,
NOPOST   OI    IEHMVV41+13,X'01'   RET CODE = 4                 VS05783
         TM    IEHMVV20,VOLUME         TEST FOR VOLUME OPERATION
   BO  SINGLE
         MVC   IEHMVV00(6),MODNAME
         MVI   IEHMVV00+6,S            ADD LETTER'S' TO MODULE NAME
         TM    IEHMVV20,TEN            DSGROUP OPERATION
         BO    GODSG
         TM    IEHMVV20+2,EOF
         BO    GOHOME
         MVI   IEHMVV00+7,T            ADD LETTER'T' TO MODULE NAME
         TM    IEHMVV20+5,X'08'        SHOULD A DUMMY FILE     @ZA16493
*                                      BE WRITTEN              @ZA16493
         BNO   ALLDONE                                         @ZA16493
         MVI   IEHMVV00+7,Y            YES GO TO IEHMVESY      @ZA16493
ALLDONE  DS    0H                      LABEL CARD ONLY
         XCTL  (2,12),EPLOC=(12)
GOHOME   MVI   IEHMVV00+7,K            ADD LETTER 'K' TO MODULE NAME
         B     ALLDONE
GODSG    DS    0H                      LABEL CARD ONLY
         MVI   IEHMVV00+7,H            ADD LETTER'H' TO MODULE NAME
         B     ALLDONE
CLOSEF   DS    0H                      LABEL CARD ONLY
         MVI   IEHMVV70+8,DEACTIVE                                 UL0H
         L     ACCE,IEHMVV30           LOAD ADDRESS OF FROM DCB
         CLOSE ((ACCE))                CLOSE FROM DATA SET
         B     FCLOSE
CLOSET   DS    0H                      LABEL CARD ONLY
         MVI   IEHMVV70+12,DEACTIVE    DEACTIVATE O'PUT TRLR EXIT  UL0H
         L     ACCE,IEHMVV31           LOAD ADDRESS OF TO DCB
         CLOSE ((ACCE))                CLOSE TO DATA SET
         B     TCLOSE
MODNAME  DC    CL6'IEHMVE'
         TITLE 'MESSAGE HANDLER'
         DROP  BASE
MSG      DS    0H                      LABEL CARD ONLY
         STM   PASSA,LINK,200(MCTABLE) STORING REGISTERS
         MVC   MV61SV,IEHMVV61         SAVE MSG CODE            YM05783
         BALR  LINK,0                  SET UP ADDRESSABILITY
         USING *,LINK
    L     BASE,ASIN
         DROP  LINK                    DROP BASE REGISTERS
      USING    SINGLE,BASE
         LR    R7,RETURN
         MVI   IEHMVV00,FF             SET UP MESSAGE AREA
         MVI   IEHMVV00+1,BLANK        BLANK OUT PRINT LINE
         MVC   IEHMVV00+2(120),IEHMVV00+1
         TM    IEHMVV61,ONE       DATA SET MESSAGE OR NORMAL MESSAGE
         BO    NORMMES            ODD NUMBER IN VV61 IS NORMAL MESSAGE
*                        PROCESS DATA SET MESSAGES                    *
*                                  HERE                               *
*                         EVEN NUMBER IN VV61                         *
         MVC   IEHMVV00+1(4),IEH4       SET UP MSG NUMBER
         LA    ACCE,127                 GET IEH4
         SR    ACCO,ACCO
         IC    ACCO,IEHMVV61
         NR    ACCO,ACCE
         L     R3,ADIEH4DD             SET UP MESSAGE DIAGNOSTIC CODE
         LA    ACCO,0(R3,ACCO)
    MVC   IEHMVV00+5(2),0(ACCO)
         MVI   IEHMVV00+7,I            END CODE WITH LETTER 'I'
         L     ACCE,MSGH                SET UP MSG HEADER
         LA    ACCO,7
         BAL   R8,MSGMOVE
         TM     IEHMVV20,X'02'         IS THIS A VOL REQUEST
         BZ     CONT                   NO  BRANCH
         TM     IEHMVV20+3,FVOLUME     YES  IS THIS THE FIRST DATA SET
         BO     CONT                   NO   BRANCH
         LA     ACCE,TEMPNAME          YES   SET UP DSNAME OF VOL
         LA     ACCO,5                 NAME LENGTH
         B      VOLNAME
CONT     L      ACCE,IEHMVV21          SET UP DSNAME
         LA    ACCO,43
VOLNAME  BAL    R8,MSGMOVE
         TM    IEHMVV61,FVOLUME        UNLOAD MESSAGE OR NOT MOVED
         BO    MSGUNLD                       UNLOAD MSG
         LA    ACCE,2                        NOT MOVED
         OI    IEHMVV20+2,X'40'        SET MIDABORT            @ZA26443
MSGAA    DS    0H                      LABEL CARD ONLY
         BAL   R8,MSGDMOVE              SET UP FINAL PART
         SR    ACCE,ACCE
         IC    ACCE,IEHMVV61                 OF MSG
         BAL   R8,MSGDMOVE
PRINTMES  SR   0,0                      TO PRINT A NORMAL MESSAGE
         LA    3,IEHMVV00
         LINK  EP=IEHMVESU        GO TO PRINT ROUTINE WITH (3) AS PARAM
         B     MSGCHECK(LINK)           CHECK RETURNS
MSGCHECK B     GOODMSG                  GOOR WRITE
         XCTL  EP=IEHMVESK              I/O ERROR
GOODMSG  LR    RETURN,R7
         MVI   IEHMVV61,ZERO           MOVE ZERO INTO MESSAGE INDICATOR
         LM    PASSA,LINK,200(MCTABLE) RESTORE REGISTERS
         BR    RETURN                  BRANCH BACK TO CALLING ROUTINE
MSGUNLD  DS    0H                      LABEL CARD ONLY
         NI    IEHMVV61,F7             TURN OFF UNLOAD MESSAGE BIT
         LA    ACCE,0
         B     MSGAA
MSGDMOVE DS    0H                      LABEL CARD ONLY
    SLL   ACCE,1
         L     R3,ADMSGST              LOAD ADDRESS OF MSGSTART
         LH    ACCO,2(R3,ACCE)         ACCESSING PROPER MESSAGE
         LH    ACCE,0(R3,ACCE)
         A     ACCE,MSGH
MSGMOVE  DS    0H                      LABEL CARD ONLY
         LA    R9,IEHMVV00+121
MSGLOOP  CLI   0(R9),BLANK             THIS LOOP WILL FIND FIRST BLANK
         BNE   MSGPL                         CHARACTER IN
         BCT   R9,MSGLOOP                    MSG AREA
MSGPL    DS    0H                      LABEL CARD ONLY
         EX    ACCO,MSMOVE             MOVE MESSAGE IN PRINTOUT AREA
         BR    R8                      BRANCH BACK TO FINISH MESSAGE
NORMMES  DS    0H                      LABEL CARD ONLY
         SR    ACCE,ACCE
         IC    ACCE,IEHMVV61            GET MESSAGE NUMBER
     LA   ACCO,127
    NR   ACCE,ACCO
         LA    ACCE,1(0,ACCE)
         SLL   ACCE,1
         L     R3,ADNORMAL             LOAD ADDRESS OF NORMAL MESSAGE
         L     ACCE,0(R3,ACCE)         GET MESSAGE POINTER
         SR    ACCO,ACCO
         IC    ACCO,0(ACCE,0)           GET MESSAGE LENGTH
         LA    ACCE,1(0,ACCE)
         EX    ACCO,NORMM               MOVE MESSAGE TO PRINT AREA
         TM    IEHMVV61,FVOLUME        IS VOLUME OPERATION      YL026VC
         BO    ADDNAME                 PUT NAME AT END          YL026VC
**********************************************************************
*                                                                    *
* FOR MESSAGE IEH474I, BAD ORGANIZATION, THE DATA SET/SPACE NAME     *
* IS PUT AT THE BEGINNING OF THE MESSAGE.                            *
*                                                                    *
**********************************************************************
         CLI   IEHMVV61,BADORG         BAD ORGANIZATION MSG.    YL026VC
         BNE   PRINTMES                PRINT MESSAGE            YL026VC
         LA    R9,IEHMVV00+7           START OF DSNAME MINUS 2  YL026VC
         LA    ACCO,43                 LENGTH OF DSNAME MINUS 1 YL026VC
         L     ACCE,IEHMVV21           ADDRESS OF DSNAME        YL026VC
         EX    ACCO,MSMOVE             MOVE DSNAME INTO MESSAGE YL026VC
         B     PRINTMES                PRINT MESSAGE            YL026VC
ADDNAME  LA    ACCO,43                 LENGTH OF DSNAME MINUS 1 YL026VC
         L     ACCE,IEHMVV21           ADDRESS OF DSNAME        YL026VC
         BAL   R8,MSGMOVE              MOVE NAME TO START OF MSG.Y02063
         B     PRINTMES                PRINT MESSAGE            YL026VC
NORMM    MVC   IEHMVV00+1(1),0(ACCE)
IEH4     DC    CL4'IEH4'
MSMOVE   MVC   2(1,R9),0(ACCE)
MSGH     DC    A(MSGHEAD)               POINTER TO MESSAGES
         ENTRY MSG
         EXTRN IEH4DD
         EXTRN MSGSTART
         EXTRN NORMALM
         EXTRN MSGHEAD
ADMSGST  DC    A(MSGSTART)
ADNORMAL DC    A(NORMALM)
ADIEH4DD DC    A(IEH4DD)
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*   THIS ROUTINE HANDLES THE OPENING OF THE VTOC TO MOVE ONE DATASET  *
*   OF A VOLUME MOVE OPERATION AND MOUNTING OF THE TO AND FROM VOLUMES*
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
IEHMVSSZ CSECT
VOLUMEMV DS    0H
         BALR  LINK,0
         USING *,LINK
    L   BASE,ASIN
         DROP  LINK
    USING  SINGLE,BASE
         TM    IEHMVV20+3,FVOLUME       FIRST DS OF VOLUME MOVE
        BO    NOT1                     NO
         L     PASSB,IEHMVV22          YES
         CLI   2(PASSB),DASDY          IS FROM VOLUME DIRECT ACCESS
         BNE   NOMOVEVO                 NO
*                                       YES
*
*                                       CALL VOL MOUNT FOR VOLUME
*
         XC    IEHMVV00(12),IEHMVV00
         MVI   IEHMVV00+4,NINETY       SET INDICATOR FOR VOLUME MOUNT
         L     LINK,CALMT
         MVI   IEHMVV00+400,TEN        SET UP VOLUME MOUNT RETURN CODE
         BALR  RETURN,LINK              GO TO CALL VOL MOUNT
KK1      DS    0H                      LABEL CARD ONLY
         ENTRY KK1
         DROP  BASE
         USING KK1,RETURN
         L     BASE,ASIN
         DROP  RETURN
         USING SINGLE,BASE
         B     VOLOKMT(LINK)            CHECK VOLUME MOUNTING
VOLOKMT  B     NOVOLMT                  NOT MOUNTED
         ST    PASSB,IEHMVV30+12   MOUNTED (STORE ADDR OF FROM DDNAME)
        MVC   IEHMVV22+4(4),IEHMVV22   ACTIVE FROM
*                                       CALL VOL MOUNT FOR TO VOLUME
         XC    IEHMVV00(12),IEHMVV00
         MVI   IEHMVV00+4,BO           SET INDICATOR FOR VOLUME MOUNT
         L     PASSB,IEHMVV23+4        PASS TO VOLUME LIST ENTRY POINT
         L     LINK,CALMT
         MVI   IEHMVV00+400,FOURTEEN   SET UP VOLUME MOUNT RETURN CODE
         BALR  RETURN,LINK              GO TO CALL VOL MOUNT
KK2      DS    0H                      LABEL CARD ONLY
         ENTRY KK2
         DROP  BASE
         USING KK2,RETURN
         L     BASE,ASIN
         DROP  RETURN
         USING SINGLE,BASE
         B     VOLTOA(LINK)             VOLUME MOUNTED
VOLTOA   B     NOTOMT                   NO
         ST    PASSB,IEHMVV31+8      YES (STORE ADDR OF TO DDNAME)
GOTOED   DS    0H                      LABEL CARD ONLY
         L     LINK,ED
         BALR  RETURN,LINK
         L     ACCE,IEHMVV30+8      'FROM'DSCB POINTER FROM XRAYVTOC
         SR    LINK,LINK
         IC    LINK,EDRETURN(ACCE)      CHECK RETURNS
         B     CKED(LINK)
CKED     B     VOLIOERR                 IO ERROR ON FROM VOLUME
         B     AEOF                     EOF ON VTOC
*                                       RETURNED A DSCB
         ST    ACCE,IEHMVV21            SET UP FROM DS NAME
         B     SINGLEOK                 RETURN TO NORMAL PROCESSING
NOT1     MVC   IEHMVV10(4),IEHMVV30+8
         NI    IEHMVV20+1,FC           RESET COMMUNICATIONS AREA
         NI    IEHMVV20+2,F2                  INDICATORS AND
         XC    IEHMVV21(8),IEHMVV21
         XC    IEHMVV30(12),IEHMVV30
         XC    IEHMVV31(8),IEHMVV31     CLEAR SOME AREAS OF
    XC   IEHMVV43(8),IEHMVV43
         XC    IEHMVV60(16),IEHMVV60
         B     GOTOED                   GO TO GET ANOTHER DSCB
NOMOVEVO DS 0H
         MVI   IEHMVV61,ONE            INDICATE MESSAGE TYPE FOR WRITER
    B    PRESTOP
PRESTOP  BAL   RETURN,MSG               GO TO MESSAGE WRITER
         OI    IEHMVV41+13,X'01'   RET CODE = 4
AEOF     DS    0H                      LABEL CARD ONLY
         TM    IEHMVV20+4,ONE          CATLG TO BE MOVED       @Z40CSJH
         BO    CAT4VOL                  YES ..... BRANCH       @Z40CSJH
         MVC   IEHMVV00(6),MODNAME
         MVI   IEHMVV00+6,S            ADD LETTER 'S' TO MODULE NAME
         TM    IEHMVV20+2,EOF
         BO    VEOF
         MVI   IEHMVV00+7,T            ADD LETTER 'T' TO MODULE NAME
LEAVEVOL DS    0H                      LABEL CARD ONLY
    IEHPOST  (14,1),TFC,
*
         XCTL  (2,12),EPLOC=(12)
VEOF     MVI   IEHMVV00+7,K            ADD LETTER 'K' TO MODULE NAME
         B     LEAVEVOL
CALMT    DC    A(CALLMT)
         EXTRN CALLMT
NOVOLMT  MVI   IEHMVV61,EIGHT          SET INDICATOR FOR MESSAGE WRITER
         B     PRESTOP
NOTOMT   MVI   IEHMVV61,IA             SET INDICATOR FOR MESSAGE WRITER
         B     PRESTOP
VOLIOERR MVI   IEHMVV61,OE             SET &ND&CATO- FO- MESSAGE W-ITER
         B     PRESTOP
ED       DC    A(XRAYVTOC)
ASIN     DC    A(SINGLE)
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                               XRAYVTOC                              *
*     THIS ROUTINE OBTAINS ONE FORMAT ONE DSCB FROM THE VTOC FOR A    *
*     VOLUME MOVE OPERATION. FOR EACH DSCB ONE DATASET WILL BE MOVED  *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
XRAYVTOC SAVE  (14,12)             SAVE REGISTERS
         ST    SAVE,XXREG13            SAVE POINTER TO SAVE AREA
         LA    SAVE,XXSAVE             SET UP SAVE AREA FOR CALLED RTN
         L     R11,IEHMVV10            ADDRESS OF PERMANENT WORK AREA
         USING XXDSCB,R11
         USING XRAYVTOC,LINK
         TM    IEHMVV20+3,FVOLUME     IS THIS FIRST VOLUME ENTRY S21042
         BO    XXEODAD                                          YA01220
         MVI   XXJFCB,FOUR             PLACE THE NAME OF THE
         MVC   XXJFCB+1(43),XXJFCB      VTOC INTO THE JFCB AND
         MVI   XXMEMBER,BLANK          STORE BLANKS INTO THE
         MVC   XXMEMBER+1(7),XXMEMBER   MEMBER NAME FIELD.
         MVI   XX00,X'08'              DON'T WRITE BACK THE JFCB A48757
         XC    XX00+1(139),XX00+1                                A48757
         MVI   XXVOLID-1,ONE
         L     R3,IEHMVV22+4           POINT AT ACTIVE FROM VOLUME
         MVC   XXVOLID(6),4(R3)        PUT VOLUME SERIAL NO. INTO JFCB
         MVI   XXVOLID+6,BLANK         BLANK OUT NEXT BYTE
         LA    PASSA,XXDCBL            LENGTH OF DCB            YA01220
         LR    R7,LINK                 SAVE REGISTER 15          YM5081
         GETMAIN R,LV=(0)              GET AREA FOR DCB         YA01220
         LR    LINK,R7                 RESTORE REGISTER 15       YM5081
         LA    R3,0(PASSB)             CLEAR HIGH ORDER BYTE    YA01220
         ST    R3,IEHMVV82+8           SAVE DCB ADDRESS         YA01220
         USING IHADCB,R3
         MVC   IHADCB(XXDCBL),XXDCBSTT SAVE DCB                 YA01220
         LA    PASSB,XXFULL            EXIT LIST ADDRES         YA01220
         O     PASSB,DCBEXLST          SAVE FIRST BYTE OF EXLST YA01220
         ST    PASSB,DCBEXLST          STORE EXLST ADDR IN DCB  YA01220
         ST    MCTABLE,XXFULL          PUT JFCB ADDRESS IN EXLSTYA01220
         MVI   XXFULL,LIST             INDICATE JFCB EXIT ONLY  YA01220
         L     PASSB,IEHMVV30+12       ADDRESS OF DDNAME        YA01220
         MVC   DCBDDNAM,0(PASSB)       PUT DDNAMEIN DCB         YA01220
         LR    R7,LINK                 SAVE REGISTER 15         YA01220
         OPEN  ((R3),INPUT),TYPE=J     OPEN THE VTOC            YA01220
         LR    LINK,R7                 RESTORE REGISTER 15      YA01220
         B     XXREAD                  GO TO READ THE VTOC      YA01220
         SPACE 1                                                YA01220
XXEODAD  EQU   *                       ROUTINE TO RESET EODAD   YA01220*
                                       AND SYNAD ADDRESSES IN   YA01220*
                                       DCB WHEN REENTRING MODULEYA01220
         SPACE 1                                                YA01220
         L     R3,IEHMVV82+8           LOAD DCB ADDRESS         YA01220
         LA    PASSB,XXEND             ADDRESS OF EODAD ROUTINE YA01220
         IC    PASSA,DCBEODAD          SAVE FIRST BYTE(DCBBFTEK)YA01220
         ST    PASSB,DCBEODAD          STORE EODAD ADDR IN DCB  YA01220
         STC   PASSA,DCBEODAD          RESET FIRST BYTE         YA01220
         LA    PASSB,XXERR             ADDRESS OF SYNAD ROUTINE YA01220
         IC    PASSA,DCBSYNAD          SAVE FIRST BYTE (DCBIOBL)YA01220
         ST    PASSB,DCBSYNAD          STORE SYNAD ADDR IN DCB  YA01220
         STC   PASSA,DCBSYNAD          RESET FIRST BYTE         YA01220
         DROP  R3                                               YA01220
         SPACE 1                                                YA01220
XXREAD   READ  XXDECB,SF,(R3),(R11),'S',MF=E  READ A DSCB       YA01220
         CHECK XXDECB
         USING *,RETURN
         L     LINK,XXBASEAD           RESET BASE REGISTER
         DROP  RETURN
         TM    IEHMVV20+3,FVOLUME      IS THIS THE FIRST VOLUME ENTRY
         BO    XXLOOK41            NO ..... BRANCH
         CLC   XXDSCB(44),XXJFCB       SEARCH FOR BEGINNING OF VTOC
         BNE   XXREAD
         CLI   XXFORMAT,ZERO           IS THIS A FORMAT ZERO DSCB
         BNE   XXSETSW             NO ..... BRANCH
         AR    PASSB,BASE              UP THE TTR 1 FOR COMPATIBILITY
         SRL   PASSB,8                 TTRZ BECOMES ZTTR
         ST    PASSB,XXCODE            SAVE TTR
         MVI   XXJFCB,FOUR
         MVC   XXJFCB+1(43),XXJFCB     44 BYTES OF HEX '04' TO SEARCH
         B     XXREAD                  GO TO READ NEXT DSCB     YA01220
XXSETSW  OI    IEHMVV20+3,FVOLUME      SET FIRST ENTRY SWITCH TO 1=OFF
         B     XXREAD
XXLOOK41 CLI   XXFORMAT,F1             IS THIS A FORMAT 1 DSCB
         BNE   XXREAD              NO ..... BRANCH
         NOTE  (R3)                    GET TTR OF THIS DSCB     YA01220
         USING *,RETURN
         ST    PASSB,IEHMVV82          SAVE TTR FOR CAT. LATER ONS21042
         L     LINK,XXBASEAD           RESET ADDRESSABILITY
         DROP  RETURN
**********************************************************************
*                                                                    *
* IF THERE ARE IEHMVOVE UTILITY DATA SETS ON THE VOLUME, THEY ARE    *
* ARE NOT INCLUDED IN THE VOLUME MOVE/COPY.  UTILITY DATA SETS ARE   *
* SYSUT1, SYSUT2, AND SYSUT3.  IEHMOVE TRIES TO MAKE THE UTILITY DATA*
* SET DISTINCTIVE BY STARTING THE DATA SET NAME WITH '**'.  NOW THE  *
* USER CAN NAME VSAM DATA SET/SPACES STARTING WITH '**' SO FIRST A   *
* TEST MUST BE MADE FOR VSAM THEN A TEST FOR UTILITY DATA SETS.      *
* VSAM DATA SETS/SPACES WILL BE CAUGHT AS AN ERROR IN ANOTHER SECTION*
* OF IEHMVSSZ IN ORDER TO CENTRALIZE ERROR CHECKING.                 *
*                                                                    *
**********************************************************************
         TM    XXDSCB+83,VSAM          IS VSAM ORGANIZATION     YL026VC
         BO    XXNORM                  ERROR NOTICED LATER      YL026VC
         CLC   XXDSCB(2),XXASTER       IEHMOVE UTILITY DATA SET?YL026VC
         BE    XXREAD                  IGNORE UTILITY DATA SETS YL026VC
**********************************************************************
*
* LINES 098900-098960 WERE REACTIVATED IN SUPPORT              @Z40CSJH
* OF THE EXTENDED MVS CVOL FUNCTION                            @Z40CSJH
*
**********************************************************************
         CLC   XXDSCB(8),XXSYSCAT       IS THIS A SYSCTLG DATA SET
         BNE   XXNORM
         OI    IEHMVV20+4,ONE          TURN ON SYSCATLG BIT
         B     XXREAD                   GO GET ANOTHER DSCB
XXNORM   MVI   XXCODE,EIGHT            POST NORMAL RETURN CODE
XXCLOSE  ST    R11,IEHMVV30+8          POST ADDRESS OF FROM DSCB
         LA    R11,148(R11)            INCREMENT PERMANENT WORK ADDRESS
         ST    R11,IEHMVV10            STORE IT
         TM    IEHMVV20+3,FVOLUME  IS THIS FIRST VOLUME ENTRY    S21042
         BO    OVERCLOS      NO......BRANCH                      S21042
         OI    IEHMVV20+3,FVOLUME SET FIRST VOL ENTRY TO 1 (=OFF)S21042
         CLOSE ((R3))                  CLOSE VTOC
         LA    PASSA,XXDCBL            LENGTH OF DCB            YA01220
         LR    PASSB,R3                ADDRESS OF DCB           YA01220
         FREEMAIN R,LV=(0),A=(1)       FREE DCB AREA            YA01220
OVERCLOS DS    0H     LABEL CARD ONLY                            S21042
         L     SAVE,XXREG13            ACCESS SAVE AREA
         RETURN (14,12)            RETURN
XXEND    MVI   XXCODE,FOUR             POST END OF FILE
          BALR RETURN,0                                          S21042
         USING *,RETURN                                          S21042
         L     LINK,XXBASEAD            RESET ADDRESSABILITY     S21042
         DROP  RETURN                                            S21042
         TM    IEHMVV20+4,X'01'        SYSCATLG BIT ON?          S21042
         BZ    *+10                     NO, SKIP                 S21042
         XC    IEHMVV82(3),IEHMVV82 CLEAR TTR FIELD WHEN M/C CAT S21042
         B     OVERERR                                           S21042
XXERR    MVI   XXCODE,ZERO             POST  I/O ERROR
         BALR  RETURN,0
         USING *,RETURN
         L     LINK,XXBASEAD           RESET ADDRESSABILITY
         DROP  RETURN
OVERERR  NI    IEHMVV20+3,X'7F' SET FIRST VOLUME ENTRY TO 0 (=ON)S21042
         B     XXCLOSE
XXASTER  DC    C'**'                    TO IDENTIFY WORK FILES
XXSYSCAT DC    CL8'SYSCTLG '            TO IDENTIFY THE CATALOG
TEMPNAME DC    C'VOLUME'
XXDCBSTT DS    0F                  LABEL THE START OF THE DCB
XXLEN1   DS    0H                      LABEL CARD ONLY
         DCB   DSORG=PS,MACRF=(RP),RECFM=U,                            .
               KEYLEN=44,BLKSIZE=96,                                   .
               EODAD=XXEND,SYNAD=XXERR
XXLEN2   DS    0H                      LABEL CARD ONLY
XXDCBL   EQU   XXLEN2-XXLEN1       LENGTH -1 OF THE DCB
XXBASEAD DC    A(XRAYVTOC)
         SPACE 3                                                YA01220
         DCBD  DSORG=PS,DEVD=DA        MAP OF DCB               YA01220
         SPACE 3                                                YA01220
         IEHMVV
         ORG   IEHMVV00
XXJFCB   DS    25D            USED FOR BUILDING A JFCB
         DS    F              NOT USED
XXREG13  DS    F
XXSAVE   DS    20F            REGISTER SAVE AREA FOR READ
XXMEMBER EQU   XXJFCB+44      8 BYTE MEMBER NAME  -WILL BE BLANKS-
XX00     EQU   XXMEMBER+8     SPACE 139 BYTES = 0, 1 BYTE = 1
XXVOLID  EQU   XXJFCB+118     6 BYTE VOLUME ID FIELD
XXFULL   DS    F              WORK AREA ON A FULL WORD
XXDECB   DC    5F'0'
MV61SV   DS    CL1                     SAVE MSG CODE            YM05783
         ORG
XXDSCB   DS    11F            DATA SET NAME
XXFORMAT DS    X              DSCB FORMAT ID
         DS    95X            DSCB INFORMATION FIELDS
XXCODE   DS    X              FOR POSTING RETURN STATUS
         ORG
         END   IEHMVESZ
