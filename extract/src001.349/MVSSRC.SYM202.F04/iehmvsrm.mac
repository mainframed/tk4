*TITLE:  IEHMVSRM
*FUNCTION/OPERATION: THIS CODE HANDLES THE FUNCTION OF WRITING DATA
*SETS IN THE UNLOADED FORMAT. IT WILL CONVERT THE OUTPUT TO 80        *
*CHARACTER RECORDS, WHICH INCLUDE CONTROL CHARACTERS USED DURING
*SUCCESSIVE LOAD OPERATIONS
*ENTRY POINTS: IEHMVERM                                               *
*INPUT: IT IS PASSED THE DATA TO BE WRITTEN VIA AN ADDRESS IN REG 1
*OUTPUT: 80 CHARACTER RECORDS IN EITHER BLOCKED OR UNBLOCKED FORMAT   *
*EXTERNAL ROUTINES: NONE                                              *
*EXITS: NORMAL EXIT TO CALLING ROUTINE
*
*ERRORS: EXIT TO CALLING ROUTINE
*TABLES/WORK AREAS: NONE                                              *
*ATTRIBUTES: SERIALLY REUSEABLE                                       *
IEHMVSRM CSECT
*A008400                                                        OY01180
*A017200,C020600,027200,A028180                                  A44360
*3001014400,021800,028200                                          9677
TESTZ    EQU   0
* 009200-009400                                                  A27588
PTA      EQU   1
PTB      EQU   2
P1       EQU   3
P2       EQU   4
GR5      EQU   5
GR6      EQU   6
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10
GR11     EQU   11
CTABLEPT EQU   12
SAREAPT  EQU   13
RETN     EQU   14
RCODE    EQU   15
TTR      EQU   X'80'
IEHMVERM SAVE  (2,12),T,ERM-8-31-65-10AM
         ENTRY IEHMVERM
         LR    GR11,RCODE               SET UP REG 11 AS
         USING IEHMVERM,GR11              PROGRAM BASE REG
         USING IEHMVV,CTABLEPT          SET UP ADDRESSABILITY TO THE
*                                         COMMUNICATIONS TABLE
         LR    GR5,PTA                  REG 5 NOW CONTAINS THE POINTER
*                                         TO THE RECORDS IN CORE THAT
*                                         ARE TO BE PUT
        IEHPRE ,TFC
         L     GR10,ADCB                SET UP                  OY01180
         MVC   57(3,GR10),SINAD+1    MOVE SYNAD ADDR TO DCB     OY01180
         LTR   TESTZ,TESTZ              TEST FIRST LAST SWITCH
         BM    INIT                     FIRST TIME IN
         BP    FILL                     LAST TIME IN
BACK     XC    LENGSAVE(4),LENGSAVE    CLEAR OUT 4 BYTES FOR     A27588
         MVC   LENGSAVE+2(2),0(GR5)    MOVE LENGTH INTO LAST TWO A27588
*                                      BYTES OF FIELD, TO KEEP   A27588
*                                      LENGTH FROM GOING NEGATIVEA27588
         L     PTB,LENGSAVE            LOAD LENGTH               A27588
         TM    2(GR5),TTR          TEST FOR TTR
         BO    ADD6                     YES THIS RECORD HAS A TTR
         LA    PTB,3(0,PTB)             ADD 3 FOR LLI
A        AR    PTB,GR5                  INITILIZE PTB
B        LA    P1,80
         LH    GR6,IEHMVV60+2           LOAD K INTO REG 6
         SR    P1,GR6                   CALCULATE P1
         LR    P2,PTB
         SR    P2,GR5                   CALCULATE P2
         CR    P2,P1                    COMPARE REG 4 AND REG 3
         BL    D                        BRANCH IF P2 IS LESS THAN P1
         BH    CONT
         TM    LLI+3,X'01'      LAST TIME SW ON
         BO    D       YES
CONT     LR    GR7,P1
         BCTR  GR7,0                    REDUCE REG 7 BY ONE TO SETUP
*                                         EXECUTE OF MOVE INSTRUCTION
         L     GR8,IEHMVV60+4           REG 8 NOW POINTS TO THE NEXT
*                                         OUTPUT BUFFER
         AR    GR8,GR6                  REG 8 NOW POINTS TO NEXT
*                                         AVAILABLE AREA IN OUTPUT
*                                         BUFFER
         EX    GR7,C                    MOVE DATA INTO OUTPUT BUFFER
         AR    GR5,P1                   UP THE PTA
REESTAB  LA    GR6,2                   REINITIALIZE K              9677
         STH   GR6,IEHMVV60+2           STORE A 2 IN K
         LH    GR6,IEHMVV60             UP COUNT OF
         LA    GR6,1(0,GR6)               P.S.
         STH   GR6,IEHMVV60               BY ONE
         L     GR7,IEHMVV60+4           REG 7 NOW POINTS TO THE NEXT
*                                         OUTPUT BUFFER
         STH   GR6,0(GR7)               STORE TWO BYTE P.S. IN
*                                         BEGINNING OF BUFFER
         L     PTA,ADCB                 LOAD ADDRESS OF DCB IN REG 1
         PUT   (1)
         ST    PTA,IEHMVV60+4           STORE ADDRESS OF NEXT OUTPUT
*                                         BUFFER
         CR    P2,P1                    COMPARE REG 4 AND REG 3
         BNE   B                       CONTINUE                  A44360
         TM    LLI+3,X'01'             LAST RECORD WRITTEN       A44360
         BO    WRAPUP                  YES FORMAT LAST RECORD    A44360
         B     RETSET                  NO RETURN TO CALLER       A44360
SINAD    DC    A(E1)      SYAAD ADDR TO BE PUT IN DCB
E1       LA    RCODE,4                  SET UP RETURN CODE FOR
*                                         PERMANENT I/O ERRORS
         B     RET
INIT     L     GR10,ADCB                SET UP
         MVC   57(3,GR10),SINAD+1    MOVE SYNAD ADDR TO DCB
         L     PTA,ADCB                 LOAD ADDRESS OF DCB IN REG 1
         PUT   (1)
         ST    PTA,IEHMVV60+4           STORE ADDRESS OF FIRST OUTPUT
*                                         BUFFER
         LA    GR6,2
         STH   GR6,IEHMVV60+2           STORE A 2 IN K
         SR    GR6,GR6
         STH   GR6,IEHMVV60             INITILIZE P.S. TO ZERO
         B     BACK
EXEC1    XC    0(1,GR10),0(GR10)       PAD BUFFER WITH ZEROES    A44360
*
*
FILL   EQU   *
         SR    0,0    TURN OFF LAST TIME IND
         OI    LLI+3,X'01'    TURN ON LAST TIME SW
         CLI   IEHMVV60+3,X'4D'        ROOM FOR LLI IN BUFF        9677
         BH    INCRESE    NO CHANGE CT TO 20
NOW      LA    1,LLI    SET UP EOD INDICATOR
         LR    5,1
         B     BACK
INCRESE  OI    LLI+1,X'14'   SET COUNT TO 20
         B     NOW
LENGSAVE DS    F                                                 A27588
LLI     DC    X'00000100'
EIGHTY   DC    H'80'                                               9677
ADD6     LA    PTB,6(0,PTB)             ADD 6 FOR LLI AND TTR
         B     A
C        MVC   0(1,GR8),0(GR5)          EXECUTE MOVE OF NUMBER OF BYTES
*                                         (EITHER P1 OR P2) TO OUTPUT
*                                         BUFFER + K FROM PTA
D        LR    GR7,P2
         BCTR  GR7,0                    REDUCE REG 7 BY ONE TO SETUP
*                                         EXECUTE OF MOVE INSTRUCTION
         L     GR8,IEHMVV60+4           REG 8 NOW POINTS TO THE NEXT
*                                         OUTPUT BUFFER
         AR    GR8,GR6                  REG 8 NOW POINTS TO NEXT
*                                         AVAILABLE AREA IN OUTPUT
*                                         BUFFER
         EX    GR7,C                    MOVE DATA INTO OUTPUT BUFFER
         LH    GR6,IEHMVV60+2           LOAD K INTO REG 6
         AR    GR6,P2                   UP K TO NEW VALUE
         CH    GR6,EIGHTY              IS BUFF FULL                9677
         LR    P2,P1                   TO BRANCH TO RESET          9677
         BE    REESTAB                 BUFF IS FULL
         STH   GR6,IEHMVV60+2           STORE K
         TM    LLI+3,X'01'      LAST TIME SW ON
         BZ   RETSET     NO
WRAPUP   LH    GR6,IEHMVV60            LOAD P.S. COUNT           A44360
         LA    GR6,1(0,GR6)    INCREMENT BY ONE
         L     GR7,IEHMVV60+4    LOAD BUFFER ADD
         STH   GR6,0(GR7)   PUT P.S. CT IN BUFFER
RETSET   SR    RCODE,RCODE              SET UP NORMAL RETURN CODE
RET      TM    LLI+3,X'01'             LAST TIME SW ON             9677
         BZ    DONE                    SWITCH NOT ON               9677
         L     GR10,IEHMVV60+4         ADDR OF OUTPUTBUFFER      A44360
         LH    GR6,IEHMVV60+2          NBR OF BYTES USED         A44360
         AR    GR10,GR6                START OF PADDING AREA     A44360
         LA    GR7,79                  80 MINUS 1 FOR EX         A44360
         SR    GR7,GR6                 LENGTH OF PADD AREA       A44360
         EX    GR7,EXEC1               PAD LAST REC WITH ZEROES  A44360
         NI    LLI+1,X'00'             TURN OFF REC LENGTH       A44360
         NI    LLI+3,X'00'            TURN OFF LAST TIME SWITCH  A44360
DONE     IEHPOST ,TC
        RETURN (14,12),T,RC=(15)
         DROP  GR11,CTABLEPT
         IEHMVV
ADCB     EQU   IEHMVV31
         END
