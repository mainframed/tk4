***********************************************************************
*        TITLE           IEHMVESJ                                     *
*                                                                    *
*        FUNCTION          THIS IS THE CONTROL CARD SCAN ROUTINE     *
*                          USED BY THE IEHMOVE PROGRAM               *
*                                                                    *
*                          IT READS AND SCANS FIELDS OF THE IEHMOVE  *
*                          CONTROL CARDS PASSING EACH FIELD AS IT IS *
*                          ENCOUNTERED TO THE IEHMOVE CONTROL CARD   *
*                          ANALYZER ROUTINE                          *
*                                                                    *
*        ENTRY POINTS    IEHMVESJ  VIA XCTL FROM THE IEHMOVE CONTROL  *
*                                  CARD ANALYZER ROUTINE, IEHMVESS    X
*                                                                     *
*                        KEOF      END OF FILE ENTRY                  *
*                                                                    *
*        INPUT             IEHMOVE COMMUNICATIONS AND WORK AREA      *
*                          DESCRIBED IN MACRO EXPANSION OF IEHMVV    *
*                                                                    *
*        OUTPUT          SAME AS INPUT AREA                           *
*                                                                     *
*        EXTERNAL ROUTINES:                                           *
*                        IEHMVESU  VIA LINK TO PRINT CARD IMAGES      *
*                                                                     *
*                        IEHMVESS  VIA XCTL TO CONTROL CARD ANALYZER  *
*                                  ROUTINE                            *
*                                                                     *
*        EXITS:                                                       *
*              NORMAL    IEHMVESS                                     *
*              ERROR     NONE                                         *
*                                                                    *
*        TABLES WORK AREA  DSACT IN MACRO EXPANSION IEHMVV           *
*                                                                    *
*        ATTRIBUTES      SERIALLY REUSEABLE                           *
*                                                                     *
***********************************************************************
         EJECT
IEHMVSSJ START
RDCDRT   CSECT
*D100800                                                        OY01173
*                                                                PM3343
*                                                                A37900
*
*                             REGISTER NOTATION AND USAGE
*
REG0     EQU   0                        PARAM REG
REG1     EQU   1                        PARAM REG
REG2     EQU   2                        WORK REG
REG3     EQU   3                        WORK REG
REG4     EQU   4                        WORK REG
REG5     EQU   5                        WORK REG
REG7     EQU   7                        BASE FOR SWITCHRD
REG9     EQU   9
REG10    EQU   10
REG11    EQU   11                       BASE FOR IEHMVESJ
REG12    EQU   12                       BASE FOR IEHMVV
REG15    EQU   15                       ENTRY POINT REG
*
*                             EQUATE STATEMENTS
*
BLANK    EQU   X'40'
ABORT    EQU   X'01'
ZERO     EQU   X'00'
*                             THE FOLLOWING VALUES ARE USED TO SET
*                             SWITCHES IN DSECT, SWITCHRD
BIT0     EQU   X'80'
BIT1     EQU   X'40'
BIT2     EQU   X'20'
BIT3     EQU   X'10'
BIT4     EQU   X'08'
BIT5     EQU   X'04'
BIT6     EQU   X'02'
BIT7     EQU   X'01'
BIT8     EQU   X'14'
RESET0   EQU   X'00'
RESET1   EQU   X'F7'
RESET2   EQU   X'98'
RESET6   EQU   X'EF'
RESET10  EQU   X'0C'
CONT     EQU   X'48'          THE FOLLOWING VALUES ARE USED TO TEST FOR
KSPLT    EQU   X'7F'     DELIMITERS IN THE INPUT STREAM.
EQUAL    EQU   X'7E'
COMMA    EQU   X'6B'
BLANKCOL EQU   X'40'
LPARN    EQU   X'4D'
RPARN    EQU    X'5D'
PERIOD   EQU   X'4B'
MINUS    EQU   X'60'
DOLLAR   EQU   X'5B'
POUND    EQU   X'7B'
AT       EQU   X'7C'
PRPARN   EQU   X'03'
EQRPN    EQU   X'90'
MSKE     EQU   X'42'
MSKG     EQU   X'60'
SLASH    EQU   X'61'
*
*                             ASSEMBLER USING STATEMENTS
*
         USING IEHMVESJ,REG11
         USING IEHMVV,REG12
         USING SWITCHRD,REG7
*
*
*                             THIS ROUTINE SAVES REGISTERS UPON ENTRY,
*                             TESTS ENTRY CONTROL SWITCHES TO CONTROL
*                             ENTRY TO THE SCAN ROUTINE, AND INITIALIZE
*                             POINTERS TO START SCAN ADDRESS.
IEHMVESJ SAVE  (14,12),T,ESJ-9-16-65
        IEHPRE (14,1),TF
         LR    REG11,REG15              LOAD BASE REGISTER
         LA    REG7,IEHMVV90
        L     REG3,DSP12
         NI    SWITCHRD,X'7F'          RESET ERROR BIT ON SWITCHRD
         TM    SWITCHRD,BIT3           TEST FIRST ENTRY SWITCH
         BZ    KIRSCD                   IF ENTRY SWITCH IS OFF, GO TO
*                                       CLEAR CONTROL SWITCHES FOR
*                                       FIRST READ OPERATION
         TM    SWITCHRD,BIT1           TEST BYPASS SWITCH
         BO    KBYPO                    IF BYPASS SWITCH IS ON, GO TO
*                                       ROUTINE WHICH DETERMINES
*                                       WHETHER THIS CARD IS TO BE
*                                       PRINTED NOW.
         TM    DSP5,BIT4           TEST RDCD SWITCH
         BO    KGTCD                    IF THE READ CARD SWITCH IS ON,
*                                       GO TO READ ANOTHER CARD.
         TM    IEHMVV20+3,X'10'         PRINT CARD NOW
         BZ    KPFOL                    NO
         BAL   REG9,KPRNT               YES PRINT IT NOW
         OI    IEHMVV20+3,X'20'
         NI    IEHMVV20+3,X'EF'         INDICATE CARD HAS BEEN PRINTED
KPFOL    TM    DSP5,BIT2           TEST PARAM FOLLOW SWITCH
         BO    SCANR                    IF PARAM SWITCH IS ON, GO TO
*                                       SCAN RTN TO GET NEXT DELIMITER
         TM    DSP5,BIT1           TEST CONTINUE SWITCH
         BO    KCONT                    IF CONTINUATION SWITCH IS ON,
*                                       GO TO SCAN CONTINUATION CARD
         LA    REG3,1(REG7)             LOAD ADDRESS OF BEGIN COLUMN
*                   THIS ROUTINE CHECKS FOR A NAME  IF NAME IS PRESENT
*                      BYPASSES NAME AND SET POINTER TO FIRST BLANK COL
*
KNAME    CLI   0(REG3),BLANKCOL
         BE    KOMMD                    IF DELIMITER FOUND, GO TO SET
*                                       SWITCHES TO LOOK FOR COMMAND
*                                       WORD
         LA    REG5,DSP3                GET ADDRESS OF COL 71
         CR    REG3,REG5                IF THIS IS COL 71 AND NO
*                                       COMMAND WORD HAS BEEN FOUND,
         BE    KNOP                     GO TO SET ERROR SWITCHES
         LA    REG3,1(REG3)             OTHERWISE ADJUST POINTER TO THE
*                                       INPUT STREAM AND
         B     KNAME                    RETURN TO SEARCH AGAIN
*                   INITIALIZE SWITCHES AND INDICATORS TO RECOGNIZE
*                      COMMAND WORD IN FIRST CARD OF A GROUP
*                      RESET PARENTHESES COUNTER
*
KOMMD    LA    REG3,1(REG3)             ADJUST PNTR TO THE INPUT STREAM
         SR    REG2,REG2
         STH   REG2,DSP9           RESET PARENTHESES COUNTER
         OI    DSP5,BIT5           SET COMMAND CONTROL SWITCH
         OI    SWITCHRD,BIT4           SET COMMAND BIT IN SWITCHRD
         NI    DSP10,RESET0        RESET DELIMITER INDICATOR
*
*
*                   ADJUST POINTER TO FIRST CHARACTER OF COMMAND
*                      IF NO COMMAND FOUND BEFORE COL 71 SET BYPASS BIT
         LA    REG5,DSP3           LOAD ADDRESS OF END COLUMN
KABC     CLI   0(REG3),BLANKCOL           IS COLUMN BLANK
         BNE   SCAN                     IF THIS COLUMN IS NOT BLANK,
*                                       GO TO SCAN FOR NEXT DELIMITER
         CR    REG3,REG5                  IS THIS THE END COLUMN
         BE    KNOP                     IF THIS IS END OF CARD AND NO
*                                       COMMAND WORD HAS BEEN FOUND, GO
*                                       TO SET ERROR SWITCHES
         LA    REG3,1(REG3)            IF NO, ADJUST PNTR TO NEXT COL
         B     KABC                     AND RETURN TO SCAN FOR BLANK
*                   RESET SWITCHRD CONTROLS  RESET EXIT CONTROLS
*                      SAVE LAST DELIMITER AND CLEAR DELIMITER IND
*
SCANR    NI    SWITCHRD,RESET1         RESET COMMAND BIT
SCAN     NI    SWITCHRD,RESET2         RESET ALL BITS BUT ERR,FST ENT
         NI    DSP5,BIT8           RESET ALL BITS BUT CMD CNTL SWITCH
SCANPP   MVC   DSP11(1),DSP10
KCPSS    NI    DSP10,RESET0
         NI    DSP8,RESET10        RESET EXIT CONTROL SWITCH
*
*                   THIS ROUTINE SCANS CARD UNTIL DELIMITER IS FOUND
*                      SAVES ADDRESS OF FIRST CHAR  COMPUTES LENGTH
*                      SET BIT IN EXIT CONTROL IF DELIM IS IN END COL
*                      OR BEFORE END COL
*
KTRT     SR    REG1,REG1
         SR    REG2,REG2
         LA    REG5,DSP3           LOAD ADDRESS OF END COLUMN
         LR    REG1,REG3
KOMPAR   CLI   0(REG3),KSPLT            IS CHAR A DELIMITER
         BL    KFIND                    IF YES,GO TO FIND WHAT KIND OF
*                                       DELIMITER
KOMPARA  LA    REG2,1(REG2)            ACCUMULATE LENGTH OF PARAM
*                                       BEING SCANNED
         CR    REG3,REG5                IS THIS END COLUMN
         BE    KPART                    IF YES, GO TO SAVE LENGTH OF
*                                       PARTIAL PARAM
         LA    REG3,1(REG3)            IF NO, ADJUST PNTR TO NEXT COL
         B     KOMPAR                   AND RETURN TO LOOK FOR DELIMITE
*                  KFIND CHECKS FOR DELIMITER OF PERIOD OR MINUS IF
*                      PRESENT RETURNS TO SCAN ROUTINE   TESTS END COL
*                      IF DELIMITER IS IN END COL SET CC2 BIT  IF BEFOR
*                      END COL SET CC1 BIT AND GOES TO TEST DELIMITER
*                      ROUTINE.
*
*
KFIND    CLI   0(REG3),PERIOD
         BE    KOMPARA                  IF DELIMITER IS A PERIOD OR A
*                                      MINUS SIGN, PUND SIGN,DOLLAR
*                                          SIGN, SLASH OR AT SIGN,
*                                          RETURN TO CONTINUE SCANNING
*                                          THE INPUT STREAM
         CLI   0(REG3),MINUS
         BE    KOMPARA
     CLI  0(REG3),DOLLAR
         BE    KOMPARA
      CLI   0(REG3),POUND
         BE    KOMPARA
      CLI   0(REG3),AT
         BE    KOMPARA
         CLI   0(REG3),SLASH
         BE    KOMPARA
         CR    REG3,REG5               IS THIS END COLUMN
         BNE   SETCC1                   IF NO, SET SWITCH TO INDICATE
*                                       DELIMITER FOUND
         OI    DSP8,BIT2                SET COND CODE 2 BIT
         TM    DSP5,BIT3                IS PARTIAL PARAM SW ON
         BO    KTERBY                   IF YES, PARAM IS TOO LONG, GO
*                                       TO SET ERROR SWITCH
         B     TESTPAR                  IF NO,GO TO CHECK FOR PARTIAL
*                                       PARAM
*                   THIS ROUTINE SETS ENTRY SWITCHES WHEN NO COMMAND
*                      WORD IS FOUND OR NO KEYWORD OR PARAMETER FOLLOWS
*                      A COMMAND WORD
*
KNOP     OI    SWITCHRD,BIT0     ERROR NO COMMAND OR PARAM IN CARD
         SR    REG1,REG1
         SR    REG2,REG2
KNOPAR   OI    SWITCHRD,BIT1     ERROR NO KEYWORD OR PARAM FOLLOW
*                                       COMMAND WORD. SET BYPASS SWITCH
         B     KPASS                    AND GO TO EXIT ROUTINE
*                   TESTPAR CHECKS FOR PRESENCE OF PAPTIAL PARAMETER
*                      AND CHECKS LENGTH FOR ZERO OR  GT ZERO.
*                      SETS CORRECT LENGTH BIT
TESTPAR  TM    DSP5,BIT3                IS'PARTIAL PARAM SW ON
         BO    KPPAR                    IF YES,GOTO CHECK IF PARAM
*                                       LENGTH IS VALID
         SR    REG4,REG4
         AR    REG4,REG2
         BZ    SETLZ                    IF RESULT OF ADDITION IS ZERO
*                                       GOTO SET LENGTH ZERO SWITCH
*                                       AND RETURN BELOW TO CHECK FOR
*                                       VALID DELIMITER.
         OI    DSP8,BIT6                SET LNG GRTR THAN ZERO BIT
*
*
*
*                   DELIM  CHECKS FOR VALID DELIMITER AND BRANCHES TO
*                      ROUTINE TO SET CONTROL SWITCHES
*                      IF INVALID DELIMITER IS FOUND BRANCHES TO KINVAL
*
DELIM    CLI   0(REG3),EQUAL
         BE    KEY                      IF DELIMITER IS AN EQUAL SIGN
*                                       GO TO SET KEYWORD SWITCH
         CLI   0(REG3),COMMA
         BE    PARAMC                   IF DELIMITER IS A COMMA, GOTO
*                                       SET PARAMETER SWITCH
         CLI   0(REG3),LPARN
         BE    PARAMLP                  IF DELIMITER IS A LEFT PAREN
*                                       SET LEFT PAREN SWITCH
         CLI   0(REG3),RPARN
         BE    PARAMRP                  IF DELIMITER IS A RIGHT PAREN
*                                       SET RIGHT PAREN SWITCH
         CLI   0(REG3),BLANKCOL
         BE    PARBLANK                 IF DELIMITER IS A BLANK,
*                                       SET BLANK SWITCH
         B     KINVALD                  IF NONE OF THE ABOVE, SET ERROR
*                                       SWITCH INDICATING INVALID
*                                       DELIMITER FOUND.
*                   DECID  CHECK THE EXIT CONTROL SW AND DELIMITER
*                      INDICATOR TO SET CORRECT ENTRY SWITCHES AND
*                      INFORMATION IN SWITCHRD FOR USER PROGRAM
*
*
*                   KIRSCO IS THE FIRST ENTRY ROUTINE  WHICH CLEARS
*                      CONTROL SWITCHES TO ZERO FOR FIRST READ
*                      GOES TO READ CARD  ROUTINE
*
KIRSCD   OI    SWITCHRD,BIT3           SET FIRST ENTRY SWITCH
         SR    REG3,REG3               *CLEAR READ ROUTINE
         ST    REG3,DSP9                SWITCH AREAS ON FIRST
         STC   REG3,DSP5                ENTRY INTO ROUTINE*
         STC   REG3,DSP8
*   KGTCD READS A CARD INTO INPUT AREA.  IF LAST CARD HAS BEEN   PM3343
*      READ GO TO KEOF.  IF NOT GO TO KGTCDR.                    PM3343
KGTCD    L     REG1,IEHMVV32       SYSIN DCB ADDRESS.            PM3343
         LA    REG1,0(REG1)        CLEAR HIGH ORDER BYTE.        PM3343
         MVC   57(3,REG1),SYNAD    ADDR OF SYSIN SYNAD TO DCB.   PM3343
         MVC   33(3,REG1),EODA  ADDR OF EOF ROUTINE TO SYSIN DCB PM3343
*                                                                PM3343
*         READ NEXT CARD                                         PM3343
*                                                                PM3343
         LA    REG0,DSP1                LOAD ADDRESS OF INPUT AREA
         GET   (REG1),(REG0)            READ A CARD
         CLC   DSP1(2),EOF         IS THIS A /* ?                 19919
         BE    KEOF                YES--THEN W'VE HIT EOF AND     19919
*                                      DATA MANAGEMENT HAS        19919
*                                      NOT SENT US TO OUR         19919
*                                      EOF ROUTINE  .             19919
         TM    IEHMVV20+3,X'20'         PRINT NOW
         BZ    KGTCDR                   NO
         BAL   REG9,KPRNT               YES PRINT IT NOW
         B     KGTCDR
*                   KGTCDR TESTS COLUMN 72 FOR BLANK AND SETS BIT FOR
*                      BLANK OR NON BLANK   TEST BYPASS BIT IF ON
*                      GO TO KBYPO IF OFF GO TO ROUTINE TO TEST ENTRY
*                      CONTROL SWITCH
*
KGTCDR   NI    DSP8,RESET0
         CLI   DSP4,BLANKCOL            IS COLUMN 72 BLANK
         BE    SET72B                   IF YES, SET END OF CARD SWITCH
         OI    DSP8,BIT5                SET 72 NB SW
RETURN   TM    SWITCHRD,BIT1           IS BYPASS SWITCH ON
         BO    KBYPO                    IF YES, GOTO TEST IF CARD IS TO
*                                       BE PRINTED NOW
         B     KPFOL                    GO TO TEST IF NEXT CARD SHOULD
*                                       BE READ.
*                   PARBLANK IS ENTERED IF DELIMITER WAS A BLANK
*                      CHECKS FOR PRESENCE OF COMMAND WORD IF COMMAND
*                      ADJUST POINTER TO NEXT NON BLANK COLUMN
*                      CHECKS LENGTH IF NOT ZERO SET BYPASS SW
*                      IF ZERO AND PRECEDED BY COMMA READ NEXT
*                      CARD
*
PARBLANK TM    DSP5,BIT5                IS CMD CNTL SW ON
         BO    KPCMD                    IF YES, GO TO SCAN FOR NEXT
*                                       KEYWORD
         OI    SWITCHRD,BIT6           SET PARAM BIT
         OI    DSP10,BIT4               SET BLANK DELIM BIT
DECID    TM    DSP8,BIT2                IS DELIM IN END COLUMN
         BO    KWENT                    IF YES, GO TO SET READ CARD SW
         TM    DSP8,BIT7                IS LENGTH EQ TO ZERO
         BO    KFORZRO                  IF YES, GO TO CHECK TYPE OF
*                                       DELIMITER
         OI    DSP5,BIT2                SET PARAM FOLLOW SW
         TM    DSP10,BIT4               IS DELIM A BLANK
         BO    KETBYP                   IF YES, SET BYPASS SWITCH
         B     KPASS                    IF NONE OF THE ABOVE, EXIT
KETBYP   OI    SWITCHRD,BIT1           SET BYPASS SWITCH
         BAL   REG9,PARAN               GO TO TEST PARAN CTR
         B     KPASS                    EXIT
KFORZRO  SR    REG1,REG1                SET REG1 AND REG2 TO ZERO
         SR    REG2,REG2
         TM    DSP10,BIT4               IS DELIM A BLANK
         BO    KETRDCD                  IF YES, SET READ CARD SWITCH
         OI    DSP5,BIT2                SET PARAM FOLLOW SW
         TM    DSP10,BIT0               IS DELIM AN EQUAL SIGN
         BO    KTER                     IF YES, GO TO SET ERROR SWITCH
*                                       AND EXIT
         TM    DSP10,BIT2               IS DELIM LEFT PAREN
         BO    KPAFOL                   IF YES, GO TO SCAN FOR NEXT
*                                       DELIMITER
         TM    DSP10,BIT3               IS DELIM RIGHT PAREN
         BO    KPASS                    IF YES, EXIT
         TM    DSP11,BIT3               WAS LAST DELIM A RT PAREN
         BO    KPAFOL                   IF YES, GO TO SCAN FOR NEXT
*                                       DELIMITER
         B     KPASS                    OTHERWISE, EXIT
KDSP     LA    REG4,DSP2               IS THIS COL 16
         CR    REG3,REG4
         BE    KBYPS                    IF YES, GO TO SET BYPASS SWITCH
*                                       AND EXIT
         TM    DSP11,EQRPN              WAS LAST DELIM AN = OR )
         BZ    KTERBY                   IF NO, SET ERROR SWITCH ANDEXIT
         B     KBYPS                    OTHERWISE, SET BYPASS SWITCH
*                                       AND EXIT
KETRDCD  OI    DSP5,CONT                SET RDCD AND CONTINUE SW
         TM    DSP8,BIT4                IS COL 72 BLANK
         BO    KDSP                     IF YES, RETURN TO CHECK FOR
*                                       COL 16
         TM    DSP11,BIT1               WAS LAST DELIM A COMMA
         BO    KGTCD                    IF YES, RETURN TO READ NEXT
*                                       RECORD
         TM    DSP11,BIT2               WAS LAST DELIM A  (
         BO    KTERBY                   IF YES, SET ERROR SWITCH AND
*                                       EXIT
         B     KBYPS                    OTHERWISE, SET BYPASS SWITCH
*                                       AND EXIT
KTERBY   OI    SWITCHRD,BIT1           SET BYPASS SWITCH
KTER     OI    SWITCHRD,BIT0           SET ERROR SW
         B     KPASS                    AND EXIT
KWENT    OI    DSP5,CONT                SET RDCD AND CONTINUE SW
         TM    DSP8,BIT4                IS COLUMN 72 BLANK
         BO    KETCTBL                  IF YES, CHECK PAREN COUNT
         TM    DSP8,BIT7                IS LENGTH EQUAL TO ZERO
         BO    KZERO                    IF YES, GO TO FIND TYPE OF
*                                       DELIMITER
         TM    DSP10,BIT4               IS DELIM BLANK
         BO    KETBYP                   IF YES, SET BYPASS SWITCH AND
*                                       CHECK PAREN COUNT
         B     KPASS                    OTHERWISE, EXIT
KZERO    SR    REG1,REG1
         TM    DSP10,BIT0               IS DELIM EQUAL
         BO    KTER                     IF YES, SET ERROR SWITCH AND
*                                       EXIT
         TM    DSP10,BIT4               IS DELIM A BLANK
         BO    KETRDCD                  IF YES, SET READ CARD SWITCH
         TM    DSP10,BIT1               IS DELIM A COMMA
         BZ    KRTPR                    IF NO, CHECK FOR A RIGHT
*                                       PAREN DELIMITER
         TM    DSP11,BIT3               WAS LAST DELIM A )
         BO    KGTCD                    IF YES, READ NEXT RECORD
         B     KPASS                    OTHERWISE, EXIT
KRTPR    TM    DSP10,BIT3               IS DELIM A RT PAREN
         BO    KPASS                    IF YES, EXIT
         B     KGTCD                    OTHERWISE, READ NEXT RECORD
KETCTBL  OI    SWITCHRD,BIT1           SET BYPASS SW
         BAL   REG9,PARAN               CHECK PARAN CTR
         TM    DSP8,BIT7                IS LENGTH EQ TO ZERO
         BO    KETLPRN                  IF YES, CHECK FOR TYPE OF
*                                       DELIMITER
         TM    DSP10,BIT1               IS DELIM A COMMA
         BO    KTER                     IF YES, SET ERROR SWITCH AND
*                                       EXIT
         B     KPASS                    OTHERWISE, EXIT
KETLPRN  SR    REG1,REG1
         TM    DSP10,BIT3               IS DELIM A RT PAREN
         BO    KPASS                    IF YES, EXIT
         TM    DSP10,BIT4               IS DELIM A BLANK
         BO    KETBLK                   IF YES, CHECK WHAT TYPE THE
*                                       PREVIOUS DELIMITER WAS
         B     KTER                     OTHERWISE, SET ERROR SWITCH
*                                       AND EXIT
KETBLK   TM    DSP11,MSKG            WAS LAST DELIM A COMMA OR LT PAREN
         BZ    KPASS                    IF NO, EXIT
         B     KTER                     OTHERWISE, SET ERROR SWITCH
*                                       AND EXIT
SET72B   OI    DSP8,BIT4                SET COL 72 BLANK SW
         B     RETURN                   RETURN TO TEST BYPASS SWITCH
KBYPO    TM    IEHMVV20+3,X'30'         HAS THIS CARD BEEN PRINTED
         BO    KBYPO1                   IF YES, TEST FOR BLANK IN COL72
         TM    IEHMVV20+3,X'20'         SHOULD WE PRINT NOW
         BO    KBYPO1                   NO
         BAL   REG9,KPRNT               YES  PRINT IT NOW
         OI    IEHMVV20+3,X'20'
         NI    IEHMVV20+3,X'EF'
KBYPO1   CLI   DSP4,BLANKCOL            IS COL 72 BLANK
         BNE   KGTCD                    IF NO, READ NEXT RECORD
         NI    SWITCHRD,BIT3           RESET SWITCHRD
         NI    DSP5,RESET0              RESET ENTRY CNTL SW
         NI    IEHMVV20+3,X'CF'         RESET PRINT SWITCH
         B     KGTCD                    RETURN TO READ NEXT RECORD
SETCC1   OI    DSP8,BIT1                SET COND CODE 1 SW
         B     TESTPAR                  RETURN TO TEST FOR PARTIAL PARM
SETLZ    OI    DSP8,BIT7                SET LENGTH ZERO SW
         B     DELIM                    RETURN TO CHECK FOR VALID
*                                       DELIMITER
KEY      OI    SWITCHRD,BIT5           SET KEYWORD BIT
         OI    DSP10,BIT0               SET DELIM EQ BIT
         B     DECID                    RETURN TO SET UP FOR KEYWORD
*                                       SCAN
PARAMC   OI    SWITCHRD,BIT6           SET PARAMETER BIT
         OI    DSP10,BIT1               SET DELIM COMMA BIT
         B     DECID                    RETURN TO SET UP FOR SUB PARAM
*                                       LIST SCAN
PARAMLP  OI    SWITCHRD,BIT6           SET PARAMETER BIT
         OI    DSP10,BIT2               SET DELIM LT PAREN BIT
         BAL   REG9,ADD
         B     DECID                    RETURN TO SET UP SCAN FOR NEXT
*                                       DELIMITER
PARAMRP  OI    SWITCHRD,PRPARN         SET PARAM AND RT PAREN BITS
         OI    DSP10,BIT3               SET DELIM RT PAREN BIT
         BAL   REG9,SUBT
         B     DECID                    RETURN TO SET UP SCAN FOR
*                                       NEXT DELIMITER
PRFOL    OI    DSP5,BIT2                SET PARAM FOLLOW TO ENTRY CNTL
         B     KPASS                    EXIT
KPRNT    LR    REG1,REG12
         MVI   0(REG1),BLANK
         MVC   1(120,REG1),0(REG1)      CLEAR PRINT BUFFER
         LA    REG2,IEHMVV90
         MVC   1(80,REG1),1(REG2)       MOVE CARD IMAGE TO PRINT BUFFER
         ST    REG3,124(REG12)
         LR    REG3,REG1
         LINK  EP=IEHMVESU
         L     REG3,124(REG12)         RESTORE REG 3
         TM    IEHMVV20+2,ABORT         IS PERM SWITCH ON?
         BO    KABORT
         BR    REG9
*
KINVAL   OI    SWITCHRD,BIT0           INVALID DELIMETER USED
         LA    REG2,1(REG2)             ADJUST POINTERS TO THE
         LA    REG3,1(REG3)             INPUT STREAM
         CLI   0(REG3),KSPLT           SET ERROR  BIT
         BL    KINV                     IF THIS IS A DELIMITER, GOTO
*                                       ADJUST INPUT STREAM POINTER
         CR    REG3,REG5
         BL    KINVAL                   IF THIS IS THE END COLUMN, GOTO
*                                       SET ERROR SWITCH, INVALID
*                                       DELIMITER USED
         TM    DSP8,BIT4                IS COLUMN 72 BLANK
         BO    KTERBY                   IF YES, SET ERROR SWITCH
*                                       AND EXIT
         LA    REG2,1(REG2)             ADJUST PARAM LENGTH COUNTER
         B     KPTERR                   AND GOTO SAVE LENGTH OF PARTIAL
*                                       PARAM
KCONT    LA    REG3,DSP1               ADDR. COL. 1 IN REG. 3    A37900
         CLI   0(REG3),BLANK           COL. 1 BLANK?        3    A37900
         BNE   KNOP                    IF NO, SYNTAX ERROR       A37900
         CLC   1(14,REG3),0(REG3)      NEXT 14 COL. BLANK?       A37900
         BNE   KNOP                    IF NO, SYNTAX ERROR       A37900
         LA    REG3,DSP2               ADDR. COL. 16 IN REG. 3   A37900
         CLI   0(REG3),BLANK           COL. 16 BLANK?            A37900
         BE    KNOP                    IF YES,SYNTAX ERROR       A37900
         B     SCANR                    RETURN TO SCAN FOR NEXT
*                                       DELIMITER
KPART    TM    DSP5,BIT3                IS PARTIAL PARAM SW ON
         BO    KTERBY                   IF YES, SET ERROR SWITCH
*                                       AND EXIT
         OI    DSP8,BIT0                SET COND CODE ZERO BIT
KPTERR   OI    DSP5,BIT3                SET PARTIAL PARAM BIT
         TM    DSP8,BIT4                IS COL 72 BLANK
         BO    KLAST                    IF YES, SET PARAM SWITCH
         MVI   DSP7,BIT1
         MVC   DSP7+1(43),DSP7
         STC   REG2,DSP6                SAVE LENGTH OF PARTIAL PARAM
         BCTR  REG2,REG0                ADJUST LENGTH FOR MOVE
         EX    REG2,MOVEP               MOVE PARTIAL TO SAVE AREA
         OI    DSP5,BIT1                SET CONTINUE BIT
         B     KCDFL                    CONTINUATION CARD EXPECTED.
*                                       GOTO TEST IF COL 72 IS NONBLANK
KLAST    OI    SWITCHRD,BIT6            SET PARAM BIT
         B     KETBYP                   SET BYPASS SWITCH AND EXIT
KPPAR    SR    REG4,REG4                CLEAR REG
         IC    REG4,DSP6                PICK UP PARTIAL LENGTH
         AR    REG4,REG2
         C     REG4,FOUR4         IS LENGTH GREATER THAN 44
         BH    KTERBY
         SR    REG4,REG4
         AR    REG4,REG2                 COMPLETE PARAMETER IN HOLD
         BZ    KOMPL                    IF LENGTH COUNT IN REG2 IS ZERO
*                                       PICK UP PARTIAL SWITCH
*                                       PREVIOUSLY SAVED
         SR    REG4,REG4
         IC    REG4,DSP6                PICK UP PARTIAL LENGTH
         LA    REG9,DSP7                ADDR OF PARTIAL HOLD AREA
         AR    REG4,REG9
         BCTR  REG2,REG0                ADJUST VALUE FOR MOVE
         EX    REG2,MOVPP
         LA    REG2,1(REG2)             ADJUST LENGTH OF PARAM
KOMPL    SR    REG4,REG4                COMPUTE LENGTH AND PLACE IN
         IC    REG4,DSP6                 REG 2
         AR    REG2,REG4
         LA    REG1,DSP7                PLACE ADDR OF HOLD AREA IN
*
         NI    DSP8,RESET10
         OI    DSP8,MSKE                SET COND CODE 1 AND LNG GT ZERO
         MVC   DSP11(1),DSP10          SAVE LAST DELIM INDICATOR
         NI    DSP10,RESET0
         NI    DSP5,RESET6              RESET PARTIAL PARAM SW
         B     DELIM                    RETURN TO CHECK FOR
*                                       VALID DELIM
KPASS    LA    REG3,1(REG3)             ADJUST PNTR TO INPUT STREAM
*                                       AND SAVE REGISTER VALUES USED
*                                       IN SCAN RTN
KPASS1   NI    DSP5,X'EB'
        ST    REG1,DSP13
        ST    REG3,DSP12
         ST    REG2,DSP14
       IEHPOST (14),T
         MVI   ESSENTRY,ZERO            SET ESS ENTRY SWITCH
         XCTL  (4,12),EP=IEHMVESS
KPCMD    LA    REG3,1(REG3)             ADJUST PNTR TO START OF NEXT
         CLI   0(REG3),BLANKCOL           KEYWORD OR PARAMETER AND GO
         BNE   KPCMA                    IF NOT A BLANK, GOTO TEST
*                                       FOR A COMMA
         CR    REG5,REG3                IS THIS THE END COLUMN?
         BE    KNOPAR                   IF YES, GOTO SET ERRROR SWITCH,
*                                       NO KEYWORD FOUND, AND EXIT
         B     KPCMD                    OTHERWISE, RETURN TO SEARCH
*                                       FOR A BLANK
KPCMA    CLI   0(REG3),COMMA              IF COMMA IS FIRST CHAR FOUND
         BE    KNOPAR                   SET ERROR SWITCH AND EXIT
         BCTR  REG3,REG0
         B     PRFOL                    SET PARAM FOLLOW SWITCH ANDEXIT
KSETP    OI    DSP5,BIT3                SET PARAM FOLLOW AND
         LA    REG2,1(REG2)             ADJUST PARAM LENGTH COUNTER
         B     KPTERR                   RETURN TO TEST FOR BLANK IN
*                                       COL 72
KINV     TM    DSP5,BIT5                IF COMD CNTL SW ON ADJ
         BO    KPCMD                    ADJUST PNTR TO NEXT PARAM
         B     PRFOL                    SET PARAM FOLLOW SWITCH ANDEXIT
KCDFL    TM    DSP8,BIT5                IF COL 72 NON BLANK GET CARD
         BO    KGTCD                    RETURN TO READ NEXT RECORD
         OI    SWITCHRD,BIT0            SET ERROR  BIT IN SWITCHRD
KBYPS    OI    SWITCHRD,BIT1            SET BYPASS SW
         B     KPASS                    EXIT
SUBT     LH    REG4,DSP9                DELIMETER WAS A CLOSE
         BCTR  REG4,REG0                PARENTESES, SUBTRACT
         STH   REG4,DSP9                ONE FROM COUNTER
         LTR   REG4,REG4                IF PAREN COUNT IS NOT LESS THAN
         BNL   KACK                     ZERO, DO NOT SET ERROR SWITCH
         OI    SWITCHRD,BIT0            SET ERROR BIT
KACK     BR    REG9
*
*
ADD      LH    REG4,DSP9                DELIMETER WAS AN OPEN
         LA    REG4,1(REG4)             PAREN, ADD
         STH   REG4,DSP9                ONE TO COUNTER
         BR    REG9
KPAFOL   LA    REG3,1(REG3)             ADJUST PNTR TO NEX COLUMN
         B     SCANR                    RETURN TO SCAN FOR NEXT
*                                       DELIMITER
KINVALD  OI    SWITCHRD,BIT0            SET ERROR BIT
         TM    DSP8,BIT2                IS DELIMETER IN END COL
         BO    KSETP                    IF YES, RETURN TO SET PARAM
*                                       FOLLOW SWITCH
         B     KINVAL                   OTHERWISE, RETURN TO SET ERROR
*                                       SWITCH, INVALID DELIMITER USED
KEOF     NI    SWITCHRD,X'00'          RESET FIRST ENTRY
         OI    SWITCHRD,X'20'          SET END OF FILE BIT
         BAL   REG9,PARAN               GOTO TEST PAREN COUNT
          TM    DSP5,BIT1          CONTINUATION EXPECTED         A32046
          BZ    KPASS              *B IF NOT                     A32046
          OI    SWITCHRD,BIT0      INDICATE SYNTAX ERROR         A32046
         B     KPASS                    EXIT
PARAN    SR    REG4,REG4
         AH    REG4,DSP9               ADD PAREN COUNT
         BZ    OKAY                     IF PAREN COUNT ZERO, RETURN
         OI    SWITCHRD,BIT0           SET ERROR BIT
OKAY     BR    REG9
KABORT   DS    0H             ENTRY POINT TO TERMINATE A REQUEST
         OI    IEHMVV41+13,X'04'       SET RETURN CODE AT 12
         SR    REG1,REG1
         SR    REG2,REG2
         OI    SWITCHRD,BIT0           SET ERROR BIT
         B     KPASS1                   EXIT
*
*                             FOLLOWING INSTRUCTIONS ARE THE OBJECT
*                             OF EXECUTE STATEMENTS
*
MOVEP    MVC   DSP7(1),0(REG1)          MOVES PARTIAL PARAM TO HOLD
*                                       AREA
MOVPP    MVC   0(1,REG4),0(REG1)        MOVES REMAINDER OF PARAM TO
*                                       HOLD AREA
*
*                             ASSEMBLER DC STATEMENTS AND DSECTS
*                             FOR THIS PROGRAM FOLLOW
*
EOF      DC    CL2'/*'                                            19919
         DS    0F
EODA     DC    AL3(KEOF)
FOUR4    DC    F'44'
SYNAD    DC    AL3(KABORT)
         ENTRY KEOF
         ENTRY  IEHMVESJ
*                             THE FOLLOWING DSECT DESCRIBES THE SWITCH
*                             SETTINGS USED BY THIS PROGRAM
*
         SPACE 3
SWITCHRD DSECT 0F
         DS    0X'80'                   ERROR INDICATOR
         DS    0X'40'                   BYPASS SWITCH
         DS    0X'20'                   END OF FILE INDICATOR
         DS    0X'10'                   FIRST ENTRY SWITCH
         DS    0X'08'                   COMMAND BIT
         DS    0X'04'                   KEYWORD
         DS    0X'02'                   PARAMETER
         DS    1X'01'                   RIGHT PARENTHESES
DSP1     DS    CL1                COLUMN  1  OF CARD
         DS    CL14
DSP2     DS    CL1                COLUMN 16  OF CARD
         DS    CL54
DSP3     DS    CL1                COLUMN 71  OF CARD
DSP4     DS    CL1                COLUMN 72  OF CARD
         DS    CL8
DSP5     DS    0X                  INTERNAL CONTROL SWITCHES
         DS    0X'80'
         DS    0X'40'                   CONTINUED ON NEXT CARD
         DS    0X'20'                   PARAMETER FOLLOW SWITCH
         DS    0X'10'                   PARTIAL PARAMETER SWITCH
         DS    0X'08'                   READ CARD SWITCH
         DS    0X'04'                   COMMAND CONTROL SWITCH
         DS    0X'02'
         DS    1X'01'
DSP6     DS    1X                       LENGTH OF PARTIAL PARAMETER
DSP7     DS    CL44                     HOLD AREA FOR PARTIAL PARAM
DSP8     DS    0X'80'              CONDITION CODE  0
         DS    0X'40'              CONDITION CODE 1
         DS    0X'20'              CONDITION CODE  2
         DS    0X'10'
         DS    0X'08'              COLUMN 72 BLANK
         DS    0X'04'              COLUMN 72 NON BLANK
         DS    0X'02'              LENGTH OF PARAM GREATER THAN ZERO
         DS    1X'01'              LENGTH OF PARAM IS ZERO
DSP9     DS    1H                  PARENTHESIS COUNTER
DSP10    DS    0X'80'              DELIMITER IS EQUAL SIGN
         DS    0X'40'              DELIMITER IS COMMA
         DS    0X'20'              DELIMITER IS LEFT PARENTHESIS
         DS    0X'10'              DELIMITER IS RIGHT PARENTHESIS
         DS    0X'08'              DELIMITER IS BLANK
         DS    0X'04'                   NOT USED
         DS    0X'02'                   NOT USED
         DS    1X'01'                   NOT USED
DSP11    DS    0X'80'              LAST DELIM WAS EQUAL SIGN
         DS    0X'40'              LAST DELIM WAS COMMA
         DS    0X'20'              LAST DELIM WAS LEFT PARENTHESIS
         DS    0X'10'              LAST DELIM WAS RIGHT PARENTHESIS
         DS    0X'08'
         DS    0X'04'                   NOT USED
         DS    0X'02'                   NOT USED
         DS    1X'01'                   NOT USED
DSP12    DS    1F                  SAVE AREA FOR START OF NEXT SCAN
DSP13    DS    1F                  POINTER TO START OF PARAM PASSED
DSP14    DS    1F                  LENGTH OF PARAMETER PASSED
         SPACE 3
         IEHMVV
ESSENTRY EQU   IEHMVV27+4
         END
