*TITLE B'I                                                            *
*   'IEHMVSSR - GETMEMBER NAME'                                       *
*FUNCTION/OPERATION:                                                  *
*   THIS ROUTINE GETS PDS DIRECTORY ENTRIES FROM A WORK DATA SET AND  *
*   PASSES THEM TO THE CALLING ROUTINE. IF ANY ALIAS ENTRIES EXIST FOR*
*   A GIVEN ENTRY THESE WILL BE PASSED SUBSEQUENT TO PASSING THE      *
*   REAL-NAME ENTRIES THE NEXT ALIAS ENTRY (IF ANY) WILL BE PASSED AS *
*   A REAL-NAME ENTRY. THE ENTRY IS PLACED IN AN AREA PROVIDED BY THE *
*   CALLING ROUTINE.                                                  *
*ENTRY POINTS:                                                        *
*        IEHMVESR - ONLY ENTRY POINT                                  *
*        FIRST TIME IN REGISTER 0=0, EACH TIME THEREAFTER REGISTER0   *
*        NOT 0. L 1,79 BYTE AREA L 15,A(IEHMVESR) BALR 14,15          *
*INPUT:                                                               *
*   UTILITY WORK FILE (SYSUT3) OF 79 BYTE RECORDS WITH 4 BYTE KEY ON A*
*   DIRECT ACCESS DEVICE.                                             *
*OUTPUT:                                                              *
*   PDS DIRECTORY ENTRY WITH 5 BYTES OF LOCATION INFORMATION APPENDED.*
*EXTERNAL ROUTINES:                                                   *
*EXITS - NORMAL                                                       *
*   RETURN EVERY TIME EXCEPT LAST. ON LAST EXIT (END OF WORK FILE)    *
*   FREEMAIN AND RETURN. REGISTER 15=0-NORMAL, REGISTER 15=4-END OF   *
*   FILE                                                              *
*ERRORS:                                                              *
*   FREEMAIN AND RETURN. REGISTER 15=8-I/O ERROR.                     *
*TABLES/WORK AREAS:                                                   *
*   160 BYTE WORK AREA                                                *
*ATTRIBUTES:                                                          *
*   READ ONLY, REENTRANT, REUSABLE                                    *
REG0     EQU   0
REG1     EQU   1
REG2     EQU   2
REG3     EQU   3
REG4     EQU   4
REG5     EQU   5
REG6     EQU   6
REG7     EQU   7
REG8     EQU   8
REG9     EQU   9
REG10    EQU   10
REG11    EQU   11
REG12    EQU   12
REG13    EQU   13
REG14    EQU   14
REG15    EQU   15
IEHMVSSR CSECT
*A018960                                                       @ZA02893
*A023000-023060,042680-042760                                  @ZA01204
*C016600,018400,052150                                         @ZA01204
*D023000-023100,042680                                         @ZA01204
*C 023000,052060                                                YA00085
*A 023100-023120,042534-042539,049570                           YA00085
*D 039800-040200,041200                                         YA00085
*A038100-038120                                                  YM5090
*C038200                                                         YM5090
*16600,18400                                                     A34117
*  ALL CODE FLAGGED S20201 WAS ADDED OR CHANGED FOR              S20201
*  MERLIN/ZEUS SUPPORT                                           S20201
         ENTRY IEHMVESR
IEHMVESR SAVE (14,12),T,SR8/26
         BALR  REG10,0
         USING IEHMVV,REG12            SET UP
         USING INXWK,REG8                     ADDRESSABILITY
         USING *,REG10
         IEHPRE ,TF
         LR    REG4,REG3               ADDR OF RECORD AREA
         L     REG8,IEHMVV43           RESTORE WORK AREA ADDR
***********************************************************************
**                                                                   **
**       DETERMINE FUNCTION TO BE PREFORMED                          **
**                                                                   **
***********************************************************************
         LTR   REG2,REG2               INITIAL ENTRY TO THIS ROUTINE
         BZ    INX1                    YES, GO TO INITIALIZE
         TM    IEHMVV20+2,ALIAS        ANY ALIAS ENTRIES
         BZ    INX4                    NO
         TM    INXSW,ALIAS             WANT ALIAS OF PREVIOUS ENTRY
         BO    INX2                    YES
         TM    INXSW,REALNAME          ANY REAL NAME ENTRIES LEFT
         BO    INX5                    NO
***********************************************************************
**                                                                   **
**           GET THE NEXT REAL NAME ENTRY ON FILE                    **
**                                                                   **
***********************************************************************
INX4     MVI   INXCPE+7,X'01'          SET COUNT IN CCW
         MVI   INXTTR,X'00'            SET RECORD TYPE CODE
         BAL   REG11,INX3              GO TO GET ENTRY
         TM    INXSW,ENTRYFND          DIRECTORY ENTRY FOUND
         BO    INX5                    NO
INX4A    OI    INXSW,ALIAS             INDICATE ALIAS WANTED NEXT
         LA    REG15,0(0,0)            RETURN CODE, NORMAL
         MVC   INXTTR+1(3),13(REG4)    SAVE TTR OF ENTRY FOUND
         B     INX4C
INX4B    FREEMAIN  R,LV=226,SP=1,A=(8)                         @ZA01204
         LA    REG15,4(0,0)            RETURN CODE
INX4C    IEHPOST ,T
         RETURN (14,12),T,RC=(15)      RETURN TO CALLER
***********************************************************************
**                                                                   **
**                    INITIALIZATION PROCEDURE                       **
**                                                                   **
***********************************************************************
INX1     GETMAIN  R,LV=226,SP=1                                @ZA01204
         LR    REG8,REG1               ADDR OF WORK AREA
         ST    REG8,IEHMVV43           SAVE WORK AREA ADDR
         LA    REG2,INXPRFX            GET ADDR IOB PREFIX       A34117
         ST    REG2,INXPRFX            PUT IT IN IOB PREFIX      A34117
         MVI   INXIOBA,X'01'                                   @ZA02893
         MVI   INXSW,X'00'             CLEAR INDICATOR
         MVI   INXCP,X'00'             CLEAR CHAN PROG AREA
         MVC   INXCP+1(INXCPL2+8-INXCP-1),INXCP                  S20201
         MVC   IOBINCAM(2),INXCP   BLOCK COUNT IN IOB
         LA    REG2,INXEXT             ADDR OF CCHHR OF FIRST RECORD
         ST    REG2,INXCP              PUT IN CCW
         LA    REG2,IEHMVV42+8         ADDR OF CCHHR OF LAST RECORD
         ST    REG2,INXCPB             PUT IN CCW
         LA    REG2,INXCP              ADDR FOR TIC
         ST    REG2,INXCPA             PUT IN CCW
         LA    REG2,INXCPE             ADDR FOR TIC
         ST    REG2,INXCPC             PUT IN CCW
         LA    REG2,INXTTR             ADDR OF ATTR OF MEMBER
         ST    REG2,INXCPE             PUT IN CCW
         LA    REG2,INXCPB             ADDR FOR TIC
         ST    REG2,INXCPF             PUT IN CCW
         ST    REG4,INXCPG             ADDR OF READ-IN AREA
         ST    REG4,INXCPH             ADDR OF CCHHR OF RECORD READ
         LA    REG2,INXCPH             ADDR FOR TIC
         ST    REG2,INXCPI             PUT IN CCW
         ST    REG2,INXCPG1            IN NEW TIC                S20201
         MVI   INXCLR+8,X'10'          INITIALIZE CLEAR CHAR   @ZA01204
         LA    REG2,INXCLR+8           POINT AT CLEAR DATA     @ZA01204
         ST    REG2,INXCPJ             PUT IN CCW               YA00085
***********************************************************************
**                                                                   **
**               PUT COMMANDS,COUNTS AND FLAGS IN CCWS               **
**                                                                   **
***********************************************************************
         OC    INXCP(CCWSIN),INXCOM    PUT CCWS IN EXEC AREA     S20201
INX1B    L     REG2,IEHMVV42           DCB ADDR
         ST    REG2,IOBDCBPT       PUT DCB ADDRESS IN IOB
         POINT (2),INXTTRK
         MVC   MBBCCHHR(8),5(REG2) PUT MBBCCHHR IN IOB
         TM    IEHMVV20+RPS,RPS        RECORD READY BIT ON TEST  S20201
         BO    INX6                    YES-BUILD CCWS            S20201
         LA    REG2,INXCP              ADDR OF CHANNEL PROGRAM
         ST    REG2,IOBSTART       PUT IN IOB
INX1A    LA    REG2,INXECB             ADDR OF ECB               S20201
         ST    REG2,IOBECBPT       PUT IN IOB
         MVC   INXEXT(5),IOBSEEK   SAVE CCHHR OF FIRST RECORD
         B     INX4
INX6     LA    REG2,INXCP              FIRST CCW                 S20201
         ST    REG2,INXCPK1            IN FIRST SECTOR TIC       S20201
         LA    REG2,INXCPH             GET RETURN SEARCH         S20201
         ST    REG2,INXCPL2            AND PUT IN TIC AFTER SET  S20201
         LA    REG2,SECTOR             PUT SECTOR                S20201
         ST    REG2,INXCPL                                       S20201
         ST    REG2,INXCPL1                                      S20201
         LA    REG2,INXCPL             PUT SEARCH CCW IN         S20201
         ST    REG2,INXEXT1            TIC CCW AFTER THE         S20201
         MVC   INXCPG1+1(ADDR),INXEXT1+1   SECOND SET SECTOR     S20201
         LA    REG2,INXCPD+2           GET A 0 VALUE             S20201
         ST    REG2,INXCPK                                       S20201
         LA    REG2,INXCPK             GET NEW FIRST CCW IN CHAINS20201
         ST    REG2,IOBSTART           AND PUT IN THE IOB        S20201
         OC    INXCPK(SECCCW),INXCOM1  PUT REST OF NEW CCWS IN   S20201
         B     INX1A                   CONTINUE                  S20201
***********************************************************************
**                                                                   **
**                GET THE NEXT ALIAS ENTRY ON FILE                   **
**                                                                   **
***********************************************************************
INX5     MVI   INXTTR,TYPCODE
         MVI   INXCPE+7,X'01'          SET COUNT IN CCW
         OI    INXSW,REALNAME          INDICATE NO REAL NAME ENTRIES
         BAL   REG11,INX3              GO TO GET ALIAS ENTRY
         NI    16(REG4),X'7F'          MAKE IT REAL NAME ENTRY
         TM    INXSW,ENTRYFND          ENTRY FOUND
         BZ    INX4A                   YES
         B     INX4B
***********************************************************************
**                                                                   **
**             GET AN ALIAS OF PREVIOUSLY FOUND ENTRY                **
**                                                                   **
***********************************************************************
INX2     MVI   INXTTR,TYPCODE          SET RECORD TYPE CODE
         MVI   INXCPE+7,X'04'          SET COUNT IN CCW
         BAL   REG11,INX3              GO TO GET ALIAS ENTRY
         TM    INXSW,ENTRYFND          ENGRY FOUND
         BO    INX4                    NO
         B     INX4A
***********************************************************************
**                                                                   **
**          EXECUTE CHANNEL PROGRAM AND INTERPRET RETURN             **
**                                                                   **
***********************************************************************
INX3     LA    REG9,INXIOBA        ADDR OF IOB
         EXCP  (REG9)
         LA    REG1,INXECB             ECB ADDR
         WAIT  ECB=(1)
         L     REG2,IEHMVV42           DCB ADDR
         TM    44(REG2),IOERROR        TEST FOR PERMANENT IO ERROR
         BO    INXERR                  YES
         NI    INXSW,IND           CLEAR INDICATOR
         L     REG2,IOBCSW         COMMAND ADDRESS FROM CSW
         N     REG2,INXMSK1            CLEAR HIGH ORDER BYTE
         LA    REG3,INXCPD             ADDR OF NO OP COMMAND
         LA    REG3,8(0,REG3)          ADD 8
         CLR   REG2,REG3               CHAN PROG EXIT AT NO OP
         BNE   0(0,REG11)              NO
         OI    INXSW,X'40'             INDICATE EXIT AT NO OP
         BR    REG11
***********************************************************************
**                                                                   **
**               I / O   E R R O R   E X I T                         **
**                                                                   **
***********************************************************************
INXERR   EQU   *                                                 YM5090
         NI    44(REG2),X'FF'-IOERROR  SET OFF PERM ERROR BITS   YM5090
         FREEMAIN R,LV=160,SP=1,A=(8)                            YM5090
         LA    REG15,8(0,0)            RETURN CODE
         B     INX4C
***********************************************************************
**                                                                   **
**                 C O N S T A N T S                                 **
**                                                                   **
***********************************************************************
INXEXT1  DS    F                       TO UPDATE CCWS            S20201
INXTTRK  DC    A(256)
INXMSK1  DC    X'00FFFFFF'
INXK8    DC    H'8'
***********************************************************************
**                                                                   **
**             TABLE OF COMMANDS,COUNTS AND FLAGS FOR CCWS           **
**                                                                   **
***********************************************************************
INXCOM   DC    X'31'                                             S20201
         DC    AL3(0)                                            S20201
         DC    X'40000005'                                       S20201
         DC    X'08'                                             S20201
         DC    AL3(0)                                            S20201
         DC    X'40000001'                                       S20201
         DC    X'B1'                                             S20201
         DC    AL3(0)                                            S20201
         DC    X'40000005'                                       S20201
         DC    X'08'                                             S20201
         DC    AL3(0)                                            S20201
         DC    X'40000001'                                       S20201
         DC    X'03'                                             S20201
         DC    AL3(0)                                            S20201
         DC    X'20000001'                                       S20201
         DC    X'A9'                                             S20201
         DC    AL3(0)                                            S20201
         DC    X'60000004'                                       S20201
         DC    X'08'                                             S20201
         DC    AL3(0)                                            S20201
         DC    X'40000001'                                       S20201
         DC    X'06'                                             S20201
         DC    AL3(0)                                            S20201
         DC    X'60000057'                                       S20201
         DC    X'08'                                             S20201
         DC    AL3(0)                                            S20201
         DC    X'40000001'                                       S20201
         DC    X'31'                                             S20201
         DC    AL3(0)                                            S20201
         DC    X'40000005'                                       S20201
         DC    X'08'                                             S20201
         DC    AL3(0)                                            S20201
         DC    X'400000FF'                                       S20201
INXCHPR  DC    X'0D'                   CCW TO DE-ACTIVATE ENTRY YA00085
         DC    AL3(0)                  FOUND                    YA00085
         DC    X'20000004'                                      YA00085
INXCOM1  DC    X'23'                                             S20201
         DC    AL3(0)                                            S20201
         DC    X'40000001'                                       S20201
         DC    X'08'                                             S20201
         DC    AL3(0)                                            S20201
         DC    X'40000001'                                       S20201
         DC    X'22'                                             S20201
         DC    AL3(0)                                            S20201
         DC    X'40000001'                                       S20201
         DC    X'23'                                             S20201
         DC    AL3(0)                                            S20201
         DC    X'40000001'                                       S20201
         DC    X'08'                                             S20201
         DC    AL3(0)                                            S20201
         DC    X'00000001'                                     @ZA01204
         DC    X'03000000'                                     @ZA01204
         DC    X'00000001'                                     @ZA01204
***********************************************************************
**                                                                   **
**             D E F I N E   W O R K   A R E A                       **
**                                                                   **
***********************************************************************
INXWK    DSECT
INXPRFX  DS    2F                                                A34117
INXIOBA  DS    10F
IOBINCAM EQU   INXIOBA+28
IOBDCBPT EQU   INXIOBA+20
IOBSTART EQU   INXIOBA+16
IOBECBPT EQU   INXIOBA+4
IOBCSW   EQU   INXIOBA+8
MBBCCHHR EQU   INXIOBA+32
IOBSEEK  EQU   INXIOBA+35
INXECB   DS    F
INXEXT   DS    5C
INXSW    DS    C
SECTOR   DS    C                       SECTOR VALUE              S20201
INXTTR   DS    F
***********************************************************************
**                                                                   **
**         C H A N N E L   P R O G R A M   A R E A                   **
**                                                                   **
***********************************************************************
INXCP    DS    D                       SEARCH ID= FIRST RECORD
INXCPA   DS    D                       TIC TO INXCP
INXCPB   DS    D                       SEARCH ID= LAST RECORD
INXCPC   DS    D                       TIC TO INXCPE
INXCPD   DS    D                       NO OP, END OF DATA
INXCPE   DS    D                       SEARCH KEY= FOR DESIRED RECORD
INXCPF   DS    D                       TIC TO INXCPB
INXCPG   DS    D                       READ DATA OF RECORD FOUND
INXCPG1  DS    D                       TIC TO NEXT SEARCH OR READS20201
INXCPH   DS    D                       SEARCH ID= OF RECORD FOUND
INXCPI   DS    D                       TIC TO INXCPH
INXCPJ   DS    D                       WRITE KEY,DATA TO CLOBBER RECORD
INXCPK   DS    D                       FIRST SET SECTOR TO 0     S20201
INXCPK1  DS    D                    TIC TO OLD BEGINNING (INXCH) S20201
INXCPL   DS    D                       READ SECTOR               S20201
INXCPL1  DS    D                       SET SECTOR                S20201
INXCPL2  DS    D                    TIC TO WRITE SEARCH (INXCPH) S20201
INXCLR   DS    C                       AREA FOR CLEAR DATA      YA00085
***********************************************************************
**                                                                   **
**                  E Q U A T E S                                    **
**                                                                   **
***********************************************************************
*
*
IND      EQU   X'BF'
ALIAS    EQU   X'80'
REALNAME EQU   X'20'
ENTRYFND EQU   X'40'
TYPCODE  EQU   X'80'
IOERROR  EQU   X'C0'
ADDR     EQU   3                                                 S20201
CCWSIN   EQU   96                      BYTES IN CCW CHAIN       YA00085
RPS      EQU   4                       RECORD READY BIT IN DSECT S20201
CODECCW  EQU   132                     BYTES TO CLEAR FOR CCWS   S20201
SECCCW   EQU   48                      BYTES IN NEW CCWS       @ZA01204
         IEHMVV
         END
