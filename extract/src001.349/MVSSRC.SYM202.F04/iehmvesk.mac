*TITLE ' IEHMVSSK'
*FUNCTION/OPERATION: THIS MODULE IS ENTERED WHEN ALL REQUEST TO       *
*IEHMOVE1 HAVE BEEN SERVICED OR WHEN A CONDITION IS DETERMINED THAT   *
*WILL NOT ALLOW ANY FURTHER PROCESSING BY IEHMOVE1 PROG.              *
*THE FUNCTION OF THIS MODULE IS TO: 1. CLOSE WORKFILES, SYSIN,        *
*SYSPRINT, DCB. 2. SCRATCH WORKFILES 3. FREEMAIN COMMUNICATION        *
*(OR WORK) AREA.                                                      *
*ENTRY POINTS: IEHMVESK                                               *
*INPUT: POINTER TO IEHMOVE1 WORK AREA IN REG 12                       *
*EXITS - NORMAL 'RETURN' TO IEHMVSSD (STEM)                           *
*3125 0070                                                         7667
IEHMVSSK CSECT
*C013200                                                       @ZA03372
*A012220,034100                                                @ZM30153
*A013200,014400,032600                                         @ZA00101
*D013200-013800,014400-015600,016600-017600,024800-026000      @ZA00101
*D032600-032800                                                @ZA00101
*A018600,C019000,028600                                          A50942
*A018600,C019000,028600                                          A50942
*A018600,C019000,028600                                          A50942
*013500-013540                                                   A48777
*008800                                                          A34084
* 021000                                                           I276
* 027800                                                        PTM3039
*                                                                A30903
*                                                                A35452
* 009400,012800                                                  A37897
* 014000-015800,016400-018000,024400-024600,032200,033000-033200 A37897
*C013200,014400,018600,019520                                    A50959
*A012920-012940,014280-014300,018300-018320,018500               A50959
         USING IEHMVV,COMMAREA
         USING IEHMVESK,BASE
IEHMVESK SAVE  (14,12),T,FINAL-WRAP-UP-DBRUCE-AUG-9-5
         ENTRY IEHMVESK
         LR    BASE,LINK
        IEHPRE (14,1),TFC
** CLOSE SYSIN-SYSPRINT-,,SYSUT1-,,SYSUT2 AND ,,SYSUT3
**
*      MOVE THE CLOSE TO IEHMVV00 TO MAINTAIN REENTRABILITY
         MVC   IEHMVV00(RENT2-RENT1),RENT1
         B     RENT2
*    THIS SECTION OF CODE WILL NOT BE EXECUTED IN LINE
*      IT WILL BE MOVED TO GOTTEN CORE AND EXECUTED TO MAINTAIN
*      REENTRABILITY
RENT1    DS    0F
         USING *,COMMAREA
         CLOSE ((R6))
         BR    10
         USING IEHMVV,COMMAREA
RENT2    DS    0F
         L     R6,IEHMVV32              SYSIN DCB ADDR
         USING IHADCB,R6                                         A35452
         TM    DCBOFLGS,OPENED         WAS SYSIN OPENED          A35452
         BZ    SCRATCH                 NO-SKIP FREEPOOL          A35452
         BALR  R10,COMMAREA       CLOSE
         FREEPOOL (R6)                 FREE SYSIN BUFFERS        A35452
SCRATCH  EQU   *                                                 A35452
         SR    DBR1,DBR1
         L     R1,4(SAVEAREA)          GET PREVIOUS SAVE ADDR      7667
         L     R1,4(R1)                SAVE AREA OF INVOLK         7667
         L     R1,24(R1)               PARAMETER ADDRESS           7667
         TM    0(R1),X'80'   DDNAME LIST                           7667
         BO    NOLIST                  NO                          7667
         L     R1,4(R1)                DDNAME LIST                 7667
         CLI   1(R1),X'38'             LIST INCLUDE UT1            7667
         BNH   NOLIST                  NO                          7667
          CLI   58(R1),X'00'       SYSUT1 CHANGED                A34084
         BE    NOLIST                  NO                          7667
         MVC   UT1(8),58(R1)           MOVE NEW UT1 NAME           7667
NOLIST   CLI   IEHMVV39+5,X'FF'        FIRST WORKFILE ALLOCATED  A37897
         BE    BCKFROM                 NO-BYPASS SCRATCH         A37897
         L     R1,LOC16                PTR TO THE CVT            A37897
         L     R1,0(R1)                 LOAD PTR TO TCB PTRS
         L     R1,0(R1)                 LOAD TCB PTR
         L     R1,12(R1)                LOAD PTR TO TIOT
         LA    R1,24(R1)                LOAD PTR TO DD ENTRY IN TIOT
         IC    DBR1,0(R1)               PUT ENTRY BYTE CNT IN REGISTER
         LA    DBR3,UT1                 LOAD ADDR OF NAME SYSUT1
COMP     CLC   0(8,DBR3),4(R1)          COMPARE IT TO TIOT DDNAME ENTRY
         BE    FIND                     BRANCH IF FOUND
         AR    R1,DBR1                  BUMP REG BY LENGTH OF ENTRY
         IC    DBR1,0(R1)               INSERT LENGTH OF NEXT ENTRY
         LTR   DBR1,DBR1                IS IT ZERO
         BZ    RC2
         B     COMP                     LOOP AND SEARCH
FIND     ICM   R1,3,18(R1)              LOAD UCB PTR           @ZM30153
         N     R1,FOXES                 CLEAR HALF OF REG      @ZM30153
         TM    2(R1),X'FF'              IS THIS A 2321
         BC    1,NOT2321                BRANCH ON NO
         MVC   VOLLST+6(6),4(R1)        MOVE IN VOL SER FROM SUB UCB
         SR    R6,R6
         IC    R6,1(R1)                 BACK UP TO POINT TO MAIN
         SLL   R6,4                        UCB TO GET THE DEVICE TYPE
         LA    R6,56(R6)
         LR    R10,R1
         SR    R10,R6
         MVC   VOLLST+2(4),16(R10)
         B     THRUMOVE
NOT2321  MVC   VOLLST+2(4),16(R1)       MOVE TYPE CODE TO PARAM LIST
         MVC   VOLLST+6(6),28(R1)       MOVE VOL SERIAL TO PARAM LIST
THRUMOVE CLI   IEHMVV39+7,X'FF'        WAS SYSUT3 ALLOCATED      A37897
         BE    SCRUT2                  NO-BYPASS SCRATCH         A37897
         L     R6,IEHMVV42                                       A37897
         TM    DCBOFLGS,OPENED         WAS SYSUT3 OPENED         A50959
         BZ    NOFR3                   NO-SKIP FREEPOOL          A50959
         BALR  R10,COMMAREA       CLOSE
NOFR3    MVC   NEWNAME+9(8),IEHMVV87   MOVE TIME TO **SYSUT3   @ZA03372
         BAL   DBR1,SCRRTN             GO TO THE SCRATCH RTN     A37897
SCRUT2   CLI   IEHMVV39+6,X'FF'        WAS SYSUT2 ALLOCATED      A37897
         BE    SCRUT1                  NO-BYPASS SCRATCH         A37897
         L     R6,IEHMVV41             PTR TO UT2 DCB            A37897
         TM    DCBOFLGS,OPENED         WAS SYSUT2 OPENED         A50959
         BZ    NOFR2                   NO-SKIP FREEPOOL          A50959
         BALR  R10,COMMAREA            CLOSE UT2                 A37897
NOFR2    MVI   NEWNAME+7,X'F2'         MAKE NAME **SYSUT2      @ZA00101
         BAL   DBR1,SCRRTN             GO TO THE SCRATCH RTN     A37897
SCRUT1   MVI   NEWNAME+7,X'F1'         MAKE NAME=SYSUT1          A37897
         L     R6,IEHMVV40              UT1 DCB ADDR
         TM    DCBOFLGS,OPENED         WAS SYSUT1 OPENED         A50959
         BZ    NOFR1                   NO-SKIP FREEPOOL          A50959
         BALR  R10,COMMAREA       CLOSE
         FREEPOOL (R6)                 FREE SYSUT1 BUFFER        A50959
NOFR1    BAL   DBR1,SCRRTN             GO TO THE SCRATCH RTN     A50959
BCKFROM  EQU   *                                                 A50942
         TM    IEHMVV51+1,X'01'         TEST FOR ERROR FROM SCRARCH
         BO    ERR2                     BRANCH IF ERROR ENCOUNTERED
BCKFROM1 MVC   IEHMVV00(RENT2-RENT1),RENT1                       A37897
         L     R6,IEHMVV33             LOAD SYSPRINT ADDR
         TM    DCBOFLGS,OPENED         WAS SYSPRINT OPENED       A35452
         BZ    RC                      NO-SKIP FREEPOOL          A50959
         BALR  R10,COMMAREA       CLOSE
         FREEPOOL (R6)       FREE SYSPRINT BUFFER                A35452
RC       TM    IEHMVV20+2,X'01'    IS PERM ABORT BIT ON
         BZ    RC1                NO CONTINUE
RC2      OI    IEHMVV41+13,X'02'     RET CODE = 8
         OI    IEHMVV32,X'08'           SET RETURN CODE BYTE
RC1      SR    LINK,LINK
         IC    R7,IEHMVV41+13      LOAD RETURN CODE
SKIP1    LA    R1,3040                                             I276
         LR    R0,R1
         LR    R1,COMMAREA        PUT ADDR OF COMMAREA INTO REG1
      FREEMAIN R,LV=(0),A=(1)           FREE COMMAREA MAIN
       IEHPOST (14,1),T
         LR    15,R7              PUT RET CODE IN REG 15
        RETURN (14,12),RC=(15)
SCRRTN   SR    R0,R0                                               LRT
         SCRATCH  SCR                                              LRT
         LA    LINK,0(LINK)       CLEAR HIGH ORDER BYTE OF REG 15
         LTR   LINK,LINK          IS RETURN CODE ZERO
         BCR   8,DBR1                   BRANCH IF SUCCESSFUL RETURN
         SRL   LINK,3                   CHECK FOR A RETURN OF 8
         LTR   LINK,LINK
         BCR   8,DBR1                   BRANCH IF GOOD RETURN
*                                          FROM SCRATCH
         MVI   IEHMVV51+1,X'01'         INDICATE ERROR ENCOUNTERED
         BR    DBR1
MESSAD   DC    A(MESS)
MESS     CSECT
         USING MESS,R1
M1       DC    CL29'IEH381I ERROR ENCOUNTERED IN '                 7667
         DC    CL21'SCRATCHING WORK FILES'                         7667
IEHMVSSK CSECT
ERR2     DS    0H
    L  R1,MESSAD
         MVI   IEHMVV00,X'40'          CLEAR PRINT AREA         PTM3039
         MVC   IEHMVV00+1(120),IEHMVV00                         PTM3039
         MVC   IEHMVV00+1(50),M1                                   7667
         OI    IEHMVV41+13,X'04'       RET CODE=12               A29950
         LR    DBR1,COMMAREA                                       7667
         LINK  EP=IEHMVESU
         B     BCKFROM1                 RETURN TO WRAP UP        A50942
LOC16    EQU   16
OPENED   EQU   X'10'                                             A35452
BASE     EQU   8
R0       EQU   0
R1       EQU   1
R2       EQU   2
DBR1     EQU   3
DBR3     EQU   4
DBCOMPR  EQU   5
COMMAREA EQU   12
SAVEAREA EQU   13
RETURN   EQU   14
LINK     EQU   15
R6       EQU   6
R7       EQU   7
R9       EQU   9
R10      EQU   10
R11      EQU   11
SCR     CAMLST SCRATCH,NEWNAME,,VOLLST,,OVRD                     A30903
NEWNAME  DC    CL44'**SYSUT3.'                                 @ZA00101
         DS    0H
VOLLST   DC    XL2'01'
         DC    XL12'00'
         DS    0F
FOXES    DC    X'0000FFFF'              CLEAR HIGH BYTES       @ZM30153
UT1      DC    CL8'SYSUT1'
         EJECT
         DCBD                                                    A35452
         IEHMVV
         END
