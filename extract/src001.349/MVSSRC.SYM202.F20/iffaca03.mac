*   NAME
*        DISATN
*
*   MODLUE NAME
*        IFFACA03
*
*   FUNCTION
*        TO CAUSE GSP TO DISREGARD SUBSEQUENT OCCURRANCE OF
*        ATTENTIONS ON THE DESIGNATED SOURCE(S) WHEN THE
*        ASSOCIATED ATTENTION LEVEL IS ACTIVE.
*
*   ENTRY
*        CSECT NAME
*
*   INPUT
*        REGISTER 1 CONTAINS THE ADDRESS OF A WORK/SAVE AREA.
*        THE FIRST WORD CONTAINS THE ADDRESS OF A PARAMETER
*        LIST CONTAINING.
*              4. ADDRESS OF A VARIABLE CONTAINING THE ADDRESS
*                 OF THE ATTENTION LEVEL(ATTNLEVEL).
*              2. ADDRESS OF ATTENTION SOURCE(S) TO BE DISABLED
*                 (ATTNSOURCE).
*                 1-31   PROGRAM FUNCTION KEYS 1-31
*                 32     END KEY
*                 33     CANCEL KEY
*                 34     LIGHT PEN
*                 35     EOS
*                 36     ASYNCHRONOUS ERROR
*
*                 TO GIVE INDIVIDUAL SOURCES, GIVE A NUMBER.
*                 TO GIVE SOURCES 5 TO 10, FOR EXAMPLE, 5 IS
*                 POSITIVE AND 10 IS NEGATIVE.
*
*   OUTPUT
*        RESET INDICATED ATTENTION SOURCE BIT(S) IN THE
*        ATTENTION TABLE ENABLED SOURCE MASKS.
*
*   EXIT
*        RETURN TO CALLING ROUTINE VIA REGISTER 14.
*
*   RETURN CODES
*        4,1   INVALID ATTENTION LEVEL
*        4,N   INVALID SOURCE CODE
*
*   ATTRIBUTES
*        REENTRANT, PROBLEM PROGRAM MODE
*   FLOWCHARTS
*        CM, CN, CO
         TITLE 'DISATN - DISABLE ATTENTIONS'
REG0     EQU   0                       PARAM REG 0
REG1     EQU   1                       PARAM REG 1
RLIST    EQU   2                       INPUT PARAM LIST
RPARAM   EQU   3                       USER PARAM LIST
RATL     EQU   4                       ATTENTION LEVEL ADDR
RGTMCB   EQU   5                       GTMCB ADR
RWORKD   EQU   7                       WORK REG
RBASE    EQU   9                       BASE REG
RWORKC   EQU   11                      WORK REG
RSAVE    EQU   13                      SAVE AREA REG
RWORKA   EQU   14                      WORK REG
RWORKB   EQU   15                      WORK REG
RSOURCE  EQU   2             CONVERTA  ATTNSOURCE ADR REG
RFIRST   EQU   3             CONVERTA  FIRST ARG REG
RINFO    EQU   4             CONVERTA  ADD INFO REG
RPFA     EQU   5             CONVERTA  PF REG
RANA     EQU   6             CONVERTA  A/N REG
RA       EQU   7             CONVERTA  WORK REG A
RB       EQU   8             CONVERTA  WORK REG B
* RBASE  EQU   9                       BASEREG DUMMY
RC       EQU   10            CONVERTA  WORK REG C
RD       EQU   11            CONVERTA  WORK REG D
RE       EQU   12            CONVERTA  WORK REG E
ADD0     EQU   0                       ADD INFO 0
ADD1     EQU   1
ADD5     EQU   2                       ADD INFO EQUAL 2 FOR ENATTN
AID      EQU   X'04'                   ATTN LEVEL ID
GACBPLUS EQU   8                       OFFSET TO GACB ADDR
HASPFK   EQU   X'10'                   HAS PF KEYBOARD
HEX01    EQU   X'01'
HEX03    EQU   X'03'
IOK      EQU   X'7F'                   I/O SUCCESSFULL CODE
LAST     EQU   X'80'                   LAST PARAMETER
NEG      EQU   X'80'                   NEGATIVE VALUE
NEXT     EQU   4                       NEXT SOURCE
ONE      EQU   1
PFK0     EQU   X'80'                   PFK0 BIT
RCC4     EQU   X'08'                   RETURN CODE 4
RC4      EQU   16                      OFFSET TO RC4
RC5      EQU   X'04'                   I/O ERROR
ZERO     EQU   0                       ZERO
IFFACA03 CSECT
*2101,513000                                                   PTM 3051
*2101,513000
         SAVE  (14,12)                 SAVE REGISTERS
         BALR  RBASE,0                 SET UP BASE REG
         USING *,RBASE                 DEFINE BASE REG
         LR    RLIST,REG1              SAVE INPUT PARAM
         USING WORKAREA,RLIST          DEFINE DSECT
         L     RPARAM,PARAM            LOAD ADDR USER PARAM LIST
         USING PLIST,RPARAM            DEFINE DSECT
         ST    RSAVE,SAVE+4            MY SAVE ARE ADDR
         LA    RSAVE,SAVE              LOAD ADDR MY SAVE AREA
         ST    RSAVE,SAVE+8            SAVE USERS R13
         L     RWORKA,RCA              LOAD RETURN CODE ARRAY ADDR
         XC    ZERO(20,RWORKA),ZERO(RWORKA)  ZERO ARRAY
         B     AROUND
ERROR    EQU   *
*   RETURN CODE MUST BE LOADED INTO REG RWORKB (15)
*   ADD INFO MUST BE LOADED INTO REG RWORKC (R11)
*   OFFSET MUST BE LOADED INTO REG RWORKD (R12)
*   UPON ENTRY TO ERROR ROUTINE
         L     RWORKA,RCA              LOAD RETURN CODE ARRAY ADDR
         STC   RWORKB,ZERO(RWORKA)     STORE RETURN CODE
         ST    RWORKC,ZERO(RWORKD,RWORKA) STO ADD INFO AT PROPER OFFSET
OUT      L     RSAVE,SAVE+4            RE-ESTABLISH OLD SAVE AREA
         RETURN (14,12),T              RETURN TO CALLER
AROUND   TM    ATNLVL,LAST             LAST PARAM
         BC    14,REQ01                BRANCH IF NO
         LA    RWORKC,ADD0             LOAD ADD INFO
REQ03    LA    RWORKB,RCC4             LOAD RETURN CODE
         LA    RWORKD,RC4              LOAD OFFSET
         B     ERROR
REQ01    L     RATL,ATNLVL             LOAD INDIRECT PTR TO ATTN LEVEL
         L     RATL,ZERO(RATL)         LOAD ATTN LEVEL ADDR
         USING ATTNLEVL,RATL           DEFINE DSECT
         CLI   ATTNLVID+1,AID          IS THIS VALID ATTN LEVEL
         BNE   REQ40                   BRANCH IF NO ADD INFO EQUAL1
*   TEST FOR ANY ATTNSOURCES SPECIFIED
         L     RWORKC,ATNSRCE          LOAD ADDR OF DATA
         LA    RWORKC,ZERO(RWORKC)     ZERO HI ORDER BIT
         LTR   RWORKC,RWORKC           IS THERE AN ADDR THERE
         BZ    INFOADD0                NO
         BAL   RWORKA,CONVERTA         BRANCH TO ATTNSOURCE CONVERSION
         LTR   RWORKB,RWORKB           RC=0
         BZ    GOOD                    YES
         LR    RWORKC,RWORKB           LOAD ADD INFO
         B     REQ03
REQ40    EQU   *
         LA    RWORKC,ADD1             LOAD ADD INFO CODE OF 1
         B     REQ03
INFOADD0 EQU   *
*   NO ATTN SOURCE PARAMETER SPECIFIED
         LA    RWORKC,ADD5             LOAD ADD INFO CODE OF 2
         B     REQ03
GOOD     EQU   *
*   SAVE PF MASK FOR LATER TEST
         ST    REG1,PFMASK             SAVE PF MASK
*   SUBTRACT A/N AND P/F MASK FROM PERM ATLV MASKS
         X     REG0,MASK              *FLIP AN MASK
         X     REG1,MASK              *FLIP PF MASK
         ST    REG0,AN1               *
         ST    REG1,PF1               *
         NC    ATTNPFEN(4),PF1        *
         NC    ATTNANEN(4),AN1        *
         NC    ATTNPFDE(4),PF1        *
         NC    ATTNANDE(4),AN1        *
         CLC   ATTNFWPT,ZERO1         *IS LEVEL ACTIVE
         BNE   RETCODE0               *
*   PUT NEW ATLV PERM MASKS IN GACB2 AND GACB3
         ST    RATL,SAVE1              SAVE ATTN LVL ADDR
AGAIN    CLI   ATTNBPID,HEX01          ATBL BP POINT TO GTMCB
         BE    LBPTR                   YES
         L     RATL,ATTNBKPT           LOAD BACKWARD PTR INTO BASE REG
         B     AGAIN
LBPTR    L     RGTMCB,ATTNBKPT         LOAD BASE
         USING GTMCB,RGTMCB            DEFINE DESECT
         L     RATL,SAVE1              RESTORE ATTN LVL ADDR
*   REPLACE AN AND PF MASKS IN ATTN LVL OF GACB 3 AND2
         L     RWORKA,GTMGACBA         LOAD GACB ADR
         LA    RWORKA,GACBPLUS(RWORKA) LOAD PTR TO TRUE START OF GACB
         MVC   8(4,RWORKA),ATTNPFEN   *
         MVC   12(4,RWORKA),ATTNANEN  *NEW ANMASK INTO ML GACB PLUS 12
         L     RWORKA,GTMGACBA        *LOAD GACB ADDR
         L     RWORKA,ZERO(RWORKA)    *SET PTR TO START OF DE GACB %2<
         LA    RWORKA,GACBPLUS(RWORKA) LOAD PTR TO TRUE START OF GACB*
         MVC   8(4,RWORKA),ATTNPFDE    NEW PFK MASK INTO DE    PTM 3051
*                                       GACB + 8               PTM 3051
         MVC   12(4,RWORKA),ATTNANDE  *NEW ANMASK INTO DE GACB PLUS 12
         TM    GTMFEATS+1,HASPFK       DOES DEVICE HAVE A PF KEYBOARD
         BC    14,OUT                  BRANCH-NO
         L     RWORKB,PFMASK          *LOAD PF MASK
         LTR   RWORKB,RWORKB          *ANY PF KEYS DISABLED
         BZ    RETCODE0               *NO
         CLI   ATTNLITE,HEX03
         BE    TURNON
*   SET PF LITES ON AS PER ENABLED KEYS
         CLI   ATTNLITE,HEX01          SYS DEFAULT OPTION IN GTMCB
         BNE   RETCODE0
         CLI   GTMLIGHT,HEX03          LIGHT ENABLED KEYS
         BNE   RETCODE0                NO
*   TURN PF LIGHTS ON
TURNON   EQU   *
         XC    DECB(36),DECB           CLEAR I/O AREA
         L     RWORKD,GTMGRDCB         LOAD DCB ADDR
         LA    RWORKC,DATA             LOAD DATA ADDR
         LA    REG1,DECB               LOAD PARM LIST ADDR
         MVC   DATA(4),ATTNPFEN        PUT KEY MASK INTO DATA (LIGHTS)
         GCNTRL (1),IND,(7),(11),MF=E  TURN LIGHTS ON
         WAIT  ECB=(1)
         CLI   DECB,IOK                I/O SUCCESSFULL
         BE    OUT                     BRANCH-I/O OK
         L     REG1,RCA                LOAD RTN CODE ARRAY ADR
         OI    ZERO(REG1),RC5          INDICATE I/O ERROR
         B     OUT                     RETURN TO CALLER
RETCODE0 EQU   *
         B     OUT                     RETURN TO CALLER
         LA    RWORKC,ADD0             LOAD ADD INFO
         LA    RWORKB,ADD0             LOAD RETURN CODE
         LA    RWORKD,RC4              LOAD OFFSET
         B     ERROR
         EJECT
CONVERTA SAVE  (2,12)                  SAVE REGS
         LA    RSOURCE,ATNSRCE         LOAD ADR OF 1ST ARG
         LA    RFIRST,NEXT             INDICATE 1ST ARG
         SR    RPFA,RPFA               CLEAR PF REG
         SR    RANA,RANA               CLEAR A/N REG
         LA    RINFO,ADD5              LOAD 1ST ADD INFO CODE
         B     REQ18                   BRANCH
REQOK    SR    RWORKB,RWORKB           ZERO RETURN CODE
         LR    REG0,RANA               LOAD A/N MASK
         LR    REG1,RPFA               LOAD P/F MASK
         B     REQ19                   BRANCH
REQERR   LR    RWORKB,RINFO            LOAD RETURN CODE
REQ19    RETURN (2,12)                 RETURN
REQ18    L     RC,ZERO(RSOURCE)        LOAD ADR OF ARG
         TM    ZERO(RC),NEG            ARG NEGATIVE
         BC    1,REQNEG                BRANCH IF NEG
         L     RB,ZERO(RC)             LOAD VALUE OF ARG
REQ24    CL    RB,MAX                  VALUE GREATER THAN ALLOWABLE
         BC    2,REQERR                BRANCH IF YES
REQ23    CL    RB,ANSIZE               VALUE GREATER THAN 31
         BC    2,REQ20                 BRANCH IF YES
         L     RE,PRIME                PRIME REG
         SRL   RE,ZERO(RB)             POSITION BIT
         OR    RPFA,RE                 ADD TO PF MASK
REQ26    TM    ZERO(RC),NEG            ARG NEG
         BC    1,REQ21                 BRANCH IF YES
REQ22    TM    ZERO(RSOURCE),LAST      MORE ARGS
         BC    1,REQOK                 BRANCH IF NO
         L     RA,ZERO(RC)             SAVE VALUE PRESENT ARG
         LA    RSOURCE,NEXT(RSOURCE)   STEP TO NEXT ARG
         SR    RFIRST,RFIRST           INDICATE NOT 1ST ARG
         LA    RINFO,ONE(RINFO)        INCREMENT ADD INFO
         B     REQ18                   BRANCH TO MORE ARGS
REQ21    LA    RE,ONE                  LOAD CONSTANT
         SR    RB,RE                   DECREMENT ARG BY 1
         CLR   RA,RB                   VALUE=PREVIOUS VALUE
         BE    REQ22                   BRANCH IF YES
         B     REQ23                   BRANCH
REQNEG   LTR   RFIRST,RFIRST           FIRST ARG
         BC    7,REQERR                BRANCH IF YES
         LTR   RA,RA                   PREV ARG NEG
         BC    4,REQERR                BRANCH IF YES
         L     RB,ZERO(RC)             LOAD VALUE OF ARG
         LPR   RB,RB                   MAKE ARG POS
         CLR   RB,RA                   VALUE GREATER THAN PREV ARG
         BC    2,REQ24                 BRANCH IF YES
         B     REQERR                  BRANCH
REQ20    LA    RD,END                  LOAD ADR OF CONSTANT
         LA    RE,ONE                  LOAD CONSTANT
REQ27    CL    RB,ZERO(RD)             COMPARE
         BE    REQ25                   BRANCH IF YES
         SLL   RE,ONE                  SHIFT BIT
         LA    RD,NEXT(RD)             LOAD NEXT CONSTANT ADR
         B     REQ27                   BRANCH
REQ25    OR    RANA,RE                 ADD BIT TO MASK
         B     REQ26                   BRANCH
         SPACE 3
*   CONSTANTS
*   END,LP,EOS,CANCEL,MAX,ANSIZE FIELDS MUST BE IN SEQUENCE
END      DC    A(32)
LP       DC    A(34)
EOS      DC    A(35)
CANCEL   DC    A(33)
MAX      DC    A(36)
ANSIZE   DC    A(31)
PRIME    DC    X'80000000'             PF PRIME VALUE
ZERO1    DC    1F'0'
MASK     DC    X'FFFFFFFF'            *
         EJECT
WORKAREA DSECT
PARAM    DS    1F
RCA      DS    1F
ANMASK   DS    1F                      SAVE AREA FOR ANMASK
PFMASK   DS    1F                      SAVE AREA FOR PFMASK
SAVE     DS    18F
DECB     DS    8F                      I/O DECB
SAVE1    DS    1F
DATA     DS    1F                      DATA AREA FOR IO
AN1      DS    1F                     *
PF1      DS    1F                     *
         SPACE 3
PLIST    DSECT
ATNLVL   DS    1F
ATNSRCE  DS    1F
ATNFLAG  DS    BL1                     ATTNDATA SWITCH
         EJECT
         COPY  ATTNLEVL
         COPY  GTMCB
         COPY  GDSCB
         COPY  GSPCB
         END
