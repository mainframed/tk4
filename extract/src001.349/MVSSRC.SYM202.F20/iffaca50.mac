*****************************     STATUS TABLE   **********************
*  ROUTINE     TABLE              NUMBER
*              DISPLACEMENT       ASSIGNED
*  INDEV       0                  1
*  TMDEV       12                 2
*  INGDS       24                 3
*  TMGDS       36                 4
*  CRATL       48                 5
*  ENATL       60                 6
*  ENATN       72                 7
*  DSATN       84                 8
*  MPATL       96                 9
*  MLPEO       108                10
*  SLPAT       120                11
*  MLITS       132                12
*  RQATN       144                13
*  EURATL      156                14
*  SETATN      168                15
*  SALRM       180                16
*  GSPRD       192                17
*  RCURS       204                18
*  ICURS       216                19
*  SDATM       228                20
*  SGRAM       240                21
*  SDATL       252                22
*  SGDSL       264                23
*  SSCIS       276                24
*  SCHAM       288                25
*  PLINE       300                26
*  PPNT        312                27
*  PSGMT       324                28
*  PTEXT       336                29
*  STPOS       348                30
*  MVPOS       360                31
*  BGSEQ       372                32
*  BGSUB       384                33
*  ENSEQ       396                34
*  ENSUB       408                35
*  LKSUB       420                36
*  INCL        432                37
*  OMIT        444                38
*  EXEC        456                39
*  RESET       468                40
*  IDPOS       480                41
*  FSMOD       492                42
*  STEOS       504                43
*  ORGDS       516                44
*  LOCPN       528                45
*  BGTRK       540                46
*  RDTRK       552                47
*  ENTRK       564                48
*  DFSTR       576                49
*  PLSTR       588                50
*  ORGEN       600                51
*  CNVRT       612                52
*  ITRC        624                53
*  ITBP        636                54
*  RTBP        648                55
*  ITST        660                56
*  SPEC        672                57
*
*
*  0   ADDRESS IF ALREADY LOADED
*
*  4   LAST 4 BYTES OF NAME
*
*  8   X'01'-LINK   X'02'-LOAD
*
*  9-11 RSVD
*
*
R0       EQU   0                       PARAM REG 0
R1       EQU   1                       PARAM REG 1
RLIST    EQU   2                       WORK AREA ADR REG
RPARM    EQU   3                       PARAM LIST ADR REG
RGSPCB   EQU   4                       GSPCB ADR REG
RSTAT    EQU   5                       STATUS TABLE ADR REG
RPREV    EQU   6                       PREVIOUS ARG REG
RINDX    EQU   6                       INDEX REG
RPRES    EQU   7                       PRES ARG REG
RTAB     EQU   7                       TABLE ADR REG
RNO      EQU   8                       ROUTINE NUMBER REG
RMUL     EQU   8                       MULTIPLY REG
RBASE    EQU   9                       BASE REG
RCODE    EQU   10                      CODE REG
RB       EQU   11                      WORK REG
RC       EQU   12                      WORK REG
RSAVE    EQU   13                      SAVE AREA REG
RD       EQU   14                      WORK REG
RA       EQU   15                      WORK REG
LINKIT   EQU   X'01'                   LINK FLAG
LOADIT   EQU   X'02'                   LOAD FLAG
ADDR     EQU   0                       ROUTINE ADR
NAME     EQU   4                       ROUTINE NAME
FLAG     EQU   8                       ROUTINE FLAG
NEXT     EQU   4                       NEXT ENTRY
ZERO     EQU   0                       ZERO
LAST     EQU   X'80'                   LAST ARGUMENT
RCC4     EQU   X'08'                   RTN CODE 4
RC4      EQU   16                      OFFSET TO RC4 ADD INFO
ADD1     EQU   1                       ADD INFO 1
ADD2     EQU   2                       ADD INFO 2
ADD3     EQU   3                       ADD INFO 3
BACK     EQU   8                       BACK CHAIN
GID      EQU   X'01'                   GSPCB ID
ONEA     EQU   X'01'                   FLAG
NEG      EQU   X'80'                   SIGN BIT
IFFACA50 CSECT
*0831,816000-820000,832000-856000,868000-908000                    000A
         SAVE  (14,12)                 SAVE REGS
         BALR  RBASE,0                 SET UP BASE REG
         USING *,RBASE                 DEFINE BASE REG
         LR    RLIST,R1                LOAD WORKAREA ADR
         USING WORKAREA,RLIST
         L     RPARM,PLIST             LOAD ADR OF USER PARM LIST
         USING PARM,RPARM
         ST    RSAVE,SAVE+4            SAVE-SAVE AREA PTR
         LA    R1,SAVE                 LOAD ADR OF MY SAVE AREA
         ST    R1,BACK(RSAVE)          BACK CHAIN SAVE AREAS
         LR    RSAVE,R1                ESTABLISH NEW SAVE AREA
         L     RA,RCA                  LOAD RTN CODE ARRAY ADR
         XC    ZERO(20,RA),ZERO(RA)    ZERO ARRAY
         XC    RTNS(56),RTNS           INITIALIZE
         B     AROUND                  BRANCH
ERROR    L     RA,RCA                  LOAD RTN CODE ARRAY ADR
         STC   RB,ZERO(RA)             STORE TRN CODE
         ST    RC,ZERO(RD,RA)          STORE ADD INFO
OUTRTN   L     RSAVE,SAVE+4            RESTORE OLD SAVE AREA
         RETURN (14,12),T              RETURN TO CALLER
AROUND   TM    GSPN,LAST               LAST PARM
         BC    14,SET03                BRANCH NO
SET01    SR    RC,RC                   ZERO RC4 ADD INFO
SET02    LA    RB,RCC4                 LOAD RTN CODE
         LA    RD,RC4                  LOAD OFFSET TO ADD INFO
         B     ERROR                   BRANCH
SET03    L     RGSPCB,GSPN             LOAD INDIRECT PTR TO GSPCB
         L     RGSPCB,ZERO(RGSPCB)     LOAD GSPCB ADR
         USING GSPCB,RGSPCB
         CLI   GSPCBID,GID             IS THIS A GSPCB
         BE    SET04                   BRANCH-YES
         LA    RC,ADD1                 LOAD ADD INFO
         B     SET02                   BRANCH
SET04    L     RSTAT,GSPASTAT          LOAD STATUS TABLE ADDR
         TM    CODE,LAST               LAST PARAM
         BO    SET01                   BRANCH-YES
         L     RCODE,CODE              LOAD INDIRECT PTR TO CODE
         L     RCODE,ZERO(RCODE)       LOAD VALUE OF CODE
         C     RCODE,ONE               CODE=ONE
         BE    SET05                   BRANCH-YES
         C     RCODE,TWO               CODE=TWO
         BE    SET05                   BRANCH-YES
         LA    RC,ADD2                 LOAD ADD INFO
         B     SET02                   BRANCH
SET05    LA    RC,ADD3                 INITIALIZE ADD INFO
         LA    RB,ONE                  INDICATE FIRST ARG
         LA    RNO,RTNO                INITIALIZE REG
         LA    RA,RTNS                 INITIALIZE
SET06    L     RPRES,ZERO(RNO)         LOAD INDIRECT PTR
         L     RPRES,ZERO(RPRES)       LOAD ARG
         LTR   RPRES,RPRES             ARG ZERO OR NEGATIVE
         BZ    SET02                   BRANCH-ZERO
         BM    SETNEG                  BRANCH-MINUS
SET11    C     RPRES,MAX               NUMBER GREATER THAN 56
         BH    SET02                   BRANCH-YES
         S     RPRES,ONE               DECREMENT BY ONE
SET09    LA    RD,ONEA                 LOAD FLAG
         STC   RD,ZERO(RPRES,RA)       SET APPROPRIATE ROUTINE FLAG
         L     RD,ZERO(RNO)            LOAD PTR TO NUMBER
         TM    ZERO(RD),NEG            NUMBER NEGATIVE
         BO    SETNEGA                 BRANCH ON NEGATIVE NUMBER
SET08    TM    ZERO(RNO),LAST          ANY MORE ARGS
         BO    SET07                   BRANCH-NO MORE ARGS
         L     RPREV,ZERO(RD)          SAVE VALUE
         SR    RB,RB                   INDICATE WDT 1ST PASS
         LA    RC,ADD1(RC)             SETP ADD INFO
         LA    RNO,NEXT(RNO)           SETP TO NEXT ARG
         B     SET06                   BRANCH TO NEXT LOOP
SETNEGA  S     RPRES,ONE               DECREMENT NUMBER
         CR    RPREV,RPRES             VALUE=PREVIOUS ARG
         BH    SET08                   BRANCH-YES
         B     SET09                   BRANCH
SETNEG   LTR   RB,RB                   1ST ARG
         BZ    SET10                   BRANCH-NO
         B     SET02                   BRANCH
SET10    LTR   RPREV,RPREV             PREVIOUS ARG NEGATIVE
         BM    SET02                   BRANCH-YES
         LPR   RPRES,RPRES             MAKE ARG POSITIVE
         CR    RPRES,RPREV             VALUE GREATER THAN PREVIOUS ARG
         BH    SET11                   BRANCH-YES
         B     SET02                   BRANCH
SET07    SR    RINDX,RINDX             INITIALIZE REG
         LA    RTAB,RTNS               INITIALIZE TABLE REG
SET13    SR    RB,RB                   ZERO REG
         IC    RB,ZERO(RTAB,RINDX)     LOAD FLAG
         LTR   RB,RB                   FALG=0
         BC    7,SET12                 BRANCH-FLAG NOT ZERO
SET14    LA    RINDX,ADD1(RINDX)       INCREMENT INDEX
         C     RINDX,MAX               END OF TABLE
         BE    OUTRTN                  BRANCH-END
         B     SET13                   CONTINUE LOOP
SET12    LR    RMUL,RINDX              PRIME MULTIPLY REG
         MH    RMUL,TWELVE+2           DETERMINE STAT TABLE OFFSET
         C     RCODE,TWO               CODE=TWO
         BE    SET15                   BRANCH=TWO
         AR    RMUL,RSTAT          ADDR OF ENTRY                   000A
         OI    FLAG(RMUL),ADD2     SET LOAD FLAG                   000A
         NI    FLAG(RMUL),X'FF'-ADD1 TURN OFF LINK FLAG            000A
         B     SET14                   BRANCH TO TEST END OF LOOP
SET15    SR    RB,RB                   CLEAR REG
         AR    RMUL,RSTAT          ADDR OF ENTRY                   000A
         TM    FLAG(RMUL),ADD2     LOAD FLAG                       000A
         BO    SET16               BRANCH LOAD FLAG                000A
SET18    OI    FLAG(RMUL),ADD1     SET LINK FLAG                   000A
         NI    FLAG(RMUL),X'FF'-ADD2 TURN OFF LOAD FLAG            000A
         B     SET14                                               000A
SET16    L     RB,ADDR(RMUL)       LOAD ADDR FIELD OF STATUS TABLE 000A
         LTR   RB,RB                   ADR FLD 0
         BZ    SET18                   BRANCH-ZERO
         TM    FLAG(RMUL),X'04'    MULTIPLE ENTRY                  000A
         BZ    SET165              NO                              000A
         SR    RB,RB               ZERO REG                        000A
         IC    RB,FLAG+1(RMUL)  PICK UP ENTRY NO OF OTHER ENTRY PT 000A
         MH    RB,TWELVE+2         COMPUTE DISPLACEMENT            000A
         AR    RB,RSTAT                                            000A
         TM    FLAG(RB),ADD2       LOAD                            000A
         BO    SET18               YES-DO NOT DELETE MODULE        000A
         XC    ADDR(4,RB),ADDR(RB) ZERO ADDR                       000A
         TM    FLAG(RB),X'08'      THIS ONE LOADED                 000A
         BZ    SET165              NO                              000A
         XI    FLAG(RB),X'08'      TURN OFF FLAG                   000A
         L     R0,NAME(RB)         PICK UP NAME                    000A
         B     SET166                                              000A
SET165   XI    FLAG(RMUL),X'08'    TURN OFF FLAG                   000A
         L     R0,NAME(RMUL)       PICK UP NAME                    000A
SET166   XC    ADDR(4,RMUL),ADDR(RMUL) ZERO ADDR FIELD             000A
         ST    R0,DEL+4            STORE NAME                      000A
         MVC   DEL(4),PRIME        MOVE IN PREFIX                  000A
         LA    R0,DEL                                              000A
         DELETE EPLOC=(0)          DELETE                          000A
         B     SET18                                               000A
         DS    0F
ONE      DC    A(1)
TWO      DC    A(2)
MAX      DC    A(56)
TWELVE   DC    A(12)
BYPASS   DC    A(156)
PRIME    DC    CL4'IFFA'
         CNOP  0,8
PARM     DSECT
GSPN     DS    1F
CODE     DS    1F
RTNO     DS    1F
         COPY  GSPCB
WORKAREA DSECT
PLIST    DS    1F
RCA      DS    1F
SAVE     DS    18F
RTNS     DS    56X
DEL      DS    2F
         END
