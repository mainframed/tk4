* TITLE:FSMOD-FORCE MODE
* STATIS:CHANGE LEVEL 0
* FUNCTION:THIS ROUTINE WILL CAUSE THE NEXT CALL TO A DATA GENERATION
*          ROUTINE TO SET THE MODE BEFORE PLACING ORDERS IN THE BUFFER
*          REGARDLESS OF WHAT THE CURRENT MODE SETTING WAS.
* ENTRY:ENTERED AT IFFAFA14 VIA A CALL TO FSMOD.
* INPUT:GR#1 CONTAINS THE ADDRESS OF A WORK/SAVE AREA.THE FIRST WORD
*       CONTAINS THE ADDRESS OF A PARAMETER LIST CONSTRUCTED AS FOLLOWS
*       1.GDSNAME-A VARIABLE WHICH CONTAINS THE ADDRESS OF THE GDSCB.
* OUTPUT:NONE TO CALLER.GDSGRMOO FIELD OF GDSCB IS SET TO ZERO.
* EXIT:RETURN TO CALLER VIA REGISTER 14.
* RETURN CODES:4,1 INVALID GDSCB
*              0,0 SUCCESSFULL
* ATTRIBUTES:RE-ENTRANT,PROBLEM PROGRAM MODE.
* NOTES:THE FOLLOWING CONTROL BLOCKS ARE EXAMINED BY THIS ROUTINE:
*       1.GDSCB
R0       EQU   0                       PARM REG 0
R1       EQU   1                       PARM REG 1
RLIST    EQU   2                       LIST REG
RPARAM   EQU   3                       PARAM REG
RGDSCB   EQU   4                       GDSCB ADR REG
RA       EQU   5                       WORK REG
RB       EQU   6                       WORK REG
RC       EQU   7                       WORK REG
RD       EQU   8                       WORK REG
RBASE    EQU   9                       BASE REG
ADD1     EQU   1                       ADD INFO=1
ZERO     EQU   0                       ZERO
RCC4     EQU   16                      OFFSET
RC4      EQU   X'08'                   INVALID PARAM RTN CODE
GID      EQU   X'03'                   GDSCB ID
IFFAFA14 CSECT
         SAVE  (14,12)
         BALR  RBASE,0                 SET UP BASE REG
         USING *,RBASE                 DEFINE BASE REG
         LR    RLIST,R1                SAVE REG
         USING WORKAREA,RLIST          DEFINE DSECT
         L     RPARAM,PLIST            LOAD ADR USER PARM LIST
         USING PARAM,RPARAM            DEFINE DSECT
         L     RA,RCA                  LOAD RTN CODE ARRAY ADR
         XC    ZERO(20,RA),ZERO(RA)    INITIALIZE ARRAY
         B     AROUND                  BRANCH
ERROR    L     RA,RCA                  LOAD RTN CODE ARRAY ADR
         STC   RB,ZERO(RA)             STORE RTN CODE
         ST    RC,ZERO(RA,RD)          STORE ADD INFO
OUTRTN   RETURN (14,12),T              RETURN
AROUND   L     RGDSCB,GDSNAME          LOAD INDIRECT PTR TO GDSCB
         LA    RGDSCB,ZERO(RGDSCB)     ZERO HI ORDER BYTE
         L     RGDSCB,ZERO(RGDSCB)     LOAD PTR TO GDSCB
         USING GDSCB,RGDSCB            DEFINE DSECT
         CLI   GDSGDSID,GID            IS THIS A VALID GDSCB
         BE    F1                      BRANCH IF YES
         LA    RB,RC4                  LOAD RET CODE
         LA    RC,ADD1                 LOAD ADD INFO
         LA    RD,RCC4                 LOAD OFFSET
         B     ERROR                   BRANCH
F1       XC    GDSGRMOD(1),GDSGRMOD    CLEAR BYTE
         B     OUTRTN
         DS    0F
ZERO1    DC    1F'0'
         CNOP  0,8
WORKAREA DSECT
PLIST    DS    1F
RCA      DS    1F
PARAM    DSECT
GDSNAME  DS    1F
         COPY  GDSCB
         END
