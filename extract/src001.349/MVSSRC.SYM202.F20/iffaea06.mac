         TITLE 'IFFAEA06 - SSCIS - SET SCISSOR OPTION'
*STATUS: CHANGE LEVEL 000
*
*FUNCTION/OPERATION:  SETS GDSSCISS TO INDICATE THE SCISSORING OPTION
*        SELECTED BY THE USER FOR THE GDS.
*
*ENTRY POINTS: IFFAEA06
*
*INPUT:  REGISTER 1 CONTAINS THE ADDRESS OF A WORKAREA.  THE FIRST WORD
*        CONTAINS THE ADDRESS OF THE PARAMETER LIST AND THE SECOND,
*        THAT OF THE RETURN ARRAY.
*
*        THE PARAMETER LIST IS
*     +0 A(POINTER TO THE GDSCB)
*
*     +4 A(OPTION CODE)
*
*OUTPUT: NONE TO USER. GDSSCISS IN THE GDSCB IS SET APPROPRIATELY
*
*EXTERNAL ROUTINES: NONE
*
*EXITS-NORMAL AND ERROR: VIA RETURN MACRO TO THE DIRECTOR
*
*TABLES/WORKAREAS: GSP WORKAREA
*
*ATTRIBUTES: PROBLEM STATE, REENTRANT
*
IFFAEA06 CSECT
         SAVE  (14,12)
         BALR  OPBASE,0
         USING *,OPBASE           SET CSECT ADDR
         L     OPWORK,0(1)        GET PARAM LIST ADDR
         L     OPSLAVE,0(OPWORK)  GET POINYER TO GDSCB
         L     OPGDSCB,0(OPSLAVE) GET GDSCB ADDR
         USING GDSCB,OPGDSCB      SET GDSCB DSECT ADDRESSABILITY
         L     OPGSPCB,4(1)            GET ADDRESS OF THE GSPARRAY
         USING GSPARRAY,OPGSPCB        SET ADDRESSABILITY FOR THE
*                                      RETURN ARRAY AND NULL VARIABLE
         MVI   GSPARRAY,X'00'     INDICATE SUCCESSFUL COMPLETION
         C     OPGDSCB,GDSGDSCB        IS DATA SET NAME VALID
         BNE   OPBADGDS           NO
         L     OPTASK,4(OPWORK)   GET CODE ADDR
         L     OPTASK,0(OPTASK)        GET CODE
         CH    OPTASK,OP3         TEST FOR MODE OPTION
         BNE   OPTST2
         MVI   GDSSCISS,X'12'     SET SCISSOR MODE - NO SCISSORING
         B     OPRETRN
OPTST2   CH    OPTASK,OP2         TEST FOR MODE OPTION
         BNE   OPTST1
         MVI   GDSSCISS,X'06'     SET MODE - SCISSOR GRID - CONT GEN
         B     OPRETRN
OPTST1   CH    OPTASK,OP1         TEST FOR MODE OPTION
         BNE   OPTSTN1
         MVI   GDSSCISS,X'0A'     SET MODE - SCISSOR SCREEN, CONT GEN
         B     OPRETRN
OPTSTN1  CH    OPTASK,OPN1        TEST FOR MODE OPTION
         BNE   OPTSTN2
         MVI   GDSSCISS,X'09'     SET MODE - SCISS SCREEN, DISCONT GEN
         B     OPRETRN
OPTSTN2  CH    OPTASK,OPN2        TEST FOR MODE OPTION
         BNE   OPPRMERR
         MVI   GDSSCISS,X'05'     SET MODE - SCISSOR GRID, DISCONT GEN
         B     OPRETRN
OPBADGDS MVI   GSPARRAY,X'08'     INDICATE PARAM ERROR
         MVC   GSPARRAY+16(4),OP1      GDSCB NOT OPEN
         B     OPRETRN
OPPRMERR MVI   GSPARRAY,X'08'     INDICATE PARAM ERROR
         MVC   GSPARRAY+16(4),OP2      INVALID CODE
OPRETRN  RETURN (14,12),T
OP3      DC    H'3'
OP2      DC    H'2'
OP1      DC    H'1'
OPN1     DC    H'-1'
OPN2     DC    H'-2'
OPBASE   EQU   9
OPGSPCB  EQU   6
OPGDSCB  EQU   8
OPWORK   EQU   2
OPTASK   EQU   3
OPSLAVE  EQU   4
         COPY  GDSCB
         COPY  GSPCB
         END
