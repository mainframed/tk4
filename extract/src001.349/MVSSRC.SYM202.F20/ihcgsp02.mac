*TITLE         TERMINATE GSP PART 1
***********************************************************************
*                                                                     *
*TITLE              TMGSP(PART 1)  TERMINATE GSP                      *
*                                                                     *
*MODULE NAME        IHCGSP02                                          *
*ENTRY              TMGSP                                             *
*FUNCTION/OPERATION                                                   *
*  LINKS TO TMGSP PART 2. ON RETURN FROM PART 2, IT DELETES ANY       *
*  STROKE TABLES WHICH HAVE BEEN LOADED, THE STATUS TABLE, AND        *
*  PART 2 OF THE DIRECTOR. ALL GSP WORK AREA IS FREED.                *
*                                                                     *
*INPUT - REGISTER 1 CONTAINS A POINTER TO THE GSPLB ADDRESS           *
*                                                                     *
*OUTPUT- NONE TO CALLER                                               *
*                                                                     *
*EXTERNAL MACROS- DELETE, FREEMAIN, LINK                              *
*                                                                     *
*EXITS- RETURN TO CALLER VIA REGISTER 14                              *
*       IF ERROR RETURN, THE PARAMETER IS SET TO ZERO                 *
*                                                                     *
*WORKAREA- SEE WKAREA DSECT                                           *
*                                                                     *
*LIBRARY - FORTLIB                                                    *
*ATTRIBUTES- REENTRANT, RESIDENT WITH USERS PROGRAM.                  *
*                                                                     *
***********************************************************************
         EJECT
*
*REGISTER UTILIZATION
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R5       EQU   5
R6       EQU   6
R8       EQU   8
R9       EQU   9
R10      EQU   10
R13      EQU   13
R4       EQU   4
         EJECT
IHCGSP02 CSECT
         ENTRY TMGSP
TMGSP    SAVE  (14,12)
         BALR  R9,R0
         USING *,R9                    ESTABLISH CSECT ADDRESSABILITY
         L     R4,0(R1)                GET GSPCB PTR
         L     R6,0(R4)                GET GSPCB ADDR
         USING GSPCB,R6                ESTABLISH GSPCB ADDRESSABILITY
         L     R5,GSPAWORK             GET WORK AREA ADDR FROM GSPCB
         USING WRKSECT,R5              SET WORK AREA ADDRESSABILITY
         ST    R13,SVAREA+4            CHAIN SAVE AREAS
         LA    R2,SVAREA
         ST    R2,8(R13)
         LR    R13,R2                  SET SAVE AREA REG
         CLC   GSPSYSID(2),GSPVALID    IS CONTROL BLOCK VALID
         BE    TR1D3                   YES
         XC    0(4,R4),0(R4)           SET PARM TO ZERO IF INVALID
         L     R13,SVAREA+4
         B     TR2D3
TR1D3    L     R3,16                   GET CVT ADDR
         L     R3,CVTLINK(R3)          GET LINKLIB DCB ADDR
         XC    SUPPLIST(16),SUPPLIST
         LINK  EP=IFFAAA02,MF=(E,(6)),SF=(E,SUPPLIST)
*
*RETURN FROM TRMGSP PART 2
*
         L     R10,GSPSTRTB             ADDRESS OF STROKE TABLE
         MVC   STROKE(16),TABNAME       SET UP 'GSP  ' IN WORK AREA
TR1D4    LTR   R10,R10
         BNE   TR1D6                    BRANCH IF TABLES TO DELETE
         DELETE EP=IFFAHA12            DELETE STATUS TABLE
         DELETE EP=IFFAHA11            DELETE DIRECTOR PART 2
         XC    GSPCBID(1),GSPCBID
         L     R8,SVAREA+4              GET SVAREA ADDRESS BEFORE FREE
         FREEMAIN R,LV=1024,A=(6)      RELEASE ALL GSP WORK AREA
         LR    R13,R8                   ADDRESS OF SVAREA
TR2D3    RETURN (14,12),T
         RETURN (14,12),T
*
         EJECT
TR1D6    L     R2,CHAIN(R10)           SAVE CHAIN ADDRESS
         L     R3,ID(R10)
         CVD   R3,SCONVERT
         UNPK  STROKE+3(2),SCONVERT+6(2) STROKE EBCIDIC NAME
         OI    STROKE+4,SIGN            TURN OFF SIGN INDICATOR
         DELETE EPLOC=STROKE            DELETE STROKE TABLE
         LR    R10,R2                  SET UP TO DELETE NEXT
         B     TR1D4
         SPACE 6
*CONSTANTS AND DSECTS
*
CVTLINK  EQU   8
ID       EQU   0
CHAIN    EQU   4
GSPVALID DC    X'F201'
TABNAME  DC    CL16'GSP '
SIGN     EQU   X'F0'
         SPACE 2
WRKSECT  DSECT
SVAREA   DS    18F
SUPPLIST DS    4F
SCONVERT DS    D
STROKE   DS    D
         COPY  GSPCB
         END
