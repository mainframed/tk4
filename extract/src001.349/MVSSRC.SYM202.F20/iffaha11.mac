*TITLE         DIRECTOR PART2
*
*MODULE NAME   IFFAHA11
*
*INPUT         OUTPUT FROM DIRECTOR PART 2
*
*              REG1  ADDRESS OF USERS PARAMETER LIST
*
*              REG2  ADDRESS OF GSPCB
*
*              REG3  DISPLACEMENT NUMBER
*
*              REG9  ENTRY ADDRESS
*
*              REG13 UNCHANGED
*
*              REG14 UNCHANGED
*
*OUTPUT        NONE TO CALLER
*ERRORS        NONE
*
*FUNCTION      USES DISPLACEMENT NUMBER * 4 IN REFERENCING
*
*              STATUS TABLE.  IT WILL CALL, LOAD AND CALL, OR
*
*              LINK TO THE MODULE IDENTIFIED IN THE STATUS
*
*              TABLE ENTRY
*
*OUTPUT TO GSP
* SUBROUTINE   REG1  ADDRESS OF WORKAREA
*
*              FIRST WORD OF WORK AREA IS ADDRESS OF USERS PLIST
*
*              SECOND WORD IS ADDRESS OF GSP RETURN ARRAY
*
*              REG13  ADDRESS OF SAVE AREA
*
*              REG14 RETURN ADDRESS ( TO DIRECTOR 2)
*
*              REG15 ENTRY ADDRESS OF INVOKED ROUTINE
         EJECT
*REGISTER EQUATES******************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9BASE   EQU   9
R12      EQU   12                                                S21016
R13      EQU   13
R14      EQU   14                      RETURN ADDR REG           S21016
R15      EQU   15
         EJECT
IFFAHA11 CSECT
*D6000000,A                                                      S21016
*A598900,C599400                                                  M1097
         USING *,R9BASE
         USING GSPCB,R2
         TS    GSPBUSYS                SET  GSP BUSY
*
*NOTE:  CONDITION CODE NOT TESTED.  NEED FOR DIRECT ENTRY
*
*STRUCTURE WORK AREA
*
         L     R4,GSPAWORK             GET ADDRESS OF WORK AREA
         ST    R1,0(R4)                SET USER PLIST INTO FIRST WORD,
         LA    R1,GSPARRAY
         ST    R1,4(R4)                SET RETURN ARRAY INTO SECOND
*
*CHAIN AND ESTABLISH SAVE AREA (IN WORK AREA)
*
         ST    R13,USERSAVE+4(R4)
         LA    R5,USERSAVE(R4)
         ST    R5,8(R13)
         LR    R13,R5
*
*LOCATE MODULE IN STATUS TABLE
*
         L     R5,GSPASTAT             GET ADDRESS OF STATUS TABLE
         LR    R10,R3              SAVE DISPLACEMENT             S21016
         MH    R3,HCON3                USE DIFF NUMBR*3 FOR INDEX
         LR    R8,R3                    SAVE COPY OF STAT ID
         AR    R8,R5
         L     R15,0(R5,R3)            GET EP ADDR FROM STATAB
         LTR   R15,R15
         BZ    AHAD2010                EP IS ZERO, MODULE NOT LOADED
*
*CALL GSP SUBROUTINE
*
*R13 HAS CHAINED SAVE AREA
*R4 HAS WORK AREA ADDR, WORK AREA IS SET UP
*R15 HAS ENTRY POINT ADDRESS
*
AHAD2040 LR    R1,R4                   PUT WORK AREA DDDR INTO REG1
         CALL  (15)                    CALL GSP SUBROUTINE
         EJECT
*
*BACK FROM ROUTINE, SET TO EXIT TO USER
*
AHAD2050 CLI   GSPARRAY,R0
         BE    AHAD2055
         L     R5,GSPASTAT        ADDRESS OF STATUS TABLE        S21016
         USING STATTBL,R5          R5 BASE FOR STATUS TABLE      S21016
         SRL   R10,TWO            DIVIDE DISP BY FOUR            S21016
         MH    R10,H5              DISP NBR * 5 FOR NAME TBL     S21016
         LA    R11,TABLE           ADDR OF NAME TABLE            S21016
         AR    R11,R10             POINTS TO SUBRTN NAME -       S21016
         TM    GSPARRAY,X'04'     TEST FOR I/O ERROR             S21016
         BO    PD10               IT WAS                         S21016
PD20     EQU   *                                                 S21016
         TM    GSPARRAY,X'08'     TEST FOR PARAMETER ERROR       S21016
         BO    PD3                IT WAS                         S21016
PD30     EQU   *                                                 S21016
         TM    GSPARRAY,X'10'     TEST FOR STORAGE EXCEEDED      S21016
         BO    PD2                IT WAS                         S21016
         B     PD4                CHECK FOR TERM MSG             S21016
PD1      MVC   N0(FIVE,R6),N0(R11) SUBRTN NAME TO MSG            S21016
         WTO   MF=(E,(R1))         ISSUE WTO                     S21016
         BR    R7                 CONTINUE PROCESSING            S21016
* AT THIS POINT CONTINUE NORMAL PROCESSING                       S21016
PD0      EQU   *                   NORMAL PROCESSING             S21016
         L     R5,GSPNULLV
         L     R5,0(R5)                 PICK UP NULL VARIABLE
         LTR   R5,R5
         BNL   AHAD2051                 BRANCH IF PLUS OR ZERO
         MVI   GSPDUMP,DUMP
         LPR   R5,R5
AHAD2051 C     R5,SEVEN
         BL    AHAD2052                 BRANCH IF LESS THAN 7
         LA    R5,6                     OTHERWISE SET VALUE TO 6
AHAD2052 IC    R5,TAB(R5)               PICK UP CONVERSION FACTOR
         STC   R5,GSPABEND                 AND SAVE IT
         CLC   GSPARRAY(1),GSPABEND
         BNH   AHAD2100                 BRANCH IF ABEND
AHAD2055 L     R13,USERSAVE+4(R4)       GET USER SAVE AREA ADDRESS
         MVI   GSPBUSYS,X'00'          SET GSP NOT BUSY
         RETURN (14,12),T
         EJECT
PD2      EQU   *                                                 S21016
         LA    R7,PD4             ADDR OF NEXT                   S21016
         CLI   GSPARRAY+F,X'01'    UPDATE FAIL?                  S21016
         BE    PD5                 YES                           S21016
         CLI   GSPARRAY+F,X'02'    GDS SPACE UNAVAILABLE?        S21016
         BE    PD6                 YES                           S21016
         CLI   GSPARRAY+F,X'03'    BUFFER NOT AVAILABLE?         S21016
         BE    PD8                YES                            S21016
         CLI   GSPARRAY+F,X'04'   MAIN STORAGE EXCEEDED?         S21016
         BE    PD9                IT WAS                         S21016
         B     PD0                TAKE NO  ACTION                S21016
PD5      EQU   *                                                 S21016
         LA    R6,NAME2            ADDR OF NAME FLD IN MSG 2     S21016
         LA    R1,MSG2             POINTS TO IFF602I             S21016
         B     PD1                 GO ISSUE WTO                  S21016
*                                                                S21016
PD6      EQU   *                                                 S21016
         LA    R6,NAME3            ADDR OF NAME FLD IN MSG 3     S21016
         LA    R1,MSG3             POINTS TO IFF603I             S21016
         B     PD1                  GO ISSUE WTO                 S21016
*                                                                S21016
*                                                                S21016
PD8      EQU   *                                                 S21016
         LA    R6,NAME4            ADDR OF NAME FLD IN MSG 4     S21016
         LA    R1,MSG4             ADDR OF IFF604I               S21016
         B     PD1                 GO ISSUE WTO                  S21016
*                                                                S21016
PD9      EQU   *                                                 S21016
         LA    R6,NAME5            POINTS TO NAME FLD IN MSG 5   S21016
         LA    R1,MSG5             POINTS TO IFF605I             S21016
         B     PD1                 GO ISSUE WTO                  S21016
*                                                                S21016
PD10     EQU   *                                                 S21016
         LA    R1,MSG7             ADDR OF IFF607I               S21016
         LA    R6,NAME7            PTS TO MSG7 IN STAT TBL       S21016
           LA    R7,PD20          ADDR OF NEXT CHECK             S21016
         B     PD1                GO ISSUE WTO                   S21016
         EJECT
PD3      EQU   *                                                 S21016
         SR    R6,R6               ZERO CONVERT REGISTER         S21016
         IC    R6,GSPARRAY+NINTN   INSERT FAILING ARG NBR        S21016
         CVD   R6,ARG(R2)         CONVERT ARG TO DECIMAL         S21016
         LA    R1,ARG(R2)         ADDRESS OF MOVE FIELD          S21016
         LA    R6,ARG1(R2)        ADDRESS OF MOVE FROM FIELD     S21016
         UNPK  N0(FOUR,R1),N0(TWO,R6)  UNPACK ARGUMENT NUMBER    S21016
         LA    R6,ARG3(R2)        ADDRESS OF UNPACKED ARG NBR    S21016
         OI    N0(R6),X'F0'       MOVE IN ZONE                   S21016
* PRINTABLE ARG NBR IN FORM FN FN NOW AT ARG+2                   S21016
         LA    R6,ARGNBR           POINT TO ARG PARM IN MSG6     S21016
         LA    R10,ARG4(R2)       ADDRESS OF ARG FOR MOVE        S21016
         MVC   N0(TWO,R6),N0(R10) MOVE ARG NBR TO MSG            S21016
         LA    R6,NAME6            POINT TO NAME FLD             S21016
         LA    R1,MSG6             POINT TO IFF606I              S21016
         LA    R7,PD30            ADDR OF NEXT                   S21016
         B     PD1                 GO ISSUE WTO                  S21016
PD4      EQU   *                   CODE TO TEST NULL VS RET CODE S21016
         SR    R1,R1               ZERO REG                      S21016
         SR    R6,R6               ZERO REG                      S21016
         TM    GSPARRAY,X'04'     I/O ERROR?                     S21016
         BO    RCEQ5              IT WAS                         S21016
         TM    GSPARRAY,X'08'     PARAMETER ERROR?               S21016
         BO    RCEQ4              YES                            S21016
         TM    GSPARRAY,X'10'     RET CODE = 3?                  S21016
         BO    RCEQ3              IT WAS                         S21016
         TM    GSPARRAY,X'40'      SCISSORING USED?              S21016
         BO    RCEQ1               YES                           S21016
         TM    GSPARRAY,X'20'      SCALING ERROR?                S21016
         BO    RCEQ2               YES                           S21016
         B     PD0                IRRELEVENT RET CODE - NA ACTIONS21016
*                                                                S21016
*                                                                S21016
RCEQ1    EQU   *                                                 S21016
         LA    R1,ONE              LOAD SCISSORING CODE          S21016
         B     CHKNULL             CHECK NULL VALUE              S21016
*                                                                S21016
*                                                                S21016
RCEQ2    EQU   *                                                 S21016
         LA    R1,TWO              SCALING ERROR CODE            S21016
         B     CHKNULL             CHECK NULL VALUE              S21016
*                                                                S21016
*                                                                S21016
*                                                                S21016
*                                                                S21016
RCEQ3    EQU   *                                                 S21016
         LA    R1,TH               LOAD 3                        S21016
         B     CHKNULL             GO COMPARE WITH NULL          S21016
*                                                                S21016
RCEQ5    EQU   *                                                 S21016
         LA    R1,FIVE             ASSUME I/O ERROR - LOAD 5     S21016
         B     CHKNULL             GO COMPARE WITH NULL          S21016
RCEQ4    EQU   *                                                 S21016
         LA    R1,FOUR             LOAD 4                        S21016
CHKNULL  EQU   *                   THIS CODE MASSAGES NULL       S21016
         L     R10,GSPNULLV        ADDR OF NULL VARIABLE         S21016
         L     R10,N0(R10)         LOAD ACTUAL NULL              S21016
         LPR   R10,R10             MAKE POSITIVE                 S21016
         CLR   R10,R1              COMPARE NULL AND RET CODE     S21016
         BH    PD0                BRANCH - NO TERMINATION MSG    S21016
* RETCODE IS IN R1; NULL IS IN R10                               S21016
PD7      EQU   *                                                 S21016
         O     R1,PRTMASK          INSERT ZONE                   S21016
         O     R10,PRTMASK         INSERT ZONE                   S21016
         STC   R1,RETCODE          STORE RET CODE                S21016
         STC   R10,NULL1           STORE NULL VALUE              S21016
         LA    R6,NAME1           ADDRESS OF NAME 1              S21016
         MVC   N0(FIVE,R6),N0(R11) MOVE SUBRTN NAME TO MSG       S21016
         LA    R1,MSG1             POINT TO IFF601I              S21016
         WTO   MF=(E,(R1))         ISSUE WTO                     S21016
         B     PD0                 CONTINUE "NORMAL" PROCESSING  S21016
         EJECT
*
*MODULE NOT IN STORAGE.  TEST FOR LOAD AND CALL OR LINK.
*
AHAD2010 L     R7,16                   GET CVT ADDRESS
         L     R7,CVTLINK(R7)          GET LINKLIB DCB ADDR
*
*MOVE 4 CHARACTERS OF MODULE NAME PAST IFFA FIELD IN STATUS TABLE
*
         L     R6,N4(R3,R5)            GET CHARACTERS FROM STATAB
         ST    R6,MODNAME+4(R5)        CONCATENATE WITH IFFA FIELD
         LA    R3,N0(R5,R3)            ADD STATAB ADDR AND STATAB INDEX
         TM    N8(R3),X'01'        LINK                            000A
         BO    AHAD2060            YES                             000A
*
*MODULE TO BE LOADED AND CALLED
*
         LR    R1,R7                   PUT LINKLIB DCB IN REG 1
         LA    R0,MODNAME(R5)          LOAD MODULE NAME ADDR
         LOAD  EPLOC=(0)
         LR    R15,R0                  PUT EP ADDRESS IN R15
         ST    R15,0(R3)               PPUT EP ADDR IN STATAB
         TM    N8(R3),X'04'        ANOTHER ENTRY PT                000A
         BZ    AHAD2015            NO                              000A
         SR    R1,R1                                               000A
         IC    R1,9(R3)            ENTRY NUMBER                    000A
         MH    R1,H12              DISP IN STATUS TABLE            000A
         LH    R7,10(R3)           DISP INTO MODULE                000A
         AR    R7,R15              COMPUTE ENTRY PT                000A
         ST    R7,0(R1,R5)         PLACE ENT PT IN OTHER MODULE    000A
AHAD2015 OI    N8(R3),X'08'        SET SWITCH-LOAD MODULE          000A
         B     AHAD2040                BRANCH TO CALL SUBROUTINE
*
*MODULE TO BE LINKED TO
*
AHAD2060 LA    R6,MODNAME(R5)          TRANSFER MODULE NAME
         XC    SUPPLIST(16,R4),SUPPLIST(R4)
         LINK  EPLOC=(6),MF=(E,(4)),SF=(E,SUPPLIST(4))
         B     AHAD2050                GO TO EXIT TO USER
         EJECT
*   RETURN CODE HAS EQUALED OR EXCEEDED NULL VALUE. ONE OF TWO ACTIONS
*   OCCURS NOW.
*
*          1. IF A USER ERROR EXIT HAS BEEN SPECIFIED IN THE CALL TO
*             INGSP, THE EXIT ROUTINE IS GIVEN CONTROL.  CONTROL DOES
*             NOT RETURN TO DIRECTOR II AFTER EXIT ROUTINE COMPLETION,
*             SINCE THE SAVE AND RETURN REGISTERS ARE THOSE PASSED TO
*             DIRECTOR II. CONTROL INSTEAD PASSES FROM EXIT ROUTINE TO
*             SUPERVISOR TO HIGHER LANGUAGE CALLING PROGRAM.
*
*          2. IF NO EXIT ROUTINE HAS BEEN SPECIFIED, THE TASK IS
*             TERMINATED.  A DUMP IS PROVIDED IF A NEGATIVE NULL VALUE
*             HAS BEEN SPECIFIED IN THE CALL  TO INGSP.
         SPACE 3
AHAD2100 L     R15,GSPAEXIT          IS ERROR EXIT SPECIFIED     S21016
         LTR   R15,R15                                           S21016
         BZ    AHAD2120                 IF NO, ABEND             S21016
         MVI   GSPBUSYS,X'00'           IF YES,SET GSP NOT BUSY   M1097
         L     R13,USERSAVE+4(R4)              AND RESTORE        M1097
         L     R14,RETURN(R13)                  REGISTERS        S21016
         LM    R0,R12,R0SAVE(R13)               FOR USER         S21016
         BR    R15                   PASS CONTROL TO EXIT RTN    S21016
         SPACE 3
AHAD2120 L     R1,CCODE              GET ABEND CODE 062          S21016
         CLI   GSPDUMP,DUMP
         BNE   AHAD2150                 BRANCH IF NO DUMP
         LA    R3,GSPARRAY              SET UP FOR ABEND
         LR    R2,R8                    ADDRESS OF STATUS TABLE ENTRY
         ABEND (1),DUMP
*
AHAD2150 ABEND (1)
         DS    0F
CCODE    DC    XL4'00062000'
         EJECT
GSPCB    DSECT
GSPGTMCB DS    F                   ADDRESS OF THE FIRST GTMCB
*                                  ON THE CHAIN
GSPDCTR2 DS    F                   ADDRESS OF PART 2 OF THE DIRECTOR
GSPSYSID DS    X                   BYTE TO SAY THIS IS A CONTROL BLK
*              X'F2'               SETTING
GSPCBID  DS    X                   ID TO SAY THIS IS A GSPCB
*              X'01'               SETTING
GSPBUSYS DS    X                   GSP BUSY SWITCH
*              X'00'               GSP NOT BUSY
*              X'FF'               GSP BUSY
GSPFLAGS DS    X                   ASSORTED FLAGS
*              X'01'               1=IN DE / 0=NOT IN DE
GSPDECNT DS    X                   COUNT OF DIRECT ENTRIES PENDING
GSPDUMP  DS    X
GSPABEND DS    X
GSPAWORK DS    F                   ADDRESS OF WORK AREA
GSPSTRTB DS    F                   ADDRESS OF THE STROKE TABLE
GSPARRAY DS    5F                  GSP RETURN ARRAY
GSPNULLV DS    F                   THE ADDRESS TO BE RECOGNIZED
*                                  AS THE ABSENCE OF A PARAMETER
GSPASTAT DS    F                   ADDRESS OF THE STATUS TABLE
GSPAEXIT DS    F             ADDR OF USER EXIT RTN, INGSP ARG 3  S21016
GSPCBLGN EQU   *-GSPCB                 LENGTH OF GSPCB
         EJECT
IFFAHA11 CSECT
N0       EQU   0
TWO      EQU   2                                                 S21016
TH       EQU   3                                                 S21016
FOUR     EQU   4                                                 S21016
N4       EQU   4
FIVE     EQU   5                                                 S21016
N8       EQU   8
CVTLINK  EQU   8
RETURN   EQU   12             USER RETURN REGISTER 14 DISPL      S21016
R0SAVE   EQU   20             USER REGISTER 0 DISPLACEMENT       S21016
R10      EQU   10                                                S21016
R11      EQU   11                                                S21016
TWLV     EQU   12                                                S21016
F        EQU   15                                                S21016
NINTN    EQU   19                                                S21016
ARG      EQU   32                                                S21016
ARG1     EQU   38                                                S21016
ARG3     EQU   35                                                S21016
ARG4     EQU   34                                                S21016
ONE      EQU   1                                                 S21016
         DS    0F                                                S21016
PRTMASK  DC    X'000000F0'                                       S21016
H5       DC    H'5'                                              S21016
DUMP     EQU   X'FF'
*
*THESE ASSUME A 1K WORKAREA
*
USERSAVE EQU   1020-GSPCBLGN-72
SUPPLIST EQU   1020-GSPCBLGN-104
*
*ASSUMES 66 TWELVE BYTE ENTRIES IN STATUS TABLE                  00000B
*
MODNAME  EQU   70*12+120
*
HCON3    DC    H'3'          FACTOR$DIFF NUM IS STATAB DISPLACEMENT
H12      DC    H'12'                                               000A
SEVEN    DC    F'7'
         SPACE 3
*CONVERSION TABLE FOR ABEND CODES
TAB      DC    X'80'
         DC    X'40'                    RETURN CODE 1   -SCISSOR
         DC    X'20'                                2   -SCALE
         DC    X'10'                                3   -STOR EXCEDED
         DC    X'08'                                4   -PARAMETER
         DC    X'04'                                5   -I/O ERROR
         DC    X'02'                                6   -
         EJECT
* THE FOLLOWING TABLE IS A LIST OF NAMES TO BE MOVED INTO WTO'S  S21016
* IN THE STATUS TABLE WHEN ERROR CONDITIONS ARE ENCOUNTERED; THE S21016
* TABLE IS, AND MUST REMAIN, IN THE SAME ORDER AS THE STATUS TABLS21016
TABLE    EQU   *                                                 S21016
         DC    C'INDEV'                                          S21016
         DC    C'TMDEV'                                          S21016
         DC    C'INGDS'                                          S21016
         DC    C'TMGDS'                                          S21016
         DC    C'CRATL'                                          S21016
         DC    C'ENATL'                                          S21016
         DC    C'ENATN'                                          S21016
         DC    C'DSATN'                                          S21016
         DC    C'MPATL'                                          S21016
         DC    C'MLPEO'                                          S21016
         DC    C'SLPAT'                                          S21016
         DC    C'MLITS'                                          S21016
         DC    C'RQATN'                                          S21016
         DC    C'     '            TAKES UP SPACE IN TABLE       S21016
         DC    C'     '            TAKES UP SPACE IN TABLE       S21016
         DC    C'SALRM'                                          S21016
         DC    C'GSPRD'                                          S21016
         DC    C'RCURS'                                          S21016
         DC    C'ICURS'                                          S21016
         DC    C'SDATM'                                          S21016
         DC    C'SGRAM'                                          S21016
         DC    C'SDATL'                                          S21016
         DC    C'SGDSL'                                          S21016
         DC    C'SSCIS'                                          S21016
         DC    C'SCHAM'                                          S21016
         DC    C'PLINE'                                          S21016
         DC    C'PPNT '                                          S21016
         DC    C'PSGMT'                                          S21016
         DC    C'PTEXT'                                          S21016
         DC    C'STPOS'                                          S21016
         DC    C'MVPOS'                                          S21016
         DC    C'BSGEQ'                                          S21016
         DC    C'BGSUB'                                          S21016
         DC    C'ENSEQ'                                          S21016
         DC    C'ENSUB'                                          S21016
         DC    C'LKSUB'                                          S21016
         DC    C'INCL '                                          S21016
         DC    C'OMIT '                                          S21016
         DC    C'EXEC '                                          S21016
         DC    C'RESET'                                          S21016
         DC    C'IDPOS'                                          S21016
         DC    C'FSMOD'                                          S21016
         DC    C'STEOS'                                          S21016
         DC    C'ORGDS'                                          S21016
         DC    C'LOCPN'                                          S21016
         DC    C'BGTRK'                                          S21016
         DC    C'RDTRK'                                          S21016
         DC    C'ENTRK'                                          S21016
         DC    C'DFSTR'                                          S21016
         DC    C'PLSTR'                                          S21016
         DC    C'ORGEN'                                          S21016
         DC    C'CNVRT'                                          S21016
         DC    C'ITRC '                                          S21016
         DC    C'RTBP '                                          S21016
         DC    C'ITBP '                                          S21016
         DC    C'ITST '                                          S21016
         DC    C'SPEC '                                          S21016
* END OF TABLE                                                   S21016
         EJECT
STATTBL  DSECT                                                   S21016
STATSTRT DS    242F                LGTH OF STATUS TABLE UP TO MSGS21016
MSG1     DS    F                   MSG LGTH AND MCS FLAGS        S21016
         DS    8C                  IFF601I MSG ID + 1 BLANK      S21016
NAME1    DS    5C                  SUBRTN NAME                   S21016
         DS    13C                                               S21016
RETCODE  DS    C                   RETURN CODE                   S21016
         DS    28C                                               S21016
NULL1    DS    C                   NULL VALUE                    S21016
         DS    C                   ADDL INFO                     S21016
         DS    F                   DESC AND ROUTING CODES        S21016
*                                                                S21016
*                                                                S21016
*                                                                S21016
MSG2     DS    F                   MSG LGTH AND MCS FLAGS        S21016
         DS    8C                  IFF602I MSG ID + 1 BLANK      S21016
NAME2    DS    5C                  SUBRTN NAME                   S21016
         DS    51C                                               S21016
         DS    F                   DESC AND ROUTING CODES        S21016
*                                                                S21016
*                                                                S21016
*                                                                S21016
MSG3     DS    F                   MSG LGTH AND MCS FLAGS        S21016
         DS    8C                  IFF603I MSG ID + 1 BLANK      S21016
NAME3    DS    5C                  SUBRTN NAME                   S21016
         DS    48C                                               S21016
         DS    F                   DESC AND ROUTING CODES        S21016
*                                                                S21016
         EJECT
*                                                                S21016
*                                                                S21016
MSG4     DS    F                   MSG LGTH AND MCS FLAGS        S21016
         DS    8C                  IFF604I MSG ID + 1 BLANK      S21016
NAME4    DS    5C                  SUBRTN NAME                   S21016
         DS    59C                                               S21016
         DS    F                   DESC AND ROUTING CODES        S21016
*                                                                S21016
*                                                                S21016
*                                                                S21016
MSG5     DS    F                   MSG LGTH AND MCS FLAGS        S21016
         DS    8C                  IFF605I MSG ID + 1 BLANK      S21016
NAME5    DS    5C                  SUBRTN NAME                   S21016
         DS    54C                                               S21016
         DS    F                   DESC AND ROUTING CODES        S21016
*                                                                S21016
*                                                                S21016
*                                                                S21016
MSG6     DS    F                   MSG LGTH AND MCS FIELDS       S21016
         DS    8C                  IFF606I MSG ID + 1 BLANK      S21016
NAME6    DS    5C                  SUBRTN NAME                   S21016
         DS    25C                ADDL INFO                      S21016
ARGNBR   DS    2C                  INVALID ARGUMENT NUMBER       S21016
         DS    8C                 ADDL INFO                      S21016
         DS    F                   DESC AND ROUTING CODES        S21016
         EJECT
*                                                                S21016
*                                                                S21016
*                                                                S21016
MSG7     DS    F                   MSG LGTH AND MCS FIELDS       S21016
         DS    8C                  IFF607I MSG ID + 1 BLANK      S21016
NAME7    DS    5C                  SUBRTN NAME                   S21016
         DS    48C                 ADDL INFO                     S21016
         DS    F                   DESC AND ROUTING CODES        S21016
*                                                                S21016
*                                                                S21016
*                                                                S21016
* END OF WTO DSECT                                               S21016
         END
