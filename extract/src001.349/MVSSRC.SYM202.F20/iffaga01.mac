         TITLE 'IFFAGA01 - LOCPN - LOCATE LIGHT-PEN'
*STATUS: CHANGE LEVEL 000
*
*FUNCTION/OPERATION:  ESTABLISHES NEW GACB TO HANDLE LIGHT-PEN
*        INTERRUPTS.  CALLS ORGEN TO INSERT AN ORDER LOOP TO BLAST THE
*        SCREEN WITH W'S IN THE SYSTEM GDS.  LOOP FOR ABOUT 1/2 SECOND.
*        IF NO LIGHT-PEN INTERRUPT OCCURS, REGENERATION IS STOPPED AND
*        RESTARTED AT THE FLOW CONTROL GSRT.  IF A LIGHT-PEN INTERRUPT
*        OCCURS, THE RASTER UNIT COORDINATES RETURNED BY ATTNINQ ARE
*        CONVERTED TO USER UNITS BY A CALL TO CNVRT. THE CONVERTED
*        COORDINATES ARE PLACED IN THE USER'S OUTPUT LOCATIONS. RESET
*        IS CALLED TO REMOVE THE ORDER LOOP FROM THE SYSTEM GDS AND
*        THE LIGHT-PEN GACB IS DISABLED.
*
*ENTRY POINTS: IFFAGA01
*
*INPUT:  REGISTER 1 POINTS TO A WORKAREA. THE FIRST WORD CONTAINS THE
*        ADDRESS OF THE PARAMETER LIST, AND THE SECOND, THAT OF THE
*        RETURN CODE ARRAY.  THE FIXED-LENGTH PARAMETER LIST IS
*    + 0 A(POINTER TO THE GDSCB)
*
*    + 4 A(X-POSITION VARIABLE)
*
*    + 8 A(Y-POSITION VARIABLE)
*
*OUTPUT: USER COORDINATES OF THE POINT WHERE THE LIGHT-PEN DETECT
*        OCCURED ARE RETURNED TO THE USER.
*
*EXTERNAL ROUTINES: ORGEN, EXEC, RESET, SPAR, DAR, ATTNINQ, GCNTRL
*                   ,CNVRT                                         3319
*
*TABLES/WORKAREAS: GSP WORKAREA
*
*ATTRIBUTES: PROBLEM STATE, REENTRANT
*
IFFAGA01 CSECT
*2102,294000-351000,615000                                         000A
*2102,591000                                                       2254
*2102,411400-421000    INSERTIONS MISFLAGGED FOR 22254             2283
*2102,021000-024000,285000-288000,540000-543000,564000             3319
*199400,269000,288000,473000,514000-515000,538400,573000         A42825
*573600-575400                                                   A42825
*A120000-128000,A310000-314500,C315000,318000,              D11 ZA15437
*A328000-332500,C333000,334000,A530000-534500,              D11 ZA15437
*C535000,536000,A566000-570500,C571000,574000               D11 ZA15437
LPN01    EQU   1                       REGISTER EQUATES-WORK REGISTERS
LPN02    EQU   2
LPN03    EQU   3
LPN04    EQU   4
LPN06    EQU   6                       REGISTER EQUATES-BASE REGISTERS
LPN07    EQU   7                        GRAPHIC DATA SET CONTROL   3319
*                                       BLOCK POINTER              3319
LPN08    EQU   8                        GSP ERROR ARRAY POINTER    3319
LPN09    EQU   9
LPN10    EQU   10
LPN11    EQU   11
LPN13    EQU   13                      REGISTER EQUATES-STANDARD
LPN14    EQU   14                      LINKAGE REGISTERS
LPN15    EQU   15
*
STAT     EQU   24                  RTNARRAY-TO STATABLE ADDRD11 OZ15437
ORGEN    EQU   X'0258'             STATAB ORGEN ENTRY DISPL D11 OZ15437
CONVERT  EQU   X'0264'             STATAB CNVRT ENTRY DISPL D11 OZ15437
EXEC     EQU   X'01C8'             STATAB EXEC  ENTRY DISPL D11 OZ15437
RESET    EQU   X'01D4'             STATAB RESET ENTRY DISPL D11 OZ15437
HSVA     EQU   4
MSVA     EQU   8
INCLUDE  EQU   1
X08      EQU   X'08'
LPN08A   EQU   8                       DISPLACEMENT TO BUFFER ADDRESS
L20      EQU   20                       LENGTH FOR MVC        LB A42825
IOERR    EQU   X'04'
ERRCOND  EQU   X'3C'                   ERROR MASK FOR GSP ERROR    3319
*                                       ARRAY                      3319
PSTCD    EQU   X'7F'
CHARW    EQU   56                       A BASIC CHARACTER WIDTH IN 2234
*                                       HEXADECIMAL UNITS          2234
CHARH    EQU   80                       A BASIC CHARACTER HIGHTH IN2234
*                                       HEXADECIMAL UNITS          2234
XRIGHT   EQU   4088                     THE RIGHT MOST CHARACTER   2234
*                                       CENTER POINT IN HEX UNITS  2234
*                                       ON X - AXIS                2234
YBOTTOM  EQU   12                       THE BOTTOM MOST CHARACTER  2254
*                                       CENTER POINT IN HEX UNITS  2254
*                                       ON Y - AXIS                2254
YTOP     EQU   4092                     THE TOP MOST CHARACTER     2254
*                                       CENTER POINT IN HEX UNITS  2254
*                                       ON Y - AXIS                2254
*
         SAVE  (14,12)
         BALR  LPN09,0                 ADDRESSABILITY FOR CSECT
         USING *,LPN09
*
         LR    LPN10,LPN01             ADDRESSABILITY FOR WORKAREA,
         USING  LOCPEN,LPN10
         LA    LPN02,LPNSAVE           CHAIN SAVE AREAS
         ST    LPN13,HSVA(LPN02)
         ST    LPN02,MSVA(LPN13)
         LR    LPN13,LPN02             ESTABLISH NEW SAVE AREA
         L     LPN11,LPNPARM           PARAMETER LIST,
         USING LPNPRMLT,LPN11
         L     LPN08,LPNRTNA           AND RETURN ARRAY
         USING GSPARRAY,LPN08
*
         L     LPN07,LPNGDS            GET ADDRESS OF GDSCB
         L     LPN07,0(LPN07)
         USING GDSCB,LPN07             ADDRESSABILITY FOR GDSCB
         C     LPN07,GDSGDSCB          VALID GDSCB
         BNE   LPN004                  NO, ERROR RETURN
*
         TM    LPNGDS,X'80'            MAKE SURE
         BO    LPN005
         TM    LPNXPOSO,X'80'          THE PARAMETER LIST
         BO    LPN005
         TM    LPNYPOSO,X'80'          IS 3    LOCATIONS
         BZ    LPN005                  IF NOT, ERROR RETURN
*
         L     LPN06,GDSGTMCB          ADDRESSABILITY FOR GTMCB
         USING GTMCB,LPN06
         MVC   LPNGACB(56),LPNMGACB    MOVE SKELETON GACB INTO
*                                      WORKAREA
         LA    LPN02,LPNGACB           GET POINTERS TO GACB,
         LA    LPN03,LPNCOM            COMMUNICATIONS AREA,
         L     LPN04,GTMGRDCB          AND DCB
         SAEC  DCB=(4),COMAREA=(3),MF=(E,(2))
         XC    LPNSUPP(16),LPNSUPP     ZERO SUPERVISOR WORK AREA
         LA    LPN03,LPNGACB
         LA    LPN02,LPNORGDS
         L     LPN01,LPN0002           STORE SPAR PARAMETER
         STH   LPN01,2(LPN02)          LIST LENGTH
         SPAR  ((3)),PRTY=126,MF=(E,(2))
*
         LTR   LPN15,LPN15              RETURN CODE ZERO           3319
         BNE   LPN006                   GO TO PARM ERR EXIT   LB A42825
*                                            BASIC FOUND AN ERROR  3319
         LA    LPN02,GTMSYGDS          GET ADDRESS OF POINTER TO
*                                      SYSTEM GDS
         ST    LPN02,LPNORGDS          STORE IN PARAMETER LIST FOR
*                                      CNVRT AND EXEC
         LA    LPN02,LPNLNGTH          POINTER TO BYTE COUNT
         ST    LPN02,LPNORCNT
         LA    LPN02,LPNORDRS          POINTER TO ORDERS
         ST    LPN02,LPNORORD
         MVC   LPNOREXT(4),GSPNULLV    SET UNUSED PARAMETERS TO
         MVC   LPNORCOR(4),GSPNULLV    TO NULL VARIABLE
         LA    LPN02,LPNKEY            STORE POINTER TO KEY
         ST    LPN02,LPNORKEY
         LA    LPN02,LPN0001           SET GENCODE FPR INCLUDE
         ST    LPN02,LPNORGCD
         OI    LPNORGCD,X'80'          END OF PARAMETER LIST FLAG
         LA    LPN02,LPNORGDS          SET UP POINTERS TO PARAMETER
         ST    LPN02,LPNORPRM          LIST
         MVC   LPNORRTN(4),LPNRTNA     AND THE RETURN ARRAY
         XC    LPNSUPP(16),LPNSUPP     ZERO SUPERVISOR WORK AREA
         LA    LPN01,LPNORPRM          SET UP CALL TO ORGEN
         L     LPN15,STAT(LPN08)   POINT TO STATUS TABLE    D11 ZA15437
         L     LPN15,ORGEN(LPN15)  AND ORGEN ENTRY POINT    D11 ZA15437
         LTR   LPN15,LPN15         ORGEN LOADED YET?        D11 ZA15437
         BZ    LPNLINK1            NO,MUST USE SYSTEM LINK  D11 ZA15437
         BALR  LPN14,LPN15         GO TO ORGEN              D11 ZA15437
         B     LPNRTN1             RETURN FROM BALR         D11 ZA15437
LPNLINK1 LA    LPN15,LPNSUPP                                D11 ZA15437
         LINK  EP=IFFAGA07,MF=(E,(1)),SF=(E,(15)) LNK ORGEN
*
LPNRTN1  TM    GSPARRAY,ERRCOND    WAS AN ERROR DETECTED    D11 ZA15437
         BNZ   LPDAR                   ERROR BRANCH           LB A42825
         OI    LPNORGDS,X'80'
         XC    LPNSUPP(16),LPNSUPP     ZERO SUPERVISOR WORK AREA
         LA    LPN01,LPNORPRM
         L     LPN15,STAT(LPN08)   POINT TO STATUS TABLE    D11 ZA15437
         L     LPN15,EXEC(LPN15)   AND TO EXEC ROUTINE EP   D11 ZA15437
         LTR   LPN15,LPN15         EXEC LOADED YET?         D11 ZA15437
         BZ    LPNLINK2            NO,MUST USE SYSTEM LINK  D11 ZA15437
         BALR  LPN14,LPN15         GO EXEC                  D11 ZA15437
         B     LPNRTN2             RETURN FROM BALR         D11 ZA15437
LPNLINK2 LA    LPN15,LPNSUPP                                D11 ZA15437
         LINK  EP=IFFAFA11,MF=(E,(1)),SF=(E,(15)) LINK EXEC
LPNRTN2  TM    GSPARRAY,ERRCOND    WAS AN ERROR DETECTED    D11 ZA15437
         BNZ   LPRESET                 BRANCH TO ERROR EXIT   LB A42825
*
LPN000   EQU   *                                                   000A
LPN001   EQU   *                                                   000A
         STIMER WAIT,DINTVL=LPN7HT                                 000A
         XC    LPNSUPP(16),LPNSUPP                                 000A
         XC    LFORM(64),LFORM          ZERO LIST                  000A
         LA    LPN02,LPNGACB                                       000A
         ATTNINQ (2),MODE=(C,LPN003),TYP=ANY,MF=(E,LFORM)          000A
         LA    LPN01,LPNSUPP           DECB
*              THE FOLLOWING TEST CAN BE MADE WHEN BASIC GRAPHICS  3319
*              IS CLEANED UP AND ATTENTION INQUIRY IS MADE TO      3319
*              RETURN A ZERO RETURN CODE FOR A CALL TO IT IF       3319
*              THERE IS NOTHING ON THE USERS ATTENTION QUEUE       3319
*        LTR   LPN15,LPN15              RETURN CODE ZERO           3319
*        BNE   LPN0085                  IF NOT TAKE BRANCH         3319
*                                            ATTNINQ FOUND AN ERROR3319
         L     LPN02,GTMGRDCB          DCB
         L     LPN03,GTMFCTBL          BUFFER ADDRESS
         LA    LPN03,LPN08A(LPN03)
*
         XC    LPNSUPP(32),LPNSUPP     ZERO DECB
         GCNTRL (LPN01),HLT,(LPN02),(LPN03),MF=E  STOP REGENERATION
         LTR   LPN15,LPN15              RETURN CODE ZERO           3319
         BNE   LPN0085                  IF NOT TAKE BRANCH         3319
*                                            GCNTRL FOUND AN ERROR 3319
         WAIT  ECB=LPNSUPP
         CLI   LPNSUPP,PSTCD           I/O SUCESSFUL
         BNE   LPN008                  NO, ERROR RETURN
*
         XC    LPNSUPP(32),LPNSUPP     ZERO DECB
         LA    LPN01,LPNSUPP
         GCNTRL (LPN01),STR,(LPN02),(LPN03),MF=E  RESTART REGENERATION
         LTR   LPN15,LPN15              RETURN CODE ZERO           3319
         BNE   LPN0085                  IF NOT TAKE BRANCH         3319
*                                            GCNTRL FOUND AN ERROR 3319
         WAIT  ECB=LPNSUPP
         CLI   LPNSUPP,PSTCD           I/O SUCESSFUL
         BNE   LPN008                  NO, ERROR RETURN
         B     LPN000                  YES, CONTINUE SCAN
*
*        LIGHT-PEN DETECT ON SCAN
         EJECT                                                     2254
*              THE HARDWARE PRESENTS A PROBLEM IN CHARACTER        2254
*              DETECTION IN THAT THE X AND Y COORDINATE VALUES     2254
*              RETURNED ARE THOSE OF THE NEXT CHARACTER CENTER     2254
*              TO BE DISPLAYED NOT THE ONE THAT THE LIGHTPEN       2254
*              DETECT IS ON. THUS THE FOLLOWING CODE WILL BACK     2254
*              UP THE X AND Y REGISTER VALUE TO THE CENTER OF      2254
*        THE CHARACTER THE DETECT WAS ON, THIS BACK UP             2254
*              TAKES INTO ACCOUNT ALSO THE JUMPING FROM LINE TO    2254
*              LINE AND BOTTOM TO TOP OF THE PAGE.                 2254
LPN003   LH    LPN02,LPNXPOS           GET COORDINATES OF
*                                       THE LIGHT - PEN INTERRUPT  2254
         LA    LPN04,CHARW              GET CHARCTER WIDTH TO      2254
         SR    LPN02,LPN04              SUBSTRACT FROM VALUE       2254
*                                       HARDWARE RETURNS SO CAN    2254
*                                       RETURN TO CALLER THE VALUE 2254
*                                       OF THE CHARACTER LIGHTPENED2254
         BNM   LPN0035                  IF THE VALUE IS NOW LESS   2254
*                                       THAN ZERO                  2254
         LA    LPN02,XRIGHT             SET X-AXIS VALUE TO RIGHT  2254
*                                       MOST CHARACTER CENTER      2254
         ST    LPN02,LPNXVAL            STORE X-AXIS VALUE         2254
         LH    LPN02,LPNYPOS            GET Y-AXIS COORDINATE OF   2254
*                                       CHARATER DETECTED ON       2254
         LA    LPN04,CHARH              UP VALUE OF Y-AXIS BY 80   2254
         AR    LPN02,LPN04          FOR NEXT LINE SINCE X VALUE WAS2254
*                                       LESS THAN ZERO MEANING THAT2254
*                                       THE DETECT WAS ON THE LAST 2254
*                                       CHARACHER OF THE LINE      2254
         LA    LPN04,YTOP               MAXIMUM Y-AXIS VALUE       2254
         CR    LPN04,LPN02              IS Y-AXIS VALUE ABOVE LIMIT2254
*                                       OF SCREEN                  2254
         BNL   LPN0036                  IF NOT TAKE BRANCH         2254
         LA    LPN02,YBOTTOM            SET Y-AXIS VALUE TO        2254
*                                       BOTTOM OF SCREEN           2254
         B     LPN0036                  GO SET Y-AXIS VALUE        2234
LPN0035  EQU   *
         ST    LPN02,LPNXVAL           THE LIGHT-PEN INTERRURT
         LH    LPN02,LPNYPOS
LPN0036  EQU   *                                                   2234
         ST    LPN02,LPNYVAL
         MVC   LPNCVGDS(4),LPNGDS      MOVE IN POINTER TO USER'S GDS
         LA    LPN02,LPN0001           CONVERT VARIABLE
         ST    LPN02,LPNCVCVT
         LA    LPN02,LPNXVAL           STORE POINTERS
         ST    LPN02,LPNCVXIN          TO INPUT VALUES
         LA    LPN02,LPNYVAL
         ST    LPN02,LPNCVYIN
         MVC   LPNCVXOT(8),LPNCVXIN    AND OUTPUT ADDRESSES
         OI    LPNCVYOT,X'80'
         LA    LPN02,LPNCVGDS          POINTER TO PARAMETER LIST
         ST    LPN02,LPNCVPRM
         MVC   LPNCVRTN(4),LPNRTNA
         LA    LPN01,LPNCVPRM
         XC    LPNSUPP(16),LPNSUPP
         L     LPN15,STAT(LPN08)   POINT TO STATUS TABLE    D11 ZA15437
         L     LPN15,CONVERT(LPN15) AND TO CONVERT EP       D11 ZA15437
         LTR   LPN15,LPN15         CONVERT LOADED YET?      D11 ZA15437
         BZ    LPNLINK3            NO,MUST USE SYSTEM LINK  D11 ZA15437
         BALR  LPN14,LPN15         GO CONVERT               D11 ZA15437
         B     LPNRTN3             RETURN FROM BALR         D11 ZA15437
LPNLINK3 LA    LPN15,LPNSUPP                                D11 ZA15437
         LINK  EP=IFFAGA08,MF=(E,(1)),SF=(E,(15)) CONVERT TO USRUNITS
LPNRTN3  TM    GSPARRAY,ERRCOND    WAS AN ERROR DETECTED    D11 ZA15437
         BNZ   LPRESET                 BRANCH TO ERROR EXIT   LB A42825
         LM    LPN02,LPN03,LPNXPOSO
         MVC   0(4,LPN02),LPNXVAL      STORE VALUES IN USER'S AREA
         MVC   0(4,LPN03),LPNYVAL
         LA    LPN03,LPDAR             NSI AFTER RESET DONE   LB A42825
*
LPN002   XC    LPNSUPP(16),LPNSUPP     ZERO SUPERVISOR AREA
         MVC   LPNRSGDS(4),LPNORGDS    GET POINTER TO GDS NAME
         MVI   LPNRSGDS,X'00'
         OI    LPNRSKEY,X'80'          LAST PARAMETER
         LA    LPN02,LPNRSGDS          POINTER TO PARAMETER LIST
         ST    LPN02,LPNRSPRM          FOR RESET
         MVC   LPNRSRTN(4),LPNRTNA
         LA    LPN01,LPNRSPRM
         L     LPN15,STAT(LPN08)   POINT TO STATUS TABLE    D11 ZA15437
         L     LPN15,RESET(LPN15)  AND TO RESET EP          D11 ZA15437
         LTR   LPN15,LPN15         RESET LOADED YET?        D11 ZA15437
         BZ    LPNLINK4            NO,MUST USE SYSTEM LINK  D11 ZA15437
         BALR  LPN14,LPN15         GO RESET                 D11 ZA15437
         B     LPNRET4             RETURN FROM BALR         D11 ZA15437
LPNLINK4 LA    LPN15,LPNSUPP                                D11 ZA15437
         LINK  EP=IFFAFA12,MF=(E,(1)),SF=(E,(15)) CALL RESET
* ADDRESS IN REG LPN03 DETERMINES IF GSPARRAY TO BE RESTORED  LB A42825
LPNRET4  BR    LPN03               CONTINUE PROCESSING      D11 ZA15437
*
LPDAR    EQU   *                       SET UP FOR DAR         LB A42825
         LA    LPN02,LPNGACB
         XC    LPNSUPP(16),LPNSUPP
         LA    LPN03,LPNSUPP
         L     LPN01,LPN0002
         STH   LPN01,2(LPN03)          DAR PARAMETER LIST LENGTH
         DAR   ((2)),MF=(E,(3))        ELIMINATE INTERRUPT HANDLING
*
         LTR   LPN15,LPN15              RETURN CODE ZERO           3319
         BNE   LPN006                  GO INDICATE PARM ERROR LB A42825
*                                            DAR FOUND AN ERROR    3319
         B     LPN007                   GO RETURN TO CALLER        3319
*
LPN004   MVC   GSPARRAY+16(4),LPN0001  BAD GDSCB
         B     LPN006
LPN005   XC    GSPARRAY+16(4),GSPARRAY+16   UNDETERMINED ORIGIN
LPN006   OI    GSPARRAY,X08            BAD PARAMETER
LPN007   L     LPN13,HSVA(LPN13)
         SR    LPN15,LPN15              SET RETURN CODE TO ZERO    3319
LPN0075  RETURN (14,12),T,RC=(15)                                  3319
*
LPN008   OI    GSPARRAY,IOERR          I/O ERROR
         B     LPRESET                 GO TO ERROR EXIT       LB A42825
LPN0085  EQU   *                                              LB A42825
         OI    GSPARRAY,X08            INDICATE PARM ERROR    LB A42825
LPRESET  EQU   *                                              LB A42825
* ERROR EXIT WHEN BOTH RESET,DAR MUST BE USED FOR CLEANUP     LB A42825
         MVC   LPNGSPAY(L20),GSPARRAY  SAVE GSP ARRAY         LB A42825
         BAL   LPN03,LPN002            GO INVOKE RESET        LB A42825
         MVC   GSPARRAY(L20),LPNGSPAY  RESTORE GSPARRAY       LB A42825
         B     LPDAR                   GO INVOKE DAR          LB A42825
*
LPNMGACB SAEC  EP=0,ATTNTYP=(R,LP),RESTART=NO
         GINIT BLC=0
LPNORDRS GSXY  LPNPOS
         GEPM  A
*                                       SET BEAM TO CENTER OF      2254
*                                       CHARACTER IN CENTER OF     2254
LPNPOS   GDV   2016,2072,U              SCREEN                     2254
         GESD
         GPDI
LPNTAG   GECP  B
         GTXT  'WWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW',0
         GCNOP C
         GTRU  LPNTAG
LPNLNGTH DC    A(LPNLNGTH-LPNORDRS)
         DS    0D                                                  000A
LPN7HT   DC    X'F0F0F0F0F0F0F5C0'                                 000A
LPN0002  DC    F'2'
LPN0001  DC    F'1'
*
LOCPEN   DSECT
LPNPARM  DS    F                       POINTER TO PARAMETER LIST
LPNRTNA  DS    F                       POINTER TO RETURN ARRAY
LFORM    DS    16F                                                 000A
LPNSAVE  DS    18F                     SAVE AREA
LPNSUPP  DS    8F
LPNGACB  DS    14F                     LIGHT-PEN ATTENTION GACB
LPNCOM   DS    4F                      COMUNICATIONS AREA
         ORG   LPNCOM+8
LPNXPOS  DS    H                       X AND Y
LPNYPOS  DS    H                       POSITION REGISTERS
         ORG   LPNCOM+16
LPNKEY   DS    F
LPNORGDS DS    F                       GDSNAME FOR ORGEN AND EXEC
LPNORORD DS    F                       POINTER TO ORDERS
LPNORCNT DS    F                       POINTER TO BYTE COUNT
LPNOREXT DS    F                       POINTER TO EXTERNAL REFERENCE
*                                      TABLE (SET TO 0)
LPNORCOR DS    F                       CORRVAL
LPNORKEY DS    F                       KEY
LPNORGCD DS    F                       GENCODE
LPNORPRM DS    F                       POINTER TO ORGEN AND EXEC
*                                      PARAMETER LIST
LPNORRTN DS    F                       POINTER TO RETURN ARRAY
*                                      FOR INTERNAL CALLS TO ORGEN
*                                      AND EXEC, REGISTER 1 WILL POINT
*                                      TO LPNORPRM
*
         ORG   LPNORGCD
LPNCVGDS DS    F                       GDSNAME FOR CNVRT
LPNCVCVT DS    F                       CONVERT VARIABLE (SET = 1)
LPNCVXIN DS    F                       POINTER TO INPUT AREAS
LPNCVYIN DS    F
LPNCVXOT DS    F                       POINTER TO OUTPUT AREAS
LPNCVYOT DS    F
LPNXVAL  DS    F                       POSITION STORAGE AREAS. USED FOR
LPNYVAL  DS    F                       INPUT AND OUTPUT FORM CNVRT
         DS    0D
LPNCVPRM DS    F                       POINTER TO CNVRT PARAMETER LIST
LPNCVRTN DS    F                       POINTER TO RETURN ARRAY
LPNGSPAY DS    5F                      SAVE GSPARRAY          LB A42825
*
         ORG   LPNOREXT
LPNRSGDS DS    F                       GDSNAME FOR RESET
LPNRSCOR DS    F                       CORRVAL (SET BY ORGEN)
LPNRSKEY DS    F                       KEY (SET BY ORGEN)
*                                      PARAMETER LIST FOR RESET
*                                      OVERLAYED OVER THAT FOR ORGEN
LPNRSPRM DS    F                       POINTER TO RESET PARAMETER LIST
LPNRSRTN DS    F                       POINTER TO RETURN ARRAY
*
LPNPRMLT DSECT
LPNGDS   DS    F                       POINTER TO GDSNAME
LPNXPOSO DS    F                       X AND Y POSITION
LPNYPOSO DS    F                       OUTPUT LOCATIONS
*
         COPY  GSPCB
         COPY  GTMCB
         COPY  GDSCB
*
         END
