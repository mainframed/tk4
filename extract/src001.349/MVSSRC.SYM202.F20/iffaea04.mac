         TITLE 'IFFAEA04 - SGDSL - SET GDS LIMITS'
*STATUS: CHANGE LEVEL 000
*
*FUNCTION/OPERATION: CONVERTS GDS LIMITS TO RASTER UNITS.  STORES THEM
*        IN THE GDS IN BOTH FLOATING-POINT AND INTEGER,  IF SDATL HAS
*        NOT BEEN CALLED,  THE DATA LIMITS ARE SET EQUAL TO THE
*        UNCONVERTED GDS LIMITS.
*
*ENTRY POINTS: IFFEA04
*
*INPUT: REGISTER 1 POINTS TO A WORKAREA. THE FIRST WORD CONTAINS THE
*        ADDRESS OF THE PARAMETER LIST AND THE SECOND, THAT OF THE
*        RETURN ARRAY.
*
*        REQUIRED PARAMETERS ARE
*    + 0 A(POINTER TO THE GDSCB)
*
*    + 4 A(LOWER-LEFT X GDS LIMIT)
*
*    + 8 A(LOWER-LEFT Y GDS LIMIT)
*
*    +12 A(UPPER-RIGHT X GDS LIMIT)
*
*    +16 A(UPPER-RIGHT Y GDS LIMIT)
*
*        OPTIONAL PARAMETERS (ALL MUST BE PROVIDED IF ANY ARE)
*    +20 A(LOWER-LEFT X SCREEN LIMIT)
*
*    +24 A(LOWER-LEFT Y SCREEN LIMIT)
*
*    +28 A(UPPER-RIGHT X SCREEN LIMIT)
*
*    +32 A(UPPER-RIGHT Y SCREEN LIMIT)
*
*OUTPUT: NONE TO USER. GDS LIMIT FIELDS ARE FILLED IN THE GDSCB. DATA
*        LIMIT FIELD MAY BE FILLED IN.
*
*EXTERNAL ROUTINES: IFFAHA15 (FLOATING-POINT TO INTEGER CONVERSION)
*
*EXITS-NORMAL AND ERROR: VIA RETURN MACRO TO DIRECTOR
*
*TABLES/WORKAREAS: GSP WORKAREA
*
*ATTRIBUTES: PROBLEM STATE, REENTRANT.
*
IFFAEA04 CSECT
         SAVE  (14,12)
         LR    SGL10,SGL01             ADDRESSABILITY FOR WORKAREA
         USING GLWORK,SGL10
         BALR  SGL09,0                 ADDRESSABILITY FOR CSECT
         USING *,SGL09
         LA    SGL02,GLWSAVE           GET ADDRESS OF SAVE AREA
         ST    SGL13,GLWSAVE+4         CHAIN SAVE AREAS
         ST    SGL02,MSVA(SGL13)
         LR    SGL13,SGL02             SET UP NEW SAVE AREA
*
         L     SGL11,GLWPARM           ADDRESSABILITY FOR PARAMETER
         USING SGLPARM,SGL11           LIST
*
         L     SGL08,GLWRTNA           ADDRESSABILITY FOR RETURN ARRAY,
         USING GSPARRAY,SGL08          NULL VARIABLE, STATUS TABLE
*
         L     SGL12,GLPGDSCB          GET POINTER TO GDSCB ADDRESS
         L     SGL12,0(SGL12)          ADDRESSABILITY FOR
         USING GDSCB,SGL12             GDSCB
*
         C     SGL12,GDSGDSCB          GDSCB OPEN
         BNE   SGL022                  NO, ERROR RETURN
*
         LA    SGL02,GSPNULLV          GET THE NULL VARIABLE
         SR    SGL03,SGL03             INITIALIZE COUNTER
         LA    SGL04,4                 INITIALIZE INCREMENT
         LA    SGL05,12                INITIALIZE COMPARAND
*
*        GET GRID LIMITS
*
SGL001   LA    SGL06,GLPGDSCB(SGL03)   GET ADDRESS OF PARAMETER
         TM    0(SGL06),X'80'          LAST PARAMETER
         BO    SGL021                  YES, ERROR
         BXLE  SGL03,SGL04,SGL001      CONTINUE CHECKING
*
         LM    SGL03,SGL06,GLPGLLX     GET ADDRESSES OF GRID LIMITS
         MVC   GLWGLLX(4),0(SGL03)     TRANSFER GRID
         MVC   GLWGLLY(4),0(SGL04)     LIMITS TO
         MVC   GLWGURX(4),0(SGL05)     WORKAREA
         MVC   GLWGURY(4),0(SGL06)
*
         TM    GLPGURY,X'80'           SCREEN PARAMETERS PASSED
         BO    SGL004                  NO, TAKE DEFAULT
*                                      YES, CHECK THEM FOR VALIDITY
         LA    SGL03,16                INITIALIZE COUNTER
         LA    SGL04,4                 INITIALIZE INCREMENT
         LA    SGL05,28                INITIALIZE COMPARAND
*
SGL002   LA    SGL06,GLPGDSCB(SGL03)   GET ADDRESS OF PARAMETER
         TM    0(SGL06),X'80'          LAST PARAMETER
         BO    SGL021                  YES, ERROR
         CLC   1(3,SGL02),5(SGL06)     NEXT PARAMETER = NULL
         BE    SGL003                  YES, CHECK FOR ERROR
         BXLE  SGL03,SGL04,SGL002
*
         LM    SGL02,SGL05,GLPSLLX     GET ADDRESS OF SCREEN LIMITS
         MVC   GLWSLLX(4),0(SGL02)     MOVE SCREEN
         MVC   GLWSLLY(4),0(SGL03)     LIMITS INTO
         MVC   GLWSURX(4),0(SGL04)     WORKAREA
         MVC   GLWSURY(4),0(SGL05)
*
         B     SGL008                  SET GDS LIMITS IN GDSCB
*
SGL003   C     SGL03,SGL0016           NULL ON FIRST SCREEN LIMIT
         BNE   SGL021                  NO, ERROR
         CLC   1(3,SGL02),GLPSLLY+1    YES, ARE ALL
         BNE   SGL021                  SCREEN LIMITS
         CLC   1(3,SGL02),GLPSURX+1    SET TO THE NULL VARIABLE
         BNE   SGL021                  NO, ERROR
         CLC   1(3,SGL02),GLPSURY+1    YES, TAKE DEFAULT FOR NO
         BNE   SGL021                  SCREEN LIMITS
*
SGL004   MVC   GLWSLLX(4),SGL0000      SET LOWER-LEFT SCREEN
         MVC   GLWSLLY(4),SGL0000      LIMITS = 0
         TM    GDSDATMD,REALX          CHECK UMODE
         BZ    SGL005                  INTEGER
         MVC   GLWSURX(4),SGL4095E     REAL-SET UPPER RIGHT LIMIT=4095
         B     SGL006
*
SGL005   MVC   GLWSURX(4),SGL4095      INTEGER X- SET UPPER-RIGHT LIMIT
SGL006   TM    GDSDATMD,REALY          CHECK VMODE
         BZ    SGL007
         MVC   GLWSURY(4),SGL4095E     SET UPPER-RIGHT
         B     SGL008                  LIMIT= 4095
SGL007   MVC   GLWSURY(4),SGL4095      IN PROPER MODE
*
SGL008   TM    GDSDATMD,REALX          CHECK MODE OF X'S
         BZ    SGL009                  INTEGER
         LE    SGL00F,GLWGURX          IS GURX VALID
         CE    SGL00F,GLWSLLX
         BNH   SGL021                  NO, ERROR
         LE    SGL00F,GLWGLLX          IS GLLX VALID
         CE    SGL00F,GLWSURX
         BNL   SGL021                  NO, ERROR
         B     SGL010                  CHECK Y LIMITS
*
SGL009   L     SGL02,GLWGURX           IS GURX VALID
         C     SGL02,GLWSLLX
         BNH   SGL021                  NO, ERROR
         L     SGL02,GLWGLLX           IS GLLX VALID
         C     SGL02,GLWSURX
         BNL   SGL021                  NO, ERROR
*
SGL010   TM    GDSDATMD,REALY          CHECK MODE OF Y'S
         BZ    SGL011                  INTEGER
         LE    SGL00F,GLWGURY          IS GURY VALID
         CE    SGL00F,GLWSLLY
         BNH   SGL021                  NO, ERROR
         LE    SGL00F,GLWGLLY          IS GLLY VALID
         CE    SGL00F,GLWSURY
         BNL   SGL021                  NO, ERROR
         B     SGL012
*
SGL011   L     SGL02,GLWGURY           IS GURY VALID
         C     SGL02,GLWSLLY
         BNH   SGL021                  NO, ERROR
         L     SGL02,GLWGLLY           IS GLLY VALID
         C     SGL02,GLWSURY
         BNL   SGL021                  NO, ERROR
SGL012   L     SGL02,GSPASTAT          SEE IF CONVERSION ROUTINE IN
         CLC   HA15(4,SGL02),SGL0000   CORE
         BNE   SGL012A                 YES, GET ADDRESS
         LOAD  EP=IFFAHA15             NO, LOAD IT
         ST    SGL00,HA15(SGL02)       STORE ADDRESS IN STAT TAB
SGL012A  MVC   GLWFIX(4),HA15(SGL02)   GET ADDRESS INTO WORKAREA
*
SGL013   TM    GDSDATMD,REALX          CHECK MODE OF X'S
         BZ    SGL015                  INTEGER
SGL014   LE    SGL00F,GLWSURX          COMPUTE SURX-SLLX
         SE    SGL00F,GLWSLLX
         BZ    SGL021                  IF DIFFERENCE = 0, ERROR
         LE    SGL02F,GLWGLLX          COMPUTE GLLX-SLLX
         SE    SGL02F,GLWSLLX
         ME    SGL02F,SGL4095E         SCALE GLLX COORDINATE
         DER   SGL02F,SGL00F
         STE   SGL02F,GLWGLLX          STORE SCALED VALUE
*
         LE    SGL02F,GLWGURX          COMPUTE GURX-SLLX
         SE    SGL02F,GLWSLLX
         ME    SGL02F,SGL4095E         SCALE GURX COORDINATE
         DER   SGL02F,SGL00F
         STE   SGL02F,GLWGURX          STORE SCALED VALUE
         B     SGL016
*
SGL015   LA    SGL01,GLWSLLX           CONVERT SLLX TO REAL
         BAL   SGL14,SGLFLOAT
         LA    SGL01,GLWSURX           CONVERT SURX TO REAL
         BAL   SGL14,SGLFLOAT
         LA    SGL01,GLWGLLX           CONVERT GLLX TO REAL
         BAL   SGL14,SGLFLOAT
         LA    SGL01,GLWGURX           CONVERT GURX TO REAL
         BAL   SGL14,SGLFLOAT
         B     SGL014                  SCALE COORDINATES IN REAL
*
SGL016   TM    GDSDATMD,REALY          CHECK MODE OF Y INPUT
         BZ    SGL018                  CONVERT TO REAL
SGL017   LE    SGL00F,GLWSURY          COMPUT SURY-SLLY
         SE    SGL00F,GLWSLLY
         BZ    SGL021                  IF SURY-SLLY, ERROR
         LE    SGL02F,GLWGLLY          COMPUTE GLLY-SLLY
         SE    SGL02F,GLWSLLY
         ME    SGL02F,SGL4095E         SCALE GLLY COORDINATE
         DER   SGL02F,SGL00F
         STE   SGL02F,GLWGLLY          AND STORE
*
         LE    SGL02F,GLWGURY          COMPUTE GURY-SLLY
         SE    SGL02F,GLWSLLY
         ME    SGL02F,SGL4095E         SCALE GURY COORDINATE
         DER   SGL02F,SGL00F
         STE   SGL02F,GLWGURY          AND STORE
         B     SGL019
*
SGL018   LA    SGL01,GLWSLLY           CONVERT SLLY TO REAL
         BAL   SGL14,SGLFLOAT
         LA    SGL01,GLWSURY           CONVERT SURY TO REAL
         BAL   SGL14,SGLFLOAT
         LA    SGL01,GLWGLLY           CONVERT GLLY TO REAL
         BAL   SGL14,SGLFLOAT
         LA    SGL01,GLWGURY           CONVERT GURY TO REAL
         BAL   SGL14,SGLFLOAT
         B     SGL017                  SCALE IN REAL
*
SGL019   L     SGL15,GLWFIX            GET REAL TO INTEGER CONVERSION
*                                      ROUTINE
         L     SGL01,GLWGLLX           CONVERT GLLX
         BALR  SGL14,SGL15             TO INTEGER
         LTR   SGL15,SGL15             TEST RETURN CODE
         BC    7,SGL021                REGISTER 15 NON-ZERO, ERROR
         ST    SGL01,GLWGLX            STORE
*
         L     SGL15,GLWFIX            CONVERT
         L     SGL01,GLWGLLY           GLLY
         BALR  SGL14,SGL15             TO
         LTR   SGL15,SGL15             INTEGER
         BC    7,SGL021                AND
         ST    SGL01,GLWGLY            STORE
*
         L     SGL15,GLWFIX            CONVERT
         L     SGL01,GLWGURX           GURX
         BALR  SGL14,SGL15             TO
         LTR   SGL15,SGL15             INTEGER
         BC    7,SGL021                AND
         ST    SGL01,GLWGUX            STORE
*
         L     SGL15,GLWFIX            CONVERT
         L     SGL01,GLWGURY           GURY
         BALR  SGL14,SGL15             TO
         LTR   SGL15,SGL15             INTEGER
         BC    7,SGL021                AND
         ST    SGL01,GLWGUY            STORE
*
         MVC   GDSXVILL(4),GLWGLX      STORE INTEGER
         MVC   GDSYVILL(4),GLWGLY      GRID LIMITS
         MVC   GDSXVIUR(4),GLWGUX      IN GDSCB
         MVC   GDSYVIUR(4),GLWGUY
*
         MVC   GDSXVRLL(4),GLWGLLX     STORE REAL
         MVC   GDSYVRLL(4),GLWGLLY     GRID LIMITS
         MVC   GDSXVRUR(4),GLWGURX     IN GDSCB
         MVC   GDSYVRUR(4),GLWGURY
*
         MVI   GSPARRAY,X'00'          SET NORMAL RETURN
         TM    GDSFLGS2,SDATLFLG       HAS SDATL BEEN CALLED
         BZ    SGL023                  NO, SET DATA LIMITS=GDS LIMITS
*
SGL020   L     SGL02,GDSAOACB          GET ADDRESS OF OACB
         TM    OACBFLGS(SGL02),NODATA  IS ANY DATA IN GDS
         BZ    SGL020B                 YES, EXIT
         SR    SGL02,SGL02             NO, SEE IF (0,0) IN GDS
         C     SGL02,GDSXVILL
         BL    SGL020A
         C     SGL02,GDSYVILL          IF IT IS, EXIT NORMALLY
         BNL   SGL020B
*
SGL020A  MVC   GDSXLAST(8),GDSXVILL    IF IT IS NOT IN THE GDS, SET
         MVC   GDSXCURR(2),GDSXVILL+2  INITIAL VALUES OF GDSXLAST,
         MVC   GDSYCURR(2),GDSYVILL+2  GDSYLAST, GDSXCURR, & GDSYCURR
         NI    GDSXCURR+1,TRUNC        TO THE LOWER-LEFT CORNER OF THE
         NI    GDSYCURR+1,TRUNC        GDS
*
SGL020B  L     SGL13,HSVA(SGL13)       GET ADDRESS OF SAVE AREA
         RETURN (14,12),T
*
*        PARAMETER ERROR
*
SGL021   MVI   GSPARRAY,PARERR         INDICATE PARAMETER ERROR
         XC    GSPARRAY+16(4),GSPARRAY+16 SET ADDITIONAL WORD = 0
         B     SGL020
*
SGL022   MVI   GSPARRAY,PARERR         INDICATE PARAMETER ERROR
         MVC   GSPARRAY+16,SGL0001     BAD GDSCB
         B     SGL020
*
SGL023   LM    SGL02,SGL05,GLPGLLX     GET ADDRESSES OF INPUT GDS LIMS
         MVC   GDSUVLLL(4),0(SGL02)    SET DATA LIMITS
         MVC   GDSVVLLL(4),0(SGL03)    EQUAL TO THE
         MVC   GDSUVLUR(4),0(SGL04)    INPUT GDS LIMITS
         MVC   GDSVVLUR(4),0(SGL05)
         B     SGL020
*
SGLFLOAT CLC   0(4,SGL01),SGL0000      NUMBER TO BE CONVERTED =0
         BCR   8,SGL14                 YES, IMMEDIATE RETURN
         MVC   GLWCONV(4),SGLCON       SET UP CONVERSION CONSTANT
         TM    0(SGL01),X'80'          IS NUMBER TO BE CONVERTED
         BO    SGLFLT2                 NEGATIVE. YES.
SGLFLT1A MVC   GLWCONV+4(4),0(SGL01)   NO, MOVE NUMBER INTO AREA
SGLFLT1  LD    SGL00F,GLWCONV          GET UNNORMALIZED NUMBER
         AD    SGL00F,SGL0000          AND NORMALIZE
         STE   SGL00F,0(SGL01)         STORE NORMALIZED NUMBER
         BR    SGL14                   RETURN
*
SGLFLT2  OI    GLWCONV,X'80'           SET SIGN BIT
         CLC   SGLMAXN(4),0(SGL01)     MAMIMUM NAGATIVE NUMBER
         BE    SGLFLT1A                YES, TREAT AS POSITIVE NUMBER
         L     SGL02,0(SGL01)          N3, GET MAGNITUDE
         LPR   SGL02,SGL02
         ST    SGL02,GLWCONV+4         STORE IN WORKAREA
         B     SGLFLT1                 AND NORMALIZE
*
*
*        CONSTANT AREA
*
SGL4095  DC    F'4095'
SGL4095E DC    E'4095'
SGLCON   DC    X'4E000000'
SGL0016  DC    F'16'
SGLMAXN  DC    X'80000000'
SGL0001  DC    F'1'
SGL0000  DC    D'0'
HSVA     EQU   4
MSVA     EQU   8
CVTLINK  EQU   8
CVTPTR   EQU   16
REALPARS EQU   X'CC'
REALX    EQU   X'C0'
REALY    EQU   X'0C'
PARERR   EQU   X'08'
SDATLFLG EQU   X'01'
NODATA   EQU   X'80'
OACBFLGS EQU   8
TRUNC    EQU   X'FC'
HA15     EQU   X'39C'                  STAT TAB DISPLACEMENT
*
*        REGISTER EQUATES
*
SGL00    EQU   0
SGL01    EQU   1
SGL02    EQU   2
SGL03    EQU   3
SGL04    EQU   4
SGL05    EQU   5
SGL06    EQU   6
SGL08    EQU   8
SGL09    EQU   9
SGL10    EQU   10
SGL11    EQU   11
SGL12    EQU   7
SGL13    EQU   13
SGL14    EQU   14
SGL15    EQU   15
SGL00F   EQU   0
SGL02F   EQU   2
SGLPARM  DSECT
GLPGDSCB DS    F
GLPGLLX  DS    F
GLPGLLY  DS    F
GLPGURX  DS    F
GLPGURY  DS    F
GLPSLLX  DS    F
GLPSLLY  DS    F
GLPSURX  DS    F
GLPSURY  DS    F
GLWORK   DSECT
GLWPARM  DS    F
GLWRTNA  DS    F
GLWSAVE  DS    18F
GLWSLLX  DS    F
GLWSLLY  DS    F
GLWSURX  DS    F
GLWSURY  DS    F
GLWGLLX  DS    F
GLWGLLY  DS    F
GLWGURX  DS    F
GLWGURY  DS    F
GLWFIX   DS    F
GLWCONV  DS    D
GLWGLX   DS    F
GLWGLY   DS    F
GLWGUX   DS    F
GLWGUY   DS    F
         COPY  GSPCB
         COPY  GDSCB
         END
