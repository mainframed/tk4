*TITLE         TERMINATE GSP PART 2
***********************************************************************
*                                                                     *
*TITLE              TMGSP(PART 2)       TERMINATE GSP                 *
*                                                                     *
*MODULE NAME        IFFAHA02                                          *
*                                                                     *
*ENTRY              IFFAHA02 - VIA LINK FOR TMGSP PART 1              *
*                                                                     *
*FUNCTION/OPERATION                                                   *
*   CALLS TMDEV, CAUSING ALL GRAPHIC DATA SETS AND DEVICES TO BE      *
*   TERMINATED. DELETES ALL LOADED GSP SUBROUTINES AS INDICATED IN    *
*   THE STATUS TABLE                                                  *
*                                                                     *
*INPUT- REGISTER 6 CONTAINS THE ADDRESS OF THE GSPCB                  *
*                                                                     *
*OUTPUT-NONE TO CALLER                                                *
*
*EXTERNAL MACROS - LINK, DELETE                                       *
*                                                                     *
*EXITS- BACK TO TMGSP PART 1 VIA REGISTER 14                          *
*                                                                     *
*WORKAREAS- SEE WKSECT DSECT                                          *
*                                                                     *
*ATTRIBUTES - REENTRANT                                               *
*                                                                     *
***********************************************************************
         EJECT
*
*REGISTER UTILIZATION
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R13      EQU   13
         EJECT
IFFAAA02 CSECT
         SAVE  (14,12)
         BALR  R11,R0
         USING *,R11                   ESTABLISH CSECT ADDRESSABILITY
         USING GSPCB,R6                ESTABLISH GSPCB ADDRESSABILITY
         L     R10,GSPAWORK            GET WORK AREA ADDR FROM GSPCB
         LA    R10,88(R10)             COMPUTE WORK AREA ADDR
         USING WRKSECT,R10             SET WORK AREA ADDRESSABILITY
         ST    R13,SVAREA+4            CHAIN SAVE AREAS
         LA    R2,SVAREA
         ST    R2,8(R13)
         LR    R13,R2                  SET SAVE AREA REG
         LA    R2,1                    INITIALIZE DEVICE SW
TR2C3    L     R3,GSPGTMCB             GET GTMCB ADDR
         LTR   R3,R3                   IS THERE A CHAIN
         BZ    TR2C4                   NO
TR2D3    L     R5,16
         L     R5,CVTLINK(R5)          GET LINKLIB DCB ADDR
         ST    R3,IGRAFD               PUT GTMCB ADDR IN VARIABLE
         LA    R4,IGRAFD
         ST    R4,PLIST                SETUP PARAM LIST FOR TMDEV
         LA    R4,PLIST                SIMULATE DCTR SETUP
         ST    R4,TRMDEV               SET UP PARAM FOR TRMDEV
         LA    R4,GSPARRAY             GET RETURN ARRAY ADDR
         ST    R4,TRMDEV+4             COMPLETE PLIST
         LA    R1,TRMDEV
         XC    SUPPLIST(16),SUPPLIST
         LINK  EP=IFFAAA04,MF=(E,(1)),SF=(E,SUPPLIST)
         B     TR2C3
TR2C4    L     R5,GSPASTAT             GET STATUS TABLE ADDR
         LR    R4,R5
         LA    R7,56                   SET LOOP CONTROL
TR2D4    L     R3,0(R5)                GET EP ADDR
         LTR   R3,R3                   IS MODULE LOADED
         BZ    TR2F4
         MVC   EPLOC+4(4,R4),4(R5)     COMPLETE MODULE NAME
         LA    R0,EPLOC(R4)            LOAD MODULE NAME ADDR
         DELETE EPLOC=(0)
TR2F4    LA    R5,12(R5)               GET ADDR OF NEXT STATAB ENTRY
         BCT   R7,TR2D4
TR2F4A   LTR   R2,R2                   ARE ALL MODULES DELETED
         BZ    TR2G4                   YES
         LA    R7,14                   RESET LOOP CONTROL
         SR    R2,R2                   INDICATE ALL DEVICES DELETED
         LA    R5,120(R5)
         B     TR2D4
TR2G4    L     R13,SVAREA+4
         RETURN (14,12),T
         COPY  GSPCB
*
*CONSTANTS AND DSECTS
*
EPLOC    EQU   70*12+120
CVTLINK  EQU   8
WRKSECT  DSECT
SVAREA   DS    18F
IGRAFD   DS    F
SUPPLIST DS    4F
PLIST    DS    F
TRMDEV   DS    10F
         END
