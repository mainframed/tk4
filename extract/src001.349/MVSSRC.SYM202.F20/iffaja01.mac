         TITLE 'ITRC-IFFAJA01-TEST RETURN CODE'
*   FUNCTION   RETURNS GSP RETURN CODE INFORMATION TO THE CALLER.
*
*   ENTRY POINT   CSECT NAME
*
*   INPUT   REGISTERS AS SET BY GSP DIRECTOR
*           PARAMETER LIST.
*              +0   GSPNAME
*              +4   CODE (INTEGER VARIABLE)
*                 WHERE CODE 1-5 TESTS FOR A RETURN CODE GREATER
*                    THAN OR EQUAL TO CODE.
*                 WHERE CODE 9 TESTS FOR ADDITIONAL INFORMATION.
*   OUTPUT   THE VALUE OF THE RETURN CODE OR ADDITIONAL INFORMATION
*               IS PLACED IN REGISTER 0.
*                  NOTE: REGISTER 0 IS SAVED AND RESTORED BY THE
*                        DIRECTOR. TO MODIFY REGISTER 0, THE VALUE
*                        TO BE RETURNED IS STORED INTO THE SAVE AREA.
*   ATTRIBUTES   NON-RESIDENT, REENTRANT
         SPACE 3
IFFAJA01 CSECT
D0       EQU   0
D1       EQU   1
D3       EQU   3
D4       EQU   4
D7       EQU   7
D20      EQU   20
D23      EQU   23
HEX00    EQU   X'00'
HEX01    EQU   X'01'
HEX02    EQU   X'02'
HEX03    EQU   X'03'
HEX04    EQU   X'04'
HEX05    EQU   X'05'
HEX08    EQU   X'08'
HEX09    EQU   X'09'
HEX10    EQU   X'10'
HEX20    EQU   X'20'
HEX40    EQU   X'40'
HEXBF    EQU   X'BF'
HEXFB    EQU   X'FB'
HEXF7    EQU   X'F7'
HEXEF    EQU   X'EF'
HEXDF    EQU   X'DF'
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5                       POINTER TO USERS SAVE AREA
R6       EQU   6
PARBSE7  EQU   7                       PARAMETER LIST BASE
R8       EQU   8
BASE9    EQU   9                       CSECT BASE REG
BASE10   EQU   10                      WKADSECT BASE REG
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 3
         SAVE  (14,12)
         BALR  BASE9,0                 INITIALIZE R9 AS CSECT BASE
         USING *,BASE9
         USING PARLIST,PARBSE7
         USING WKADSECT,BASE10         ESTABLISH ADDRESSABILITY
         L     PARBSE7,D0(R1)          GET ADDR OF PARAM LIST
         LR    BASE10,R1               INITIALIZE R10 AS WORK AREA BASE
*   GET ADDRESS OF SAVE AREA OF CALLING PROGRAM
         L     R4,D4(R13)              GET POINTER TO USERS SAVE AREA
         ST    R13,SAVE+4
         LA    R1,SAVE
         ST    R1,8(13)
         LR    R13,R1
*   IS INPUT CODE 1 THRU 5
         L     R5,D4(BASE10)           GET ADDR OF RETURN ARRAY
         L     R3,D4(PARBSE7)          GET POINTER TO USERS CODE NUMBR
         CLI   D3(R3),HEX05            ARG ONE PARM 5 OR LESS
         BH    TESTNINE                NO-GO TEST FOR NINE
         CLI   D3(R3),HEX01            ARG1 PARAM LESS THAN 1
         BL    TESTNINE                YES-GO TEST FOR NINE
         MVI   D1(R5),HEX00            ZERO LAST CODE TESTED FIELD
*   LOCATE HIGHEST VALUE RETURN CODE FROM BYTE 0 OF RET ARRAY
         TM    D0(R5),HEX04            IS BIT 5 ON
         BO    FIVE                    YES
         TM    D0(R5),HEX08            IS BIT FOUR ON
         BO    FOUR                    YES
         TM    D0(R5),HEX10            IS BIT THREE ON
         BO    THREE                   YES
         TM    D0(R5),HEX20            IS BIT TWO ON
         BO    TWO                     YES
         TM    D0(R5),HEX40            IS BIT ONE ON
         BC    14,ZONE                 BRANCH-NOT ONE
         NI    D0(R5),HEXBF            RESET BIT ONE
         MVI   TMPRCHLD,HEX01          CVT HIGHEST BIT SETTING TO VALUE
TSTCODES CLC   D3(1,R3),TMPRCHLD       TEST INPUT CODE AGAINST BIT VAL
         BC    12,STOVALUE             INP CDE LESS THAN HIGHEST RET CD
ZONE     MVC   D20(4,R4),ZERO          IF NO-ZERO USERS REG 0
ALLDONE  EQU   *
         L     R13,SAVE+4
         RETURN (14,12),T
STOVALUE MVC   D20(4,R4),ZERO          ZERO USERS REG 0
         MVC   D23(D1,R4),TMPRCHLD     STO HIGHER VALUE RET CDE IN R0
         MVC   D1(D1,R5),TMPRCHLD      AND IN BYTE 1 OF RET ARRAY
         B     ALLDONE                 RETURN
TESTNINE CLI   D3(R3),HEX09            ARG1 PARAM EQUAL NINE
         BNE   STORENEG                NO
*   IS THERE A LAST CODE TESTED FIELD OCCUPIED IN THE RETURN ARRAY
         TM    D1(R5),HEX04            IS LAST CODE TESTED A FOUR
         BO    STOWORD4
         TM    D1(R5),HEX03            IS LAST CODE TESTED A THREE
         BO    STOWORD3
         TM    D1(R5),HEX02            IS LAST CODE TESTED A TWO
         BO    STOWORD2
         TM    D1(R5),HEX01            IS LAST CODE TESTED A ONE
         BO    STOWORD1
STORENEG MVC   D20(D4,R4),NEGNUM       PUT NEG VALUE IN USERS R0
         B     ALLDONE                 RETURN
FIVE     EQU   *
         MVI   TMPRCHLD,HEX05          CVT HIGHEST BIT SETTING TO VALUE
         NI    D0(R5),HEXFB            RESET BIT FIVE
         B     TSTCODES                TEST VALUE
FOUR     MVI   TMPRCHLD,HEX04          CVT HIGHEST BIT SETTING TO VALUE
         NI    D0(R5),HEXF7            RESET BIT FOUR
         B     TSTCODES                TEST VALUE
THREE    MVI   TMPRCHLD,HEX03          CVT HIGHEST BIT SETTING TO VALUE
         NI    D0(R5),HEXEF            RESET BIT THREE
         B     TSTCODES                TEST VALUE
TWO      MVI   TMPRCHLD,HEX02          CVT HIGHEST BIT SETTING TO VALUE
         NI    D0(R5),HEXDF            RESET BIT TWO
         B     TSTCODES                TEST VALUE
STOWORD4 MVC   D20(D4,R4),16(R5)       MOVE ADD INFO 4 TO USER R 0
         B     ALLDONE
STOWORD3 MVC   D20(D4,R4),12(R5)       MOVE ADD INFO 3 TO USER R 0
         B     ALLDONE
STOWORD2 MVC   D20(D4,R4),8(R5)        MOVE ADD INFO 2 TO USER R 0
         B     ALLDONE
STOWORD1 MVC   D20(D4,R4),4(R5)        MOVE ADD INFO 1 TO USER R 0
         B     ALLDONE
ZERO     DC    F'0'
NEGNUM   DC    F'-1'
         EJECT
*   THIS DSECT PROVIDES SYMBOLIC REFERENCES TO
*   FIELDS OF THE WORK AREA OF USERS CORE
WKADSECT DSECT
         DS    F                       CONTAINS POINTER TO PARM LIST
         DS    F                       POINTS TO RET ARRAY
SAVE     DS    18F
TMPRCHLD DS    X
         EJECT
PARLIST  DSECT
GDSCB    DS    F
CODE     DS    F
         END
