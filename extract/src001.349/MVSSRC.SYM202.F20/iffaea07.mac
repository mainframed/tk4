         TITLE 'IFFAEA07 - CHAM - SET CHARACTER MODE'
*STATUS:  CHANGE LEVEL 000
*
*
*        CHANGE ACTIVITY
*          ZA30013  (02/23/78)
*FUNCTION/OPERATIONS: SETS GDSCHARS IN THE GDSCB TO INDICATE THE
*        CHARACTER SIZE (BASIC OR LARGE) AND TYPE (PROTECTED OR
*        UNPROTECTED) SELECTED BY THE USER.
*
*ENTRY POINTS: IFFAEA07
*
*INPUT:  REGISTER 1 CONTAINS THE ADDRESS OF A WORKAREA. THE FIRST WORD
*        CONTAINS THE ADDRESS OF THE PARAMETER LIST, AND THE SECOND,
*        THAT OF THE RETURN ARRAY.
*
*        THE PARAMETER LIST IS
*     +0 A(POINTER TO THE GDSCB)
*
*     +4 A(OPTION CODE)
*
*OUTPUT:  NONE TO USER. GDSCHARS IN THE GDSCB IS SET APPROPRIATELY.
*
*EXTERNAL ROUTINES: NONE
*
*EXITS-NORMAL AND ERROR: VIA RETURN MACRO TO THE DIRECTOR
*
*TABLES/WORKAREAS: GSP WORKAREA
*
*ATTRIBUTES: PROBLEM STATE, REENTRANT.
*
IFFAEA07 CSECT
         SAVE  (14,12)
         BALR  CHBASE,0
         USING *,CHBASE           SET CSECT ADDRESSABILITY
         L     CHWORK,0(1)        GET PARAM LIST ADDR
         L     CHSLAVE,0(CHWORK)  GET POINTER TO GDSCB
         L     CHGDSCB,0(CHSLAVE) GET GDSCB ADDR
         USING GDSCB,CHGDSCB      SET GDSCB DSECT ADDRESSSABILITY
         L     CHGSPCB,4(1)            GET ADDRESS OF GSPARRAY
         USING GSPARRAY,CHGSPCB        ADDRESSABILITY FOR RETURN ARRAY
*                                      AND THE NULL VARIABLE
         MVI   GSPARRAY,X'00'     INDICATE SUCCESSFUL COMPLETION
         C     CHGDSCB,GDSGDSCB        IS DATA SET NAME VALID
         BNE   CHBADGDS           NO
         L     CHTASK,4(CHWORK)   GET CODE ADDR
         L     CHTASK,0(CHTASK)        GET CODE
         CH    CHTASK,CH4         TEST FOR MODE OPTION
         BNE   CHTST3
         MVI   GDSCHARS,X'01'     SET CHAR MODE - LARGE, UNPROTECTED
         B     CHRETRN
CHTST3   CH    CHTASK,CH3         TEST FOR MODE OPTION
         BNE   CHTST2
         MVI   GDSCHARS,X'02'     SET CHAR MODE - BASIC, UNPROTECTED
         B     CHRETRN
CHTST2   CH    CHTASK,CH2         TEST FOR MODE OPTION
         BNE   CHTST1
         MVI   GDSCHARS,X'04'     SET CHAR MODE - LARGE, PROTECTED
         B     CHRETRN
CHTST1   CH    CHTASK,CH1         TEST FOR MODE OPTION
         BNE   CHPRMERR
         MVI   GDSCHARS,X'08'     SET CHAR MODE - BASIC PROTECTED
         B     CHRETRN
CHBADGDS MVI   GSPARRAY,X'08'     INDICATE PARAM ERROR
         MVC   GSPARRAY+16(4),CHERR1   GDSCB NOT OPEN       E12 ZA30013
         B     CHRETRN
CHPRMERR MVI   GSPARRAY,X'08'     INDICATE PARAM ERROR
         MVC   GSPARRAY+16(4),CHERR2   INVALID CODE         E12 ZA30013
CHRETRN  RETURN (14,12),T
CH4      DC    H'4'
CH3      DC    H'3'
CH2      DC    H'2'
CH1      DC    H'1'
CHERR1   DC    F'1'                                         E12 ZA30013
CHERR2   DC    F'2'                                         E12 ZA30013
CHBASE   EQU   9
CHGSPCB  EQU   6
CHWORK   EQU   2
CHTASK   EQU   3
CHSLAVE EQU 4
CHGDSCB  EQU   8
         COPY  GDSCB
         COPY  GSPCB
         END
