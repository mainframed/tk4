*
*        TMDEV IS ENTERED FROM DIRECTOR PART 2 WITH THE ADDRESS OF
*        THE WORK AREA IN REGISTER 1.  THE FIRST WORD OF THE WORK
*        AREA CONTAINS THE ADDRESS OF THE PARAMETER LIST LISTED
*        BELOW:
*
*        +0   A(GTMCBVAR)
*
*        EXTERNAL REFERENCES ARE MADE TO:
*
*        TMGDS-IFFAAAO6
*        BUFFER MANAGEMENT-IFFAHAO2
*        FLOW CONTROL MANAGEMENT-IFFAHAO1
*
*        EXTERNAL MACROS USED ARE:
*
*        LINK
*        DAR
*        CLOSE
*        FREEMAIN
*        ENATL-IFFACAO1
*
IFFAAA04 CSECT
*
* C805000                                                         M4680
*
*        ON ENTRY THE BASE REGISTER IS SET UP, THE WORK AREA DSECT
*        IS INITIALIZED, AND THE RETURN ARRAY IS INITIALIZED.
*
*        THE GTMCB IS CHECKED FOR VALIDITY AND IF VALID THE DSECT
*        IS INITIALIZED AND THE SAVE AREAS ARE CHAINED.
GTMCLS   SAVE  (14,12)
         BALR  BASE,0
         USING *,BASE
         LR    WORK,PARM           ADDR OF WORK AREA
         USING WORKAREA,WORK
         XC    WRKSPVSR(12),WRKSPVSR ZERO SUPERVISOR PARAMETER LIST
         L     PARM,WRKPARM        PARM LIST
         L     GTMREG,0(PARM)      ADDR OF GTMCB
         L     GTMREG,0(GTMREG)
         USING GTMCB,GTMREG
         L     ERRCD,WRKRTNCD      ADDRESS OF RETURN CODE ARRAY
         XC    0(20,ERRCD),0(ERRCD) ZERO RETURN ARRAY
         C     GTMREG,GTMGTMCB     IS THE TERMINAL OPEN
         BNE   ERR1                NO
         LA    REGA,4
         ST    REGA,WRKSPACE       BUILD THE FLOW CTL PARM
         ST    GTMREG,WRKSPACE+4   LIST
         ST    SAVE,WRKSAVE+4      SET UP SAVE AREA
         LA    REGA,WRKSAVE
         ST    REGA,8(SAVE)        CHAIN SAVE AREAS
         LR    SAVE,REGA
*
*        THE FLOW CONTROL IS CALLED TO DELETE THE FLOW CONTROL
*        STRUCTURE AND BUFFER MANAGEMENT IS CALLED TO FREE ALL
*        BUFFER ASSIGNED TO THIS TERMINAL.
*///
*///     CALL  FLOW CTL TO DELETE STRUCTURE
*///
         LA    PARM,WRKSPACE
         ST    PARM,WRKSAVE+72
         MVC   WRKSAVE+76(4),WRKRTNCD
         LA    PARM,WRKSAVE+72
         L     REGC,16
         L     REGC,CVTLINK(REGC)  LINK LIB DCB
         LINK  EP=IFFAHA01,MF=(E,(1)),SF=(E,WRKSPVSR)
         LA    REGA,7              SET UP PARMS FOR
         ST    REGA,WRKSPACE       BUFFER MGT
*///
*///     CALL  BUFFER MGT TO FREE ALL CORE
*///
         LA    PARM,WRKSAVE+72
         L     R15,STAT(ERRCD)     STATUS TABLE
         L     R15,BMGT(R15)       BMGT ADDR
         BALR  R14,R15             CALL
*
*        ANY GDSS THAT NEED TO BE CLOSED ARE CLOSED NEXT.  TMGDS
*        WILL CHECK TO SEE IF FLOW CONTROL AND BUFFER MANAGEMENT
*        ARE ALREADY CLOSED AND WHEN IT DETERMINES THAT THEY ARE
*        IT WILL BYPASS CALLS TO THESE ROUTINES.
TCL00010 L     REGA,GTMGDSCB       ANY GDS' TO BE CLOSED
         LTR   REGA,REGA
         BE    TCL00020            NO
         LA    REGA,GTMGDSCB       ADDR OF WORD CONT. ADDR OF GDS
         ST    REGA,WRKSPACE       PLACE IN PARM LIST
         LA    PARM,WRKSPACE       LOAD ADDR OF PARM LIST
         EJECT
*///
*///     CLOSE GDS
*///
         ST    PARM,WRKSAVE+72
         MVC   WRKSAVE+76(4),WRKRTNCD
         LA    PARM,WRKSAVE+72
         LINK  EP=IFFAAA06,MF=(E,(1)),SF=(E,WRKSPVSR)
         B     TCL00010            GO CHECK IF ANY MORE
*
*        ANY ATTENTION LEVELS STILL OPEN WILL BE ENDED.
*
TCL00020 LA    REGA,GTMLATBL       ADDR OF ATTN LEVEL
         ST    REGA,WRKSPACE       STORE IN PARM LIST
         OI    WRKSPACE,X'80'      SET VARIABLE LIST SWITCH
TCL00030 L     REGA,GTMLATBL       CHECK IF ANY LEFT TO
         LTR   REGA,REGA           BE CLOSED
         BE    TCL00040            NO
         LA    PARM,WRKSPACE       LOAD ADDR OF PARM LIST
*
*        CALL  ENDATL              END THE ATTN LEVEL
*
         ST    PARM,WRKSAVE+72
         LA    PARM,WRKSAVE+72
         LINK  EP=IFFACA01,MF=(E,(1)),SF=(E,WRKSPVSR)
         B     TCL00030
*
*        THE GTMCB VALIDITY FLAGS WILL BE SET TO ZERO AND THE
*        THREE GACBS WILL BE DARED.
*
TCL00040 XC    GTMGTMCB(4),GTMGTMCB ZERO OUT TERM ADDR
         XC    GTMTERID(1),GTMTERID ZERO OUT ID
         MVC   WRKSPACE,=F'3'
         DAR   (GACB1,GACB2,GACB3),MF=(E,WRKSPACE)
*
*        THE GTMCB WILL BE REMOVED FROM THE CHAIN OF GTMCBS IN
*        THE GSPCB AND THE DCB WILL BE CLOSED.  FOLLOWING THIS
*        ALL STORAGE WILL BE FREED AND CONTROL WILL RETURN TO
*        THE CALLING PROGRAM.
*
         L     GSPREG,GTMGSPCB     PICK UP GSPCB
         USING GSPCB,GSPREG
         LA    REGA,GSPGTMCB     LOAD GSPGTMCB ADDR
TCL00050 L     REGB,0(REGA)
         CR    GTMREG,REGB
         BE    TCL00060
         LR    REGA,REGB
         B     TCL00050
TCL00060 MVC   0(4,REGA),GTMNXGTM
         MVI   WRKSPACE,128        SET OPTION BYTE
         CLOSE (DCB),MF=(E,WRKSPACE)
         L     REGZERO,GETMNLEN
         LR    PARM,GTMREG
         FREEMAIN R,LV=(0),A=(1)   FREE ALL CORE
         L     SAVE,4(SAVE)        RESET SAVE AREA
         RETURN (14,12),T
ERR1     MVI   0(ERRCD),X'08'
         MVI   19(ERRCD),X'01'     INVALID GTMCB
         RETURN (14,12),T
         EJECT
REGZERO  EQU   0
PARM     EQU   1
REGA     EQU   2
REGB     EQU   3
WORK     EQU   5
GSPREG   EQU   6
GTMREG   EQU   7
BASE     EQU   9
ERRCD    EQU   10
REGC     EQU   11
SAVE     EQU   13
R14      EQU   14
R15      EQU   15
BMGT     EQU   816
STAT     EQU   24
NXGTM    EQU   0                   DISPLACEMENT OF GTMCB CHAIN
GETMNLEN DC    F'664'                                             M4680
CVTLINK  EQU   8
         EJECT
WORKAREA DSECT
WRKPARM  DS    F
WRKRTNCD DS    F
WRKSPACE DS    4F
WRKSPVSR DS    3F
WRKSAVE  DS    18F
         COPY  GSPCB
         COPY  GTMCB
*
*        THE FOLLOWING WORDS ARE ADDED TO THE GTMCB FOR THE
*                        CLOSE ROUTINE.
DCB      DS    13F
BCT      DS    384X
GACB1PTR DS    F
GACB1GTM DS    F
GACB1    DS    14F
COM1     DS    4F
GSCB2PTR DS    F
GACB2GTM DS    F
GACB2    DS    14F
COM2     DS    4F
GACB3PTR DS    F
GACB3GTM DS    F
GACB3    DS    14F
COM3     DS    4F
         COPY  GDSCB
         END
