* TITLE        DIRECTOR PART1
*
*MODULE NAME   IHCGSP03
*
*INPUT         OS STANDARD FORMAT
*
*              FIRST PARAMETER MUST BE ADDRESS OF FIELD CONTAINING
*
*              ADDRESS OF VALID GSP CONTROL BLOCK
*
*OUTPUT        NONE TO CALLER
*
*ERRORS        ABEND, USER CODE 8 IF INPUT NOT CORRECT (TEMPORARY)
*
*FUNCTION      LOCATES DIRECTOR PART2 FROM GSPCB, AND BRANCHES.
*
*              CONTROL RETURNS DIRECTLY TO USER FROM DIRECTOR 2
*
*OUTPUT TO
*DIRECTOR 2    REG1 UNCHANGED
*
*              REG2 ADDRESS OF GSPCB
*
*REG3          DISPLACEMENT NUMBER OF ORIGINAL ENTRY POINT
*
*REG9          ADDRESS OF DIRECTOR 2
*
*REG13         UNCHANGED.  REGISTERS HAVE BEEN SAVED
         EJECT
IHCGSP03 CSECT
*2430                                                              RSTG
         USING *,15
         ENTRY INDEV
INDEV    B     DCTR1
         USING *,15
         ENTRY TMDEV
TMDEV    B     DCTR1
         USING *,15
         ENTRY INGDS
INGDS    B     DCTR1
         USING *,15
         ENTRY TMGDS
TMGDS    B     DCTR1
         USING *,15
         ENTRY CRATL
CRATL    B     DCTR1
         USING *,15
         ENTRY ENATL
ENATL    B     DCTR1
         USING *,15
         ENTRY ENATN
ENATN    B     DCTR1
         USING *,15
         ENTRY DSATN
DSATN    B     DCTR1
         USING *,15
         ENTRY MPATL
MPATL    B     DCTR1
         USING *,15
         ENTRY MLPEO
MLPEO    B     DCTR1
         USING *,15
         ENTRY SLPAT
SLPAT    B     DCTR1
         USING *,15
         ENTRY MLITS
MLITS    B     DCTR1
         USING *,15
         ENTRY RQATN
RQATN    B     DCTR1
         USING *,15
         ENTRY EURATL
EURATL   B     DCTR1
         USING *,15
         ENTRY SETATN
SETATN   B     DCTR1
         USING *,15
         ENTRY SALRM
SALRM    B     DCTR1
         USING *,15
         ENTRY GSPRD
GSPRD    B     DCTR1
         USING *,15
         ENTRY RCURS
RCURS    B     DCTR1
         USING *,15
         ENTRY ICURS
ICURS    B     DCTR1
         USING *,15
         ENTRY SDATM
SDATM    B     DCTR1
         USING *,15
         ENTRY SGRAM
SGRAM    B     DCTR1
         USING *,15
         ENTRY SDATL
SDATL    B     DCTR1
         USING *,15
         ENTRY SGDSL
SGDSL    B     DCTR1
         USING *,15
         ENTRY SSCIS
SSCIS    B     DCTR1
         USING *,15
         ENTRY SCHAM
SCHAM    B     DCTR1
         USING *,15
         ENTRY PLINE
PLINE    B     DCTR1
         USING *,15
         ENTRY PPNT
PPNT     B     DCTR1
         USING *,15
         ENTRY PSGMT
PSGMT    B     DCTR1
         USING *,15
         ENTRY PTEXT
PTEXT    B     DCTR1
         USING *,15
         ENTRY STPOS
STPOS    B     DCTR1
         USING *,15
         ENTRY MVPOS
MVPOS    B     DCTR1
         USING *,15
         ENTRY BGSEQ
BGSEQ    B     DCTR1
         USING *,15
         ENTRY BGSUB
BGSUB    B     DCTR1
         USING *,15
         ENTRY ENSEQ
ENSEQ    B     DCTR1
         USING *,15
         ENTRY ENSUB
ENSUB    B     DCTR1
         USING *,15
         ENTRY LKSUB
LKSUB    B     DCTR1
         USING *,15
         ENTRY INCL
INCL     B     DCTR1
         USING *,15
         ENTRY OMIT
OMIT     B     DCTR1
         USING *,15
         ENTRY EXEC
EXEC     B     DCTR1
         USING *,15
         ENTRY RESET
RESET    B     DCTR1
         USING *,15
         ENTRY IDPOS
IDPOS    B     DCTR1
         USING *,15
         ENTRY FSMOD
FSMOD    B     DCTR1
         USING *,15
         ENTRY STEOS
STEOS    B     DCTR1
         USING *,15
         ENTRY ORGDS
ORGDS    B     DCTR1
         USING *,15
         ENTRY LOCPN
LOCPN    B     DCTR1
         USING *,15
         ENTRY BGTRK
BGTRK    B     DCTR1
         USING *,15
         ENTRY RDTRK
RDTRK    B     DCTR1
         USING *,15
         ENTRY ENTRK
ENTRK    B     DCTR1
         USING *,15
         ENTRY DFSTR
DFSTR    B     DCTR1
         USING *,15
         ENTRY PLSTR
PLSTR    B     DCTR1
         USING *,15
         ENTRY ORGEN
ORGEN    B     DCTR1
         USING *,15
         ENTRY CNVRT
CNVRT    B     DCTR1
         USING *,15
         ENTRY ITRC
ITRC     B     DCTR1
         USING *,15
         ENTRY ITBP
ITBP     B     DCTR1
         USING *,15
         ENTRY RTBP
RTBP     B     DCTR1
         USING *,15
         ENTRY ITST
ITST     B     DCTR1
         USING *,15                                                RSTG
         ENTRY SPEC                                                RSTG
SPEC     B     DCTR1                                               RSTG
* REGISTER EQUATES*****************************************************
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R9BASE   EQU   9
R15      EQU   15
         EJECT
DCTR1    SAVE  (14,12)
*
*NOTE DIRECTOR1 DOES NOT CONTAIN A SAVE AREA OR CHANGE REG 13
*
         BALR  R9BASE,0
         USING *,R9BASE
         L     R2,0(R1)                GET ADDRESS OF FIRST PARAMETER,
         L     R2,0(R2)                CONTENTS MUST BE CNTRLBLK ADDR
         USING CBSECT,R2               GENERAL CONTROL BLOCK DSECT
         CLI   CBID,X'F2'              X'F2' IS GSP CONTROL BLOCK ID
         BNE   ERR1                    BRANCH IF NOT A GSP CNTRL BLOCK
*CONTROL BLOCK ID'S ARE
*
* GSPCB  X'01'
* GTMCB  X'02'
* GDSCB  X'03'
* ATLCB  X'04'
         CLI   CBCBID,X'00'
         BE    ERR1
         CLI   CBCBID,X'02'
         BL    GSPDC030                BRANCH IF GSPCB
         BE    GSPDC020                BRANCH IF GTMCB
         CLI   CBCBID,X'04'
         BL    GSPDC010                BRANCH IF GDSCB
         BE    GSPDC040                BRANCH IF ATLCB
         DROP  R2
*
*INVALID CONTROL BLOCK
*
*GSPCB CANNOT BE LOCATED, DIRECTOR PART 2 CANNOT BE LOCATED,
*
*RETURN ARRAY CANNOT BE LOCATED.
*
*
ERR1     L     R1,0(R1)
         XC    0(4,R1),0(R1)           SET VARIABLE TO ZERO IF INVALID
         RETURN (14,12),T
         EJECT
*
*HERE FOR GDSCB
*
         USING GDSCB,R2
GSPDC010 L     R2,GDSGTMCB             GET GTMCB FROM GDSCB
         DROP  R2
*
*HERE FOR GTMCB
*
         USING GTMCB,R2
GSPDC020 L     R2,GTMGSPCB             GET GSPCB FROM GTMCB
         DROP  R2
*
*HERE FOR GSPCB
*
         USING GSPCB,R2
GSPDC030 LA    R3,0(R15)
         L     R4,TOPTBLAD             GET ADDRESS OF FIRST ENTRY
         SR    R3,R4                   FORM DIFFERENCE NUMBER.  THIS
*                                      NUMBER IS USED AGAINST STATUS
*                                      TABLE IN DIEECTOR 2 TO LOCATE
*                                      ROUTINE REQUESTED.
         L     R9BASE,GSPDCTR2         GET ADDRESS OF DIRECTOR PART2
         BR    R9BASE                  GO TO DIRECTOR PART2
         DROP  R2
*
*HERE IF ATLCB
*
         USING ATTNLEVL,R2
GSPDC040 L     R2,ATTNBKPT             PICK UP BACK POINTER
         DROP  R2
         USING GTMCB,R2
         CLI   GTMTERID,X'02'          TEST IF POINTING AT GTMCB
         BE    GSPDC020                BRANCH IF YES
         B     GSPDC040                ELSE, CONTINUE TILL FOUND
         EJECT
*CONSTANTS AND DUMMY SECTIONS
TOPTBLAD DC    A(IHCGSP03)             ADDRESS OF TOP OF TABLE
CBSECT   DSECT
         DS    2F
CBID     DS    XL1           ALL CONTROL BLKS HAVE X'F2' HERE
CBCBID   DS    XL1           VALUE IS X'01'-X'04' FOR GSP CONTROL BLOCK
         COPY  GSPCB
         COPY  GTMCB
         COPY  GDSCB
         COPY  ATTNLEVL
         END
