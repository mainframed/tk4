*
*
*                             SLPAT INPUT PARAMETER LIST
*
*                             +0   A(GDSVAR)
*                             +4   A(DETECT)
*
*                             0= PERMIT DETECT INTERRUPTS
*                             1= DISABLE DETECT INTERRUPTS
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
         EJECT
IFFACA06 CSECT
*C440000,A624500-625500,D632000,A626000-639988,A720500,A760500  YA03229
         SAVE  (14,12)
         BALR  BASE,0
         USING *,BASE
         LR    WORK,PARM
         L     REGA,PRMLST(WORK)   ADDR OF WORK AREA
         L     ERRCD,ERR(WORK)     ADDR OF ERROR RETURN ARRAY
         XC    0(20,ERRCD),0(ERRCD) ZERO OUT RETURN ARRAY
*        WE NOW HAVE PICKED UP THE WORKAREA, REMOVED THE PARAMETER
*        LIST AND THE RETURN ARRAY ADDRESS, WE WILL NOW CHECK
*        PARAMETERS FOR VALIDITY AND QUANTITY.
*        IF THE CODE IS ZERO WE WILL POINT REGB TO THE GESD ORDER. IF
*        CODE IS ONE REGB WILL POINT TO THE GDPD ORDER.
         L     GDSREG,GDS(REGA)    ADDR OF GDS VAR
         L     GDSREG,0(GDSREG)    ADDR OF GDSCB
         USING GDSCB,GDSREG
         C     GDSREG,GDSGDSCB     GDSCB VALID
         BNE   ERR1                NO
         TM    GDS(REGA),LAST      ENOUGH PARMS
         BO    ERR1A               NO
         L     REGB,CODE(REGA)     PICK UP CODE
         L     REGB,0(REGB)
         C     REGB,ONE            IS IT PERMIT
         BE    SLP00300            YES
         C     REGB,TWO            DISABLE
         BNE   ERR2                NO INVALID CODE
         LA    REGB,GDPD           ADDR OF DISABLE ORDER
         NI    GDSFLAGS,DSBLE      SET FLAG IN GDSCB TO DISABLE
         TM    GDSFLAGS,EQUIV      IS THIS AN EQUIV
         BO    SLP00301            YES SET GDSFLG IN SHARED GDS YA03229
         EJECT
*        NOW THE GTMCB DSECT WILL BE INITIALIZED AND SAVE AREAS WILL
*        BE CHAINED. OLD I/O WILL BE WAITED.  THEN THE ORDER POINTED
*        TO BY REGB WILL BE WRITTEN OUT TO FLOW CONTROL SO THAT THE
*        ENTIRE GDS WILL BE IN THE PROPER PEN MODE.
SLP00100 L     GTMREG,GDSGTMCB     GTMCB
         USING GTMCB,GTMREG
         CLC   GDSFCBUF,ZERO       FLOW CONTROL INITIALIZER
         BE    SLP00200            NO DATA STORE WILL WRITE OUT
         LA    REGA,SAVEARA(WORK)  CHAIN SAVE AREAS
         ST    REGA,8(SAVE)
         ST    SAVE,4(REGA)
         LR    SAVE,REGA
         L     REGA,GTMGRDCB       DCB ADDR
         L     PARM,GTMDECB1       WAIT ON OLD I/O
         WAIT  ECB=(1)
         L     PARM,GTMDECB1       RELOAD REG1
         CLI   0(PARM),X'7F'
         BNE   ERR3
         NI    0(PARM),X'BF'
*                                  WRITE OUT ORDER TO FLOW CTL
         GWRITE (PARM),STR,(REGA),2,(REGB),GDSFCBUF,GDSFCBUF,MF=E
         LTR   REG15,REG15        I/O STARTED               E12 ZA30875
         BNZ   ERR3               NO, DON'T WAIT            E12 ZA30875
         L     PARM,GTMDECB1      RELOAD REG1               E12 ZA30875
         WAIT  ECB=(1)            WAIT FOR I/O COMPLETE     E12 ZA30875
         L     PARM,GTMDECB1      RELOAD REG1               E12 ZA30875
         CLI   0(PARM),X'7F'      COMPLETE OK?              E12 ZA30875
         BNE   ERR3               NO                        E12 ZA30875
         L     SAVE,4(SAVE)        RESET SAVE AREA
SLP00200 RETURN (14,12)            RETURN
SLP00300 LA    REGB,GESD           ADDR OF ENABLE SW DETECT
         OI    GDSFLAGS,PERMIT     SET FLAG IN GDSCB TO PERMIT
         TM    GDSFLAGS,EQUIV     IS THIS AN EQUIV GDS?         YA03229
         BZ    SLP00100           NO,GO WRITE OUT ORDER         YA03229
SLP00301 EQU   *                                                YA03229
* FOR EQUIVALENT GDS'S,WE WILL GO DOWN THE FORWARD AND BACKWARD YA03229
* CHAINS AND SET THE PROPER BIT(ENABLE OR DISABLE LIGHT PEN     YA03229
* DETECT) IN GDSFLAGS BECAUSE ALL GDS'S IN THE EQUIVALENCE WILL YA03229
* BE IN THE SAME DETECTABLE MODE .                              YA03229
         L     REGA,GDSSHDBK      LOAD BACK SHARED PTR          YA03229
         LR    GDSSAVE,GDSREG     SAVE ORIGINAL GDS PTR         YA03229
         L     GDSFWD,GDSSHDFD    LOAD FORWARD POINTER          YA03229
         LR    GDSREG,REGA        BASE NEXT GDS                 YA03229
SLP00302 EQU   *                                                YA03229
         LTR   GDSREG,GDSREG      END OF CHAIN?                 YA03229
         BZ    SLP00305           YES,SEARCH FWD CHAIN          YA03229
         CH    REGB,GESD          ENABLE ORDER DESIRED?         YA03229
         BE    SLP00303           YES,SET ENABLE SWITCH         YA03229
         NI    GDSFLAGS,DSBLE     SET DISABLE FLAG BIT          YA03229
         B     SLP00304           GET NEXT SHARED GDS           YA03229
SLP00303 EQU   *                                                YA03229
         OI    GDSFLAGS,PERMIT    SET ENABLE FLAG BIT           YA03229
SLP00304 EQU   *                                                YA03229
         L     GDSREG,GDSSHDBK    NEXT SHARED GDS PTR           YA03229
         B     SLP00302           LOOP TO END OF CHAIN          YA03229
SLP00305 EQU   *                                                YA03229
         LR    GDSREG,GDSFWD      FORWARD SHARED PTR            YA03229
SLP00306 EQU   *                                                YA03229
         LTR   GDSREG,GDSREG      END OF CHAIN?                 YA03229
         BNZ   SLP00307           NO,CONTINUE PROCESSING        YA03229
         LR    GDSREG,GDSSAVE     RESTORE ORIGINAL PTR          YA03229
         B     SLP00100           WRITE ORDER TO BUFFER         YA03229
SLP00307 EQU   *                                                YA03229
         CH    REGB,GESD          ENABLE ORDER DESIRED?         YA03229
         BE    SLP00308           YES,SET ENABLE FLAG BIT       YA03229
         NI    GDSFLAGS,DSBLE     SET DISABLE BIT               YA03229
         B     SLP00309           GET NEXT PTR                  YA03229
SLP00308 EQU   *                                                YA03229
         OI    GDSFLAGS,PERMIT    SET ENABLE FLAG BIT           YA03229
SLP00309 EQU   *                                                YA03229
         L     GDSREG,GDSSHDFD    GET NEXT PTR                  YA03229
         B     SLP00306           LOOP TO END OF CHAIN          YA03229
         EJECT
ERR1     MVC   PARERR(4,ERRCD),ONE BAD GDSCB
ERR1A    MVI   ERCODE(ERRCD),PARMEROR PARM ERROR
         B     SLP00200
ERR2     MVC   PARERR(4,ERRCD),TWO BAD CODE
         B     ERR1A
ERR3     OI    ERCODE(ERRCD),IO
         B     SLP00200
         EJECT
* REGISTERS
PARM     EQU   1
REGA     EQU   2
REGB     EQU   3
GDSSAVE  EQU   4                  WORK REGISTER                 YA03229
WORK     EQU   5
GTMREG   EQU   7
GDSREG   EQU   8
BASE     EQU   9
ERRCD    EQU   10
GDSFWD   EQU   11                 WORK REGISTER                 YA03229
SAVE     EQU   13
REG15    EQU   15                 RETURN CODE REG           E12 ZA30875
         EJECT
PRMLST   EQU   0
ERR      EQU   4
SAVEARA  EQU   8
GDS      EQU   0
CODE     EQU   4
LAST     EQU   X'80'
PARERR   EQU   16
ERCODE   EQU   0
PARMEROR EQU   X'08'
DSBLE    EQU   X'FE'
PERMIT   EQU   X'01'
EQUIV    EQU   X'80'
IO       EQU   X'04'
         EJECT
ZERO     DC    F'0'
ONE      DC    F'1'
TWO      DC    F'2'
         GINIT
GDPD     GDPD
GESD     GESD
         COPY  GDSCB
         COPY  GTMCB
         COPY  GSPCB
         END
