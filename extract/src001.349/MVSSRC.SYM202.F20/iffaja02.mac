* TITLE ITBP/RTBP-TEST INTEGER/REAL BEAM POSITION
* STATUS CHANGE LEVEL 0
* FUNCTION TO RETURN TO THE USER EITHER THE X OR Y COORDINATE WHERE THE
*        2250 BEAM IS CURRENTLY POSITIONED OR THE X OR Y COORDINATE
*        WHERE THE USER INTENDED THE BEAM TO BE POSITIONED.
* ENTRY ENTERED AT IFFAJA02 VIA A CALL TO ITBP OR AT IFFAJA03 VIA A
*        CALL TO RTBP.
* INPUT GR=1 CONTAINS THE ADDRESS OF A WORK/SAVE AREA.THE FIRST WORD OF
*        THIS AREA CONTAINS THE ADDRESS OF A PARAMETER LIST CONSTRUCTED
*        AS FOLLOWS
*        1.GDSNAME-ADDRESS OF A VARIABLE WHICH CONTAINS THE ADDRESS OF
*          A GDSCB.
*        2.INFO-ADDRESS OF A VARIABLE OR CONSTANT WHICH CONTAINS ONE OF
*          THE FOLLOWING VALUES
*          (1)-RETURN X COORDINATE WHERE BEAM IS SUPPOSED TO BE.
*          (2)-RETURN X COORDINATE WHERE BEAM ACTUALLY IS.
*          (3)-SAME AS 1 EXCEPT FOR Y.
*          (4)-SAME AS 2 EXCEPT FOR Y.
* OUTPUT GR=0(ITBP) OR FLOATING POINT REG 0(RTBP) CONTAINS THE SCALED
*        PROGRAMMER COORDINATE REQUESTED OF THE TYPE(INTEGER-ITBP OR
*        REAL-RTBP) AND FORM (ABSOLUTE OR INCREMENTAL) CURRENTLY
*        APPLICABLE FOR THE SPECIFIED GRAPHIC DATA SET.
* EXIT RETURN TO CALLER VIA REGISTER 14.
* ERROR EXIT IN THE EVENT OF INVALID PARAMETERS OR A CALL TO RTBP IN
*        INTEGER MODE OR A CALL TO ITBP IN REAL MODE,GR=0(ITBP) OR
*        FLOATING POINT REG=0(RTBP) WILL CONTAIN THE MAXIMUM
*        NEGATIVE VALUE UPON RETURN TO THE CALLER.
* ATTRIBUTES RE-ENTRANT,PROBLEM PROGRAM MODE.
* EXTERNAL ROUTINES CNVRT
* NOTES THIS ROUTINE EXAMINES THE FOLLOWING CONTROL BLOCKS
*        1.GDSCB
*        2.GTMCB
*        3.GSPCB
         EJECT
IFFAJA02 CSECT
*C220000,C440000,C768000                                     LD YA00814
*A204000,264000-268000,A716000-725000,C726000-728000        D11 ZA15437
         SPACE 3
R0       EQU   0                       PARM REG 0
R1       EQU   1                       PARM REG 1
RLIST    EQU   2                       WORK AREA REG
RPARM    EQU   3                       PARAMETER(USER) REG
RDATA    EQU   4                       DATA REG
RGDSCB   EQU   5                       GDSCB REG
RA       EQU   6                       WORK REG
RB       EQU   7                       WORK REG
RC       EQU   8                       WORK REG
RBASE    EQU   9                       BASE REG
SPARE    EQU   12                      RSVD PL/1
RSAVE    EQU   13                      SAVE AREA PTR
R14      EQU   14                      LINKAGE REG          D11 ZA15437
R15      EQU   15                      ENTRY/WORK REG
ZERO     EQU   0                       ZERO
LAST     EQU   X'80'                   LAST PARM
RTBPA    EQU   X'80'                   RTBP ENTRY SWITCH
PRIOR    EQU   4                       OFFSET TO NEXT HIGHER SAVE AREA
REG0     EQU   20                      OFFSET TO REG 0 IN SAVE AREA
CHAIN    EQU   8                       CHAIN SAVE AREAS
GID      EQU   X'03'                   GDSCB ID
XINT     EQU   X'30'                   X INTEGER
XREAL    EQU   X'C0'                   X REAL                LD YA00814
YINT     EQU   X'03'                   Y INTEGER
YREAL    EQU   X'0C'                   Y REAL
RTNCDE   EQU   X'00'                   RTN CODE =ZERO
ONEA     EQU   1                       ONE
STAT     EQU   48                 A(STATAB) DISPL IN GSP    D11 ZA15437
CONVERT  EQU   X'0264'             STATAB CNVRT ENTRY DISPL D11 ZA15437
         EJECT
         ENTRY IFFAJA02                ITBP
         ENTRY IFFAJA03                RTBP
         USING *,15
         SAVE  (14,12)                 ITBP ENTRY POINT
         B     NEXT
IFFAJA03 SAVE  (14,12)                 RTBP ENTRY POINT
         SR    R15,R15                 INDICATE ENTERED BY RTBP
NEXT     BALR  RBASE,0                 SET UP BASE REG
         DROP  15
         USING *,RBASE                 DEFINE BASE REG
         LR    RLIST,R1                LOAD WORK AREA REG
         USING WORKAREA,RLIST          DEFINE DSECT
         XC    SWITCH(1),SWITCH        CLEAR SWITCH
         LTR   R15,R15                 IS THIS AN ITBP ENTRY
         BC    7,BP01                  BRANCH IF YES
         OI    SWITCH,RTBPA            INDICATE RTBP ENTRY
BP01     L     RPARM,PLIST             LOAD ADDRESS OF USER PARM LIST
         USING PARM,RPARM              DEFINE DSECT
         L     RDATA,PRIOR(RSAVE)      LOAD ADR OF PRIOR SAVE AREA
         ST    RSAVE,SAVE+4            SAVE REG 13 DAVE AREA PTR
         LA    R15,SAVE                LOAD ADR OF MY SAVE AREA
         ST    R15,CHAIN(RSAVE)        CHAIN SAVE AREAS
         LA    RSAVE,SAVE              ESTABLISH NEW  SAVE AREA
         B     AROUND                  BRANCH
ERROR    TM    SWITCH,RTBPA            RTBP CALL
         BO    BP02                    BRANCH IF YES
         L     R15,MAX                 LOAD MAX NEG NUMBER
         ST    R15,REG0(RDATA)         INDICATE ERROR IN GR=0
         B     OUTRTN                  BRANCH
BP02     LE    R0,MAXR                 INDICATE ERROR IN FP REG=0
OUTRTN   L     RSAVE,SAVE+4            RESTORE SAVE AREA PTR
         RETURN (14,12),T              RETURN TO CALLER
         EJECT
AROUND   TM    GDSNAME,LAST            LAST PARAM
         BO    ERROR                   BRANCH IF LAST PARAMETER
         L     RGDSCB,GDSNAME          LOAD INDIRECT PTR TO GDSCB
         L     RGDSCB,ZERO(RGDSCB)     LOAD GDSCB ADR
         USING GDSCB,RGDSCB            DEFINE DSECT
         CLI   GDSGDSID,GID            VALID GDSCB
         BNE   ERROR                   BRANCH-NOT VALID
         L     RA,GDSGTMCB             LOAD GTMCB ADR
         USING GTMCB,RA                DEFINE DSECT
         L     RB,GTMGSPCB             LOAD GSPCB ADR
         USING GSPCB,RB                DEFINE DSECT
         L     RC,INFO                 LOAD PTR TO INFO
         LA    RC,ZERO(RC)             ZERO HI ORDER BYTE
         CL    RC,GSPNULLV             INFO=NULL
         BE    ERROR                   BRANCH IF YES
         L     RC,ZERO(RC)             LOAD VALUE OF INFO
         XC    GDSA(24),GDSA           CLEAR INPUT PARM AREA LD YA00814
         CL    RC,ONE                  INFO=1
         BE    BP03                    BRANCH=1
         CL    RC,TWO                  INFO=2
         BE    BP04                    BRANCH=2
         CL    RC,THREE                INFO=3
         BE    BP05                    BRANCH=3
         CL    RC,FOUR                 INFO=4
         BE    BP06                    BRANCH=4
         B     ERROR                   BRANCH
BP03     L     RA,GDSXLAST             LOAD XLAST FIELD
         ST    RA,XIA                  STORE INTO PARAM LIST
         B     BP07                    BRANCH
BP04     SR    RA,RA                   CLEAR REG
         LH    RA,GDSXCURR             LOAD X CURR FIELD
         ST    RA,XIA                  STORE INTO PARAM LIST
         B     BP07                    BRANCH
BP05     L     RA,GDSYLAST             LOAD YLAST FIELD
         ST    RA,YIA                  STORE IN PARAM LIST
         B     BP08                    BRANCH
BP06     SR    RA,RA                   CLEAR REG
         LH    RA,GDSYCURR             LOAD Y CURR FIELD
         ST    RA,YIA                  STORE IN PARAM LIST
         B     BP08                    BRANCH
BP07     TM    SWITCH,RTBPA            RTBP ENTRY
         BO    BP09                    BRANCH IF RTBP
         TM    GDSDATMD,XINT           X=INTEGER
         BC    8,ERROR                 BRANCH-X NOT INTEGER MODE
         B     BP10                    BRANCH
BP09     TM    GDSDATMD,XREAL          X=REAL
         BC    8,ERROR                 BRANCH- X NOT REAL MODE
         B     BP10                    BRANCH
BP08     TM    SWITCH,RTBPA            RTBP ENTRY
         BO    BP11                    BRANCH-RTBP
         TM    GDSDATMD,YINT           Y=INTEGER
         BC    8,ERROR                 BRANCH- Y NOT INTEGER MODE
         B     BP10                    BRANCH
BP11     TM    GDSDATMD,YREAL          Y=REAL
         BC    8,ERROR                 BRANCH-Y NOT REAL MODE
BP10     ST    RGDSCB,GDSA             ADR OF GDSCB TO PARAM LIST
         LA    RA,ONEA                 LOAD CONSTANT
         ST    RA,CONA                 STORE VALUE IN PARAM LIST
         LA    RA,GDSA                 LOAD ADR
         ST    RA,GDS                  PUT ADR IN PARAM LIST
         LA    RA,CONA                 LOAD ADR
         ST    RA,CON                  STORE IN PARAM LIST
         LA    RA,XIA                  LOAD ADR
         ST    RA,XI                   STORE IN PARAM LIST
         LA    RA,YIA                  LOAD ADR
         ST    RA,YI                   STORE IN PARAM LIST
         LA    RA,XOA                  LOAD ADR
         ST    RA,XO                   STORE ADR IN PARAM LIST
         LA    RA,YOA                  LOAD ADR
         ST    RA,YO                   STORE IN PARAM LIST
         MVI   YO,LAST                 INDICATE LAST PARAM
         LA    RA,GDS                  LOAD ADR
         ST    RA,PARAM                STORE ADR IN PARAM LIST
         LA    RA,RCC                  LOAD ADR
         ST    RA,RCB                  STORE IN PARAM LIST
         XC    RCC(20),RCC             CLEAR RETURN CODE ARRAY
         LA    R1,PARAM                LOAD ADR
         L     R15,STAT(RB)        POINT TO STATUS TABLE    D11 ZA15437
         L     R15,CONVERT(R15)    AND TO CONVERT EP        D11 ZA15437
         LTR   R15,R15             CONVERT LOADED?          D11 ZA15437
         BZ    BPLINK1             NO,MUST USE SYSTEM LINK  D11 ZA15437
         BALR  R14,R15             GO CONVERT               D11 ZA15437
         B     BPRTN1              RETURN FROM CONVERT      D11 ZA15437
BPLINK1  LINK  EP=IFFAGA08,MF=(E,(1))  CALL CONVERT         D11 ZA15437
BPRTN1   CLI   RCC,RTNCDE              CONVERT SUCCESSFUL   D11 ZA15437
         BNE   ERROR                   BRANCH IF NOT SUCCESSFUL
         CL    RC,TWO                  X OR Y OUTPUT
         BC    2,BP12                  BRANCH FOR Y OUTPUT
         TM    SWITCH,RTBPA            REAL OR INTEGER OUTPUT
         BO    BP13                    BRANCH -REAL OUTPUT
         L     RA,XOA                  LOAD X RESULTS
         ST    RA,REG0(RDATA)          STORE RESULTS
         B     OUTRTN                  BRANCH
BP13     LE    R0,XOA                  RESULTS TO FP REG 0
         B     OUTRTN                  BRANCH
BP12     TM    SWITCH,RTBPA            REAL OR INTEGER OUTPUT
         BO    BP14                    BRANCH-REAL OUTPUT
         L     RA,YOA                  LOAD RESULTS
         ST    RA,REG0(RDATA)          STORE RESULTS
         B     OUTRTN                  BRANCH
BP14     LE    R0,YOA                  RESULTS TO FP REG = 0
         B     OUTRTN                  BRANCH
         EJECT
         DS    0F
MAX      DC    X'80000000'
MAXR     DC    X'FFFFFFFF'                                   LD YA00814
ONE      DC    A(1)
TWO      DC    A(2)
THREE    DC    A(3)
FOUR     DC    A(4)
         CNOP  0,8
PARM     DSECT
GDSNAME  DS    1F
INFO     DS    1F
WORKAREA DSECT
PLIST    DS    1F
RCA      DS    1F
SWITCH   DS    1F
*              BIT 0=0 IFFAJ02(ITBP) ENTRY
*              BIT 0=1 IFFAJ03(RTBP) ENTRY
SAVE     DC    18F'0'
PATCH    DC    64X'FF'             PATCH AREA               D11
GDS      DS    1F                      ARD OR GDSA
CON      DS    1F                      ADR OF CONA
XI       DS    1F                      ADR OF XIA
YI       DS    1F                      ADR OF YIA
XO       DS    1F                      ADR OF XOA
YO       DS    1F                      ADR OF YOA + X'80' IN HI BYTE
GDSA     DS    1F                      ADR OF GDSCB
CONA     DS    1F                      VALUE OF ONE
XIA      DS    1F                      VALUE OF X INPUT OR 0
YIA      DS    1F                      VALUE OF Y INPUT OR 0
XOA      DS    1F                      LOCATION WHERE X RESULTS STORED
YOA      DS    1F                      LOCATION WHERE Y RESULTS STORED
RCC      DS    5F                      RTN CODE ARRAY
PARAM    DS    1F                      ADR OF GDS
RCB      DS    1F                      ADR OF RTN CODE ARRAY (RCC)
         COPY  GSPCB
         COPY  GTMCB
         COPY  GDSCB
         END
