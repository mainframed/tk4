         TITLE 'IFFAEA02 - SGRAM - SET GRAPHIC MODE'
*STATUS: CHANGE LEVEL 000
*
*FUNCTION/OPERATION: SETS SWITCHES IN THE GDSCB TO INDICATE MODE
*        (ABSOLUTE, INCREMENTAL, OR OPTIMIZED) OF GRAPHIC OUTPUT FOR
*        A GDS.
*
*ENTRY POINTS: IFFAEA02
*
*INPUT:  REGISTER 1 POINTS TO A WORKAREA. THE FIRST WORD CONTAINS THE
*        ADDRESS OF THE PARAMETER LIST.  THE SECOND CONTAINS THE
*        ADDRESS OF THE RETURN ARRAY.
*
*        THE FIXED-LENGTH PARAMETER LIST IS
*     +0 A(POINTER TO THE GDSCB)
*
*     +4 A(GRAPHIC MODE CODE)
*
*OUTPUT: NONE TO USER. GDSCURMD SET AS APPROPRIATE.
*
*EXTERNAL ROUTINES: NONE
*
*EXITS-NORMAL AND ERROR: VIA RETURN MACRO TO DIRECTOR
*
*TABLES/WORKAREAS: GSP WORKAREA
*
*ATTRIBUTES: PROBLEM STATE, REENTRANT
*
IFFAEA02 CSECT
         SAVE  (14,12)
         BALR  GRBASE,0
         USING *,GRBASE           SET CSECT ADDRESSABILITY
         L     GRWORK,0(1)        GET PARAM LIST ADDR
         L     GRSLAVE,0(GRWORK)  GET POINTER TO GDSCB
         L     GRGDSCB,0(GRSLAVE) GET GDSCB ADDR
         USING GDSCB,GRGDSCB      SET GDSCB DSECT ADDRESSABILITY
         L     GRGSPCB,4(1)            GET ADDRESS OF GSPARRAY
         USING GSPARRAY,GRGSPCB        RETURN ARRAY AND NULL VARIABLE
*                                      ADDRESSABILITY
         MVI   GSPARRAY,X'00'     INDICATE SUCCESSFUL COMPLETION
         C     GRGDSCB,GDSGDSCB        IS DATA SET NAME VALID
         BNE   GRBADGDS           NO
         L     GRTASK,4(GRWORK)   GET GMODE ADDR
         L     GRTASK,0(GRTASK)        GET CODE
         CH    GRTASK,GR3         TEST FOR MODE OPTION
         BNE   GRTST2
         MVI   GDSCURMD,X'01'     SET GRAPHIC MODE - INCREMENTAL
         B     GRRETRN
GRTST2   CH    GRTASK,GR2         TEST FOR MODE OPTION
         BNE   GRTST1
         MVI   GDSCURMD,X'02'     SET GRAPHIC MODE - ABSOLUTE
         B     GRRETRN
GRTST1   CH    GRTASK,GR1         TEST FOR MODE OPTION
         BNE   GRPRMERR
         MVI   GDSCURMD,X'04'     SET GRAPHIC MODE - OPTIMIZED
         B     GRRETRN
GRBADGDS MVI   GSPARRAY,X'08'     INDICATE PARAM ERROR
         MVC   GSPARRAY+16(4),GR1      GDSCB NOT OPENED
         B     GRRETRN
GRPRMERR MVI   GSPARRAY,X'08'     INDICATE PARAM ERROR
         MVC   GSPARRAY+16(4),GR2      INVALID CODE
GRRETRN  L     GRWORK,GDSGTMCB         GET ADDRESS OF GTMCB
         USING GTMCB,GRWORK
         TM    GDSCURMD,GRINCR         IF ONLY ABSOLUTE ORDERS USED,
         BZ    GRRETRNA                THEN RETURN
         TM    GTMFEATS,GRMOD3         IS A MOD 3 AVAILABLE,
         BO    GRRETRNA                YES, INCREMENTAL ORDERS O.K.
         TM    GTMFEATS,GRMOD1         IS DEVICE A MOD 1
         BZ    GRRETRNB                NO, FORCE TO ABSOLUTE ORDERS
         TM    GTMFEATS+1,GRGDF        DOES IT HAVE GDF
         BO    GRRETRNA                YES, INCREMENTAL ORDERS O.K.
GRRETRNB MVI   GDSCURMD,GRABS          NO, FORCE TO ABSOLUTE
GRRETRNA RETURN (14,12),T
GR3      DC    H'3'
GR2      DC    H'2'
GR1      DC    H'1'
GRBASE   EQU   9
GRGSPCB  EQU   6
GRGDSCB  EQU   8
GRWORK   EQU   2
GRTASK   EQU   3
GRSLAVE  EQU   4
GRMOD1   EQU   X'01'
GRMOD3   EQU   X'03'
GRGDF    EQU   X'04'
GRABS    EQU   X'02'
GRINCR   EQU   X'05'
         COPY  GDSCB
         COPY  GTMCB
         COPY  GSPCB
         END
