*TITLE         INIGSP (PART2)
***********************************************************************
*                                                                     *
*TITLE              INGSP(PART 2)       INITIATE GSP                  *
*                                                                     *
*MODULE NAME        IFFAAA01                                          *
*                                                                     *
*ENTRY              IFFAAA01                                          *
*                                                                     *
*INPUT- SET UP BY INGSP PART 1. REGISTER 1 POINTS TO A PARAMETER LIST *
*   +0  ADDRESS OF A 1K STORAGE AREA SP=0                             *
*    FIRST WORD OF STORAGE AREA CONTAINS ADDRESS OF PARAMETER LIST    *
*    AS PASSED TO INGSP PART 1                                        *
*                                                                     *
*OUTPUT  ADDRESS OF GSPCB IN FIRST PARAMETER PASSED TO INGSP PART 1   *
*                                                                     *
*FUNCTION/OPERATION- THE FIELDS OF THE GSPCB ARE INITIALIZED. THE     *
*   STATUS TABLE (IFFAHA12) AND PART 2 OF THE DIRECTOR (IFFAHA11)     *
*   ARE LOADED FROM THE LINKLIB AND THEIR ADDRESSES PLACED IN THE     *
*   GSPCB.                                                            *
*EXTERNAL MACROS-LOAD                                                 *
*                                                                     *
*EXITS-RETURN TO CALLER VIA REGISTER 14                               *
*      ERROR- GSPNAME FIELD SET TO ZERO- RETURN VIA REGISTER 14       *
*                                                                     *
*TABLES/WORKAREAS- SEE GSPCB,PSECT DSECTS                             *
*                                                                     *
*ATTRIBUTES-REENTRANT                                                 *
*                                                                     *
***********************************************************************
         EJECT
*
*                          --REGISTER EQUATES
*
R0       EQU   0
R1P      EQU   1
R2WRK    EQU   2
R3WRK    EQU   3
R4WRK    EQU   4
R5WRK    EQU   5
R9BASE   EQU   9
RTWRK    EQU   10
R13      EQU   13
         EJECT
IFFAAA01 CSECT
         SAVE  (14,12)                 ENTRY HOUSEKEEPING
         BALR  R9BASE,0
         USING *,R9BASE
         LR    R2WRK,R1P               ESTABLISH GSPCB ADDRESSIBILITY
         USING GSPCB,R2WRK
         L     R3WRK,0(R2WRK)          GET ADDRESS OF USER PLIST
         USING PSECT,R3WRK
         XC    GSPCB(GSPCBLNG),GSPCB   CLEAR GSPCB TO ZERO
         ST    R13,TEMPSAVE+4          CHAIN SAVE AREAS
         LA    R13,TEMPSAVE            EXTABLIST SAVE AREA
         L     R4WRK,GSPNAME           GET ADDRESS OF GSP ADDR RTRNFLD
         ST    R2WRK,0(R4WRK)          STORE INTO RETURN FIELD
         TM    GSPNAME,X'80'           TEST IF GSPNAME IS ONLY INPUT,
         BO    AAA300                  BRANCH IF REQ PARMS NOT PRESENT
AAA020   L     R4WRK,NULLVAR           STORE NULL VARIABLE ADDRESS
         LA    R4WRK,0(R4WRK)          INTO GSPCB
         ST    R4WRK,GSPNULLV
*
*                          --INITIALIZE GSPCB
*
AAA080   MVC   GSPSYSID(2),IDFIELD     SET HALFWORD IDENT FIELD
         LA    R4WRK,GSPASTAT+8        WORK AREA FOLLOWS ADDR
         ST    R4WRK,GSPAWORK          OF STATUS TABLE
*
*                          --LOAD STATUS TABLE, DIRECTOR PART2
*
         L     R4WRK,16
         L     R4WRK,CVTLINK(R4WRK)    GET LINKLIB DCB FROM CVT
         LOAD  EP=IFFAHA12             LOAD STATUS TABLE
         ST    R0,GSPASTAT             STORE ADDRESS IN GSPCB
         LOAD  EP=IFFAHA11             LOAD DIRECTOR, PART 2
         ST    R0,GSPDCTR2             STORE ADDRESS IN GSPCB
*
*                          --GSPCB INITIALIZED, SET TO EXIT
*
AAA900   L     R13,TEMPSAVE+4
         RETURN (14,12),T
*
AAA300   L     R5WRK,GSPNAME
         XC    0(4,R5WRK),0(R5WRK)     SET VARIABLE TO ZERO IF INVALID
         B     AAA900
         EJECT
*
*                          --EQUATES AND CONSTANTS
*
CVTLINK  EQU   8
IDFIELD  DC    X'F201'                 IDENTIFIEL GSPCB
         EJECT
         COPY  GSPCB
*
*                          --DUMMY SECTIONS
*
         EJECT
*WORK AREA FOLLOWS GSPCB
GSPCBLNG EQU   *-GSPCB
TEMPSAVE DS    9D            USER WORK AREA FOR SAVE AREA
         EJECT
PSECT    DSECT                         PARAMETER LIST DSECT
GSPNAME  DS    A
NULLVAR  DS    A
         END
