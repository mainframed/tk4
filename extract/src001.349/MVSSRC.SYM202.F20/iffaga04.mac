         TITLE 'IFFAGA04 - ENTRK - END LIGHT-PEN TRACKING'
*STATUS: CHANGE LEVEL 000
*
*FUNCTION/OPERATION: REMOVES THE TRACKING SYMBOL FROM THE DEVICE
*        ASSOCIATED WITH THE GDS PASSED.
*
*ENTRY POINTS: IFFAGA04
*
*INPUT:  REGISTER 1 POINTS TO A WORKAREA.  THE FIRST WORD CONTAINS THE
*        ADDRESS OF THE PARAMETER LIST, AND THE SECOND, THAT OF THE
*        RETURN CODE ARRAY.  THE FIXED-LENGTH PARAMETER LIST IS
*    + 0 A(POINTER TO THE GDSCB)
*
*OUTPUT: NONE TO THE USER.  THE SYSTEM GDS IS RESET, REMOVING THE
*        TRACKING ROUTINE FROM THE DEVICE.
*
*EXTERNAL ROUTINES: RESET
*
*EXITS-NORMAL AND ERROR: VIA RETURN MACRO TO DIRECTOR
*
*TABLES/WORKAREAS: GSP WORKAREA
*
*ATTRIBUTES: PROBLEM STATE, REENTRANT
*
IFFAGA04 CSECT
         SAVE  (14,12)
         BALR  ETK09,0                 CSECT ADDRESSABILITY
         USING *,ETK09
         LR    ETK10,ETK01             WORKAREA ADDRESSABILITY
         USING ENDTRK,ETK10
*
         LA    ETK02,ETKSAVE           CHAIN SAVE AREAS
         ST    ETK02,MSVA(ETK13)
         ST    ETK13,HSVA(ETK02)
         LR    ETK13,ETK02             ESTABLISH NEW SAVE AREA
*
         L     ETK08,ETKPARM           GET ADDRESS OF PARAMETER LIST
         L     ETK08,0(ETK08)          GET POINTER TO GDSNAME
         L     ETK08,0(ETK08)          GET ADDRESS OF GDSCB
         USING GDSCB,ETK08             GDSCB ADDRESSABILITY
         L     ETK07,ETKRTNA
         USING GSPARRAY,ETK07          RETURN ARRAY ADDRESSABILITY
*
         C     ETK08,GDSGDSCB          VALID GDS
         BNE   ETK002                  NO, ERROR RETURN
*
         L     ETK06,GDSGTMCB          YES, GET GTMCB
         USING GTMCB,ETK06
*
         TM    GTMFLAGS,TRKFLG         TRACKING IN
         BZ    ETK003                  NO, INVALID CALL
*
         LA    ETK02,GTMSYGDS          YES, GET ADDRESS OF POINTER TO
         ST    ETK02,ETKRSGDS          THE SYSTEM GDS
         OI    ETKRSGDS,X'80'
         LA    ETK02,ETKRSGDS          SET UP PARAMETERS FOR RESET
         ST    ETK02,ETKRSPRM
         MVC   ETKRSRTN(4),ETKRTNA
         XC    ETKSUPV(16),ETKSUPV     CLEAR SUPERVISOR WORK AREA
*
         LA    ETK01,ETKRSPRM
         LA    ETK15,ETKSUPV
         LINK  EP=IFFAFA12,MF=(E,(1)),SF=(E,(15)) CALL RESET
         NI    GTMFLAGS,TRKFLGO        TURN OFF FLAG BIT
         XC    GTMREPOS(2),GTMREPOS    RESET REPOSITIONING ADDRESS=0
         MVI   GSPARRAY,X'00'          CLEAR RETURN CODE
*
ETK001   L     ETK13,HSVA(ETK13)       RETURN
         RETURN (14,12),T,RC=0
*
ETK002   OI    GSPARRAY,PARERR         SET RETURN CODE
         MVC   GSPARRAY+16(4),ETK0001  BAD GDSCB
         B     ETK001
*
ETK003   OI    GSPARRAY,PARERR         CALLED WITHOUT TRACKING IN
         XC    GSPARRAY+16(4),GSPARRAY+16
         B     ETK001
*
TRKFLG   EQU   X'08'
TRKFLGO  EQU   X'F7'
MSVA     EQU   8
HSVA     EQU   4
PARERR   EQU   X'08'
*
ETK0001  DC    F'1'
*
ETK01    EQU   1                       REGISTER EQUATES
ETK02    EQU   2
ETK06    EQU   6                       REGISTERS ETK06,ETK07,ETK08,
ETK07    EQU   7                       ETK09, AND ETK10 ARE
ETK08    EQU   8                       BASE REGISTERS
ETK09    EQU   9
ETK10    EQU   10
ETK13    EQU   13
ETK15    EQU   15
*
ENDTRK   DSECT
ETKPARM  DS    F
ETKRTNA  DS    F
ETKSAVE  DS    18F
ETKSUPV  DS    4F
ETKRSGDS DS    F
ETKRSPRM DS    F
ETKRSRTN DS    F
*
         COPY  GSPCB
         COPY  GTMCB
         COPY  GDSCB
         END
