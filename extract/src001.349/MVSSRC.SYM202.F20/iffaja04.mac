* TITLE:ITST-TEST STATUS
* STATUS:CHANGE LEVEL 0
* FUNCTION:TO RETURN TO THE USER A VALUE CORRESPONDING TO THE TYPE OF
*          DATA CURRENTLY IN EFFECT FOR A PARTICULAR OPTION DEFINITION
*          SUBROUTINE FOR THE ASSOCIATED GRAPHIC DATA SET.
* ENTRY:ENTERED AT IFFAJA04 VIA A CALL TO ITST.
* INPUT:GR#1 CONTAINS THE ADDRESS OF A WORK/SAVE AREA.THE FIRST WORD OF
*       THIS AREA CONTAINS THE ADDRESS OF A PARAMETER LIST CONSTRUCTED
*       AS FOLLOWS:
*        1.GDSNAME-ADDRESS OF A VARIABLE WHICH CONTAINS THE ADDRESS OF
*          A GDSCB.
*        2.OPTIONSUBRTN-ADDRESS OF A VARIABLE OR CONSTANT WHOSE VALUE
*          INDICATES ONE OF THE FOLLOWING:
*         (1)SDATM SUBROUTINE(X COORDINATE)
*         (2)SDATM SUBROUTINE(Y COORDINATE)
*         (3)SGRAM SUBROUTINE
*         (4)SCHAM SUBROUTINE
* OUTPUT:GR#0 WILL CONTAIN ONE OF THE FOLLOWING VALUES:
*        SUBRTN  VALUE  MEANING
*        ******  *****  ***********************************************
*        SDATM    1     X(Y) IS REAL,ABSOLUTE
*                 2     X(Y) IS REAL,INCREMENTAL
*                 3     X(Y) IS INTEGER,ABSOLUTE
*                 4     X(Y) IS INTEGER,INCREMENTAL
*        SGRAM    1     OUTPUT OPTIMIZED
*                 2     OUTPUT ABSOLUTE
*                 3     OUTPUT INCREMENTAL
*        SCHAM    1     CHARACTERS ARE BASIC SIZE AND PROTECTED
*               2       CHARACTERS ARE LARGE SIZE AND PROTECTED
*                 3     CHARACTERS ARE BASIC SIZE AND UNPROTECTED
*                 4     CHARACTERS ARE LARGE SIZE AND UNPROTECTED
* EXIT:RETURN TO CALLER VIA REGISTER 14.
* ERROR EXIT:IN THE EVENT OF INVALID PARAMETERS OR REQUESTED INFORMATIN
*            NOT AVAILABLE,A VALUE OF 0 WILLBE RETURNED IN GR#0.
* ATTRIBUTES:RE-ENTRANT,PROBLEM PROGRAM MODE
* NOTES:THIS ROUTINE EXAMINES THE FOLLOWING CONGROL BLOCKS:
*       1.GDSCB
*       2.GTMCB
*       3.GSPCB
R0       EQU   0                       PARM REG 0
R1       EQU   1                       PARM REG 1
RLIST    EQU   2                       WORK AREA REG
RPARM    EQU   3                       USER PARAMETERS REG
RDATA    EQU   4                       DATA REG
RA       EQU   5                       WORK REG
RB       EQU   6                       WORK REG
RGDSCB   EQU   7                       GDSCB REG
RBASE    EQU   9                       BASE REG
RSAVE    EQU   13                      SAVE REG
PRIOR    EQU   4                       PREVIOUS SAVE AREA
REG0     EQU   20                      OFFSET TO REG 0
XRA      EQU   X'80'                   X-REAL-ABSOLUTE
XRI      EQU   X'40'                   X-REAL-INCREMENTAL
XIA      EQU   X'20'                   X-INT-ABSOLUTE
XII      EQU   X'10'                   X-INT-INCREMENTAL
YRA      EQU   X'08'                   Y-REAL-ABSOLUTE
YRI      EQU   X'04'                   Y-REAL-INCREMENTAL
YIA      EQU   X'02'                   Y-INT-ABSOLUTE
YII      EQU   X'01'                   Y-INT-INCREMENTAL
BP       EQU   X'08'                   BASIC CHAR-PROTECTED
BUP      EQU   X'02'                   BASIC CHAR-UNPROTECTED
LP       EQU   X'04'                   LARGE CHAR-PROTECTED
LUP      EQU   X'01'                   LARGE CHAR-UNPROTECTED
OPTI     EQU   X'04'                   OUTPUT OPTIMIZED
ABS      EQU   X'02'                   OUTPUT ABSOLUTE
INC      EQU   X'01'                   OUTPUT INCREMENTAL
LAST     EQU   X'80'                   LAST PARAM
ZERO     EQU   0                       ZERO
GID      EQU   X'03'                   GDSCB ID
IFFAJA04 CSECT
         SAVE  (14,12)                 SAVE REGS
         BALR  RBASE,0                 SET UP BASE REG
         USING *,RBASE                 DEFINE BASE REG
         LR    RLIST,R1                LOAD REG
         USING WORKAREA,RLIST          DEFINE DSECT
         L     RPARM,PLIST             LOAD ADR OF USER PARM LIST
         USING PARM,RPARM              DEFINE DSECT
         L     RDATA,PRIOR(RSAVE)      LOAD ADR OF PREVIOUS SAVE AREA
         B     AROUND                  BRANCH
ERROR    SR    RB,RB                   INDICATE ERROR
OUTRTN   ST    RB,REG0(RDATA)          STORE DATA
         RETURN (14,12),T              RETURN TO CALLER
AROUND   TM    GDS,LAST                LAST PARAM
         BO    ERROR                   BRANCH IF YES
         L     RGDSCB,GDS              LOAD INDIRECT PTR TO GDSCB
         L     RGDSCB,ZERO(RGDSCB)     LOAD GDSCB ADR
         USING GDSCB,RGDSCB            DEFINE DSECT
         CLI   GDSGDSID,GID            VALID GDSCB
         BNE   ERROR                   BRANCH-NOT VALID
         L     RA,GDSGTMCB             LOAD GTMCB ADR
         USING GTMCB,RA                DEFINE DSECT
         L     RB,GTMGSPCB             LOAD GSPCB ADR
         USING GSPCB,RB                DEFINE DSECT
         DROP  RA
         L     RA,OPT                  LOAD OPTSUBROUTINE VALUE PTR
         LA    RA,ZERO(RA)             ZERO HI ORDER BYTE
         CL    RA,GSPNULLV             EQUAL NULL ADR
         BE    ERROR                   BRANCH=NULL
         DROP  RB
         L     RA,ZERO(RA)             LOAD VALUE
         CL    RA,ONE                  VALUE=1
         BE    IT05                    BRANCH TO SDATM X CO-OR CHECKS
         CL    RA,TWO                  VALUE=2
         BE    IT06                    BRANCH TO SDATM Y CO-OR CHECKS
         CL    RA,THREE                VALUE=3
         BE    IT07                    BRANCH TO SGRAM CHECKS
         CL    RA,FOUR                 VALUE=4
         BE    IT08                    BRANCH TO SCHAM CHECKS
         B     ERROR                   BRANCH
IT01     L     RB,ONE                  LOAD CONSTANT
         B     OUTRTN                  BRANCH
IT02     L     RB,TWO                  LOAD CONSTANT
         B     OUTRTN                  BRANCH
IT03     L     RB,THREE                LOAD CONSTANT
         B     OUTRTN                  BRANCH
IT04     L     RB,FOUR                 LOAD CONSTANT
         B     OUTRTN                  BRANCH
IT05     TM    GDSDATMD,XRA            X-REAL-ABS
         BO    IT01                    BRANCH IF YES
         TM    GDSDATMD,XRI            X-REAL-INC
         BO    IT02                    BRANCH IF YES
         TM    GDSDATMD,XIA            X-INT-ABS
         BO    IT03                    BRANCH IF YES
         TM    GDSDATMD,XII            X-INT-INC
         BO    IT04                    BRANCH IF YES
         B     ERROR                   BRANCH
IT06     TM    GDSDATMD,YRA            Y-REAL-ABS
         BO    IT01                    BRANCH IF YES
         TM    GDSDATMD,YRI            Y-REAL-INC
         BO    IT02                    BRANCH IF YES
         TM    GDSDATMD,YIA            Y-INT-ABS
         BO    IT03                    BRANCH IF YES
         TM    GDSDATMD,YII            Y-INT-INCR
         BO    IT04                    BRANCH IF YES
         B     ERROR                   BRANCH
IT07     TM    GDSCURMD,OPTI           OUTPUT OPTIMIZE
         BO    IT01                    BRANCH IF YES
         TM    GDSCURMD,ABS            OUTPUT ABSOLUTE
         BO    IT02                    BRANCH IF YES
         TM    GDSCURMD,INC            OUTPUT INCREMENTAL
         BO    IT03                    BRANCH IF YES
         B     ERROR                   BRANCH
IT08     TM    GDSCHARS,BP             CHARS=BASIC PROTECTED
         BO    IT01                    BRANCH IF YES
         TM    GDSCHARS,LP             CHARS=LARGE PROTECTED
         BO    IT02                    BRANCH IF YES
         TM    GDSCHARS,BUP            CHARS=BASIC-UNPROTECTED
         BO    IT03                    BRANCH IF YES
         TM    GDSCHARS,LUP            CHARS=LARGE=UNPROTECTED
         BO    IT04                    BRANCH IF YES
         B     ERROR                   BRANCH
         DS    0F
ONE      DC    A(1)
TWO      DC    A(2)
THREE    DC    A(3)
FOUR     DC    A(4)
         CNOP  0,8
WORKAREA DSECT
PLIST    DS    1F
PARM     DSECT
GDS      DS    1F
OPT      DS    1F
         COPY  GDSCB
         COPY GTMCB
         COPY  GSPCB
         END
