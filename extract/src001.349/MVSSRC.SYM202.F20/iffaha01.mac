         TITLE 'IFFAHA01 - FLOWCTL - FLOW CONTROL MANAGEMENT'      1720
*
*                        FLOW CONTROL MANAGEMENT
*
*        FLOW CONTROL MANAGEMENT IS ENTERED FROM SEVERAL ROUTINES FOR
*        SEVERAL DIFFERENT PURPOSES.
*
*        CODE 1
*        ENTRY AT CODE 1 IS MADE TO SET UP THE FLOW CONTROL TABLE
*        AND BUILD THE FLOW CONTROL STRUCTURE.  IT IS CALLED BY
*        INDEV-IFFAAA03.  REGISTER ONE CONTAINS THE ADDRESS OF A
*        WORK AREA.  THE FIRST WORD OF THE WORK AREA CONTAINS THE
*        ADDRESS OF THE PARAMETER LIST CONSISTING OF THE FOLLOWING:
*
*        +0   A(1)
*        +4   A(GTMCB)
*
*        CODE 2
*        ENTRY AT CODE 2 IS MADE TO ADD A GDS TO THE FLOW CONTROL
*        STRUCTURE IN THE BUFFER.  IT IS CALLED BY DATA STORE-
*        IFFAHA05.  THE PARAMETER LIST IS LISTED BELOW:
*
*        +0   A(2)
*        +4   A(GDSCB)
*        +8   A(BUFFER)
*
*        CODE 3
*        CODE 3 IS ENTERED FROM TMGDS-IFFAAA06 TO REMOVE A GDS
*        FROM THE FLOW CONTROL STRUCTURE.  THE PARAMETER LIST
*        IS AS FOLLOWS:
*
*        +0   A(3)
*        +4   A(GDSCB)
*
*        CODE 4
*        CODE 4 IS ENTERED FROM TMDEV-IFFAAA04 TO DELETE THE FLOW
*        CONTROL STRUCTURE AND THE FLOW CONTROL TABLE.  THE PARAMETER
*        LIST CONSISTS OF THE FOLLOWING:
*
*        +4   A(4)
*        +4   A(GTMCB)
*
*        THE SYSTEM DECB IS SET UP WHEN THE FLOW CONTROL TABLE IS
*        INITIALIZED AND IS USED FOR THE FIRST TIME BY FLOW CONTROL
*        WHICH LEAVES THE LAST I/O PENDING.  WHEN FLOW CONTROL IS
*        CALLED TO TERMINATE THE LAST I/O IS WAITED ON BEFORE THE
*        DECB IS DESTROYED.
*
*        MACROS USED ARE:
*
*        ASGNBFR
*        GETMAIN
*        GWRITE
*        WAIT
*        GCNTRL
*        FREEMAIN
*        RLSEBFR
*
*
*        THE FLOW CONTROL TABLE APPEARS AS FOLLOWS:
*       *************************************
*     +0*        A(FREE ENTRY)              *
*     +4*          IN USE LENGTH 2 PER ENTRY*
*     +8*A(BUFFER)       *  BUFFER+6        *  ENTRIES
*    +12*   BUFFER+16    *  BUFFER+26       *
*    +16*   BUFFER+36    *  BUFFER+46       *
*    +20*   BUFFER+56    *  BUFFER+66       *
*    +24*   BUFFER+76    *  BUFFER+86       *
*    +28*   BUFFER+96    *  BUFFER+106      *
*    +32*   BUFFER+116   *  BUFFER+126      *
*    +36*   BUFFER+136   *  BUFFER+146      *
*    +40*   BUFFER+156   *  BUFFER+166      *
*    +44*   BUFFER+176   *  BUFFER+186      *
*    +48*   BUFFER+196   *  BUFFER+206      *
*    +52*   BUFFER+216   *  BUFFER+226      *
*    +56*   BUFFER+236   *  BUFFER+246      *
*       *************************************
         EJECT
IFFAHA01 CSECT
*2412,562000-564000                                                5743
*2131,002000,790000                                                1720
*
*        REGARDLESS OF THE REASON OF THIS CALL THE CODES PERFORM THE
*        FOLLOWING: THE BASE REGISTER IS INITIALIZED AND THE WORK
*        AREA DSECT IS SET UP. THE SAVE AREAS ARE CHAINED. THE REASON
*        FOR ENTRY IS DETERMINED AND DEPENDING ON THE SERVICE DESIRED,
*        CONTROL IS TURNED OVER TO A SPECIFIC SECTION OF THE PROGRAM
FLOWCTL  SAVE  (14,12)
         BALR  BASE,0
         USING *,BASE
LABEL    LR    WORK,PARM           ADDRESS OF WORK AREA
         USING WORKAREA,WORK
         L     PARM,WRKPARM        PARAMETER LIST
         ST    SAVE,WRKSAVE+4      SET UP SAVE AREA CHAIN
         LA    REGA,WRKSAVE
         ST    REGA,8(SAVE)
         LR    SAVE,REGA           PLACE MY SAVE AREA IN THE REGISTER
         CLI   CODE(PARM),X'01'    CODE 1
         BE    FLC00010
         CLI   CODE(PARM),X'02'    CODE 2
         BE    FLC00040
         CLI   CODE(PARM),X'03'    CODE 3
         BE    FLC00050
         CLI   CODE(PARM),X'04'    CODE 4
         BE    FLC00100
         B     FLC00030
         EJECT
**
**
**       CODE 1
**       SET UP FLOW CONTROL STRUCTURE AT GTMCB OPEN TIME.
**
**
*
*        TO SET UP THE FLOW CONTROL STRUCTURE AND TABLE THE REQUIRED
*        BUFFER, 256 BYTES, IS REQUESTED, AND THE REQUIRED STORAGE
*        FOR THE TABLE IS OBTAINED.
*
FLC00010 L     GTMREG,4(PARM)      ADDR OF TERM CTL BLK
         USING GTMCB,GTMREG
         LA    REGA,256            LENGTH REQD
         STH   REGA,WRKSPACE+8
         L     REGA,GTMGRDCB
         SPACE 3                                                   1720
         ASGNBFR (REGA),WRKSPACE+8,MF=(E,WRKSPACE)  GET BUFFER
         LTR   15,15
         BNE   ERR1                NO BUFFER AVAILABLE
         SPACE 3                                                   1720
         GETMAIN EC,LV=FLCTLTBL,A=WRKSPACE+12,SP=0,MF=(E,WRKSPACE)
         LTR   15,15
         EJECT                                                     1720
         BNE   ERR2                NO CORE AVAILABLE
*
*        THE TABLE IS INITIALIZED WITH POINTERS TO THE FREE LIST
*        AND THE LENGTH OF THE INUSE LIST. THE INUSE LIST IS
*        INITIALLY TWO BECAUSE OF THE FIRST ENTRY BEING RESERVED
*        FOR THE SYSTEM GDS. THEN THE ADDRESSES IN THE BUFFER
*        OBTAINED WHICH WILL BE AVAILABLE FOR EACH ENTRY ARE
*        COMPUTED AND PLACED IN THE TABLE.
*
         L     TABLE,WRKSPACE+12   ADDR OF CORE OBTAINED
         LA    REGA,12(TABLE)      FREE LIST STARTS TWELVE BEYOND
         ST    REGA,FREE(TABLE)    PTR SET
         LA    REGA,2
         ST    REGA,INUSE(TABLE)   LENGTH OF INUSE LIST
         L     REGA,GTMGRDCB       ADDR OF DCB
         LH    REGB,16(REGA)       ADDR OF BUFFER OBTAINED
         STH   REGB,BUFADR(TABLE)  IN TABLE
         LA    REGA,TBLEND(TABLE)  DECB ADDR
         ST    REGA,GTMDECB1       IN GTMCB
         LA    REGB,6(REGB)        DUMMY ENTRY BUFFER ADDR
         STH   REGB,DUMMY(TABLE)   IN TABLE
         LA    REGA,24             COUNT
         LA    REGC,DUMMY+2        ADDR OF FREE ELEMENTS
FLC00020 LA    REGB,10(REGB)       EACH TEN BYTES BEYOND LAST
         STH   REGB,0(REGC,TABLE)  STORE BUFFER ADDR
         LA    REGC,2(REGC)        NEXT IN TABLE
         BCT   REGA,FLC00020       DO ALL 24 ENTRIES
         EJECT                                                     1720
**
**       THE TABLE HAS BEEN INITIALIZED. THE BUFFER MUST NOW BE INIT.
**
*
*        THE SKELETON FOR THE FLOW CONTROL STRUCTURE ARE
*        PLACED IN THE WORK AREA AND THE ADDRESSES ARE RESOLVED.
         MVC   WRKSPACE(16),FIRSTORD MOVE SKELETON ORDERS IN WORK AREA
         LH    REGB,BUFADR(TABLE)
         STH   REGB,WRKSPACE+14    BUFFER ADDR IN LAST GTRU
         LA    REGB,6(REGB)        ADDRESS TO GO IN 1ST GTRU
         STH   REGB,WRKSPACE+4     PLACE IN WORK AREA
         LA    REGB,6(REGB)        DUMMY THE SYS GDS
         STH   REGB,WRKSPACE+10
         EJECT
*
*        BUILD DECB BELOW FLOW CONTROL TABLE
*
*
*        THE DECB IS BUILT BEYOND THE FLOW CONTROL TABLE. THIS
*        WILL SERVE AS THE SYSTEM DECB FOR THE DATA GENERATING
*        ROUTINES WHILE THIS 2250 IS OPEN. THE FLOW CONTROL
*        STRUCTURE IS WRITTEN TO THE BUFFER AND THE I/O IS NOT
*        WAITED UPON. THIS IS THE START OF THE I/O OVERLAP. FUTURE
*        ROUTINES THAT WILL DO I/O USING THIS DECB WILL FIRST WAIT
*        AND THEN ISSUE THEIR I/O. ADDRESS OF THE DECB AND
*        THE FOLOW CONTROL TABLE ARE PLACED IN THE GTMCB AND
*        CONTROL IS RETURNED TO THE CALLING PROGRAM.
         XC    TBLEND(32,TABLE),TBLEND(TABLE) ZERO OUT DECB
         LA    REGB,TBLEND(TABLE)  DECB ADDR
         L     REGA,GTMGRDCB       DCB ADDR
         LA    REGC,BUFADR(TABLE)
         LR    PARM,REGB
         SPACE 3                                                   1720
         GWRITE (PARM),BUF,(REGA),16,WRKSPACE,(REGC),MF=E
         ST    TABLE,GTMFCTBL      SAVE ADDR OF FLOW CTL TABLE
FLC00030 L     SAVE,4(SAVE)
         RETURN (14,12),RC=0       RETURN
         EJECT
**
**
**       CODE 2
**       ADD A GDS
**
**
*
*        TO ADD A GDS TO THE FLOW CONTROL STRUCTURE THE GDSCB AND
*        GTMCB DSECTS ARE FIRST INITIALIZED. THE ADDRESS OF THE
*        FLOW CONTROL TABLE IS TAKEN FROM THE GTMCB AND ALSO
*        THE DCB ADDRESS AND THE DECB ADDRESS. A CHECK IS MADE
*        TO SEE IF THIS GDS IS THE SYSTEM GDS AND IF SO IT IS
*        ADDED TO THE TOP OF THE TABLE. OTHERWISE A CHECK IS
*        MADE TO SEE IF ALL THE FLOW CONTROL ENTRIES HAVE BEEN
*        USED.  IF YES THIS IS AN ERROR CONDITION AND CONTROL IS
*        RETURNED TO THE CALLING PROGRAM WITH A RETURN CODE.
FLC00040 L     GDSREG,GDS(PARM)    GDSCB ADDR
         USING GDSCB,GDSREG
         L     GTMREG,GDSGTMCB     ADDRESS OF GTMCB
         L     TABLE,GTMFCTBL      FLOW CTL TABLE
         L     REGA,INUSE(TABLE)   COUNT OF INUSE ELEM
         L     REGC,GTMDECB1       DECB
         L     REGE,GTMGRDCB       DCB ADDR
         C     GDSREG,GTMSYGDS     SYS GDS
         BE    FLC00045            YES
         C     REGA,INUSELEN       ALL USED
         BE    ERR1                YES- ERROR
         EJECT                                                     1720
*
*        IF THERE IS A FREE ELEMENT THE ADDRESS IS REMOVED FROM THE
*        TABLE AND THE ENTRY IS ADDED TO THE IN USE LIST AND A COUNT
*        OF TWO IS ADDED TO THE INUSE COUNT.  THE SKELETON ORDERS
*        FOR THE FLOW CONTROL ENTRY ARE MOVED INTO THE WORK AREA AND
*        THE ADDRESSES IN THE BUFFER ARE RESOLVED.  THE PREVIOUS
*        I/O CALL IS WAITED UPON AND THE FLOW CONTROL ENTRY IS
*        WRITTEN TO THE BUFFER.
         L     REGB,FREE(TABLE)    FREE ELEMENT ADDR
         LA    REGA,2(REGA)
         ST    REGA,INUSE(TABLE)   UPDATE INUSE COUNT
         LA    REGA,2(REGB)
         ST    REGA,FREE(TABLE)    FIX FREE PTR
         LA    ENTRY,4
         SR    REGA,ENTRY          POSITION TO PREV ENTRY
         MVC   WRKSPACE(10),ENTRYORD MOVE IN SKELETON ORDERS
         MVC   WRKSPACE+4(2),10(PARM) MOVE IN GDS BUFFER ADDR
         MVC   WRKSPACE+8(2),BUFADR(TABLE) MOVE IN RETURN TO F/C
         LR    PARM,REGC
         WAIT  ECB=(1)             WAIT ON I/O
         CLI   0(REGC),X'7F'
         BNE   ERR3
         XC    0(4,REGC),0(REGC)   ZERO ECB
         LR    PARM,REGC
         GWRITE (PARM),BUF,(REGE),10,WRKSPACE,(REGB),MF=E
         EJECT
*
*        THE RETURN ADDRESS TO THE FLOW CONTROL ENTRY FROM THE GDS
*        BUFFER IS COMPUTED AND PLACED IN THE GDSOVDAT FIELD OF THE
*        GDSCB AS IS THE START OF THE FLOW CONTROL ENTRY PLACED IN
*        THE GDSFCBUF FIELD.  THE PREVIOUS I/O IS WAITED ON AND THE
*        ADDRESS OF THIS ENTRY IS WRITTEN OUT TO THE PREVIOUS
*        ENTRY SO THAT IT IS ADDED TO THE FLOW.  IF THIS IS
*        SYSTEM GDS THIS IS NOT NECESSARY BECAUSE THE FLOW CONTROL
*        ENTRY FOR THE SYSTEM GDS ALREADY GOES TO THE NEXT ENTRY
*        IN THE STRUCTURE.
*
         LH    REGB,0(REGB)
FLC00042 STH   REGB,GDSFCBUF       ADDR OF FLOW CTL ENTRY
         STH   REGB,GDSFCBUF       ADDR OF FLOW CTL ENTRY
         LA    REGB,6(REGB)
         STH   REGB,GDSOVDAT+2     RETURN PT IN FLOW CTL ENTRY
         LR    PARM,REGC
         WAIT  ECB=(1)             WAIT ON I/O
         CLI   0(REGC),X'7F'
         BNE   ERR3
         XC    0(4,REGC),0(REGC)   ZERO ECB
         C     GDSREG,GTMSYGDS
         BE    FLC00044
         LH    REGB,0(REGA)        ADDR OF PREV ENTRY
         LA    REGB,8(REGB)        ADDR OF TRANS LOC
         LA    REGA,2(REGA)        ADDR OF DATA
         STH   REGB,WRKSPACE
         LR    PARM,REGC
         GWRITE  (PARM),BUF,(REGE),2,(REGA),WRKSPACE,MF=E
         LR    PARM,REGC
         WAIT  ECB=(1)             WAIT ON I/O
         CLI   0(REGC),X'7F'
         BNE   ERR3
         XC    0(4,REGC),0(REGC)   ZERO ECB
         EJECT                                                     1720
*
*        THE PREVIOUS I/O IS WAITED ON AND A GTRU BACK TO THE
*        FLOW CONTROL ENTRY IS WRITTEN OUT TO THE USER BUFFER
*        SO THAT THE CYCLE IS COMPLETE.  WITH THIS CALL REGENERATION
*        OF THE BUFFER IS STARTED.
*
FLC00044 LR    PARM,REGC
         L     REGB,WRKPARM                                        5743
         L     REGC,GDSAOACB       ADDR OF OACB                    5743
* PLACE ADDR OF BUFFER INTO OACB TO REFERENCE FOR THE WRITE        5743
         MVC   BUFSTART+2(2,REGC),10(REGB)                         5743
         LA    REGB,BUFSTART+2(REGC)                               5743
         LA    REGC,BUFADR(TABLE)
         GWRITE (PARM),STR,(REGE),4,GDSOVDAT,(REGB),(REGC),MF=E
         B     FLC00030
*
         EJECT                                                     1720
*        FOR THE SYSTEM GDS THE FLOW CONTROL ENTRY ALREADY EXISTS.
*        THEREFORE, THE ADDRESS OF THE SYSTEM GDS BUFFER IS ALL THAT
*        IS NEEDED TO COMPLETE THE ENTRY AND THAT ADDRESS IS WRITTEN
*        TO THE BUFFER AFTER THE PREVIOUS I/O IS WAITED UPON.
FLC00045 LH    REGB,DUMMY(TABLE)   ADDR OF DUMMY FOR SYSGDS
         LA    REGF,4(REGB)        ADDR TO WRITE TO
         STH   REGF,WRKSPACE+2
         MVC   WRKSPACE(2),10(PARM) BUFFER ADDR TO BE WRITTEN IN DUMMY
         LR    PARM,REGC
         WAIT  ECB=(1)             WAIT ON I/O
         CLI   0(REGC),X'7F'
         BNE   ERR3
         XC    0(4,REGC),0(REGC)   ZERO ECB
         LR    PARM,REGC
         GWRITE (PARM),BUF,(REGE),2,WRKSPACE,WRKSPACE+2,MF=E
         B     FLC00042
         EJECT
**
**
**       CODE 3
**       DELETE A GDS
**
**
*
*        WHEN A CALL IS MADE TO DELETE THE GDS A CHECK IS FIRST MADE
*        TO SEE IF THE FLOW CONTROL STRUCTURE IS ALREADY DELETED.  THIS
*        MAY OCCUR WHEN THE GDS IS BEING TERMINATED BY TMDEV AND
*        TMDEV HAS ALREADY DELETED THE FLOW CONTROL STRUCTURE.
*        A CHECK IS MADE TO SEE IF A FLOW CONTROL ENTRY WAS ADDED
*        FOR THIS GDS.  TWO IS SUBTRACTED FROM THE INUSE COUNT AND THE
*        ELEMENT IS REMOVED FROM THE INUSE LIST.  THE ADDRESS OF THE
*        ENTRY BEFORE AND AFTER IS SAVED.  THE ADDRESS OF THE ENTRY
*        AFTER THIS ONE (OR THE START OF THE TABLE IF THIS IS THE
*        LAST ENTRY) IS WRITTEN IN THE ENTRY PRECEEDING THIS ONE.
*        THUS THIS ENTRY IS BYPASSED IN THE REGENERATION.
FLC00050 L     GDSREG,4(PARM)      ADDR OF GDSCB
         L     GTMREG,GDSGTMCB     ADDR OF THE GTMCB
         L     TABLE,GTMFCTBL      FLOW CONTROL TABLE
         LTR   TABLE,TABLE         ALREADY CLOSED?
         BE    FLC00030            YES, EXIT
         LH    REGB,GDSFCBUF       ADDR OF FLOW CONTROL ENTRY
         LTR   REGB,REGB           WAS AN ENTRY MADE
         BZ    FLC00030            NO, EXIT
         LA    REGA,10             FIRST IN USE ELEMENT
         L     REGE,INUSE(TABLE)   COUNT
         S     REGE,TWO            LESS TWO
         ST    REGE,INUSE(TABLE)   NEW COUNT
FLC00060 CH    REGB,2(REGA,TABLE)  FOUND ? SKIP 1 ELEM - DUMMY NEVER DL
         BE    FLC00070
         LA    REGA,2(REGA)        NEXT
         S     REGE,TWO
         B     FLC00060
FLC00070 LH    REGC,0(REGA,TABLE)  ONE BEFORE
         LA    ENTRY,4(REGA,TABLE)
         C     ENTRY,FREE(TABLE)
         BE    FLC00090
         LH    ENTRY,4(REGA,TABLE)      ONE AFTER
FLC00080 AR    REGA,TABLE
         BCTR  REGE,0              SUBTRACT 1 FOR EX OF MOVE
         EX    REGE,MOVE           MOVE UP TABLE
         L     REGE,FREE(TABLE)    ADDR OF FREE
         S     REGE,TWO            ADD ONE ENTRY
         STH   REGB,0(REGE)        ADD TO FREE LIST
         ST    REGE,FREE(TABLE)    UPDATE ADDRESS
         STH   ENTRY,WRKSPACE
         L     PARM,GTMDECB1
         WAIT  ECB=(1)             WAIT ON I/O
         EJECT
         LA    REGC,8(REGC)
         STH   REGC,WRKSPACE+4
         L     REGB,GTMGRDCB       LOAD DCB ADDR
         LA    REGA,BUFADR(TABLE)  START ADDR
         L     PARM,GTMDECB1
         CLI   0(PARM),X'7F'
         BNE   ERR3
         NI    0(PARM),X'BF'
         GWRITE (PARM),BUF,(REGB),2,WRKSPACE,WRKSPACE+4,MF=E
         L     PARM,GTMDECB1
         WAIT  ECB=(1)
         L     PARM,GTMDECB1
         CLI   0(PARM),X'7F'
         BNE   ERR3
         NI    0(PARM),X'BF'
         GCNTRL (PARM),STR,(REGB),(REGA),MF=E
*
         EJECT                                                     1720
*        THE GDSOVDAT AND GDSFCBUF FIELDS IN THE GDSCB ARE SET TO
*        ZERO TO INDICATE THAT THERE IS NO FLOW CONTROL ENTRY FOR
*        THIS GDS AND CONTROL IS RETURNED TO THE CALLING PROGRAM.
         XC    GDSOVDAT(4),GDSOVDAT ZERO GDSCB FIELDS
         XC    GDSFCBUF(2),GDSFCBUF
         B     FLC00030            EXIT
FLC00090 LH    ENTRY,BUFADR(TABLE)
         B     FLC00080
         EJECT
**
**
**       CODE 4
**       DELETE THE FLOW CONTROL STRUCTURE
**
**
*
*        TO DELETE THE FLOW CONTROL STRUCTURE AND TABLE THE FOLLOWING
*        OCCURS.  FIRST OUTSTANDING I/O IS WAITED ON.  THEN THE
*        REGENERATION IN THE BUFFER IS HALTED.  THIS IS WAITED ON AND
*        THEN THE STORAGE OBTAINED FOR THE FLOW CONTROL TABLE AND THE
*        DECB IS FREED.
*
FLC00100 L     GTMREG,4(PARM)      LOAD GTMCB
         L     REGB,GTMDECB1       LOAD DECB
         LR    PARM,REGB
         WAIT  ECB=(1)             WAIT ON PRIOR I/O
         CLI   0(REGB),X'7F'
         BNE   ERR3
         XC    0(4,REGB),0(REGB)   ZERO ECB
         L     TABLE,GTMFCTBL      TABLE ADDR
         LA    REGA,BUFADR(TABLE)  ADDRESS AT WHICH TO STOP
         L     REGC,GTMGRDCB       DCB ADDR
         LR    PARM,REGB
         GCNTRL (PARM),HLT,(REGC),(REGA),MF=E HALT REGENERATION
         LR    PARM,REGB
         WAIT  ECB=(1)             WAIT ON I/O
         CLI   0(REGB),X'7F'
         BNE   ERR3
         LA    REGZERO,FLCTLTBL         LENGTH OF TABLE            1720
         LR    PARM,TABLE          ADDR OF TABLE
         FREEMAIN R,LV=(0),A=(1)   FREE TABLE & DECB
*
*        BUFFER IS NOT FREED BECAUSE BUFFER MANAGEMENT WILL
*        DO A RLSEALL WHEN TRMDEV CALLS IT.
*
*
*        THE ADDRESS OF THE FLOW CONTROL TABLE AND THE DECB ARE SET
*        TO ZERO IN THE GDMCB AND CONTROL IS RETURNED TO THE CALLING
*        PROGRAM.
*
         XC    GTMFCTBL(4),GTMFCTBL ZERO ADDR OF FLOW CTL TABLE
         XC    GTMDECB1(4),GTMDECB1 ZERO DECB ADDR
         B     FLC00030            EXIT
         EJECT
*
ERR1     L     REGA,WRKRTNCD
         MVC   STOREXCD(4,REGA),BUFFER SET BUFFER EXCEED
ERR1A    MVI   0(REGA),EXCEED      SET STORAGE EXCEEDED CODE
ERR1B    L     SAVE,4(SAVE)
         RETURN (14,12),RC=4
*
ERR2     L     REGA,GTMGRDCB
         MVC   WRKSPACE+8(2),16(REGA) MOVE IN BUFFER ADDR
         LA    REGB,WRKSPACE+8
         RLSEBFR (REGA),(REGB),MF=(E,WRKSPACE) RELEASE BUFFER
         L     REGA,WRKRTNCD
         MVC   STOREXCD(4,REGA),STORAGE
         B     ERR1A
ERR3     L     REGA,WRKRTNCD
         OI    0(REGA),IO
         B     ERR1B
         EJECT
FLC00110 EQU   ERR2
REGZERO  EQU   0
PARM     EQU   1
REGA     EQU   2
REGB     EQU   3
REGE     EQU   4
WORK     EQU   5
GSPREG   EQU   6
REGF     EQU   6                   ONLY USED IN CODE 2
GTMREG   EQU   7
GDSREG   EQU   8
BASE     EQU   9
TABLE    EQU   10
REGC     EQU   11
SAVE     EQU   13
RETURN   EQU   14
ENTRY    EQU   15
FREE     EQU   0
INUSE    EQU   4
BUFADR   EQU   8
DUMMY    EQU   10
TBLEND   EQU   60
FLCTLTBL EQU   92
GETMN    EQU   4
ASGNBFR  EQU   71
INUSELEN DC    F'50'
IO       EQU   X'04'
BUFSTART EQU   24                                                  5743
MOVE     MVC   2(1,REGA),4(REGA)   OVERLAY TABLE TO REMOVE ENTRY
         EJECT
ORDER    GINIT
FIRSTORD GSRT
         GTRU  *+2
         GESD
         GTRU  *+2
         GTRU  FIRSTORD
ENTRYORD GDPD
         GTRU  *+2
         GTRU  FIRSTORD
CODE     EQU   3
GDS      EQU   4
TWO      DC    F'2'
STOREXCD EQU   12
BUFFER   DC    F'3'
EXCEED   EQU   X'10'
STORAGE  DC    F'4'
         EJECT
WORKAREA DSECT
WRKPARM  DS    F
WRKRTNCD DS    F
WRKSPACE DS    4F
WRKSAVE  DS    18F
         COPY  GSPCB
         COPY  GTMCB
         COPY  GDSCB
         END
