* TITLE:MLITS-MODIFY PROGRAM FUNCTION INDICATOR LIGHTS
* STATUS:CHANGE LEVEL 0
* FUNCTION:TO DEFINE WHICH PROGRAM FUNCTION INDICATOR LIGHTS ARE TO BE
*          ON AND WHICH ARE TO BE OFF.
* ENTRY:ENTERED AT IFFACA07 VIA A CALL TO MLITS
* INPUT:GR#1 CONTAINS THE ADDRESS OF A WORK/SAVE AREA.THE FIRST WORD
*       CONTAINS THE ADDRESS OF A PARAMETER LIST CONSTRUCTED AS FOLLOWS
*       1.DEVICENAME/ATTNLEVEL-ADDRESS OF A VARIABLE CONTAINING EITHER
*         THE ADDRESS OF A GTMCB OR AN ATTENTION LEVEL.
*       2.STATUS-ADDRESS OF A VALUE WHICH INDICATES:
*         A. 1-USE CURRENTLY APPLICABLE SYSTEM DEFAULT
*         B. 2-TURN OFF ALL PROGRAM FUNCTION INDICATORS
*         C. 3-LIGHT PROGRAM FUNCTION INDICATORS TO CORRESPOND TO
*              CURRENTLY ENABLED PROGRAM FUNCTION KEYS.
*         D. 4-USE LIGHTS ARGUMENTS AS PREVIOUSLY SPECIFIED BY ANOTHER
*              CALL OR AS FOLLOWS.
*       3.LIGHTS-ADDRESS(ES) OF A VALUE(S) SPECIFYING WHICH PROGRAM
*         FUNCTION INDICATOR LIGHTS ARE TO BE TURNED ON IN GTMCB OR
*         ATTENTION LEVEL LIGHT MASK.
* OUTPUT:DESIGNATED PROGRAM FUNCTION INDICATORS ARE LIGHTED.
* EXIT:RETURN TO CALLING PROGRAM
* RETURN CODES: 4,0 INCORRECT NUMBER OF PARAMETERS
*               4,1 INVALID DEVICENAME/ATTNLEVEL
*               4,2 INVALID STATUS
*               4,3-N INVALID LIGHTS ARGUMENT
*               0,0-SUCCESSFULL EXECUTION.
* ATTRIBUTES:RE-ENTRANT,PROBLEM PROGRAM MODE
* NOTES:THE FOLLOWING CONTROL BLOCKS ARE EXAMINED BY THIS ROUTINE
*       1.GTMCB
*       2.GSPCB
* EXTERNAL ROUTINES:GIOCR
R0       EQU   0                       PARM REG 0
R1       EQU   1                       PARM REG 1
RLIST    EQU   2                       WORK AREA REG
RSOURCE  EQU   2                       SOURCE ADR REG
RFIRST   EQU   3                       FIRST ARG REG
RPARM    EQU   3                       USER PARAM LIST REG
RATL     EQU   4                       ATLV ADR REG
RLIGHTS  EQU   4                       LIGHT MASK REG
RGTMCB   EQU   5                       GTMCB ADR REG
RINFO    EQU   5                       ADD INFO REG
RGSPCB   EQU   6                       GSPCB ADR REG
RVAL     EQU   6                       ARG VALUE REG
RARG     EQU   7                       ARG ADR REG
RE       EQU   8                       WORK REG
RSHIFT   EQU   8                       SHIFT REG
RBASE    EQU   9                       BASE REG
RC       EQU   10                      WORK REG
RPREV    EQU   10                      PREVIOUS ARG VALUE REG
RD       EQU   11                      WORK REG
SPARE1   EQU   12                      RSVD FOR PL1
RSAVE    EQU   13                      SAVE AREA REG
RA       EQU   14                      WORK REG
RB       EQU   15                      WORK REG
ADD1     EQU   1                       ADD INFO 1
ADD2     EQU   2                       ADD INFO 2
ADD3     EQU   3                       ADD INFO 3
AID      EQU   X'04'                   ATTN LVL ID
BPID     EQU   X'01'                   BP ID
GID      EQU   X'02'                   GTMCB ID
HASPFK   EQU   X'10'                   HAS PF KEYBOARD
IOK      EQU   X'7F'                   I/O SUCCESSFULL CODE
LAST     EQU   X'80'                   LAST PARAM
LIT1     EQU   X'01'                   OPTION 1
LIT2     EQU   X'02'                   OPTION 2
LIT3     EQU   X'03'                   OPTION 3
LIT4     EQU   X'04'                   OPTION 4
NEG      EQU   X'80'                   NEG ARG
NEXT     EQU   4                       NEXT ARG
OFFSAVE  EQU   8                       SAVE AREA CHAIN
ONEA     EQU   1                       ONE
PFK0     EQU   X'80'                   PFK 0
RCC4     EQU   16                      OFFSET TO RTN CODE 4
RC4      EQU   X'08'                   RTN CODE 4
RC5      EQU   X'04'                   I/O ERROR
ZERO     EQU   0                       ZERO
IFFACA07 CSECT
         TITLE 'MLITES - MODIFY PF LIGHTS'
         SAVE  (14,12)                 SAVE REGS
         BALR  RBASE,0                 SET UP BASE REG
         USING *,RBASE                 DEFINE BASE REG
         LR    RLIST,R1                LOAD REG
         USING WORKAREA,RLIST          DEFINE DSECT
         L     RPARM,PLIST             LOAD ADR USER PARAM LIST
         USING PARAM,RPARM             DEFINE DSECT
         ST    RSAVE,SAVE+4            SAVE-SAVE AREA PTR
         LA    RD,SAVE                 LOAD ADR OF MY SAVE AREA
         ST    RD,OFFSAVE(RSAVE)       STORE IN USER SAVE AREA
         LA    RSAVE,SAVE              ESTABLISH NEW SAVE AREA
         L     RA,RCA                  LOAD RTN CODE ARRAY ADR
         XC    ZERO(20,RA),ZERO(RA)    INITIALIZE ARRAY
         XC    DECB(36),DECB           CLEAR I/O AREA
         B     AROUND                  BRANCH
ERROR    L     RA,RCA                  LOAD RTN CODE ARRAY ADR
         STC   RB,ZERO(RA)             STORE RETURN CODE
         ST    RC,ZERO(RA,RD)          STORE ADD INFO
OUTRTN   L     RSAVE,SAVE+4            RESTORE OLD SAVE AREA PTR
         RETURN (14,12),T              RETURN TO CALLING PROGRAM
AROUND   TM    DVCENAME,LAST           LAST PARAM
         BC    14,ML01                 BRANCH IF NOT LAST PARAM
ML11     SR    RC,RC                   ADD INFO=0
ML02     LA    RB,RC4                  LOAD RTN CODE
         LA    RD,RCC4                 LOAD OFFSET
         B     ERROR
ML01     L     RATL,ATNLV              LOAD PTR
         L     RATL,ZERO(RATL)         LOAD CONTROL BLOCK ADR
         USING ATTNLEVL,RATL           DEFINE DSECT
         CLI   ATTNLVID+1,AID          IS THIS AN ATLCB
         BNE   ML03                    BRANCH IF NO
         ST    RATL,LEVEL              SAVE ADR
         XC    DEVICE(4),DEVICE        CLEAR DEVICE ADR
         LR    RA,RATL                 SAVE REG
ML32     CLI   ATTNBPID,BPID           ATTN LEVEL POINT TO A GTMCB
         BE    ML31                    BRANCH IF YES
         L     RATL,ATTNBKPT           LOAD PTR NEXT HIGHER ATTN LEVEL
         B     ML32                    BRANCH
ML31     L     RGTMCB,ATTNBKPT         LOAD GTMCB ADR
         LR    RATL,RA                 RESTORE REG
         B     ML04                    BRANCH
ML03     CLI   ATTNLVID+1,GID          IS THIS A GTMCB
         BE    ML05                    BRANCH IF YES
ML06     LA    RC,ADD1                 LOAD ADD INFO
         B     ML02                    BRANCH
ML05     LR    RGTMCB,RATL             LOAD REG
         USING GTMCB,RGTMCB            DEFINE DSECT
         CL    RGTMCB,GTMGTMCB         HAS THIS GTMCB BEEN INITIALIZE
         BNE   ML06                    BRANCH IF NO
         ST    RGTMCB,DEVICE           SAVE ADR
         XC    LEVEL(4),LEVEL          CLEAR LEVEL ADR
ML04     L     RE,STATUS               LOAD PTR TO STATUS
         LA    RE,ZERO(RE)             CLEAR HIGH ORDER BYTE
         L     RGSPCB,GTMGSPCB         LOAD GSPCB ADR
         USING GSPCB,RGSPCB            DEFINE DSECT
         CL    RE,GSPNULLV             STATUS=ADR OF NULL
         BE    ML30                    BRANCH IF YES
         L     RE,ZERO(RE)             LOAD STATUS VALUE
         CL    RE,ONE                  STATUS=1
         BE    ML07                    BRANCH IF YES
         CL    RE,TWO                  STATUS=2
         BE    ML08                    BRANCH IF YES
         CL    RE,THREE                STATUS=3
         BE    ML09                    BRANCH IF YES
         CL    RE,FOUR                 STATUS=4
         BE    ML10                    BRANCH IF YES
ML30     LA    RC,ADD2                 LOAD ADD INFO
         B     ML02                    BRANCH
ML07     CLC   LEVEL(4),ZERO1          LEVEL SPECIFIED
         BE    ML11                    BRANCH IF LEVEL NOT SPECIFIED
         L     RATL,LEVEL              LOAD LEVEL ADR
         LA    RE,LIT1                 LOAD OPTION
         STC   RE,ATTNLITE             STORE OPTION
ML13     CLC   ATTNFWPT(4),ZERO1       LEVEL ACTIVE
         BNE   OUTRTN                  BRANCH IF NO
ML12     LR    RE,RATL                 SAVE REG
ML15     CLI   ATTNBPID,BPID           POINT TO GTMCB
         BE    ML14                    BRANCH IF YES
         L     RATL,ATTNBKPT           LOAD ADR OF NEXT LEVEL
         B     ML15                    BRANCH
ML14     L     RGTMCB,ATTNBKPT         LOAD GTMCB ADR
         LR    RATL,RE                 RESTORE REG
         CLI   ATTNLITE,LIT1           OPTION 1
         BE    ML16                    BRANCH IF YES
         CLI   ATTNLITE,LIT2           OPTION 2
         BE    ML19                    BRANCH IF YES
         CLI   ATTNLITE,LIT3           OPTION 3
         BE    ML18                    BRANCH IF YES
         MVC   DATA(4),ATTNPFLM        MOVE IN LIGHT MASK
         B     ML19                    BRANCH
ML18     MVC   DATA(4),ATTNPFEN        MOVE IN ENABLED KEYS
         B     ML19                    BRANCH
ML16     CLI   GTMLIGHT,LIT2           OPTION 2
         BE    ML19                    BRANCH IF YES
         CLI   GTMLIGHT,LIT3           OPTION 3
         BE    ML18                    BRANCH IF YES
         MVC   DATA(4),GTMPFKSS        MOVE IN LIGHT MASK
ML19     EQU   *
         LA    RC,DATA                 LOAD ADR OF DATA
         L     RD,GTMGRDCB             LOAD DCB ADR
         LA    R1,DECB                 LOAD I/O AREA ADR
         TM    GTMFEATS+1,HASPFK       DOES DEVICE HAVE A PF KEYBOARD
         BC    14,OUTRTN               BRANCH-NO
         GCNTRL (1),IND,(11),(10),MF=E
         WAIT  ECB=(1)
         CLI   DECB,IOK                I/O SUCCESSFULL
         BE    OUTRTN                  BRANCH-I/O OK
         L     RA,RCA                  LOAD RTN CODE ARRAY ADR
         OI    ZERO(RA),RC5            INDICATE I/O ERROR
         B     OUTRTN                  BRANCH
ML08     CLC   DEVICE(4),ZERO1         DEVICE OR LEVEL
         BE    ML20                    BRANCH-LEVEL
         L     RGTMCB,DEVICE           LOAD GTMCB ADR
         LA    RE,LIT2                 LOAD OPTION
ML22     STC   RE,GTMLIGHT             STORE OPTION
         L     RATL,GTMLATBL           LOAD ADR ACTIVE LEVEL
         LTR   RATL,RATL               IS THERE AN ACTIVE LEVEL
         BZ    OUTRTN                  BRANCH-NO ACTIVE LEVEL
         CLI   ATTNLITE,LIT1           OPTION 1
         BE    ML16                    BRANCH IF OPT 1
         B     OUTRTN                  BRANCH
ML20     L     RATL,LEVEL              LOAD LEVEL ADR
         LA    RE,LIT2                 LOAD OPTION
ML23     STC   RE,ATTNLITE             STORE OPTION
         B     ML13                    BRANCH
ML09     CLC   DEVICE(4),ZERO1         DEVICE OR LEVEL
         BE    ML21                    BRANCH-LEVEL
         L     RGTMCB,DEVICE           LOAD GTMCB ADR
         LA    RE,LIT3                 LOAD OPTION
         B     ML22                    BRANCH
ML21     L     RATL,LEVEL              LOAD LEVEL ADR
         LA    RE,LIT3                 LOAD OPTION
         B     ML23                    BRANCH
ML10     TM    STATUS,LAST             LIGHTS ARGUMENT
         BC    1,ML24                  BRANCH IF NO
         BAL   RA,CONVERT              BRANCH TO CONVERT
         LTR   RB,RB                   LIGHTS OK
         BZ    ML25                    BRANCH IF YES
         LR    RC,RB                   LOAD RTN CODE
         B     ML02                    BRANCH
ML25     CLC   DEVICE(4),ZERO1         DEVICE OR LEVEL
         BE    ML26                    BRANCH-LEVEL
         L     RGTMCB,DEVICE           LOAD GTMCB ADR
         ST    R0,GTMPFKSS             STORE MASK
ML28     LA    RE,LIT4                 LOAD OPTION
         B     ML22                    BRANCH
ML26     L     RATL,LEVEL              LOAD LEVEL ADR
         ST    R0,ATTNPFLM             STORE MASK
ML29     LA    RE,LIT4                 LOAD OPTION
         B     ML23                    BRANCH
ML24     CLC   DEVICE(4),ZERO1         DEVICE OR LEVEL
         BE    ML27                    BRANCH-LEVEL
         L     RGTMCB,DEVICE           LOAD GTMCB ADR
         B     ML28                    BRANCH
ML27     L     RATL,LEVEL              LOAD LEVEL ADR
         B     ML29                    BRANCH
CONVERT  SAVE  (2,12)                  SAVE REGS
         LA    RSOURCE,LIGHTS          LOAD ADR OF FIRST ARG
         LA    RFIRST,NEXT             INDICATE 1ST ARG
         SR    RLIGHTS,RLIGHTS         CLEAR REG
         LA    RINFO,ADD3              LOAD 1ST ADD INFO
         B     MLA18                   BRANCH
MLAOK    SR    RB,RB                   ZERO RETURN CODE
         LR    R0,RLIGHTS              LOAD LIGHTS ARG
         B     MLA19                   BRANCH
MLAERR   LR    RB,RINFO                LOAD RETURN CODE
MLA19    RETURN (2,12)                 RETURN
MLA18    L     RARG,ZERO(RSOURCE)      LOAD ADR OF ARG
         LA    RARG,ZERO(RARG)         ZERO HI ORDER BYTE
         TM    ZERO(RARG),NEG          ARG NEG
         BO    MLANEG                  BRANCH IF NEG
         L     RVAL,ZERO(RARG)         LOAD VALUE OF ARG
MLA24    CL    RVAL,MAX                VALUE GREATER THAN ALLOWABLE
         BC    2,MLAERR                BRANCH IF YES
MLA23    L     RSHIFT,PRIME            PRIME REG
         SRL   RSHIFT,ZERO(RVAL)       POSITION BIT
         OR    RLIGHTS,RSHIFT          ADD BIT TO LIGHT MASK
MLA26    TM    ZERO(RARG),NEG          ARG NEG
         BO    MLA21                   BRANCH IF YES
MLA22    TM    ZERO(RSOURCE),LAST      MORE ARGS
         BO    MLAOK                   BRANCH-NO MORE ARGS
         L     RPREV,ZERO(RARG)        SAVE VALUE PRESENT ARG
         LA    RSOURCE,NEXT(RSOURCE)   STEP TO NEXT ARG
         SR    RFIRST,RFIRST           INDICATE NOT 1ST ARG
         LA    RINFO,ONEA(RINFO)       INCREMENT ADD INFO
         B     MLA18                   BRANCH
MLA21    LA    RSHIFT,ONEA             LOAD CONSTANT
         SR    RVAL,RSHIFT             DECREMENT ARG BY ONE
         CLR   RPREV,RVAL              VALUE=PREVIOUS VALUE
         BE    MLA22                   BRANCH IF YES
         B     MLA23                   BRANCH
MLANEG   LTR   RFIRST,RFIRST           FIRST ARG
         BC    7,MLAERR                BRANCH IF YES
         LTR   RPREV,RPREV             PREV ARG NEGATIVE
         BC    4,MLAERR                BRANCH IF YES
         L     RVAL,ZERO(RARG)         LOAD VALUE OF ARG
         LPR   RVAL,RVAL               MAKE ARG POSITIVE
         CLR   RVAL,RPREV              VALUE GREATER THAN PREV ARG
         BC    2,MLA24                 BRANCH IF YES
         B     MLAERR                  BRANCH
         DS    0F
ZERO1    DC    1F'0'
ONE      DC    A(1)
TWO      DC    A(2)
THREE    DC    A(3)
FOUR     DC    A(4)
MAX      DC    A(31)
PRIME    DC    X'80'
         DC    AL3(0)
         CNOP  0,8
         COPY  ATTNLEVL
         COPY  GTMCB
         COPY  GSPCB
PARAM    DSECT
DVCENAME DS    0F
ATNLV    DS    1F
STATUS   DS    1F
LIGHTS   DS    1F
WORKAREA DSECT
PLIST    DS    1F
RCA      DS    1F
SAVE     DS    18F
DECB     DS    8F
DATA     DS    1F
DEVICE   DS    1F
LEVEL    DS    1F
         END
