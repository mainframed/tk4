         TITLE 'IFFAFA19 - ORGDS - ORDER GDS'
*STATUS: CHANGE LEVEL 000
*
*FUNCTION/OPERATION: BUILDS A LIST OF ALL GDS'S ON THE TERMINAL IN THE
*        NEW DISPLAY ORDER. READS THE FLOW CONTROL STRUCTURE INTO CORE.
*        MODIFY THE FLOW CONTROL STRUCTURE TO ESTABLISH NEW DISPLAY
*        ORDER AND BUILD NEW FLOW CONTROL TABLE. WRITE OUT NEW FLOW
*        CONTROL STRUCTURE AND OVERLAY OLD FLOW CONTROL TABLE WITH THE
*        NEW ONE.
*
*ENTRY POINTS: IFFAFA19
*
*INPUT:  REGISTER 1 POINTS TO A WORK AREA.  THE FIRST WORD CONTAINS THE
*        ADDRESS OF THE PARAMETER LIST, AND THE SECOND, THAT OF THE
*        RETURN CODE ARRAY.  THE PARAMETER LIST IS OF VARIABLE LENGTH,
*        AT LEAST ONE ENTRY   LONG, WITH EACH ENTRY CONTAINING THE
*        ADDRESS OF A POINTER TO A GDSCB.
*
*OUTPUT: NONE TO THE USER.  THE MODIFIED FLOW CONTROL STRUCTURE AND
*        THE NEW FLOW CONTROL TABLE REPLACE THOSE WHICH EXISTED
*        PREVIOUSLY.
*
*EXTERNAL ROUTINES: GREAD, GWRITE
*
*EXITS-NORMAL AND ERROR: VIA RETURN MACRO TO DIRECTOR
*
*TABLES/WORKAREAS: GSP WORKAREA, FLOW CONTROL TABLE
*
*ATTRIBUTES:  PROBLEM STATE, REENTRANT
*
IFFAFA19 CSECT
ORG00    EQU   0                       REGISTER EQUATES
ORG01    EQU   1
ORG02    EQU   2
ORG03    EQU   3
ORG04    EQU   4
ORG05    EQU   5
ORG06    EQU   6
ORG07    EQU   7
ORG08    EQU   8
ORG09    EQU   9
ORG10    EQU   10
ORG11    EQU   11
ORG13    EQU   13
ORG15    EQU   15
*
         SAVE  (14,12)
         BALR  ORG09,0                 CSECT ADDRESSABILITY
         USING *,ORG09
         LR    ORG10,ORG01             WORKAREA ADDRESSABILITY
         USING ORDGDS,ORG10
         LA    ORG01,ORGSAVE           CHAIN SAVE AREAS
         ST    ORG01,MSVA(ORG13)
         ST    ORG13,HSVA(ORG01)
         LR    ORG13,ORG01             ESTABLISH NEW SAVE AREA
*
         L     ORG11,ORGPARM           POINTER TO PARAMETER LIST
         L     ORG07,ORGRTNA           RETURN ARRAY ADDRESSABILITY
         USING GSPARRAY,ORG07
         MVI   GSPARRAY,X'00'          SUCESSFUL COMPLETION CODE
*
*        STAGE 1:
*        CHECK PARAMETERS FOR VALIDITY AND BUILD LIST OF GDS'S
*        IN NEW EXECUTION ORDER
*
         L     ORG08,0(ORG11)          GET ADDRESS OF GDSCB
         L     ORG08,0(ORG08)
         USING GDSCB,ORG08             GDSCB ADDRESSABILITY
         C     ORG08,GDSGDSCB          VALID GDSCB
         BNE   ORG017                  NO, ERROR
         L     ORG06,GDSGTMCB          YES, GET THE GTMCB
         USING GTMCB,ORG06             GTMCB ADDRESSABILITY
         L     ORG05,GTMFCTBL          FLOW CONTROL TABLE
         USING FCTABLE,ORG05           ADDRESSABILITY
*
         MVC   ORGFCTBL(FCTHEAD),FCTABLE COPY FIRST 3 WORDS OF FCT
*                                      INTO WORKAREA
*
         XC    ORGFCTBL+FCTHEAD(48),ORGFCTBL+FCTHEAD
         L     ORG02,GTMSYGDS          PLACE SYSTEM GDS IN GDS LIST
         ST    ORG02,ORGDSLST
         LA    ORG03,4                 INITIALIZE GDS LIST POINTER
*
ORG001   LA    ORG04,2                 SET   FLOW CONTROL TABLE POINTER
         LH    ORG02,GDSFCBUF          GET BUFFER ADDRESS OF FLOW
*                                      CONTROL ELEMENT
*        SEARCH FLOW CONTROL TABLE FOR CORESPONDING ENTRY
*        AND ENTER IT IN WORK TABLE
*
ORG002   CH    ORG02,FCTSYGDS(ORG04)   ENTRY = BUFFER ADDRESS
         BE    ORG003                  YES, CONTINUE
         LA    ORG04,2(ORG04)          NO, INCREMENT POINTER
         C     ORG04,FCTINUSE          CHECKED ALL IN-USE ENTRIES
         BL    ORG002                  NO, CHECK NEXT
         B     ORG004A                 YES, SKIP GDS SPECIFIED
*
ORG003   STH   ORG02,ORGFCTSY(ORG04)   ENTER IN WORK TABLE
         ST    ORG08,ORGDSLST(ORG03)   ENTER GDS IN GDS LIST
*
         LA    ORG03,4(ORG03)          NO, INCREMENT POINTERS
ORG004A  TM    0(ORG11),X'80'          LAST PARAMETER CHECK
         BO    ORG005                  YES, CHECK GDSLIST FOR
*                                      COMPLETENESS
         C     ORG03,ORG0100           NO. ALL PERMISSABLE GDS'S PASSED
         BNL   ORG015                  YES, ERROR
ORG004   LA    ORG11,4(ORG11)
*
         L     ORG08,0(ORG11)          GET NEXT GDS
         L     ORG08,0(ORG08)
         C     ORG08,GDSGDSCB          A GOOD GDSCB
         BNE   ORG017                  NO, ERROR
         C     ORG06,GDSGTMCB          ON SAME TERMINAL
         BE    ORG001                  YES, CHECK FLOW CONTROL TABLE
         B     ORG017                  NO, ERROR
*
*        ADD ANY GDS'S NOT SPECIFIED IN CALL BUT ON TERMINAL
*        TO LIST OF GDS'S
*
ORG005   LA    ORG04,2                 INITIALIZE POINTERS
         LA    ORG02,ORGFCTSY+2
*
ORG006   C     ORG04,FCTINUSE          ALL IN USE FLOW CONTROL ENTRIES
         BE    ORG011                  CHECKED. YES, CONTINUE
         LA    ORG01,FCTSYGDS(ORG04)   NO, GET ADDRESS OF ENTRY
         CLC   0(2,ORG01),0(ORG02)     THIS GDS BEING REORDERED
         BNE   ORG008                  NO, SEARCH FOR GDS
ORG007   LA    ORG04,2(ORG04)          CHECK NEXT ENTRY
         LA    ORG02,2(ORG02)
         B     ORG006
*
ORG008   L     ORG08,GTMGDSCB          GET FIRST GDSCB ON CHAIN
*
ORG009   C     ORG08,ORG0000           LAST GDS ON CHAIN
         BE    ORG007                  CHECK NEXT FLOW CONTROL ENTRY
         CLC   GDSFCBUF(2),0(ORG01)    THIS THE GDS BEING LOOKED FOR
         BE    ORG010                  YES, PLACE IT IN THE GDS LIST
         L     ORG08,GDSNXGDS          NO, GET NEXT AND CHECK IT
         B     ORG009
*
ORG010   C     ORG03,ORG0100           25 GDS'S ALREADY LOCATED
         BE    ORG015                  YES, ERROR
         ST    ORG08,ORGDSLST(ORG03)   PLACE GDS IN GDS LIST
         LA    ORG03,4(ORG03)
         B     ORG007                  CHECK NEXT FLOW CONTROL ENTRY
*
ORG011   LA    ORG02,ORGDSLST-4(ORG03) GET ADDRESS OF LAST ENTRY IN
         OI    0(ORG02),X'80'          GDS LIST AND SET FLAG BIT=1
         LA    ORG02,49
         L     ORG01,ORGFCTIU          GET BYTES IN USE IN FCT
         SR    ORG02,ORG01             NO. OF BYTES TO COPY -1
         LA    ORG03,FCTSYGDS(ORG01)   START COPY FROM
         LA    ORG01,ORGFCTSY(ORG01)   START COPY TO
         EX    ORG02,ORG019            COPY PARTS OF FCT NOT IN USE
*
*        READ IN FLOW CONTROL STRUCTURE AND RESTART GENERATION
*
         XC    ORGDECB(32),ORGDECB     CLEAR DECB
         LA    ORG01,ORGDECB           DECB
         L     ORG02,GTMGRDCB          DCB
         LA    ORG03,ORGFCBUF          INPUT AREA
         LA    ORG04,FCTBUFFR          BUFFER ADDRESS & RESTART ADDRESS
         GREAD (ORG01),STR,(ORG02),256,(ORG03),(ORG04),(ORG04),MF=E
         LTR   ORG15,ORG15             TEST RETURN CODE
         BP    ORG018                  NON-ZERO,I/O ERROR
         LA    ORG01,ORGDECB
         WAIT  ECB=(1)
         CLI   ORGDECB,POSTCODE        POST CODE NOT= 7F
         BNE   ORG018                  I/O ERROR
*
*        END OF STAGE1. BEGINNING OF STAGE 2. CHANGE ADDRESSABILITY
*        WHERE NECESSARY
         DROP  ORG07,ORG06,ORG05
*
*        STAGE 2:
*        MODIFY FLOW CONTROL STRUCTURE AND BUILD NEW FLOW
*        CONTROL TABEL
*
         LA    ORG11,ORGDSLST          POINTER TO GDS LIST
         LA    ORG07,ORGFCTUS          POINTER TO FIRST AVAILABLE
*                                      ENTRY IN FLOW CONTROL TABLE
         TM    0(ORG11),X'80'          SYSTEM GDS ONLY ONE
         BO    ORG014                  YES, DON'T BOTHER MODIFYING
*                                      FLOW CONTROL
         LH    ORG06,ORGFCTSY          GET DISPLACEMENT TO SYSTEM
         SH    ORG06,ORGFCTBF          GDS FLOW CONTROL ELEMENT
*
ORG012   LA    ORG11,4(ORG11)          INCREMENT POINTER TO NEXT GDS
*
         L     ORG08,0(ORG11)          GET GDS ADDRESS
         LH    ORG05,GDSFCBUF          GET BUFFER ADDRESS OF FLOW
*                                      CONTROL ELEMENT
         STH   ORG05,ORGFCBUF+ELGTRU(ORG06) STORE IN PREVIOUS FLOW
*                                      CONTROL ELEMENT
         STH   ORG05,0(ORG07)          AND FLOW CONTROL TABLE
         LR    ORG06,ORG05             DISPLACEMENT TO THIS ELEMENT
         SH    ORG06,ORGFCTBF
*
         TM    0(ORG11),X'80'          LAST PARAMETER
         BO    ORG013
         LA    ORG07,2(ORG07)          NO, INCREMENT FLOW CONTROL
         B     ORG012                  TABLE POINTER
*
ORG013   LH    ORG05,ORGFCTBF          GET BUFFER ADDRESS OF FLOW
*                                      CONTROL SEGMENT AND STORE IN
         STH   ORG05,ORGFCBUF+ELGTRU(ORG06) THE FLOW CONTROL ELEMENT
*
*        END OF STAGE 2. BEGINNING OF STAGE 3. REESTABLISH
*        ADDRESSABILITY TO THE RETURN ARRAY AND THE GTMCB
*
         L     ORG07,ORGRTNA
         USING GSPARRAY,ORG07
         L     ORG08,ORGDSLST          GET SYSTEM GDS
         L     ORG06,GDSGTMCB          AND GTMCB
         USING GTMCB,ORG06
*
*        STAGE 3:
*        PUT OUT THE NEW FLOW CONTROL STRUCTURE AND THE NEW
*        FLOW CONTROL TABLE
*
         XC    ORGDECB(32),ORGDECB     CLEAR DECB
         LA    ORG01,ORGDECB           DECB
         L     ORG02,GTMGRDCB          DCB
         LA    ORG03,ORGFCBUF          OUTPUT AREA
         LA    ORG04,ORGFCTBF          BUFFER ADDRESS & RESTART ADDRESS
*
         GWRITE (ORG01),STR,(ORG02),256,(ORG03),(ORG04),(ORG04),MF=E
         LTR   ORG15,ORG15             RETURN CODE NON-ZERO,
         BP    ORG018                  I/O ERROR
         LA    ORG01,ORGDECB
         WAIT  ECB=(1)
         CLI   ORGDECB,POSTCODE        POST CODE NOT= 7F
         BNE   ORG018                  I/O ERROR
*
         L     ORG05,GTMFCTBL          IF I/O SUCESSFUL, OVERLAY OLD
         MVC   0(60,ORG05),ORGFCTBL    FLOW CONTROL TABLE WITH NEW
*
ORG014   L     ORG13,HSVA(ORG13)       RETURN
         RETURN (14,12),T
*
*        ERROR PROCESSING
*
ORG015   XC    GSPARRAY+16(4),GSPARRAY+16   SHORT/LONG PARAMETER LIST
ORG016   OI    GSPARRAY,PARERR         PARAMETER ERROR
         B     ORG014                  RETURN
*
ORG017   S     ORG11,ORGPARM           GET DISPLACEMENT FROM BEGINNING
*                                      OF PARAMETER LIST
         SRA   ORG11,2                 GET PARAMETER NUMBER
         LA    ORG11,1(ORG11)
         ST    ORG11,GSPARRAY+16       BAD GDSCB
         B     ORG016
*
ORG018   OI    GSPARRAY,IOERR          I/O ERROR
         B     ORG014
*
ORG019   MVC   0(0,ORG01),0(ORG03)
*        CONSTANTS, EQUATES, AND DSECTS
*
ORG0000  DC    F'0'
ORG0100  DC    F'100'
*
FCTHEAD  EQU   12
POSTCODE EQU   X'7F'
ELGTRU   EQU   8
ELLNGTH  EQU   10
PARERR   EQU   X'08'
IOERR    EQU   X'04'
MSVA     EQU   8
HSVA     EQU   4
*
ORDGDS   DSECT
ORGPARM  DS    F                       ADDRESS OF PARAMETER LIST
ORGRTNA  DS    F                       ADDRESS OF RETURN ARRAY
ORGSAVE  DS    18F                     SAVE AREA
ORGDSLST DS    25F                     GDS LIST
ORGFCTBL DS    15F                     FLOW CONTROL TABLE WORKAREA
         ORG   ORGFCTBL
ORGFCTFR DS    F
ORGFCTIU DS    F
ORGFCTBF DS    H
ORGFCTSY DS    H
ORGFCTUS DS    24H
ORGFCBUF DS    64F                     FLOW CONTROL STRUCTURE
ORGDECB  DS    8F                      DECB FOR I/O OPERATIONS
*
FCTABLE  DSECT
FCTFREE  DS    F
FCTINUSE DS    F
FCTBUFFR DS    H
FCTSYGDS DS    H
FCTGDS   DS    24H
*
         COPY  GSPCB
         COPY  GTMCB
         COPY  GDSCB
*
         END
