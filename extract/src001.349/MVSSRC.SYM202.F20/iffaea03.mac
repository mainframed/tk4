         TITLE 'IFFAEA03 - SDATL - SET DATA LIMITS'
*STATUS: CHANGE LEVEL 000
*
*FUNCTION/OPERATION: SETS THE DATA LIMITS FIELDS IN THE GDSCB EQUAL
*        TO THE VALUES SPECIFIED BY THE USER. A FLAG IS ALSO SET IN THE
*        GDSCB TO INDICATE THAT THIS ROUTINE HAS BEEN CALLED
*
*ENTRY POINTS: IFFAEA04
*
*INPUT:  REGISTER 1 CONTAINS THE ADDRESS OF A WORKAREA. THE FIRST WORD
*        CONTAINS THE ADDRESS OF THE PARAMETER LIST AND THE SECOND,
*        THAT OF THE RETURN ARRAY.
*
*        THE FIXED-LENGTH PARAMETER LIST IS
*    + 0 A(POINTER TO THE GDSCB)
*
*    + 4 A(LOWER-LEFT X DATA LIMIT)
*
*    + 8 A(LOWER-LEFT Y DATA LIMIT)
*
*    +12 A(UPPER-RIGHT X DATA LIMIT)
*
*    +16 A(UPPER-RIGHT Y DATA LIMIT)
*
*OUTPUT: NONE TO USER. DATA LIMIT FIELDS IN THE GDSCB ARE SET. GDSFLGS2
*        IS SET TO INDICATE THE ROUTINE HAS BEEN CALLED.
*
*EXTERNAL ROUTINES: NONE
*
*EXITS-NORMAL AND ERROR: VIA RETURN MACRO TO DIRECTOR.
*
*TABLES/WORKAREAS:  GSP WORKAREA
*
*ATTRIBUTES:  PROBLEM STATE, REENTRANT.
IFFAEA03 CSECT
         SAVE  (14,12)
         BALR  SDL09,0                 ADDRESSABILITY FOR
         USING *,SDL09                 CSECT
*
         LR    SDL02,SDL01             TEMPORARY ADDRESSABILITY
         USING SDLWORK,SDL02           FOR WORK AREA
         L     SDL10,SDWPARM           ADDRESSABILITY FOR
         USING SDLPARM,SDL10           PARAMETER LIST
*
         L     SDL12,SDWRTNA           ADDRESSABILITY FOR NULL VARIABLE
         USING GSPARRAY,SDL12          AND RETURN ARRAY
         DROP  SDL02                   DROP WORKAREA ADDRESSABILITY
*
         L     SDL11,SDPGDSCB          GET POINTER TO GDSCB
         L     SDL11,0(SDL11)          ADDRESSABILITY FOR
         USING GDSCB,SDL11             GDSCB
*
         C     SDL11,GDSGDSCB          IS GDSCB OPEN
         BNE   SDL002                  NO
*
         SR    SDL04,SDL04             INITIALIZE COUNTER
         LA    SDL02,4                 INITIALIZE INCREMENT
         LA    SDL03,12                INITIALIZE COMPARAND
*
SDL001   LA    SDL05,SDPGDSCB(SDL04)   GET ADDRESS OF PARAMETER
         TM    0(SDL05),X'80'          IS IT THE LAST ONE
         BO    SDL005                  YES, ERROR
         CLC   1(3,SDL05),GSPNULLV+1   EQUAL TO THE NULL VARIABLE
         BE    SDL004                  YES, ERROR
         BXLE  SDL04,SDL02,SDL001
         CLC   1(3,SDL05),GSPNULLV+1   EQUAL TO THE NULL VARIABLE
         BE    SDL004                  VARIABLE. YES, ERROR
*
         LM    SDL02,SDL05,SDPU1       GET ADDRESSES OF PARAMETERS
         CLC   0(4,SDL02),0(SDL04)     PARAMETER ERROR IF U1=U2
         BE    SDL005
         CLC   0(4,SDL03),0(SDL05)     OR IF V1=V2
         BE    SDL005
         MVC   GDSUVLLL(4),0(SDL02)    MOVE U1 INTO GDSCB
         MVC   GDSVVLLL(4),0(SDL03)    MOVE V1 INTO GDSCB
         MVC   GDSUVLUR(4),0(SDL04)    MOVE U2 INTO GDSCB
         MVC   GDSVVLUR(4),0(SDL05)    MOVE V2 INTO GDSCB
         MVI   GSPARRAY,ZERO           GOOD RETURN
         OI    GDSFLGS2,SDATLFLG       SET FLAG TO SHOW SDATL CALLED
         RETURN (14,12)
*
SDL002   MVC   GSPARRAY+16(4),SDL0001  GDSCB NOT OPEN
SDL003   MVI   GSPARRAY,INVCD          INVALID CODE
         RETURN (14,12)
*
SDL004   LA    SDL02,SDL0001(SDL04)    GET NUMBER OF BAD PARAMETER
         MVC   GSPARRAY+16(4),0(SDL02) SET RETURN CODE
         B     SDL003
*
SDL005   MVC   GSPARRAY+16(4),SDL0000  SHORT PARAMETER LIST
         B     SDL003
*
SDL0000  DC    F'0'
SDL0001  DC    F'1'
SDL0002  DC    F'2'
SDL0003  DC    F'3'
SDL0004  DC    F'4'
SDL0005  DC    F'5'
ZERO     EQU   0
INVCD    EQU   X'08'                   INVALID CODE FLAG
SDATLFLG EQU   X'01'
*
*        REGISTER EQUATES
*
SDL01    EQU   1                       PARAMETER LIST REGISTER
SDL02    EQU   2                       WORK REGISTERS
SDL03    EQU   3
SDL04    EQU   4
SDL05    EQU   5
SDL09    EQU   9                       BASE REGISTER-CSECT
SDL10    EQU   10                      BASE REGISTER-PARAMETER LIST
SDL11    EQU   11                      BASE REGISTER-GDSCB
SDL12    EQU   8                       BASE REGISTER-RETURN ARRAY
*                                           AND NULL VARUABLE
SDLPARM  DSECT
SDPGDSCB DS    F
SDPU1    DS    F
SDPV1    DS    F
SDPU2    DS    F
SDPV2    DS    F
SDLWORK  DSECT
SDWPARM  DS    F
SDWRTNA  DS    F
         COPY  GDSCB
         COPY  GSPCB
         END
