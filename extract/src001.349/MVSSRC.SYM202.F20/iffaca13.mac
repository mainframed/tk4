* TITLE:SALRM-SOUND AUDIBLE ALARM
* STATUS:CHANGE LEVEL 0
* FUNCTION:TO SOUND THE AUDIBLE ALARM ON A GRAPHIC TERMINAL.
* INPUT:GR#1CONTAINS THE ADDRESS OF A WORK/SAVE AREA,THE FIRST WORD
*       CONTAINS THE ADDRESS OF A PARAMETER LIST CONSTRUCTED AS FOLLOWS
*       1.DEVICENAME-ADDRESS OF A VARIABLE WHICH CONTAINS THE ADDRESS
*         OF THE GTMCB.
* OUTPUT:AUDIBLE ALARM SOUNDED ON TERMINAL
* EXIT:RETURN TO CALLER VIA REGISTER 14
* ENTRY:ENTERED AT INFFACA13 VIA A CALL TO SALR.
* RETURN CODES:4,1 INVALID GTMCB
*              0,0 SUCCESSFULL
* ATTRIBUTES:RE-ENTRANT,PROBLEM PROGRAM MODE
* EXTERNAL ROUTINES:GIOCR
* NOTES:THIS ROUTINE EXAMINES THE FOLLOWING CONTROL BLOCKS:
*       1.GTMCB
R0       EQU   0                       PARM REG 0
R1       EQU   1                       PARM REG 1
RLIST    EQU   2                       INPUT WORK/SAVE AREA
RPARM    EQU   3                       USER PARM LIST
RGTMCB   EQU   4                       GTMCB ADR REG
RA       EQU   5                       WORK REG
RB       EQU   6                       WORK REG
RC       EQU   7                       WORK REG
RBASE    EQU   9                       BASE REG
RD       EQU   10                      WORK REG
RSAVE    EQU   13                      SAVE REG
ZERO     EQU   0                       ZERO
GID      EQU   X'02'                   GTMCB ID
ADD1     EQU   1                       ADD INFO
OFFSAVE  EQU   8                       SAVE AREA CHAIN
RC4      EQU   X'08'                   RTN CODE 4
RCC4     EQU   16                      OFFSET TO ADD INFO
RC5      EQU   X'04'                   I/O ERROR
IOK      EQU   X'7F'                   I/O SUCCESSFULL CODE
IFFACA13 CSECT
         SAVE  (14,12)                 SAVE REGS
         BALR  RBASE,0                 SET UP BASE REG
         USING *,RBASE                 DEFINE BASE REG
         LR    RLIST,R1                LOAD REG
         USING WORKAREA,RLIST          DEFINE DSECT
         L     RPARM,PLIST             LOAD ADR OF USER PARM LIST
         USING PARAM,RPARM             DEFINE DSECT
         L     RA,RCA                  LOAD ADR RTN CODE ARRAY
         XC    ZERO(20,RA),ZERO(RA)    INITIALIZE ARRAY
         L     RGTMCB,DEVICE           LOAD GTMCB INDIRECT PTR
         LA    RGTMCB,ZERO(RGTMCB)     ZERO HI ORDER BYTE
         L     RGTMCB,ZERO(RGTMCB)     LOAD GTMCB ADR
         USING GTMCB,RGTMCB            DEFINE DSECT
         CLI   GTMTERID,GID            VALID GTMCB
         BE    SA01                    BRANCH IF VALID
         B     ERROR                   BRANCH
SA01     CL    RGTMCB,GTMGTMCB         GTMCB INITIALIZED
         BE    SA02                    BRANCH IF INITIALIZED
ERROR    L     RA,RCA                  LOAD RTN CODE ARRAY ADR
         MVI   ZERO(RA),RC4            RTN CODE 4 TO ARRAY
         LA    RB,ADD1                 LOAD ADD INFO
         ST    RB,RCC4(RA)             STORE ADD INFO
OUTRTN   RETURN (14,12),T              RETURN
SA02     ST    RSAVE,SAVE+4            SAVE-SAVE AREA PTR
         LA    RD,SAVE                 LOAD ADR OF MY SAVE AREA
         ST    RD,OFFSAVE(RSAVE)       STORE IN USER SAVE AREA
         LA    RSAVE,SAVE              ESTABLISH NEW SAVE AREA
         XC    DECB(32),DECB           CLEAR I10 AREA
         LA    R1,DECB                 LOAD ADR I10 AREA
         L     RC,GTMGRDCB             LOAD DCB ADR
         GCNTRL (1),ALM,(7),MF=E       SOUND ALARM
         WAIT  ECB=(1)                 WAIT FOR COMPLETION
         L     RSAVE,SAVE+4            RESTORE OLD SAVE AREA PTR
         CLI   DECB,IOK                I/O SUCCESSFULL
         BE    OUTRTN                  BRANCH IF I/O OK
         L     RA,RCA                  LOAD RTN CODE ARRAY ADR
         OI    ZERO(RA),RC5            INDICATE I/O ERROR
         B     OUTRTN                  BRANCH
         CNOP  0,8
PARAM    DSECT
DEVICE   DS    1F
WORKAREA DSECT
PLIST    DS    1F
RCA      DS    1F
SAVE     DS    18F
DECB     DS    8F
         COPY  GTMCB
         END
