         TITLE 'IFFAGA02 - BGTRK - BEGIN LIGHT-PEN TRACKING'
*STATUS: CHANGE LEVEL 000
*
*FUNCTION/OPERATION:  CALLS ORGEN TO RELOCATE LIGHT-PEN TRACKING
*        ROUTINE AND EXEC TO PLACE IT ON SCREEN.  CALLS CNVRT TO
*        CONVERT USER SUPPLIED COORDINATES TO RASTER UNIT COORDINATES.
*        ASSEMBLES A GDV FROM THESE COORDINATES AND WRITES IT TO THE
*        BUFFER.  BUFFER ADDRESS OF THE REPOSITIONING VECTOR IS PLACED
*        AT GTMREPOS.  GTMFLAGS SET TO INDICATE TRACKING ROUTINE IS IN
*        BUFFER
*
*ENTRY POINTS: IFFAGA02
*
*INPUT:  REGISTER 1 POINTS TO A WORKAREA.  THE FIRST WORD CONTAINS THE
*        ADDRESS OF THE PARAMETER LIST, AND THE SECOND, THAT OF THE
*        RETURN CODE ARRAY.  THE FIXED-LENGTH PARAMETER LIST IS
*    + 0 A(POINTER TO THE GDSCB)
*
*    + 4 A(ABSOLUTE X POSITIONING VALUE)
*
*    + 8 A(ABSOLUTE Y POSITIONING VALUE)
*
*OUTPUT: NONE TO USER.  TRACKING SYMBOL WILL APPEAR ON DEVICE WITH
*        SPECIFIED GDS.
*
*EXTERNAL ROUTINES: ORGEN, EXEC, CNVRT, GWRITE
*
*EXITS-NORMAL AND ERROR: VIA RETURN MACRO TO DIRECTOR
*
*TABLES/WORKAREAS: GSP WORKAREA
*
*ATTRIBUTES: PROBLEM STATE, REENTRANT
*
*A98000,A200000-204500,C205000,206000,A264000-268500        D11 ZA15437
*C269000,270000,A276000-280500,C281000,282000               D11 ZA15437
*652000-658000                                              D11 ZA15437
IFFAGA02 CSECT
BTK01    EQU   1
BTK02    EQU   2
BTK03    EQU   3
BTK04    EQU   4
BTK05    EQU   5
BTK06    EQU   6                   GTM ADDRESSIBILITY
BTK07    EQU   7                   RETCODE ARRAY ADDRESSIBILITY
BTK08    EQU   8                   GDS ADDRESSIBILITY REG
BTK09    EQU   9                   BASEREG
BTK10    EQU   10                  WORKAREA ADDRESSIBILITY
BTK11    EQU   11                  PARMLIST ADDRESSIBILITY
BTK13    EQU   13                  SAVEAREA POINTER
BTK14    EQU   14                  LINKAGE REGISTER         D11 ZA15437
BTK15    EQU   15
         SAVE  (14,12)
         BALR  BTK09,0                 CSECT ADDRESSABILITY
         USING *,BTK09
         LR    BTK10,BTK01        WORKAREA ADDRESSABILITY
         USING BEGTRK,BTK10
*
         LA    BTK02,BTKSAVE           CHAIN SAVE AREAS
         ST    BTK02,MSVA(BTK13)
         ST    BTK13,HSVA(BTK02)
         LR    BTK13,BTK02             ESTABLISH NEW SAVE AREA
*
         L     BTK11,BTKPARM           PARAMETER LIST ADDRESSABILITY
         USING BEGPARM,BTK11
*
         L     BTK08,BTKGDSCB          USER GDSCB ADDRESSABILITY
         L     BTK08,0(BTK08)
         USING GDSCB,BTK08
         L     BTK07,BTKRTNA           RETURN ARRAY ADDRESSABILITY
         USING GSPARRAY,BTK07
         MVI   GSPARRAY,X'00'          CLEAR RETURN CODE
*
         C     BTK08,GDSGDSCB          VALID GDSCB
         BNE   BTK006                  NO, ERROR RETURN
         L     BTK06,GDSGTMCB          GTMCB ADDRESSABILITY
         USING GTMCB,BTK06
*
         TM    GTMFEATS,MOD3           TRACKING REQUESTED ON MODEL 3
         BM    BTK008                  NO, ERROR RETURN
         TM    BTKGDSCB,X'80'          IF PARAMETER LIST
         BO    BTK008
         TM    BTKXCOOR,X'80'          IS NOT THREE ENTRIES LONG,
         BO    BTK008
         TM    BTKYCOOR,X'80'          GIVE AN ERROR RETURN
         BZ    BTK008
*
         MVC   BTKCVGDS(4),BTKGDSCB    POINTER TO GDSNAME
         LA    BTK02,BTK0002           CONVERT VARIABLE =2
         ST    BTK02,BTKCVCVT
         MVC   BTKCVXIN(8),BTKXCOOR    POINTER TO COORDINATES
         NI    BTKCVYIN,X'7F'          SET END-OF-LIST BIT=0
         LA    BTK02,BTKXABS           POINTERS TO
         ST    BTK02,BTKCVXOT          OUTPUT LOCATIONS
         LA    BTK02,BTKYABS
         ST    BTK02,BTKCVYOT
         LA    BTK02,BTKCVGDS          POINTER TO PARAMETER LIST
         ST    BTK02,BTKCVPRM
         MVC   BTKCVRTN(4),BTKRTNA     POINTER TO RETURN ARRAY
         XC    BTKSUPV(16),BTKSUPV
         LA    BTK01,BTKCVPRM                               D11
         L     BTK15,STAT(BTK07)   POINT TO STATUS TABLE    D11 ZA15437
         L     BTK15,CONVERT(BTK15) AND TO CONVERT EP       D11 ZA15437
         LTR   BTK15,BTK15         CONVERT LOADED YET?      D11 ZA15437
         BZ    BTKLINK1            NO,MUST USE SYSTEM LINK  D11 ZA15437
         BALR  BTK14,BTK15         GO CONVERT               D11 ZA15437
         B     BTKRTN1             RETURN FROM BALR         D11 ZA15437
BTKLINK1 LA    BTK15,BTKSUPV                                D11 ZA15437
         LINK  EP=IFFAGA08,MF=(E,(1)),SF=(E,(15)) CALL CONVERT
BTKRTN1  TM    GSPARRAY,SCALERR    SCALING SUCCESSFUL       D11 ZA15437
         BO    BTK004                  NO, POINT OFF-SCREEN
*
         SR    BTK02,BTK02             IF POSITION
         C     BTK02,BTKXABS
         BH    BTK005                  PROVIDED IS
         C     BTK02,BTKYABS
         BH    BTK005                  INVALID,
         LA    BTK02,4095
         C     BTK02,BTKXABS           SET RETURN CODE
         BL    BTK005
         C     BTK02,BTKYABS
         BL    BTK005
*
BTK001   TM    GTMFLAGS,TRKIN          SEE IF TRACKING ROUTINE OUT
         BO    BTK003                  YES, UPDATE POSITION ONLY
*
         LA    BTK02,GTMSYGDS          POINTER TO SYSTEM GDS
         ST    BTK02,BTKORGDS
         LA    BTK02,POSEL             POINTER TO ORDERS
         ST    BTK02,BTKORORD
         LA    BTK02,TRKCNT            BYTE COUNT
         ST    BTK02,BTKORCNT
         OI    BTKORCNT,X'80'          END-OF-LIST FLAG
         LA    BTK02,BTKORGDS          GET POINTERS TO
         ST    BTK02,BTKORPRM          PARAMETER LIST
         MVC   BTKORRTN(4),BTKRTNA     AND RETURN ARRAY
         XC    BTKSUPV(16),BTKSUPV
         LA    BTK01,BTKORPRM                               D11
         L     BTK15,STAT(BTK07)   POINT TO STATUS TABLE    D11 ZA15437
         L     BTK15,ORGEN(BTK15)      AND ORGEN EP         D11 ZA15437
         LTR   BTK15,BTK15         ORGEN LOADED YET?        D11 ZA15437
         BZ    BTKLINK2            NO,MUST USE SYSTEM LINK  D11 ZA15437
         BALR  BTK14,BTK15         GOTO ORGEN               D11 ZA15437
         B     BTKRTN2             RETURN FROM BALR         D11 ZA15437
BTKLINK2 LA    BTK15,BTKSUPV                                D11 ZA15437
         LINK  EP=IFFAGA07,MF=(E,(1)),SF=(E,(15))  LINK ORGEN
BTKRTN2  OI    BTKORGDS,X'80'                               D11 ZA15437
         XC    BTKSUPV(16),BTKSUPV
         LA    BTK01,BTKORPRM                               D11
         L     BTK15,STAT(BTK07)   POINT TO STATUS TABLE    D11 ZA15437
         L     BTK15,EXEC(BTK15)   AND TO EXEC EP           D11 ZA15437
         LTR   BTK15,BTK15         EXEC LOADED YET?         D11 ZA15437
         BZ    BTKLINK3            NO,MUST USE SYSTEM LINK  D11 ZA15437
         BALR  BTK14,BTK15         GOTO EXEC                D11 ZA15437
         B     BTKRTN3             RETURN FROM BALR         D11 ZA15437
BTKLINK3 LA    BTK15,BTKSUPV                                D11 ZA15437
         LINK  EP=IFFAFA11,MF=(E,(1)),SF=(E,(15)) LINK EXEC
BTKRTN3  TM    GSPARRAY,IOERR          I/O ERROR IN EXEC    D11 ZA15437
         BO    BTK002                  IMMEDIATE EXIT
*                                      SYMBOL OUT ON SCREEN
         OI    GTMFLAGS,TRKIN          SET FLAG
         L     BTK08,GTMSYGDS          CHANGE ADDRESSABILITY TO
*                                      SYSTEM GDSCB
         LH    BTK03,GDSBCTEL          GET BUFFER CONTROL ELEMENT
         MH    BTK03,BTK0256H          GET BUFFER ADDRESS OF
         LA    BTK03,XDISPL+2(BTK03)   THE REPOSITIONING VECTOR
         STH   BTK03,GTMREPOS          AND STORE IT
*
*
BTK003   MVC   BTKWORK(2),BTKXABS+2    ASSEMBLE POSITIONING VECTOR
         MVC   BTKWORK+2(2),BTKYABS+2
         XC    BTKSUPV(32),BTKSUPV     CLEAR DECB
         LA    BTK01,BTKSUPV           DECB
         L     BTK02,GTMGRDCB          DCB
         LA    BTK03,BTKWORK           OUTPUT AREA
         LA    BTK04,GTMREPOS          BUFFER ADDRESS
         L     BTK05,GTMFCTBL          START ADDRESS
         LA    BTK05,BTK08A(BTK05)
         GWRITE (BTK01),STR,(BTK02),4,(BTK03),(BTK04),(BTK05),MF=E
         LA    BTK01,BTKSUPV
         WAIT  ECB=(1)
         CLI   BTKSUPV,PSTCD           I/O SUCESSFUL
         BE    BTK002                  YES, EXIT
         OI    GSPARRAY,IOERR          NO, SET RETURN CODE
BTK002   L     BTK13,HSVA(BTK13)       RETURN
         RETURN (14,12),T,RC=0
*
BTK004   MVI   GSPARRAY,X'00'          CLEAR THE RETURN CODE
BTK005   OI    GSPARRAY,SCISERR        SET SCISSORING ERROR CODE
         XC    GSPARRAY+4(8),GSPARRAY+4
         LA    BTK02,2048              SET POSITION TO CENTER
         ST    BTK02,BTKXABS           OF SCREEN
         ST    BTK02,BTKYABS
         B     BTK001                  BRANCH TO PUT OUT SYMBOL
*
BTK006   MVC   GSPARRAY+16(4),BTK0001  BAD GDSCB
BTK007   OI    GSPARRAY,PARERR         PARAMETER ERROR
         B     BTK002
BTK008   XC    GSPARRAY+16(4),GSPARRAY+16   INCORRECT LENGTH
         B     BTK007
*
BTK0001  DC    F'1'
BTK0002  DC    F'2'
BTK0003  DC    F'3'
BTK0256H DC    H'256'
TRKCNT   DC    A(TRKEND-POSEL)
*
         GINIT
POSEL    GENSD
         GDRD
         GEPM  A
POSVEC   GDV   2048,2048,U
*
         GTDD  TRK01
HXGN1    GEPI2
         GDV   40,0,U
         GTDD  TRK01
         GEPI2
         GDV   -20,32,U
         GTDD  TRK01
         GEPI2
         GDV   -40,0,U
         GTDD  TRK01
         GEPI2
         GDV   -20,-32,U
         GTDD  TRK01
         GEPI2
         GDV   20,-32,U
         GTDD  TRK01
         GEPI2
         GDV   40,0,U
         GTDD  TRK01
*
HXGN2    GEPI2
         GDV   60,32,U
         GTDD  TRK01
         GEPI2
         GDV   -20,32,U
         GTDD  TRK01
         GEPI2
         GDV   -20,32,U
         GTDD  TRK01
         GEPI2
         GDV   -40,0,U
         GTDD  TRK01
         GEPI2
         GDV   -40,0,U
         GTDD  TRK01
         GEPI2
         GDV   -20,-32,U
         GTDD  TRK01
         GEPI2
         GDV   -20,-32,U
         GTDD  TRK01
         GEPI2
         GDV   20,-32,U
         GTDD  TRK01
         GEPI2
         GDV   20,-32,U
         GTDD  TRK01
         GEPI2
         GDV   40,0,U
         GTDD  TRK01
         GEPI2
         GDV   40,0,U
         GTDD  TRK01
         GEPI2
         GDV   20,32,U
         GTDD  TRK01
*
HXGN3    GEPI2
         GDV   60,32,U
         GTDD  TRK01
         GEPI2
         GDV   -20,32,U
         GTDD  TRK01
         GEPI2
         GDV   -20,32,U
         GTDD  TRK01
         GEPI2
         GDV   -20,32,U
         GTDD  TRK01
         GEPI2
         GDV   -40,0,U
         GTDD  TRK01
         GEPI2
         GDV   -40,0,U
         GTDD  TRK01
         GEPI2
         GDV   -40,0,U
         GTDD  TRK01
         GEPI2
         GDV   -20,-32,U
         GTDD  TRK01
         GEPI2
         GDV   -20,-32,U
         GTDD  TRK01
         GEPI2
         GDV   -20,-32,U
         GTDD  TRK01
         GEPI2
         GDV   20,-32,U
         GTDD  TRK01
         GEPI2
         GDV   20,-32,U
         GTDD  TRK01
         GEPI2
         GDV   20,-32,U
         GTDD  TRK01
         GEPI2
         GDV   40,0,U
         GTDD  TRK01
         GEPI2
         GDV   40,0,U
         GTDD  TRK01
         GEPI2
         GDV   40,0,U
         GTDD  TRK01
         GEPI2
         GDV   20,32,U
         GTDD  TRK01
         GEPI2
         GDV   20,32,U
         GTDD  TRK01
         GEVI2
         GDV   -100,32,B
TRK01    GSXY  POSVEC
         GPDI
TRKEND   DS    0H
PATCH    DC    64X'FF'             PATCH AREA               D11
HSVA     EQU   4
MSVA     EQU   8
PARERR   EQU   X'08'
SCALERR  EQU   X'20'
SCISERR  EQU   X'40'
XDISPL   EQU   6
YDISPL   EQU   8
BTK08A   EQU   8
TRKIN    EQU   X'08'
IOERR    EQU   X'04'
PSTCD    EQU   X'7F'
MOD3     EQU   X'03'
CONVERT  EQU   X'0264'             STATAB CNVRT ENTRY DISPL D11 ZA15437
EXEC     EQU   X'01C8'             STATAB EXEC  ENTRY DISPL D11 ZA15437
ORGEN    EQU   X'0258'             STATAB ORGEN ENTRY DISPL D11 ZA15437
STAT     EQU   24                A(STATAB)-A(RTNARR) IN GSP D11 ZA15437
*
BEGTRK   DSECT
BTKPARM  DS    F
BTKRTNA  DS    F
BTKSAVE  DS    18F
BTKSUPV  DS    8F
*
BTKXABS  DS    F
BTKYABS  DS    F
*
BTKCVGDS DS    F
BTKCVCVT DS    F
BTKCVXIN DS    F
BTKCVYIN DS    F
BTKCVXOT DS    F
BTKCVYOT DS    F
BTKCVPRM DS    F
BTKCVRTN DS    F
*
         ORG   BTKCVGDS
BTKWORK  DS    4F
*
BTKORGDS DS    F
BTKORORD DS    F
BTKORCNT DS    F
BTKORPRM DS    F
BTKORRTN DS    F
BEGPARM  DSECT
BTKGDSCB DS    F
BTKXCOOR DS    F
BTKYCOOR DS    F
*
         COPY  GSPCB
         COPY  GDSCB
         COPY  GTMCB
*
         END
