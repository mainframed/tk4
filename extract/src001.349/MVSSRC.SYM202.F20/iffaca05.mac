* TITLE:MLPEO-MODIFY LIGHT PEN OR END ORDER SEQUENCE OPTIONS.
* STATUS:CHANGE LEVEL 0
* FUNCTION:ALLOWS MODIFICATION TO THE OPTIONS THAT SPECIFY THE TYPE OF
*          INFORMATION THAT IS TO BE QUEUED FOR AN ATTENTION LEVEL
*          ABOUT A LIGHT PEN OR END ORDER SEQUENCE ATTENTION.ALSO
*          ALLOWS THE OPTION TO RESTART OR NOT ON LIGHT PEN OR END
*          ORDER SEQUENCE ATTENTIONS.
* ENTRY:ENTERED AT IFFACA05 VIA CALL TO MLPED.
* INPUT:GR#1 CONTAINS THE ADDRESS OF A WORK/SAVE AREA.THE FIRST WORD
*       CONTAINS THE ADDRESS OF A PARAMETER LIST CONSTRUCTED AS FOLLOWS
*       1.ATTNLEVEL-ADDRESS OF A VARIABLE THAT CONTAINS THE ADDRESS OF
*         THE ATTENTION LEVEL.
*       2.ATTNTYP-ADDRESS OF A VALUE INDICATING:
*         A.(1)-THIS CALL REFERS TO END ORDER SEQUENCE ATTENTIONS.
*         B.(2)-THIS CALL REFERS TO LIGHT PEN ATTENTIONS.
*       3.INFO-ADDRESS OF A VALUE INDICATING:
*         A.(1)-NO DATA RETURN.(DEFAULT)
*         B.(2)-RETURN XY COORDINATES
*         C.(3)-RETURN CHARACTER INFORMATION ON LIGHT PEN ATTENTION.
*         D.(4)-B AND C AS ABOVE.
*       4.RESTART-ADDRESS OF A VALUE INDICATING:
*         A.(1)-RESTART DISPLAY.(DEFAULT)
*         B.(2)-DO NOT RESTART DISPLAY.
* OUTPUT:APPROPRIATE OPTION BITS SET IN ATTENTION LEVEL AND GACB IF
*        SPECIFIED ATTENTION LEVEL IS ACTIVE.
* EXIT:RETURN TO CALLING PROGRAM.
* RETURN CODES:4,0 INCORRECT NUMBER OF PARAMETERS.
*              4,1 INVALID ATTNLEVEL.
*              4,2 INVALID ATTNTYP.
*              4,3 INVALID INFO.
*              4,4 INVALID RESTART.
*              0,0 SUCCESSFULL EXECUTION
* ATTRIBUTES:RE-ENTRANT,PROBLEM PROGRAM MODE.
* NOTES:THIS ROUTINE EXAMINES THE FOLLOWING CONTROL BLOCKS:
*       1.GSPCB
*       2.GTMCB
*       3.MAINLINE GACB
*       4.DIRECT ENTRY GACB
R0       EQU   0                       PARAM REG 0
R1       EQU   1                       PARAM REG 1
RLIST    EQU   2                       LIST REG
RPARAM   EQU   3                       USER PARAM LIST REG
RATL     EQU   4                       ATLV REG
RTYP     EQU   5                       ATTNTYP REG
RINFO    EQU   6                       INFO REG
RSTART   EQU   7                       RESTART REG
RE       EQU   8                       WORK REG
RBASE    EQU   9                       BASE REG
RD       EQU   10                      WORK REG
RGTMCB   EQU   10                      GTMCB ADR REG
RB       EQU   11                      WORK REG
SPARE1   EQU   12                      RSVD PL1
RSAVE    EQU   13                      SAVE AREA PTR REG
RC       EQU   14                      WORK REG
RA       EQU   15                      WORK REG
ADD1     EQU   1                       ADD INFO 1
ADD2     EQU   2                       ADD INFO 2
ADD3     EQU   3                       ADD INFO 3
ADD4     EQU   4                       ADD INFO 4
AID      EQU   X'04'                   ATNLV ID
BPID     EQU   X'01'                   BPID
EOSRT    EQU   X'40'                   EOS RESTART
EOSXY    EQU   X'80'                   EOS XY
GACBOFF  EQU   8                       START OF GACB
LAST     EQU   X'80'                   LAST PARAM
LPCHAR   EQU   X'10'                   LP CHAR
LPSRT    EQU   X'08'                   LP RESTART
LPXY     EQU   X'20'                   LP XY
MASK     EQU   X'FF'                   ALL MASK
RCC4     EQU   16                      RETURN CODE OFFSET
RC4      EQU   X'08'                   RETURN CODE 4
ZERO     EQU   0                       ZERO
IFFACA05 CSECT
         SAVE  (14,12)                 SAVE REGS
         BALR  RBASE,0                 SET UP BASE REG
         USING *,RBASE                 DEFINE BASE REG
         LR    RLIST,R1                SAVE REG
         USING WORKAREA,RLIST          DEFINE DSECT
         L     RPARAM,PLIST            LOAD ADR USER PARM LIST
         USING PARAM,RPARAM            DEFINE DSECT
         L     RA,RCA                  LOAD RTN CODE ARRAY ADR
         XC    ZERO(20,RA),ZERO(RA)    INITIALIZE ARRAY
         B     AROUND                  BRANCH
ERROR    L     RA,RCA                  LOAD RCA ADR
         STC   RB,ZERO(RA)             STORE RETURN CODE
         ST    RC,ZERO(RA,RD)          STORE ADD INFO CODE
OUTRTN   RETURN (14,12),T              RETURN
AROUND   TM    ATNLV,LAST              ONLY PARAM
         BO    MLP01                   BRANCH IF YES
         L     RATL,ATNLV              LOAD INDIRECT PTR
         L     RATL,ZERO(RATL)         LOAD ATLCB ADR
         USING ATTNLEVL,RATL           DEFINE DSECT
         CLI   ATTNLVID+1,AID          IS THIS A VALID ATTN LEVEL
         BE    MLP02                   BRANCH IF VALID
         LA    RC,ADD1                 LOAD ADD INFO
MLP03    LA    RB,RC4                  LOAD RTN CODE
         LA    RD,RCC4                 LOAD OFFSET
         B     ERROR                   BRANCH
MLP01    SR    RC,RC                   ADD INFO=0
         B     MLP03                   BRANCH
MLP02    LR    RE,RATL                 SAVE ADR
MLP05    CLI   ATTNBPID,BPID           POINT TO GTMCB
         BE    MLP04                   BRANCH IF YES
         L     RATL,ATTNBKPT           LOAD ADR NEXT LEVEL
         B     MLP05                   BRANCH
MLP04    L     RGTMCB,ATTNBKPT         LOAD GTMCB ADR
         USING GTMCB,RGTMCB            DEFINE DSECT
         LR    RATL,RE                 RESTORE REG
         L     RE,GTMGSPCB             LOAD GSPCB ADR
         USING GSPCB,RE                DEFINE DSECT
         L     RTYP,ATNTYP             LOAD ATTN TYPE ADR
         LA    RTYP,ZERO(RTYP)         CLEAR HIGH ORDER BYTE
         CL    RTYP,GSPNULLV           ATTNTYP=NULL
         BE    MLP01                   BRANCH IF YES
         L     RTYP,ZERO(RTYP)         LOAD ATTNTYP VALUE
         CL    RTYP,ONE                ATTNTYP=1
         BE    MLP06                   BRANCH IF YES
         CL    RTYP,TWO                ATTNTYP=2
         BE    MLP06                   BRANCH IF YES
         LA    RC,ADD2                 LOAD ADD INFO
         B     MLP03                   BRANCH
MLP06    TM    ATNTYP,LAST             LAST PARAM
         BC    14,MLP07                BRANCH-NO
         L     RINFO,ONE               ASSUME VALUE OF ONE
MLP11    L     RSTART,ONE              ASSUME VALUE OF ONE
         B     MLP08                   BRANCH
MLP07    L     RINFO,INFO              LOAD ADR OF INFO
         LA    RINFO,ZERO(RINFO)       ZERO HI ORDER BYTE
         CL    RINFO,GSPNULLV          INFO=NULL
         BNE   MLP09                   BRANCH IF NO
         L     RINFO,ONE               ASSUME VALUE OF ONE
         B     MLP10                   BRANCH
MLP09    L     RINFO,ZERO(RINFO)       LOAD INFO VALUE
         CL    RINFO,ONE               INFO=1
         BE    MLP10                   BRANCH-YES
         CL    RINFO,TWO               INFO=2
         BE    MLP10                   BRANCH-YES
         CL    RINFO,THREE             INFO=3
         BE    MLP10                   BRANCH-YES
         CL    RINFO,FOUR              INFO=4
         BE    MLP10                   BRANCH-YES
         LA    RC,ADD3                 LOAD ADD INFO CODE
         B     MLP03                   BRANCH
MLP10    TM    INFO,LAST               LAST PARAM
         BO    MLP11                   BRANCH-LAST
         L     RSTART,RESTART          LOAD ADR OF RESTART
         LA    RSTART,ZERO(RSTART)     ZERO HI ORDER BYTE
         CL    RSTART,GSPNULLV         EQUAL NULL
         BE    MLP11                   BRANCH IF YES
         L     RSTART,ZERO(RSTART)     LOAD VALUE OF RESTART
         CL    RSTART,ONE              RESTART=1
         BE    MLP08                   BRANCH-YES
         CL    RSTART,TWO              RESTART=2
         BE    MLP08                   BRANCH-YES
         LA    RC,ADD4                 LOAD ADD INFO
         B     MLP03                   BRANCH
MLP08    CL    RTYP,TWO                ATNTYP=2
         BE    MLP12                   BRANCH=2
         CL    RINFO,ONE               INFO=ONE
         BE    MLP13                   BRANCH=1
         OI    ATTNFLGA,EOSXY          SET EOS XY BIT
         B     MLP14                   BRANCH
MLP13    NI    ATTNFLGA,MASK-EOSXY     CLEAR EOS XY BIT
MLP14    CL    RSTART,ONE              RESTART=ONE
         BE    MLP15                   BRANCH=1
         NI    ATTNFLGA,MASK-EOSRT     TURN OFF EOS RESTART BIT
         B     OUTRTN                  BRANCH
MLP15    OI    ATTNFLGA,EOSRT          SET EOS RESTART BIT
         B     OUTRTN                  BRANCH
MLP12    CL    RINFO,ONE               INFO=1
         BNE   MLP16                   BRANCH NOT= 1
         NI    ATTNFLGA,MASK-LPXY-LPCHAR  CLEAR LP CHAR AND XY BITS
         B     MLP17                   BRANCH
MLP16    CL    RINFO,TWO               INFO=2
         BE    MLP18                   BRANCH=2
         CL    RINFO,THREE             INFO=3
         BE    MLP19                   BRANCH=3
         OI    ATTNFLGA,LPXY+LPCHAR    SET UP CHAR AND XY BITS
         B     MLP17                   BRANCH
MLP18    OI    ATTNFLGA,LPXY           SET RETURN XY ON LP
         B     MLP17                   BRANCH
MLP19    OI    ATTNFLGA,LPCHAR         SET LP CHAR BIT
MLP17    CL    RSTART,ONE              RESTART=1
         BE    MLP20                   BRANCH=1
         NI    ATTNFLGA,MASK-LPSRT     CLEAR LP RESTART FLAG
         B     MLP21                   BRANCH
MLP20    OI    ATTNFLGA,LPSRT          SET UP RESTART FLAG
MLP21    CLC   ATTNFWPT,ZERO1          LEVEL ACTIVE
         BNE   OUTRTN                  BRANCH-NOTACTIVE
         L     RB,GTMGACBA             LOAD PTR TO GACB
         LA    RA,GACBOFF(RB)          LOAD GACB ADR
         LA    RE,MLP22                LOAD RETURN ADR
MLP25    TM    ATTNFLGA,LPSRT          RESTART DESIRED
         BO    MLP23                   BRANCH-YES
         B     MLP24                   BRANCH-NO
MLP22    L     RB,ZERO(RB)             LOAD ADR NEXT GACB PTR
         LA    RA,GACBOFF(RB)          LOAD GACB ADR
         LA    RE,OUTRTN               LOAD RETURN ADR
         B     MLP25                   BRANCH
MLP23    SAEC  RESTART=YES,MF=(E,(15))
         BR    RE                      RETURN
MLP24    SAEC  RESTART=NO,MF=(E,(15))
         BR    RE                      RETURN
         DS    0F
ZERO1    DC    1F'0'
ONE      DC    A(1)
TWO      DC    A(2)
THREE    DC    A(3)
FOUR     DC    A(4)
         CNOP  0,8
         COPY  ATTNLEVL
         COPY  GSPCB
         COPY  GTMCB
PARAM    DSECT
ATNLV    DS    1F
ATNTYP   DS    1F
INFO     DS    1F
RESTART  DS    1F
WORKAREA DSECT
PLIST    DS    1F
RCA      DS    1F
         END
