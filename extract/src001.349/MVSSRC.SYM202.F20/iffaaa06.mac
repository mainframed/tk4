*
*        TMGDS IS ENTERED FROM DIRECTOR PART 2 WITH THE ADDRESS OF
*        THE WORK AREA IN REGISTER 1.  THE FIRST WORD OF THE WORK
*        AREA CONTAINS THE ADDRESS OF THE PARAMETER LIST.
*
*        TMGDS INPUT PARAMETER LIST:
*
*        +0    A(GDSVAR)
*
*        EXTERNAL REFERENCES ARE MADE TO:
*
*        BUFFER MANAGEMENT   IFFAHAO2
*        FLOW CONTROL MANAGEMENT  IFFAHAO1
*        KEY TABLE MANAGEMENT  IFFAHAO3
*
*        EXTERNAL MACROS USED ARE:
*
*        LINK
*        FREEMAIN
*
*
*        ON ENTRY THE BASE REGISTER IS SET UP, REGISTERS ARE
*        SAVED, THE RETURN ARRAY IS INITIALIZED, AND THE WORK
*        AREA DSECT IS INITIALIZED.
*
IFFAAA06 CSECT
*0114,335000                                                       000A
*C855000,A920500-921000                                          YM1963
CLSGDS   SAVE  (14,12)
         BALR  BASE,0
         USING *,BASE
         LR    WORK,PARM           SET UP WORK AREA
         USING WORKAREA,WORK
         L     PARM,WRKPARM        PARM LIST
         L     GDSREG,0(PARM)      ASDR OF GDS CONST
         L     GDSREG,0(GDSREG)    ADDRESS OF THE GDSCB
         USING GDSCB,GDSREG
         L     ERRCD,WRKRTNCD
         XC    0(20,ERRCD),0(ERRCD) ZERO RETURN ARRAY
         XC    WRKSPVSR(12),WRKSPVSR ZERO SUPERVISOR PARAMETER LIST
*
*        THE INPUT PARAMETER IS CHECKED FOR VALIDITY AND IF VALID,
*        THE GDSCB DSECT IS INITIALIZED.  THE SAVE AREAS ARE THEN
*        CHAINED, AND THE GTMCB DSECT IS INITIALIZED.  A CHECK IS
*        MADE TO SEE IF ANY DATA WAS EVER GENERATED.  IF NOT,
*        A BRANCH IS MADE TO THE FREEMAIN.  A CHECK IS MADE TO
*        SEE IF THERE IS A KEY TABLE AND IF SO, IT IS FREED BY
*        KEY TABLE MANAGEMENT.
*
         C     GDSREG,GDSGDSCB     IS THIS A VALID GDS
         BNE   ERR1                NO
         ST    SAVE,WRKSAVE+4      SET UP SAVE AREA CHAIN
         LA    REGA,WRKSAVE
         ST    REGA,8(SAVE)
         LR    SAVE,REGA
         L     GTMREG,GDSGTMCB     PICK UP ADDR OF THE GTMCB
         USING GTMCB,GTMREG
         L     REGB,16             CVT
         L     REGB,CVTLINK(REGB)  LINK LIB DCB
         CLC   GDSFCBUF,ZERO
         BE    DSC00012            FLOW CONTROL NEVER ENTERED
         LA    REGD,DSC00005            SET TO FALL THROUGH
DSC00003 L     REGA,GDSKEYTB            ADDR OF KEY TABLE
         LTR   REGA,REGA           IN EXISTANCE
         BCR   8,REGD                   NO
         ST    GDSREG,WRKPRLST
         LA    REGA,FOUR           BUILD PARM LIST TO DELETE
         ST    REGA,WRKPRLST+4     THE KEY TABLE
         LA    PARM,WRKPRLST
         L     REGA,GTMGSPCB       GET POINTER TO GSPCB            000A
         L     REGA,FE(REGA)       GET POINTER TO STATUS TABLE     000A
         L     REG15,ETE(REGA)     GET ADRS OF KEY TABLE MGMT      000A
         BALR  RETURN,REG15        BRANCH TO KEY TABLE MGMT        000A
         BR    REGD                     RETURN
*
*        NEXT A CHECK IS MADE TO SEE IF INDEV HAS CLOSED OUT
*        BUFFER MANAGEMENT AND FLOW CONTROL MANAGEMENT.  IF NOT,
*        BOTH ARE NOW CALLED TO REMOVE THE GDS.
*
DSC00005 CLC   GTMFCTBL,ZERO
         BE    DSC00012            FLOW CONTROL ALREADY CLOSED
         LA    REGA,3              CODE FOR DELETE A
         ST    REGA,WRKSPACE       DATASET FROM FLOW CTL
         ST    GDSREG,WRKSPACE+4
         LA    PARM,WRKSPACE
*//
*//      CALL FLOW CONTROL
*//
         ST    PARM,WRKSAVE+72
         MVC   WRKSAVE+76(4),WRKRTNCD
         LA    PARM,WRKSAVE+72
         LINK  EP=IFFAHA01,MF=(E,(1)),SF=(E,WRKSPVSR)
         LA    REGA,4              CODE TO FREE SUBSEGMT
         CLC   GDSGDOAL,SUBSEG     IS BUFFER A SUB SEGMENT
         BNH   DSC00010            YES
         LA    REGA,1(REGA)        NO INCREASE CODE TO FULL SEGMT
DSC00010 ST    REGA,WRKSPACE       STORE CHAIN
         LA    PARM,WRKSPACE
*
*        CALL BUFFER MANAGMENT     RELEASE ALL BUFFER
*
         LA    PARM,WRKSAVE+72
         L     REG15,STAT(ERRCD)   STATUS TABLE
         L     REG15,BMGT(REG15)   ADDR OF BUFFER MGT
         BALR  RETURN,REG15        CALL
         EJECT
*
*        THE GDSCB IS REMOVED FROM THE CHAIN OF GDSCBS ON THE
*        GTMCB.  CORE IS THEN FREED FOR THE GDSCB, ITS OACBS
*        AND GDAOS.  A CHECK IS MADE TO SEE IF THERE WERE ANY
*        EQUIVALENCE GDSS.  IF SO THEY ARE ALSO REMOVED FROM THE
*        CHAIN OF GDSCBS ON THE GTMCB AND THE STORAGE FOR EACH IS
*        FREED.
*
DSC00012 LA    RETURN,DSC00040     SET UP RETURN
DSC00015 LA    REGB,GTMGDSCB
DSC00020 L     REGA,0(REGB)
         CR    REGA,GDSREG
         BE    DSC00030
         LA    REGB,NXGDS(REGA)
         B     DSC00020
DSC00030 MVC   0(4,REGB),GDSNXGDS  REMOVE FROM CHAIN
         XC    GDSGDSCB(4),GDSGDSCB ZERO OUT ADDR TO MAKE INVALID
         XC    GDSGDSID(1),GDSGDSID
         BR    RETURN
DSC00040 LH    REGC,GDSGDOAL       PICK UP GDOA LENGTH
         SLL   REGC,1              DOUBLE IT
         A     REGC,DBLOACB
         A     REGC,GDSLEN         LENGTH OF GETMAIN AREA
         TM    GDSFLAGS,X'08'      IS THIS SHARED
         BO    DSC00080            YES
         XC    WRKPARM(4),WRKPARM  ZERO OUT FOR SWITCH
DSC00050 LR    REGZERO,REGC
         LR    PARM,GDSREG
         FREEMAIN R,LV=(0),A=(1)   FREE CORE
         CLC   WRKPARM,=F'0'
         BE    DSC00090            EXIT
         L     GDSREG,WRKSHRDF     ALL FOREWARD FREED
         LTR   GDSREG,GDSREG
         BZ    DSC00070            YES
         BAL   REGD,DSC00003            FREE KEY TABLE
         MVC   WRKSHRDF(4),GDSSHDFD NO, REMOVE ONE FROM CHAIN
DSC00060 BAL   RETURN,DSC00015     REMOVE FROM GDS CHAIN AND ZERO ID
         B     DSC00050
DSC00070 L     GDSREG,WRKSHRDB     PICK UP BACK PTR
         LTR   GDSREG,GDSREG       ANY MORE
         BZ    DSC00090            NO
         BAL   REGD,DSC00003            FREE KEY TABLE
         MVC   WRKSHRDB(4),GDSSHDBK REMOVE FROM CHAIN
         B     DSC00060
DSC00080 MVC   WRKSHRDF(4),GDSSHDFD SAVE FOREWARD PTR
         MVC   WRKSHRDB(4),GDSSHDBK SAVE BACKWARD PTR
         B     DSC00050
DSC00090 L     SAVE,4(SAVE)        RESET SAVE AREA
         RETURN (14,12),T
ERR1     MVI   0(ERRCD),X'08'
         MVI   19(ERRCD),X'01'     INVALID GDSCB
         RETURN (14,12),T
         EJECT
REGZERO  EQU   0
PARM     EQU   1
REGA     EQU   2
REGB     EQU   3
REGD     EQU   4
WORK     EQU   5
GTMREG   EQU   7
GDSREG   EQU   8
BASE     EQU   9
REGC     EQU   10
ERRCD    EQU   11
SAVE     EQU   13
RETURN   EQU   14
REG15    EQU   15
BMGT     EQU   816
CVTLINK  EQU   8
STAT     EQU   24
NXGDS    EQU   0
ETE      EQU   828                                                 000A
FE       EQU   48                                                  000A
ZERO     DC    F'0'
FOUR     DC    F'4'
DBLOACB  DC    F'120'              DOUBLE OACB LENGTH
GDSLEN   DC    F'124'              LENGTH OF GDSCB               YM1963
SUBSEG   DC    H'128'
         EJECT
WORKAREA DSECT
WRKPARM  DS    F
WRKRTNCD DS    F
WRKSPVSR DS    3F
WRKSPACE DS    3F
WRKSHRDF DS    F
WRKSHRDB DS    F
WRKSAVE  DS    18F
WRKPRLST DS    2F
         COPY  GSPCB
         COPY  GTMCB
         COPY  GDSCB
GDSXCOR  DS    F                   X CORR FACTOR FOR DGEN        YM1963
GDSYCOR  DS    F                   Y CORR FACTOR FOR DGEN        YM1963
         END
