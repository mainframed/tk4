* TITLE:IDPOS-INDICATE POSITION
* STATUS:CHANGE LEVEL 0
* FUNCTION:TO PASS TO GSP THE POSITION OF THE BEAM FOR SUBSEQUENT DATA
*          GENERATION.
* ENTRY:ENTERED AT IFFAFA13 VIA A CALL TO IDPOS.
* INPUT:GR#1 CONTAINS THE ADDRESS OF A WORK/SAVE AREA.THE FIRST WORD
*       CONTAINS THE ADDRESS OF A PARAMETER LIST CONSTRUCTED AS FOLLOWS
*       1.GDSNAME:ADDRESS OF A VARIABLE WHICH CONTAINS THE ADDRESS OF
*         THE GDSCB.
*       2.XCOOR:ADDRESS OF A CONSTANT OR VARIABLE,THE VALUE OF WHICH
*         DESIGNATES THE POSITION THAT IS TO BE USED AS THE ABSOLUTE
*         STARTING POINT FROM WHICH OTHER X CO-ORDINATES ARE TO BE
*         COMPUTED.
*       3.YCOOR-SAME AS X EXCEPT IN THE Y DIRECTION.
*             4.XBEAM-ADDRESS OF A CONSTANT OR VARIABLE WHICH CONTAINS
*                     THE VALUE OF THE XBEAM POSITION.
*             5.YBEAM-SAME AS XBEAM EXCEPT IN Y DIRECTION.
* OUTPUT:NONE TO CALLER.THE GPSXLAST AND GPSYLAST FIELDS OF THE GPSCB
*        ARE UPDATED TO REFLECT THE NEW POSITION.GDSXCURR AND
*        GDSYCURR FIELDS ARE SET EQUAL TO GDSXLAST AND GDSYLAST(NO BEAM
*        PARAMETERS PASSED) OR ARE UPDATED TO REFLECT THE NEW BEAM
*        POSITIONS.
* EXIT:RETURN TO CALLER VIA REGISTER  14.
* RETURN CODES:2,0  SCALING ERROR
*              4,0  INSUFFICIENT NUMBER OF PARAMETERS
*              4,1  INVALID GDSCB
*              0,0  SUCCESSFULL
* ATTRIBUTES:RE-ENTRANT,PROBLEM PROGRAM MODE
* EXTERNAL ROUTINES:SCALE
* NOTES:THIS ROUTINE EXAMINES THE FOLLOWING CONTROL BLOCKS:
*       1.GDSCB
*       2.GTMCB
*       3.GSPCB
R0       EQU   0                       PARM REQ 0
R1       EQU   1                       PARM REG 1
RLIST    EQU   2                       WORKAREA REG
RPARAM   EQU   3                       USER PARAM LIST REG
RGDSCB   EQU   4                       GDSCB ADR REG
RE       EQU   8                       WORK REG
RBASE    EQU   9                       BASE REG
RA       EQU   10                      WORK REG
RB       EQU   11                      WORK REG
SPARE1   EQU   12                      RSVD PL1
RSAVE    EQU   13                      SAVE AREA PTR
RC       EQU   14                      WORK REG
RD       EQU   15                      WORK REG
ZERO     EQU   0                       ZERO
RC4      EQU   X'08'                   RTN CODE 4
RCC4     EQU   16                      OFFSET TO RC 4
ADD1     EQU   1                       ADD INFO
LAST     EQU   X'80'                   LAST PARAM
GID      EQU   X'03'                   GDSCB ADR
OFFSAVE  EQU   8                       SAVE AREA CHAIN
XREAL    EQU   X'C0'                   REAL MODE X
XINTG    EQU   X'30'                   INTEGER MODE X
YREAL    EQU   X'0C'                   REAL MODE Y
YINTG    EQU   X'03'                   INTEGER MODE Y
XREL     EQU   X'80'                   X-REAL-ABSOLUTE
XINT     EQU   X'20'                   X-INTEGER ABSOLUTE
YREL     EQU   X'08'                   Y-REAL-ABSOLUTE
YINT     EQU   X'02'                   Y-INTEGER-ABSOLUTE
IFFAFA13 CSECT
         SAVE  (14,12)
         BALR  RBASE,0                 SET UP BASE REG
         USING *,RBASE                 DEFINE BASE REG
         LR    RLIST,R1                LOAD REG
         USING WORKAREA,RLIST          DEFINE DSECT
         L     RPARAM,PLIST            LOAD ADR USER PARM LIST
         USING PARAM,RPARAM            DEFINE DSECT
         ST    RSAVE,SAVE+4            SAVE-SAVE AREA PTR
         LA    RD,SAVE                 LOAD ADR OF MY SAVE AREA
         ST    RD,OFFSAVE(RSAVE)       STORE IN USER SAVE AREA
         LA    RSAVE,SAVE              ESTABLISH NEW SAVE AREA
         L     RA,RCA                  LOAD RTN CODE ARRAY ADR
         XC    ZERO(20,RA),ZERO(RA)    INITIALIZE ARRAY
         B     AROUND                  BRANCH
ERROR    L     RA,RCA                  LOAD RTN CODE ARRAY ADR
         STC   RB,ZERO(RA)             STORE RTN CODE
         ST    RC,ZERO(RA,RD)          STORE ADD INFO
OUTRTN   L     RSAVE,SAVE+4            RESTORE OLD SAVE AREA PTR
         RETURN (14,12),T              RETURN TO CALLING PROGRAM
AROUND   TM    GDSNM,LAST              LAST PARAM
         BO    IND01                   BRANCH-YES
         TM    XCOOR,LAST              LAST PARAM
         BO    IND01                   BRANCH-YES
         B     IND02                   BRANCH
IND01    SR    RC,RC                   ADD INFO=0
IND03    LA    RB,RC4                  LOAD RTN CODE
         LA    RD,RCC4                 LOAD OFFSET
         B     ERROR                   BRANCH
IND02    L     RGDSCB,GDSNM            LOAD IN DIRECT PTR TO GDSCB
         L     RGDSCB,ZERO(RGDSCB)     LOAD PTR TO GDSCB
         USING GDSCB,RGDSCB            DEFINE DSECT
         CLI   GDSGDSID,GID            IS THIS VALID GDSCB
         BE    IND04                   BRANCH IF YES
         LA    RC,ADD1                 LOAD ADD INFO
         B     IND03                   BRANCH
IND04    L     RA,GDSGTMCB             LOAD GTMCB ADR
         USING GTMCB,RA                DEFINE DSECT
         L     RB,GTMGSPCB             LOAD GSPCB ADR
         USING GSPCB,RB                DEFINE DSECT
         L     RC,XCOOR                LOAD XCOOR PARM
         CL    RC,GSPNULLV             XCOOR=NULL
         BE    IND01                   BRANCH-YES
         L     RC,YCOOR                L YCOOR PARM
         LA    RC,ZERO(RC)             ZERO HI-ORDER BYTE
         CL    RC,GSPNULLV             YCOOR=NULL
         BE    IND01                   BRANCH-YES
         XC    DATARAY(52),DATARAY     CLEAR INPUT DATA ARRAY
         ST    RGDSCB,DATARAY          GDSCB ADR TO ARRAY
         MVC   RTNARAY(4),RCA          ADR OF RTN CODE ARRAY TO ARRAY
         L     RA,XCOOR                LOAD ADR OF X CO-ORD INATE
         MVC   XABS(4),ZERO(RA)        X CO-ORD TO ARRAY
         L     RA,YCOOR                LOAD ADR OF Y CO-ORDINATE
         LA    RA,ZERO(RA)             CLEAR HI ORDER BYTE
         MVC   YABS(4),ZERO(RA)        Y CO-ORD TO ARRAY
         IC    RE,GDSDATMD             SAVE DATA MODE
         XC    MODE(1),MODE            CLEAR TEMP MODE AREA
         TM    GDSDATMD,XREAL          IS X REAL
         BC    5,IND05                 BRANCH-X REAL
         OI    MODE,XINT               SET X=INTEGER
         B     IND06                   BRANCH
IND05    OI    MODE,XREL               SET X=REAL
IND06    TM    GDSDATMD,YREAL          IS Y REAL
         BC    5,IND07                 BRANCH-Y REAL
         OI    MODE,YINT               SET Y= INTEGER
         B     IND08                   BRANCH
IND07    OI    MODE,YREL               SET Y=REAL
IND08    MVC   GDSDATMD(1),MODE        MOVE IN DUMMY MODE
         LA    R1,DATARAY              LOAD INPUT PARAM LIST ADR
         LINK  EP=IFFAHA06,MF=(E,(1))
         LTR   RD,RD                   WAS SCALE SUCCESSFULL
         BZ    IND09                   BRANCH IF YES
         STC   RE,GDSDATMD             RESTORE DATA MODE
         B     OUTRTN                  BRANCH
IND09    MVC   SAVEX(4),GDSXLAST       SAVE VALUE
         MVC   SAVEY(4),GDSYLAST       SAVE VALUE
         MVC   GDSXLAST(4),XABS        MOVE IN SCALED DATA
         MVC   GDSYLAST(4),YABS        MOVE IN SCALED DATA
         TM    YCOOR,LAST              WAS BEAM PASSED
         BO    IND10                   BRANCH-NO BEAM PASSED
         B     IND11                   BRANCH TO BEAM PROCESSING
IND10    MVC   GDSXCURR(2),XABS+2      MOVE IN SCALLED DATA
         MVC   GDSYCURR(2),YABS+2      MOVE IN SCALED DATA
         STC   RE,GDSDATMD             RESTORE DATA MODE IN GDSCB
         B     OUTRTN                  BRANCH
IND11    XC    DATARAY(52),DATARAY     CLEAR DATA ARRAY
         ST    RGDSCB,DATARAY          GDSCB ADR TO DATA ARRAY
         MVC   RTNARAY(4),RCA          MOVE IN ADR OF RTN CODE ARRAY
         L     RA,GDSGTMCB             LOAD GTMCB ADR
         L     RB,GTMGSPCB             LOAD GSPCB ADR
         L     RA,XBEAM                LOAD ADR OF XBEAM
         LA    RA,ZERO(RA)             ZERO HI ORDER BYTE
         CL    RA,GSPNULLV             XBEAM=NULL
         BE    IND17                   BRANCH=NULL
         MVC   XABS(4),ZERO(RA)        X BEAM TO INPUT ARRAY
IND12    TM    XBEAM,LAST              LAST PARAM
         BO    IND17                   BRANCH LAST PARAM
         L     RA,YBEAM                LOAD ADR OF YBEAM
         LA    RA,ZERO(RA)             ZERO HI ORDER BYTE
         CL    RA,GSPNULLV             YBEAM=NULL
         BE    IND17                   BRANCH=NULL
         MVC   YABS(4),ZERO(RA)        MOVE IN YBEAM TO INPUT ARRAY
IND14    LA    R1,DATARAY              LOAD ADR OF PARAM LIST
         LINK  EP=IFFAHA06,MF=(E,(1))  LINK TO SCALE
         LTR   RD,RD                   WAS SCALE SUCCESSFULL
         BZ    IND15                   BRANCH-SCALE SUCCESSFULL
         STC   RE,GDSDATMD             RESTORE DATA MODE
         MVC   GDSXLAST(4),SAVEX       RESTORE GDSXLAST TO PREVIOUS VAL
         MVC   GDSYLAST(4),SAVEY       RESTORE GDSYLAST TO PREVIOUS VAL
         B     OUTRTN                  BRANCH
IND15    STC   RE,GDSDATMD             RESTORE DATA MODE
         MVC   GDSXCURR(2),XABS+2      MOVE IN SCALED XBEAM
         MVC   GDSYCURR(2),YABS+2      MOVE IN SCALED YBEAM
         B     OUTRTN                  BRANCH
IND17    MVC   GDSXLAST(4),SAVEX       RESTORE XLAST
         MVC   GDSYLAST(4),SAVEY       RESTORE YLAST
         STC   RE,GDSDATMD             RESTORE DATA MODE
         B     IND01                   BRANCH
         CNOP  0,8
         COPY  GTMCB
         COPY  GSPCB
         COPY  GDSCB
PARAM    DSECT
GDSNM    DS    1F
XCOOR    DS    1F
YCOOR    DS    1F
XBEAM    DS    1F
YBEAM    DS    1F
WORKAREA DSECT
PLIST    DS    1F
RCA      DS    1F
SAVE     DS    18F
SAVEX    DS    1F
SAVEY    DS    1F
MODE     DS    1F
DATARAY  DS    1F
RTNARAY  DS    1F
DUMMY1   DS    4F
XABS     DS    1F
YABS     DS    1F
DUMMY2   DS    5F
         END
