*STATUS: CHANGE LEVEL 000
*
*FUNCTION/OPERATION: CONVERTS ABSOLUTE USER COORDINATES TO ABSOLUTE
*        INTEGER RASTER UNIT COORDINATES BY CALLING SCALE OR IT
*        CONVERTS ABSOLUTE INTEGER RASTER UNIT COORDINATES INTO
*        ABSOLUTE USER COORDINATES.  THE X AND/OR THE Y COORDINATES
*        MAY BE CONVERTED
*
*ENTRY POINTS: IFFAGA08
*
*INPUT:  REGISTER 1 POINTS TO A WORKAREA.  THE FIRST WORD CONTAINS THE
*        ADDRESS OF THE PARAMETER LIST, AND THE SECOND, THAT OF THE
*        RETURN CODE ARRAY.
*
*        THE VARIABLE LENGTH PARAMETER LIST IS AS FOLLOWS.
*        REQUIRED PARAMETERS
*    + 0 A(POINTER TO THE GDSCB)
*
*    + 4 A(CONVERT FUNCTION VARIABLE)
*
*        OPTIONAL PARAMETERS
*    + 8 A(X-INPUT VALUE)    IF NON-NULL, NEXT 2 PARAMETERS MUST BE
*                            PASSED WITH THE X-OUTPUT VALUE NON-NULL
*    +12 A(Y-INPUT VALUE)    IF NON-NULL, NEXT 2 PARAMETERS MUST BE
*                            PASSED WITH THE Y-OUTPUT VALUE NON-NULL
*    +16 A(X-OUTPUT VALUE)   MUST BE NON-NULL IF X-INPUT VALUE NON-NULL
*
*    +20 A(Y-OUTPUT VALUE)   MUST BE NON-NULL IF Y-INPUT VALUE NON-NULL
*
*OUTPUT: CONVERTED COORDINATES AS APPROPRIATE
*
*EXTERNAL ROUTINES: SCALE (BOTH ENTRY POINTS IFFAHA06 AND IFFAAA15)
*
*EXITS-NORMAL AND ERROR:  VIA RETURN MACRO TO CALLING PROGRAM
*
*TABLES/WORKAREAS: GSP WORKAREA
*
* ATTRIBUTES: PROBLEM STATE, REENTRANT
IFFAGA08 CSECT
*2421,438000,537000                                                6292
         SAVE  (14,12)
         BALR  CVT09,0                 ADDRESSABILITY FOR
         USING *,CVT09                 CSECT
         LR    CVT08,CVT01             ADDRESSABILITY FOR WORK AREA
         USING CVTWORK,CVT08
         LA    CVT02,CVTSAVE           CHAIN
         ST    CVT13,CVTSAVE+HSVA      SAVE
         ST    CVT02,MSVA(CVT13)       AREAS
         LR    CVT13,CVT02
         L     CVT07,CVTPARM           ADDRESSABILITY FOR PARAMETERS
         USING CVTPARTB,CVT07
         L     CVT10,CVTGDSCB          GET POINTER TO GDSCB
         L     CVT10,0(CVT10)          ADDRESSABILITY FOR GDSCB
         USING GDSCB,CVT10
         L     CVT11,CVTRTNA           ADDRESSABILITY FOR RETURN ARRAY,
         USING GSPARRAY,CVT11          NULL VARIABLE, AND STATUS TABLE
         XC    CVTCTRL(1),CVTCTRL      SET CONTROL SWITCHES = 0
         MVI   GSPARRAY,X'00'          SUCCESSFUL COMPLETION
         C     CVT10,GDSGDSCB          INVALID GDSCB
         BNE   CVT018                  YES, ERROR RETURN
*
*
*        CHECK PARAMETER VALIDITY
*
         TM    CVTGDSCB,X'80'          IS GDSCB PTR. ONLY PARAMETER
         BO    CVT019                  YES, ERROR-SHORT PARAMETER LIST
         CLC   CVTCNVRT+1(3),GSPNULLV+1 CONVERT VARIABLE NULLED
         BE    CVT022                  YES, ERROR RETURN
         L     CVT02,CVTCNVRT
         L     CVT02,0(CVT02)          GET CONVERT VARIABLE
         C     CVT02,CVT0001           LESS THAN 1
         BL    CVT022                  YES, ERROR RETURN
         C     CVT02,CVT0002           GREATER THAN 2
         BH    CVT022                  YES, ERROR RETURN
         TM    CVTCNVRT,X'80'          CONVERT LAST PARAMETER
         BO    CVT013                  YES, IMMEDIATE RETURN
*
         CLC   CVTXINP+1(3),GSPNULLV+1 IS X-INPUT NULLED
         BE    CVT001                  YES, CHECK Y
         TM    CVTXINP,X'80'           NO, IS IT END OF LIST
         BO    CVT020                  YES, ERROR-X OUTPUT NOT PASSED
         TM    CVTYINP,X'80'           Y INPUT PRESENT
         BO    CVT020                  NO, ERROR-X OUTPUT NOT PASSED
         CLC   CVTXOUT+1(3),GSPNULLV+1 IS X OUTPUT NULLED
         BE    CVT020                  YES, ERROR-X OUTPUT NOT PASSED
         OI    CVTCTRL,XPROC           NO, X TO BE PROCESSED
*
CVT001   TM    CVTXINP,X'80'           X INPUT END OF LIST
         BO    CVT013                  YES, IMMEDIATE RETURN
         CLC   CVTYINP+1(3),GSPNULLV+1 IS Y INPUT NULLED
         BE    CVT002                  YES, DO ANY PROCESSING
         TM    CVTYINP,X'80'           NO, IS IT END OF LIST
         BO    CVT021                  YES, ERROR-Y OUTPUT NOT PASSED
         TM    CVTXOUT,X'80'           IS X OUTPUT PRESENT
         BO    CVT021                  NO, ERROR-Y OUTPUT NOT PASSED
         CLC   CVTYOUT+1(3),GSPNULLV+1 IS Y OUTPUT NULLED
         BE    CVT021                  YES, ERROR-Y OUTPUT NOT PASSED
         OI    CVTCTRL,YPROC           NO, Y TO BE PROCESSED
*
CVT002   CLC   CVTCNVRT+1(3),GSPNULLV+1  CNVRT VARIABLE NULLED
         BE    CVT022                  YES, ERROR- CNVRT VARIABLE BAD
         L     CVT02,CVTCNVRT          GET ADDRESS OF VARIABLE
         CLC   0(4,CVT02),CVT0001      IS IT = 1
         BE    CVT014                  YES, CONVERT TO USER UNITS
         CLC   0(4,CVT02),CVT0002      IS IT = 2
         BNE   CVT022                  NO, ERROR- CNVRT VARIABLE BAD
*                                      YES, CONVERT TO RASTER UNITS
*        IN CONVERTING TO RASTER UNITS, THE DATA MODE IS TEMPORARILY
*        FORCED TO ABSOLUTE.  A PARAMETER TABLE FOR SCALE IS THEN
*        GENERATED IN THE WORKAREA.  SCALE IS THEN CALLED TO PERFORM
*        THE CONVERSION.
*
         MVC   CVTDATA(1),GDSDATMD     SAVE DATA MODE SWITCH
         TM    CVTDATA,XREAL           CHECK X DATA MODE
         BZ    CVT003
         MVI   GDSDATMD,XREALABS       FORCE TO
         B     CVT004                  ABSOLUTE INPUT
CVT003   MVI   GDSDATMD,XINTABS        IN PROPER MODE
*
CVT004   TM    CVTDATA,YREAL           CHECK Y DATA MODE
         BZ    CVT005
         OI    GDSDATMD,YREALABS       FORCE TO
         B     CVT006                  ABSOLUTE INPUT
CVT005   OI    GDSDATMD,YINTABS        IN PROPER MODE
CVT006   TM    CVTCTRL,XPROC           X TO BE PROCESSED
         BZ    CVT007                  NO, SET TO ZERO
         L     CVT02,CVTXINP           YES, GET ADDRESS OF INPUT VALUE
         MVC   CVTXABS(4),0(CVT02)     MOVE INTO PARAMETER TABLE
         B     CVT008
CVT007   XC    CVTXABS(4),CVTXABS
*
CVT008   TM    CVTCTRL,YPROC           Y TO BE PROCESSED
         BZ    CVT009                  NO, SET TO ZERO
         L     CVT02,CVTYINP           GET ADDRESS OF INPUT VALUE
         MVC   CVTYABS(4),0(CVT02)     MOVE INTO PARAMETER TABLE
         B     CVT010
CVT009   XC    CVTYABS(4),CVTYABS
*
CVT010   XC    CVTSCNT(4),CVTSCNT      SET COUNT TO ZERO
         ST    CVT10,CVTGDS2           STORE POINTERS TO GDSCB
         ST    CVT11,CVTRTNA2          AND RETURN ARRAY
         L     CVT02,GSPASTAT          GET ADDRESS OF SCALE
         L     CVT15,HA06(CVT02)
         LA    CVT01,CVTGDS2           GET POINTER TO WORKAREA
         BALR  CVT14,CVT15             CALL SCALE
         MVC   GDSDATMD(1),CVTDATA     RESTORE DATA MODE SWITCH
         LTR   CVT15,CVT15             ANY SCALING ERROR
         BP    CVT024
*
CVT011   TM    CVTCTRL,XPROC           X OUTPUT
         BZ    CVT012
         L     CVT02,CVTXOUT           YES, GET OUTPUT LOCATION
         MVC   0(4,CVT02),CVTXABS      PLACE VALUE THERE
*
CVT012   TM    CVTCTRL,YPROC           Y OUTPUT
         BZ    CVT013
         L     CVT02,CVTYOUT           YES, GET OUTPUT LOCATION
         MVC   0(4,CVT02),CVTYABS      PLACE VALUE THERE
*
CVT013   L     CVT13,HSVA(CVT13)
         RETURN (14,12),T
*
CVT014   TM    CVTCTRL,XPROC           X TO BE PROCESSED
         BZ    CVT016                  NO, GO DO Y
         L     CVT02,CVTXINP           YES, GET INPUT VALUE
         MVC   CVTXABS(4),0(CVT02)
*
         TM    GDSDATMD,XREAL          IS X INPUT REAL
         BZ    CVT015                  NO, INTEGER
         LA    CVT01,CVTXABS           FLOAT RASTER UNIT COORDINATE
         BAL   CVT14,CVTFLOAT
*
         LE    CVT00F,CVTXABS          COMPUTE  X-GLLX
         SE    CVT00F,GDSXVRLL
         LE    CVT02F,GDSUVLUR         COMPUTE  GURU-GLLU
         SE    CVT02F,GDSUVLLL
         MER   CVT00F,CVT02F           COMPUTE (X-GLLX)(GURU-GLLU)
         LE    CVT02F,GDSXVRUR         COMPUTE (GURX-GLLX)
         SE    CVT02F,GDSXVRLL
         DER   CVT00F,CVT02F           COMPUTE U
         AE    CVT00F,GDSUVLLL         ADD GLLU
         STE   CVT00F,CVTXABS          AND STORE
         B     CVT016
*
CVT015   L     CVT03,CVTXABS           COMPUTE (X-GLLX)
         S     CVT03,GDSXVILL
         L     CVT04,GDSUVLUR          COMPUTE (GURU-GLLU)
         S     CVT04,GDSUVLLL
         MR    CVT02,CVT04             COMPUTE (X-GLLX)(GURU-GLLU)
         L     CVT04,GDSXVIUR          COMPUTE (GURX-GLLX)
         S     CVT04,GDSXVILL
         DR    CVT02,CVT04             COMPUTE U
         LTR   CVT02,CVT02             CHECK FOR REMAINDER         6292
         BZ    CVT0151                 BRANCH IF NO REMAINDER      6292
         A     CVT03,CVT0001           ADD ONE  ROUNDING UPER      6292
CVT0151  A     CVT03,GDSUVLLL          ADD GLLU
         ST    CVT03,CVTXABS           AND STORE
*
CVT016   TM    CVTCTRL,YPROC           Y TO BE PROCESSED
         BZ    CVT011                  NO, STORE ANY VALUES
         L     CVT02,CVTYINP           YES. GET INPUT VALUE
         MVC   CVTYABS(4),0(CVT02)
*
         TM    GDSDATMD,YREAL          IS Y INPUT REAL
         BZ    CVT017                  NO, INTEGER
         LA    CVT01,CVTYABS           FLOAT RASTER UNIT COORDINATE
         BAL   CVT14,CVTFLOAT
*
         LE    CVT00F,CVTYABS          COMPUTE Y-GLLY
         SE    CVT00F,GDSYVRLL
         LE    CVT02F,GDSVVLUR         COMPUTE GURV-GLLV
         SE    CVT02F,GDSVVLLL
         MER   CVT00F,CVT02F           COMPUTE (Y-GLLY)(GURV-GLLV)
         LE    CVT02F,GDSYVRUR         COMPUTE GURY-GLLY
         SE    CVT02F,GDSYVRLL
         DER   CVT00F,CVT02F           COMPUTE V
         AE    CVT00F,GDSVVLLL         ADD GLLV
         STE   CVT00F,CVTYABS          AND STORE
         B     CVT011
*
CVT017   L     CVT03,CVTYABS           COMPUTE  Y-GLLY
         S     CVT03,GDSYVILL
         L     CVT04,GDSVVLUR          COMPUTE  GURV-GLLV
         S     CVT04,GDSVVLLL
         MR    CVT02,CVT04             COMPUTE (Y-GLLY)(GURV-GLLV)
         L     CVT04,GDSYVIUR          COMPUTE  GURY-GLLY
         S     CVT04,GDSYVILL
         DR    CVT02,CVT04             COMPUTE V
         LTR   CVT02,CVT02             CHECK FOR REMAINDER         6292
         BZ    CVT0171                 BRANCH IF NO REMAINDER      6292
         A     CVT03,CVT0001           ADD ONE  ROUNDING UP        6292
CVT0171  A     CVT03,GDSVVLLL                                      6292
         ST    CVT03,CVTYABS           AND STORE
         B     CVT011
*
CVT018   MVC   GSPARRAY+16(4),CVT0001  BAD GDSCB
         B     CVT023
*
CVT019   XC    GSPARRAY+16(4),GSPARRAY+16 BAD PARAMETER LIST
         B     CVT023
*
CVT020   MVC   GSPARRAY+16(4),CVT0005  X OUTPUT NOT PASSED
         B     CVT023
*
CVT021   MVC   GSPARRAY+16(4),CVT0006  Y OUTPUT NOT PASSED
         B     CVT023
*
CVT022   MVC   GSPARRAY+16(4),CVT0002  INVALID CNVRT VARIABLE
*
CVT023   MVI   GSPARRAY,PARERR         SET PARAMETER ERROR FLAG
         B     CVT013
*
CVT024   MVI   GSPARRAY,SCERR          SCALING ERROR
         XC    GSPARRAY+8(4),GSPARRAY+8
         B     CVT013
CVTFLOAT CLC   0(4,CVT01),CVT0000D     VALUE TO BE FLOATED = 0
         BCR   8,CVT14                 YES, RETURN
         MVC   CVTDBLE(4),CVTFCON      SET UP CONVERSION CONSTANT
         TM    0(CVT01),X'80'          IS NUMBER NEGATIVE
         BO    CVTFLT2                 YES
CVTFLT1A MVC   CVTDBLE+4(4),0(CVT01)   NO, MOVE MAGNITUDE INTO AREA
CVTFLT1  LD    CVT00F,CVTDBLE          GET UNNORMALIZED NUMBER INTO
*                                      FLOATING-POINT REGISTER
         AD    CVT00F,CVT0000D         NORMALIZE
         STE   CVT00F,0(CVT01)         STORE CONVERTED VALUE
         BR    CVT14                   RETURN
*
CVTFLT2  OI    CVTDBLE,X'80'           SET SIGN BIT
         CLC   CVTMXNEG(4),0(CVT01)    MAXIMUM NEGATIVE INTEGER
         BE    CVTFLT1A                YES, TREAT AS POSITIVE
         L     CVT02,0(CVT01)          NO, GET MAGNITUDE
         LPR   CVT02,CVT02             OF NUMBER
         ST    CVT02,CVTDBLE+4         STORE IN CONVERSION AREA
         B     CVTFLT1
CVT0001  DC    F'1'
CVT0002  DC    F'2'
CVT0005  DC    F'5'
CVT0006  DC    F'6'
CVTFCON  DC    X'4E000000'
         DS    0D
CVT0000D DC    XL8'00'
CVTMXNEG DC    XL4'80000000'
XPROC    EQU   X'F0'
YPROC    EQU   X'0F'
XREAL    EQU   X'C0'
YREAL    EQU   X'0C'
XREALABS EQU   X'80'
YREALABS EQU   X'08'
XINTABS  EQU   X'20'
YINTABS  EQU   X'02'
HA06     EQU   864
PARERR   EQU   X'08'
SCERR    EQU   X'20'
HSVA     EQU   4
MSVA     EQU   8
*        REGISTER EQUATES
CVT01    EQU   1
CVT02    EQU   2
CVT08    EQU   8
CVT09    EQU   9
CVT13    EQU   13
CVT14    EQU   14
CVT07    EQU   7
CVT10    EQU   10
CVT11    EQU   11
CVT15    EQU   15
CVT00F   EQU   0
CVT02F   EQU   2
CVT03    EQU   3
CVT04    EQU   4
CVTPARTB DSECT
CVTGDSCB DS    F                       ADDRESS OF POINTER TO GDSCB
CVTCNVRT DS    F                       CONVERT VARIABLE
CVTXINP  DS    F                       X INPUT
CVTYINP  DS    F                       Y INPUT
CVTXOUT  DS    F                       X OUTPUT
CVTYOUT  DS    F                       Y OUTPUT
CVTWORK  DSECT
CVTPARM  DS    F
CVTRTNA  DS    F
CVTSAVE  DS    18F           SAVE AREA
CVTGDS2  DS    F             PARAMETERS FOR CALL TO SCALE
CVTRTNA2 DS    F
CVTDATA  DS    C             SAVE AREA FOR GDSDATMD
CVTCTRL  DS    C
*              X'F0'         X TO BE PROCESSED
*              X'0F'         Y TO BE PROCESSED
         DS    H
         DS    3F
CVTXABS  DS    F
CVTYABS  DS    F
         DS    4F
CVTSCNT  DS    F
CVTDBLE  DS    D
         COPY  GSPCB
         COPY  GTMCB
         COPY  GDSCB
         END
