         TITLE 'IFFAGA03 - RDTRK - READ POSITION OF TRACKING SYMBOL'
*STATUS: CHANGE LEVEL 000
*
*FUNCTION/OPERATION:  READS THE TRACKING SYMBOL'S POSITIONING VECTOR
*        INTO MAIN CORE.  CONVERTS THE RASTER UNIT COORDINATES IN THE
*        VECTOR INTO USER COORDINATES BY A CALL TO CNVRT.
*
*ENTRY POINTS: IFFAGA03
*
*INPUT:  REGISTER 1 POINTS TO A WORKAREA.  THE FIRST WORD CONTAINS THE
*        ADDRESS OF THE PARAMETER LIST, AND THE SECOND, THAT OF THE
*        RETURN CODE ARRAY.  THE FIXED LENGTH PARAMETER LIST IS
*    + 0 A(POINTER TO THE GDSCB)
*
*    + 4 A(X POSITION OF TRACKING SYMBOL)
*
*    + 8 A(Y POSITION OF TRACKING SYMBOL)
*
*OUTPUT: USER COORDINATES OF THE LATEST POSITION OF THE TRACKING SYMBOL
*
*EXTERNAL ROUTINES: CNVRT, GREAD
*
*EXITS-NORMAL AND ERROR: VIA RETURN MACRO TO DIRECTOR
*
*TABLES/WORKAREAS: GSP WORKAREA
*
*ATTRIBUTES: PROBLEM STATE, REENTRANT
*
*A92000,A142000-144000,C637000-651000                       D11 ZA15437
IFFAGA03 CSECT
RTK01    EQU   1                       REGISTER EQUATES
RTK02    EQU   2
RTK03    EQU   3
RTK04    EQU   4
RTK05    EQU   5
RTK06    EQU   6                   GTM ADDRESSIBILITY
RTK07    EQU   7                   RETCODE ARRAY ADDRESSIBILITY
RTK08    EQU   8                   GDS ADDRESSIBILITY
RTK09    EQU   9                   BASEREG
RTK10    EQU   10                  WORKAREA ADDRESSIBILITY
RTK11    EQU   11                  PARMLIST ADDRESSIBILITY
RTK13    EQU   13
RTK14    EQU   14                  LINKAGE REGISTER         D11 ZA15437
RTK15    EQU   15
*
HSVA     EQU   4
MSVA     EQU   8
FCT8     EQU   8
PARERR   EQU   X'08'
TRKIN    EQU   X'08'
STAT     EQU   24                A(STATAB)-A(RTNARR) IN GSP D11 ZA15437
CONVERT  EQU   X'0264'             STATAB CNVRT ENTRY DISPL D11 ZA15437
*
         SAVE  (14,12)
         BALR  RTK09,0                 CSECT ADDRESSABILITY
         USING *,RTK09
         LR    RTK10,RTK01
         USING READTRK,RTK10           WORKAREA ADDRESSABILITY
*
         LA    RTK02,RTKSAVE           CHAIN SAVE AREAS
         ST    RTK02,MSVA(RTK13)
         ST    RTK13,HSVA(RTK02)
         LR    RTK13,RTK02             ESTABLISH NEW SAVE AREA
*
         L     RTK07,RTKRTNA           RETURN ARRAY ADDRESSABILITY
         USING GSPARRAY,RTK07
         L     RTK11,RTKPARM           PARAMETER ADDRESSABILITY
         USING READPARM,RTK11
         L     RTK08,RTKGDSCB
         L     RTK08,0(RTK08)          GDSCB ADDRESSABILITY
         USING GDSCB,RTK08
*
         C     RTK08,GDSGDSCB          VALID GDS
         BNE   RTK001                  NO, ERROR RETURN
*
         TM    RTKGDSCB,X'80'          MAKE SURE PARAMETER
         BO    RTK002
         TM    RTKXPOS,X'80'           LIST HAS THREE ENTRIES
         BO    RTK002
         TM    RTKYPOS,X'80'           NO, ERROR RETURN
         BZ    RTK002
*
         L     RTK06,GDSGTMCB          GTMCB ADDRESSABILITY
         USING GTMCB,RTK06
*
         TM    GTMFLAGS,TRKIN          TRACKING ROUTINE IN
         BZ    RTK002                  NO, ERROR RETURN
*
         XC    RTKSUPV(32),RTKSUPV     YES, CLEAR DECB AREA
         LA    RTK01,RTKSUPV           GET ADDRESS OF DECB
         L     RTK02,GTMGRDCB          GET ADDRESS OF DCB
         LA    RTK03,RTKIOBUF          INPUT AREA
         LA    RTK04,GTMREPOS          BUFFER READ ADDRESS
         L     RTK05,GTMFCTBL          BUFFER START ADDRESS
         LA    RTK05,FCT8(RTK05)
         GREAD (RTK01),STR,(RTK02),4,(RTK03),(RTK04),(RTK05),MF=E
         LA    RTK01,RTKSUPV
         WAIT  ECB=(1)
*
*
         LH    RTK02,RTKIOBUF          GET X VALUE
         ST    RTK02,RTKXVAL
         LH    RTK02,RTKIOBUF+2        AND Y VALUE
         ST    RTK02,RTKYVAL
*
*        SET UP PARAMETERS FOR CALL TO CNVRT
*
         MVC   RTKCVGDS(4),RTKGDSCB    GDSNAME
         LA    RTK02,RTK0001           CONVERT VARIABLE (SET = 1)
         ST    RTK02,RTKCVCVT
         LA    RTK02,RTKXVAL           X INPUT VALUE
         ST    RTK02,RTKCVXIN
         LA    RTK02,RTKYVAL           Y INPUT VALUE
         ST    RTK02,RTKCVYIN
         MVC   RTKCVXOT(8),RTKCVXIN    OUTPUT VALUES
         OI    RTKCVYOT,X'80'          END OF LIST FLAG
*
         LA    RTK02,RTKCVGDS          POINTER TO PARAMETERS
         ST    RTK02,RTKCVPRM
         MVC   RTKCVRTN(4),RTKRTNA     AND RETURN ARRAY
         XC    RTKSUPV(16),RTKSUPV     CLEAR SUPERVISOR AREA
         LA    RTK01,RTKCVPRM
         L     RTK15,STAT(RTK07)   POINT TO STATUS TABLE    D11 ZA15437
         L     RTK15,CONVERT(RTK15) AND TO CONVERT EP       D11 ZA15437
         LTR   RTK15,RTK15         CONVERT LOADED YET?      D11 ZA15437
         BZ    RTKLINK1            NO,MUST USE SYSTEM LINK  D11 ZA15437
         BALR  RTK14,RTK15         GO CONVERT               D11 ZA15437
         B     RTKRTN1             RETURN FROM BALR         D11 ZA15437
RTKLINK1 LA    RTK15,RTKSUPV                                D11 ZA15437
         LINK  EP=IFFAGA08,MF=(E,(1)),SF=(E,(15)) LINK CONVERT
RTKRTN1  LM    RTK02,RTK03,RTKXPOS     GET OUTPUT LOCATIONS D11 ZA15437
         MVC   0(4,RTK02),RTKXVAL      STORE OUTPUT VALUES
         MVC   0(4,RTK03),RTKYVAL
         MVI   GSPARRAY,X'00'          SET RETURN CODE
         L     RTK13,HSVA(RTK13)       RETURN
         RETURN (14,12),T,RC=0
*
RTK001   MVC   GSPARRAY+16(4),RTK0001  BAD GDSCB
         B     RTK003
RTK002   XC    GSPARRAY+16(4),GSPARRAY+16 BAD PARAMETER LIST
RTK003   OI    GSPARRAY,PARERR         SET RETURN CODE
         L     RTK13,HSVA(RTK13)       RETURN
         RETURN (14,12),T,RC=0
*
*
RTK0001  DC    F'1'
PATCH    DC    64X'FF'             PATCH AREA               D11
*
READTRK  DSECT
RTKPARM  DS    F
RTKRTNA  DS    F
RTKSAVE  DS    18F                     SAVE AREA
RTKSUPV  DS    8F                      SUPERVISOR WORK AREA
*                                      PARAMETERS FOR CNVRT
RTKCVGDS DS    F                            GDSNAME
RTKCVCVT DS    F                            CONVERT VARIABLE (SET = 1)
RTKCVXIN DS    F                            INPUT VALUES
RTKCVYIN DS    F
RTKCVXOT DS    F                            OUTPUT VALUES
RTKCVYOT DS    F
RTKXVAL  DS    F                            COORDINATES TO BE CONVERED
RTKYVAL  DS    F
RTKIOBUF DS    2F                      I/O BUFFER
RTKCVPRM DS    F                       POINTER TO PARAMETERS FOR CNVRT
RTKCVRTN DS    F                       POINTER TO RETURN ARRAY
*
READPARM DSECT
RTKGDSCB DS    F                       POINTER TO GDS
RTKXPOS  DS    F                       OUTPUT LOCATIONS
RTKYPOS  DS    F
         COPY  GSPCB
         COPY  GDSCB
         COPY  GTMCB
         END
