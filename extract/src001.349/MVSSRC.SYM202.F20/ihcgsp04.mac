         TITLE 'IHCGSP04'
*STATUS: CHANGE LEVEL 000
*FUNCTION/OPERATION: CONVERTS USER DATA TO SPECIFIED FORMAT
*   ACCORDING TO USER-SPECIFIED CODE.  VALID CODES:
*   101-FLT PT TO EBCDIC, E FORMAT
*   102-FLT PT TO INTEGER, F FORMAT
*   103-INTEGER TO EBCDIC, I FORMAT
*   104-HEX TO EBCDIC, Z FORMAT
*   201-EBCDIC, E FORMAT, TO FLT PT
*   202-EBCDIC, F FORMAT, TO FLT PT
*   203-EBCDIC, I FORMAT, TO INTEGER
*   204-EBCDIC, Z FORMAT, TO HEX
*ENTRY POINT: BCNV
*INPUT: REGISTER 1 CONTAINS TH ADDRESS OF A PARAMETER LIST
*   CONSISTING OF:
*   +0 -A(INPUT)
*   +4 -A(OUTPUT)
*   +8 -A(CONVERSION CODE)
*   +12-A(WIDTH OF EBCDIC FIELD)
*   +16-A(DEC LGTH OF FRACTION FOR F AND E FORMATS)
*OUTPUT: CONVERTED DATA IN USER'S OUTPUT AREA
*EXTERNAL ROUTINES: IHCFCOMH (FORTRAN CONVERSION ROUTINE);
*   IHCERRM (FORTRAN ERROR-HANDLING ROUTINE)
*EXITS-NORMAL: RETURN TO CALLER VIA REG 14
*     -ERROR: RETURN TO CALLER WITH FORTRAN ERROR MESSAGE
*TABLES/WORKAREAS: NONE
*ATTRIBUTES: NON-REENTRANT, SERIALLY REUSABLE
*NOTES: NONE
*  ****************************************************************TMGA
         EJECT
IHCGSP04 CSECT
*A585000-586000,689000-690000,C668000-672000                     A34723
*C616200,A616300-616400,C654000,A654500-655000,              LD YA00800
*A869000-869500                                              LD YA00800
         ENTRY BCNV
         EXTRN IBCOM#                                            A34723
BCNV     SAVE  (14,12)                                             TMGA
         BALR  BASE,0                                              TMGA
         USING *,BASE                                              TMGA
         LA    REGA,SAVE           CHAIN SAVE AREAS                TMGA
         ST    REGA,8(R13)                                         TMGA
         ST    R13,SAVE+4                                          TMGA
         LR    R13,REGA                                            TMGA
*                                                                  TMGA
         L     REGE,0(PARM)        INPUT                           TMGA
         L     REGB,4(PARM)        OUTPUT                          TMGA
         L     REGA,8(PARM)                                        TMGA
         L     REGA,0(REGA)        CONVERSION CODE                 TMGA
         L     REGG,12(PARM)       WIDTH                           TMGA
         L     REGG,0(REGG)                                        TMGA
         LTR   REGG,REGG                                           TMGA
         BNH   ERR                                                 TMGA
         CH    REGG,C255                                           TMGA
         BH    ERR                                                 TMGA
         CH    REGA,C204                                           TMGA
         BE    BCNV0040                                            TMGA
         CH    REGA,C104                                           TMGA
         BNE   BCNV0090                                            TMGA
*                                  CONVERSION OF HEXIDECIMAL TO Z FTMGA
         SR    REGF,REGF           ZERO OUT REGISTER               TMGA
         L     REGE,0(REGE)        PLACE INPUT IN REGISTER         TMGA
         LA    REGI,8              LENGTH OF INPUT IN HALF BYTES   TMGA
BCNV0010 BCTR  REGG,0              REDUCE OUTPUT BY 1              TMGA
         LTR   REGG,REGG           MINUS-WE'RE DONE                TMGA
         BM    BCNVEXIT            EXIT                            TMGA
         BCTR  REGI,0              REDUCE BY 1                     TMGA
         LTR   REGI,REGI           IF MINUS, 8 ARE DONE            TMGA
         BM    BCNV0020                                            TMGA
         SRDL  REGE,4              SHIFT OFF  4                    TMGA
         SRL   REGF,28             POSITION TO USE AS INDEX        TMGA
         IC    REGF,TRTAB1(REGF)   PICK UP CHARACTER               TMGA
         STC   REGF,0(REGG,REGB)   PLACE IN OUTPUT                 TMGA
         B     BCNV0010            CONTINUE                        TMGA
*                                                                  TMGA
*                                                                  TMGA
BCNV0020 IC    REGF,BLANK          PICK UP A BLANK                 TMGA
BCNV0030 STC   REGF,0(REGG,REGB)   BLANK REMAINING FIELD           TMGA
         BCTR  REGG,0                                              TMGA
         LTR   REGG,REGG           DONE                            TMGA
         BNL   BCNV0030            NO                              TMGA
BCNVEXIT L     R13,4(R13)                                          TMGA
         RETURN (14,12)                                            TMGA
         EJECT
BCNV0040 SR    REGH,REGH           ZERO RESULT REGISTER            TMGA
         SR    REGF,REGF           COUNTER                         TMGA
         SR    REGI,REGI           TRANSLATE INDEX                 TMGA
         SR    REGA,REGA           COUNTER                         TMGA
         STC   REGI,SW                                             TMGA
BCNV0050 CH    REGA,H8             8 VALID DONE                    TMGA
         BE    BCNV0070            YES                             TMGA
         IC    REGI,0(REGF,REGE)   PICK UP EBCDIC CHARACTER        TMGA
         CH    REGI,BLK            IS IT BLANK                     TMGA
         BE    BCNV0080                                            TMGA
         N     REGI,AND            AND OFF ZONE-EXCEPT 1 BIT IF THETMGA
         IC    REGI,TRTAB2(REGI)   PICK UP NEW CHAR                TMGA
         SLL   REGI,28             SHIFT 4 SIGNIFICANT BITS TO TOP TMGA
         SLDL  REGH,4              SHIFT INTO REGISTER CONT. HEX NUTMGA
         OI    SW,VLD              SET SW VALID NUMBER PROCESSED   TMGA
         LA    REGA,1(REGA)                                        TMGA
BCNV0060 LA    REGF,1(REGF)                                        TMGA
         BCT   REGG,BCNV0050                                       TMGA
BCNV0070 ST    REGH,0(REGB)        STORE CONV IN OUTPUT AREA       TMGA
         B     BCNVEXIT            EXIT                            TMGA
*                                                                  TMGA
BCNV0080 TM    SW,VLD              VALID CHARS ENCOUNTERED         TMGA
         BO    BCNV0070            YES WE'RE DONE                  TMGA
         B     BCNV0060            NO WE HAVEN'T STARTED YET       TMGA
*                                                                  TMGA
BCNV0090 STC   REGG,WW             WIDTH IN PARM LIST              TMGA
         TM    12(PARM),X'80'      LAST PARM                       TMGA
         BO    BCNV0115            YES                             TMGA
         L     REGF,16(PARM)                                       TMGA
         MVC   DD(1),3(REGF)       DECIMAL INPARAMETER LIST        TMGA
BCNV0100 MVI   SS,X'00'            SET SCALE FACTOR=0              TMGA
         CH    REGA,C200                                           TMGA
         BE    ERR                                                 TMGA
         BNL   BCNV0120                                            TMGA
         SH    REGA,C100                                           TMGA
         CH    REGA,C4                                             TMGA
         BNL   ERR                                                 TMGA
         CH    REGA,C0             VALID INPUT                     TMGA
         BNH   ERR                 NO                              TMGA
         MVI   0(REGB),X'40'       BLANK BUFFER                    TMGA
         BCTR  REGG,0              REDUCE BY TWO                   TMGA
         BCTR  REGG,0                                              TMGA
         LTR   REGG,REGG           ONLY EQUAL 1 ORIG               TMGA
         BM    BCNV0110            BYPASS MOVE                     TMGA
         EX    REGG,MOVE           MOVE IN REST OF BLANKS          TMGA
BCNV0110 LA    REGC,4                                              TMGA
         B     BCNV0130                                            TMGA
         EJECT
BCNV0115 MVC   DD(1),C0                                            TMGA
         B     BCNV0100                                            TMGA
*                                                                  TMGA
BCNV0120 SH    REGA,C200                                           TMGA
         CH    REGA,C4             VALID CODE                      TMGA
         BNL   ERR                                                 TMGA
         LR    REGG,REGE           SWITCH INPUT/OUTPUT REG         TMGA
         LR    REGE,REGB           CORE ADDR IN REGE               TMGA
         LR    REGB,REGG           EBCDIC BUFFER ADDR IN REGB      TMGA
         SR    REGC,REGC                                           TMGA
BCNV0130 IC    REGD,LLTAB(REGA)    LENGTH ALWAYS 4                 TMGA
         STC   REGD,LL                                             TMGA
         IC    REGA,ADCONTAB(REGA)                                 TMGA
         AR    REGA,REGC           ADD 4 FOR INPUT TO EBCDIC       TMGA
         L     PARM,VADCON         ADCON TABLE                     TMGA
         L     PARM,0(PARM,REGA)   ENTRY ADDR                      TMGA
         LR    REGC,PARM                                           TMGA
         LR    REGA,REGE                                           TMGA
         L     REGE,ADIBCOM           ADDRESS OF IBCOM           A34723
         STM   R14,R13,FORTSAVE(REGE) SAVE REGS IN IBCOM         A34723
         TM    LL,X'40'            I CONVERSION                    TMGA
         BO    BCNV0140                                            TMGA
         BALR  REGG,0              ADDR OF CALLING SEQ             TMGA
CALLSEQ  BALR  REG0,PARM                                           TMGA
LL       DC    X'00'                                               TMGA
WW       DC    X'00'                                               TMGA
DD       DC    X'00'                                               TMGA
SS       DC    X'00'                                               TMGA
         OI    FORTSAVE(REGE),FORTFLGS  SET FLAGS SO...      LD YA00800
*                                       FORTRAN WILL KNOW... LD YA00800
*                                       WE HAVE EXITED       LD YA00800
         B     BCNVEXIT                                            TMGA
*                                                                  TMGA
BCNV0140 MVC   LL1,LL                                              TMGA
         MVC   WW1,WW                                              TMGA
         XI    LL1,X'40'                                           TMGA
         STM   14,13,FORTSAVE(REGE)                              A34723
         BALR  REGG,0                                              TMGA
CALLSEQ1 BALR  REG0,PARM                                           TMGA
LL1      DC    X'00'                                               TMGA
WW1      DC    X'00'                                               TMGA
         OI    FORTSAVE(REGE),FORTFLGS  SET FLAGS SO...      LD YA00800
*                                       FORTRAN WILL KNOW... LD YA00800
*                                       WE HAVE EXITED       LD YA00800
         B     BCNVEXIT                                            TMGA
         EJECT
* THE FOLLOWING CODE SETS UP A PARAMETER LIST AND BALR'S TO THE    TMGA
* FORTRAN ERROR HANDLING MODULE-FORTRAN LINKAGE CONVENTIONS ARE  A34723
* FOLLOWED, USING IBCOM= PLUS 7C FOR REGISTER,SAVE AREA          A34723
*                                                                  TMGA
ERR      EQU   *                                                   TMGA
         LA    PARM,ERRLIST        POINTER TO MSG LENGTH           TMGA
         L     R15,ADERRMON        ADDRESS OF ERRMON               TMGA
         L     REGE,ADIBCOM           ADDRESS 0F IBCOM           A34723
         STM   14,13,FORTSAVE(REGE)                              A34723
         BALR  R14,R15             BRANCH                          TMGA
         B     BCNVEXIT            RETURN TO CALLER                TMGA
*                                                                  TMGA
ERRLIST  DC    A(MSGLNGTH)                                         TMGA
         DC    A(RETCODE)                                          TMGA
         DC    A(ERRNBR)                                           TMGA
         DC    X'80'                                               TMGA
         DC    AL3(ERRNBR)                                         TMGA
MSGLNGTH DC    A(MSG-ENDMSG)                                       TMGA
MSG      DC    C'IHC302I IBCOM - PARAM ERROR IN CALL TO BCNV'      TMGA
ENDMSG   EQU   *                                                   TMGA
RETCODE  DS    F                                                   TMGA
         LM    14,13,FORTSAVE(REGE)                              A34723
ERRNBR   DC    F'302'                                              TMGA
         EJECT
REG0     EQU   0                                                   TMGA
PARM     EQU   1                                                   TMGA
REGA     EQU   2                                                   TMGA
REGB     EQU   3                                                   TMGA
REGC     EQU   4                                                   TMGA
REGD     EQU   5                                                   TMGA
REGE     EQU   6                                                   TMGA
REGF     EQU   7                                                   TMGA
REGG     EQU   8                                                   TMGA
BASE     EQU   9                                                   TMGA
REGH     EQU   10                                                  TMGA
REGI     EQU   11                                                  TMGA
R13      EQU   13                                                  TMGA
R14      EQU   14                                                  TMGA
R15      EQU   15                                                  TMGA
*                                                                  TMGA
BLK      DC    H'64'                                               TMGA
         DS    0F                                                  TMGA
AND      DC    X'0000001F'                                         TMGA
C255     DC    H'255'                                              TMGA
C104     DC    H'104'                                              TMGA
C204     DC    H'204'                                              TMGA
C0       DC    H'0'                                                TMGA
H8       DC    H'8'                                                TMGA
SAVE     DS    18F                                                 TMGA
SW       DC    X'0'                                                TMGA
BLANK    DC    C' '                                                TMGA
TRTAB1   DC    C'0123456789ABCDEF'                                 TMGA
TRTAB2   DC  X'000A0B0C0D0E0F00000000000000000000010203040506070809'
*                0102030405060708090A0B0C0D0E0F10111213141516171819
VLD      EQU   X'01'                                               TMGA
FORTSAVE EQU   X'7C'              DISPLACEMENT FOR SAVE AREA     A34723
FORTFLGS EQU   X'FF'         FLAGS TO INDICATE WE'VW EXITED  LD YA00800
*                            FROM THE FORTRAN I/O ROUTINE    LD YA00800
C200     DC    H'200'                                              TMGA
C4       DC    H'4'                                                TMGA
MOVE     MVC   1(0,REGB),0(REGB)                                   TMGA
C100     DC    H'100'                                              TMGA
LLTAB    DC    X'00040444'                                         TMGA
*                   E F I - 4 BIT ON FOR INTEGER                   TMGA
ADCONTAB DC    X'00080020'                                         TMGA
*                   E F I                                          TMGA
VADCON   DC    V(ADCON#)                                           TMGA
ADERRMON DC    V(ERRMON)                                           TMGA
ADIBCOM  DC    A(IBCOM#)          FORT COMMUNICATIONS AREA       A34723
         END
