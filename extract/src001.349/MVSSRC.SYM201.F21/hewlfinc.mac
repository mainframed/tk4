         TITLE    'HEWLFINC INCLUDE AND AUTOMATIC LIBRARY CALL PROC.'
*
*STATUS: CHANGE LEVEL 000
*
* C519100                                                       ZA01413
* C379500                                                      @ZA07584
* C568700  (2657=LRECL DEL)(12241=NCP=01) 7/30/76     @ZA02657,@ZA12241
         EJECT
*FUNCTION/OPERATION:
*  1-THE INCLUDE PROCESSOR INCLUDES INTO THE CURRENT INPUT A DATA SET
*    THAT HAS BEEN REQUESTED BY AN INCLUDE STATEMENT.   TO DO THIS,IT
*    TAKES THE FIRST ITEM OF THE INCLUDE CHAIN AND MAKES IT READY TO BE
*    READ BY THE INPUT PROCESSOR. THIS ITEM CAN INDICATE A SEQUENTIAL
*    DATA SET OR A PARTITIONED DATA SET .
*       1-A-  IF IT INDICATES A SEQUENTIAL DATA SET, THE ITEM CONTAINS
*             THE DDNAME, ON WHICH 'OPEN' IS PERFORMED.
*             THE DATA SET IS THEN READY TO BE READ BY THE INPUT PRO-
*             -CESSOR
*       1-B-  IF IT INDICATES A PARTITIONED DATA SET , THE ITEM CON-
*             -TAINS THE MEMBER NAME AND A POINTER TO THE DDNAME.
*             'OPEN' IS PERFORMED ON THE DDNAME. 'BLDL' IS PERFORMED
*             ON THE MEMBERNAME. 'FIND' POSITIONS TO BEGINNING OF MEM-
*             -BER
*    THE INCLUDE PROCESSOR KNOWS AFTER 'OPEN' WHAT DATA SET IS COMING,
*    BY LOOKING AT THE FIELD 'RECFM' (RECORD FORMAT). THIS DCB FIELD
*    IS ZEROED BEFORE EACH OPEN ,AND RECEIVES WHAT HAS BEEN SPECIFIED
*    IN THE DCB FIELD OF THE DDCARD (OR WHAT WAS IN THE DSCB, IF APPLI-
*    -CABLE)
*  2-THE AUTOMATIC LIBRARY CALL PROCESSOR (HEWLCAUT), TRY TO RESOLVE
*    THE EXTERNAL REFERENCES IN THE CESD ,WHICH ARE STILL UNRESOLVED AT
*    THE END OF THE PRIMARY INPUT STREAM.   IT CALLS FROM PARTITIONED
*    DATA SETS,MEMBERS WHOSE NAMES ARE THE EXTERNAL REFERENCES IN THE
*    CESD.
*          IT BEGINS BY THE EXTERNAL REFERENCES WHOSE CALLS HAVE BEEN
*    GUIDED (BY MEANS OF A LIBRARY CARD)TOWARDS A SPECIFIED DATA SET.
*    THESE CALLS ARE MADE DATA SET BY DATA SET, ALL THE CALLS INSIDE
*    A SINGLE DATA SET BEING MADE CONSECUTIVELY.
*          IT CONTINUES WITH THE EXTERNAL REFERENCES NOT GUIDED BY
*    LIBRARY CARDS ; IT SEARCHES FOR THESE NAMES ON THE STANDARD LIBRA-
*    -RY WHOSE NAME IS IN THE ALL PURPOSE TABLE . THE STANDARD LIBRARY
*    DSNAME IS 'SYSLIB' WHEN THE LINKAGE EDITOR GETS CONTROL FROM THE
*    TASK SCHEDULER ; IT IS THE FOURTH ITEM IN THE PARAMETER LIST WHEN
*    THE LINKAGE EDITOR IS 'LINKED TO'
*
*ENTRY POINTS:
*    HEWLFINC   FOR THE INCLUDE PROCESSOR                             *
*    HEWLCAUT   FOR THE AUTOMATIC LIBRARY CALL PROCESSOR
*
*               CALLING SEQ.       L     15,=V(HEWLFINC)              *
*                                  BALR  14,15
*           L     15,=V(HEWLCAUT)
*           BALR  14,15
*    NO DATA IS PASSED DIRECTLY UPON CALLING,BUT IS FOUND IN OR BY
*    MEANS OF THE ALL PURPOSE TABLE WHOSE ADDRESS IS CONSTANTLY IN
*    REGISTER 2
*
*INPUT:   1-ALL PURPOSE TABLE
*         2-ITEMS IN THE CESD:
***********************************************************************
*BYTES 0-7*BYT 8*BYT 9*BYT 10-11*BYT 13*BYT 14-15*  DESIGNATION       *
* SYMBOL  *TYPE *     *         *SUBTYP*         *                    *
***********************************************************************
*MEMBER-  *X'02'* CHAIN ADDRESS *X'D0' * POINTER *CONTROL INCLUDE     *
*NAME     *     *               *      * TO DDNAM*WITH PTER           *
***********************************************************************
*DDNAME   *X'02'* CHAIN ADDRESS *X'C0' *         *CONTROL INCLUDE     *
*         *     *               *      *         *WITHOUT PTER        *
***********************************************************************
*DDNAME   *X'02'*               *X'B0' * PTER,IF * DDNAME ITEM        *
*         *     *               *      * FOR LIB.*                    *
***********************************************************************
*MBER NAME*X'02'*     *REVRS CH-*X'02' *DIRECT CH* UNMATCHED LIBRARY  *
*         *     *     *AIN PTER *      *AIN PTER * MEMBER             *
***********************************************************************
*MBER NAME*X'02'*     *REVERSE  *X'03' *DCT CHAIN* MATCHED LIBRARY    *
*         *     *     *CH. PTER *      *PTER     * MEMBER             *
***********************************************************************
*SYMBOL   *X'02'*               *X'04' *         * NO CALL,UNMATCHED  *
***********************************************************************
*SYMBOL   *X'02'*               *X'05' *         * NO CALL,MATCHED    *
***********************************************************************
*SYMBOL   *X'02'*               *X'06' *         * NEVER CALL         *
***********************************************************************
*
*         3-THE INFORMATION SLOT BY OPEN IN OUR LIBRARY DCB (HEWLKLDB)
*         4-THE DIRECTORY ENTRIES OF THE MEMBERS ON WHICH WE BLDL
*
*OUTPUT:
*   FLAGS IN THE ALL PURPOSE TABLE
*
*EXTERNALS ROUTINES
*   HEWLFLOG   ERROR ROUTINE                                          *
*   HEWLFADA   ADDRESS ASSIGNMENT                                     *
*   HEWLFINP   INPUT PROCESSOR                                        *
*   HEWLFEND   END PROCESSOR
*
*EXITS-NORMAL:
*           1- RETURN TO THE CALLER EXCEPT IN CASE 2-
*           2- WHEN HEWLCAUT (AUTOMATIC LIBRARY CALL)IS CALLED AND NO
*              EXTERNAL REFERENCE CORRESPONDING TO AN AVAILABLE MEMBER
*              CAN BE FOUND, CONTROL IS PASSED TO HEWLKADA (ADDRESS)
*              ASSIGNMENT)
*     -ERROR:
***********************************************************************
*ERR CODE* OTHER PARAMS *    CONDITION CAUSING ERROR                  *
*IN REG0 *              *                                             *
***********************************************************************
*X'222B' *LIB. DDNNAME  * OPEN UNSUCCESSFUL ON SPECIFIED LIBRARY      *
*        *ADDR. IN R1   *                                             *
***********************************************************************
*X'3222' *MBR.NAM IN R13* MEMBER NOT FOUND ON LIBRARY                 *
*        *LIB.NAM IN R1 *                                             *
***********************************************************************
*X'2235' *LIB.NAM IN R1 * OBJECT MODULE BUFFER'S SIZE EXCEEDED        *
***********************************************************************
*X'2233' *LIB.NAM IN R1 * INCL.CRD.INDICATES OBJECT MODULE BUT RECFM=U*
***********************************************************************
*X'2234' *LIB.NAM IN R1 * DATA SET CONTAINS UNACCEPTABLE RECFM (I.E.  *
*        *              * OTHER THAN F OR U                           *
***********************************************************************
*X'321B' *LIB.NAM IN R1 * LOAD MODULE CANNOT BE PROCESSED BY MODEL E  *
*        *MBRNAME IN R13* LINK.EDITOR ('DOWNARD UNCOMPATIBILITY')     *
***********************************************************************
*
*TABLES/WORK AREAS:
*                  CESD,APT, PREVIOUSLY MENTIONED
*ATTRIBUTES
*           HEWLCINC IS IN A SEGMENT OF AN OVERLAY STRUCTURE
*           AS DESCRIBED HERE (TABLES CONSIDERED AS INPUT) IT IS
*           REUSABLE
*
*NOTES    HEWLFINC CONTAINS THE MACROS CVT,DCBD,DCB,OPEN,
*            BLDL,FIND,CLOSE
********************
HEWLFINC CSECT
******** ERROR MSG ****************************************************
MSG      EQU   0
IEW0432X EQU   X'222B'    ERR OPEN FAILED ON SPECIFIED LIBRARY
IEW0342X EQU   X'3222'    ERR.  MEMBER NOT FOUND ON SPECIFIED LIBRARY
IEW0532X EQU   X'2235'    ERR  SYSLIN BUFF SIZE EXCEEDED
IEW0512X EQU   X'2233'  L.E. CRD SYNTAX CONFLICTS WITH RECFM OF LIB
IEW0522X EQU   X'2234'  SPECIFIED DATA SET CONTAINS NON ACCEPTABLE RCFM
IEW0272X EQU   X'321B'     ERROR:'MODULE FROM SPECIFIED LIBRARY CANNOT
*                          BE PROCESSED BY LEVEL E LINKAGE EDITOR'
*                          MEMBER NAME AND DDNAME PRINTED
***********************************************************************
FF       EQU   X'FF'       IMMEDIATE BYTE OF ALL ONES           YM4650
BLDSIZ   EQU   26      SIZE OF ENTRY IN BLDL LIST
APT      EQU   2
         EJECT
         EXTRN  HEWLCEOD
         EXTRN HEWLCRO1
         USING APTDSECT,APT
         USING BLDLMBRN,6
         USING  IHADCB,07
         ST    12,8(15)               SAVE BASE OF CALLER
         BC    15,12(15)
         DC    XL4'00000000'
         BALR  12,0
         USING *,12
         B     INCLU050                BRANCH AROUND MODULE IDENT
***********************************************************************
         DC    C' HEWLFINC '
         DC    C'07/30/76 '
         DC    C'OZ12241 ',0H'0'
         EJECT
INCLU050 STM   13,11,SAVAREA          SAVE OTHER REGISTERS OF CALLER
         L     07,SYSLIBAD   ADDR OF LIB DCB
         NI    IND3,X'DF'   RESET 'AUTO LIB ' TO OFF
**********
*  CHOOSE IN THE INCLUDE CHAIN THE FIRST ITEM TO BE INCLUDED
*
INCLU100 L     09,CRRTINCL            INITIATE THE INCLUDE CHAIN BREA-
         TM    APT2,X'08'              HAS INCLUDE BEEN INITIATED
        BC    8,INCLU115              NO
         TM    IND3,X'10'              IS SINGLE BLDL MODE IN OPERATION
         BC    1,INCLU115              YES
INCLU110 TM    IND3,X'08'              WAS BLDL DONE ON LIST
        BC    1,GETNEXT1
INCLU115 OI    APT2,X'08'              SET INCLUDE PREVIOUSLY INITIATED
         CLI   13(9),X'D0'             IS THIS LINE INCL WITH POINTER
         BC    8,INCLU160              YES
         OI    IND3,X'80'              SET PHSICAL SEQUENTIAL ON
         LR    8,9
         BAL   14,LIBOP                TO OPEN LIBRARY
         BAL   14,NULLINE              TO NULL LINE
         ST    9,CRRTINCL              UPDATE CRRTINCL
         ST    9,INCBRKPT              AND BREAK POINT
         BC    15,INCLU155             TOWARDS INPUT
INCLU160 NI    IND3,X'7F'              TURN OFF PHYSICAL SEQUENTIAL IND
         TM    IND3,X'10'              CHECK FOR SINGLE BLDL MODE
         BC    8,INCLU168              NO
         LA    6,SINGBLDL              ADDRESS OF SINGLE BLDL LIST
         BC    15,INCLU250
INCLU168 MVC   PTRSAV(2),14(9)         SAVE DDNAME POINTER
INCLU170 CLI   NUMENT+1,X'00'          FIRST ENTRY IN LIST
         BC    7,INCLU172              NO
         L     6,TABLSTRT              YES--GET START OF TABLE
         SH    6,ENTSIZ                MAKE IT ONE ENTRY PRIOR
         BC    15,INCLU180
INCLU172 LH    6,NUMENT                NUMBER OF ENTRIES
        MH    6,ENTSIZ                TIMES ENTRY SIZE
         A     6,TABLSTRT              ADD START OF TABLE
INCLU171 SH    6,ENTSIZ                GO TO PREVIOUS ENTRY
         CLC   0(8,6),0(9)             COMPARE NEW ENTRY TO PREVIOUS
         BC    8,INCLU182              IF EQUAL IGNORE IT
         BC    4,INCLU180              IS HIGH PUT IT  IN TABLE
         MVC   BLDSIZ(10,6),0(6)       IF LOW MOVE PREVIOUS ENTRY DOWN
         C     6,TABLSTRT              CHECK IF END OF TABLE
         BC    7,INCLU171              NO
         SH    6,ENTSIZ                TO MOVE INTO FIRST ENTRY
INCLU180 MVC   BLDSIZ(8,6),0(9)        MOVE NAME TO LIST
         LH    5,NUMENT                CURRENT ENTRY NUMBER
         LA    5,1(0,5)                UPDATE BY ONE
         STH   5,NUMENT                SAVE NEW NUMBER
         STH   5,BLDSIZ+8(0,6)         IS ALSO SEQUENCE NUMBER
         TM    IND3,X'20'              IS ENTRY FROM AUTOCALL
         BC    8,INCLU182              NO--LEAVE SEQUENCE NUMBER IN
         S     9,CHESD                 YES--SAVE ID INSTEAD
         SRL   9,4
         STH   9,BLDSIZ+8(0,6)
INCLU182 TM    IND3,X'20'              IS ENTRY FROM AUTOCALL
         BC    1,TESTMAX               YES--BYPASS NULL,CHAIN CHECK
         BAL   14,NULLINE              GO NULL THE LINE
         LTR   9,9                     IS THIS END OF CHAIN
         BC    8,INCLU185              YES
TESTMAX  CH    5,MAXENTRY              IS TABLE FULL
         BC    8,INCLU185              YES
         TM    IND3,X'20'              IS ENTRY FROM AUTOCALL
         BCR   1,14                    YES--RETURN TO AUTOCALL
         CLI   13(9),X'D0'             IS NEXT ITEM INCLUE WITH PTR
         BC    7,INCLU185              NO
         LH    5,14(9)                 YES--IS IT SAME POINTER
         CH    5,PTRSAV
         BC    8,INCLU172              YES
INCLU185 TM    IND3,X'20'              IS AUTO LIB IN OPERATION
         BC    1,INCLU186              YES
         ST    9,CRRTINCL              UPDATE CRRTINCL
         ST    9,INCBRKPT              AND BREAK POINT
         LH    8,PTRSAV                DDNAME POINTER
         SLL   8,4                     TIMES SIXTEEN
         A     8,CHESD                 PLUS CESD ADDRESS
INCLU186 LH    5,NUMENT                MOVE SEQUENCE NUMBERS TO APPEND
         LA    10,APPEND
        LA    6,BLDLLST
MOVESEQ  MVC   0(2,10),8(6)
         LA    10,2(0,10)              UPDATE POINTERS
         LA    6,BLDSIZ(0,6)
         BCT   5,MOVESEQ               LOOP
INCLU175 BAL   14,LIBOP                TO OPEN LIBRARY
         TM    IND3,X'40'              ANY OPEN ERROR
         BC    8,BUILDLST              NO--DO BLDL
         BC    15,GETNEXT5             YES
BUILDLST LA    13,REGSA                ADDRESS OF SAVE AREA
         BLDL  (7),NUMENT              ISSUE BLDL
         STC   15,TEMP1
         CLI   TEMP1,X'08'             CHECK FOR COMPLETION CODE = 8
         BC    7,BUILDOK               NO--IS OK
         LH    5,NUMENT                IS 8 ENSURE ALL ENTRIES IGNORED
         LA    6,BLDLLST
BUILDBAD MVI   10(6),X'00'
         AH    6,ENTSIZ
         BCT   5,BUILDBAD
BUILDOK  OI    IND3,X'08'              SET BLDL DONE ON LIST INDICATOR
         B     GETNEXT1                GO TO READ FIRST MODULE
GETNEXT  CLC   CURINCNO(1),NUMENT+1    ARE ALL ITEMS IN LIST DONE
         BC    7,INCLU200              NO
GETNEXT5 NI    IND3,X'F7'              YES--TURN OFF BLDL DONE ON LIST
         MVI   CURINCNO,X'00'          INITIALIZE CURRENT INCLUDE NO
         MVI   NUMENT+1,X'00'          INITIALIZE NUMBER OF ENTRIES
         TM    IND3,X'20'              IS AUTO LIB IN OPERATION
         BC    8,INCLU155              NO--GO CHECK FOR MORE INCLUDES
         TM    IND3,X'40'              YES--WAS THERE AN ERROR
         BC    1,AUTOCL1               YES--RETURN TO AUTOCALL
         BC    15,INCLU201             NO--GO READ MODULE
GETNEXT1 LA    10,APPEND-2             ADDRESS OF APPENDAGE SEQ NUMBERS
         SR    6,6
         IC    6,CURINCNO              UPDATE CURINCNO
         LA    6,1(0,6)
         STC   6,CURINCNO
         TM    IND3,X'20'              IS AUTOLIB IN OPERATION
         BC    8,GETNEXT9              NO
         AR    10,6                    GET ADDRESS OF NEXT ID NUMBER
         AR    10,6                    REG10 + 2 * REG6
         LH    10,0(0,10)              GET ID NUMBER
         SLL   10,4                    CONVERT TO CESD ADDRESS
         A     10,CHESD
         MVC   TEMP1(1),8(10)          GET TYPE
         NI    TEMP1,X'0F'             AND OUT LEFT HALF
         CLI   TEMP1,X'02'             IS ITEM STILL UNRESOLVED
         BC    8,GETNEXT8              YES--GO TO READ IT
         OI    IND3,X'40'              INDICATE MODULE NOT FOUND
         BC    15,GETNEXT4             NO--GO CHECK FOR MORE IN LIST
GETNEXT9 SR    6,6                     FIND NEXT ITEM TO BE READ
GETNEXT2 LA    6,1(0,6)
         LA    10,2(0,10)
         CLC   CURINCNO(1),1(10)       IS THIS NEXT ITEM TO BE READ
         BC    7,GETNEXT2              NO--TRY NEXT
GETNEXT8 BCTR  6,0
         MH    6,ENTSIZ
         A     6,TABLSTRT              ADD START OF TABLE
         CLI   10(6),X'00'             WAS MODULE FOUND
         BC    7,GETNEXT3              YES
         LA    14,GETNEXT4             NO--GIVE ERROR MESSAGE
         ST    14,SAV14L1              PREPARE RETURN ADDRESS
         LA    8,LIBOPEN               ADDRESS OF DDNAME OPEN
         BC    15,INCLU615             GO PRINT ERROR MESSAGE
GETNEXT4 CLC   CURINCNO(1),NUMENT+1    ARE ALL ITEMS IN LIST DONE
         BC    8,GETNEXT5              YES
         BC    15,GETNEXT1             NO--GO CHECK NEXT ONE
GETNEXT3 TM    IND3,X'20'              IS AUTOCALL ON
         BC    8,GETNEXT7              NO
         L     8,SAVAD  GET ADDRESS OF DDNAME FOR AUTOCALL OR LIB CALL
         BC    15,GETNEXT6
GETNEXT7 LH    8,PTRSAV                GET POINTER TO DDNAME
         SLL   8,4                     POINTER TIMES FOUR
         A     8,CHESD                 PLUS CESD ADDRESS
GETNEXT6 BAL   14,LIBOP                CHECK IF LIBRARY STILL OPEN
         TM    IND3,X'40'              ANY ERROR
         BC    1,GETNEXT5              YES--SKIP REST OF LIST
         LA    14,GETNEXT              PREPARE ADDRESS OF EXIT
         ST    14,SAV14L1
         BC    15,INCLU602             TO FIND AND PREPARE TO READ
* AS HEWLCINC IS GIVEN CNTRL, THE CHAIN ADDR. IN THE LINE 'CRRTINCL'
* IS NECESSERALY NON NULL
NULLINE  MVI   8(9),X'07'              NULLIFY TYPE
         MVI   7(09),C','   ALTER THE SYMBOL
         MVC   12(4,09),FLCD       STICK NULL LINE TO HOLES CHAIN
         ST    09,FLCD             UPDATE FLCD
         L     9,8(9)   LD CHAIN ADDR (AND TYPE)
         LA    9,0(9)   (WIPE TYPE)
         LTR   9,9                     IS POINTER ZERO (END OF CHAIN)
         BCR   8,14                    YES--RETURN TO CALLER
         ST    9,CRRTINCL      UPDATE CRRTINCL
         TM    IND3,X'20'              IS SINGLE BLDL MODE ON
         BCR   1,14                    YES--RETURN
INCLU120 ST    09,INCBRKPT      INITIATE INCLUDE BREAK POINT
         BCR   15,14                   RETURN
         OI   APT2,X'08'   SET 'INCLUDE PREVIOUSLY INITIATED'
**                                    ADDR.OF INCLUDE LINE NOW IN  9
**********
*
*  DETERMINE IF THIS IS THE INCLUDING OF A MEMBER OF A PARTITIONED
*  DATA SET , OR A PHYSICAL SEQUENTIAL DATA SET
*
*
         CLI   13(09),X'D0'         IS THIS LINE CNTRL.INCL WITH PTER
         BC    8,INCLU250            YES
         OI    IND3,X'80'            SET 'PHYSICAL SEQUENTIAL' ON.
         LR    8,9             TAKE ADDR.OF THIS LINE AS ADDR.OF -
*                            - NEWDDNAME SYMBOL
**        NEWDDNAME  POINTED TO BY REG. 8
**********
*
*  BRANCH TO THE SUBROUTINE LIBOP
*
INCLU150 BAL   14,LIBOP
*
*
**********
*
*
*  SET THE 'INCLUDE' BIT ON IN THE ALL PURPOSE TABLE, IN CASE MORE
*  INCLUDE WILL HAVE TO BE DONE AFTER THIS ONE .
*  THE INPUT PROCESSOR WILL TEST THIS BIT AT THE END OF THE INCLUDED
*  DATA SET , TO DECIDE IF THE INCLUDE PROCESSOR HAS TO BE GIVEN
*  CONTROL AGAIN
*
*
INCLU155 OC    CRRTINCL(4),CRRTINCL    IS CHAIN ADDRESS 0
         BC    8,INCLU300      YES
         OI    APT1,X'80'   SET INCLUDE BIT TO 1
         TM    IND3,X'40'        WAS MODULE 'NOT FOUND'
         BC    1,INCLU100        BR. IF NOT FOUND
**********
*
*
*  GO TO THE INPUT PROCESSOR
*
*
INCLU200 TM    IND3,X'08'              WAS BLDL DONE ON LIST
         BC    8,INCLU201              NO
         TM    IND3,X'20'              YES--IS AUTOLIB IN OPERATION
         BC    1,INCLU201              YES--DON'T SET MORE INCLUDES
         OI    APT1,X'80'              NO--SET MORE INCLUDES TO COME
INCLU201 LA    15,INCLU050+8
         SH    15,FOURTEEN
         MVC   SAVAREA+60(4),0(15)
         MVC   SAVAREA+8(4),INPUTPRA
         LM    13,12,SAVAREA
         BCR  15,15
*
*  MAKE DDNAME AND MEMBER NAME AVAILABLE FOR THE LIBOP SUBROUTINE
*
*
INCLU250 NI    IND3,X'7F'        RESET  'PHYSICAL SEQUENTIAL' TO 0
*                             LOOK FOR NEWDDNAME ADDR. :
         LH    8,14(09)       LOAD  CHAINED ID
         SLL   8,4            MULTIPLY ID BY 16
         A     8,CHESD             ADD CESD ADDR. MINUS 16
*          NEWDDNAME  POINTED TO BY REGISTER 8
         MVC   BLDLMBRN(8),0(09)   MOVE MBRNAME INTO THE BLDL AREA
         BAL   14,NULLINE              TO NULL LINE AND UPDATE POINTER
         C     9,INCBRKPT              HAS BREAK POINT BEEN REACHED
         BC    7,INCLU150              NO--STAY IN SINGLE BLDL MODE
         NI    IND3,X'EF'              YES--TURN OFF SINGLE BLDL
         BC    15,INCLU150
INCLU300 NI    APT1,X'7F'          RESET INCLUDE BIT TO 0
         NI    APT2,X'F7'              TURN OFF INCLUDE INITIATED BIT
*                  (WHEN THIS MODULE WILL BE FINISHED, THE INPUT
*                   PROCESSOR MUST NOT BRANCH TO THE INCLUDE PROC.)
* (INPUT PROC. WILL HAVE THEN TO SWITCH LIBADRC  TO SYSLIN )
*  NULLIFICATION OF DDNAME LINE LAST USED
*
* ONE CANNOT DESTRUCT THE LAST USED DDLINE HERE BECAUSE THIS DDLINE
* MIGHT BE THE BASE OF A LIBRAARY CHAIN
         TM    IND3,X'40'   WAS THERE A LIBOP FAILURE
         BC   8,INCLU310        NO
         NI    APT1,X'DF'  RESET 'LOAD MODULE BIT ' TO 0
         LA    6,HEWLCRBN
         ST    6,HEWLCLAC         HEWLCLAC TO SYSLIN
INCLU310 BC   15,INCLU200   TOWARDS INPUT PROCESSOR
*****
*
*
*
* SUBROUTINE LIBOP  ,IS COMMON TO INCLUDE PROCESSOR AND ...
*  ..AUTOMATIC LIBRARY CALL PROCESSOR
*
*
*
* WHEN THE SUBROUTINE LIBOP GETS CONTROL, IT SUPPOSES THAT THE
* DDNAME IS POINTED TO BY REG. 8 . IF THE DATA SET IS A MEMBER TO FIND
* IN A LIBRARY , LIBOP ALSO SUPPOSES THE MEMBER NAME IS IN THE
* BLDL AREA (AT BLDLMBRN).
*   REGISTER  7  CONTAINS THE ADDRESS OF THE LIBRARY DCB
* LIBOP CHECKS IF THE DATA SET WOULD NOT BE ALREADY OPENED.
* LIBOP WORKS ON THE 'SYSLIB' DCB WHICH IS IN THE ALL PURPOSE
* TABLE(APT).THIS DCB CAN BE EITHER PARTIONED OR SEQUENTIAL
* LIBOP TESTS THE INDICATORS IND3 (OF THIS MODULE) AND APT3(OF THE APT)
*
*
LIBOP    ST    14,SAV14L1
*                             SET LIBADRC ON SYSLIB READ CONTROL BLOCK
         NI   IND3,X'BF'    RESET 'LIBOP FAILURE' TO 0
         LA    14,HEWLCRBB             SET HEWLCLAC
         ST    14,HEWLCLAC             TO SYSLIB READ BLOCK
         TM    APT3,X'01'          IS  SYSLIB OPENED ?
         BC    8,INCLU350          NO
         TM    HEWLCRBB+24,X'80'   IS THERE AN UNCHECKED READ
         BC    1,LIBOPA                NO
         OI    HEWLCRBB+24,X'88'   YES-INDICATE IGNORE EOF OR ERR
         L     1,HEWLCRBB          ADDRESS OF SYSLIB DECB
         LA    13,REGSA                ADDRESS OF SAVE AREA
         CHECK (1)
         NI    HEWLCRBB+24,X'F7'   TURN OFF IGNORE BIT
LIBOPA   TM    IND3,X'80'              IS PHYSICAL SEQUENTIAL ON
         BC    1,INCLU320   GO TO CLOSE
         CLC   0(8,8),LIBOPEN        COMPARE NEW DDNAME TO DDNAME OPEN
         BC   8,INCLU455   BR. IF EQUAL
INCLU320 LA    13,REGSA                ADDRESS OF SAVE AR%A
         CLOSE ((07))
         NI   APT3,X'FE'   TURN 'SYSLIB OPENED' TO OFF
INCLU350 MVC   0(SPLDCBLG,07),SPLEDCBP   LAY FRESH COPY OF AN
*                                            UNOPENED PO DCB (THIS
*                                  ZEROES WHAT NEED TO BE 0 BEFORE OPEN
         TM    IND3,X'80'           IS 'PHYSICAL SEQUENTIAL' ON
         BC    8,INCLU450           NO
INCLU400 MVI   DCBDSORG,X'40'       MAKE THE DCB TO BE A PHYS.SEQ. ONE
         MVC   DCBSYNAD+1(3),SYNADAD+1  MOVE IN SYNAD FOR BSAM
         NI    DCBMACR,FF-DCBMRPT1      TURN OFF POINT BIT IN   YM4650
*                                       MACRF FIELD             YM4650
INCLU450 MVC   DCBDDNAM(8),0(8)     SLOT NEWDDNAME INTO FIELD OF DCB
         MVC   LIBOPEN(8),0(8)         SAVE DDNAME
         LA    13,REGSA        ADDR OF REG SAVE AREA FOR SYSTEM MACRO
         OPEN  ((07))
         TM   DCBOFLGS,X'10'  IS OPEN FLG 'ON' IN SYSLIB DCB
         BC   1,INCLU453     YES
         LR    1,8            DDNAME ADDR.
         LH    0,IEW0432      ERROR.  OPEN FAILED ON SPECIFIED LIBRARY
         L     15,ERDIG
         BALR  14,15
         NI    APT3,X'FE'   SET 'SYSLIB NOT OPENED'
         OI   IND3,X'40'    SET 'LIBOP FAILURE'
         BC   15,INCLU610   TO EXIT OF LIBOP
INCLU453 OI   APT3,X'01'   SET 'SYSLIB OPENED'
INCLU455 MVC  TEMP1(1),DCBRECFM
         NI    TEMP1,X'C0'    SAVE ONLY F OR U INDICATORS
         CLI   TEMP1,X'C0'    IS IT A  U FORMAT
         BC    8,INCLU500      YES
         CLI   TEMP1,X'80'   IS IT A 'F' FORMAT
         BC    8,INCLU460    YES
         LH    0,IEW0522  ERROR CODE INTO 0
         LR    1,8
         L     15,ERDIG
         BALR  14,15
         OI    IND3,X'40'    SET FAILURE BIT 'ON'
         BC    15,INCLU610      EXIT OF LIBOP
INCLU460 NI    APT1,X'DF'              RESET LOAD MOD BIT TO ZERO
         MVI   HEWLCRBB+19,X'01'   INDICATE TO PRIME BUFFERS
         OI    HEWLCRBB+24,X'80'
         TM    HEWLCRBB+24,X'04'   WAS BLOCK SIZE OK
         BC    8,INCLU550              YES
         LH    0,IEW0532               NO--GIVE ERROR MESSAGE
         BC    15,INCLU510
INCLU500 TM    IND3,X'80'  DOES CONTROL CARD ANNOUNCES PHYS.SEQ.
         BC    8,INCLU520     NO
         LH    0,IEW0512  ERR CODE,SEVERITY,DISPOSITION,INTO 1
INCLU510 LR    1,8      ADDR OF DDNAME INTO 1
         L     15,ERDIG
         BALR  14,15    BR TO ERR ROUTINE
        CLOSE  ((7))    CLOSE D/S AFTER ERROR                  @ZA07584
         NI    APT3,X'FE'   TURN 'SYSLIB OPENED' TO OFF        @ZA07584
         OI    IND3,X'40'   SET LIBOP FAILURE
         BC    15,INCLU610   TO EXIT OF LIBOP
INCLU520 OI    APT1,X'20'  SET BIT 'LOAD MODULE'TO ONE
INCLU550 EQU   *
INCLU600 TM    IND3,X'80'         IS THE DATA SET PHYSICAL SEQUENTIAL
         BC    1,INCLU610         YES
         TM    IND3,X'10'              IS SINGLE BLDL MODE ON
         BC    8,INCLU610              NO--RETURN
         LA    13,REGSA        ADDR OF REG SAVE AREA FOR SYSTEM MACRO
        BLDL  (7),BLDLDES1
         XI    *+1,X'FF'        ***DEBUG TOKEN ***
         LTR   15,15              TEST RETURN CODE IN 15
         BC    7,INCLU615         BR.IF MODULE SEARCH NOT SUCCESSFUL
INCLU602 NI    IND3,X'BF'              RESET MODULE NOT FOUND BIT
         TM    APT1,X'20'
         BC    8,INCLU605
         TM    BLDLATT2,X'01'      IS INC MODULE REFRESHABLE
         BC    8,INCNREFR           NO
INCLU601 TM    APT1,X'20'          IS COMING MODULE A LOAD MODULE
         BC    8,INCLU605   NO
*            LOGIC  FOR  THE OUTPUT DIRECTORY
         OI    BLDLATT1,X'3F'
         NC    PDSE7(1),BLDLATT1
         TM    BLDLATT2,X'08'   IS MODULE NOT EDIT ABLE
         BC    8,INCLU605
INCLERNE LH    0,IEW0272  ERR CODE IN REG 0
         LA    13,BLDLMBRN    ADDR. OF MBER NAME INTO 13
         BC    15,INCLU510    TO ERROR AND LIBOP FAILURE EXIT
INCNREFR NI    PDSE8,X'FE'         IF INC MODULE IS NOT REFRESHABLE
*                         THEN ENTIRE LOAD MODULE WILL NOT BE.        *
         B     INCLU601
INCLU605 LA    13,REGSA       ADDR OF REG SAVE AREA FOR SYSTEM MACRO
         FIND  (7),BLDLTTR1,C
INCLU610 L     14,SAV14L1
         BCR   15,14             RETURN FROM THIS SUBROUTINE LIBOP
*
*****
INCLU615 OI    IND3,X'40'     SET 'MODULE NOT FOUND' BIT TO 1
         TM    IND3,X'20'    IS THIS AUTO LIB
         BC    7,INCLU610       YES, TO EXIT OF LIBOP
         LH    0,IEW0342
         LR    1,8                 DDNAME   ADDR
         LA   13,BLDLMBRN      MEMBER NAME ADDR.
         L     15,ERDIG
         BALR  14,15
         ST    10,SAVE10               SAVE CONTENTS OF REG 10
         L     10,RCCB                 ADDR OF CHNG/REPL CHAIN
         LTR   10,10                   DOES IT CONTAIN ZEROS?
         BZ    INCLU620                YES - BRANCH
         LR    10,12                   SAVE BASE IN REG 10
         LA    13,REGSA                ADDR OF SAVE AREA
         L     15,ENDADDR              ADDR OF HEWLFEND
         BALR  14,15                   GO TO DELETE CHNG/REPL
*                                      CHAIN FROM THE CESD
         LR    12,10                   RESTORE BASE REG
INCLU620 L     10,SAVE10               RESTORE REG 10
         BC    15,INCLU610    TOWARD RETURN FROM LIBOP
         ENTRY HEWLCAUT
         CNOP  0,4
HEWLCAUT S    15,16(15)      SET 15 AT ADDRESS VALUE OF HEWLCINC
         ST    12,8(15)      PASS VALUE OF REG. 12
          LA    12,14(15)   PUT IN 12 THE ADDRESS CORRESP. TO THE
*                      UNIQUE 'USING' STATEMENT OF THIS CSECT
         BC    15,*+8
         DC    A(HEWLCAUT-HEWLFINC)
         STM   13,11,SAVAREA
         L     07,SYSLIBAD   ADDR OF LIBRARY DCB
         OI    IND3,X'20'    SET 'AUTO LIB '  ON
         TM    IND3,X'08'              WAS BLDL DONE ON LIST
         BC    1,GETNEXT1              YES--GO GET NEXT MODULE
         NI   IND3,X'7F'     SET PHYSICAL SEQUENTIAL TO 0
AUTOCL1  L     4,CHESD
         LH    3,ENCDC             INITIALIZE COUNT FOR CESD SCAN
         LTR   3,3
         BC    8,INCLU710              BRANCH IF NOTHING IN CESD
         TM    APT0,X'01'              WAS A LIBRARY CARD READ
          BC    8,INCLU670   NO
*
*
*   ESD SCANNING TO FIND DDNAMES WITH POINTER
*
*
*
INCLU630 LA    4,16(4)
         MVC   TEMP1(1),8(4)    TAKE TYPE FIELD OF THIS LINE
         NI    TEMP1,X'0F'      CLEAR INFORMATION OTHER THAN TYPE
         CLI   TEMP1,X'02'      IS TYPE  ER
         BC    7,INCLU660       BR.IF NOT
         CLI   13(4),X'B0'     IS SUBTYPE DDNAME
         BC    7,INCLU660       BR.IF NOT
         LH    15,14(4)         LOAD THE 2 BYTE PTER TO LIB.MBER
         LTR   15,15            IS IT NULL
         BC    8,INCLU640       BR IF YES   GO TO NULLIFY THIS DDNAMLIN
INCLU635 SLL   15,4             MULTIPLY  THE PTER BY 16
         A     15,CHESD            ADD ADDR. OF CESD MINUS 16
         MVC   TBPTRSAV(2),14(15)  SAV.PTR.FOUND IN LINE JUST REACHED
         CLI   13(15),X'03'        IS THIS A MATCHED LIBR.MBR.
         BC    7,INCLU650      BR. IF NOT
         TM    9(15),X'80'   HAS A BLDL ALREADY BEEN DONE ON THIS
*                                  MEMBER
         BC    1,INCLU650          BR. IF YES
         OI    9(15),X'80'   SET BIT 'BLDL ALREADY ATTEMPTED'
         LR    8,4    PTER TO DDNAME IN 8 FOR LIBOP SUBROUTINE
         ST    8,SAVAD                 SAVE DDNAME ADDRESS
         LR    9,15                    ADDRESS OF MEMBER NAME INTO 9
         BAL   14,INCLU170             GO PUT NAME IN LIST
         B     INCLU650                GO CHECK FOR END OF CESD
INCLU640 MVI   8(4),X'07'          MAKE TYPE'NULL'
         XC    0(8,4),0(4)         MAKE SYMBOL BINARY ZEROS
         MVC   12(4,4),FLCD        STICK THIS LINE TO HOLES CHAIN
         ST    4,FLCD              UPDATE HOLES CHAIN BEGIN ADDR
         BC    15,INCLU660
INCLU650 LH    15,TBPTRSAV         IS THE SAVED TWO BYTES PTER =0
         LTR   15,15
         BC    7,INCLU635          BR. IF NOT
         CLI   NUMENT+1,X'00'          ANY ITEMS LEFT IN BLDL LIST
         BC    7,INCLU186              YES--GO PURGE THEM
INCLU660 BCT   3,INCLU630              LOOP TO TAKE NXT CESD LNE
* (THIS LINE WAS NOT PROPERLY CHANGED TO FIX A21451 FOR RELEASE 18)
*
*
* ESD SCANNING TO FIND PURE ER'S
*
*
INCLU670 L     4,CHESD             INITIATE SCAN FOR ...
         LH    3,ENCDC             STANDARD(SYSLIB) LIBRARY CALLS
         LA    8,LIBNAME           FIX SYSLIB AS NEW DDNAME
         ST    8,SAVAD                 SAVE DDNAME ADDRESS
INCLU680 LA    4,16(4)             SCAN CESD
         MVC   TEMP1(1),8(4)       TAKE TYPE FIELD FROM THIS LINE
         NI    TEMP1,X'0F'         CLEAR INFORMATION OTHER THAN TYPE
         CLI   TEMP1,X'02'         IS TYPE ER
         BC    7,INCLU700      BR. IF NOT
         CLI   13(4),X'00'         IS SUBTYPE NULL (ONLY ONE TO BE CALL
*                                  FROM SYSLIB)
         BC    7,INCLU690          BR.IF NOT
         TM    9(4),X'80'         A BLDL PREVIOUSLY ATTEMPTD FOR MBER ?
         BC    1,INCLU700           BR.IF YES
         MVI   9(4),X'80'           INDIC BLDL ISSUED           ZA01413
         LR    9,4                     ADDRESS OF MEMBER NAME INTO 9
         BAL   14,INCLU170             GO PUT NAME IN LIST
         BC    15,INCLU700             GO CHECK FOR END OD LIST
INCLU690 CLI   13(4),X'90'          IS SUBTYPE 'CONTROL OVERLAY'
         BC    7,INCLU700           BR.IF NOT
         MVI  8(4),X'07'   NULLIFY THE LINE
*TO ZEROE THE SYMBOL WOULD TAKE ME TIME. THE SYMB HAS FEW CHANCE TO BE
* MATCHED BY ESD PROC. IF IT IS,NULL TYPE WILL LEAD TO NO CONFUSION
         MVC   12(4,4),FLCD        STICK THIS LINE TO THE HOLE CHAIN
         ST    4,FLCD              UPDATE FLCD
INCLU700 BCT   3,INCLU680          TAKE NXT ELT.OF THE CESD
         CLI   NUMENT+1,X'00'          ANY ITEMS LEFT IN BLDL LIST
         BC    7,INCLU186              YES--GO PURGE THEM
INCLU710 LA    6,HEWLCRBN
         ST    6,HEWLCLAC
* THE SYSLIN READ BLOCK
         L     15,RATADD               GO TO TXT/RLD FOR PURGE
         OI    NEWSW,X'01'             INDICATE PURGE TO TXT/RLD
         BALR  14,15
         USING *,15                    RETURNS ON 15
         L     15,ADRASSEY
         DROP  15
         BCR   15,15               GO TO  ADDRESS ASSIGNMENT
         DS    0F
SAVAREA  DC    16F'0'
SAV14L1  DC    F'0'
SAVE10   DC    F'0'
         EXTRN HEWLFLDB
        EXTRN HEWLCADA
         EXTRN APTEXLST
INPUTPRA DC    V(HEWLFINP)
ENDADDR  DC    V(HEWLFEND)
SYSLIBAD DC    A(HEWLFLDB)
******** ERROR MSG ****************************************************
IEW0432  DC    AL2(IEW0432X)
IEW0342  DC    AL2(IEW0342X)
IEW0532  DC    AL2(IEW0532X)
IEW0512  DC    AL2(IEW0512X)
IEW0522  DC    AL2(IEW0522X)
IEW0272  DC    AL2(IEW0272X)
***********************************************************************
FOURTEEN DC    X'000E'
TBPTRSAV DC    X'0000'
TEMP1    DC    X'00'
IND3     DC    X'00'                   SWITCHES
*              X'80'  PHYSICAL SEQUENTIAL
*              X'40'  MODULE NOT FOUND
*              X'20'  AUTOLIB IN OPERATION
*              X'10'  SINGLE BLDL IN OPERATION
*              X'08'  BLDL DONE ON LIST
*              X'04'  BLDL LIST FULL
*  SAMPLE DCB FOR A PARTIONED ORGANIZATION TO BE READ
         TITLE 'HEWLFINC INCLUDE DCB'
         DC    C'INCLUDE DCB'
         DC    0F'0'
SPLEDCBP DCB   DSORG=PO,MACRF=(R),EODAD=HEWLCEOD,SYNAD=HEWLCRO2,       X
               EXLST=APTEXLST,NCP=1                   @ZA02657,@ZA12241
DCBPNDM1 EQU   *
SPLDCBLG EQU   DCBPNDM1-SPLEDCBP       LENGTH OF DCB
         EXTRN HEWLCRO2
SYNADAD  DC    A(HEWLCRO1)
ADRASSEY DC    V(HEWLFADA)             ADDRESS OF ADDRESS ASSIGNMENT
RATADD   DC    V(HEWLFRAT)             ADDRESS OF TXT/RLD PROCESSOR
NUMENT   DC    H'00'                   NUMBER OF ENTRIES IN LIST
ENTSIZ   DC    H'26'
BLDLLST  DC    260C' '
MAXENTRY DC    H'10'                   MAXINUM NUMBER OF ENTRIES
APPEND   DS    20C
SAVAD    DS    1F
TABLSTRT DC    A(BLDLLST)
PTRSAV   DC    H'0'                    SAVE POINTER TO DDNAME
CURINCNO DC    X'00'                   CURRENT ITEM BEING READ
         DS    0H
BLDLDES1 DC    X'0001' 1ST BYTE OF LIST DESCRIPTION FIELD (NO OF ENTR.)
BLDLDES2 DC    X'0036' 2ND BYTE OF LIST DESCRIPTION FIELD (LGTH OFEACH)
SINGBLDL DC    26C' '
***********************************************************************
*        PATCH AREA                                                   *
***********************************************************************
PATCH    DC    50F'0',C'PATCH AREA'    TEMPORARY PATCH AREA
DUMMY    DSECT
BLDLMBRN DC    XL8'00'   MEMBER NAME
BLDLTTR1 DC    XL4'00'   TTR OF FIRST RECORD OF PDS & CONCAT.NO.
BLDLZEUS DC    XL2'00'   ZERO BYTE (JOB/LINK LIB.IND.) AND USER DESCR.
BLDLTTR2 DC    XL4'00'   TTR OF FIRST TEXT RECORD AND ZERO
BLDLTTR3 DC    XL3'00'   TTR OF NOTE LIST OR SCAT.TRANS.TBL.
BLDLN0NL DC    XL1'00'   NO OF ENTRIES IN NOTE LIST (OR ZERO)
BLDLATT1 DC    XL1'00'   FIRST BYTE OF ATTRIBUTES
*                      BIT 0   , X'80'    REENTERABLE
*                      BIT 1   , X'40'    REUSABLE
*                      BIT 2   , X'20'    OVERLAY
*                      BIT 3   , X'10'    IN TEST
*                      BIT 4   , X'08'    ONLY LOADABLE
*                      BIT 5   , X'04'    BLOCK-SCATTER (BLOCK=0)
*                      BIT 6   , X'02'    EXECUTABLE
*                      BIT 7   , X'01'    MOD.CONTAINS 1 TXT RCD,NO RLD
BLDLATT2 DC    XL1'00'   SECOND BYTE OF ATTRIBUTES
*                      BIT 0   , X'80'   NO DOWNWARD COMPATIBILITY IF=1
*                      BIT 1   , X'40'   ORIGIN OF 1ST TXT RCD IS ZERO
*                      BIT 2   , X'20'   LINK.EDIT.ASSIGND ENTR.PT.=0
*                      BIT 3   , X'10'   MOD.CONTAINS RLD ITEMS
*                      BIT 4   , X'08'   MOD.CAN BE REPROCESSED BY LKED
*                      BIT 5   , X'04'   MOD.DOES'NT CONTAIN SYM CARDS
BLDLE9   DC   XL3'00' TOTAL MAIN STORAGE REQUIREMENT OF THIS MODULE
BLDLE10  DC   XL2'00' LENGTH OF FIRST TXT RECORD
BLDLE11  DC   XL3'00' ENTRY POINT ADDRESS
BLDLE12  DC   XL3'00' ORIGIN OF FIRST TXT RECORD
*
BLDLE13  DC    XL2'00'      NO. OF BYTES IN SCATTER LIST
BLDLE14  DC    XL2'00'      NO. OF BYTES IN THE TRANSLATION TBLE
BLDLE15  DC    XL2'00'      ESDID OF THE 1ST TXT RCD
BLDLE16  DC    XL2'00'      ESDID OF THE CONTR.SECT.CONTAINING ENTRY PT
*
BLDLE17  DC    XL3'00'      ENTRY PT OF MAIN MBER NAME
BLDLE18  DC    XL8'00'      MEMBER NAME OF MODULE
         TITLE 'HEWLFINC APT'
         DC    0F'0'
         HEWAPT
         DCBD DSORG=(PS,PO)
         END
