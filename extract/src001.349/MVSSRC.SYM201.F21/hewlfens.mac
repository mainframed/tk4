         TITLE    'HEWLFENS  ENTER SEGMENT NUMBERS'
***********************************************************************
*                                                                     *
*STATUS: CHANGE LEVEL                                                 *
*                                                                     *
*FUNCTION/OPERATION:                                                  *
*    1) ENTER SEGMENT NUMBERS FOR LABEL REFERENCES IN CESD            *
*    2) DETERMINE NUMBER OF ENTAB LINES REQUIRED PER SEGMENT (OVERLAY)*
*                                                                     *
*ENTRY POINTS:                                                        *
*    HEWLFENS- CONTROL SECTION NAME
*                                                                     *
*INPUT:                                                               *
*    1) REG 2 - ADDRESS OF ALL PURPOSE TABLE                          *
*    2) REG 13- ADDRESS OF CALLER SUPPLIED REGISTER SAVE AREA         *
*    3) TABLES LISTED BELOW                                           *
*                                                                     *
*OUTPUT:                                                              *
*    1) UPDATED COMPOSITE EXTERNAL SYMBOL DICTIONARY-LABEL REFERENCES *
*    2) COUNT OF ENTAB LINES PER SEGMENT                              *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*    1) HEWLELOG - ERROR DIAGNOSTIC ROUTINE                           *
*    2) HEWLCAD1 - AN ENTRY POINT IN EITHER HEWLKADA OR HEWLFADA      *
*        A) HEWLCAD1 IS ENTERED FROM HEWLKENS TO RECORD THE NUMBER OF *
*           LINES OF ENTAB -   REQUIRED PER SEGMENT-                  *
*          1)IN THE 'F' LINKAGE EDITOR HEWLCAD1 RECORDS THE ENTAB LINE*
*            COUNT IN THE SEGMENT LENGTH TABLE AND SETS THE 'E' BIT TO*
*            1-INDICATING THERE IS AN ENTAB FOR THIS SEGMENT          *
*        B) HEWLCAD1 IS ENTERED FOR EACH SEGMENT WHICH CONTAINS AN    *
*           ENTAB                                                     *
*                                                                     *
*EXITS:                                                               *
*   RETURNS TO ADDRESS PASSED IN REGISTER 14                          *
*                                                                     *
*TABLES:                                                              *
*   1) CALLS LIST                                                     *
*   2) COMPOSITE EXTERNAL SYMBOL DICTIONARY                           *
*   3) DOWNWARDS CALLS LIST (INTERNAL)                                *
*   4) SEGMENT TABLE 1                                                *
*   5) ALL PURPOSE TABLE                                              *
*                                                                     *
*ATTRIBUTES:                                                          *
*                                                                     *
***********************************************************************
HEWLFENS CSECT
*                                                                     *
*                                                                     *
*
*
*
***********************************************************************
*
*             REGISTERS USED IN HEWLKENS                              *
***********************************************************************
*
ERRCODRG EQU   0                        ERROR CODE REGISTER
WORK4    EQU   0                        WORK
WORK1    EQU   1                        WORK
ERRSYMRG EQU   1                        ADDRESS OF SYMBOL
APT      EQU   2                        ALL PURPOSE TABLE ADDRESS
CALLADD  EQU   3                        ADDRESS OF CALLS LIST
TEMP2    EQU   3                       TEMPORARY WORK REGISTER
CURCHNAD EQU   3                        CURRENT CHAIN ADDRESS
CURINDEX EQU   4                        CURRENT INDEX
CURPPTR  EQU   5                        CURRENT P POINTER
CURRPTR  EQU   6                        CURRENT R POINTER
CURPSEGN EQU   7                        CURRENT P SEGMENT NUMBER
CURRSEGN EQU   8                        CURRENT R SEGMENT NUMBER
CURSEGNO EQU   9                        CURRENT SEGMENT NUMBER
TEMP1    EQU   9                       TEMPORARY WORK REGISTER
BASE     EQU   12                       BASE REGISTER
ACUMZERO EQU   13                       ACCUMULATO
CULTOTCT EQU   14                       CULMULATIVE TOTAL NO. BYTES -2
RETURN   EQU   14                       RETURN (RETURN FROM CPTH)
WORK2    EQU   10                       WORK
BRANCH   EQU   15
CULRELCT EQU   15                       CULMULATIVE REL. NO. BYTES
ERRADDRG EQU   15                       ADDRESS OF ERROR ROUTINE
WORK3    EQU   15                       WORK
WORK5    EQU   15                       WORK   (NOT USED PRESENTLY)
*********************************************************************
*              DISPLACEMENTS
*********************************************************************
DISP     EQU   0                       DISPLACEMENT FOR SAVE AREA
***********************************************************************
*              MASKS
OVLYBIT  EQU   X'20'                    MASK FOR TESTING FOR OVERLAY
*
XCAL     EQU   X'04'                    XCAL TEST MASK
***********************************************************************
***********************************************************************
*              SAVE CALLERS REGISTERS AND INITIALIZE BASE  REGISTER
***********************************************************************
         STM   14,12,DISP(13)           SAVE CALLERS REGS
         USING ENS00500+2,BASE
         USING APTDSECT,APT   ESTABLISH ADDRESSABILITY FOR  APT
ENS00500 BALR  BASE,0                   INITIALIZE BASE
         BC    15,MODID+6              BRANCH AROUND MODID
MODID    DC    C'01MENS'
         ST    13,SAVERG13              SAVE 13 -ADD OF CALLERS  REGS
***********************************************************************
*
***********************************************************************
*              SCAN CESD  - PUT PROPER  SG NO OF LR IN CESD LR ENTRY
***********************************************************************
ENS0070  L     WORK1,CHESD        CESD ADD
         LH    WORK2,ENCDC         CESD CURR CT
ENS00750 LA    WORK1,16(WORK1)         INC CESD ADD
         MVN   TESTTYPE(1),8(WORK1)     MOVE TYPE TO TEST FOR LR
         CLI   TESTTYPE,X'03'           IS TYPE = LR
         BC    7,ENS00770               NO / GO TEST END OF LOOP
***********************************************************************
*              TYPE = LR
***********************************************************************
         LH    WORK3,14(WORK1)         ID
         SLL   WORK3,4                 REL ADD
         A     WORK3,CHESD        ADD OF CS
         MVC   12(1,WORK1),12(WORK3)   PUT CS.SG.NO. IN SG.FD. OF LR
ENS00770 BCT   WORK2,ENS00750          BRANCH IF LOOP NOT DONE
***********************************************************************
*              TEST FOR OVELAY / NO RETURN TO CALLER /YES CONTINUE PROC
***********************************************************************
         TM    PDSE7,OVLYBIT       IS PROGRAM IN OVERLAY
         BC    8,ENS115                 NO/ GO TO RETURN TO CALLER
***********************************************************************
*              TEST FOR NO ENTRYS IN CALLS LIST - NO V CONSTANTS-ERR  *
***********************************************************************
         NI    PSEGONE,X'00'           INIT-TO TEST FOR VALID CALL
*                                      FROM SEGMENT 1
         SR    ACUMZERO,ACUMZERO
         CH    ACUMZERO,ENCLC      IS CALL LIST EMPTY
         BC    8,ENS110                YES / GO TO PRINT ERROR MESS.
***********************************************************************
*              SCAN CALLS LIST AND INSERT RELAYIVE ADDRESS (CA)       *
***********************************************************************
         L     CALLADD,CLLT        ADDRESS OF CALLS LIST
         SR    CULTOTCT,CULTOTCT
ENS00800 SR    CULRELCT,CULRELCT
ENS00900 LA    CULRELCT,2(CULRELCT)     INCREMENT CUL RELATIVE CT BY 2
         CH    ACUMZERO,0(CALLADD,CULRELCT)  IS CALLS LIST ENTRY = 0
         BC    7,ENS00900                    NO
         STH   CULRELCT,0(CALLADD)       T)  INSERT CURRENT CHAIN ADD.
         LA    CALLADD,0(CALLADD,CULRELCT)
         LA    CULTOTCT,0(CULTOTCT,CULRELCT) IN CHAIN / FIND TOTAL CT
         CH    CULTOTCT,ENCLC      IS SCAN DONE
         BC    7,ENS00800              NO
*
*
         NI    PSEGONE,X'00'            INIT. TO TEST VALID CALL FR SG1
***********************************************************************
*              SET DOWNWARD CALLS LIST (ENTRY LIST) TO ZERO + INITALIZE
***********************************************************************
         L     WORK1,RECNT         ADD OF ENTRY LIST
         SR    TEMP1,TEMP1
         LH    TEMP2,ENCDC
         SRL   TEMP2,2                 NO. OF FULL WORDS IN LIST
         LA    TEMP2,1(TEMP2)          IF CESD CT LESS 4-SRL = 0 - T2 0
ENS011   LA    WORK1,4(WORK1)          INC ADDRESS OF NEXT ENTRY
         ST    TEMP1,4(WORK1)           SET THIS ENTRY TO 0
         BCT   TEMP2,ENS011            BRANCH IF LOOP NOT DONE
         SR    CURSEGNO,CURSEGNO
***********************************************************************
*              INCREMENT CURRENT SEGMENT NUMBER/TEST IF ALL SEGMENTS
*              PROCESSED/YES RETURN TO CALLER / NO CONTINUE
***********************************************************************
ENS015   LA    CURSEGNO,1(CURSEGNO)
         L     CURCHNAD,CLLT       ADD OF CALLS LIST
         SR    ACUMZERO,ACUMZERO
         CH    CURSEGNO,CSNO       HAVE ALL SEGMENTS BEEN PROCESSED
         BC    2,ENS110                YES
***********************************************************************
*              INITIALIZE TO FIND NEXT P IN CALLS LIST
***********************************************************************
ENS017   SR    CURINDEX,CURINDEX
         SR    CURPSEGN,CURPSEGN
         LA    CURINDEX,2(CURINDEX)          INDEX TO P PTR
         LH    CURPPTR,0(CURCHNAD,CURINDEX)  CURRENT P PTR
         LR    WORK1,CURPPTR                 CURRENT P PTR
         SLL   WORK1,4                       MULT BY 16
         A     WORK1,CHESD         POINTS TO CESD LINE  ID=P
         IC    CURPSEGN,12(WORK1)       CURRENT SEGMENT NO OF P
***********************************************************************
*              DOES THE CONTROL SECTION WITH ITS ESDID = THIS P LIE IN
*              THE CURRENT SEGMENT/ NO GO TO TEST END OF CALLS LIST
***********************************************************************
***********************************************************************
*              TEST FOR A CONTROL SECTION IN SEGMENT 1
***********************************************************************
         LA    WORK1,1                  INIT WORK1 TO 1
         CLR   WORK1,CURPSEGN           IS THIS P IN SEGMENT 1
         BC    7,ENS018                 NO / CONTINUE
         OI    PSEGONE,X'80'            SET IND A CALL FROM SEG 1 SEEN
ENS018   CLR   CURSEGNO,CURPSEGN        DOES CURR.P SEG.NO.= CURR.SEG.
         BC    7,ENS1200                NO
***********************************************************************
*              INCREMENT TO NEXT CALLS LIST ENTRY / TEST IF ALL RS FOR
*              THIS P HAVE BEEN PROCESSED
***********************************************************************
ENS020   LA    CURINDEX,2(CURINDEX)     INCREMENT TO NEXT R OR CHAIN
         CH    CURINDEX,0(CURCHNAD)     IS SCAN FOR THIS P PTR DONE
         BC    8,ENS1200                YES
***********************************************************************
*              USING R POINTER REFERENCE CESD TO DETERMINE WHAT ITS
*              TYPE IS / ER,PR NO GOOD
***********************************************************************
         LH    CURRPTR,0(CURCHNAD,CURINDEX)  PICK UP CURRENT R PTR
         LR    WORK1,CURRPTR                 CURRENT R PTR
         SLL   WORK1,4                       MULT BY 16
         A     WORK1,CHESD         POINTS TO ENTRY IN CESD R=ID
***********************************************************************
         MVN   TESTTYPE,8(WORK1)        MOVE LOW 4 BITS FOR TEST
         CLI   TESTTYPE,X'08'           IS TYPE = SD WITH RLDS
         BC    2,ENS020                 HIGH - GO TO NEXT R OR CA
         BC    8,ENS030                 YES - PROCESS SD
         CLI   TESTTYPE,X'05'           IS TYPE = CM
         BC    2,ENS020                 HIGH - GO TO NEXT R OR CA
         BC    8,ENS030                 YES - PROCESS CM
         TM    TESTTYPE,X'03'           IS TYPE = SD OR LR
         BC    4,ENS020                 NO - GO TO PROCESS NEXT R OR CA
*                                       TYPES SD,SD W/RLD,LR,CM OK
***********************************************************************
*              FIND SEGMENT NUMBER OF THIS R POINTER - USE CESD
***********************************************************************
ENS030   SR    CURRSEGN,CURRSEGN
         IC    CURRSEGN,12(WORK1)       PICK UP SEGMENT NO OF R = SR
***********************************************************************
*              TEST FOR UPWARD CALL - YES - NO ENTAB LINES REQUIRED
***********************************************************************
         CLR   CURRSEGN,CURPSEGN        DOES  SR = SP
         BC    8,ENS020                 YES UPWARD CALL / NO LINES
         LR    WORK1,CURRSEGN           PASS SR TO COMMON PATH
         LR    WORK2,CURPSEGN           PASS SP TO COMMON PATH
         BAL   RETURN,ENS1500
         CLR   WORK2,CURRSEGN           DOES  SR = SC
         BC    8,ENS020                 YES  (UPWARD CALL) / NO LINES
***********************************************************************
*              TEST FOR DOWNWARD CALL - YES - MAY REQUIRE ENTAB
***********************************************************************
         CLR   WORK2,CURPSEGN           DOES  SP = SC
         BC    8,ENS080                 YES  (DOWNWARD CALL)
***********************************************************************
*              DETERMINE  REGION NUMBERS FOR SEGMENT NUMBERS P AND R
***********************************************************************
         SR    WORK1,WORK1
         SR    WORK2,WORK2
         SR    WORK4,WORK4
ENS040   IC    WORK4,LSTS(WORK1)   HI SEG OF THIS REG
         LA    WORK1,1(WORK1)                REG NO.FOR SP IN WORK1
         CR    CURPSEGN,WORK4          DOES SP LIE IN THIS REG
         BC    2,ENS040                NO
ENS050   IC    WORK4,LSTS(WORK2)   HI SEG OF THIS REG
         CR    CURRSEGN,WORK4  DOES SR LIE IN THIS REG
         LA    WORK2,1(WORK2)                REG NO.FOR SR IN WORK2
         BC    2,ENS050                NO
***********************************************************************
*              DO BOTH SEGMENTS LIE IN THE SAME REGION / YES MAY B ERR
***********************************************************************
         CLR   WORK1,WORK2              DOES  RSP = RSR
         BC    7,ENS080                 NO / CALL TO DIFFERENT REGION
***********************************************************************
*              CONVERT SEGMENT NUMBER FROM WHICH THIS EXCLUSIVE CALL  *
*              HAS BEEN MADE FROM BINARY TO DECIMAL FOR PRINTING MESS.*
***********************************************************************
         LR    WORK3,CURPSEGN           FROM SEGMENT
         CVD   WORK3,CVDAREA            CONVERT TO DECIMAL
         UNPK  CVDAREA+0(5),CVDAREA+6(3)  UNPACK TO SUPPLY ZONE
         CLI   CVDAREA,C'0'            IS FIRST DIGIT ZERO
         BC    7,ENS055                NO
         MVI   CVDAREA,C' '            YES--MOVE IN BLANK
         CLI   CVDAREA+1,C'0'          IS SECOND DIGIT ZERO
         BC    7,ENS055                NO
         MVI   CVDAREA+1,C' '          YES--MOVE IN BLANK
ENS055   MVI   CVDAREA+3,C' '
         MVC   CVDAREA+4(4),CVDAREA+3  BLANK REMAINING FIVE CHARACTERS
*                                       PRINTING 8 CHARACTERS
***********************************************************************
*              USING DOWNWARD CALLS LIST DETERMINE IF THERE IS AN ENTAB
*              IN A SEGMENT COMMON TO CURRENT(SP) TO SYMBOL WITH ESDID
*              = TO R /YES-IS VALID XCAL / NO- IS INVALID XCALL
***********************************************************************
         L     WORK3,RECNT         ENTRY LIST ADDRESS
         IC    WORK4,4(WORK3,CURRPTR)   GET ENTRY AT THIS R = SPCR
         LTR   WORK2,WORK4              DOES SEG.NO = 0/ PUT SPCR WORK2
         BC    8,ENS070                 YES/NO PREVIOUS CALLS TO THIS R
         LR    WORK1,CURPSEGN           PASS SP TO CPTH
         BAL   RETURN,ENS1500           GO TO COMMON PATH  ANS =  SC
         CLR   WORK2,WORK4              DOES SC = SPCR
         BC    7,ENS070                 NO /NO CALL TO THIS R FROM A
*                                       SEGMENT IN ROOT PATH OF SP
***********************************************************************
*              TEST IF XCALL OPTION SPECIFIED - YES -EXECUTE
***********************************************************************
         LH    ERRCODRG,ERRPARM1        ASSUME XCALL ON/NO ERROR-WARN
         TM    APT0,XCAL           IS XCAL OPTION = ON
         BC    1,ENS060                 XCALL ON/ GO PRINT WARNING
         LH    ERRCODRG,ERRPARM2        XCALL OFF /EXECUTE ON LET ONLY
*                                       SEVERITY 3
***********************************************************************
*              GO TO ERROR DIAGNOSTIC ROUTINE TO PRINT MESSAGE
***********************************************************************
ENS060   ST    ACUMZERO,SAVECT          SAVE ENTAB COUNT
         LR    ACUMZERO,CURRPTR         ID OF R - TO SYMBOL
         SLL   ACUMZERO,4               RELATIVE ADD OF SYMBOL
         A     ACUMZERO,CHESD      TRUE ADDRESS OF SYMBOL
         L     ERRADDRG,ERDIG      ERROR ADD
         LA    ERRSYMRG,CVDAREA        ADDRESS OF SEGMENT NUMBER
         BALR  RETURN,ERRADDRG
         L     ACUMZERO,SAVECT          RESTORE COUNT OF ENTAB LINES
         BC    15,ENS020
***********************************************************************
*              AN INVALID CALL HAS BEEN DETECTED  SET UP TO PRINT MESS.
***********************************************************************
ENS070   LH    ERRCODRG,ERRPARM3        INVALID XCALL / EXECUTE ON LET
         BC    15,ENS060
***********************************************************************
*              TEST IF A PREVIOUS CALL HAS BEEN MADE TO THIS SYMBOL
*              FROM A SEGMENT COMMON TO THE CURRENT SEGMENT
***********************************************************************
ENS080   L     WORK3,RECNT         ADD OF DOWNWARD CALLS LIST
         SR    WORK2,WORK2
         IC    WORK2,4(WORK3,CURRPTR)   GET SEG NO OF PREVIOUS CALL
         LTR   WORK4,WORK2              WAS THERE A PREVIOUS CALL
         BC    8,ENS090                 NO
         LR    WORK1,CURPSEGN           PASS SP TO CPTH
         BAL   RETURN,ENS1500           GO TO COMMON PATH/ANS IS = SC
         CLR   WORK2,WORK4              DOES SC = SPCR
         BC    8,ENS020                 YES /AN ENTAB LINE EXISTS IN
*                                       THE ROOT PATH OF SP FOR R
***********************************************************************
*              PUT THE CURRENT SEGMENT NUMBER IN THE DOWNWARDS CALL
*              LIST AT THE R TH ENTRY-- R IS THE ESDID OF THE CALLED
*              SYMBOL
***********************************************************************
ENS090   L     WORK3,RECNT
         STC   CURPSEGN,4(WORK3,CURRPTR)
         LA    ACUMZERO,1(ACUMZERO)
***********************************************************************
*              TEST IF A CALL HAS BEEN MADE FROM SEG 1 - YES-TO VALID
*              SYMBOL / YES - SET INDICATOR IN PSEGONE -BIT 2
***********************************************************************
         TM    PSEGONE,X'80'            IS THIS CALL FROM SEGMENT 1
         BC    8,ENS020                 NO /
         OI    PSEGONE,X'40'            YES / AN A VALID CALL WAS  MADE
         BC    15,ENS020
***********************************************************************
*              TEST IF A VALID CALL HAS BEEN FROM SEGMENT 1 TO A SYMBOL
*              NOT IN SEGMENT 1
***********************************************************************
ENS110   LH    ERRCODRG,ERRPARM4
         TM    PSEGONE,X'40'
         BC    1,ENS115                 YES/ DO NOT PRINT ERROR MESS
         L     ERRADDRG,ERDIG      ADD OF ERROR DIAGNOSTIC ROUTINE
         BALR  RETURN,ERRADDRG          GO TO ERROR DIAGNOSTIC ROUTINE
***********************************************************************
*              RESTORE REGISTERS AND RETURN TO CALLER
***********************************************************************
ENS115   L     13,SAVERG13              RESTORE ADDRESS OF CALLERS AREA
         LM    14,12,DISP(13)           RESTORE CALLERS REGISTERS
         BCR   15,14                    RETURN TO CALLER
***********************************************************************
*              SET INDICATOR FOR PROCESSING A CONTROL SECTION IN SG1TO0
***********************************************************************
ENS1200  NI    PSEGONE,X'7F'            SET P IN SEGMENT 1 TO 0
***********************************************************************
*              TEST FOR END OF PROCESSING CURRENT SEGMENT
***********************************************************************
         AH    CURCHNAD,0(CURCHNAD)     ADD.CURRENT CHAIN ADDRESS TO
*                                       ADD.OF PREVIOUS CHAIN ADDRESS
ENS010   LH    WORK1,0(CURCHNAD)        CURRENT CHAIN
         LTR   WORK1,WORK1              IS SCAN FOR CURRENT SEGMENT DON
         BC    2,ENS017                NO  / LOOK AT NEXT P
***********************************************************************
*              TEST IF THERE ANY ENTAB LINES FOR THIS SEGMENT/ YES GO
*              TO HEWLCAD1 TO RECORD COUNT.
***********************************************************************
         LTR   ACUMZERO,ACUMZERO        ANY ENTAB LINES FOR THIS SEG
         BC    8,ENS015                 NO  GO TO PROCESS NXT SEGMENT
         LM    RETURN,BRANCH,ENS1300    SET UP RETURN & BRANCH REGS
         BCR   15,BRANCH                GO TO PCEB / ENTER ENTAB INCESD
***********************************************************************
***********************************************************************
*              CPTH -  COMMON PATH SUBROUTINE                         *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
*                   REQUIRED REGISTERS  & PARAMETERS                  *
*                        BASE                                         *
*                        WORK1     SEG A  (NOT SAVED)                 *
*                        WORK2     SEG B  (NOT SAVED)  ANS PUT HERE   *
*                        WORK3     WORK REGISTER (NOT SAVED)          *
*                        RETURN    RETURN ADDRESS                     *
*                        APT       ALL PURPOSE TABLE                  *
***********************************************************************
***********************************************************************
ENS1500  L     WORK3,SGT1          ADDRESS OF SEGTAB1
ENS1600  CLR   WORK2,WORK1              DOES  SEG B = SEG A
         BCR   8,RETURN                 YES - ANS IN WORK 2
         BC    2,ENS1700                NO  -SEG B GTR SEG A
         XR    WORK2,WORK1              NO  -SEG B LOW SEG A
         XR    WORK1,WORK2              EXCHANGE PARAMETERS
         XR    WORK2,WORK1
ENS1700  IC    WORK2,0(WORK3,WORK2)     GET PREVIOUS SEGMENT NUMBER
         LTR   WORK2,WORK2              IS PREVIOUS SEGMENT NUMBER = 0
         BC    2,ENS1600                NO
         LA    WORK2,1                  SET ANS = 1
         BCR   15,RETURN
***********************************************************************
***********************************************************************
ENS1300  DC    A(ENS015)                RETURN ADDRESS
         DC    V(HEWLCAD1)
SIX      DC    H'6'                     CONSTANT OF 6
*
TESTTYPE DC    X'00'                    USED FOR TESTING LOW 4 BITS OF
*                                       ESD TYPE
PSEGONE  DC    X'00'                    USED TO INDICATE AVALID CALL
*                                       FROM SEGMENT 1
*                                       BIT 1- INIT= 0  1= CALL FROMSG1
*                                       BIT 2- INIT= 0  1= CALL FROMSG1
*                                        TO A VALID SYMBOL NOT IN SEG 1
CVDAREA  DS    2D          '            PRINT IMAGE FOR XCALL MESSAGE
SAVECT   DC    F'0'                     SAVE  ENTAB COUNT WHEN GOING TO
*                                       ERROR DIAGNOSTIC ROUTINE
SAVERG13 DS    F                        ADDRESS OF CALLERS REG SAVEAREA
***********************************************************************
*              ERROR PARAMETERS                                       *
***********************************************************************
MESS1    EQU   X'3110'
MESS2    EQU   X'3211'
MESS3    EQU   X'3212'
MESS4    EQU   X'020F'
ERRPARM1 DC    AL2(MESS1)               WARNING XCLUSIVE CALL-XCALL ON
ERRPARM2 DC    AL2(MESS2)               ERROR XCLUSIVE  CALL -XCALL OFF
ERRPARM3 DC    AL2(MESS3)               ERROR INVALID XCLUSIVE CALL
ERRPARM4 DC    AL2(MESS4)               NO CALLS FROM SEG 1 TO VALID
*
*
*
***********************************************************************
         HEWAPT
***********************************************************************
         END
