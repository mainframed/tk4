         TITLE 'HEWLFEND  END CARD PROCESSOR'
*FUNCTION/OPERATION: THIS MODULE PROCESSES THE ENTRY POINTS ON END    *
*      CARDS,INSERTS THE LENGTH OF CSECTS IN THE CESD FOR THE FIRST   *
*      CSECT INPUT WITH ZERO LENGTH IN THE INPUT MODULE(THE LENGTH IS *
*      OBTAINED FROM COLS 29-32 OF THE END CARD),CLEARS THE RENUMBER- *
*      ING TABLE, AND REMOVES REPLACE/CHANGE CARD ENTRIES FROM THE    *
*      CESD AS WELL AS REMOVING THOSE CESD LINES WHICH ARE STILL MARK-*
*      ED 'DELETE' AT THE END OF THE MODULE.                          *
*                                                                     *
*ENTRY POINTS HEWLFEND - BEGINNING  OF CSECT     LA,13,SAVEAREA       *
*                                               BALR 14,15            *
*                                                                     *
*INPUT:   REGISTERS-                                                  *
*      REGISTER  4- LENGTH FROM END CARD(IF PRESENT)                  *
*      REGISTER  5- ID OF ABSOLUTE ENTRY POINT ON END CARD(IF PRESENT)*
*      REGISTER 13- ADDRESS OF A SAVEAREA                             *
*      REGISTER 14- RETURN ADDRESS                                    *
*                                                                     *
*OUTPUT:  NOT APPLICABLE                                              *
*EXTERNAL ROUTINES: NOT APPLICABLE                                    *
*EXITS-NORMAL:  AFTER PROCESSING ENTRY POINT, LENGTH,CESD AND RNT     *
*                BCR 15,14                                            *
*              ERRORS: ERROR IN INPUT- ABSOLTE ENTRY POINT ID EXCEEDS *
*                            MODULE LIMITATION                        *
*TABLES/WORK AREAS: NOT APPLICABLE                                    *
*ATTRIBUTES: THIS MODULE IS READ ONLY,UNPRIVELEGED AND ENABLED        *
*NOTES:  THIS MODULE IS USED BY BOTH E AND F LEVEL LINKAGE EDITORS    *
HEWLFEND CSECT
**********************************************************************
*165000-177000,681000-744000                                     S01904
* D234000,I351000,C354000                                       ZA01411
* D642000-669000,C672000                                        ZA01413
**********************************************************************
SYSBRNCH EQU   15
BASE     EQU   12
APT      EQU   2
ZERO     EQU   0
RNT      EQU   7
REG13    EQU   13
ENTORABS EQU   X'0C'
RNTX     EQU   5
TEMP     EQU   8
ENTRYPNT EQU   X'08'
NOLEN    EQU   X'C0'
LENGTH   EQU   4
CESDADD  EQU   236
CESD     EQU   6
NOLENAND EQU   X'18'
CESDXR   EQU   10
TEMP1    EQU   9
ERRORCOD EQU   X'20A'
TEMP2    EQU   10
RNTTYPE  EQU   3
CESDTYPE EQU   8
CHID     EQU   14
TEMP3    EQU   11
TEMP4    EQU   3
SUBTYPE  EQU   13
SYSRET   EQU   14
R0       EQU   0                                                S01904
R1       EQU   1                                                S01904
R14      EQU   14                                               S01904
R15      EQU   15                                               S01904
MATCH    EQU   X'80'                   MATCH FLAG IN ORDER TBL  S01904
*              THE END CARD PROCESSOR IS ENTERED FROM THE INPUT       *
*              PROCESSOR                                              *
*              REGISTER 5 CONTAINS THE ID OF THE ASSEMBLED ADDRESS    *
*              ENTRY                                                  *
*              REGISTER 4 CONTAINS THE LENGTH FOR A NO LENGTH CONTROL *
*              SECTION IF ONE WAS PROCESSED IN THIS INPUT MODULE      *
*                        ********************                         *
         USING APTDSECT,APT        ESTABLISH ADDRESSABILITY WITH APT
         LR    BASE,SYSBRNCH
         USING HEWLFEND,BASE
*              INITIALIZE BASES FOR CESD AND RNT AND SET REGISTER     *
*              ZERO TO 0                                              *
         BC    15,MODID+6              BRANCH AROUND MODULE IDENT
MODID    DC    C'02MEND'
         STM   SYSRET,BASE,12(REG13)
         ST    SYSRET,REGSA14
         L     RNT,RECNT
         L     CESD,CHESD
*              IF THERE IS AN ABSOLUTE ENTRY POINT ON THE END CARD    *
*              AND NO OTHER ENTRY POINT HAS BEEN PROCESSED-           *
*              RENUMBER THE INPUT ID                                  *
         TM    APT1,ENTORABS
         BC    5,END1
         LTR   RNTX,RNTX               ILLEGAL ID
         BC    12,ERROR                YES
         CH    RNTX,ENCDX
         BC    2,ERROR
         SLL   RNTX,2
         LH    TEMP,0(RNT,RNTX)
         LA    TEMP2,0(RNT,RNTX)       ADDRESS OF RNT LINE
         TM    RNTTYPE(TEMP2),X'02'    IS TYPE ER
         BC    8,END03                 NO RELATIVE
         TM    RNTTYPE(TEMP2),X'10'    IS TYPE DELETE
         BC    8,END02                 NO
         LR    TEMP2,TEMP              COMPUTE
         SLL   TEMP2,4                 CESD
         AR    TEMP2,CESD              ADDRESS
         TM    CESDTYPE(TEMP2),X'40'   IS CESD TYPE CHAINED
         BC    1,END01                 YES
         TM    CESDTYPE(TEMP2),X'10'   IS CESD TYPE DELETE
         BC    8,END01A                NO
         TM    SUBTYPE(TEMP2),X'08'    IS SUBTYPE DDLETE
         BC    8,END01A                NO
         OI    APT1,X'04'          ENTRY IS SYMBOL
         BC    15,END1
END01    LH    TEMP,CHID(TEMP2)        CHAIN ID
END01A   L     TEMP4,EPSM          ORIGINAL ADDRESS
         SL    TEMP4,CESDTYPE(TEMP2)   DELINK
         ST    TEMP4,EPSM          RESTORE ADDRESS
END02    OI    APTSWS,X'20'        SET ABSOLUTE BIT ON
END03    STH   TEMP,ENDID              RENUMBER
         MVC   PDSE16(2),ENDID
         OI    APT1,ENTRYPNT
*              IF A NO LENGTH CONTROL SECTION WAS ENCOUNTERED IN THE  *
*              PRECEEDING INPUT MODULE-                               *
*              SET THE LENGTH FROM THE END CARD INTO THE CORRECT CESD *
*              LINE                                                   *
END1     SR    ZERO,ZERO               CLEAR REG FOR RNT CLEAR  ZA01411
         TM    APT2,NOLEN                                       ZA01411
         BC    12,END2
         L     TEMP,PDSE18
         NC    13(3,TEMP),13(TEMP)
         BC    8,END1A
         L     11,12(0,TEMP)
         LA    11,0(0,11)
         CLR   11,LENGTH
         BC    2,END2
END1A    IC    TEMP1,12(TEMP)
         ST    LENGTH,12(TEMP)
         STC   TEMP1,12(TEMP)
*              CLEAR THE NO LENGTH INDICATORS                         *
END2     NI    APT2,NOLENAND
*              INITIALIZE THE REPLACE/CHANGE CHAIN ADDRESS            *
         XC    RCCE(8),RCCE
         LR    CESDXR,CESD
         LH    TEMP,ENCDC
*              SEARCH THE CESD LOOKING FOR LINES TO BE DELETED        *
         LA    TEMP,1(TEMP)
END3     LA    CESDXR,16(CESDXR)
         BCT   TEMP,END3A
         BC    15,END10
*              IS THE PRESENT CESD LINE FOR AN EXTERNAL REFERENCE     *
END3A    TM    8(CESDXR),X'02'
          BC        8,END7
         TM    8(CESDXR),X'0D'
          BC        5,END7
*              NO-BRANCH                                              *
*              IS TYPE A CARD                                         *
         TM    13(CESDXR),X'80'
*              NO-BRANCH                                              *
         BC    8,END8
*              YES-IS THIS A REPLACE-CHANGE-DELETE CARD LINE          *
         TM    13(CESDXR),X'60'
         BC    12,END3
*              NO-BRANCH                                              *
*              YES-DELETE THE CESD LINE AND ADD THE LINE TO THE       *
*              CHAIN OF DELETED LINES                                 *
END4     XC    0(16,CESDXR),0(CESDXR)
         MVI   8(CESDXR),X'07'
         L     TEMP1,FLCD
*              THERE ARE NO LINES IN THE CHAIN IF THE POINTER TO THE  *
*              FIRST CHAIN LINE IS ZERO                               *
         ST    TEMP1,12(CESDXR)
END6     ST    CESDXR,FLCD
         BC    15,END3
*                                                                     *
*                                                                     *
*              IS THIS LINE CHAINED TO ANOTHER                        *
END7     TM    8(CESDXR),X'40'
         BC    1,END9
*              YES-INDEX TO LINE CHAINED TO AND IF THE LINE           *
*              IS NOT AN ER,DELETE THIS LINE FROM THE CESD            *
*                                                                     *
*                                                                     *
*              NO- IS THIS LINE STILL MARKED DELETE                   *
END7A    TM    8(CESDXR),X'10'
         BC    1,END4
*              YES- CLEAR OUT THE LINE                                *
         BC    15,END3
*              NO-CONTINUE THE SEARCH OF THE CESD                     *
*                                                                     *
*                                                                     *
*                                                                     *
*              IF ER IS NOT DELETE OR REPLACE , CLEAR DELETE BIT  IN  *
*              SUBTYPE FIELD                                          *
*              IF ER IS A REPLACE , CLEAR DELETE BIT IN TYPE FIELD    *
*              IF ER IS A DELETE , CLEAR OUT THE LINE                 *
END8     TM    8(CESDXR),X'10'
         BC    8,END8A
         TM    13(CESDXR),X'08'
         BC    8,END8B
         BC    15,END4
END8B    NI    8(CESDXR),X'EF'
END8A    NI    13(CESDXR),X'F7'
         BC    15,END7
*              IF THE LINE CHAINED TO IS AN ER-UNMATCHED LIBRARY      *
*              AND THIS LINE IS DELETE-CLEAR THIS LINE                *
END9     LH    TEMP1,14(CESDXR)
         SLL   TEMP1,4
         LA    TEMP1,0(TEMP1,CESD)
         TM    8(TEMP1),X'0D'
          BC        5,END4
         TM    8(TEMP1),X'02'
         BC    8,END4
         TM    13(TEMP1),X'06'
         BC    8,END4
*              OTHERWISE , SET THE LINE WHICH IS CHAINED TO AS A      *
*              MATCHED MEMBER BEFORE CLEARING THE CHAINED LINE        *
         TM    8(CESDXR),X'10'
         BC    1,END4
         OI    13(TEMP1),X'01'
         BC    15,END4
*              CLEAR OUT THE RNT                                      *
END10    LH    TEMP,ENCDX
END10A   ST    ZERO,4(RNT)                                      ZA01413
         LA    RNT,4(RNT)
         BCT   TEMP,END10A
         LH    R1,ORDRCUR              CURR BYTES IN ORDER TBL  S01904
         LTR   R1,R1                   ANY ENTRIES IN THE TBL   S01904
         BZ    END10E                  NONE,BRANCH OUT          S01904
         L     R15,ORDRADR             ADDR OF ORDER TBL        S01904
         AR    R1,R15                  ADD TO POINT OT TBL END  S01904
         LA    R0,3                    3 BYTES PER ENTRY        S01904
         SR    R1,R0                   ADJUST TO POINT TO LAST ENTRY
END10C   MVC   IDSAVE(2),1(R15)       GET ID OF ORDER TBL ENTRY S01904
         LH    R14,IDSAVE                                       S01904
         SLL   R14,4                   COMPUTE CORRESPONDING    S01904
         A     R14,CHESD               ENTRY IN CESD.           S01904
         CLI   0(R14),0                IS NAME FIELD NULL?      S01904
         BE    END10D                  YES-RESET MATCH FLAG     S01904
         MVN   IDSAVE(1),8(R14)        MOVE TYPE                S01904
         CLI   IDSAVE,0                TYPE SD?                 S01904
         BE    END10F                  YES,FLAG A MATCH         S01904
         CLI   IDSAVE,X'05'            TYPE COMMON?             S01904
         BNE   END10D                  IF NOT,DO NOT FLAG MATCH S01904
END10F   OI    0(R15),MATCH            SD & CM - MATCH ORDER TBL
         B     END10G                  PROCEED ORDER TBL SCAN   S01904
END10D   NI    0(R15),X'FF'-MATCH      RESET MATCH FLAG         S01904
END10G   BXLE  R15,R0,END10C           CONTINUE SCAN            S01904
END10E   L     SYSRET,REGSA14
         LM    SYSBRNCH,1,16(REG13)
         LM    3,BASE,32(REG13)
         BCR   15,SYSRET
ERROR    L     SYSBRNCH,ERDIG
         OI    APT1,X'40'
         LA    ZERO,ERRORCOD
*              **** ERROR ****                                        *
*              ID FOR ABSOLUTE ENTRY POINT EXCEEDS MODULE LIMITATION  *
         BALR  SYSRET,SYSBRNCH
         BC    15,END1
ENDID    DC    H'0'
IDSAVE   EQU   ENDID
*              ENDID IS TEMPORARY STORE FOR THE RENUMBERED ID         *
*              IT IS MOVED TO AN OFF WORD BOUNDARY IN THE APT         *
REGSA14  DC    F'0'
         HEWAPT
         END
