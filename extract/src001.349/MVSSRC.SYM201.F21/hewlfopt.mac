         TITLE    'HEWLFOPT  ATTRIBUTES AND OPTIONS PROCESSOR'
*                                                                     *
***********************************************************************
*                                                                     *
*                                                                     *
* STATUS -  CHANGE LEVEL  000                                         *
*                                                                     *
* FUNCTION/OPERATION -   THIS ROUTINE SCANS & ANALYZES THE ATTRIBUTES *
* & OPTIONS CONTROL INFORMATION WHICH IS PASSED TO IT INDIRECTLY      *
* FROM THE PARAMETER LIST.                                            *
*                                                                     *
*     THIS PARAMETER INFORMATION INDICATES WHAT MODULE ATTRIBUTES     *
*  AND  L/E.  PROCESSING OPTIONS ARE TO BE USED IN GENERATING THE     *
*  TYPE OF LOAD MODULE DESIRED. THE INFORMATION IS BASED ON THE       *
*  PARAMETERS  SPECIFIED IN THE  'EXEC' CONTROL CARD BY THE           *
*  PROGRAMMER USER, OR THOSE PASSED BY A LINK, XCTL, OR ATTACH MACRO  *
*   FROM ANOTHER PROGRAM.                                             *
*                                                                     *
*  THESE OPTIONS & ATTRIBUTES ARE CHECKED FOR VALIDITY & ARE ALSO     *
*  ANALYZED  TO   DETERMINE IF THEY ARE REQUESTED IN COMBINATIONS     *
* WHICH  ARE  ALLOWABLE. THE RESULTS OF THIS PROCESSING ARE THEN      *
* INDICATED BY  -
*                                                                     *
*  1.- THE SETTING OF INTERNAL INDICATORS IN THE ALL PURPOSE TABLE    *
*  2.- PRINTED MESSAGES INDICATING WHAT ATTRIBUTES/OPTIONS  WERE      *
*  SPECIFIED & WHETHER OR NOT THEY  WERE VALID & ACCEPTED.            *
*                                                                     *
*  THIS ROUTINE HAS A CHARACTER CODE DEPENDENCE                       *
*       THE HEXADECIMAL REPRESENTATION OF A ZERO                      *
*            IS EXPECTED TO BE  F0                                    *
*       THE ZONE OF ANY DIGIT IS EXPECTED TO BE  F                    *
*                                                                     *
*                                                                     *
*  ENTRY POINTS -                                                     *
*                                                                     *
*   (1)  HEWLFOPT = MODULE NAME                                       *
*                                                                     *
* ENTERED FROM INITIAL PROCESSOR - BY  THIS'CALLING  SEQUENCE '-      *
*                                                                     *
*       L  1,       ADDRESS OF PARAMETER LIST BYTE  COUNT             *
*       L  15,      HEWLFOPT = ENTRY POINT                            *
*       BALR 14,15  LINKAGE                                           *
*                                                                     *
* ENTERED FORM SCN -'BY THIS CALLING SEQUENCE'                        *
*  L RA = LENGTH OF PARMS ON THE PARM CARD.                           *
*  L RB = ADDRESS OF THE PARMS IN THE SYSLIN BUFFER.                  *
* L RF = ADDRESS OF HEWLFOPT (ENTRY POINT).                           *
* BALR RE,RF                                                          *
*                                                                     *
*                                                                     *
*                                                                     *
*  INPUT -                                                            *
*                                                                     *
*        THE PARAMETER LIST ADDRESS IS PASSED  IN REG. NO.1  WHICH IS *
* USED TO OBTAIN A  BYTE COUNT & ASSOCIATED PARAMETER INFORMATION.    *
*                                                                     *
*                                                                     *
*      THE FOLLOWING  ARE COMPLETE LISTS  OF  THE  'ATTRIBUTE   AND   *
*  OPTION ' SYMBOLS  WHICH ARE AVAILABLE  FOR  USE, AND  THEY WILL    *
*  DETERMINE THE CHARACTERISTICS OF THE LOAD  MODULE TO BE CREATED.
*                                                                     *
*      (A)       - MODULE  ATTRIBUTES -                               *
*                                                                     *
* 1.- SCTR =  FORMAT OF MODULE IS SUITABLE FOR EITHER SCATTER OR      *
*             BLOCK LOADING.                                          *
*                                                                     *
* 2.- OVLY =  MODULE STRUCTURED IN OVERLAY.                           *
*                                                                     *
* 3.- HIAR = MODULE TO BE LOADED INTO SELECTED STORAGE HIERARCHIES.   *
*                                                                     *
* 4.- RENT = MODULE IS RE-ENTERABLE                                   *
*                                                                     *
* 5.- REUS = MODULE IS SERIALLY  RE-USABLE                            *
*                                                                     *
* 6.- REFR = MODULE IS REFRESHABLE                                    *
*                                                                     *
* 7.- TEST = MODULE CONTAINS TESTING SYMBOL TABLES FOR USE BY THE     *
*            TEST TRANSLATOR                                          *
*                                                                     *
*                                                                     *
*      (B) -  PROCESSING  OPTIONS -                                   *
*                                                                     *
*                                                                     *
*  1.-  NCAL  =     NO  AUTOMATIC  LIBRARY  CALL .                    *
*                                                                     *
*  2.-  LET   =    LOAD MODULE IS MARKED EXECUTABLE - ALLOWING MINOR  *
*                  ERROR  CONDITIONS DETECTED  DURING PROCESSING.     *
*                                                                     *
*  3.- XCAL   =    ALLOWS 'EXCLUSIVE BRANCH'  TYPE ERRORS , ONLY.     *
*                                                                     *
*  4.- LIST   =   OUTPUTS LISTING OF ALL L/E. CONTROL  STATEMENTS.    *
*                  IN CARD - IMAGE FORMATS.                           *
*                                                                     *
*  5.- MAP    =   PRODUCES A MAP  OF THE LOAD MODULE'S STRUCTURE.     *
*                                                                     *
*  6.- XREF   =  PRODUCES A CROSS - REFERENCE TABLE OF THE            *
*                 MODULE WHICH ALSO INCLUDES A MAP.                   *
*                                                                     *
*                                                                     *
* 7.- PRINT  = WRITE HEADERS, ALLOW XREF,MAP,LIST IF SPECIFIED.       *
* 8.-NOPRINT =  PRINT ONLY ERRORS.                                    *
* 9.-NOXREF  =  DO NOT PERFORM AN XREF LISTING FOR THIS MODULE.       *
*10.- NOCALL =  DO NOT PERFORM AUTOMATIC CALL FOR UNRESOLVED ERS.     *
*11.- CALL   =  PERFORM AUTOMATIC CALL ON SYSLIB FOR UNRESOLVED ERS.  *
*12.- NORENT =  DO NOT MARK THIS MODULE AS RE-ENTRANT.                *
*13.- NOREUS =  DO NOT MARK THIS MODULE AS RE-USEABLE.                *
*14.- TERM   =  THE TERMINAL IS BEING SUPPORTED.                      *
*15.- NOTERM = THE TERMINAL  IS NOT BEING SUPPORTED FOR THIS RUN.     *
*                                                                     *
*  * SPECIAL OPTIONS -                                                *
*                                                                     *
*  1. - DC    =  'DOWNWARD COMPATIBILTY' - MEANS THIS MODULE IS       *
*                 ACCEPTABLE AS INPUT  TO ALL LEVELS OF LINKAGE       *
*                 EDITORS.                                            *
*                                                                     *
* 2. -  OL    = ' ONLY LOADABLE '- INDICATOR IS SET - ON.             *
*                                                                     *
* 3. -  NE    = ' NOT EDITABLE ' -MEANS  THAT  THE ' ESD ' INFORMATION*
*                IS ELIMINATED  AND THEREFORE THIS MODULE WILL  NOT   *
*                BE ACCEPTED  BY  LINKAGE EDITOR FOR RE-PROCESSING.   *
*                                                                     *
* 4.- SIZE = OPTION PERMITTING SPECIFICATION OF
*                 A) MAXIMUM NUMBER BYTES OF MAIN  STORAGE AVAILABLE
*                   TO THE LINKAGE EDITOR  (VALUE1)
*                   WHERE VALUE1=(DESIGN PT X 1024) TO 9999999
*                              OR (DP K) TO 9999K
*                 B) SIZE OF LOAD MODULE BUFFER (VALUE2)
*                   WHERE VALUE2= 6144 TO 102400
*                              OR 6K TO 100K
*
*                 THE FOLLOWING FORMATS ARE ACCEPTED:                 *
*                               SIZE=VALUE1                           *
*                                 SIZE=(VALUE1)                       *
*                                   SIZE=(,VALUE2)                    *
*                                     SIZE=(VALUE1,VALUE2)            *
*                                       SIZE=(,)                      *
*                 DEFAULT VALUES ARE SET DURING SYSTEM GENERATION     *
*                                                                     *
* 5.- ALIGN2 = OPTION SPECIFYING THAT 2K-PAGE BOUNDARY IS TO BE USED
*              WHERE PAGE ALIGNMENT IS REQUIRED. DEFAULT VALUE FOR
*              PAGE ALIGNMENT IS 4K.
*                                                                     *
*                                                                     *
*                                                                     *
*      THESE PARAMETERS MUST BE SPECIFIED IN CERTAIN VALID            *
*  COMBINATIONS (INDICATED IN TABLE) IN ORDER TO BE ACCEPTED FOR      *
*  ANY GIVEN LINKAGE EDITOR PROCESSING.                               *
*                                                                     *
* ** TABLE - COMBINATIONS OF  'INCOMPATIBLE' ATTRIBUTES & OPTIONS **  *
*---------------------------------------------------------------------*
*                                                                     *
* PARM. SPECIFIED  ***  COMBINATIONS - 'NOT ALLOWABLE ' WITH.         *
* ---------------        ------------------------------------         *
*                                                                     *
*   1.  SCTR       *    OVLY                                          *
*                                                                     *
*   2.  OVLY       *    SCTR,RENT, REUS, HIAR, REFR                   *
*                                                                     *
*   3.  HIAR       *    OVLY                                          *
*                                                                     *
*   4.  RENT       *    REUS, REFR, OVLY                              *
*                                                                     *
*   5.  REUS       *    RENT, REFR, OVLY                              *
*                                                                     *
*   7.  TEST       *    NE                                            *
*                                                                     *
*   6.  REFR       *    RENT, REUS, OVLY                              *
*                                                                     *
*   8.  LET        *    XCAL                                          *
*                                                                     *
*   9.  XCAL       *    LET                                           *
*                                                                     *
*   10. MAP        *    XREF, NE                                      *
*                                                                     *
*   11. XREF       *    MAP, NE                                       *
*                                                                     *
*   12. NE         *    MAP, XREF, TEST                               *
*                                                                     *
*   13. NCAL       *    -N/A-                                         *
*                                                                     *
*   14. LIST       *    -N/A-                                         *
*                                                                     *
*   15. OL         *    -N/A-                                         *
*                                                                     *
*   16. DC         *    -N/A-                                         *
*                                                                     *
*   17. SIZE       *    -N/A-                                         *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*  OUTPUT -                                                           *
*                                                                     *
*        MESSAGES ARE PRINTED - OUT, TO INFORM  THE USER OF  WHAT -   *
*  LINKAGE EDITOR OPTIONS & ATTRIBUTES WERE PROCESSED & AS TO         *
*  WHETHER OR NOT THEY ARE VALID & COMPATIBLE.                        *
*                                                                     *
* IN THE CASE OF THE NOPRINT OPTION THE ONLY FORM OF OUTPUT ON SYSPRINT
* THAT SHALL OCCUR IS, THE PRINTING OF ERROR MEASAGES.                *
*                                                                     *
*   EXTERNAL  ROUTINES -      NONE                                    *
*                                                                     *
*  EXITS - NORMAL -                                                   *
*                                                                     *
*        AT THE COMPLETION OF PROCESSING ALL OPTIONS/ATTRIBUTES,      *
*  RETURN CONTROL IS TRANSFERED BACK  TO THE INITIALIZER (THAT IS TO  *
*  THE NEXT SEQUENTIAL INSTRUCTION, THE POINT AT WHICH EXIT  WAS      *
*  MADE  FROM  ' HEWLFINT ' MODULE).                                  *
*                                                                     *
*                                                                     *
*                                                                     *
*  EXITS - ERROR -  NONE                                              *
*                                                                     *
*  TABLES/WORK AREAS -  N/A.                                          *
*                                                                     *
*  ATTRIBUTES -  N/A.                                                 *
*                                                                     *
*  NOTES -  N/A.                                                      *
*                                                                     *
*                                                                     *
***********************************************************************
*
*
HEWLFOPT CSECT
**********************************************************************
*397600,413600                                                   S01904
*184600-700,266100-200,3011-302312,423700,433100-400             Y01048
*417500                                                           M4507
*A189100-189300                                                 OY00338
*C416500,C422500                                                 YM5557
*185500                                                         XA05449
**********************************************************************
         SPACE 1
R0       EQU   0    PARAMATER  REGISTER
R1       EQU   1    PARAMATER  REGISTER
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12   BASE REGISTER
RD       EQU   13   SAVE AREA POINTER REGISTER
RE       EQU   14   RETURN REGISTER
RF       EQU   15   GO TO REGISTER
 EJECT
PARMREG  EQU   1
DISP8    EQU   8
BYTES8   EQU   8
CONCODE8 EQU   8
DISP4    EQU   4
DISP1    EQU   1
DISP2    EQU   2
BYTES1   EQU   1
BYTES4   EQU   4
BLANK    EQU   C' '
K        EQU   C'K'
COMMA    EQU   C','
LPAREN   EQU   C'('
RPAREN   EQU   C')'
NULL     EQU   X'00'
NOOVLY   EQU   X'BF'
EQUAL    EQU   C'='
SIZE     EQU   X'01'
LPRENRCV EQU   X'02'
RPRENRCV EQU   X'04'
VAL1RCVD EQU   X'08'   VALUE1 RECIEVED OR ACCEPTED FOR DEFAULT
GENOPTST EQU   X'10'     INDICATES A GENERATED DEFAULT ACTIVATED
USEDEF   EQU   X'20'   ERROR ENCOUNTERED WHICH NEGATES USER VALUES DEF.
KFORM    EQU   X'40'
INCOMPT  EQU   X'80'  INCOMPATABLE OPTIONS MESSAGE NECESSARY
NOXML    EQU   X'97' RST MASK TO TURN OFF XREF,MAP AND LIST FOR NOPRINT
SCNDTIME EQU   X'20'  MOTP ENTERED FOR THE SECOND TIME
RESET    EQU   X'F0'   RESET MASK FOR SIZE SWITCHES
NUMBER   EQU   X'F0'   MASK FOR CHECKING VALID NUMERICAL ZONES
RSTKFORM EQU   X'BF'   RESET MASK  FOR K FORM
HIAR     EQU   X'04'
NOHIAR   EQU   X'FB'
SCTR     EQU   X'04'
NOSCTR   EQU   X'FB'
RENT     EQU   X'10'
XREF     EQU   X'40'
TEST     EQU   X'10'
REFR     EQU   X'01'
CALL     EQU   X'7F'
NORNR    EQU   X'3F'
RENTREUS EQU   X'C0'
NOREFR   EQU   X'FE'
MAP      EQU   X'20'
NE       EQU   X'08'
NONE     EQU   X'F7'
NOMAP    EQU   X'DF'
NOXREF   EQU   X'BF'
NOLIST   EQU   X'F7'
NOTERM   EQU   X'FD'
NOTEST   EQU   X'EF'
DCBS     EQU   X'10'
TERM     EQU   X'02'
OL       EQU   X'08'
NCAL     EQU   X'80'
XCAL     EQU   X'04'
LET      EQU   X'10'
REUS     EQU   X'40'
LIST     EQU   X'08'
DC       EQU   X'7F'
OVLY     EQU   X'20'
NORENT   EQU   X'7F'
NOREUS   EQU   X'BF'
TABLEEND EQU   X'FF'
MULT1024 EQU   10
NEWPAGE  EQU   X'19'
 EJECT
         USING VRT,R6
         USING APTDSECT,R2
         USING DEFDSECT,R1
         USING *,15
         STM   RE,RD,SAVEA              SAVE REGISTERS
         DROP 15
         BALR  RC,R0
         USING *,RC
         BC    15,MOID+6
MOID     DC    C'01MOPT'
 EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  THIS IS THE OPTION SCANNING ROUTINE . IT WILL CHECK THE PARM.      *
*   FIELD FOR VALID OPTIONS. INVALID OPTIONS AND VALID OPTIONS WILL   *
*    BE PLACED IN THE PRINT BUFFER TO BE PRINTED UPON RETURN TO INT.  *
*    PARMREG HAS POINTER TO 1ST BYTE OF THE PARAMATER FIELD PASSED ON *
*    THE EXEC CARD.                                                   *
* R6 ALWAYS POINTS TO A WORK AREA                                     *
* R2 ALWAYS POINTS TO APT                                             *
* R8 WHILE SCANNING FOR PARAMATERS HAS THE ADR OF NXT AVAIL POS IN    *
* PARAMATER MESSAGE TO PLACE THE PARAMATER
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
  SPACE 2
         TM    APTSWS,SCNDTIME    SECOND TIME IN OPT
         BZ    OPT01
         L     R6,TXTIO           USE THE TXTIO TABLE FOR BUFFER
         MVI   NOTVLD,TABLEEND    DO NOT ACCEPT TABLE ALLOCATING PARMS.
         MVI   MESGNO1,NEWPAGE     ITERATIONS MUST BEGIN ON NEW PAGE
         B     OPT02
 SPACE 1
OPT01    LH    RA,0(0,PARMREG)     NO. OF BYTES PASSED IN THE LIST
         LA    RB,DISP2(0,PARMREG) ADDRESS OF THE FIRST BYTE
         SPACE 1
         GETMAIN R,LV=274         ACQUIRE DYNAMIC WORK SPACE
         LR    R6,R1              PRIME DSCT REG
         SPACE 1
OPT02    MVI   INTERN1,NULL       CLEAR THE SWITCHES
         XC    MSGONE(4),MSGONE
         XC    MSGTWO(4),MSGTWO
         XC    MSGTHREE(4),MSGTHREE
         LTR   RA,RA               IS LENGTH THAT WAS PASSED = 0
         BNP   NOTANY             NO PARAMATERS
         MVI   TRTBUF,NULL        CLEAR FIRST BYTE
         MVC   TRTBUF+BYTES1(255),TRTBUF CLR REST OF BUFFER
         SPACE 1
         MVI   TRTBUF+COMMA,COMMA MOVE COMMA TO COMMA DISPLACEMENT
         MVI   TRTBUF+EQUAL,EQUAL MOVE EQUAL TO EQUAL DISPLACEMENT
         MVI   TRTBUF+LPAREN,LPAREN MOVE IN LEFT PARENTHESIS
         MVI   TRTBUF+RPAREN,RPAREN MOVE IN RIGHT PARENTHESIS
         MVI   TRTBUF+BLANK,BLANK MOVE IN A BLANK(MAKES PARMS FREE-FORM
         MVC   OPTIONS(76),BLK1 RESET THE LINE
         MVC   GENDFLTS(20),BLK2    CLEAR THE LINE
         LA    R8,OPTIONS         ADDRESS OF PLACE OPTIONS AREA
         L     R1,DEFADR          ADDRESS OF DEF
SCANOPT  BCTR  RA,0                -1 FOR MACHINE LENGTH OF LIST
         LR    R0,R2               SAVE DSECT BASE
         EX    RA,SCAN             SCAN FOR = OR
         BNE   OPTFOUND            IF STOPPED CHECK FOR VALID OPTION
         LA    R9,DISP1(0,RA)     PRINE R 9 WITH TRUE LENGTH
         TM    INTERN1,APF        AM I SEARCHING                 Y01048
         BO    APFRTN60           FOR AUTHORIZATION CODE         Y01048
         TM    INTERN1,SIZE       WAS I IN THE SIZE SEARCH
         BO    OPT111             IF SO CONTINUE SIZE EXTRACTION
         LTR   R7,RA              TRANSFER THE LENGTH
         BNM   LASTSCN            IF MORE THAN 0 CONTINUE SCAN  XA05449
OPTEXIT  BCTR  R8,0                -1 TO GET ADDRESS OF LAST COMMA
         CLI   0(R8),RPAREN       STOPPED LAST BY A )
         BE    EXIT               IF SO DO NOT BLANK IT OUT
         MVI   0(R8),BLANK         BLANK OUT LAST COMMA
EXIT     EQU   *
         LA    R3,MESGNO1    POINT TO START OF TEXT             OY00338
         SR    R8,R3         CALC LENGTH OF TEXT                OY00338
         STC   R8,HEADRS     SAVE ACTUAL TEXT LENGTH            OY00338
 EJECT
*** TEST FOR ALLOWABLE OPTIONS
 SPACE 1
         TM    NEWSW,HIAR         IS HIAR OPTION ON
         BZ    OPT11B             NO PROCESS OTHER OPTIONS
         OI    PDSE7,SCTR         SET SCTR OPTION ON
OPT11B   TM    APT0,XREF          IS XREF OPTION ON
         BZ    OPT12              NO
         TM    APT0,MAP           IS MAP OPTION ON
         BZ    OPT13              BRANCH IF NOT
         NI    APT0,NOMAP         SET MAP OPTION OFF
         B     OPT13
OPT12    TM    APT0,MAP           IS THE MAP OPTION ON
         BZ    OPT14              NO
OPT13    TM    PDSE8,NE           IS NE OPTION ON
         BZ    OPT14              NO
         NI    PDSE8,NONE         SET NE OFF - WANTS MAP -
         OI    INTERN1,INCOMPT    INDICATE INCOMPATABLE OPTIONS
OPT14    TM    PDSE7,TEST         IS TEST ON
         BZ    OPT16              NO
         TM    PDSE8,NE                IS NE ON
         BZ    OPT16              NO CONTINUE
         NI    PDSE8,NONE         CLEAR NE INDICATOR
         OI    INTERN1,INCOMPT    INDICATE INCOMPATABLE OPTIONS
OPT16    TM    PDSE7,OVLY         IS OVERLAY OPTION ON
         BZ    OPT26              EXIT
         TM    PDSE7,RENTREUS     IS RENT OR REUS ON
         BNZ   OPT16B             YES -RESET OPTION -
         TM    PDSE8,REFR         IS REFR ON
         BZ    OPT16A             NO -TEST FOR SCTR-
OPT16B   NI    PDSE8,NOREFR       CLEAR REFR
         NI    PDSE7,NORNR        CLEAR REUS AND RENT
         OI    INTERN1,INCOMPT    INDICATE INCOMPATABLE OPTIONS
OPT16A   TM    PDSE7,SCTR         IS SCTR ON
         BZ    OPT26              EXIT
         NI    PDSE7,NOSCTR       CLEAR SCTR
         NI    NEWSW,NOHIAR       CLEAR HIAR
         OI    INTERN1,INCOMPT    INDICATE INCOMPATABLE OPTIONS
OPT26    EQU   *    ** END OF INCOMPATABLE OPTIONS SCAN **
 EJECT
***
*** THIS SECTION CHECKS IF USER SPECD VAL 1 OR 2
*** DEFAULTS VALUES NOT SPECD DEFAULTS BOTH IF CONFLICT WITH DESIGN PT.
**
*** R3 = VALUE 1  0 IF SPECIFIED INCORRECTLY OR IF USER WANTS DEFAULT
*** R4 = VALUE 2  0 IF SPECIFIED INCORRECTLY OR IF USER WANTS DEFAULT
         SPACE 2
         L     R1,DEFADR           ADDRESS OF HEWLFDEF
         MVC   IDENT(BYTES4),DEFNAME DESIGN NAME OF EDITOR
         LM    R3,R4,VALUE1       USER(IF SPECD) R3=VALUE1,R4=VALUE2
         LM    R9,RB,DEFDSNPT DEFAULTS R9=DSNPT,RA=DEFVAL1,RB=DEFVAL2
  SPACE 1
         TM    INTERN1,USEDEF     SIZE SPECD INCORRECTLY
         BO    OPT320             IF SO USE DEFAULTS
         LTR   R3,R3               DID USER SPECIFY A VALID VALUE
         BP    OPT300
         LR    R3,RA              GET DEFAULT VALUE 1
         ST    RA,VALUE1          USE DEFAULT VALUE 1
         OI    INTERN1,USEDEF     INDICATE DEFAULT(S) USED
         B     OPT330              CHECK VALUE2
         SPACE 1
OPT300   CR    R3,R9              IS VALUE 1 LT DSNPT
         BNL   OPT330              NO CHECK VALUE 2
OPT320   STM   RA,RB,VALUE1       USE DEFAULT VALUES 1 AND 2
         B     OPT380               DEF VALUES IN EXIT
OPT330   LTR   R4,R4              DID USER SPEC. VALUE 2
         BNP   OPT350              USER WANTS DEFAULT VALUE 2
         CR    R3,R4              IS VALUE 2 GT VALUE 1
         BL    OPT350              YES  ERROR
         C     R4,MINVAL2         VALUE 2 LT MINIMUM
         BL    OPT350              USE DEFAULT VALUE2
         C     R4,MAXVAL2         DOES VALUE 2 EXCEED MAXIMUM
         BNH   OPT390              VALUE 2 ACCEPTABLE
OPT350   ST    RB,VALUE2          USE DEFAULT VALUE 2
         SPACE 1
OPT380   OI    INTERN1,USEDEF      INDICATE VALUE DEFAULTED
 EJECT
***
*** THIS SECTION SETS UP A MESSAGE INDICATING THE VALUES USED AND
** WETHER THEY ARE DEFAULTS OR VARIABLE OPTIONS SPECIFIED BY THE USER,
***
 SPACE 2
OPT390   LA    R8,VALUES          ADDRESS OF MESSAGE 1 VALUES USED SLOT
         L     R1,VALUE1          QUANTITY VALUE1
         BAL   RE,OPT500          DELETE LEADING ZEROS - PLACE IN MSG
         MVI   0(R8),COMMA        INSERT THE COMMA SEPARATOR
         LA    R8,DISP1(0,R8)     BUMP POINTER TO NEXT BLANK
         L     R1,VALUE2          QUANTITY VALUE2
         BAL   RE,OPT500          DELETE LEADING ZEROS - PLACE IN MSG
         MVI   0(R8),RPAREN       INSERT THE R PAREN TERMINATOR
         TM    INTERN1,USEDEF          DEFAULTS USED?
         BZ    OPT510                  NO
         MVC   DEFAULTM+10(17),DEFS    YES - MOVE IN DEFAULT
OPT510   EQU   *
 SPACE 2
***
***      THIS SECTION SETS UP THE MESSAGES RETURNED TO INT
***   THIS SECTION DELETS HEADER MESSAGES WHEN THE NO PRINT OPTION
*** HAS BEEN CODED IN THE PARAMATER FIELD.
***
 SPACE 2
         LM    R3,R5,HEADRS       LN AND ADR OF MESSAGES 1 AND 2
         STM   R3,R4,MSGONE       PS TO INT THROUGH APT
OPT775   TM    INTERN1,INCOMPT    WERE THERE INCOMPATABLE OPTIONS
         BZ    OPT800             NO -DO NOT ISSUE MESSAGE -
         ST    R5,MSGTHREE        SAVE IN APT
OPT800   EQU   *
 EJECT
***
*** EPILOG
***
 SPACE 1
         TM    APTSWS,SCNDTIME    SECOND TIME IN OPT
         BO    OPT999             DO NOT ISSUE THE FREE MAIN
         FREEMAIN R,LV=274,A=(6) FREE DYNAMIC WORK SPACE
 SPACE 2
RETURN   LM    RE,RD,SAVEA
         BR    RE                       RETURN
    SPACE 1
*        THIS SECTION PRINTS THE HEADER MESSAGES AFTER A PARM CARD
*        HAS  BEEN ENCOUNTERED.  HEWLFINT PRINTS THE MESSAGES FROM
*        THE PARMS ON THE EXEC CARD.
*
OPT999   EQU   *
         L     RF,=V(HEWLEPNT)    ADDRESS OF THE PRINT ROUTINE
         LA    R3,MSGONE               GET ADDRESS OF LENGTH AND ADD.
*                                      OF FIRST MESSAGE
         LA    R5,MSGTHREE        PRIME END REG
         LA    R4,4               INDEX
OPT997   L     R8,HEWLCWBB+20     ADDRESS OF SYSPRINT BUFFER
         SR    R9,R9
         IC    R9,0(0,R3)              GET LENGTH OF THE MESSAGE
         LTR   R9,R9              IS IT ZERO
         BZ    OPT998             NO PRINT SKIP
         L     RB,0(0,R3)              PUT ADDRESS OF MESSAGE IN RB
         EX    R9,MVOPT2M         MOOE THE OPTION TO THE MESSAGE
         BALR  RE,RF              PRINT THE HEADER
OPT998   BXLE  R3,R4,OPT997
         B     RETURN
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*RB  IS USED TO HOLD THE LAST(STOPPED AT) ADDRESS IN THE PARM FIELD   *
*RA  IS USED TO HOLD THE NUMBER OF BYTES OF OPTIONS LEFT IN PARM FIELD*
* R9 IS USED TO HOLD THE TRUE LENGTH OF AN OPTION FOR ADDRESS UPDATEING
* R7 IS USED TO HOLD MACHINE LENGTH OF OPTION FOR EX INSTRUCTIONS.    *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 2
OPTFOUND STC   R2,FUNCBYTE         SAVE THE STOPPER BYTE.
         LR    R2,R0               RESTORE DSECT BASE.
         LR    R7,R1               TIC CURRENT(STOPPED AT) ADDRESS.
         SR    R7,RB               LAST ADR-CUR ADR = LENGTH OF OPTION
         LR    R9,R7              TRANSFERR LENGTH TO R9
         TM    INTERN1,APF        AM I SEARCHING                 Y01048
         BO    APFRTN             FOR AUTHORIZATION CODE         Y01048
         TM    INTERN1,SIZE  AM I LOOKING FOR SIZE AFT(SIZE=)
         BZ    CHECKOPT            IF NOT DETERMINE IF VALID OPTION.
         MVI   TRTBUF+EQUAL,NULL DELETE = FROM TRT TABLE
* R7 CONTAINS TRUE LENGTH NOT MACH LENGTH BECAUSE (, MEANS DEFAULT
* THAT VALUE WHERE AS LIST,,, MEANS IGNORE AND WOULD NOT CAUSE VAL1 OR
* VAL2 RCVD SWITCH SETTING
         SPACE 1
         TM    INTERN1,LPRENRCV    ( RECIEVED ?
         BZ    OPT101              NO
         CLI   FUNCBYTE,COMMA      STOPPED BY COMMA
         BE    OPT100              YES CHECK IF VAL1 RCVD YET
         CLI   FUNCBYTE,BLANK     STOPPED BY A BLANK
         BE    OPT107             YES SKIP FREE FORM
* AT THIS POINT HAD TO BE STOPPED BY A ) BECAUSE NO OTHERS ARE STOPPERS
         OI    INTERN1,RPRENRCV    SET ) RECIEVED
         TM    INTERN1,VAL1RCVD    VALUE 1 RECIEVED YET
         BO    OPT102              YES CHECK FOR VALUE 2 RECIEVED
         LA    RF,VALUE1          ADDRESS OF VALUE1
         BAL   RE,MOVEVAL          CHANGE RADIX OF VALUE POINTED BY RB
         OI    INTERN1,VAL1RCVD   INDICATE VALUE 1 R*CIEVED
         B     OPT103
         SPACE 1
OPT101   CLI   FUNCBYTE,LPAREN     STOPPED BY (
         BE    OPT104              YES CHECK LENGTH
         CLI   FUNCBYTE,COMMA      STOPPED BY ,
         BE    OPT105              YES
         CLI   FUNCBYTE,BLANK     STOPPED BY A BLANK
         BE    OPT112             YES SKIP FREE FORM
         CLI   FUNCBYTE,EQUAL     STOPPED BY EQUAL -FREE FORM -
         BE    OPT112             YES SKIP FREE FORM
         B     SIZEINVL            -ERROR- POSSIBLE *SIZE=)*
         SPACE 1
OPT104   OI    INTERN1,LPRENRCV   SET ( RECIEVED
         B     OPT112
         SPACE 1
OPT100   TM    INTERN1,VAL1RCVD    VALUE 1 RECIEVED YET
         BO    OPT102              YES CHECK IF VALUE 2 RECIEVED
         LA    RF,VALUE1          ADDRESS OF VALUE1
         BAL   RE,MOVEVAL          CONVERT FROM RADIX 10 TO RADIX 2
         OI    INTERN1,VAL1RCVD    INDICATE VALUE 1 RECIEVED
         B     GOBY                CONTINUE
         SPACE 1
OPT102   LA    RF,VALUE2          ADDRESS OF VALUE2
OPT106   BAL   RE,MOVEVAL         CONVERT FROM RADIX 10 TO RADIX 2
OPT103   NI    INTERN1,RESET       CLEAR SWITCHES USED FOR SIZE SRCH
         MVI   TRTBUF+EQUAL,EQUAL RESTORE = SIGN TO TRT TABLE
         B     GOBY                CONTINUE
 SPACE 1
OPT105   LA    RF,VALUE1          ADDRESS OF VALUE1
         B     OPT106             END OF SCAN -SIZE=100K-
OPT107   TM    INTERN1,VAL1RCVD   VALUE 1 RECIEVED YET
         BO    OPT108             YES
         LA    RF,VALUE1          ADDRESS OF VALUE1
         BAL   RE,MOVEVAL
         B     GOBY
 EJECT
OPT108   LA    RF,VALUE2          ADDRESS OF VALUE2
         BAL   RE,MOVEVAL
         B     GOBY
 SPACE 1
OPT111   LR    R7,R9              LAST SCAN R7=R9
         B     OPT100 CONTINUE
         SPACE 2
SIZEINVL OI    INTERN1,USEDEF      SIZE SPEC. INVALIDLY
* AT THIS POINT POSSIBLE TOGIVE MESSAGE
         B     INVALIDR
         SPACE 1
ZEROLN   LA    RB,DISP1(0,RB)      UPDATE LAST(STOPPED AT) ADDRESS.
         B     TSTCNT1
         EJECT
*                                                                Y01048
*        APFRTN-CHECK VALUE FOR AC=N, IF VALUE OKAY              Y01048
*               MOVE TO APT                                      Y01048
*                                                                Y01048
*        R1    WORK                                              Y01048
*        R2    BASE OF DSECT                                     Y01048
*        R4    WORK                                              Y01048
*        R5    WORK                                              Y01048
*        R7    MACHINE LENGTH OF VALUE                           Y01048
*        R9    REAL LENGTH OF VALUE                              Y01048
*        RA    NUM. OF BYTES LEFT IN PARM FIELD                  Y01048
*        RB    POINTS TO VALUE TO BE CONVERTED                   Y01048
APFRTN   DS    0H                                                Y01048
         LTR   R9,R7              LENGTH = 0?                    Y01048
         BNP   OPT112             YES - SKIP                     Y01048
         CLI   FUNCBYTE,COMMA     IS STOPPER A COMMA?            Y01048
         BE    APFRTN10           YES - OKAY                     Y01048
         CLI   FUNCBYTE,BLANK     IS STOPPER A BLANK?            Y01048
         BE    APFRTN10           YES - OKAY                     Y01048
         BCTR  R7,0               NO                             Y01048
         B     ACINVALD           ERROR                          Y01048
APFRTN10 EQU   *                                                 Y01048
         BCTR  R7,0               GET MACHINE LENGTH             Y01048
         LA    R3,8               MAX NUM DIGITS                 Y01048
         CR    R3,R9              ARE THERE TOO MANY?            Y01048
         BL    ACINVALD           YES - ERROR                    Y01048
         LA    R5,0(RB,R7)        LAST NUM IN STR                Y01048
         LR    R1,RB              FIRST NUM IN STR               Y01048
         LA    R4,1               INDEX                          Y01048
APFRTN30 EQU   *                                                 Y01048
         TM    0(R1),NUMBER       IS IT A DIGIT?                 Y01048
         BNO   ACINVALD           NO - ERROR                     Y01048
         BXLE  R1,R4,APFRTN30     SCAN ALL DIGITS                Y01048
         XC    WORK,WORK          CLEAR WORK                     Y01048
         EX    R7,PACK            PACK VALUE                     Y01048
         CVB   R1,WORK            CONVERT TO BINARY              Y01048
         LA    R4,K255            MAX ALLOWED                    Y01048
         CR    R1,R4              GREATER THAN MAX?              Y01048
         BH    ACINVALD           YES - ERROR                    Y01048
         STC   R1,APTAPFAC        SAVE APF CODE FOR BOTH         Y01048
         STC   R1,PDSAPFAC        REAL AND DEFAULT VALUES        Y01048
APFRTN50 NI    INTERN1,FF-APF     OFF INTERNAL SW                Y01048
         B     GOBY               MOVE TO PRINT                  Y01048
APFRTN60 LR    R7,R9              PRIME R7                       Y01048
         B     APFRTN10           LAST SCAN                      Y01048
ACINVALD NI    INTERN1,FF-APF     OFF INTERNAL SWITCH            Y01048
         B     INVALIDR           MESSAGE                        Y01048
         EJECT
CHECKOPT EQU   *                   ENTRY FOR FIRST TIME
         LTR   R9,R7               TRANSFER BACK TO AND TEST FOR 0 INR9
         BNP   OPT112             LENGTH OF OPTION 0 (,,OR==OR=,OR,=)
* ,( OR (( OR =( OR (= OR (, OR ), OR )= OR )) OR ,) OR ),
         BCTR  R7,0                -1 FOR MACHINE LENGTH
LASTSCN  EQU   *
         LA    R3,OPTFIELD        ADDRESS OF ACCEPTABLE OPTIONS
         SR    R4,R4              CLEAR LENGTH REGISTER
         SR    R5,R5              CLEAR INDEX REG
         SPACE 1
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
* DETERMINES IF OPTION IS A VALID SELECTION IF NOT INSERT INVALID& SKIP
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
CHKOPT   IC    R4,0(0,R3)     LENGTH OF THE OPTION
         CR    R4,R9              LN OF OPT IN LIST=LN OF OPT FOUND
         BNE   UPPTR              NO CHECK THE NEXT ENTRY
         EX    R7,COMPOPT         COMPARE THE TWO OPTIONS FOR =
         BE    OPT10              SET THE OPTION
         SPACE 1
UPPTR    LA    R3,1(R4,R3)        LN OF INST+LN BYTE(1)+LN OPT=NXT OPT
         LA    R5,DISP4(0,R5)     UP INDEX TO NEXT OPTION SETTER
         CLI   0(R3),TABLEEND     END OF ACCEPTABLE OPTIONS
         BNE   CHKOPT             CHECK THE NEXT OPTION IN THE LIST
* AT THIS POINT THE OPTION WAS NOT ONE OF THE ACCEPTABLE OPTIONS
 SPACE 1
INVALIDR MVC   0(7,R8),INVMSG     OPTION INVALID MOVE MSG TO BUFFER
         MVC   7(BYTES1,R8),FUNCBYTE MOVE STOPPER BYTE
         LA    R8,DISP8(0,R8)      UPDATE POINTER
         B     RSTPTR              SAVE   POINTER
OPT10    EX    R0,SETOPTS(R5)     SET OR RESET THE SWITCH(OPTION)
GOBY     EX    R7,MVOPT2M          MOVE THE OPTION TO HEADER MESSAGE
OPT112   EQU   *
         LA    R3,0(R8,R9)        LN OF OPT+MSG ADR=POS FOR STOPPER
         MVC   0(BYTES1,R3),FUNCBYTE MOVE IN THE STOPPER BYTE
         LTR   R9,R9              WAS THE LENGTH OF THE OPTION 0
         BNP   SHRT               IF SO SKIP THE ALTERNATE INS
         CLI   FUNCBYTE,BLANK     WAS THE STOPPER A BLANG
         BE    OPT113             IF SO DO NOT MOVE IT
         LA    R8,DISP2(R7,R8)     A(MSG)+MACH. LENGTH+2(,+1)=NEW PTR.
RSTPTR   LA    RB,DISP1(R9,RB)    UPDATE THE ADDRESS
TSTCNT1  SR    RA,R9               LENGTH LEFT - AMMOUNT PROCESSED.
TSTCNT2  LTR   RA,RA               IS LENGTH STILL >0
         BNP   OPTEXIT             TERMINATE  IF O OR -
         B     SCANOPT             CONTINUE
         SPACE 2
SHRT     CLI   0(R3),BLANK        STOPPED BY A BLANK CHARACTER
         BE    ZEROLN             DO NOT MOVE BLANKS TO MESSAGE
         LA    R8,DISP1(R7,R8)
         B     ZEROLN
  SPACE 1
OPT113   LA    R8,0(R9,R8)        DO NOT INCLUDE THE LENGTH OF THE BLK
         B     RSTPTR
 SPACE 1
         EJECT
***
*** UPON ENTRY R1 HAS THE VALUE TO BE CONVERTED
*** THIS SECTION DELETES LEADING ZEROES FORM NUMBERS TO PRINT
***
 SPACE 1
OPT500   LA    R4,7               MAX LENGTH OF THE VALUE
         CVD   R1,WORK            CONVERT TO DECIMAL NUMBER
         OI    WORK+7,X'0F'       CHANGE ZONE
         UNPK  WORK1,WORK+DISP4(BYTES4)   UNPACK FOR PRINTING
         LA    RB,WORK1+DISP1     ADDRESS OF UNPACKED VALUE
OPT508   CLI   0(RB),C'0'         IS THIS A LEADING ZERO
         BNE   OPT512             DON'T DELETE
         LA    RB,1(0,RB)         CHECK THE NEXT ONE
         BCT   R4,OPT508          CONTINUE DLETING LEADING ZEROS
OPT512   LA    R4,WORK1+BYTES8     LAST BYTE OF VALUE IN WORK
         SR    R4,RB              AM NUL-LN OF VAL=NO VLD NON-ZERO
         BCTR  R4,0               - 1 FOR THE EXECUTE LENGTH
         EX    R4,MVOPT2M         MOVE THE VALUE TO THE MESSAGE
         LA    R8,DISP1(R4,R8) MC LN OF VAL+1ST ADR=ADR OF NXT BLK
         BR    RE                 RETURN
 EJECT
***
***  UPON ENTRY INTO THIS ROUTINE (WHICH CONVERTS DECIMAL NUMBER IN K O
***  EXPLICIT FORMAT TO A HEXADECIMAL NUMBER)
***  R1  UTILITY
***  R2  BASE OF DSECT
***  R3  UTILITY
***  R4  UTILITY
***  R5  POINTS TO THE LOCATION TO PLACE IT IN
***  R7  MACHINE LENGTH OF VALUE   R9-1
***  R9 LENGTH OF VALUE (TRUE)
***  RA  # BYTES LEFT IN PARM FIELD
***  RB  POINTS TO THE VALUE TO BE CONVERTED
***  RE HOLDS THE RETURN ADDRESS
         SPACE 2
MOVEVAL  LTR   R4,R9               LN OF VALUE 0 -USE DEFAULT-
         BCR   CONCODE8,RE         YES -RETURN USER DEFAULTS THIS VALUE
         BCTR  R7,0               LN OF OPT-1 = MACH LN
         LA    R1,0(RB,R7) MACH.LN+A(VAL)=A(LAST BYTE OF VAL)*R7=R9-1*
         CLI   0(R1),K             IS VALUE IMPLICIT  K
         BNE   EXPLICIT
         OI    INTERN1,KFORM       INDICATE IMPLICIT VALUE
         TM    INTERN1,VAL1RCVD   VALUE 1 RECIEVED YET
         BZ    OPT600             YES CHECK LENTH
         LA    R3,4                MAX.NO.CHARS. FOR IMPLICIT VALUE
OPT633   BCTR  R4,0               -1 FROM CHAR CNT, TO NOT CHECK K
         BCTR  R7,0       -1 FROM CHAR CNT FOR K
OPT750   CR    R3,R9              CHARACTER COUNT OF VALUE TOO BIG
         BL    SIZEINVL
CHKZONES BCTR  R4,0
         LA    R5,0(RB,R4)         LAST NUMBER IN STRING
         LR    R1,RB              STARTING ADDRESS OF VALUE
         LA    R4,1               INDEX
NUMCK    TM    0(R1),NUMBER        ARE THE ZONES VALID
         BNO   SIZEINVL            ZONES INVALID
         BXLE  R1,R4,NUMCK        CONTINUE CHECKING ZONES
         XC    WORK,WORK          CLEAR WORK
         EX   R7,PACK      PACK VALUE
         CVB   R1,WORK             CONVERT TO RADIX 2
         TM    INTERN1,KFORM       WAS VALUE IMPLICIT
         BZ    STOREVAL            NO
         NI    INTERN1,RSTKFORM    RESET SW
         SLL   R1,MULT1024        MULT BY 1024 MAKE EXPLICIT
         LA    R7,1(R7)   ADD 1 TO IMPL. CHAR CNT FOR MACH LN
STOREVAL ST    R1,0(0,RF)         RECORD THE VALUE
         BR    RE
         SPACE 1
EXPLICIT TM    INTERN1,VAL1RCVD   VALUE 1 RECIEVED YET
         BO    OPT700             YES CHECK VALUE 2 RECIEVED
         LA    R3,6                MAX NO CHARS FOR EXPLICIT FORM
         B     OPT750
         SPACE 2
OPT600   LA    R3,5                MAX.NO.CHARS. FOR IMPLICIT VALUE
         B     OPT633
         SPACE 2
OPT700   LA    R3,7                MAX NO CHARS FOR EXPLICIT FORM
         B     OPT750
         SPACE 1
NOTANY   MVC   OPTIONS+2(BYTES4),NONEM  NO OPTIONS SPECIFIED
         B     EXIT
         SPACE 1
***   THIS SECTION ISOLATES THOSE  OPTIONS SET ON BY GENERATED
***   DEFAULTS
***
 SPACE 1
OPT950   NC    SAVATS(BYTES1),0(R3) ISOLATE OPTIONC
         BR    RE
 SPACE 1
MVOPT2M  MVC   0(0,R8),0(RB)     MOVE OPTION TO HEADER MESSAGE
COMPOPT  CLC   0(0,RB),1(R3) COMPARE FOR VALID OPTION
SCAN     TRT   0(0,RB),TRTBUF      TRT THROUGH THE PARM FIELD
PACK     PACK  WORK,0(0,RB)  PACK SIZE FOR RADIX CONVERSION
TSWITCH  TM    SAVATS,0 TEST THE ISOLATED OPTIONS
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
* THESE ARE THE ALLOWABLE OPTIONS.                                    *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 2
OPTFIELD EQU   *                  ** LINKAGE EDITOR OPTIONS **
 SPACE 1
GENLST2  EQU   *
         DC    X'04',C'XREF'      CROSS REFERENCE LISTING
         DC    X'03',C'MAP'       A MAP OF THE OUTPUT MODULE
         DC    X'04',C'LIST'      LIST THE CONTROL CARDS
GENLST1  EQU   *
         DC    X'04',C'RENT'      MODULE IS RE-ENTRANT
         DC    X'04',C'REUS'      MODULE IS SERIALLY RE-USEABLE
         DC    X'04',C'TEST'      MARK MODULE TEST
         DC    X'03',C'LET'       DO NOT MARK NOT EXECUTABLE FOR ERRORS
         DC    X'04',C'XCAL'      IGNORE EXCLUSIVE CALLS IN OVERLAY
         DC    X'04',C'NCAL'      NO AUTOMALIC CALL LIBRARY
         DC    X'02',C'OL'        MARK -FETCH ONLY BY LOAD MACRO-
         DC    X'02',C'NE'        NOT-EDITABLE- NO CESD PRODUCED
         DC    X'02',C'DC'        1K RECORDS ON SYSLMOD FOR E'15-18-
         DC    X'04',C'REFR'      MATK -REFETCH AT ANY POINT- ER RECVY
         DC    X'04',C'TERM'      SUPPORT TERMINAL IN OS/TS
         DC    X'04',C'CALL'      USE SYSLIB FOR UNRES ERS
***
*** NEGATIVE OPTIONS SUPPORT
***
         DC    X'06',C'NOTEST'    DO NOT MARK IN TEST
         DC    X'06',C'NOTERM'    NO TERMINAL SUPPORT
         DC    X'06',C'NOLIST'    DO NOT LIST CONTROL CARDS
         DC    X'06',C'NOXREF'    NO CROSS-REFERENCE LISTING
         DC    X'05',C'NOMAP'     NO MAP OF LOAD MODULE
         DC    X'06',C'NORENT'    MODULE NOT REENTRABLE
         DC    X'06',C'NOREUS'    MODULE NOT REUSEABLE
         DC    X'06',C'NOCALL'    SAME AS NCAL
NOTVLD   EQU   *
         DC    X'04',C'HIAR'      NOTE STORAGE HIARCHIES
         DC    X'04',C'SIZE'      USE FOLLOWING SIZES FOR EDIT RUN
         DC    X'04',C'DCBS'      USE DDCARD DCBBLKSIZE IF CAN
         DC    X'04',C'OVLY'      MODULE IS AN OVERLAY STRUCTURE
         DC    X'04',C'SCTR'      MARK MODULE BLOCK OR SCATTER LOAD
         DC    X'06',C'ALIGN2'    ALIGN ON 2K PAGE BOUNDARY      S01904
         DC    X'02',C'AC'        AUTHORIZATION CODE             Y01048
         DC    X'FF'              *** END OF OPTIONS ***
  EJECT
SETOPTS  DS    0H
 SPACE 1
         OI    APT0,XREF          XREF OPTION
         OI    APT0,MAP           MAP OPTION
         OI    APT0,LIST          LIST OPTION
         OI    PDSE7,RENTREUS     RENT, AND REUS
         OI    PDSE7,REUS         REUSABLE REUS OPTION
         OI    PDSE7,TEST         TEST OPTION
         OI    APT0,LET           LET OPTION
         OI    APT0,XCAL          XCAL - EXCLUSIVE CALLS- OPTION
         OI    APT0,NCAL          NCAL - NO CALL- OPTION
         OI    PDSE7,OL            OL ONLY LOADABLE OPTION
         OI    PDSE8,NE           NOT EDITABLE -NE- OPTION
         NI    PDSE8,DC           DC OPTION
         OI    PDSE8,REFR         MARK REFRESHABLE
         OI    NEWSW,TERM         INDICATE TERMINAL OUTPUT
         NI    APT0,CALL          PERFORM AUTOCALL FOR UNRES - ER
         NI    PDSE7,NOTEST       RESET TEST ATTRIBUTE
         NI    NEWSW,NOTERM       NO TERMINAL OUTPUT
         NI    APT0,NOLIST        NO LIST
         NI    APT0,NOXREF        NO XREF
         NI    APT0,NOMAP         NO MAP
         NI    PDSE7,NORENT       INDICATE NOT-REENTRANT
         NI    PDSE7,NOREUS       INDICATE NOT REUSEABLE
         OI    APT0,NCAL          NO AUTO CALL
         OI    NEWSW,HIAR         HIARCHY OPTION
         OI    INTERN1,SIZE       SIZEOPTION
         OI    APTSWS,DCBS        DCBS OPTION
         OI    PDSE7,OVLY         OVLY OPTION
         OI    PDSE7,SCTR         SCTR OPTION ON
         OI    APT4,ALIGN2        SET SWITCH FOR ALIGN2 OPTION   S01904
         OI    INTERN1,APF        AUTHORIZATION CODE             Y01048
 EJECT
         DS    0F
GENSET1  DC    AL4(2,GENLST1,GENMASK1,3)
GENSET2  DC    AL4(GENLST2,GENMASK2,3)
DEFADR   DC    V(HEWLFDEF)
HEADRS   DC    AL1(152),AL3(MESGNO1) LENGTH & ADD OF MESSAGE 1   YM5557
         DC    AL1(59),AL3(MESGNO2)    LENGTH AND ADDR OF MSG 2
         DC    AL1(029),AL3(MESGNO3) LENGTH AND ADDRESS OF MSG 3  M4507
MINVAL2  DC    A(6*1024) MINIMUM VALUE 2
MAXVAL2  DC    A(100*1024)
GENMASK1 DC    AL1(RENT,REUS,TEST)
GENMASK2 DC    AL1(XREF,MAP,LIST)
    SPACE 1
INVMSG   DC    CL7'INVALID'
MESGNO1  DC    X'09'
IDENT    DC    C'XXXX-LEVEL LINKAGE EDITOR OPTIONS SPECIFIED'
BLK1     DC    CL1' '  BLANK TO RESET THE LINE
OPTIONS  DC    27CL4' '                                          YM5557
MESGNO2  DC    X'09'
DEFAULTM DC    CL11' '                 LEADING BLANKS
         DC    C'VARIABLE OPTIONS USED -  SIZE=('
VALUES   DC    CL16' '
BLK2     DC    CL1' '  BLANK TO CLEAR LINE
GENDFLTS DC    8CL4' '
MESGNO3  DC    X'09'
         DC    C'   ** OPTIONS INCOMPATIBLE **'
DEFS     DC    CL17'DEFAULT OPTION(S)'
NONEM    DC    C'NONE'
         DS    0F
SAVEA    DS    16F  SAVE AREA
         DS    20F   PROVIDE 20 FULL WORDS OF MAINTENANCE AREA *******
     EJECT
VRT      DSECT
         DS    0D
WORK     DC    1D'0'
WORK1    DC    1D'0'
TRTBUF   DC    256XL1'0'
FUNCBYTE DC XL1'0'
INTERN1 DC XL1'0'
APF      EQU   X'10'              AUTH CODE BEING HANDLED        Y01048
K255     EQU   255                AUTH CODE MAX                  Y01048
FF       EQU   X'FF'                                             Y01048
X01      EQU   X'01'                                             Y01048
APTDSECT HEWAPT
 EJECT
DEFDSECT DSECT
DEFID    DS    CL4 MODULE IDENTIFIER
DEFDSNPT DS    F   DESIGN POINT
DEFVAL1  DS    F   DEFAULT VALUE 1
DEFVAL2  DS    F   DEFAULT VALUE 2
DEFNAME  DS    CL4 EDITOR NAME F44,F88,F128,F256
         END
