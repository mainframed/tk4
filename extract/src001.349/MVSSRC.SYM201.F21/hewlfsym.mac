         TITLE    'HEWLFSYM  SYM RECORD PROCESSOR'
***********************************************************************
*
*STATUS: CHANGE LEVEL
*
*FUNCTION/OPERATION:
*   PASS SYMS AND ESDS WHEN THE LOAD MODULE OUTPUT OF THE LINKAGE
*   EDITOR HAS THE ATTRIBUTE - TEST. SYM CARDS AND ESDS CARDS FROM AN
*   OBJECT MODULE ARE PACKED 3 CARDS PER RECORD (240 BYTES),EXCEPT FOR
*   THE LAST RECORD PER OBJECT MODULE WHICH MAY BE EITHER 80,160 OR
*   240 BYTES. SYM RECORDS FROM LOAD MODULES ARE PASSED IN THE SAME
*   FORM AS THEY ARE READ IN. IF A LOAD MODULE (AS PART OF INPUT) DOES
*   NOT CONTAIN SYM RECORDS THEN THE ESD RECORDS ARE PASSED. ALL SYM
*   RECORDS CONSIST OF A 4 BYTE HEADER PRECEEDING THE SYM/ESD DATA. THE
*   FIRST BYTE IS A 40(HEX),THE SECOND BYTE WILL BE EITHER A (1XXXXXXX)
*   WHICH MEANS THAT THIS SYM RECORD CONTAINS ESDS FROM A LOAD MODULE
*   THAT WAS NOT IN TEST WHEN IT WAS ORIGINALLY LINK-EDITED; OR IT MAY
*   BE A (0XXXXXXX) WHICH IS THE OPPOSITE OF (1XXXXXXX). BYTES 3 AND 4
*   WILL CONTAIN THE DATA BYTE COUNT IN THIS SYM RECORD.
*
*ENTRY POINTS:
*        HEWLFSYM - CONTROL SECTION NAME                              *
*
*INPUT:
*    SYM/ESD CARDS OR SYM/ESD RECORDS
*
*OUTPUT:
*   SYM RECORDS
*
*EXTERNAL ROUTINES:
*    N/A
*
*EXITS:
*    NORMAL:
*    RETURNS VIA ADDRESS PASSED IN REGISTER 14
*    ERRORS:
*    N/A
*
*TABLES/WORK AREAS:
*    LOAD MODULE BUFFER
*    ALL PURPOSE TABLE
*
*    REGISTER INPUT
*    REGISTER  2 - ADDRESS OF ALL PURPOSE TABLE
*    REGISTER  4 - BYTE CT = 80 OR 0; 0 MEANS PURGE SYM BUFFER
*    REGISTER  6 - ADDRESS OF SYM/ESD CARD TO BE MOVED TO BUFFER
*    REGISTER 13 - ADDRESS OF A REGISTER SAVE AREA (16 WORDS)
*    REGISTER 14 - RETURN ADDRESS
*
*ATTRIBUTES:
*   REUSABLE
*NOTES:
*    HEWLCSYM  ASSUMES 2 BITS WILL BE INITIALIZED TO 0 AT THE END OF
*    EACH INPUT MODULE, THEY ARE USED FOR INITIALIZATION .
*    1) USED TO INDICATE INITIALIZATION OF BUFER BYTE COUNT (1 = INIT)
*    2) NO SYM RECORDS HAVE BEEN PROCESSD IN THIS MODULE (1= SYMS SEEN)
***********************************************************************
***********************************************************************
*                                                                     *
*                   SYM  PROCESSOR -  COMMON TO E AND F               *
*                                                                     *
***********************************************************************
***********************************************************************
*        REGISTER ASSIGNMENT                                          *
***********************************************************************
APT      EQU   2                   ADDRESS OF ALL PURPOSE TABLE
BUFFCT   EQU   3
INCT     EQU   4                   INPUT PARM - NO BYTES OF SYM (80)
BUFFADD  EQU   5                   ADDRESS OF LOAD MODULE BUFFER
INADD    EQU   6                   INPUT PARM - ADDRESS OF SYMS TO MVE
TEMP1    EQU   7                   TEMPORARY REG
BASE     EQU   12                  BASE REGISTER
TEMP2    EQU   13                  13 - ADD OF SYSLMOD DCB
***********************************************************************
*        ALL PURPOSE TABLE DISPLACEMENTS                              *
***********************************************************************
DISP     EQU   0                       DISPO WHERE TO PUT REGS REL 13
INIT     EQU   X'04'               BIT TEST - FOR FIRST TIME ENTER
LOADMOD  EQU   X'20'                    LOAD MODULE BIT
SYMCTL   EQU   X'40'               CTL IND. FOR HEADER OF SYM RCD
SYMLDMOD EQU   X'20'                   SYM RECORD IN LOAD MOD
SYMPRES  EQU   X'04'                    SYM PRESENT BIT
HEWLFSYM CSECT
         EXTRN HEWLCMDB
*
*        SAVE ALL REGISTERS AND INITIALIZE BASE (REGISTER 12)
         USING *,15
         BC    15,MODID+6
MODID   DC     C'00MSYM'
*
         STM   14,12,DISP(13)          SAVE ALL REGISTERS
         USING SYM00100,BASE
         BALR  BASE,0                   INITIALIZE BASE REGISTER
         USING APTDSECT,APT        ESTABLISH BASE REG FOR APT DSECT
*
*        INITIALIZE
*
         DROP   15
SYM00100 ST    13,SAVERG13             SAVE ADDRESS OF CALLERS AREA
         L     BUFFADD,RLDINPAD    RLD INPUT BUFFER ADDRESS
         OI    PDSE8,SYMPRES       SET SYM PRESENT IN LD MD
*
*
         TM    APT1,LOADMOD        IS THIS A LOAD MODULE
         BC    8,SYM00600               NO
*
*
         CLI   0(BUFFADD),SYMCTL        IS THIS A SYM RECORD
         BC    7,SYM00700               NO / IT IS AN ESD -
*
*
         OI    APT2,SYMLDMOD       SET IND = ON / SYM IN LD MODULE
SYM00200 LH    BUFFCT,2(BUFFADD)        PICK UP LENGTH OF THIS DATA
*
*
         LTR   INCT,INCT                IS BUFFER TO BE PURGED
         BC    2,SYM00800              NO/OBJECT MOD - CK COUNT = 240
*
*        INITIALIZE FOR WRITE ,CHECK ROUTINE
*
SYM00300 LA    BUFFCT,4(BUFFCT)         INC WRITE CT 4 - FOR HEADER
         MVI   0(BUFFADD),SYMCTL        PUT HEADER ON DATA / SYM RCD
         L     TEMP1,DCBLMOD
         LA    TEMP2,REGSA         ADD TO SAVE REGISTER AREA
*
*        WRITE SYM RECORD ON OUTPUT FILE
*
         WRITE SYMDECB,SF,(TEMP1),(BUFFADD),(BUFFCT)
*
*        CHECK THE WRITE
*
         CHECK SYMDECB
*
*
SYM00400 SR    BUFFCT,BUFFCT           ZERO BUFFER BYTE CT
         OI    APT2,INIT           SET INDICATOR - ENTERED FOR THIS MOD
*
*
         LTR   INCT,INCT                IS IN BYTE CT = 0
         BC    2,SYM00900               NO / GET DATA LENGTH AND CONT.
*
*        RESTORE REGISTERS AND RETURN TO CALLER
*
SYM00500 L     13,SAVERG13             RESTORE ADD OF CALLERS SAVEAREA
         LM    14,12,DISP(13)          RESTORE CALLERS REGISTERS
         BCR   15,14                    RETURN TO CALLER
*
*
SYM00600 TM    APT2,INIT           1ST TIME ENTERED IN OBJECT MOD
         BC    8,SYM00400              YES / GO TO RESTORE CT TO = 0
         MVI   1(BUFFADD),X'00'         SET NO ESD FROM LOAD MOD(NOT IN
*                                      TEST) IN THIS RECORD
         BC    15,SYM00200              NO  / GO GET LENGTH OF DATA
*
*
SYM00700 TM    APT2,SYMLDMOD       HAVE SYM BEEN SEEN YET (INLDMD)
         BC    1,SYM00500               YES / DONOT PASS ESDS/DONE B 4
         LA    BUFFADD,4(BUFFADD)       INC BUFF ADD TO START OF DATA 4
         MVI        1(BUFFADD),X'80'    SET ESDS FROM LOAD MODULE  (NOT
*                                      IN TEST) IN THIS RECORD
         BC    15,SYM00200             NO / PROCESS ESDS AS SYMS
*
*
SYM00800 LA    TEMP1,240                240- MAX NO.SYM BYTES IN BUFFER
         CLR   BUFFCT,TEMP1             IS BUFFER FULL
         BC    8,SYM00300               YES / GO TO PURGE BUFFER
*
*        MOVE SYM/ESD TO BUFFER - UPDATE BUFFER BYTE CT - GO TO RETURN
*
SYM00900 AR    INCT,BUFFCT             INC CT BY 80
         STH   INCT,2(BUFFADD)         RESTORE BUFFER CT
         AR    BUFFADD,BUFFCT           ADDRESS OF WHERE TO PUT SYMS
         MVC   4(80,BUFFADD),0(INADD)   MOVE SYMS TO LOAD MOD BUFFER
         BC    15,SYM00500              GO TO RESTORE REGS
SAVERG13 DS    F                       SAVE CALLERS REG 13
DCBLMOD  DC    A(HEWLCMDB)
         HEWAPT
*
*
***********************************************************************
         END
