         TITLE  'IEBVMS - IEBCOPY MESSAGE HANDLING ROUTINE '
IEBVMS   CSECT
*C840000-842000                                                  A36047
*C835000-837000                                                  A36049
*C418000                                                         A41780
*C618000,956000-962000                                           A38720
* 618000,716000-718000,800000-804000,856000-864000,              X010XX
*D102000,150000                                                  A44144
*A100500-103600,148500-151500,040500-041600                      A44144
* 878000-880000,892000-896000,908000-924000,964000               X010XX
*C300000,3100000                                               @YA02613
*A312500                                                       @YA02613
*A950500                                                       @ZA04370
*C950000                                                       @ZA04370
***********************************************************************
GR0      EQU   0
GR1      EQU   1
GR2      EQU   2
GR3      EQU   3
GR4      EQU   4
GR5      EQU   5
GR6      EQU   6
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10                 BASE REGISTER 1
GR11     EQU   11                 BASE REGISTER 2
GR12     EQU   12
GR13     EQU   13
GR14     EQU   14
GR15     EQU   15
TRIPLESP EQU   C'-'
SKPSPACE EQU   C' '
C0       EQU   C'0'               EQUATES                        A44144
C1       EQU   C'1'                 TO SET UP                    A44144
C2       EQU   C'2'                   RETURN CODE                A44144
C9       EQU   C'9'                     IN EOJ MSG               A44144
FF       EQU   X'FF'
FIFTYSIX EQU   56
BLNK     EQU   C' '               BLANK CHARACTER
BUFL     EQU   120                LENGTH (MINUS 1) OF MSG BUFFER
CCLEN    EQU   80                 LENGTH OF CONTROL CARD
CHAR0    EQU   C'0'               FOR SETTING PAGE NBR ZONE BITS
COFF     EQU   20                 OFFSET IN BFR FOR CTL CD IMAGE
IEBMNO   EQU   4                  OFFSET INTO 'IEB1  I' FOR MSG NBR
LFT2     EQU   2                  FOR USE WITH 'SLL' INSTRUCTION
LINE1    EQU   1                  FOR ADDING 1 TO LINE COUNT
LINES3   EQU   3                  FOR ADDING 3 TO LINE COUNT
LNKBK    EQU   4                  FOR STD LINKAGE USE
LNKTHS   EQU   8                  FOR STD LINKAGE USE
MSGK     EQU   3                  CONSTANT LENGTH OF 3 BYTES WHICH BE-
*                                 GIN EACH MESSAGE 'DC'
M2       EQU   2                  LENGTH OF EACH MSG LIST ENTRY
NAMLEN   EQU   8                  LENGTH OF NAME
NBRLEN   EQU   7                  LENGTH OF NUMBER AND OF 'IEB1  I' CON
NLEN     EQU   2                  LENGTH OF MSG NBR PUT INTO 'IEB1  I'
OFF172I  EQU   12                 OFFSET INTO MSG IEB172I FOR NAME
PGOFF    EQU   116                OFFSET INTO PAGE HEADER FOR PAGE NBR
PNLEN    EQU   4                  LENGTH OF PAGE NUMBER
RCOFF    EQU   25                 OFFSET IN EOJ MSG (WTO) FOR RET CODE
RCOFFP   EQU   16                 OFFSET IN EOJ MSG (NOT WTO) FOR R.C.
SYN1     EQU   62                 LENGTH OF 1ST PART OF SYNADAF MSG
SYN2     EQU   58                 LENGTH OF 2ND PART OF SYNADAF MSG
TXTOFF   EQU   9                  OFFSET IN BFR FOR MSG TEXT
VALOFF   EQU   3                  OFFSET INTO VALIDATION ERR MSG
ZRO      EQU   0                  CONSTANT OF ZERO
L1       EQU   1                  LENGTH OF 1
L3       EQU   3                  LENGTH OF 3
L8       EQU   8                  LENGTH OF 8
P1       EQU   1                  TO BE ADDED TO SOMETHING
P4       EQU   4                  TO ADD 4 TO SOMETHING
X0       EQU   0                  OFFSET OF ZERO
X1       EQU   1                  OFFSET OF ONE
LSYN1    EQU   SYN1+P1
         EJECT
         SAVE  (14,12),,*
         LR    GR10,GR15          ADDRESSIBILITY
         USING IEBVMS,GR10
         USING IEBMCA,4
         LA    GR12,SAVE
         ST    GR12,LNKTHS(GR13)  STD LINKAGE
         ST    GR13,LNKBK(GR12)   ADDR OF CALLER'S SAVE AREA
         LR    GR13,GR12
         LA    GR2,MSGLIST
         TM    IOEF2,SPRNOPN      I/O ERROR ON SYSPRINT
         BZ    PRNTMSG            NO - PROCEED TO WRITE MSG
         TM    X0(GR2),IOERF      IS I/O ERR MSG IN BUFFER ALREADY
         BO    WTOS               ISSUE SYNADAF MSG AS WTO
         CLI   X1(GR2),BADPRINT   IS SYSPRINT BAD - WTO TO BE ISSUED
         BNE   TESTM53            NO - SEE IF 'END OF JOB' WTO TO
*                                 BE WRITTEN
         L     GR9,ATXT           ADDR OF MESSAGE TEXT CSECT
         USING TXTCSECT,GR9
         MVC   IEB172I+OFF172I(NAMLEN),NAMEFLD PUT NAME INTO WTO TEXT
*                                 MOVE NAME OF SYSPRINT DD WHICH
*                                 COULD NOT BE OPENED INTO
*                                 WTOLFBAD WTO
         LA    GR1,IEB172I        POINT TO WTO MESSAGE
         WTO   MF=(E,(1))
TESTM53  CLI   X1(GR2),M53        IS 'END OF JOB' WTO TO BE ISSUED NOW
         BNE   RETURN             IF NOT, RETURN TO CALLER NOW
         LA    GR1,WTOLF5         ADDRESS OF LIST FORM
*                                 'END OF JOB' WTO
         CLI   RCBUF,C9           IS RETURN CODE 10 OR HIGHER    A44144
         BH    WTORC12            YES, MOVE IN 12                A44144
         MVI   RCOFF-P1(GR1),C0   NO, MOVE IN 0X                 A44144
         MVC   RCOFF(L1,GR1),RCBUF  MOVE IN RETURN CODE          A44144
         B     GOWTO              AND WRITE EOJ WTO MSG          A44144
WTORC12  EQU   *                                                 A44144
         MVI   RCOFF-P1(GR1),C1   MOVE IN 10                     A44144
         MVI   RCOFF(GR1),C2      MOVE IN 2                      A44144
GOWTO    EQU   *                                                 A44144
         WTO   MF=(E,(1))         EXECUTE 'END OF JOB' WTO
         B     RETURN             NOW EXIT TO CALLER
PRNTMSG  EQU   *
         SR    GR3,GR3
         IC    GR3,LINECT
         LTR   GR3,GR3            DOES LINE-COUNT NOW = 0
         BZ    PRNTHEAD           IF SO, GO PRINT PAGE HEADER
RDYWRT1  EQU   *
         MVI   MSGBUF,SKPSPACE
RDYWRTHD TM    X0(GR2),IOERF      IS THERE A MSG IN THE BUFFER
         BO    WRTMSG             IF SO, GO WRITE IT
RDYWRT2  EQU   *
         LA    GR6,MSGBUF+P1
         TM    X0(GR2),CTLCD      IS A CONTROL STMT TO BE PRINTED
         BO    ADCC               IF ON, GO PRINT THE CONTROL STMT
         SR    GR11,GR11
         IC    GR11,X1(GR2)       GET MESSAGE CODE
         SLL   GR11,LFT2          MULTIPLY MSG CODE NUMBER BY 4 TO
*                                 USE IT AS AN INDEX TO THE TABLE OF
*                                 POINTERS TO MESSAGE TEXT
         L     GR5,MSGTAB(GR11)   OBTAIN THE POINTER TO THE MESSAGE TXT
         TM    X0(GR2),RCODE      IS A RETURN CODE TO BE PUT INTO MSG
         BZ    ANYPARM            NO- SEE IF ANY PARAMETERS
         CLI   RCBUF,C9           IS RETURN CODE 10 OR HIGHER    A44144
         BH    WRTRC12            YES, MOVE IN 12                A44144
         MVI   RCOFFP-P1(GR5),C0  NO, MOVE IN 0X                 A44144
         MVC   RCOFFP(L1,GR5),RCBUF MOVE IN RETURN CODE          A44144
         B     ANYPARM            AND WRITE EOJ MSG              A44144
WRTRC12  EQU   *                                                 A44144
         MVI   RCOFFP-P1(GR5),C1  MOVE IN 10                     A44144
         MVI   RCOFFP(GR5),C2     MOVE IN 2                      A44144
ANYPARM  EQU   *
         TM    X0(GR2),PBIT       ARE THERE ANY PARAMETERS
         BZ    NOPARAM            NO - CONTINUE PROCESSING
         TM    PARAMS,NAME        IS THERE A NAME PARAMETER
         BZ    NBRPARM            NO - IS THERE A NBR PARAMETER
         SR    GR8,GR8
         IC    GR8,NAMEDISP       DISPLACEMENT OF NAME FROM BEGINING
*                                 OF MSG IS IN GR8
         AR    GR8,GR5            ADD DISPLACEMENT TO MESSAGE ADDRESS
         LA    GR8,MSGK(GR8)      POINT TO START OF MSG TEXT
         MVC   X0(NAMLEN,GR8),NAMEFLD  PUT NAME INTO MSG TEXT
NBRPARM  TM    PARAMS,NBR         IS THERE A NUMBER PARAMETER
         BZ    NOPARAM            NO - CONTINUE PROCESSING
         SR    GR8,GR8
         IC    GR8,NODISP         DISPLACEMENT OF NAME FROM
*                                 BEGINNING OF MSG IS IN GR8
         AR    GR8,GR5            ADD DISPLACEMENT TO MESSAGE ADDRESS
         LA    GR8,MSGK(GR8)      POINT TO START OF MSG TEXT
         MVC   X0(NBRLEN,GR8),NOFLD    PUT NUMBER INTO MSG TEXT
NOPARAM  EQU   *
         CLI   X1(GR2),GENERUAL   IS THIS A 'VALIDATION ERROR' MSG
         BNE   NOTVALID           IF NOT, NO SPECIAL SETUP REQUIRED
         MVC   VALOFF(NAMLEN,GR5),DDVALNM   PUT NAME BEING VALIDATED IN
*                                 GENERVAL MSG
NOTVALID EQU   *
         MVC   X0(NBRLEN,GR6),IEBCON   SET UP 'IEB1XXI' IN BUFFER
*                                 BUFFER
         MVC   IEBMNO(NLEN,GR6),X1(GR5) PUT MSG NBR INTO 'IEB1  I'
         SR    GR7,GR7
         IC    GR7,X0(GR5)        GET LENGTH OF MSG TEXT (IN BYTES)
         BCTR  GR7,GR0            SUBTR 1 FOR EX
         EX    GR7,MOVEMSG        LENGTH OF MSG IS IN GR7
         TM    PARAMS,MSGULLD     MSG CONTAINING 'COPIED'        X010XX
         BZ    WRTMSG             NO,GO WRITE MSG                X010XX
         NI    PARAMS,FF-MSGULLD  RESET FLAG                     X010XX
         SR    GR8,GR8                                           X010XX
         IC    GR8,COPDISP        DISPLAC OF ' COPIED ' FROM     X010XX
*                                 BEGIN OF MSG IS IN GR8         X010XX
         LA    GR8,TXTOFF(GR8,GR6)     GET POINTER TO ' COPIED ' X010XX
*                                      IN MSG BUFFER             X010XX
         TM    ULLDST,LOAD        DOING A LOAD                   X010XX
         BZ    VMS00220           DONT ALTER MSG                 X010XX
         MVC   X0(NAMLEN,GR8),LD  MOVE IN 'LOADED'               X010XX
VMS00220 EQU   *                                                 X010XX
         TM    ULLDST,UNLOAD      DOING AN UNLOAD                X010XX
         BZ    WRTMSG             NO,GO WRITE MSG                X010XX
         MVC   X0(NAMLEN,GR8),UNL MOVE IN 'UNLOADED'             X010XX
         B     WRTMSG             GO WRITE THE MSG
ADCC     LA    GR5,CCIMAGE
         MVC   COFF(CCLEN,GR6),X0(GR5) PUT CONTROL CARD IMAGE
*                                 INTO THE BUFFER
WRTMSG   EQU   *
         LA    GR6,MSGBUF
WRTHEAD  EQU   *
         LA    GR7,PRTDCB
         LA    GR8,WTOS           ADDRESS TO BE USED BY SYNAD ROUTINE
*                                 IF I/O ERROR OCCURRS WHILE 'PRINTING'
         PUT   (GR7),(GR6)        PRINT MESSAGE
         TM    IOEF2,SPRNOPN      I/O ERROR ON SYSPRINT
         BZ    NOPUTERR           NO - CONTINUE PROCESSING
WTOS     MVC   WTOLF1+P4(SYN1),MSGBUF+P1 PUT 1ST PART OF SYNADAF
*                                 MESSAGE INTO WTO BUFFER
         MVC   WTOLF2+P4(SYN2),MSGBUF+LSYN1 PUT SECOND PART OF
*                                 SYNADAF MESSAGE INTO WTO BUFFER
         LA    GR1,WTOLF1         ADDRESS OF LIST FORM WTO IN GR1
         WTO   MF=(E,(1))         EXECUTE WTO
         LA    GR1,WTOLF2         ADDRESS OF LIST FORM WTO IN GR1
         WTO   MF=(E,(1))         EXECUTE WTO
*                                 TWO WTO'S MUST BE ISSUED BECAUSE
*                                 IMPLEMENTATION OF MCS DOES NOT
*                                 ALLOW MORE THAN 72 BYTES PER LINE
*                                 ON THE OPERATORS CONSOLE
         B     TESTM53            SEE IF END OF JOB MSG TO BE WRITTEN
NOPUTERR EQU   *
         TM    SWITCH,WRTHDMSG    WAS A HEADER JUST PRINTED
         BO    NISWOFF            YES - DONT CLEAR BUFFER
         MVI   MSGBUF,BLNK        SET MSG BUFFER TO BLANKS AFTER WRITE
         MVC   MSGBUF+P1(BUFL),MSGBUF
         B     NOTHEAD            SINCE MSG ISNT A HEADER, GO TO
*                                 GET THE NEXT MESSAGE
NISWOFF  EQU   *
         NI    SWITCH,FF-WRTHDMSG TURN OFF HEADER SWITCH
         MVI   MSGBUF,TRIPLESP    IF A HEADER WAS PRINTED,
*                                 SET CARRIAGE CONTROL CHARACTER TO
*                                 TRIPLE SPACE
         LA    GR3,LINES3(GR3)    ADD 3 TO LINE COUNT
         B     RDYWRTHD           BRANCH TO SET UP FOR PRINTING THE
*                                 MESSAGE WHICH FOLLOWS THE HEADER
WTOLF1   WTO   '                                                       X
                      ',MF=L,ROUTCDE=11 1ST PART OF MSG        @YA02613
WTOLF2   WTO   '                                                       X
                  ',MF=L,ROUTCDE=11     2ND PART OF MSG        @YA02613
WTOLF5   WTO   'IEB147I END OF JOB - X WAS HIGHEST SEVERITY CODE',MF=L,X
               ROUTCDE=11                                      @YA02613
         SPACE 1
NOTHEAD  EQU   *
         LA    GR3,LINE1(GR3)     ADD 1 TO LINE COUNT
         STC   GR3,LINECT
         CLI   LINECT,FIFTYSIX    WAS THE 56'TH LINE JUST PRINTED
         BL    RETCAL             IF NOT, SEE IF NECESSARY TO RETURN
*                                 TO CALLER
         MVI   LINECT,ZRO         RESET LINE COUNT
RETCAL   TM    X0(GR2),LASTMSG    IS THIS THE LAST MSG TO HANDLE NOW
         BO    RETURN             IF SO, EXIT TO CALLER
         LA    GR2,M2(GR2)        ELSE POINT TO NXT MSG LIST ENTRY
         CLI   LINECT,ZRO         DOES PAGE HEADER NEED TO BE PRINTED
         BNE   RDYWRT1            IF NOT, GO TO SET UP NEXT MESSAGE
         SR    GR3,GR3
PRNTHEAD OI    SWITCH,WRTHDMSG
         LA    GR6,HEADBUF
         MVC   PGOFF(PNLEN,GR6),PGNO   PUT PAGE NBR INTO HEADER MSG
         PACK  PGCONVRT(L8),PGNO+P1(L3) NOW INCREMENT IT
         CVB   GR8,PGCONVRT       CONVERT PACKED PAGE NUMBER TO
*                                 BINARY
         LA    GR8,X1(GR8)        ADD 1 TO PAGE NUMBER
         CVD   GR8,PGCONVRT       CONVERT BACK TO DECIMAL
         UNPK  PGNO+P1(L3),PGCONVRT
         OI    PGNO+L3,CHAR0      CHANGE SIGN OF LOW ORDER BYTE TO ZONE
         B     WRTHEAD            GO WRITE THE HEADER
RETURN   L     GR13,SAVE+LNKBK
         RETURN  (14,12)
PGCONVRT DC    D'0'               AREA FOR PAGE NBR CONVERSION
ATXT     DC    A(TXTCSECT)        ADDR OF MESSAGE TEXT CSECT
SAVE     DC    18F'0'             REGISTER SAVE AREA
MOVEMSG  MVC   TXTOFF(X0,GR6),MSGK(GR5) EXECUTED TO PUT MSG INTO BUFFER
SWITCH   DC    X'00'              INTERNAL SWITCH
WRTHDMSG EQU   X'80'         ON = A PAGE HEADER IS BEING WRITTEN
*              LOW ORDER 7 BITS NOT USED
IEBCON   DC    C'IEB1  I'         MESSAGE NUMBER CONSTANT
LD       DC    CL8' LOADED '                                     X010XX
UNL      DC    CL8'UNLOADED'                                     X010XX
* THE FOLLOWING 121 BYTES ARE THE PAGE HEADER TEXT
HEADBUF  DC    C'1'            CARRIAGE CONTROL=EJECT
         DC    40C' '
         DC    C'IEBCOPY MESSAGES AND CONTROL STATEMENTS '
         DC    30C' '
         DC    C'PAGE '
         DC    5C' '
         DS    0F
PATCHLEN EQU   (*-IEBVMS)/20      LENGTH OF 5 PER CENT PATCH AREA
PATCH    DC    XL(PATCHLEN)'00'   5 PER CENT PATCH AREA          A41780
         DS    0F
MSGTAB   EQU   *-4                MSGTAB IS A TABLE OF POINTERS TO
*                                 THE MESSAGES. IT MUST START AT THE
*                                 FIRST ENTRY-4 BECAUSE THERE IS NO
*                                 MESSAGE WITH THE NUMBER 0
* NOTE - EACH ADCON IS ACCESSED AS NEEDED BY USING THE MESSAGE CODE AND
*        MULTIPLYING IT BY 4.  IT IS THEREFORE IMPORTANT TO RETAIN THE
*        SAME SEQUENCE OF ADCONS AND MESSAGE CODES.
*        DC    AL4(IEB100I)       MESSAGE TEXT NOT IN THIS MODULE
*        DC    AL4(IEB101I)       MESSAGE TEXT NOT IN THIS MODULE
*        DC    AL4(IEB102I)       MESSAGE TEXT NOT IN THIS MODULE
*        DC    AL4(IEB103I)       MESSAGE TEXT NOT IN THIS MODULE
         DC    AL4(IEB104I)
         DC    AL4(IEB105I)
         DC    AL4(IEB106I)
         DC    AL4(IEB107I)
         DC    AL4(IEB108I)
         DC    AL4(IEB109I)
         DC    AL4(IEB110I)
         DC    AL4(IEB111I)
         DC    AL4(IEB112I)
         DC    AL4(IEB113I)
         DC    AL4(IEB114I)
         DC    AL4(IEB115I)
         DC    AL4(IEB116I)
         DC    AL4(IEB117I)
         DC    AL4(IEB118I)
         DC    AL4(IEB119I)
         DC    AL4(IEB120I)
         DC    AL4(IEB121I)
         DC    AL4(IEB122I)
         DC    AL4(IEB123I)
         DC    AL4(IEB124I)
         DC    AL4(IEB125I)
         DC    AL4(IEB126I)
         DC    AL4(IEB127I)
         DC    AL4(IEB128I)
         DC    AL4(IEB129I)
         DC    AL4(IEB130I)
         DC    AL4(IEB131I)
         DC    AL4(IEB132I)
         DC    AL4(IEB133I)
         DC    AL4(IEB134I)
         DC    AL4(IEB135I)
         DC    AL4(IEB136I)
         DC    AL4(IEB137I)
         DC    AL4(IEB138I)
*        DC    AL4(IEB139I)       SYNADAF MSG - TEXT NOT IN THIS MODULE
         DC    AL4(IEB140I)
         DC    AL4(IEB141I)
         DC    AL4(IEB142I)
         DC    AL4(IEB143I)
         DC    AL4(IEB144I)
         DC    AL4(IEB145I)
         DC    AL4(IEB146I)
         DC    AL4(IEB147I)
         DC    AL4(IEB148I)
         DC    AL4(IEB149I)
         DC    AL4(IEB150I)
         DC    AL4(IEB151I)
         DC    AL4(IEB152I)
         DC    AL4(IEB153I)
         DC    AL4(IEB154I)
         DC    AL4(IEB155I)
         DC    AL4(IEB156I)
         DC    AL4(IEB157I)
         DC    AL4(IEB158I)
         DC    AL4(IEB159I)
         DC    AL4(IEB160I)
         DC    AL4(IEB161I)
         DC    AL4(IEB162I)
         DC    AL4(IEB163I)
         DC    AL4(IEB164I)
         DC    AL4(IEB165I)
         DC    AL4(IEB166I)
         DC    AL4(IEB167I)
         DC    AL4(IEB168I)
         DC    AL4(IEB169I)
         DC    AL4(IEB170I)
*        DC    AL4(IEB171I)       I/O ERROR MSG - TEXT IN IEBIOE MODULE
         DC    AL4(IEB172I)
*        DC    AL4(IEB173I)       MESSAGE TEXT NOT IN THIS MODULE
*        DC    AL4(IEB174I)       MESSAGE TEXT NOT IN THIS MODULE
*        DC    AL4(IEB175I)       MESSAGE TEXT NOT IN THIS MODULE
*        DC    AL4(IEB176I)       MESSAGE TEXT NOT IN THIS MODULE
         DC    AL4(IEB177I)
         DC    AL4(IEB178I)                                      X010XX
         DC    AL4(IEB179I)                                      X010XX
         DC    AL4(IEB180I)                                      X010XX
         DC    AL4(IEB181I)                                      X010XX
*        DC    AL4(IEB182I)       MESSAGE TXT NOT IN THIS MODULE X010XX
*        DC    AL4(IEB183I)       MESSAGE TXT NOT IN THIS MODULE X010XX
*        DC    AL4(IEB184I)       MESSAGE TXT NOT IN THIS MODULE X010XX
         DC    AL4(IEB185I)                                      X010XX
         DC    AL4(IEB186I)                                      X010XX
         DC    AL4(IEB187I)                                      X010XX
*        DC    AL4(IEB188I)       MSG TEXT NOT IN THIS MODULE    A38720
         EJECT
IEBVMTXT CSECT
TXTCSECT EQU   IEBVMTXT
***********************************************************************
* MESSAGE TEXT FORMAT -
* A 1 BYTE HEXADECIMAL CONSTANT , WHICH IS THE HEXADECIMAL            *
* EQUIVALENT OF THE NUMBER OF BYTES  IN THE MESSAGE TEXT , IS         *
* DEFINED                                                             *
* FOLLOWING THE 1 BYTE HEXADECIMAL CONSTANT IS THE MESSAGE TEXT.      *
* THE MESSAGE TEXT IS A VARIABLE LENGTH CHARACTER DEFINED FIELD       *
* OF WHICH THE FIRST TWO BYTES ARE THE VARIABLE TO BE USED AS THE     *
* MESSAGE NUMBER IN IEB1  I CONSTANT FIELD
***********************************************************************
*                                                                     *
* NOTE - NOT ALL MESSAGES ISSUED BY THIS UTILITY PROGRAM HAVE THEIR
*        MESSAGE TEXT CONTAINED WITHIN THIS MODULE.  THOSE MESSAGES NOT
* CONTAINED HEREIN ARE NOTED IN THEIR APPROPRIATE LOCATIONS WITHIN THE
*        MESSAGES HERE.
* MESSAGE NUMBERS CURRENTLY IN USE ARE -
*IEB100I THROUGH IEB187I
*                                                                     *
***********************************************************************
* THE FOLLOWING 4 MESSAGES ARE CONTAINED WITHIN IEBIOE MODULE -
*IEB100I - I/O ERROR READING MEMBER (MBRNAME)
*IEB101I - I/O ERROR WRITING MEMBER DATA AT TTR = (TTR) /READBACK CHECK
*IEB102I MEMBER (NAME) NOT COPIED DUE TO I/O ERROR
*IEB103I MEMBERS (MBRNAME) THROUGH END OF DATA SET ARE NOT ACCESSIBLE
*        DUE TO I/O ERROR
IEB104I  DC    X'1A'              MSG LENGTH
         DC    C'04INVALID COMMAND OR KEYWORD'
IEB105I  DC    X'11'              MSG LENGTH
         DC    C'05PARAMETER INVALID'
IEB106I  DC    X'13'              MSG LENGTH
         DC    C'06UNEQUAL PARENTHESIS'
IEB107I  DC    X'14'              MSG LENGTH
         DC    C'07INVALID CONTINUATION'
IEB108I  DC    X'20'              MSG LENGTH
         DC    C'08MEMBER WITHOUT SELECT OR EXCLUDE'
IEB109I  DC    X'37'              MSG LENGTH
         DC    C'09NO MIXING OF SELECT AND EXCLUDE MODES IN SAME'
         DC    C' COPY STEP'
IEB110I  DC    X'19'              MSG LENGTH
         DC    C'10INVALID REPLACE SPECIFIED'
IEB111I  DC    X'0F'              MSG LENGTH
         DC    C'11NULL PARAMETERS'
IEB112I  DC    X'20'              MSG LENGTH
         DC    C'12CANNOT RENAME/REPLACE ON EXCLUDE'
IEB113I  DC    X'1B'              MSG LENGTH
         DC    C'13OUTDD OR INDD NOT SPECIFIED'
IEB114I  DC    X'1B'              MSG LENGTH
         DC    C'14OUTDD/LIST NOT ON COPY CARD'
IEB115I  DC    X'14'              MSG LENGTH
         DC    C'15END OF FILE ON SYSIN'
IEB116I  DC    X'3D'              MSG LENGTH
         DC    C'16MIXING CONTROL STATEMENTS FROM OLD AND NEW '
         DC    C'VERSION OF IEBCOPY'
IEB117I  DC    X'1C'              MSG LENGTH
         DC    C'17TABLES EXCEED ALLOCATED CORE'
IEB118I  DC    X'17'              MSG LENGTH
         DC    C'18CONTROL STATEMENT ERROR'
IEB119I  DC    X'18'              MSG LENGTH
         DC    C'19STATEMENT SEQUENCE ERROR'
IEB120I  DC    X'19'              MSG LENGTH
         DC    C'20         VALIDATION ERROR'
IEB121I  DC    X'0A'              MSG LENGTH
         DC    C'21OPEN ERROR'
IEB122I  DC    X'1A'              MSG LENGTH
         DC    C'22DSCB COULD NOT BE OBTAINED'
IEB123I  DC    X'0D'              MSG LENGTH                     X010XX
         DC    C'23INVALID DSORG'                                X010XX
IEB124I  DC    X'0D'              MSG LENGTH
         DC    C'24INVALID LRECL'
IEB125I  DC    X'11'              MSG LENGTH
         DC    C'25INVALID BLOCKSIZE'
IEB126I  DC    X'29'              MSG LENGTH
         DC    C'26         REFERENCES AN UNMOVABLE DATA SET'
IEB127I  DC    X'12'              MSG LENGTH
         DC    C'27RECFM INCOMPATIBLE'
IEB128I  DC    X'26'              MSG LENGTH
         DC    C'28CANNOT REBLOCK TRACK OVERFLOW DATA SET'
IEB129I  DC    X'1E'              MSG LENGTH
         DC    C'29CANNOT REBLOCK KEYED DATA SETS'
IEB130I  DC    X'13'              MESSAGE LENGTH
         DC    C'30KEY LENGTHS UNEQUAL'
IEB131I  DC    X'1E'              MSG LENGTH
         DC    C'31CANNOT COMPRESS KEYED DATA SET'
IEB132I  DC    X'16'              MSG LENGTH
         DC    C'32INVALID RE/DE-BLOCKING'
IEB133I  DC    X'24'              MSG LENGTH
         DC    C'33MINIMUM REQUESTED CORE NOT AVAILABLE'
IEB134I  DC    X'26'              MSG LENGTH
         DC    C'34CANNOT COMPRESS WITH SELECT OR EXCLUDE'
IEB135I  DC    X'22'              MSG LENGTH
         DC    C'35MINIMUM I/O BUFFER NOT ALLOCATABLE'
IEB136I  DC    X'36'              MSG LENGTH                     YM7924
         DC    C'36CANNOT ALLOCATE TWO TRACKS OF I/O BUFFERS FOR '
         DC    C'COMPRESS'                                       YM7924
IEB137I  DC    X'4E'
         DC    C'37CANNOT SPECIFY DUPLICATE MEMBERNAMES FOR SELECT/EXCLX
               UDE/RENAME - NAME=        '
IEB138I  DC    X'2A'              MSG LENGTH
         DC    C'38CANNOT PROCESS ALL OLD/NEW-NAMES SPECIFIED'
*IEB139I - SYNADAF MESSAGE TEXT, NOT CONTAINED WITHIN THIS MODULE
IEB140I  DC    X'29'              MSG LENGTH
         DC    C'40         REFERENCES A NULL INPUT DATA SET'
IEB141I  DC    X'3E'              MSG LENGTH
         DC    C'41CANNOT RE/DE-BLOCK WITH NOTE-LIST/USER TTRN IN '
         DC    C'MEMBER         '
IEB142I  DC    X'1F'              MSG LENGTH
         DC    C'42CANNOT CONTINUE TO BUILD CTLTAB'
IEB143I  DC    X'40'              MSG LENGTH                     X010XX
         DC    C'43ALL SELECTED MEMBERS  COPIED  - DID NOT USE ' X010XX
         DC    C'ALL SPECIFIED INDD''S'                          X010XX
IEB144I  DC    X'49'              MSG LENGTH
         DC    C'44THERE ARE         UNUSED TRACKS IN OUTPUT DATA SET '
         DC    C'REFERENCED BY         '
IEB145I  DC    X'27'              MSG LENGTH
         DC    C'45CANNOT COMPRESS TRACK OVERFLOW DATA SET'
IEB146I  DC    X'23'              MSG LENGTH
         DC    C'46CANNOT COMPRESS WITH RE/DE-BLOCKING'
IEB147I  DC    X'28'              MSG LENGTH
         DC    C'47END OF JOB - X WAS HIGHEST SEVERITY CODE'
IEB148I  DC    X'4F'              MSG LENGTH
         DC    C'48NO SPACE IN OUTPUT DIRECTORY FOR DIRECTORY ENTRIES'
         DC    C' FROM INPUT DATA SET         '
IEB149I  DC    X'3E'              MSG LENGTH
         DC    C'49THERE ARE          UNUSED DIRECTORY BLOCKS IN '
         DC    C'OUTPUT DIRECTORY'
IEB150I  DC    X'56'              MSG LENGTH                     A36049
         DC    C'50**WARNING** THE OUTPUT DS REF BY XXXXXXXX CONTAINS T*
               OO MANY DIRECTORY BLOCKS PER TRACK'               A36049
IEB151I  DC    X'20'              MSG LENGTH                     A36047
         DC    C'51JOB HAS TERMINATED WITH ERROR(S)'             A36047
IEB152I  DC    X'38'              MSG LENGTH
         DC    C'52         COMPRESSED - WAS ALREADY IN PLACE AND NOT '
         DC    C'MOVED'
IEB153I  DC    X'37'              MSG LENGTH
         DC    C'53ALL MEMBERS COMPRESSED - ALL WERE ORIGINALLY '
         DC    C'COMPRESSED'
IEB154I  DC    X'27'              MSG LENGTH                     X010XX
         DC    C'54         HAS BEEN SUCCESSFULLY  COPIED '      X010XX
IEB155I  DC    X'3A'              MSG LENGTH                     X010XX
         DC    C'55         HAS BEEN SUCCESSFULLY  COPIED  AND ' X010XX
         DC    C'IS A ''NEWNAME'''                               X010XX
IEB156I  DC    X'1C'              MSG LENGTH
         DC    C'56NOT A DIRECT ACCESS DATA SET'
IEB157I  DC    X'16'              MSG LENGTH
         DC    C'57DD STATEMENT NOT FOUND'
IEB158I  DC    X'1D'              MSG LENGTH
         DC    C'58PARM EQUAL COMPRESS NOT VALID'
IEB159I  DC    X'3E'              MSG LENGTH                     X010XX
         DC    C'59NO MEMBERS  COPIED  FROM INPUT DATA SET '     X010XX
         DC    C'REFERENCED BY         '
IEB160I  DC    X'16'              MSG LENGTH
         DC    C'60CONCATENATED DATA SETS'
IEB161I  DC    X'2D'              MSG LENGTH
         DC    C'61COMPRESS TO BE DONE USING INDD NAMED         '
IEB162I  DC    X'1B'              MSG LENGTH                     X010XX
         DC    C'62PARM EQUAL COMPRESS IGNORED'                  X010XX
IEB163I  DC    X'2F'              MSG LENGTH
         DC    C'63NO MEMBER NAMES FOR PARTIAL COPY, '
         DC    C'WILL NOT COPY'
IEB164I  DC    X'12'              MSG LENGTH
         DC    C'64TOTAL COPY ASSUMED'
IEB165I  DC    X'4B'              MSG LENGTH                     X010XX
         DC    C'65         ''FOUND'' BUT NOT  COPIED , DUE TO ' X010XX
         DC    C'I/O ERROR READING INPUT DIRECTORY'              X010XX
IEB166I  DC    X'36'              MSG LENGTH                     X010XX
         DC    C'66NO MEMBERS  COPIED  TO DATA SET REFERENCED '  X010XX
         DC    C'BY         '                                    X010XX
IEB167I  DC    X'49'              MSG LENGTH                     X010XX
         DC    C'67FOLLOWING MEMBER(S)  COPIED  FROM INPUT '     X010XX
         DC    C'DATA SET REFERENCED BY          -'              X010XX
IEB168I  DC    X'5C'              MSG LENGTH
         DC    C'68**WARNING** DUE TO ERROR, POSSIBLE LOSS OF '
         DC    C'ACCESS TO MEMBER DATA AND/OR INCOMPLETE DIRECTORY'
IEB169I  DC    X'4A'              MSG LENGTH
         DC    C'69**WARNING** DUE TO I/O ERROR ON SYSUT4, OUTPUT '
         DC    C'DIRECTORY MAY BE INCOMPLETE'
IEB170I  DC    X'5D'              MSG LENGTH
         DC    C'70**WARNING** DUE TO SYSUT3 I/O ERROR, COMPRESS-IN-PLAX
               CE NOT DONE AND COPY OPERATION TERMINATED'
* THE FOLLOWING MESSAGE IS CONTAINED WITHIN IEBIOE MODULE -
*IEB171I **WARNING** DIRECTORY MAY NOT REFLECT VALID LOCATION OF MEM-
*        BER DATA
IEB172I  WTO   'IEB172I          COULD NOT BE OPENED',MF=L,    @ZA04370*
               ROUTCDE=11                                      @ZA04370
*IEB173I - MESSAGE TEXT FOR THIS MSG IS IN IEBDV1 MODULE
* THE FOLLOWING 3 MESSAGES ARE CONTAINED WITHIN THE IEBWSU MODULE -
*IEB174I ** WARNING ** INPUT RECORD IS A SHORT LENGTH RECORD     A38720
*        -DDNAME=          -OUTPUT TTRN=                         A38720
*IEB175I ** WARNING ** INPUT RECORD IS GREATER THAN OUTPUT       A38720
*        BLKSIZE  -DDNAME=          -OUTPUT TTRN=                A38720
*IEB176I MEMBER        IN DATASET REFERENCED BY        HAS MORE  A38720
*        THAN ONE NOTELIST POINTER                               A38720
IEB177I  DC    X'39'              MSG LENGTH
         DC    C'77         WAS SELECTED BUT NOT FOUND IN ANY INPUT DATX
               A SET'
IEB178I  DC    X'20'              MSG LENGTH                     X010XX
         DC    C'78NOT AN IEBCOPY UNLOADED DATA SET'             X010XX
IEB179I  DC    X'2B'              MSG LENGTH                     X010XX
         DC    C'79COMPRESS IN PLACE NOT VALID FOR LOAD/UNLOAD'  X010XX
IEB180I  DC    X'3B'              MSG LENGTH                     X010XX
         DC    C'80MORE THAN ONE INPUT DATA SET SPECIFIED FOR '  X010XX
         DC    C'UNLOAD OPERATION'                               X010XX
IEB181I  DC    X'34'              MSG LENGTH                     X010XX
         DC    C'81CORE CAN NOT BE ALLOCATED FOR LOAD/UNLOAD '   X010XX
         DC    C'PROCESSING'                                     X010XX
*      THE FOLLOWING MESSAGES ARE CONTAINED IN THE IEBIOE MODULE X010XX
* IEB182I  UNLOAD DATA SET REFERENCED BY (NAME) HAS BEEN         X010XX
*          FLAGGED AS NOT LOADABLE                               X010XX
* IEB183I  END OF FILE READ ON LOAD DATA SET                     X010XX
*          REFERENCED BY (NAME)                                  X010XX
* IEB184I  MEMBER DATA RECORD READ FOR MEMBER (NAME) REFERENCED  X010XX
*          BY (NAME) IS LONGER THAN DATA SET BLOCKSIZE PLUS      X010XX
*          KEYLENGTH,CCHHRKDD=(NUMBER)                           X010XX
IEB185I  DC    X'1A'              MSG LENGTH                     X010XX
         DC    C'85IS NOT A LOADABLE DATA SET'                   X010XX
IEB186I  DC    X'2D'              MSG LENGTH                     X010XX
         DC    C'86INPUT AND OUTPUT DATASETS ON SAME TAPE '      X010XX
         DC    C'VOLUME'                                         X010XX
IEB187I  DC    X'23'              MSG LENGTH                     X010XX
         DC    C'87NOT A DIRECT ACCESS OR TAPE DATASET'          X010XX
* THE FOLLOWING MESSAGE IS CONTAINED WITHIN MODULE IEBWSU -      A38720
*IEB188I MEMBER        IN DATASET REFERENCED BY        HAS       A38720
*        RECORDS GREATER THAN BLKSIZE                            A38720
         DS    0D                                                X010XX
         EJECT
IEBMCA   DSECT
         IEBMCA
         END
