SCAN     TITLE 'IEBCOPY  CONTROL CARD SCAN- IEBSCN'
IEBSCN   CSECT
*C946800                                                         A41780
*A481000-481080                                                  A41802
*C464200                                                         A45150
*A464220-464260,466100                                           A45150
*D464700                                                         A45150
*C481700                                                         A45174
*A481720-481780                                                  A45174
*A114500-115100,639900-640900,649800-749300,770100-770400        A48742
*D145060-145260                                                  A48742
*C768800                                                         A48742
*A474100                                                         A45185
*A642600-642632,064620                                           A44144
*C642520,632500,754910                                           A44144
*D754850-754880,754970-755030                                    A44144
*A116500-116700                                                  A48800
*C471800,472200,473600,498690                                    A48799
*D366860-367040                                                  A48799
*A581500-582200,366860,471900-472120                             A48799
*A041300,374120-374140                                          OY01183
*A222050,374925,822450-822800,852050                           @ZA07365
*C822400,823200,852000                                         @ZA07365
*A487100,487120                         (ORG)@XA16031/@YA14042/@ZA13792
**********************************************************************
         ENTRY IEBSCF
         SPACE 1
*                                                                     *
*TITLE- IEBCOPY CONTROL CARD SCAN AND ANALYSIS CSECT- IEBSCN          *
*                                                                     *
*STATUS- CHANGE LEVEL 001                                             *
*                                                                     *
*FUNCTION/OPERATION-  THIS MODULE SCANS CONTROL CARDS AND GIVES       *
*       MESSAGES FOR SYNTAX OR SEQUENCE ERRORS. IT BUILDS AN INDD     *
*       TABLE OF DD NAMES OF INPUT DATA SETS WHICH CONTAIN MEMBERS    *
*       TO BE COPIED. IT BUILDS AN SE TABLE OF MEMBER NAMES TO BE     *
*       SELECTED OR EXCLUDED IN THE COPY. IT STORES THE NAME OF       *
*       THE OUTPUT DD STATEMENT IN BUFFER 'OUTNAME'.                  *
*                                                                     *
*ENTRY POINTS- ENTERED AT IEBSCN.                                     *
*        ENTERED AT IEBSEF ON EOF ON SYSIN                            *
*                                                                     *
*INPUT- SYSIN WHICH CONSISTS OF CONTROL STATEMENTS.                   *
*                                                                     *
*OUTPUT- SYSPRINT WHICH CONTAINS THE DIAGNOSTIC MESSAGES AND THE      *
*        CONTENTS OF SYSIN                                            *
*                                                                     *
*EXITS-  ERROR- IF AN ERROR OCCURS, A CODE IS STORED, A MESSAGE IS    *
*        GIVEN TO THE USER EXPLAINING THE ERROR CONDITION AND A       *
*        RETURN TO THE CALLING PROGRAM IS GIVEN.                      *
*                                                                     *
*EXTERNAL ROUTINES- READ CARD (GET), PRINT CARD OR ERROR MESSAGE (PUT)*
*                                                                     *
*TABLES/WORK AREAS-                                                   *
*      -KEYTAB-    LIST OF VALID KEYWORDS                             *
*      -COMDTABL-  LIST OF VALID COMMAND WORDS                        *
*      -CCIMAGE-   BUFFER CONTAINING CONTROL CARD IMAGE               *
*      -INDD TABLE-POINTED TO BY INBEGIN                              *
*      -SE TABLE-  POINTED TO BY SEBEGIN                              *
*      -CTAD-      POINTER TO CONTROL TABLE BUILT WHEN SELECTIVE COPY *
*      -PARMSWCH-  INTERNAL SCAN SWITCHES                             *
*      -CCSWITCH-  EXTERNAL SCAN SWITCHES                             *
*      -COMDCDSW-  EXTERNAL SCAN SWITCHES                             *
*      -CPARAMSW-  INTERNAL SCAN SWITCHES                             *
*      -SV2-       REGISTER SAVE AREA                                 *
*                                                                     *
*ATTRIBUTES- SERIAL REUSABLE                                          *
*                                                                     *
         EJECT
         SPACE 1
*                                                                     *
*                    SCAN ROUTINE CONSTANTS                           *
*                                                                     *
ANAT     EQU   C'@'                    NATIONAL SYMBOL- OKAY IN MEMBER
APOUND   EQU   C'#'                    NATIONAL SYMBOL- OKAY IN MEMBER
ADOLLAR  EQU   C'$'                    NATIONAL SYMBOL- OKAY IN MEMBER
AC       EQU   C'C'
ANUNDRSC EQU   C'_'                                             OY01183
ANA      EQU   X'C0'                   START OF VALID CHARACTER
EQUAL    EQU   C'='                    HEX 'EQUAL'
COMMA    EQU   C','                    HEX 'COMMA'
BLANKCOL EQU   C' '                    HEX 'BLANK'
PARENLFT EQU   C'('                    LEFT PARENTHESIS
PARENRGT EQU   C')'                    RIGHT PARENTHESIS
CRESET0  EQU   X'00'                   RESETS SWITCHES
         SPACE 1
*                                                                     *
*                      SYMBOLIC REGISTER FOR SCAN                     *
         SPACE 1
LENGTH   EQU   9                       LENGTH OF PARAMETER REGISTER 9
SCANADR  EQU   1                       ADDRESS OF PARAMETER REGISTER 1
GR0      EQU   0
GR1      EQU   1
GR2      EQU   2
GR3      EQU   3
GR4      EQU   4                       COMMUNICATION AREA POINTER
GR5      EQU   5
GR6      EQU   6
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10
GR11     EQU   11
GR12     EQU   12                      REGISTER USED FOR BASE ADDRESS
GR13     EQU   13
GR14     EQU   14
GR15     EQU   15
FF       EQU   X'FF'              ALL BITS ON
LEN8     EQU   8                  LENGTH OF COMMAND OR KEYWORD
LEN12    EQU   12                 LENGTH OF ENTRY
SAV4     EQU   4                  OLD SAVE AREA
SAV8     EQU   8                  SAVE
TABLE0   EQU   0                  ZERO TABLE DISPLACEMENT
INDD1    EQU   1                  ENTRIES IN INDD TABLE
LEN10    EQU   10                 LENGTH OF  10
LEN2     EQU   2                  LENGTH OF  2
LEN4     EQU   4                  LENGTH OF  4
COL71    EQU   71                 COLUMN 72
COL70    EQU   70                 COLUMN 71
SCAN0    EQU   0                  COLUMN BEING SCANNED
UP1      EQU   1                  COLUMN UPDATE
PARCNT   EQU   X'01'              PARENTHESIS COUNT
MS7      EQU   7                  MASK OF 7
MS5      EQU   5                  MASK OF 5
EX       EQU   C'E'               E FOR EXCLUDE
IN       EQU   C'I'               I FOR INCLUDE
COMPCD   EQU   C'4'               COMPLETION CODE
COMPCDE  EQU   C'8'               COMPLETION CODE                A44144
MS8      EQU   8                  MASK OF 8
LEN3     EQU   3                  LENGTH OF 3
MS2      EQU   2                  MASK OF 2
         EJECT
         SAVE  (14,12),,*
*
*                    BASE ADDRESS REGISTER GR12-- COMMUNICATION AREA 4
*
         BALR  GR12,GR0           ESTABLISH ADDRESSABILITY
         USING NEXT,GR12
         USING IEBMCA,4
         SPACE 1
NEXT     EQU   *
         ST    GR13,SV2+SAV4      SAVE POINTER TO OLD SAVE AREA
         LA    GR15,SV2                NEW SAVE AREA THIS CSECT
         ST    GR15,SAV8(GR13)    POINTER TO NEW SAVE AREA IN OLD
         LR    GR13,GR15               POINTER TO THIS CSECT SAVE AREA
         SPACE 1
         TM    CCSWITCH,SYSINEOF       END OF FILE ON LAST READ
         BO    TERMS              YES- TERMINATE JOB-- SYSIN DONE
         TM    PARMSWCH,FLUSHSW        FLUSHING
         BO    IGNORE             YES- GET NEXT CARD
         TM    CCSWITCH,IEBCOPYC       HAVE IEBCOPY CONTROL CARDS
         BO    SCANERRM           YES- MIXED CODES
         NI    COMDCDSW,FF-SELECTSC-EXCLUDES-MEMBRCD1 RESET SWITCHES
         B     TOSCAN                  CONTINUE
         SPACE 1
IGNORE   EQU   *
         TM    CPARAMSW,COL72BLK       IS THIS CARD CONTINUED    A48742
         BO    IGNRENXT                NO                        A48742
         NI    SCANSWCH,FF-NOCMMEXP    RESET BYPASS SCAN SWITCH  A48742
         B     IGNOREON                AND CONTINUE              A48742
IGNRENXT EQU   *                                                 A48742
         OI    SCANSWCH,NOCMMEXP       SET BYPASS SCAN SWITCH    A48742
IGNOREON EQU   *                                                 A48742
         MVI   COMDCDSW,CRESET0
         MVI   CCDELIM2,CRESET0
         TM    CCSWITCH,CARDPRTD  WAS THE CARD PRINTED           A48800
         BO    GOONSC             YES                            A48800
         BAL   GR9,PRNTCRD        NO, PRINT THE CARD             A48800
         SPACE 1
GOONSC   EQU   *
         MVI   CCSWITCH,CRESET0        CLEAR SWITCHES FIRST TIME THRU
GOONS    EQU   *
         MVI   CPARAMSW,READ1          FORCE READ FIRST CONTROL CARD
         SPACE 1
TOSCAN   EQU   *
         BAL   GR14,RDCARD             GO TO CONTROL CARD SCAN
RETRTOSC EQU   *
         L     LENGTH,CSTOREG          LENGTH OF PARAMETER
         L     SCANADR,CSTOREG+SAV4 START OF PARAMETER
         LTR   LENGTH,LENGTH           IS SCAN LENGTH ZERO
         BZ    BADPARM                 BAD PARAMETER
         TM    CCSWITCH,COMDNOW        IS COMMAND SWITCH ON
         BO    OPRLUP             YES- CHECK COMMAND WORDS
*                                                                     *
*   KEYWORD LOOKUP ROUTINE
         LA    GR5,KEYTAB              START OF KEY WORD TABLE
         LA    GR7,KEYEND              END OF KEY WORD TABLE
         B     SCANTBL                 SEARCH FOR KEY WORD
         SPACE 1
*   COMMAND WORD LOOKUP TABLE
OPRLUP   LA    GR5,COMDTABL            START OF COMMAND TABLE
         LA    GR7,COMDEND             END OF COMMAND TABLE
SCANTBL  EQU   *
         LA    GR6,LEN8           MAXIMUM LENGTH OF KEYWORD OR COMMAND
         CR    LENGTH,GR6              TEST IF LENGTH EXCEEDS 8
         BH    PRTBAD                  PRINT BAD CARD
         LA    GR6,LEN12          INCREMENT VALUE- 12 BYTE ENTRIES
         MVC   SARG(LEN8),BLANKS8 MOVE BLANKS TO SEARCH ARGUMENT
         BCTR  LENGTH,GR0         DECREMENT FOR EXECUTE COUNT
         EX    LENGTH,OPRLUP3          MOVE PARAMETER TO 'SARG'
OPRCMP   CLC   SARG(LEN8),TABLE0(GR5) TEST IF SEARCH ARGUMENT IN TABLE
         BNE   OPRLUP2                 NO- CONTINUE SCAN OF TABLE
         LA    GR5,LEN8(GR5)      ENTRY ADDRESS OF ROUTINE
         BR    GR5                     ENTER PROPER ROUTINE
OPRLUP2  BXLE  GR5,GR6,OPRCMP          LOOP TO CONTINUE SCAN
PRTBAD   EQU   *
         LA    GR2,INALCNTR            INVALID COMMAND OR KEYWORD
         B     GIVESCNE           PRT CARD, SET RC TO 4, WRT MSG
         EJECT
*     COMMAND LOOKUP TABLE
* FORMAT OF TABLE -
*     1) KEYWORD OR ABBREVIATION (ALWAYS AN 8-BYTE 'DC')
*     2) BRANCH TO ANALYZER SUBROUTINE
COMDTABL DS    0F                      START OF VALID COMMAND TABLE
         DC    C'COPY    '        KEYWORD
         B     CHKCOPY            BRANCH TO ANALYZER
         DC    C'C       '        ABBREVIATION
         B     CHKCOPY            BRANCH TO ANALYZER
* SUBSEQUENT TABLE ELEMENTS ARE IN SAME FORMAT AS THE ABOVE
         DC    C'EXCLUDE '
         B     CHKEXCLD           CHECK EXCLUDE
         DC    C'E       '
         B     CHKEXCLD           CHECK EXCLUDE
         DC    C'MEMBER  '
         B     CHKMEMB            MEMBER COMMAND
* NOTE - NO ABBREVIATION ALLOWED FOR 'MEMBER' WHEN USED AS A COMMAND,
*        BECAUSE THIS CONTEXT IS FOR PREVIOUS VERSIONS OF IEBCOPY.
         DC    C'SELECT  '
         B     CHKSELCT           CHECK SELECT
COMDEND  DC    C'S       '             END OF COMMAND WORD TABLE
         B     CHKSELCT           CHECK SELECT
         EJECT
*     KEYWORD LOOKUP TABLE
* FORMAT OF TABLE -
*     1) KEYWORD OR ABBREVIATION (ALWAYS AN 8-BYTE 'DC')
*     2) BRANCH TO ANALYZER SUBROUTINE
KEYTAB   DS    0F                 START OF KEY WORD TABLE
         DC    C'MEMBER  '        KEYWORD
         B     MEMBRANL           BRANCH TO ANALYZER
         DC    C'M       '        ABBREVIATION
         B     MEMBRANL           BRANCH TO ANALYZER
* SUBSEQUENT TABLE ELEMENTS ARE IN SAME FORMAT AS THE ABOVE
         DC    C'OUTDD   '
         B     OUTDDANL           CHECK OUTDD
         DC    C'O       '
         B     OUTDDANL           CHECK OUTDD
         DC    C'INDD    '
         B     INDDANL            CHECK INDD
         DC    C'I       '
         B     INDDANL            CHECK INDD
         DC    C'LIST    '
         B     LISTANL            CHECK LIST KEYWORD
         DC    C'L       '
         B     LISTANL            CHECK LIST KEYWORD
* NOTE - THE FOLLOWING 3 KEYWORDS ARE FOR COMPATIBILITY WITH PREVIOUS
*        VERSIONS, AND HAVE NO ABBREVIATION
         DC    C'NAME    '
         B     NAMEANL            NAME KEYWORD
         DC    C'TYPCOPY '
         B     TYPCPANL           TYPCOPY KEYWORD
KEYEND   DC    C'MAXNAME '             END OF TABLE OF KEYWORDS
         B     NOPMAX             MAXNAME KEYWORD
         EJECT
CHKMEMB  EQU   *
         BAL   GR9,PRNTCRD             PRINT THE CARD
         TM    CCSWITCH,IEBCOPYC       WITH IEBCOPY CONTROL CARDS
         BZ    SCANERRM           NO- MIXING CONTROL CARDS
         TM    COMDCDSW,EXCLUDES+SELECTSC  WITH SELECT OR EXCLUDE
         BZ    SCANSEQR           NO- CARD MAYBE OUT OF SEQUENCE
         TM    COMDCDSW,MEMBRCD1       ALREADY HAD A MEMBER CARD
         BZ    ARNDS              NO
         OI    CCSWITCH,MULTSE         FLAG AS MULTIPLE MEMBER CARDS
ARNDS    EQU   *
         OI    COMDCDSW,MEMBRCD1       SET MEMBER COMMAND SWITCH
         B     TOSCAN                  CONTINUE SCANNING
         SPACE 1
NOPMAX   EQU   *
         TM    PARMSWCH,COPYNOW        ON COPY STATEMENT
         BZ    SCANSEQR           NO- SEQUENCE ERROR
         TM    CCSWITCH,FIRSTSCN       FIRST ENTRY
         BZ    SCANERRM           NO, MIXED CODE
         OI    CCSWITCH,IEBCOPYC       FLAG AS IEBCOPY CONTROL CARDS
         BAL   GR14,RDCARD             GET PARAMETER AFTER EQUAL SIGN
         SPACE 1
CHLAST   EQU   *
         TM    CCSWITCH,LASTPARM       LAST PARAMETER ON CARD SCANNED
         BZ    TOSCAN             NO- CONTINUE SCANNING CARD
         MVI   PARMSWCH,CRESET0        RESET 'COPYNOW' SWITCH
         OI    COMDCDSW,COPYDONE       SET AS COPY CARD SCANNED
         B     GOONS                   GET NEXT CARD
         SPACE 1
NAMEANL  EQU   *
         TM    CCSWITCH,IEBCOPYC       WITH IEBCOPY CONTROL CARDS
         BZ    SCANERRM           MIXED CONTROL CARDS
         TM    COMDCDSW,MEMBRCD1       WITH MEMBER STATEMENT
         BZ    SCANERR            NO- ERROR
         TM    CCSWITCH,MULTSE         MULTIPLE MEMBER CARDS
         BO    STRMEMBS           ADD THESE NAMES TO TABLE
         LA    GR9,STRMEMBS            RETURN FROM SET4OLDC ROUTINE
         SPACE 1
SET4OLDC EQU   *
         L     GR8,INBEGIN             START OF INDD TABLE
         LA    GR6,INDD1          COUNT OF ENTRIES
         STH   GR6,INDDCT              MAKE INDD COUNT 1
         MVC   TABLE0(LEN10,GR8),FLAGBYTS CLEAR FLAG BYTES AND NAME
         LA    GR6,INDCB               INPUT DATA SET DCB ADDRESS
         MVC   LEN2(LEN8,GR8),DDNAME1(GR6) MAKE INDD ENTRY INDCB-SYSUT1
         LA    GR6,OUTDCB              OUTPUT DATA SET DCB ADDRESS
         MVC   OUTNAME(LEN8),DDNAME1(GR6) OUTPUT DATA SET SYSUT2
         LA    GR8,LEN10(GR8)     UP TABLE POINTER
         ST    GR8,SEBEGIN             STORE START AND END OF SETABLE
         ST    GR8,SESTOP
         BR    GR9                     RETURN
         SPACE 1
OUTDDANL EQU   *
         TM    PARMSWCH,FLUSHSW        DOING A FLUSH
         BO    IGNORE             YES- SKIP THIS CARD
         TM    PARMSWCH,COPYNOW        ON COPY STATEMENT
         BZ    INVALODL           ERROR- OUTDD NOT ON COPY CARD
         TM    COMDCDSW,COMPRESS       PARM= COMPRESS ON EXEC CARD
         BO    COMPEXEC           YES- MEANINGLESS IN IEBDSCPY
         SPACE 1
COMPRETN EQU   *                       RETURN POINT AFTER MESSAGE
         OI    PARMSWCH,SCANNAME  INDICATE SCAN OF NAME        @ZA07365
         OI    COMDCDSW,NEWOUT         SET OUTDD SCANNED SWITCH
         OI    FLG4,NEWODS+NEWCOPOP    SET SWITCHES TO INDICATE A NEW
*                                        COPY OPERATION HAS BEEN FOUND
         TM    CPARAMSW,DELIMEND       NAME ON NEXT CARD (= IN COL 71)
         BZ    PICKNAME           NO- NORMAL PROCESSING
         BAL   GR10,GETPARM            GET NEXT CARD- POSITION TO PARM
         SPACE 1
PICKNAME EQU   *
         BAL   GR14,RDCARD             GET PARAMETER
         L     SCANADR,CSTOREG+SAV4 START OF PARAMETER
         L     LENGTH,CSTOREG          SIZE OF PARAMETER
         LA    GR6,LEN8           MAXIMUM LENGTH OF NAME
         CR    GR6,LENGTH
         BL    BADPARM                 NAME GREATER THAN 8 BYTES
         LA    GR6,OUTDCB              OUTPUT DATA SET DCB ADDRESS
         MVC   DDNAME1(LEN8,GR6),BLANKS8 CLEAR DCB DDNAME FIELD
         BCTR  LENGTH,GR0         DECREMENT FOR EXECUTE COUNT
         EX    LENGTH,MOVEOUTD         MOVE OUTDD NAME TO OUTPUT DCB
         MVC   OUTNAME(LEN8),DDNAME1(GR6) MOVE OUTDD NAME TO COMMON
*                                         AREA
         TM    CCSWITCH,LASTPARM       LAST PARAMETER ON CARD
         BZ    TOSCAN             NO- CHECK FOR LIST OPTION
         B     CHKCOMSC                CHECK FOR CONTINUED COMMENTS
         SPACE 1
LISTANL  EQU   *
         TM    PARMSWCH,FLUSHSW        FLUSHING
         BO    IGNORE             YES- IGNORE THIS CARD
         TM    PARMSWCH,COPYNOW        ON COPY STATEMENT
         BZ    INVALODL           NOT ON COPY STATEMENT
         TM    CPARAMSW,DELIMEND       NAME ON NEXT CARD (= IN COL 71)
         BZ    ONTHISC            NO- NORMAL PROCESSING
         BAL   GR10,GETPARM            GET NEXT CARD- POSITION TO PARM
         SPACE 1
ONTHISC  EQU   *
         BAL   GR14,RDCARD             GET PARAMETER
         L     SCANADR,CSTOREG+SAV4 START OF PARAMETER
         L     LENGTH,CSTOREG          LENGTH OF PARAMETER
         LA    GR6,LEN2
         CR    LENGTH,GR6
         BNE   BADPARM                 PARAMETER LARGER THAN 'NO'
         CLC   SCAN0(LEN2,SCANADR),LISTALLC  IS PARAMETER 'NO'
         BNE   BADPARM                 ERROR
         TM    CCSWITCH,IEBCOPYC       WITH IEBCOPY STATEMENTS
         BO    SCANERRM           YES- MIXING STATEMENT TYPES
         OI    COMDCDSW,LISTSW         SET TO NOT LIST MEMBERS COPIED
         TM    CCSWITCH,LASTPARM       LAST PARAMETER ON CARD
         BZ    TOSCAN             NO- CONTINUE
         TM    COMDCDSW,NEWOUT         HAVE OUTDD ALREADY
         BZ    NOINDDSP           INDD/LIST WITHOUT OUTDD
CHKCOMSC EQU   *
         TM    CPARAMSW,COL72BLK       CONTINUED CARD
         BZ    COPYALLD           FINISH UP COPY CARD SCAN
         BAL   GR9,CHK4COMT            CHECK FOR CONTINUED COMMENTS
COPYALLD EQU   *
         OI    COMDCDSW,COPYDONE       SET DONE WITH COPY CARD SWITCH
         MVI   PARMSWCH,CRESET0        RESET ALL, ESPECIALLY 'COPYNOW'
         B     GOONSC                  GET NEXT CARD
         SPACE 1
CHKEXCLD EQU   *
         TM    PARMSWCH,FLUSHSW        FLUSHING
         BO    IGNORE             YES- IGNORE THIS CARD
         TM    COMDCDSW,SELECTSC       SELECT CARD AHEAD
         BO    MULTS              YES- ERROR TO HAVE BOTH
         LA    GR7,RETRNEV             RETURN ADDRESS
         TM    COMDCDSW,EXCLUDES       MULTIPLE EXCLUDES
         BZ    TESTCSE            NO
         B     MULTSET                 SET MULTIPLE S/E SWITCH
         SPACE 1
RETRNEV  EQU   *
         OI    COMDCDSW,EXCLUDES       SET EXCLUDE SWITCH
         NI    COMDCDSW,FF-MEMBRCD1    INDIC MEMBER KEYWORD FOR  A38724
*           THIS EXCLUDE COMMAND NOT YET FOUND                   A38724
         B     TOSCAN                  CONTINUE
         SPACE 1
CHKSELCT EQU   *
         LA    GR7,SELCTOK             RETURN FROM FOLLOWING ROUTINE
         TM    PARMSWCH,FLUSHSW        DOING A FLUSH
         BO    IGNORE             YES- SKIP THIS CARD
         TM    COMDCDSW,EXCLUDES       EXCLUDE CARD AHEAD OF SELECT
         BO    MULTS              YES- ERROR TO MIX THEM
         TM    COMDCDSW,SELECTSC       PREVIOUS SELECT
         BZ    TESTCSE            ONLY ONE SELECT
MULTSET  EQU   *
         OI    CCSWITCH,MULTSE         MULTIPLE SELECT/EXCLUDE
TESTCSE  EQU   *
         TM    PARMSWCH,COMDPART       PART OF COMMAND ON NEXT CARD
         BZ    NOGOON             NO- GO ON AND PRINT CARD
         MVI   PARMSWCH,CRESET0        RESET ENTIRE SWITCH
         B     AFTRPRT                 DO NOT PRINT CARD
         SPACE 1
NOGOON   EQU   *
         BAL   GR9,PRNTCRD             PRINT THE CARD
AFTRPRT  EQU   *
         TM    COMDCDSW,NEWINDD        HAVE AN INDD
         BZ    NOINDDSP           NO- SEQUENCE ERROR
         TM    CCSWITCH,LASTPARM       MORE PARAMETERS ON CARD
         BO    NULLP              NO- MUST HAVE MEMBER KEYWORD
         NI    CCSWITCH,FF-COMDNOW RESET COMMAND WORD SWITCH
         BR    GR7                     RETURN
         SPACE 1
SELCTOK  EQU   *
         OI    COMDCDSW,SELECTSC       SET SELECT SWITCH
         NI    COMDCDSW,FF-MEMBRCD1    INDIC MEMBER KEYWORD FOR  A38724
*           THIS EXCLUDE COMMAND NOT YET FOUND                   A38724
         B     TOSCAN                  CONTINUE
         SPACE 1
INDDANL  EQU   *
         TM    PARMSWCH,FLUSHSW        DOING A FLUSH
         BO    IGNORE             YES- SKIP THIS CARD
         TM    CCSWITCH,IEBCOPYC       IEBCOPY STATEMENTS
         BO    SCANERRM           YES- MIXED MODES
         TM    PARMSWCH,COPYNOW        INDD ON COPY CARD
         BO    NEXTI              YES
         TM    COMDCDSW,SELECTSC+EXCLUDES  JUST GOT SELECT OR EXCLUDE
         BM    RESETIN            YES- DO A COPY
         BAL   GR9,PRNTCRD             PRINT INDD CARD
         TM    COMDCDSW,NEWINDD        JUST HAD AN INDD
         BO    STRMEMBS           ADD THESE NAMES TO TABLE
         NI    CCSWITCH,FF-MULTSE RESET MULTIPLE S/E SWITCH
         B     GOGOGO                  GO BUILD THE INDD TABLE
         SPACE 1
NEXTI    EQU   *
         TM    COMDCDSW,NEWINDD        INDD ON COPY CARD ALREADY
         BO    SCANERR            YES- ONLY ONE INDD PER COPY CARD
GOGOGO   EQU   *
         OI    COMDCDSW,NEWINDD        FLAG AS HAVE AN INDD
         NI    COMDCDSW,FF-SELECTSC-EXCLUDES-MEMBRCD1 RESET SWITCHES
         SPACE 1
**********                                                   **********
**********         BUILD TABLE OF INDD NAMES                 **********
**********                                                   **********
         SPACE 1
         XC    COUNT(LEN2),COUNT  CLEAR COUNT
         XC    ENCT(LEN2),ENCT    CLEAR SE TABLE ENTRY COUNT
         XC    NNCT1(LEN2),NNCT1  CLEAR NEW NAME COUNT
         XC    INDDCT(LEN2),INDDCT CLEAR INDD COUNT
         MVC   SESTOP(LEN4),INBEGIN NO SE OR INDD ENTRIES YET
STRMEMBS EQU   *
         TM    CPARAMSW,DELIMEND       NAME ON NEXT CARD (= IN 71)
         BZ    CONTSC             NO- CONTINUE NORMAL PROCESSING
GETNEXTC EQU   *
         LA    GR10,SCANSOME           RETURN FROM FOLLOWING ROUTINE
         SPACE 1
*  ROUTINE 'GETPARM' READS NEXT CARD AND POSITIONS TO CONTINUATION
*   PARAMETER. GR10 SET ON ENTRY TO ROUTINE WITH RETURN ADDRESS.
         SPACE 1
GETPARM  EQU   *
         BAL   GR9,READCC              GET NEXT CARD
         BAL   GR9,PRNTCRD             PRINT THE CARD
         MVI   CPARAMSW,CRESET0        CLEAR PARAMETER SWITCH
         CLI   CCIMAGE+COL71,BLANKCOL COL 72 BLANK
         BE    ARNDNXT                 YES- NOT CONTINUED
         OI    CPARAMSW,COL72BLK       SET COLUMN 72 NOT BLANK SWITCH
ARNDNXT  EQU   *
         LA    GR6,CCIMAGE+COL70  END OF CARD PARAMETER COLUMN
         BAL   GR9,NAMESCAN            SCAN OVER NAME IF ANY
         SPACE 1
LOOP2    EQU   *
         CLI   SCAN0(GR3),BLANKCOL COLUMN BLANK
         BNE   STRTPARM                NO- START OF PARAMETER
LOOPPARM EQU   *
         LA    GR3,UP1(GR3)       UPDATE TO NEXT COLUMN
         CR    GR3,GR6
         BNH   LOOP2                   SCAN TO COLUMN 71
         B     GETPARM                 GET NEXT CARD
         SPACE 1
STRTPARM EQU   *
         ST    GR3,CSTOREG+SAV8   STORE START OF PARAMETER
         NI    PARMSWCH,FF-COMDPART RESET PARAMETER CONTINUED SWITCH
         OI    CPARAMSW,PARMCOME       SET TO EXPECT PARAMETER
         BR    GR10                    RETURN
         SPACE 1
CONTSC   EQU   *
         L     GR3,CSTOREG+SAV8   COLUMN TO START SCAN
         NI    CCSWITCH,FF-UNECPARN RESET PARENTHESIS SWITCH
         XC    LEFTPCNT(LEN4),LEFTPCNT CLEAR PARENTHESIS COUNT BUFFER
         CLI   SCAN0(GR3),PARENLFT  START WITH LEFT PAREN
         BNE   SCANSOME                GET NEXT PARAMETER
         OI    CCSWITCH,UNECPARN       SET PARENTHESIS SWITCH
         LA    GR3,UP1(GR3)       UP TO ACTUAL NAME- SET 4 BCTR
         MVI   LEFTPCNT+UP1,PARCNT MAKE LEFT PAREN COUNT 1
SCANSOME EQU   *
         NI    PARMSWCH,COPYNOW        RESET ALL BUT COPY NOW SWITCH
         SR    GR7,GR7                 CLEAR PARAMETER SIZE
         BCTR  GR3,GR0            BACK UP POINTER
         SPACE 1
***********************************************************************
*** START OF GENERAL INDD OR MEMBER NAME SCAN ROUTINE AT INDDLOOP.  ***
***********************************************************************
         SPACE 1
INDDLOOP EQU   *
         LA    GR3,UP1(GR3)       UP TO NEXT COLUMN
         TM    CPARAMSW,DELIMEND       UP TO 71
         BZ    NOGET              NO
         BAL   GR10,GETPARM            GET PARAMETER FROM NEXT CARD
NOGET    EQU   *
         BAL   GR9,TESTLAST            CHECK IF UP TO COLUMN 71
         CLI   SCAN0(GR3),COMMA   COMMA
         BH    TRYNAME                 NO- HIGHER THAN COMMA
         BE    CHKCOMMA                YES- DELIMITER
         CLI   SCAN0(GR3),PARENLFT     LEFT PARENTHESIS
         BE    CHKPARNL                YES
         CLI   SCAN0(GR3),PARENRGT     RIGHT PARENTHESIS
         BE    CHKPARNR                YES
         CLI   SCAN0(GR3),ADOLLAR      IS IT A NATIONAL SYMBOL '$'
         BE    STRNAME                 YES- OKAY
         CLI   SCAN0(GR3),BLANKCOL     BLANK
         BNE   BADPARM                 BAD PARAMETER
         TM    PARMSWCH,SCANNAME       BEEN SCANNING NAME
         BO    BLNKSTP                 YES- STOP NAME SCAN
         SPACE 1
SET4READ EQU   *
         CLC   LEFTPCNT(LEN2),RGHTPCNT EQUAL NUMBER LEFT AND RIGHT PARE
         BNE   MISSPARN                ERROR- MISSING PARENTHESIS
         TM    CPARAMSW,COL72BLK       CARD CONTINUED
         BZ    GOONS                   NO GET NEXT CARD
         BAL   GR9,CHK4COMT            CHECK FOR CONTINUED COMMENTS
         B     GOONS                   GET NEXT CARD
         SPACE 1
CHK4COMT EQU   *
         ST    GR9,SARG                SAVE RETURN ADDRESS
         LA    GR6,CCIMAGE+COL71       COLUMN 72
LOOPCOM  EQU   *
         CLI   SCAN0(GR3),BLANKCOL     NEXT COLUMN BLANK
         BNE   ACOMMENT                NO- START OF COMMENT
         LA    GR3,UP1(GR3)
         CR    GR3,GR6                 END OF CARD NO COMMENT FOUND
         BNE   LOOPCOM                 CONTINUE COMMENT SCAN
         B     CONTINV                 INVALID CONTINUATION      A48799
         SPACE 1
ACOMMENT EQU   *
         BAL   GR9,READCC              READ NEXT CARD
         BAL   GR9,PRNTCRD             PRINT THE COMMENT CARD
         CLI   CCIMAGE+COL71,BLANKCOL  COMMENT CONTINUED
         BNE   ACOMMENT                YES- GET NEXT COMMENT CARD
         NI    CPARAMSW,FF-COL72BLK    RESET CONTINUED SWITCH
         L     GR9,SARG                LOAD RETURN ADDRESS
         BR    GR9                     RETURN TO CALLER
         SPACE 1
CHKIFSTP EQU   *
         LA    GR3,UP1(GR3)
         TM    PARMSWCH,COMDPART       UP TO COL 71 PARM CONTINUED
         BO    GETNEXTC                YES- GET NEXT CARD
         CLI   SCAN0(GR3),BLANKCOL     NEXT COLUMN BLANK
         BNE   SCANSOME                NO- SCAN SOME MORE
         B     CHKCONTY                CHECK IF CARD CONTINUED
         SPACE 1
TRYNAME  EQU   *
         CLI   SCAN0(GR3),ANA
         BNL   STRNAME                 OKAY- ALPHABETIC OR NUMERIC
         SPACE 1
*  THREE NATIONAL SYMBOLS ALLOWED IN MEMBER NAMES- 'AT' SIGN, POUND
*    SIGN, AND DOLLAR SIGN. CHECK BEFORE FOR DOLLAR SIGN MUST CHECK
*    HERE FOR 'AT' AND POUND SYMBOL.
         SPACE 1
         CLI   SCAN0(GR3),ANAT         IS IT AN 'AT' SIGN
         BE    STRNAME                 YES- OKAY
         CLI   SCAN0(GR3),APOUND       IS IT A 'POUND' SIGN
         BE    STRNAME            YES - OKAY
         CLI   SCAN0(GR3),ANUNDRSC     AN UNDER SCORE           OY01183
         BE    STRNAME                 YES,OKAY                 OY01183
         CLI   SCAN0(GR3),EQUAL        COULD IT BE AN EQUAL SIGN
         BNE   BADPARM            IF ITS NOT, ITS INVALID
         TM    COMDCDSW,MEMBRCD1       EQUAL SIGN IN MEMBER NAME SCAN
         BO    BADPARM                 YES- NOT ALLOWED
         TM    PARMSWCH,COPYNOW        EQUAL SIGN WITH INDD ON COPY
         BZ    BADPARM                 NO- NOT ALLOWED IF NOT ON COPY
         TM    PARMSWCH,COMDPART       DELIMITER IN COLUMN 71
         BZ    TSTDLM                  NOT IN 71
         OI    CPARAMSW,DELIMEND       SET IN 71 SWITCH
TSTDLM   EQU   *
         TM    CPARAMSW,CONTINY        PARAMETER WAS CONTINUED
         BZ    SETRETOK                NO
         NI    CCSWITCH,FF-COMDNOW     RESET COMMAND SWITCH
         BAL   GR9,KCREATE             RECREATE KEYWORD
         OI    CPARAMSW,PARMCOME  SET BIT
         SPACE 1
*          NAME NOT SCANNED, IF AN EQUAL SIGN IS A KEYWORD, POINTERS
*   ALL SET,   GO TO KPASS AND WILL RETURN TO CHECK KEYWORD JUST AS IF
*   RDCARD ROUTINE HAD SCANNED THE KEYWORD.
         SPACE 1
SETRETOK EQU   *
         NI    PARMSWCH,FF-SCANNAME RESET SCAN OF NAME SW      @ZA07365
         LA    GR14,RETRTOSC           RETURN FROM INITIALIZED 'KPASS'
         ST    GR14,SARG               STORE RETURN SO RDCARD CAN FIND
         B     KPASS                   SET UP AS IF 'RDCARD' SCANNED
         SPACE 1
STRNAME  EQU   *
         LA    GR7,UP1(GR7)            UP COUNT OF CHARACTERS IN NAME
         TM    PARMSWCH,SCANNAME       ALREADY SCANNING NAME
         BO    ALLSCAN                 YES
         OI    PARMSWCH,SCANNAME       SET NAME SCANNING SWITCH
         LR    GR1,GR3                 LOAD POINTER TO START OF NAME
ALLSCAN  EQU   *
         TM    PARMSWCH,COMDPART       CONTINUED NAME
         BZ    SAMECARD                NO- CARD COMPLETE
         OI    CPARAMSW,CONTINY        SET TO CONTINUE ON NEXT CARD
         LA    GR14,INDDLOOP           RETURN ADDRESS
         ST    GR14,SARG               STORE RETURN ADDRESS FOR SCAN
         B     KPART                   SET UP FIRST PART OF PARAMETER
         SPACE 1
SAMECARD EQU   *
         TM    CPARAMSW,DELIMEND       CHARACTER IN 71, NOT CONTINUED
         BZ    INDDLOOP                NO- CONTINUE SCAN
         B     ENDNAME                 STORE NAME
         SPACE 1
CHKPARNL EQU   *
*
*    NAMES ON MEMBER STATEMENT ONLY IN () IF RENAME OR REPLACE OPTION
*   WAS SPECIFIED.  HOWEVER, FOR COMPATIBILITY, ALSO ALLOW LIST OF
*   MBR NAMES WITHIN PARENS.
*
         TM    CCSWITCH,IEBCOPYC       IEBCOPY CONTROL CARDS
         BZ    TSTRGHT                 NO
         NI    PARMSWCH,FF-ONELEFT     RESET LEFT PARENTHESIS SWITCH
         B     CONTCPPY           ADD 1 TO LEFT-PAREN COUNT
         SPACE 1
TSTRGHT  EQU   *
         TM    COMDCDSW,EXCLUDES       REPLACE/RENAME ON EXCLUDE
         BO    NORENAME           IF SO, ITS INVALID
         TM    PARMSWCH,ONELEFT   WAS A LEFT PAREN ALREADY SCANNED
         BO    BADREPLC                YES- INVALID REPLACE SPECIFIED
         OI    PARMSWCH,SET4REPL+ONELEFT  SET SWITCHES FOR REPLACE
         SPACE 1
CONTCPPY EQU   *
         LH    GR6,LEFTPCNT
         LA    GR6,UP1(GR6)            UP COUNT BY 1
         STH   GR6,LEFTPCNT            STORE NEW COUNT
         TM    PARMSWCH,SCANNAME       LEFT PAREN WHILE SCAN OF NAME
         BO    BADREPLC                YES- INVALID REPLACE SPECIFIED
         B     INDDLOOP                CONTINUE SCAN
         SPACE 1
CHKDUPC  EQU   *
         TM    CPARAMSW,DELIMEND       COMMA IN COLUMN 71
         BO    ENDNAME                 YES- END SCAN OF NAME
         CLI   UP1(GR3),COMMA          NEXT CHARACTER ALSO A COMMA
         BE    NULLP                   YES- NULL PARAMETER ERROR
         CLI   UP1(GR3),PARENRGT       IS COMMA FOLLOWED BY RIGHT PAREN
         BE    NULLP              YES - NULL PARAMETER
         B     ENDNAME                 CONTINUE
         SPACE 1
BLNKSTP  EQU   *
         MVI   CCDELIM,BLANKSGN        SET AS DELIMITER A BLANK
ENDNAME  EQU   *
         TM    CPARAMSW,CONTINY        PARM CONTINUED
         BZ    NOCONT                  NO
         NI    CCSWITCH,FF-COMDNOW     RESET COMMAND SWITCH
         BAL   GR9,KCREATE             YES- RECREATE PARAMETER
NOCONT   EQU   *
         LA    GR9,LEN8
         CR    GR7,GR9
         BH    BADPARM                 BAD PARAMETER
         L     GR8,SESTOP              NEXT SLOT IN INDD TABLE
         LR    GR6,GR8            SAVE NEXT SLOT ADDRESS
         LA    GR8,LEN10(GR8)     SEE IF ENOUGH ROOM FOR THIS ENTRY
         C     GR8,HICOR               EXCEEDING TABLE SPACE
         BNL   OUTOFCOR                YES- ERROR
         LR    GR8,GR6            RESTORE NEXT SLOT ADDRESS
         MVC   TABLE0(LEN10,GR8),FLAGBYTS  CLEAR FLAGS, BLANK NAME
         BCTR  GR7,GR0                 DECREMENT COUNT FOR EXECUTE
         EX    GR7,MOVEDDNM            MOVE DD NAME TO TABLE
         NI    PARMSWCH,FF-SCANNAME    RESET SCANNING NAME SWITCH
         TM    COMDCDSW,MEMBRCD1       MEMBER STATEMENT
         BO    MEMBRCD                 YES- HANDLE DATA SET NAMES
         TM    PARMSWCH,SET4REPL       THIS NAME A REPLACE
         BZ    REGNAME                 NO- CONTINUE
         OI    TABLE0(GR8),REPLACOP    SET REPLACE OPTION BIT
         NI    PARMSWCH,FF-SET4REPL-HASNEWNM  RESET SWITCHES
         SPACE 1
REGNAME  EQU   *
         LH    GR6,INDDCT
         LA    GR6,UP1(GR6)            UP DD COUNT
         STH   GR6,INDDCT              STORE NEW COUNT
         LA    GR8,LEN10(GR8)          NEXT SLOT IN INDD BUFFER
         ST    GR8,SESTOP              END OF SE TABLE
         SR    GR7,GR7                 CLEAR COUNT REGISTR
         TM    CCDELIM,BLANKSGN        END OF NAMES REACHED
         BZ    INDDLOOP                NO- SCAN FOR MORE NAMES
         CLI   SCAN0(GR3),COMMA        REALLY STOPPED AT A COMMA
         BE    CHKCONTY                YES
ISDONE   EQU   *
         NI    PARMSWCH,FF-COPYNOW     RESET COPY CARD SCAN SWITCH
         TM    COMDCDSW,NEWOUT         HAD AN OUTDD ON COPY CARD
         BZ    NOINDDSP                NO OUTDD SPECIFIED
         OI    COMDCDSW,COPYDONE+NEWOUT+NEWINDD BE SURE ALL ARE SET
         B     SET4READ                GET NEXT CARD
         SPACE 1
RESETIN  EQU   *
         NI    COMDCDSW,FF-NEWINDD     RESET HAVE AN INDD SWITCH
         LA    GR3,CCIMAGE             START OF CARD
         NI    CPARAMSW,COL72BLK       RESET ALL BUT CONTINUATION PUNCH
         ST    GR3,CSTOREG+SAV8        STORE COLUMN TO SCAN NEXT
         NI    CCSWITCH,CRESET0        CLEAR SWITCH
         B     GETOUT                  RETURN TO MAINSTREAM
         SPACE 1
TESTLAST EQU   *
         LA    GR5,CCIMAGE+COL70       LAST COLUMN (71)
         CR    GR3,GR5                 UP TO COLUMN 71
         BCR   MS7,GR9                 NO- CONTINUE
         OI    CPARAMSW,DELIMEND       CHARACTER IN COLUMN 71
         TM    CPARAMSW,COL72BLK       CONTINUATION PUNCH IN COLUMN 72
         BO    SETCONT                 SET FOR CONTINUED PARAMETER
         MVI   CCDELIM,BLANKSGN        SET TO STOP SCAN
         BR    GR9                     RETURN
SETCONT  EQU   *
         OI    PARMSWCH,COMDPART       SET AS CONTINUED PARAMETER
         BR    GR9                     RETURN
         SPACE 1
CHKCOMMA EQU   *
         TM    PARMSWCH,SCANNAME       SCANNING NAME
         BZ    CHKIFSTP                NO- CHECK IF CAN STOP (N,R),NAME
         TM    CPARAMSW,DELIMEND       COMMA IN COLUMN 71
         BO    CHKCONT                 YES                       A45150
         CLI   UP1(GR3),BLANKCOL       NEXT COLUMN BLANK         A45150
         BNE   NOCMBLNK                NO                        A45150
CHKCONT  EQU   *                                                 A45150
         TM    PARMSWCH,SET4REPL+HASNEWNM REPLACE/RENAME SPECIFIED
         BC    MS5,CONTINV             YES ILLEGAL TO CONTINUE
         TM    CPARAMSW,COL72BLK       PUNCH IN COLUMN 72
         BZ    CONTINV                 NO- CONTINUATION ERROR
         CLI   UP1(GR3),BLANKCOL       NEXT COLUMN BLANK
         BE    BLNKSTP                 YES
NOCMBLNK EQU   *                                                 A45150
         TM    PARMSWCH,SET4REPL       REPLACE EXPECTED
         BZ    CHKDUPC            NO - CHECK NEXT COLUMN FIRST
         NI    PARMSWCH,FF-ONELEFT     RESET LEFT PARENTHESIS SWITCH
         TM    COMDCDSW,MEMBRCD1       DOING MEMBER SCAN
         BO    REPMEMB                 YES CHECK RENAME REPLACE
         SPACE 1
ON2TST   EQU   *
         TM    CPARAMSW,DELIMEND
         BO    CONTINV            INVALID TO CONTINUE REPLACE    A48799
         LA    GR3,UP1(GR3)       UP SCAN POINTER                A48799
         BAL   GR9,TESTLAST       CHECK IF UP TO COLUMN 71       A48799
         TM    CPARAMSW,DELIMEND  UP TO COLUMN 71                A48799
         BO    CONTINV            INVALID TO CONTINUE REPLACE    A48799
         CLC   SCAN0(LEN2,GR3),RPARN  DOES 'R)' FOLLOW           A48799
         BNE   BADREPLC           IF NOT, ITS INVALID
         LA    GR3,UP1(GR3)       UP SCAN POINTER                A48799
         BAL   GR9,TESTLAST            CHECK IF UP TO COLUMN 71  A45185
         SPACE 1
CHKPARNR EQU   *
         TM    PARMSWCH,ONELEFT        WAS SWITCH RESET
         BO    BADREPLC           IF NOT, INVALID CTL CARD
         LH    GR6,RGHTPCNT
         LA    GR6,UP1(GR6)            UP COUNT RIGHT PARENS
         STH   GR6,RGHTPCNT            STORE NEW COUNT
         CLC   LEFTPCNT(LEN2),RGHTPCNT COMPARE PARENTHESIS COUNT
         BNE   CHKIFUNC                CHECK IF STARTED WITH LEFT PAREN
         TM    CCSWITCH,UNECPARN       STARTED WITH PARENTHESIS
         BZ    ALLOK                   NO
         CLI   UP1(GR3),BLANKCOL       NEXT COLUMN BLANK
         BNE   MORETEST           KEEP ON
         TM    COMDCDSW,MEMBRCD1  MEMBER NAME
         BO    ALLOK                   YES,STORE NAME
         TM    PARMSWCH,SCANNAME  WAS A NAME BEING SCANNED
         BO    BLNKSTP                 IF SO, GO TO STORE THE NAME
         B     ISDONE             DD NAME STORED
MORETEST EQU   *
         CLI   UP1(GR3),PARENLFT       NEXT CHARACTER LEFT PAREN
         BE    MISSPARN                UNEQUAL PARENTHESIS
         CLI   UP1(GR3),PARENRGT       NEXT CHARACTER RIGHT PAREN
         BE    MISSPARN                UNEQUAL PARENTHESIS
         TM    PARMSWCH,COPYNOW        COPY CARD                 A41802
         BO    ALLOK                   YES                       A41802
         B     BADREPLC                INVALID CTL CARD          A41802
ALLOK    EQU   *
         TM    PARMSWCH,SCANNAME       SCANNING NAME
         BO    NAMEFND                 YES- NAME COMPLETE        A45174
         TM    CPARAMSW,DELIMEND       UP TO 71                  A45174
         BO    GOONS                   YES- GET NEXT CARD        A45174
         B     INDDLOOP                NO- CONTINUE SCAN         A45174
NAMEFND  EQU   *                                                 A45174
         TM    CPARAMSW,DELIMEND       UP TO 71
         BO    BLNKSTP                 YES- SET AS IF BLANK STOPPED
         CLI   UP1(GR3),BLANKCOL       IS NEXT COLUMN BLANK
         BNE   ENDNAME                 NO- END SCAN OF THIS NAME
         B     BLNKSTP                 SET STOPPED ON BLANK SWITCH
         SPACE 1
CHKIFUNC EQU   *
         TM    CCSWITCH,UNECPARN       STARTED WITH PARENTHESIS
         BZ    MISSPARN                NO- UNEQUAL PAREN COUNT
         B     ENDNAME                 STORE NAME
         SPACE 1
MEMBRANL EQU   *
         TM    COMDCDSW,SELECTSC+EXCLUDES   SELECT OR EXCLUDE
         BZ    BADMEMBS           IF NEITHER, CANT SAY 'MEMBER'
         TM    CCSWITCH,IEBCOPYC  IEBCOPY CONTROL STATEMENTS
         BO    SCANERRM           YES MIXED CODES
         TM    COMDCDSW,MEMBRCD1  WAS MEMBER KEYWRD FOUND ALRDY  A38724
         BO    BADMEMBS           YES-CANT SAY 'MEMBER'          A38724
         TM    CCDELIM,EQUALSGN   WAS LAST DELIMITER AN EQUAL? @ZA13792
         BNO   PRTBAD             NO,GIVE ERROR MSG 104        @ZA13792
         OI    COMDCDSW,MEMBRCD1       SET MEMBER KEYWORD SWITCH
         TM    CCSWITCH,MULTSE         CONSECUTIVE S/E
         BO    STRMEMBS                ADD NAMES TO TABLE
         MVC   SEBEGIN(LEN4),SESTOP    START OF SE TABLE
         B     STRMEMBS                START INTERNAL MEMBER NAME SCAN
         SPACE 1
REPMEMB  EQU   *
         LA    GR3,UP1(GR3)            UP TO NEXT NAME
         BAL   GR9,TESTLAST            CHECK IF UP TO COLUMN 71
         CLI   SCAN0(GR3),ADOLLAR      VALID MEMBER NAME CHARACTER
         BE    OK4NAMEM                YES
         CLI   SCAN0(GR3),COMMA        NEXT CHARACTER A COMMA
         BE    CHK4R                   YES- MUST BE R)
         BL    BADPARM            IF LT COMMA, ITS AN INVALID CHARACTER
OK4NAMEM EQU   *
         ST    GR3,CSTOREG+SAV4        SAVE POINTER START OF NEW NAME
         SR    GR10,GR10               SECOND PARAMETER COUNT REGISTER
         SPACE 1
NAME2LOP EQU   *
         LA    GR10,UP1(GR10)          UP NAME CHARACTER COUNT
         TM    CPARAMSW,DELIMEND
         BO    CONTINV               INVALID TO CONTINUE REPLACE A48799
         CLI   SCAN0(GR3),ANA          USUAL CHARACTERS IN NAME
         BNL   OKCHAR                  YES- VALID CHARACTER
         CLI   SCAN0(GR3),ADOLLAR      A DOLLAR SIGN
         BE    OKCHAR                  YES, OK
         CLI   SCAN0(GR3),ANAT         AN 'AT' SIGN
         BE    OKCHAR                  YES- VALID CHARACTER
         CLI   SCAN0(GR3),APOUND       A 'POUND' SIGN
         BNE   BADPARM                 WILL NOT BE , OR ) HERE
OKCHAR   EQU   *
         LA    GR3,UP1(GR3)            POINT TO NEXT CHARACTER
         BAL   GR9,TESTLAST            TEST IF IN COLUMN 71
         CLI   SCAN0(GR3),COMMA        COMMA
         BL    CHKRTPAR                CHECK IF RIGHT PARENTHESIS
         BH    NAME2LOP                SCAN TO END OF NAME
         OI    PARMSWCH,SET4REPL+HASNEWNM  SET REPLACE-NEW NAME SWITCH
         B     ON2TST                  UP POINTER AND RIGHT PAREN COUNT
         SPACE 1
CHKRTPAR EQU   *
         CLI   SCAN0(GR3),ADOLLAR      VALID MEMBER NAME CHARACTER
         BE    NAME2LOP                YES
         CLI   SCAN0(GR3),PARENRGT     RIGHT PARENTHESIS
         BNE   BADPARM            IF NOT, ITS AN INVALID CHARACTER
         NI    PARMSWCH,FF-SET4REPL    RESET REPLACE SWITCH
         OI    PARMSWCH,HASNEWNM       SET NEW NAME- NO REPLACE
         B     CHKPARNR                UP RIGHT PAREN COUNT
         SPACE 1
CHK4R    EQU   *
         OI    PARMSWCH,SET4REPL       SET REPLACE- NO NEW NAME SW
         B     ON2TST                  UP POINTERS
         SPACE 1
MEMBRCD  EQU   *
         TM    PARMSWCH,HASNEWNM       HAVE A NEW NAME
         BZ    ONLYOLD                 NO
         OI    TABLE0(GR8),SEBIT2      FLAG AS HAS A NEW NAME
ONLYOLD  TM    PARMSWCH,SET4REPL       REPLACE OPTION
         BZ    NOREPL                  NO REPLACE
         OI    TABLE0(GR8),REPLACOP    SET REPLACE OPTION
NOREPL   EQU   *
         TM    PARMSWCH,HASNEWNM       NEW NAME
         BZ    NAMEDONE           NO
         LA    GR9,LEN8
         LA    GR8,LEN10(GR8)          UP SE TABLE POINTER
         CR    GR10,GR9                NEW NAME GREATER THAN 8 BYTES
         BH    BADPARM            IF SO, ITS TOO LONG
         MVC   TABLE0(LEN10,GR8),FLAGBYTS CLEAR FLAGS, BLK NAME SE TAB
         L     GR1,CSTOREG+SAV4        GET START OF NEW NAME
         BCTR  GR10,GR0                DECREMENT LENGTH FOR EXECUTE
         EX    GR10,MOVEDDNM           MOVE MEMBER NAME TO SE TABLE
         OI    TABLE0(GR8),SEBIT1      FLAG AS THIS IS NEW NAME
         LH    GR6,ENCT
         LA    GR6,UP1(GR6)
         STH   GR6,ENCT                UP SE ENTRY COUNT
         LH    GR6,NNCT1
         LA    GR6,UP1(GR6)            INCREMENT NEW NAME COUNT
         STH   GR6,NNCT1               STORE UPPED NEW NAME COUNT
         TM    PARMSWCH,SET4REPL       REPLACE OPTION
         BZ    NAMEDONE                NO REPLACE
         OI    TABLE0(GR8),SEBIT3      FLAG AS MEMBER REPLACE OPTION
         SPACE 1
NAMEDONE EQU   *
         LA    GR8,LEN10(GR8)          UP TO NEXT SLOT SE TABLE
         LH    GR6,ENCT
         LA    GR6,UP1(GR6)
         STH   GR6,ENCT                UP SE ENTRY COUNT
         NI    PARMSWCH,FF-SET4REPL-HASNEWNM  RESET SWITCHES
         ST    GR8,SESTOP              END OF SE TABLE
         SR    GR7,GR7
         TM    CCDELIM,BLANKSGN        JUST REACHED A BLANK
         BZ    INDDLOOP                NO- SCAN TILL BLANK
         CLI   SCAN0(GR3),COMMA        REALLY STOPPED AT COMMA
         BNE   SET4READ                NO- ALL DONE
         SPACE 1
CHKCONTY EQU   *
         TM    CPARAMSW,COL72BLK       PUNCH IN CONTINUE COLUMN 72
         BZ    CONTINV            IF NOT, SET UP INVALID CONTINUE MSG
         MVI   CCDELIM,CRESET0         RESET STOPPED ON BLANK SWITCH
         LA    GR10,SCANSOME           RETURN FROM READING NEXTCARD
         B     GETPARM                 GET NEXT CARD
         SPACE 1
TYPCPANL EQU   *
         TM    PARMSWCH,COPYNOW        ON COPY STATEMENT
         BZ    SCANSEQR                NO- SEQUENCE ERROR
         TM    CCSWITCH,FIRSTSCN       FIRST ENTRY
         BZ    SCANERRM                NO, MIXED MODE
         OI    CCSWITCH,IEBCOPYC       FLAG AS IEBCOPY CONTROL CARDS
         BAL   GR14,RDCARD             GET PARAMETER
         CLI   CSTOREG+LEN3,PARCNT  PARAMETER COUNT GREATER THAN 1
         BNE   BADPARM            IF SO, INVALID CTL STMT
         L     SCANADR,CSTOREG+SAV4    START OF PARAMETER
         CLI   SCAN0(SCANADR),EX       E FOR EXCLUDE
         BNE   CHKIS                   CHECK IF INCLUSIVE (SELECT)
         OI    COMDCDSW,EXCLUDES       SET EXCLUDE SWITCH
         B     CHLAST                  CHECK IF LAST PARAMETER SCANNED
         SPACE 1
CHKIS    EQU   *
         CLI   SCAN0(SCANADR),IN       I FOR SELECT
         BNE   BADPARM                 BAD PARAMETER
         OI    COMDCDSW,SELECTSC       SET SELECT SWITCH
         B     CHLAST                  CHECK IF LAST PARAMETER
         SPACE 1
BADPARM  EQU   *
         LA    GR2,INVALSPR
         B     GIVESCNE           PRT CARD, SET RC TO 4, WRT MSG
         SPACE 1
INVALODL EQU   *
         LA    GR2,INVALIST            OUTDD NOT ON COPY CARD
         B     GIVESCNE           PRT CARD, SET RC TO 4, WRT MSG
         SPACE 1
BADMEMBS EQU   *
         LA    GR2,MEMNOSE
         B     GIVESCNE           PRT CARD, SET RC TO 4, WRT MSG
         SPACE 1
CONTINV  EQU   *                                                 A48799
         LA    GR2,INVALCON            INVALID CONTINUATION      A48799
         B     GIVESCNE                GIVE MESSAGE              A48799
         SPACE 1
BADREPLC EQU   *
         LA    GR2,INVALREP
         B     GIVESCNE           PRT CARD, SET RC TO 4, WRT MSG
         SPACE 1
NORENAME EQU   *
         LA    GR2,NORREN
         B     GIVESCNE           PRT CARD, SET RC TO 4, WRT MSG
         SPACE 1
MISSPARN EQU   *
         LA    GR2,ONEQPARN       MESSAGE CODE
         B     GIVESCNE           PRT CARD, SET RC TO 4, WRT MSG
         SPACE 1
PRTNULL1 EQU   *
         TM    PARMSWCH,FLUSHSW        FLUSHING
         BO    IGNORE                  YES- GET NEXT CARD
NULLP    EQU   *
         LA    GR2,NULLPARM            NULL PARAMETER MESSAGE
         B     GIVESCNE           PRT CARD, SET RC TO 4, WRT MSG
         SPACE 1
NOINDDSP EQU   *
         LA    GR2,NOINDD
         B     GIVESEQS                GIVE MESSAGES
         SPACE 1
OUTOFCOR EQU   *
         LA    GR2,COREGONE
         B     ERRSTOP                 GIVE MESSAGE AND QUIT
         SPACE 1
SCANERRM EQU   *
         LA    GR2,MODEERR
         B     ERRSTOP                 GIVE MESSAGE
         SPACE 1
MULTS    EQU   *
         LA    GR2,MULTSSEE
         SPACE 1
GIVESEQS EQU   *
         BAL   GR11,WRITEOUT      PRT CARD, SET RC TO 4, WRT MSG
SCANSEQR EQU   *
         LA    GR2,SEQERROR            STATEMENT SEQUENCE ERROR
         B     ERRSTOP                 GIVE MESSAGE
         SPACE 1
MSSEQER  EQU   *
         LA    GR2,SEQERROR
         BAL   GR11,WRITEOUT      PRT CARD, SET RC TO 4, WRT MSG
         B     NEWSCAN                 SCAN THIS COPY- DON'T FLUSH IT
         SPACE 1
COMPEXEC EQU   *
         LA    GR2,PARMEXEC            MESSAGE NUMBER
         LA    GR11,COMPRETN           RETURN TO OUTDD ANAL
         NI    COMDCDSW,FF-COMPRESS    RESET COMPRESS SWITCH
         B     SETC4                   GIVE MESSAGE              A44144
         SPACE 1
GIVESCNE EQU   *
         BAL   GR11,WRITEOUT      PRT CARD, SET RC TO 4, WRT MSG
SCANERR  EQU   *
         LA    GR2,SCANMSG
ERRSTOP  EQU   *
         TM    PARMSWCH,FLUSHSW        STATEMENT ERROR WHILE FLUSHING
         BO    IGNORE             YES
         MVI   PARMSWCH,FLUSHSW        SET FLUSH SWITCH
         TM    CPARAMSW,COL72BLK       IS THIS CARD CONTINUED    A48742
         BZ    WRITEOUT                NO                        A48742
         OI    SCANSWCH,NOCMMEXP       SET BYPASS SCAN SWITCH    A48742
         SPACE 1
WRITEOUT EQU   *
         TM    CCSWITCH,CARDPRTD       WAS CARD PRINTED
         BO    SETC8                   YES                       A44144
         BAL   GR9,PRNTCRD             PRINT THE CARD
SETC8    EQU   *                                                 A44144
         CLI   RCBUF,COMPCDE           IS RETURN CODE 8 OR MORE  A44144
         BNL   STRMESS                 YES- DO NOT CHANGE IT     A44144
         MVI   RCBUF,COMPCDE           SET CODE TO 8             A44144
         B     STRMESS                 AND STORE MSG NUMBER      A44144
SETC4    EQU   *
         CLI   RCBUF,COMPCD            IS COMPLETION CODE 4 OR MORE
         BNL   STRMESS                 YES- DO NOT CHANGE IT
         MVI   RCBUF,COMPCD            SET CODE TO 4
STRMESS  EQU   *
         STH   GR2,MSGLIST             STORE MESSAGE NUMBER
         MVI   MSGLIST,LASTMSG         FLAG AS LAST MESSAGE
KEEPCODE EQU   *
         L     GR15,VIEBVMS            MESSAGE WRITER
         BALR  GR14,GR15               GIVE THE MESSAGE
         TM    PARMSWCH,FLUSHSW        ERROR
         BCR   MS8,GR11                RETURN CARD OR MESSAGE PRINTED
         NI    PARMSWCH,FF-FLUSHSW     RESET FLUSH SWITCH
         NI    SCANSWCH,FF-NOCMMEXP    RESET BYPASS SCAN SWITCH  A48742
         MVI   VTMFLG1,UNUSEND         SET SWITCH FOR VTM
         L     GR15,VIEBVTM            ADDRESS OF TERMINATE ROUTINE
         BALR  GR14,GR15               ENTER TERMINATE-- NO RETURN
         SPACE 1
IEBSEF   EQU   *                       SYSIN EODAD ROUTINE
IEBSCF   EQU   IEBSEF
         OI    CCSWITCH,SYSINEOF       FLAG AS END OF FILE SYSIN
         TM    CPARAMSW,COL72BLK  EOF AFTER CONTINUED CARD
         BO    CONTINV            YES- INVALID CONTINUATION
         TM    PARMSWCH,FLUSHSW        EOF WHILE FLUSHING CARDS
         BO    TERMS                   GIVE EOF MESSAGE AND QUIT
         TM    COMDCDSW,COPYDONE       HAD A COPY STATEMENT
         BZ    FULLCOPY                NO- IEBCOPY FULL COPY
         TM    CCSWITCH,IEBCOPYC       IEBCOPY CONTROL CARDS
         BO    CHKSMEM                 YES- CHECK IF HAVE ALL CARDS
         TM    COMDCDSW,NEWOUT+NEWINDD HAD AN OUTDD AND AN INDD
         BO    GETOUT                  BEGIN COPY
         BM    NOINDDSP                NO- SET TO ISSUE ERROR MESSAGE
         SPACE 1
FULLCOPY EQU   *
         OI    CCSWITCH,IEBCOPYC       FLAG AS IEBCOPY STATEMENT
         BAL   GR9,SET4OLDC            SET UP INDD TABLE ENTRY SYSUT1
         TM    CCSWITCH,SYSINEOF       LAST CARD AN EOF
         BO    SETODS                  YES- DON'T READ ANY MORE CARDS
         OI    COMDCDSW,COPYDONE       SET AS COPY CARD SCANNED
         BAL   GR9,READCC              READ ANOTHER CARD
         SPACE 1
SETODS   EQU   *
         OI    FLG4,NEWODS+NEWCOPOP    SET SWITCHES TO INDICATE A NEW
*                                        COPY OPERATION HAS BEEN FOUND
         SPACE 1
GETOUT   EQU   *
         MVC   COUNT(LEN2),ENCT        NEED SE COUNT EXCLUDE/SELECT
         TM    COMDCDSW,SELECTSC       SELECT OPERATION
         BZ    TESTEX                  NO- EXCLUDE OR FULL COPY
         MVC   CTAD(LEN4),SEBEGIN      STORE CTAD FOR SELECT COPY
         B     DOCOPY                  CONTINUE
         SPACE 1
TESTEX   EQU   *
         XC    NNCT1(LEN2),NNCT1       CLEAR NEW NAME COUNT
         XC    ENCT(LEN2),ENCT         CLEAR FOR EXCLUDE/FULL
         MVC   CTAD(LEN4),SESTOP       SET TO SESTOP ON FULL/EXCLUDE
         TM    COMDCDSW,EXCLUDES       EXCLUSIVE COPY
         BO    DOCOPY                  YES- FINISH UP
         XC    COUNT(LEN2),COUNT       CLEAR SE COUNT ON FULL COPY
         MVC   SEBEGIN(LEN4),SESTOP    FULL COPY NO ENTRIES IN SE TABLE
         SPACE 1
DOCOPY   EQU   *
         NI    CCSWITCH,FF-FIRSTSCN    TURN OFF FIRST ENTRY SWITCH
         L     GR13,LEN4(GR13)         SAVE AREA POINTER
         RETURN (14,12)                RETURN TO CALLER
         SPACE 1
TERMS    EQU   *
         LA    GR2,ENDMESS             SYSIN EOF MESSAGE
         OI    PARMSWCH,FLUSHSW        SET TO RETURN TO TERMINATE
         OI    IOEF2,NOSYSIN           SET SWITCH FOR VTM
         B     STRMESS                 GIVE MESSAGE- NO RETURN
         SPACE 1
READCC   EQU   *
         LA    GR0,CCIMAGE             CONTROL CARD BUFFER
         LA    GR1,CARDCB              SYSIN DCB
         LA    GR8,SYSINERX            SYSIN ERROR RETURN ADDRESS
         GET   (GR1),(GR0)             GET NEXT STATEMENT
         NI    CCSWITCH,FF-CARDPRTD    RESET CARD PRINTED SWITCH
         BR    GR9                     RETURN
         SPACE 1
SYSINERX EQU   *
         MVI   VTMFLG1,UNUSEND         SET SWITCH FOR VTM
         OI    IOEF2,NOSYSIN
         OI    CCSWITCH,SYSINEOF       FAKE AN EOF READ
         OI    PARMSWCH,FLUSHSW        SET FLUSH SWITCH
         B     KEEPCODE                GIVE SYNAD MESSAGE
         SPACE 1
PRNTCRD  EQU   *
         MVI   MSGLIST,LASTMSG+CTLCD   SET TO PRINT CONTROL CARD
         MVI   MSGLIST+UP1,CRESET0     CLEAR MSG NUMBER
         L     GR15,VIEBVMS            MESSAGE WRITER
         BALR  GR14,GR15               PRINT THE CARD
         OI    CCSWITCH,CARDPRTD       SET CARD PRINTED SWITCH
         BR    GR9                     RETURN- CARD IS PRINTED
         SPACE 1
CHKCOPY  EQU   *                       ANALYSE COPY COMMAND
         TM    PARMSWCH,FLUSHSW        DOING A FLUSH
         BO    NEWSCAN                 YES- RESET COMMAND SWITCHES
         NI    CCSWITCH,FF-COMDNOW-MULTSE  RESET SWITCHES
         TM    COMDCDSW,COPYDONE       PREVIOUS COPY DONE
         BZ    NEWSCAN                 NO- SCAN REST OF CARD
         SPACE 1
*  NOW MUST ENTER MAINFLOW TO FINISH UP PREVIOUS COPY OPERATION
         SPACE 1
KEEPCOPY EQU   *
         TM    CCSWITCH,IEBCOPYC       HAVE IEBCOPY CONTROL CARDS
         BO    CHKSMEM                 YES- CHECK IF HAVE ALL CARDS
         TM    COMDCDSW,NEWINDD        INDD KEYWORD BEFORE COPY
         BZ    MSSEQER                 NO- SEQUENCE ERROR COPY FOLLOWS
         NI    COMDCDSW,SELECTSC+EXCLUDES+COMPRESS+LISTSW RESET REST
         NI    CPARAMSW,COL72BLK       KEEP FLAG COLUMN 72 NOT BLANK ON
         MVI   CCSWITCH,CRESET0        RESET SWITCH
         OI    PARMSWCH,COPYNOW        SET SWITCH TO SHOW COPY PENDING
         B     GETOUT                  FINISH THIS COPY STEP
         SPACE 1
NEWSCAN  EQU   *
         MVI   PARMSWCH,COPYNOW        SET FOR SCAN, RESET FLUSH
         NI    SCANSWCH,FF-NOCMMEXP    RESET BYPASS SCAN SWITCH  A48742
         TM    CCSWITCH,CARDPRTD       IF CONTINUED WAS PRINTED
         BO    PRNTDONE                ALREADY PRINTED
         MVI   LINECT,CRESET0          RESET LINE COUNT TO FORCE HEADER
         BAL   GR9,PRNTCRD             PRINT HEADER AND COPY CARD
PRNTDONE EQU   *
         NI    COMDCDSW,COMPRESS       SAVE COMPRESS FLAG
         NI    CPARAMSW,FF-PARTPARM-CONTINY  RESET SOME SWITCHES
         TM    CCSWITCH,LASTPARM       LAST PARAMETER SCANNED
         BZ    TOSCAN                  NO- GET PARAMETERS
         TM    CCSWITCH,FIRSTSCN       FIRST ENTRY
         BZ    SCANERRM                NO, MIXED CODE
         B     FULLCOPY                DO FULL COPY ONLY
         SPACE 1
CHKSMEM  EQU   *
         TM    COMDCDSW,EXCLUDES+SELECTSC  SELECT OR EXCLUDE
         BZ    SETODS                  NO
         TM    COMDCDSW,MEMBRCD1       HAVE MEMBER CARD
         BO    SETODS                  YES
         TM    COMDCDSW,EXCLUDES       EXCLUSIVE COPY- NO MEMBER NAMES
         BO    DOFULLY                 DEFAULT TO FULL COPY
         LA    GR2,NOCOPY              WILL NOT COPY
         B     ERRSTOP                 GIVE MESSAGE AND QUIT
         SPACE 1
DOFULLY  EQU   *
         NI    COMDCDSW,FF-EXCLUDES    RESET EXCLUSIVE COPY INDICATOR
         LA    GR2,DOFULLCP
         BAL   GR11,SETC4              GIVE MESSAGE              A44144
         BAL   GR9,SET4OLDC            SET UP INDD TABLE
         B     SETODS                  CONTINUE
         EJECT
***********************************************************************
*                      CONTROL STATEMENT ANALYSIS ROUTINES            *
***********************************************************************
         SPACE 1
RDCARD   EQU   *
         MVI   CCDELIM,CRESET0         RESET DELIMITER SWITCH
         ST    GR14,SARG               SAVE RETURN REGISTER
         TM    CPARAMSW,READ1          IS READ CARD SWITCH ON
         BZ    GOTCARD                 DON'T READ ANOTHER CARD YET
         SPACE 1
KGTCD    EQU   *
         NI    CPARAMSW,PARTPARM+CONTINY+COL72BLK SAVE ALL CONT SWITS.
         BAL   GR9,READCC              GET NEXT CARD FROM SYSIN
         NI    CPARAMSW,FF-COL72BLK    RESET CONTINUED CARD SWITCH
         CLI   CCIMAGE+COL71,BLANKCOL  COLUMN 72 BLANK
         BE    SCANCHK                 YES                       A48742
         OI    CPARAMSW,COL72BLK       COLUMN 72 NOT BLANK
SCANCHK  EQU   *                                                 A48742
         TM    SCANSWCH,NOCMMEXP       SCAN NEEDED               A48742
         BO    IGNORE                  NO                        A48742
         B     KPFOL                   START SCAN                A48742
         SPACE 1
GOTCARD  EQU   *
         L     GR3,CSTOREG+SAV8        LOAD ADDRESS OF LAST PASS
         TM    CPARAMSW,PARMCOME       DOES A PARAMETER FOLLOW
         BO    SCANCARD                YES- SCAN THE CARD
         SPACE 1
KPFOL    EQU   *
         LA    GR9,KOMMD               RETURN ADDRESS
NAMESCAN EQU   *
         LA    GR3,CCIMAGE             START OF CONTROL STATEMENT
         CLC   SCAN0(LEN3,GR3),SLASHEOF  /* CARD
         BE    IEBSEF                  YES- ENTER EODAD ROUTINE
         SR    GR7,GR7                 CLEAR COUNT BUFFER
         LA    GR1,LEN8
KNAME    EQU   *
         CLI   SCAN0(GR3),BLANKCOL     IS COLUMN BLANK
         BCR   MS8,GR9                 FIRST COLUMN BLANK OR NAME ENDS
         LA    GR7,UP1(GR7)            UP CHARACTER COUNT
         CR    GR7,GR1                 NAME GREATER THAN 8 CHARACTERS
         BH    BADPARM                 YES- INVALID NAME
         LA    GR3,UP1(GR3)            GO TO NEXT COLUMN
         B     KNAME                   CHECK FOR BLANK
         SPACE 1
KOMMD    LA    GR3,UP1(GR3)            SPACE TO NEXT COLUMN AFTER NAME
         LA    GR5,CCIMAGE+COL70       LAST COLUMN (71)
         OI    CCSWITCH,COMDNOW        SET COMMAND SWITCH
KABC     CLI   SCAN0(GR3),BLANKCOL     IS COLUMN BLANK
         BNE   SCANCARD                NO- CHECK PARAMETERS
         CR    GR3,GR5                 IS THIS THE END COLUMN
         BE    PRTNULL1                YES- END OF CARD REACHED
         LA    GR3,UP1(GR3)            ADJUST POINTER TO NEXT COLUMN
         B     KABC                    CONTINUE SCAN
         SPACE 1
SCANCARD EQU   *
         MVC   CCDELIM2(UP1),CCDELIM   SAVE DELIMETER SWITCH SETTING
         MVI   CCDELIM,CRESET0         RESET DELIMITER SWITCH
         LR    GR1,GR3                 SAVE POINTER TO PRESENT COLUMN
         ST    GR3,CSTOREG+SAV4        SAVE START OF PARAMETER
         SR    GR7,GR7                 CLEAR LENGTH REGISTER
         LA    GR5,CCIMAGE+COL70       LAST COLUMN (71)
         TM    CPARAMSW,PARTPARM       PARTIAL PARAMETER
         BZ    KCONT                   NO- CONTINUE
         TM    PARMSWCH,COMDPART       COMMAND WORD
         BO    DONTRSTC                YES- KEEP COMDNOW SWITCH ON
         NI    CCSWITCH,FF-COMDNOW     RESET ANALYSING COMMAND SWITCH
DONTRSTC EQU   *
         TM    CCSWITCH,CARDPRTD  WAS CARD PRINTED
         BO    NOPR               IF SO, DONT PRINT IT AGAIN
         BAL   GR9,PRNTCRD        ELSE PRINT CONTINUED CARD
NOPR     EQU   *
         L     GR1,CSTOREG+SAV4        RESTORE POINTER
         TM    CPARAMSW,CONTINY        FROM MEMBER/INDD SCAN
         BZ    KOMPAR                  NO- CONTINUE SCAN
         BCTR  GR3,GR0                 BACK UP POINTER BY ONE
         NI    PARMSWCH,FF-COMDPART    RESET COMDPART SWITCH
         B     TESTRETN                STORE AND RETURN
         SPACE 1
KFORZRO  EQU   *
         TM    CCDELIM,BLANKSGN        IS IT A BLANK
         BZ    BADPARM                 INVALID NAME/PARAMETER
         B     NOTFIRST                NO FIRST PARAMETER PART FAKE IT
         SPACE 1
KCONT    EQU   *
         NI    CCSWITCH,FF-LASTPARM    RESET SWITCH
         CR    GR3,GR5                 SCANNED BEYOND COLUMN 71
         BNH   KOMPAR                  NO- CHECK PARAMETERS
         SPACE 1
NOTFIRST EQU   *
         TM    CPARAMSW,COL72BLK       CONTINUATION PUNCH IN 72
         BZ    CONTINV            IF NOT, CONTINUATION IS INVALID
         SR    GR7,GR7                 NO COUNT FOR FIRST PARAMETER
         B     APARM0                  SET UP FOR PARTIAL PARAMETER
         SPACE 1
KOMPAR   CLI   SCAN0(GR3),EQUAL        IS CHARACTER A DELIMITER
         BH    KEEPON             NO DELIMITER. CONT SCAN      @ZA07365
         TM    PARMSWCH,SCANNAME  SCANNING NAME?               @ZA07365
         BNO   DELIMIT            NO,GO AND TEST DELIMITER     @ZA07365
         CLI   SCAN0(GR3),ADOLLAR IS CHARACTER A DOLLAR?       @ZA07365
         BE    KEEPON             YES.VALID.CONTINUE SCAN      @ZA07365
         CLI   SCAN0(GR3),APOUND  IS CHARACTER A POUND?        @ZA07365
         BE    KEEPON             YES.VALID.CONTINUE SCAN      @ZA07365
         CLI   SCAN0(GR3),ANAT    IS CHARACTER AN AT?          @ZA07365
         BNE   DELIMIT            NO,GO AND TEST DELIMITER     @ZA07365
KEEPON   LA    GR7,UP1(GR7)       ADD ONE TO LENGTH            @ZA07365
         CR    GR3,GR5                 IS THIS THE END COLUMN
         BE    KPTERR                  YES- SAVE PARTIAL PARAMETER
         LA    GR3,UP1(GR3)            UP POINTER TO NEXT COLUMN
         B     KOMPAR                  CONTINUE SCANNING FOR DELIMITER
         SPACE 1
KPTERR   EQU   *
         TM    CCSWITCH,COMDNOW   SCANNING COMMAND WORD
         BZ    NOTCOMD                 NO
         CLI   SCAN0(GR1),AC           WORD STARTS WITH C
         BNE   PRTANDGO                NO
         TM    COMDCDSW,COPYDONE       MIGHT BE COPY
         BO    KEEPCOPY                NOT FIRST COPY
         MVI   LINECT,CRESET0          RESET LINE COUNT TO FORCE HEADER
PRTANDGO EQU   *
         BAL   GR9,PRNTCRD             PRINT COPY CARD
         L     GR1,CSTOREG+SAV4        RESTORE POINTER TO START OF PARM
         OI    PARMSWCH,COMDPART       SET TO KEEP 'COMDNOW' SET
NOTCOMD  EQU   *
         TM    CPARAMSW,COL72BLK       IS COLUMN 72 BLANK
         BZ    KETBYP                  YES- SET LAST PARM GET NEXT CARD
KPART    EQU   *
         LR    GR2,GR7
         BCTR  GR2,GR0                 DECREMENT FOR EXECUTE
         EX    GR2,MOVEP               SAVE PARTIAL PARAMETER
APARM0   EQU   *
         OI    CPARAMSW,PARTPARM       SET PARTIAL PARAMETER SWITCH
         ST    GR7,CSTOREG             SAVE PARTIAL PARAMETER COUNT
         B     KGTCD                   GET NEXT CARD
         SPACE 1
KETBYP   EQU   *
         OI    CCSWITCH,LASTPARM       NO MORE PARAMETERS
KPASS    LA    GR3,UP1(GR3)            UP POINTER TO NEXT COLUMN
KWENT    EQU   *
         NI    CPARAMSW,FF-READ1-PARMZERO  RESET SWITCHES
         ST    GR7,CSTOREG             STORE LENGTH REGISTER
TESTRETN EQU   *
         L     GR14,SARG               RESTORE RETURN REGISTER
         ST    GR1,CSTOREG+SAV4        STORE SCAN ADDRESS REGISTER
         ST    GR3,CSTOREG+SAV8        STORE ADDRESS REGISTER
         BR    GR14                    RETURN TO MAINSTREAM
         SPACE 1
DELIMIT  NI    PARMSWCH,FF-SCANNAME RESET SCAN OF NAME SWMN    @ZA07365
         CR    GR3,GR5            IS THIS END COLUMN           @ZA07365
         BE    SETCC2                  YES- SET INDICATOR
         TM    CPARAMSW,PARTPARM       IS PARTIAL PARAMETER SWITCH ON
         BO    KPPAR                   YES-COMPLETE PARAMETER
         B     TESTPAR                 CHECK PARAMETER
         SPACE 1
SETCC2   EQU   *
         OI    CPARAMSW,DELIMEND       SET DELIMITER IN END COLUMN SW
         CLI   SCAN0(GR3),BLANKCOL     BLANK
         BE    ABLNKCOL                YES
         TM    CPARAMSW,COL72BLK       PUNCH IN COLUMN 72
         BZ    CONTINV            IF NOT, CTL STMT IS INVALID
         TM    CPARAMSW,PARTPARM       PARTIAL PARAMETER
         BO    CONTINV                 SECOND LEVEL CONTINUATION
         SPACE 1
TESTPAR  LTR   GR7,GR7                 TEST IF LENGTH ZERO
         BC    MS2,DELIM               LENGTH NOT ZERO
         OI    CPARAMSW,PARMZERO       SET LENGTH ZERO SWITCH
         SPACE 1
DELIM    CLI   SCAN0(GR3),EQUAL        TEST IF DELIMITER IS AN EQUAL
         BE    KEY                     YES- GO TO KEY WORD ROUTINE
         CLI   SCAN0(GR3),COMMA        TEST FOR COMMA
         BE    PARAMC                  YES- GO TO PARAMETER ROUTINE
         CLI   SCAN0(GR3),BLANKCOL     TEST FOR BLANK
         BNE   BADPARM                 BAD PARAMETER
ABLNKCOL EQU   *
         TM    CCSWITCH,COMDNOW        IS COMMAND SWITCH ON
         BO    KPCMD                   YES-GO TO ADJUST POINTER
         MVI   CCDELIM,BLANKSGN        SET BLANK DELIMITER
DECID    TM    CPARAMSW,DELIMEND       IS DELIMITER IN END COLUMN
         BO    KWENT                   YES- SET UP COUNT OF FIRST PART
         TM    CPARAMSW,PARMZERO       LENGTH EQUAL ZERO
         BO    KFORZRO                 YES- SET UP ZERO LENGTH
         OI    CPARAMSW,PARMCOME       SET PARAMETER FOLLOWS SWITCH
         TM    CCDELIM,BLANKSGN        IS IT A BLANK
         BZ    KPASS              NO- RETURN WITH POINTERS SET
         B     KETBYP                  CONTINUE
         SPACE 1
KEY      EQU   *
         NI    CCSWITCH,FF-COMDNOW     RESET ANALYSING COMMAND SWITCH
         MVI   CCDELIM,EQUALSGN        SET DELIMITER AN EQUAL
         CLI   UP1(GR3),BLANKCOL       COLUMN AFTER = BLANK
         BNE   DECID                   GO TO CHECK DELIMITER
         TM    COMDCDSW,NEWOUT         HAD AN OUTDD
         BZ    NULLP              IF NOT, SET UP NULL PARAMS MSG
         B     PRTNULL1                PRINT THE CARD
         SPACE 1
PARAMC   EQU   *
         MVI   CCDELIM,COMMASGN        SET DELIMITER COMMA
         CLI   UP1(GR3),BLANKCOL       COLUMN AFTER COMMA BLANK
         BNE   DECID                   NO- CONTINUE
         TM    CPARAMSW,COL72BLK       COLUMN 72 BLANK
         BZ    CONTINV            IF NOT, CTL STMT IS INVALID
         B     DECID                   GO TO CHECK DELIMITER
         SPACE 1
KPPAR    EQU   *
        LA    GR9,DELIM               RETURN FROM PARAMETER CREATE
KCREATE  EQU   *
         L     GR8,CSTOREG             SIZE OF FIRST PART OF PARAMETER
         LTR   GR7,GR7            IS LENGTH OF SECOND PART ZERO
         BC    MS8,KOMPL               YES- THE PARAMETER IS COMPLETE
         LA    GR6,SAVEPAPR            NO- ADDRESS OF FIRST PART
         AR    GR6,GR8                 ADDRESS TO MOVE IN SECOND PART
         LR    GR10,GR7
         BCTR  GR10,GR0                DECREMENT COUNT FOR EXECUTE
         EX    GR10,MOVEPP             MOVE SECOND PART TO BUFFER
KOMPL    AR    GR7,GR8                 TOTAL LENGTH OF PARAMETER
         LA    GR1,SAVEPAPR            LOAD PARAMETER BUFFER ADDRESS
         MVC   CCDELIM2(UP1),CCDELIM   SAVE DELIMITER
         MVI   CCDELIM,CRESET0         CLEAR DELIMITER SETTINGS
         NI    CPARAMSW,FF-CONTINY-PARTPARM  RESET CONTINUED SWITCHES
         NI    PARMSWCH,FF-COMDPART    RESET COMDPART SWITCH
         BR    GR9                     RETURN
         SPACE 1
KPCMD    LA    GR3,UP1(GR3)            ADJUST POINTER TO NEXT COLUMN
         CLI   SCAN0(GR3),BLANKCOL     IS COLUMN BLANK
         BNE   KPCMA                   NO- CHECK FOR COMMA
         CR    GR5,GR3                 IS THIS THE LAST COLUMN
         BE    KETBYP                  YES- NO PARAMETER FOLLOWS
         B     KPCMD                   CONTINUE SCAN
         SPACE 1
KPCMA    CLI   SCAN0(GR3),COMMA        IS COMMA THE FIRST CHARACTER
         BE    KETBYP                  YES- NO PARAMETER FOLLOWS
         BCTR  GR3,GR0                 BACK UP REGISTER 3 TO PARAMETER
         OI    CPARAMSW,PARMCOME       SET PARAMETER FOLLOWS SWITCH
         B     KPASS                   GO TO RETURN
         SPACE 1
*
*                 EXECUTE STATEMENTS USED IN SCAN ROUTINE
*
         SPACE 1
MOVEP    MVC   SAVEPAPR(UP1),SCAN0(GR1) STORE PARTIAL PARAMETER
MOVEPP   MVC   SCAN0(UP1,GR6),SCAN0(GR1) RECREATES PARAMETER
OPRLUP3  MVC   SARG(UP1),SCAN0(SCANADR)  MOVE TO SEARCH ARGUMENT
MOVEDDNM MVC   LEN2(UP1,GR8),SCAN0(GR1)  MOVE DD NAME TO INDD TABLE
MOVEOUTD MVC   DDNAME1(UP1,GR6),SCAN0(SCANADR) MOVE OUTDD NAME - OUTDCB
         SPACE 1
LISTALLC DC    C'NO'                   DO NOT LIST MEMBERS COPIED
RPARN    DC    C'R)'                   FLAGS RENAME OR REPLACE
SLASHEOF DC    C'/* '                  SIGNALS EOF
         SPACE 1
FLAGBYTS DC    XL10'00004040404040404040'  INDD SE TABLE ENTRY FORM
BLANKS8  EQU   FLAGBYTS+2              DOUBLE WORD OF BLANKS
         SPACE 1
         SPACE 1
         SPACE 1
PATCHLEN EQU   (*-IEBSCN)/20      LENGTH OF 5 PER CENT PATCH AREA
PATCH    DC    XL(PATCHLEN)'00'   5 PER CENT PATCH AREA          A41780
         EJECT
         SPACE 1
         SPACE 1
         DCBD  DSORG=PS
BOFLGS   EQU   DCBOFLGS-IHADCB         OPEN FLAGS IN DCB
DDNAME1  EQU   DCBDDNAM-IHADCB         DD NAME FIELD IN DCB
SYNADADR EQU   DCBSYNAD-IHADCB         SYNAD ADDRESS IN DCB
EOFADDRS EQU   DCBEODAD-IHADCB+1       EODAD ADDRESS IN DCB
         SPACE 1
         EJECT
IEBMCA   DSECT
         IEBMCA
         SPACE 1
         END
