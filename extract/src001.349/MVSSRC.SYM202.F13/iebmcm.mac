CMCM     TITLE 'COMPRESS-IN-PLACE SUBROUTINE OF IEBCOPY- IEBMCM'
IEBMCM   CSECT
*C582000-594000                                                  A34431
*C938000                                                         A41780
*C270000,876000-891000                                           A44144
*C574500-575000                                                  A48753
*A575100-575200                                                  A48753
*C603500,604500                                                @ZA11964
*A605100,605200                                                @ZA13707
*C605100,605200                                                @ZA13751
*C375000                                                       @ZA26248
**********************************************************************
*
*        FUNCTION - PROCESS FOR IN-PLACE COMPRESSION ALL MEMBERS WHICH
*                   ARE ALREADY IN PLACE.  WHEN THE FIRST NOT-IN-PLACE
*                   MEMBER IS ENCOUNTERED, SET TO HANDLE THIS AND ALL
*                   SUBSEQUENT MEMBERS AS THOUGH THIS WAS A NON-
*                   COMPRESS FULL COPY.
*
*        INPUT - R15 = ADDR OF IEBMCM
*              - R4  = ADDR OF COMMUNICATION AREA
*              - R13 = CALLER'S SAVE AREA
*
*        SUBROUTINES CALLED -
*              IEBVMS - MESSAGE WRITER
*              IEBDRD - READ ROUTINE
*              IEBMGI - FIND INPUT DIR. ENT. USING CTLTAB
*              TTOCC  - CONVERT TTR TO MBBCCHHR
*
*        EXITS - TO THE MAIN PROGRAM (CALLER), WITH R15 SET AS FOLLOWS-
*              - 0 = NEED TO PROCESS,CURRENT AND ANY FURTHER MEMBERS
*                    SIMILARLY TO FULL COPY
*              - 4 = ALL MBRS WERE ALREADY IN PLACE - NONE WERE MOVED
*
*        REGISTER DEFINITION
*
R0       EQU   0                  WORK/PARAM REG
R1       EQU   1                  WORK/PARAM REG
R2       EQU   2                  WORK REG
R3       EQU   3                  WORK REG
R4       EQU   4                  ADDR OF IEBMCA
R5       EQU   5                  WORK REG
R6       EQU   6                  WORK REG
R7       EQU   7                  WORK REG
R8       EQU   8                  WORK REG
R9       EQU   9                  WORK REG
R10      EQU   10                 WORK REG
R11      EQU   11                 WORK REG
R12      EQU   12                 BASE REG
R13      EQU   13                 ADDR OF REG SAVE AREA
R14      EQU   14                 PARAM/LINK REG
R15      EQU   15                 PARAM/LINK REG
         SPACE 2
C8       EQU   C'8'               FOR SETTING COMPLETION CODE    A44144
TKOFLO   EQU   X'20'         MASK FOR TESTING TRACK OVERFLOW IN DCB
ZERO     EQU   0
FF       EQU   X'FF'
         EJECT
         SAVE  (14,12),,*
         BALR  R12,R0             ADDRESSIBILITY
         USING *,R12
         USING IEBMCA,R4          ADDR OF COMMUNICATION AREA
         LA    R2,SV6
         ST    R13,X4(R0,R2)      FORWARD AND BACKWARD LINKAGE
         ST    R2,X8(R13)
         LR    R13,R2
         LA    R1,OUTDCB
         USING IHADCB,R1
         NI    FLG4,FF-LE         ASSURE THAT 'LE' IS OFF
         OI    TAG,TAG8           SET SWITCH FOR IEBVWS        @ZA26248
         TM    TAG,CTOUT          IS THERE A NEED TO COUNT OD BLOCKS
         BZ    RNDX               IF NOT, DONT RESET COUNT
         XC    DIRBCNT(P2),DIRBCNT ELSE INITIALIZE COUNT TO ZERO
         MVI   NDBTR,ZERO          RESET NDBTR                   A36049
RNDX     MVI   SENSE,SENSE4       INDICATE TO READ WITH NO DATA XFR
         NI    STATUS,STAT5+STAT7 RESET MOST STATUS BITS
         OI    STATUS,STAT2       INDIC READING OUTPUT DIRECTORY FROM
*                                 OUTPUT DATA SET
         MVI   OUTSCTOR,ZERO      SET OUTPUT SECTOR VALUE TO 0   S20201
*                                   (USED ONLY IF ON AN RPS      S20201
*                                   DEVICE)                      S20201
         MVC   READNEXT(L4),BCB   SET POINTER FOR READ
         L     R15,VIEBDRD
         BALR  R14,R15            LINK TO READ TO EOF - NO DATA XFR
         NI    FLG2,FF-RDEOF      RESET EOF-REACHED SWITCH
         NI    TAG,FF-CTOUT       INDIC OUTPUT DIRECTORY BLOCKS ALLO-
*                                 CATED ARE NOW COUNTED
         NI    STATUS,STAT5+STAT7 RESET MOST STATUS BITS
         OI    STATUS,STAT4       INDIC READING INPUT MBR DATA
         MVI   INSCTOR,ZERO       SET INPUT SECTOR VALUE TO 0    S20201
*                                   (USED ONLY IF ON AN RPS      S20201
*                                   DEVICE)                      S20201
         OI    FLG7,COP           INDICATE NEED TO GET PTRS IN TTR SEQ
         MVI   SWITCH1,ZERO       RESET IEBMGI'S SWITCH
CMPNGI   L     R15,VIEBMGI
         BALR  R14,R15            GO FIND THE 1ST/NEXT DIRECTORY ENTRY
         TM    SWITCH1,MGINODE    WAS NO IDE RETURNED DUE TO UT3 ERROR
         BZ    GOTTHEDE           NO - AN IDE WAS RETURNED
         NI    SWITCH1,FF-MGINODE RESET 'NO IDE' SWITCH
         MVI   MSG1+P1,WONTCOM    SET MSG CODE - DUE TO ERROR READING
*                                 FROM SYSUT3, COMPRESS-IN-PLACE OPERA-
*                                 TION TERMINATED
         NI    IOEF1,FF-ERF8      RESET POSSIBLE I/O ERROR FLAG
         B     CMCRC              SET RETURN CODE
GOTTHEDE EQU   *
         MVI   SENSE,SENSE4       INDICATE READ WITH NO DATA XFR
*                                 (BIT MAY HAVE BEEN TURNED OFF BY
*                                 IEBMGI IF DIRECTORY ENTRY WAS READ
*                                 FROM SYSUT3)
         L     R7,IDEAD           POINT TO DE JUST LOCATED
         LA    R1,INDCB           POINT TO DCB
         LA    R2,DCBFDAD         AREA TO GET MBBCCHHR
         MVI   WKA1+P3,ZERO
         MVC   WKA1(L3),X8(R7)    SET UP TO CONVERT MBR-TTR
         L     R0,WKA1            SET R0 TO 'TTR0'
         L     R15,VTTOCC
         BALR  R14,R15            GO CONVERT INPUT MBR TTR TO MBBCCHHR
         IC    R6,DCBFDAD+P7      DECREMENT MBBCCHHR OF MBR BY R-1, TO
         BCTR  R6,R0              SET APPROPRIATE SEEK ADDRESS FOR READ
         STC   R6,DCBFDAD+P7
CMBB     LA    R1,OUTDCB
         LA    R2,DCBFDAD
         L     R15,VCCOTT         SET TO CONVERT MBBCCHHR OF LAST REC-
*                                 ORD WRITTEN TO TTR
         BALR  R14,R15            LINK TO ROUTINE
         MVC   OUTDS1(L4),WKA1    UPON RETURN FROM CONVERT ROUTINE, TTR
*                                 IS IN WKA1.  MOVE IT TO AREA FOR USE
*                                 BY IEBWSU ROUTINE
         IC    R6,WKA1+P2              ADD
         LA    R6,X1(R6)               ONE
         STC   R6,WKA1+P2              TO R
         LA    R1,INDCB
         CLC   X8(L3,R7),WKA1     INPUT TTR VS OUTPUT TTR+1
         BE    INPLACE            IF EQUAL, (IN PLACE) FIND      A48753
*                                  EOF OF THIS MEMBER            A48753
         BL    TSTLE              IF LOW, (ALIAS) IGNORE         A48753
*                                  READING THIS MEMBER AGAIN     A48753
         MVC   WORKWORD(L3),X8(R7) MOVE TTR FOR THIS MEMBER      A34431
*                                  TO FULL WORD BOUNDRY          A34431
         LH    R15,WORKWORD       LOAD TT                        A34431
         BCTR  R15,R0              AND DECREMENT BY 1            A34431
         SH    R15,WKA1           DO THE TT'S DIFFER BY MORE     A34431
*                                  THAN ONE                      A34431
         BP    SETPROC            YES, A GAP HAS BEEN FOUND,     A34431
*                                  GO PREPARE TO COPY THIS AND   A34431
*                                  ALL SUBSEQUENT MEMBERS        A34431
         CLI   X10(R7),X1         DOES R OF TTR FOR THIS         A34431
*                                  MEMBER=1                      A34431
         BNE   SETPROC            NO, A GAP HAS BEEN FOUND, GO   A34431
*                                  PREPARE TO COPY THIS AND      A34431
*                                  ALL SUBSEQUENT MEMBERS        A34431
* MCM MUST NOW CHECK TO SEE IF THE FIRST MEMBER RECORD COULD     A34431
* HAVE BEEN WRITTEN ON THE PREVIOUS TRACK FOLLOWING THE LAST     A34431
* EOF THAT WAS READ.  IEBDRD IS CALLED TO READ THE COUNT FIELD   A34431
* OF THE FIRST RECORD OF THE MEMBER.  BY INSPECTING THE KDD PART A34431
* OF IT'S COUNT FIELD AND THE TRACK BALANCE FOLLOWING THE LAST   A34431
* EOF READ, IT CAN BE DETERMINED IF A GAP REALLY EXISTS.         A34431
         SPACE 1                                                 A34431
         LA    R1,INDCB           LOAD PTR TO INPUT DCB          A34431
         MVC   FDADSAVE(L8),DCBFDAD SAVE DCB FDAD FIELD - INDCB  A34431
         MVC   READNEXT(L4),ADNL  SET FOR READ INTO NOTELIST     A34431
*                                   BUFFER                       A34431
         L     R0,WORKWORD        LOAD TTR FOR CONVERSION        A34431
         L     R15,VTTOCC         CONVERSION ROUTINE ADDRESS     A34431
         LA    R2,DCBFDAD         WANT OUTPUT OF CONVERSION      A34431
*                                   IN INPUT DCB                 A34431
         BALR  R14,R15            CONVERT TTR TO FDAD            A34431
         SR    R15,R15            CLEAR WORK REG                 A34431
         IC    R15,X7(R0,R2)      INSERT R OF FDAD               A34431
         BCTR  R15,R0             DECREMENT BY 1                 A34431
         STC   R15,X7(R0,R2)      STORE DECREMENTED R            A34431
         MVI   SENSE,SENSE3       INDICATE READ COUNT FIELD      A34431
         L     R15,VIEBDRD        READ ROUTINE ADDRESS           A34431
         BALR  R14,R15            GO READ FIRST MEMBER RECORD    A34431
*                                   INTO NOTELIST BUFFER         A34431
         MVI   SENSE,SENSE4       RESTORE SENSE (READ WITH NO    A34431
*                                  DATA XFER)                    A34431
         LA    R1,INDCB           POINTER TO INPUT DCB           A34431
         MVC   DCBFDAD(L8),FDADSAVE RESTORE INDCB FDAD           A34431
         MVC   READNEXT(L4),BCB   RESTORE READNEXT               A34431
         SPACE 1                                                 A34431
         LA    R15,INCHAR         POINTER TO INPUT DEVTYPE INFO  A34431
         USING DEVTAB,R15         ADDRESSABILITY USING DEVTAB    A34431
         L     R14,ADNL           POINTER TO RECORD JUST READ    A34431
         SR    R9,R9              CLEAR WORK REG                 A34431
         IC    R9,X9(R0,R14)      INSERT KEY LENGTH              A34431
         MVC   WORKWORD(L2),X10(R14) MOVE DATA LENGTH TO WORD    A34431
*                                   BOUNDRY                      A34431
         AH    R9,WORKWORD        ADD DATA LENGTH TO KEY LENGTH  A34431
         SR    R8,R8              CLEAR WORK REG                 A34431
         IC    R8,OVERL           INSERT LAST RECORD OVERHEAD    A34431
         TM    DEVFLAG,HALFOVER   TWO BYTE OVERHEAD VALUE FOR    A34431
*                                   THIS DEVICE                  A34431
         BNO   NOTTWO        NO,USE ONE BYTE                   @ZA11964
         LH    R8,OVERI           LOAD TWO BYTE OVERHEAD         A34431
NOTTWO   OC    WORKWORD(L2),WORKWORD IS DATA LNGTH ZERO (EOF)  @ZA11964
         BNZ   NOTHALF            NO, GO ON                      A34431
         CLI   DEVCODE+3,X'08'    IS IT A 2314?                @ZA13751
         BE    NOTHALF            YES,DON'T ADD ONE BYTE       @ZA13751
         SPACE 1                                                 A34431
*  FOR I/O DEVICES WITH A TWO BYTE OVERHEAD VALUE, THE ONE BYTE  A34431
* OF DATA WRITTEN BY THE HARDWARE FOR AN EOF RECORD MUST BE      A34431
* INCLUDED IN TRACK BALANCE COMPUTATIONS                         A34431
         SPACE 1                                                 A34431
         LA    R9,X1(R0,R9)       YES, ADD 1 TO KEYLEN PLUS DATA A34431
*                                  LENGTH FOR EOF                A34431
NOTHALF  EQU   *                                                 A34431
         AR    R9,R8              ADD OVERHEAD TO DD + K         A34431
         CLI   X9(R14),X0         IS KEY LENGTH 0                A34431
         BNZ   SKIPDEC            NO, GO AROUND                  A34431
         SR    R8,R8              CLEAR WORK REG                 A34431
         IC    R8,OVERK           INSERT KEY OVERHEAD            A34431
         SR    R9,R8              SUBTRACT KEY OVERHEAD          A34431
SKIPDEC  EQU   *                                                 A34431
         CH    R9,OUTDS1+P4       WOULD THIS RECORD HAVE FIT ON  A34431
*                                   THE PREVIOUS TRACK           A34431
         BNH   SETPROC            YES, A GAP HAS BEEN FOUND, GO  A34431
*                                  PREPARE TO COPY THIS AND ALL  A34431
*                                  SUBSEQUENT MEMBERS            A34431
         B     INPLACE            NO, IT IS INPLACE, FIND EOF OF A34431
*                                  THIS MEMBER                   A34431
* SET UP FOLLOWING MESSAGE -
* IEB583I XXXXXXXX    COMPRESSED - WAS ALREADY IN PLACE AND NOT MOVED
TSTLE    MVC   NAMEFLD(L8),X0(R7) PUT MEMBERNAME INTO MSG PARM AREA
         MVI   MSG1+P1,M59        MESSAGE CODE
         MVI   NAMEDISP,ZERO      DISPLACEMENT FOR NAME IN MSG
         MVI   PARAMS,NAME        INDICATE PRESENCE OF A NAME
         MVI   MSG1,LASTMSG+PBIT
         L     R15,VIEBVMS        ADDR OF MSG WRITER
         BALR  R14,R15            LINK TO WRT MSG
         TM    FLG4,LE            ARE THERE MORE INPUT MBRS TO CHECK
         BZ    CMPNGI                  LOCATE NEXT INPUT DIR. ENTRY
* SET UP MSG CODE FOR MSG -
* IEB585I ALL MEMBERS COMPRESSED - ALL WERE ORIGINALLY COMPRESSED
         MVI   MSG1+P1,M60        MESSAGE CODE
         MVI   MSG1,LASTMSG
         L     R15,VIEBVMS        ADDR OF MSG WRITER
         BALR  R14,R15            LINK TO WRT MSG
         NI    COMDCDSW,FF-COMPRESS    RESET INDICATION OF COMPRESS
* MAKE SURE THAT WHEN THE OUTPUT DATASET IS CLOSED, THE DSCB WILL SHOW
* ALL SPACE AFTER THE LAST MEMBER IS AVAILABLE
         LA    R1,OUTDCB          POINT TO OUTPUT DCB
         MVC   OUTFDAD(L8),DCBFDAD SAVE LAST RECORDS FULL DISK ADDRESS
         MVC   OUTPTTR(L8),OUTDS1  SAVE LAST RECORDS TTRN AND TRKBALAN
FAKERTRN EQU   *
         OI    FLG7,NOPRNBDB+SUPPRMSG SUPPRESS MEMBER NAME AND UNUSED
*                                      BLOCKS MESSAGE.
         LA    R15,X4             SET RET CODE TO GO TO IEBVTM - NO
*                                 MERGE OF DIRECTORY IS REQUIRED
RETURN   MVI   SENSE,ZERO         RESET SENSE4 TO ALLOW DATA XFR LATER
         L     R13,X4(R13)
         RETURN (14,12),RC=(15)   RETURN WITH CODE IN R15
SETPROC  EQU   *                  SET UP TO GO COPY THIS AND ALL SUB-
*                                 SEQUENT MEMBERS
         MVC   OLDTTR(L3),X8(R7)  SAVE THIS INPUT MBR TTR
         SR    R15,R15            SET RET CODE TO GO COPY THIS MBR
         B     RETURN             GO TO RETURN TO MAIN PGM
INPLACE  MVC   OLDTTR(L3),X8(R7)  SET THIS MEMBER TTR AS 'OLD'
         L     R15,VIEBDRD
         BALR  R14,R15            LINK TO READ TO EOF OF THIS MEMBER
         LA    R2,INDCB
         LA    R1,OUTDCB
         MVC   DCBFDAD(L8),FDAD(R2) PUT THIS MEMBER'S EOF MBBCCHHR INTO
*                                 THE DCBFDAD FIELD OF THE OUTPUT DCB
         MVC   OUTSCTOR(L1),INSCTOR  SET OUTPUT SECTOR VALUE     S20201
*                                      EQUAL INPUT SECTOR VALUE  S20201
*                                      (USED ONLY IF ON AN RPS   S20201
*                                      DEVICE)                   S20201
         NI    FLG2,FF-RDEOF      RESET EOF-REACHED SWITCH
         B     TSTLE              SEE IF ANY MORE INPUT MEMBERS
CMCRC    CLI   RCBUF,C8           IS HIGHEST COMPL CODE LT/EQ 8  A44144
         BNL   CMSK8              IF SO, DONT SET IT             A44144
         MVI   RCBUF,C8           ELSE SET COMPLETION CODE TO 8  A44144
CMSK8    MVI   MSG1,LASTMSG       INDIC 1 MSG TO BE WRITTEN      A44144
         MVI   VTMFLG1,UNUSEND    INDICATE UNUSUAL END OF OPERATION -
*                                 RESET 'TERMIN' AND 'IOERROR' IF ON
         MVI   PARMSWCH,FLUSHSW   INDIC TO LOOK FOR NEXT COPY OPERATION
         L     R15,VIEBVMS        ADDR OF MSG ROUTINE
         BALR  R14,R15            GO WRITE MESSAGE
         B     FAKERTRN           GO TO CAUSE MAIN PROGRAM TO EXIT DI-
*                                 RECTLY TO IEBVTM
PATCHLEN EQU   (*-IEBMCM)/20      LENGTH OF 5 PER CENT PATCH AREA
PATCH    DC    XL(PATCHLEN)'00'   5 PER CENT PATCH AREA          A41780
         EJECT                                                   A34431
FDADSAVE DC    2F'0'              DCBFDAD SAVE AREA              A34431
WORKWORD DC    F'0'               FULL WORD WORK AREA            A34431
         EJECT
*   THE FOLLOWING EQUATES ARE USED TO REFERENCE SYMBOLIC LOCATIONS
*  PLUS A CONSTANT
P1       EQU   1
P2       EQU   2
P3       EQU   3
P4       EQU   4                                                 A34431
P7       EQU   7
P8       EQU   8                                                 A34431
P16      EQU   16                                                A34431
P20      EQU   20                                                A34431
P23      EQU   23                                                A34431
*   THE FOLLOWING EQUATES ARE USED FOR INSTRUCTIONS REQUIRING LENGTH
*  SPECIFICATIONS SUCH AS MVC
L1       EQU   1                                                 S20201
L2       EQU   2                                                 A34431
L3       EQU   3
L4       EQU   4
L8       EQU   8
*   THE FOLLOWING EQUATES ARE USED FOR OFFSETS WITH REGISTER NOTATION
X0       EQU   0
X1       EQU   1
X4       EQU   4
X7       EQU   7                                                 A34431
X8       EQU   8
X9       EQU   9                                                 A34431
X10      EQU   10                                                A34431
         EJECT                                                   A34431
DEVTAB   DSECT                    DEVTYPE DSECT                  A34431
DEVCODE  DS    F                  DEVICE TYPE CODE               A34431
MAXSIZE  DS    F                  MAX SIZE UNKEYED BLOCKS        A34431
MAXCC    DS    H                  NUMBER OF CYLINDERS            A34431
MAXHH    DS    H                  NUMBER OF TRACKS PER CYLINDER  A34431
TRKCAP   DS    H                  MAX TRACK LENGTH               A34431
OVERI    DS    XL1                OVERHEAD FOR NOT LAST RECORD   A34431
OVERL    DS    XL1                OVERHEAD FOR LAST RECORD       A34431
OVERK    DS    XL1                OVERHEAD REDUCTION IF NO KEY   A34431
DEVFLAG  DS    XL1                FLAG FIELD                     A34431
TOLER    DS    H                  TOLERANCE FACTOR               A34431
HALFOVER EQU   X'08'              OVERHEAD LAST=OVERHEAD NOT     A34431
*                                  LAST AND IS A 2 BYTE VALUE    A34431
         EJECT
         DS    0D
IEBMCA   DSECT
         IEBMCA
         EJECT
         DCBD  DSORG=PO
FDAD     EQU   DCBFDAD-IHADCB     OFFSET INTO DCB FOR DCBFDAD FIELD
         END
