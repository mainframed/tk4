         TITLE 'BLSR3270--BLSR327J--VALIDATE INPUT'
*---------------------------------------------------------------------*
*                                                                     *
*BLSR327J--VALIDATE INPUT                                             *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
BLSR327J LA    R9,3               ACCOUNT FOR AID, ADDR, ADDR
         LA    R3,LJ000100        RETURN POINT FROM ANALYSIS SUBS
         ST    R7,SAVER7          WILL NEED THIS LATER
         BAL   RE,EA001400        DECOMPOSE CURSOR ADDR INTO ROW/COL
         CH    R5,=H'0'           IS IT ON ROW ONE?
         BNE   LJ000100           -NO, DON'T LOOK ANY FURTHER
         CH    R4,=H'1'           UNDER 3END
         BL    LJ000100           -IF BETWEEN
         CH    R4,=H'4'           -COLUMNS 1-4
         BNH   LD001500           QUIT
LJ000100 SR    R7,R9              ANY INPUT LEFT?
         BNP   LJ001000           -NO, SEE WHAT HAPPENED
         ALR   RA,R9              TO NEXT DATA FIELD DELIMITER
         LA    R1,0(RA,R7)        IN CASE TRT GETS CC=0
         LR    RF,R7              HOW MUCH THERE IS LEFT
         BCTR  RF,0               BYPASS 1ST SBA
         LR    R5,RA              PUT BUFFER ADDRESS IN SAFE PLACE
LJ000102 LR    R4,RF              HOW MUCH THERE IS LEFT
         CH    R4,=H'256'         MORE THAN ONE TRT CAN HANDLE?
         BNH   LJ000104           -NO, PROCEED
         LA    R4,256             -YES, WE WILL HAVE TO LOOP THROUGH
LJ000104 SR    RF,R4              REMEMBER TO SUBTRACT FROM TOTAL
         BCTR  R4,0               DOWN ONE FOR EXECUTE
         EX    R4,IJ000106        LOOK FOR NEXT SBA ORDER
         BNZ   LJ000110           IF NON-ZERO, WE FOUND SBA
         LTR   RF,RF              IS THERE ANYTHING LEFT?
         BNP   LJ000110           -NO
         LA    R5,256(R5)         CORRECT BUFFER ADDRESS
         B     LJ000102           KEEP LOOKING
IJ000106 TRT   1(0,R5),SBATRTTB   *** SUBJ OF EX, ABOVE
         SPACE
*---------------------------------------------------------------------*
*        R1 NOW POINTS TO NEXT SBA, OR PAST END OF DATA               *
*        R2 MAY HAVE BEEN CLOBBERED BY A X'FF'                        *
*---------------------------------------------------------------------*
         SPACE
LJ000110 LR    R9,R1              STOPPING ADDR
         SLR   R9,RA              LENGTH THIS FIELD
         BAL   RE,EA001400        DECOMPOSE FIELD ADDRESS
         ALR   R5,R5              DOUBLE ROW ADDR FOR YCON INDEX
         CH    R5,=H'10'          IN DATA DISPLAY AREA?
         BNL   LJ002900           -YES, SEE WHAT IS GOING ON
         LA    R8,BLSR327B
         AH    R8,YJ000120(R5)    ->ANALYSIS ROUTINE
         BR    R8                 GO TO ANALYSIS ROUTINE
         SPACE
*---------------------------------------------------------------------*
*        PROCESSOR VECTOR FOR MANUAL INPUT RECEIVED FROM LINES 1-5    *
*---------------------------------------------------------------------*
         SPACE
YJ000120 DC    Y(LJ002500-BLSR327B) LINE 1--CONSTANTS
         DC    Y(LJ002600-BLSR327B) LINE 2--ADDRESS IN STACK
         DC    Y(LJ002700-BLSR327B) LINE 3--NOTATION IN STACK
         DC    Y(LJ000150-BLSR327B) LINE 4--INPUT LINE
         DC    Y(LJ002800-BLSR327B) LINE 5--SUBCOMMAND
         SPACE
*---------------------------------------------------------------------*
*        CHANGE TO LINE 4                                             *
*---------------------------------------------------------------------*
         SPACE
LJ000150 IC    R4,AJ000200(R4)    TRANSLATE COLUMN NUM TO INDEX
         LH    RF,YJ000300(R4)    PICK UP BRANCH INDEX VALUE
         LA    R8,BLSR327B(RF)    ->ANALYSIS ROUTINE
         BR    R8
         SPACE
*---------------------------------------------------------------------*
*        DATA FIELD TRANSLATION VECTOR                                *
*---------------------------------------------------------------------*
         SPACE
AJ000200 DC    6AL1(0)            INVALID
         DC    1AL1(2)            ADDRESS FIELD CHANGED
         DC    11AL1(0)           INVALID
         DC    1AL1(4)            ASID FIELD CHANGED
         DC    8AL1(0)            INVALID
         DC    1AL1(6)            FORMAT FIELD CHANGED
         DC    6AL1(0)            INVALID
         DC    1AL1(8)            AREA FIELD CHANGED
         DC    15AL1(0)           INVALID
         DC    1AL1(10)           WINDOW A ALLOCATION CHANGED
         DC    4AL1(0)            INVALID
         DC    1AL1(12)           WINDOW B ALLOCATION CHANGED
         DC    4AL1(0)            INVALID
         DC    1AL1(14)           WINDOW C ALLOCATION CHANGED
         DC    4AL1(0)            INVALID
         DC    1AL1(16)           WINDOW D ALLOCATION CHANGED
         DC    7AL1(0)            INVALID
         DC    1AL1(18)           SKIP FIELD CHANGED
         DC    8AL1(0)            INVALID
         SPACE
*---------------------------------------------------------------------*
*        PROCESSING VECTOR FOR CHANGES ON LINE 4                      *
*---------------------------------------------------------------------*
         SPACE
YJ000300 DC    Y(LC000200-BLSR327B) PROTECTED CAPTION CHANGED
         DC    Y(LJ000400-BLSR327B) ADDRESS CHANGED
         DC    Y(LJ000500-BLSR327B) ASID CHANGED
         DC    Y(LJ000600-BLSR327B) FORMAT CHANGED
         DC    Y(LJ000700-BLSR327B) AREA CHANGED
         DC    Y(LJ000800-BLSR327B) WINDOW A CHANGED
         DC    Y(LJ000800-BLSR327B) WINDOW B CHANGED
         DC    Y(LJ000800-BLSR327B) WINDOW C CHANGED
         DC    Y(LJ000800-BLSR327B) WINDOW D CHANGED
         DC    Y(LJ000900-BLSR327B) SKIP CHANGED
         SPACE
*---------------------------------------------------------------------*
*        ADDRESS FIELD CHANGED                                        *
*---------------------------------------------------------------------*
         SPACE
LJ000400 OI    FLAG1,FLAG1ADR       MUST REWRITE OLD (OR NEW) DATA
         BAL   RE,EA003700          CONVERT AND VALIDATE INPUT
         B     LJ000420             VALIDATION FAILED IN SUBROUTINE
         MVC   EADDRVAL,PACKDEC+3   SAVE NEW ADDRESS
         TM    ERLN4,ERLN4ADR       WAS IT INVALID LAST TIME?
         BNO   LJ000415             -NO, TURN OFF ERROR INDICATOR
LJ000410 MVI   EADDRFLG,VREWRITE    REWRITE THIS ATTRIBUTE
         OI    FLAG0,FLAG0RW4       LINE 4 ORDERS MUST BE REWRITTTEN
LJ000415 NI    ERLN4,255-ERLN4ADR   HAVE VALID ASID
         BR    R3                   PROCESS REMAINING INPUT
LJ000420 OI    ERLN4,ERLN4ADR       INDICATE ERROR ENCOUNTERED
         BR    R3                   PROCESS REMAINING INPUT
         SPACE
*---------------------------------------------------------------------*
*        ASID FIELD CHANGED                                           *
*---------------------------------------------------------------------*
         SPACE
LJ000500 OI    FLAG1,FLAG1ASI       MUST REWRITE OLD (OR NEW) DATA
         BAL   RE,EA003700          CONVERT AND VALIDATE INPUT
         B     LJ000520             VALIDATION FAILED IN SUBROUTINE
         LA    R1,VPASPL            BLSRVPAS PARM LIST
         L     RF,VPASP             ->BLSRVPAS
         BALR  RE,RF                PERFORM MORE SOPHISTICATED CHECK
         LTR   RF,RF                DID ASID PASS TEST?
         BNZ   LJ000520             -NO
         MVC   EASIDVAL,PACKDEC+5   SAVE NEW ASID
         TM    ERLN4,ERLN4ASI       WAS IT INVALID LAST TIME?
         BNO   LJ000515             -NO, TURN OFF ERROR INDICATOR
LJ000510 MVI   EASIDFLG,VREWRITE    REWRITE THIS ATTRIBUTE
         OI    FLAG0,FLAG0RW4       LINE 4 ORDERS MUST BE REWRITTEN
LJ000515 NI    ERLN4,255-ERLN4ASI   HAVE VALID ASID
         BR    R3                   PROCESS REMAINING INPUT
LJ000520 OI    ERLN4,ERLN4ASI       INDICATE ERROR ENCOUNTERED
         BR    R3                   PROCESS REMAINING INPUT
         SPACE
*---------------------------------------------------------------------*
*        FORMAT FIELD CHANGED                                         *
*---------------------------------------------------------------------*
         SPACE
LJ000600 OI    FLAG1,FLAG1FMT       MUST REWRITE OLD (OR NEW) DATA
         LR    RF,R9                PUT LENGTH IN SAFE PLACE
         SH    RF,=H'3'             HOW MUCH DID WE GET?
         BNP   LJ000620             NOTHING, ERROR
         BCT   RF,LJ000620          MORE THAN 1 BYTE, ERROR
         OI    3(RA),C' '           CONVERT TO UPPER CASE
         CLI   3(RA),C'C'           CHARACTER FORMAT REQUESTED?
         BE    LJ000640             -YES
         CLI   3(RA),C'X'           HEX FORMAT REQUESTED?
         BE    LJ000640             -YES
LJ000620 OI    ERLN4,ERLN4FMT       INDICATE ERROR ENCOUNTERED
         BR    R3                   PROCESS REMAINING INPUT
LJ000640 MVC   EFMTVAL,3(RA)        SAVE FORMAT FIELD
         TM    ERLN4,ERLN4FMT       WAS IT INVALID LAST TIME?
         BNO   LJ000670             -NO, TURN OFF ERROR INDICATOR
LJ000660 MVI   EFMTFLG,VREWRITE     ATTRIBUTE MUST BE REWRITTEN
         OI    FLAG0,FLAG0RW4       LINE 4 ORDERS MUST BE REWRITTEN
LJ000670 NI    ERLN4,255-ERLN4FMT   HAVE VALID FORMAT
         BR    R3                   PROCESSING REMAINING INPUT
         SPACE
*---------------------------------------------------------------------*
*        AREA FIELD CHANGED                                           *
*---------------------------------------------------------------------*
         SPACE
LJ000700 OI    FLAG1,FLAG1ARE       MUST REWRITE OLD (OR NEW) DATA
         LR    RF,R9                SAVE INPUT LENGTH
         SH    RF,=H'3'             DID WE GET ANYTHING?
         BNP   LJ000720             -NO, ERROR
         BCT   RF,LJ000720          MORE THAN 1 BYTE, ERROR
         OI    3(RA),C' '           CONVERT TO UPPER CASE
         CLI   3(RA),C'A'           CHECK FOR RANGE A-D
         BL    LJ000720             TOO LOW
         CLI   3(RA),C'D'
         BNH   LJ000740             GOOD
LJ000720 OI    ERLN4,ERLN4ARE       INDICATE ERROR ENCOUNTERED
         BR    R3                   PROCESS REMAINING INPUT
LJ000740 MVC   EAREAVAL,3(RA)       SAVE AREA VALUE
         TM    ERLN4,ERLN4ARE       WAS IT INVALID LAST TIME?
         BNO   LJ000770             -NO, TURN OFF ERROR INDICATOR
LJ000760 MVI   EAREAFLG,VREWRITE    REWRITE ATTRIBUTE BYTE
         OI    FLAG0,FLAG0RW4       REWRITE LINE 4 ORDERS
LJ000770 NI    ERLN4,255-ERLN4ARE   HAVE VALID AREA
         BR    R3                   PROCESS REMAINING INPUT
         SPACE
*---------------------------------------------------------------------*
*        WINDOW FIELD CHANGED                                         *
*---------------------------------------------------------------------*
         SPACE
* R4 HAS INDEX TO YCON TABLE - 10, 12, 14, 16 DEPENDING ON WHICH
* FIELD WAS MODIFIED
         SPACE
LJ000800 OI    FLAG1,FLAG1LIN       MUST REWRITE OLD (OR NEW) DATA
         SRL   R4,1                 BACK TO BYTE
         SH    R4,=H'5'             0 ORIGIN
         LA    R5,EWNDSFLG(R4)      -> CURRENT WINDOW FLAG
         BAL   RE,EA004100          CONVERT AND VALIDATE INPUT
         B     LJ000840             CONVERSION FAILED
         STC   RF,EWNDSVAL(R4)      VALUE RETURNED IN RF
         TM    0(R5),EPREVERR       WAS IT INVALID LAST TIME?
         BO    LJ000820             -YES, SET REWRITE INDICATOR
         MVI   0(R5),0              RESET ERROR INDICATOR
         BR    R3                   PROCESS REMAINING INPUT
LJ000820 MVI   0(R5),VREWRITE       REWRITE THIS ATTRIBUTE
         OI    FLAG0,FLAG0RW4       LINE 4 ORDERS MUST BE REWRITTEN
         BR    R3                   PROCESS REMAINING INPUT
LJ000840 MVI   0(R5),EPREVERR       SET INDICATOR FOR NEXT TIME
         BR    R3                   PROCESS REMAINING INPUT
         SPACE
*---------------------------------------------------------------------*
*        SKIP FIELD CHANGED                                           *
*---------------------------------------------------------------------*
         SPACE
LJ000900 OI    FLAG1,FLAG1SKP       MUST REWRITE OLD (OR NEW) DATA
         BAL   RE,EA003700          VALIDATE INPUT
         B     LJ000940             VALIDATION FAILED
         MVC   ESKIPVAL,PACKDEC+3   SAVE NEW SKIP VALUE
         TM    ERLN4,ERLN4SKP       WAS IT INVALID LAST TIME?
         BNO   LJ000930             -NO, TURN OF ERROR INDICATOR
LJ000920 MVI   ESKIPFLG,VREWRITE    REWRITE THIS ATTRIBUTE
         OI    FLAG0,FLAG0RW4       LINE 4 ORDERS MUST BE REWRITTEN
LJ000930 NI    ERLN4,255-ERLN4SKP   HAVE VALID SKIP FIELD
         BR    R3                   PROCESS REMAINING INPUT
LJ000940 OI    ERLN4,ERLN4SKP       INDICATE ERROR ENCOUNTERED
         BR    R3                   PROCESS REMAINING INPUT
         SPACE
*---------------------------------------------------------------------*
*        EXAMINE RESULTS OF INPUT VALIDITY CHECKING                   *
*---------------------------------------------------------------------*
         SPACE
LJ001000 LA    R4,4                 LOOK AT ALL FOUR WINDOWS
         NI    ERLN4,255-ERLN4WND   START WITH NO ERRORS
         LA    R5,EWNDSFLG          INITIAL WINDOW FLAG
LJ001050 TM    0(R5),EPREVERR       WAS AN ERROR ENCOUNTERED?
         BNO   LJ001075             -NO, CONTINUE LOOP
         OI    ERLN4,ERLN4WND       SET MASTER ERROR FLAG
LJ001075 LA    R5,1(R5)             -> NEXT WINDOW FLAG
         BCT   R4,LJ001050          LOOK AT NEXT WINDOW
         SPACE
         CLI   ERLN4,0              ANY OUTSTANDING ERRORS?
         BE    LC000320             -NO
         OI    NEWWCC,WCCALARM      BEEP
         BAL   R8,LB000020          INITIALIZE TPUT/TGET BUFFER
         SPACE
*---------------------------------------------------------------------*
*        PUT OUT ERROR MESSAGE                                        *
*---------------------------------------------------------------------*
         SPACE
         MVC   0(ORDLNSBA,RA),RW1UMGOR  ORDERS INTO SCREEN BUFFER
         MVC   ORDLNSBA(MSGIERLN,RA),MSGIER MSG INTO BUFFER
         LA    RA,ORDLNSBA+MSGIERLN(0,RA) CORRECT BUFFER ADDRESS
         SPACE
*---------------------------------------------------------------------*
*        PUT OUT LINE 4 ORDERS AND DATA                               *
*---------------------------------------------------------------------*
         SPACE
         MVC   0(ORDLNSBA,RA),RW4AADOR SBA FOR ADDR ATTRIB
         MVC   ORDLNSBA(2,RA),INTNSESF ASSUME INTENSIFY
         TM    ERLN4,ERLN4ADR       SHOULD IT HAVE BEEN INTENSIFIED?
         BO    LJ001100             -YES, UPDATE BUFFER ADDRESS
         TM    EADDRFLG,VREWRITE    SHOULD IT HAVE BEEN NORMAL?
         BNO   LJ001200             -NO, SHOULDN'T OUTPUT
         MVI   EADDRFLG,0           CLEAR FOR NEXT TIME
         MVI   ORDLNSBA+1(RA),NORMAL UNINTENSIFY ADDR FIELD
LJ001100 LA    RA,ORDLNSBA+2(RA)    CORRECT BUFFER ADDRESS
         SPACE
LJ001200 MVC   0(ORDLNSBA,RA),RW4AASOR SBA FOR ASID ATTRIB
         MVC   ORDLNSBA(2,RA),INTNSESF ASSUME INTENSIFY
         TM    ERLN4,ERLN4ASI       SHOULD IT HAVE BEEN INTENSIFIED?
         BO    LJ001300             -YES, UPDATE BUFFER ADDRESS
         TM    EASIDFLG,VREWRITE    SHOULD IT HAVE BEEN NORMAL?
         BNO   LJ001400             -NO, SHOULDN'T OUTPUT
         MVI   EASIDFLG,0           CLEAR FOR NEXT TIME
         MVI   ORDLNSBA+1(RA),NORMAL UNINTENSIFY ASID FIELD
LJ001300 LA    RA,ORDLNSBA+2(RA)    CORRECT BUFFER ADDRESS
         SPACE
LJ001400 MVC   0(ORDLNSBA,RA),RW4AFMOR SBA FOR FORMAT ATTRIB
         MVC   ORDLNSBA(2,RA),INTNSESF ASSUME INTENSIFY
         TM    ERLN4,ERLN4FMT       SHOULD IT HAVE BEEN INTENSIFIED?
         BO    LJ001500             -YES, UPDATE BUFFER ADDRESS
         TM    EFMTFLG,VREWRITE     SHOULD IT HAVE BEEN NORMAL?
         BNO   LJ001600             -NO, SHOULD'T OUTPUT
         MVI   EFMTFLG,0            CLEAR FOR NEXT TIME
         MVI   ORDLNSBA+1(RA),NORMAL UNINTENSIFY FORMAT FIELD
LJ001500 LA    RA,ORDLNSBA+2(RA)    CORRECT BUFFER ADDRESS
         SPACE
LJ001600 MVC   0(ORDLNSBA,RA),RW4AAROR SBA FOR AREA FLAG
         MVC   ORDLNSBA(2,RA),INTNSESF ASSUME INTENSIFY
         TM    ERLN4,ERLN4ARE       SHOULD IT HAVE BEEN INTENSIFIED?
         BO    LJ001700             -YES, UPDATE BUFFER ADDRESS
         TM    EAREAFLG,VREWRITE    SHOULD IT HAVE BEEN NORMAL?
         BNO   LJ001800             -NO, SHOULD'T OUTPUT
         MVI   EAREAFLG,0           CLEAR FOR NEXT TIME
         MVI   ORDLNSBA+1(RA),NORMAL UNINTENSIFY AREA FIELD
LJ001700 LA    RA,ORDLNSBA+2(RA)    CORRECT BUFFER ADDRESS
         SPACE
LJ001800 LA    R3,EWNDSFLG          INITIAL INDEX FOR WINDOW LOOP
         LA    R4,1                 INCREMENT
         LA    R5,EWNDSFLG+3        COMPARAND
         LA    R8,RW4AL1OR          BEGINNING OF WINDOW ORDERS
LJ001900 TM    0(R3),VREWRITE+EPREVERR NEED TO REWRITE WINDOW
         BZ    LJ002100             -NO, CHECK NEXT FIELD
         MVC   0(ORDLNSBA,RA),0(R8) SBA FOR WINDOW FIELD
         MVC   ORDLNSBA(2,RA),INTNSESF ASSUME INTENSIFY
         TM    0(R3),EPREVERR       SHOULD IT HAVE BEEN?
         BO    LJ002000             -YES, UPDATE BUFFER ADDRESS
         MVI   0(R3),0              CLEAR FOR NEXT TIME
         MVI   ORDLNSBA+1(RA),NORMAL UNINTENSIFY WINDOW FIELD
LJ002000 LA    RA,ORDLNSBA+2(RA)    CORRECT BUFFER ADDRESS
LJ002100 LA    R8,ORDLNSBA(R8)      NEXT WINDOW SBA
         BXLE  R3,R4,LJ001900       LOOK AT NEXT WINDOW
         SPACE
         MVC   0(ORDLNSBA,RA),RW4ASPOR SBA FOR SKIP FIELD
         MVC   ORDLNSBA(2,RA),INTNSESF ASSUME INTENSIFY
         TM    ERLN4,ERLN4SKP       SHOULD IT HAVE BEEN INTENSIFIED?
         BO    LJ002200             -YES, UPDATE BUFFER ADDRESS
         TM    ESKIPFLG,VREWRITE    SHOULD IT HAVE BEEN NORMAL?
         BNO   LJ002300             -NO, SHOULDN'T OUTPUT
         MVI   ESKIPFLG,0           CLEAR FOR NEXT TIME
         MVI   ORDLNSBA+1(RA),NORMAL UNINTENSIFY SKIP FIELD
LJ002200 LA    RA,ORDLNSBA+2(RA)    CORRECT BUFFER ADDRESS
         SPACE
*---------------------------------------------------------------------*
*        OUTPUT FLAGGED INPUT                                         *
*---------------------------------------------------------------------*
         SPACE
LJ002300 LA    R8,LA000700          TPUT RETURN ADDRESS
         MVC   0(4,RA),WKACORDS     ADD AN INSERT CURSOR ORDER
         LA    RA,4(0,RA)           CORRECT BUFFER ADDRESS
         B     LA000500             OUTPUT BUFFER
         SPACE
*---------------------------------------------------------------------*
*        LINE 1 SHOULD NOT BE MODIFIED - SEE WHAT HAPPENED            *
*---------------------------------------------------------------------*
         SPACE
LJ002500 IC    R4,AJ002510(R4)      TRANSLATE COLUMN NUMBER TO INDEX
         LH    RF,YJ002520(R4)      PICK UP BRANCH INDEX VALUE
         LA    R8,BLSR327B(RF)      ->ANALYSIS ROUTINE
         BR    R8                   GO TO ANALYSIS ROUTINE
         SPACE
*---------------------------------------------------------------------*
*        DATA FIELD TRANSLATION VECTOR FOR LINE 1                     *
*---------------------------------------------------------------------*
         SPACE
AJ002510 DC    1AL1(0)              ATTRIBUTE - ERROR
         DC    1AL1(2)              3END - RESEND
         DC    5AL1(0)              ATTRIBUTE - ERROR
         DC    1AL1(2)              7<-SCROLL+>8 - RESEND
         DC    13AL1(0)             ATTRIBUTE - ERROR
         DC    1AL1(2)              9STACK - RESEND
         DC    7AL1(0)              ATTRIBUTE - ERROR
         DC    1AL1(2)              10<-SKIP+>11 - RESEND
         DC    50AL1(0)             ATTRIBUTE OR MESSAGE - ERROR
         SPACE
*---------------------------------------------------------------------*
*        PROCESSOR VECTOR FOR CHANGES TO FIELDS ON LINE 1             *
*---------------------------------------------------------------------*
         SPACE
YJ002520 DC    Y(LC000200-BLSR327B) PROTECTED FIELD CHANGED - BAD
         DC    Y(LJ002550-BLSR327B) CONSTANTS FIELD CHANGED - RESEND
         SPACE
LJ002550 OI    NEWWCC,WCCALARM      BEEP
         BR    R3
         SPACE
*---------------------------------------------------------------------*
*        MODIFICATIONS TO LINE 2 - ADDRESS STACK                      *
*---------------------------------------------------------------------*
         SPACE
LJ002600 SH    R4,=H'3'            BYPASS THE FIRST FIELD
         BNP   LC000200            PROTECTED FIELD CHANGED - BAD
         SRDA  R4,32               PREPARE FOR DIVISION
         D     R4,=F'7'            REMAINDER SHOULD BE ONE
         BCT   R4,LC000200         PROTECTED FIELD CHANGED - BAD
         OI    FLAG0,FLAG0RW2      MUST RESURRECT THE PREVIOUS DATA
         B     LJ002550            GO BEEP
         SPACE
*---------------------------------------------------------------------*
*        MODIFICATIONS TO LINE 3 - STACK NOTATION                     *
*---------------------------------------------------------------------*
         SPACE
LJ002700 SH    R4,=H'3'             BYPASS THE FIRST FIELD
         BNP   LC000200             PROTECTED FIELD CHANGED - BAD
         SRDA  R4,32                PREPARE FOR DIVISION
         D     R4,=F'7'             REMAINDER SHOULD BE ONE
         BCT   R4,LC000200          PROTECTED FIELD CHANGED - BAD
         LA    RF,L'WKANB1          LENGTH OF A NOTES ENTRY
         SLR   R4,R4                CLEAR R4 FOR MULTIPLY
         MR    R4,RF                COMPUTE DISPLACEMENT TO ENTRY
         LA    R5,SAVSTK(R5)        COMPUTE ADDRESS OF NOTES ENTRY
         LR    RF,R9                PUT LENGTH IN SAFE PLACE
         SH    RF,=H'3'             GET PAST SBA
         BP    LJ002720             HE MAY HAVE JUST NULLED FIELD
         MVI   0(R5),NULVALUE       INDICATE NULLS
         BR    R3                   PROCESS REMAINING INPUT
LJ002720 MVI   0(R5),C' '           INITIALIZE TO
         MVC   1(L'WKANB1-1,R5),0(R5) BLANKS
         BCTR  RF,R0                DOWN ONE FOR EXECUTE
         EX    RF,IJ002750          MOVE FROM BUFFER TO SAVE AREA
         BR    R3                   PROCESS REMAINING INPUT
IJ002750 MVC   0(0,R5),3(RA)        *** SUBJ OF EX, ABOVE
         SPACE
*---------------------------------------------------------------------*
*        MODIFICATIONS TO LINE 5 - SUBCOMMAND                         *
*---------------------------------------------------------------------*
         SPACE
LJ002800 CH    R4,=H'16'            PROTECTED FIELD BELOW THIS
         BL    LC000200             BAD
         LR    RF,R9                PUT LENGTH IN SAFE PLACE IT
         SH    RF,=H'3'             GET PAST SBA
         BP    LJ002850             HE MAY HAVE JUST NULLED FIELD
         MVI   SAVSBCMD,NULVALUE    INDICATE NULLS
         BR    R3                   PROCESS REMAINING INPUT
LJ002850 MVI   SAVSBCMD,C' '        INITIALIZE TO
         MVC   SAVSBCMD+1(65),SAVSBCMD BLANKS
         LA    R5,SAVSBCMD          -> SAVSBCMD
         BCTR  RF,R0                DOWN ONE FOR EXECUTE
         EX    RF,IJ002750          MOVE FROM BUFFER TO SAVE AREA
         BR    R3                   PROCESS REMAINING INPUT
         SPACE
*---------------------------------------------------------------------*
*        MODIFICATIONS TO LINES 6-24 - DUMP DISPLAY AREA              *
*---------------------------------------------------------------------*
         SPACE
LJ002900 SH    R5,=H'10'            ORIGIN LINE NUMBER AT ZERO
         SRL   R5,1                 GO BACK TO BYTE INDEX
         LA    RF,WKALNCTL          INDEX TO
         AR    RF,R5                -PROPER LINE
         SLR   R8,R8                START WITH ZERO
         IC    R8,WKALNCTL(R5)      VECTOR INTO AREA TABLE
         BCTR  R8,0                 FORM ZERO ORIGIN INDEX
         SLL   R8,2                 *(2**2)=4
         LA    R8,WKAVCTA(R8)       PROPER CONTROL WORD FOR THIS AREA
         LTR   R4,R4                ONLY VALID MODIFICATIONS ARE
         BNP   LC000200             -BEYOND COLUMN 1
         CLI   34(R8),C'C'          IS IT CHARACTER FORMATTING?
         BNE   LJ002950             -NO, PERFORM CHECK FOR HEX
         OI    0(RF),WKALNCTW       LINE MUST BE REWRITTEN
         BR    R3                   PROCESS REMAINING INPUT
         SPACE
LJ002950 SH    R4,=H'8'             GET BY ADDRESS FIELD
         BP    LJ002960             ADDRESS FIELD WAS NOT MODIFIED
         OI    0(RF),WKALNCTW       LINE MUST BE REWRITTEN
         BR    R3                   PROCESS REMAINING INPUT
LJ002960 SRDA  R4,32                PREPARE FOR DIVISION
         D     R4,=F'9'             EACH DATA FIELD IS #CCCCCCCC
         BCT   R4,LC000200          REMAINDER SHOULD BE ONE, ELSE ERR
         OI    0(RF),WKALNCTW       LINE MUST BE REWRITTEN
         BR    R3                   PROCESS REMAINING INPUT
         SPACE
