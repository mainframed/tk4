         MACRO
&N       BLSCAMSL &TEXT,&END=YES
.** START OF SPECIFICATIONS *******************************************
.*
.*  MACRO NAME= BLSCAMSL
.*
.*  DESCRIPTIVE NAME= IPCS DAS ALLOCATION MODEL AMS SYSIN TEXT STREAM
.*                    GENERATOR
.*
.*  COPYRIGHT= NONE
.*
.*  STATUS= SU57 RELEASE 1, LEVEL 0
.*
.*  FUNCTION=
.*             PREPARE AN ACCESS METHOD SERVICES SYSIN TEXT STREAM IN
.*             RECFM=V FORMAT TO BE USED BY THE BLSCAAMS MODULE. THE
.*             GENERATED TEXT STREAM WILL RESIDE IN THE BLSCAMOD MODULE
.*
.*  NOTES= NONE
.*
.*  MODULE TYPE= DATA AREA GENERATION MACRO
.*
.*     PROCESSOR= ASSEMBLER F
.*
.*     ATTRIBUTES= REENTRABLE (READ ONLY DATA)
.*
.*  ENTRY POINT= NONE
.*
.*  INVOCATION SYNTAX=
.*
.*    LOCSYMB BLSCAMSL POSITIONALPARM ×,END=NO×
.*
.*            LOCSYMB
.*              4 TO 7 BYTE LOCATION SYMBOL NAME OF THE FORM AMSNN.
.*              NN IS THE LOCATION SYMBOL OF THIS MACRO'S PARENT MODEL.
.*              THE LOCATION SYMBOL WILL BE REFERENCED BY AN ADDRESS
.*              CONSTANT GENERATED BY THE BLSCAMMM MACRO. IF THE
.*              LOCSYMBOL IS OMITTED, THE BOUNDARY ALIGNMENT
.*              INSTRUCTION DC 0H'0' IS OMITTED TO PERMIT CONCATENATION
.*              OF BLSCAMSL INVOCATIONS TO BUILD A SINGLE IDCAMS SYSIN
.*              STREAM.
.*
.*            POSITIONALPARM
.*              A MACRO POSITIONAL PARAMETER IN THE FORM:
.*                ('TEXT LINE 1','TEXT LINE 2',....,'TEXT LINE N')
.*              THE MAXIMUM LENGTH OF A TEXT LINE IS 71 BYTES. ONE BYTE
.*              OF BLANK IS CONCATENATED AS THE LEADING BYTE OF EACH
.*              LINE BECAUSE IDCAMS IGNORES THE FIRST BYTE OF EACH
.*              SYSIN RECORD.
.*
.*           END=NO
.*              THIS KEYWORD WITH A VALUE OF 'NO' PREVENTS THE
.*              APPENDING TO THE TWO BYTE STRING OF ZEROS THAT
.*              INDICATES END OF SYSIN TEXT STREAM TO THE USING
.*              PROGRAM. THIS KEYWORD PERMITS THE CONCATENATION OF
.*              INVOCATIONS OF THE BLSCAMSL MACRO TO BUILD A SINGLE
.*              IDCAMS SYSIN TEXT STREAM.
.*
.*  OUTPUT=  ASSEMBLER 'DC STATEMENTS ARE BUILT FOR EACH TEXT SEGMENT
.*           IN THE FORM:
.*               DC   HL2'L''TEXTSEG+4',HL2'0',C'TEXTSEG'
.*           FOLLOWING THE LAST TEXT SEGMENT, A TWO BYTE STRING OF
.*           HEX ZEROS IS APPENDED TO INDICATE END-OF-DATA IF THE 'END'
.*           KEYWORD IS NOT PRESENT WITH A VALUE OF 'NO'.
.*
.*  EXTERNAL REFERENCES= NONE
.*
.*  TABLES=  NONE
.*
.*  MACRO=  NONE
.*
.*  CHANGE ACTIVITY=
.*        THIS MODULE IS WRITTEN FOR @G57LPSR
.*
.*  MESSAGES ISSUED=
.*     MSG. NO     MESSAGE TEXT
.*     MNOTE 8, SUB PARM NUMBER 'NN' EXCEEDS 71 BYTES. PARAMETER IS
.*           TRUNCATED
.*
.** END OF SPECIFICATIONS *********************************************
         LCLA  &CTR           LOOP COUNTER
         LCLA  &NUM           NUMBER OF TEXT SEGMENTS
         LCLA  &L             TEXT SEGMENT LENGTH
         LCLA  &LL            TEXT SEGMENT MOVE LENGTH
         LCLA  &LLL           TEXT SEGMENT LENGTH + 4
         LCLC  &C             TEXT SEGMENT
         LCLC  &CC            TRUNCATED TEXT SEGMENT
         LCLC  &PAD           ONE BLANK
&CTR     SETA  0              INIT LOOP COUNTER
&NUM     SETA  N'&TEXT        INIT LOOP LIMIT
&PAD     SETC  ' '
         AIF   (N'&N EQ 0).CNT  TEST FOR NO LOCSYMB
&N       DC    0H'0'          ALIGN LIST
         AGO   .CNT
.LOOP    ANOP
&C       SETC  '&TEXT(&CTR)'  TEXT SEGMENT
&L       SETA  K'&C-1
         AIF   (&L LE 72).OK  LENGTH OK
&L       SETA  72             TRUNCATED LENGTH
         MNOTE 8,'SUB PARM NUMBER ''&CTR'' EXCEEDS 71 BYTES. PARAMETER *
               IS TRUNCATED'
.OK      ANOP
&LL      SETA  &L-1           MOVE LENGTH
&LLL     SETA  &L+4           ENTRY LENGTH
&CC      SETC  '&C'(2,&LL)    REMOVE QUOTES AND TRUNCATE IF NECESSARY
         DC    HL2'&LLL',HL2'0',CL&L'&PAD&CC' TEXT SEGMENT
.CNT     ANOP
&CTR     SETA  &CTR+1         BUMP LOOP COUNTER
         AIF   (&CTR LE &NUM).LOOP  TEST LOOP END
         AIF   ('&END' EQ 'NO').MEND
         DC    HL2'0'         END OF TEXT
.MEND    MEND
