         TITLE 'BLSR3270--BLSR327B--BUILD 3277 ORDER STREAM'
*---------------------------------------------------------------------*
*                                                                     *
*BLSR327B--BUILD 3277 ORDER STREAM--USE SAME BUFFER USED FOR TGET     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
BLSR327B LA    R8,LB000100        INITIALIZE RETURN REGISTER
         SPACE 2
*---------------------------------------------------------------------*
*INITIALIZE THE OUTPUT BUFFER                                         *
*---------------------------------------------------------------------*
         SPACE
LB000020 LA    RA,SCRNBUFR        ->TGET/TPUT BUFFER
         TM    FLAG0,FLAG0FMT     ENTIRE SCREEN TO BE WRITTEN?
         BNO   LB000050           NO
         MVI   ERLN4,0            YES - RESET ERROR INDICATOR
         MVC   0(ERSWRTLN,RA),ERSWRT SEND ERASE/WRITE SEQUENCE
         LA    RA,ERSWRTLN(0,RA)  CORRECT BUFFER ADDRESS
LB000050 MVC   0(ESCWRTLN,RA),ESCWRITE
         TRT   NEWWCC(1),T3270TAB PICK UP CORRECT 3270 CHAR
         STC   R2,0(0,RA)         NOTE - R1 ALSO DESTROYED BY TRT
         LA    RA,ESCWRTLN(0,RA)  CORRECT BUFFER ADDRESS
         BR    R8
         SPACE 2
*---------------------------------------------------------------------*
*ADD ORDERS TO FORMAT THE SCREEN                                      *
*---------------------------------------------------------------------*
         SPACE
LB000100 TM    FLAG0,FLAG0FMT     SELDOM NECESSARY, HOPEFULLY
         BNO   LB000200
         MVC   0(ERASELEN,RA),ERASEALL  ERASE ENTIRE SCREEN
         LA    RA,ERASELEN(0,RA)
LB000200 MVC   0(RWL1ORLN,RA),RWL1ORDS  ALWAYS REWRITE
         LA    RA,RWL1ORLN(0,RA)        -LINE 1
         TM    FLAG0,FLAG0FMT           COMPLETE REWRITE?
         BNO   LB000500                 -NO
         MVC   0(R2T4ORLN,RA),R2T4ORDS  LINE 2-3 ORDERS
         MVC   R2T4ORLN(RWL4ORLN,RA),RWL4ORDS LINE 4 ORDERS
         LA    RA,R2T4ORLN+RWL4ORLN(0,RA) CORRECT BUFFER ADDRESS
         MVC   0(RWL5ORLN,RA),RWL5ORDS  LINE 5 ORDERS
         LA    RA,RWL5ORLN(0,RA)  CORRECT BUFFER ADDRESS
         CLI   WKASKNUM,0         ANY ADDRESSES IN THE STACK?
         BE    LB000300           NO, NONE TO WRITE OUT
         OI    FLAG0,FLAG0RW2     YES, NOTE IT FOR LATER
LB000300 CLI   WKANBNUM,0         HOW ABOUT THE NOTE LIST?
         BE    LB000400           NO
         OI    FLAG0,FLAG0RW3     YES, MARK IT FOR LATER
LB000400 MVI   FLAG1,FLAG1ADR+FLAG1ASI+FLAG1FMT+FLAG1ARE+FLAG1LIN+FLAG1*
               SKP                BETTER LET HIM SEE ALL OF IT
         OC    WKALNCTL(19),=19AL1(WKALNCTW) MARK ALL TO BE WRITN
         SLR   RF,RF              IS SUBCMD/CLIST STRING NULL?
         ICM   RF,B'0001',WKABLSRN THIS IS COUNT OF CHARS
         BZ    LB000420           YES, NO STRING TO PUT OUT
         BCTR  RF,0               DECREMENT COUNT FOR EX
         MVC   0(ORDLNSBA,RA),RW5UCMOR POINT TO SCREEN ADDR
         EX    RF,IB005200        AND PUT DATA IN STREAM
         LA    RA,ORDLNSBA+1(RF,RA) CORRECT BUFFER ADDR
         SPACE
LB000420 BAL   R8,LA000500        PUT OUT THIS PART OF THE SCREEN
         LA    RA,SCRNBUFR        ->TGET/TPUT BUFFER
         BAL   R8,LB000050        RESET OUTPUT BUFFER
         EJECT
*---------------------------------------------------------------------*
*ADD ORDERS TO DISPLAY MESSAGE                                        *
*---------------------------------------------------------------------*
         SPACE
LB000500 L     R1,ZZ1P            ->ZZ1 (R1 NOT PERMANENT AS BASE)
         CLI   FLAG2,0            ANY MESSAGE NEEDED?
         BE    LB000600           - NO
         MVC   0(ORDLNSBA,RA),RW1UMGOR ORDERS INTO SCREEN BUFFER
         LA    RA,ORDLNSBA(0,RA)      CORRECT BUFFER ADDR
         MVC   0(MSGSTKLN,RA),MSGSTK  MSG INTO BUFFER
         LA    RA,MSGSTKLN(0,RA)      CORRECT BUFFER ADDR
         SPACE 2
*---------------------------------------------------------------------*
*ADD ORDERS TO DISPLAY THE STACK, LINES 2, 3, AND 4 ON THE SCREEN     *
*---------------------------------------------------------------------*
         SPACE
LB000600 TM    FLAG0,FLAG0RW2     ANY UPDATE NEEDED?
         BNO   LB000900           NO
         MVC   0(ORDLNSBA,RA),RW2SBAOR POINT TO FIRST STACK ENTRY
         LA    RA,ORDLNSBA(0,RA)
         SLR   R8,R8              CLEAR FOR IC OF ACTIVE STACK ENTRY#
         IC    R8,WKASKNUM        FOR LOOP CONTROL
         LTR   R8,R8              COULD HAVE JUST DELETED ALL ENTRIES
         BZ    LB000800           SO WILL NULL THEM OUT
         LA    R9,WKASTACK        POINT TO MY COPY OF STACK
LB000700 UNPK  0(7,RA),1(4,R9)    POINTER=>ZONED HEXADECIMAL
         TR    0(6,RA),ZZ1TRHEX(R1) ZONED HEXADECIMAL=>EBCDIC
         MVI   6(RA),ORDERPT      TAB TO NEXT STACK FIELD
         LA    RA,7(0,RA)         UPDATE SCREEN ADDRESS
         LA    R9,4(0,R9)         UPDATE STACK AREA ADDRESS
         BCT   R8,LB000700        LOOP TILL COUNT EXHAUSTED
         BCTR  RA,0               DON'T NEED LAST PT ORDER
LB000800 CLI   WKASKNUM,11        DO WE NEED THIS ERASE?
         BE    LB000900           NO
         MVC   0(ORDLNEUA,RA),ERASR2OR ORDERS INTO SCREEN BUFFR
         LA    RA,ORDLNEUA(0,RA)
LB000900 TM    FLAG0,FLAG0RW4     DO WE NEED LINE 4 ORDERS?
         BNO   LB000950           NO
         OI    FLAG1,FLAG1FMT+FLAG1ARE+FLAG1LIN REWRITE CONSTANTS
         MVC   0(RWL4ORLN,RA),RWL4ORDS LINE 4 ORDERS
         LA    RA,RWL4ORLN(0,RA)  CORRECT BUFFER ADDRESS
LB000950 TM    FLAG0,FLAG0RW3     NEED TO UPDATE?
         BNO   LB001200           NO
         MVC   0(ORDLNSBA,RA),RW3SBAOR ORDERS INTO BUFFER
         LA    RA,ORDLNSBA(0,RA)
         SLR   R8,R8              FOR IC
         ICM   R8,B'0001',WKANBNUM NUMBER OF ACTIVE LIST ENTRIES
         BZ    LB001100           NO, NULL ROW
         LA    R9,WKANB1          YES, POINT TO FIRST NOTE
LB001000 MVC   0(6,RA),0(R9)      DATA INTO ORDERS STREAM
         MVI   6(RA),ORDERPT      TAB TO NEXT FIELD
         LA    RA,7(0,RA)         NEXT BUFFER POSITION
         LA    R9,8(0,R9)         NEXT NOTE LIST ENTRY
         BCT   R8,LB001000        TILL COUNT EXHAUSTED
         BCTR  RA,0               DON'T NEED LAST PT
         MVI   SCRPO,SCRPOAD1     REMEMBER WHERE WE ARE
LB001100 CLI   WKANBNUM,11        ALREADY AT END
         BE    LB001200           YES
         MVC   0(ORDLNEUA,RA),ERASR3OR NO, SET ORDERS UP
         LA    RA,ORDLNEUA(0,RA)
         MVI   SCRPO,SCRPOER3     NOT SAME AS SCRPOAD1---DIF RA
         EJECT
*---------------------------------------------------------------------*
*ADD ORDERS TO DISPLAY LINE 4 ON THE SCREEN                           *
*---------------------------------------------------------------------*
         SPACE
LB001200 TM    FLAG1,FLAG1ADR+FLAG1ASI+FLAG1FMT+FLAG1ARE+FLAG1LIN+FLAG1*
               SKP                ANYTHING ON ROW 4 TO PUT OUT?
         BZ    LB003100           NO
         TM    FLAG1,FLAG1ADR     MUST ADDR BE UPDATED?
         BNO   LB001500           NO
         CLI   SCRPO,SCRPOER3     ALREADY AT ADDR FIELD?
         BE    LB001400           YES
         CLI   SCRPO,SCRPOAD1     ARE WE ALMOST THERE?
         BNE   LB001300           NO
         LA    RA,1(0,RA)         PAST PT IN BUFFER ALREADY
         B     LB001400           PUT DATA INTO SCREEN BUFFER
LB001300 MVC   0(ORDLNSBA,RA),RW4UADOR
         LA    RA,ORDLNSBA(0,RA)
LB001400 UNPK  0(7,RA),WKAADDR+1(4) POINTER=>ZONED HEXADECIMAL
         TR    0(6,RA),ZZ1TRHEX(R1) ZONED HEXADECIMAL=>EBCDIC
         MVI   SCRPO,SCRPOAD9     REMEMBER WHERE WE ARE
         LA    RA,6(0,RA)         IN BUFFER, TOO
LB001500 TM    FLAG1,FLAG1ASI     DO WE?
         BNO   LB001800           NO
         CLI   SCRPO,SCRPOAD9     NEED AN SBA HERE?
         BNE   LB001600           NO
         MVI   0(RA),ORDERPT      YES, TAB TO ASID FIELD
         LA    RA,1(0,RA)         UPDATE BUFFER ADDRESS
         B     LB001700           GO DO IT
LB001600 MVC   0(ORDLNSBA,RA),RW4UASOR POINT TO ASID FIELD
         LA    RA,ORDLNSBA(0,RA)
LB001700 UNPK  0(5,RA),WKAASID(3) BIT=>ZONED HEXADECIMAL
         TR    0(4,RA),ZZ1TRHEX(R1) ZONED HEXADECIMAL=>EBCDIC
         LA    RA,4(0,RA)
         MVI   SCRPO,SCRPOAS9     REMEMBER WHERE WE ARE
LB001800 TM    FLAG1,FLAG1FMT     FORMAT NEED UPDATING? (X OR C)
         BNO   LB002100           NO, AREA ID NEXT ON SCREEN
         CLI   SCRPO,SCRPOAS9     IN THE RIGHT SPOT NOW?
         BNE   LB001900           NO
         MVI   0(RA),ORDERPT      YES, TAB TO FMT FIELD
         LA    RA,1(0,RA)
         B     LB002000
LB001900 MVC   0(ORDLNSBA,RA),RW4UFMOR NECESSARY ORDERS
         LA    RA,ORDLNSBA(0,RA)
LB002000 MVC   0(1,RA),WKAFMT     FORMAT INTO DATA STREAM
         LA    RA,1(0,RA)
         MVI   SCRPO,SCRPOFM9     REMEMBER WHERE WE ARE
LB002100 TM    FLAG1,FLAG1ARE     NEED AREA THIS TIME?
         BNO   LB002400           NO
         CLI   SCRPO,SCRPOFM9     CORRECTLY POSITIONED?
         BNE   LB002200           NO
         MVI   0(RA),ORDERPT      YES, TAB TO AREA FIELD
         LA    RA,1(0,RA)
         B     LB002300
LB002200 MVC   0(ORDLNSBA,RA),RW4UAROR NECESSARY ORDERS
         LA    RA,ORDLNSBA(0,RA)
LB002300 MVC   0(1,RA),WKAAREA    AREA INTO DATA STREAM
         LA    RA,1(0,RA)
         MVI   SCRPO,SCRPOWI9     REMEMBER WHERE WE ARE
LB002400 TM    FLAG1,FLAG1LIN     NEED LINES/AREA THIS TIME?
         BNO   LB002800           NO
         CLI   SCRPO,SCRPOWI9     CORRECTLY POSITIONED?
         BNE   LB002500           NO
         MVI   0(RA),ORDERPT      YES, TAB TO AREA FIELD
         LA    RA,1(0,RA)
         B     LB002600
LB002500 MVC   0(ORDLNSBA,RA),RW4ULNOR NECESSARY ORDERS
         LA    RA,ORDLNSBA(0,RA)
LB002600 LA    R8,4               NUMBER OF ENTRIES
         SLR   RF,RF              CLEAR FOR IC
         LA    R9,WKALINES        START SOURCE ADDRESS
LB002700 IC    RF,0(0,R9)         BINARY NUMBER OF LINES
         CVD   RF,PACKDEC         INTO PACKED DECIMAL
         UNPK  0(2,RA),PACKDEC+6(2) INTO DATA STREAM
         OI    1(RA),C'0'         MAKE LAST DIGIT NUMBERIC
         MVI   2(RA),ORDERPT      TAB TO NEXT FIELD ON SCREEN
         LA    R9,1(0,R9)         UPDATE TO NEXT AREA
         LA    RA,3(0,RA)
         BCT   R8,LB002700        PUT THEM ALL IN
         BCTR  RA,0               IN CASE WE DON'T NEED PT TO SKIP
         MVI   SCRPO,SCRPOPT9     REMEMBER WHERE WE ARE
LB002800 TM    FLAG1,FLAG1SKP     NEED SKIP VALUE THIS TIME?
         BNO   LB003100           NO, END OF ROW 4 OF SCREEN
         CLI   SCRPO,SCRPOPT9     CORRECTLY POSITIONED?
         BNE   LB002900           NO
         LA    RA,1(0,RA)
         B     LB003000           TO SET UP THE SKIP VALUE
LB002900 MVC   0(ORDLNSBA,RA),RW4USPOR POINT TO SKIP FIELD
         LA    RA,ORDLNSBA(0,RA)
LB003000 UNPK  0(7,RA),WKASKIP+1  POINTER=>ZONED HEXADECIMAL
         TR    0(6,RA),ZZ1TRHEX(R1) ZONED HEXADECIMAL=>EBCDIC
         LA    RA,6(0,RA)
         EJECT
*---------------------------------------------------------------------*
*ADD ORDERS TO DISPLAY AREAS A, B, C, AND D                           *
*                                                                     *
*        THIS ROUTINE IS DRIVEN BY A 19-BYTE VECTOR IN THE WORKAREA,  *
*        SET BY THE FIELD DEFINITION ANALYSIS ROUTINE AND UPDATED BY  *
*        THE VARIOUS 'COMMAND' PROCESSORS.  A CHANGE IN FIELD         *
*        DEFINITION WILL CAUSE ALL LOWER LINES IN THE SCREEN TO BE    *
*        REWRITTEN; A CHANGE IN THE ACTIVE AREA, OR A NEW EFFECTIVE   *
*        ADDRESS, WILL CAUSE ONLY THE ACTIVE AREA TO BE UPDATED.      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
LB003100 MVI   SCRPO,0            POSITION NO LONGER USEFUL
         SLR   R6,R6              TO START LOOP
         SLR   R2,R2              FOR IC'S IN LOOP
         MVC   WKAVCTB(4*4),WKAVCTA COPY CURRENT START ADDRESSES
         NI    FLAG0,255-(FLAG0POK+FLAG0NUL) OFF TO START WITH
LB003200 IC    R2,WKALNCTL(R6)    VECTOR INTO AREA TABLE
         LA    RF,255-WKALNCTW    UPDATE FLAG
         CLR   R2,RF              NEED TO UPDATE THIS LINE?
         BNH   LB004600           NO
         NR    R2,RF              NULL LINE REQUIRED?
         BZ    LB004300           YES
         SPACE 2
*---------------------------------------------------------------------*
*        THINK WE NEED TO WRITE SOMETHING-- BUT WHAT? FIRST MAKE SURE *
*        THERE REALLY IS SOMETHING IN THIS AREA TO WRITE. IF SO,      *
*        CHECK THAT BUFFER ADDRESS IS CORRECT, AND THAT WE DON'T      *
*        NEED TO COMPLETE A NULL LINE OR AREA.                        *
*---------------------------------------------------------------------*
         SPACE
         BCTR  R2,0               FORM ZERO ORIGIN INDEX
         SLL   R2,2               *(2**2=4) FOR WORD ARRAY
         LA    RF,WKAVCTA(R2)     PROPER CONTROL WORD FOR THIS AREA
         TM    35(RF),WKAVCTAI    HAS FIELD EVER BEEN USED?
         BO    LB004300           NO, CAN'T HAVE DATA TO WRITE THERE
         SPACE 2
*---------------------------------------------------------------------*
*        AN ACTIVE AREA NEEDS UPDATING --- PERFORM SCREEN BUFFER      *
*        ADDRESS HOUSEKEEPING.                                        *
*---------------------------------------------------------------------*
         SPACE
         TM    FLAG0,FLAG0POK+FLAG0NUL HOW DOES BUFFER LOOK NOW?
         BM    LB003500           POSITIONED OK, AND NEED NO NULLS
         LA    RE,0(R6,R6)        FORM INDEX INTO BUFFER ADDR TAB
         MVI   0(RA),ORDERSBA     ASSUME SET BUFFER ADDR ORDER NEEDED
         BZ    LB003300           DON'T NEED NULLS
         MVC   0(2,RA),PROTAREA   ESTABLISH PROTECTED FIELD
         LA    RA,2(RA)           CORRECT BUFFER ADDRESS
         MVI   0(RA),ORDERRA      NEED REPEAT-TO-ADDRESS ORDER
LB003300 LH    RE,BUFADTAB(RE)    BUFFER ADDRESS TO USE
         STCM  RE,B'0011',1(RA)   PUT IT INTO SCREEN STREAM
         BZ    LB003400
         MVI   3(RA),0            INSERT NULL INTO STREAM
         LA    RA,1(0,RA)         ACCOUNT FOR NULL INSERTED
         NI    FLAG0,255-FLAG0NUL RESET NULL NEEDED
LB003400 LA    RA,ORDLNSBA(0,RA)  HOUSEKEEPING COMPLETE
         OI    FLAG0,FLAG0POK     SAY SO
         SPACE 2
*---------------------------------------------------------------------*
*        SET UP CORRECT ATTRIBUTE BYTE FOR THIS LINE. INTENSIFY THE   *
*        FIRST LINE IN EACH WINDOW                                    *
*---------------------------------------------------------------------*
         SPACE
LB003500 MVC   SAVELATR,NORMAREA+1  ASSUME INTENSIFY NOT NEEDED
         MVC   0(2,RA),NORMAREA   DEFINE THE START OF A FIELD
         CLC   0(4,RF),4*4(RF)    AT FIRST LINE OF FIELD?
         BNE   LB003600           -NO
         MVI   SAVELATR,INTENSE   -YES, SAVE FOR LATER FIELDS
         MVC   1(1,RA),SAVELATR   SET CORRECT ATTRIBUTE
LB003600 CLI   34(RF),C'C'        CHARACTER FORMATTING NEEDED?
         BE    LB004800           YES
         SPACE 2
*---------------------------------------------------------------------*
*        FORMAT A HEXADECIMAL DISPLAY LINE                            *
*---------------------------------------------------------------------*
         SPACE
         L     RE,4*4(RF)         PICK UP CURRENT ADDRESS
         ST    RE,PACKDEC         INTO WORK AREA FOR UNPACKING, ETC
         N     RE,=X'FFFFFFE0'    ROUND DOWN TO BOUNDARY
         ST    RE,PACKDEC+4       ADDRESS TO ASK PRDMP FOR
         LR    R0,RE              SAVE PARAMETER FOR PRDMP STG ACCESS
         L     R1,ZZ1P            ->ZZ1
         UNPK  2(7,RA),PACKDEC+5(4) POINTER=>ZONED HEXADECIMAL
         TR    2(6,RA),ZZ1TRHEX(R1) ZONED HEXADECIMAL=>EBCDIC
         SPACE 2
*---------------------------------------------------------------------*
*        RETRIEVE DUMP DATA                                           *
*---------------------------------------------------------------------*
         SPACE
         L     R1,ZZ2P            ->ZZ2
         MVC   ZZ2AMDA(2,R1),32(RF) ASID
         L     RF,ZZ2AMDS(0,R1)   ->BLSRACCL
         BALR  RE,RF              CALL BLSRACCL
         LTR   RF,RF              DATA IN DUMP?
         BNZ   LB004200           NO
         LR    R9,R0              ->DUMP DATA BUFFER
         L     R0,PACKDEC         CALCULATE INDENTATION ON LINE
         S     R0,PACKDEC+4       FROM NUMBER OF BYTES
         SRA   R0,2               WORDS = BYTES/(4=2**2)
         LA    RE,LB003800        SET RETURN ADDR FROM INDENT SUBRTNE
         BNZ   LB003700           R0 NOT ZERO - MUST DO INDENT
         XC    8(1,RA),8(RA)      NULL ONE BYTE AFTER SF,
         LA    RA,9(0,RA)         ATTR, AND DATA ADDR. UP
         BR    RE                 STREAM ADDRESS, AND BYPASS SUBR.
         SPACE 2
*---------------------------------------------------------------------*
*        CALCULATE BUFFER ADDRESS TO INDENT TO                        *
*              USED AS SUBROUTINE BY CHARACTER DATA FORMAT, TOO       *
*---------------------------------------------------------------------*
         SPACE
LB003700 MVI   8(RA),ORDERRA      START REPT-TO-ADDR ORDER SEQUENCE
         LR    R5,R0              INPUT IS NUMBER OF 'WORDS' TO INDENT
         MH    R5,=H'9'           8 BYTES + 1 SEPARATOR PER 'WORD'
         AH    R5,=H'8'           ADDR FIELD TAKES 1 ATTR, 6 BYTES, 1
*                                 SEPARATOR
         LA    R4,5(0,R6)         ZERO ORIGIN ROW NUMBER
         MH    R4,=H'80'          EIGHTY POSITIONS PER ROW
         ALR   R4,R5              FORM BINARY SCREEN POSITION, 0-ORIGN
         SRDL  R4,6               DIVIDE BY (64=2**6)
         SRL   R5,26              AND SAVE REMAINDER
         IC    R4,T3270TAB(R4)
         IC    R5,T3270TAB(R5)
         STC   R4,9(0,RA)         FIRST ADDR BYTE
         STC   R5,10(0,RA)        SECOND ADDR BYTE
         MVI   11(RA),0           NULL CHARACTER INTO STREAM
         LA    RA,12(0,RA)        UPDATE SCREEN BUFFER ADDR
         BR    RE                 RETURN TO CHAR FMT, OR TO NSI
LB003800 LA    R8,8               MAX WORDS PER LINE
         SLR   R8,R0              LESS NUMBER SKIPPED
         SLL   R0,2               WORDS*(4=2**2) = BYTES TO SKIP
         ALR   R9,R0              WHERE TO FETCH DUMP DATA FROM
         L     R1,ZZ1P            ->ZZ1
LB003900 MVC   0(2,RA),NORMAREA   DEFINE THE START OF A FIELD
         MVC   1(1,RA),SAVELATR   SET CORRECT ATTRIBUTE
         UNPK  2(9,RA),0(5,R9)    BIT=>ZONED HEXADECIMAL
         TR    2(8,RA),ZZ1TRHEX(R1) ZONED HEXADECIMAL=>EBCDIC
         LA    RA,10(0,RA)        NEXT BUFFER POSITION
         LA    R9,4(0,R9)         NEXT DUMP DATA POSITION
         BCT   R8,LB003900        DO ALL WORDS FOR THIS LINE
         SPACE 2
*---------------------------------------------------------------------*
*        FINISHED PROCESSING CURRENT LINE--UPDATE INDEX AND CONTINUE  *
*        LOOP                                                         *
*---------------------------------------------------------------------*
         SPACE
LB004100 CH    R6,=H'18'          OUT OF VECTOR?
         BNL   LB004150           YES
         IC    R2,WKALNCTL(R6)    VECTOR INTO AREA TABLE
         LA    RF,255-WKALNCTW    CLEAR OUT REWRITE
         NR    R2,RF               FLAG
         BZ    LB004125           NULL LINE
         BCTR  R2,0               FORM ZERO ORIGIN INDEX
         SLL   R2,2               *(2**2=4) FOR WORD ARRAY
         LA    RF,WKAVCTA(R2)     PROPER CONTROL WORD FOR THIS AREA
         L     RE,4*4(RF)         PICK UP CURRENT ADDRESS
         CLI   34(RF),C'X'        IS IT HEX?
         BNE   LB004110           NO
         N     RE,=X'FFFFFFE0'    ROUND DOWN TO HEX BOUNDARY
         LA    RE,32(0,RE)        ADD HEX BOUNDARY VALUE=LINE LENGTH
         B     LB004120
LB004110 N     RE,=X'FFFFFFC0'    ROUND DOWN TO CHAR BOUNDARY
         LA    RE,64(0,RE)        ADD CHAR BOUNDARY VALUE=LINE LENGTH
LB004120 ST    RE,16(0,RF)        SAVE FOR NEXT PASS THIS AREA
LB004125 LA    R6,1(0,R6)         INCREMENT ROW INDEX
         B     LB003200           CONTINUE ROW LOOP
LB004150 TM    FLAG0,FLAG0NUL     NEED TO NULL REST OF SCREEN?
         BNO   LB005300           NO
         MVC   0(2,RA),PROTAREA   ESTABLISH PROTECTED FIELD
         MVC   2(ORDLNRA,RA),RW6RAORD NULL TO 0,0
         LA    RA,ORDLNRA+2(0,RA) CORRECT BUFFER ADDRESS
         B     LB005300
         SPACE 2
*---------------------------------------------------------------------*
*        UNABLE TO ACCESS DATA FOR THIS LINE - ADDRESS OF DATA IS     *
*        ALREADY IN SCREEN STREAM                                     *
*---------------------------------------------------------------------*
         SPACE
LB004200 MVC   8(4,RA),=C' N/A'   RIGHT NEXT TO ADDRESS
         LA    RA,12(0,RA)        UPDATE BUFFER PTR
         OI    FLAG0,FLAG0NUL+FLAG0POK
         B     LB004100
         SPACE 2
*---------------------------------------------------------------------*
*        NEED TO WRITE A NULL LINE--EITHER A SEPARATOR BETWEEN FIELDS *
*        OR A LINE IN AN UNUSED AREA                                  *
*---------------------------------------------------------------------*
         SPACE
LB004300 TM    FLAG0,FLAG0POK+FLAG0NUL ALREADY NULLING SCREEN?
         BO    LB004100           YES
         BM    LB004400           NO, BUT POSITION IS OK
         MVI   0(RA),ORDERSBA     SBA TO THIS LINE ADDR
         LA    RE,0(R6,R6)        FORM INDEX INTO ARRAY
         LH    RE,BUFADTAB(RE)    AND ACCESS 3270 LINE ADDR
         STCM  RE,B'0011',1(RA)   ADDR INTO STREAM
         LA    RA,ORDLNSBA(0,RA)  UPDATE BUFFER ADDRESS
LB004400 OI    FLAG0,FLAG0POK+FLAG0NUL POS. OK, NULLS NEEDED
         B     LB004100           CHECK NEXT ROW
         SPACE 2
*---------------------------------------------------------------------*
*        AT A LINE NOT TO BE UPDATED. CHECK IF NULLIFICATION NEEDED   *
*        AT THIS POINT, AND IF SO, IF LINE CONTAINS USEFUL DATA TO    *
*        BE LEFT ON THE SCREEN.                                       *
*---------------------------------------------------------------------*
         SPACE
LB004600 TM    FLAG0,FLAG0POK+FLAG0NUL ANYTHING PENDING?
         BZ    LB004100           NO
         BO    LB004700           YES, CHECK IF ANY DATA ON SCREEN
         NI    FLAG0,255-FLAG0POK BUFFER NO LONGER SET UP
         B     LB004100
         SPACE 2
*---------------------------------------------------------------------*
*        BOTH FLAG0NUL AND FLAG0POK ON, CHECK IF WE CAN LEAVE         *
*        THEM ON, OR MUST WE BYPASS THIS SCREEN LINE.                 *
*---------------------------------------------------------------------*
         SPACE
LB004700 LTR   R2,R2              IS THIS LINE ALREADY NULL?
         BZ    LB004100           YES
         BCTR  R2,0               NO, FORM INDEX VALUE INTO AREA
         SLL   R2,2               *(4=2**2) CONTROL VECTORS
         LA    RF,WKAVCTA(R2)
         TM    35(RF),WKAVCTAI    ANYTHING IN THE AREA?
         BO    LB004100           NO
         MVC   0(2,RA),PROTAREA   ESTABLISH PROTECTED FIELD
         LA    RA,2(RA)           CORRECT BUFFER AREA
         MVI   0(RA),ORDERRA      INSERT REPEAT-TO ADDRESS ORDER
         LA    RE,0(R6,R6)        FORM INDEX TO BUFADTAB
         LH    RE,BUFADTAB(RE)    PICK UP SCREEN ADDR
         STCM  RE,B'0011',1(RA)   PUT IT INTO SCREEN STREAM
         MVI   3(RA),0            PUT A NULL TO COMPLETE ORD SEQ
         LA    RA,ORDLNRA(0,RA)   UPDATE BUFFER PTR
         NI    FLAG0,255-(FLAG0POK+FLAG0NUL) RESET SWITCHES
         B     LB004100           CHECK NEXT LINE
         SPACE 2
*---------------------------------------------------------------------*
*        FORMAT A CHARACTER LINE ON THE SCREEN                        *
*---------------------------------------------------------------------*
         SPACE
LB004800 L     RE,4*4(0,RF)       CURRENT ADDR FOR THIS AREA
         ST    RE,PACKDEC         INTO WORK AREA
         N     RE,=X'FFFFFFC0'    DOWN TO PROPER BOUNDARY
         ST    RE,PACKDEC+4       ADDRESS TO REQUEST
         LR    R0,RE              PARM FOR PRINT-DUMP
         L     R1,ZZ1P            ->ZZ1
         UNPK  2(7,RA),PACKDEC+5(4) ADDRESS INTO SCREEN STREAM
         TR    2(6,RA),ZZ1TRHEX(R1) ZONED HEXADECIMAL=>EBCDIC
         L     R1,ZZ2P            ->ZZ2
         MVC   ZZ2AMDA(2,R1),32(RF) ASID
         L     RF,ZZ2AMDS(0,R1)   ->BLSRACCL
         BALR  RE,RF              CALL BLSRACCL
         LTR   RF,RF              DUMP DATA AVAILABLE?
         BNZ   LB004200           NO
         LR    R9,R0              PRESERVE 'REAL' DATA ADDR
         L     R0,PACKDEC         CALCULATE INDENTATION, IF ANY
         S     R0,PACKDEC+4       FROM BYTES TO SKIP
         SRA   R0,3               BYTES/(8=2**3) = 'WORDS' TO SKIP
         BNZ   LB004900           SOME
         XC    8(2,RA),8(RA)      NULL TWO POSITIONS
         LA    RA,10(0,RA)        POINT TO NEXT STREAM POSITION
         B     LB005000
LB004900 BAL   RE,LB003700        SET UP SKIP ORDERS
         MVI   0(RA),0            INSERT NULL FOR FIRST SEPARATOR
         LA    RA,1(RA)           CORRECT BUFFER ADDRESS
LB005000 LA    R8,8               SET UP TRANSLATE LOOP
         SLR   R8,R0              WORDS LEFT AFTER INDENT
         SLL   R0,3               *(8=2**3) = BYTES TO SKIP
         ALR   R9,R0              WHERE TO FETCH FIRST DATA FROM
         L     R1,ZZ1P            ->ZZ1
LB005100 MVC   0(8,RA),0(R9)      DATA INTO SCREEN STREAM
         TR    0(8,RA),ZZ1TREBC(R1) NON-ALPHAMERIC TO BLANKS
         MVI   8(RA),C'×'         INSERT SEPARATOR BETWEEN 'WORDS'
         LA    R9,8(0,R9)         UPDATE DATA SOURCE ADDRESS
         LA    RA,9(0,RA)         UPDATE DATA TARGET ADDRESS
         BCT   R8,LB005100        PROCESS ALL 'WORDS' THIS LINE
         BCTR  RA,0               DON'T NEED FINAL SEPARATOR
         B     LB004100           GET A NEW DATA ROW
IB005200 MVC   ORDLNSBA(0,RA),WKABLSRC *** SUBJ OF EX INSTR ***
         SPACE 2
*---------------------------------------------------------------------*
*ADD AN INSERT-CURSOR ORDER                                           *
*---------------------------------------------------------------------*
         SPACE
LB005300 MVC   0(4,RA),WKACORDS
         LA    RA,4(0,RA)         CORRECT BUFFER ADDRESS
         LA    R8,LA000700        SET UP TPUT RETURN ADDRESS
