         TITLE 'BLSR3270--BLSR327G--POST-PROCESS MANUAL INPUT'
*---------------------------------------------------------------------*
*                                                                     *
*BLSR327G--POST-PROCESS MANUAL INPUT                                  *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
BLSR327G CLC   WKAADDR+1(3),ESAULAD+1 HAS THE ADDRESS BEEN CHANGED?
         BE    LG000600           NO
         TM    WKAADDR,WKAADRLL   IS THE OLD ADDRESS ALREADY
*                                 IN THE STACK, OR RESULT OF A ROLL?
         BNZ   LG000600           YES
LG000200 CLI   WKASKNUM,11        STACK FULL NOW?
         BNE   LG000400           NO
         MVC   WKASTACK(40),WKASTACK+4
         MVC   WKASTACK+41(3),WKAADDR+1
         MVC   WKANOTES(80),WKANOTES+8
         XC    WKANOTES+80(8),WKANOTES+80
         MVC   WKASKASI(20),WKASKASI+2
         MVC   WKASKASI+20,WKAASID
         OI    FLAG0,FLAG0RW2     WRITE STACK BACK
         ICM   RF,1,WKANBNUM      NUMBER OF ACTIVE NOTE LIST ENTRIES
         BZ    LG000600
         BCTR  RF,0               DECREMENT
         STC   RF,WKANBNUM        ADDRESS STACK ENTRY OFF
         OI    FLAG0,FLAG0RW3     WRITE NOTE LIST BACK ALSO
         B     LG000600           A FULL STACK HAS BEEN UPDATED
IG000300 OC    0(0,R6),=19AL1(WKALNCTW) *** SUBJ EX, B
LG000400 SLR   RF,RF              PREPARE FOR INSERT CHAR
         IC    RF,WKASKNUM        CURRENT NUMBER OF ACTIVE ENTRIES
         LA    R0,1(0,RF)         UP 1
         STC   R0,WKASKNUM        SAVE NEW ACTIVE ENTRY NUMBER
         CLI   WKASKNUM,11        DID THE STACK JUST FILL UP?
         BNE   LG000500           -NO
         OI    NEWWCC,WCCALARM    YES, BEEP AT HIM
LG000500 ALR   RF,RF              DOUBLE TO FORM HWORD INDEX
         LH    R0,WKAASID         MOVE ASID TO STACK
         STH   R0,WKASKASI(RF)    INTO PROPER SLOT
         ALR   RF,RF              DOUBLE HWORD INDEX TO GET WORD INDEX
         L     R0,WKAADDR         MOVE CURRENT ADDRESS
         ST    R0,WKASTACK(RF)    INTO PROPER SLOT
         OI    FLAG0,FLAG0RW2     RE-WRITE STACK LINE
LG000600 CLI   WKASKNUM,11        IS THE ADDRESS STACK FULL?
         BL    LG000605           -NO
         OI    FLAG2,FLAG2STK     -YES, REMEMBER TO PUT OUT MESSAGE
LG000605 TM    FLAG1,FLAG1ADR+FLAG1ASI+FLAG1FMT+FLAG1ARE  UPDATE
*                                 -CONTROL ARRAY?
         BZ    LG000800           NO
         TM    FLAG1,FLAG1ADR+FLAG1ASI NEW ADDRESS?
         BZ    LG000615           NO
         MVC   WKAADDR(4),ESAULAD NEW ADDRESS
         TM    FLAG1,FLAG1RLL     RESULT OF SKIP,ROLL OR STACK?
         BZ    LG000610           NO
         OI    WKAADDR,WKAADRLL   SUPPRESS IMPLICIT STACK OPERATIONS
LG000610 MVC   WKAASID(2),ESAUAS2+2 NEW ASID
LG000615 MVC   WKAFMT,ESAUDTY     NEW FORMAT
         CLI   WKAFMT,C'C'        HANDLE CASE WHEN
         BE    LG000620           -NO INITIALIZATION
         MVI   WKAFMT,C'X'        -HAS BEEN DONE
LG000620 EQU   *
         IC    RF,WKAAREA         PREPARE TO UPDATE CONTROL ARRAY
         N     RF,=X'00000007'    KEEP JUST LOW THREE BITS
         LR    R0,RF              SAVE BYTE INDEX (1-ORG)
         SLL   RF,2               FORM WORD INDEX 1-ORG
         LR    R6,RF              SAVE WORD INDEX (1-ORG)
         LA    RF,WKAVCTA-4(RF)   ADDRESS PROPER ARRAY
         MVC   WKAVCTA-WKAVCTA(4,RF),WKAADDR NEW ADDRESS
         MVC   WKAVASI-WKAVCTA(2,RF),WKAASID NEW ASID
         MVC   WKAVFMT-WKAVCTA(1,RF),WKAFMT NEW FORMAT
         NI    WKAVFLAG-WKAVCTA(RF),255-WKAVCTAI OFF INVALID FLAG
         LR    RF,R0              AREA VALUE BACK IN GOOD INDEX REG
         IC    RF,WKALINES-1(RF)  NUMBER LINES THIS AREA
         LTR   RF,RF              ANY LINES ALLOCATED?
         BNZ   LG000700           YES
         MVI   NEWWCC,WCCALARM+WCCRSMDT
         B     LG000800           GO BLOW YOUR HORN
LG000700 BCTR  RF,0               DOWN 1 FOR EX OF OC
         L     R6,WKALNCTV-4(R6)  OFFSET INTO CONTROL VECTOR
         LA    R6,WKALNCTL(R6)    PLACE TO START WRITING
         EX    RF,IG000300        MARK LINES TO BE RE-WRITTEN
LG000800 TM    FLAG0,FLAG0SUS     MUST WE DEPART NOW?
         BNO   BLSR327B           NO - PREPARE OUTPUT
         B     LA005600           YES, HANDLE SUBCOMMAND
