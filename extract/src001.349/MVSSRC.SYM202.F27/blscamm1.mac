         MACRO
         BLSCAMM1 &LIST
.** START OF SPECIFICATIONS *******************************************
.*
.*  MACRO NAME= BLSCAMM1
.*
.*  DESCRIPTIVE NAME= COUNT PARAMETER SUBLIST ENTRIES
.*
.*  COPYRIGHT= NONE
.*
.*  STATUS= SU57 RELEASE 1, LEVEL 0
.*
.*  FUNCTION=
.*             COUNT PARAMETER SUBLIST ENTRIES.
.*
.*  NOTES= NONE
.*
.*  MODULE TYPE= EXECUTABLE MACRO
.*
.*     PROCESSOR= ASSEMBLER F
.*
.*     ATTRIBUTES= REENTRABLE
.*
.*  INVOCATION SYNTAX=
.*
.*           BLSCAMM1 LIST
.*
.*           LIST
.*             PARAMETER SUBLIST FOR WHICH ENTRY COUNT IS TO BE MADE.
.*
.*  OUTPUT= GBLA &BLSCAGA IS SET TO THE NUMBER OF SUBLIST ENTRIES.
.*
.*  EXTERNAL REFERENCES= SEE BELOW
.*
.*     ROUTINES= NONE
.*
.*     DATA AREAS=  NONE
.*
.*     CONTROL BLOCKS=  NONE
.*
.*     CONDITIONAL ASSEMBLY SET SYMBOLS=  BLSCAGA    GBLA
.*
.*  MACROS=  NONE
.*
.*  CHANGE ACTIVITY=
.*         THIS MODULE IS WRITTEN FOR @G57LPSR
.*
.*  MESSAGES ISSUED=
.*     MACRO COMPILE TIME ERROR MESSAGES (MNOTES)
.*     8,LIST CONTAINS AN INNER SUBLIST
.*     8,INVALID LIST FORMAT
.*
.** END OF SPECIFICATIONS *********************************************
         GBLA  &BLSCAGA            PARAMETER SUBLIST COUNT
         LCLA  &CTR                CURRENT LIST BYTE COUNTER
         LCLA  &LEN                LIST LENGTH
         LCLC  &STR                LIST CHARACTER STRING
&CTR     SETA  1                   CURRENT BYTE COUNTER
&STR     SETC  '&LIST'             LIST CHAR STRING
&LEN     SETA  K'&STR              LIST LENGTH
         AIF   (&LEN GT 0).SK1     NON NULL LIST
&BLSCAGA SETA  0                   NULL LIST
         AGO   .END
.SK1     AIF   ('&STR' NE '()').SK2 NOT NULL LIST
&BLSCAGA SETA  0                   NULL LIST
         AGO   .END
.SK2     AIF   ('&STR'(1,1) EQ '(').SK3  MULTI ELEMENT LIST CANDIDATE
&BLSCAGA SETA  1                   ONE ELEMENT LIST
         AGO   .END
.SK3     ANOP
&BLSCAGA SETA  1                   SET UP ELEMENT COUNT LOOP
         AGO   .LPTST              ENTER LOOP
.LOOP    AIF   ('&STR'(&CTR,1) NE '(').SK4
         MNOTE 8,'LIST CONTAINS AN INNER SUBLIST'
         AGO   .ERR                ERROR EXIT
.SK4     AIF   ('&STR'(&CTR,1) NE ',').SK5  NOT ELEMENT END
&BLSCAGA SETA  &BLSCAGA+1           BUMP ELEMENT COUNT
         AGO   .LPTST
.SK5     AIF   ('&STR'(&CTR,1) NE ')').LPTST  NOT END OF PLIST
         AIF   (&CTR EQ &LEN).END   LIST END MATCHES LOGICAL END
         MNOTE 8,'INVALID LIST FORMAT'
         AGO   .ERR
.LPTST   ANOP
&CTR     SETA  &CTR+1               BUMP CURRENT BYTE COUNTER
         AIF   (&CTR LE &LEN).LOOP  NOT END OF LIST
         AGO   .END
.ERR     ANOP
&BLSCAGA SETA  -1                   INDICATE ERROR TO CALLER
.END     MEND
