         TITLE 'BLSR3270--BLSR327F--POST-PROCESS CURSOR'
*---------------------------------------------------------------------*
*                                                                     *
*BLSR327F--POST-PROCESS CURSOR COMMANDS                               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
BLSR327F LM    R4,R5,WKACRPOS RELOAD SAVED INDICES
         LTR   R5,R5              ON ROW ONE - A CURSOR COMMAND?
         BNZ   BLSR327G           NO, NO POST-PROCESS NEEDED
         LH    RF,YF000200(R4)    INDEX BASED ON COLUMN
         B     BLSR327B(RF)       GO TO APPROPRIATE ROUTINE
         SPACE 2
YF000200 DC    Y(BLSR327G-BLSR327B) ATTRIBUTE MESSAGE OR BLANK
         DC    Y(LA005600-BLSR327B) END
         DC    Y(LF000300-BLSR327B) -SCROLL
         DC    Y(LF000400-BLSR327B) +SCROLL
         DC    Y(LG000200-BLSR327B) STACK
         DC    Y(LF000700-BLSR327B) -SKIP
         DC    Y(LF000800-BLSR327B) +SKIP
         SPACE 2
*                                  ************************************
*                                  *                                  *
*                                  *   POST-PROCESS PF1 = ROLL BACKWD *
*                                  *                                  *
*                                  ************************************
LF000300 BAL   RE,LF000600        PERFORM COMMON CALCULATIONS FOR ROLL
*        R0    HAS NUMBER OF BYTES DISPLAYED IN CURRENT AREA
*        R1    HAS ROUNDED DOWN ADDR FROM WKAVCTA ENTRY
*        RF HAS INDEXED WORK ARRAY ADDRESS
         SLR   R1,R0              CALCULATE NEW START ADDR
         B     LF000500           GO STORE, SET SWITCHES
*                                 MUST WAIT FOR ANY SCREEN PARAMETER
*                                 UPDATES TO BE PROCESSED.
         SPACE 2
*                                  ************************************
*                                  *                                  *
*                                  *   POST-PROCESS PF2 = ROLL FORWARD*
*                                  *                                  *
*                                  ************************************
LF000400 BAL   RE,LF000600        SEE COMMENTARY ABOVE, AND BELOW
         ALR   R1,R0              CALCULATE NEXT ADDRESS
LF000500 LA    R1,0(R1)           CLEAR HIGH-ORDER BYTE
         ST    R1,ESAULAD         NEW ADDRESS INTO ENTRY LINE
         OI    FLAG1,FLAG1ADR+FLAG1RLL
         B     BLSR327G           GO CHECK STACK, ETC
LF000600 IC    RF,WKAAREA         PICK UP AREA TO BE ROLLED
         N     RF,=X'00000007'    KEEP ONLY LOW 4 BITS
         SLR   R0,R0              CLEAR FOR LATER ADDITION
         IC    R0,WKALINES-1(RF)  LINES THAT WERE DISPLAYED
         SLL   R0,5               BYTES TO MOVE
         SLL   RF,2               WORD INDEX
         LA    RF,WKAVCTA-4(RF)   ADDRESS NEW CURRENT ARRAY
         L     R1,WKAVCTA-WKAVCTA(RF) PICK UP OLD START ADDR
         N     R1,=X'FFFFFFE0'    ROUND DOWN TO 32-BYTE BDY
         CLI   WKAVFMT-WKAVCTA(RF),C'C' WAS IT CHARACTER?
         BNER  RE                 NO
         N     R1,=X'FFFFFFC0'    ROUND DOWN TO 64-BYTE BDY
         ALR   R0,R0              DOUBLE DIFFERENCE
         BR    RE
         SPACE 2
*                                  ************************************
*                                  *                                  *
*                                  *  POST-PROCESS PF10 = SKIP BACKWD *
*                                  *                                  *
*                                  ************************************
LF000700 BAL   RE,LF000900        PICK UP ADDRESS, SKIP AMOUNT
         SLR   R1,R0              DECREMENT OLD BY SKIP AMOUNT
         BR    RF
         SPACE 2
*                                  ************************************
*                                  *                                  *
*                                  * POST-PROCESS PF11 = SKIP FORWARD *
*                                  *                                  *
*                                  ************************************
LF000800 BAL   RE,LF000900        PICK UP ADDRESS, SKIP AMOUNT
         ALR   R1,R0              INCREMENT OLD BY SKIP AMOUNT
         BR    RF
LF000900 LA    RF,LF000500        WHERE TO GO WHEN DONE
         L     R0,WKASKIP         HOW MUCH TO CHANGE ADDR BY
         L     R1,WKAADDR         ADDR TO CHANGE
         BR    RE
