         MACRO
         BLSRDTD &DTD,&FIND=,&NORMAL=,&SCAN=
.*--------------------------------------------------------------------*
.* BLSRDTD--DEFINE PROCESSING FOR ONE DATA TYPE                       *
.*--------------------------------------------------------------------*
         GBLA  &DTDIM
         GBLC  &GP,&GD(100),&GN(100),&GS(100),&GV(100)
.*------------ PROCESS DATA TYPE NAME  ------------------------------
&DTDIM   SETA  &DTDIM+1                DIMENSION OF DATA TYPE ARRAY
&GD(&DTDIM) SETC '&DTD'                DATA TYPE
.*------------ PROCESS NORMALIZED DATA TYPE NAME --------------------*.
         AIF   (T'&NORMAL EQ 'O').L01  IMPLICIT NORMALIZED DATA TYPE
&GN(&DTDIM) SETC '&NORMAL'             NORMAL DATA TYPE
         AGO   .L02
.L01     ANOP
&GN(&DTDIM) SETC '&DTD'                NO NORMAL DATA TYPE
.*------------ PROCESS FIND EXIT NAME  ------------------------------*.
.L02     AIF   (T'&FIND EQ 'O').L03    NO FIND EXIT
&GS(&DTDIM) SETC '&FIND'               FIND EXIT NAME
         AGO   .L04
.L03     ANOP
&GS(&DTDIM) SETC ' '                   NO FIND EXIT
.*------------ PROCESS SCAN EXIT NAME  ------------------------------*.
.L04     AIF   (T'&SCAN EQ 'O').L05    NO SCAN EXIT
&GV(&DTDIM) SETC '&SCAN'               SCAN EXIT NAME
         AGO   .L06
.L05     ANOP
&GV(&DTDIM) SETC ' '                   NO SCAN EXIT
.L06     ANOP
         MEND
