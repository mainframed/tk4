         MACRO
&LABEL   BLSRDEND &PREFIX
.*--------------------------------------------------------------------*
.* BLSRDEND--TERMINATE A DATA TYPE PROCESSING TABLE DEFINITION        *
.*--------------------------------------------------------------------*
         GBLA  &DTDIM
         GBLC  &GP,&GD(100),&GN(100),&GS(100),&GV(100)
         LCLA  &JD,&JL,&JX
         LCLC  &CD,&CN,&CS,&CV,&CX
&GP.DIM  DC    F'&DTDIM'               DIMENSION OF ARRAY
*------- END OF TABLE HEADING ----------------------------------------*
         SPACE
*------- BEGIN BODY OF TABLE -----------------------------------------*
&JD      SETA  1                       CURRENT DATA NAME INDEX
.L01     AIF   (&JD GT &DTDIM).L99     ALL DATA NAME PROCESSED
&CD      SETC  '&GD(&JD)'              CURRENT DATA NAME
&JL      SETA  K'&CD                   LENGTH OF DATA NAME
&CN      SETC  '&GN(&JD)'              NORMALIZED DATA NAME
&CS      SETC  '&GS(&JD)'              FIND EXIT ENTRY POINT NAME
&CV      SETC  '&GV(&JD)'              SCAN EXIT ENTRY POINT NAME
&JX      SETA  &JD                     NORMALIZED SEARCH INDEX
         AIF   ('&CD' EQ '&CN').L03    SELF-NORMALIZED
&JX      SETA  &DTDIM                  NORMALIZED SEARCH INDEX
&CX      SETC  '&GD(&DTDIM)'           SEARCH DATA NAME
.L02     AIF   ('&CN' EQ '&CX').L03    NORMALIZED TYPE FOUND
&JX      SETA  &JX-1                   NORMALIZED SEARCH INDEX
&CX      SETC  '&GD(&JX)'              SEARCH DATA NAME
         AIF   (&JX GE 1).L02          CONTINUE NORMALIZED TYPE SEARCH
         MNOTE 8,'NORMALIZED DATA NAME ERROR FOR &CD'
&JX      SETA  &JD                     SUBSTITUTE DATA NAME
.L03     ANOP
&GP.&JD  DC    FL1'&JL',CL31'&CD',F'&JX',CL8'&CS',CL8'&CV'
&JD      SETA  &JD+1                   DATA NAME INDEX
         AGO   .L01
.L99     ANOP
         SPACE
*------- END OF DATA NAME PROCESSING TABLE ---------------------------*
         MEND
