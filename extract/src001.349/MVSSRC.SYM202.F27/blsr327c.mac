         TITLE 'BLSR3270--BLSR327C--PRE-PROCESS ATTENTION IDENTIFIER'
*---------------------------------------------------------------------*
*                                                                     *
*BLSR327C--PRE-PROCESS ATTENTION IDENTIFIER                           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
*        BY CONVERTING THE ATTENTION ID BACK TO THE 3270 INTERNAL
*        REPRESENTATION, WE ARRIVE AT A COLLATING SEQUENCE THAT
*        STARTS WITH THE 'SHORT-READ' IDS, THROUGH THE PF KEYS, TO
*        THE ENTER AND LIGHT PEN IDS.
         IC    RF,0(RA)           FIRST CHAR IN BUFFER IS AID
         N     RF,=X'0000003F'    KEEP ONLY LOW SIX BITS
         CH    RF,=X'0000003E'    LIGHT PEN ATTENTION
         BE    LC000200           YES
         SH    RF,=Y(AIDIGNOR)    SHORT READ
         BNM   LC000300           NO
LC000200 OI    FLAG0,FLAG0FMT     MUST REWRITE ENTIRE SCREEN
         MVC   WKACORDS+1(2),PREVCURS RESTORE OLD CURSOR POSITION
         B     BLSR327B           PREPARE OUTPUT
LC000300 CH    RF,=H'2'           IS IT END?
         BE    LA005600           -YES SKIP REST
         ALR   RF,RF              DOUBLE TO INDEX YCON TABLE
         STH   RF,WKAAIDNX        SAVE INDEX
         B     BLSR327J           CHECK INPUT FOR ERRORS
         SPACE
LC000320 LA    RA,SCRNBUFR        -> INPUT BUFFER
         L     R7,SAVER7          RESTORE INPUT LENGTH
         LH    RF,WKAAIDNX        RESTORE INDEX
         LH    RF,YC000400(RF)    OFFSET TO ROUTINE
         B     BLSR327B(RF)       GO TO PROPER PRE-PROCESSOR
         SPACE 2
*---------------------------------------------------------------------*
*        ATTENTION IDENTIFIER (AID) PRE-PROCESSOR VECTOR TABLE        *
*        AID   AID&X'3F'          MEANING OF AID                      *
*        --    ---------          --------------                      *
*        60    00                 NOP TO POLL OF DISPLAY              *
*        E6    26                 CARD READER READ A STRIPE           *
*        E8    28                 NOP TO POLL OF PRINTER              *
*        6B    2B                 PA3                                 *
*        6C    2C                 PA1                                 *
*        6D    2D                 CLEAR                               *
*        6E    2E                 PA2                                 *
*        F0    30                 TEST REQUEST                        *
*---------------------------------------------------------------------*
         SPACE
YC000400 DC    Y(BLSR327D-BLSR327B) F1-->31 PF1 ROUTINE OFFSET=ENTER
         DC    Y(BLSR327D-BLSR327B) F2-->32 PF2 ROUTINE OFFSET=ENTER
         DC    Y(LA005600-BLSR327B) F3-->33 PF3 ROUTINE OFFSET=END
         DC    Y(BLSR327D-BLSR327B) F4-->34 PF4 ROUTINE OFFSET=ENTER
         DC    Y(BLSR327D-BLSR327B) F5-->35 PF5 ROUTINE OFFSET=ENTER
         DC    Y(BLSR327D-BLSR327B) F6-->36 PF6 ROUTINE OFFSET=ENTER
         DC    Y(LD001800-BLSR327B) F7-->37 PF7 ROUTINE OFFSET=-SCROLL
         DC    Y(LD001800-BLSR327B) F8-->38 PF8 ROUTINE OFFSET=+SCROLL
         DC    Y(LD001800-BLSR327B) F9-->39 PF9 ROUTINE OFFSET=STACK
         DC    Y(LD001800-BLSR327B) 7A-->3A PF10 ROUTINE OFFSET=-SKIP
         DC    Y(LD001800-BLSR327B) 7B-->3B PF11 ROUTINE OFFSET=+SKIP
         DC    Y(BLSR327D-BLSR327B) 7C-->3C PF12 ROUTINE OFFSET=ENTER
         DC    Y(BLSR327D-BLSR327B) 7D-->3D ENTER ROUTINE OFFSET
