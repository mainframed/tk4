         MACRO
&L       BLSCGETF &R,&LV=
.*** START OF SPECIFICATIONS *****************************************/
.**                                                                  */
.** MODULE NAME= BLSCGETF                                            */
.**                                                                  */
.** DESCRIPTIVE NAME= IPCS DAS FREE PSUEDO GETMAIN MACRO             */
.**                                                                  */
.** COPYRIGHT= NONE                                                  */
.**                                                                  */
.** STATUS= SU57 RELEASE 1, LEVEL 0                                  */
.**                                                                  */
.** FUNCTION=  BLSCGETF PERFORMS A PSEUDO-GETMAIN FUNCTION FOR THE   */
.**            IPCS DATA ACCESS SERVICES FREE MODULE.                */
.**                                                                  */
.** NOTES= SEE BELOW                                                 */
.**                                                                  */
.**    REGISTER CONVENTIONS=                                         */
.**                                                                  */
.**       @00 - REG  0 - INITIALLY CONTAINS THE NUMBER OF BYTES TO   */
.**                      ALLOCATE FROM THE DMCB FREE STORAGE AREA.   */
.**                      AT EXIT IT WILL CONTAIN THE ADDRESS OF THE  */
.**                      NEXT AVAILABLE BYTE IN THE FREE STORAGE     */
.**                      AREA.                                       */
.**                                                                  */
.**       @01 - REG  1 - CONTAINS A POINTER TO THE ADDRESS OF THE    */
.**                      DMCB FROM WHICH THE FREE STORAGE IS TO BE   */
.**                      OBTAINED.  AT EXIT IT WILL CONTAIN THE      */
.**                      ADDRESS OF THE STORAGE WHICH WAS ALLOCATED. */
.**                                                                  */
.**       @14 - REG 14 - USED TO ADDRESS DMCB DURING STORAGE         */
.**                      ALLOCATION.  NOT RESTORED BY MACRO.         */
.**                                                                  */
.** MODULE TYPE= MACRO                                               */
.**                                                                  */
.**    PROCESSOR= ASSEMBLER                                          */
.**                                                                  */
.**    MODULE SIZE= TO BE COMPLETED                                  */
.**                                                                  */
.** ENTRY POINT= BLSCGETF                                            */
.**                                                                  */
.**    PURPOSE= SEE FUNCTION                                         */
.**                                                                  */
.**    LINKAGE=                                                      */
.**             R01 LOADED WITH A POINTER TO THE DMCB ADDRESS        */
.**                                                                  */
.**             R00 LOADED WITH REQUESTED STORAGE SIZE.              */
.**                                                                  */
.**             THIS MACRO IS INVOKED BY THE PROLOGUE CODE  IN LIEU  */
.**             OF GETMAIN FOR THE IPCS DAS FREE MODULE.             */
.**                                                                  */
.** EXIT NORMAL=                                                     */
.**                                                                  */
.**             REGISTER 1 CONTAINS THE ADDRESS OF THE AREA WHICH    */
.**             WAS ALLOCATED.                                       */
.**                                                                  */
.** EXIT ERROR=                                                      */
.**                                                                  */
.**             IF THE DMCB FREE STORAGE AREA CONTAINS INSUFFICIENT  */
.**             STORAGE A USER ABEND 999 WILL OCCUR.  THIS SITUATION */
.**             SHOULD NEVER OCCUR.                                  */
.**                                                                  */
.** EXTERNAL REFERENCES=                                             */
.**                                                                  */
.**    CONTROL BLOCKS=  DATA ACCESS SERVICES CONTROL BLOCK (DMCB)    */
.**                                                                  */
.** CHANGE ACTIVITY= AS FOLLOWS:                                     */
*         THIS MODULE IS WRITTEN FOR @G57LPSR                        */
.**                                                                  */
.*** END OF SPECIFICATIONS *******************************************/
         LCLC  &BPT
&BPT     SETC 'DMG&SYSNDX'
&L       L     @14,0(,@01)       USE REGISTER 14 TO ADDRESS DMCB
         LR    @01,@00           LENGTH INTO REG 1 FOR INC BY LOAD ADDR
         LA    @00,7(@01)        THIS AND NEXT 2 INSTRUCTIONS
         SRL   @00,3             ROUND LENGTH TO A MULTIPLE OF EIGHT
         SLL   @00,3             FOR COMPATIBILITY WITH GETMAIN
         A     @00,DMCBFRE(@14)  COMPUTE NEXT FREE ADDRESS
         C     @00,DMCBEOB(@14)  IS THERE ENOUGH ROOM?
         BL    &BPT              IF SO THEN PROCEED
         ABEND 999,DUMP          SHOULD NEVER OCCUR
&BPT     L     @01,DMCBFRE(@14)  PICK UP ADDRESS TO USE FOR @DATD
         ST    @00,DMCBFRE(@14)  STORE ADDRESS OF NEXT FREE AREA
         MEND
