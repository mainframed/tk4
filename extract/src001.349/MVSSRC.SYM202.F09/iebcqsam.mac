 SPACE 2
***********************************************************************
*                                                                     *
* NAME   IEBCQSAM                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
* FUNCTION/OPERATION                                                  *
*                                                                     *
*     THIS MODULE IS ENTERRED WHEN PROCESS DATA SETS CONTAINING VS    *
*   RECORDS BY QSAM IS DESIRED. ITS FUNCTIONS ARE AS FOLLOWS          *
*                                                                     *
*   1. TO SET ITS OWN ADDRESSABILITY                                  *
*   2. TO SET COMMUNICATION REGION'S ADDRESSABILITY                   *
*   3. TO OPEN THE TWO INPUT DATA SETS FOR QSAM MOVE MODE PROCESSING  *
*   4. TO VERIFY THE LITTLE LL FIELD OF CORRESPONDING RECORDS         *
*   5. BRACH TO COMPAR TO COMPARE THE CORRESPONDING LOGICAL RECORDS   *
*   6. BRACH TO BLPRT TO COVERT AND PRINT THE EXTRA RECORDS ON EITHER *
*      INPUT DATA SETS                                                *
*   7. BRANCH TO BEFNREND OF IEBCMAIN FOR TERMINATION AFTER BOTH INPUT*
*      DATA SETS REACHED EOD CONDITION                                *
*                                                                     *
***********************************************************************
*                                                                     *
* ENTRY POINTS                                                        *
*                                                                     *
*   'QSAM'  IS THE ONLY ENTRY POINT TO THIS MODULE                    *
*                                                                     *
***********************************************************************
*                                                                     *
* INPUT                                                               *
*                                                                     *
*   INPUT TO THIS MODULE ARE THE USER DDNAMES FOR SYSUT1 AND SYSUT2   *
* FROM THE COMMUNICATION REGION OF ROOT SEGMENT                       *
*                                                                     *
***********************************************************************
*                                                                     *
* OUTPUT                                                              *
*                                                                     *
*   OUTPUT FROM THIS MODULE ARE DIAGNOSTIC MESSAGES                   *
*                                                                     *
***********************************************************************
*                                                                     *
* EXTERNAL ROUTINES                                                   *
*                                                                     *
*   THIS MODULE USES ROOT SEGMENT'S PRINT ROUTINE,JPRINTRT, TO PRINT  *
* DIAGNOSTIC MESSAGES. IT USES IEBCMAIN'S COMPAR ROUTINE TO COMPARE   *
* RECORDS. IT USES IEBCMAIN'S BLPRT ROUTINE TO CONVERT AND PRINT EXTRA*
* RECORDS ON EITHER INPUT DATA SETS. IT ALSO BRANCH TO IEBCULET VIA   *
* LABEL EXITS ENTRY IN DCBEXLST FOR USER INPUT HEADER/TRAILER LABEL   *
* PROCESSING.                                                         *
*                                                                     *
***********************************************************************
*                                                                     *
* EXITS                                                               *
*                                                                     *
*   THE ONLY EXITS FROM THIS MODULE IS A BRACH TO BEFNREND OF IEBCMAIN*
* FOR TERMINATION AFTER BOTH INPUT DATA SETS REACHED EOD CONDITION    *
*                                                                     *
***********************************************************************
*                                                                     *
* TABLES/WORK AREA                                                    *
*                                                                     *
*   THERE IS NO TABLE/WORK AREA IN THIS MODULE                        *
*                                                                     *
***********************************************************************
*                                                                     *
* ATTRIBUTES                                                          *
*                                                                     *
*     NON-REUSABLE                                                    *
*                                                                     *
***********************************************************************
  EJECT
***********************************************************************
*                                                                     *
COMMX    DSECT
         USING COMMX,10
SAVE1315 DS    3F             SAVE AREA FOR REGS 13,14,15
SAVEREGS DS    18F            MY REGISTER SAVE AREA
PARALIST DS    8F             THE EXITS PARAMETER LIST-USER ROUTS
*                               1. INHDR PARAMETER NAME
*                               2. INTLR PARAMETER NAME
*                               3. ERROR PARAMETER NAME
*                               4. PRECOMP PARAMETER NAME
DDSYSIN  DS    2F             SYSIN PARAMETER
DDSUBNAM DS    4F             DDNAME PARAMETER LIST
*                               1. SYSUT1 DDNAME
*                               2. SYSUT2 DDNAME
HICONCOD DS    2X             RETURN CODE STORAGE AREA
CCADSORG DS    1X             SET TO '01' IF PARTITIONED DATA SET
ABORTSW  DS    1X             ABORT SWTICH-SET TO '01' IF ON
LABEL    DS    1X             SET TO X'00' IF DATA=YES             UL0H
*                                    X'01' IF DATA=NO              UL0H
*                                    X'10' IF DATA=ONLY            UL0H
*                                    X'20' IF DATA=ALL             UL0H
UEXIT    DS    1X             SET TO 01 IF OPEN TOOK USER LAB EXIT UL0H
UHRADR   DS    1F             CONTAIN ADDR. OF INHDR               UL0H
UTRADR   DS    1F             CONTAIN ADDR. OF INTLR               UL0H
INHD1    DS    1F             HAS ADDR. OF SYSUT1 HD LAB SAVE AREA UL0H
INHD2    DS    1F             HAS ADDR. OF SYSUT2 HD LAB SAVE AREA UL0H
INHDCT1  DS    1F             CONT. NO. OF HEADER LABELS FOR SYSUT1UL0H
INHDCT2  DS    1F             CONT. NO. OF HEADER LABELS FOR SYSUT2UL0H
INTL1    DS    1F             HAS ADDR. OF SYSUT1 TL LAB SAVE AREA UL0H
INTL2    DS    1F             HAS ADDR. OF SYSUT2 TL LAB SAVE AREA UL0H
INTLCT1  DS    1F             CONT. NO. OF TRAILER LABEL FOR SYSUT1UL0H
INTLCT2  DS    1F             CONT. NO. OF TRAILER LABEL FOR SYSUT2UL0H
BUFF1    DS    1F             CONTAIN ADDR. OF READ AREA-DS1       VS0H
BUFF2    DS    1F             CONTAIN ADDR. OF READ AREA-DS2       VS0H
LBUFF1   DS    1F             LENGTH OF BUFFER FOR SYSUT1 RCD PROC VS0H
LBUFF2   DS    1F             LENGTH OF BUFFER FOR SYSUT2 RCD PROC VS0H
LORECO   DS    1F             TOTAL LOGICAL RECORD COUNTER         VS0H
RECLTH   DS    1H             LOGICAL RECORD LENGTH-DS1            VS0H
RECLH2   DS    1H             LOGICAL RECORD LENGTH-DS2            VS0H
DSET1    DS    1F            ADDRS. OF SYSUT1 DCB IN IEBCQSAM      VS0H
DSET2    DS    1F            ADDRS. OF SYSUT2 DCB IN IEBCQSAM      VS0H
INPUTLOC DS    1F            ADDR. OF INPUT BUFFER FOR BLPRT RT    VS0H
NOEXTSW  DS    1X             NO EXIT TO USER LAB RTN IF SET TO 01 UL0H
TERMSW   DS    1X             TERM. AFTER LAB. PROC. IF SET TO 01  UL0H
EXIT1SW  DS    1X             SET TO 01 IF THERE IS AN USER INHDR  UL0H
EXIT3SW  DS    1X             SET TO 01 IF THERE IS AN USER INTLR  UL0H
VSSWT    DS    1X             SET TO 01 IF BSAM USED TO PROC VS RCDVS0H
*                             SET TO 02 IF QSAM USED TO PROC VS RCDVS0H
NOLB     DS    1X             SET TO 01 IF NO USER LABEL ON SYSUT1 UL0H
*                             SET TO 02 IF NO USER LABEL ON SYSUT2 UL0H
LABSW    DS    1X             SET TO 01; SYSUT1 HEADER LABEL PROC  UL0H
*                             SET TO 02; SYSUT2 HEADER LABEL PROC  UL0H
*                             SET TO 04; SYSUT2 TRAILER LABEL PROC UL0H
*                             SET TO 08; SYSUT2 TRAILER LABEL PROC UL0H
PRINTOUT DS    0F             SYSPRINT DCB
*                                                                     *
***********************************************************************
QSAM     CSECT
* 570000                                                         A27718
*A657500-657600       CORRECTION BLANKS BYTE BOUNDARY
*2551428000                                                        SA0I
*                                                                     *
*        THIS MODULE IS TOTALLY NEW FOR RELEASE 17                    *
*                                                                     *
         EXTRN DDERROR1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         BALR  R9,R0          ESTABLISH REG9 AS BASE REG.
         USING *,R9
         L     R11,VSTARTP
         LR    R12,R11
         A     R12,K4         ADD DISPLACEMENT 4096
         MVI   VSSWT,X'02'    SET VSSWT TO 02 TO IND. VS RECORD PROC.
*                             BY QSAM MOVE MODE
         LA    R1,DS1         LOAD ADDR OF SYSUT1 DCB
         ST    R1,DSET1       STORE ADDR. OF SYSUT1 DCB AT COMM. REGION
         CLI   DDSUBNAM,X'00' IS THERE A DIFFERENT DDNAME FOR SYSUT1
         BE    OP1            NO, BRANCH
         MVC   DDNAM1(8),DDSUBNAM  YES, SAVE ACTUAL DDNAME FOR SYSUT1
OP1      OPEN  DS1            OPEN SYSUT1 DCB
         LA    R1,DS1
         TM    48(R1),X'10'   WAS OPEN ACCOMPLISHED
         BZ    NOTOPEN        NO, GO ABORT JOB
         MVI   EXLST1,X'01'   ACTIVATE USER INPUT LABEL EXITS
*
         LA    R1,DS2         LOAD ADDR. OF SYSUT2 DCB
         ST    R1,DSET2       STORE ADDR. OF SYSUT1 DCB AT COMM. REGION
         CLI   DDSUBNAM+8,X'00'    IS THERE A DIFFERENT DDNAME FOR
*                                  SYSUT2
         BE    OP2                 NO, BRANCH
         MVC   DDNAM2(8),DDSUBNAM+8  YES, SAVE USER DDNAME FOR SYSUT2
OP2      OPEN  DS2            OPEN SYSUT2 DCB
         LA    R1,DS2
         TM    48(R1),X'10'   WAS OPEN ACCOMPLISHED
         BZ    NOTOPEN        NO, GO ABORT JOB
         MVI   EXLST2,X'01'   ACTIVATE USER INPUT LABEL EXITS
*        GET BUFFER FOR SYSUT1 AND SYSUT2
         LH    R0,RECLTH      LOAD MAX. LRECL OF SYSUT1 INTO REG0
         ST    R0,LBUFF1      SAVE LENGTH OF GET STORAGE
         GETMAIN R,LV=(R0)
         ST    R1,BUFF1       STORE ADDR. OF BUFFER AT BUFF1
         LH    R0,RECLH2      LOAD MAX. LRECL OF SYSUT2 INTO REG0
         ST    R0,LBUFF2      SAVE LENGTH OF GET STORAGE
         GETMAIN R,LV=(R0)
         ST    R1,BUFF2       STORE ADDR. OF BUFFER AT BUFF2
*        PROCESSING DATA RECORDS
AGAIN    L     R7,BUFF1
         GET   DS1,(R7)
         TM    TERMSW,X'01'   DID USER INHDR/INTLR REQUEST TERMINATION
         BO    USTERM         YES, BRANCH
         L     R8,BUFF2
         GET   DS2,(R8)
         TM    TERMSW,X'01'   DID USER INHDR/INTLR REQUEST TERMINATION
         BO    USTERM         YES, BRANCH
         LH    R5,0(R7)       REG5 CONTAIN LITTLE LL OF RECORD1
         CH    R5,MAXLLL      IS LITTLE LL GREATER THAN 32756
         BH    PERR           BRANCH IF HIGH
         CH    R5,FOUR        IS LITTLE LL EQ OR LESS THAN 4
         BNH   PERR           BRANCH IF NOT HIGHER THAN 4
         STH   R5,RECLTH      SET RCD LENGTH OF SYSUT1 TO LITTLE LL
         LH    R3,0(R8)       REG3 CONTAIN LITTLE LL OF RECORD2
         CH    R3,MAXLLL      IS LITTLE LL GREATER THAN 32756
         BH    PERR           YES, BRANCH
         CH    R3,FOUR        IS LITTLE LL GREATER THAN 4
         BNH   PERR           NO, BRANCH
         STH   R3,RECLH2      SET RCD LENGTH OF SYSUT2 TO LITTLE LL
         SR    R4,R4          CLEAR REG4
         SR    R6,R6          CLEAR REG6
         L     R7,ADCOMP
         BALR  R3,R7          GO TO COMPARE RECORDS
         L     R2,LORECO
         LA    R2,1(R2)       BUMP TOTAL LOGICAL RECORD COUNT
         ST    R2,LORECO
         B     AGAIN          BRANCH
NOTOPEN  L     R15,ADMSG      LOAD ADDR. OF MESSAGE
         MVC   7(8,R15),40(R1) MOVE DDNAME INTO MESSAGE
         LA    R1,29          GO PRINT MESSAGE
         L     R15,PRINCONS
         BALR  R14,R15        GO TO PRINT RTN
         LA    R15,12         LOAD RETURN CODE OF 12 INTO REG15
         B     ABORT          BRANCH
USTERM   LA    R1,57          TERM AFTER USER VOL-SWITCH LAB PROC
         L     R15,PRINCONS
         BALR  R14,R15                  GO TO PRINT RTN
         LA    R15,16
         STC   R15,HICONCOD
         B     ABORT          BRANCH
PERR     LA    R1,17              PERMANENT INPUT ERROR            SA0I
         L     R15,PRINCONS
         BALR  R14,R15
         LA    R15,12         LOAD RET. CODE OF 12 INTO REG15
         STC   R15,HICONCOD
         B     ABORT          BRANCH
INHDEXT1 MVI   LABSW,X'01'
         L     R2,ADULAB
         BR    R2             GO TO IEBCULET
INTLEXT1 MVI   LABSW,X'02'
         L     R2,ADULAB
         BR    R2             GO TO IEBCULET
INHDEXT2 MVI   LABSW,X'01'
         L     R2,ADULAB
         BR    R2             GO TO IEBCULET
INTLEXT2 MVI   LABSW,X'02'
         L     R2,ADULAB
         BR    R2             GO TO IEBCULET
QINEOD1  MVI   EODSW1,X'01'   SET SW ON TO IND. SYSUT1 REACH EOD
         TM    EODSW2,X'01'   HAS SYSUT2 REACH EOD YET
         BO    END            YES, BRANCH
GET2     L     R8,BUFF2
         GET   DS2,(R8)
         TM    TERMSW,X'01'   DID USER INHDR/INTLR REQUEST TERMINATION
         BO    USTERM         YES, BRANCH
         CLI   HICONCOD+1,8   IS HIGHEST RET.CODE EQUAL 8
         BNH   PRTMSG         BRANCH IF NOT HIGHER
         MVI   HICONCOD+1,X'08'
PRTMSG   LA    R1,23                    EXTRA RECORD MESSAGE
         L     R15,PRINCONS
         BALR  R14,R15        GO TO PRINT RTN
         LA    R1,DDNAM2
         BAL   R3,PRTREC      GO PRINT EXTRA RECORD
         B     GET2           BRANCH
*        EOD ROUTINE FOR SYSUT2
QINEOD2  MVI   EODSW2,X'01'   SET SW ON TO IND. SYSUT2 REACH EOD
         TM    EODSW1,X'01'
         BO    END            YES,BRANCH
         L     R8,BUFF1
         B     PRTMSG1       PRINT EXTRA RCD ON SYSUT1 MSG
GET1     L     R8,BUFF1
         GET   DS1,(R8)
         TM    TERMSW,X'01'   DID USER INHDR/INTLR REQ TERM.
         BO    USTERM         YES, BRANCH
         CLI   HICONCOD+1,8   IS HIGHEST RET. CODE EQUAL 8
         BNH   PRTMSG1        BRANCH IF NOT HIGHER
         MVI   HICONCOD+1,X'08'
PRTMSG1  LA    R1,24               EXTRA RECORD MESSAGE
         L     R15,PRINCONS
         BALR  R14,R15        GO TO PRINT RTN
         LA    R1,DDNAM1
         BAL   R3,PRTREC                GO PRINT EXTRA RECORD
         B     GET1           BRANCH
PRTREC   ST    R3,SAVE3       SAVE RETURN ADDRESS
         MVC   MYPRTARE(120),BLANKS
         MVC   MYPRTARE+4(9),DDN        MOVE DDNAME=
         MVC   MYPRTARE+13(8),0(R1)     MOVE ACTUAL DDNAME
         LA    R0,119                   PRINT LINE
         LA    R1,MYPRTARE
         L     R15,MYPRTCON
         BALR  R14,R15        GO TO PRINT RTN
         ST    R8,INPUTLOC              SET ADDR. OF RECORD FOR BLPRT
*                                       SUBROUTINE
         LH    R7,0(R8)                 SET LENGTH OF RCD FOR BLPRT
         SR    R5,R5
         L     R15,ADBLPRT
         BALR  R4,R15         GO TO CONVERT AND PRINT RTN
         L     R3,SAVE3
         BR    R3             BRANCH
END      L     R15,ADBEND     BRANC BACK TO MAIN PROCESSING  FOR
         BR    R15            CLOSE DATA SET AND TERMINATION
ABORT    L     R3,ADNORD      BRANCH TO MAIN PROC. FOR CLOSING
          BR   R3             AND TERM.
INERR    SYNADAF    ACSMETH=QSAM   GET I/O ERROR STATUS          A27718
         MVC   MYPRTARE(120),BLANKS  CLEAR OUTPUT BUFFER         A27718
         MVC   MYPRTARE(18),IOERR  MOVE MSG. NO. INTO OUTPUT BUF A27718
         MVC   MYPRTARE+19(79),49(R1) MOVE I/O ERROR STATUS INTO A27718
*                                    OUTPUT BUFFER               A27718
         LA    R0,119                NO. OF BYTES TO BE PRINTED  A27718
         LA    R1,MYPRTARE           ADDRESS OF OUTPUT BUFFER    A27718
         L     R15,MYPRTCON          GET ADDRESS OF PRINT ROUTINEA27718
         BALR  R14,R15               GO TO PRINT MSG             A27718
         SYNADRLS                    RELEASE AREAS GOT BY SYNADAIA27718
         B     END                   BRANCH FOR ERROR TERMINATIONA27718
*
*****    SYSUT1 DCB
DS1      DCB   DDNAME=SYSUT1,                                          *
               DSORG=PS,                                               *
               MACRF=GM,                                               *
               BFALN=F,                                                *
               EODAD=QINEOD1,                                          *
               DEVD=DA,                                                *
               SYNAD=INERR,                                            *
               EXLST=EXLST1
*****    SYSUT2 DCB
DS2      DCB   DDNAME=SYSUT2,                                          *
               DSORG=PS,                                               *
               MACRF=GM,                                               *
               BFALN=F,                                                *
               EODAD=QINEOD2,                                          *
               DEVD=DA,                                                *
               SYNAD=INERR,                                            *
               EXLST=EXLST2
         DS    0F
EXLST1   DC    X'81'
         DC    AL3(INHDEXT1)
         DC    X'83'
         DC    AL3(INTLEXT2)
EXLST2   DC    X'81'
         DC    AL3(INHDEXT2)
         DC    X'83'
         DC    AL3(INTLEXT2)
********                     CONSTANT AREA                     ********
VSTARTP  DC    V(STARTP)      ADDR. OF
ADNORD   DC    V(NORMEND)
ADBLPRT  DC    V(BLPRT)       ADDR. OF CONVERT AND PRINT ROUTINE
MYPRTCON DC    V(ZZPR)
PRINCONS DC    V(JPRINTRT)
ADCOMP   DC    V(COMPAR)
ADBEND   DC    V(BEFNREND)
ADULAB   DC    V(USERLAB)
K4       DC    F'4096'
FOUR     DC    H'4'
MAXLLL   DC    X'7FF4'
EODSW1   DC    X'00'          SET TO 01 IF SYSUT1 REACH EOD
EODSW2   DC    X'00'          SET TO 01 IF SYSUT2 REACH EOD
DDN      DC    CL9'DDNAME = '
IOERR    DC    CL18'IEB267I I/O ERROR,'                          A27718
         CNOP 2,4             ALIGN BLANKS BYTE
         DC    X'40'          ON CORRECT BOUNDARY
BLANKS   DC    X'40'
MYPRTARE DS    30F
SAVE3    DS    1F
ADMSG    DC    A(DDERROR1)
DDNAM1   DC    CL8'SYSUT1  '
DDNAM2   DC    CL8'SYSUT2  '
         END
