***********************************************************************
*                                                                     *
* NAME             *****   *   *   *****   *       *   *              *
*                  *   *   **  *   *   *   *        * *               *
*                  *****   * * *   *****   *         *                *
*                  *   *   *  **   *   *   *         *                *
*                  *   *   *   *   *   *   *****     *                *
*                                                                     *
***********************************************************************
*                                                                     *
* FUNCTION/OPERATION                                                  *
*                                                                     *
*      THIS IS THE CONTROL CARD ANALYZER MODULE FOR THE DATA SET      *
*   UTILITY IEBCOMPR.  ITS MAIN FUNCTIONS ARE THE FOLLOWING:          *
*                                                                     *
*       1. PRINT THE CONTROL CARDS FOR IEBCOMPR                       *
*       2. PRINT DIAGNOSTIC MESSAGES ABOUT THE CONTROL CARDS, AFTER   *
*            THE CARDS HAVE BEEN THROUGHLY ANALYZED BY THE MODULE     *
*       3. SET THE ABORT SWITCH WHEN APPLICABLE                       *
*       4. SET UP THE EXITS PARAMETER LIST FOR THE MAIN PROCESSOR     *
*       5. PRINT THE DIAGNOTIC MESSAGE TO INDICATE THAT SYSIN COULD   *
*            NOT BE OPENED IF THE CARD SCANNER FINDS THIS TO BE THE   *
*            CASE                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
* ENTRY POINTS                                                        *
*                                                                     *
*   1. 'ANALY'      THIS IS THE ENTRY POINT TO THE FIRST OVERLAY      *
*                 SEGMENT. CONTROL IS PASSED TO THIS POINT FROM THE   *
*                 ROOT SEGMENT.                                       *
*                                                                     *
*   2. 'DDRTN'      THE CARD SCANNER ENTERS HERE IF IT FINDS THAT     *
*                 SYSIN CANNOT BE OPPENED.                            *
*                                                                     *
*   3. 'BLKRTN'     THE CARD SCANNER ENTERS HERE IF IT FINDS THAT* BS0H
*                 SYSIN HAS AN INVALID BLOCKSIZE.                * BS0H
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* INPUT                                                               *
*                                                                     *
*       INPUT TO THE ANALYZER COMES FROM THE GENERALIZED CARD SCANNER.*
*   PARAMETERS ARE PASSED ONE AT A TIME FROM THE SCANNER TO THE       *
*   ANALYZER, AT WHICH TIME THE PARAMETERS ARE INTERROGATED.          *
*   WHEN CONTROL IS RECEIVED FROM THE CCS02 SCAN ROUTINE,         *SA0I
*   REGISTER 0 CONTAINS THE LENGTH OF THE WORD BEING ANALYZED AND *SA0I
*   REGISTER 1 IS A POINTER TO THAT WORD.                         *SA0I
*                                                                     *
***********************************************************************
*                                                                     *
* OUTPUT                                                              *
*                                                                     *
*     THE OUTPUT OF THE ANALYZER IS SIMPLY THE SETTING OF SWITCHES,   *
*   THE BUILDING OF PARAMETER LISTS FOR THE MAIN PROCESSOR, OR THE    *
*   SETTING OF PARAMETERS FOR THE GENERALIZED MESSAGE WRITER.         *
*                                                                     *
***********************************************************************
*                                                                     *
* EXTERNAL ROUTINES                                                   *
*                                                                     *
*     THE CONTROL CARD SCANNER IS CALLED EACH TIME A NEW PARAMETER IS *
*   WANTED.  THE ONLY OTHER EXTERNAL ROUTINES ARE THE PRINT ROUTINES  *
*   WHICH ARE LOCATED IN THE ROOT SEGMENT.  THESE ROUTINES ARE 'ZZPR' *
*   AND 'JPRINTRT', AND ARE REACHED BY BRANCH AND LINK INSTRUCTIONS.  *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* EXITS                                                               *
*                                                                     *
*     THE ONLY EXIT FROM THE ANALYZER IS A BRANCH ON REGISTER 14,     *
*   TAKING ONE BACK TO THE ROOT SEGMENT FOR IEBCOMR.                  *
*                                                                     *
***********************************************************************
*                                                                     *
* TABLES /WORK AREAS                                                  *
*                                                                     *
*     THERE ARE NO TABLES AS SUCH, JUST PARAMETER LISTS AND SWITCHES  *
*   WHICH ARE CONSTRUCTED BY THE ANALYZER OR INTERROGATED BY IT.      *
*                                                                     *
***********************************************************************
*                                                                     *
* ATTRIBUTES                                                          *
*                                                                     *
*     NON-REENTRANT AND NON-REUSEABLE                                 *
*                                                                     *
***********************************************************************
         EJECT
COMMX    DSECT
         USING COMMX,10
SAVE1315 DS    3F                  SAVE AREA FOR REGS 13,14, AND 15
SAVEREGS DS    18F                 MY REGISTER SAVE AREA
PARALIST DS    8F            THE EXITS PARAMETER LIST-USER RTNS    SA0I
*                                    1. INHDR PARAMETER NAME
*                                    2. INTLR PARAMETER NAME
*                                    3. ERROR PARAMETER NAME
*                                    4. PRECOMP PARAMETER NAME
DDSYSIN  DS    2F                  SYSIN PARAMETER
DDSUBNAM DS    4F                  DDNAME PARAMETER LIST
*                                    1. SYSUT1 DDNAME
*                                    2. SYSUT2 DDNAME
HICONCOD DS    2X                  RETURN CODE STORAGE AREA
CCADSORG DS    1X                  SET TO '01' IF PARTITIONED DATA SET
ABORTSW  DS    1X                  ABORT SWITCH - SET TO '01' IF ON
LABEL    DS    1X             SET TO X'00' IF DATA=YES             UL0H
*                                    X'01' IF DATA=NO              UL0H
*                                    X'10' IF DATA=ONLY            UL0H
*                                    X'20' IF DATA=ALL             UL0H
***********************************************************************
ANALY    CSECT
* 044970-045600,045620                                           A27718
*8047046600                                                        BS0H
*2652018200,020980,023460-023600,024000,026200,026900-028400,      SA0I
* 029600-030600,030700,031000,031130,031200,031600-031900,         SA0I
* 032600,032700,033400,034000,034200,035000,035800,035960-         SA0I
* 036600,036800,037200,037400,038000,038200,038800,040400,041600,  SA0I
* 042800,044000,044032,044056,044088,044096,044186-045200,045800,  SA0I
* 045900,050800-051400,053400                                      SA0I
* 030160,030800,030852-030856                                     M2002
* REGISTER ASSIGNMENTS                                             BS0H
PARMREG  EQU   1                                                   BS0H
R15      EQU   15                                                  BS0H
R14      EQU   14
R0       EQU   0                  CONTAINS LENGTH OF WORD SCANNED  SA0I
R1       EQU   1                                                   SA0I
R2       EQU   2                  CONTAINS MESSAGE CODE            SA0I
R3       EQU   3                                                   SA0I
R4       EQU   4                                                   SA0I
         ENTRY DDRTN
         ENTRY BLKRTN                                              BS0H
         SAVE  (14,12)
         BALR  11,0
         USING *,11
         STM   13,14,SAV13A14
         LA    13,SAVANALR
         XC    CTLAREA(144),CTLAREA  FILL WORK AREA WITH ZEROS
         MVI   PARALIST,X'40'      FILL PARAMETER LIST
         MVC   PARALIST+1(31),PARALIST  WITH BLANKS
         MVC   DDJOSIN(8),DDSYSIN
CALLSCAN CALL  ACTCCS,(CTLAREA,DDJOSIN),ID=1
         TM    CTLAREA,X'20'       END OF FILE TEST
         BO    ENDOFANA              BRANCH BACK TO ROOT SEG (IS EOF)
         CLI   CTLAREA+144,X'01'  IS NEW CARD SW ON                SA0I
         BH    ERRFRSCA              INPUT ERROR - ABORT JOB
         BL    ERRTST             NO-BRANCH TO ERROR TESTS         SA0I
***********************************************************************
***      GO PRINT CONTROL CARD AND CONTINUE PROCESSING
***********************************************************************
         STM   0,1,SAVEREG0
         LA    0,79
         LA    1,CTLAREA+1
         L     15,MYPRTCON
         BALR  14,15
         LM    0,1,SAVEREG0
***********************************************************************
*******************************************************************SA0I
*                TEST FOR ERRORS FLAGGED BY CCS02 SCAN ROUTINE    *SA0I
*******************************************************************SA0I
ERRTST   TM    CTLAREA,X'80'      IS ERROR BIT ON                  SA0I
         BZ    BPASTST            NO-TEST BYPASS BIT               SA0I
         TM    CTLAREA,X'40'      YES-IS BYPASS BIT ON             SA0I
         BO    COMTST             YES THEN DETERMINE TYPE OF ERROR SA0I
         LA    R1,26              NO-WARNING INVALID NAME          SA0I
         L     R15,PRINCONS       LOAD PRINT ROUTINE ADDRESS       SA0I
         BALR  R14,R15            BRANCH TO PRINT ERROR MESSAGE    SA0I
         B     CALLSCAN           RETURN TO SCAN ROUTINE           SA0I
COMTST   TM    CTLAREA,X'08'      IS COMMAND BIT ON                SA0I
         BZ    KEYTST             NO-THEN TEST KEYWORD BIT         SA0I
         OI    FIRSTCOC,X'01'     YES-SET SWITCH IF NOT ON         SA0I
         TM    CTLAREA,X'06'      IS KEYWORD OR PARAMETER BIT ON   SA0I
         BZ    WRNGCOM            NO-BRANCH FOR INVALID COMMAND    SA0I
         LA    R2,60              YES MISSING COMMAND              SA0I
         B     COLCALC            CALCULATE ERROR COLUMN NUMBER    SA0I
WRNGCOM  LA    R2,65              INVALID COMMAND                  SA0I
         B     COLCALC            CALCULATE ERROR COLUMN NUMBER    SA0I
KEYTST   TM    CTLAREA,X'04'      IS KEYWORD BIT ON                SA0I
         BZ    PARATST            NO-THEN TEST PARAMETER BIT       SA0I
WRNGKEY  NI    PARMSW,X'00'       YES-TURN OFF PARMSW              SA0I
         LA    R2,59              INVALID KEYWORD                  SA0I
         B     COLCALC            CALCULATE ERROR COLUMN NUMBER    SA0I
PARATST  TM    CTLAREA,X'02'      IS PARAMETER BIT ON              SA0I
         BZ    KEYREQ             NO-TEST KEYWORD REQUIRED BIT     SA0I
         LA    R2,61              YES-INVALID PARAMETER            SA0I
         B     COLCALC            CALCULATE ERROR COLUMN NUMBER    SA0I
KEYREQ   TM    CTLAREA+81,X'02'   IS KEYWORD REQUIRED BIT ON       SA0I
         BZ    PARAREQ            NO-TEST PARAMETER REQUIRED BIT   SA0I
         LA    R2,62              MISSING KEYWORD                  SA0I
         B     COLCALC            CALCULATE ERROR COLUMN NUMBER    SA0I
PARAREQ  TM    CTLAREA+81,X'01'   IS PARAMETER REQUIRED BIT ON     SA0I
         BZ    WRNGCOL            NO-THEN CONTINUATION IN ERROR    SA0I
         LA    R2,63              MISSING PARAMETER                SA0I
         B     COLCALC            CALCULATE ERROR COLUMN NUMBER    SA0I
WRNGCOL  LA    R1,66              CONTINUATION CARD IN WRONG COL   SA0I
         L     R15,PRINCONS       LOAD PRINT ROUTINE ADDRESS       SA0I
         BALR  R14,R15            BRANCH TO PRINT ERROR MESSAGE    SA0I
         OI    HICONCOD+1,X'0C'   RETURN CODE                      SA0I
         OI    ABORTSW,X'01'      TURN ON ABORT SWITCH             SA0I
         B     CALLSCAN           BRANCH TO SCAN ROUTINE           SA0I
BPASTST  TM    CTLAREA,X'40'      IS BYPASS BIT ON                 SA0I
         BZ    COMPCD             NO-THEN NO SYNTAX ERRORS FOUND   SA0I*
                                  BY CCS02, CONTINUE ANALYSIS      SA0I
         TM    CTLAREA,X'01'      IS END OF PARAMETER BIT ON       SA0I
         BZ    CALLSCAN           NO-THEN RETURN TO SCAN,          SA0I*
                                  YES-THEN CONTINUE ANALYSIS       SA0I
*******************************************************************SA0I
*        TEST FOR COMPARE COMMAND ON FIRST CONTROL CARD           *SA0I
*******************************************************************SA0I
COMPCD   TM    FIRSTCOC,1         HAS COMPARE CARD BEEN READ       SA0I
         BO    TESFORER              YES-BRANCH AROUND
         MVI   FIRSTCOC,1          SET SWITCH IND. COMPARE CD. READ
         CLC   COMWORD1(7),0(1)    DOES COMMAND WORD = COMPARE
         BE    LENG7              YES-THEN TEST WORD LENGTH        SA0I
         LA    R1,64           NO-FIRST CONTROL CARD NOT 'COMPARE' SA0I
         L     R15,PRINCONS       LOAD PRINT ROUTINE ADDRESS       SA0I
         BALR  R14,R15            BRANCH TO PRINT ERROR MESSAGE    SA0I
         B     BYPASCD            SET BY PASS SW ON                SA0I
LENG7    LA    R3,7               IS WORD LENGTH=7                 SA0I
         CLR   R0,R3                                               SA0I
         BE    COMPST             YES THEN COMMAND IS VALID        SA0I
         LA    R2,65              INVALID COMMAND                  SA0I
         B     COLCALC            CALCULATE ERROR COLUMN NUMBER    SA0I
COMPST   OI    CDSW,X'01'         SET SW TO COMPARE STMNT ANALYSIS SA0I
         B     CALLSCAN           BRANCH TO SCAN ROUTINE           SA0I
TESFORER TM    CTLAREA,X'02'      PARAMETER TEST                   SA0I
         BO    PARSET                POSITIVE RESPONSE
         TM    CTLAREA,X'04'       KEYWORD TEST
         BO    KEYWORDT              POSITIVE RESPONSE
         TM    CTLAREA,X'08'       COMMAND WORD TEST
         BO    COMWORDT              POSITIVE RESPONSE
         LA    R1,1               INVALID CONTROL STATEMENT        SA0I
         L     R15,PRINCONS       LOAD PRINT ROUTINE ADDRESS       SA0I
         BALR  R14,R15            BRANCH TO PRINT ERROR MESSAGE    SA0I
         B     BYPASCD            GO TO SET BYPASS SW              SA0I
*******************************************************************SA0I
*                  TEST FOR VALID COMMAND WORD                    *SA0I
*******************************************************************SA0I
COMWORDT LA    R3,5               IS COMMAND LENGTH=5              SA0I
         CLR   R0,R3                                               SA0I
         BNE   COMWDT2            CHECK OTHER COMMANDS            M2002
         CLC   COMWORD2(5),0(R1)  DOES COMMAND WORD=EXITS          SA0I
         BNE   ERMSG                   NOT EXITS                   SA0I
         MVI   CDSW,X'02'     YES-SET SW IND. IN EXITS STMNT ANAL. SA0I
         OI    CTLAREA+81,X'02'   TURN ON KEYWORD REQUIRED BIT     SA0I
         B     CALLSCAN            BRANCH                          UL0H
COMWDT2  CLC   COMWORD3(6),0(R1)  DOES COMMAND WORD = LABELS      M2002
         BNE   ERMSG          NO                                   UL0H
         LA    R3,6               CONSTANT SIX IN REG             M2002
         CLR   R0,R3              IS LENGTH = SIX                 M2002
         BNE   ERMSG              NO, WRITE ERR MSG               M2002
         TM    CCADSORG,X'01'      ARE DATA SETS PARTITIONED       UL0H
         BO    ULABPO              YES, BRANCH                     UL0H
         MVI   CDSW,X'04'      NO-SET SW IND IN LABEL STMNT ANAL.  SA0I
         OI    CTLAREA+81,X'02'   TURN ON KEYWORD REQUIRED BIT     SA0I
         B     CALLSCAN      BRANCH                                UL0H
ERMSG    CLC   COMWORD1(5),0(R1)  IS COMMAND=COMPARE               SA0I
         BE    COMPRD             YES-CHECK FOR DUPLICATE          SA0I
         LA    R2,65              NO-INVALID COMMAND               SA0I
         B     COLCALC            CALCULATE ERROR COLUMN NUMBER    SA0I
COMPRD   TM    CDSW,X'01'         HAS COMPARE CARD BEEN READ       SA0I
         BZ    ILLSEQ             NO-THEN INVALID SEQUENCE         SA0I
         LA    R1,13              YES-REPETITIOUS CARD INVALID     SA0I
         B     WRTMSG             PRINT ERROR MESSAGE              SA0I
ILLSEQ   LA    R1,16              ILLEGAL CONTROL CARD SEQUENCE    SA0I
WRTMSG   L     R15,PRINCONS       LOAD PRINT ROUTINE ADDRESS       SA0I
         BALR  14,15               BRANCH TO PRINT ERROR MESSAGE
         B     BYPASCD            SET BYPASS SW ON                 SA0I
*******************************************************************SA0I
*                    TEST FOR A VALID KEYWORD                     *SA0I
*******************************************************************SA0I
KEYWORDT NI    PARMSW,X'00'       TURN OFF PARMSW                  SA0I
         TM    CDSW,X'01'         IS COMPARE STMNT ANALYSIS        SA0I
         BZ    KCOMP2         NO                                   UL0H
         LA    R3,6               YES IS KEYWORD LENGTH=6          SA0I
         CLR   R0,R3                                               SA0I
         BNE   BADKEYW            NO-INVALID KEYWORD               SA0I
         CLC   KEYWORD1(6),0(R1) DOES KEYWORD=TYPORG               UL0H
         BNE   BADKEYW        NO,ILLEGAL KEY WORD                  UL0H
         MVI   KWORDSW,X'01'         YES, SET KWORDSW = 1
TURNOF   NI    CTLAREA+81,X'FD'   TURN OFF KEYWORD REQUIRED BIT    SA0I
         OI    CTLAREA+81,X'01'   TURN ON PARAMETER REQUIRED BIT   SA0I
         B     CALLSCAN           BRANCH TO SCAN ROUTINE           SA0I
KCOMP2   TM    CDSW,X'02'         IS EXITS STMNT ANALYSIS          SA0I
         BZ    KCOMP6         NO                                   UL0H
         LA    R3,5               YES-IS LENGTH=5                  SA0I
         CLR   R0,R3                                               SA0I
         BNE   KCOMP5             NO-THEN TEST FOR LENGTH=7        SA0I
         CLC   KEYWORD2(5),0(R1) DOES KEYWORD=INHDR                UL0H
         BNE   KCOMP3                NO
         TM    CCADSORG,X'01'      ARE DATA SETS PARTITIONED       UL0H
         BO    ULABPO              YES, BRANCH                     UL0H
         MVI   KWORDSW,X'02'      NO, SET KWORDSW=2                SA0I
         B     TURNOF             PARAMETER IS REQUIRED            SA0I
KCOMP3   CLC   KEYWORD3(5),0(1)    DOES KEYWORD = INTLR
         BNE   KCOMP4                NO
         TM    CCADSORG,X'01'      ARE DATA SETS PARTITIONED       UL0H
         BO    ULABPO              YES, BRANCH                     UL0H
         MVI   KWORDSW,X'04'      NO,SET KWORDSW=4                 SA0I
         B     TURNOF             PARAMETER IS REQUIRED            SA0I
KCOMP4   CLC   KEYWORD4(5),0(1)    DOES KEYWORD = ERROR
         BNE   KCOMP5                NO
         MVI   KWORDSW,X'08'         YES, SET KWORDSW = 8
         B     TURNOF             PARAMETER IS REQUIRED            SA0I
KCOMP5   CLC   KEYWORD5(7),0(1)    DOES KEYWORD = PRECOMP
         BNE   BADKEYW               NO
         LA    R3,7               IS LENGTH=7                      SA0I
         CLR   R0,R3                                               SA0I
         BNE   BADKEYW            NO-INVALID KEYWORD               SA0I
         MVI   KWORDSW,X'10'         YES, SET KWORDSW = 10
         B     TURNOF             PARAMETER IS REQUIRED            SA0I
KCOMP6   CLC   KEYWORD6(4),0(R1) DOES KEYWORD=DATA                 UL0H
         BNE   BADKEYW            NO-INVALID KEYWORD               SA0I
         LA    R3,4               IS WORD LENGTH=4                 SA0I
         CLR   R0,R3                                               SA0I
         BNE   BADKEYW        NO,ILLEGAL KEY WORD                  UL0H
         MVI   KWORDSW,X'20'  YES SET KWORDSW=X'20'                UL0H
         B     TURNOF             PARAMETER IS REQUIRED            SA0I
BADKEYW  LA    R2,59              INVALID KEYWORD                  SA0I
         B     COLCALC            CALCULATE ERROR COLUMN NUMBER    SA0I
*******************************************************************SA0I
*                    TEST FOR A VALID PARAMETER                   *SA0I
*******************************************************************SA0I
PARSET   LTR   0,0                 DOES PARAMETER LENGTH EQUAL ZERO
         BNZ   KEYTYP             NO-TEST FOR VALID PARAMETER      SA0I
         LA    R2,63              MISSING PARAMETER                SA0I
         B     COLCALC            CALCULATE ERROR COLUMN NUMBER    SA0I
KEYTYP   TM    PARMSW,X'01'       HAS A PARAMETER BEEN READ        SA0I
         BO    PARERR             YES, INVALID PARAMETER           SA0I
         OI    PARMSW,X'01'       NO, TURN ON PARMSW               SA0I
         TM    KWORDSW,X'01'      TEST FOR TYPORG KEYWORD          SA0I
         BZ    PTEST2                BRANCH - NOT TYPORG
         LA    R3,2               IS PARAMETER LENGTH=2            SA0I
         CLR   R0,R3                                               SA0I
         BE    TYPRD              YES-CHECK FOR DUPLICATE          SA0I
PARERR   LA    R2,61              INVALID PARAMETER                SA0I
         B     COLCALC            CALCULATE ERROR COLUMN NUMBER    SA0I
TYPRD    TM    TYPARA,X'01'       HAS TYPORG BEEN READ             SA0I
         BO    REPRTN             YES-INVALID REPETITIOUS CARD     SA0I
         OI    TYPARA,X'01'       NO-SET TYPORG SW ON              SA0I
         CLC   PARWORD2(2),0(1)       IS TYPORG PS
         BNE   TYPPO              NO-THEN IS PO                    SA0I
         NI    CTLAREA+81,X'FC'   TURN OFF PARAMETER REQUIRED BIT  SA0I
         B     CALLSCAN           RETURN TO SCAN ROUTINE           SA0I
TYPPO    CLC   PARWORD1(2),0(R1)  IS TYPORG=PO                     SA0I
         BNE   PARERR                NO-BRANCH-PARAMETER ERROR
         MVI   CCADSORG,X'01'        YES-SET SW TO PO PARAMETER
PARMOFF  NI    CTLAREA+81,X'FC'   TURN OFF PARAMETER REQ BIT       SA0I
         B     CALLSCAN           RETURN TO SCAN ROUTINE           SA0I
PTEST2   CH    0,EIGHT             IS LENGTH GREATER THAN EIGHT
         BH    PARERR                YES-PARAMETER ERROR
         TM    KWORDSW,X'02'       IS KEYWORD=INHDR
         BZ    PTEST3                NO
         SH    0,ONE
         STC   0,MOVE1+1           MOVE INHDR ROUTINE NAME
MOVE1    MVC   PARALIST(1),0(1)      INTO PARAMETER LIST
         TM    CTLAREA,X'01'      IS END OF PARAMETER BIT ON       SA0I
         BC    1,PARMOFF          YES, TURN OFF PARAMETER REQ BIT  SA0I
         XI    CTLAREA+81,X'03'   NO, TURN ON KEYWORD REQ BIT      SA0I
         B     CALLSCAN           BRANCH TO SCAN ROUTINE           SA0I
PTEST3   TM    KWORDSW,X'04'       IS KEYWORD=INTLR
         BZ    PTEST4                NO
         SH    0,ONE
         STC   0,MOVE2+1           MOVE INTLR ROUTINE NAME
MOVE2    MVC   PARALIST+8(1),0(1)    INTO PARAMETER LIST
         TM    CTLAREA,X'01'      IS END OF PARAMETER BIT ON       SA0I
         BC    1,PARMOFF          YES, TURN OFF PARAMETER REQ BIT  SA0I
         XI    CTLAREA+81,X'03'   NO, TURN ON KEYWORD REQ BIT      SA0I
         B     CALLSCAN           BRANCH TO SCAN ROUTINE           SA0I
PTEST4   TM    KWORDSW,X'08'       IS KEYWORD=ERROR
         BZ    PTEST5                NO
         SH    0,ONE
         STC   0,MOVE3+1           MOVE ERROR ROUTINE NAME
MOVE3    MVC   PARALIST+16(1),0(1)   INTO PARAMETER LIST
         TM    CTLAREA,X'01'      IS END OF PARAMETER BIT ON       SA0I
         BC    1,PARMOFF          YES, TURN OFF PARAMETER REQ BIT  SA0I
         XI    CTLAREA+81,X'03'   NO, TURN ON KEYWORD REQ BIT      SA0I
         B     CALLSCAN           BRANCH TO SCAN ROUTINE           SA0I
PTEST5   TM    KWORDSW,X'10'       IS KEYWORD=PRECOMP
         BZ    PTEST6         NO                                   UL0H
         SH    0,ONE
         STC   0,MOVE4+1           MOVE PRECOMP ROUTINE NAME
MOVE4    MVC   PARALIST+24(1),0(1)   INTO PARAMETER LIST
         TM    CTLAREA,X'01'      IS END OF PARAMETER BIT ON       SA0I
         BC    1,PARMOFF          YES, TURN OFF PARAMETER REQ BIT  SA0I
         XI    CTLAREA+81,X'03'   NO, TURN ON KEYWORD REQ BIT      SA0I
         B     CALLSCAN           BRANCH TO SCAN ROUTINE           SA0I
PTEST6   TM    KWORDSW,X'20'       DOES KEYWORD=DATA               UL0H
         BZ    PARERR              NO,INVALID PARAMETER            UL0H
         TM    CTLAREA+81,X'01'   HAS PARAMETER BEEN READ          SA0I
         BZ    PARERR             YES-INVALID PARAMETER            SA0I
         LA    R3,2               NO-IS PARAMETER LENGTH=2         SA0I
         CLR   R0,R3                                               SA0I
         BNE   PTEST7             NO-THEN IS LENGTH=3              SA0I
         CLC   DATA1(2),0(R1)      YES,DATA=NO                     UL0H
         BNE   PARERR             NO-INVALID PARAMETER             SA0I
         MVI   LABEL,X'01' X       YES,SET SW IND. DATA=NO         UL0H
         NI    CTLAREA+81,X'FC'   TURN OFF PARAMETER REQUIRED BIT  SA0I
         B     CALLSCAN       BRANCH                               UL0H
PTEST7   LA    R3,3               IS PARAMETER LENGTH=3            SA0I
         CLR   R0,R3                                               SA0I
         BNE   PTEST8              NO                              UL0H
         CLC   DATA2(3),0(R1)     IS DATA=YES                      SA0I
         BNE   PTEST9             NO-TEST FOR DATA=ALL             SA0I
         MVI   LABEL,X'00'         YES,SET SW IND. DATA=YES        UL0H
         NI    CTLAREA+81,X'FC'   TURN OFF PARAMETER REQUIRED BIT  SA0I
         B     CALLSCAN       BRANCH                               UL0H
PTEST8   LA    R3,4               IS PARAMETER LENGTH=4            SA0I
         CLR   R0,R3                                               SA0I
         BNE   PARERR             NO-INVALID PARAMETER             SA0I
         CLC   DATA3(4),0(R1)     IS DATA=ONLY                     SA0I
         BNE   PARERR             NO-INVALID PARAMETER             SA0I
         MVI   LABEL,X'10'         YES,SET SW IND. DATA=ONLY       UL0H
         NI    CTLAREA+81,X'FC'   TURN OFF PARAMETER REQUIRED BIT  SA0I
         B     CALLSCAN       BRANCH                               UL0H
PTEST9   CLC   DATA4(3),0(R1)      DATA=ALL                        UL0H
         BNE   PARERR              NO, INVALID PARAM               UL0H
         MVI   LABEL,X'20'         YES, SET SW IND. DATA=ALL       UL0H
         NI    CTLAREA+81,X'FC'   TURN OFF PARAMETER REQUIRED BIT  SA0I
         B     CALLSCAN       BRANCH                               UL0H
ULABPO   LA    R1,50          USER LABEL NOT SUPPORT BY PO DATA SETUL0H
         L     R15,PRINCONS                                        UL0H
         BALR  R14,R15             PRINT                           UL0H
         MVI   HICONCOD+1,X'0C'                                    UL0H
         NI    CTLAREA+81,X'FC'   TURN OFFKEYWORD REQUIRED BIT     SA0I
         B     BYPASCD            SET BYPASS SW ON                 SA0I
*******************************************************************SA0I
*                  CALCULATE CONTROL CARD COLUMN WHERE            *SA0I
*                       ERROR IS FOUND                            *SA0I
*******************************************************************SA0I
COLCALC  LR    R4,R1              ADDRESS OF ERROR IN CTLAREA      SA0I
         LA    R3,CTLAREA                                          SA0I
         SR    R4,R3              R4 CONTAINS CARD COL NO WHERE    SA0I*
                                  ERROR IS FOUND                   SA0I
         CVD   R4,DECAREA                                          SA0I
         OI    DECAREA+7,X'0F'                                     SA0I
         UNPK  PKARA(2),DECAREA(8)                                 SA0I
*******************************************************************SA0I
*                   MOVE COLUMN NUMBER INTO MESSAGE               *SA0I
*                  AND BRANCH TO PRINT ROUTINE                    *SA0I
*******************************************************************SA0I
         LR    R1,R2              MESSAGE NUMBER IN R1             SA0I
         LR    R3,R2              MESSAGE NUMBER IN R3             SA0I
         S     R3,THRE1   GET RELATIVE DISPLACEMENT FROM MSG31     SA0I
         M     R2,SEV2            MULTIPLY BY MESSAGE LENGTH       SA0I
         A     R3,MSGMOD          ADD LOAD ADDRESS OF MSG31        SA0I
         MVC   47(2,R3),PKARA     INSERT COLUMN NUMBER             SA0I
         L     R15,PRINCONS       LOAD PRINT ROUTINE ADDRESS       SA0I
         BALR  R14,R15            PRINT ERROR MESSAGE              SA0I
         B     BYPASCD            BRANCH TO SET ERROR SWITCHES     SA0I
ERRFRSCA MVC   MYPRTARE(120),BLANKS  CLEAR OUTPUT BUFFER         A27718
         MVC   MYPRTARE(18),IOERR  MOVE MSG. NO. INTO OUTPUT BUF A27718
         MVC   MYPRTARE+19(79),49(R1) MOVE I/O ERROR STATUS INTO A27718
*                                BY IEBCCS02 INTO OUTPUT BUFFER  A27718
         LA    R0,79               FREE 79 BYTES GOT BY          A27718
         FREEMAIN  R,LV=(0),A=(1)  SYSIN'S SYNADAF               A27718
         LA    R0,119                NO. OF BYTES TO BE PRINTED  A27718
         LA    R1,MYPRTARE           ADDRESS OF OUTPUT BUFFER    A27718
         L     R15,MYPRTCON          GET ADDR. OF PRINT ROUTINE  A27718
         BALR  R14,R15               GO TO PRINT MSG             A27718
         MVI   HICONCOD+1,X'0C'   RETURN CODE                      SA0I
         OI    ABORTSW,X'01'       TURN ON ABORT SW              A27718
         B     ENDOFANA            BRANCH TO WRAP UP ROUTINE     A27718
REPRTN   LA    R1,13              REPETITIOUS CARD INVALID         SA0I
         L     R15,PRINCONS       LOAD PRINT ROUTINE ADDRESS       SA0I
         BALR  R14,R15            BRANCH TO PRINT ERROR MESSAGE    SA0I
*******************************************************************SA0I
*        AN ERROR HAS BEEN FOUND-SET BYPASS SWITCH TO INFORM      *SA0I
*        CCS02 THAT THE REST OF THE CONTROL CARD NOW BEING        *SA0I
*        ANALYZED SHOULD BE IGNORED.                              *SA0I
*******************************************************************SA0I
BYPASCD  NI    CTLAREA+81,X'FC'   TURN OFF KEYWORD AND PARAMETER   SA0I*
                                  REQUIRED BITS                    SA0I
         MVI   HICONCOD+1,X'0C'   SET RETURN CODE                  SA0I
         OI    CTLAREA,X'40'      TURN ON BYPASS CARD BIT          SA0I
         OI    ABORTSW,X'01'      TURN ON ABORT SWITCH             SA0I
         B     CALLSCAN           RETURN TO SCAN ROUTINE           SA0I
ENDOFANA LM    13,14,SAV13A14
         RETURN (2,12)
BLKRTN   LA    PARMREG,30     INVALID BLKSIZE PARAMETER -PRINT MSG BS0H
         B     RTNOUT                   PRINT MESSAGE              BS0H
DDRTN    LA    1,29               COULD NOT OPEN SYSIN-PRINT MESSAGE
RTNOUT   L     R15,PRINCONS                                        BS0H
         BALR  14,15
         LA    15,12              LOAD RETURN CODE OF '0C' IN REG15
         LM    13,14,SAVE1315
         RETURN (2,12)            COULD NOT OPEN SYSPRINT
***********************************************************************
*                   CONSTANT AND WORK AREA
PRINCONS DC    V(JPRINTRT)         ADDR OF ROUTINE TO SET UP PARAMETERS
*                                    FOR PRINT ROUTINE
MYPRTCON DC    V(ZZPR)             ADDR OF PRINT ROUTINE
SAVANALR DS    18F                 REGISTER SAVE AREA
SAV13A14 DS    2F                  SAVE AREA FOR REGS 13-14
DDJOSIN  DC    D'0'                SYSIN DDNAME FIELD FOR CD SCAN
SAVEREG0 DS    2F                  SAVE AREA FOR REGS O-1
EIGHT    DC    H'8'
ONE      DC    H'1'
CTLAREA  DS    145X                CARD IMAGE AREA-WORK AREA-LAST BYTE
*                                    IS SWITCH WHICH WHEN 01 INDICATES
*                                    INPUT ERROR FROM CD SCAN
FIRSTCOC DC    X'00'               SWITCH  00=COMPARE CTL CARD NOT READ
*                                          01=COMPARE CD READ
CDSW     DC    X'00'          SET TO X'01' IN COMPARE STMT ANALY   UL0H
*                                    X'02' IN EXITS STMT ANALY     UL0H
*                                    X'04' IN LABEL STMT ANALY     UL0H
KWORDSW  DC    X'00'              SWITCH  2 BIT KEYWORD=DATA       SA0I
*                                         3 BIT KEYWORD=PRECOMP    SA0I
*                                         4 BIT KEYWORD=ERROR      SA0I
*                                         5 BIT KEYWORD=INTLR      SA0I
*                                         6 BIT KEYWORD=INHDR      SA0I
*                                         7 BIT KEYWORD=TYPORG     SA0I
COMWORD1 DC    CL7'COMPARE'
COMWORD2 DC    CL5'EXITS'
COMWORD3 DC    CL6'LABELS'                                         SA0I
KEYWORD1 DC    CL6'TYPORG'
KEYWORD2 DC    CL5'INHDR'
KEYWORD3 DC    CL5'INTLR'
KEYWORD4 DC    CL5'ERROR'
KEYWORD5 DC    CL7'PRECOMP'
KEYWORD6 DC    CL4'DATA'                                           UL0H
PARWORD1 DC    CL2'PO'
PARWORD2 DC    CL2'PS'
DATA1    DC    CL2'NO'                                             UL0H
DATA2    DC    CL3'YES'                                            UL0H
DATA3    DC    CL4'ONLY'                                           UL0H
DATA4    DC    CL3'ALL'                                            UL0H
IOERR    DC    CL18'IEB267I I/O ERROR,'                          A27718
BLANKS   DC    X'40'                                             A27718
MYPRTARE DS    30F                                               A27718
DECAREA  DC    D'0'                                                SA0I
PKARA    DC    C'00'                                               SA0I
LABCD    DC    X'00'              LABEL CARD SWITCH                SA0I
TYPARA   DC    X'00'              TYPORG CARD SWITCH               SA0I
MSGMOD   DC    V(MSG31)           ADDR OF MESSAGE MODULE           SA0I
THRE1    DC    F'31'              CONSTANT 31                      SA0I
SEV2     DC    F'72'              CONSTANT 72                      SA0I
PARMSW   DC    X'00'              PARAMETER SW- SET TO '01' WHEN   SA0I
*                                 A PARAMETER HAS BEEN READ        SA0I
          END
