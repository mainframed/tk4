 SPACE 2
***********************************************************************
*                                                                     *
* NAME   IEBCULET                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
* FUNCTION/OPERATION                                                  *
*                                                                     *
*     THIS ROUNTINE WILL ALWAYS BE ENTERRED VIA DCBEXLST ENTRY FOR    *
*  USER INPUT HEADER/TRAILER LABEL EXITS. ITS MAIN FUNCTIONS ARE AS   *
*  FOLLOWS                                                            *
*                                                                     *
*   1. TO SET ITS OWN ADDRESSABILITY                                  *
*   2. TO SET COMMUNICATION REGION'S ADDRESSABILITY                   *
*   3. THEN BRANCH TO PROPER ADDRESS FOR LABEL SAVING OR EXIT TO USER *
*      INHDR/INTLR                                                    *
*        'HDRLBEX1' ENTRY FOR SYSUT1 USER INPUT HEADER                *
*        'HDRLBEX2' ENTRY FOR SYSUT2 USER INPUT HEADER                *
*        'TLRLBEX1' ENTRY FOR SYSUT1 USER INPUT TRAILER               *
*        'TLRLBEX2' ENTRY FOR SYSUT2 USER INPUT TRAILER               *
*   4. TO SAVE THE LABELS FOR LATER COMPARISON IF REQUESTED BY USER   *
*   5. GIVE CONTROL TO USER INHDR/INTLR IF THERE IS ONE               *
*   6. INTERPRET USER RETURN CODE                                     *
*   7. PRINT USER WISH TO STOP RECEIVING ANY MORE LABEL MESSAGE       *
*   8. EXIT TO OPEN/CLOSE/EOV AFTER EACH LABEL BEING HANDLED PROPERLY *
*                                                                     *
***********************************************************************
*                                                                     *
* ENTRY POINTS                                                        *
*                                                                     *
*   'USERLAB' IS THE ONLY ENTRY POINT TO THIS MODULE VIA USER LABEL   *
*      ENTRY IN DCBEXLST                                              *
*                                                                     *
***********************************************************************
*                                                                     *
* INPUT                                                               *
*                                                                     *
*   INPUT TO THIS MODULE ARE AS FOLLOWS                               *
*                                                                     *
*   1. SWITCHES AND PARAMETERS WHICH ARE SET UP BY IEBCANAL AND       *
*      IEBCMAIN IN THE COMMUNICATION REGION OF ROOT SEGMENT           *
*   2. 16 BYTES PARAMETER LIST WHICH IS SET UP BY OPEN/CLOSE/EOV FOR  *
*      EACH LABEL TO BE VERIFIED                                      *
*                                                                     *
***********************************************************************
*                                                                     *
* OUTPUT                                                              *
*                                                                     *
*     THE OUTPUT FROM THIS MODULE IS DIAGNOSTIC AND INFORMATIONAL     *
*   MESSAGES.                                                         *
*                                                                     *
***********************************************************************
*                                                                     *
* EXTERNAL ROUTINES                                                   *
*                                                                     *
*     THIS MODULE USES ROOT SEGMENT'S PRINT ROUTINE, ZZPR AND JPRINTRT*
*   SUBROUTINE TO PRINT MESSAGES. OTHER POSSIBLE EXTERNAL ROUTINES ARE*
*   THE USER INHDR/INTLR ROUTINES. LINKAGE TO THESE ROUTINES IS MADE  *
*   BY BRANCH AND LINK INSTRUCTION.                                   *
*                                                                     *
***********************************************************************
*                                                                     *
* EXITS                                                               *
*                                                                     *
*     THE ONLY EXIT MADE FROM THIS MODULE IS A RETURN BACK TO         *
*   OPEN/CLOSE/EOV AFTER EACH LABEL BEING HANDLED ACCORDINGLY         *
*                                                                     *
***********************************************************************
*                                                                     *
* TABLES/WORK AREA                                                    *
*     THERE IS NO TABLES/WORK AREA IN THIS MODULE                     *
*                                                                     *
***********************************************************************
*                                                                     *
* ATTRIBUTES                                                          *
*                                                                     *
*     NON-REUSABLE                                                    *
*                                                                     *
***********************************************************************
  EJECT
COMMX    DSECT
         USING COMMX,10
SAVE1315 DS    3F             SAVE AREA FOR REGS 13,14,15
SAVEREGS DS    18F            MY REGISTER SAVE AREA
PARALIST DS    8F             THE EXITS PARAMETER LIST-USER ROUTS
*                               1. INHDR PARAMETER NAME
*                               2. INTLR PARAMETER NAME
*                               3. ERROR PARAMETER NAME
*                               4. PRECOMP PARAMETER NAME
DDSYSIN  DS    2F             SYSIN PARAMETER
DDSUBNAM DS    4F             DDNAME PARAMETER LIST
*                               1. SYSUT1 DDNAME
*                               2. SYSUT2 DDNAME
HICONCOD DS    2X             RETURN CODE STORAGE AREA
CCADSORG DS    1X             SET TO '01' IF PARTITIONED DATA SET
ABORTSW  DS    1X             ABORT SWTICH-SET TO '01' IF ON
LABEL    DS    1X             SET TO X'00' IF DATA=YES             UL0H
*                                    X'01' IF DATA=NO              UL0H
*                                    X'10' IF DATA=ONLY            UL0H
*                                    X'20' IF DATA=ALL             UL0H
UEXIT    DS    1X             SET TO 01 IF OPEN TOOK USER LAB EXIT UL0H
UHRADR   DS    1F             CONTAIN ADDR. OF INHDR               UL0H
UTRADR   DS    1F             CONTAIN ADDR. OF INTLR               UL0H
INHD1    DS    1F             HAS ADDR. OF SYSUT1 HD LAB SAVE AREA UL0H
INHD2    DS    1F             HAS ADDR. OF SYSUT2 HD LAB SAVE AREA UL0H
INHDCT1  DS    1F             CONT. NO. OF HEADER LABELS FOR SYSUT1UL0H
INHDCT2  DS    1F             CONT. NO. OF HEADER LABELS FOR SYSUT2UL0H
INTL1    DS    1F             HAS ADDR. OF SYSUT1 TL LAB SAVE AREA UL0H
INTL2    DS    1F             HAS ADDR. OF SYSUT2 TL LAB SAVE AREA UL0H
INTLCT1  DS    1F             CONT. NO. OF TRAILER LABEL FOR SYSUT1UL0H
INTLCT2  DS    1F             CONT. NO. OF TRAILER LABEL FOR SYSUT2UL0H
BUFF1    DS    1F             CONTAIN ADDR. OF READ AREA-DS1       VS0H
BUFF2    DS    1F             CONTAIN ADDR. OF READ AREA-DS2       VS0H
LBUFF1   DS    1F             LENGTH OF BUFFER FOR SYSUT1 RCD PROC VS0H
LBUFF2   DS    1F             LENGTH OF BUFFER FOR SYSUT2 RCD PROC VS0H
LORECO   DS    1F             TOTAL LOGICAL RECORD COUNTER         VS0H
RECLTH   DS    1H             LOGICAL RECORD LENGTH-DS1            VS0H
RECLH2   DS    1H             LOGICAL RECORD LENGTH-DS2            VS0H
DSET1    DS    1F            ADDRS. OF SYSUT1 DCB IN IEBCQSAM      VS0H
DSET2    DS    1F            ADDRS. OF SYSUT2 DCB IN IEBCQSAM      VS0H
INPUTLOC DS    1F            ADDR. OF INPUT BUFFER FOR BLPRT RT    VS0H
NOEXTSW  DS    1X             NO EXIT TO USER LAB RTN IF SET TO 01 UL0H
TERMSW   DS    1X             TERM. AFTER LAB. PROC. IF SET TO 01  UL0H
EXIT1SW  DS    1X             SET TO 01 IF THERE IS AN USER INHDR  UL0H
EXIT3SW  DS    1X             SET TO 01 IF THERE IS AN USER INTLR  UL0H
VSSWT    DS    1X             SET TO 01 IF BSAM USED TO PROC VS RCDVS0H
*                             SET TO 02 IF QSAM USED TO PROC VS RCDVS0H
NOLB     DS    1X             SET TO 01 IF NO USER LABEL ON SYSUT1 UL0H
*                             SET TO 02 IF NO USER LABEL ON SYSUT2 UL0H
LABSW    DS    1X             SET TO 01; SYSUT1 HEADER LABEL PROC  UL0H
*                             SET TO 02; SYSUT2 HEADER LABEL PROC  UL0H
*                             SET TO 04; SYSUT1 TRAILER LABEL PROC UL0H
*                             SET TO 08; SYSUT2 TRAILER LABEL PROC UL0H
PRINTOUT DS    0F             SYSPRINT DCB
***********************************************************************
*        USER INPUT HEADER LABEL EXITS ARE TAKEN VIA THIS SECTION OF  *
*        CODE  USER INPUT HEADER LABELS ARE SAVED FOR LATER COMPARISON*
*        IF REQUESTED BY USER                                         *
***********************************************************************
USERLAB  CSECT
*                                                                     *
*        THIS MODULE IS TOTALLY NEW FOR RELEASE 17                    *
*                                                                     *
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         BALR  R2,R0         ESTABLISH ADDRESSABILITY
         USING *,R2
         STM   R1,R14,USAVE
         DROP  R2
         BALR  R12,R0        RE-ESTABLISH ADDRESSABILITY
         USING *,R12
         LA    R13,MYSAVE
         CLI   LABSW,X'01'    IS IN SYSUT1 HEADER LABEL PROC.
         BE    HDRLBEX1       YES, BRANCH
         CLI   LABSW,X'02'    IS IN SYSUT2 HEADER LABEL PROC.
         BE    HDRLBEX2       YES, BRANCH
         CLI   LABSW,X'04'    IS IN SYSUT1 TRAILER LABEL PROC.
         BE    TLRLBEX1       YES, BRANCH
         CLI   LABSW,X'08'    IS IN SYSUT2 TRAILER LABEL PROC.
         BE    TLRLBEX2       YES, BRANCH
         B     RET1           BRANCH
HDRLBEX1 MVC   LABMSG2+9(8),PARALIST *  INITIALIZE MSG: XX EXITS TO  *
         MVI   LAB2+3,X'F1'          *  (INHDR) IS MADE FOR SYSUT(1) *
*                                    *  RETURN CODE FROM USER ROUTINE*
*                                    *  IS XX                        *
         OI    UEXIT,X'01'     IN SYSUT1 LABEL EXIT
         TM    8(R1),X'80'    DID UNCORRECTABLE I/O ERROR OCCUR
         BO    IOERR1         WHILE READ USER INPUT HD ON SYSUT1
NTEST1   L     R3,0(R1)       GET ADDR. OF LABEL BUFFER AREA
         C     R3,ZERO        IS ADDR. OF BUFFER AREA ZERO
         BE    FSTNOHD1       YES, BRANCH
         CLI   3(R3),X'F1'    IS THIS THE 1ST LABEL OF THE GROUP
         BE    FSTHD1         YES, BRANCH
         TM    FSW1,X'02'     IS THIS THE VOL-SWITCH HD ON SYSUT1
         BO    INHEXT1        YES, BRANCH
         TM    LABEL,X'01'    IS DATA=NO
         BO    INHEXT1        YES, BRANCH
         B     INIT1          NO, GO SAVE THE LABEL
FSTHD1   TM    FSW1,X'01'     IS THIS THE 1ST GROUP OF HD ON SYSUT1
         BZ    INTLAB1        NO, THIS IS VOL-SWITCH TIME HDS
         MVI   FSW1,X'00'     YES, TURN SW OFF TO IND. 1ST HD OF
*                             1ST HD GROUP ON SYSUT1 HAS READ
FSTIME1  TM    LABEL,X'01'    IS DATA=NO
         BO    INHEXT1        YES, LABEL SAVING IS NOT NECESSARY
         LR    R2,R1          NO, GET 640 BYTES TO SAVE LABEL
         GETMAIN   R,LV=640
         ST    R1,INHD1
         LR    R1,R2
         B     INIT1          BRANCH
FSTNOHD1 TM    FSW1,X'01'     IS THIS THE FIRST GROUP OF HD ON DS1
         BZ    INTLAB1        NO, BRANCH
         XI    NOLB,X'01'     YES,TURN NOLB SW TO IND. NO LABEL
*                            ON SYST1
          MVI  FSW1,X'00'
         B     INHEXT1        BRANCH
INIT1    L     R2,INHD1      GET ADDR. OF SYSUT1 HD LAB SAVE AREA
         L     R5,INHDCT1
         M     R4,EIGHTY
         AR    R2,R5          REG.2 CONT. ADDR.OF NEXT AVAIL.LABEL
*                             SAVE AREA FOR SYSUT1
         L     R5,INHDCT1
         LA    R5,1(R5)       ADD 1 TO INPUT HEADER LAB CT FOR
         ST    R5,INHDCT1     SYSUT1
         B     LABSAVE        BRANCH
HDRLBEX2 MVC   LABMSG2+9(8),PARALIST *  INITIALIZE MSG : XX EXITS TO *
         MVI   LAB2+3,X'F2'          *  (INHDR) IS MADE FOR SYSUT(2) *
*                                    *  RETURN CODE FROM USER ROUTINE*
*                                    *  IS XX                        *
         OI    UEXIT,X'02'     IN SYSUT2 LABEL EXIT
         TM    8(R1),X'80'    DID I/O ERROR OCCUR WHILE READ USER
         BO    IOERR2         INPUT HEADER ON SYSUT2
NTEST2   L     R3,0(R1)       GET ADDR. OF LABEL BUFFER AREA
         C     R3,ZERO        IS ADDR. OF LABEL BUFFER ZERO
         BE    FSTNOHD2       YES, BRANCH
         CLI   3(R3),X'F1'    IS THIS THE 1ST LABEL OF THE GROUP
         BE    FSTHD2         YES, BRANCH
         TM    FSW2,X'02'     IS THIS THE VOL-SWITCH HD ON SYSUT2
         BO    INHEXT2        YES, BRANCH
         TM    LABEL,X'01'    IS DATA=NO
         BO    INHEXT2        YES, BRACH
         B     INIT2          BRANCH
*
*        THIS IS INH1 ON SYSUT2
FSTHD2   TM    FSW2,X'01'     IS THIS THE 1ST GROUP OF HD ON SYSUT1
         BZ    INTLAB2        NO, THIS IS VOL-SWITCH TIME HDS
         MVI   FSW2,X'00'     YES,TURN SW OFF TO IND. 1ST HD OF
*                             1ST HD GROUP ON SYSUT2 HAS READ
FSTIME2  TM    LABEL,X'01'    IS DATA=NO
         BO    INHEXT2        YES, LABEL SAVING IS NOT NECESSARY
         LR    R2,R1
         GETMAIN   R,LV=640
         ST    R1,INHD2
         LR    R1,R2
         B     INIT2          BRANCH
*
*        NO HEADER LABEL ON SYSUT2
FSTNOHD2 TM    FSW2,X'01'     IS THIS THE 1ST GROUP OF HD ON SYSUT2
         BZ    INTLAB2        NO,BRANCH
         XI    NOLB,X'02'     YES,TURN NOLB SW ON TO IND. NO HEADER
*                             LABEL ON SYSUT2
          MVI  FSW2,X'00'
         B     INHEXT2        BRANCH
INIT2    L     R2,INHD2       GET ADDR. OF SYSUT2 HD LAB SAVE AREA
         L     R5,INHDCT2
         M     R4,EIGHTY
         AR    R2,R5          REG.2 CONT. ADDR OF NEXT AVAIL. LABEL
*                             SAVE AREA FOR SYSUT2
         L     R5,INHDCT2
         LA    R5,1(R5)       ADD 1 TO INPUT HEADER LAB CT FOR
         ST    R5,INHDCT2     SYSUT2
*        THIS SECTION OF CODE WILL SAVE INPUT HEADER LABEL FOR LATER  *
*        COMPARISON.                                                  *
LABSAVE  L     R3,0(R1)       GET ADDR. OF LABEL BUFFER
         MVC   0(80,R2),0(R3) MOVE LABEL INTO LABEL SAVE AREA
         TM    EXIT1SW,X'01'  IS THERE AN USER INHDR
         BZ    RET            NO
         TM    NOEXTSW,X'01'  YES,IS NO EXIT TO INHDR SW ON
         BO    RET            YES,NO EXIT TO INHDR SW IS ON
GOUHR    L     R15,UHRADR    NO,GO TO USER INHDR
         BALR  R14,R15        BRANCH
         B     CKRCODE        BRANCH
*        INITIALIZATION FOR SYSUT1 VOL-SWITCH HEAER PROCESSING
INTLAB1  TM    TERMSW,X'01'   WAS TERM SW SET BY VOL-SWITCH TRAILER
*                             LABEL PROCESSING
         BO    RET1           YES, BRANCH
         MVI   FSW1,X'02'     SET FSW1 TO IND. IN VOL-SWITCH HEAD
         MVC   INHDCT1(4),ZERO  SET HEADER LAB, COUNT TO ZERO
INHEXT1  L     R5,INHDCT1
         LA    R5,1(R5)
         ST    R5,INHDCT1
         B     INHEXT         BRANCH
*        INITIALIZATION FOR SYSUT2 VOL SWITCH HEADER PROCESSING
INTLAB2  TM    TERMSW,X'01'   WAS TEMM SW SET BY VOL-SWITCH TRAILER
*                             LABEL PROCESSING
         BO    RET1           YES, BRANCH
         MVI   FSW2,X'02'     SET FSW2 TO IND. IN VOL-SWITCH HEAD
         MVC   INHDCT2(4),ZERO     SET HEADER LAB. COUNT TO ZERO
INHEXT2  L     R5,INHDCT2
         LA    R5,1(R5)
         ST    R5,INHDCT2
INHEXT   TM    EXIT1SW,X'01'  IS THERE AN USER INHDR
         BZ    RET1           NO, IGNORE THE REST OF LABELS
         B     GOUHR          BRANCH
***********************************************************************
*        USER INPUT TRAILER LABEL EXITS ARE TAKEN VIA THIS SECTION OF *
*        CODE. USER INPUT TRAILER LABELS ARE SAVED FOR LATER COMPARISO*
*        IF REQUESTED BY USER                                         *
***********************************************************************
TLRLBEX1 MVC   LABMSG2+9(8),PARALIST+8 * INIT. MSG: X EXITS TO (INTLR)*
         MVI   LAB2+3,X'F1'            * IS MADE FOR SYSUT(2) RETURN  *
*                                      * CODE FROM USER ROUTINE IS XX *
         L     R3,0(R1)       GET ADDR. OF LABEL BUFFER AREA
         C     R3,ZERO        IS ADDR. OF LABEL BUFFER AREA ZERO
         BNE   TTESTIO1       NO, BRANCH
         XI    NOLB,X'01'     YES,TURN NOLB  SW ON TO IND. NO LAB
*                             ON SYSUT1
         MVC   INTLCT1(4),ZERO     SET SYSUT1 TRAILER LAB COUNT 0
         B     TEXT1          BRANCH
TTESTIO1 TM    8(R1),X'80'    DID I/O ERROR OCCUR WHILE READ USER
         BO    TIOERR1        INPUT TRAILER ON SYSUT1
TNTEST1  TM    LABEL,X'01'    DATA=NO
         BO    INTEXT1        YES,DATA=NO
         CLI   3(R3),X'F1'    IS THIS THE 1ST LABEL OF THE GROUP
         BNE   INITT1         NO, BRANCH
         MVC   INTLCT1(4),ZERO     SET SYSUT1 TRAILER LAB COUNT 0
         NI    NOLB,X'20'     TURN NOLB SW OFF
         CLC   INTL1(4),ZERO  HAS VOL-SWITCH TIME TRAILER LABELS
*                             BEEN SAVED
         BNE   INITT1         YES, USE THE SAME SAVE AREA TO OVER
*                             LAY THOSE LABELS
         LR    R2,R1
         GETMAIN   R,LV=640
         ST    R1,INTL1       SAVE GOTTEN CORE ADDR. AT INTL1
         LR    R1,R2
INITT1   L     R2,INTL1
         L     R5,INTLCT1
         M     R4,EIGHTY
         AR    R2,R5          REG2.HAS ADDR. OF NEXT AVAIL LABEL
*                             SAVE AREA FOR SYSUT1
         L     R5,INTLCT1
         LA    R5,1(R5)       ADD 1 TO INPUT TRAILER LABEL CT FOR
         ST    R5,INTLCT1     SYSUT1
         B     TLABSAVE       BRANCH
*
TLRLBEX2 MVC   LABMSG2+9(8),PARALIST+8 * INIT. MSG: X EXITS TO (INTLR)*
         MVI   LAB2+3,X'F2'            * IS MADE FOR SYSUT(2) RETURN  *
*                                      * CODE FROM USER ROUTINE IS XX *
         L     R3,0(R1)       GET ADDR. OF LABEL BUFFER AREA
         C     R3,ZERO        IS ADDR. OF LABEL BUFFER AREA ZERO
         BNE   TTESTIO2       NO, BRANCH
         XI    NOLB,X'02'     YES, TURN NOLB SW ON TO IND. NO LAB
         MVC   INTLCT2(4),ZERO     SET SYSUT2 TRAILER LABEL COUNT 0
         B     TEXT2          BRANCH
TTESTIO2 TM    8(R1),X'80'    DID I/O ERROR OCCUR WHILE READ USER
         BO    TIOERR2        INPUT TRAILER ON SYSUT2
TNTEST2  TM    LABEL,X'01'    DATA=NO
         BO    INTEXT2        YES, DATA=NO
         CLI   3(R3),X'F1'    IS THIS THE 1ST LABEL OF THE GROUP
         BNE   INITT2         NO, BRANCH
         MVC   INTLCT2(4),ZERO     SET SYSUT2 TRAILER LABEL COUNT 0
          NI   NOLB,X'01'    TURN NOLB SW OFF
         CLC   INTL2(4),ZERO  HAS VOL-SWITCH TIME TRAILER LABEL
*                             BEEN SAVED
         BNE   INITT2         YES,USE THE SAME SAVE ARER TO OVER
*                             LAY THOSE LABELS
         LR    R2,R1
         GETMAIN   R,LV=640
         ST    R1,INTL2       SAVE GOTTEN CORE ADDR. AT INTL2
         LR    R1,R2
INITT2   L     R2,INTL2
         L     R5,INTLCT2
         M     R4,EIGHTY
         AR    R2,R5          REG.2 HAS ADDR. OF NEXT AVAIL. LABEL
*                             SAVE AREA FOR SYSUT2
         L     R5,INTLCT2
         LA    R5,1(R5)       ADD 1 TO INPUT TRAILER LABEL CT FOR
         ST    R5,INTLCT2     SYSUT2
*
TLABSAVE L     R3,0(R1)       GET ADDR. OF LABEL BUFFER
         MVC   0(80,R2),0(R3) MOVE LABEL INTO LABEL SAVE AREA
         TM    EXIT3SW,X'01'  IS THERE AN USER INTLR
         BZ    RET            NO,THERE IS NO USER INTLR
         TM    NOEXTSW,X'01'  YES,IS NO MORE EXIT TO INTLR SW ON
         BO    RET            YES,NO MORE EXIT TO INTLR
GOTLR    L     R15,UTRADR    NO,GO TO USER INTLR
         BALR  R14,R15        GO TO USER INTLR
CKRCODE  STC   R15,RETCODE
         CLI   RETCODE,X'04'  RET. CODE FROM USER = 4
         BNE   CKMORE
         B     RET
CKMORE   CLI   RETCODE,X'00'   RET CODE FROM USER = 0
         BE    NOEXT          YES, BRANCH
         MVI   TERMSW,X'01'   NO,SET TERM. AFTER LABEL PROC. SW ON
NOEXT    MVI   NOEXTSW,X'01' TURN NO EXIT TO USER LAB. PROC. SW ON
* INIT. MSG: IEB248I (NO.) EXITS TO (USER EXIT RTN) IS MADE FOR       *
*                    SYSUT(1/2) RETURN CODE FROM USER ROUTINE IS (NO.)*
         CVD   R15,CONVTDEC             CONVERT THE USER RET CODE
         UNPK  PTAREA(3),CONVTDEC+6(2)  TO PRINTABLE CHARACTER
         OI    PTAREA+2,X'F0'
         MVC   LAB3+8(2),PTAREA+1
         CVD   R5,CONVTDEC              CONVERT THE NO. OF EXITS
         UNPK  PTAREA(3),CONVTDEC+6(2)
         OI    PTAREA+2,X'F0'           PRINTABLE CHARACTER
         MVC   LABMSG+8(1),PTAREA+2
         MVC   MYPRTARE(120),BLANKS
         MVC   MYPRTARE(84),LABMSG
         LA    R0,119
         LA    R1,MYPRTARE
         L     R15,MYPRTCON
         BALR  R14,R15        GO TO PRINT THE MSG
         TM    LABEL,X'30'    IS DATA=ALL OR DATA=ONLY
         BM    RET            YES, GO GET MORE LABEL
RET1     LA    R15,0
         LM    R1,R14,USAVE
           BR  R14            RET. TO OPEN/CLOSE/EOV
RET      LA    R15,4          GO GET MORE LABELS
         LM    R1,R14,USAVE
         BR    R14            RET. TO OPEN/CLOSE/EOV
INTEXT1  CLI   3(R3),X'F1'    IS THIS THE 1ST LABEL OF THE GROUP
         BNE   TEXT1          NO, BRANCH
         MVC   INTLCT1(4),ZERO     SET SYSUT1 TRAILER LAB COUNT 0
TEXT1    L     R5,INTLCT1
         LA    R5,1(R5)
         ST    R5,INTLCT1
         B     INTEXT         BRANCH
INTEXT2  CLI   3(R3),X'F1'    IS THIS THE 1ST LABEL OF THE GROUP
         BNE   TEXT2          BRANCH
         MVC   INTLCT2(4),ZERO  SET SYSUT2 TRAILER LABEL COUNT 0
TEXT2    L     R5,INTLCT2
         LA    R5,1(R5)
         ST    R5,INTLCT2
INTEXT   TM    EXIT3SW,X'01'  IS THERE AN USER INTLR
         BZ    RET1           NO, IGNORE THE REST OF THE LABELS
         B     GOTLR          BRANCH
*
IOERR1   ST    R1,SAVE1
         LA    R1,43          I/O ERROR WHILE READ INPUT HEADER ON DS1
         L     R15,PRINCONS
         BALR  R14,R15        PRINT MSG
         L     R1,SAVE1
         B     NTEST1         BRANCH
*
IOERR2   ST    R1,SAVE1
         LA    R1,44          I/O ERROR WHILE READ INPUT HEADER ON DS2
         L     R15,PRINCONS
         BALR  R14,R15        PRINT MSG
         L     R1,SAVE1
         B     NTEST2         BRANCH
TIOERR1  ST    R1,SAVE1
         LA    R1,45          I/O ERROR WHILE READ INPUT TRAILER ON DS1
         L     R15,PRINCONS
         BALR  R14,R15        PRINT MSG
         L     R1,SAVE1
         B     TNTEST1        BRANCH
*
TIOERR2  ST    R1,SAVE1
         LA    R1,46          I/O ERROR WHILE READ INPUT TRAILER ON DS2
         L     R15,PRINCONS
         BALR  R14,R15        PRINT MSG
         L     R1,SAVE1
         B     TNTEST2        BRANCH
*
PRINCONS DC    V(JPRINTRT)
MYPRTCON DC    V(ZZPR)
SAVE1    DS    1F
SAVE14   DC    F'0'           REG.14 SAVE AREA
EIGHTY   DC    F'80'
ZERO     DC    F'0'
BLANKS   DC    CL4'    '
MYPRTARE DS    30F
USAVE    DS    14F
MYSAVE   DS    18F
PTAREA   DS    1F
CONVTDEC DS    1D
C16      DC    X'F1F6'
FSW1     DC    X'01'          FIRST TIME SW FOR SYSUT1 INHDR EXIT
FSW2     DC    X'01'          FIRST TIME SW FOR SYSUT2 INHDR EXIT
RETCODE  DC    X'00'          RET. CODE FROM USER LABEL RTN
TFSW1    DC    X'01'          FIRST TIME SW FOR SYSUT1 INTLR EXIT
TFSW2    DC    X'01'          FIRST TIME SW FOR SYSUT2 INTLR EXIT
***********************************************************************
*        THE FOLLOWING MESSAGE IS BUILT AT EXECUTION TIME             *
LABMSG   DC    CL10'IEB248I   '
LABMSG2  DC    CL32'EXITS TO          IS MADE FOR SY'
LAB2     DC    CL32'SUT    RETURN CODE FROM USER ROU'
LAB3     DC    CL10'TINE IS   '
         END
