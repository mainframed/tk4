         TITLE 'IEFSD094 - MVT SYSOUT WRITER JOB SEPARATOR'
***********************************************************************
*
*          RELEASE 22 DELETIONS/CHANGES
*          RELEASE 21 DELETIONS/CHANGES
* 109760                                                         A64011
* 069067                                                         Y00188
*3524                                                            M3018
*3524                                                            A41000
*          RELEASE 20 DELETIONS/CHANGES
*1043                                                            A32649
* 015000-036000,040612-040760,045200-048400,048600-052600        MAPMAC
* 066640,066720,067600,068000,109520,109760                      MAPMAC
* 066440-066900,067200-068200,069005-069027,109280-109920        A32649
*1043                                                            A35780
*1043066200,069080                                               M2204
*
***********************************************************************
*                                                                     *
*                                                                     *
*TITLE-- IEFSD094 - SYSOUT WRITER JOB SEPARATOR ROUTINE               *
*                                                                     *
*STATUS CHANGE LEVEL 006
*FUNCTION/OPERATION--                                                 *
*   THIS ROUTINE AND ITS LOAD MODULE IS USED IN PCP.               SM0I
*   THIS ROUTINE SEPARATES JOBS IN ONE OF TWO WAYS DEPENDING ON THE   *
*   OUTPUT DATA SET TYPE :                                            *
*        1.PRINTER TYPE --- THE JOBNAME AND CLASS WILL BE PRINTED IN  *
*          BLOCK FORMAT ON TNREE PAGES AND A LINE OF ASTERISKS WILL   *
*          BE PRINTED TWICE ( OVER PRINTED ) ON THE FOLD PRECEEDING   *
*          EACH PAGE AND AFTER THE THIRD PAGE CONTAINING THE BLOCK    *
*          FORM OUTPUT .                                              *
*        2.PUNCH TYPE --- THE JOBNAME AND CLASS WILL BE PUNCHED IN    *
*          THE MIDDLE OF THREE CARDS .                                *
*                                                                     *
*OPERATION OF THIS MODULE--                                           *
*ENTRY POINT--                                                        *
*   IEFSD094 WHICH IS LINKED TO FROM IEFSD078 WHEN A JOB SEPARATOR IS *
*   SPECIFIED .                                                       *
*                                                                     *
*INPUT--                                                              *
*   REGISTER 1 POINTS TO THE PARAMETER LIST CONTAINING SWITCHES ,     *
*   ADDRESS OF OUTPUT DCB , ADDRESS OF 8 CHARACTER JOBNAME FIELD ,    *
*   AND THE ADDRESS OF 1 CHARACTER SYSOUT CLASS .                     *
*   REGISTER 6 POINTS TO PARLIST WHEN NOT PCP SYSTEM.              SM0I
*                                                                     *
*EXIT POINT 0.5 --                                                 VS0H
*   BRANCH TO IEFSDXXX IF OUTPUT TO BE SPANNED.                    VS0H
*OUTPUT AT EXIT POINT 0.5 --                                       VS0H
*        REGISTER 7 POINTS TO THE WORK LIST USED BY DSB AND SMB    VS0H
*        ROUTINE.                                                  VS0H
*        REGISTER 8 POINTS TO THE PARAMETER LIST GIVEN TO SMB AND  VS0H
*        DSB PROCESSING ROUTINES.                                  VS0H
*                                                                  VS0H
*EXIT POINT 1 --                                                      *
*   BRANCH TO IEFSD088 TO DO THE START TRANSITION ROUTINE             *
*                                                                     *
*OUTPUT AT EXIT POINT 1 --                                            *
*   REGISTER 7 POINTS TO THE WORK LIST USED BY DSB AND SMB ROUTINE    *
*   REGISTER 8 POINTS TO THE WORK AREA FOR STANDARD OUTPUT ROUTINE    *
*                                                                     *
*EXIT POINT 2 --                                                      *
*   BRANCH TO IEFSD089 TO DO THE PUT FOR EACH RECORD                  *
*                                                                     *
*OUTPUT AT EXIT POINT 2 --                                            *
*   REGISTER 7 POINTS TO THE WORK LIST USED BY DSB AND SMB ROUTINE    *
*   REGISTER 8 POINTS TO THE WORK AREA FOR STANDARD OUTPUT ROUTINE    *
*                                                                     *
*EXIT POINT 3 --                                                      *
*                                                                     *
*OUTPUT AT EXIT POINT 3 --                                            *
*   REGISTER 7 POINTS TO THE WORK LIST USED BY DSB AND SMB ROUTINE    *
*   REGISTER 8 POINTS TO THE WORK AREA FOR STANDARD OUTPUT ROUTINE    *
*                                                                     *
*EXIT POINT 4 --                                                      *
*   BRANCH TO IEFSD095 TO DO THE BLOCK LETTER ROUTINE                 *
*                                                                     *
*OUTPUT AT EXIT POINT 4 --                                            *
*   REGISTER 1 POINTS TO THE PARAMETER LIST CONTAINING THE ADDRESS OF *
*   CHARACTER TO BE PIT OUT,THE ADDRESS OF THE LINE COUNT,            *
*   THE ADDRESS OF THE CONSTRUCTION AREA,  AND THE ADDRESS OF THE     *
*   NUMBER OF CHARACTERS .                                            *
*                                                                     *
*EXIT POINT 5 --                                                      *
*   RETURN TO IEFSD078 IF SYSOUT WORK DONE                            *
*                                                                     *
*OUTPUT AT EXIT POINT 5 --                                            *
*   THE REGISTERS ARE RESTORED TO THERE CONTENTS AS THEY WERE ON      *
*   LEAVING IEFSD078 EXCEPT REGISTER 15 CONTAINS A RETURN CODE -0-FOR *
*   CORRECT OPERATION OR -8-FOR UNRECOVERABLE OUTPUT ERROR            *
*                                                                     *
*EXTERNAL ROUTINES --                                                 *
*                                                                     *
*   IEFSD088   THE TRANSITION ROUTINE                                 *
*   IEFSD089   THE OUTPUT ROUTINE                                     *
*   IEFSD89M   MESSAGES FOR IEFSD089                                  *
*   IEFSD078   SYSOUT WRITER LINK-TO-JOB SEPARATOR-OR-TO-DSB/SMB      *
*   IEFSD095   BLOCK LETTER ROUTINE                                   *
*   IEFSDXXX   SPANNED DATASETS ROUTINE                            VS0H
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*ATTRIBUTES - REENTRANT                                               *
*                                                                     *
***********************************************************************
*                        NEEDED DSECTS
*
IEFSD094 CSECT                     FOR EQUATES                   Y00188
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*DEFINE DCB                                                        VS0H
         DCBD  DSORG=PS                                            VS0H
         IEFSDPAR                                                MAPMAC
JSPARMS  DSECT
*THE PARMAMETER LIST INPUT TO THIS MODULE
PARMLJS  DS    0F
JSSWTCHS DS    C                       SWITCHES
         DS    0F
JSDCBOUT DS    F                       OUTPUT DCB ADDRESS
JSNAMEAD DS    F                       JOBNAME FIELD ADDRESS
JSSYSCLA DS    F                       JOB CLASS ADDRESS
JSENT078 EQU   JSSWTCHS+1         X'FF' IF ENTRY FROM IEFSD078   A32649
ENTRY078 EQU   X'FF'              ENTRY SWITCH                   A32649
JSEND    EQU   *                       END OF PARM LIST
         IEFSDWKT                                                MAPMAC
*THE AREA USED TO BUILD JOB SEPARATOR OUTPUT                     F40172
OUTAREAS DS    0F                                                F40172
         DS    30F                                               F40172
SAVER131 DS    F                   SAVE R13 FOR ENT TO TRAN1     F40172
SAVER132 DS    F                   SAVE R13 FOR ENT TO PUT       F40172
SAVER133 DS    F                   SAVE R13 FOR ENT TO TRANS2    F40172
SAVER134 DS    F                                                 F40172
SAVETRAN DS    18F                                               F40172
SAVEPUT  DS    18F                                               F40172
SAVETRA2 DS    18F                                               F40172
SAVEPUT2 DS    18F                                               F40172
ADR3     DS    F                                                 F40172
ADR11    DS    F                                                 F40172
OUTAREND EQU   *                        END OF AREA               TTE0K
         IEFSDSDR                                                MAPMAC
*EQUATES
SKIPONE  EQU   9                  MACHINE C/C SINGLE SPACE       F40172
ASASW    EQU   4                   ASA CONTROL CHARACTER
MACSW    EQU   2                   MACHINE CONTROL CHARACTER
VARSW    EQU   64                  VARIABLE SWITCH WHEN NOT FIXED
FIXSW    EQU   128                 FIXED SWITCH WHEN NOT VARIABLE
UNKSW    EQU   VARSW+FIXSW         UNKNOWN SWITCH
*                                                                     *
*                   THIS ROUTINE IS THE STANDARD JOB SEPARATOR        *
*                   ROUTINE WHICH MAY BE USED TO SEPARATE JOBS        *
*                   IN THE OUTPUT STREAM . IT RECEIVES A FOUR         *
*                   WORD REGISTER SAVE AREA THE ADDRESS OF            *
*                   WHICH IS IN REGISTER 1 . THE AREA HAS THE         *
*                   FOLLOWING APPEARANCE .                            *
*                                                                     *
*                    *************************************            *
*                    *        *        *        *        *            *
*                    *SWITCHES*        *        *        *            *
*                    *        *        *        *        *            *
*                    *************************************            *
*                    *                                   *            *
*                    *        ADDRESS OF OUTPUT DCB      *            *
*                    *                                   *            *
*                    *************************************            *
*                    *                                   *            *
*                    *  ADDRESS OF 8 CHAR. JOBNAME FIELD *            *
*                    *                                   *            *
*                    *************************************            *
*                    *                                   *            *
*                    *  ADDRESS OF 1 CHAR. CLASS FIELD   *            *
*                    *                                   *            *
*                    *************************************            *
*                                                                     *
*                   SWITCHES ARE AS FOLLOWS .                         *
*                                                                     *
*                    LEFT MOST BIT (128) IS ON IF THE OUTPUT          *
*                    UNIT IS A 1442 PUNCH                             *
*                                                                     *
*                    NEXT BIT (64) IS ON IF THE OUTPUT UNIT           *
*                    IS A PUNCH OR IF IT IS A TAPE WHICH HAS          *
*                    A PUNCH AS ITS ULTIMATE DESTINATION              *
*                                                                     *
*                    NEXT BIT (32) IS ON IF THE OUTPUT UNIT           *
*                    IS A PRINTER OR A PUNCH                          *
*   BRANCH TO IEFSD088 TO DO THE END TRANSITION ROUTINE               *
*                                                                     *
*                   A USER MAY WRITE A ROUTINE TO USE INSTEAD         *
*                   OF THIS . HE WILL HAVE THE ROUTINE IN THE         *
*                   LINK LIBRARY AND THE PROGRAM NAME IN THE          *
*                   PARM FIELD OF THE EXEC STATEMENT AND BY           *
*                   SPECIFYING THE REQUIRED PROCEEDURE NAME           *
*                   IN THE START COMMAND FOR THE SYSOUT               *
*                   WRITER . SEE JCL PUBLICATION .                    *
*                                                                     *
*                   THE USER MUST FREE ANY CORE HE OBTAINED           *
*                   DURING THE OPERATION OF HIS ROUTINE . HE          *
*                   MUST SET UP HIS OWN SYNCHRONOUS ERROR EXIT        *
*                   ROUTINE AND PLACE ITS ADDRESS IN THE              *
*                   DCBSYNAD FIELD OF THE OUTPUT DCB . HE MUST        *
*                   SAVE REGISTERS 0 TO 12 AND 14 IN THE 15 WORD      *
*                   AREA WHICH STARTS 12 BYTES BEYOND THE LOCATION    *
*                   IN REGISTER 13 . HE MUST ALSO SAVE REGISTER 13 .  *
*                                                                     *
*INITIALIZE
IEFSD094 CSECT
*3656065500                                                        SM0I
* 042600-045000,066600,067000,067800,109400-109800               F40172
         SAVE  (14,12)             SAVE REGISTERS
         BALR  R9,0                SET BASE REGISTER
         USING *,R9
         LPR   R5,R1                    SAVE PARM REGISTER       M22040
*                                       A3578                    M2204
         USING PARMLJS,R5
*GET CORE FOR WORK AREA                                          A32649
         LA    R0,WKSOE-WKSOR+OUTAREND-OUTAREAS+WORKEND-WORKLST  A32649
*                                  GET SIZE OF WORK AREAS        A32649
         GETMAIN R,LV=(0)          GET CORE                      A32649
         USING PARLIST,R6                                          VS0H
         LR    R8,R1              ESTABLISH WORKSOR ADDRESS      A32649
         LA    R1,WKSOE-WKSOR(R8) ESTABLISH OUTAREA ADDRESS      A32649
         LA    R7,OUTAREND-OUTAREAS+WKSOE-WKSOR(R8)              A32649
*                                 ESTABLISH WORKLIST ADDRESS     A32649
         USING WKSOR,R8                                          A32649
         USING OUTAREAS,R1                                       A32649
         USING WORKLST,R7                                        A32649
         XC    WORKLST(WORKEND-WORKLST),WORKLST  CLEAR CORE      M2204
         ST    R13,WOSAP
         XC    16(4,R13),16(R13)
         ST    R13,SRETR           SAVE RETURN REGISTER
         LA    R13,SMSVA           GET SAVE AREA ADDRESS
*IS ENTRY FROM IEFSD078                                          A32649
         XC    WOXXX(8),WOXXX     CLEAR SPANNING ADDRESSES       A32649
         TM    JSENT078,ENTRY078  IS ENTRY FROM IEFSD078         A32649
         BNO   PCP20              NO BYPASS SPANNING CHECK       A32649
*SAVE THE ADDRESS OF THE SPANNING MODULE AND GOT CORE              VS0H
         MVC   WOXXX(8),PARXXX                                     VS0H
*                                                                F40172
*           INITIALIZE CONTROL CHARACTERS                        F40172
*                                                                F40172
         MVI   SAVEC,SKIPONE      INITIALIZE SAVEC               F40172
         MVI   SAVED,SKIPONE      INITIALIZE SAVED               F40172
         ST    R6,WOSPS                 SAVE PARLIST ADDRESS     A32649
PCP20    EQU   *                                                   VS0H
         LR    R6,R1                                               VS0H
         DROP  R1                                                  VS0H
         USING OUTAREAS,R6                                         VS0H
*CLEAR TRANSITION WRITE SWITCH
         MVI   BCTSW,X'00'         PUT COUNT TO ZERO
*TURN OFF SWITCHES
         MVI   SEGIN,0                 INDICATE FIRST SEGMENT      VS0H
         MVI   SORS1,0
         MVI   SORS2,0                  TURN OFF SWITCH          A35780
         L     R0,JSDCBOUT
         ST    R0,DCBOT                STORE OUTPUT DCB ADDR
         L     R0,JSSWTCHS
         ST    R0,VARBA                STORE OUTPUT TYPE
*IS VARIABLE SPANNED OUTPUT                                        VS0H
         L     R15,WOXXX                GET SPANNED MODULE ADDRESS VS0H
         LTR   R15,R15                  IF A MODULE NOT PRESENT    VS0H
         BZ    GOTON                    THEN DONT ENTER IT         VS0H
*BRANCH TO THE SPANNING MODULE                                     VS0H
         O     R15,BTWLV                USE 12 TO INDICATE MESSAGE VS0H
         BALR  R14,R15                  GO TO SPANNING MODULE      VS0H
GOTON    EQU   *                                                   VS0H
*DO TRANSITION  ROUTINE
         ST    R13,SAVER131
         LA    R13,SAVETRAN
         STM   14,12,12(13)
         L     R13,SAVER131
         L     R15,SD88EXIT        GET ADDRESS FOR EXIT
         BALR  R14,R15
         LA    R13,SAVETRAN
         LM    14,12,12(13)
         L     R13,SAVER131
*IS THERE AN UNRECOVERABLE ERROR
         L     R10,WOSAP           GET ADDRESS ERROR RETURN PUT AT
         TM    19(R10),X'FF'       TEST FOR ERROR IF SO
         BNE   UNREIOER            BRANCH TO FREE WORK AREAS
*IS THERE AN UNOPENED DATA SET
         TM    SORS1,LRERS+INERS   TEST FOR UNOPENED DATA SET
         BO    SD94EOFX            IF SO BRANCH TO END OF FILE
*IS THIS PRINTER TYPE UNIT
         TM    VARBA,PUNSH         TEST FOR PUNCH IF SO
         BO    PCHRT               BRANCH TO PUNCH ROUTINE
         L     R2,ONE              SET R2 TO ONE
         LR    R3,R2               SET R3 TO ONE
         LR    R12,R2              SET R12 TO ONE
BACK     LR    R1,R12              IS R12 EVEN IF SO
         N     R1,ONE              THIS IS NOT JOBNAME
         BM    AROUND
         L     R10,JSSYSCLA
         L     R11,ONE             GET NUMBER OF CHARACTERS
         LA    R4,OUTAREAS+54      GET OUT AREA
         B     GO
AROUND   L     R10,JSNAMEAD                                        4686
         L     R11,EIGHT           GET NUMBER OF CHARACTERS
         LA    R4,OUTAREAS+10      GET OUT AREA
GO       MVI   OUTAREAS,C' '
         MVC   OUTAREAS+1(119),OUTAREAS
         ST    R3,ADR3
         LA    R3,ADR3
         ST    R11,ADR11
         LA    R11,ADR11
         GETMAIN  R,LV=24                                          4686
         ST    R2,16(R1)           SAVE REGISTER TWO               4686
         LR    R2,R1               SAVE REGISTER ONE               4686
         CALL  IEFSD095,((10),(3),(4),(11)),MF=(E,(1))             4686
         LR    R1,R2               RESTORE REGISTER ONE            4686
         L     R2,16(R1)           RESTORE REGISTER TWO            4686
* END
         FREEMAIN  R,LV=24,A=(1)                                   4686
         L     R3,ADR3                                             4686
         L     R11,ADR11
         CR    R3,R2               COMPARE R3 TO 1 IF NOT EQUAL
         BNE   ARND                BRANCH AROUND
*IS THIS JOBNAME
         LR    R1,R12              IS (R12) EVEN IF SO
         N     R1,ONE              THIS IS NOT JOBNAME SO
         BZ    ARND1               BRANCH AROUND
         MVI   CONCH,C'9'               SKIP TO CHANNEL 9 AND WRITE CC
ASK      L     R1,OTWY                  GET 120
         ST    R1,INLSF                 SET LENGTH OF INPUT
         LA    R1,FANFOLD               GET ADDR OF ASTERISKS
         ST    R1,INLOC                 SET INPUT ADDR
         OI    SORS1,OLDSW              TURN ON OLD SWITCH
         ST    R13,SAVER132             SAVE R13
         LA    R13,SAVEPUT              GET ADDR OF SAVE AREA
         STM   14,12,12(13)             SAVE REG.S
         L     R13,SAVER132             RESTORE R13
         L     R15,SD89EXIT             GET ADDR OF PUT ROUTINE
         BALR  R14,R15                  GO TO PUT OUT ASTERISKS
         LA    R13,SAVEPUT              GET ADDR OF SAVE AREA
         LM    14,12,12(13)             RESTORE REG.S
         L     R13,SAVER132             RESTORE R13
         L     R10,WOSAP                GET ADDR ERROR RETURN PUT AT
         TM    19(R10),X'FF'            TEST FOR ERROR IF SO
         BNE   UNREIOER                 BRANCH TO FREE WORK AREAS
*IS PERMANENT IO ERROR SWITCH ON
         TM    SORS1,INERS              TEST FOR IO ERROR IF SO
         BO    SD94EOFX                 BRANCH TO END OF FILE
         TM    CONCH,C'+'               HAS LINE OF ASTERISKS BEEN
*                                       OVER PRINTED   IF SO
         BO    WAY                      BRANCH TO OUTPUT JOBNAME AND
*                                       CLASS IN BLOCK FORM
         MVI   CONCH,C'+'               NO SKIP AND WRITE CC
         B     ASK                      GO BACK AND OVER PRINT LINE
*                                       OF ASTERISKS
WAY      MVI   CONCH,C'1'               SKIP TO CHANNEL 1 AND WRITE CC
         B     OUT                 AND BRANCH OUT
ARND1    MVI   CONCH,C'-'          SET CC TO SKIP 3
         B     OUT                 AND BRANCH OUT
ARND     MVI   CONCH,C' '          SET CC TO SKIP 1
OUT      L     R1,OTWY             GET 120
         ST    R1,INLSF            SET LENGTH OF INPUT
         LA    R1,OUTAREAS         GET INPUT ADDRESS
         ST    R1,INLOC            SET ADDRESS OF INPUT
         OI    SORS1,OLDSW         TURN ON OLD SWITCH
         ST    R13,SAVER132
         LA    R13,SAVEPUT
         STM   14,12,12(13)
         L     R13,SAVER132
         L     R15,SD89EXIT        GET ADDRESS FOR EXIT
         BALR  R14,R15
         LA    R13,SAVEPUT
         LM    14,12,12(13)
         L     R13,SAVER132
*IS THERE AN UNRECOVERABLE ERROR
         L     R10,WOSAP           GET ADDRESS ERROR RETURN PUT AT
         TM    19(R10),X'FF'       TEST FOR ERROR IF SO
         BNE   UNREIOER            BRANCH TO FREE WORK AREAS
*IS PERMANENT IO INPUT ERROR SWITCH ON
         TM    SORS1,INERS         TEST FOR IO ERROR IF NOT
         BZ    CONT                BRANCH TO CONT
*TURN ON OLD SWITCH
SD94EOFX OI    SORS1,OLDSW
*FIX NUMBER OF TRANSITION WRITES
         MVI   BCTSW,1             PUT IN ONE FOR PRINTER
         TM    VARBA,PUNSH         TEST FOR PUNCH IF NOT
         BZ    DOTRAN              BRANCH TO DO TRANSITION
         MVI   BCTSW,3             PUT IN 3 FOR PUNCH
*DO TRANSITION ROUTINE
DOTRAN   ST    R13,SAVER133
         LA    R13,SAVETRA2
         STM   14,12,12(13)
         L     R13,SAVER133
         L     R15,SD88EXIT
         BALR  R14,R15
         LA    R13,SAVETRA2
         LM    14,12,12(13)
         L     R13,SAVER133
*IS DCB SET UP FOR EXCP                                            CC0H
         L     R1,DCBOT                 GET DCB ADDRESS            CC0H
         TM    DCBMACRF-IHADCB(R1),X'80' TEST FOR EXCP. IF NOT     CC0H
         BZ    UNREIOER                 BRANCH AROUND FAKE TRUNC   CC0H
*FAKE TRUNCATE                                                     CC0H
         SR    R15,R15                  INDICATE FAKE TRUNC        CC0H
         L     R4,WOSPS                                          M3018
         USING PARLIST,R4                                        M3018
         XC    PARWKSOR,PARWKSOR        TO BYPASS XYZ CC TEST    M3018
         DROP  R4                                                M3018
         L     R11,48(0,R1) LOAD REG 11 FROM DCB                   CC0H
         BALR  R14,R11 LINK TO FAKE PUT ROUTINE                    CC0H
*DID TRUNC FAIL                                                    CC0H
         LTR   R1,R1                    TEST FOR ERROR             CC0H
         BNZ   UNREIOER                 IF NOT BRANCH TO DONE      CC0H
*PUT IN ERROR INDICATION                                           CC0H
         L     R10,WOSAP                GET ADDRESS OF ERROR RETURNCC0H
         MVI   19(R10),8                UNRECOVERABLE ERROR        CC0H
         B     UNREIOER            BRANCH TO FREE WORK AREAS
CONT     AR    R3,R2               R3=R3+1
         C     R3,THIR             IF R3 IS NOT EQUAL TO 13
         BNE   BACK                BRANCH TO BACK
         L     R3,ONE              SET R3 TO 1
         AR    R12,R2              R12=R12+1
         C     R12,SEVN            IF R12 IS NOT EQUAL TO SEVEN
         BNE   BACK                BRANCH BACK
         MVI   CONCH,C'9'               SKIP TO CHANNEL 9 AND WRITE CC
ASK1     L     R1,OTWY                  GET 120
         ST    R1,INLSF                 SET LENGTH OF INPUT
         LA    R1,FANFOLD               GET ADDR OF ASTERISKS
         ST    R1,INLOC                 SET INPUT ADDR
         OI    SORS1,OLDSW              TURN ON OLD SWITCH
         ST    R13,SAVER132             SAVE R13
         LA    R13,SAVEPUT              GET ADDR OF SAVE AREA
         STM   14,12,12(13)             SAVE REG.S
         L     R13,SAVER132             RESTORE R13
         L     R15,SD89EXIT             GET ADDR OF PUT ROUTINE
         BALR  R14,R15                  GO TO PUT OUT ASTERISKS
         LA    R13,SAVEPUT              GET ADDR OF SAVE AREA
         LM    14,12,12(13)             RESTORE REG.S
         L     R13,SAVER132             RESTORE R13
*IS THERE AN UNCORRECTABLE ERROR
         L     R10,WOSAP                GET ADDR ERROR RETURN PUT AT
         TM    19(R10),X'FF'            TEST FOR ERROR IF SO
         BNE   UNREIOER                 BRANCH TO FREE WORK AREAS
*IS PERMANENT IO ERROR SWITCH ON
         TM    SORS1,INERS              TEST FOR IO ERROR IF SO
         BO    SD94EOFX                 BRANCH TO END OF FILE
         TM    CONCH,C'+'               HAS LINE OF ASTERISKS BEEN
*                                       OVER PRINTED   IF SO
         BO    SD94EOFX                 BRANCH TO END OF FILE
         MVI   CONCH,C'+'               NO SKIP AND WRITE CC
         B     ASK1                     GO BACK AND OVER PRINT LINE
*                                       OF ASTERISKS
*DO PUNCH TYPE
PCHRT    L     R2,ONE              SET R2 TO 1
         LR    R3,R2               SET R3 TO 1
BACK1    MVI   OUTAREAS,C' '       MOVE BLANKS INTO OUTAREAS
         MVC   OUTAREAS+1(79),OUTAREAS
         L     R1,8(R5)                 GET ADDRESS OF JOBNAME
         MVC   OUTAREAS+35(8),0(R1)     MOVE IN JOBNAME
         L     R1,12(R5)                GET ADDRESS OF CLASS
         MVC   OUTAREAS+45(1),0(R1)     MOVE IN CLASS NAME
         MVI   CONCH,C'V'               MOVE IN CC (STACKER 1)
         LA    R1,OUTAREAS              GET INPUT ADDRESS
         ST    R1,INLOC                 SET ADDR OF INPUT
         OI    SORS1,OLDSW              TURN ON OLD SWITCH
         L     R1,ETY                   GET LENGTH OF INPUT
         ST    R1,INLSF                 SET LENGTH OF INPUT
         ST    R13,SAVER134
         LA    R13,SAVEPUT2
         STM   14,12,12(13)
         L     R13,SAVER134
         L     R15,SD89EXIT             GET ADDR FOR EXIT
         BALR  R14,R15
         LA    R13,SAVEPUT2
         LM    14,12,12(13)
         L     R13,SAVER134
*IS THERE AN UNRECOVERABLE ERROR
         L     R10,WOSAP                GET ADDR ERROR RETURN PUT AT
         TM    19(R10),X'FF'            TEST FOR ERROR IF SO
         BNE   UNREIOER                 BRANCH TO FREE WORK AREAS
*IS PERMANENT INPUT ERROR SWITCH ON
         TM    SORS1,INERS              TEST FOR ERROR IF NOT
         BZ    CONT1                    BRANCH TO CONT1
         B     SD94EOFX                 IF SO BRANCH TO EOF
CONT1    AR    R3,R2                    R3=R3+1
         C     R3,FOR                   IF R3 IS NOT EQUAL TO 4
         BNE   BACK1                    BRANCH TO BACK1
         B     SD94EOFX
*FREE WORK AREAS
UNREIOER L     R13,SRETR                GET RETURN REGISTER
         LA    R0,WKSOE-WKSOR+OUTAREND-OUTAREAS+WORKEND-WORKLST  A32649
         FREEMAIN R,LV=(0),A=(8)  FREE WORK AREAS                A32649
         DROP  R8,R6,R7                 WKSOR OUTAREAS WORKLST   A64011
*INDICATE NO SYNAD ROUTINE
         L     R1,JSDCBOUT              GET OUTPUT DCB ADDRESS   A64011
         USING IHADCB,R1                DCB ADDRESSABILITY       A41000
         MVC   DCBSYNAD+1(3),ONE+1      PUT ONE IN SYNAD ADDRESS A41000
*SAVE RETURN INDICATION
         RETURN (14,12)                 RETURN TO IEF078
*CONSTANTS
         DS    0F
ONE      DC    F'1'
OTWY     DC    F'120'
THIR     DC    F'13'
SEVN     DC    F'7'
EIGHT    DC    F'8'
ETY      DC    F'80'
FOR      DC    F'4'
BTWLV    DC    X'0C000000'              TO INDICATE MESSAGE INPUT  VS0H
SD89EXIT DC    V(IEFSD089)              OUTPUT ROUTINE
SD88EXIT DC    V(IEFSD088)              TRANSITION ROUTINE
FANFOLD  DC    120C'*'
         DS    0F                                                A27785
         DC    C'SUPERZAP'
         DC    C'SUPERZAP'
         DC    C'SUPERZAP'
         DC    C'SUPERZAP'
         DC    C'SUPERZAP'
         DC    C'SUPERZAP'
         DC    C'SUPERZAP'
         END
