IEFSDTTE CSECT
         TITLE 'IEFSDTTE - 3211 PRINTER SUPPORT MODULE'
***********************************************************************
*1320                                                            A53806
*0000                                                            M5709
*0000                                                            M5878
*0000                                                            M5882
*1320                                                            A42459
*                                                                     *
*TITLE-- IEFSDTTE - 3211 PRINTER SUPPORT MODULE                       *
*                                                                     *
* STATUS CHANGE LEVEL 001
*                                                                     *
*FUNCTION/OPERATION-- THIS ROUTINE ANALYZES THE USER REQUESTS FOR     *
*   UCS/FCB ENVIRONMENTS AND DYNAMICALLY CAUSES LOADING OF THE 3211   *
*   PRINTER UNIVERSAL CHARACTER SET BUFFER AND THE FORMS CONTROL      *
*   BUFFER FOR EACH OUTPUT DATA SET.                                  *
*                                                                     *
*ENTRY POINT-- IEFSDTTE - FROM THE STANDARD SYSTEM OUTPUT WRITER IN   *
*   IEFSD087 PRIOR TO OPENING EACH INPUT DATA SET DESTINED FOR OUTPUT *
*   TO THE 3211 PRINTER.                                              *
*   FROM THE SMB PROCESSOR IN IEFSD086 PRIOR TO THE WRITING OF EACH   *
*   GROUP OF SMBS TO:                                                 *
*        1. FORCE BLOCKING OF DATA CHECKS                             *
*        2. PROVIDE UCS/FCB IMAGE-IDS WHICH ARE DEFAULTS              *
*   FROM THE LINKOR ROUTINE IN IEFSD078 PRIOR TO PROCESSING           *
*   JOB SEPERATORS TO.                                                *
*        1. FORCE BLOCKING OF DATA CHECKS                             *
*        2. PROVIDE UCS/FCB IMAGE-IDS WHICH ARE DEFAULTS              *
*                                                                     *
*INPUT-- REGISTER 7 POINTS TO THE WORK LIST USED BY DSD AND SMB       *
*        ROUTINE.                                                     *
*        REGISTER 8 POINTS TO THE WORK AREA FOR STANDARD OUTPUT       *
*        ROUTINE.                                                     *
*                                                                     *
*OUTPUT-- ON RETURN, REGISTER 15 WILL CONTAIN A RETURN                *
*   CODE INDICATING UCS/FCB ENVIRONMENTAL CONDITIONS.                 *
*        REGISTER 15 RETURN CODE=00 - SUCCESSFUL COMPLETION.          *
*        REGISTER 15 RETURN CODE<0C AND                               *
*             SWITCH SORS1=INERS - INPUT ERROR.                       *
*        REGISTER 15 RETURN CODE>08 - OUTPUT ERROR.                   *
*                                                                     *
*EXTERNAL ROUTINES--                                                  *
*        IEFSD089 TO OUTPUT ERROR MESSAGE                             *
*                                                                     *
*EXIT-NORMAL-- RETURNS TO THE INSTRUCTION FOLLOWING THE ONE WHICH     *
*   LINKED TO THIS ROUTINE TO RESUME NORMAL WRITER PROCESSING.        *
*                                                                     *
*EXIT-ERROR-- RETURNS TO FREE WORKFILES AND ON                        *
*   INPUT ERRORS-- SKIP INPUT DATA SET.                               *
*   OUTPUT ERRORS-- REENQUEUE DATA SET AND TERMINATE WRITER.          *
*                                                                     *
*TABLES/WORK AREAS-- THE WORKLST AND WKSOR WRITER COMMUNICATION LISTS,*
*   THE PARLIST COMMUNICATION REGION,                                 *
*   THE OUTPUT DCB, THE INPUT JFCB, AND THE OUTPUT UCB.               *
*                                                                     *
*ATTRIBUTES-- THIS ROUTINE IS REENTRANT AND REFRESHABLE.              *
*                                                                     *
*NOTES-- THE SETPRT MACRO IS USED TO REQUEST DATA MANAGEMENT LOADING  *
*   OF THE UCS/FCB BUFFERS.                                           *
*                                                                     *
***********************************************************************
*                                                                     *
         EJECT
*DEFINE DCB
         DCBD  DSORG=PS
*DEFINE JFCB
IEFJFCBN DSECT
         IEFJFCBN
*DEFINE UCB
IEFUCBOB DSECT
         IEFUCBOB
*DEFINE PARAMETER LIST
         IEFSDPAR
*DEFINE SETPRT LIST
         IEFSDSET
*DEFINE WKSOR WORK AREA
         IEFSDSDR
*DEFINE WORKLST
         IEFSDWKT
         EJECT
*                                                                     *
***********************************************************************
*                                                                     *
*        ESSENTIAL EQUATES
***********************************************************************
ZERO     EQU   0                        LENGTH OF ZERO
ONE      EQU   1                        LENGTH OF ONE
XZERO    EQU   X'00'                    ZERO FCB RETURN CODE
TWO      EQU   2                        LENGTH OF TWO
THREE    EQU   3                        CONSTANT OF THREE
FOUR     EQU   4                        LENGTH OF FOUR
NINETEEN EQU   19                       CONSTANT OF 19
ONEBYTE  EQU   8                        LENGTH OF 1-BYTE SHIFT
TWOBYTES EQU   16                       LENGTH OF 2-BYTE SHIFT
THREEBYT EQU   24                       LENGTH OF 3-BYTE SHIFT
INERR    EQU   4                        INPUT ERR RET CODE TO 087
OUTERR   EQU   8                        OUTPUT ERR RET CODE TO 087
DDDOFSET EQU   16                            WTR DEVICE ADDR OFFSET
RCOFFSET EQU   19                       RET CODE IN GR15 SAVE AREA
SDTTMLGN EQU   102                      WTO MAX MESSAGE LENGTH
SDTTMLG1 EQU   78                       WTO MESSAGE LENGTH
FCBTST   EQU   X'80'                    FCB TEST IN PROCESS INDICATOR
ASAS1    EQU   C' '                     ASA CNTRL CHAR SPACE 1
MCCS1    EQU   X'09'                    MACHINE CNTRL CHAR SPACE 1
MACSW    EQU   2                        MACHINE CNTRL CHAR
RCHIGH   EQU   X'1C'                    HIGHEST VALID RETURN CODE
FLAGF    EQU   X'FF'                    ONE BYTE FLAG OF F
ENTSW    EQU   FLAGF-PARTTE-PARTTE1     USED TO TURN OFF ENTRYSW M5878
DCBLK    EQU   X'80'                    DATA CHECK BLOCK REQUEST
DCUNBLK  EQU   X'40'                    DATA CHECK UNBLOCK REQUEST
DCFOLD   EQU   X'04'                    DATA CHECK FOLD REQUEST
DCUNFOLD EQU   X'08'                    DATA CHECK UNFOLD REQUEST
DCBUNBLK EQU   X'40'                    DATA CHECKS UNBLOCKED
DEBUCBAD EQU   32                       UCB ADDR OFFSET IN DEB
FCBVERFY EQU   X'80'                    FCB VERIFY REQUEST
FCBALIGN EQU   X'01'                    FCB ALIGN REQUEST
JFCBFVR  EQU   X'04'                    JFCB FCB VERIFY REQUEST
JFCBFAL  EQU   X'08'                    JFCB FCB ALIGN REQUEST
JFCBUVR  EQU   X'10'                    JFCB UCS VERIFY REQUEST
JFCBUF   EQU   X'40'                    JFCB UCS FOLD REQUEST
JFCBUBLK EQU   X'40'                    JFCB UNBLOCK (ALLOW DATA CHECK)
LMFOLD   EQU   X'40'                    LOAD MODE FOLD REQUEST
LMUNFOLD EQU   X'00'                    LOAD MODE UNFOLD
UCBFDF   EQU   X'80'                    UCB FCB ID IS A DEFAULT ID
UCBUDF   EQU   X'80'                    UCB UCS ID IS A DEFAULT ID
UCBUPE   EQU   X'01'                    UCB UCS PARITY ERROR
UCBUF    EQU   X'40'                    UCB UCS FOLD MODE
EIGHT    EQU   8                       LENGTH OF EIGHT           M5709
SVSIZE   EQU   72                       SIZE OF SAVE AREA        M5709
UCSVERFY EQU   X'10'                    UCS VERIFY REQUEST
*
***********************************************************************
*
*        REGISTER EQUATES
***********************************************************************
*
PR0      EQU   0                        PARAMETER REGISTER 0
PR1      EQU   1                        PARAMETER REGISTER 1
MSGRET   EQU   2                        ERROR MSG RETURN REG
PARLISTR EQU   3                        PARLIST BASE
JFCBR    EQU   4                        INPUT JFCB BASE
UCBR     EQU   5                        OUTPUT UCB PRT EXTENSION BASE
DCBR     EQU   6                        OUTPUT DCB BASE
WORKLSTR EQU   7                        WORKLIST BASE
WKSOREG  EQU   8                        WKSOR BASE
BASER    EQU   9                        BASE REGISTER
SETPR    EQU   10                       SETPRT LIST BASE
MSGSTRTR EQU   11                       ERROR MSG STARTING ADDRESS
BRTABR   EQU   11                       BRANCH TABLE BASE REGISTER
MSGLGNR  EQU   12                       ERROR MSG LENGTH
WORKR    EQU   12                       WORK REGISTER
RCREG    EQU   12                       RETURN CODE PROCESSING REG
SAVER    EQU   13                       SAVE AREA POINTER
RETR     EQU   14                       RETURN REGISTER
BRANCHR  EQU   15                       MSG WTR BRANCH REGISTER
         EJECT
*                                                                     *
***********************************************************************
*                                                                     *
*        THE 3211 PRINTER PROCESSOR MODULE                            *
*                                                                     *
***********************************************************************
*                                                                     *
IEFSDTTE CSECT
         SPACE
*INITIALIZE REGISTERS
         SAVE  (14,12)                  SAVE REGISTERS
         BALR  BASER,0                  ESTABLISH
         USING *,BASER                    CSECT BASE
         LR    PARLISTR,PR1             SAVE CONTENTS OF REG 1   M5709
         LA    PR0,SVSIZE               LOAD SIZE OF SAVE AREA   M5709
         GETMAIN R,LV=(0)               GET CORE FOR SAVE AREA   M5709
         ST    SAVER,FOUR(PR1)          BUILD SAVE AREA CHAIN    M5709
         ST    PR1,EIGHT(SAVER)                                  M5709
         LR    SAVER,PR1                POINTER TO NEW SAVE AREA M5709
         LR    PR1,PARLISTR             RESTORE REG 1            M5709
         USING WORKLST,WORKLSTR           WORK LIST BASE
         L     PARLISTR,WOSPS           GET ADDR OF PARLIST
         USING PARLIST,PARLISTR         ESTABLISH ADDRESSIBILITY
         USING WKSOR,WKSOREG              WKSOR BASE
         USING INFMJFCB,JFCBR             INPUT JFCB BASE
         L     DCBR,DCBOT               ESTABLISH
         USING IHADCB,DCBR                OUTPUT DCB BASE
         L     UCBR,PARUCB              GET OUTPUT UCB ADDRESS
         USING UCBOB,UCBR                 OUTPUT UCB BASE
         L     UCBR,UCBXTADR            SHIFT TO UCB/UCS EXTENSIONTTE0K
         DROP  UCBR                                              A42459
         USING UCBUCS,UCBR              BASE FOR UCS EXTENSION   A42459
         SPACE
*GET CORE FOR DYNAMICALLY CHANGED SETPRT LIST
         LR    WORKR,PR1          COPY ADDR OF 089 PASSED BY     TEST
*                                         086 OR 087             TEST
         LA    PR0,184+SPLEND-SPLIST   GET SIZE OF SETPRT LIST   TEST
         GETMAIN R,LV=(0)               GET CORE FOR LIST
         LR    SETPR,PR1                ESTABLISH
         USING SPLIST,SETPR               SETPRT LIST BASE
*INITIALIZE GOTTEN CORE
         XC    SPLIST(SPLLGN),SPLIST   CLEAR SETPRT LIST AREA    TEST
         MVC   0(SPLLGN-8,SETPR),SETPRTL                         TEST
*                                 COPY SETPRT LIST TO GOT CORE   TEST
         ST    WORKR,SPLOP        SAVE ADDR OF 089 IN GOT CORE   TEST
         OI    SPLDACK,DCBLK            DEFAULT TO BLOCK DATA CHECKS
         OI    SPLDACK,DCUNFOLD         DEFAULT TO UNFOLD IMAGE
         MVC   SPLDCBAD(FOUR),DCBOT     GET OUTPUT DCB ADDR FOR SETPRT
         TM    PARSWCH,PARTTE+PARTTE1   IS ENTRY FROM 086 OR 078
         BZ    FCBID1                   NO, ENTRY FROM IEFSD087
         SPACE
*
***********************************************************************
*
         TM    UCBUCSOP,UCBUDF          PRESENT UCS IMAGE DEFAULT
         BO    DEFAULT3                 YES, DONT CHANGE IT
         CLC   UCBUCSID(FOUR),PARUCSID  DEFAULT IMAGE ALREADY LOADED
         BE    DEFAULT3                 YES, DONT RELOAD IT
         MVC   SPLUCSID(FOUR),PARUCSID  PLACE DEFAULT UCS IMAGE-ID
*                                         SAVED IN PARLIST INTO SETPRT
DEFAULT3 EQU   *
         TM    UCBFCBOP,UCBFDF          PRESENT FCB IMAGE DEFAULT
         BO    DEFAULT4                 YES, DONT CHANGE IT
         CLC   UCBFCBID(FOUR),PARFCBID  DEFAULT IMAGE ALREADY LOADED
         BE    DEFAULT4                 YES, DONT RELOAD IT
         MVC   SPLFCBID(FOUR),PARFCBID  PLACE DEFAULT FCB IMAGE-ID
*                                         SAVED IN PARLIST INTO SETPRT
DEFAULT4 EQU   *
         TM    DCBOPTCD,DCBUNBLK        DATA CHECKS PRESENTLY UNBLOCKED
         BZ    MAC1                     NO, ISSUE SETPRT FOR IMAGE-IDS
         NI    SPLDACK,FLAGF-DCUNBLK    TURN OFF UNBLOCK DATA CK BIT
         OI    SPLDACK,DCBLK            PLACE BLOCK REQ IN SETPRT LIST
         B     MAC1                     ISSUE SETPRT MACRO
*
***********************************************************************
         EJECT
*ENVIRONMENTAL ANALYSIS
*                                                                     *
***********************************************************************
*                                                                     *
*   THE INPUT JFCB CONTAINS THE USER REQUESTS FOR THE ENVIRONMENT     *
*   REQUIRED FOR EACH OUTPUT DATA SET AS DESCRIBED ON THE USERS OUTPUT*
*   DD CARDS.  THE OUTPUT UCB CONTAINS THE PRESENT 3211 OUTPUT UCS/FCB*
*   DESCRIPTION.  THIS CODE CHECKS THE JFCB FOR REQUESTS WHICH DIFFER *
*   FROM THE PRESENT UCB ENTRIES.  THOSE THAT DIFFER CONSTITUTE CHANGE*
*   REQUESTS VIA THE SETPRT MACRO TO ESTABLISH A NEW ENVIRONMENT FOR  *
*   THE NEXT OUTPUT DATA SET.                                         *
*                                                                     *
***********************************************************************
*                                                                     *
         SPACE
FCBID1   EQU   *
         L     JFCBR,WJFCB              LOAD JFCB POINTER        M5882
         CLC   JFCFCBID(FOUR),INVALADR  FCB IMAGE-ID SPECIFIED
         BNE   FCBID1P                  YES, COMPARE WITH PRESENT ID
         TM    UCBFCBOP,UCBFDF          PRESENT IMAGE-ID A DEFAULT
         BZ    DEFAULT1                 NO, USE DEFAULT PARAMETERS
FCBVER1  EQU   *
         TM    JFCUCSOP,JFCBFVR         FCB IMAGE-ID VERIFY REQUEST
         BO    FCBVERM                  YES, OUTPUT NO IMAGE MSG.
FCBALN1  EQU   *
         TM    JFCUCSOP,JFCBFAL         FCB IMAGE-ID ALIGNMENT REQUEST
         BO    FCBALNM                  YES, OUTPUT NO IMAGE MSG.
UCSID1   EQU   *
         CLC   JFCUCSID(FOUR),INVALADR  UCS IMAGE-ID SPECIFIED
         BNE   UCSID1P                  YES, COMPARE WITH PRESENT ID
         TM    UCBUCSOP,UCBUPE          RELOAD REQUEST FROM OPEN
         BO    RELOAD                   YES, PREPARE TO RELOAD
         TM    UCBUCSOP,UCBUDF          PRESENT IMAGE-ID A DEFAULT
         BZ    DEFAULT2                 NO, USE DEFAULT PARAMETERS
UCSVER1  EQU   *
         TM    JFCUCSOP,JFCBUVR         UCS IMAGE-ID VERIFY REQUEST
         BO    UCSVER1P                 YES, OUTPUT NO IMAGE MSG.
FOLD1    EQU   *
         TM    JFCUCSOP,JFCBUF          UCS FOLD MODE REQUEST
         BO    FOLD1P                   YES, PREPARE TO FOLD
UNBLK1   EQU   *
         TM    JFCOPTCD,JFCBUBLK        ALLOW DATA CK.-UNBLOCK REQUEST
         BO    UNBLKREQ                 YES, PREPARE TO UNBLOCK
*                                         ELSE DEFAULT TO BLOCK
***********************************************************************
*
LISTCLR1 EQU   *
         LA    PR0,184+SPLEND-SPLIST   GET SIZE OF SETPRT LIST   TEST
         LR    PR1,SETPR                GET ADDR OF AREA TO FREE
         FREEMAIN R,LV=(0),A=(1)        FREE SETPRT LIST CORE
*LEAVE THIS MODULE
WRAPUP   EQU   *
         NI    PARSWCH,ENTSW            TURN OFF ENTRY SW 78/86  M5878
         LR    PR1,SAVER                AREA TO FREE             M5709
         L     SAVER,FOUR(PR1)          REST SAVE AREA POINTER   M5709
         LA    PR0,SVSIZE               SIZE OF CORE TO FREE     M5709
         FREEMAIN  R,LV=(0),A=(1)       FREE SAVE AREA CORE      M5709
         RETURN  (14,12)
*                                         RETURN TO CALLER
*
***********************************************************************
*
         EJECT
*COMPARE NEW JFCB REQUEST WITH PRESENT UCB
FCBID1P  EQU   *
         OC    JFCFCBID(FOUR),BLANKS    BLANK OUT TRAILING ZEROES
         CLC   JFCFCBID(FOUR),UCBFCBID  JFCB AND UCB ID EQUAL
         BE    FCBVER2                  YES, IGNORE CHANGE REQUEST
         MVC   SPLFCBID(FOUR),JFCFCBID  PLACE NEW FCB-ID REQ. IN SETPRT
FCBVER2  EQU   *
         TM    JFCUCSOP,JFCBFVR         FCB IMAGE-ID VERIFY REQUEST
         BZ    FCBALN2                  NO, SKIP REQUEST
         OI    SPLFCBVA,FCBVERFY        PLACE NEW FCB VERIFY REQ. IN
*                                         SETPRT LIST
         B     UCSID2                   GO TO CHECK UCS ID REQUEST
FCBALN2  EQU   *
         TM    JFCUCSOP,JFCBFAL         FCB IMAGE-ID ALIGNMENT REQUEST
         BZ    UCSID2                   NO, GO TO CHECK UCS ID REQUEST
FCBALP   EQU   *
         OI    SPLFCBVA,FCBALIGN        PLACE NEW FCB ALIGN REQUEST
*                                         IN SETPRT LIST
UCSID2   EQU   *
         CLC   JFCUCSID(FOUR),INVALADR  UCS IMAGE-ID SPECIFIED
         BNE   UCSID1P                  YES, COMPARE WITH PRESENT ID
         TM    UCBUCSOP,UCBUPE          RELOAD REQUEST FROM OPEN
         BO    RELOAD                   YES, PREPARE TO RELOAD
         TM    UCBUCSOP,UCBUDF          PRESENT IMAGE-ID A DEFAULT
         BZ    DEFAULT2                 NO, USE DEFAULT PARAMETERS
UCSVER3  EQU   *
         TM    JFCUCSOP,JFCBUVR         UCS IMAGE-ID VERIFY REQUEST
         BO    UCSVER3P                 YES, OUTPUT NO IMAGE MSG.
FOLD3    EQU   *
         TM    JFCUCSOP,JFCBUF          UCS FOLD MODE REQUEST
         BO    FOLD1P                   YES, PREPARE TO FOLD
UNBLK2   EQU   *
         TM    JFCOPTCD,JFCBUBLK        ALLOW DATA CK.-UNBLOCK REQUEST
         BZ    MAC1                     NO, ISSUE SETPRT MACRO
UNBLKREQ EQU   *
         NI    SPLDACK,FLAGF-DCBLK      TURN OFF BLOCK DATA CK BIT
         OI    SPLDACK,DCUNBLK          PLACE UNBLOCK REQ IN SETPRT
         SPACE
MAC1     EQU   *
         LR    PR1,SETPR                GET ADDR OF SETPRT LIST
         SETPRT (DCBR),OPTCD=B,MF=(E,(PR1))
         SPACE
***********************************************************************
*
*TEST RETURN CODES FROM SETPRT MACRO
         LTR   BRANCHR,BRANCHR          SUCCESSFUL SETPRT EXEC
         BZ    LISTCLR                  YES, CLEAR SETPRT LIST
*PICK UP FIRST FCB THEN UCS RETURN CODE
         LR    JFCBR,BRANCHR            SAVE RETURN CODE         A53806
         L     UCBR,PARUCB             RESTORE POINTER TO UCB
         OI    SPLOP,FCBTST             TURN ON FCB TEST ENTRY SWITCH
RCTST    EQU   *
         LA    BRTABR,BRTABLE           GET ADDR OF BRANCH TABLE
         TM    PARSWCH,PARTTE1          IS ENTRY FROM 078
         BZ    RCTST1                  NO, CONTINUE
         LA    BRTABR,BRTABLE1          YES, ADVANCE TO SECOND BRANCH
*                                         TABLE
RCTST1   EQU   *
         LR    RCREG,BRANCHR            COPY FCB RETURN CODE
         SLL   RCREG,TWOBYTES           CLEAR TWO HIGH BYTES
         TM    SPLOP,FCBTST             FCB RET CODE TEST IN PROCESS
         BO    TESTGO                   YES, BYPASS UCS PROCESS
         SLL   RCREG,ONEBYTE            CLEAR EXTRA BYTE W/FCB CODE
TESTGO   EQU   *
         SRL   RCREG,THREEBYT           RIGHT JUSTIFY RET CODE
         LA    PR1,RCHIGH               HIGHEST VALID RETURN CODE
         CR    RCREG,PR1                IS RET CODE > 1C
         BNH   TESTGO1                  NO, ANALIZE IT
         B     WTO1                     YES, OUTPUT MESSAGE IEF327I
TESTGO1  EQU   *
         AR    BRTABR,RCREG             INCREMENT INTO BRTABLE
         BR    BRTABR                     TO ACT ON RETURN CODE
*RETURN CODE DETERMINES BRANCH TABLE ENTRY
*TABLE IF ENTRY IS FROM 086 OR 087
BRTABLE  EQU   *
*                                  RETURN CODE =
         B     LISTCLR             00 - SUCCESSFUL
         B     IDNAM               04 - INPUT ERROR MSG WRITE  IEF325I
         B     IDBLEM              08 - INPUT ERROR MSG WRITE  IEF326I
         B     WTO1                0C - OUTPUT ERROR MSG WRITE IEF327I
         B     WTO2                10 - OUTPUT ERROR MSG WRITE IEF328I
         B     WTO3                14 - OUTPUT ERROR MSG WRITE IEF329I
         B     PLIM                18 - INPUT ERROR MSG WRITE  IEF311I
         B     WTO4                1C - OUTPUT ERROR MSG WRITE IEF331I
*
*TABLE IF ENTRY IS FROM 078
BRTABLE1 EQU   *
*                                  RETURN CODE =
         B     LISTCLR             00 - SUCCESSFUL
         B     WTO5                04 - INPUT ERROR MSG WRITE  IEF325I
         B     WTO6                08 - INPUT ERROR MSG WRITE  IEF326I
         B     WTO1                0C - OUTPUT ERROR MSG WRITE IEF327I
         B     WTO2                10 - OUTPUT ERROR MSG WRITE IEF328I
         B     WTO3                14 - OUTPUT ERROR MSG WRITE IEF329I
         B     WTO7                18 - INPUT ERROR MSG WRITE  IEF311I
         B     WTO4                1C - OUTPUT ERROR MSG WRITE IEF331I
***********************************************************************
         SPACE
UCSID1P  EQU   *
         OC    JFCUCSID(FOUR),BLANKS    BLANK OUT TRAILING ZEROES
         CLC   JFCUCSID(FOUR),UCBUCSID  JFCB AND UCB ID EQUAL
         BE    UCSVER2                  YES, IGNORE CHANGE REQUEST
         MVC   SPLUCSID(FOUR),JFCUCSID  PLACE NEW UCS-ID REQ. IN SETPRT
UCSVER2  EQU   *
         TM    JFCUCSOP,JFCBUVR         UCS IMAGE ID VERIFY REQUEST
         BZ    FOLD2                    NO, SKIP REQUEST
UCSVER2P EQU   *
         OI    SPLUCSV,JFCBUVR          PLACE NEW UCS VERIFY REQ. IN
*                                         SETPRT LIST
FOLD2    EQU   *
         TM    JFCUCSOP,JFCBUF          UCS FOLD MODE REQUEST
         BZ    UNBLK2                   NO, TEST FOR UNBLOCK REQUEST
FOLD2P   EQU   *
         OI    SPLLMO,JFCBUF            PLACE FOLD REQ. IN SETPRT LIST
         B     UNBLK2                   CONTINUE WITH UNBLOCK TEST
FOLD1P   EQU   *
         OI    SPLDACK,JFCBUF           PLACE FOLD REQ. IN SETPRT LIST
         B     UNBLK2                   CONTINUE WITH UNBLOCK TEST
RELOAD   EQU   *
         MVC   SPLUCSID(FOUR),UCBUCSID  PLACE UCS IMAGE-ID PRESENTLY
*                                         IN UCB INTO SETPRT LIST
         B     UCSVER2                  CONTINUE WITH VERIFY TEST
DEFAULT1 EQU   *
         CLC   UCBFCBID(FOUR),PARFCBID  DEFAULT IMAGE ALREADY LOADED
         BE    FCBVER2                  YES, DONT RELOAD IN
         MVC   SPLFCBID(FOUR),PARFCBID  PLACE DEFAULT FCB IMAGE-ID
*                                         SAVED IN PARLIST INTO SETPRT
         B     FCBVER2                  CONTINUE WITH VERIFY TEST
DEFAULT2 EQU   *
         CLC   UCBUCSID(FOUR),PARUCSID  DEFAULT IMAGE ALREADY LOADED
         BE    UCSVER2                  YES, DONT RELOAD IN
         MVC   SPLUCSID(FOUR),PARUCSID  PLACE DEFAULT UCS IMAGE-ID
*                                         SAVED IN PARLIST INTO SETPRT
         B     UCSVER2                  CONTINUE WITH VERIFY TEST
*
***********************************************************************
*
FCBVERM  EQU   *
         LA    MSGRET,FCBALN1           LD REG FOR RET AFTER MSG WRT
         LA    MSGSTRTR,MSFCBV          GET START OF MESSAGE IEF322I
         LA    MSGLGNR,MSFCBVE-MSFCBV   GET LENGTH OF MESSAGE
ERRMSGOT EQU   *
*PUT IN ERROR MESSAGE CONTROL CHARACTER
         MVI   CONCH,ASAS1              INIT TO ASA SPACE 1 CNTRL CHAR
         TM    DCBRECFM,MACSW           MACH CNTRL CHAR SPECIFIED
         BZ    OUTMSG                   NO, OUTPUT MSG
         MVI   CONCH,MCCS1              GET MACH SPACE 1 CNTRL CHAR
*TURN ON OLD SWITCH
OUTMSG   EQU   *
         OI    SORS1,OLDSW
         L     PR1,WOSAP                GET ADDR ERR RET PUT AT
         CLI   RCOFFSET(PR1),OUTERR     IS PRESENT RC OUTPUT ERROR
         BNL   OUTMSG1                  YES, LEAVE CODE TO CAUSE TERM
         MVI   RCOFFSET(PR1),INERR      INDICATE I/O INPUT ERROR
         OI    SORS1,INERS              TURN ON INPUT I/O ERR SW
OUTMSG1  EQU   *
*OUTPUT MESSAGE
         STM   MSGSTRTR,MSGLGNR,INLSF   PASS MSG START AND LENGTH
         LR    RCREG,BRANCHR            COPY AND SAVE RETURN CODE
         L     BRANCHR,SPLOP            GET ADDR OF 089 PUT ROUTINE
         LA    BRANCHR,ZERO(BRANCHR)    CLEAR HI BYTE FLAG
         BALR  RETR,BRANCHR             OUTPUT MESSAGE
         LR    BRANCHR,RCREG            RESTORE RETURN CODE
         BR    MSGRET                   RETURN TO NEXT OPERATION
UCSVER1P EQU   *
         LA    MSGRET,FOLD1             LD REG FOR RET AFTER MSG WRT
UCSVER1X EQU   *
         LA    MSGSTRTR,MSUCSV          GET START OF MESSAGE IEF324I
         LA    MSGLGNR,MSUCSVE-MSUCSV   GET LENGTH OF MESSAGE
         B     ERRMSGOT                 OUTPUT ERROR MSG
UCSVER3P EQU   *
         LA    MSGRET,FOLD3             LD REG FOR RET AFTER MSG WRT
         B     UCSVER1X                 PICK UP MESSAGE LGN AND START
FCBALNM  EQU   *
         LA    MSGRET,UCSID1            LD REG FOR RET AFTER MSG WRT
         LA    MSGSTRTR,MSFCBAL         GET START OF MESSAGE IEF323I
         LA    MSGLGNR,MSFCBALE-MSFCBAL GET LENGTH OF MESSAGE
         B     ERRMSGOT                 OUTPUT ERROR MESSAGE
IDNAM    EQU   *
         LA    MSGSTRTR,MSIDNA          GET START OF MESSAGE IEF325I
         LA    MSGLGNR,MSIDNAE-MSIDNA   GET LENGTH OF MESSAGE
         LA    MSGRET,LISTCLR           LOAD REG FOR RET AFT MSG WRT
         B     ERRMSGOT                 OUTPUT ERROR MESSAGE
IDBLEM   EQU   *
         LA    MSGSTRTR,MSBLE           GET START OF MESSAGE IEF326I
         LA    MSGLGNR,MSBLEE-MSBLE     GET LENGTH OF MESSAGE
         LA    MSGRET,LISTCLR           LOAD REG FOR RET AFT MSG WRT
         B     ERRMSGOT                 OUTPUT ERROR MESSAGE
PLIM     EQU   *
         LA    MSGSTRTR,MSPLI           GET START OF MESSAGE IEF311I
         LA    MSGLGNR,MSPLIE-MSPLI     GET LENGTH OF MESSAGE
         LA    MSGRET,LISTCLR           LOAD REG FOR RET AFT MSG WRT
         B     ERRMSGOT                 OUTPUT ERROR MESSAGE
         SPACE
***********************************************************************
         SPACE
*A WTO IS ISSUED WHEN THE SYSOUT PRINTER CANNOT BE USED
WTO1     EQU   *
         LA    MSGSTRTR,SDTTM1          GET START OF MESSAGE IEF327I
         B     WTOUT                    OUTPUT MSG
WTO2     EQU   *
         LA    MSGSTRTR,SDTTM2          GET START OF MESSAGE IEF328I
         B     WTOUT                    OUTPUT MSG
WTO3     EQU   *
         LA    MSGSTRTR,SDTTM3          GET START OF MESSAGE IEF329I
         B     WTOUT                    OUTPUT MSG
WTO4     EQU   *
         LA    MSGSTRTR,SDTTM4          GET START OF MESSAGE IEF331I
WTOUT    EQU   *
         LA    PR0,SDTTMLGN             GET LENGTH OF MESSAGE
         GETMAIN R,LV=(0)               GETCORE FOR MESSAGE BUILD
         MVC   0(SDTTMLGN,PR1),0(MSGSTRTR)   MOVE IN MESSAGE LIST
         DROP  UCBR                                              A42459
         USING UCBOB,UCBR               BASE FOR UCB             A42459
         MVC   DDDOFSET(THREE,PR1),UCBNAME   PUT IN WTR NAME
         LR    MSGSTRTR,PR1             SAVE ADDR OF GOTTEN CORE
         WTO   MF=(E,(1))               WRITE MSG TO CONSOLE
         LA    PR0,SDTTMLGN             GET LENGTH OF AREA TO FREE
         LR    PR1,MSGSTRTR             GET ADDR OF AREA TO FREE
         FREEMAIN R,LV=(0),A=(1)        FREE MSG BUILD CORE
         L     WORKR,WOSAP              GET ADDR ERR RET PUT AT
         MVI   NINETEEN(WORKR),OUTERR   IND I/O OUTPUT ERROR
LISTCLR  EQU   *
         TM    SPLOP,FCBTST             RET CODE TESTS COMPLETED
         BZ    HICODE                   YES, RETURN HI ERROR CODE
         NI    SPLOP,FLAGF-FCBTST       NO, SET UP TO DO
         LR    BRANCHR,JFCBR            RESTORE RETURN CODE      A53806
         B     RCTST                      UCS RETURN CODE TEST
*RETURN HIGHER OF UCS AND FCB RETURN CODES TO CALLER
HICODE   ST    BRANCHR,SPLWORK          STORE RETURN CODES
         OC    SPLUCS,SPLFCB            COMBINE RETURN CODES
         NI    SPLFCB,XZERO             SET FCB RET CODE TO ZERO
         L     BRANCHR,SPLWORK          RELOAD SINGLE RETURN CODE
         B     LISTCLR1                 RETURN TO LEAVE MODULE   M5709
*A WTO IS ISSUED IN PLACE OF A SYSOUT MESSAGE IF INPUT ERROR IS ON THE
*JOB SEPERATOR ROUTINE.
WTO5     EQU   *
         LA    MSGSTRTR,SDTTM5          GET START OF MESSAGE IEF325I
         B     WTOUT1                   OUTPUT MSG
WTO6     EQU   *
         LA    MSGSTRTR,SDTTM6          GET START OF MESSAGE IEF326I
         B     WTOUT1                   OUTPUT MSG
WTO7     EQU   *
         LA    MSGSTRTR,SDTTM7          GET START OF MESSAGE IEF311I
WTOUT1   EQU   *                        OUTPUT MSG
         LA    PR0,SDTTMLG1             GET LENGTH OF MESSAGE
         GETMAIN R,LV=(0)               GETCORE FOR MESSAGE BUILD
         MVC   0(SDTTMLG1,PR1),0(MSGSTRTR)   MOVE IN MESSAGE LIST
         LR    MSGSTRTR,PR1             SAVE ADDR OF GOTTEN CORE
         WTO   MF=(E,(1))               WRITE MESSAGE TO CONSOLE
         LA    PR0,SDTTMLG1             GET LENGTH OF AREA TO FREE
         LR    PR1,MSGSTRTR             GET ADDRESS OF AREA TO FREE
         FREEMAIN R,LV=(0),A=(1)        FREE MSG BUILD CORE
         B     LISTCLR                  CK FOR RC TESTS COMPLETE
         SPACE
***********************************************************************
SETPRTL  SETPRT OPTCD=B,MF=L            DEFAULT TO BLOCK DATA CHECKS
BLANKS   DC    CL4' '
INVALADR DC    1F'0'                    INVALID ADDRESS CONSTANT
         DC    C'ZAPSZAPSZAPSZAPS'                                 VS0H
         DC    C'ZAPSZAPSZAPSZAPS'                                 VS0H
         DC    C'ZAPSZAPSZAPSZAPS'                                 VS0H
         DC    C'ZAPSZAPSZAPSZAPS'                                 VS0H
         DC    C'ZAPSZAPSZAPSZAPS'                                 VS0H
***********************************************************************
         EJECT
IEFSDTTM EQU   *
         DS    0F
MSPLI    DC    C'IEF311I NOP--SETPRT PARAMETER LIST INVALID.'
MSPLIE   EQU   *
MSFCBV   DC    C'IEF322I NO FCB IMAGE-ID SPECIFIED FOR VERIFICATION.'
MSFCBVE  EQU   *
MSFCBAL  DC    C'IEF323I NO FCB IMAGE-ID SPECIFIED FOR ALIGNMENT.'
MSFCBALE EQU   *
MSUCSV   DC    C'IEF324I NO UCS IMAGE-ID SPECIFIED FOR VERIFICATION.'
MSUCSVE  EQU   *
MSIDNA   DC    C'IEF325I OPERATOR CANCELLED LOAD.  UCS/FCB IMAGE-ID/'
         DC    C'CHAIN NOT AVAILABLE.'
MSIDNAE  EQU   *
MSBLE    DC    C'IEF326I PERMANENT I/O ERROR ON BLDL LOCATE OF UCS/FCB'
         DC    C' IMAGE IN SYSTEM LIBRARY.'
         DS    0F                       ASSURE FWD BDY ALIGNMENT
MSBLEE   EQU   *
SDTTM1   EQU   *
         WTO   'IEF327I WTR DDD CLOSED.  PERMANENT I/O ERROR WHILE LOADX
               ING UCS/FCB BUFFER.                            ',       X
               MF=L,ROUTCDE=(2,7,10,11),DESC=(4)
         DS    0F                       ASSURE FWD BDY ALIGNMENT
SDTTM2   EQU   *
         WTO   'IEF328I WTR DDD CLOSED.  PERMANENT I/O ERROR ON UCS/FCBX
                IMAGE VERIFICATION.                           ',       X
               MF=L,ROUTCDE=(2,7,10,11),DESC=(4)
         DS    0F                       ASSURE FWD BDY ALIGNMENT
SDTTM3   EQU   *
         WTO   'IEF329I WTR DDD CLOSED.  OPERATOR CANCELLED LOAD.  INCOX
               RRECT UCS/FCB IMAGE DISPLAYED FOR VERIFICATION.',       X
               MF=L,ROUTCDE=(7,11),DESC=(5)
         DS    0F                       ASSURE FWD BDY ALIGNMENT
SDTTM4   EQU   *
         WTO   'IEF331I WTR DDD CLOSED.  SETPRT NOP-UNCORRECTABLE OUTPUX
               T ERROR ON PREVIOUS OPERATION.                 ',       X
               MF=L,ROUTCDE=(2,7,10,11),DESC=(6)
         DS    0F                       ASSURE FWD BDY ALIGNMENT
SDTTM5   EQU   *
         WTO   'IEF325I OPERATOR CANCELLED LOAD.  UCS/FCB IMAGE-ID/CHAIX
               N NOT AVAILABLE.       ',                               *
               MF=L,ROUTCDE=(2,7,11),DESC=(5)
         DS    0F                       ASSURE FWD BDY ALIGNMENT
SDTTM6   EQU   *
         WTO   'IEF326I PERMANENT I/O ERROR ON BLDL LOCATE OF UCS/FCB IX
               MAGE IN SYSTEM LIBRARY.',                               *
               MF=L,ROUTCDE=(2,7,10,11),DESC=(4)
         DS    0F                       ASSURE FWD BDY ALIGNMENT
SDTTM7   EQU   *
         WTO   'IEF311I NOP--SETPRT PARAMETER LIST INVALID.            X
                                      ',                               *
               MF=L,ROUTCDE=(2,7,11),DESC=(6)
*
***********************************************************************
*
         END
