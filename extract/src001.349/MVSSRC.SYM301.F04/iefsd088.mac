         TITLE 'IEFSD088 - VMS SYSOUT TRANSITION ROUTINE'
*
*          RELEASE 22 DELETIONS/CHANGES
*          RELEASE 21 DELETIONS/CHANGES
*          RELEASE 20 DELETIONS/CHANGES
* 022800-043800,051000-058400                                    MAPMAC
*0772                                                            A35780
 TITLE 'IEFSD088' - THE TRANSITION ROUTINE
***********************************************************************
*                                                                     *
*STATUS CHANGE LEVEL 002
*                                                                     *
*FUNCTION OF THIS MODULE:                                             *
*                                                                     *
*OPERATION OF THIS MODULE:                                            *
*
*        WHEN A GROUP OF MESSAGES OR A DATASET IS TO BE WRITTEN BY
*        MEANS OF THE STANDARD OUTPUT ROUTINE OR A USERS SPECIAL
*        PROGRAM IT IS NECESSARY TO BUFFER THE MESSAGES FROM DATASETS
*        AND THE DATASETS FROM MESSAGES AND OTHER DATASETS. THIS
*        IS DONE BY PUTTING OUT EXTRA RECORDS BEFORE OR AFTER A
*        DATASET OR GROUP OF MESSAGES WHENEVER NECESSARY. IT IS
*        NECESSARY TO KEEP OVERPRINTING FROM OCCURING WHEN AN
*        ASA DATASET IS FOLLOWED BY A MACHINE DATASET. IT IS ALSO
*        NECESSARY, AT TIMES, TO PUT OUT EXTRA RECORDS AT THE END
*        OF A DATASET OR GROUP OF MESSAGES TO ALLOW A FINAL SPACE
*        CHARACTER TO BE EFFECTIVE. IT IS ALSO NECESSARY TO PUT
*        OUT EXTRA RECORDS AT THE END OF A DATASET OR GROUP OF
*        MESSAGES SO THAT IF THERE IS A PAUSE BEFORE A DATASET ALL
*        OF THE RECORDS WILL HAVE BEEN PRINTED OR PUNCHED BEFORE
*        THE PAUSE TAKES EFFECT.
*
*        THE FOLLOWING DIAGRAMS SHOW WHAT BUFFERING IS DONE AT THE
*        START AND END OF OUTPUTTING A GROUP OF MESSAGES OR A
*        DATASET. THE COMMENTS IN THE OUTPUT ROUTINE EXPLAIN THE
*        OUTPUTTING OF RECORDS OTHER THAN THOSE USED FOR BUFFERING
*
*
*              BUFFERING OF PRINTER TYPE RECORDS: MACHINE CONTROL CHAR.
*
*                       //-------/-------/-------//
*   MACHINE C.C. IN     // P1 S1 / P2 S2 / P3 S3 //
*                       //-------/-------/-------//
*
*
*                   //-------/-------/-------/-------//
*   ASA C.C. OUT    // S0 P1 / S1 P2 / S2 P3 / S3 P0 //
*                   //-------/-------/-------/-------//
*
*                       //-------/-------/-------/-------//
*   MACHINE C.C. OUT    // P1 S1 / P2 S2 / P3 S3 / P0 SX //
*                       //-------/-------/-------/-------//
*
*                       //-------/-------/-------/-------//
*   NO C.C. OUT         //  P1   /  P2   /  P3   /  P0   //
*                       //-------/-------/-------/-------//
*
*        P0 = A RECORD MADE UP OF BLANKS
*        S0 = SPACE 1
*        SX = SPACE SUPRESS
*        SN = SKIP TO CHANNEL 1
*
*              BUFFERING OF PRINTER TYPE RECORDS: ASA CONTROL CHAR.
*                       //-------/-------/-------//
*   ASA C.C. IN         // S1 P1 / S2 P2 / S3 P3 //
*                       //-------/-------/-------//
*
*
*                       //-------/-------/-------/-------//
*   ASA C.C. OUT        // S1 P1 / S2 P2 / S3 P3 / SX P0 //
*                       //-------/-------/-------/-------//
*
*                   //-------/-------/-------/-------/-------//
*   MACHINE C.C. OUT// P0 S1 / P1 S2 / P2 S3 / P3 S0 / P0 SX //
*                   //-------/-------/-------/-------/-------//
*
*                       //-------/-------/-------/-------//
*   NO C.C. OUT         //  P1   /  P2   /  P3   /  P0   //
*                       //-------/-------/-------/-------//
*
*
*              BUFFERING OF PRINTER TYPE RECORDS: NO CONTROL CHAR.
*
*                       //-------/-------/-------//
*   NO  C.C. IN         //  P1   /  P2   /  P3   //
*                       //-------/-------/-------//
*
*
*                       //-------/-------/-------/-------//
*   ASA C.C. OUT        // SN P1 / S0 P2 / S0 P3 / SX P0 //
*                       //-------/-------/-------/-------//
*
*                   //-------/-------/-------/-------/-------//
*   MACHINE C.C. OUT// P0 SN / P1 S0 / P2 S0 / P3 S0 / P0 SX //
*                   //-------/-------/-------/-------/-------//
*
*                       //-------/-------/-------/-------//
*   NO  C.C. OUT        //  P1   /  P2   /  P3   /  P0   //
*                       //-------/-------/-------/-------//
*
*              BUFFERING OF PUNCH TYPE RECORDS IS SIMPLER THAN
*              ABOVE. ALL THAT IS NECESSARY IS TO WRITE OUT AN
*              EXTRA BLANK RECORD WITH THE CONTROL CHARACTER
*              INDICATING STACKER 1.
*                                                                     *
*    ENTRY POINT 1 - IEFSD088 WHICH IS BRANCHED TO FROM IEFSD086,     *
*        IEFSD087, AND IEFSD089.                                      *
*                                                                     *
*    EXIT POINT 1 - BRANCH TO IEFSD089 TO DO THE OUTPUT ROUTINE.      *
*    OUTPUT AT EXIT POINT 1:                                          *
*        REGISTER 7 POINTS TO THE WORK LIST USED BY DSB AND SMB       *
*        ROUTINE.                                                     *
*        REGISTER 8 POINTS TO THE WORK AREA FOR STANDARD OUTPUT       *
*        ROUTINE.                                                     *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*                                                                     *
*    IEFSD089  THE OUTPUT ROUTINE                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*ATTRIBUTES - REENTRANT                                               *
*                                                                     *
***********************************************************************
*
*                        NEEDED DSECTS
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*DEFINE DCB LABELS FOR DCB OF MAXIMUM SIZE
         DCBD  DSORG=PS
*DEFINE DCB AND OPEN AND CLOSE. (EODAD,SYNAD,OPEN,AND CLOSE TO ADD)
TODCB    DSECT
         DCB   DSORG=PS,                                               *
               MACRF=GL,                                               *
               BUFNO=2
SPOPN    OPEN  (0,INPUT),MF=L
SPCLO    CLOSE (0),MF=L
SPEXL    DC    F'0'                     DCB EXIT LIST
BODCB    EQU   *
         IEFSDWKT                                                MAPMAC
         IEFSDSDR                                                MAPMAC
*EQUATES
ASASW    EQU   4                        ASA CONTROL CHARACTER
MACSW    EQU   2                        MACHINE CONTROL CHARACTER
VARSW    EQU   64                       VARIABLE SWITCH WHEN NOT FIXED
FIXSW    EQU   128                      FIXED SWITCH WHEN NOT VARIABLE
UNKSW    EQU   VARSW+FIXSW              UNKNOWN SWITCH
*
*                   THIS ROUTINE BUFFERS DATASETS AND GROUPS
*                   OF MESSAGES SO THAT CHANGE OF INPUT CONTROL
*                   CHARACTERS FROM ASA TO MACHINE AND VICA
*                   VERSA DO NOT CAUSE TROUBLE AND SO THAT
*                   AT THE END OF PROCESSING DATASETS OR
*                   MESSAGES FOR UNIT RECORD DEVICES ALL
*                   RECORDS HAVE BEEN PRINTED OR PUNCHED.
*
*INITIALIZE
IEFSD088 CSECT
         BALR  R10,0                   SET BASE REGISTER
         USING *,R10
         USING WORKLST,R7               SET PARAMETER BASE
         USING WKSOR,R8                 SET WORK AREA BASE
         USING IHADCB,R5                SET OUTPUT DCB BASE
*IS THIS PRINTER TYPE UNIT
         TM    VARBA,PUNSH              TEST FOR PUNCH IF SO
         BO    PUNRO                    BRANCH TO PUNCH ROUTINE
*IS THERE AN OUTPUT ASA CONTROL CHARACTER
         L     R5,DCBOT                 GET OUTPUT DCB
         TM    DCBRECFM,ASASW           TEST FOR ASA SWITCH IF NOT
         BZ    TOMCC                    BRANCH TO SEE IF MACHINE
*IS OLD SWITCH ON
         TM    SORS1,OLDSW              TEST FOR END TRANSITION IF SO
         BO    TMICC                    BRANCH TO DO END TRANSITION
*IS THERE AN INPUT MACHINE CONTROL CHARACTER
         TM    SORS1,MACHS              TEST FOR MACHINE SWITCH IF NOT
         BCR   8,R14                    RETURN
*
*                   WHEN PRINTER TYPE WRITER, OUTPUT ASA,
*                   INPUT MACHINE, BUFFERING AT THE START
*                   CONSISTS OF MAKING ASA SPACE 1 THE
*                   TRANSLATION OF THE CONTROL CHARACTER GOT
*                   FROM THE 'PREVIOUS' RECORD. IT WILL BE USED
*                   FOR THE FIRST RECORD WRITTEN OUT.
*
*MAKE ASA SPACE 1 THE CONTROL CHARACTER FOR THE NEXT RECORD
         MVI   SAVEC,C' '
         BR    R14                      RETURN
*IS THERE AN INPUT MACHINE CONTROL CHARACTER
TMICC    TM    SORS1,MACHS              TEST FOR MACHINE SWITCH IF NOT
         BZ    ASPCC                    BRANCH TO MAKE ASA SUPRESS C.C.
*
*                   WHEN PRINTER TYPE WRITER, OUTPUT MACHINE,
*                   INPUT MACHINE, BUFFERING AT THE END
*                   CONSISTS OF PUTTING OUT A BLANK LINE
*                   WITH SUPPRESS CONTROL CHARACTER. THIS
*                   IS DONE TO MAKE THE LAST RECORD BE
*                   PUT OUT BEFORE MORE WORK IS FOUND AND
*                   A PAUSE MAY OCCUR.
*
*
*                   WHEN PRINTER TYPE WRITER, OUTPUT ASA,
*                   INPUT MACHINE, BUFFERING AT THE END
*                   CONSISTS OF PUTTING OUT A BLANK LINE.
*                   THE SPACING CHARACTER WILL COME FROM
*                   THE LAST RECORD GOT. THE SUPRESS CONTROL
*                   CHARACTER TAKES EFFECT ON THE FOLLOWING
*                   RECORD IF MORE ARE NEEDED BEFORE PAUSE.
*
*PUT IN MACHINE SUPRESS CHARACTER
MSPCC    MVI   CONCH,X'01'
*
*                   WHEN PRINTER TYPE WRITER NO CONTROL
*                   CHARACTER IN OUTPUT, INPUT ASA OR
*                   MACHINE, BUFFERING AT THE END CONSISTS
*                   OF PUTTING OUT A BLANK LINE. THIS IS
*                   DONE TO MAKE THE LAST RECORD BE PUT
*                   OUT BEFORE MORE WORK IS FOUND AND A
*                   PAUSE MAY OCCUR.
*
*SET UP CONSTANTS FOR BLANK LINE
SCFBL    LA    R11,1                    MAKE LENGTH ONE
         LA    R12,BLANK                BEGINNING OF RECORD A BLANK
         STM   R11,R12,INLSF            SAVE LOCATION AND BEGINNING
*DO OUTPUT ROUTINE
DOUTR    L     R15,SD89EXIT             GET ADDRESS FOR EXIT
         BR    R15                      WILL NOT COME BACK TO HERE
*IS THERE AN OUTPUT MACHINE CONTROL CHARACTER
TOMCC    TM    DCBRECFM,MACSW           TEST FOR MACHINE SWITCH IF NOT
         BZ    NCCOT                    BRANCH TO NO CONTROL CHAR.
*IS OLD SWITCH ON
         TM    SORS1,OLDSW              TEST FOR END TRANSITION IF SO
         BO    SUFBL                    BRANCH TO DO END TRANSITION
*IS THERE AN INPUT MACHINE CONTROL CHARACTER
         TM    SORS1,MACHS              TEST FOR MACHINE SWITCH IF SO
         BCR   1,R14                    RETURN
*
*                   WHEN PRINTER TYPE WRITER, OUTPUT MACHINE,
*                   INPUT ASA, BUFFERING AT THE START CONSISTS
*                   OF SETTING UP AN INNOCUOUS LOCATION
*                   NEEDED BY THE OUTPUT ROUTINE TO PUT A
*                   CHARACTER IN, AND PUTTING OUT A BLANK
*                   LINE WHICH WILL GET THE CONTROL FROM THE
*                   FIRST ASA RECORD.
*
*MAKE LOCATION TO PUT FIRST CONTROL CHARACTER IN
         LA    R4,LOCAT                 GET USELESS LOCATION
         ST    R4,LOCAT                 TO PUT FIRST CONTROL CHAR. IN
*MAKE ASA SPACE 1 THE CONTROL CHARACTER
MASPO    MVI   CONCH,C' '
         B     SCFBL                    BRANCH TO SET UP FOR BLANK LINE
*IS THERE AN INPUT MACHINE CONTROL CHARACTER
SUFBL    TM    SORS1,MACHS              TEST FOR MACHINE SWITCH IF SO
         BO    MSPCC                    BRANCH TO USE MACHINE SUPRESS
*                   WHEN PRINTER TYPE WRITER, OUTPUT MACHINE,
*                   INPUT ASA, BUFFERING AT THE END CONSISTS
*                   OF PUTTING A SPACE 1 CONTROL CHARACTER         285
*                   IN THE PREVIOUS LINE AND PUTTING OUT           285
*                   A BLANK LINE WITH A SUPRESS SPACE.             285
*                   CONTROL CHARACTER. THIS IS DONE TO MAKE
*                   THE LAST RECORD BE PUT OUT BEFORE MORE
*                   WORK IS FOUND AND A PAUSE MAY OCCUR.
*
*MAKE MACHINE SUPRESS THE CONTROL CHARACTER FOR LAST LINE          285
         MVI   SAVEC,X'01'                                         285
         B     MASPO                    BRANCH TO USE SPACE 1
*
*                   WHEN PRINTER TYPE WRITER OUTPUT ASA,
*                   INPUT ASA, BUFFERING AT THE END CONSISTS
*                   OF PUTTING OUT A BLANK LINE WITH SUPRESS
*                   CONTROL CHARACTER. THIS IS DONE TO MAKE
*                   THE LAST RECORD BE PUT OUT BEFORE MORE
*                   WORK IS FOUND AND A PAUSE MAY OCCUR.
*
*MAKE ASA SUPRESS THE CONTROL CHARACTER
ASPCC    MVI   CONCH,C'+'
         B     SCFBL                    BRANCH TO SET UP FOR BLANK LINE
*IS OLD SWITCH ON
NCCOT    TM    SORS1,OLDSW              TEST FOR OLD SWITCH IF ON
         BO    SCFBL                    BRANCH TO SET UP FOR BLANK LINE
         BR    R14                      RETURN
*IS OLD SWITCH ON
PUNRO    TM    SORS1,OLDSW              TEST FOR END TRANSITION IF NOT
         BCR   8,R14                    RETURN
*
*                   WHEN PUNCH TYPE WRITER BUFFERING AT THE
*                   END CONSISTS OF PUTTING OUT 3 BLANK LINES
*                   WITH ASA OR MACHINE STACKER 1 CONTROL
*                   CHARACTER AS IS NECESSARY. THIS IS DONE
*                   TO MAKE THE LAST RECORD BE PUT OUT BEFORE
*                   MORE WORK IS FOUND AND A PAUSE MAY OCCUR.
*
*MAKE ASA STACKER 1 THE CONTROL CHARACTER
         MVI   CONCH,C'V'
*IS THERE AN INPUT MACHINE CONTROL CHARACTER
         TM    SORS1,MACHS              TEST FOR MACHINE SWITCH IF NOT
         BZ    SCFBL                    BRANCH TO SET UP FOR BLANK CARD
*MAKE MACHINE STACKER 1 THE CONTROL CHARACTER
         MVI   CONCH,X'01'              PUT IN 2540 STACKER 1
         TM    VARBA,FTFTW              TEST FOR PUNCH IS 1442 IF NOT
         BZ    SCFBL                    BRANCH TO SET UP FOR BLANK CARD
         MVI   CONCH,X'81'              PUT IN 1442 STACKER 1
         B     SCFBL                    BRANCH TO SET UP FOR BLANK CRAD
*CONSTANTS
         DS    0F
SD89EXIT DC    V(IEFSD089)              OUTPUT ROUTINE
BLANK    DC    C' '                     BLANK TO WRITE BLANK RECORD
         END
