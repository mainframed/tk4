IASXWR00 CSECT
*
*          RELEASE 22 DELETIONS/CHANGES
*          RELEASE 21 DELETIONS/CHANGES
*          RELEASE 20 DELETIONS/CHANGES
* 041900-043300,044200-045000                                    MAPMAC
* 022030,022038,022100-023350                                    A32047
*
*                                                                 M3785
* 020800-021205                                                   M3070
* 044400-044500                                                    I272
*3245000260-000360                                                 SM0I
*025100                                                          A29072
*                                                                S20201
*                                                                A42459
         TITLE 'IASXWR00 - EXTERNAL WRITER INITIALIZATION'
**********************************************************************
*                                                                    *
*TITLE-- IASXWR00 - EXTERNAL WRITER INITIALIZATION ROUTINE           *
*                                                                    *
*0371                                                            A36842
*STATUS CHANGE LEVEL 007
*FUNCTION/OPERATION--                                                *
*   THIS ROUTINE SERVES TO INITIALIZE THE SYSOUT WRITER, PERFORMING  *
*   THE FOLLOWING STEPS                                              *
*   1. GETS CERTAIN BLOCKS OF STORAGE FOR THE SYSOUT WRITER, INCL.   *
*      A PARAMETER LIST COMMON TO ALL OF ITS ROUTINES                *
*   2. INITIALIZES PARTS OF THE PARAMETER LIST                       *
*   3. GETS DCB FOR PUT LOCATE. READS IN JFCB AND TESTS TO SEE IF  CC0H
*      CHAIN SCHEDULING IS TO BE DONE ON OUTPUT. IF SO THE DCB IS  CC0H
*      REPLACED BY A DCB FOR EXCP AND INFORMATION IS SAVED WHICH   CC0H
*      WILL BE NEEDED TO CONSTRUCT TABLES FOR EXCP.                CC0H
*   4. SCANS PARAMETER FIELD  PROVIDED IN EXEC PROC STATEMENT FOR THE *
*      JOB SEPARATOR PROGRAM NAME.IF FOUND,IT IS SAVED LEFT ADJUSTED  *
*      AND PADDED WITH BLANKS TO MAKE IT 8 CHARACTER FIELD,IF NOT     *
*      FOUND,THE ENTIRE 8 CHAR JOB SEPARATOR NAME FIELD IS MADE BLANKS*
*   5. EXITS TO IEFSD081,WITH THE ADDRESS OF THE PARAMETER LIST       *
*      IN THE STANDARD PARAMETER REGISTER                            *
*                                                                    *
*ENTRY POINT--                                                       *
*        IASXWR00 - BY WAY OF ATTACH BY MASTER SCHEDULER, IN         *
*   RESPONSE TO A  'START WTR'  COMMAND.                             *
*                                                                    *
*INPUT--                                                             *
*   EXPLICIT--  ADDRESS OF START PARAMETER LIST (SAL) IN REG. 1.   CC0H
*   IMPLICITLY--  TCB, JFCB, TIOT, UCB.                            CC0H
*                                                                    *
*OUTPUT--                                                            *
*   IN REGISTER 1, THE ADDRESS OF A 13-WORD PARAMETER LIST         CC0H
*   COMMON TO ALL SYSOUT WRITER ROUTINES.                          CC0H
*                                                                    *
*                                                                    *
*EXTERNAL ROUTINES--NONE                                             *
*                                                                    *
*EXIT--  AT END OF INITIALIZATION, CONTROL IS PASSED TO              *
*        IASXSD81, THE EXTERNAL WRITER 'CLASSNAME SET-UP' ROUTINE    *
*                                                                    *
*TABLES/WORKAREAS--                                                  *
*   THIS ROUTINE OBTAINS MAIN STORAGE FOR A  DCB AND A PARAMETER     *
*   LIST. IT ALSO GETS CORE FOR THE JFCB WHICH IT SUBSEQUENTLY     CC0H
*   FREES.                                                         CC0H
*                                                                    *
*NOTES--                                                             *
*   THE FOLLOWING SYSTEM MACROS ARE USED--                           *
*        GETMAIN, FREEMAIN, DCB, DCBD, RDJFCB                      CC0H
*                                                                    *
*ATTRIBUTES--                                                        *
*   THIS ROUTINE IS REENTRANT AND REFRESHABLE.                     CC0H
*                                                                    *
**********************************************************************
***                                                                ***
*        ESSENTIAL EQUATES                                           *
***                                                                ***
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
MFTWO    EQU   X'74'                                               000H
MFTBT    EQU   X'20'                                               000H
TCBPIB   EQU   X'7C'                                               000H
PIBWTR   EQU   X'10'                                               000H
HIAR1    EQU   X'84'                    TEST JFCBHIAR FIELD      A36842
SET0     EQU   X'7F'                    SET PARUCS TO HIAR 0     A36842
SET1     EQU   X'80'                    SET PARUCS TO HIAR 1     A36842
WTRPART  EQU   X'02'              USED TO SET BIT 6 WTR IN PARTIT M3785
DEVLNGH  EQU   8                       AMOUNT OF CORE FOR DEVTPE A32047
*                                                                    *
**********************************************************************
         EJECT
**********************************************************************
*        IASXWR00                  THE PROGRAM                       *
**********************************************************************
***                                                                ***
*        ESTABLISH BASE REGISTER AND SAVE INPUT REGISTERS            *
***                                                                ***
         SAVE  (14,12)                  SAVE REGISTERS
         BALR  R9,0                     ESTABLISH
         USING *,R9                       BASE REGISTER
         LR    R7,R1                    SAVE PARAM REG
         MODESET MODE=SUP
***                                                                ***
*        GETMAIN AND ESTABLISH ADDRESSABILITY                        *
***                                                                ***
         LA    R0,CORELENG              LOAD LTH OF DCB+PARLIST+4  CC0H
         GETMAIN R,LV=(0)
         LA    R0,CORELENG              REESTABLISH SIZE           SM0I
         ST    R0,0(R1)                 STORE LENGTH IN FIRST 4 BYTES
         LA    R2,4(R1)                 INCREMENT ADDR PAST LENGTH
*                                        FIELD TO DCB ADDR
         LA    R3,PARDCBLN(R2)          LOAD ADDR OF PARAMETER LIST,
*                                        HENCEFORTH CALLED 'PARLIST'
*     SET UP ADDR OF JFCB AND DEVDTYP AREAS IN R4
         LA    R4,JFCBDISP(R1)     LOAD ADDR OF DEVD & JFCB
         USING IHADCB,R2                ADDRESSABILITY FOR DCB
         USING PARLIST,R3               ADDRESSABILITY FOR PARLIST
         USING SPLAD,R7                 ADDRESSABILITY FOR SPL
***                                                                ***
*        INITIALIZE PARTS OF THE PARAMETER LIST                      *
***                                                           *** M3070
         ST    R2,PARDCB           STORE DCB ADDR IN PARLIST      M3070
         ST    R7,PARSPL           STORE SAVED SPL ADDR IN LIST   M3070
         ST    R4,PARWORK          SET UP ADDR. OF WORK SPACE IN LIST
         LR    R1,R2               ADDRESS OF EXTRACT LIST
         MVC   0(COMMLENG,1),COMMLIST  MOVE LIST TO GOTTEN CORE   M3070
         EXTRACT PARCOMM,MF=(E,(1))    EXECUTE EXTRACT SVC        M3070
***                                                             ***000H
         XC    PARSMF(4),PARSMF    ZERO OUT SMF ADDRESS            SM0I
***                                                                ***
*        PREPARE DCB FOR OUTPUT OPEN                                 *
***                                                                ***
CONTINUE MVC   0(PARDCBLN,R2),SD80DCB  MOVE OUTPUT DCB INTO GOTCORE000H
         XC    PAREXCP(4),PAREXCP CLEAR OUT EXCP POINTERS        A32047
         XC    PARWORK1(16),PARWORK1   CLEAR WRKAREA & SWITCHES  A32047
*GET CORE FOR JFCB AND DCB EXIT LIST AND MAKE UP EXIT LIST         CC0H
         L     R6,16                   GET TCB ADDRESS           A32047
         L     R6,0(R6)                                          A32047
         L     R6,4(R6)                NOW HAVE TCB ADDRESS      A32047
         USING TCBAR,R6                ADDRESSABILITY FOR TCB    A32047
         L     R6,TCBTIO               GET TIOT ADDRESS          A32047
         USING TIOTAR,R6               ADDRESSIBILITY FOR TIOT   A32047
         MVC   DCBDDNAM(8),TIOEDDNM    MOVE DDNAME TO DCB        A32047
         DEVTYPE DCBDDNAM,(R4)     IS DATA SET DUMMY?
         CLC   PAREXCP(4),0(R4)    TEST FOR DUMMY DATA SET
         BNE   SD80EXT             NO - DON'T SET SWITCH
SETDUMSW OI    PARSWCH,PARDUMMY        TURN ON DD DUMMY SWITCH   A32047
         MVC   PARWTRNM(3),TIOCNJOB     USE JOB AS WTR ID
SD80EXT  EQU   *
         XC    DCBEXLST+1(3),DCBEXLST+1 CLEAR OUT EXIT LIST PTR.   CC0H
         LA    R4,PARJSNM                                          CC0H
         ST    R4,PARJBSEP              SET PTR TO JOB SEP NAME FIELD.
         MVI   0(R4),C' '               BLANK OUT NAME FIELD.
         MVC   1(7,R4),0(R4)
**********************************************************************
*        EXIT                                                        *
**********************************************************************
         LR    R1,R3                    LOAD ADDRESS OF PARLIST.
         L     R15,SD80EXIT             LOAD ADDR FOR EXIT
         BALR  R14,R15                  LEAVE
**********************************************************************
*                                                                    *
*                                                                    *
*                                                                    *
**********************************************************************
*        CONSTANTS AND DSECTS                                        *
**********************************************************************
COMMLIST EXTRACT ,FIELDS=COMM,MF=L     EXTRACT LIST FORM           I272
COMMLENG EQU   *-COMMLIST              LENGTH OF EXTRACT PARAMETERSI272
SD80EXIT DC    V(IASXSD81)         EXIT ADDRESS (CLASSNAME
*                                        SET-UP RTN OR XCTL RTN)
SD80EREX EQU   0                        *** TEMP***                  *
VARSW    EQU   X'40'                    VARIABLE SWITCH            CC0H
FIXSW    EQU   X'80'                    FIXED SWITCH               CC0H
SD80JFCB RDJFCB (0),MF=L                READ JFCB LIST             CC0H
SD80EXC  DCB   DSORG=PS,                                           CC0HX
               CENDA=C5,                                         A29072*
               MACRF=E                                             CC0H
SD80DCB  DCB   DSORG=PS,                                               X
               MACRF=PL,                                               X
               BFTEK=S,                                                X
               SYNAD=SD80EREX
*                                                                    *
*                                                                    *
*                                                                    *
*                                                                    *
*        N O T E      THAT IN THE FOLLOWING EXPANSION USED FOR       *
*        AN OPEN LIST   THE  DCB ADDRESS IS  OVERRIDDEN IN THE       *
*        ABOVE CODE BY THE ADDRESS OF THE  DCB  WHICH IS ACTUALLY    *
*        TO BE OPENED, NAMELY, THE ONE IN GOTTEN CORE                *
*                                                                    *
*                                                                    *
         DCBD  DSORG=PS
         IEFSDPAR                                                MAPMAC
PARDCBLN EQU   DCBEOB+4-IHADCB                                   MAPMAC
JFCBDISP EQU   PARLENGT+PARDCBLN+4
         EJECT
**********************************************************************
JFCAR    DSECT
         IEFJFCBN
CORELENG EQU   PARLENGT+PARDCBLN+JFCBLGTH+8  SIZE OF GETMAIN
         EJECT
**********************************************************************
TIOTAR   DSECT
         IEFTIOT1
**********************************************************************
TCBAR    DSECT
         DS    2F
TCBDEB   DS    1F
TCBTIO   DS    1F
**********************************************************************
DEBAR    DSECT                                                     CC0H
         DS    8F                                                  CC0H
DEBUCB   DS    1F                                                  CC0H
*******************************************************************CC0H
UCBAR    DSECT                                                     CC0H
         IEFUCBOB
         END
