*                   TITLE 'GENERATE UTILITY - EDIT SUBROUTINE'
*
* THIS SUBROUTINE MOVES THE LOGICAL RECORDS FROM THE INPUT BUFFER TO
* THE OUTPUT BUFFER AND INITIATES REQUESTED CONVERSION OF EACH FIELD.
* PARAMETERS FOR MOVING AND CONVERSION COME FROM THE END DATA CONTROL
* BLOCKS- EDCB'S FOR THE CURRENT RECORD GROUP.
*        *
*        *     CALL  HEDIT,(CUREDCB,OUTAREA,INAREA,LENGTH,LITTAB)
*        *          CUREDCB IS A POINTER TO THE CURRENT EDCB
*        *          OUTAREA IS A POINTER TO THE OUTPUT AREA
*        *          INAREA IS A POINTER TO THE INPUT AREA
*        *          LENGTH CONTAINS THE INPUT RECORD LENGTH
*        *          LITTAB (OPTIONAL) IS A POINTER TO THE LITERAL TABLE
*        *
*6017                                                             10651
IEBEDIT2 CSECT
HEDIT    SAVE  (14,12)                 SAVE REGS 14 THRU 12
         LR    2,15                    INITIALIZE BASE REG 2
         USING HEDIT,2
         STM   13,14,HSAVE13           SAVE REGS13,14
         LA    3,HEDRS
         ST    3,8(13)
         LA    13,HEDRS
         LR    12,1                    LOAD PARAM LIST ADDR IN R12
         L     3,0(12)                  LOAD ADDRESS OF CURRENT
         L     4,0(3)                   EDCB INTO R4
HEDIT1   LH    5,HECBL(4)              LOAD EDCB INPUT LENGTH AND
         ST    5,HLEN                  STORE IN HLEN
         L     3,4(12)                 LOAD ADDRESS OF OUTPUT AREA
         L     5,0(3)                  IN R5
         AH    5,HOUST(4)              COMPUTE OUTPUT ADDRESS AND
         ST    5,HTO                   STORE IN HTO
         TM    HLB(4),HLTMSK           CHECK IF LITERAL IS PRESENT?
         BZ    HEDIT2                  NO
         L     3,16(12)                LOAD POINTER TO LITERAL TAB ADDR
         B     HEDIT3
HEDIT2   L     3,8(12)                 LOAD POINTER TO INPUT AREA ADDR
HEDIT3   L     5,0(3)                  LOAD LITR TAB/INPUT AREA ADDR
         LH    6,HINST(4)
         AR    5,6                     COMPUTE INPUT LOCATION AND
         ST    5,HFR                   STORE IN HFR
         TM    HCVB(4),HCXMSK          CHECK IF ZP CONVERSION IS REQD?
         BZ    HEDIT3B                 NO
         TM    HLB(4),HLTMSK       IS THIS A LITERAL              10651
         BZ    HEDIT3C             NO                             10651
         ST    6,LITSAVE+40        SAVE REGISTER SIX              10651
         L     6,HLEN              LEHGTH OF LITERAL              10651
         BCTR  6,0                                                10651
         EX    6,ABC               MOVE LITERAL                   10651
         LA    6,LITSAVE           ADDRESS OF LITERAL             10651
         ST    6,HFR               NEW INPUT LOCATION             10651
         L     6,LITSAVE+40        RESTORE REGISTER SIX           10651
HEDIT3C  EQU   *                                                  10651
         CALL  HZP,(HFR,HLEN)          CONVERT ZONED TO PACKED DECIMAL
         ST    15,HLEN                 STORE NEW LENGTH IN HLEN
HEDIT3B  CALL  HMOVE,(HTO,HFR,HLEN)    MOVE FIELD TO OUTPUT AREA
         LTR   15,15
         BZ    HEDIT7
         TM    HCVB(4),HCVMSK          PZ CONVERSION REQD?
         BO    HEDIT4                  YES
         TM    HCVB(4),HCWMSK          HE CONVERSION REQD?
         BO    HEDIT5                  YES
         B     HEDIT7                  NO
HEDIT4   CALL  HPZ,(HTO,HLEN)          CONVERT PACKED TO ZONED DECIMAL
         B     HEDIT7
HEDIT5   CALL  HSETR,(HTO,HLEN)        CONVERT H-SET BCD TO EBCDIC
HEDIT7   TM    HLFB(4),HLFMSK          CHECK IF IT IS LAST FIELD OF REC
         BO    HEDIT8                  YES
         LA    4,8(0,4)                UPDATE REG4 FOR NEXT EDCB
         B     HEDIT1
HEDIT8   LM    13,14,HSAVE13           RESTORE REGS 13,14
         RETURN (2,12)                RESTORE REGS 2-12 AND RETURN
ABC      MVC   LITSAVE(1),0(5)                                    10651
*
* CONSTANTS
*
HTO      DS    1F                      LOC TO STORE OUTPUT ADDR
HFR      DS    1F                      LOC TO STORE INPUT ADDR
HLEN     DS    1F                      LOC TO STORE INPUT LENGTH
HEDRS    DS    18F
HSAVE13  DS    2F                      REGS 13,14 SAVE AREA
LITSAVE  DS    11F                 LITERAL AREA                   10651
HCVB     EQU   0                       EDCB DISPLACEMENT FOR CONVERSION
HLFB     EQU   0                       EDCB DISPLACEMENT FOR LAST FIELD
HLB      EQU   0                       EDCB DISPLACEMENT FOR LITERAL
HINST    EQU   2
HECBL    EQU   4
HOUST    EQU   6
HCWMSK   EQU   X'20'                   HE CONVERSION MASK
HCVMSK   EQU   X'10'                   PZ CONVERSION MASK
HCXMSK   EQU   X'08'                   ZP CONVERSION MASK
HLTMSK   EQU   X'40'                   LITERAL       MASK
HLFMSK   EQU   X'80'                   LAST FIELD    MASK
         ENTRY HEDIT
         END
