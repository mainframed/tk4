*                   TITLE 'GENERATE UTILITY- PZ CONVERSION SUBROUTINE'
*
* THIS SUBROUTINE CONVERTS THE DATA FROM PACKED TO ZONED DECIMAL FORMAT
* THE CONVERSION PROCEEDS FROM RIGHT TO LEFT.
*        *
*        *     CALL  HPZ,(ADDRESS,LENGTH)
*        *          ADDRESS IS A WORD IN CORE CONTAINING THE
*        *          LOCATION OF THE NUMBER
*        *          LENGTH IS A WORD IN CORE CONTAINING THE
*        *          LENGTH IN BYTES OF THE NUMBER
*        *
*        *     RETURN IN R15 IS NO. OF BYTES OF OUTPUT,OR ZERO
*        *     IF NO CONVERSION IS MADE
*        *
IEBCONP2 CSECT
HPZ      SAVE  (14,7)                   SAVE REGS  14-7
         LR    2,15                     INITIALIZE BASE REG 2
         USING HPZ,2
         LM    3,4,0(1)                 LOAD ADRS OF LOCS CONT. NO & LN
         L     4,0(4)                   LOAD LENGTH IN REG4
         SR    15,15                    CLEAR REG 15
         SR    15,15
         LTR   4,4                      TEST FOR LENGTH GT ZERO.IF NOT,
         BP    HPZ2                     EXIT WITH RETURN CODE ZERO
HPZX     RETURN (2,7)
HPZ2     L     3,0(3)                   ADDRESS IN REG3
         LR    5,4                      LENGTH IN REG5
         SLL   5,1                      COMPUTE OUTLENGTH=2*INLEN-1
         BCTR  5,0
         LR    15,5                     OUTLENGTH IN REG5 AND REG15
         BCTR  5,0                      LOW-ORDER OUTPUT BYTE ADDRESS
         AR    5,3                      INTO REG5
         BCTR  4,0                      LOW-ORDER INPUT BYTE ADDRESS
         AR    4,3                      INTO REG4
         SR    6,6
         SR    7,7                      CLEAR GP REG 7
         IC    6,0(4)                   SIGN BYTE INTO REG6 AND REG7
         LR    7,6
         SLL   6,4                      SIGN TO TOP OF BYTE
         SRL   7,4                      DIGIT TO BOTTOM OF BYTE
         AR    6,7                      INVERT SIGN AND DIGIT OF BYTE
         STC   6,0(5)                   STORE NEW BYTE IN LOW OUTPT POS
HPZ3     CR    4,5                      CURRENT BYTE= HIGH ORDER BYTE?
         BE    HPZX                     YES, EXIT
         BCTR  4,0                      DECREMENT XRS 4 AND
         BCTR  5,0                      5  BY 1
         MVN   0(1,5),0(4)              MOVE SECOND INPUT DIGIT TO OUTP
         OI    0(5),X'F0'               INSERT ZONE
         BCTR  5,0                      DECREMENT REG 5 BY 1
         IC    6,0(4)
         SRL   6,4                      SHIFT DIGIT
         STC   6,0(5)                   MOVE FIRST INPUT DIGIT TO OUTPT
         OI    0(5),X'F0'               INSERT SIGN
         B     HPZ3
         ENTRY HPZ
         END
