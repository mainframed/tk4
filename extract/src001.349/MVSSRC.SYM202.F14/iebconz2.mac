*                   TITLE 'GENERATE UTILITY- ZP CONVERSION SUBROUTINE'
*
* THIS SUBROUTINE CONVERTS DATA FROM ZONED TO PACKED DECIMAL FORMAT.
* THE CONVERSION PROCEEDS FROM LEFT TO RIGHT.
*        *
*        *     CALL  HZP,(ADDRESS,LENGTH)
*        *          ADDRESS IS A WORD IN CORE CONTAINING THE
*        *          LOCATION OF THE NUMBER
*        *          LENGTH IS A WORD IN CORE CONTAINING THE
*        *          LENGTH IN BYTES OF THE NUMBER
*        *
*        *     RETURN IN R15 IS NO. OF BYTES OF OUTPUT,OR ZERO IF
*        *     NO CONVERSION IS MADE
*        *
IEBCONZ2 CSECT
HZP      SAVE  (14,9)                   SAVE REGS 14-9
         LR    2,15                     INITIALIZE BASE REG 2
         USING HZP,2
         LM    3,4,0(1)                 LOAD ADRS OF LOCS CONT. NO & LN
         L     5,0(4)                   LENGTH IN REG5
         SR    15,15
         LTR   5,5                      TEST FOR LENGTH GT ZERO.IF NOT,
         BP    HZP2                     EXIT WITH RETURN CODE ZERO
HZPX     RETURN (2,9)
HZP2     L     6,0(3)                   ADDRESS IN REG6
         LR    7,6                      AND REG7
         LA    5,2(5)                   COMPUTE OUTLENGTH=(INLEN+2)/2
         SRL   5,1
         LR    15,5                     OUTLENGTH IN REG5 AND REG15
         TM    3(4),X'01'               TEST FOR ODD LENGTH
         BO    HZP3                     IF EVEN,INITIALIZE REG6 TO
         BCTR  6,0                      INSERT LEADING ZERO
         SR    8,8
         B     HZP4
HZP3     IC    8,0(6)                   GET INPUT BYTE
         SLL   8,4                      MOVE DIGIT TO TOP OF BYTE
HZP4     BCT   5,HZP4A                  SPECIAL PROCEDURE FOR LAST BYTE
         B     HZP5
HZP4A    NI    1(6),X'0F'               ZERO ZONE OF NEXT BYTE
         IC    9,1(6)                   INSERT DIGIT OF NEXT BYTE IN R9
         ALR   8,9                      REG8= PACKED BYTE
         STC   8,0(7)                   STORE PACKED BYTE IN OUTPUT
         LA    6,2(6)                   INCREMENT XRS FOR
         LA    7,1(7)                   NEXT BYTE
         B     HZP3
HZP5     SR    9,9                      FOR LAST BYTE,
         IC    9,0(6)                   INVERT SIGN AND DIGIT
         SRL   9,4
         AR    8,9
         STC   8,0(7)                   STORE SIGN BYTE IN OUTPUT
         B     HZPX                     RETURN
         ENTRY HZP
         END
