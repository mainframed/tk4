*                TITLE 'GENERATE UTILITY- LENGTH PREDICTOR SUBROUTINE'
*
* THIS SUBROUTINE IS EXECUTED EVERY TIME A NEW GROUP OF RECORDS IS
* ENCOUNTERED IN THE INPUT DATA SET FOR WHICH NEW EDITING PARAMETERS
* ARE SUPPLIED BY THE USER. ON THE BASES OF THE LENGTHS OF THE FIELDS
* TO BE MOVED TO OUTPUT, AND THE CONVERSION TO BE DONE ON EACH, IT
* COMPUTES THE TOTAL LENGTH OF THE OUTPUT RECORDS.
*
*        *
*        *     CALL  HPRL,(LENGTH,CUREDCB,SAVEDCB)
*        *          LENGTH IS A 4-BYTE FIELD WHERE THE PREDICTED
*        *          LENGTH IS TO BE STORED
*        *          CUREDCB IS A POINTER TO THE CURRENT EDCB
*        *          SAVEDCB IS A 4-BYTE FIELD WHERE THE ADDRESS OF
*        *          THE NEXT EDCB IS TO BE STORED
*        *
IEBLENP2 CSECT
HPRL     SAVE  (14,12)                 SAVE REGS 14 THRU 12
         LR    2,15                    INITIALIZE BASE REG 2
         USING HPRL,2
         L     3,4(1)                   PUT CURRENT EDCB ADDRESS
         L     7,0(3)                   INTO R7
         SR    4,4                      ZERO ACCUMULATOR
HPRL1    LH    5,HEFL(7)                LOAD INPUT FIELD LENGTH
         TM    HCVB(7),HCVMSK           TEST FOR CONVERSION
         BZ    HPRL2                    IF PZ CONVERSION,
         SLL   5,1                      OUTLENGTH=2*INLENGTH-1
         BCTR  5,0
         B     HPRL3
HPRL2    TM    HCVB(7),HCXMSK          ZP CONVERSION REQD?
         BZ    HPRL3                   NO
         LA    5,2(0,5)                YES- SET REG5= INLENGTH+2
         SRL   5,1                      OUTLENGTH=(INLENGTH+2)/2
HPRL3    LH    8,HOUST(7)              LOAD OUTPUT LOCATION IN REG8
         AR    8,5                      FIND LAST BYTE OF FIELD
         CR    4,8                      IF HIGHER THAN PREVIOUS
         BNL   HPRL4                    HIGH BYTE,STORE
         LR    4,8                     LOAD R4 WITH NEW HIGH BYTE
HPRL4    TM    HLFB(7),HLFMSK          TEST FOR LAST FIELD OF RECORD
         LA    7,8(0,7)                 UPDATE EDCB POINTER
         BO    HPRL5
         B     HPRL1                    LOOP FOR NEXT FIELD
HPRL5    L     3,8(1)                  LOAD LOC. TO STORE NEXT EDCB ADR
         ST    7,0(3)                  STORE NEXT EDCB ADDR
         L     3,0(1)                  LOAD LOC. TO STORE LENGTH PREDIC
         STH   4,0(3)                   STORE LENGTH PREDICTION
         RETURN (2,12)                 RESTORE REGS 2-12 AND RETURN
*
* CONSTANTS
*
HEFL     EQU   4                       EDCB DISPL FOR INPUT FIELD LENTH
HCVB     EQU   0                       EDCB DISPLACEMENT FOR CONVERSION
HLFB     EQU   0                       EDCB DISPLACEMENT FOR LAST FIELD
HOUST    EQU   6                       EDCB DISPLACEMENT FOR OUTPUT ADR
HCVMSK   EQU   X'10'                   PZ CONVERSION MASK
HCXMSK   EQU   X'08'                   ZP CONVERSION MASK
HLFMSK   EQU   X'80'                   LAST FIELD    MASK
         ENTRY HPRL
         END
