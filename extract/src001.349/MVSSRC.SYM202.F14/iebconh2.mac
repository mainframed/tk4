*              TITLE 'GENERATE UTILITY- H SET TRANSLATION SUBROUTINE'
*
* THIS SUBROUTINE CONVERTS DATA FROM H-SET BCD TO EBCDIC CHARACTERS.
* IT CONVERTS ANY LENGTH FIELD. IT USES TR INSTRUCTION AND TRANSLATES
* 256 BYTES AT A TIME
*
*        *
*        *     CALL  HSETR,(ADDRESS,LENGTH)
*        *          ADDRESS IS A CORE LOC. CONTAINING THE FIELD ADDRESS
*        *          LENGTH IS A CORE LOCATION CONTAINING THE LENGTH
*        *          OF THE FIELD (ONE WORD,ALIGNED)
*        *
*        *     RETURN CODE IN R15 IS NO. OF BYTES CONVERTED
*        *
IEBCONH2 CSECT
HSETR    SAVE  (14,12)                 SAVE REGS 14-12
         LR    2,15                    INITIALIZE BASE REG2
         USING HSETR,2
         L     3,4(1)                  LOAD LENGTH
         L     15,0(3)                 IN REG 15
         LTR   15,15                   LENGTH GR THAN 0?
         BP    HSETR1                  YES
         SR    15,15                   NO- CLEAR REG 15
HSETRX   RETURN (2,12)                 RESTORE REGS 2-12 AND RETURN
HSETR1   L     3,0(1)                  LOAD FIELD ADDRESS
         L     4,0(3)                  IN REG 4
         LR    5,15                    LOAD LENGTH INTO REG 5
HSETR2   CH    5,HH256                 LENGTH GR THAN 256 BYTES?
         BH    HSETR4                  YES
         BCTR  5,0                     NO- REDUCE LENGTH BY 1
         STC   5,HSETR3+1              STORE EXACT LENGTH IN TR INSTR.
HSETR3   TR    0(1,4),HSETRT           TRANSLATE EXACT NUMBER OF BYTES
         B     HSETRX
HSETR4   TR    0(256,4),HSETRT         TRANSLATE 256 BYTES
         SH    5,HH256                 REDUCE LENGTH BY 256 BYTES
         AH    4,HH256                 ADVANCE FIELD ADDR BY 256 BYTES
         B     HSETR2
*
* TRANSLATION TABLE *
*
HSETRT   DC    X'000102030405060708090A0B0C0D0E0F'
         DC    X'101112131415161718191A1B1C1D1E1F'
         DC    X'202122232425262728292A2B2C2D2E2F'
         DC    X'303132333435363738393A3B3C3D3E3F'
         DC    X'404142434445464748494A4B5D4D4C4F'
         DC    X'4E5152535455565758595A5B5C5D5E5F'
         DC    X'606162636465666768696A6B4D6D6E6F'
         DC    X'707172737475767778797A7E7D7A6E7F'
         DC    X'808182838485868788898A8B8C8D8E8F'
         DC    X'909192939495969798999A9B9C9D9E9F'
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    X'6FC1C2C3C4C5C6C7C8C9CACBCCCDCECF'
         DC    X'5AD1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
*
*CONSTANT
*
HH256    DC    H'256'
         ENTRY HSETR
         END
