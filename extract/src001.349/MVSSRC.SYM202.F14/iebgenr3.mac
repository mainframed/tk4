*******************************************************************
**************************** I E B G E N R 3 **********************
*********
* ENTRY  IEBGENR3 =CSECT NAME,CONTROL IS TRANSFERRED FROM GENRT TO
*                  THIS NAME
*        GENS3COP =NAME OF SWITCH THAT INDICATES WHETHER GENS3 HAS
*                  TO DO A STRAIGHT COPY OR NOT
*        HDCBIN   =NAME OF INPUT DCB AND IS REFERRED TO IN GEN03
*                  AND GENS3
*        HDCBOUT  =NAME OF OUTPUT DCB AND IS REFERRED TO IN GEN03
*                  AND GENS3
*********
* INPUT  REGISTER 1 IS POINTIG TO A TABLE CONTAINING THE ADDRESSES
*        OF TABLES CONSTRUCTED BY IEBGSCAN
*********
* OUTPUT TRANSFERRING CONTROL TO GEN03 OR GENS3 THE CONTENTS OF
*        REGISTER 1 IS THE SAME AS AT ENTRY TIME
*********
* EXTRN  IEBGEN03 =NAME OF PROCESSOR FOR CONVENTIONAL DATASETS
*        IEBGENS3 =NAME OF PROCESSOR FOR SPANNED DATASETS
*        DDERROR1 =NAME OF MESSAGE USED AS A DIAGNOSTIC WHEN
*                  DD SASTEMENT NAMES ARE IMPROPERLY SPECIFIED
*        HMSGEXIT =MESSAGE TO PROVIDE THE USER WITH THE NUMBER OF
*                  EXITS TAKEN TO A CERTAIN USER LABEL EXIT ROUTINE
*        HMSGRETC =MESSAGE TO PROVIDE THE USER WITH THE LAST RETURN
*                  CODE FROM A CERTAIN USER LABEL EXIT ROUTINE
*        HMSGIOER =MESSAGE GIVEN WHEN THERE IS AN I/O ERROR WHILE
*                  READING OR WRITING USER LABELS
*        HWRMSG   =THE NAME OF THE MESSAGE WRITER
*********
* ERROR CONDITIONS      IEBGENR3 WILL TERMINATE ON THE FOLLOWING
*                  CONDITIONS
*        1.AFTER PROCESSING HEADER LABELS IF LABELS DATA=ONLY IS
*          SPECIFIED
*        2.IF A USER EXIT ROUTINE REQUESTS TERMINATION
*        3.IF THE DATA SETS NOT SUCCESFULLY CAN BE OPENED
*********
* FUNTION/OPERATION
*        IF USER EXITS OR LABEL PROCSESING IS SPECIFIED THE DCB
*        EXIT LISTS ARE ACTIVATED. THEN THE DCB'S ARE OPENED FOR
*        B S A M  ,TAKING LABEL EXITS IF USER EXITS AND/OR LABEL
*        PROCESSING IF REQUIRED
*        AFTER BOTH DCB'S ARE OPEN THE RECFM'S OF BOTH DCB'S ARE
*        COMPARED,AND SO ARE THE LRECL'S AND BLKSIZE'S.
*        IF ONE OR BOTH OF THE RECFM'S IS VS/VBS AND THE LRECL'S
*        AND/OR THE BLKSIZE'S ARE NOT EQUAL OR ANY KIND OF EDITING
*        OR CONVERSION IS TO BE PERFORMED,THE DCB'S ARE CLOSED AND
*        RE-OPENED FOR Q S A M .AND AFTER THIS CONTROL IS
*        TRANSFERRED TO EITHER GEN03 (IN CASE OF CONVENTIONAL
*        DATA SETS) OR GENS3(IN CASE OF SPANNED DATA SETS )
*******************************************************************
         TITLE   'IEBGENR3-PROCESSOR ROOT SEGMENT (RELEASE 17)'
IEBGENR3 CSECT
*                                                                A29998
*                                                                A31435
*647259,688259                                                  ATM5314
*                                                                A34101
*A113059-113259,124259,134759,1348592,143759,143859,171259,      A36140
*A176259,188359-188959,206759-207159,217261-217323,227259,227261,A36140
*A258259,258759,265259,266259,268259,268559-268759,268859-       A36140
*A269159,269259,647759,648359,649759-651259,688759,688859,       A36140
*A690759-691759,867359,867459                                    A36140
*A115959-117359,143339-143659,149259,190259-190859,196759        A36140
*A205459-205859,212759,649769-649809                             A36140
*C191259,193259,209259,402259                                    A36140
*D116259,117259,124259,147259,148259,171259,176259-182259,       A36140
*D227259,258259-264259,265259,266259,268259,268559-273259,275259,A36140
*D653259-674259,688759,692259-711259                             A36140
*D118259-122259,149259-153259,190259                             A36140
*A131759                                                         A44327
*C359259,422259,457259,49659                                     A44327
*A216259-216709                                                  A45131
*D216259                                                         A45131
*A217279,217280,217285-217645,217855,217963,218387-219115,395759,A48786
*A395859,402759-402959,812759-814759,858959,897959               A48786
*D394259,395259,403259,404259,817259                             A48786
*C860459                                                       @YM02300
*A274359-27624,276247                                          @YM04133
*C268559                                                       @YM04133
*D265259-268259                                                @YM04133
*A206759,977959                                                @YM03265
*C274759,275259                                                @AZ07398
*A017659-017759,141759-142223,142359-142959,188879,            @ZA11962
*A189199-189543,275359-275459,275879-275919,276251             @ZA11962
*A613759-614259,869279-869579                                  @ZA11962
*C018059,142259,188859,274259,275759-275859,275959             @ZA11962
*D274359-274459                                                @ZA11962
*A189359-189379,276199-276204                                  @ZA16460
*A141863-141875,189059-189159,869379-869399,869567             @ZA18366
*A141879,189251,869459,869579                                  @ZA18366
*A142060-142090,142224-142226                                  @ZA24127
*A141876-141881,869469-869470                                  @ZA24162
*C141871,141879,189059-189159,869459                           @ZA24162
*A332559-332859,388559-388859,435559-435859,474559-474859      @ZA26660
*A218457-218459                         @YA19584-@XA20183-(ORG)@ZA26153
*C218460                                @YA19584-@XA20183-(ORG)@ZA26153
*D689259                                @YA19584-@XA20183-(ORG)@ZA26153
*C141863-141867                                                @ZA28426
*A189399,276209                                                @ZA28777
*C189359-189379,276199-276204                                  @ZA28777
*A141759-142329,142409-143039,189079-189111,216389,216539,     @ZA30822
*A216689,275159-275789,276249-276255,614759-615259,            @ZA30822
*A692259-692759,869579-869643                                  @ZA30822
*C188859,533259,534259,613759                                  @ZA30822
*D141863-142259,142359-142959,189059-189543,274259,            @ZA30822
*D275359-275959,276251,613759-621259                           @ZA30822
*C218459                                @YA20777-@XA21726-(ORG)@ZA32107
* THIS MODULE WAS CREATED IN RELEASE 17 IN SUPPORT OF VRE AND
* USER LABELS
*          REGISTER ASSIGNMENTS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
R3START  SAVE  (14,12),T,IEBGENR3-OZ32107                      @ZA32107
         LR    R2,R15
         LA    R7,4095(R2)            SET R7 AS SECOND BASEREG @ZA11962
         LA    R7,1(R7)                INCREASE R7 WITH ONE    @ZA11962
         USING R3START,R2,R7                                   @ZA11962
         LA    R3,R3SAVER              STORE REG 13
         ST    R3,8(R13)               POINT TO NEW SAVE AREA
         ST    R13,4(R3)               STOR  REG 13
         LR    R13,R3
         ST    R1,SAVEREG1             SAVE REG1 FOR 03 AND S3
         L     R3,0(R1)                LOAD EXIT TABLE ADDRESS
         LR    R4,R1                    SAVE REGISTER 1
         MVC   LABPROSW(1),92(R3)      MOVE LABEL PROCC SWITCH
         TM    LABPROSW,X'FF'          TEST THE SWITCH
         BM    CKIHLR                  IF NO  BITS
         BO    CKIHLR                  ARE SET ,THEN
         OI    LABPROSW,X'80'          SET DEFAULT=YES
CKIHLR   DS    0H
         TM    LABPROSW,X'B0'          LABEL PROC. IS NO/INPUT
         BZ    TESTX1                  YES-BRANCH
         MVI   HINXL,X'01'             ACTIVATE INPUT HEADER EXIT
TESTX1   CLI   8(R3),X'40'             USER INPUT HEADER EXIT
         BC    8,CKOHLR                 NO, GO TO NEXT CHECK
         LR    R10,R3                  PICK UP USER
         LA    R10,8(R10)                 INPUT  HEADER
         MVI   HINXL,X'01'              YES, ACTIVATE IHLE
         LOAD  EPLOC=(R10)             LOAD USER INPUT HEADER   RTN
         ST    R0,ADINUHLR                ROUTINE AND SAVE ADDRESS
*
CKOHLR   DS    0H
         TM    LABPROSW,X'40'          LABEL PROC. IS NO
         BO    TESTX2                  YES-BRANCH
         MVI   HOUXL+4,X'02'           ACTIVATE OUTPUT HEADER EXIT
TESTX2   CLI   16(R3),X'40'            USER OUTPUT HEADER EXIT
         BC    8,CKITLR                 NO, GO TO NEXT CHECK
         LR    R10,R3                  PICK UP USER
         LA    R10,16(R10)               OUTPUT HAEDER
         MVI   HOUXL+4,X'02'              YES, ACTIVATE OHLE
         LOAD  EPLOC=(R10)             LOAD USER OUTPUT HEADER  RTN
         ST    R0,ADOTUHLR                ROUTINE AND SAVE ADDRESS
*
CKITLR   DS    0H
         TM    LABPROSW,X'90'          LABEL PROC. IS YES/ALL
         BZ    TESTX3                  NO-BRANCH
         MVI   HINXL+4,X'03'           ACTIVATE INPUT TRAILER EXIT
TESTX3   CLI   24(R3),X'40'            USER INPUT TRAILER EXIT
         BC    8,CKOTLR                 NO, GO TO NEXT CHECK
         LR    R10,R3                  PICK UP USER
         LA    R10,24(R10)               INPUT  TRAILER
         MVI   HINXL+4,X'03'            YES, ACTIVATE ITLE
         LOAD  EPLOC=(R10)             LOAD USER INPUT TRAILER  RTN
         ST    R0,ADINUTLR                ROUTINE AND SAVE ADDRESS
*
CKOTLR   DS    0H
         TM    LABPROSW,X'98'          LABEL PROC. IS YES/ALL/INPUT
         BZ    TESTX4                  NO-BRANCH
         MVI   HOUXL+8,X'04'           ACTIVATE OUTPUT TRAILER EXIT
TESTX4   CLI   32(R3),X'40'            USER OUTPUT TRAILER EXIT
         BC    8,SWSTORE                NO, GO TO NEXT CHECK
         LR    R10,R3                  PICK UP USER
         LA    R10,32(R10)               OUTPUT TRAILER
         MVI   HOUXL+8,X'04'            YES, ACTIVATE OTLE
         LOAD  EPLOC=(R10)             LOAD USER OUTPUT TRAILER RTN
         ST    R0,ADOTUTLR                ROUTINE AND SAVE ADDRESS
*
SWSTORE  DS    0H
         LR    R1,R4                    RESTORE REGISTER 1
         MVC   HCOPYSW(1),93(R3)       MOVE COPY SWITCH
         MVC   HPOID(1),94(R3)
         MVC   LABPOLAT(8),80(R3)      MOVE LABEL POOL ADDRESSES
         MVC   HDCBIN+MACRF(2),BSREAD  MAKE SYSUT1 DCB FOR BSAM
         MVC   HDCBOUT+MACRF(2),BSWRITE MAKE SYSUT2 DCB FOR BSAM
         SR    R4,R4
         C     R4,88(R3)
         BZ    HGENP1
         MVC   HOUXL(4),88(R3)         STORE TOT.AREA ADDR IN
         MVI   HOUXL,X'0A'             EXITLIST AND ACTIVATE
HGENP1   TM    LABPROSW,X'40'          TEST LABEL PROC. FOR 'NO'
         BO    HGENP1A                 NO LABEL PROC - BEANCH
         LR    R4,R1                   SAVE REGISTER 1
         LA    R1,640                  GET STORAGE
         GETMAIN R,LV=(R1)             FOR BACK UP COPY
         ST    R1,BACKUP               OF OUTPUT HEADER LABELS
         TM    LABPROSW,X'08'          TEST LABELPROC. FOR 'INPUT'
         BO    HGENP1B                 'INPUT' - BRANCH
         LA    R1,640                  GET LENGTH OF CORE
         GETMAIN R,LV=(R1)             GET STORAGE
         ST    R1,LABPOLAT             FOR HEADER LABELS
         TM    LABPROSW,X'20'          LABEL PROCESSING IS ONLY
         BO    HGENP1B                 YES-BRANCH
         LA    R1,640                  GET LENGT OF CORE
         GETMAIN R,LV=(R1)             GET STORAGE FOR
         ST    R1,LABPOLAT+4           TRAILER LABELS
HGENP1B  LR    R1,R4                   RESTORE REFSITER 1
         SR    R4,R4
HGENP1A  LA    R1,4(R1)                LOAD MEMBER NAME TABLE ADDR
         LA    R5,HDCBIN                LOAD SYSUT1 DCB ADDR
         C     R4,HINDD(R1)             INPUT DD NAME GIVEN?
         BE    HGENP2                   NO
         MVC   DDNAM(8,R5),HINDD(R1) USER DDNAME IN UT1
HGENP2   MVC   INPUTDD(8),DDNAM(R5)    STORE INPUT DD NAME       A29998
         C     R4,HOUDD(R1)            CHECK FOR OUTPUT DD       A29998
         LA    R5,HDCBOUT               LOAD SYSUT2 DCB ADDR
         BE    HGENP3                   BRANCH IF NO OUTPUT DD NAME
         MVC   DDNAM(8,R5),HOUDD(R1)  USER DDNAME IN UT2
HGENP3   MVC   OUTPUTDD(8),DDNAM(R5)   STORE OUTPUT DDNAME       A29998
         OC    DSORG(1,R5),HPOID       SET SYSUT2                A29998
         TM       DSORG(R5),X'42'       DEB FOR
         BM    HGENP4                   PS OR
         NI       DSORG(R5),X'BF'       PO
HGENP4   MVI   OPENSW,X'F0'            SET OPENIN SWITCH
         L     R4,LABPOLAT             LOAD LABEL POOL ADDRESS
         LA    R4,0(R4)                STORE BEGIN ADDRESS
         ST    R4,HLABINDX             OF HEADER LABELS
         OI    USEXSW,X'80'            INITIALIZE USER EXIT SWITCH
         MVI   EXCOUNT,X'00'
         OPEN  (HDCBIN,INPUT)          OPEN SYSUT1 FOR INPUT
         CLI   HCONCODE+1,X'0C'   COND CODE EQ 12 ?              A36140
         BE    HTERMU             YES, END PROCESSING            A36140
         NI    GENS3SW,X'0F'      SET SW FOR ERR PURP. OFF       A36140
         LA    R14,HDCBIN       ADDR OF SYSUT1 DCB
         LA    R15,INPUTDD             POINT REG TO INPUT DD     A29998
*                                      NAME                      A29998
         TM    OFLGS(R14),X'10'    IS IT OPEN
         BZ    DDERROR      NO
         LA    R14,HDCBOUT         LOAD ADDR. OF OUTP. DCB       A36140
         TM    OFLGS(R14),X'10'    WAS DCB OPENED ?              A36140
         BO    AFTWRT2             YES,TEST FOR USER TERM REQ.   A36140
AFTWRT1  CLI   HTERMSW,X'FF'           TEST FOR USER TERM.REQUEST
         BNE   TESTFU                  CONTINUE TESTING          A36140
         LA    R15,SYSUT1H             ADDR OF INPUT DCB ID
HGENP4A  MVI   HTERMSW,X'00'           CLEAR TERM SW BEFORE OPEN
*                                      SYSUT2
         L     R14,VADDR1              ADDR OF ERROR MESSAGE
         MVC   5(8,R14),0(R15)         MOVE DCB ID TO MSG
         LA    R1,5                    LOAD MSG CODE
         SLL   R1,8
         LA    R4,5                    LOAD MESS. NUMBER         A44327
ERRORPNT AR    R1,R4                   SET REG1=MSGCODE+MSG2:
         L     R15,HWRMSGA              LOAD MSG WRITER SUBR ADDR
         BALR  R14,R15                  WRITE MESSAGE AND
         TM    GENS3SW,X'40'      MESS PR. DURING OPEN ?         A36140
         BO    RETOUT             YES, RETURN TO UT2 OPEN        A36140
         B     HTERMU                   TERMINATE
HGENP4B  MVI   OPENOUTS,X'01'          SET OPENOUT SWITCH
         L     R4,LABPOLAT             INITIALIZE
         LA    R4,0(R4)                THE ADDRESS
         ST    R4,HLABINDX             OF FIRST LABEL
         L     R4,BACKUP               INITIALIZE
         LA    R4,0(R4)                INDEX
         ST    R4,BACKUPX              IN BACK UP AREA
         OI    USEXSW,X'80'            INITIALIZE EXIT SWITCH
         MVI   EXCOUNT,X'00'           INITIALIZE EXIT COUNTER
FINDUCB  EQU   *
         L     R4,16                   LOAD PTR TO CVT         @ZA30822
         L     R4,0(R4)                LOAD PTR TO TCB PTR     @ZA30822
         L     R4,4(R4)                LOAD CURRENT TCB        @ZA30822
         L     R4,12(R4)               LOAD TIOT PTR           @ZA30822
         LA    R4,24(R4)               ADJ TIOT PTR TO POINT   @ZA30822
*                                      TO DD NAME              @ZA30822
UCB      EQU   *                                               @ZA30822
         LA    R6,HDCBOUT              GET ADDRESS TO UT2 DCB  @ZA30822
         CLC   40(8,R6),4(R4)          LOOK FOR SYSUT2 UCB     @ZA30822
         BE    UCBFOUND                UCB FOR SYSUT2 FOUND    @ZA30822
         SR    R6,R6                   CLEAR R6                @ZA30822
         IC    R6,0(R4)                GET TIOT LENGTH         @ZA30822
         AR    R4,R6                   POINT TO NEXT DD ENTRY  @ZA30822
         IC    R6,0(R4)                GET TIOT LENGTH         @ZA30822
         LTR   R6,R6                   IS TIOT LENGTH ZERO     @ZA30822
         BZ    OPEN                    YES, NO DD CARD FOR     @ZA30822
*                                      SYSUT2. GO AND OPEN     @ZA30822
*                                      SYSUT2 EVEN IF NO DD    @ZA30822
*                                      CARD AND MSG IEB316I    @ZA30822
*                                      WILL BE ISSUED.         @ZA30822
         B     UCB                     NO, CHECK NEXT UCB      @ZA30822
UCBFOUND EQU   *                                               @ZA30822
         SR    R6,R6                   CLEAR R6                @ZA30822
         ICM   R6,3,18(R4)             LOAD UCB ADDRESS        @ZA30822
         TM    18(R6),X'80'            IS SYSUT2 TAPE          @ZA30822
         BNO   OPEN                    NO, BRANCH              @ZA30822
RDJFCB   DS    0H                                              @ZA11962
         XC    JFCBAREA,JFCBAREA       CLEAR JFCBAREA          @ZA11962
         RDJFCB (HDCBOUT)              READ JBCB.              @ZA11962
         TM    JFCBIND2,X'40'          DISP NEW OR OLD         @ZA30822
         BNO   OPEN                    NO, DISP=MOD            @ZA30822
         OI    TAPESW,X'11'            SET TAPE SW1 AND SW2    @ZA30822
         LA    R1,HDCBOUT              LOAD UT2 DCB ADDRESS    @ZA30822
         MVC   BLKSI(2,R1),JFCBLKSI    MOVE BLKSIZE TO DCB     @ZA30822
         MVC   LRECL(2,R1),JFCLRECL    MOVE LRECL TO DCB       @ZA30822
         MVC   RECFM(1,R1),JFCRECFM    MOVE RECFM TO DCB       @ZA30822
         B     OPENOUXL                                        @ZA30822
AFTJFCB  EQU   *                                               @ZA30822
         NI    TAPESW,X'EF'            RESET TAPE SW1          @ZA30822
         TM    GENS3SW,X'04'           QSAM                    @ZA30822
         BNO   OPEN                    NO DONT CHANGE ACCESS   @ZA30822
*                                      METHOD                  @ZA30822
         MVC   HDCBOUT+MACRF(2),QSPUTMOV QSAM OUTPUT           @ZA30822
OPEN     EQU   *                                               @ZA30822
         OI    OUXL,X'80'              SET OUXL AS LAST ENTRY  @ZA30822
         MVI   JFCB,X'00'              RESET JFCB ENTRY        @ZA30822
         OPEN  (HDCBOUT,OUTPUT)        OPEN SYSUT2 FOR OUTPUT  @ZA30822
         OI    USEXSW,X'80'            RESET EXIT SWITCH
         L     R4,ADOTUHLR             WAS OUTPUT HEADER EXIT    A36140
         LTR   R4,R4                   ROUTINE SPECIFIED ?       A36140
         BZ    RETINP                  NO, RTRN TO INP DCB EXIT  A36140
         LA    R4,OTPUTHDR             LOAD OUTPUT HEADER IND.   A36140
         BAL   R14,WRTEXMSG            WRITE MSG'S               A36140
         B     RETINP                  RETURN TO OPEN INPUT      A36140
TESTFU   EQU   *                                                 A36140
         LA    R14,HDCBOUT             ADDR OF SYSUT2 DCB
         LA    R15,OUTPUTDD            POINT REG TO OUTPUT DD    A29998
*                                      NAME                      A29998
         TM    OFLGS(R14),X'10'        IS IT OPEN
         BZ    DDERROR                 NO
         B     HGENP5              YES, CONT CHECKING            A36140
AFTWRT2  CLI   HTERMSW,X'FF'           TEST FOR USER TERM.REQUEST
         BNE   HGENP5                  NO-BRANCH
         LA    R15,SYSUT2H             SET DCB ID
         B     HGENP4A                 BRANCH
HGENP5   CLI   HCONCODE+1,X'0C'         TEST FOR USER KEY ERROR
         BNE   HGENP5A                  NO ERROR
         LA    R1,HMSGIIP               LOAD MSG CODE
         SLL   R1,8
         LA    R4,2               MESSAGE ID FOR PARM ERROR
         B     HSTOW3                   TERMINATE
HGENP5A  TM    DELTERSW,X'80'          DTD INPUT LABEL ROUTINE
         BZ    HGENP5B                 REQUIRE TERMINATION
         LA    R15,16                  LOAD CONDITION CODE=16
         BAL   R14,CHCONC              TEST FOR HIGHEST
         LA    R1,HMSDELTR             YES-SET
         SLL   R1,8
         LA    R4,22                   MESSAGE AND
         B     ERRORPNT                BRANCH
HGENP5B  TM    LABPROSW,X'20'          LABEL PROCESS SWITCH='ONLY'
          BZ    CHKERBLS          TEST FOR OPEN ERRORS           A36140
         LA    R15,8                   LOAD CONDITON CODE=8
         BAL   R14,CHCONC              TEST FOR HIGHEST
         LA    R1,MLABONLY             YES-SET MESSAGE OFFSET
         SLL   R1,8                    WARNING
         LA    R4,23                   ID AND
         B     ERRORPNT                BRANCH
CHKERBLS  TM    ERRBLKSW,ERRBLKS1 INP RECFM,BLKSI,LRECL ERROR ?  A36140
         BO    ERRBLK1                 YES,BRANCH
         TM    ERRBLKSW,ERRBLKS2       BLOCKSIZE ERROR ON SYSUT2
         BO    ERRBLK2                 YES,BRANCH
         NI    OPENSW,X'00'            RESET OPENIN AND OPENOUT
*                                      SWITCHES
         L     R1,SAVEREG1             RESTORE REG1 FOR GEN03
         LA    R5,HDCBIN               LOAD INPUT DCB ADDRESS    A36140
         LA    R6,HDCBOUT              LOAD OUTPUT DCB ADDRESS   A36140
         TM    GENS3SW,X'08'           GENS3 MODULE TO CALL  ?   A36140
         BO    CALLS3A                 YES                     @ZA30822
*                                      NO, CALL IEBGEN03       @ZA11962
         NI    OPENSW,X'00'            RESET OPENIN(OUT) SWITCH  A36140
         OI    ITLX,X'80'          SET TRAILER LABEL EXIT AS   @ZA30822
*                                      LAST ENTRY IN CASE OF   @ZA30822
*                                         CONCATINATED INPUT.  @ZA30822
         CALL  IEBGEN03
         BAL   R14,CHCONC              TEST COND.CODE FROM IEBGEN03
         CH    R15,TWELVE              RETURN CODE =12             UL0I
         BE    HTERMU                  YES - ABNORMAL TERM         UL0I
         CH    R15,SIXTEEN             RETURN CODE =16             UL0I
         BE    HTERMU                  YES - ABNORMAL TERM         UL0I
CLOSER3  LA    R1,HDCBIN           LOAD ADDR. OF  INP. DCB       A36140
         TM    OFLGS(R1),X'10'     WAS DCB OPENED ?              A36140
         BZ    AFTCLOS1            NO, DO NOT CLOSE              A36140
         CLOSE HDCBIN              CLOSE INP. DATA SET           A36140
AFTCLOS1 L     R4,ADINUTLR         WAS INPUT TRAILER EXIT        A36140
         LTR   R4,R4                   ROUTINE SPECIFIED
LABPREV1 BZ    AFTWRT3             NO, BRANCH                    A36140
         LA    R4,INPUTTLR             LOAD INPUT TRAILER IND.
         BAL   R14,WRTEXMSG            LINK TO WRITE ROUTINE
AFTWRT3  CLI   HTERMSW,X'FF'           TEST FOR USER TERM.REQUEST
         MVI   LABPREV1+1,X'F0'    PREVENT SEC. MSG PRINTING     A36140
         BNE   CLOSE2
         LA    R15,SYSUT1T             ADDR OF DCB ID
         B     HGENP4A                 OUTPUT MSG
CLOSE2   MVI   CLOSEOUT,X'02'          SET CLOSE SWITCH
         OI    USEXSW,X'80'            RESET EXIT SWITCH
         MVI   EXCOUNT,X'00'           RESET EXIT COUNTER
         L     R4,LABPOLAT+4           INITIALIZE ADDRESS OF LABEL
         LA    R4,0(R4)                POOL
         ST    R4,TLABINDX             SAVE ADDRESS
         LA    R1,HDCBOUT          LOAD ADDR. OF OUTP. DCB       A36140
         TM    OFLGS(R1),X'10'     WAS DCB OPENED ?              A36140
         BZ    SKIPFPOO            NO, DO NOT CLOSE              A36140
         CLOSE HDCBOUT                 CLOSE SYSUT2
         LA    R1,HDCBIN               POINT TO INPUT DCB      @YM03265
         TM    IND2(R1),X'09'          QSAM OPEN WITH BUF POOL @YM03265
         BNO   SKIPFPOO                NO,  SKIP FREEPOOL        A36140
         FREEPOOL HDCBIN               FREE DCB MAIN STORAGE     A36140
         FREEPOOL HDCBOUT              FREE DCB MAIN STORAGE     A36140
SKIPFPOO EQU   *                                                 A36140
         L R4,ADOTUTLR              WAS OUTPUT TRAILER EXIT
         LTR   R4,R4                   ROUTINE SPECIFIED
LABPREV2 BZ    AFTWRT4             NO, BRANCH                    A36140
         LA    R4,OTPUTTLR             LOAD OUTPUT TRAILER IND.
         BAL   R14,WRTEXMSG            LINK TO WRITE ROUTINE
AFTWRT4  CLI   HTERMSW,X'FF'           TEST FOR USER TERM.REQUEST
         MVI   LABPREV2+1,X'F0'    PREVENT SEC. MSG PRINTING     A36140
         BNE   CLOSE3                  NO-BRANCH
         LA    R15,SYSUT2T             ADDR OF OUTPUT DCB
         B     HGENP4A
CLOSE3   L     R1,BACKUP          LOAD BACKUP COPY ADDR.         A45131
         LTR   R1,R1              ADDRESS PRESENT ?              A45131
         BZ    TSTHDRFR           NO,TEST INPUT LABEL ADDRESS    A45131
         LA    R0,640             YES,LOAD BACKUP LENGTH         A45131
         FREEMAIN R,A=(1),LV=(0)  FREEMAIN BACKUP CORE           A45131
         XC    BACKUP,BACKUP      SET FREEMAIN DONE            @ZA30822
TSTHDRFR L     R1,LABPOLAT        LOAD INPUT LABELS ADDR.        A45131
         LTR   R1,R1              ADDRESS PRESENT ?              A45131
         BZ    TSTTLRFR           NO, TEST OUTPUT LABELS ADDR.   A45131
         LA    R0,640             YES, LOAD INP. LABELS LENGTH   A45131
         FREEMAIN R,A=(1),LV=(0)  FREEMAIN INP. LABELS CORE      A45131
         XC    LABPOLAT,LABPOLAT  SET FREEMAIN DONE            @ZA30822
TSTTLRFR L     R1,LABPOLAT+4      LOAD OUTPUT LABELS ADDRESS     A45131
         LTR   R1,R1              ADDRESS PRESENT ?              A45131
         BZ    RETGENER           NO, RETURN TO GENRT            A45131
         LA    R0,640             YES, LOAD OUTP. LABELS LENGTH  A45131
         FREEMAIN R,A=(1),LV=(0)  FREEMAIN OUTP. LABELS CORE     A45131
         XC    LABPOLAT+4,LABPOLAT+4 SET FREEMAIN DONE         @ZA30822
RETGENER L     R13,4(R13)         LOAD REGSAV ADDRESS            A45131
         LH    R15,HCONCODE            LOAD CONDITON CODE
         RETURN  (14,12),RC=(15)
CHKRECFM OI    GENS3SW,X'40'      SET BIT FOR ERR. MESS. PURPOS. A36140
         LA    R5,HDCBIN          LOAD INP. DCB ADDRESS          A36140
         LA    R6,HDCBOUT         LOAD OUTP. DCB ADDRESS         A36140
         CLC   BLKSI(2,R5),=X'0000'    BLKSIZE SPECIFIED ?       A36140
         BNE   TESTLRE1           YES, TEST LRECL                A36140
         OI    BLKSI+1(R5),X'0A'  NO, SET DEFAULT BLKSI=10       A48786
         MVC   LRECL(2,R5),=X'0005'    NO, SET DEFAULT LRECL=5   A48786
         OI    ERRBLKSW,ERRBLKS1  SET INP. ERROR BIT             A36140
TESTLRE1 CLC   LRECL(2,R5),=X'0000'    LRECL SPECIFIED ?         A36140
         BNE   TSTDCBVA           YES, CHECK ERRORS IN UT2 PAR.  A48786
         TM    RECFM(R5),X'48'    RECFM INPUT VS/VBS ?           A48786
         BO    SETERR1            YES, SET ERRBIT                A48786
         TM    RECFM(R5),X'40'    RECFM INPUT V/VB ?             A48786
         BO    STVARDF1           YES, SET DEFAULT LRECL         A48786
         TM    RECFM(R5),X'10'    RECFM INPUT FB ?               A48786
         BO    SETERRFB           YES, SET DEFAULT  AND ERRBIT   A48786
         MVC   LRECL(2,R5),BLKSI(R5) RECFM=F, SO LRECL=BLKSI     A48786
         B     TSTDCBVA           SCAN OUTPUT DCB VALUES         A48786
STVARDF1 LA    R3,4               LOAD REG. 3 WITH 4             A48786
         LH    R4,BLKSI(R5)       LOAD INPUT BLKSIZE IN R4       A48786
         SR    R4,R3              SET R4 = R4 - 4                A48786
         STH   R4,LRECL(R5)       SET INPUT LRECL = BLKSI - 4    A48786
         B     TSTDCBVA           SCAN OUTPUT DCB VALUES         A48786
SETERRFB MVC   LRECL(2,R5),BLKSI(R5) MOVE UT1 BLKSI TO UT2       A48786
SETERR1  OI    ERRBLKSW,ERRBLKS1  SET INP. ERROR BIT             A48786
TSTDCBVA CLC   BLKSI(2,R6),=X'0000' UT2 BLKSIZE ZERO ?           A48786
         BNE   TESTRFM2           NO, SCAN UT2 DCB PARAM. AGAIN  A48786
         CLC   LRECL(2,R6),=X'0000' UT2 LRECL ZERO ?             A48786
         BNE   TESTRFM2           NO, SCAN UT2 DCB PARAM. AGAIN  A48786
         TM    RECFM(R6),X'C0'    UT2 RECFM U ?                  A48786
         BNO   TESTRFM2           NO, SCAN UT2 DCB PARAM. AGAIN  A48786
         MVC   BLKSI(2,R6),BLKSI(R5) MOVE UT1 BLKSI TO UT2       A48786
         MVC   LRECL(2,R6),LRECL(R5) MOVE UT1 LRECL TO UT2       A48786
         MVC   RECFM(1,R6),RECFM(R5) MOVE UT1 RECFM TO UT2       A48786
ENDTST   LA    R1,HMSEQDCB        LOAD MSG NBR IEB352I           A48786
         SLL   R1,8               SHIFT LEFT 8 BITS              A48786
         LA    R1,52(R1)          ADD MSG ADDR. DISPLMNT         A48786
         L     R15,HWRMSGA        LOAD MSG ROUTINE ADDR.         A48786
         BALR  R14,R15            PRINT MESSAGE IEB352I          A48786
         B     ENDERROR           SKIP REST OF ERR.TSTS          A48786
TESTRFM2 CLC   BLKSI(2,R6),=X'0000'    BLKSIZE SPECIFIED ?       A36140
         BNE   TESTLRE2           YES, TEST LRECL                A36140
         OI    BLKSI+1(R6),X'0A'  NO, SET DEFAULT BLKSI=10       A48786
         MVC   LRECL(2,R6),=X'0005'    NO, SET DEFAULT LRECL=5   A48786
         OI    ERRBLKSW,ERRBLKS2  SET OUT. ERROR BIT             A36140
TESTLRE2 CLC   LRECL(2,R6),=X'0000'    LRECL SPECIFIED ?         A36140
         BNE   ENDERROR           YES, END OF ERROR CHECKING     A36140
         TM    RECFM(R6),X'48'    RECFM OUTPUT VS/VBS ?          A48786
         BO    SETDEFLR           YES, SET DEFAULT AND ERRBIT    A48786
         TM    RECFM(R6),X'40'    RECFM OUTPUT V/VB ?            A48786
         BO    STVARDF2           YES, SET DEFAULT LRECL         A48786
         TM    RECFM(R6),X'10'    RECFM OUTPUT FB ?              A48786
         BO    SETDEFLR           YES, SET DEFAULT  AND ERRBIT   A48786
         B     SETL2EB2           SCAN OUTPUT DCB VALUES         A48786
STVARDF2 TM    HCOPYSW,X'FF'      ANY EDITING ?                  A48786
         BZ    CALCLR2            YES, CALC. DEFAULT VALUE       A48786
MOVEL1L2 EQU   *                                               @ZA26153
         TM    RECFM(6),X'C0'     SYSUT2 RECFM U ?             @ZA26153
         BO    ENDERROR           YES BRANCH                   @ZA32107
         MVC   LRECL(2,R6),LRECL(R5) SET LRECL2 = LRECL1       @ZA26153
         B     ENDTST             SCAN OUTPUT DCB VALUES         A48786
CALCLR2  LA    R3,4               LOAD REG. 3 WITH 4             A48786
         LH    R4,BLKSI(R6)       LOAD OUTPUT BLKSIZE IN R4      A48786
         SR    R4,R3              SET R4 = R4 - 4                A48786
         STH   R4,LRECL(R6)       SET OUTPUT LRECL = BLKSI - 4   A48786
         B     ENDERROR           SCAN OUTPUT DCB VALUES         A48786
SETDEFLR TM    HCOPYSW,X'FF'      ANY EDITING ?                  A48786
         BZ    SETERR2            YES, SET ERRBIT                A48786
         B     MOVEL1L2           SET DEFAULT LRECL2 = LRECL1    A48786
SETERR2  OI    ERRBLKSW,ERRBLKS2  SET OUT. ERROR BIT             A48786
SETL2EB2 MVC   LRECL(2,R6),BLKSI(R6) SET  LRECL=BLKSI            A48786
ENDERROR LA    R3,ERRBLK1         LOAD LABEL ERRBLK1 ADDRESS     A48786
AGAINOUT TM    RECFM(R5),X'40'    RECFM VAR.UND ?                A48786
         BO    TSTVBSRF           YES, SKIP BLKSI MULT.OF LRECL  A48786
         TM    RECFM(R5),X'10'    RECFM F ?                      A48786
         BO    BLKLREC            NO, RECFM = FB                 A48786
         CLC   LRECL(2,R5),BLKSI(R5) LRECL = BLKSI (RECFM = F )  A48786
         BE    TSTVAR2            YES, SCAN OUTPUT DCB VAL.      A48786
         B     SETMSG             NO, SET ERRBIT + MSG IEB311I   A48786
BLKLREC  SR    R8,R8              ZERO REGISTER 8                A48786
         LH    R9,BLKSI(R5)       LOAD INP BLKSIZE               A48786
         LH    R4,LRECL(R5)       LOAD INP LRECL                 A48786
         DR    R8,R4              DIVIDE BLKSI BY LRECL          A48786
         LTR   R8,R8              REMAINDER ?                    A48786
         BZ    TSTVAR2            NO, OKE                        A48786
SETMSG   MVC   LRECL(2,R5),BLKSI(R5) MOVE UT1 BLKSI TO LRECL     A48786
SETMSG2  MVI   11(R3),X'0C'       CHANGE MSG IEB317I             A48786
         MVI   19(R3),X'0B'       IN MSG IEB311I                 A48786
INOUTSW  OI    ERRBLKSW,ERRBLKS1  SET INPUT ERR. SWITCH          A48786
         B     TSTVAR2            TEST FOR OUTPUT                A48786
TSTVBSRF TM    RECFM(R5),X'88'    INP. RECFM VS,F,U ?            A48786
         BNZ   TSTVAR2            YES, SKIP BLKSI=LRECL + 4 TST  A48786
         LH    R9,LRECL(R5)       LOAD INP LRECL                 A48786
         LA    R9,4(R9)           ADD 4 TO LRECL                 A48786
         CH    R9,BLKSI(R5)       BLKSI >= LRECL + 4 ?           A48786
         BNH   TSTVAR2            YES, TEST OUTPUT               A48786
         STH   R9,BLKSI(R5)       SET BLKSI TO PREV. ABEND       A48786
         B     SETMSG2            SET ERRBIT + MSG IEB311I       A48786
TSTVAR2  CR    R5,R6              OUTP. DCB VAL ALREADY TESTED ? A48786
         BE    TSTVS1A2           YES, SCAN FURTHER              A48786
         LR    R5,R6              NO, LOAD OUT. DCB ADDR. IN R5  A48786
         LA    R3,ERRBLK2         LOAD LABEL ERRBLK2 ADDRESS     A48786
         MVI   INOUTSW+1,ERRBLKS2 SET CORRECT ERRSW.             A48786
         B     AGAINOUT           SCAN OUTPUT DCB VALUES         A48786
TSTVS1A2 LA    R5,HDCBIN          LOAD INPUT DCB ADDRESS         A48786
         TM    RECFM(R5),X'48'    INP. RECFMAT VAR. SPANNED      A48786
         BO    CHSTRCOP           YES, CHECK FOR STRAIGHT COPY   A36140
         TM    RECFM(R6),X'48'    OUTP. FORMAT VAR. SPANNED ?    A36140
         BO    CHLRECL            YES, CHECK FOR LRECL'S         A36140
         B     RETOUT             RETURN TO OPEN                 A36140
CHSTRCOP CLC   RECFM(1,R6),RECFM(R5)   COMPARE RECORD FORMATS
         BNE   CHLRECL                 NOT EQUAL-BRANCH
         CLC   LRECL(2,R6),LRECL(R5)   COMPARE LRECL'S
         BNE   CHLRECL                 NOT EQUAL-BRANCH
         CLC   BLKSI(2,R6),BLKSI(R5)   COMPARE BLOCK SIZES
         BNE   CHLRECL                 NOT EQUAL-BRANCH
         TM    HCOPYSW,X'FF'           ANY EDITING
         BZ    CHLRECL
         MVI   GENS3COP,X'80'          SET COPY SWITCH
         LA    R1,34                   LOAD MSG OFFSET
         SLL   R1,8                    AND
         LA    R1,34(R1)               MSG ID
         L     R15,HWRMSGA             LOAD ADDRESS OF MSG WRITER
         BALR  R14,R15                 AND LINK
         OI    GENS3SW,X'08'      SET GENS3 SW. FOR MODULE SEL.  A36140
         B     RETOUT             RETURN TO OPEN UT2             A36140
CHLRECL  TM    LRECL(R5),X'80'         INPUT LRECL EXCEEDS 32K
         BZ    CHKLREC2                NO
RECMSG   LA    R1,28
         SLL   R1,8
         LA    R4,28
         LA    R15,12                  LOAD CONDITION CODE=12
         BAL   R14,CHCONC              TEST FOR HIGHEST
         B     ERRORPNT
CHKLREC2 TM    LRECL(R6),X'80'         OUTPUT LRECL EXCEEDS 32K'
         BO    RECMSG                  YES-BRANCH
         TM    HDCBOUT+17,X'20'        OUTPUT DEVICE IS DA'
         BZ    CHKKEYIN
         CLI   HDCBOUT+16,X'00'        KEYLENGTH IS ZERO?
         BE    CHKKEYIN                YES-BRANCH
         LA    R1,20
         SLL   R1,8
         LA    R4,20                   AND
           LA  R15,12                  LOAD CONDITION CODE =12
         BAL   R14,CHCONC              TEST FOR HIGHEST
         B     ERRORPNT                PRINT
CHKKEYIN TM    HDCBIN+17,X'20'         INPUT DEVICE IS DA
         BZ    INTMCLOS                NO-BRANCH
         CLI   HDCBIN+16,X'00'         KEYLENGTH IS ZERO
         BE    INTMCLOS                YES-BRANCH
         LA    R1,21
         SLL   R1,8
         LA    R4,21
         LA    R15,12                  LOAD CONDITION CODE = 12
         BAL   R14,CHCONC              TEST FOR HIGHEST
         B     ERRORPNT                WRITE MSG AND TERMINATE
INTMCLOS CLI   HPOID,X'02'        OUTPUT PO ?                    A36140
         BE    VBSERROR           YES, NOT ALLOWED IN PDS        A36140
OPENQSAM OI    GENS3SW,X'0C'      SET SW FOR GENS3 MOD + QSAM  @YM04133
         B     RETOUT             RETURN TO OPEN                 A36140
VBSERROR LA    R1,29              LOAD MESS ID                   A36140
         SLL   R1,8                                              A36140
         LA    R4,29              LOAD MESS IEB329I              A36140
         LA    R15,12             LOAD COND CODE EQ 12           A36140
         BAL   R14,CHCONC         TEST FOR HIGHEST COND CODE     A36140
         B     ERRORPNT           WRITE MESS AND TERM AFT. OPEN  A36140
CALLS3A  EQU   *                                               @ZA30822
         NI    OPENSW,X'00'           RESET OPENIN(OUT) SWITCH @ZA30822
         TM    GENS3SW,X'04'          EDITING TO BE DONE       @ZA30822
         BNO   TOGENS3                NO, DON'T OPEN FOR QSAM  @ZA30822
         CLOSE (HDCBIN,REREAD)         CLOSE INPUT DATA SET    @AZ07398
         TM    TAPESW,X'01'           SYSUT2 TAPE              @ZA30822
         BO    TAPE                   YES, DON'T REOPEN UT2    @ZA30822
         CLOSE (HDCBOUT,REREAD)        CLOSE OUTPUT DATA SET   @AZ07398
         MVC   HDCBIN+MACRF(2),QSGETMOV YES, OPEN INPUT/OUTPUT @ZA11962
         MVC   HDCBOUT+MACRF(2),QSPUTMOV   FOR QSAM.           @ZA11962
         B     SECOPEN                 OPEN SYSUT1/SYSUT2      @ZA30822
TAPE     EQU   *                                               @ZA30822
         NI    TAPESW,X'FE'            RESET TAPE SW2          @ZA30822
         MVI   INXL,X'00'              RESET OPENINXL FOR      @ZA30822
*                                      SECOND OPEN OF SYSUT1   @ZA30822
         OI    ITLX,X'80'              SET HITLX AS LAST ENTRY @ZA30822
         MVC   HDCBIN+MACRF(2),QSGETMOV QSAM INPUT             @ZA30822
SECOPEN  EQU   *                                               @ZA30822
         OPEN  (HDCBIN,INPUT)          REOPEN FOR QSAM/BSAM    @ZA30822
         LA    14,HDCBIN               POINT TO INPUT DCB      @YM04133
         TM    OFLGS(R14),X'10'        SUCCESSFUL OPEN?        @YM04133
         BZ    DDERROR                 NO -WRITE MSG AND TERM  @YM04133
         LA    14,HDCBOUT              POINT TO OUTPUT DCB     @YM04133
         TM    OFLGS(R14),X'10'        SUCCESSFUL OPEN?        @YM04133
         BZ    DDERROR                 NO -WRITE MSG AND TERM  @YM04133
         L     R1,SAVEREG1             RESTORE REG1 FOR GENS3  @YM04133
TOGENS3  EQU   *                                               @ZA30822
         OI    ITLX,X'80'          SET TRAILER LABEL EXIT AS   @ZA30822
*                                      LAST ENTRY IN CASE OF   @ZA30822
*                                         CONCATINATED INPUT.  @ZA30822
         CALL  IEBGENS3
         BAL   R14,CHCONC              TEST COND.CODE FROM IEBGENS3
         CH    R15,TWELVE              RETURN CODE =12             UL0I
         BE    HTERMU                  YES - ABNORMAL TERM         UL0I
         CH    R15,SIXTEEN             RETURN CODE =16             UL0I
         BE    HTERMU                  YES - ABNORMAL TERM         UL0I
         B     CLOSER3
HSTOW3   AR    R1,R4
         L     R15,HWRMSGA
         BALR  R14,R15
HTERMU   LH    R15,HCONCODE
         NI    HOUXL+4,X'00'           DEACTIVATE
         OI    HOUXL+4,X'80'           OUTPUT
         NI    HINXL,X'00'             AND INPUT
         OI    HINXL,X'80'             EXIT LISTS
         B     CLOSER3
HEOD     DS    0F                      DUMMY
         B     CLOSER3                 DUMMY
HOUER    EQU   HEOD                    DUMMY
HINER    EQU   HEOD                    DUMMY
WRTEXMSG ST    R14,SAVE14              SAVE REGISTER 14
         L     R1,VADDR2               LOAD ADDRESS OF MESSAGE
         SR    R14,R14                 LOAD EXIT COUNT
         IC    R14,EXCOUNT
         LA    R14,240(R14)            MAKE EBCDIC CODE
         STC   R14,0(R1)               STORE NUMBER INTO MESSAGE
         MVC   11(14,R1),0(R4)         SET ROUTINE IND.
         LA    R1,24                   MESSAGE OFSET
         SLL   R1,8
         LA    R4,24                   MSG ID
         AR    R1,R4
         L     R15,HWRMSGA             LOAD MSG WRITER ADDR
         BALR  R14,R15                 LINK
         SR    R5,R5                   CLEAR REGISTERS
         SR    R4,R4                   4 &5
         IC    R5,LASTRETC             LOAD LAST RETURNCODE
         LA    R3,10                   DIVIDE BY
         DR    R4,R3                   TEN
         L     R1,VADDR3               LOAD MSG ADDRESS
         LA    R4,240(R4)              MAKE EBCDIC OF FIRST DIGIT
         LA    R5,240(R5)              MAKE EBCDIC OF SEC  DIGIT
         STC   R5,21(R1)               STORE FIRST DIGIT
         STC   R4,22(R1)               STORE SECOND DIGIT
         LA    R1,25                   LOAD MSG CODE
         SLL   R1,8
         LA    R4,25                   LOAD MSG ID
         AR    R1,R4
         L     R15,HWRMSGA
         BALR  R14,R15                 LINK
         MVI   EXCOUNT,X'00'           RESET EXIT COUNT
         MVI   LASTRETC,X'00'          RESET LAST RETURN CODE
         L     R14,SAVE14              RESTORE REGISTER 14
         BR    R14                     RETURN
*
CHCONC   CH    R15,HCONCODE            TEST NEW COND.CODE AGAINST OLD
         BCR   4,R14                   NEW IS LOW-RETURN
         STH   R15,HCONCODE            OLD IS LOW -STORE NEW ONE
         BR    R14                     RETURN
HDCBIN   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GM),                      *
               EXLST=HINXL,SYNAD=HINER,EODAD=HEOD
HDCBOUT  DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(PM),                      *
               EXLST=HOUXL,SYNAD=HOUER
*
HIHLX    LA    R3,OFFSET1              COMPUTE AND LOAD
         SR    R15,R3                  THE BASE ADDRESS OF GENR3
         LR    R2,R15                  IN REGISTER 2
         LA    R7,4095(R2)            SET R7 AS SECOND BASEREG @ZA26660
         LA    R7,1(R7)                INCREASE R7 WITH ONE    @ZA26660
         ST    R14,SAVE14              SAVE REGISTER 14
         MVI   RETURNC,X'04'           SET ASSUMED RETURNCODE
         TM     8(R1),X'80'            I/O ERROR?
         BO    LABIOER1                YES-BRANCH
         B     HIHLX1
HIHRETRN TM    OPENOUTS,X'01'          ARE WE IN OPEN OUTPUT
         BZ    RETRNA                  NO - BRANCH
         LTR   R15,R15                 IS THERE A OUTPUT LABEL
         BZ    RETRNA                  NO - BRANCH
         L     R3,0(R1)
         LTR   R3,R3                   IS THERE A LABEL BUFFER
         BZ    RETRNA                  NO - BRANCH
         SR    R4,R4                   LOAD NUMBER OF LABELS
         IC    R4,BACKUP               IN BACK UP AREA
         LA    R4,1(R4)                INCREASE BY ONE
         STC   R4,BACKUP               STORE NEW NUMBER
         L     R4,BACKUPX              LOAD INDEZ IN BACK UP AREA
         MVC   0(80,R4),0(R3)          MOVE OUTPIT LABEL IN BACK UP
         LA    R4,80(R4)               AREA AND INCREASE INDEX
         ST    R4,BACKUPX              STORE NEW INDEX
RETRNA   L     R14,SAVE14              RESTORE REG 14
         RETURN ,
HIHLX1   L     R3,0(R1)                FIRST LABEL
         LTR   R3,R3                   LABEL BUFFER ADDRESS IS ZERO
         BZ    CHUSHIEX                YES-BRANCH-TEST USER EXIT
         CLI   3(R3),X'F1'             OF THE GROUP?
         BNE   HIHLX2                  NO-BRANCH
         OI    USEXSW,X'80'            RESET USER EXIT SWITCH
HIHLX2   TM    OPENSW,X'F0'            OPENIN SWITCH ON
         BZ    CHUSHIEX                NO-BRANCH
         TM    LABPROSW,X'B0'          LABEL PROCESSING IS NO/INPUT
         BZ    CHUSHIEX                YES-BRANCH
         IC    R4,LABPOLAT             LOAD NUMBER OF STORED LABELS
         LA    R4,1(R4)                INCREASE NUMBER BY ONE
         STC   R4,LABPOLAT             STORE NUMBER OF LABELS
         L     R4,HLABINDX             LOAD ADDRESS FOR NEXT LABEL
         MVC   0(80,R4),0(R3)          MOVE LABEL INTO SAVE AREA
         LA    R4,80(R4)               UPDATE
         ST    R4,HLABINDX             LABEL POOL INDEX
CHUSHIEX TM    USEXSW,X'80'            USER EXIT REQUIRED?
         BZ    SETUSEXS                NO-RETURN
         L     R15,ADINUHLR            LOAD ADDRESS OF USER ROUTINE
         LTR   R15,R15                 TEST FOR USER ADDRESS
         BZ    SETUSEXS                NO ADDRESS-BRANCH         A44327
         IC    R14,EXCOUNT             INCREASE
         LA    R14,1(R14)              EXIT
         STC   R14,EXCOUNT             COUNTER
         BALR  R14,R15
         STC   R15,LASTRETC            STORE LAST RETURN CODE
AFTLINK1 TM    OPENSW,X'F0'            ARE WE IN OPEN
         BZ    RETRFUS                 NO-BRANCH
TESTLABP TM    LABPROSW,X'38'          LABEL PROCESS ALL/ONLY/INPUT
         BZ    RETRFUS                 NO-BRANCH
         LTR   R15,R15                 RETURNCODE=0?
         BZ    SETUSEXS                YES-BRANCH
         CH    R15,SIXTEEN             RETURN CODE=16?
         BC    7,HIHRETRN              NO-BRANCH
         OI    DELTERSW,X'80'          SET DELAYED TERMINATION SW
SETUSEXS NI    USEXSW,X'7F'            SET USER EXIT SWITCH OFF
         SR    R15,R15                 LOAD
         IC    R15,RETURNC             RETURNCODE
         B     HIHRETRN                RETURN
RETRFUS  CH    R15,SIXTEEN             RETURN CODE IS 16
         BNE   HIHRETRN                NO-BRANCH
         OI    HTERMSW,X'FF'           SET USER TERMINATION SWITCH
         B     HIHRETRN
LABIOER1 LA    R4,INPUTHDR             LOAD INPUT HEADER IND.
         BAL   R14,WRTLABER            LINK TO WRITE ROUTINE
         B     CHUSHIEX                BRANCH TO USER
*
HITLX    LA    R3,OFFSET2              COMPUTE AND LOAD
         SR    R15,R3                  THE BASE ADDRESS OF GENR3
         LR    R2,R15                  IN REGISTER 2
         LA    R7,4095(R2)            SET R7 AS SECOND BASEREG @ZA26660
         LA    R7,1(R7)                INCREASE R7 WITH ONE    @ZA26660
         ST    R14,SAVE14              SAVE REGISTER 14
         MVI   RETURNC,X'04'            SET ASSUMED RETURCODE
         TM     8(R1),X'80'            I/O ERROR?
         BO    LABIOER2                YES-BRANCH
         L     R3,0(R1)                LOAD LABEL BUFFER ADDRESS
         LTR   R3,R3                   LABEL BUFFER ADDRESS IS ZERO
         BZ    CHUSTIEX                YES-BRANCH-TEST USER EXIT
         TM    FIRSTSW,X'F0'      FIRST LABEL ?                  A48786
         BO    HITLX1             NO, SKIP COUNT/SWITCHES INIT.  A48786
         OI    USEXSW,X'80'            RESET USER EXIT SWITCH
         MVI   EXCOUNT,X'00'           RESET EXIT COUNTER
         MVI   LASTRETC,X'00'
HITLX1   TM    LABPROSW,X'B0'          LABEL PROCESSING IS NO/INPUT
         BZ    CHUSTIEX                YES-BRANCH
         TM    4(R1),X'80'             TEST FOR END OF DATA
         BNO   CHUSTIEX            NO, INTERMED. TRAILER         A36140
         TM    FIRSTSW,X'F0'      FIRST LABEL ?                  A48786
         BO    HITLX2             NO, SKIP COUNT/SWITCHES INIT.  A48786
         MVI   FIRSTSW,X'F0'      SET FIRSTSW OFF                A48786
         L     R4,LABPOLAT+4           LOAD ADDRESS OF TRAILER
         LA    R4,0(R4)                LABEL POOL
         ST    R4,TLABINDX             INITIALE LABELPOOL INDEX
         SR    R4,R4                   SET NUMBER OF
         STC   R4,LABPOLAT+4           LABEL IS 0
HITLX2   IC    R4,LABPOLAT+4           LOAD NUMBER OF STORED LABELS
         LA    R4,1(R4)                INCREASE BY ONE
         STC   R4,LABPOLAT+4           STORE
         L     R4,TLABINDX             LOAD ADDRESS FOR NEXT LABEL
         L     R3,0(R1)                LOAD ADDRESS OF CURR LABEL
         MVC   0(80,R4),0(R3)          MOVE LABEL
         LA    R4,80(R4)               UPDATE
         ST    R4,TLABINDX             LABELPOOL INDEX
CHUSTIEX TM    USEXSW,X'80'            USER EXIT REQUIRED
         BZ    SETUSEXS                NO-RETURN
         L     R15,ADINUTLR            LOAD ADDRESS OF USER ROUTINE
         LTR   R15,R15                 CHECK USER EXIT
         BZ    SETUSEXS                NO EXIT-BRANCH            A44327
         IC    R14,EXCOUNT             UPDATE
         LA    R14,1(R14)              EXIT
         STC   R14,EXCOUNT             COUNTER
         BALR  R14,R15                 LINK
         STC   R15,LASTRETC            STORE LAST RETURN CODE
         B     TESTLABP                BRANCH TO HEADER ROUTINE
LABIOER2 LA    R4,INPUTTLR             LOAD INPUT TRAILER IND.
         BAL   R14,WRTLABER            LINK TO WRITE ROUTINE
         B     CHUSTIEX                BRANCH CHECK FOR USER EXIT
*
HOHLX    LA    R3,OFFSET3              COMPUTE AND LOAD
         SR    R15,R3                  THE BASE ADDRESS OF GENR3
         LR    R2,R15                  IN REGISTER 2
         LA    R7,4095(R2)            SET R7 AS SECOND BASEREG @ZA26660
         LA    R7,1(R7)                INCREASE R7 WITH ONE    @ZA26660
         ST    R14,SAVE14              SAVE REGISTER 14
         MVI   RETURNC,X'08'           SET ASSUMED RETURNCODE
         TM     8(R1),X'80'            I/O ERROR
         BO    LABIOER3
         TM    OPENOUTS,X'01'          ARE WE IN OPEN
         BZ    CHUSHOEX                NO-BRANCH
         SR    R4,R4                   ZERO OUT REG 4
         IC    R4,LABPOLAT             LOAD NUMBER OF LABELS TO
         LTR   R4,R4                   STORE AND TEST FOR ZERO
         BZ    RETRNC0A                ZERO-DO NOT STORE
         L     R3,0(R1)                LOAD ADDRESS OF LABELBUFFER
         LTR   R3,R3                   LABEL BUFFER ADDRESS IS ZERO
         BZ    CHUSHOEX                YES-BRANCH-TEST USER EXIT
         BCTR  R4,0                    DECREASE NUMBER
         STC   R4,LABPOLAT             STORE NEW NUMBER
         L     R4,HLABINDX             LOAD ADDRESS FOR NEXT LABEL
         MVC   0(80,R3),0(R4)          MOVE LABEL INTO BUFFER
         LA    R4,80(R4)               INCREASE INDEX
         ST    R4,HLABINDX             AND RESTORE
         TM    USEXSW,X'80'            USER EXIT REQUIRED
         BZ    SETUSEXS                NO-RETURN
CHUSHOEX L     R15,ADOTUHLR            YES-LOAD ADDRESS OF USER
         LTR   R15,R15                 CHECK USER EXIT
         BZ    SETUSEXS                NO EXIT-BRANCH            A44327
         IC    R14,EXCOUNT             UPDATE
         LA    R14,1(R14)              EXIT
         STC   R14,EXCOUNT             COUNTER
         BALR  R14,R15                 ROUTINE AND LINK
         STC   R15,LASTRETC            STORE RETURN CODE
AFTLINK2 TM    OPENOUTS,X'01'          ARE WE IN OPEN
         BZ    RETRFUS                 NO-BRANCH
         B     TESTLABP
RETRNC0A MVI   RETURNC,X'00'           SET RETURNCODE=0
         B     CHUSHOEX
LABIOER3 LA    R4,OTPUTHDR             LOAD OUTPUT HEADER IND.
         BAL   R14,WRTLABER            LINK
         B     CHUSHOEX                BRANCH TO CHECK FOR USER
*
HOTLX    LA    R3,OFFSET4              COMPUTE AND LOAD
         SR    R15,R3                  THE BASE ADDRESS OF GENR3
         LR    R2,R15                  IN REGISTER 2
         LA    R7,4095(R2)            SET R7 AS SECOND BASEREG @ZA26660
         LA    R7,1(R7)                INCREASE R7 WITH ONE    @ZA26660
         ST    R14,SAVE14              SAVE REGISTER 14
         MVI   RETURNC,X'08'           SET ASSUMED RETURNCODE
         TM    8(R1),X'80'             I/O ERROR
         BO    LABIOER4
         TM    CLOSEOUT,X'02'          ARE WE IN CLOSE ?
         BZ    CHUSTOEX                NO-BRANCH
         SR    R4,R4                   ZERO OUT REG 4
         IC    R4,LABPOLAT+4           LOAD NUMBER OF LABELS TO
         LTR   R4,R4                   STORE AND TEST FOR ZERO
         BZ    RETRNC0B                ZERO-BRANCH
         BCTR  R4,0                    DECREASE NUMBER
         STC   R4,LABPOLAT+4           STORE NEW NUMBER
         L     R4,TLABINDX             LOAD ADDRESS OF NEXT LABEL
         L     R3,0(R1)                LOAD ADDRESS OF LABEL BUFFER
         LTR   R3,R3                   LABEL BUFFER ADDRESS IS ZERO
         BZ    CHUSTOEX                YES-BRANCH-TEST USER EXIT
         MVC   0(80,R3),0(R4)          MOVE LABEL INTO BUFFER
         LA    R4,80(R4)               INCREASE INDEX
         ST    R4,TLABINDX             AND RESTORE
         TM    USEXSW,X'80'            USER EXIT REQUIRED
         BZ    SETUSEXS                NO-RETURN
CHUSTOEX L     R15,ADOTUTLR            YES-LOAD ADDRESS OF USER
         LTR   R15,R15                 CHECK USER EXIT
         BZ    SETUSEXS                NO EXIT-BRANCH            A44327
         IC    R14,EXCOUNT             UPDATE
         LA    R14,1(R14)              EXIT
         STC   R14,EXCOUNT             COUNT
         BALR  R14,R15                 ROUTINE AND LINK
         STC   R15,LASTRETC            STORE LAST RETURN CODE
AFTLINK3 TM    CLOSEOUT,X'02'          ARE WE IN CLOSE
         BZ    RETRFUS                 NO-BRANCH
         B     TESTLABP
RETRNC0B MVI   RETURNC,X'00'           SET RETURN CODE =0
         B     CHUSTOEX
LABIOER4 LA    R4,OTPUTTLR             LOAD OUTPUT TRAILER IND.
         BAL   R14,WRTLABER
         B     CHUSTOEX
WRTLABER STM   R13,R1,SAVE14D          SAVE 13 TILL2
         LA    R13,PUTSAVAR            POINT13 TO NEW SAVE AREA
         L     R1,VADDR4               LOAD MESSAGE ADDR.
         MVC   0(14,R1),0(R4)          MOVE LABEL IND
         LA    R1,26
         SLL   R1,8
         LA    R4,26
         AR    R1,R4
         L     R15,HWRMSGA
         BALR  R14,R15
         MVI   RETURNC,X'00'           SET RETURN CODE =0
           LA  R15,12                  LOAD CONDITION CODE =12
         BAL   R14,CHCONC              TEST FOR HIGHEST
         MVI   LABIORSW,X'FF'          SET LABEL I/O ERROR SWIRCH
         LM    R13,R1,SAVE14D          RESTORE REGISTERS
         BR    R14
QSOHLX   TM    8(R1),X'80'             OUTPUT ERROR ON LABEL
         BO    LABIOER5                YES - BRANCH
         SR    R4,R4                   LOAD NUMBER OF LABELS
         IC    R4,BACKUP               IN BACK UP AREA
         LTR   R4,R4                   TEST FOR ZERO
         BZ    QSRET0                  ZERO - BRANCH
         L     R3,0(R1)
         LTR   R3,R3                   IS THERE A LAVEL BUFFER
         BZ    QSRET0                  NO - BRANCH
         BCTR  R4,0                    DECREASE NUMBER OF LABELS IN
         STC   R4,BACKUP               BACK UP AREA AND STORE
         L     R4,BACKUPX              LOAD INDEX IN BACK UP AREA
         MVC   0(80,R3),0(R4)          MOVE LABEL TO BUFFER
         LA    R4,80(R4)               INCREASE BACK UP AREA INDEX
         ST    R4,BACKUPX              AND STORE
         LA    R15,8                   LOAD RETURN CODE
         RETURN
LABIOER5 ST    R14,SAVE14              SAVE REG 14
         LA    R4,OTPUTHDR             LOAD OUTPUT HEADER IND.
         BAL   R14,WRTLABER            LINK TO WRITE ROUTINE
         L     R14,SAVE14              RESTORE REG 14
QSRET0   SR    R15,R15
         RETURN
*
HINXL    DS    0F                      INPUT EXIT LIST
         DC    X'00'
         DC    AL3(HIHLX)
ITLX     DC    X'00'                                           @ZA30822
         DC    AL3(HITLX)
INXL     DC    X'85'                    INPUT DCB EXIT         @ZA30822
         DC    AL3(OPENINXL)
*
HOUXL    DS    0F                       OUTPUT EXIT LIST
         DC    F'0'                    TOTALING AREA ADDRESS
         DC    X'00'
         DC    AL3(HOHLX)
         DC    X'00'
         DC    AL3(HOTLX)
OUXL     DC    X'05'                    OUTPUT DCB EXIT        @ZA30822
         DC    AL3(OPENOUXL)
JFCB     DC    X'87'                    INDICATE JFCB ENTRY    @ZA30822
         DC    AL3(JFCBAREA)            ADDRESS TO JFCB AREA   @ZA30822
*
OPENINXL LH    R4,BLKSI(R1)            GET BLKSIZE FROM DCB
         TM    RECFM(R1),X'C0'   RECFM. UNDEFINED               PTM5314
         BM    SKIPIN        NO, RECFM F(B) OR V(B)(S)           A36140
         MVC   LRECL(2,R1),BLKSI(R1)  YES, SET LRECL EQUAL BLKS PTM5314
         OI    RECFM(R1),X'C0'     SET DEFAULT RECFM TO U.       A36140
SKIPIN   MVI   BUFNO(R1),ZERO  SET BUFFER TO ZERO                A31435
         MVC   BUFL(2,R1),ZEROES  SET BUFFER LENGTH TO ZERO      A31435
         STM  R13,R14,SAVEINP     SAVE REGISTERS                 A36140
         L     R4,ADINUHLR             WAS INPUT HEADER EXIT     A36140
         LTR   R4,R4                   ROUTINE SPECIFIED ?       A36140
         BZ    HGENP4B                 NO, OPEN SYSUT2           A36140
         LA    R4,INPUTHDR             LOAD INPUT HEADER IND.    A36140
         BAL   R14,WRTEXMSG            LINK TO WRITE ROUTINE     A36140
         CLI   HTERMSW,X'FF'      TERMIN USER REQUEST ?          A36140
         BE    RETINP             YES, END OPEN AND RETURN       A36140
         B     HGENP4B            OPEN SYSUT2 NOW                A36140
RETINP   LM    R13,R14,SAVEINP    RESTORE REGISTERS              A36140
         BR    R14                RETURN TO OPEN SYSUT1          A36140
*
OPENOUXL LH    R4,BLKSI(R1)            GET BLKSIZE FROM DCB
         TM    RECFM(R1),X'C0'   RECFM. UNDEFINED               PTM5314
         BM    SKIPOUT       NO, RECFM=F(B)(S) OR V(B)(S)        A36140
         OI    RECFM(R1),X'C0'     SET DEFAULT RECFM TO U.       A36140
SKIPOUT  MVI   BUFNO(R1),ZERO  SET BUFFER TO ZERO                A31435
         MVC   BUFL(2,R1),ZEROES  SET BUFFER LENGTH TO ZERO      A31435
         STM   R13,R14,SAVEOUT    SAVE REGISTERS                 A36140
         B     CHKRECFM           GO TO RECFM CHECK ROUTINE      A36140
RETOUT   LM    R13,R14,SAVEOUT    RESTORE REGISTERS              A36140
         TM    TAPESW,X'10'       IS TAPE SW1 ON               @ZA30822
         BO    AFTJFCB            YES, GO BACK AND OPEN UT2    @ZA30822
         BR    R14                 RETURN TO OPEN
*
DDERROR  CLI   LABIORSW,X'FF'          TEST LABEL I/O ERROR SWITVH
         BNE   DDERR1                  NO I/O ERROR - BRANCH
         MVI   LABIORSW,X'00'          RESET SWITCH
         B     HTERMU                  TERMINATE
DDERR1   L     R14,VADDR               ADDR OF ERROR MESSAGE     A29998
         MVC   7(8,R14),0(R15)   MOVE DDNAME TO MSG
         LA    R1,15      MSG OFFSET
         SLL   R1,8
         LA    R4,16      MSG NUMBER
         AR    R1,R4                   COMPUTE COMPLETE MSG CODE
         L     R15,HWRMSGA             LOAD MSG WRITER ADDRESS
         BALR  R14,R15                 LINK TO MSG WRITER
         LA    R15,12      SET CONDITION CODE
         STH   R15,HCONCODE
         B     HTERMU                  TERMINATE
VADDR    DC    A(DDERROR1)
         EXTRN DDERROR1
*
ERRBLK1  LA    R15,12                  SET RETURN CODE OF 12
         STH   R15,HCONCODE
         LA    R1,HMSGBLK1             PUT MSG CODE IN BYTE 2
         SLL   R1,8                       OF REG1
         LA    R4,17                   MESSAGE ID
         TM    ERRBLKSW,ERRBLKS2  ALSO OUTPUT ERROR ?            A48786
         BNO   HSTOW3             NO, PRINT MSG IEB317I/IEB312I  A48786
         AR    R1,R4              ADD MSG ADDR. DISPL.           A48786
         L     R15,HWRMSGA        LOAD MSG WRITER ADDRESS        A48786
         BALR  R14,R15            BRANCH TO MSG WR. ROUTINE      A48786
ERRBLK2  LA    R15,12                  SET RETURN CODE OF 12
         STH   R15,HCONCODE
         LA    R1,HMSGBLK2             PUT MSG CODE IN BYTE 2
         SLL   R1,8                       OF REG1
         LA    R4,18                   MESSAGE ID
         B     HSTOW3
         DS    0D
         DC    CL8'DEB.AREA'           SUPERZAP DEBUGGING WORKAREA
         DC    38F'0'                  SHOULD BE DELETED AT COMPLET
GENS3SW  DC    X'00'
EXCOUNT  DC    X'00'
RETURNC  DC    X'00'
LABPOLAT DC    2F'0'                   LABEL POOL ADDRESSES TABLE
BACKUP   DC    F'0'                    BACK UP AREA ADDRESS
BACKUPX  DC    F'0'                    BACK UP AREA INDEX
VADDR2   DC    V(HMSGEXIT)
VADDR3   DC    V(HMSGRETC)
LASTRETC DC    X'00'
INPUTHDR DC    CL14' INPUT  HEADER'
OTPUTHDR DC    CL14'OUTPUT  HEADER'
INPUTTLR DC    CL14' INPUT TRAILER'
OTPUTTLR DC    CL14'OUTPUT TRAILER'
SAVE14D  DS    5F                      SAVE AREA FOR REGS 13-1
VADDR4   DC    V(HMSGIOER)
R3SAVER  DS    18F                     REGISTER SAVE AREA
PUTSAVAR DS    18F                     SAVE AREA FOR PUT
SHOUXL   DS    4C
HCOPYSW  DS    1C
HPOID    DS    1C
LABPROSW DS    1C
POSWITCH DS    1C
SAVE14   DS    1F
SAVEREG1 DS    1F
SHINXL   DS    1C
USEXSW   DS    1C
INPUTDD  DC    8C' '                   AREA FOR INPUT DD NAME    A29998
OUTPUTDD DC    8C' '                   AREA FOR OUTPUT DD NAME   A29998
ZEROES   DC    H'0'                                              A31435
HWRMSGA  DC    V(HWRMSG)
HTERMSW  DC    X'00'
FIRSTSW  DC    X'00'              FIRST ENTRY SWITCH             A48786
HCONCODE DC    H'0'
         DS    0H
BSREAD   DC    BL2'0010000000000000'
BSWRITE  DC    BL2'0000000000100000'   MACRF FOR BSAM WRITE    @YM02300
QSGETMOV DC    BL2'0101000000000000'   MACRF FOR GET MOVE
QSPUTMOV DC    BL2'0000000001010000'   MACRF FOR PUT MOVE
QSOHLXAD DC    AL3(QSOHLX)
SIXTEEN  DC    H'16'
TWELVE   DC    H'12'                                               UL0I
LABIORSW DC    X'00'
DELTERSW DC    X'00'
GENS3COP DC    X'00'
OPENSW   DC    X'00'
OPENOUTS EQU   OPENSW
CLOSEOUT EQU   OPENSW
OFFSET1  EQU   HIHLX-R3START
OFFSET2  EQU   HITLX-R3START
OFFSET3  EQU   HOHLX-R3START
OFFSET4  EQU   HOTLX-R3START
ADINUHLR DC    F'0'
ADINUTLR DC    F'0'
ADOTUHLR DC    F'0'
ADOTUTLR DC    F'0'
HLABINDX DS    1F
SAVEINP  DS    2F                 REGISTERS DUR. OPEN UT1        A36140
SAVEOUT  DS     2F                REGISTERS DUR. OPEN UT2        A36140
TLABINDX DS    1F
SYSUT1H  DC    CL8'1 HEADER'
HMSGTLR  DC    CL32'SYSUT         EXIT -REQ.JOB TERM'
VADDR1   DC    A(HMSGTLR)
SYSUT2H  DC    CL8'2 HEADER'
TAPESW   DC    X'00'                   TAPE SW1 AND SW2        @ZA30822
SAVEXPDT DS    CL3                     SAVE AREA FOR EXPDT     @ZA11962
SAVEIND1 DS    CL1                     SAVE AREA FOR JFCBIND1  @ZA11962
         DS    0F                                              @ZA11962
JFCBAREA DS    0CL176                  JFCB AREA               @ZA11962
JFCBDSNM DS    CL44                    DATA SET NAME           @ZA18366
JFCBELNM DS    CL8                     MEMBER NAME             @ZA18366
         DS    CL14                                            @ZA24162
JFCBLTYP DS    CL1                     LABEL TYP               @ZA24162
         DS    CL16                                            @ZA24162
JFCBXPDT DS    CL3                     EXPDT DATE              @ZA11962
JFCBIND1 DS    CL1                     JFCBIND1                @ZA11962
JFCBIND2 DS    CL1                     JFCBIND2                @ZA18366
         DS    CL5                                             @ZA30822
JFCBDEV  DS    CL1                                             @ZA30822
         DS    CL4                                             @ZA30822
JFCDSORG DS    CL2                                             @ZA30822
JFCRECFM DS    CL1                                             @ZA30822
JFCOPTCD DS    CL1                                             @ZA30822
JFCBLKSI DS    CL2                                             @ZA30822
JFCLRECL DS    CL2                                             @ZA30822
         DS    CL70                                            @ZA30822
         ENTRY GENS3COP
         ENTRY HMSGTLR
         ENTRY HDCBIN
         ENTRY HDCBOUT
SYSUT2T  DC    CL8'2 TRAILR'
SYSUT1T  DC    CL8'1 TRAILR'
HOUP2J   EQU   CLOSER3                 DUMMY
HINDD    EQU   24
HOUDD    EQU   32
HMSDELTR EQU   22
ZERO     EQU   0                                                 A31435
HMSGIIP  EQU   1
HMSGBLK1 EQU   17
HMSGBLK2 EQU   18
HMSEQDCB EQU   51                 WARNING MESSAGE IEB352I        A48786
LIXL     EQU   1
LOXL     EQU   4
MLABONLY EQU   23
ERRBLKSW DC    FL1'0'                  NO BLKSIZE SPECIFIED --
ERRBLKS1 EQU   1                            FOR INPUT DCB
ERRBLKS2 EQU   2                            FOR OUTPUT DCB
         DCBD  DSORG=PS
DDNAM    EQU   DCBDDNAM-IHADCB
OFLGS    EQU   DCBOFLGS-IHADCB
DSORG    EQU   DCBDSORG-IHADCB
BLKSI    EQU   DCBBLKSI-IHADCB
KEYLE    EQU   DCBKEYLE-IHADCB
BUFNO    EQU   DCBBUFNO-IHADCB                                   A31435
BUFL     EQU   DCBBUFL-IHADCB
RECFM    EQU   DCBRECFM-IHADCB
EXLST    EQU   DCBEXLSA-IHADCB                                 @ZA16460
LRECL    EQU   DCBLRECL-IHADCB
MACRF    EQU   DCBMACR-IHADCB
IND2     EQU   DCBCIND2-IHADCB                                 @YM03265
         END
