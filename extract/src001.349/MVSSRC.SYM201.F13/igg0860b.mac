         TITLE 'IGG0860B - THIRD LOAD MODULE FOR SVC 86'         S20201
         COPY  LCGASMSW
IGG0860B CSECT
*                                           SA66582 = OY02760 = OX03068
*
*        VS1 RELEASE 5.0 DELETIONS/CHANGES                     @X50RSAG
*A 520500                                                      @X50RSAG
*C 209600                                                      @X50RSAG
*D 209620-209900                                               @X50RSAG
* FIX BAD HA/R0 MSG.                                           @ZM40498
*          RELEASE 21 DELETIONS/CHANGES
*          RELEASE 20 DELETIONS/CHANGES
*0792001000-002000                                               S20201
* 181000-182000,186000,189000-194000                             M4693
*
*STATUS CHANGE LEVEL 002
*
*TITLE IGG0860B - THIRD LOAD MODULE OF SVC 86                         *
*                                                                     *
*FUNCTION/OPERATION                                                   *
*   IGG0860B ESTABLISHES ITS OWN ADDRESSABILITY, THAT OF THE 688-     *
*BYTE WORK AREA GOTTEN BY IGC0008F, AND THAT OF THE COUNT FIELD WORK  *
*AREA GOTTEN BY IGG0860A.  IT EXECUTES A CHANNEL PROGRAM TO READ HOME *
*ADDRESS, RECORD ZERO, AND COUNT FIELDS OF THE TRACK IN ERROR IN      *
*ORDER TO GET A COMPLETE DESCRIPTION OF THE TRACK.  SHOULD AN ERROR   *
*BE ENCOUNTERED IN READING A COUNT, NEW CHANNEL PROGRAMS MUST BE      *
*EXECUTED TO ASSIST IN CALCULATING KEY AND DATA LENGTHS FOR THE       *
*COUNT.                                                               *
*                                                                     *
*ENTRY POINTS                                                         *
*   IGG0860B GETS A DESCRIPTION OF THE TRACK CONTAINING THE RECORD    *
*IN ERROR.                                                            *
*                                                                     *
*INPUT                                                                *
*   CONSISTS OF THE WORK AREAS AS COMPLETED BY IGG0860A.              *
*                                                                     *
*OUTPUT                                                               *
*   CONSISTS OF THE WORK AREAS AND THE FOLLOWING RETURN CODES-        *
*                                                                     *
*        20 - INVALID I/O SENSE INDICATION                            *
*        36 - R0-HOME ADDRESS ERRORS ON BAD TRACK                     *
*        44 - UNRECOVERABLE HARD I/O ERROR IN A COUNT FIELD           *
*                                                                     *
*EXTERNAL ROUTINES                                                    *
*   NONE                                                              *
*                                                                     *
*EXITS                                                                *
*   ERROR - TRANSFERS CONTROL VIA XCTL TO IGG086AE WITH A RETURN      *
*           CODE IN REGISTER 4                                        *
*   NORMAL - TRANSFERS CONTROL VIA XCTL TO IGG0860C                   *
*                                                                     *
*TABLES/WORK AREAS                                                    *
*   THERE ARE TWO WORK AREAS AS FOLLOW-                               *
*                                                                     *
*        1.  A 688-BYTE WORK AREA INCLUDING THE FORMAT 4 DSCB,        *
*            MAXIMUM NUMBER OF COUNT FIELDS PER TRACK, A POINTER      *
*            TO THE USER'S PARAMETER LIST, THE ECB, IOB, DEB, AND     *
*            DCB CONSTRUCTED BY IGC0008F, A POINTER TO THE COUNT      *
*            FIELD WORK AREA, AN INDICATION OF THE ORIGINAL ERROR     *
*            TYPE, XCTL AREA, ADDRESS OF THE FIRST COUNT FIELD,       *
*            ADDRESS OF FIRST CCW IN NEW PORTION OF THE CHANNEL       *
*            PROGRAM, AND THE ADDRESS OF THE COUNT FIELD FOR THE      *
*            CCW IN ERROR.                                            *
*                                                                     *
*        2.  A CHANNEL PROGRAM TO READ HOME ADDRESS AND/OR RECORD     *
*            ZERO AND COUNTS.  IT ALSO INCLUDES THE AREA CONTAINING   *
*            THIS INFORMATION.                                        *
*                                                                     *
*ATTRIBUTES                                                           *
*   REENTRANT, REFRESHABLE                                            *
*                                                                     *
*
*
***********************************************************************
*                                                                     *
*                               EQUATES                               *
*                                                                     *
***********************************************************************
OFFSET   EQU   24                       USED TO EQUALIZE IOB-CCW OX3068
*                                       AND IOB-CSW CCW PTRS     OX3068
BIT1     EQU   X'40'
BIT4     EQU   X'08'
BIT6     EQU   X'02'
BIT7     EQU   X'01'
***********************************************************************
*          COMPLETION CODES                                           *
***********************************************************************
CC20     EQU   20                       NOT VALID ERROR CONDITION
CC36     EQU   36                       HA OR R0 ERRORS
CC44     EQU   44                       NO ALTERNATE ASSIGNED
***********************************************************************
*          DISPLACEMENT CONSTANTS                                     *
***********************************************************************
D0       EQU   0
D1       EQU   1
D2       EQU   2
D3       EQU   3
D4       EQU   4
D6       EQU   6
D8       EQU   8
D14      EQU   14
D16      EQU   16
D24      EQU   24
***********************************************************************
*          LENGTH CONSTANTS                                           *
***********************************************************************
L1       EQU   1
L2       EQU   2
L3       EQU   3
L4       EQU   4
L5       EQU   5
L7       EQU   7
L8       EQU   8
L20      EQU   20
**********************************************************************
*        DEVICE EQUATES                                              *
**********************************************************************
D23051   EQU   X'06'                    2305-1 DEVICE TYPE       S20201
D23052   EQU   X'07'                    2305-2 DEVICE TYPE       S20201
D3330    EQU   X'09'                    3330   DEVICE TYPE       S20201
         AIF   ('&LIB' EQ 'LIB1').X225600                       XL03145
D3330C   EQU   X'0D'                   3330 C DEVICE            XL03145
D3340    EQU   X'0A'                   3340 DEVICE              XL03130
.X225600 ANOP                                                   XL03145
***********************************************************************
*                                                                     *
***********************************************************************
BADTRK   EQU   X'02'
CSWOK    EQU   X'41'
EOF      EQU   X'0D'                    END OF FILE
ERRS2    EQU   X'02'
ERRS3    EQU   X'03'
IOEROR   EQU   X'20'
OUTEXTNT EQU   X'42'                    OUT OF EXTENT
RC1      EQU   X'01'
RHA      EQU   X'1A'                    READ HOME ADDRESS
R0ERR    EQU   X'01'                    R0 ERROR SWITCH
RR0      EQU   X'16'                    READ RECORD 0
SPCT     EQU   X'0F'                    SPACE COUNT
TIC      EQU   X'08'
X0       EQU   0
ZEROS    EQU   X'00'
*
*
         EJECT
* REGISTER ASSIGNMENTS
PARMA    EQU   0                        PARAMETER REGISTER
PARMB    EQU   1                        PARAMETER REGISTER
WORKE    EQU   2                        EVEN WORK REGISTER
WORKO    EQU   3                        ODD WORK REGISTER
FOUR     EQU   4                        RETURN CODE REGISTER
FIVE     EQU   5                        WORK REGISTER
SIX      EQU   6                        *
SEVEN    EQU   7                        *
EIGHT    EQU   8                        *
NINE     EQU   9                        *
TEN      EQU   10                       *
AREAREG  EQU   11                       MAIN WORK AREA REGISTER
BASEREG  EQU   12                       BASE REGISTER
SAVEREG  EQU   13                       WORK REGISTER
RETREG   EQU   14                       WORK REGISTER
ADDREG   EQU   15                       COMPLETION CODE REGISTER
         BALR  BASEREG,X0               ESTABLISH
         USING EXPROG,BASEREG              OWN ADDRESSABILITY
         USING WORKDSCT,AREAREG         ESTABLISH ADDRESSABILITY FOR
*                                          WORK AREA
         USING CNTAREA,TEN              ESTABLISH ADDRESSABILITY FOR
*                                          COUNT FIELD WORK AREA
* THIS SECTION EXECUTES THE CHANNEL PROGRAM
EXPROG   BAL   EIGHT,SETBLKS            CLEAR ECB AND IOB
         EXCP  IOB                      READ COUNTS
         WAIT  1,ECB=ECB                WAIT FOR I/O TO COMPLETE
         TM    HOMEAD,BADTRK            CHECK FOR FLAGGED        M4693
*                                       PRIMARY                  M4693
         BO    ISALT                    YES, PRIMARY TRACK IS    M4693
*                                       BAD                      M4693
MAKECHK  CLI   ECB,OUTEXTNT             IS TERMINATION DUE TO OUT OF
*                                          EXTENT
         BE    OUTEXT                   YES
         CLI   ECB,CSWOK                ARE CSW CONTENTS USEFUL
         BNE   SETCODE                  NO, SET ERROR CODE
         LA    SIX,IOB                  PICK UP ADDRESS OF IOB
         L     FIVE,IOBFLAG3            GET A(CCW) IN ERROR
         LA    FIVE,D0(X0,FIVE)         CLEAR HIGH ORDER BYTE
         S     FIVE,FULL8
         SPACE
*A TEST IS MADE TO SEE IF THE ERROR OCCURED IN ATLAS' CHANNEL PROGRAM.
*IF IT DID NOT HOME ADDRESS OR R0 IS IN ERROR.
         L     SEVEN,IOBSTART           GET START ADDRESS OF CHANNEL
*                                          PROGRAM
         LA    SEVEN,D0(X0,SEVEN)       CLEAR HOGH ORDER BYTE
         CLR   FIVE,SEVEN               IS CCW ADDR GREATER THAN IOB
*                                          START ADDRESS
         BNL   CHECKHI                  YES, CHECK HIGH LIMIT
TESTADDR LA    SEVEN,CCW31              GET BEGINNING ADDR OF POSSIBLE
*                                          NEW CHANNEL PROGRAM
         CLR   FIVE,SEVEN
         BL    ERRCODE                  ERROR NOT IN ATLAS' CHANNEL
*                                          PROGRAM
         LA    SEVEN,CHECKHA            CHECK HIGH LIMIT
         CLR   FIVE,SEVEN
         BNL   ERRCODE                  ERROR NOT IN ATLAS' CHANNEL
*                                          PROGRAM
         B     TESTERR                  ERROR IN ATLAS CHANNEL   M4693
*                                       PROGRAM                  M4693
CHECKHI  L     SEVEN,SAVEADDR           LOAD ADDRESS OF FIRST COUNT
*                                          FIELD
         CLR   FIVE,SEVEN               CHECK HIGH LIMIT
         BL    TESTERR                  CHECK IF ERROR IS IN NEW M4693
*                                       CHANNEL PROGRAM
         B     TESTADDR                 WAS ERROR IN NEW CHANNEL PROG
         SPACE
ISALT    EQU   *                        GET DESCRITPTION OF      M4693
*                                       ALTERNATE                M4693
         L     SEVEN,IOBSTART           START OF CCWS           OX03068
         L     FIVE,IOBFLAG3            CSW CCW PTR             OX03068
         LA    FIVE,0(FIVE)             ZERO HIGH BYTE          OX3068
         LA    SEVEN,OFFSET(SEVEN)      EQUALIZE PTRS           OX3068
         CLR   FIVE,SEVEN               COMPARE PTRS            OX3068
         BL    ERRCODE                  BRCH IF HA OR R0 ERR    OX3068
         MVC   SEEK+D3(L5),R0           MOVE ADDRESS OF ALTERNATE TO
*                                          SEEK ADDRESS
         MVC   DEBSTRCC(L4),R0          SET UP DEB EXTENTS
         MVC   DEBENDCC(L4),R0
         B     EXPROG                   EXECUTE CHANNEL PROGRAM TO GET
*                                          DESCRIPTION OF ALTERNATE
         SPACE
TESTERR  TM    IOBCSWST,BIT6            DID UNIT CHECK OCCUR
         BNO   SETCODE                  NO, SET ERROR CODE
         TM    SENSE0(SIX),BIT4         CHECK IOB SENSE BYTE0 FOR A
*                                          DATA CHECK
         BO    TESTCCW                  YES, DATA CHECK
         TM    SENSE1(SIX),BIT6         CHECK FOR MISSING ADDRESS
*                                          MARKER
         BZ    SETCODE                  NOT MISSING, SET ERROR CODE
         CLI   DEVTAB+D3,D3330          CHECK FOR 3330           S20201
         BNL   SETCODE                 YES, INVALID IOB SENSE  @X50RSAG
         TM    SENSE1(SIX),BIT4         CHECK FOR NOT REGULAR MISSING
*                                          ADDRESS MARKER
         BO    SETCODE                  YES, NOT NORMAL, SET ERROR CODE
         TM    SENSE0(SIX),BIT7         CHECK FOR BALLAST CELL FOUND
         BZ    TESTCCW                  SITUATION UNDER CONTROL
SETCODE  LA    FOUR,CC20                SET COMPLETION CODE TO REFLECT
*                                          NOT VALID IOB SENSE
*                                          INDICATION
BADCASE  MVC   XCTLID(L2),ATLAS06       MOVE ERROR LOAD NAME IN
         MVC   TTR(L3),TTR6             MOVE ERROR LOAD TTR IN
         SPACE
GETOUT   XC    XCTLPTR+D4(L4),XCTLPTR+D4 CLEAR SECOND WORD OF POINTER
         L     PARMB,PNTSNAP            GET SNAP LIST POINTER   YL02912
         LTR   PARMB,PARMB              SNAP MODE               YL02912
         BZ    NOSNAP                   NO - DONT SNAP          YL02912
         SNAP  ID=4,MF=(E,(1))          SNAP                    YL02912
NOSNAP   EQU   *                        *                       YL02912
         XCTL  ,MF=(E,(1)),SF=(E,XCTLPTR)
         SPACE
TESTCCW  CLI   D0(FIVE),RHA             IS COMMAND A SINGLE TRACK READ
*                                         HA
         BE    ERRCODE                  YES, HOME ADDRESS ERROR
         CLI   D0(FIVE),RR0             IS COMMAND A SINGLE TRACK
*                                         READ R0
         LA    SEVEN,PICKUP             LOAD NEXT BRANCH ADDRESS
         BNE   CHKNUMB                  CHECK FOR THREE ERROR
*                                          RECORDS
         BAL   SEVEN,SETCNT             UPDATE COUNT OF ERRORS
         MVI   RECNUMB+D1,ZEROS         SAVE ERROR RECORD NUMBER
         CLI   ERRTYPE,R0ERR            DID UTILITY PASS R0
         BNE   ERRCODE                  NO,ERROR IN R0
*                                       THE SVC HAS INSUFFICIENT
*                                          DATA TO HANDLE R0 ERROR
         LM    SIX,SEVEN,CCWRHA         LOAD READ HOME ADDRESS CCW
         ALR   SIX,TEN
         STM   SIX,SEVEN,CCW22          STORE IN SECOND CCW SLOT
         LA    SIX,CCW22                PUT POINTER TO CHANNEL PROGRAM
         ST    SIX,IOBSTART                INTO IOB
         B     EXPROG                   BRANCH TO GENERATE READ COUNTS
CHKNUMB  CLI   NUMBERR+D1,ERRS3         HAVE THERE BEEN THREE
*                                          ERROR RECORDS
         BNE   SETCNT                   NO, UPDATE COUNT OF ERRORS
         B     INVALID                  SET RETURN CODE TO 44 FOR MORE
*                                          THAN THREE ERROR RECORDS
*                                          ENCOUNTERED
SETCNT   IC    NINE,NUMBERR+D1          PICK UP NUMBER ERROR RECORDS
*                                          ENCOUNTERED
         A     NINE,FULL1               INCREMENT BY ONE
         STC   NINE,NUMBERR+D1
         BR    SEVEN                    CONTINUE PROCESSING
         SPACE
PICKUP   L     SEVEN,D0(X0,FIVE)        PICK UP ADDRESS OF COUNT FIELD
*                                          FOR CCW IN ERROR
         ST    SEVEN,CNTADDR            SAVE ADDRESS OF LAST COUNT
*                                          FIELD
         L     PARMB,USERPARM           PICK UP POINTER TO USER'S
*                                          PARAMETER LIST
         LR    WORKE,PARMB              SAVE POINTER TO LIST
         L     PARMB,D4(X0,PARMB)       PICK UP POINTER TO USER'S
*                                          COUNT
         MVC   D0(L4,SEVEN),D0(PARMB)   MOVE USER'S COUNT TO COUNT
*                                          FIELD AREA
         LR    SIX,FIVE
         S     SIX,FULL8                PICK UP ADDRESS OF PREVIOUS CCW
         CLI   D0(SIX),RR0              WAS PREVIOUS COMMAND READ R0
         BE    SETONE                   YES INSERT RECORD NUMBER
         CLI   D0(SIX),RHA              WAS PREVIOUS COMMAND A RD HA
         BNE   GETNUMB                  NO,GET RECORD NUMBER
SETONE   MVI   D4(SEVEN),RC1            INSERT RECORD NUMBER = 1
         CLI   NUMBERR+D1,ERRS2         WAS RO IN ERROR
         BNE   FIRSTERR                 NO,R1 IS THE FIRST ERROR
         MVI   RECNUMB+D2,RC1           STORE RECORD NUMBER
         B     ISITEOF                  TEST FOR ZERO DATA LENGTH
FIRSTERR MVI   RECNUMB+D1,RC1           SAVE RECORD NUMBER
         B     ISITEOF                  TEST FOR END OF FILE
GETNUMB  SR    SIX,SIX                  CLEAR REGISTER
         LR    NINE,SEVEN               GET ADDRESS OF RECORD NUMBER
         S     NINE,FULL4                  FIELD IN PREVIOUS COUNT
         IC    SIX,D0(X0,NINE)          PICK UP NUMBER
         LA    SIX,D1(X0,SIX)           INCREMENT BY ONE TO GET NUMBER
         STC   SIX,D4(X0,SEVEN)            OF RECORD IN ERROR
         XR    FOUR,FOUR                CLEAR REGISTER
         IC    FOUR,NUMBERR+D1          PICK UP NUMBER OF ERRORS
         LA    NINE,RECNUMB             DETERMINE DISPLACEMENT INTO
         ALR   FOUR,NINE                   ERROR RECORD SAVE AREA
         STC   SIX,D0(FOUR)             SAVE NUMBER OF ERROR RECORD
ISITEOF  SR    SIX,SIX                  CLEAR REGISTER
         CH    SIX,D6(X0,SEVEN)         IS DATA LENGTH EQUAL TO ZERO
         LA    FOUR,SETREAD             REGULAR PROCESS
         BNE   SETUP                    NO, REGULAR COUNT ERROR
         TM    D4(WORKE),BIT1           WAS A FULL COUNT FIELD GIVEN
         BO    INVALID                  NO, MAY BE EOF ERROR
         XR    FOUR,FOUR                CLEAR REGISTER
         IC    FOUR,NUMBERR+D1          PICK UP NUMBER OF ERROR RECORDS
         LA    NINE,RECNUMB
         ALR   FOUR,NINE                GET POINTER TO LAST ERROR
*                                          RECORD NUMBER
         CLC   D4(L1,PARMB),D0(FOUR)    IS THIS USER'S ERROR RECORD
         BNE   INVALID                  NO, CAN'T HANDLE EOF
         LA    FOUR,SETEOF              ADDRESS FOR ROUTINE TO SET EOF
SETUP    BAL   EIGHT,SETBLKS            BRANCH TO CLEAR CONTROL BLOCKS
         LM    SIX,SEVEN,CCWTIC         REPLACE READ COUNT COMMAND WITH
         ALR   SIX,AREAREG                 A TIC TO NEW CHANNEL PROGRAM
         L     EIGHT,INCRMNT            GET POINTER TO NEW CHANNEL
         ALR   SIX,EIGHT                   PROGRAM
         ST    SIX,PROGADDR             SAVE ADDRESS OF NEW CHANNEL
*                                          PROGRAM
         MVC   D4(L20,SIX),CCWSPACE+D4  MOVE NEW CHANNEL PROGRAM
*                                          TO WORK AREA
         L     WORKO,TICADDR            GET ADDR OF PREVIOUS
*                                          LAST TIC
         LR    NINE,FIVE
         LA    FIVE,D8(X0,FIVE)         PUT ADDRESS OF CCW FOLLOWING
         ST    FIVE,D16(X0,SIX)            ERROR CCW IN RETURN TIC
         LA    FIVE,D16(X0,SIX)         GET ADDRESS OF LAST TIC
         MVI   D0(FIVE),TIC             INSERT COMMAND CODE IN TIC CCW
         ST    FIVE,TICADDR             SAVE ADDRESS OF LAST TIC CCW
         LA    EIGHT,D24(X0,EIGHT)      UPDATE THE INCREMENT
         ST    EIGHT,INCRMNT
         STM   SIX,SEVEN,D0(NINE)       REPLACE READ COUNT CCW WITH
*                                          TIC TO NEW CHANNEL PROGRAM
         S     NINE,FULL8               GET POINTER TO CCW
*                                          PREVIOUS TO ERROR CCW
         CLI   D0(NINE),TIC             IS CCW PREVIOUS TO ERROR
*                                          CCW A TIC COMMAND
         BNE   NOTIC                    NO,NOT A DOUBLE TIC
         S     WORKO,FULL8
         MVC   D0(L4,WORKO),D8(NINE)    TIC TO SPACE COUNT CCW
NOTIC    L     SEVEN,CNTADDR            PICK UP ADDRESS OF ERROR COUNT
*                                           FIELD
         A     SEVEN,FULL5              GET ADDRESS OF KEY AND DATA
*                                          LENGTH FIELDS
         ST    SEVEN,D0(X0,SIX)         COMPLETE SPACE COUNT COMMAND
         MVI   D0(SIX),SPCT             INSERT COMMAND CODE
         BR    FOUR                     BRANCH TO NEXT ROUTINE
SETREAD  SR    WORKE,WORKE              CLEAR WORK REGISTER
         IC    WORKE,D0(X0,SEVEN)       PICK UP KEY LENGTH
         LA    SEVEN,D1(X0,SEVEN)       GET ADDRESS OF DATA LENGTH
         LH    WORKO,D0(X0,SEVEN)       PICK UP DATA LENGTH
         AR    WORKE,WORKO              GET KEY + DATA LENGTH
         STH   WORKE,D14(X0,SIX)        INSERT BYTE COUNT INTO READ
*                                           KEY DATA CCW
         EXCP  IOB                      READ COUNT WITH READ KD
         WAIT  1,ECB=ECB                WAIT FOR I/O TO COMPLETE
         B     TESTECB                  CHECK CONDITION OF ECB
SETEOF   S     SEVEN,FULL5              GET ADDRESS OF ERROR COUNT
         MVC   D0(L8,SEVEN),D0(PARMB)   MOVE IN USER'S COUNT
         B     REPLCCW                  CHANGE READ KEY/DATA TO TIC
SETBLKS  XC    ECB(L4),ECB              ZERO ECB
         XC    IOBSENS0(L2),IOBSENS0    ZERO SENSE BYTES
         XC    IOBCSW(L7),IOBCSW        ZERO LOW ORDER BYTES OF IOB CSW
         BR    EIGHT                    RETURN
         SPACE
TESTECB  TM    ECB,IOEROR               TEST FOR PERMANENT I/O ERROR
         BZ    CHKLAST                  YES, PERMANENT I/O ERROR
         SPACE
REPLCCW  MVC   D8(L8,SIX),D0(FIVE)      REPLACE READ KEY/DATA
*                                          WITH TIC
         B     EXPROG                   GO TO ORIGINAL EXCP
         SPACE
CHKLAST  EQU   *
         CLC   IOBCSW(L3),TICADDR+D1    COMPARE ADDRESS OF LAST CCW
*                                          WITH ADDRESS OF LAST TIC CCW
         BNE   INVALID                  IF NOT EQUAL, SET ERROR CODE
CHKREC   L     PARMB,USERPARM
         TM    D4(PARMB),BIT1           WAS A FULL COUNT FIELD GIVEN
         BO    INVALID                  UNRECOVERABLE ERROR IN KDD OF
*                                          COUNT FIELD
         L     FOUR,D4(X0,PARMB)        PICK UP ADDRESS OF CCHH
*                                           PARAMETER
         XR    SEVEN,SEVEN              CLEAR REGISTER
         LA    EIGHT,RECNUMB            GET POINTER TO R OF LAST
         IC    SEVEN,NUMBERR+D1            ERROR RECORD
         ALR   SEVEN,EIGHT
         CLC   D4(L1,FOUR),D0(SEVEN)    WAS THE LAST ERROR RECORD THE
*                                          ORIGINAL RECORD IN ERR
         BNE   INVALID                  NO, SET ERROR CODE
         L     SEVEN,CNTADDR            LOAD ADDR OF COUNT FIELD
         MVC   D0(L8,SEVEN),D0(FOUR)    MOVE IN USER'S COUNT
         B     REPLCCW                  BRANCH TO REPLACE READ KEY/DATA
*                                          CCW WITH TIC
         SPACE
ERRCODE  LA    FOUR,CC36                SET COMPLETION CODE TO 36 FOR
*                                          HOME ADDRESS OR R0 ERROR
         B     BADCASE                  GO TO ERROR LOAD
INVALID  LA    FOUR,CC44                SET COMPLETION CODE FOR ERROR
*                                          IN EOF RECORD OR KDD OF
*                                          COUNT FIELD
         B     BADCASE                  EXIT TO ERROR LOAD
OUTEXT   MVC   XCTLID(L2),ATLAS04       MOVE NEXT LOAD NAME IN
         MVC   TTR(L3),TTR4             MOVE NEXT LOAD TTR IN
         SR    FOUR,FOUR                SET COMPLETION CODE TO ZERO
         B     GETOUT                   EXIT TO NEXT LOAD
SENSE0   EQU   2
SENSE1   EQU   3
STATUS0  EQU   12                       FIRST CSW STATUS BYTE
CCWSPACE CCW   X'0F',0,X'40',3          SPACE COUNT
CCWRKD   CCW   X'0E',CCW31-SVRBEX,X'10',0  READ KEY/DATA
CCWTIC   CCW   X'08',CCW31-SVRBEX,X'40',0  TIC
CCWRHA   CCW   X'1A',HOMEAD-CNTAREA,X'40',5  READ HOME ADDRESS
CCWRR0   CCW   X'16',R0-CNTAREA,X'40',16  READ RECORD ZERO
CCWRC    CCW   X'92',0,X'40',8          READ COUNT
FULL8    DC    F'8'                     *
FULL1    DC    F'1'                     *
FULL4    DC    F'4'                     *
FULL5    DC    F'5'                     *
MAINT    DC    2CL25'IGG0860B MAINTENANCE AREA'                 YL02912
         SPACE
*XCTL BY TTR TABLE
         SPACE
         ORG   IGG0860B+1000            FIRST ORG
         SPACE
TTRTABLE EQU   *
ATLAS04  DC    C'0C'                    ID OF NEXT ATLAS LOAD
TTR4     DC    X'00000000'              TTR OF NEXT ATLAS LOAD
ATLAS06  DC    C'AE'                    ID OF ERROR LOAD
TTR6     DC    X'00000000'              TTR OF ERROR LOAD
         DC    X'0000'                  LAST TABLE ENTRY CODE
         ORG   IGG0860B+1020            SECOND ORG
         DC    C'086'                   ATLAS SVC CODE
         DC    X'7D'                    RELATIVE TABLE START ADDRESS
*                                          IN DOUBLE WORDS
WORKDSCT DSECT
SVRBEX   DS    1F                       SAVE AREA FOR SVRB EXTENSION
*                                       POINTER
DEVTAB   DS    1F                       DEVICE TYPE INDICATOR FROM UCB
SWITCH   DS    CL1                      LOGIC SWITCH
*                                       0 BIT INDICATES CHANNEL
*                                          PROGRAM NOT RE-EXECUTABLE
*                                       1 BIT INDICATES WRITE SPECIAL
*                                          REQUIRED
*                                       2 BIT INDICATES TRACK OVERFLOW
*                                          DATA SET
*                                       7 BIT INDICATES VTOC HAS BEEN
*                                       ENQUEUED UPON
NUMBERR  DS    CL2                      NUMBER OF ERROR RECORDS
VTOCADR  DS    CL5                      VTOC ADDRESS CCHHR
SPLIT    DS    X'00'                    SPLIT CYLINDER SWITCH
RETPT    DS    1F                       POINTER TO NEXT ROUTINE
         DS    0F
         IECSDSL1 (4)
TICADDR  DS    1F                       ADDRESS OF TIC CCW IN NEW
*                                          CHANNEL PROGRAM
SAVEADDR DS    1F                       ADDRESS OF FIRST COUNT FIELD
CNTCORE  DS    1F                       NUMBER BYTES IN COUNT FIELD
*                                          WORK AREA
COREADDR DS    1F                       ADDRESS OF COUNT FIELD
*                                          WORK AREA
RECADDR  DS    1F                       ADDRESS OF CORE GOTTEN FOR
*                                          LONGEST RECORD SIZE
RECCORE  DS    1F                       NUMBER OF BYTES IN THIS AREA
DDNAME   DS    1D                      DDNAME SAVE AREA         XL03130
DEVINFO  DS    5F                      DEVICE INFO              XL03130
PARMUSER DS    1F                       POINTER USER PARM LIST  YL02912
PNTEST   DS    1F                       TEST MODE               YL02912
PNTSNAP  DS    1F                       SNAP MODE               YL02912
PNTSNS   DS    CL2                      SENSE BYTE - USER       YL02912
         DS    CL1                      FILLER                  YL02912
PNTMIOB  DS    CL1                      M OF IOB                YL02912
PNTIOB   DS    1F                       IOB POINTER             YL02912
PNTCNT   DS    1F                       COUNT POINTER           YL02912
PARMCNT  DS    2F                       COUNTS                  YL02912
PNTIOT   DS    CL2                      TIOT OFFSET             YL02912
         DS    CL1                      FILLER                  YL02912
PNTOFLO  DS    CL1                      TRACK OVERFLO           YL02912
PNTDEB   DS    1F                       DEB POINTER             YL02912
HOLDKEY  DS    1F                       USER KEY                YL02912
REGHOLD  DS    1F                       TEMP REG SAVE           YL02912
CNTADDR  DS    1F                       ADDRESS OF COUNT FIELD FOR
*                                          CCW IN ERROR
ERRTYPE  DS    CL1                      TYPE OF ORIGINAL ERROR
RECNUMB  DS    1F                       R OF ERROR RECORDS
REGSAVE  DS    5F                       SAVE AREA FOR REGISTERS
ECB      DS    1F                       ECB
IOB      DS    8F                       IOB
IOBSENS0 EQU   IOB+2
IOBFLAG3 EQU   IOB+8
IOBCSW   EQU   IOB+9
IOBCSWST EQU   IOB+12
IOBSTART EQU   IOB+16
SEEK     DS    2F                       SEEK ADDRESS
DEB      DS    12F                      DEB
DEBSTRCC EQU   DEB+38
DEBENDCC EQU   DEB+42
DEBPTR   DS    F                        DCB SLOT
MIELNAME DS    CL6                      VOLUME SERIAL SLOT
DCB      EQU   DEBPTR-44
R0AREA   DS    2D                       AREA FOR R0
DEQAREA  DS    2D                       DEQ TABLE AREA
MJELNAME DS    2F                       MAJOR QUEUE NAME AREA
ENQAREA  DS    2D                       ENQ TABLE AREA
USERPARM DS    1F                       POINTER TO USER'S PARM LIST
NUMALT   DS    1H                       NUMBER OF ALTERNATES TO BE
*                                         TRIED
MAXCOUNT DS    1H                       MAXIMUM NUMBER OF COUNT FIELDS
*                                          PER TRACK+1
XCTLPTR  DS    2F                       PTR TO XCTL WORK AREA TABLE
XCTLAREA DS    0CL32
XCTLNM   DS    CL6                      COMMON BYTES OF ATLAS NAME
XCTLID   DS    CL2                      ID OF ATLAS MODULE
         DS    CL6                      *
TTR      DS    CL3                      TTR OF ATLAS MODULE
         DS    CL5                      *
TTRATTB  DS    CL2                      ATTRIBUTES OF ATLAS - ALL LOADS
TTRCREQ  DS    CL3                      CORE REQUIREMENT OF ATLAS LOAD
TTRLNGTH DS    CL2                      LENGTH OF ATLAS LOAD
         DS    CL3                      *
INCRMNT  DS    1F                       CCW POINTER
PROGADDR DS    1F                       ADDRESS OF NEW CHANNEL PROGRAM
ALTHA    DS    2F                       ALTERNATE TRACK HOME ADDRESS
ALTR0    DS    4F                       ALTERNATE TRACK R0
CCW1     DS    1D                       CCW AREA
CCW2     DS    1D                       *
CCW3     DS    1D                       *
CCW44    DS    1D                       *
CCW45    DS    1D                       *
CCW56    DS    1D                       *
CCW57    DS    1D                       *
CCW31    DS    1D                       *
CCW32    DS    1D                       *
CCW33    DS    1D                       *
CCW34    DS    1D                       *
CCW35    DS    1D                       *
CCW36    DS    1D                       *
CCW37    DS    1D                       *
CCW38    DS    1D                       *
CCW39    DS    1D                       *
CHECKHA  DS    2F                       AREA FOR HOME ADDRESS OF
*                                       ALTERNATE
FLAG     DS    1F                       TO BE REMOVED
*
CVT      DSECT
UCB      DSECT
CNTAREA  DSECT
         DS    F                        FOR DBL WD ALGNMT      @ZM40498
MADSD    DS    CL4                      3350 SKIP DISP         @X50RSPC
HOMEAD   DS    2F                       CCW AREA
R0       DS    4F                       *
CCW21    DS    2F                       *
CCW22    DS    2F                       *
CCW23    DS    2F                       *
         END
