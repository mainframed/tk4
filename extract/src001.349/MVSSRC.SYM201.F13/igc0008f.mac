         TITLE 'IGC0008F - FIRST LOAD MODULE FOR SVC 86'         S20201
         COPY  LCGASMSW
IGC0008F CSECT
*                                                               YM7385
*A                                                             PTMFIX
*                                                               YM3004
*
*          RELEASE 22 DELETIONS/CHANGES
*          RELEASE 21 DELETIONS/CHANGES
*          RELEASE 20 DELETIONS/CHANGES
*0791001000-002000,260000,282000-290000                          S20201
*
*STATUS CHANGE LEVEL 002
*
*TITLE 'IGC0008F' - FIRST LOAD MODULE OF SVC 86                       *
*                                                                     *
*FUNCTION/OPERATION                                                   *
*   IGC0008F ESTABLISHES ITS OWN ADDRESSABILITY AND THAT OF ITS       *
*688 -BYTE WORK AREA WHICH IT GETS.  BEING THE FIRST LOAD MODULE OF   *
*SVC 86, THIS MODULE HAS FOUR MAJOR FUNCTIONS -
*                                                                     *
*        1.  TEST FOR USER'S CHANNEL PROGRAM BEING RE-EXECUTABLE      *
*        2.  CHECK IOB FOR VALID ERROR INDICATIONS - DATA CHECK       *
*            OR MISSING ADDRESS MARKER                                *
*        3.  PICK UP DEVICE TYPE FROM THE UCB AND ASSOCIATE WITH      *
*            IT 10 PER CENT OF THE ASSIGNABLE ALTERNATES AND THE      *
*            MAXIMUM NUMBER OF COUNT FIELDS PER TRACK                 *
*        4.  BUILD A DEB,DCB,IOB AND ECB                              *
*                                                                     *
*ENTRY/POINTS                                                         *
*   IGC0008F GETS THE DEVICE TYPE, TYPE OF ERROR AND INFORMATION AS   *
*TO THE NUMBER OF ALTERNATE TRACKS AVAILABLE AND THE MAXIMUM NUMBER   *
*OF COUNT FIELDS PER TRACK                                            *
*                                                                     *
*INPUT                                                                *
*   FROM THE USER INPUT IS THE ADDRESS (VIA REGISTER 1) OF A TWO-     *
*WORD PARAMETER LIST INCLUDING A POINTER TO THE IOB AND A POINTER     *
*TO THE COUNT.  WHEN IGC0008F RECEIVES CONTROL REGISTER 5 POINTS TO   *
*THE SVRB.                                                            *
*                                                                     *
*OUTPUT                                                               *
*   CONSISTS OF THE WORK AREA AND THE FOLLOWING RETURN CODES          *
*                                                                     *
*         4 - WRONG DEVICE TYPES                                      *
*         8 - NO MORE ALTERNATES                                      *
*        12 - CORE NOT AVAILABLE                                      *
*        20 - INVALID I/O SENSE INDICATION                            *
*        56 - SYSTEM DOES NOT SUPPORT TRACK OVERFLOW                  *
*        60 - TRACK SPECIFIED BY USER IS OUT OF DEB EXTENTS           *
*        64 - VIRTUAL DEVICES NOT SUPPORTED                     YL02912
*                                                                     *
*EXTERNAL ROUTINES                                                    *
*   THE TTR CONVERSION ROUTINE IS USED TO GET THE ABSOLUTE ADDRESS    *
*OF THE VTOC.  THE ADDRESS OF THE CONVERSION ROUTINE IS PICKED UP     *
*FROM THE CVT.                                                        *
*                                                                     *
*EXITS                                                                *
*   ERROR - XCTL TO IGG086AE                                          *
*   NORMAL - XCTL TO IGG0860A                                         *
*                                                                     *
*TABLES/WORK AREAS                                                    *
*   A 688-BYTE WORK AREA IS ACQUIRED BY THIS MODULE.  INFORMATION     *
*PLACED IN THIS AREA INCLUDES-     THE MAXIMUM NUMBER OF              *
*COUNT FIELDS PER TRACK, A POINTER TO THE USER'S PARAMETER LIST, AND  *
*THE ECB, IOB, AND DCB CONSTRUCTED BY THIS MODULE.                    *
*   TABDEV TABLE - IS USED TO ASSOCIATE 10% OF ALTERNATES       XL03291
*AVAILABLE, THE MAXIMUM NUMBER OF COUNT FIELDS AND THE          XL03291
*ADDRESS TO GO TO BASED ON THE DEVICE CODE.                     XL03291
*                                                                     *
*ATTRIBUTES                                                           *
*                                                                     *
*   REENTRANT, REFRESHABLE                                            *
*                                                                     *
*
***********************************************************************
*                                                                     *
*                               EQUATES                               *
*                                                                     *
***********************************************************************
BIT0     EQU   X'80'
BIT1     EQU   X'40'
BIT2     EQU   X'20'
BIT4     EQU   X'08'
BIT5     EQU   X'04'
BIT6     EQU   X'02'
BIT7     EQU   X'01'
***********************************************************************
*          COMPLETION CODES                                           *
***********************************************************************
CC4      EQU   4                        DEVICE TYPE NOT SUPPORTED
CC12     EQU   12                       CORE NOT AVAILABLE
CC20     EQU   20                       NOT VALID ERROR CONDITION
CC56     EQU   56                       TRACK OVERFLOW NOT LEGAL
CC60     EQU   60                       TRACK ADDRESS OUT OF EXTENTS
CC64     EQU   64                       NO VIRTUAL DEVICE SUPP  YL02912
***********************************************************************
*          DISPLACEMENT CONSTANTS                                     *
***********************************************************************
D0       EQU   0
D1       EQU   1
D2       EQU   2
D3       EQU   3
D4       EQU   4
D6       EQU   6
D8       EQU   8
D12      EQU   12
D16      EQU   16                       DISPLACEMENT OF 16      YL02912
D28      EQU   28                       DISPLACEMENT OF 28      YL02912
D17      EQU   17                       DISPLACEMENT OF 17      YL02912
D32      EQU   32                       *
D33      EQU   33
D34      EQU   34
D40      EQU   40
D56      EQU   56
D96      EQU   96
D100     EQU   100
***********************************************************************
*          LENGTH CONSTANTS                                           *
***********************************************************************
L1       EQU   1                        LENGTH 1                YL02912
L2       EQU   2
L3       EQU   3
L4       EQU   4
L5       EQU   5
L6       EQU   6
L7       EQU   7
L8       EQU   8
L16      EQU   16
L177     EQU   177
L255     EQU   255
***********************************************************************
***********************************************************************
*
***********************************************************************
BITS4    EQU   4
BITS8    EQU   8
BITS16   EQU   16
DA       EQU   X'04'
DEBID    EQU   X'0F'                    DEB ID FLAG
DEVFM    EQU   X'C0'
DOG8     EQU   X'D8'
D2314    EQU   X'08'                    DEVICE TYPES
D2321    EQU   X'05'                        *
D2311    EQU   X'01'                        *
D2302    EQU   X'04'                        *
D23051   EQU   X'06'                        *                    S20201
D23052   EQU   X'07'                        *                    S20201
D3330    EQU   X'09'                        *                    S20201
         AIF   ('&LIB' EQ 'LIB1').X225600                       XL03130
D3340    EQU   X'0A'                   DEVICE TYPE 3340         XL03130
D3330C   EQU   X'0D'                   DEVICE TYPE 3330C        XL03145
.X225600 ANOP                                                   XL03130
FFSW     EQU   X'FF'
FLAG1    EQU   X'42'
NOREEX   EQU   X'80'
ONEXT    EQU   X'01'                    ONE EXTENT
OVFLW    EQU   X'20'                    RECORD OVERFLOW
TWO55    EQU   255
X0       EQU   0
DBLZERO  EQU   X'00'
*
*
         EJECT
* REGISTER ASSIGNMENTS
PARMA    EQU   0                        PARAMETER REGISTER
PARMB    EQU   1                        PARAMETER REGISTER
WORKE    EQU   2                        EVEN WORK REGISTER
WORKO    EQU   3                        ODD WORK REGISTER
FOUR     EQU   4                        RETURN CODE REGISTER
FIVE     EQU   5                        WORK REGISTER
SIX      EQU   6                        *
SEVEN    EQU   7                        *
EIGHT    EQU   8                        *
NINE     EQU   9                        *
TEN      EQU   10                       *
AREAREG  EQU   11                       MAIN WORK AREA REGISTER
BASEREG  EQU   12                       BASE REGISTER
SAVEREG  EQU   13                       WORK REGISTER
RETREG   EQU   14                       WORK REGISTER
ADDREG   EQU   15                       COMPLETION CODE REGISTER
***********************************************************************
*        START OF SVC                                           YL02912
***********************************************************************
         BALR  BASEREG,X0               ESTABLISH OWN
         USING BEGIN,BASEREG            ADDRESSABILITY
BEGIN    LR    FOUR,PARMB
         L     PARMB,D28(FIVE)          POINT TO PREVIOUS RB    YL02912
         IC    WORKE,D17(PARMB)         GET KEY FROM PSW        YL02912
         LR    SIX,RETREG               SAVE RETURN             YL02912
***********************************************************************
*        KEY 5                                                  YL02912
***********************************************************************
         MODESET EXTKEY=DATAMGT         KEY 5                   YL02912
***********************************************************************
*        GET WORK AREA FOR DSECT                                YL02912
***********************************************************************
         GETMAIN R,LV=688,SP=229        WORK AREA               YM3004
         LTR   ADDREG,ADDREG            CHECK FOR GOTTEN CORE
         BZ    GOTCORE                  YES, CORE GOTTEN FOR WORK AREA
         LA    ADDREG,CC12              SET RETURN CODE TO 12 TO INDI-
*                                          CATE CORE NOT AVAILABLE
         LR    RETREG,SIX               RESTORE RETURN REG      YL02912
         BR    RETREG                   RETURN                  YL02912
GOTCORE  EQU   *                                                YM3004
***********************************************************************
*        SUPERVISOR KEY                                         YM3004
***********************************************************************
         MODESET EXTKEY=SUPR            KEY 0                   YM3004
         ST    PARMB,D100(FIVE)         STORE ADDRESS OF AREA
***********************************************************************
*        KEY 5                                                  YM3004
***********************************************************************
         MODESET EXTKEY=DATAMGT         KEY 5                   YM3004
***********************************************************************
*        ZERO OUT WORK AREA                                     YL02912
***********************************************************************
         LR    AREAREG,PARMB            ESTAB. ADDRESSABILITY TO YM3004
*                                          WORK AREA
         USING WORKDSCT,AREAREG
         MVI   SVRBEX,DBLZERO           CLEAR WORK AREA TO ZEROS
         MVC   SVRBEX+D1+D0*TWO55(L255),SVRBEX+D0*TWO55
         MVC   SVRBEX+D1+D1*TWO55(L255),SVRBEX+D1*TWO55
         MVC   SVRBEX+D1+D2*TWO55(L177),SVRBEX+D2*TWO55
***********************************************************************
*        SET UP POINTERS                                        YL02912
***********************************************************************
         LA    FIVE,D96(X0,FIVE)
         ST    FIVE,SVRBEX              SAVE EXTENDED SVRB POINTER
         LA    FIVE,XCTLAREA            GET POINTER TO XCTL LIST
         ST    FIVE,XCTLPTR             STORE POINTER
         MVC   XCTLID(L2),ATLAS02       MOVE NEXT LOAD NAME IN
         MVC   TTR(L3),TTR2             MOVE NEXT LOAD TTR IN
         ST    SIX,REGHOLD              SAVE RETURN             YL02912
         ST    FOUR,PARMUSER            SAVE USER PARM POINTER  YL02912
         STC   WORKE,HOLDKEY            SAVE USER KEY           YL02912
***********************************************************************
*        USER KEY                                               YL02912
***********************************************************************
         MODESET KEYADDR=(2)            GET INTO USER'S KEY     YL02912
         LM    FIVE,SIX,D0(FOUR)        IOB/USER COUNT POINTERS YL02912
         LM    WORKE,WORKO,D8(FOUR)     TEST/SNAP               YL02912
         L     EIGHT,USDCBPT(FIVE)      DCB POINTER FROM IOB    YL02912
         LM    NINE,TEN,D40(EIGHT)      TIOT OFFSET DEB POINTER YL02912
         IC    NINE,DCBRECFM(EIGHT)     TRACK OVERFLO CODE      YL02912
         LM    SEVEN,EIGHT,D0(SIX)      USER COUNT FIELD        YL02912
         LH    FOUR,USSENS0(FIVE)       USER IOB SENSE BYTES    YL02912
         SLL   FOUR,D16                 SHIFT SENSE BYTES       YL02912
         IC    FOUR,USSEEK(FIVE)        GET M OF IOB ADDRESS    YL02912
***********************************************************************
*        KEY 5                                                  YL02912
***********************************************************************
         MODESET EXTKEY=DATAMGT         KEY 5                   YL02912
         STM   WORKE,TEN,PNTEST         TEST/SNAP               YL02912
*                                       SENSE BYTE/M-IOB/IOB/   YL02912
*                                       COUNT/COUNTS/TIOTOFLO/D YL02912
         LA    SEVEN,PARMCNT            NEW COUNT FIELD POINTER YL02912
         IC    SIX,PNTCNT               GET PREVIOUS CODE       YL02912
         ST    SEVEN,PNTCNT             SET NEW POINTER         YL02912
         STC   SIX,PNTCNT               RESET PREVIOUS CODE     YL02912
         LA    SIX,PNTIOB               POINT TO NEW LIST       YL02912
         ST    SIX,USERPARM             SET NEW LIST POINTER    YL02912
         LA    TEN,D0(TEN)              CLEAR HO BYTE           YL02912
         MVC   HOLDKEY+D1(L3),29(TEN)   SAVE TO APVT ADDR       YL02912
***********************************************************************
*        CHECK USERS SENSE BYTES                                YL02912
***********************************************************************
REEX     LTR   FIVE,FIVE                CHECK FOR CHANNEL PROGRAM
         LA    FIVE,PNTSNS-D2           POINT TO NEW SENSE BYTE YL02912
*                                          BEING RE-EXECUTABLE
         BP    LEAVESW                  YES, CHANNEL PROGRAM IS RE-EXE-
*                                          CUTABLE
         OI    SWITCH,NOREEX            SET SWITCH FOR CHANNEL PROGRAM
*                                          NOT RE-EXECUTABLE
LEAVESW  TM    USSENS0(FIVE),BIT4       CHECK USER'S IOB SENSE BYTE 0
         BO    DATACK                   YES, DATA CHECK
         TM    USSENS1(FIVE),BIT6       CHECK FOR MISSING ADDRESS
*                                          MARKER
         BZ    SETERCD                  NO, GO SET ERROR CODE
         TM    USSENS1(FIVE),BIT4       CHECK FOR NOT REGULAR MISSING
*                                          ADDRESS MARKER
         BO    SETERCD                  YES, NOT NORMAL MISSING ADDRESS
*                                          MARKER
         TM    USSENS0(FIVE),BIT7       CHECK FOR BALLAST CELL FOUND
         BZ    DATACK                   SITUATION UNDER CONTROL
***********************************************************************
*        FREEWORK AREA AND GET OUT                              YL02912
***********************************************************************
SETERCD  LA    FOUR,CC20                SET COMPLETION CODE TO REFLECT
*                                          NOT A VALID IOB SENSE INDI-
*                                          CATION
FREEWORK MVC   XCTLID(L2),ATLAS06       MOVE ERROR LOAD NAME IN
         MVC   TTR(L3),TTR6             MOVE ERROR LOAD TTR IN
***********************************************************************
         SPACE
*SET UP COMMON XCTL FIELDS
***********************************************************************
         SPACE
XCTL     MVC   XCTLAREA(L6),ATLASNM     MOVE IN COMMON NAME BYTES
         MVC   TTRATTB(L7),TTRCON       MOVE IN ATTRIBUTES OF ALL LOADS
         L     PARMB,PNTSNAP            GET SNAP LIST POINTER   YL02912
         LTR   PARMB,PARMB              SNAP WANTED             YL02912
         BZ    XCTL1                                            YL02912
         SNAP  ID=2,MF=(E,(1))          SNAP PROBLEM PROGRAM    YL02912
XCTL1    EQU   *                                                YL02912
         XCTL  ,MF=(E,(1)),SF=(E,XCTLPTR)
*
***********************************************************************
*        CHECK EXTENTS                                          YL02912
***********************************************************************
DATACK   LA    NINE,PARMCNT             POINT TO CCHH           YL02912
         XR    SIX,SIX
         L     EIGHT,PNTDEB             POINT TO DEB            YL02912
         IC    SIX,DEBNOEXT(EIGHT)      LOAD NUMBER OF EXTENTS
         TM    DEBOFLGS(EIGHT),BIT5     CHECK FOR SPLIT CYLINDER
         BNO   NOTSPLIT                 BRANCH IF NOT SPLIT
         MVI   SPLIT,FFSW               SET SPLIT SWITCH
NOTSPLIT LA    EIGHT,DEBDASD(EIGHT)     BUMP POINTER TO FIRST EXTENT
*                                          DESCRIPTION
STARTEST CLC   D0(L4,NINE),DEBSTRCC(EIGHT) IS GIVEN BAD TRCK LOWR THAN
*                                          START OF EXTENT
         BL    DECREMNT                 YES,TEST NEXT EXTENT
         CLC   D0(L4,NINE),DEBENDCC(EIGHT) IS GIVN BAD TRK HIGHER THAN
*                                          END OF EXTENT
         BH    DECREMNT                 YES,TEST NEXT EXTENT
         CLI   SPLIT,FFSW               SPLIT SWITCH ON
         BNE   EXTOK                    BRANCH,TRACK IS WITHIN EXTENT
         CLC   D2(L2,NINE),DEBSTRHH(EIGHT) IS GIVEN BAD TRACK HH LOWER
*                                          THAN EXTENT
         BL    DECREMNT                 YES,CONTINUE TESTING
         CLC   D2(L2,NINE),DEBENDHH(EIGHT) IS GIVEN BAD TRCK HIGHR THEN
*                                          HH EXTENT
         BH    DECREMNT                 YES,TEST NEXT EXTENT
         B     EXTOK                    BRANCH,TRACK IS WITHIN EXTENT
DECREMNT BCT   SIX,INCREMNT             DECREMENT NUMBER OF EXTENTS
         LA    FOUR,CC60                SET COMPLETION CODE TO REFLECT
*                                          TRACK ADDRESS IS OUT OF
*                                          EXTENTS
         B     FREEWORK                 BRANCH TO ERROR LOAD
INCREMNT LA    EIGHT,NEXTEXT(EIGHT)     BUMP POINTER TO NEXT DEB EXTENT
         B     STARTEST                 CONTINUE TESTING
EXTOK    XR    SIX,SIX
         IC    SIX,PNTMIOB              PICK UP M OF IOB        YL02912
         L     EIGHT,PNTDEB             GET DEB POINTER         YL02912
DIRECT   SLL   SIX,BITS4                MULTIPLY M OF SEEK ADDRESS BY
*                                          16
         LA    NINE,D32(X0,EIGHT)       GET ADDRESS OF END OF BASIC
*                                          DEB
         AR    NINE,SIX                 INDEX INTO APPROPRIATE DEVICE
*                                          DEPENDENT EXTENT IN DEB
         L     SIX,D0(X0,NINE)          PICK UP ADDRESS OF UCB FOR THIS
*                                          EXTENT
         USING UCB,SIX                                         @Y30LSFY
         TM    UCBTBYT2,UCB2OPT4        VIRTUAL UCB ?          @Y30LSFY
         BO    RC64                     YES-NO SUPPORT         @Y30LSFY
         TM    D0(SIX),BIT0             VIRTUAL UCB ?           YL02912
         BNO   NOTVIRT                  NO - CONTINUE           YL02912
RC64     EQU   *                                               @Y30LSFY
         LA    FOUR,CC64                NO VIRTUAL UCB SUPPORT  YL02912
         B     FREEWORK                 TO ERROR LOAD           YL02912
         USING UCB,SIX                                         YM3004
***********************************************************************
*        GET DEVICE INFO FROM DEVICE TABLE                      YL02912
***********************************************************************
NOTVIRT  EQU   *                        NOT VIRTUAL UCB         YL02912
         MVC   DEVTAB(L4),UCBTBYT1       SAVE DEVICE TYPE       YM3004
         DROP  SIX                                              YM3004
         LA    WORKE,TABDEV            GET DEVICE TABLE ADDRESS XL03291
         USING TABDEV,WORKE  ADDRESSABILITY TO THE TABLE        XL03291
DEVLOOK  CLC   TABDEV1,DEVTAB+D3       CHECK DEVICE TYPE        XL03291
         BE    GOTDEV                  DEVICE TYPE FOUND        XL03291
         LA    WORKE,D8(WORKE)         INCREMENT THRU TABLE     XL03291
         CLI   TABDEV2,TWO55           TEST FOR DELIMITOR       XL03291
         BNE   DEVLOOK                 CONTINUE LOOKING         XL03291
GOTDEV   MVC   NUMALT(L4),TABDEV1      SET NUMALT AND MAXCOUNT  XL03291
         MVI   NUMALT,X0               CLEAN UP HIGH ORDER      XL03291
         B     TABDEV4                 GO TO BRANCH INSTRUCTION XL03291
         DROP  WORKE                   RELEASE REGISTER         XL03291
*                                                               XL03291
A3330    TM    USSENS1(FIVE),BIT6      MISSING ADDRESS MARKER?  XL03291
         BO    SETERCD                 YES, INVALID IOB SENSE   XL03291
         B     DISLIKE                 CONTINUE                 XL03291
NONSUP   LA    FOUR,CC4                NONSUPPORTED DEVICE      XL03291
         B     FREEWORK                RETURN                   XL03291
         AIF   ('&LIB' EQ 'LIB1').X225601                       XL03130
A3340    EQU   *                                                XL03130
         TM    USSENS1(FIVE),BIT6      MISSING ADDRESS MARKER   XL03130
         BO    SETERCD                 YES,INVALID SENSE BYTE   XL03130
         LH    WORKE,PNTIOT             OFFSET TO DDENTRY TIOT  YL02912
         USING CVT,WORKO                                        XL03130
         L     WORKO,CVTPTR            GET CVT POINTER          XL03130
         L     WORKO,CVTTCBP           GET TCB POINTER          XL03130
         DROP  WORKO                   RELEASE REGISTER         XL03130
         L     WORKO,D4(WORKO)         GET CURRENT TCB          XL03130
         L     WORKO,D12(WORKO)        GET POINTER TO TIOT      XL03130
         LA    WORKO,D4(WORKE,WORKO)   POINT TO DDNAME IN TIOT  XL03130
         MVC   DDNAME(L8),D0(WORKO)    SAVE DDNAME              XL03130
         DEVTYPE   DDNAME,DEVINFO,DEVTAB     GET DEVICE INFO    XL03130
         CLC   WINCYL(L2),CYL696       IS THIS A 72 MEG         XL03130
         BE    DISLIKE                 CONTINUE                 XL03130
         MVI   NUMALT+D1,CC12          CHANGE NO. ALTS TO 1     YM7385
.X225601 ANOP                                                   XL03130
***********************************************************************
*        CHECK VTOC ETC                                         YL02912
***********************************************************************
         USING UCB,SIX                                         YM3004
DISLIKE  EQU   *                                                XL03291
         L     PARMA,UCBVTOC           PICK UP VTOC ADDRESS IN   YM3004
*                                          TTR0 TRIED            YM3004
         DROP  SIX                                             YM3004
         TM    PNTOFLO,BIT2             TRACK OVERFLO DATA SET  YL02912
         BNO   DEBBUILD                 NO, NOT AN OVERFLOW DATA SET
         OI    SWITCH,OVFLW             DATA SET IS TRACK OVERFLOW
         TM    DEVTAB+D1,BIT1           DOES SYSTEM SUPPORT TRACK
*                                          OVERFLOW
         BO    DEBBUILD                 YES, OK
         LA    FOUR,CC56                SET COMPLETION CODE TO REFLECT
*                                          TRACK OVERFLOW DATA SET WITH
*                                          NO SYSTEM SUPPORT
         B     FREEWORK                 ERROR, GET OUT
         EJECT
***********************************************************************
* THIS SECTION BUILDS A DEB
***********************************************************************
DEBBUILD MVI   DEBNMEXT,ONEXT           SET NUMBER OF EXTENTS TO ONE
         LA    PARMB,DCB                LOAD POINTER TO DCB
         USING CVT,ADDREG
         L     ADDREG,CVTPTR            LOAD COMM VECT TABLE BASE
         L     WORKE,CVTXAPG            GET IOS APPENDAGE TABLE ADDR.
         LR    WORKO,SIX                LOAD UCB POINTER
         USING UCB,SIX
         MVC   MIELNAME(L6),SRTEVOLI    MOVE VOL SER FOR ENQ
         SR    FOUR,FOUR                LOAD BB AND START CC
         AIF   ('&LIB' NE 'LIB1').X225604                       YL02912
         CLI   DEVTAB+D3,D2321          CHECK FOR A 2321
         BNE   MAINUCB                  NO, OTHER THAN 2321
         IC    FOUR,D33(X0,FIVE)        PICK UP SECOND BYTE OUT OF SEEK
         SLL   FOUR,BITS8
         IC    FOUR,D34(X0,FIVE)        PICK UP THIRD BYTE OF SEEK
         LR    FIVE,FOUR
         SLL   FOUR,BITS16              POSITION BB
         SLL   FIVE,BITS4               INDEX FOR SUB-UCB
         LA    FIVE,D56(FIVE,SIX)       CALCULATE SUB-UCB ADDRESS
         DROP  SIX
         USING DATACELL,FIVE
         MVC   MIELNAME(D6),DCELVOLI    MOVE VOL SER FOR ENQ
         L     PARMA,DCELVTOC           LOAD TTR OF VTOC START
         DROP  FIVE
.X225604 ANOP                                                   YL02912
         DROP  SIX                                              YL02912
MAINUCB  LM    FIVE,SIX,EXTCON1         LOAD START HH,END CCHH AND TRKS
         STM   PARMB,SIX,DEBPROTG       STORE IN DEB
         L     PARMB,PNTDEB             POINT TO DEB            YL02912
         CLC   PNTEST,TESTDC            TEST MODE               YL02912
         BNE   MAINUC1                  NO                      YL02912
         MVC   DEBEXSCL+D1(L3),HOLDKEY+1  SET APVT POINTER      YL02912
MAINUC1  EQU   *                                                YL02912
         MVI   DEBPROTG,DEBID           INSERT DEB ID
         MVI   DEBEXSCL,DA              INSERT EXTENT SCALE
         MVI   DEBDVMOD,DEVFM           INSERT DEVICE MODE
*
***********************************************************************
* THIS SECTION BUILDS DCB,IOB,ECB
***********************************************************************
*
         MVI   IOB,FLAG1                SET FLAG BYTE FOR COMMAND
*                                          CHAINING AND UNRELATED
         LA    PARMB,DEB                PUT IN DEB POINTER
         ST    PARMB,DEBPTR
         LA    WORKE,ECB                PUT IN ECB POINTER
         ST    WORKE,IOBECBCC
         LA    WORKE,CCW1               PUT IN CHAN PROG POINTER
         LA    WORKO,DCB                PUT IN DCB POINTER
         STM   WORKE,WORKO,IOBSTART
         L     ADDREG,CVTPCNVT          LOAD CONVERSION ROUTINE ADDR
         DROP  ADDREG
         IC    PARMA,ZEROS              CLEAR LOW ORDER BYTE OF TTR
         LA    PARMB,DEB                SET UP DEB POINTER
         LA    WORKE,SEEK               LOAD POINTER TO 8 BYTE AREA FOR
*                                          CONVERT ROUTINE TO RETURN
*                                          ABSOLUTE ADDRESS OF VTOC IN
         LR    WORKO,AREAREG            SAVE WORK AREA POINTER
         STM   NINE,SAVEREG,REGSAVE     SAVE REGISTERS
         BALR  RETREG,ADDREG            GO TO CONVERT ROUTINE
         LM    NINE,SAVEREG,REGSAVE-SVRBEX(WORKO)  RESTORE REGISTERS
         MVC   VTOCADR(L5),SEEK+D3      SAVE VTOC ADDRESS
***********************************************************************
* THIS SECTION RELOCATES THE CHANNEL PROGRAM TO THE WORK AREA
***********************************************************************
         LM    PARMA,FIVE,CCWP1         LOAD THREE CCW'S
         ALR   PARMA,AREAREG
         ALR   WORKE,AREAREG
         ALR   FOUR,AREAREG
         STM   PARMA,FIVE,CCW1          STORE THREE CCW'S
         B     XCTL                    TRANSFER CONTROL TO SECOND LOAD
*                                          MODULE
         EJECT
***********************************************************************
GETL     GETMAIN EC,LV=1024,MF=L        FOR MAIN WORK AREA
***********************************************************************
*        DEVICE TABLE                                           YL02912
***********************************************************************
*                                                               XL03921
* THE PARAMETERS OF THE TABLE IS AS FOLLOWS                     XL03921
*  TABDEV1 - DEVICE TYPE                                        XL03921
*  TABDEV2 - 10% OF ALTERNATES ORIGINALLY AVAILABLE             XL03921
*            100% IF A 2305                                     XL03921
*  TABDEV3 - MAXIMUM NUMBER OF COUNT FIELDS PER TRACK + 1       XL03921
*            NOT +1 IF A 2305                                   XL03921
*  TABDEV4 - WHERE TO GO IF DEVICE IS EQUAL                     XL03921
*                                                               XL03921
TABDEV   DS    0F                      TABLE BEGINNING          XL03921
TABDEV1  DC    X'08'                   2314                     XL03921
TABDEV2  DC    X'06'                                            XL03921
TABDEV3  DC    H'73'                                            XL03921
TABDEV4  B     DISLIKE                                          XL03921
         DC    X'09'                   3330                     XL03921
         DC    X'13'                                            XL03921
         DC    H'98'                                            XL03921
         B     A3330                                            XL03921
         DC    X'0B'                   3350                    PTMFIX
         DC    X'15'                                           PTMFIX
         DC    H'105'                                          PTMFIX
         B     A3330                                           PTMFIX
         DC    X'06'                   2305-1                   XL03921
         DC    X'01'                                            XL03921
         DC    H'34'                                            XL03921
         B     DISLIKE                                          XL03921
         DC    X'07'                   2305-2                   XL03921
         DC    X'01'                                            XL03921
         DC    H'75'                                            XL03921
         B     DISLIKE                                          XL03921
         AIF  ('&LIB' NE 'LIB1').X225603
         DC    X'01'                   2311                     XL03921
         DC    X'03'                                            XL03921
         DC    H'60'                                            XL03921
         B     DISLIKE                                          XL03921
         DC    X'04'                   2302                     XL03921
         DC    X'18'                                            XL03291
         DC    H'81'                                            XL03291
         B     DISLIKE                                          XL03291
         DC    X'05'                   2321                     XL03291
         DC    X'40'                                            XL03921
         DC    H'65'                                            XL03921
         B     DISLIKE                                          XL03291
.X225603 ANOP
         AIF   ('&LIB' EQ 'LIB1').X225602                       XL03291
         DC    X'0D'                   3330C                    XL03145
         DC    X'13'                                            XL03145
         DC    H'98'                                            XL03145
         B     A3330                                            XL03145
         DC    X'0A'                   3340                     XL03130
         DC    X'18'                   ALTERNATE VALUE MAY BE   YM7385
*                                      CHANGED BY PROGRAM LOGIC XL03130
         DC    H'52'                                            XL03130
         B     A3340                                            XL03130
.X225602 ANOP                                                   XL03291
* END OF TABLE WITH DELIMITOR                                   XL03291
         DC    X'FF'                                            XL03291
         DC    X'FF'                   DELIMITOR                XL03291
         DC    X'FFFF'                                          XL03291
         B     NONSUP                                           XL03291
* END OF TABDEV TABLE                                           XL03291
***********************************************************************
*                                                               XL03291
         DS    0F                                               XL03291
EXTCON1  DC    X'0000'                  CONSTANT FOR END CCHH
         DC    X'FFFFFFFF7FFF'          OF DEB
VTOCNAME DC    CL8'SYSVTOC'             MAJOR QUE ELEMENT NAME
TESTDC   DC    CL4'TEST'                TEST MODE               YL02912
CCWP1    CCW   X'1A',DEQAREA-SVRBEX,X'40',5  READ HOME ADDRESS
CCWP2    CCW   X'12',IECSDSF4-SVRBEX,X'40',8  READ COUNT FIELD
CCWP3    CCW   X'06',IECSDSF4-SVRBEX,X'00',96  READ DATA FOR F4 DSCB
ENABLE   DC    X'FF'                    ENABLE CONSTANT
TTRCON   DC    X'C378'                  MODULE ATTRIBUTES
         DC    X'000400'                CORE REQUIREMENTS IN BYTES
         DC    X'0400'                  MODULE LENGTH IN BYTES
ATLASNM  DC    CL6'IGG086'              COMMON NAME
CYL696   DC    H'696'                  NO. CYL 72 MEGABYTE WIN. YM7385
MAINT    DC    2CL25'IGC0008F MAINTENANCE AREA'                 YL02912
***********************************************************************
         SPACE
*XCTL BY TTR TABLE
***********************************************************************
         SPACE
         ORG   IGC0008F+1000            FIRST ORG
TTRTABLE EQU   *
ATLAS02  DC    C'0A'                    ID OF NEXT ATLAS LOAD
TTR2     DC    X'00000000'              TTR OF NEXT ATLAS LOAD
ATLAS06  DC    C'AE'                    ID OF ERROR ATLAS LOAD
TTR6     DC    X'00000000'              TTR OF ERROR ATLAS LOAD
         DC    X'0000'                  LAST TABLE ENTRY CODE
         ORG   IGC0008F+1020            SECOND ORG
         DC    C'086'                   ATLAS SVC CODE
         DC    X'7D'                    RELATIVE TABLE START ADDRESS
*                                          IN DOUBLE WORDS
***********************************************************************
ZEROS    EQU   EXTCON1
EXIT     EQU   3                        RETURN SVC
USSENS0  EQU   2                        SENSE BYTE ZERO-IOB
USSENS1  EQU   3                        SENSE BYTE ONE -IOB
USCSW    EQU   12                       ADDRESS OF LAST EXECUTED CCW+8
*                                         AS PRESENTED IN USER'S IOB
USSEEK   EQU   32                       SEEK ADDRESS   -IOB
USDCBPT  EQU   20                       POINTER TO DCB - IOB
USDEBAD  EQU   44                       POINTER TO DEB -DCB
USEXSCL  EQU   28                       EXTENT SCALE   -DEB
DEBOFLGS EQU   8
DEBDASD  EQU   32
DEBSTRCC EQU   6
DEBSTRHH EQU   8
DEBENDCC EQU   10
DEBENDHH EQU   12
DEBNOEXT EQU   16
NEXTEXT  EQU   16
         EJECT
***********************************************************************
WORKDSCT DSECT
SVRBEX   DS    1F                       SAVE AREA FOR SVRB EXTENSION
*                                          POINTER
DEVTAB   DS    1F                       DEVICE TYPE INDICATOR FROM UCB
SWITCH   DS    CL1                      LOGIC SWITCH
*                                       0 BIT INDICATES CHANNEL
*                                          PROGRAM NOT RE-EXECUTABLE
*                                       1 BIT INDICATES WRITE SPECIAL
*                                          REQUIRED
*                                       2 BIT INDICATES TRACK OVERFLOW
*                                          DATA SET
*                                       4 BIT INDICATES NO ALTERNATES
*                                          AVAILABLE
*                                       6 BIT INDICATES AN ERROR ON
*                                          OTHER THAN THE ORIGINAL
*                                          ERROR RECORD
*                                       7 BIT INDICATES VTOC HAS BEEN
*                                          ENQUEUED UPON
NUMBERR  DS    CL2                      NUMBER OF ERROR RECORDS
VTOCADR  DS    CL5                      VTOC ADDRESS CCHHR
SPLIT    DS    X'00'                    SPLIT CYLINDER SWITCH
RETPT    DS    1F                       POINTER TO NEXT ROUTINE
***********************************************************************
         DS    0F
         IECSDSL1 (4)
***********************************************************************
TICADDR  DS    1F                       ADDRESS OF TIC CCW IN NEW
*                                          CHANNEL PROGRAM
SAVEADDR DS    1F                       ADDRESS OF FIRST COUNT FIELD
CNTCORE  DS    1F                       NUMBER OF BYTES IN COUNT FIELD
*                                          WORK AREA
COREADDR DS    1F                       ADDRESS OF COUNT FIELD
*                                          WORK AREA
RECADDR  DS    1F                       ADDRESS OF CORE GOTTEN FOR
*                                          LONGEST RECORD SIZE
RECCORE  DS    1F                       NUMBER OF BYTES IN THIS AREA
DDNAME   DS    1D                      SAVE DDNAME              XL03130
DEVINFO  DS    5F                      DEVICE INFO              XL03130
WINCYL   EQU   DEVINFO+D8               NUMBER OF CYLINDERS     XL03130
PARMUSER DS    1F                       POINT TO USER PARM LIST YL02912
PNTEST   DS    1F                       TEST MODE               YL02912
PNTSNAP  DS    1F                       SNAP LIST               YL02912
PNTSNS   DS    CL2                      SENSE BYTE - USER       YL02912
         DS    CL1                      FILLER                  YL02912
PNTMIOB  DS    CL1                      M OF IOB                YL02912
PNTIOB   DS    1F                       POINT TO IOB            YL02912
PNTCNT   DS    1F                       POINT TO COUNTS         YL02912
PARMCNT  DS    2F                       COUNTS                  YL02912
PNTIOT   DS    CL2                      TIOT OFFSET             YL02912
         DS    CL1                      FILLER                  YL02912
PNTOFLO  DS    CL1                      TRACK OVERFLO           YL02912
PNTDEB   DS    1F                       DEB POINTER             YL02912
HOLDKEY  DS    1F                       USER KEY                YL02912
REGHOLD  DS    1F                       TEMP REG SAVE           YL02912
CNTADDR  DS    1F                       ADDRESS OF COUNT FIELD FOR
*                                          CCW IN ERROR
ERRTYPE  DS    CL1                      TYPE OF ORIGINAL ERROR
RECNUMB  DS    1F                       R OF ERROR RECORDS
REGSAVE  DS    5F                       SAVE AREA FOR REGISTERS
***********************************************************************
ECB      DS    1F                       ECB
IOB      DS    8F                       IOB
IOBECBCC EQU   IOB+4
IOBSTART EQU   IOB+16
SEEK     DS    2F                       SEEK ADDRESS
DEB      DS    12F                      DEB
DEBNMEXT EQU   DEB+16
DEBPROTG EQU   DEB+24
DEBEXSCL EQU   DEB+28
DEBDVMOD EQU   DEB+32
DEBNMTRK EQU   DEB+46
DEBPTR   DS    F                        DCB SLOT
MIELNAME DS    CL6                      VOLUME SERIAL SLOT
DCB      EQU   DEBPTR-44
R0AREA   DS    2D                       AREA FOR R0
DEQAREA  DS    2D                       DEQ TABLE AREA
MJELNAME DS    2F                       MAJOR QUEUE NAME AREA
ENQAREA  DS    2D                       ENQ TABLE AREA
USERPARM DS    1F                       POINTER TO USER'S PARM LIST
NUMALT   DS    1H                       NUMBER OF ALTERNATES TO BE
*                                          TRIED
MAXCOUNT DS    1H                       MAXIMUM NUMBER OF COUNT FIELDS
*                                          PER TRACK+1
XCTLPTR  DS    2F                       PTR TO XCTL WORK AREA TABLE
XCTLAREA DS    0CL32
XCTLNM   DS    CL6                      COMMON BYTES OF ATLAS NAME
XCTLID   DS    CL2                      ID OF ATLAS MODULE
         DS    CL6                      *
TTR      DS    CL3                      TTR OF ATLAS MODULE
         DS    CL5                      *
TTRATTB  DS    CL2                      ATTRIBUTES OF ATLAS - ALL LOADS
TTRCREQ  DS    CL3                      CORE REQUIREMENT OF ATLAS LOAD
TTRLNGTH DS    CL2                      LENGTH OF ATLAS LOAD
         DS    CL3                      *
INCRMNT  DS    1F                       INCREMENT FOR READ COUNTS
PROGADDR DS    1F                       ADDRESS OF NEW CHANNEL PROGRAM
ALTHA    DS    2F                       ALTERNATE TRACK HOME ADDRESS
ALTR0    DS    4F                       ALTERNATE TRACK R0
***********************************************************************
CCW1     DS    1D                       CCW AREA
CCW2     DS    1D                       *
CCW3     DS    1D                       *
CCW44    DS    1D                       *
CCW45    DS    1D                       *
CCW56    DS    1D                       *
CCW57    DS    1D                       *
CCW31    DS    1D                       *
CCW32    DS    1D                       *
CCW33    DS    1D                       *
CCW34    DS    1D                       *
CCW35    DS    1D                       *
CCW36    DS    1D                       *
CCW37    DS    1D                       *
CCW38    DS    1D                       *
CCW39    DS    1D                       *
CHECKHA  DS    2F                       AREA FOR HOME ADDRESS OF
DCBRECFM EQU   36
*                                       ALTERNATE
***********************************************************************
CVT      DSECT
         CVT   SYS=MIN
***********************************************************************
UCB      DSECT
         IEFUCBOB
         END
