         TITLE 'IGG086AE - SIXTH LOAD MODULE FOR SVC 86'         S20201
         COPY  LCGASMSW
IGG086AE CSECT
* FIX WHA CMD FOR 3350.                                        @ZM41672
*                                                               XM4406
*
*        VS1 RELEASE 5.0 DELETIONS/CHANGES                     @Z30RSAG
* 3350 DEVICE SUPPORT.                                         @Z30RSAG
* FIX BAD HA/R0 MSG.                                           @ZM40498
*          RELEASE 21 DELETIONS/CHANGES
*3650179000-180000,187000-188000,190000-195000,333000-334000,    S21040
*3650382000,384000                                               S21040
*          RELEASE 20 DELETIONS/CHANGES
*0792001000-002000,320000,329000                                 S20201
*
*STATUS CHANGE LEVEL 003
*                                                                     *
*TITLE 'IGG086AE' - SIXTH LOAD MODULE OF SVC 86                       *
*                                                                     *
*FUNCTION/OPERATION                                                   *
*    IGG086AE HAS TWO MAJOR FUNCTIONS.  FIRST, IT UPDATES THE FORMAT  *
*4 DSCB TO REFLECT THE CURRENT NUMBER OF ALTERNATE TRACKS AVAILABLE   *
*AND THE ADDRESS OF THE NEXT AVAILABLE ALTERNATE.  IT THEN WRITES     *
*HOME ADDRESS AND RECORD ZERO ON THE BAD TRACK.  IF THE USER'S CHANNEL*
*PROGRAM IS RE-EXECUTABLE IT WILL ATTEMPT TO EXECUTE THE PROGRAM.     *
*SECOND, IT DEQUEUES THE VTOC, FREES GOTTEN CORE, AND EXITS.          *
*                                                                     *
*ENTRY POINT                                                          *
*    IGG086AE TESTS REGISTER FOUR FOR AN INDICATION OF WHETHER THE    *
*MODULE WERE ENTERED DUE TO AN ERROR CONDITION OR FOR NORMAL          *
*PROCESSING.                                                          *
*                                                                     *
*INPUT                                                                *
*    CONSISTS OF THE 688-BYTE WORK AREA POINTED TO BY REGISTER 11,    *
*A WORK AREA FOR COUNT FIELDS AND TRACK DESCRIPTION CHANNEL PROGRAM   *
*POINTED TO BY REGISTER 10, AND A CONDITION CODE IN REGISTER FOUR.    *
*                                                                     *
*OUTPUT                                                               *
*    RETURN CODE IN REGISTER FIFTEEN INDICATING THE STATUS OF         *
*COMPLETION OF THE JOB AND THE UPDATED FORMAT FOUR DSCB.  A RETURN    *
*PARAMETER LIST IS LOADED INTO REGISTER ZERO.  THE FIRST BYTE SHOWS   *
*THE NUMBER OF RECORD NUMBERS RETURN IN THE REMAINING THREE BYTES OF  *
*THE REGISTER.  THE RECORD NUMBERS OF A MAXIMUM OF THREE ERROR        *
*RECORDS ARE GIVEN TO IDENTIFY THOSE RECORDS WHICH COULD NOT BE READ  *
**OR WRITTEN WITHOUT ERROR.                                           *
*                                                                     *
*EXTERNAL ROUTINES                                                    *
*   NONE                                                              *
*                                                                     *
*EXITS                                                                *
*    ERROR - BRANCHES TO BEGIN, THAT PORTION OF THE PROGRAM WHICH     *
*            DEQUEUES THE VTOC, FREES GOTTEN CORE, AND EXITS          *
*    NORMAL - SVC 3 EXIT                                              *
*                                                                     *
*TABLES/WORK AREAS                                                    *
*    MAIN WORK AREA CONTAINING THE FORMAT 4 DSCB, MAXIMUM NUMBER OF   *
*COUNT FIELDS PER TRACK, A POINTER TO USER'S PARAMETER LIST, ECB,     *
*IOB, DEB, AND DCB.                                                   *
*                                                                     *
*ATTRIBUTES                                                           *
*    REENTRANT, REFRESHABLE                                           *
*                                                                     *
*
*
***********************************************************************
*                                                                     *
*                               EQUATES                               *
*                                                                     *
***********************************************************************
***********************************************************************
*          COMPLETION CODES                                           *
***********************************************************************
CC24     EQU   24                       ERROR READING FORMAT4 DSCB
CC36     EQU   36                       HA OR R0 ERRORS
CC52     EQU   52                       ERROR RE-EXECUTING USER PROGRAM
***********************************************************************
***********************************************************************
*          DISPLACEMENT CONSTANTS                                     *
***********************************************************************
D0       EQU   0
D1       EQU   1
D2       EQU   2
D3       EQU   3
D4       EQU   4
D5       EQU   5
D6       EQU   6
D7       EQU   7                        *                        S21040
D11      EQU   11                      3350 HA LENGTH          @X50RSPC
MADHA    EQU   124                     3350 HA SKIP DISP       @Z30RSAG
MADR0C   EQU   218                     3350 R0 CNT SKIP DISP   @Z30RSAG
MADR0D   EQU   310                     3350 R0 DATA SKIP DISP  @Z30RSAG
D22      EQU   22                                               REFIX
***********************************************************************
*          LENGTH CONSTANTS                                           *
***********************************************************************
L1       EQU   1
L2       EQU   2
L3       EQU   3
L4       EQU   4
L5       EQU   5
L6       EQU   6                                                REFIX
L7       EQU   7
L11      EQU   11                       *                        S21040
L24      EQU   24                                               REFIX
SILI     EQU   X'20'                    CCW FLAG                REFIX
**********************************************************************
*        DEVICE EQUATES                                              *
**********************************************************************
D3330    EQU   X'09'                    3330 DEVICE TYPE         S20201
D3350    EQU   X'0B'                   3350 DEVICE TYPE        @Z30RSAG
***********************************************************************
*                                                                     *
***********************************************************************
BADTRK   EQU   X'02'
D2314    EQU   X'08'                    DEVICE TYPES
D2321    EQU   X'05'                        *
         AIF   ('&LIB' EQ 'LIB1').X225603                       XL03130
SENSE    EQU   X'04'                    SENSE CCW OP CODE       XM4406
NE       EQU   7                        NOT EQUAL CONDITION CODE XM4406
GOOD     EQU   X'7F'                    GOOD ECB COND CODE      XM4406
D3330C   EQU   X'0D'         3330C                              XL03145
D3340    EQU   X'0A'                             3340           XL03130
RHA      EQU   X'1A'                   READ HOME ADDRESS        XL03130
SDHA     EQU   X'6E'                   HA SKIP VALUE            XL03130
SDROC    EQU   X'C8'                   R0 COUNT SKIP VAL        XL03130
SDROD    EQU   X'11D'                  R0 DATA SKIP VAL         XL03130
.X225603 ANOP                                                   XL03130
ENQSW    EQU   X'01'                    ENQUEUE SWITCH
EOF      EQU   X'0D'                    END OF FILE
FFSW     EQU   X'FF'
FLAG1    EQU   X'42'
IOEROR   EQU   X'20'
NOREEX   EQU   X'80'
R0DD8    EQU   X'08'                    RECORD ZERO DATA LENGTH  S21040
RR0      EQU   X'16'                    READ RECORD 0
X0       EQU   0
*
*
         EJECT
***********************************************************************
*REGISTER ASSIGNMENTS
***********************************************************************
PARMA    EQU   0                        PARAMETER REGISTER
PARMB    EQU   1                        PARAMETER REGISTER
WORKE    EQU   2                        WORK REGISTER
WORKO    EQU   3                        WORK REGISTER
FOUR     EQU   4                        WORK REGISTER - RETURN CODE
FIVE     EQU   5                        WORK REGISTER
SIX      EQU   6                        WORK REGISTER
SEVEN    EQU   7                        WORK REGISTER
EIGHT    EQU   8                        WORK REGISTER
NINE     EQU   9                        WORK REGISTER
TEN      EQU   10                       ADDRESS OF COUNT WORK AREA
AREAREG  EQU   11                       ADDRESS OF WORK AREA
BASEREG  EQU   12                       ADDRESS OF MODULE
SAVEREG  EQU   13                       WORK REGISTER
RETREG   EQU   14                       WORK REGISTER
ADDREG   EQU   15                       COMPLETION CODE REGISTER
***********************************************************************
*        START OF MODULE
***********************************************************************
         SPACE
         BALR  BASEREG,X0               ESTABLISH
         USING FIRST,BASEREG               OWN ADDRESSABILITY
         USING WORKDSCT,AREAREG         ESTABLISH ADDRESSABILITY OF
*                                          WORK AREA
         USING CNTAREA,TEN              ESTABLISH ADDRESSABILITY OF
*                                          COUNT FIELD WORK AREA
***********************************************************************
         SPACE
*THIS SECTION BUILDS A CHANNEL PROGRAM TO WRITE AND READ BACK CHECK
*HOME ADDRESS AND RECORD ZERO ON THE BAD TRACK
***********************************************************************
         SPACE
FIRST    LTR   FOUR,FOUR                WAS THIS MODULE ENTERED
*                                          BECAUSE OF ERRORS
         LA    EIGHT,NORMAL
         ST    EIGHT,RETPT
         BZ    UPDATE                   NO.UPDATE VTOC
         CH    FOUR,HALF48              DOES RETURN CODE INDICATE NO
         BE    BEGIN                       ERRORS FOUND ON TRACK
         CH    FOUR,HALF16              DOES CODE INDICATE ERROR IN
*                                          ASSIGNING ALTERNATE
         BNE   TEST3                    NO, MAKE ANOTHER TEST
         LA    EIGHT,BEGIN              ADDRESS OF EXIT ROUTINE
         ST    EIGHT,RETPT
         B     UPDATE                   BRANCH TO UPDATE THE VTOC
TEST3    CH    FOUR,HALF40              DOES CODE INDICATE ERR IN OTHER
*                                          THAN ORIGINAL ERROR RECORD
         BNE   BEGIN                    PREPARE FOR EXIT
         LA    EIGHT,NORMAL             ADDRESS OF CHANNEL PROGRAM
         ST    EIGHT,RETPT
         B     UPDATE                   BRANCH TO UPDATE VTOC
NORMAL   L     PARMB,USERPARM           PICK UP ADDRESS OF USER'S
*                                          PARAMETER LIST
         L     WORKO,D4(X0,PARMB)       GET ADDRESS OF USER'S CCHH
         LM    FIVE,EIGHT,CCWONE        LOAD FIRST TWO CCW'S
         LA    NINE,HOMEAD              PICK UP ADDRESS OF HOME ADDRESS
         CLI   DEVTAB+D3,D3350         IS IT A 3350 ?          @ZM41672
         BNE   NOTMAD                  NO                      @ZM41672
         LA    NINE,MADSD              GET 3350 HA ADDR        @ZM41672
NOTMAD   ALR   FIVE,NINE                   TO BE WRITTEN       @ZM41672
         LA    NINE,R0                  PICK UP ADDRESS OF RECORD ZERO
         ALR   SEVEN,NINE                  TO BE WRITTEN
         STM   FIVE,EIGHT,CCW1          STORE TWO CCW'S IN WORK AREA
         LM    FIVE,EIGHT,CCWTHREE      LOAD LAST TWO CCW'S
         LA    NINE,HOMEAD              PICK UP ADDRESS TO READ BACK
         ALR   FIVE,NINE                   CHECK HOME ADDRESS
         LA    NINE,R0                  PICK UP ADDRESS TO READ BACK
         ALR   SEVEN,NINE                  CHECK RECORD ZERO
         STM   FIVE,EIGHT,CCW3          STORE LAST 2 CCW'S IN WORK AREA
         MVC   SEEK+D3(L5),D0(WORKO)    SET SEEK ADDRESS TO BAD TRACK
         MVC   DEBSTRCC(L4),D0(WORKO)   SET DEB FOR SINGLE TRACK FXTENT
         MVC   DEBENDCC(L4),D0(WORKO)      USING BAD TRACK
         BAL   EIGHT,SETBLKS            BRANCH TO RESET CONTROL BLOCKS
         LA    EIGHT,CCW1               PUT ADDRESS OF CHANNEL PROGRAM
         ST    EIGHT,IOBSTART              INTO IOB
         AIF   ('&LIB' EQ 'LIB1').X225600                       XL03130
         CLI   DEVTAB+D3,D3350         IS IT A 3350 ?          @Z30RSAG
         BNE   NOMAD                   NO                      @Z30RSAG
         MVI   CCW1+D7,D11             SET 3350 HA LNGTH       @X50RSPC
         B     FIXCCW                                          @Z30RSAG
NOMAD    EQU   *                                               @Z30RSAG
***********************************************************************
*        CHECK FOR 3340
***********************************************************************
         CLI   DEVTAB+D3,D3340                   IS IT A 3340   XL03130
         BNE   NOTWIN                  NO,MUST BE A 5 BYTE HA   XL03130
         MVI   CCW1+D7,D7              SET BYTE COUNT TO 7 WHA  XL03130
FIXCCW   EQU   *                                               @Z30RSAG
         MVC   HOMEAD+D3(L4),D0(WORKO) SET UP HA                XL03130
         MVI   HOMEAD+D2,BADTRK        INDICATE DEFECTIVE TRACK XL03130
         XC    HOMEAD(L2),HOMEAD       CLEAR 1ST 2 BYTES        XL03130
         BAL   NINE,GETSD               GET SD BYTES            XM4406
         B     ISWIN                   BYPASS 5 BYTE CODE       XL03130
         EJECT
*        THE FOLLOWING WILL RETREIVE THE 3340 SKIP-DISPLACEMENT (SD)
***********************************************************************
*        SET UP HOME ADDRESS
***********************************************************************
*        BYTES.
GETSD    EQU   *                                                XM4406
         LA    WORKE,SDARSZ             LOAD WORK AREA SIZE     REFIX
         GETMAIN R,LV=(WORKE),SP=229                           @ZA06533
         USING SDAREA,PARMB
         STM   WORKE,EIGHT,REGSAV       SAVE WORK REG           REFIX
         LR    WORKO,PARMB               LOAD BASE REG           REFIX
         USING SDAREA,WORKO
         LM    FIVE,EIGHT,CCWTHREE      GET CCW MASK            XM4406
         LA    WORKE,HOMEAD             GET ADDR OF CURRENT HA  XM4406
         ALR   FIVE,WORKE               PUT HA IN RDHA          XM4406
         LA    WORKE,SENSA              ADDR OF SENSE AREA      XM4406
         ALR   SEVEN,WORKE              GET SENSE AREA ADDR     XM4406
         STM   FIVE,EIGHT,SDCCW1        STORE CCWS IN W.A.      XM4406
         MVI   SDCCW2,SENSE              RESET OPCODE TO SENSE   XM4406
         MVI   SDCCW2+L4,SILI            RESET FLAG BYTE         XM4406
         MVI   SDCCW2+L7,L24             RESET LENGTH BYTE       XM4406
         L     FOUR,IOBSTART            GET CURRENT CCW ADDR    XM4406
         LA    SEVEN,SDCCW1             LOAD NEW CCW ADDR       XM4406
         ST    SEVEN,IOBSTART           RESET IOB CCW ADDR      XM4406
         EXCP  IOB
         WAIT  ECB=ECB
         ST    FOUR,IOBSTART            RESTORE IOB CCW ADDR    XM4406
         CLI   ECB,GOOD                 SENSE RETURNED OK ?     XM4406
         BNE   BRNINE                 NO, EXIT                XM4406
         MVC   HOMEAD(L2),SDBYTES      MOVE SD BYTES TO HA     XM4406
         CLI   DEVTAB+D3,D3350         IS IT 3350 ?            @Z30RSAG
         BNE   BRNINE                  NO                      @Z30RSAG
         MVC   MADSD(L4),MADSDB        GET FIRST 4 BYTES       @X50RSPC
BRNINE   EQU   *                                                REFIX
         LA    PARMA,SDARSZ             LOAD WORK AREA SIZE     REFIX
         LR    PARMB,WORKO              LOAD W.A. PTR           REFIX
         LM    WORKE,EIGHT,REGSAV       RESTORE REG             REFIX
         FREEMAIN   R,LV=(PARMA),A=(PARMB),SP=229              @ZA06533
         BR    NINE                     EXIT                    XM4406
         EJECT
NOTWIN   EQU   *                       NOT A 3340               XL03130
.X225600 ANOP                                                   XL03130
         MVC   HOMEAD+D1(L4),D0(WORKO)  SET UP HOME ADDRESS
         MVI   HOMEAD,BADTRK            INDICATE DEFECTIVE TRACK
         XC    HOMEAD+D5(L3),HOMEAD+D5  CLEAR REMAINING BYTES
ISWIN    EQU   *                                                XL03130
         MVC   R0(L4),ALTHA+D1          POINT DEFECTIVE PRIMARY TRACK
*                                          TO ALTERNATE
         XC    R0+D5(L11),R0+D5         SET KDD AND DATA FIELD   S21040
*                                       TO 0                     S21040
         MVI   R0+D7,R0DD8              SET DATA LENGTH TO 8     S21040
***********************************************************************
         SPACE
*THIS SECTION WRITES HOME ADDRESS AND R0 ON THE BAD TRACK AND READS
*BACK CHECK
***********************************************************************
         SPACE
CHPROG   EXCP  IOB                      FLAG BAD TRACK
         WAIT  1,ECB=ECB                TEST FOR I/O COMPLETE
         TM    ECB,IOEROR               CHECK FOR SUCCESSFUL I/O S21040
         BNO   CHEKDEV                  NO, CHECK DEVICE TYPE    S21040
***********************************************************************
         SPACE
         SPACE
*THIS SECTION RE-EXECUTES THE USER'S CHANNEL PROGRAM IF POSSIBLE
***********************************************************************
         SPACE
REEX     TM    SWITCH,NOREEX            IS USER'S CHANNEL PROGRAM
*                                          RE-EXECUTABLE
         BO    BEGIN                    NO,GO TO EXIT ROUTINE
         L     PARMB,PARMUSER           GET USERS PARAM         YL02912
***********************************************************************
         MODESET KEYADDR=HOLDKEY,WORKREG=2   GET USER KEY       YL02912
***********************************************************************
         L     PARMB,D0(X0,PARMB)       PICK UP POINTER TO IOB
         LR    EIGHT,PARMB
         L     WORKE,D4(X0,PARMB)       PICK UP POINTER TO ECB
         EXCP  (1)                      RE-EXECUTE USER'S CHANNEL
*                                          PROGRAM
         LR    PARMB,WORKE              SET ECB POINTER
         WAIT  1,ECB=(1)                WAIT FOR I/O COMPLETE
         TM    D0(WORKE),IOEROR         CHECK FOR SUCCESSFUL COMPLETION
         BO    REEX1                    YES,SUCCESSFUL COMPLETE YL02912
         CLI   CSW(EIGHT),EOF           WAS CONDITION END OF FILE
         BO    REEX1                    YES, EXIT               YL02912
         LA    FOUR,CC52                SET COMPLETION CODE FOR USER'S
*                                          CHANNEL PROGRAM RE-EXECUTED
*                                          WITH AN I/O ERROR
REEX1    EQU   *                                                YL02912
***********************************************************************
         MODESET EXTKEY=DATAMGT         KEY 5                   YL02912
***********************************************************************
         B     BEGIN                    GO TO EXIT ROUTINE
***********************************************************************
         SPACE
*THIS SECTION  SETS UP AND EXECUTES A CHANNEL PROGRAM TO WRITE AND
*READ BACK CHECK THE FORMAT 4 DSCB
***********************************************************************
         SPACE
UPDATE   BAL   EIGHT,SETBLKS            RESET CONTROL BLOCKS
         AIF   ('&LIB' NE 'LIB1').X225605                       YL02912
.X225605 ANOP                                                   YL02912
NOT2321  LH    EIGHT,DS4HCCHH+D2        PICK UP HH OF NEXT ASSIGNABLE
*                                          ALTERNATE
         LA    EIGHT,D1(X0,EIGHT)       INCREMENT BY ONE
         CH    EIGHT,DS4DEVSZ+D2        CHECK FOR NEED TO RESET TRACK
*                                          NUMBER TO ZERO
         BL    NOUP                     NO, TRACK NUMBER IS VALID
         XC    DS4HCCHH+D2(L2),DS4HCCHH+D2  SET TRACK NUMBER TO ZERO
         LH    EIGHT,DS4HCCHH           PICK UP CC OF NEXT ASSIGNABLE
*                                          ALTERNATE
         LA    EIGHT,D1(X0,EIGHT)       INCREMENT BY ONE
         STH   EIGHT,DS4HCCHH
         SPACE
REDUCE   LH    EIGHT,DS4NOATK           REDUCE NUMBER OF AVAILABLE
         BCTR  EIGHT,X0                    ALTERNATES BY ONE
         STH   EIGHT,DS4NOATK
         B     LOADCCW                  RELOCATE F4 CHANNEL PROGRAM
NOUP     STH   EIGHT,DS4HCCHH+D2        STORE NEW TRACK NUMBER
         B     REDUCE                   REDUCE NUMBER OF AVAILABLE
*                                          ALTERNATES
***********************************************************************
*        SET UP CCWS FOR WRITING FORMAT 4
***********************************************************************
         AIF   ('&LIB' NE 'LIB1').X225604                       YL02912
STORE1   MVC   DS4HCCHH+D3(L1),SEEK+D6  MOVE IN NEW TRACK ADDRESS
         B     REDUCE                   REDUCE NUMBER OF AVAILABLE
*                                          ALTERNATES
STORE2   MVC   DS4HCCHH+D2(L1),SEEK+D5  MOVE IN NEW CYLINDER ADDRESS
         B     REDUCE                   REDUCE NUMBER OF AVAILABLE
*                                          ALTERNATES
STORE3   MVC   DS4HCCHH+D1(L1),SEEK+D4  MOVE IN NEW STRIP ADDRESS
         B     REDUCE                   REDUCE NUMBER OF AVAILABLE
*                                          ALTERNATES
.X225604 ANOP                                                   YL02912
LOADCCW  LM    FIVE,EIGHT,CCWFIVE
         LA    NINE,SEEK+D3             PICK UP ADDRESS OF CCHH FOR
*                                          SEARCH
         ALR   FIVE,NINE                INSERT IN SEARCH CCW
         LA    NINE,CCW1                SET ADDRESS OF COMMAND FOR TIC
         ALR   SEVEN,NINE
         STM   FIVE,EIGHT,CCW1          RELOCATE FIRST TWO CCW'S
         LM    FIVE,SIX,CCWSEVEN        LOAD WRITE CCW
         LA    NINE,IECSDSF4            GET POINTER TO WRITE
         ALR   FIVE,NINE                   FORMAT 4 DSCB
         LM    SEVEN,EIGHT,CCWFIVE      LOAD SID CCW
         LA    NINE,SEEK+D3             PICK UP ADDRESS OF CCHH FOR
*                                          SEARCH
         ALR   SEVEN,NINE
         STM   FIVE,EIGHT,CCW3          RELOCATE SECOND TWO CCW'S
         LM    FIVE,SIX,CCWSIX          LOAD TIC CCW
         LA    NINE,CCW44               LOAD ADDRESS FOR TIC
         ALR   FIVE,NINE
         LM    SEVEN,EIGHT,CCWEIGHT     LOAD READ BACK CHECK CCW
         LA    NINE,IECSDSF4            GET POINTER TO READ BACK CHECK
         ALR   SEVEN,NINE                  FORMAT 4 DSCB
         STM   FIVE,EIGHT,CCW45         STORE LAST TWO CCW'S
         LA    NINE,CCW1                PUT ADDRESS OF CHANNEL PROGRAM
         ST    NINE,IOBSTART               INTO IOB
         MVC   SEEK+D3(L5),VTOCADR      SET SEEK ADDRESS TO F4 DSCB
         MVC   DEBSTRCC(L4),VTOCADR     SET DEB FOR SINGLE TRACK EXTENT
         MVC   DEBENDCC(L4),VTOCADR        USING F4 DSCB
***********************************************************************
*        REWIRE FORMAT 4 DSCB
***********************************************************************
         EXCP  IOB                      REWRITE F4 DSCB
         WAIT  1,ECB=ECB                WAIT FOR I/O COMPLETE
         TM    ECB,IOEROR               CHECK FOR PERMANENT I/O ERROR
         BZ    SETERR                   BRANCH TO SET ERROR CODE
         L     EIGHT,RETPT
         BR    EIGHT                    BRANCH TO NEXT ROUTINE
         SPACE
SETERR   LA    FOUR,CC24                SET COMPLETION CODE TO 24 FOR
*                                          F4 DSCB CANNOT BE READ
         B     BEGIN                    WRAP UP
***********************************************************************
         SPACE
*THIS SECTION CHECKS DEVICE TYPE AND SETS FLAG TO MOVE HOME ADDRESS
*OVER 705 BYTES IF DEVICE IS 2314 OR 2321, AND 2370 BYTES FOR 3330
***********************************************************************
         SPACE
CHEKDEV  EQU   *                       CHECK DEVICE TYPE        XL03291
         AIF   ('&LIB' EQ 'LIB1').X225601                       XL03291
         CLI   DEVTAB+D3,D3340                   IS IT A 3340   XL03130
         BE    CHKSKIP                 ANALYZE SKIP             XL03130
         CLI   DEVTAB+D3,D3350         IS IT 3350 ?            @Z30RSAG
         BE    CHKSKIP                 YES                     @Z30RSAG
.X225601 ANOP                                                   XL03291
         CLI   DEVTAB+D3,D2314          CHECK FOR 2314
         BNL   SETBIT                  YES, SET FLAG           @Z30RSAG
         CLI   DEVTAB+D3,D2321          CHECK FOR 2321
         BNE   ERRCOND                  NO, NOT A 2321
         SPACE
SETBIT   MVI   HOMEAD,FLAG1             SET FLAG TO INDICATE DEFECTIVE
*                                          TRACK AND TO MOVE HOME
*                                          ADDRESS OVER 705 BYTES
*                                          FOR 2314 OR 2321 AND
*                                          2370 BYTES FOR 3330
***********************************************************************
*        SKIP DISPLACEMENT ON 3340
***********************************************************************
WIN1234  EQU   *                       IMPLEMENT SKIP DISPL     XL03291
         BAL   EIGHT,SETBLKS            RESET CONTROL BLOCKS
         EXCP  IOB                      RETRY HA AND R0 WRITE
         WAIT  1,ECB=ECB                WAIT FOR I/O COMPLETE
         TM    ECB,IOEROR               CHECK FOR SUCCESSFUL I/O S21040
         BO    REEX                     YES, TO TRY TO REEXECUTE S21040
*                                          USERS CHANNEL PROGRAM
***********************************************************************
         SPACE
ERRCOND  LA    FOUR,CC36                SET RETURN CODE TO 36 - COULD
*                                          NOT FLAG BAD TRACK, HOME
*                                          ADDRESS OR R0 ERROR
         B     BEGIN                    WRAP UP
***********************************************************************
*        CHECK SKIP DISPLACEMENT
***********************************************************************
         SPACE
         AIF   ('&LIB' EQ 'LIB1').X225602                       XL03130
CHKSKIP  EQU   *                       ANAL. ERR & SET SKIP     XL03130
         LA    EIGHT,EIGHT             SET VALUE OF 8           XL03130
         L     NINE,IOBFLAG3           GET POINTER FROM IOBCSW  XL03130
         LA    NINE,D0(NINE)           CLEAR HIGH ORDER BYTE    XL03130
         SR    NINE,EIGHT              POINT TO LAST CCW        XL03130
         CLI   DEVTAB+D3,D3350         IS IT 3350 ?            @Z30RSAG
         BE    SETMAD                  YES                     @Z30RSAG
         LA    EIGHT,SDHA              SET SKIP DISPLACE FOR HA XL03130
         CLI   D0(NINE),RHA            READ HOME ADDRESS ?      XL03130
         BE    SETSKIP                 SET VALUE                XL03130
         LA    EIGHT,SDROC             SET R0 SKIP DISPLACE     XL03130
         CLI   IOBCSWST+D3,R0DD8         RESIDUAL COUNT 8 ?     XL03130
         BE    SETSKIP                 SET VALUE                XL03130
         LA    EIGHT,SDROD             SET R0 DATA SKIP DISPL   XL03130
SETSKIP  STH   EIGHT,HOMEAD            SET SKIP DISPLACE VALUE  XL03130
         B     WIN1234                 CONTINUE                 XL03130
***********************************************************************
SETMAD   EQU   *                                               @Z30RSAG
         LA    EIGHT,MADHA             SET SKIP DISP FOR HA    @Z30RSAG
         CLI   D0(NINE),RHA            READ HOME ADDRESS ?     @Z30RSAG
         BE    SETSKIP                 YES, SET VALUE          @Z30RSAG
         LA    EIGHT,MADR0C            SET R0 CNT SKIP DISP    @Z30RSAG
         CLI   IOBCSWST+D3,R0DD8       RESIDUAL COUNT = 8 ?    @Z30RSAG
         BE    SETSKIP                 YES, SET VALUE          @Z30RSAG
         LA    EIGHT,MADR0D            SET R0 DATA SKIP DISP   @Z30RSAG
         B     SETSKIP                 GO SET VALUE            @Z30RSAG
.X225602 ANOP                                                   XL03130
         SPACE
SETBLKS  XC    ECB(L4),ECB              ZERO ECB
         XC    IOBSENS0(L2),IOBSENS0    ZERO SENSE BYTES
         XC    IOBCSW(L7),IOBCSW        ZERO LOW ORDER BYTES OF IOB CSW
         BR    EIGHT                    RETURN
BEGIN    MVC   RECNUMB(L1),NUMBERR+D1   MOVE IN NUMBER OF ERROR RECORDS
         L     PARMB,PNTSNAP            GET SNAP LIST POINTER   YL02912
         LTR   PARMB,PARMB              SNAP MODE               YL02912
         BZ    NOSNAP                   NO - DONT SNAP          YL02912
         SNAP  ID=7,MF=(E,(1))          SNAP                    YL02912
NOSNAP   EQU   *                        *                       YL02912
         TM    SWITCH,ENQSW             HAS ENQUEUE BEEN ISSUED
         BZ    NODEQ                    NO. DEQUEUE NOT REQUIRED
***********************************************************************
         SPACE
* THIS SECTION DEQUES FROM THE VTOC
***********************************************************************
DEEQ     EQU   *
         MVI   DEQAREA,FFSW             SET LAST ENTRY CODE
         SR    PARMB,PARMB              CLEAR OPTION
         STH   PARMB,DEQAREA+D2
REGDEQ   DEQ   (MJELNAME,MIELNAME,6,SYSTEMS),MF=(E,DEQAREA)
***********************************************************************
*        FREE CORE FOR LARGEST RECORD
***********************************************************************
NODEQ    OC    RECADDR(L4),RECADDR      DOES CORE FOR LARGEST RECORD
*                                          HAVE TO BE FREED
         BZ    NEXTCORE                 NO, CORE HAS BEEN FREED
         L     WORKE,RECCORE            NUMBER OF BYTES TO BE   YL02912
*                                          FREED
         L     PARMB,RECADDR            LOAD POINTER TO CORE TO BE
*                                          FREED
         FREEMAIN R,LV=(2),A=(1),SP=229                         YL02912
***********************************************************************
*        FREE CORE FOR COUNT FIELDS
***********************************************************************
NEXTCORE OC    COREADDR(L4),COREADDR    DOES CORE FOR COUNT FIELDS HAVE
*                                          TO BE FREED
         BZ    FREEMAIN                 NO, CORE HAS BEEN FREED
         L     WORKE,CNTCORE            NUMBER OF BYTES TO BE   YL02912
*                                          FREED
         L     PARMB,COREADDR           LOAD POINTER TO CORE TO BE
*                                          FREED
         FREEMAIN R,LV=(2),A=(1),SP=229                         YL02912
***********************************************************************
*        FREE MAIN WORK AREA
***********************************************************************
FREEMAIN LR    PARMB,AREAREG            LOAD POINTER TO CORE TO BE
*                                          FREED
         L     RETREG,REGHOLD           SET RETURN              YM3004
         L     FIVE,RECNUMB             LOAD RETURN PARAMETER LIST
         FREEMAIN R,LV=688,A=(1),SP=229                         YL02912
*                                          NUMBERS
         LR    PARMA,FIVE               RETURN PARAMETER LIST IN R0
         LR    ADDREG,FOUR              SET FINAL COMPLETION CODE
         MODESET  EXTKEY=SUPR           KEY 0                   YL02912
         BR    RETREG                   RETURN                  YL02912
***********************************************************************
CCWONE   CCW   X'19',0,X'40',5          WRITE HOME ADDRESS
CCWTWO   CCW   X'15',0,X'40',16         WRITE RECORD ZERO        S21040
CCWTHREE CCW   X'1A',0,X'50',5          READ HOME ADDRESS
CCWFOUR  CCW   X'16',0,X'10',16         READ RECORD ZERO         S21040
CCWFIVE  CCW   X'31',0,X'40',5          SID - FORMAT 4 DSCB
CCWSIX   CCW   X'08',0,X'40',0          TIC
CCWSEVEN CCW   X'05',0,X'40',96         WRITE DATA OF F4 DSCB
CCWEIGHT CCW   X'06',0,X'10',96         READ DATA OF F4 DSCB
***********************************************************************
VTOCNAME DC    CL8'SYSVTOC'             VTOC NAME FOR ENQUEUE AND
*                                          DEQUEUE
CYLNO    DC    X'05'                    NUMBER OF CYLINDERS PER TRACK
*                                          FOR A 2321
STRIPNO  DC    X'0A'                    NUMBER OF STRIPS PER SUBCELL
*                                          FOR A 2321
FULL8    DC    F'8'                     *
HALF48   DC    H'48'                    *
HALF16   DC    H'16'                    *
HALF40   DC    H'40'                    *
MAINT    DC    2CL25'IGG086AE MAINTENANCE AREA'                 YL02912
         ORG   IGG086AE+1021            FIRST AND ONLY ORG
END      DS    CL3'END'                 THE END
***********************************************************************
WORKDSCT DSECT
SVRBEX   DS    1F                       SAVE AREA FOR SVRB EXTENSION
*                                          POINTER
DEVTAB   DS    1F                       DEVICE TYPE INDICATOR FROM UCB
SWITCH   DS    CL1                      LOGIC SWITCH
*                                       0 BIT INDICATES CHANNEL
*                                          PROGRAM NOT RE-EXECUTABLE
*                                       1 BIT INDICATES WRITE SPECIAL
*                                          REQUIRED
*                                       2 BIT INDICATES TRACK OVERFLOW
*                                          DATA SET
*                                       3 BIT INDICATES ENQUEUE WAS
*                                          WITH STEP MUST COMPLETE
*                                       4 BIT INDICATES NO ALTERNATES
*                                          AVAILABLE
*                                       6 BIT INDICATES AN ERROR ON
*                                          OTHER THAN THE ORIGINAL
*                                          ERROR RECORD
*                                       7 BIT INDICATES VTOC HAS BEEN
*                                          ENQUEUED UPON
NUMBERR  DS    CL2                      NUMBER OF ERROR RECORDS
VTOCADR  DS    CL5                      VTOC ADDRESS CCHHR
SPLIT    DS    X'00'                    SPLIT CYLINDER SWITCH
RETPT    DS    1F                       POINTER TO NEXT ROUTINE
***********************************************************************
         DS    0F
         IECSDSL1 (4)
***********************************************************************
TICADDR  DS    1F                       ADDRESS OF TIC CCW IN NEW
*                                          CHANNEL PROGRAM
SAVEADDR DS    1F                       ADDRESS OF FIRST COUNT FIELD
CNTCORE  DS    1F                       NUMBER OF BYTES IN COUNT FIELD
*                                          WORK AREA
COREADDR DS    1F                       ADDRESS OF COUNT FIELD
*                                          WORK AREA
RECADDR  DS    1F                       ADDRESS OF CORE GOTTEN FOR
*                                          LONGEST RECORD SIZE
RECCORE  DS    1F                       NUMBER OF BYTES IN THIS AREA
DDNAME   DS    1D                       DDNAME SAVE AREA        XL03130
DEVINFO  DS    5F                      DEVICE INFO              XL03130
PARMUSER DS    1F                       POINTER USER PARM LIST  YL02912
PNTEST   DS    1F                       TEST MODE               YL02912
PNTSNAP  DS    1F                       SNAP MODE               YL02912
PNTSNS   DS    CL2                      SENSE BYTE - USER       YL02912
         DS    CL1                      FILLER                  YL02912
PNTMIOB  DS    CL1                      M OF IOB                YL02912
PNTIOB   DS    1F                       IOB POINTER             YL02912
PNTCNT   DS    1F                       COUNT POINTER           YL02912
PARMCNT  DS    2F                       COUNTS                  YL02912
PNTIOT   DS    CL2                      TIOT OFFSET             YL02912
         DS    CL1                      FILLER                  YL02912
PNTOFLO  DS    CL1                      TRACK OVERFLO           YL02912
PNTDEB   DS    1F                       DEB POINTER             YL02912
HOLDKEY  DS    1F                       USER KEY                YL02912
REGHOLD  DS    1F                       TEMP REG SAVE           YL02912
CNTADDR  DS    1F                       ADDRESS OF COUNT FIELD FOR
*                                          CCW IN ERROR
ERRTYPE  DS    CL1                      TYPE OF ORIGINAL ERROR
RECNUMB  DS    1F                       R OF ERROR RECORDS
REGSAVE  DS    5F                       SAVE AREA FOR REGISTERS
***********************************************************************
ECB      DS    1F                       ECB
IOB      DS    8F                       IOB
CSW      EQU   12
IOBSENS0 EQU   IOB+2
IOBFLAG3 EQU   IOB+8
IOBCSW   EQU   IOB+9
IOBCSWST EQU   IOB+12
IOBSTART EQU   IOB+16
SEEK     DS    2F                       SEEK ADDRESS
DEB      DS    12F                      DEB
DEBSTRCC EQU   DEB+38
DEBENDCC EQU   DEB+42
DEBPTR   DS    F                        DCB SLOT
MIELNAME DS    CL6                      VOLUME SERIAL SLOT
DCB      EQU   DEBPTR-44
R0AREA   DS    2D                       AREA FOR R0
DEQAREA  DS    2D                       DEQ TABLE AREA
MJELNAME DS    2F                       MAJOR QUEUE NAME AREA
ENQAREA  DS    2D                       ENQ TABLE AREA
USERPARM DS    1F                       POINTER TO USER'S PARM LIST
NUMALT   DS    1H                       NUMBER OF ALTERNATES TO BE
*                                          TRIED
MAXCOUNT DS    1H                       MAXIMUM NUMBER OF COUNT FIELDS
*                                          PER TRACK+1
XCTLPTR  DS    2F                       PTR TO XCTL WORK AREA TABLE
XCTLAREA DS    0CL32
XCTLNM   DS    CL6                      COMMON BYTES OF ATLAS NAME
XCTLID   DS    CL2                      ID OF ATLAS MODULE
         DS    CL6                      *
TTR      DS    CL3                      TTR OF ATLAS MODULE
         DS    CL5                      *
TTRATTB  DS    CL2                      ATTRIBUTES OF ATLAS - ALL LOADS
TTRCREQ  DS    CL3                      CORE REQUIREMENT OF ATLAS LOAD
TTRLNGTH DS    CL2                      LENGTH OF ATLAS LOAD
         DS    CL3                      *
INCRMNT  DS    1F                       INCREMENT FOR READ COUNTS
PROGADDR DS    1F                       ADDRESS OF NEW CHANNEL PROGRAM
ALTHA    DS    2F                       ALTERNATE TRACK HOME ADDRESS
ALTR0    DS    4F                       ALTERNATE TRACK R0
***********************************************************************
CCW1     DS    1D                       CCW AREA
CCW2     DS    1D                       *
CCW3     DS    1D                       *
CCW44    DS    1D                       *
CCW45    DS    1D                       *
CCW56    DS    1D                       *
CCW57    DS    1D                       *
CCW31    DS    1D                       *
CCW32    DS    1D                       *
CCW33    DS    1D                       *
CCW34    DS    1D                       *
CCW35    DS    1D                       *
CCW36    DS    1D                       *
CCW37    DS    1D                       *
CCW38    DS    1D                       *
CCW39    DS    1D                       *
CHECKHA  DS    2F                       AREA FOR HOME ADDRESS OF
*                                       ALTERNATE
*
***********************************************************************
***********************************************************************
CVT      DSECT
***********************************************************************
UCB      DSECT
SDAREA   DSECT
***********************************************************************
REGSAV   DS    7F                       REGISTER SAVE AREA      REFIX
SDCCW1   DS    D                        READ HA                 REFIX
SDCCW2   DS    D                        SENSE CCW               REFIX
SENSA    DS    3D                       AREA FOR SENSE          REFIX
MADSDB   EQU   SENSA+18                3350 SIX SD BYTES       @Z30RSAG
SDBYTES  EQU   SENSA+D22                SKIP DISPL BYTES        REFIX
SDEND    DS    0F                       END                     REFIX
SDARSZ   EQU   SDEND-SDAREA             SIZE OF AREA            REFIX
CNTAREA  DSECT
         DS    F                       FOR DBL WD ALGNMT.      @ZM40498
MADSD    DS    CL4                     FIRST TWO 3350 SD BYTES @Z30RSAG
HOMEAD   DS    2F                       CCW AREA
R0       DS    4F                       *
CCW21    DS    2F                       *
CCW22    DS    2F                       *
CCW23    DS    2F                       *
         END
