         TITLE 'IGG0860A - SECOND LOAD MODULE FOR SVC 86'        S20201
         COPY  LCGASMSW
IGG0860A CSECT
*                                                               YM3064
*A 467000                              FIX MVS 0C4.            @ZM42073
*                                                               YM3004
* FIX BAD HA/R0 MSG.                                           @ZM40498
*
*          RELEASE 22 DELETIONS/CHANGES
*          RELEASE 21 DELETIONS/CHANGES
*          RELEASE 20 DELETIONS/CHANGES
*0791002000-004000                                               S20201
*
*STATUS CHANGE LEVEL 002
*
*TITLE 'IGG0860A' - SECOND LOAD MODULE OF SVC 86                      *
*                                                                     *
*FUNCTION/OPERATION
*   IGG0860A, THE SECOND LOAD MODULE OF SVC 86 ESTABLISHES ITS OWN    *
*ADDRESSABILITY, THAT OF THE 688-BYTE WORK AREA GOTTEN BY IGC0008F,   *
*AND THAT OF A WORK AREA WHICH IT GETS.  ITS FIRST FUNCTION IS TO     *
*EXECUTE A CHANNEL PROGRAM TO READ THE FORMAT 4 DSCB AND PICK UP THE  *
*NUMBER OF ALTERNATES AVAILABLE.  IN ADDITION THIS MODULE ALSO WILL   *
*COMPUTE THE AMOUNT OF CORE NEEDED TO BUILD A CHANNEL PROGRAM TO READ *
*HOME ADDRESS, R0, AND THE MAXIMUM NUMBER OF COUNT FIELDS PER TRACK   *
*+1.  IT GETS ADDITIONAL CORE TO CONTAIN HOME ADDRESS, R0, AND THE    *
*COUNT FIELDS.  SECOND, IT GENERATES THE CHANNEL PROGRAM.             *
*                                                                     *
*ENTRY POINT                                                          *
*   IGG0860A PICKS UP THE MAXIMUM NUMBER OF COUNT FIELDS PER TRACK    *
*FROM INFORMATION OBTAINED BY IGC0008F.                               *
*                                                                     *
*INPUT                                                                *
*   CONSISTS OF THE 688-BYTE WORK AREA SET UP BY IGC0008F.            *
*                                                                     *
*OUTPUT                                                               *
*     CONSISTS OF THE WORK AREA AND THE FOLLOWING RETURN CODES        *
*
*                                                                     *
*         8 - ALL ALTERNATES ASSIGNED                                 *
*        12 - CORE NOT AVAILABLE                                      *
*        24 - FORMAT 4 IN ERROR                                       *
*        28 - USER'S RECORD IS FORMAT4                                *
*                                                                     *
*EXTERNAL ROUTINES                                                    *
*    NONE                                                             *
*                                                                     *
*EXITS                                                                *
*   ERROR - TRANSFERS CONTROL VIA XCTL TO IGG086AE WITH A RETURN      *
*           CODE IN REGISTER 4.                                       *
*   NORMAL - TRANSFERS CONTROL VIA XCTL TO IGG0860B                   *
*                                                                     *
*TABLES/WORK AREAS                                                    *
*   THERE ARE TWO WORK AREAS AS FOLLOW                                *
*                                                                     *
*        1.  A 688-BYTE WORK AREA INCLUDING THE FORMAT 4 DSCB,        *
*            MAXIMUM NUMBER OF COUNT FIELDS PER TRACK + 1, A POINTER  *
*            TO THE USER'S PARAMETER LIST, THE ECB, IOB, DEB, AND     *
*            DCB CONSTRUCTED BY IGC0008F, A POINTER TO THE COUNT      *
*            FIELD WORK AREA, AND AN INDICATION OF THE ORIGINAL       *
*            ERROR TYPE.                                              *
*        2.  A CHANNEL PROGRAM TO READ HOME ADDRESS AND/OR RECORD     *
*            ZERO AND COUNTS.  IT ALSO INCLUDES THE SPACE INTO WHICH  *
*            THE INFORMATION WILL BE READ.                            *
*                                                                     *
*ATTRIBUTES                                                           *
*   REENTRANT, REFRESHABLE                                            *
*                                                                     *
*
*
***********************************************************************
*                                                                     *
*                               EQUATES                               *
*                                                                     *
***********************************************************************
BIT1     EQU   X'40'
***********************************************************************
*          COMPLETION CODES                                           *
***********************************************************************
CC8      EQU   8                        NO ALTERNATES AVAILABLE
CC12     EQU   12                       CORE NOT AVAILABLE
CC24     EQU   24                       ERROR READING FORMAT4 DSCB
CC28     EQU   28                       FORMAT4 DSCB IN ERROR
***********************************************************************
*          DISPLACEMENT CONSTANTS                                     *
***********************************************************************
D0       EQU   0
D1       EQU   1
D2       EQU   2
D3       EQU   3
D4       EQU   4
D7       EQU   7
D8       EQU   8
D48      EQU   48
D255     EQU   255
***********************************************************************
*          LENGTH CONSTANTS                                           *
***********************************************************************
L2       EQU   2
L3       EQU   3
L4       EQU   4
L5       EQU   5
L8       EQU   8
L16      EQU   16
***********************************************************************
*                                                                     *
***********************************************************************
BITS3    EQU   3
BITS4    EQU   4
CCOFF    EQU   X'00'                    COMMAND CHAIN OFF
DOG8     EQU   X'D8'
ENQSW    EQU   X'01'                    ENQUEUE SWITCH
FFSW     EQU   X'FF'
IOEROR   EQU   X'20'
ONEXT    EQU   X'01'                    ONE EXTENT
R0ERR    EQU   X'01'                    R0 ERROR SWITCH
TWO55    EQU   255
X0       EQU   0
ZEROS    EQU   X'00'
*
*
         EJECT
***********************************************************************
* REGISTER ASSIGNMENTS
***********************************************************************
PARMA    EQU   0                        PARAMETER REGISTER
PARMB    EQU   1                        PARAMETER REGISTER
WORKE    EQU   2                        EVEN WORK REGISTER
WORKO    EQU   3                        ODD WORK REGISTER
FOUR     EQU   4                        RETURN CODE REGISTER
FIVE     EQU   5                        WORK REGISTER
SIX      EQU   6                        *
SEVEN    EQU   7                        *
EIGHT    EQU   8                        *
NINE     EQU   9                        *
TEN      EQU   10                       *
AREAREG  EQU   11                       MAIN WORK AREA REGISTER
BASEREG  EQU   12                       BASE REGISTER
SAVEREG  EQU   13                       WORK REGISTER
RETREG   EQU   14                       WORK REGISTER
ADDREG   EQU   15                       COMPLETION CODE REGISTER
***********************************************************************
         SPACE
         BALR  BASEREG,X0               ESTABLISH OWN
         USING BEGIN,BASEREG               ADDRESSABILITY
         USING WORKDSCT,AREAREG         ESTABLISH ADDRESSABILITY FOR
*                                       WORK AREA
*
***********************************************************************
* ROUTINE TO ENQUEUE THE VTOC ON THIS VOLUME
***********************************************************************
*
BEGIN    MVC   MJELNAME(L8),VTOCNAME    MOVE MAJOR QUE NAME TO WORKAREA
         MVI   ENQAREA,FFSW             SET LAST ENTRY CODE
         L     SIX,DEBDVMOD             RELOAD
         SR    PARMB,PARMB              CLEAR CODE AND RETURN FIELDS
         STH   PARMB,ENQAREA+D2
*
         RESERVE (MJELNAME,MIELNAME,E,6,SYSTEMS),RET=HAVE,             *
               MF=(E,ENQAREA),UCB=DEB+32
         OI    SWITCH,ENQSW             ENQUEUE HAS BEEN EXECUTED
         EJECT
*
*
***********************************************************************
* THIS SECTION READS IN THE DATA PORTION OF THE FORMAT 4 DSCB.
***********************************************************************
*
         EXCP  IOB                      READ FORMAT 4 DSCB
         WAIT  1,ECB=ECB                WAIT FOR I/O COMPLETE
         TM    ECB,IOEROR               CHECK FOR PERMANENT I/OERROR
         BO    CKALT                    NO, SUCCESSFUL COMPLETION
*
***********************************************************************
* THIS SECTION ANALYZES THE ERROR IN THE FORMAT4 DSCB READ CHANNEL
* PROGRAM
***********************************************************************
         L     FOUR,USERPARM
         TM    D4(FOUR),BIT1            WAS FULL COUNT SPECIFIED
         BO    ERR24                    NO,SET RETURN CODE
         L     FOUR,D4(X0,FOUR)         GET POINTER TO COUNT FIELD
         CLC   VTOCADR(L5),D0(FOUR)     CHECK FOR USER'S RFCORD IN
*                                          ERROR BEING FORMAT 4
         BE    SETCODE                  YES, FORMAT 4 IS USER'S
*                                          RECORD
***********************************************************************
ERR24    EQU   *
         LA    FOUR,CC24                SET RETURN CODF - FORMAT 4 IN
         B     BADCASE                     ERROR
SETCODE  LA    FOUR,CC28                SET RETURN CODE FOR RECORD IN
*                                          ERROR BEING FORMAT 4
         B     BADCASE                  GO TO ERROR LOAD
***********************************************************************
* THIS SECTION CHECKS FOR AVAILABLE ALTERNATES AND PICKS UP THE NUMBER
* OF AVAILABLE ALTERNATES AND THE ADDRESS OF THE NEXT AVAILABLE ONE
***********************************************************************
CKALT    MVI   DEBNMTRK,ZEROS           SET NUMBER OF TRACKS IN DEB
         MVI   DEBNMTRK+D1,ONEXT           EXTENT TO ONE
         MVI   DEBDVMOD,DOG8            SET DEB MASK TO ALLOW ALL
*                                          WRITES BUT INHIBIT ALL SEEKS
         OC    DS4NOATK(L2),DS4NOATK    CHECK FOR ANY ALTERNATES
         BNZ   PICKLESS                 YES ALTERNATE TRACKS AVAILABLE
         LA    FOUR,CC8                 SET COMPLETION CODE FOR NO
*                                          ALTERNATE TRACKS AVAILABLE
         B     BADCASE                  GO TO ERROR LOAD
PICKLESS CLC   DS4NOATK(L2),NUMALT      LESS ALTERNATES THAN 10
*                                          PER CENT
         BNL   FIRST                    NO,MORE ALTERNATES THAN 10
*                                          PER CENT
         MVC   NUMALT(L2),DS4NOATK      USE NUMBER AVAILABLE
***********************************************************************
* THIS SECTION CALCULATES CORE REQUIREMENTS FOR A CHANNEL PROGRAM TO
* READ COUNT FIELDS AND THEN ISSUES A CONDITIONAL GETMAIN
***********************************************************************
*
FIRST    EQU   *                                                YM3004
         LH    WORKO,MAXCOUNT           PICK UP MAXIMUM NUMBER OF
*                                          COUNT FIELDS PER TRACK+1
         SLL   WORKO,BITS4              MULTIPLY BY 16
         LA    WORKO,CNTSIZE(X0,WORKO)  ADD FOR READING        @X50RSAG
*                                          HOME ADDRESS AND R0 AND A
*                                          FLAG FIELD
***********************************************************************
         MODESET EXTKEY=DATAMGT         KEY 5                   YL02912
***********************************************************************
         GETMAIN R,LV=(WORKO),SP=229                            YM3004
         LTR   ADDREG,ADDREG            CHECK FOR GOTTEN CORE
         BZ    GOTCORE                  YES, CORE GOTTEN
***********************************************************************
*        GET OUT                                                YL02912
***********************************************************************
         LA    FOUR,CC12                SET RETURN CODE TO 12 FOR
*                                          CORE NOT AVAILABLE
BADCASE  MVC   XCTLID(L2),ATLAS06       MOVE ERROR LOAD NAME IN
         MVC   TTR(L3),TTR6             MOVE ERROR LOAD TTR IN
         SPACE
GETOUT   XC    XCTLPTR+D4(L4),XCTLPTR+D4   CLEAR SECOND WORD OF POINTER
         L     PARMB,PNTSNAP            GET SNAP LIST POINTER   YL02912
         LTR   PARMB,PARMB              SNAP MODE               YL02912
         BZ    NOSNAP                   NO - DONT SNAP          YL02912
         SNAP  ID=3,MF=(E,(1))          SNAP                    YL02912
NOSNAP   EQU   *                        *                       YL02912
         XCTL  ,MF=(E,(1)),SF=(E,XCTLPTR)
***********************************************************************
         SPACE
GOTCORE  EQU   *                                                YM3004
***********************************************************************
         MODESET EXTKEY=SUPR            KEY 0                   YM3004
***********************************************************************
         ST    PARMB,SVRBEX             STORE WORK AREA ADDR    YM3004
***********************************************************************
         MODESET EXTKEY=DATAMGT         KEY 5                   YM3004
***********************************************************************
         LR    TEN,PARMB                LOAD ADDR OF WORK AREA  YM3004
         USING CNTAREA,TEN              ESTABLISH ADDRESSABILITY TO
*                                          COUNT FIELD WORK AREA
         ST    WORKO,CNTCORE            SAVE NUMBER BYTES IN COUNT
*                                          FIELD WORK AREA
         ST    TEN,COREADDR             SAVE ADDRESS OF THIS AREA
         LR    FIVE,WORKO
***********************************************************************
         SPACE
* THIS SECTION CLEARS THE COUNT FIELD WORK AREA TO ZEROES
***********************************************************************
         L     PARMB,USERPARM           PICK UP ADDRESS OF USER'S
*                                          PARAMETER LIST
         LR    SEVEN,TEN
         MVI   D0(SEVEN),FFSW           MOVE HEX FF TO BUFFER   YM3064
         BCTR  WORKO,X0                DECRE FOR INIT BYTE     @ZM42073
CLRCORE  LA    SIX,TWO55
         CR    SIX,WORKO                ARE THERE MORE THAN 255 BYTES
         BH    LESS255                  NO, BRANCH TO ZERO REMAINING
*                                          BYTES
         BCTR  SIX,X0                   DECREMENT LENGTH BY 1 FOR
*                                          EXECUTE INSTRUCTION
         EX    SIX,CLEARS               EXECUTE INSTRUCTION TO CLEAR
*                                          255 BYTES OF WORK AREA
         S     WORKO,FULL255            SUBTRACT 255 TO GET REMAINING
*                                          NUMBER BYTES
         LA    SEVEN,D255(X0,SEVEN)     UPDATE POINTER TO AREA
         B     CLRCORE                  RETURN TO CLEAR CORE
CLEARS   MVC   D1(X0,SEVEN),D0(SEVEN)                            YM3064
LESS255  LTR   WORKO,WORKO              DO ANY UNCLEARED BYTES REMAIN
         BZ    GETRK                    NO, GET ADDR OF TRACK IN ERROR
         BCTR  WORKO,X0                 DECREMENT LENGTH BY 1 FOR
*                                          EXECUTE INSTRUCTION
         EX    WORKO,CLEARS             CLEAR REMAINING BYTES
***********************************************************************
*        GET TRACK INFORMATION                                  YL02912
***********************************************************************
         SPACE
GETRK    S     FIVE,FULL8               COMPUTE POINTER TO LAST COUNT
         ALR   FIVE,TEN                    FIELD
         MVI   D0(FIVE),FFSW            SET FLAG FOR END OF COUNTS
         L     FIVE,D4(X0,PARMB)        PICK UP ADDRESS OF USER'S CCHH
         MVC   DEBSTRCC(L4),D0(FIVE)    INSERT CYLINDER AND TRACK
         MVC   DEBENDCC(L4),D0(FIVE)       ADDRESS FOR SINGLE TRACK
*                                          EXTENT
         TM    D4(PARMB),BIT1           WAS ONLY CCHH SPECIFIED
         BZ    SEEKMV                   NO,MOVE IN FULL COUNT
         MVC   SEEK+D3(L4),D0(FIVE)     MOVE CCHH TO SEEK ADDRESS
         B     GENCOM                   GENERATE READ HA AND READ R0
***********************************************************************
SEEKMV   MVC   SEEK+D3(L5),D0(FIVE)     MOVE CCHHR TO SEEK ADDRESS
*                                          COUNT
         CLI   SEEK+D7,ZEROS            IS ERROR RECORD R0
         BNE   GENCOM                   NO, GENERATE READ HA AND
*                                          READ R0
         LTR   FIVE,FIVE                WAS THE UTILITY THE CALLER
         BP    GENCOM                   NO,GENERATE READ HA AND READ R0
         MVI   ERRTYPE,R0ERR            SET SWITCH TO INDICATE ERROR
*                                          RECORD IS R0 AND THE UTILITY
*                                          IS THE CALLER
***********************************************************************
         SPACE
* THIS SECTION RELOCATES THE READ HOME ADDRESS AND READ RECORD ZERO
* COMMANDS IN THE WORK AREA
***********************************************************************
GENCOM   LM    SIX,NINE,CCWRHA          LOAD READ HOME ADDRESS AND
         ALR   SIX,TEN                     READ R0 CCW'S
         ALR   EIGHT,TEN
         STM   SIX,NINE,CCW21           STORE IN WORK AREA
         LA    SIX,CCW21                PUT CHANNEL PROGRAM POINTER
         ST    SIX,IOBSTART                INTO IOB
***********************************************************************
         SPACE
* THIS SECTION GENERATES READ COUNT COMMANDS FOR THE MAXIMUM NUMBER OF
* COUNT FIELDS ON A TRACK
***********************************************************************
GENCOUNT LH    NINE,MAXCOUNT            PICK UP MAXIMUM NUMBER COUNT
*                                          FIELDS
         LR    FIVE,NINE                SET UP COUNTER
         LA    SIX,CCW23                PICK UP ADDRESS OF FIRST READ
*                                          COUNT CCW
         SLL   NINE,BITS3               MULTIPLY NUMBER FIELDS BY 8
         ALR   NINE,SIX                 GET ADDRESS OF FIRST SLOT FOR
*                                          COUNT
         ST    NINE,SAVEADDR            SAVE COUNT FIELD ADDRESS
         LM    SEVEN,EIGHT,CCWRC        LOAD READ COUNT CCW
         ALR   SEVEN,NINE               INSERT DATA ADDRESS
GENPROG  STM   SEVEN,EIGHT,D0(SIX)      STORE CCW IN WORK AREA
         LA    SIX,D8(X0,SIX)           UPDATE ADDRESS FOR CCW
         A     SEVEN,FULL8              UPDATE DATA ADDRESS
         BCT   FIVE,GENPROG             IF COUNTER DOES NOT EQUAL ZERO
*                                          GENERATE ANOTHER READ COUNT
         S     SIX,FULL4
         MVI   D0(SIX),CCOFF            TURN OFF CHAIN COMMAND FLAG
         SPACE
         MVC   XCTLID(L2),ATLAS03       MOVE NEXT LOAD NAME IN
         MVC   TTR(L3),TTR3             MOVE NEXT LOAD TTR IN
         B     GETOUT                   XCTL TO NEXT LOAD
***********************************************************************
         SPACE
GETL     GETMAIN  EC,MF=L               L FORM FOR COUNTS AND TRACK
VTOCNAME DC    CL8'SYSVTOC'             MAJOR QUE ELEMENT NAME
*                                          DESCRIPTOR CHANNEL PROGRAM
***********************************************************************
CCWRHA   CCW   X'1A',HOMEAD-CNTAREA,X'40',5  READ HOME ADDRESS
CCWRR0   CCW   X'16',R0-CNTAREA,X'40',16  READ RECORD ZERO
CCWRC    CCW   X'92',0,X'40',8          READ COUNT
***********************************************************************
FULL255  DC    F'255'                   *
FULL8    DC    F'8'                     *
FULL4    DC    F'4'                     *
MAINT    DC    1CL25'IGG0860A MAINTENANCE AREA'                 YL02912
***********************************************************************
         SPACE
*XCTL BY TTR TABLE
***********************************************************************
         SPACE
         ORG   IGG0860A+1000            FIRST ORG
         SPACE
TTRTABLE EQU   *
         SPACE
ATLAS03  DC    C'0B'                    ID OF NEXT ATLAS LOAD
TTR3     DC    X'00000000'              TTR OF NEXT ATLAS LOAD
ATLAS06  DC    C'AE'                    ID OF ERROR LOAD
TTR6     DC    X'00000000'              TTR OF ERROR LOAD
         DC    X'0000'                  LAST TABLE ENTRY CODE
         ORG   IGG0860A+1020            SECOND ORG
         DC    C'086'                   ATLAS SVC CODE
         DC    X'7D'                    RELATIVE TABLE START ADDRESS
*                                          IN DOUBLE WORDS
***********************************************************************
         SPACE
         EJECT
WORKDSCT DSECT
SVRBEX   DS    1F                       SAVE AREA FOR SVRB EXTENSION
*                                          POINTER
DEVTAB   DS    1F                       DEVICE TYPE INDICATOR FROM UCB
SWITCH   DS    CL1                      LOGIC SWITCH
*                                       0 BIT INDICATES CHANNEL
*                                          PROGRAM NOT RE-EXECUTABLE
*                                       1 BIT INDICATES WRITE SPECIAL
*                                          REQUIRED
*                                       2 BIT INDICATES TRACK OVERFLOW
*                                          DATA SET
*                                       4 BIT INDICATES NO ALTERNATES
*                                          AVAILABLE
*                                       6 BIT INDICATES AN ERROR ON
*                                          OTHER THAN THE ORIGINAL
*                                          ERROR RECORD
*                                       7 BIT INDICATES VTOC HAS BEEN
*                                          ENQUEUED UPON
NUMBERR  DS    CL2                      NUMBER OF ERROR RECORDS
VTOCADR  DS    CL5                      VTOC ADDRESS CCHHR
SPLIT    DS    X'00'                    SPLIT CYLINDER SWITCH
RETPT    DS    1F                       POINTER TO NEXT ROUTINE
         DS    0F
***********************************************************************
         IECSDSL1 (4)
***********************************************************************
TICADDR  DS    1F                       ADDRESS OF TIC CCW IN NEW
*                                          CHANNEL PROGRAM
SAVEADDR DS    1F                       ADDRESS OF FIRST COUNT FIELD
CNTCORE  DS    1F                       NUMBER OF BYTES IN COUNT FIELD
*                                          WORK AREA
COREADDR DS    1F                       ADDRESS OF COUNT FIELD
*                                          WORK AREA
RECADDR  DS    1F                       ADDRESS OF CORE GOTTEN FOR
*                                          LONGEST RECORD SIZE
RECCORE  DS    1F                       NUMBER OF BYTES IN THIS AREA
DDNAME   DS    1D                      DDNAME SAVE AREA         XL03130
DEVINFO  DS    5F                      DEVICE INFO              XL03130
PARMUSER DS    1F                       POINTER USER PARM LIST  YL02912
PNTEST   DS    1F                       TEST MODE               YL02912
PNTSNAP  DS    1F                       SNAP MODE               YL02912
PNTSNS   DS    CL2                      SENSE BYTE - USER       YL02912
         DS    CL1                      FILLER                  YL02912
PNTMIOB  DS    CL1                      M OF IOB                YL02912
PNTIOB   DS    1F                       IOB POINTER             YL02912
PNTCNT   DS    1F                       COUNT POINTER           YL02912
PARMCNT  DS    2F                       COUNTS                  YL02912
PNTIOT   DS    CL2                      TIOT OFFSET             YL02912
         DS    CL1                      FILLER                  YL02912
PNTOFLO  DS    CL1                      TRACK OVERFLO           YL02912
PNTDEB   DS    1F                       DEB POINTER             YL02912
HOLDKEY  DS    1F                       USER KEY                YL02912
REGHOLD  DS    1F                       TEMP REG SAVE           YL02912
CNTADDR  DS    1F                       ADDRESS OF COUNT FIELD FOR
*                                          CCW IN ERROR
ERRTYPE  DS    CL1                      TYPE OF ORIGINAL ERROR
RECNUMB  DS    1F                       R OF ERROR RECORDS
REGSAVE  DS    5F                       SAVE AREA FOR REGISTERS
***********************************************************************
ECB      DS    1F                       ECB
IOB      DS    8F                       IOB
IOBSTART EQU   IOB+16                   ADDRESS OF CHANNEL PROGRAM
SEEK     DS    2F                       SEEK ADDRESS
DEB      DS    12F                      DEB
DEBSTRCC EQU   DEB+38
DEBENDCC EQU   DEB+42
DEBNMTRK EQU   DEB+46
DEBDVMOD EQU   DEB+32
DEBPTR   DS    F                        DCB SLOT
MIELNAME DS    CL6                      VOLUME SERIAL SLOT
DCB      EQU   DEBPTR-44
R0AREA   DS    2D                       AREA FOR R0
DEQAREA  DS    2D                       DEQ TABLE AREA
MJELNAME DS    2F                       MAJOR QUEUE NAME AREA
ENQAREA  DS    2D                       ENQ TABLE AREA
USERPARM DS    1F                       POINTER TO USER'S PARM LIST
NUMALT   DS    1H                       NUMBER OF ALTERNATES TO BE
*                                          TRIED
MAXCOUNT DS    1H                       MAXIMUM NUMBER OF COUNT FIELDS
*                                          PER TRACK+1
XCTLPTR  DS    2F                       PTR TO XCTL WORK AREA TABLE
XCTLAREA DS    0CL32
XCTLNM   DS    CL6                      COMMON BYTES OF ATLAS NAME
XCTLID   DS    CL2                      ID OF ATLAS MODULE
         DS    CL6                      *
TTR      DS    CL3                      TTR OF ATLAS MODULE
         DS    CL5                      *
TTRATTB  DS    CL2                      ATTRIBUTES OF ATLAS - ALL LOADS
TTRCREQ  DS    CL3                      CORE REQUIREMENT OF ATLAS LOAD
TTRLNGTH DS    CL2                      LENGTH OF ATLAS LOAD
         DS    CL3                      *
INCRMNT  DS    1F                       INCREMENT FOR READ COUNTS
PROGADDR DS    1F                       ADDRESS OF NEW CHANNEL PROGRAM
ALTHA    DS    2F                       ALTERNATE TRACK HOME ADDRESS
ALTR0    DS    4F                       ALTERNATE TRACK R0
***********************************************************************
CCW1     DS    1D                       CCW AREA
CCW2     DS    1D                       *
CCW3     DS    1D                       *
CCW44    DS    1D                       *
CCW45    DS    1D                       *
CCW56    DS    1D                       *
CCW57    DS    1D                       *
CCW31    DS    1D                       *
CCW32    DS    1D                       *
CCW33    DS    1D                       *
CCW34    DS    1D                       *
CCW35    DS    1D                       *
CCW36    DS    1D                       *
CCW37    DS    1D                       *
CCW38    DS    1D                       *
CCW39    DS    1D                       *
CHECKHA  DS    2F                       AREA FOR HOME ADDRESS OF
*                                       ALTERNATE
*
***********************************************************************
CVT      DSECT
***********************************************************************
UCB      DSECT
CNTAREA  DSECT
         DS    F                       RESRV FOR DBL WD ALGNMT @ZM40498
MADSD    DS    CL4                     1ST FOUR 3350 SD BYTES  @X50RSPC
***********************************************************************
HOMEAD   DS    2F                       CCW AREA
R0       DS    4F                       *
CCW21    DS    2F                       *
CCW22    DS    2F                       *
CCW23    DS    2F                       *
ENDCA    DS    0F                                              @X50RSAG
CNTSIZE  EQU   ENDCA-CNTAREA           SIZE OF AREA            @X50RSAG
         END
