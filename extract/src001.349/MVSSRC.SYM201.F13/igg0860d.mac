         TITLE 'IGG0860D - FIFTH LOAD MODULE FOR SVC 86'         S20201
         COPY  LCGASMSW
IGG0860D CSECT
*
*
*
*          RELEASE 22 DELETIONS/CHANGES
*          RELEASE 21 DELETIONS/CHANGES
*          RELEASE 20 DELETIONS/CHANGES
*0792001000-002000,238000,359000-360000                          S20201
*
*STATUS CHANGE LEVEL 002
*
*TITLE 'IGG0860D' - FIFTH LOAD MODULE OF SVC 86                       *
*FUNCTION/OPERATION                                                   *
*   IGG0860D ESTABLISHES ITS OWN ADDRESSABILITY AND THAT OF ITS 688-  *
*BYTE WORK AREA.  BEING THE FIFTH LOAD MODULE OF SVC 86 THIS MODULE   *
*HAS FOUR MAJOR FUNCTIONS                                             *
*        1.  READS ALL RECORDS ON DEFECTIVE TRACK EXCEPT HOME         *
*            ADDRESS AND R0                                           *
*        2.  WRITES ALL RECORDS ON CHOSEN ALTERNATE TRACK             *
*        3.  FREES CORE FOR AREA FOR LARGEST RECORD, AREA FOR TRACK   *
*            DESCRIPTION CHANNEL PROGRAM AND COUNT FIELDS             *
*                                                                     *
*ENTRY POINT                                                          *
*   IGG0860D                                                          *
*                                                                     *
*INPUT                                                                *
*   INPUT CONSISTS OF A MAIN WORK AREA POINTED TO BY REGISTER 11,     *
*A WORK AREA FOR COUNT FIELDS AND TRACK DESCRIPTION CHANNEL PROGRAM   *
*POINTED TO BY REGISTER 10, AND A WORK AREA FOR THE LARGEST RECORD ON *
*THE DEFECTIVE TRACK POINTED TO BY REGISTER 9.                        *
*                                                                     *
*OUTPUT                                                               *
*   OUTPUT CONSISTS OF THE MAIN WORK AREA AND IN AN ERROR SITUATION   *
*ONE OF THE FOLLOWING RETURN CODES IN REGISTER FOUR                   *
*        16 - I/O ERROR IN ALTERNATE TRACK ASSIGNMENT                 *
*        20 - ERROR OTHER THAN DATA CHECK OR MISSING ADDRESS MARKER   *
*        40 - RECOVERABLE ERRORS ENCOUNTERED                          *
*        48 - NO ERRORS FOUND ON TRACK                                *
*                                                                     *
*EXTERNAL ROUTINES                                                    *
*   NONE                                                              *
*                                                                     *
*EXITS                                                                *
*   NORMAL - XCTL TO MODULE IGG086AE                                  *
*   ERROR - XCTL TO MODULE IGG086AE                                   *
*   ERROR ON ALTERNATE - XCTL TO MODULE IGG0860C                      *
*                                                                     *
* TABLES/WORK AREAS                                                   *
*   MAIN WORK AREA CONTAINING THE FORMAT 4 DSCB, MAXIMUM NUMBER OF    *
*COUNT FIELDS PER TRACK, A POINTER TO USER'S PARAMTER LIST, ECB, IOB, *
*DEB, AND DCB.                                                        *
*                                                                     *
*ATTRIBUTES                                                           *
*   REENTRANT, REFRESHABLE                                            *
*                                                                     *
*                                                                     *
*
*
***********************************************************************
*                                                                     *
*                               EQUATES                               *
*                                                                     *
***********************************************************************
BIT0     EQU   X'80'
BIT1     EQU   X'40'
BIT4     EQU   X'08'
BIT6     EQU   X'02'
BIT7     EQU   X'01'
***********************************************************************
*          COMPLETION CODES                                           *
***********************************************************************
CC16     EQU   16                       ERROR IN ALTERNATE ASSIGNMENT
CC20     EQU   20                       NOT VALID ERROR CONDITION
CC40     EQU   40                       ALTERNATE ASSIGNED
CC48     EQU   48                       TRACK NOT FOUND DEFECTIVE
***********************************************************************
*          DISPLACEMENT CONSTANTS                                     *
***********************************************************************
D0       EQU   0
D1       EQU   1
D3       EQU   3
D4       EQU   4
D5       EQU   5
D6       EQU   6
D7       EQU   7
D8       EQU   8
***********************************************************************
*          LENGTH CONSTANTS                                           *
***********************************************************************
L1       EQU   1
L2       EQU   2
L3       EQU   3
L4       EQU   4
L5       EQU   5
L8       EQU   8
**********************************************************************
*        DEVICE EQUATES                                              *
**********************************************************************
D3330    EQU   X'09'                    3330 DEVICE TYPE         S20201
         AIF   ('&LIB' EQ 'LIB1').X225600                       XL03145
D3330C   EQU   X'0D'                   3330 C DEVICE            XL03145
D3340    EQU   X'0A'                   3340 DEVICE              XL03130
.X225600 ANOP                                                   XL03145
***********************************************************************
*                                                                     *
***********************************************************************
BITS8    EQU   8
CCSKIP   EQU   X'50'                    COMMAND CHAIN & SKIP BITS
CNTLENG  EQU   X'08'                    COUNT LENGTH
CSWOK    EQU   X'41'
EOF      EQU   X'0D'                    END OF FILE
ERRS3    EQU   X'03'
FFSW     EQU   X'FF'
IOEROR   EQU   X'20'
OTHEROR  EQU   X'02'
OUTEXTNT EQU   X'42'                    OUT OF EXTENT
RCKD     EQU   X'1E'                    READ COUNT KEY DATA
RCNT     EQU   X'92'                    READ COUNT
RKD      EQU   X'0E'                    READ KEY DATA
SLISKP   EQU   X'30'                    SLI & SKIP BITS
TIC      EQU   X'08'
WSCKD    EQU   X'01'                    WRITE SPECIAL COUNT KEY DATA
X0       EQU   0
ZEROS    EQU   X'00'
*
*
***********************************************************************
         EJECT
* REGISTER ASSIGNMENTS
***********************************************************************
PARMA    EQU   0                        PARAMETER REGISTER
PARMB    EQU   1                        PARAMETER REGISTER
WORKE    EQU   2                        EVEN WORK REGISTER
WORKO    EQU   3                        ODD WORK REGISTER
FOUR     EQU   4                        RETURN CODE REGISTER
FIVE     EQU   5                        WORK REGISTER
SIX      EQU   6                        *
SEVEN    EQU   7                        *
EIGHT    EQU   8                        *
NINE     EQU   9                        *
TEN      EQU   10                       *
AREAREG  EQU   11                       MAIN WORK AREA REGISTER
BASEREG  EQU   12                       BASE REGISTER
SAVEREG  EQU   13                       WORK REGISTER
RETREG   EQU   14                       WORK REGISTER
ADDREG   EQU   15                       COMPLETION CODE REGISTER
***********************************************************************
         BALR  BASEREG,X0               ESTABLISH
         USING BEGIN,BASEREG               OWN ADDRESSABILITY
         USING WORKDSCT,AREAREG         ESTABLISH ADDRESSABILITY FOR
*                                          WORK AREA
         USING CNTAREA,TEN              ESTABLISH ADDRESSABILITY FOR
*                                          COUNT FIELD WORK AREA
***********************************************************************
*        CHECK FOR ANY RECOVERABLE COUNT FIELD ERRORS
***********************************************************************
BEGIN    CLI   NUMBERR+D1,ZEROS         DID ANY RECOVERABLE COUNT FIELD
*                                          ERRORS OCCUR
         BE    LD23                     NO,NO NEED TO RESET COUNTER
         XC    RECNUMB(L4),RECNUMB      CLEAR ERROR RECORD SAVE AREA
         XC    NUMBERR(L2),NUMBERR      RESET ERROR RECORD COUNTER TO
*                                          ZERO
         OI    NUMBERR,BIT0             SET HIGH ORDER BIT TO INDICATE
*                                          RECOVERABLE COUNT ERRORS
LD23     LA    WORKO,CCW23              PICK UP ADDRESS OF FIRST READ
*                                         COUNT CCW
         L     WORKE,SAVEADDR           PICK UP ADDRESS OF FIRST COUNT
*                                          FIELD
         L     NINE,RECADDR             LOAD RECORD BUFFER ADDRESS
***********************************************************************
*        GET LENGTHS
***********************************************************************
PICKLENT SR    SIX,SIX
         ST    WORKO,CCWPTR             SAVE POINTER TO LAST MODIFIED
         IC    SIX,D5(WORKE)            PICK UP KEY LENGTH
*                                          CCW
         AH    SIX,D6(X0,WORKE)         ADD DATA LENGTH
*                                         KEY AND DATA
*                                          COMMAND
         LTR   SIX,SIX                  CHECK FOR END OF FILE
         BZ    WRITEREC                 YES, END OF FILE- SKIP READ OF
***********************************************************************
*        SET UP CCWS
***********************************************************************
RELOCATE LM    FOUR,FIVE,ASCCW51        PICK UP READ COUNT KEY AND DATA
         ALR   FOUR,NINE                PUT IN BUFFER ADDRESS
         ALR   FIVE,SIX                 PUT IN RECORD LENGTH
         CLI   D0(WORKO),TIC            IS CCW A TIC
         BNE   STORE                    NO, STORE CCW
         L     SEVEN,D0(WORKO)          PICK UP ADDRESS OF SPACE COUNT
         LA    SEVEN,D8(SEVEN)          GET POINTER TO FOLLOWING TIC
         MVC   D8(L8,SEVEN),D0(SEVEN)   REPLACE RETURN TIC WITH
*                                          TIC TO SPACE COUNT
         S     FIVE,FULL8               SUBTRACT 8 FROM TOTAL LENGTH
*                                         FOR COUNT FIELD
         LA    FOUR,D8(X0,FOUR)         ADJUST ADDRESS FOR READ KD
         STM   FOUR,FIVE,D0(SEVEN)      STORE NEW READ KEY DATA CCW
         MVI   D0(SEVEN),RKD            SET TO READ KEY DATA ONLY
         ST    SEVEN,CCWPTR             SAVE POINTER TO LAST MODIFIED
*                                          CCW
         B     PICKUP                   GET ADDRESS OF FIRST CCW
STORE    STM   FOUR,FIVE,D0(WORKO)      STORE READ COUNT KEY DATA CCW
         EJECT
PICKUP   LA    FIVE,CCW21               PICK UP ADDRESS OF FIRST CCW
*                                          FOR IOB START ADDRESS
         MVC   SEEK+D3(L4),HOMEAD+D1    MOVE IN SEEK ADDRESS FOR BAD
*                                          TRACK
         BAL   SEVEN,SETUP              EXECUTE CHANNEL PROGRAM
         CLI   D0(WORKO),TIC            WAS READ COUNT REPLACED
*                                          BY A TIC
         BNE   IOERR                    NO,TEST FOR I/O ERROR
         L     SEVEN,CCWPTR             POINTER TO READ KD CCW
         MVC   D0(L8,SEVEN),D8(SEVEN)   REPLACE WITH TIC CCW
IOERR    TM    ECB,IOEROR               CHECK FOR I/O ERROR
         BO    WRITEREC                 NO, I/O SUCCESSFUL
         CLI   ECB,CSWOK                ARE CSW CONTENTS USEFUL
         BE    ERR41                    YES,CSW CONTENTS ARE VALID
         CLI   ECB,OUTEXTNT             TEST FOR INDICATION THAT THE
***********************************************************************
* ERROR IS A RESULT OF READING AN OVERFLOW RECORD AND HAVING ONLY A
* SINGLE TRACK DEB EXTENT.  IF SO, THEN THE CSW CONTENTS CAN BE
* CONSIDERED RELIABLE
***********************************************************************
         BNE   SETERCD                  ERROR CAN NOT BE HANDLED
ERR41    EQU   *
         LA    SEVEN,BITS8
         L     FIVE,IOBFLAG3            PICK UP ADDRESS OF CCW IN
*                                          ERROR +8 FROM CSW
         SR    FIVE,SEVEN
         CLI   IOBCSWST,EOF             END OF FILE
         BE    EOFSTOP                  YES, END OF FILE
         CLI   NUMBERR+D1,ERRS3         HAVE THERE BEEN THREE ERROR
*                                          RECORDS
         BE    VALIDCHK                 CONTINUE PROCESSING BUT DO NOT
*                                          SAVE ERROR RECORD NUMBERS
         XR    EIGHT,EIGHT              CLEAR REGISTER
         IC    EIGHT,NUMBERR+D1         PICK UP NUMBER OF ERROR RECORDS
         A     EIGHT,FULL1              INCREMENT BY ONE
         STC   EIGHT,NUMBERR+D1
         LA    SEVEN,RECNUMB            ADDRESS OF ERROR SAVE AREA
         ALR   EIGHT,SEVEN              GET DISPLACEMENT INTO RECNUMB
         MVC   D0(L1,EIGHT),D4(WORKE)   MOVE ERROR RECORD NUMBER TO
*                                          RECNUMB SAVE AREA
***********************************************************************
*        CHECK FOR VALID ERROR
***********************************************************************
VALIDCHK TM    IOBCSWST,BIT6            DID UNIT CHECK OCCUR
         BNO   SETERCD                  NO, UNIT CHECK DID NOT OCCUR
         TM    IOBSENS0,BIT4            CHECK IOB SENSE BYTE FOR A
*                                          DATA CHECK
         BO    DATACK                   YES, DATA CHECK
         TM    IOBSENS1,BIT6            CHECK FOR MISSING ADDRESS
*                                          MARKER
         BO    CHECKM                   YES, MISSING ADDRESS MARKER
         TM    IOBSENS1,BIT7            CHECK FOR OVERFLOW INCOMPLETE
         BO    WRITEREC                 YES, TAKE CARE OF SITUATION
*                                          IN ANOTHER SECTION (LASTREC)
***********************************************************************
*        GET OUT
***********************************************************************
SETERCD  LA    FOUR,CC20                SET COMPLETION CODE
ERNAME   MVC   XCTLID(L2),ATLAS06       MOVE ERROR LOAD NAME IN
         MVC   TTR(L3),TTR6             MOVE ERROR LOAD TTR IN
XCTL     XC    XCTLPTR+D4(L4),XCTLPTR+D4  CLEAR SECOND WORD OF POINTER
         L     PARMB,PNTSNAP            GET SNAP LIST POINTER   YL02912
         LTR   PARMB,PARMB              SNAP MODE               YL02912
         BZ    NOSNAP                   NO - DONT SNAP          YL02912
         SNAP  ID=6,MF=(E,(1))          SNAP                    YL02912
NOSNAP   EQU   *                        *                       YL02912
         XCTL  ,MF=(E,(1)),SF=(E,XCTLPTR)
***********************************************************************
CHECKM   CLI   DEVTAB+D3,D3330          CHECK FOR 3330           S20201
         BNL   SETERCD                  BRCH IF 3330,3340,3350 @ZM40498
         TM    IOBSENS1,BIT4            CHECK FOR NOT REGULAR    S20201
*                                       MISSING                  S20201
*                                          ADDRESS MARKER
         BO    SETERCD                  YES, NOT NORMAL MISSING
*                                          ADDRESS MARKER
         TM    IOBSENS0,BIT7            CHECK FOR BALLAST CELL NOT
*                                          FOUND
         BO    SETERCD                  YES, NOT A NORMAL MISSING
*                                          ADDRESS MARKER
***********************************************************************
*        CHECK FOR READ COUNT KEY DATA OR READ KEY
***********************************************************************
DATACK   CLI   D0(FIVE),RCKD            CHECK FOR CCW BEING A READ
*                                          COUNT KEY DATA
         BNE   USERREC                  NO,CCW IS READ KEY DATA
         MVI   D0(FIVE),RCNT            CHANGE READ COUNT KEYDATA TO
*                                       READ COUNT
         MVI   D7(FIVE),CNTLENG         SET LENGTH TO 8
         MVI   D6(FIVE),ZEROS
USERREC  L     FOUR,USERPARM            LOAD PARAMETER LIST ADDRESS
         TM    D4(FOUR),BIT1            WAS ONLY CCHH PASSED BY USER
         BO    WRITEREC                 YES,USER HAS NOT PASSED
*                                          FULL COUNT
         L     FOUR,D4(FOUR)            LOAD ADDRESS OF COUNT FIELD
         CLC   D0(L5,WORKE),D0(FOUR)    IS THIS THE ERROR RECORD
*                                          PASSED BY CALLER
         BE    WRITEREC                 YES,CALLER'S RECORD
         OI    SWITCH,OTHEROR           SET SWITCH TO INDICATE ERROR
*                                          ENCOUNTERED IN OTHER THAN
*                                          RECORD PASSED
***********************************************************************
*        WRITE RECORD
***********************************************************************
WRITEREC MVC   D0(L8,NINE),D0(WORKE)    MOVE IN COUNT FIELD TO BE
*                                          WRITTEN
         LM    FOUR,FIVE,CPCCW1         PICK UP FIRST CCW
         ALR   FOUR,AREAREG             RE-ADJUST POINTER
         STM   FOUR,FIVE,CCW1           STORE IN FIRST AND FOURTH SLOTS
         STM   FOUR,FIVE,CCW44
         LM    FOUR,FIVE,CPCCW2         PICK UP SECOND CCW
         LA    PARMB,CCW1
         ALR   FOUR,PARMB               RE-ADJUST POINTER
         STM   FOUR,FIVE,CCW2
         LM    FOUR,FIVE,CPCCW3         PICK UP THIRD CCW
         ALR   FOUR,NINE                RE-ADJUST POINTER
         ALR   FIVE,SIX                 ADJUST LENGTH
         STM   FOUR,FIVE,CCW3
         LM    FOUR,FIVE,CPCCW2         PICK UP FIFTH CCW
         LA    PARMB,CCW44
         ALR   FOUR,PARMB               RE-ADJUST POINTER
         STM   FOUR,FIVE,CCW45
         LM    FOUR,FIVE,CPCCW4         PICK UP SIXTH CCW
         ALR   FOUR,NINE
         ALR   FIVE,SIX                 RE-ADJUST LENGTH
         STM   FOUR,FIVE,CCW56
         IC    FOUR,D4(NINE)            PICK UP RECORD NUMBER
         BCTR  FOUR,X0                  REDUCE RECORD NUMBER BY ONE
         STC   FOUR,SEARCH+D4           STORE RECORD NUMBER TO BE USED
*                                          FOR SEARCH
         MVC   SEARCH(L4),D0(NINE)      MOVE IN CCHH FOR SEARCH
NOTSET   LA    FIVE,CCW1                PICK UP ADDRESS OF FIRST CCW
*                                          FOR IOB START ADDRESS
         MVC   SEEK+D3(L4),DS4HCCHH     MOVE IN CCHH OF ALTERNATE
         BAL   SEVEN,SETUP              SET UP CONTROL BLOCKS
         TM    ECB,IOEROR               CHECK FOR IO ERROR
         BO    GREAT                    YES, NO I/O ERROR
         CLI   IOBCSWST,EOF             IS CONDITION END OF FILE
         BE    GREAT                    YES, END OF FILE (OK)
***********************************************************************
*        EXIT
***********************************************************************
SETBACK  LA    FOUR,CC16                SET CODE IN FOUR AND XCTL TO
*                                          IGG0860C
         MVC   XCTLID(L2),ATLAS04       MOVE PREVIOUS LOAD NAME IN
         MVC   TTR(L3),TTR4             MOVE PREVIOUS LOAD TTR IN
         B     XCTL                     EXIT TO APPROPRIATE MODULE
***********************************************************************
GREAT    CLI   D8(WORKE),FFSW           CHECK FOR LAST COUNT FIELD
         BE    LASTREC                  YES,LAST RECORD
         L     SIX,CCWPTR               PICK UP POINTER TO LAST CCW
*                                          MODIFIED
         OI    D4(SIX),CCSKIP           TURN ON COMMAND CHAIN AND SKIP
*                                          FLAGS IN RKD COMMAND
         LA    WORKE,D8(X0,WORKE)       BUMP COUNT FIELD POINTER
         LA    WORKO,D8(X0,WORKO)       BUMP CCW POINTER
         B     PICKLENT                 PICK UP NEXT RECORD
***********************************************************************
* THIS SECTION RE-READS THE LAST RECORD ON THE 'BAD' TRACK TO SEE IF A
* WRITE SPECIAL COUNT KEYDATA IS NEEDED, UNLESS IT IS AN EOF RECORD.
***********************************************************************
LASTREC  EQU   *
         OC    D6(L2,WORKE),D6(WORKE)   DATA LENGTH = ZERO
         BZ    ENDUP                    YES, EXIT
         L     FOUR,CCWPTR              GET POINTER TO LAST READ CCW
         CLI   D0(FOUR),RCNT            IS LAST CCW A RC
         BNE   NOTRC                    NO, GO ON
         MVI   D0(FOUR),RCKD            REPLACE RC WITH RCKD CCW
         XR    EIGHT,EIGHT              CLEAR REGISTER
         IC    EIGHT,D5(WORKE)          LOAD KEY LENGTH
         AH    EIGHT,D6(WORKE)          ADD DATA LENGTH
         ST    EIGHT,D4(FOUR)           STORE KEY DATA LENGTH IN CCW
NOTRC    EQU   *
         CLI   D0(FOUR),TIC             IS LAST CCW A TIC
         BNE   NOTIC                    NO, WAS A READ CKD
         XR    EIGHT,EIGHT              CLEAR REGISTER
         IC    EIGHT,D5(WORKE)          LOAD KEY LENGTH
         AH    EIGHT,D6(WORKE)          ADD DATA LENGTH
         ST    EIGHT,D4(FOUR)           STORE KEY DATA LENGTH IN CCW
         MVI   D0(FOUR),RKD             REPLACE TIC WITH READ KD
NOTIC    EQU   *
         TM    SWITCH,BIT1              IS WRITE SPECIAL REQUIRED
         BO    WRITSPEC                 YES, SET UP CHANNEL PROGRAM
         L     EIGHT,D4(FOUR)           LOAD LENGTH PROTION OF CCW
         LA    EIGHT,D1(EIGHT)          ADD ONE TO FORCE OVERFLOW
*                                          INCOMPLETE
         ST    EIGHT,D4(FOUR)           STORE LENGTH
         OI    D4(FOUR),SLISKP          TURN ON SKIP AND SLI BITS
         LA    FIVE,CCW21               PICK UP ADDRESS OF FIRST CCW
*                                          FOR IOB START ADDRESS
         MVC   SEEK+D3(L4),HOMEAD+D1    MOVE IN SEEK ADDRESS FOR BAD
*                                          TRACK
         BAL   SEVEN,SETUP              EXECUTE CHANNEL PROGRAM
         TM    IOBSENS1,BIT7            CHECK FOR OVERFLOW INCOMPLETE
         BNO   ENDUP                    NO, BRANCH TO EXIT ROUTINE
***********************************************************************
* THIS SECTION IS ENTERED IF A WRITE SPECIAL IS REQUIRED
***********************************************************************
WRITSPEC MVI   CCW3,WSCKD               CHANGE WRITE COUNT KEY DATA CCW
*                                          TO A WRITE SPECIAL CCW
         LA    FIVE,CCW1                LOAD ADDRESS OF FIRST CCW FOR
*                                          IOB START ADDRESS
         MVC   SEEK+D3(L4),DS4HCCHH     MOVE IN SEEK ADDRESS OF
*                                          ALTERNATE
         BAL   SEVEN,SETUP              EXECUTE CHANNEL PROGRAM
         TM    ECB,IOEROR               CHECK FOR I/O ERROR
         BO    ENDUP                    NO I/O ERROR
         TM    IOBSENS1,BIT7            CHECK FOR OVERFLOW       S20201
*                                       INCOMPLETE               S20201
         BNO   SETBACK                  NOT OVERFLOW INCOMPLETE  S20201
***********************************************************************
*        FREE CORE
***********************************************************************
ENDUP    L     WORKE,RECCORE            NUMBER OF BYTES TO BE   YL02912
*                                          FREED
         L     PARMB,RECADDR            LOAD POINTER TO CORE TO BE
*                                          FREED
         FREEMAIN R,LV=(2),A=(1),SP=229                         YL02912
         XC    RECADDR(L8),RECADDR
         SR    FOUR,FOUR                SET REGISTER FOUR TO INDICATE
*                                         NORMAL ENTRY INTO LAST LOAD
         L     SIX,USERPARM             ADDRESS OF PARAMETER LIST
         TM    NUMBERR,BIT0             WERE THERE ANY RECOVERABLE
*                                          COUNT FIELD ERRORS
         BO    ISO                      YES,AN ALTERNATE MUST BE
*                                          ASSIGNED
         CLI   NUMBERR+D1,ZEROS         WERE THERE ANY ERRORS IN KEY/
*                                          DATA FIELDS
         BNE   ANYR                     YES,WAS A FULL COUNT SPECIFIED
         LA    FOUR,CC48                SET RETURN CODE FOR NO ERRORS
         B     NEXT                        FOUND ON TRACK
ISO      CLI   NUMBERR+D1,ZEROS         WERE THERE ANY ERRORS IN KEY/
*                                          DATA FIELDS
         BE    NEXT                     NO,RETURN CODE IS SET FOR
*                                          SUCCESSFUL COMPLETION
ANYR     TM    D4(SIX),BIT1             WAS ONLY CCHH PASSED
         BO    OTHERR                   YES,RETURN CODE IS 40
         TM    SWITCH,OTHEROR           WAS THERE AN ERROR ON OTHER
*                                          THAN THE USER SPECIFIED
*                                          RECORD
         BZ    NEXT                     NO,RETURN CODE IS ZERO
OTHERR   LA    FOUR,CC40                SET RETURN CODE FOR ERROR/
*                                          ERRORS ENCOUNTERED,AN
*                                          ALTERNATE IS ASSIGNED
NEXT     MVC   XCTLID(L2),ATLAS06       MOVE NEXT LOAD NAME IN
         MVC   TTR(L3),TTR6             MOVE NEXT LOAD TTR IN
         B     XCTL                     EXIT
***********************************************************************
*        WRITE/READ RECORD
***********************************************************************
SETUP    ST    FIVE,IOBSTART            STORE ADDRESS OF FIRST CCW
         MVC   DEBSTRCC(L4),SEEK+D3     SET UP DEB EXTENTS
         MVC   DEBENDCC(L4),SEEK+D3
         XC    ECB(L4),ECB              CLEAR ECB
         XC    IOBFLAG3(L8),IOBFLAG3    CLEAR SENSE BYTES
         EXCP  IOB                      WRITE/READ RECORD
         WAIT  1,ECB=ECB                WAIT FOR I/O COMPLETE
         BR    SEVEN                    RETURN TO CALLER
EOFSTOP  EQU   *
         CLI   D0(FIVE),RCKD            CHECK FOR CCW BEING READ CKD
         BNE   ISRKD                    NO,CCW IS A RKD
         MVI   D0(FIVE),RCNT            CHANGE RCKD TO RC
         MVI   D7(FIVE),CNTLENG         SET LENGTH TO 8
         NI    D6(FIVE),ZEROS
ISRKD    B     WRITEREC                 EVERYTHING OK
***********************************************************************
CPCCW1   CCW   X'31',SEARCH-SVRBEX,X'40',5  SEARCH ID EQUAL
CPCCW2   CCW   X'08',0,X'40',0          TIC
CPCCW3   CCW   X'1D',0,X'40',8          WRITE COUNT, KEY,DATA
CPCCW4   CCW   X'1E',0,X'10',8          READ COUNT,KEY,DATA
ASCCW51  CCW   X'1E',0,X'00',8          READ COUNT KEY DATA
SPACECT  CCW   X'0F',0,X'40',3          SPACE COUNT
CCWNOP   CCW   X'03',CCW31-SVRBEX,X'60',1  NO-OP
***********************************************************************
***********************************************************************
         EJECT
         SPACE
FULL8    DC    F'8'                     *
FULL1    DC    F'1'                     *
MAINT    DC    2CL25'IGG0860D MAINTENANCE AREA'                 YL02912
*XCTL BY TTR TABLE
         SPACE
         ORG   IGG0860D+1000            FIRST ORG
         SPACE
TTRTABLE EQU   *
ATLAS04  DC    C'0C'                    ID OF PREVIOUS LOAD
TTR4     DC    X'00000000'              TTR OF PREVIOUS LOAD
ATLAS06  DC    C'AE'                    ID OF ERROR/NEXT LOAD
TTR6     DC    X'00000000'              TTR OF ERROR/NEXT LOAD
         DC    X'0000'                  LAST TABLE ENTRY CODE
         ORG   IGG0860D+1020            SECOND ORG
         DC    C'086'                   ATLAS SVC CODE
         DC    X'7D'                    RELATIVE TABLE START ADDRESS
*                                          IN DOUBLE WORDS
***********************************************************************
WORKDSCT DSECT
SVRBEX   DS    1F                       SAVE AREA FOR SVRB EXTENSION
*                                          POINTER
DEVTAB   DS    1F                       DEVICE TYPE INDICATOR FROM UCB
SWITCH   DS    CL1                      LOGIC SWITCH
*                                       0 BIT INDICATES CHANNEL
*                                          PROGRAM NOT RE-EXECUTABLE
*                                       1 BIT INDICATES WRITE SPECIAL
*                                          REQUIRED
*                                       2 BIT INDICATES TRACK OVERFLOW
*                                          DATA SET
*                                       4 BIT INDICATES NO ALTERNATES
*                                          AVAILABLE
*                                       6 BIT INDICATES AN ERROR ON
*                                          OTHER THAN THE ORIGINAL
*                                          ERROR RECORD
*                                       7 BIT INDICATES VTOC HAS BEEN
*                                          ENQUEUED UPON
NUMBERR  DS    CL2                      NUMBER OF ERROR RECORDS
VTOCADR  DS    CL5                      VTOC ADDRESS CCHHR
SPLIT    DS    X'00'                    SPLIT CYLINDER SWITCH
RETPT    DS    1F                       POINTER TO NEXT ROUTINE
***********************************************************************
         DS    0F
         IECSDSL1 (4)
***********************************************************************
TICADDR  DS    1F                       ADDRESS OF TIC CCW IN NEW
*                                          CHANNEL PROGRAM
SAVEADDR DS    1F                       ADDRESS OF FIRST COUNT FIELD
CNTCORE  DS    1F                       NUMBER OF BYTES IN COUNT FIELD
*                                          WORK AREA
COREADDR DS    1F                       ADDRESS OF COUNT FIELD
*                                          WORK AREA
RECADDR  DS    1F                       ADDRESS OF CORE GOTTEN FOR
*                                          LONGEST RECORD SIZE
RECCORE  DS    1F                       NUMBER OF BYTES IN THIS AREA
DDNAME   DS    1D                      DDNAME SAVE AREA         XL03130
DEVINFO  DS    5F                      DEVICE INFO              XL03130
PARMUSER DS    1F                       POINTER USER PARM LIST  YL02912
PNTEST   DS    1F                       TEST MODE               YL02912
PNTSNAP  DS    1F                       SNAP MODE               YL02912
PNTSNS   DS    CL2                      SENSE BYTE - USER       YL02912
         DS    CL1                      FILLER                  YL02912
PNTMIOB  DS    CL1                      M OF IOB                YL02912
PNTIOB   DS    1F                       IOB POINTER             YL02912
PNTCNT   DS    1F                       COUNT POINTER           YL02912
PARMCNT  DS    2F                       COUNTS                  YL02912
PNTIOT   DS    CL2                      TIOT OFFSET             YL02912
         DS    CL1                      FILLER                  YL02912
PNTOFLO  DS    CL1                      TRACK OVERFLO           YL02912
PNTDEB   DS    1F                       DEB POINTER             YL02912
HOLDKEY  DS    1F                       USER KEY                YL02912
REGHOLD  DS    1F                       TEMP REG SAVE           YL02912
CNTADDR  DS    1F                       ADDRESS OF COUNT FIELD FOR
*                                          CCW IN ERROR
ERRTYPE  DS    CL1                      TYPE OF ORIGINAL ERROR
RECNUMB  DS    1F                       R OF ERROR RECORDS
REGSAVE  DS    5F                       SAVE AREA FOR REGISTERS
***********************************************************************
ECB      DS    1F                       ECB
IOB      DS    8F                       IOB
IOBSENS0 EQU   IOB+2
IOBSENS1 EQU   IOB+3
IOBFLAG3 EQU   IOB+8
IOBCSWST EQU   IOB+12
IOBSTART EQU   IOB+16
SEEK     DS    2F                       SEEK ADDRESS
DEB      DS    12F                      DEB
DEBSTRCC EQU   DEB+38
DEBENDCC EQU   DEB+42
DEBPTR   DS    F                        DCB SLOT
MIELNAME DS    CL6                      VOLUME SERIAL SLOT
DCB      EQU   DEBPTR-44
R0AREA   DS    2D                       AREA FOR R0
DEQAREA  DS    2D                       DEQ TABLE AREA
MJELNAME DS    2F                       MAJOR QUEUE NAME AREA
ENQAREA  DS    2D                       ENQ TABLE AREA
USERPARM DS    1F                       POINTER TO USER'S PARM LIST
NUMALT   DS    1H                       NUMBER OF ALTERNATES TO BE
*                                          TRIED
MAXCOUNT DS    1H                       MAXIMUM NUMBER OF COUNT FIELDS
*                                          PER TRACK+1
XCTLPTR  DS    2F                       PTR TO XCTL WORK AREA TABLE
XCTLAREA DS    0CL32
XCTLNM   DS    CL6                      COMMON BYTES OF ATLAS NAME
XCTLID   DS    CL2                      ID OF ATLAS MODULE
         DS    CL6                      *
TTR      DS    CL3                      TTR OF ATLAS MODULE
         DS    CL5                      *
TTRATTB  DS    CL2                      ATTRIBUTES OF ATLAS - ALL LOADS
TTRCREQ  DS    CL3                      CORE REQUIREMENT OF ATLAS LOAD
TTRLNGTH DS    CL2                      LENGTH OF ATLAS LOAD
         DS    CL3                      *
INCRMNT  DS    1F                       INCREMENT FOR READ COUNTS
PROGADDR DS    1F                       ADDRESS OF NEW CHANNEL PROGRAM
ALTHA    DS    2F                       ALTERNATE TRACK HOME ADDRESS
ALTR0    DS    4F                       ALTERNATE TRACK R0
***********************************************************************
CCW1     DS    1D                       CCW AREA
CCW2     DS    1D                       *
CCW3     DS    1D                       *
CCW44    DS    1D                       *
CCW45    DS    1D                       *
CCW56    DS    1D                       *
CCW57    DS    1D                       *
CCW31    DS    1D                       *
CCW32    DS    1D                       *
CCW33    DS    1D                       *
CCW34    DS    1D                       *
CCW35    DS    1D                       *
CCW36    DS    1D                       *
CCW37    DS    1D                       *
CCW38    DS    1D                       *
CCW39    DS    1D                       *
CHECKHA  DS    2F                       AREA FOR HOME ADDRESS OF
*                                       ALTERNATE
SEARCH   DS    1D                       SEARCH RECORD ID
CCWPTR   DS    1F                       CCW POINTER
*
***********************************************************************
CNTAREA  DSECT
         DS    F                        FOR DBL WD ALGNMT      @ZM40498
MADSD    DS    CL4                      1ST 4 3350 SD BYTES    @ZM40498
HOMEAD   DS    2F                       CCW AREA
R0       DS    4F                       *
CCW21    DS    2F                       *
CCW22    DS    2F                       *
CCW23    DS    2F                       *
         END
