         TITLE 'IGG0860C - FOURTH LOAD MODULE FOR SVC 86'        S20201
         COPY  LCGASMSW
IGG0860C CSECT
***********************************************************************
*                 CHANGES THIS MODULE
*                 LATEST FIRST.
***********************************************************************
*
* CHANGE ONLY         NONRELEVANT BITS ARE   @ZA32949=@XA22017=@YA20974
*                     TESTED IN FLAG BYTE.   @ZA32949=@XA22017=@YA20974
*
*                                                               YM3004
*
*      VS1 RELEASE  5 DELETIONS/CHANGES                        @X50RSAG
*A 214500,482100,516100,517000,602000,966000                   @X50RSAG
*D 604000-660000                                               @X50RSAG
*
* FIX BAD HA/R0 MSG.                                           @ZM40498
*          RELEASE 21 DELETIONS/CHANGES
*          RELEASE 20 DELETIONS/CHANGES
*0791002000-004000                                               S20201
*
*STATUS CHANGE LEVEL 002
*
***********************************************************************
        EJECT
*TITLE 'IGG0860C' - FOURTH LOAD MODULE OF SVC 86                      *
*FUNCTION/OPERATION                                                   *
*   IGG0860C - ESTABLISHES ITS OWN ADDRESSABILITY, THAT OF THE 688-   *
*BYTE WORK AREA, AND THAT OF THE WORK AREA FOR COUNT FIELDS.  BEING   *
*THE FOURTH LOAD MODULE OF SVC 86, THIS MODULE HAS THREE MAJOR        *
*FUNCTIONS -                                                          *
*                                                                     *
*        1.  CHECKS FOR A COUNT FIELD ERROR ON THE LAST RECORD        *
*            ON A TRACK                                               *
*        2.  COMPUTES SIZE OF LARGEST RECORD ON THE BAD TRACK         *
*            AND ISSUES A CONDITIONAL GETMAIN FOR THE LARGEST         *
*            RECORD                                                   *
*        3.  WRITES HA AND R0 ON THE CHOSEN ALTERNATE                 *
*                                                                     *
*ENTRY POINTS                                                         *
*   IGG0860C - CHECKS FOR ENTRANCE DUE TO NORMAL PROCESSING OR RETURN *
*FROM IGG0860D.                                                       *
*                                                                     *
*INPUT                                                                *
*   INCLUDED IN THE INPUT ARE A 688-BYTE MAIN WORK AREA AND A WORK    *
*AREA FOR CHANNEL PROGRAMS AND COUNT FIELDS FOR THE BAD TRACK         *
*                                                                     *
*OUTPUT                                                               *
*   THE OUTPUT CONSISTS OF THE 688-BYTE MAIN WORK AREA, A WORK AREA   *
*FOR CHANNEL PROGRAMS AND COUNT FIELDS FOR THE BAD TRACK AND A WORK   *
*AREA FOR THE LARGEST RECORD ON THE BAD TRACK.  IN AN ERROR SITUATION *
*ONE OF THE FOLLOWING RETURN CODES WILL BE SET                        *
*        12 - CORE NOT AVAILABLE                                      *
*        16 - I/O ERROR ON ALTERNATE AFTER N TRIES                    *
*        32 - ERROR IN COUNT FIELD OF LAST RECORD ON THE TRACK        *
*        36 - SEARCH HA ERROR ON ALTERNATE                            *
*                                                                     *
*EXTERNAL ROUTINES                                                    *
*   NONE                                                              *
*                                                                     *
*EXITS                                                                *
*   NORMAL - XCTL TO MODULE IGG0860D                                  *
*   ERROR - XCTL TO MODULE IGG086AE - RETURN CODE IN REGISTER FOUR    *
*                                                                     *
*TABLES/WORK AREAS                                                    *
*   A 688-BYTE WORK AREA CONTAINING THE FORMAT 4 DSCB, MAXIMUM        *
*NUMBER OF COUNT FIELDS PER TRACK, A POINTER TO USER'S PARAMETER LIST,*
*ECB, IOB, DEB, AND DCB.                                              *
*                                                                     *
*ATTRIBUTES                                                           *
*   REENTRANT, REFRESHABLE                                            *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
*                               EQUATES                               *
*                                                                     *
***********************************************************************
BIT1     EQU   X'40'
BIT2     EQU   X'20'
BIT3     EQU   X'10'
BIT4     EQU   X'08'
***********************************************************************
*          COMPLETION CODES                                           *
***********************************************************************
CC12     EQU   12                       CORE NOT AVAILABLE
CC16     EQU   16                       ERROR IN ALTERNATE ASSIGNMENT
CC32     EQU   32                       LAST RECORD COUNT ERROR
CC36     EQU   36                       HA OR R0 ERRORS
***********************************************************************
*          DISPLACEMENT CONSTANTS                                     *
***********************************************************************
D0       EQU   0
D1       EQU   1
D2       EQU   2
D3       EQU   3
D4       EQU   4
D5       EQU   5
D6       EQU   6
D8       EQU   8
D9       EQU   9
***********************************************************************
*          LENGTH CONSTANTS                                           *
***********************************************************************
L1       EQU   1
L2       EQU   2
L3       EQU   3
L4       EQU   4
L7       EQU   7
L8       EQU   8
L16      EQU   16
***********************************************************************
*                                                                     *
***********************************************************************
BADALT   EQU   X'03'                    TRACK STATUS FLAG BYTE
BITS8    EQU   8
CCB      EQU   X'40'                    COMMAND CHAINING BIT
D2321    EQU   X'05'                        *
         AIF   ('&LIB' EQ 'LIB1').X225600                       XL03130
D3340    EQU   X'0A'                   3340 DEVICE              XL03130
D3350    EQU   X'0B'                   3350 DEVICE             @X50RSAG
.X225600 ANOP                                                   XL03130
FFSW     EQU   X'FF'
GDTRK    EQU   X'01'                    TRACK STATUS FLAG
IOEROR   EQU   X'20'
OVFLW    EQU   X'20'                    RECORD OVERFLOW
RCNT     EQU   X'92'                    READ COUNT
R0ERR    EQU   X'01'                    R0 ERROR SWITCH
SHA      EQU   X'39'                    SEARCH HOME ADDRESS
X0       EQU   0
ZEROS    EQU   X'00'
*
*
***********************************************************************
         EJECT
* REGISTER ASSIGNMENTS
***********************************************************************
PARMA    EQU   0                        PARAMETER REGISTER
PARMB    EQU   1                        PARAMETER REGISTER
WORKE    EQU   2                        WORK EVEN REGISTER
WORKO    EQU   3                        ODD WORK REGISTER
FOUR     EQU   4                        RETURN CODE REGISTER
FIVE     EQU   5                        WORK REGISTER
SIX      EQU   6                        WORK REGISTER
SEVEN    EQU   7                        WORK REGISTER
EIGHT    EQU   8                        WORK REGISTER
NINE     EQU   9                        RECORD BUFFER REGISTER
TEN      EQU   10                       TRACK DESCRIPTOR CHANNEL
*                                          PROGRAM AREA
AREAREG  EQU   11                       MAIN WORK AREA REGISTER
BASEREG  EQU   12                       BASE REGISTER
SAVEREG  EQU   13                       WORK REGISTER
RETREG   EQU   14                       WORK REGISTER
ADDREG   EQU   15                       WORK REGISTER
***********************************************************************
         BALR  BASEREG,X0               ESTABLISH
         USING OUTEXT,BASEREG              OWN ADDRESSABILITY
         USING WORKDSCT,AREAREG         ESTABLISH ADDRESSABILITY FOR
*                                          WORK AREA
         USING CNTAREA,TEN              ESTABLISH ADDRESSABILITY FOR
*                                          COUNT FIELD WORK AREA
         B     APARNO                   BRANCH AROUND APARNO   @ZA32949
         DC    C'IGG0860C ZA32949'      LAST APAR FIXED IN MOD @ZA32949
APARNO   EQU   *                                               @ZA32949
***********************************************************************
*        CHECK IF MODULE REQUESTED BY 0B OR 0D                  YL02912
***********************************************************************
OUTEXT   LTR   FOUR,FOUR                CHECK FOR ENTRY FROM IGG0860D
         BNZ   SECONDEN                 YES, ENTRY FROM IGG0860D
         L     SIX,IOBFLAG3             LOAD ADDRESS OF LAST CCW
         S     SIX,FULL16               GET ADDRESS OF LAST VALID CCW
         CLI   D0(SIX),RCNT             WAS THERE AN ERROR ON LAST
*                                          COUNT RECORD
         BNE   CHEKSPEC                 YES, THERE WAS A COUNT ERROR
         L     SIX,D0(X0,SIX)           PICK UP ADDRESS OF LAST COUNT
*                                          FIELD
         A     SIX,FULL8                GET ADDRESS OF BYTE FOLLOWING
         MVI   D0(SIX),FFSW             SET FLAG TO INDICATE END OF
*                                          COUNTS
***********************************************************************
         SPACE
* THIS SECTION COMPUTES THE SIZE OF THE LARGEST RECORD ON THE TRACK
***********************************************************************
FINDSIZE SR    EIGHT,EIGHT              CLEAR WORK REGISTERS
         L     FIVE,SAVEADDR            PICK UP ADDRESS OF FIRST COUNT
*                                          FIELD
FINDMAX  CLI   D0(FIVE),FFSW            DOES IT CONTAIN A COUNT
         BE    MORECORE                 NO, LAST COUNT
         SR    SIX,SIX
         IC    SIX,D5(X0,FIVE)          PICK UP KEY LENGTH
         AH    SIX,D6(X0,FIVE)             AND ADD DATA LENGTH
         LA    FIVE,D8(X0,FIVE)         ADDRESS OF NEXT COUNT FIELD
         CLR   SIX,EIGHT                IS THE NEW TOTAL THE GREATEST
         BNH   FINDMAX                  NO, GET NEXT TOTAL
         LR    EIGHT,SIX                PUT NEW HIGH IN COMPARE REG
         B     FINDMAX                  COMPLETE LOOP
***********************************************************************
*        GET CORE FOR LARGEST RECORD ON TRACK                   YL02912
***********************************************************************
         SPACE
MORECORE EQU   *                                                YM3004
         LA    EIGHT,D8(X0,EIGHT)       ADD 8 FOR COUNT
***********************************************************************
         MODESET EXTKEY=DATAMGT         KEY 5                   YL02912
***********************************************************************
         GETMAIN R,LV=(8),SP=229        LARGEST RECORRD         YM3004
*                                          PLUS EIGHT
         LTR   ADDREG,ADDREG            CHECK FOR GOTTEN CORE
         BZ    SAVE                     YES, CORE GOTTEN
         LA    FOUR,CC12                SET RETURN CODE FOR CORE NOT
*                                          AVAILABE
         B     FREECORE                 EXIT TO ERROR LOAD
***********************************************************************
*        CHECK IF OVERFLOW SPECIFIED                            YL02912
***********************************************************************
CHEKSPEC TM    SWITCH,OVFLW             DID USER'S DCB SPECIFY TRACK
*                                          OVERFLOW
         BNO   GOBACK                   NO, CONTINUE PROCESSING
         L     PARMB,USERPARM           PICK UP ADDRESS OF USER'S
*                                          PARAMETER LIST
         TM    D4(PARMB),BIT2           WAS WRITE SPECIAL INDICATED
         BO    SETSW                    YES, SET SWITCH
         TM    D4(PARMB),BIT3           WAS 'NOT WRITE SPECIAL'
*                                          SPECIFIED
         BNO   CNTERR                   NO, CAN'T HANDLE ERROR
***********************************************************************
*        SPACE COUNT INFO                                       YL02912
***********************************************************************
GOBACK   L     SIX,D0(X0,SIX)           PICK UP ADDRESS OF SPACE COUNT
         L     SIX,D0(X0,SIX)           PICK UP ADDRESS OF KEY LENGTH
*                                          IN COUNT TO BE SKIPPED
         A     SIX,FULL3                GET ADDRESS FO FLAG BYTE
         MVI   D0(SIX),FFSW             SET FLAG TO INDICATE END OF
*                                          COUNTS
         B     FINDSIZE                 BRANCH TO GET MAXIMUM RECORD
*                                          SIZE
SETSW    OI    SWITCH,BIT1              SET SWITCH TO INDICATE WRITE
*                                          SPECIAL IS REQUIRED
         B     GOBACK                   CONTINUE PROCESSING
***********************************************************************
CNTERR   LA    FOUR,CC32                SET COMPLETION CODE FOR ERROR
*                                          IN COUNT FIELD OF LAST
*                                          RECORD ON THE TRACK
         B     FREECORE                 EXIT TO ERROR LOAD
SAVE     EQU   *                                                YM3004
***********************************************************************
         MODESET EXTKEY=SUPR            KEY 0                   YL02912
***********************************************************************
         ST    PARMB,SVRBEX             STORE ADDR OF CORE      YM3004
***********************************************************************
         MODESET EXTKEY=DATAMGT         KEY 5                   YM3004
***********************************************************************
         LR    NINE,PARMB               LOAD ADDR OF CORE       YM3004
         ST    NINE,RECADDR             SAVE THIS ADDRESS IN WORK AREA
         ST    EIGHT,RECCORE            SAVE NUMBER BYTES GOTTEN
***********************************************************************
* THIS SECTION OF CODE DOES A PRELIMINARY READ HA OF THE ALTERNATE
* TRACK AND TESTS THE FLAG BYTE FOR A DEFECTIVE ALTERNATE INDICATION
* IF THE ALTERNATE IS NOT FLAGGED DEFECTIVE THEN CCWS ARE BUILT TO
* WRITE HOME ADDRESS AND R0 ON THE ALTERNATE. . A DEFECTIVE INDICATION
* CAUSES THE FORMAT 4 TO BE UPDATED TO DETERMINE THE NEXT AVAILABLE
* ALTERNATE TRACK
***********************************************************************
A        MVC   SEEK+D3(L4),DS4HCCHH     SET SEEK ADDRESS AND DEB EXTENT
         MVC   DEBSTRCC(L4),DS4HCCHH       TO REFLECT ADDRESS OF ALTER-
         MVC   DEBENDCC(L4),DS4HCCHH       NATE
         XC    ECB(L4),ECB              CLEAR ECB
         LA    FOUR,CCW1
         ST    FOUR,IOBSTART            STORE ADDRESS OF CHANNEL
*                                          PROGRAM INTO IOB START FIELD
         XC    IOBFLAG3(L8),IOBFLAG3    CLEAR CSW AND ERROR FLAGS
         LM    WORKE,SEVEN,CCWONE
         SR    EIGHT,EIGHT
         LA    EIGHT,SEEK+D3            PICK UP ADDRESS OF CCHH OF
*                                          ALTERNATE
         ALR   WORKE,EIGHT
         LA    EIGHT,CHECKHA            PICK UP ADDRESS OF HOME ADDRESS
*                                         AREA
         ALR   SIX,EIGHT
         LA    EIGHT,CCW1               PICK UP ADDRESS OF CCW TO TIC
*                                          TO
         ALR   FOUR,EIGHT
         STM   WORKE,SEVEN,CCW1         STORE FIRST 3 CCW'S
         BAL   NINE,EXECUTE             DO READ HA
         LA    NINE,GOODALT             SET BRANCH              XL03130
         AIF   ('&LIB' EQ 'LIB1').X225601                       XL03130
         CLI   DEVTAB+D3,D3350         IS IT 3350              @X50RSAG
         BE    SKIPWHA                 YES, SKIP WHA           @X50RSAG
         CLI   DEVTAB+D3,D3340         IS IT A 3340             XL03130
         BE    SKIPWHA                 YES,MUST NOT WRITE HA    XL03130
***********************************************************************
*        SET UP FOR WHA                                         YL02912
***********************************************************************
.X225601 ANOP                                                   XL03130
         OI    CCW3+D4,CCB              TURN ON COMMAND CHAINING BIT OF
*                                          READ HA
         LM    WORKE,WORKO,CCWTHREE     PICK UP NEXT CCW
         LA    EIGHT,ALTHA              PICK UP ADDRESS OF HOME ADDRESS
*                                         TO BE WRITTEN
         ALR   WORKE,EIGHT
         STM   WORKE,WORKO,CCW44        STORE NEXT CCW
***********************************************************************
*        BUILD CCWS                                             YL02912
***********************************************************************
SKIPWHA  EQU   *                                                XL03130
         LM    WORKE,SEVEN,CCWFOUR      PICK UP LAST CCW'S
         LA    EIGHT,ALTR0              PICK UP ADDRESS OF R0 TO BE
*                                          WRITTEN
         ALR   WORKE,EIGHT
         ALR   SIX,EIGHT
         LA    EIGHT,ALTHA              PICK UP ADDRESS OF HA
         ALR   FOUR,EIGHT
         AIF   ('&LIB' EQ 'LIB1').X225602                       XL03130
         CLI   DEVTAB+D3,D3350         IS IT 3350              @X50RSAG
         BE    YESMAD                  YES, DO IT TO IT        @X50RSAG
         CLI   DEVTAB+D3,D3340        IS IT A 3340              XL03130
         BNE   NOTWIN                  NO, ALL OTHER DEVICES    XL03130
YESMAD   EQU   *                                               @X50RSAG
         STM   WORKE,SEVEN,CCW3        STORE LAST 3 CCW'S       XL03130
         B     ISWIN                   CONTINUE                 XL03130
NOTWIN   EQU   *                                                XL03130
.X225602 ANOP                                                   XL03130
         STM   WORKE,SEVEN,CCW45        STORE NEXT 3 CCW'S
ISWIN    EQU   *                                                XL03130
         MVI   ALTHA,GDTRK              INDICATE HOME ADDRESS IS FOR
         MVC   ALTHA+D1(L4),DS4HCCHH       AN OPERATIVE ALTERNATE TRACK
         L     FOUR,USERPARM
         L     FOUR,D4(X0,FOUR)         PICK UP POINTER TO USER'S CCHH
         MVC   ALTR0(L16),R0            MOVE INTO NEW R0 THE ORIGINAL
*                                          PRIMARY TRACK ADDRESS FROM
*                                          R0 OF THE PRIMARY TRACK
         CLI   ERRTYPE,R0ERR            R0 PASSED BY UTILITY IN ERROR
         BNE   EXECUTE                  NO, R0 NOT PASSED BY UTILITY
*                                          AND IN ERROR
         MVC   ALTR0(L8),D0(FOUR)       MOVE IN CALLER'S COUNT
*                                          PORTION OF R0
         MVI   ALTR0+D8,ZEROS           CLEAR DATA PORTION OF R0
         MVC   ALTR0+D9(L7),ALTR0+D8
***********************************************************************
*        TAKE CARE OF HA/R0                                     YL02912
***********************************************************************
EXECUTE  EXCP  IOB                      REWRITE HA & R0 ON ALTERNATE
         WAIT  1,ECB=ECB                WAIT FOR I/O COMPLETE
         TM    ECB,IOEROR               CHECK FOR AN I/O ERROR
         BO    SUCCESS                  NO, NO I/O ERROR
         LA    EIGHT,BITS8
         L     SEVEN,IOBFLAG3           PICK UP ADDRESS OF CCW IN ERROR
*                                          +8 FROM CSW IN IOB
         SR    SEVEN,EIGHT
         CLI   D0(SEVEN),SHA            CHECK FOR SEARCH HOME ADDRESS
*                                          ERROR
         BE    LOWOIL                   YES, A SEARCH HOME ADDRESS
*                                          ERROR
***********************************************************************
*        REDUCE NUMBER OF ALTERNATES                            YL02912
***********************************************************************
SECONDEN LH    EIGHT,NUMALT             PICK UP NUMBER OF ALTERNATES TO
*                                          BE TRIED
         BCTR  EIGHT,X0                 REDUCE BY ONE
         STH   EIGHT,NUMALT             STORE BACK NUMBER TO BE TRIED
         LTR   EIGHT,EIGHT              CHECK FOR MORE ALTERNATES
         BNZ   LOCATE                   YES, MORE ALTERNATES
         LA    FOUR,CC16                SET COMPLETION CODE TO 16
         OI    SWITCH,BIT4              SFT SWITCH TO INDICATE NO
*                                          ALTERNATES AVAILABLE
***********************************************************************
*        EXIT                                                   YL02912
***********************************************************************
FREECORE MVC   XCTLID(L2),ATLAS06       MOVE ERROR LOAD NAME IN
         MVC   TTR(L3),TTR6             MOVE ERROR LOAD TTR IN
XCTL     XC    XCTLPTR+D4(L4),XCTLPTR+D4 CLEAR SECOND WORD OF POINTER
         L     PARMB,PNTSNAP            GET SNAP LIST POINTER   YL02912
         LTR   PARMB,PARMB              SNAP MODE               YL02912
         BZ    NOSNAP                   NO - DONT SNAP          YL02912
         SNAP  ID=5,MF=(E,(1))          SNAP                    YL02912
NOSNAP   EQU   *                        *                       YL02912
         XCTL  ,MF=(E,(1)),SF=(E,XCTLPTR)
***********************************************************************
LOWOIL   LA    FOUR,CC36                SET COMPLETION CODE FOR 36-
*                                          SEARCH HOME ADDRESS ERROR
         B     FREECORE                 EXIT TO ERROR LOAD
LOCATE   LH    EIGHT,DS4HCCHH+D2       GET HH NEXT ASGNABLE    @X50RSAG
         AIF   ('&LIB' NE 'LIB1').X730510                       YL02912
.X730510 ANOP                                                   YL02912
***********************************************************************
*        INCREMENT HEAD NUMBER                                  YL02912
***********************************************************************
*                                          ALTERNATE
         LA    EIGHT,D1(X0,EIGHT)       ADD ONE
         CH    EIGHT,DS4DEVSZ+D2        COMPARE FOR NEED TO RESET
*                                       TRACK NUMBER TO ZERO
         BL    NOUP                     NO, TRACK NUMBER IS VALID
         XC    DS4HCCHH+D2(L2),DS4HCCHH+D2 SET TRACK NUMBER FO ZERO
         LH    EIGHT,DS4HCCHH
         LA    EIGHT,D1(X0,EIGHT)       INCREMENT CYLINDER NUMBER BY
         STH   EIGHT,DS4HCCHH              ONE
REDUCE   LH    EIGHT,DS4NOATK           REDUCE NUMBER OF AVAILABLE
         BCTR  EIGHT,X0                    ALTERNATES BY ONE
         STH   EIGHT,DS4NOATK
         B     A                        TRY ANOTHER ALTERNATE
NOUP     STH   EIGHT,DS4HCCHH+D2        STORE NEW TRACK NUMBER
         B     REDUCE                   REDUCE NUMBER OF ALTERNATES
         AIF   ('&LIB' NE 'LIB1').X730511                       YL02912
STORE1   MVC   DS4HCCHH+D3(L1),SEEK+D6  MOVE IN NEW TRACK ADDRESS
         B     REDUCE                   REDUCE NUMBER OF ALTERNATES
STORE2   MVC   DS4HCCHH+D2(L1),SEEK+D5  MOVE IN NEW CYLINDER ADDRESS
         B     REDUCE                   REDUCE NUMBER OF ALTERNATES
STORE3   MVC   DS4HCCHH+D1(L1),SEEK+D4  MOVE IN NEW STRIP ADDRESS
         B     REDUCE                   REDUCE NUMBER OF ALTERNATES
.X730511 ANOP                                                   YL02912
SUCCESS  TM    CHECKHA,BADALT           IS ALTERNATE DEFECTIVE @ZA32949
         BO    SECONDEN                 YES, ALTERN IS DEFECT  @ZA32949
         BR    NINE                     BRANCH TO WRITE HA OR BRANCH
*                                          TO NEXT LOAD
***********************************************************************
GOODALT  EQU   *
         MVC   XCTLID(L2),ATLAS05       MOVE NEXT LOAD NAME IN
         MVC   TTR(L3),TTR5             MOVE NEXT LOAD TTR IN
         B     XCTL                     EXIT TO NEXT LOAD MOUDLE
***********************************************************************
CCWONE   CCW   X'39',0,X'40',4          SEARCH HOME ADDRESS
CCWTWO   CCW   X'08',0,X'40',0          TIC TO SEARCH HA
CCWTOA   CCW   X'1A',0,X'00',5          READ HOME ADDRESS
CCWTHREE CCW   X'19',0,X'40',5          WRITE HOME ADDRESS
CCWFOUR  CCW   X'15',0,X'40',16         WRITE R0
CCWFIVE  CCW   X'1A',0,X'40',5          READ HOME ADDRESS
CCWSIX   CCW   X'16',0,X'10',16         READ R0
***********************************************************************
GETL     GETMAIN EC,MF=L                L FORM FOR BUFFER
CYLNO    DC    X'05'                    NUMBER OF CYLINDERS PER TRACK
*                                          FOR A 2321
STRIPNO  DC    X'0A'                    NUMBER OF STRIPS PER SUBCELL
*                                          FOR A 2321
FULL16   DC    F'16'                    *
FULL8    DC    F'8'                     *
FULL3    DC    F'3'                     *
MAINT    DC    2CL25'IGG0860C MAINTENANCE AREA'                 YL02912
***********************************************************************
         SPACE
*XCTL BY TTR TABLE
         SPACE
         ORG   IGG0860C+1000            FIRST ORG
         SPACE
TTRTABLE EQU   *
ATLAS05  DC    C'0D'                    ID OF NEXT LOAD
TTR5     DC    X'00000000'              TTR OF NEXT LOAD
ATLAS06  DC    C'AE'                    ID OF ERROR LOAD
TTR6     DC    X'00000000'              TTR OF ERROR LOAD
         DC    X'0000'                  LAST TABLE ENTRY CODE
         ORG   IGG0860C+1020            SECOND ORG
         DC    C'086'                   ATLAS SVC CODE
         DC    X'7D'                    RELATIVE TABLE START ADDRESS
*                                          IN DOUBLE WORDS
***********************************************************************
WORKDSCT DSECT
SVRBEX   DS    1F                       SAVE AREA FOR SVRB EXTENSION
*                                          POINTER
DEVTAB   DS    1F                       DEVICE TYPE INDICATOR FROM UCB
SWITCH   DS    CL1                      LOGIC SWITCH
*                                       0 BIT INDICATES CHANNEL
*                                          PROGRAM NOT RE-EXECUTABLE
*                                       1 BIT INDICATES WRITE SPECIAL
*                                          REQUIRED
*                                       2 BIT INDICATES TRACK OVERFLOW
*                                          DATA SET
*                                       4 BIT INDICATES NO ALTERNATES
*                                          AVAILABLE
*                                       6 BIT INDICATES AN ERROR ON
*                                          OTHER THAN THE ORIGINAL
*                                          ERROR RECORD
*                                       7 BIT INDICATES VTOC HAS BEEN
*                                          ENQUEUED UPON
NUMBERR  DS    CL2                      NUMBER OF ERROR RECORDS
VTOCADR  DS    CL5                      VTOC ADDRESS CCHHR
SPLIT    DS    X'00'                    SPLIT CYLINDER SWITCH
RETPT    DS    1F                       POINTER TO NEXT ROUTINE
         DS    0F
***********************************************************************
         IECSDSL1 (4)
***********************************************************************
TICADDR  DS    1F                       ADDRESS OF TIC CCW IN NEW
*                                          CHANNEL PROGRAM
SAVEADDR DS    1F                       ADDRESS OF FIRST COUNT FIELD
CNTCORE  DS    1F                       NUMBER OF BYTES IN COUNT FIELD
*                                          WORK AREA
COREADDR DS    1F                       ADDRESS OF COUNT FIELD
*                                          WORK AREA
RECADDR  DS    1F                       ADDRESS OF CORE GOTTEN FOR
*                                          LONGEST RECORD SIZE
RECCORE  DS    1F                       NUMBER OF BYTES IN THIS AREA
DDNAME   DS    1D                      DDNAME SAVE AREA         XL03130
DEVINFO  DS    5F                      DEVICE INFO              XL03130
PARMUSER DS    1F                       POINTER USER PARM LIST  YL02912
PNTEST   DS    1F                       TEST MODE               YL02912
PNTSNAP  DS    1F                       SNAP MODE               YL02912
PNTSNS   DS    CL2                      SENSE BYTE - USER       YL02912
         DS    CL1                      FILLER                  YL02912
PNTMIOB  DS    CL1                      M OF IOB                YL02912
PNTIOB   DS    1F                       IOB POINTER             YL02912
PNTCNT   DS    1F                       COUNT POINTER           YL02912
PARMCNT  DS    2F                       COUNTS                  YL02912
PNTIOT   DS    CL2                      TIOT OFFSET             YL02912
         DS    CL1                      FILLER                  YL02912
PNTOFLO  DS    CL1                      TRACK OVERFLO           YL02912
PNTDEB   DS    1F                       DEB POINTER             YL02912
HOLDKEY  DS    1F                       USER KEY                YL02912
REGHOLD  DS    1F                       TEMP REG SAVE           YL02912
CNTADDR  DS    1F                       ADDRESS OF COUNT FIELD FOR
*                                          CCW IN ERROR
ERRTYPE  DS    CL1                      TYPE OF ORIGINAL ERROR
RECNUMB  DS    1F                       R OF ERROR RECORDS
REGSAVE  DS    5F                       SAVE AREA FOR REGISTERS
ECB      DS    1F                       ECB
IOB      DS    8F                       IOB
IOBFLAG3 EQU   IOB+8
IOBSTART EQU   IOB+16
SEEK     DS    2F                       SEEK ADDRESS
DEB      DS    12F                      DEB
DEBSTRCC EQU   DEB+38
DEBENDCC EQU   DEB+42
DEBPTR   DS    F                        DCB SLOT
MIELNAME DS    CL6                      VOLUME SERIAL SLOT
DCB      EQU   DEBPTR-44
R0AREA   DS    2D                       AREA FOR R0
DEQAREA  DS    2D                       DEQ TABLE AREA
MJELNAME DS    2F                       MAJOR QUEUE NAME AREA
ENQAREA  DS    2D                       ENQ TABLE AREA
USERPARM DS    1F                       POINTER TO USER'S PARM LIST
NUMALT   DS    1H                       NUMBER OF ALTERNATES TO BE
*                                          TRIED
MAXCOUNT DS    1H                       MAXIMUM NUMBER OF COUNT FIELDS
*                                          PER TRACK+1
XCTLPTR  DS    2F                       PTR TO XCTL WORK AREA TABLE
XCTLAREA DS    0CL32
XCTLNM   DS    CL6                      COMMON BYTES OF ATLAS NAME
XCTLID   DS    CL2                      ID OF ATLAS MODULE
         DS    CL6                      *
TTR      DS    CL3                      TTR OF ATLAS MODULE
         DS    CL5                      *
TTRATTB  DS    CL2                      ATTRIBUTES OF ATLAS - ALL LOADS
TTRCREQ  DS    CL3                      CORE REQUIREMENT OF ATLAS LOAD
TTRLNGTH DS    CL2                      LENGTH OF ATLAS LOAD
         DS    CL3                      *
INCRMNT  DS    1F                       INCREMENT FOR READ COUNTS
PROGADDR DS    1F                       ADDRESS OF NEW CHANNEL PROGRAM
ALTHA    DS    2F                       ALTERNATE TRACK HOME ADDRESS
ALTR0    DS    4F                       ALTERNATE TRACK R0
***********************************************************************
CCW1     DS    1D                       CCW AREA
CCW2     DS    1D                       *
CCW3     DS    1D                       *
CCW44    DS    1D                       *
CCW45    DS    1D                       *
CCW56    DS    1D                       *
CCW57    DS    1D                       *
CCW31    DS    1D                       *
CCW32    DS    1D                       *
CCW33    DS    1D                       *
CCW34    DS    1D                       *
CCW35    DS    1D                       *
CCW36    DS    1D                       *
CCW37    DS    1D                       *
CCW38    DS    1D                       *
CCW39    DS    1D                       *
CHECKHA  DS    2F                       AREA FOR HOME ADDRESS OF
*                                       ALTERNATE
*
***********************************************************************
CNTAREA  DSECT
         DS    F                       FOR DBL WD ALGNMT.      @ZM40498
MADSD    DS    CL4                     3350 SKIP DISP          @X50RSPC
HOMEAD   DS    2F                       CCW AREA
R0       DS    4F                       *
CCW21    DS    2F                       *
CCW22    DS    2F                       *
CCW23    DS    2F                       *
         END
