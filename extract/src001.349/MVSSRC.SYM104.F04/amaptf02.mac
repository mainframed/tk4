AMAPTF02 CSECT
         TITLE 'AMAPTF02 - PTF APPLICATION FUNCTION SERVICE PROGRAM'
*D 192000                                                        Y01983
*A 884500                                                        Y01983
*C 588000,858500                                                  M4012
*C 786000,828500-829000,942500                                    M4008
*A 798500-799500,942600                                           M4008
*D 834000,836000                                                  M4003
*C 504000,684000                                                  M4003
*A 558500-560000,828500-829000,840500-841000,942500-943000        M4003
*A 600000,624500-630000                                           M3990
*              STATUS:
*                       OS/VS2 RELEASE 02.0
*
*        FUNCTION:
*
*              THIS ROUTINE TAKES THE NAME POINTED TO BY REG 1 AND
*
*              DETERMINES THE LINKEDIT ATTRIBUTES. THIS IS DONE BY
*
*              OPENING THE DD SYSLMOD AND DOING A BLDL TO GET THE
*
*              MEMBER ATTRIBUTES. AFTER THE BLDL ,LINKEDIT PARM INFO
*
*              IS BUILT AND PASSED IN THE AREA SUPPLIED. THE PARM LIST
*
*              IS IN THE FORMAT LLXXX,XXX,...,XXX WHERE LL IS A HALF
*
*              WORD COUNT OF THE ENTIRE LIST AND THE XXX'S ARE LINKEDIT
*
*              ATTRIBUTES.
*
*              ENTRY  POINTS:
*                                  AMAPTF02
*
*              INPUT:
*                                  PARAMETER LIST WITH MODULE NAME
*                                  POINTED TO BY REG 1 AND AREA TO
*                                  RETURN LINK EDIT ATTRIBUTES.
*
*              OUTPUT:
*
*              EXTERNAL REFERENCES:
*                                  SYNADEXT    FOR IO ERRORS
*
*              EXITS:
*                        ERROR:
*                                  ERROR CODE IN WORK AREA SUPPLIED.
*                                  0 IN LL FIELD MEANS MEMBER NOT FOUND
*
*                        NORMAL:
*                                  BRANCH REG 14.
*
*              TABLES / WORK AREAS:
*                                  AREA FOR BLDL PASSED IN REG 1.
*
*              ATTRIBUTES:
*                                  REUSEABLE
*
*              CHARACTOR CODE DEPENDENTENCY:
*
*                                  NONE
*
*              NOTES:
*                                  NONE
*
*
*
*
*
*
*
*/*AMAPTF02: CHART AMAPTF02 */
*/* E AMAPTF02 */
*/* P SAVE PARAMETER LIST POINTER */
*/* L OPEN DCB FOR SYSLMOD DD */
*/* D (YES,,NO,AMAERROP) OPEN SUCCESSFUL */
*/* D (YES,,NO,AMA00) IS BLDL FOR LINKAGE EDITOR NAME */
*/* P (,AMA000) SET DCB ADDRESS TO 0 */
*/*AMA00: P SET DCB ADDRESS FOR SYSLMOD DD */
*/*AMA000: P SET UP BLDL FLAGS */
*/* L BLDL FOR NAME PASSED */
*/* D (YES,,NO,AMAERROR) BLDL SUCCESSFUL */
*/* P SET UP COUNT */
*/* D (YES,,NO,AMA001) REFRESHABLE BIT SET */
*/* P MOVE 'REFR,' OPTION TO BUFFER */
*/* P INCREMENT COUNT BY 5 */
*/*AMA001: D (YES,,NO,AMA002) REENTRANT BIT SET */
*/* P MOVE 'RENT,' OPTION TO BUFFER */
*/* P INCREMENT COUNT BY 5 */
*/*AMA002: D (YES,,NO,AMA003:R) REUSABLE BIT SET */
*/* P MOVE 'REUS,' OPTION TO BUFFER */
*/* P (,AMA003:R) INCREMENT COUNT BY 5 */
*/*AMA003: D (YES,,NO,AMA004) DOWNWARD COMPATIBLE BIT SET */
*/* P MOVE 'DC,' OPTION TO BUFFER */
*/* P INCREMENT COUNT BY 3 */
*/*AMA004: D (YES,,NO,AMA005) ONLY LOADABLE BIT SET */
*/* P MOVE 'OL,' OPTION TO BUFFER */
*/* P INCREMENT COUNT BY 3 */
*/*AMA005: D (YES,,NO,AMA006P) NOT EDITABLE BIT SET */
*/* P MOVE 'NE,' OPTION TO BUFFER */
*/* P (,AMA006) INCREMENT COUNT BY 3 */
*/*AMA006P: P MOVE 'XREF,' OPTION TO BUFFER */
*/* P (,AMA006:R) INCREMENT COUNT BY 5 */
*/*AMA006: D (YES,,NO,AMA007) SCATTER LOADABLE BIT SET */
*/* P MOVE 'SCTR,' OPTION TO BUFFER */
*/* P INCREMENT COUNT BY 5 */
*/*AMA007: P MOVE 'LIST' OPTION TO BUFFER */
*/* P INCREMENT COUNT BY 4 */
*/* P STORE COUNT IN BUFFER */
*/*AMACLOSE: L CLOSE SYSLMOD DD */
*/*RETCODE: R RETURN */
*/*AMAERROP: P (,RETCODE) INDICATE SYSLMOD OPEN FAILED */
*/*AMAERROR: P (,AMACLOSE) INDICATE BLDL FAILED */
*/*AMAPTF02: END */
               EJECT
ZERO     EQU   0                   VALUE OF ZERO
ATTB1    EQU   26                  VALUE OF 26 FOR LKED ATTRIBUTES
ATTB2    EQU   27                  VALUE OF 27 FOR LKED ATTRIBUTES
SCTR1    EQU   4                   FOR SCTR TEST
REFR1    EQU   1
OL1      EQU   8                   FOR ONLY LOADABLE TEST
NE1      EQU   8                   FOR NOT EDITABLE  TEST
REUS1    EQU   64                  FOR REUS TEST
DC1      EQU   128                 FOR DC TEST
RENT1    EQU   128                 FOR REENTRANT TEST
BASE     EQU   12                  BASE REG
BASE2    EQU   2                   FOR BASE REG2
BASE3    EQU   3                   FOR BASE REG3
BLDL     EQU   18                  FOR BLDL SVC
GOODBLDL EQU   0                   FOR BLDL TEST
COUNT    EQU   2                   REG FOR BYTE COUNT
PARMREG1 EQU   1                   FOR REG1
PARMREG0 EQU   0                   FOR REG0
GOODOPEN EQU   X'10'               FOR OPEN CHECK                 M4003
RETURN   EQU   14
BRREG    EQU   15
SAVEREG  EQU   13
BLANK    EQU   X'40'               BLANK CHARACTER
*
*
         USING *,BASE
         STM   RETURN,BASE,BASE(SAVEREG)
         LR    BASE,BRREG          BASE REG = R12
         LA    BASE2,AMASAVE       GET ADDR OF MY SAVE AREA       M4003
         ST    BASE2,8(SAVEREG)    FORWARD CHAIN SAVE AREA        M4003
         ST    SAVEREG,4(BASE2)    BACK CHAIN SAVE AREA           M4003
         LR    SAVEREG,BASE2       PUT MY SAVE AREA ADDR IN 13    M4003
         LR    BASE3,PARMREG1      PUT AREA ADDR IN R3
         OPEN  (AMADDCD,(INPUT))   OPEN SYSLMOD
         TM    AMAOBIT,GOODOPEN    OPEN GOOD ?
         BZ    AMAERROP            NO BRANCH                      M4012
         LR    PARMREG0,BASE3      SET UP
         CLI   ZERO(BASE3),ZERO IS THE BLDL FOR THE LINKAGE EDIT. M3990
         BNE   AMA00               NO - GO LOAD THE DCB ADDRESS
         LA    PARMREG1,ZERO       YES - USE A DCB ADDRESS OF ZERO
         B     AMA000              AND ISSUE THE BLDL
AMA00    LA    PARMREG1,AMADDCD    PARAMETER REG FOR BLDL
AMA000   MVC   ZERO(L'CONST,BASE3),CONST   SET BLDL IN R3         M3990
         SVC   BLDL                ISSUE BLDL SVC                 M3990
         STC   BRREG,CK
         CLI   CK,GOODBLDL         WAS BLDL GOOD ?
         BNE   AMAERROR            NO BR
         LA    COUNT,2(BASE3)      SET UP COUNT REG
         TM    ATTB2(BASE3),REFR1  REFREFR BIT ON ?
         BZ    AMA001              NO BR
         MVC   ZERO(L'REFR,BASE2),REFR
         LA    BASE2,L'REFR(BASE2)    UPDATE REG
AMA001   TM    ATTB1(BASE3),RENT1  REENTRANT BIT ON ?             M4003
         BZ    AMA002              NO BR
         MVC   ZERO(L'RENT,BASE2),RENT     RENT PARM
         LA    BASE2,L'RENT(BASE2) UPDATE  REG
AMA002   TM    ATTB1(BASE3),REUS1  REUSABLE BIT ON ?
         BZ    AMA003              NO BR
         MVC   ZERO(L'REUS,BASE2),REUS  MOVE IN REUS,
         LA    BASE2,L'REUS(BASE2) UPDATE REG
AMA003   TM    ATTB2(BASE3),DC1    DOWNWARD COMPATABLE?
         BO    AMA004              NO  BR
         MVC   ZERO(L'DC,BASE2),DC MOVE IN DC,
         LA    BASE2,L'DC(BASE2)   UPDATE REG
AMA004   TM    ATTB1(BASE3),OL1    ONLY LOADABLE ?
         BZ    AMA005              NO BR
         MVC   ZERO(L'OL,BASE2),OL MOVE IN OL,
         LA    BASE2,L'OL(BASE2)   UPDATE REG
AMA005   TM    ATTB2(BASE3),NE1    NOT EDITABLE ?
         BZ    AMA006P             NO BR                          M4008
         MVC   ZERO(L'NE,BASE2),NE MOVE IN NE,
         LA    BASE2,L'NE(BASE2)   UPDATE REG
         B     AMA006                                             M4008
AMA006P  MVC   ZERO(L'XREF,BASE2),XREF   MOVE IN XREF OPTION      M4008
         LA    BASE2,L'XREF(BASE2)       UPDATE REG               M4008
AMA006   TM    ATTB1(BASE3),SCTR1  SCTR ?
         BZ    AMA007              NO BR
         MVC   ZERO(L'SCTR,BASE2),SCTR  MOVE IN SCTR,
         LA    BASE2,L'SCTR(BASE2) UPDATE REG
AMA007   EQU   *
         MVC   ZERO(L'LIST,BASE2),LIST   PUT IN LIST              M4008
         LA    BASE2,L'LIST(BASE2)       BUMP PTR                 M4008
         SR    BASE2,BASE3         GET COUNT OF PARM DATA
         LA    PARMREG0,COUNT      DECREMENT BY 2                 M4003
         SR    COUNT,PARMREG0      LENGTH OF OPTION LIST          M4003
AMACLOSE STH   COUNT,ZERO(BASE3)   STORE COUNT
         CLOSE AMADDCD
RETCODE  L     SAVEREG,4(SAVEREG)  GET PTR TO BACK SAVE AREA      M4003
         LM    RETURN,BASE,BASE(SAVEREG)     RESTORE REGS
         BR    RETURN              RETURN
AMAERROP MVI   0(BASE3),X'FF'      SET BAD OPEN INDICATOR         M4012
         B     RETCODE             RETURN TO CALLER               M4012
AMAERROR LA    COUNT,ZERO          SET COUNT TO ZERO
         B     AMACLOSE
*
CONST    DC    X'00010044'         BLDL CONSTANTS
REFR     DC    C'REFR,'
RENT     DC    C'RENT,'
REUS     DC    C'REUS,'
SCTR     DC    C'SCTR,'
DC       DC    C'DC,'
OL       DC    C'OL,'
NE       DC    C'NE,'
CK       DS    C
LIST     DC    C'LIST'                                            M4008
XREF     DC    C'XREF,'                                           M4008
PATCH    DC    5F'0'                    PATCH AREA               Y01983
AMASAVE  DC    18F'0'                                             M4003
AMADDCD  DCB   DDNAME=SYSLMOD,DSORG=PO,RECFM=U,MACRF=R,NCP=1
AMAOBIT  EQU   AMADDCD+48
         END
