* /* START OF SPECIFICATIONS ***                                      *
*01* PROCESSOR = ASSEM;                                               *
**** END OF SPECIFICATIONS ***/                                       *
 TITLE 'IEHINITT-IEHINITT ROOT MODULE'
* TITLE:                                                          M2840
*    'IEHINITT: TAPE LABELING UTILITY PROGRAM'                    M2840
* STATUS:                                                         M2840
*    CHANGE LEVEL 000                                             M2840
* FUNCTION/OPERATION:                                             M2840
*    THIS ROUTINE EXAMINES THE INITT CONTROL CARD AND BUILDS AN   M2840
*    IMAGE IN CORE OF THE LABEL TO BE PUT ON THE USER'S TAPE.     M2840
*    IT ALSO KEEPS TRACK OF THE NUMBER OF TAPES THAT ARE TO BE    M2840
*    LABELED AND CALLS THE SVC FOR EACH TAPE INCREMENTING THE     M2840
*    VOLUME SERIAL NUMBER BY ONE EACH TIME THE SVC IS CALLED      M2840
*    FOR A SINGLE LABEL GROUP.                                    M2840
*    THIS ROUTINE DETERMINES AND CONSTRUCTS THE VOLUME HEADER    S20038
*    LABEL AS AN OS/360 STANDARD LABEL OR AS AN ASCII STANDARD   S20038
*    LABEL.  THE DUMMY HEADER LABEL IS THEN CONSTRUCTED IN       S20038
*    CONTIGUOUS STORAGE TO THE VOLUME LABEL.  THIS LABEL GROUP   S20038
*    IS THEN TRANSLATED FROM EBCDIC TO ASCII, IF SPECIFIED BY    S20038
*    THE USER, BEFORE CALLING THE TAPE LABELING SVC.             S20038
* ENTRY POINTS:
*    IEHINITT IS THE ONLY ENTRY POINT. IT IS ENTERED TO
*    SET UP A PARAMETER LIST FOR LABELING TAPES
* INPUT:
*    THERE IS NO INPUT PASSED IN REGISTERS. IT IS THE CONTROL
*    CARDS IN THE CARD READER THAT WILL INDICATE HOW MANY AND HOW
*    THE TAPES ARE LABELED
* OUTPUT:
*    OUTPUT IS THE TAPES THAT HAVE BEEN SUCCESSFULLY LABELED
* EXTERNAL ROUTINES:
*         IEHSCAN - SCAN ROUTINE. SCANS CARD INPUT IMAGE IN
*         CORE FOR OPTIONS SPECIFIED
*         IEHPRNT -  PRINT ROUTINE. PRINTS OUT CARDS AND ERROR
*         MESSAGES. ALSO PUT HEADING AND PAGE NUMBERS ON EACH PAGE
*         SVC 39 - WRITES A LABEL GROUP ON A TAPE                S20038
*         GETMAIN - REQUESTS CORE STORAGE FOR WORK AREA          S20038
*         OPEN - OPENS READ AND PRINT DATA SETS                  S20038
*         RDJFCB - READS JOB FILE CONTROL BLOCK FOR DENSITY      S20038
*         XLATE - ISSUES TRANSLATE SVC FOR ASCII OR EBCDIC CODE  S20038
*         CLOSE - CLOSES READ AND PRINT DATA SETS                S20038
*         FREEMAIN - RELEASES CORE STORAGE FOR WORK AREA         S20038
* EXITS-NORMAL:
*    CONTROL IS RETURNED TO THE CALLER WITH ALL REGS AS THEY
*    WERE WHEN THE UTILITY WAS CALLED
* EXITS, ERROR:
*    EXIT IS THE SAME AS ABOVE EXCEPT PREVIOUS TO EXIT AN
*    ERROR MESSAGE HAS BEEN PRINTED OUT
*    THE RETURN CODE IN REGISTER 15 INDICATES THE SEVERITY OF
*    THE ERROR
* TABLES/WORK AREAS:
*    THE WORK AREA CONTAINS A CARD IMAGE, TAPE LABEL
*    IMAGE, ERROR MESSAGES AND SWITCHES
*    THIS AREA IS OBTAINED WITH AN UNCONDITIONAL GETMAIN AND
*    IS SHARED BY IEHINITT, IEHSCAN, AND IEHPRNT
* ATTRIBUTES:
*    REENTRANT
* CHARACTER CODE DEPENDENCY -                                    S20038
*    THE OPERATION OF THE SECTION, 'CHECK', WHICH VERIFIES THE   S20038
*    SERIAL NUMBER, DEPENDS UPON A CLASSIFICATION OF THE         S20038
*    EXTERNAL CHARACTER SET BY MEANS OF A TABLE.  THE TABLE IS   S20038
*    CONSTRUCTED FOR THE EBCDIC CHARACTER SET AND IS ARRANGED SO S20038
*    THAT REDEFINITION OF ALL 'CHARACTER' CONSTANTS, BY REASSEM- S20038
*    BLY, WILL RESULT IN A CORRECT TABLE FOR THE NEW DEFINITIONS S20038
*    IF THE EXTERNAL CHARACTER SET REMAINS UNCHANGED.            S20038
*    INSTRUCTIONS FOR MODIFYING THE TABLE TO ADD, DELETE, OR     S20038
*    CHANGE THE MEANING OF CHARACTERS ARE CONTAINED WITH THE     S20038
*    TABLE LOCATED IN THE 'CONSTANTS' SECTION.                   S20038
IEHINITT CSECT
* 008600,064000                                                  A25710
* 071600-071800,073400,081500,112400                             A23445
*0251                                                              1599
*0251030000,075600-076400                                          3260
*                                                                M3286
* 032200,032800,049400-050200,051400-05220,053200-053400,        S20038
* 059200-060200,063600-063800,064800-065200,058000-058200,       S20038
* 058600,112800,112920,124920,127800,128200,131800,141360,144000 S20038
* 032600,117000-117800,129000-131800,085000
*A083310                                                         A38606
*C 146260                                                         M6029
*A 125161                                                      @ZA01679
*C 008400                                                      @ZA01679
*C 017830,018000                                               @ZA01680
*C 021400,022600                                               @ZA01744
*C 094600                                                      @ZA04374
*A135684-135692,135701-135708                                  @ZA16491
*C135678,135700,135710                                         @ZA16491
IEHINIT  SAVE  (14,12),T,IEHINITT-OZ16491  SAVE REGS           @ZA16491
         LR    INITBASE,REG15          LOAD IEHINITT BASE REG     M2840
         USING IEHINIT,INITBASE        ESTABLISH ADDRESSIBILITY   M2840
         USING IEHINIT2,MSGBASE        ADDRESSABILITY FOR CSECT2  M2840
         GETMAIN R,LV=0+LAST-FIRST     GET WORK AREA              M2840
         SR    UCBSEQ,UCBSEQ           KEEP THE                   M2840
*                                         SEQUENCE NUMBER OF      15673
*                                         THE UCB BEING USED.     15673
         LR    GOTTEN,REG1             SAVE ADDRESS OF WORK AREA  M2840
*
*                  THIS SECTION CLEARS OUT THE GETMAIN AREA AND
*                     INITIALIZE THE LABEL FIELD TO THE DEFAULT OPTIONS
*                     RELATED TO THE ITITT CARD
*
         MVI   0(REG1),BINZERO         ZERO OUT THE WORK AREA     M2840
         LA    REG2,ABS255             GET LENGTH OF 255          M2840
         LA    REG3,0+LAST-FIRST       GET LENGTH OF WORK AREA    M2840
         BCTR  REG3,REG0               SUB FIRST BYTE CLEARED     M2840
EXEC     EX    REG2,MVZO               CLEAR 255 BYTES            M2840
         LA    REG1,ABS256(REG1)       BUMP REG 1 BY 256       @ZA01679
         S     REG3,TWO56              SIZE OF AREA NOT CLEAR     M2840
         C     REG3,TWO55              LESS THAN 255 BYTES?       M2840
         BC    2,EXEC                  NO, GO CLEAR 255 BYTES     M2840
         EX    REG3,MVZO               CLEAR REMAINDER OF BYTES   M2840
         USING WORK,GOTTEN             SET UP ADDRESSIBILITY FOR  M2840
*                                         WORK AREA
         ST    REG13,SAVE+DSP4         SAVE CALLER'S REG 13       M2840
*
*
         LA    STWREG,PRINT            GET ADDRESS OF PRINT DCB   M2840
         ST    STWREG,PRINTAD          SAVE IT FOR EXECUTE OPEN   M2840
         MVI   PRINTAD,OPENOUT         SET END-OF-LIST, OUTPUT    M2840
         LA    STWREG,LBLDCB           DUMMY ADDR OF JFCB DCB     M2840
         ST    STWREG,AGO              SAVE FOR EXECUTE, RDJFCB   M2840
         MVI   AGO,ENDLIST             SET END-OF-LIST, RDJFCB    M2840
         LA    STWREG,READ             GET ADDRESS OF READ DCB    M2840
         ST    STWREG,READAD           SAVE IT FOR EXECUTE OPEN   M2840
         MVI   READAD,ENDLIST          END-OF-LIST, INPUT         M2840
         MVC   PRINT(L'PRINT),PRINT1   COPY PRNT DCB TO WORK AREA M2840
         MVC   READ(L'READ),READ1      COPY READ DCN TO WORK AREA M2840
         EJECT
*                                                                 M2840
*                  CHECK TO SEE IF ANY PARAMETERS WERE PASSED TO  M2840
*                     IEHINITT BY LINK OR ATTACH, OR BY THE PARM  M2840
*                     FIELD OF THE EXEC STATEMENT.                M2840
*                                                                 M2840
INVOC    L     REG13,SAVE+DSP4         PICK UP PARAMETER LIST PTR M2840
         L     REG1,L24(REG13)             PASSED WHEN INVOKED    M2840
         SR    REG3,REG3               CLEAR WORK REGISTER        M2840
         C     REG3,0(REG1)            ARE ANY OPTIONS SPECIFIED
         BE    NOOPT                   BRANCH IF NO OPTIONS       M2840
         L     REG2,0(REG1)            PICK UP POINTER TO OPTION
*                                         PARAMETER LIST
         CLC   0(TWO,REG2),ZERO        IS LIST LENGTH ZERO?       M2840
         BC    8,NOOPT                 BRANCH IF LENGTH IS ZERO
*                                                                 M2840
*                  THE OPTIONADDR PARAMETER WAS SPECIFIED.  ITS   M2840
*                     LENGTH MUST BE 10 BYTES, THE FIRST 8 BYTES  M2840
*                     MUST CONTAIN 'LINECNT=', AND THE LAST TWO   M2840
*                     BYTES MUST BE NUMERIC.                      M2840
*                                                                 M2840
         LH    REG3,0(REG2)            LENGTH OF OPT PARM LIST    M2840
         CH    REG3,MAXCT              LENGTH OF LINECNT PARM=10? M2840
         BNE   MESSAGE1                BRANCH IF INVALID LENGTH   M2840
LNCNT    CLC   TWO(L'LINCT,REG2),LINCT IS PARAMETER 'LINECNT='    M2840
         BNE   MESSAGE1                BRANCH IF INVALID PARM     M2840
FOUND    XC    HERE(LNG8),HERE         CLEAR USED WORK AREA       M2840
         MVC   HERE(TWO),10(REG2)      MOVE IN LINECNT         @ZA01680
         NC    HERE+ONE1(ONE1),HERE                              S20038
         TM    HERE+ONE1,FOXZERO       TEST FOR NUMERICS         S20038
         BNO   MESSAGE1                BRANCH IF NOT NUMERICS    S20038
         XC    HERE(TWO),HERE          RESTORE AREA              S20038
         PACK  HERE+DSP6(TWO),10(TWO,REG2)  GET LINES          @ZA01680
         CVB   STWREG,HERE             CONVERT LINES TO BINARY    M2840
         B     LASTP                   GO STORE PASSED LINE COUNT M2840
MESSAGE1 OI    PARMERR,BIT7            SET LINECOUNT ERROR FLAG   M2840
NOOPT    LA    STWREG,ABS60            SET DEFAULT LINES TO 60    M2840
LASTP    STH   STWREG,LINETOT          TOTAL LINES PER PAGE       M2840
         STH   STWREG,LINELEFT         LINES LEFT YET TO PRINT    M2840
         TM    0(REG1),ENDLIST         TEST FOR LAST PTR IN LIST  M2840
         BO    NONAMES                 BRANCH IF LAST             M2840
*                                                                 M2840
*                  THE DDNAMEADDR PARAMETER WAS SPECIFIED.  CHECK M2840
*                     ENTRIES 5 AND 6 FOR ALTERNATE NAMES FOR     M2840
*                     SYSIN AND SYSPRINT RESPECTIVELY.  IF        M2840
*                     ALTERNATE NAMES ARE PRESENT, PUT THOSE      M2840
*                     NAMES INTO THE READ AND PRINT DCB'S.        M2840
*                                                                 M2840
NAME     L     REG2,DSP4(REG1)         PICK UP POINTER TO DDNAMES M2840
         CLC   0(L'FIVE,REG2),FIVE     IS EITHER SYSIN OR         M2840
*                                         SYSPRINT SPECIFIED      M2840
         BL    NONAMES                 BRANCH, NEITHER SPECIFIED  M2840
         CLC   D34(LNG8,REG2),ZERO     POSITION BINARY ZEROS?     M2840
         BE    NOINNAME                BRANCH IF NOT SPECIFIED    M2840
         MVC   RDDNAME,D34(REG2)       FILL DDNAME IN DCB      @ZA01744
PRNAME   CLC   0(L'SIX,REG2),SIX       LNGTH PRESENT FOR SYSPRINT M2840
         BC    4,NOPRNAME              NO, USE DEFAULT NAME       M2840
         CLC   D42(LNG8,REG2),ZERO     SUBSTITUTE NAME SPECIFIED? M2840
         BE    NOPRNAME                NO, USE DEFAULT NAME       M2840
         MVC   PDDNAME,D42(REG2)     YES, MOVE DDNAME INTO DCB @ZA01744
         B     LASTDDNM                GO BEGIN CHECK FOR PAGE    M2840
NOINNAME MVC   RDDNAME,SYSIN           FILL IN DEFAULT FOR SYSIN  M2840
         B     PRNAME                  SEE IF NAME FOR SYSPRINT   M2840
NONAMES  MVC   RDDNAME,SYSIN           FILL IN DEFAULT FOR SYSIN  M2840
NOPRNAME MVC   PDDNAME,SYSPRINT        FILL DEFAULT FOR SYSPRINT  M2840
*                                                                 M2840
*                  PICK UP PAGE NUMBER IF HDINGADDR SPECIFIED.    M2840
*                     IF NOT SPECIFIED, START WITH PAGE 1.        M2840
*                                                                 M2840
LASTDDNM TM    0(REG1),ENDLIST         MORE OPTIONS SPECIFIED?    M2840
         BO    DFALT                   NO MORE POINTERS           M2840
         TM    DSP4(REG1),ENDLIST      MORE OPTIONS SPECIFIED?    M2840
         BO    DFALT                   NO MORE POINTERS           M2840
PAGE     L     REG2,DSP8(REG1)         POINT AT PAGE NUMBER AREA  M2840
         CLC   0(TWO,REG2),ZERO        IS PAGE NUMBER SPECIFIED   M2840
         BE    DFALT                   NO PAGE NUM, TAKE DEFAULT  M2840
CVT      XC    HERE(LNG8),HERE         CLEAR USED PART OF WORKA   M2840
         MVC   HERE(LNG4),TWO(REG2)    MOVE IN PAGECNT           S20038
         NC    HERE+ONE1(LNG3),HERE                              S20038
         TM    HERE+LNG3,FOXZERO       TEST FOR NUMERICS          M2840
         BNO   MESSAGE2                BRANCH IF NOT NUM         S20038
         CLC   LNG0(TWO,REG2),FOUR1    PAGECNT CORRECT LENGTH?   S20038
         BNE   MESSAGE2                BRANCH IF INCORRECT       S20038
         XC    HERE(LNG4),HERE         RESTORE AREA              S20038
         PACK  HERE+DSP5(LNG3),TWO(LNG4,REG2)                     M2840
         CVB   STWREG,HERE             CONVERT PAGE NO TO BINARY  M2840
         B     OUT                     GO STORE PAGE NUMBER       M2840
MESSAGE2 OI    PARMERR,BIT7            SET PAGECOUNT ERROR FLAG   M2840
DFALT    LA    STWREG,ABS1             DEFAULT OPTION IS PAGE ONE M2840
OUT      STH   STWREG,PAGENUM          SAVE STARTING PAGE NUMBER  M2840
         EJECT
*                                                                 M2840
*                  OPEN SYSIN AND SYSPRINT (OR SUBSTITUTES)       M2840
*                                                                 M2840
         LA    REG13,SAVE              PTR TO IEHINITT SAVE AREA  M2840
         MVI   DFLG,NULL               INITIALIZE ERROR FLAGS     M2840
         MVI   DFLAG,NULL                                         M2840
         OPEN  MF=(E,PRINTAD)          EXECUTE OPEN CODE STORED IN WORK
*                                         AREA FOR PRINTER
         LA    REG13,SAVE              PTR TO IEHINITT SAVE AREA  M2840
         OPEN  MF=(E,READAD)           EXECUTE OPEN CODE STORED IN WORK
*                                         AREA FOR READER
         LA    STWREG,JFCBLST          MOVE EXLST ADDR INTO DCB   M2840
         ST    STWREG,JFLSTAD                                     M2840
         MVC   GO+ONE1(LNG3),JFLSTAD+ONE1 MOVE EXLST TO SAVE AREA M2840
*                                                                 M2840
*                   CALL IEHPRNT TO PRINT FIRST PAGE HEADING      M2840
*                                                                 M2840
         LA    REG13,SAVE              PTR TO IEHINITT SAVE AREA  M2840
         LA    REG12,COMTAB            PTR TO WORK AREA           M2840
         LINK  EP=IEHPRNT              GO PRINT HEADING           M2840
         C     REG15,ZERO              IS RETURN CODE ZERO        M2840
         BNE   RETURN                  BRANCH, CODE NOT ZERO      M2840
         TM    PARMERR,BIT7            TEST FOR LINECNT OR        M2840
*                                      PAGECNT ERROR             S20038
         BO    MESSAGE                 BRANCH IF ERROR           S20038
SKIPRINT TM    ROPENFLG,BADOPEN        OPEN READ DCB SUCCESFUL    M2840
         BZ    BAD                     BRANCH IF NO               M2840
         TM    DFLAG,BADOPEN           SYSIN BLKSIZE ERROR FLAG   M2840
         BO    BAD                     BRANCH IF ERROR            M2840
         MVI   RC,NULL                 INITIALIZE RETURN CODE     M2840
         EJECT
REDO     MVI   LABEL,BLANKCOL          INITIALIZE LABEL TO BLANKS M2840
         MVC   LABEL+ONE1(L79),LABEL   CLEAR REMAINDER OF LABEL   M2840
         MVC   LABEL(L'VLM),VLM        MOVE CONSTANT 'VOL1'       M2840
         MVI   NUMTPS,BIT7             DEFAULT OPTION FOR NUMBER  M2840
         NI    KEY,NULL                TURN OFF ALL KEYWORD BITS  M2840
         NI    SWITCHAL,NULL           CLEAR ASCII LABEL SWITCH  S20038
         OC    SVCLST(L'UNLD),UNLD     DET DEFAULT TO UNLOAD      M2840
         LA    STWREG,HOMEJFCB         PICK UP ADDR OF JFCB AREA  M2840
         ST    STWREG,JFCBLST          SAVE IT FOR JFCB READ      M2840
         MVI   JFCBLST,ENDJFCB         SET END-OF-LIST, JFCB      M2840
         L     STWREG,ABS16            PICK UP CVT ADDRESS        M2840
         L     REG5,0(STWREG)          PICK UP DOUBLE WORD        M2840
*                                         CONTAINING TCB ADDRESS  M2840
         L     STWREG,DSP4(REG5)       PICK UP TCB ADDRESS        M2840
         ST    STWREG,TCB              SAVE TCB PTR JUST FOUND    M2840
         MVI   SWITCHRD,NULL           CLEAR SWITCHRD INDICATING  M2840
*                                         FIRST ENTRY             M2840
         EJECT
SCAN1    LA    REG13,SAVE              PTR TO IEHINITT SAVE AREA  M2840
         LA    REG12,WORK              PTR TO COMMON WORK AREA    M2840
SCAN     LINK  EP=IEHSCAN
*
*                  THE FOLLOWING ROUTINE CHECKS FOR SUCCESS OF THE
*                     SCAN AND ALSO CHECKS TO SEE IF A DDNAME,COMMAND
*                     WORD, KEYWORD OR PARAMETER IS RETURNED
*
         TM    DISP11,BIT7             SYNAD ENTERED IN IEHSCAN   M2840
         BO    BAD                     YES,BR.                     9837
         TM    SWITCHRD,BIT0           IS ERROR BIT ON            M2840
         BNO   DNAM                    NO, SEE WHAT WAS PASSED    M2840
ERR      BAL   REG14,ERRMSG            ERROR MESSAGE SET UP       M2840
         MVC   MSGTEXT(L'ERR1),ERR1    INVALID STATEMENT MSG TEXT M2840
ERRPTR   MVI   RC,RCODE08              SET RETURN CODE TO 8       M2840
         LINK  EP=IEHPRNT
         C     REG15,ZERO              IS RETURN CODE ZERO        M2840
         BNE   RETURN                  RETURN CODE NOT ZERO       M2840
NEXT     TM    SWITCHRD,BIT2           IS END OF FILE BIT ON
         BO    RETURN                  BRANCH ON YES              M2840
         TM    SWITCHRD,BIT1           IS BYPASS BIT ON           M2840
         BO    REDO                    YES, SET UP FOR NEXT GROUP M2840
         B     SCAN1                   JUST READ AND PRINT CARDS  M2840
         EJECT
*                                                                 M2840
*                  IEHSCAN SAVES THREE VALUES IN THE COMMON WORK  M2840
*                     AREA BEFORE RETURNING TO IEHINITT.  THESE   M2840
*                     VALUES ARE:                                 M2840
*                        PARMPTR - THE ADDRESS OF THE FIRST       M2840
*                                  POSITION IN CARD FOR THE VALUE M2840
*                                  PASSED.                        M2840
*                        LNGPARM - THE LENGTH OF THE VALUE PASSED M2840
*                        DISP12  - THE ADDRESS OF THE LAST        M2840
*                                  POSITION SCANNED               M2840
*                                                                 M2840
DNAM     L     REG1,PARMPTR            STARTING COLUMN            M2840
         L     REG2,LNGPARM            LENGTH OF SCAN             M2840
         L     REG3,DISP12             LAST COLUMN SCANNED        M2840
         TM    DISP10,BIT5             TEST FOR DDNAME BIT ON     M2840
         BC    NO,COMDWRD              SEE IF COMMAND WORD        M2840
         MVI   DDNAM,BLANKCOL          CLEAR DDNAME FIELD         M2840
         MVC   DDNAM+ONE1(LNG7),DDNAM  CLEAR DDNAME FIELD         M2840
         C     REG2,EIGHT              DDNAME LONGER THAN 8       M2840
         BH    ERR                     YES, ERROR                 M2840
         BCTR  REG2,REG0               DECREASE LENGTH BY 1       M2840
         EX    REG2,DDMVE              MOVE DDNAME TO SAVE AREA   M2840
         CLC   DDNAM,LASTDD            IS DDNAM SAME AS BEFORE?   M2840
         BE    THRU                    IF SO, DON'T REINITIALIZE  M2840
         SR    UCBSEQ,UCBSEQ           IF NOT, DO REINITIALIZE    M2840
         MVC   LASTDD,DDNAM            SAVE NEW 'DDNAM'.          M2840
         B     THRU                    GO HOUSEKEEP               M2840
COMDWRD  TM    SWITCHRD,BIT4           TEST FOR COMMAND WORD BIT
         BZ    KEYWRD                  NOT COMMAND WORD, BRANCH   M2840
CHKCOM   CLC   INITT,0(REG1)           IS COMMAND WORD CORRECT    M2840
         BE    THRU                    THE OPERATION IS 'INITT'   M2840
         OI    SWITCHRD,BIT1           SET ON BYPASS SWITCH
         B     ERR                     BAD COMMAND WORD, ERROR    M2840
         EJECT
KEYWRD   TM    SWITCHRD,BIT5           TEST FOR KEYWORD BIT       M2840
         BNO   PARM                    NOT KEYWORD, BRANCH        M2840
*                                                                 M2840
*                  THE FOLLOWING FIVE TESTS (SERL, DISPL, OWNR,   M2840
*                     NUMBTAPE, AND LABTYP) TEST FOR VALID        M2840
*                     KEYWORDS.  IF THE KEYWORD IS NOT ONE OF THE M2840
*                     SPECIFIED KEYWORDS, A BRANCH IS MADE TO     M2840
*                     'WRONG' TO SET UP 'INVALID KEYWORD' ERROR   M2840
*                     MESSAGE.                                    M2840
*                                                                 M2840
SERL     CLC   SER,0(REG1)             IS KEYWORD SERIAL NUMBER?  M2840
         BNE   DISPL                   NO, KEYWORD NOT SERIAL NO. M2840
         OI    KEY,BIT0                TURN ON SERIAL NUMBER BIT
         B     SCAN1                   RETURN TO SCAN FOR PARM    M2840
DISPL    CLC   DISP,0(REG1)            IS KEYWORD DISPOSITION     M2840
         BNE   OWNR                    NO, KEYWORD NOT DISP       M2840
         OI    KEY,BIT1                TURN ON DISP BIT
         B     SCAN1                   RETURN TO SCAN FOR PARM    M2840
OWNR     CLC   OWNRN,0(REG1)           IS KEYWORD OWNER?          M2840
         BC    7,NUMBTAP               BRANCH ON NO TO NEXT CHK  S20038
         OI    KEY,BIT2                TURN ON OWNER NAME BIT
         B     SCAN1                   RETURN TO SCAN FOR PARM    M2840
NUMBTAP  CLC   NUMBTAPE,0(REG1)        KEYWORD NUMBTAPE?          M2840
         BC    7,LABTYP                IF NO,BRANCH TO NEXT CHK  S20038
         OI    KEY,BIT4                TURN ON NUMBTAPE BIT
         B     SCAN1                   RETURN TO SCAN FOR PARM    M2840
LABTYP   CLC   LABTYPE,0(REG1)         IS KEYWORD LABTYPE?        M2840
         BC    7,WRONG                 IF NO,BRANCH TO ERROR RTN S20038
         OI    KEY,BIT6                TURN ON LABTYPE BIT       S20038
         B     SCAN1                   RETURN TO SCAN FOR PARM    M2840
WRONG    BAL   REG14,ERRMSG            ERROR MESSAGE SET UP       M2840
         MVC   MSGTEXT(L'ERR2),ERR2    INVALID KEYWORD MSG TEXT   M2840
         OI    SWITCHRD,BIT1           SET ON BYPASS SWITCH
         B     ERRPTR                  GO PRINT ERROR MSG         M2840
         EJECT
PARM     TM    SWITCHRD,BIT6           IS THIS A PARAMETER
         BO    CHECK                   YES, HANDLE AS PARAMETER   M2840
         TM    SWITCHRD,BIT1           TEST BYPASS BIT            M2840
         BC    1,COL72B                BRANCH IF ON               M2840
         B     EOF                     GO SET UP FOR NEXT GROUP   M2840
*                                                                 M2840
*                  THE FOLLOWING FIVE TESTS (CHECK, CHKDISP,      M2840
*                     CHKOWNR, CHKNUMTP, AND CHKLAB) TEST FOR     M2840
*                     VALID PARAMETERS.  IF THE PARAMETER DOES    M2840
*                     NOT MEET THE SPECIFIED REQUIREMENTS, A      M2840
*                     BRANCH IS MADE TO 'BADPARM' TO SET UP       M2840
*                     'INVALID PARAMETER VALUE' ERROR MESSAGE     M2840
*                                                                 M2840
CHECK    TM    KEY,BIT0                IS SER # BIT ON
         BC    8,CHKDISP               IF NO BRANCH
         STM   REG1,REG3,MYSAVE        SAVE ALL REGISTERS         M2840
         LR    REG3,REG1               REG3 CONTAINS ADDR OF I/P  M2840
         TRT   0(LNG7,REG3),TABLE      TEST LEGAL CHARACTERS      M2840
         S     REG1,MYSAVE             NO. OF CHARACTERS SEARCHED M2840
         C     REG1,MYSAVE+DSP4        NO OF CHARACTERS CHANGED?  M2840
         BNE   BADPARM                 YES, GO TO ERROR ROUTINE  S20038
         LM    REG1,REG3,MYSAVE        RESTORE SAVED REGISTERS    M2840
         MVI   SERIAL,BLANKCOL         CLEAR FIRST BYTE OF SERIAL M2840
         MVC   SERIAL+ONE1(LNG5),SERIAL CLEAR REMAINDER OF SERIAL M2840
         LA    STWREG,L'SERIAL         GET LENGTH OF SERIAL       M2840
         CR    REG2,STWREG             IS PARAMETER TOO LONG      M2840
         BH    BADPARM                 YES, SET UP PARAMETER ERR  M2840
         BCTR  REG2,REG0               DECREASE LENGTH BY 1       M2840
         EX    REG2,MOVSER             MOVE SERIAL TO LABEL AREA  M2840
         B     THRU                    VALID SERIAL PARM, CONT.   M2840
CHKDISP  TM    KEY,BIT1                IS DISP BIT ON
         BC    8,CHKOWNR               IF NO BRANCH
         CLC   REWIND,0(REG1)          IS THE DISP REWIND         M2840
         BNE   THRU                    NO, ASSUME REWIND-UNLOAD   M2840
RE       MVC   SVCLST(L'RWND),RWND     SET FOR REWIND OPTION      M2840
         B     THRU                    VALID DISP. PARM, CONTINUE M2840
CHKOWNR  TM    KEY,BIT2                IS OWNER NAME BIT ON
         BC    8,CHKNUMTP              BRANCH ON NO              S20038
         LTR   REG2,REG2               IS PARM LENGTH EQ ZERO    S20038
         BZ    BADPARM                 BRANCH IF ZERO            S20038
         C     REG2,FOURTEEN           IS OWNER NAME G.T. 14     S20038
         BH    BADPARM                 BRANCH ON YES             S20038
         OI    SWITCHAL,OWNRSW         FLAG OWNERID SPECIFIED    S20038
         ST    REG2,OWNRLNG            SAVE LENGTH FOR LATER CHK S20038
         BCTR  REG2,REG0               DECREASE LENGTH BY 1       M2840
         EX    REG2,ONAMHOLD           HOLD OWNER FOR LATER CHK  S20038
         B     THRU                    VALID OWNER PARM, CONT     M2840
*                                                                 M2840
*                  THE NUMBER OF TAPES CAN RANGE FROM 1 TO 3      M2840
*                     BYTES (1 TO 255 TAPES) AND THE LOCATION FOR M2840
*                     THE PACK INSTRUCTION MUST VARY ACCORDINGLY  M2840
*                                                                 M2840
CHKNUMTP TM    KEY,BIT4                IS NUMBTAPE BIT ON
         BZ    CHKLAB                  NOT NUMBTAPE PARM, BRANCH  M2840
         C     REG2,FTHREE             MORE THAN THREE DIGITS     M2840
         BH    BADPARM                 YES, SET UP PARAMETER ERR  M2840
         XC    THERE(LNG3),THERE       CLEAR AREA FOR NUMB TAPES  M2840
         LA    STWREG,THERE+TWO        POINT AT LOW-ORDER BYTE    M2840
         BCTR  REG2,REG0               DECREASE PASSED LNGTH BY 1 M2840
         SR    STWREG,REG2             IF NUMBER OF TAPES GREATER M2840
*                                         THAN SINGLE DIGIT,      M2840
*                                         ADJUST MVC START COL.   M2840
         EX    REG2,TAPENUM            MOVE PARM TO AREA 'THERE'  M2840
         XC    HERE(LNG8),HERE         CLEAR PART OF WORK AREA    M2840
         PACK  HERE+DSP6(TWO),THERE(LNG3)
         CVB   STWREG,HERE             CONVERT NO OF TAPES        M2840
         STC   STWREG,NUMTPS           STORE NUMBER OF TAPES      M2840
         NI    KEY,NULL                TURN OFF NUMBTAPE BIT      M2840
         CLI   NUMTPS,NULL             CHECK FOR NO TAPE          M2840
         BC    8,BADPARM               BRANCH ON YES             A25710
         B     THRU                    VALID NUMBTAPE PARAM, CONT M2840
CHKLAB   TM    KEY,BIT6                IS LABTYPE BIT ON         S20038
         BZ    BADPARM                 NOT LABTYPE PARM, BRANCH   M2840
         LA    STWREG,L'AL             GET LENGTH OF 'AL'         M2840
         CR    REG2,STWREG             IS PARAMETER TOO LONG      M2840
         BH    BADPARM                 YES, SET UP PARAMETER ERR  M2840
         CLC   AL(L'AL),0(REG1)        IS PARM EQUAL TO 'AL'      M2840
         BNE   BADPARM                 BRANCH ON NO              S20038
         OI    SWITCHAL,ASCIISW        FLAG ASCII LAB SPECIFIED  S20038
         B     THRU                    VALID LABTYPE PARM, CONT.  M2840
BADPARM  BAL   REG14,ERRMSG            ERROR MESSAGE SET UP       M2840
         MVC   MSGTEXT(L'ERR3),ERR3    INVALID PARAMETER TEXT     M2840
         B     ERRPTR                  GO SET UP TO CALL IEHPRNT  M2840
         EJECT
THRU     MVI   KEY,NULL                RESET KEYWORD FLAG         M2840
         TM    SWITCHRD,BIT1           IS THE BYPASS SWITCH ON
         BZ    SCAN1                   BRANCH IF NO              S20038
COL72B   MVI   SWITCHRD,NULL           CLEAR SWITCH BYTE          M2840
         CLI   SERIAL,BLANKCOL         IS SERIAL NUMBER SPECIFIED M2840
         BC    8,ERR                   BRANCH IF NO SER NUMBER IS
*                                         SPECIFIED
         TM    SWITCHAL,ASCIISW        IS ASCII LABEL SPECIFIED  S20038
         BNO   NONASCII                BRANCH ON NO              S20038
         TM    SWITCHAL,OWNRSW         IS OWNER NAME SPECIFIED   S20038
         BNO   ASCISEC                 BRANCH ON NO              S20038
         NI    SWITCHAL,OWNEROFF       TURN OFF OWNER BIT        S20038
         L     STWREG,OWNRLNG          SETUP MOVE FOR OWNER       M2840
         BCTR  STWREG,REG0             SET UP MVC LENGTH          M2840
         EX    STWREG,ONAMASCI         MOVE ASCII OWNER TO LABEL  M2840
ASCISEC  MVI   SECURITY,BLANKCOL       ASCII SECURITY DFLT=SPACE  M2840
         MVI   LABEL+DSP79,ASCIISTD    ASCII LABEL STANDARD=1    S20038
         B     TIOTPTR                 GO GET TIOT PTR            M2840
NONASCII TM    SWITCHAL,OWNRSW         IS OWNER NAME SPECIFIED   S20038
         BNO   EBCDSEC                 BRANCH ON NO              S20038
         NI    SWITCHAL,OWNEROFF       TURN OFF OWNER BIT        S20038
         CLC   OWNRLNG(L'OWNRLNG),TEN  IS OWNER NAME G.T. TEN    S20038
         BH    BADPARM                 IF YES GO TO ERROR RTN    S20038
         L     STWREG,OWNRLNG          SETUP MOVE FOR OWNER       M2840
         BCTR  STWREG,REG0             SET UP MVC LENGTH          M2840
         EX    STWREG,ONAMEBCD         MOVE EBCDIC OWNER TO LBL   M2840
EBCDSEC  MVI   SECURITY,SECURE         EBCDIC SECURITY DFLT=0     M2840
         EJECT
TIOTPTR  L     STWREG,TCB              PICK UP PTR TO THE TIOT    M2840
         L     REG5,D12(STWREG)        NOW CONTAINS PTR TO TIOT   M2840
         LA    REG5,D24(REG5)          START OF TIOT DD ENTRY     M2840
NEXTTIOT SR    REG6,REG6               CLEAR WORK REGISTER        M2840
         IC    REG6,0(REG5)            GET LENGTH BYTE IN TIOT    M2840
         LTR   REG6,REG6               END OF TIOT
         BC    8,ERR                   NO MATCHING DDNAME, ERROR  M2840
         CLC   DSP4(LNG8,REG5),DDNAM   RIGHT DD NAME              M2840
         BE    DDTIOT                  BRANCH IF YES              M2840
         AR    REG5,REG6               GET NEXT DDNAME            M2840
         B     NEXTTIOT                GO TEST NEXT DDNAME        M2840
DDTIOT   S     REG6,SIX10              SUB LENGTH OF DD ENTRY(16) M2840
         SRL   REG6,2                  DIVIDE BY 4                M2840
         STC   REG6,UCBNUM
         A     REG5,SIX10              POINT TO FIRST UCB POINTER
         SLL   REG6,2                                            A23445
         EX    REG6,MVEUCB             MOVE UCB PTR INTO PROG    A23445
         L     REG5,0(REG5)            PICK UP UCB POINTER
         ST    REG5,TCB                SAVE UCB POINTER           M2840
         SR    REG4,REG4               USE REG 4 TO KEEP COUNT OF BAD
*                                         UCB POINTERS
         SR    REG6,REG6               USE REG 6 TO KEEP COUNT OF THE
         IC    REG6,UCBNUM                TOTAL NUMBER OF UCB POINTERS
         LA    REG5,UCBAREA            POINT TO UCB PTRS
         EJECT
*                                      THIS CODE READS THE JFCB  A46764
*                                      TO DETERMIN IF A          A46764
*                                      VALID DENSITY WAS         A46764
*                                      SPECIFIED OR A DEFAULT    A46764
*                                      IS TO BE TAKEN            A46764
VALRJFCB LA    REG13,GPSAVE            READ JFCB TO DETERMIN DEN A46764
         MVI   GO+12,BIT6              SET DCB TO LOOK UNOPENED   M2840
         XC    GO+13(3),GO+13          ZERO DCBIFLG AND DCBMACR  A46764
         RDJFCB MF=(E,AGO)                                       A46764
         L     REG1,TCB                POINT TO UCB              A46764
         USING UCB,REG1                                          A46764
         LA    REG2,HOMEJFCB           POINT TO JFCB             A46764
         USING JFCB,REG2                                         A46764
*                                      THIS CODE IS USED TO      A46764
*                                      INITIALIZE FLAGS          A46764
         MVI   VALCOMP,BIT0+BIT1+BIT3  INIT DENSITY WORK AREA    Y02128
         MVI   VALFLGS,NULL            CLEAR VALID WORK FLAGS     M2840
*                                      VALFLGS BITS 0-5   UNUSED A46764
*                                      X'02'   6 DEFAULTING      A46764
*                                      X'01'   7 DEN IS VALID    A46764
         TM    JFCDEN,BIT6+BIT7        WILL I DEFAULT ?           M2840
         BO    VALNODEF                B IF A DEN IS SPECIFIED   A46764
         OI    VALFLGS,BIT6+BIT7       SET DEFAULTING AND NOMSG   M2840
VALNODEF NC    VALCOMP,JFCDEN          VALDEN=DENSITY BITS ONLY   M2840
*                                      BRANCH TABLE DETERMINES   A46764
*                                      PATH FOR DRIVE            A46764
*                                      SELECTED                  A46764
         TM    UCBTBYT2,UCB2OPT0+UCB2OPT1  IS IT 7 TRK ?          M2840
         BNZ   VAL7TRK                 B IF 7 TRK                A46764
         OI    SWITCHAL,ASCIMODE       ASCI IS VALID FOR 9 TRK   A46764
         TM    UCBTBYT2,UCB2OPT2       IS IT DUAL DENSITY ?       M2840
         BO    VALDDEN                 B IF DUAL DEN             A46764
         TM    UCBTBYT2,X'10'          IS IT 1600/6250 BPI?      Y02128
         BO    VAL6250                 BRANCH IF YES             Y02128
         TM    UCBTYP,UCB1FEA5         IS IT 1600 DEN?           Y02128
         BO    VALPE                   B IF 1600                 Y02128
         TM    UCBTYP,X'02'            IS IT 6250 BPI?           Y02128
         BZ    VAL9ONLY                BRANCH IF NOT 6250        Y02128
         MVI   SVCLST+15,X'D3'         SET 6250 BPI              Y02128
         CLI   VALCOMP,X'D0'           WAS 6250 SPECIFIED?       Y02128
         BE    VALID                   B IF 6250 SPECIFIED       Y02128
         B     VALCK                   SEE IF I DEFAULTED        Y02128
*                                      THIS CODE FOR 9 TRK ONLY  A46764
VAL9ONLY MVI   SVCLST+15,X'AB'         MOVE CONST. FOR 9 TRK     A46764
         CLI   VALCOMP,BIT0            WAS 800 SPECIFIED ?        M2840
         BE    VALID                   B IF YES                  A46764
         B     VALCK                   SEE IF I DEFAULTED        A46764
*                                      THIS CODE FOR DUEL DEN    A46764
VALDDEN  MVI   SVCLST+15,X'CB'         SET 800 BPI               A46764
         CLI   VALCOMP,BIT0            WAS 800 SPECIFIED ?       A46764
         BE    VALID                   B IF IT WAS               A46764
*                                      THIS IS ALSO FOR P.E.     A46764
VALPE    MVI   SVCLST+15,X'C3'         SET 1600 BPI              A46764
         CLI   VALCOMP,BIT0+BIT1       WAS 1600 SPECIFIED ?       M2840
         BE    VALID                   B IF YES                  A46764
         B     VALCK                   SEE IF I DEFAULTED        A46764
VAL6250  MVI   SVCLST+15,X'D3'         SET 6250 BPI              Y02128
         CLI   VALCOMP,X'D0'           WAS 6250 SPECIFIED?       Y02128
         BE    VALID                   B IF 6250 SPECIFIED.      Y02128
         CLI   VALCOMP,X'C0'           WAS 1600 SPECIFIED?       Y02128
         BNE   VALCK                   IF NOT, SEE IF DEFAULT    Y02128
         MVI   SVCLST+15,X'C3'         ELSE SET 1600 BPI         Y02128
         B     VALID                                             Y02128
*                                      THIS IS THE 7 TRK CODE    A46764
VAL7TRK  MVI   SVCLST+15,X'2B'         SET 7 TRK MASK            A46764
         CLI   VALCOMP,BIT0+BIT1       INVALID 1600 SPECIFIED ?   M2840
         BE    VAL800                  B IF YES AND DEFAULT      A46764
         OI    VALFLGS,BIT7            SET VALID SWITCH           M2840
         TM    VALFLGS,BIT6            DO I DEFAULT ?             M2840
         BO    VAL800                  IF SO GO DEFAULT          A46764
         OC    SVCLST+15(1),VALCOMP    SET SPECIFIED DENSITY     A46764
         B     VALCK                   SEE IF ERROR MSG NEEDED   A46764
VAL800   OI    SVCLST+15,BIT0          SET DEN 800                M2840
*                                      THIS IS THE MSG RTN FOR   A46764
*                                      INVALID DENSITY REQUESTS  A46764
VALCK    TM    VALFLGS,BIT7            VALID REQUEST ?            M2840
         BO    VALID                   SKIP THE ERROR MSG        A46764
         MVI   RC,RCODE08              SET RETURN CODE TO 8       M2840
         BAL   REG14,ERRMSG            ERROR MSG SET UP          A46764
         MVC   MSGTEXT(L'ERRB+L'ERRBC),ERRB    MOVE IN MSG        M2840
         LINK  EP=IEHPRNT              LINK TO PRINT RTN         A46764
VALID    EQU   *                                                 A46764
         EJECT
DROP     LA    REG1,0(REG1)
         DROP  REG1
         DROP  REG2
         TM    SWITCHAL,ASCIISW        IS ASCII LABEL SPECIFIED  S20038
         BNO   UCBLOOP                 BRANCH ON NO              S20038
         TM    SWITCHAL,ASCIMODE       IS VALID TAPE MODE SET    S20038
         BO    TURNOFF                 BRANCH ON YES             S20038
ASCIIERR BAL   REG14,ERRMSG            ERROR MESSAGE              M2840
         MVC   MSGTEXT(L'ERR9+L'ERR9C),ERR9                       M2840
         B     ERRPTR                  GO PRINT MESSAGE           M2840
TURNOFF  NI    SWITCHAL,MODEOFF        TURN OFF ASCII MODE SET   S20038
         SPACE 3
UCBLOOP  CR    UCBSEQ,REG6             IS THIS THE LAST UCB PTR   M2840
         BC    4,CONT                  BRANCH ON NO               M2840
         SR    UCBSEQ,UCBSEQ           REINITIALIZE TO FIRST UCB  M2840
         LA    REG5,UCBAREA            REINITIALIZE              A23445
CONT     LR    REG8,UCBSEQ             HAS THIS UCB BEEN ZEROED   M2840
         SLL   REG8,2                     ZEROED OUT
         AR    REG8,REG5                                          10432
         MVC   TCB(LNG4),0(REG8)       SAVE CURRENT UCB PTR       M2840
         CLC   0(LNG4,REG8),ZERO       IS CURRENT POINTER ZERO    M2840
         BC    7,DUMMYBLD              BRANCH IF NOT ZERO        S20038
         LA    UCBSEQ,BUMP1(I0,UCBSEQ) POINT TO NEXT UCB          M2840
         LA    REG4,BUMP1(REG4)        HAVE ALL UCBS              M2840
         CR    REG4,REG6                  BEEN ZEROED OUT
         BC    10,ALLBAD                  BRANCH IF ALL BAD
         B     UCBLOOP                 EXAMINE NEXT UCB           M2840
         EJECT
DUMMYBLD MVC   DUMMYHDR(L'HEADER1),HEADER1  CREATE DUMMY HDR LBL S20038
         MVC   DUMMYHDR+5(75),DUMMYHDR+4 PROPAGATE EBCDIC ZEROES S20038
         MVC   HERE(L'HERE),LABEL      HOLD VOL ID AND SERIAL NO.S20038
         TM    SWITCHAL,ASCIISW        IS ASCII LABEL SPECIFIED  S20038
         BNO   FILL                    BRANCH IF NO              S20038
         MVI   DUMMYHDR+53,BLANKCOL    BLANK IN ACCESS. BYTE     A38606
         LA    REG0,L'LABEL+L'DUMMYHDR NO.OF BYTES TO BE XLATED  S20038
         XLATE LABEL,(0),TO=A          TRANSLATE LABEL TO ASCII  S20038
FILL     STC   UCBSEQ,SVCLST+3         INSERT UCB SEQUENCE NUMBER M2840
         LA    REG1,DDNAM              GET ADDR OF DDNAME         M2840
         ST    REG1,SVCLST+4           MAKE IT 2ND WORD, SVCLIST
         LA    REG1,HERE               PASS ADDR OF EBCDIC       S20038
         ST    REG1,SVCLST+16          VOL ID AND SERIAL NO.     S20038
         LA    REG1,SVCLST
         OC    D12(LNG4,REG1),L80      SET LENGTH EQUAL TO 80 BYTES AND
*                                         NUMBER OF LABELS TO ONE
         LA    REG1,LABEL              PUT ADDRESS OF TAPE LABEL INTO
         ST    REG1,SVCLST+8              PARAMETER LIST FOR SVC
         L     REG4,ZERO                GOOD UCB SO INITIALIZE COUNTER
*                                          TO ZERO  AND START COUNTING
*                                          BAD UCB'S AGAIN
         LABEL MF=(E,SVCLST)           TAPE LABEL MACRO          S20038
         TM    SWITCHAL,ASCIISW        WAS ASCII LAB SPECIFIED   S20038
         BNO   OUTCOME                 BRANCH ON NO              S20038
         ST    REG15,HERE              SAVE SVC 39 RETURN CODE   S20038
         LA    REG0,L'LABEL            NO.OF BYTES TO BE XLATED  S20038
         XLATE LABEL,(0),TO=E          RESTORE EBCDIC FOR PRINTG S20038
         L     REG15,HERE              RESTORE SVC 39 RET. CODE  S20038
OUTCOME  C     REG15,EIGHT             UNACCEPTABLE PARM LIST    S20038
         BC    7,OPRATOR                  SYSTEM ERROR
         BAL   REG14,ERRMSG            ERROR MESSAGE             S20038
         MVC   MSGTEXT(L'ERR5),ERR5    MOVE IN ERROR MESSAGE      M2840
         L     REG3,TCB                GET DEVICE NAME FROM UCB   M2840
         USING UCB,REG3                                           M2840
         MVC   MSGWTR+L'ERR5(L'UCBNAME),UCBNAME                   M2840
         DROP  REG3                                               M2840
         B     PERIO2                  GO PRINT ERROR MESSAGE     M2840
ALLBAD   MVI   RC,RCODE08              SET RETURN CODE TO 8       M2840
         BAL   REG14,ERRMSG            ERROR MESSAGE             S20038
         MVC   MSGTEXT(L'ERR7),ERR7    UNUSEABLE                  M2840
         LINK  EP=IEHPRNT
         B     EOF                     SEE IF ANOTHER LABEL SET   M2840
OPRATOR  C     REG15,FOUR              OPERATOR COULD NOT MOUNT TAPE
         BC    7,PERIO                 RETURN CODE NOT 4, BRANCH  M2840
         MVI   RC,RCODE08              SET RETURN CODE TO 8       M2840
         BAL   REG14,ERRMSG            ERROR MESSAGE             S20038
         MVC   MSGTEXT(L'ERR4),ERR4    OPERATOR SUPPRESSED VOL    M2840
         MVC   MSGTEXT+L'ERR4(L'SERIAL),SERIAL     LABEL       @ZA04374
         LINK  EP=IEHPRNT
         C     REG15,ZERO              ERROR IN IEHPRNT           M2840
         BC    7,RETURN                YES, GO END JOB            M2840
         B     PAST1                   NO, SET UP FOR NEXT LABEL  M2840
GOOD     BAL   REG14,ERRMSG            MESSAGE AREA SET UP        M2840
         MVC   MSGTEXT(L'LABEL),LABEL  PRINT LABEL JUST WRITTEN   M2840
         LINK  EP=IEHPRNT
PAST1    LA    UCBSEQ,BUMP1(I0,UCBSEQ) INCREMENT UCB COUNTER.     M2840
         SR    STWREG,STWREG           CLEAR WORK REG             M2840
         IC    STWREG,NUMTPS           TAPES LEFT TO LABEL        M2840
         BCT   STWREG,AGAIN            IF MORE TAPES TO LABEL FOR M2840
*                                         THIS CONTROL CARD, THEN M2840
*                                         BRANCH AND SAVE NUMTPS. M2840
         B     EOF                     IF NOT, GO TO EOF.         M2840
PERIO    C     REG15,C                 IS THIS A PERM IO ERROR    M2840
         BC    6,GOOD                  BRANCH IF NO               M2840
         BAL   REG14,ERRMSG            ERROR MESSAGE             S20038
         MVC   MSGTEXT(L'ERR6),ERR6    PERM I/O ERROR MSG TEXT    M2840
         L     REG3,TCB                GET DEVICE NAME FROM UCB   M2840
         USING UCB,REG3                                           M2840
         MVC   MSGTEXT+L'ERR6(L'UCBNAME),UCBNAME                  M2840
         DROP  REG3                                               M2840
PERIO2   MVC   0(LNG4,REG8),ZERO       ZERO OUT UCB               M2840
         MVI   RC,RCODE08              SET UP RETURN CODE OF 8    M2840
         LINK  EP=IEHPRNT              GO PRINT ERROR MESSAGE     M2840
         C     REG15,ZERO              ERROR IN IEHPRNT           M2840
         BC    7,RETURN                YES, GO END JOB            M2840
         B     UCBLOOP                 NO, SET UP FOR NEXT LABEL  M2840
AGAIN    STC   STWREG,NUMTPS           SAVE TAPES LEFT TO LABEL   M2840
         MVC   PARMKEEP(6),SERIAL      SERIAL NUMBER ALL NUMERIC  M2840
         NC    PARMKEEP+ONE1(LNG5),PARMKEEP                       M2840
         TM    PARMKEEP+5,FOXZERO      TEST FOR NUMERICS          M2840
         BNO   EOF                     BRANCH IF NOT NUMERICS    S20038
         XC    HERE(LNG8),HERE         CLEAR PART OF WORK AREA    M2840
         PACK  HERE+4(4),SERIAL        INCREMENT THE SERIAL       M2840
         CVB   STWREG,HERE                NUMBER BY ONE           M2840
         LA    STWREG,BUMP1(STWREG)                               M2840
         CVD   STWREG,HERE             SAVE INCREMENTED NUMBER    M2840
         UNPK  SERIAL,HERE+4(4)        RESTORE UPDATED SERIAL     M2840
         OI    SERIAL+5,FOXZERO        CLEAR SIGN BITS            M2840
         B     UCBLOOP                 SET UP FOR NEXT TAPE       M2840
         SPACE 3
ERRMSG   MVI   MSGTEXT,BLANKCOL        CLEAR AREA FOR MESSAGE     M2840
         MVC   MSGTEXT+1(119),MSGTEXT  TO BLANKS                  M2840
         L     REG9,CS2                BASE FOR SECOND CSECT      M2840
         LA    REG13,SAVE              IEHINITT SAVE AREA         M2840
         LA    REG12,COMTAB            PORTION OF COMMON AREA     M2840
         BR    REG14                   RETURN                    S20038
         SPACE 3
MESSAGE  BAL   REG14,ERRMSG            SET UP FOR ERROR MSG      S20038
         MVC   MSGTEXT(L'ERRA+L'ERRAC),ERRA   INVALID PARM        M2840
         B     CONTBAD                 BRANCH TO PRINT RTN       S20038
BAD      BAL   REG14,ERRMSG            SET UP FOR ERROR MSG      S20038
         MVC   MSGTEXT(L'ERR8),ERR8    TERMINATION                M2840
CONTBAD  LINK  EP=IEHPRNT                                         M2840
TERMIN   MVI   RC,RCODE16              SET RETURN CODE OF 16      M2840
         B     GOAHEAD                 GO CLOSE AND FREEMAIN      M2840
EOF      TM    SWITCHRD,BIT2           CHECK TO SEE IF THIS WAS THE
         BC    8,REDO                     LAST GROUP OF LABELS
RETURN   TM    POPENFLG,BADOPEN        PRINT DCB OPENED           M2840
         BO    GOAHEAD                 BR IF SYSPRINT IS OPEN      9837
*                                                                 M2840
*                  INCREMENT RETURN CODE BY FOUR WHEN SYSPRINT    M2840
*                     CARD IS OMITTED                             M2840
*                                                                 M2840
         SR    STWREG,STWREG           CLEAR WORK REGISTER        M2840
         IC    STWREG,RC               GET SAVED RETURN CODE      M2840
         LA    STWREG,DSP4(STWREG)     ADD 4 TO SAVED RC          M2840
         STC   STWREG,RC               RETURN CODE TO SAVE AREA   M2840
GOAHEAD  L     REG13,SAVE+4            RESTORE CALLER'S REG 13    M2840
         CLOSE MF=(E,READAD)
         CLOSE MF=(E,PRINTAD)
*                                                                 M2840
*                  IF IEHINITT INVOKED, SEE IF UPDATED PAGE       M2840
*                     TO BE RETURNED TO CALLING PROGRAM           M2840
*                                                                 M2840
         L     REG1,24(REG13)          GET CALLER'S PARM REG      M2840
         TM    0(REG1),ENDLIST         OPT. HIGH ORDER BIT ON?    M2840
         BO    SKPSTR                  PAGE NOT PASSED, BRANCH    M2840
         TM    DSP4(REG1),ENDLIST      DDNAME HIGH ORDER BIT ON?  M2840
         BO    SKPSTR                  PAGE NOT PASSED, BRANCH    M2840
         L     REG1,DSP8(REG1)         GET ADDR OF PAGE IN LIST   M2840
         LH    REG2,PAGENUM            PICK UP NEXT PAGE NUMBER   M2840
         CVD   REG2,HERE
         UNPK  TWO(LNG4,REG1),HERE+5(LNG3) UNPACK PAGE BACK TO   S20038
*                                           PARAMETER LIST       S20038
         OI    DSP5(REG1),FOXZERO      SET LOW ORDER ZONE         M2840
SKPSTR   SR    REG6,REG6               PRESERVE RETURN CODE        000C
         IC    REG6,RC
         LA    REG1,LABEL
         FREEMAIN R,LV=0+LAST-FIRST,A=(1)
         SR    REG15,REG15
         LR    REG15,REG6   SET RETURN CODE IN RETURN CODE REG.    9527
BACK     RETURN (14,12),T,RC=(15)                                 10626
         EJECT
OUTEXIT  LH    REG5,PRINT+62            LOAD SYSPRINT BLKSIZE     M2840
         LTR   REG5,REG5                BLZSIZE BINARY ZEROS      M2840
         BC    8,DCBSIZ                 BRANCH IF NONE SPECIFIED   BS0H
         LA    REG6,ABS121              TEST FOR MODULAR BLKSIZE   BS0H
         SR    REG4,REG4                CLEAR REMAINDER REG       M2840
         DR    REG4,REG6
         LTR   REG4,REG4
         BCR   8,REG14                  RETURN IF MODULAR BLKSIZE  BS0H
         MVI   DFLG,BIT3                SYSPRINT BLKSIZE ERROR    M2840
DCBSIZ   MVC   PBLKSZ,PLRECL            LRECL (121) TO BLKSIZE    M2840
         RETURN
         SPACE 3
INEXIT   LH    REG5,READ+62             LOAD SYSIN BLKSIZE         BS0H
         LTR   REG5,REG5                                           BS0H
         BC    8,DCBFIX                 BRANCH IF NONE SPECIFIED   BS0H
         LA    REG6,80                  TEST FOR MODULAR BLKSIZE   BS0H
         SR    REG4,REG4                                           BS0H
         DR    REG4,REG6                                           BS0H
         LTR   REG4,REG4                                           BS0H
         BCR   8,REG14                  RETURN IF MODULAR BLKSIZE  BS0H
         MVI   DFLAG,X'10'        SET SYSIN BLKSIZE ERROR FLAG     BS0H
         MVC   READ+62(2),READ+82       MOVE LRECL TO BLKSIZE      BS0H
         RETURN
DCBFIX   MVI   READ+63,X'50'            INSERT BLKSIZE -- 80       BS0H
         RETURN
         SPACE 3
*
*              THE FOLLOWING MOVE INSTRUCTIONS ARE EXECUTED
*
MVZO     MVC   1(0,REG1),0(REG1)        ZERO OUT WORK AREA
MVEUCB   MVC   UCBAREA(0),0(REG5)      MOVE IN UCB PTRS          A23445
TAPENUM  MVC   0(0,STWREG),0(REG1)     MOVE IN NUMBER OF TAPES    M2840
ONAMHOLD MVC   OWNRHOLD(0),0(REG1)     MOVE OWNER TO HOLD AREA   S20038
ONAMEBCD MVC   LABEL+41(0),OWNRHOLD    MOVE OWNER TO EBCDIC LAB  S20038
ONAMASCI MVC   LABEL+37(0),OWNRHOLD    MOVE OWNER TO ASCII LABEL S20038
MOVSER   MVC   SERIAL(0),0(REG1)       MOVE IN SERIAL NUMBER      M2840
DDMVE    MVC   DDNAM(0),CARD
         SPACE 3
*                                                                S20038
* REGISTER ALLOCATION, MASK AND BIT SETTINGS                     S20038
*                                                                S20038
REG0     EQU   0
REG1     EQU   1
REG2     EQU   2
REG3     EQU   3
REG4     EQU   4
REG5     EQU   5
REG6     EQU   6
REG7     EQU   7
REG8     EQU   8
UCBSEQ   EQU   7                       REGISTER FOR UCB SEQ NO    M2840
REG9     EQU   9
MSGBASE  EQU   9                       REMOVE AND MODIFY REG 9
REG10    EQU   10
GOTTEN   EQU   10                      REG TO ADDR GETMAIN AREA   M2840
REG11    EQU   11
INITBASE EQU   11                      BASE FOR IEHINITT ROOT MOD M2840
REG12    EQU   12
STWREG   EQU   12                      SHORT TIME WORK REGISTER   M2840
REG13    EQU   13
REG14    EQU   14
REG15    EQU   15
BIT0     EQU   X'80'                   MASK TO SET AND TEST BIT 0 M2840
BIT1     EQU   X'40'                   MASK TO SET AND TEST BIT 1 M2840
BIT2     EQU   X'20'                   MASK TO SET AND TEST BIT 2 M2840
BIT3     EQU   X'10'                   MASK TO SET AND TEST BIT 3 M2840
BIT4     EQU   X'08'                   MASK TO SET AND TEST BIT 4 M2840
BIT5     EQU   X'04'                   MASK TO SET AND TEST BIT 5 M2840
BIT6     EQU   X'02'                   MASK TO SET AND TEST BIT 6 M2840
BIT7     EQU   X'01'                   MASK TO SET AND TEST BIT 7 M2840
NO       EQU   8                       BRANCH CONDITION           M2840
YES      EQU   1                       BRANCH CONDITION           M2840
ASCIISW  EQU   X'80'                   ASCII LAB.PARM SPECIFIED  S20038
OWNRSW   EQU   X'40'                   OWNER PARM WAS SPECIFIED  S20038
ASCIMODE EQU   X'10'                   9 TRK TAPE MODE SETTING   S20038
OWNEROFF EQU   X'BF'                   TO SET OFF THE OWNER FLAG S20038
MODEOFF  EQU   X'EF'                   TO SET OFF ASCII MODE SET S20038
SECURE   EQU   X'F0'                   EBCDIC SECURITY DEFAULT   S20038
ASCIISTD EQU   X'F1'                   ASCII LABEL STDS MET      S20038
NULL     EQU   X'00'                   TO CLEAR A 1-BYTE SWITCH  S20038
ELEVEN   EQU   11
ONE1     EQU   1                       USED AS LENGTH & DISP     S20038
TWO      EQU   2                       LENGTH & DISPLACEMENT     S20038
DSP1     EQU   1                       DISPLACEMENT VALUE         M2840
DSP4     EQU   4                       DISPLACEMENT VALUE         M2840
DSP5     EQU   5                       DISPLACEMENT VALUE         M2840
DSP6     EQU   6                       DISPLACEMENT VALUE         M2840
DSP8     EQU   8                       DISPLACEMENT VALUE OF 8    M2840
DSP9     EQU   9                       DISPLACEMENT VALUE         M2840
D12      EQU   12                      DISPLACEMENT VALUE         M2840
D24      EQU   24                      DISPLACEMENT VALUE         M2840
D34      EQU   34                      DISPLACEMENT VALUE         M2840
D42      EQU   42                      DISPLACEMENT VALUE         M2840
DSP79    EQU   79                      DISPLACEMENT VALUE         M2840
LNG0     EQU   0                       LENGTH ATTRIBUTE          S20038
LNG3     EQU   3                       LENGTH ATTRIBUTE          S20038
LNG4     EQU   4                       LENGTH ATTRIBUTE          S20038
LNG5     EQU   5                       LENGTH ATTRIBUTE           M2840
LNG7     EQU   7                       LENGTH ATTRIBUTE           M2840
LNG8     EQU   8                       LENGTH ATTRIBUTE           M2840
L24      EQU   24                      LENGTH ATTRIBUTE           M2840
L79      EQU   79                      LENGTH ATTRIBUTE           M2840
L119     EQU   119                     LENGTH ATTRIBUTE           M2840
NINE     EQU   9                       DISPLACEMENT              S20038
ABS1     EQU   1                       ABSOLUTE VALUE OF 1        M2840
ABS16    EQU   16                      ABSOLUTE 16 (CVT PTR)      M2840
ABS60    EQU   60                      ABSOLUTE VALUE OF 60       M2840
ABS121   EQU   121                     ABSOLUTE VALUE OF 121      M2840
ABS255   EQU   255                     ABSOLUTE VALUE OF 255      M2840
ABS256   EQU   256                     ABSOLUTE VALUE OF 256   @ZA01679
BUMP1    EQU   1                       INCREMENT REG WITH LD ADDR M2840
I0       EQU   0                       INDEX REG VALUE OF ZERO    M2840
BADOPEN  EQU   X'10'                   DCB OPEN NOT SUCCESSFUL    M2840
RCODE08  EQU   X'08'                   RETURN CODE OF EIGHT       M2840
RCODE16  EQU   16                      RETURN CODE OF SIXTEEN     M2840
BLANKCOL EQU   X'40'                   CHARACTER OF 'BLANK'       M2840
BINZERO  EQU   X'00'                   BINARY ZERO CLEAR CHAR     M2840
OPENOUT  EQU   X'8F'                   END OF LIST, OUTPUT FILE   M2840
ENDLIST  EQU   X'80'                   END OF PARAMETER LIST      M2840
ENDJFCB  EQU   X'87'                   END OF LIST, JFCB          M2840
FOXZERO  EQU   X'F0'                   MASK TO TEST NUMERICS      M2840
*
*                  SET UP THE DCB'S TO READ IN THE CARD FROM THE CARD
*                     READER AND PRINT THEM OUT AGAIN
*
* PRINT DATA CONTROL BLOCK MACRO                                 S20038
*                                                                S20038
PRINT1   DCB   DSORG=PS,MACRF=(PM),RECFM=FBA,LRECL=121,            BS0H*
               EXLST=OPT1,BUFNO=1                                  BS0H
*                                                                S20038
* READ DATA CONTROL BLOCK MACRO                                  S20038
*                                                                S20038
READ1    DCB   DSORG=PS,MACRF=(GM),RECFM=FB,LRECL=80,              BS0H*
               EXLST=OPT2,BUFNO=1                                 17128
*
* CONSTANTS                                                      S20038
* ERROR MESSAGES, CONTROL CARD INFORMATION, TABLE FOR SERIAL NO. S20038
* MAKE-UP, AND MISCELLANEOUS CONSTANTS                           S20038
*                                                                S20038
OPT1     DC    X'85'                                               BS0H
         DC    AL3(OUTEXIT)                                        BS0H
OPT2     DC    X'85'                                               BS0H
         DC    AL3(INEXIT)                                         BS0H
TWO55    DC    F'255'                  USED TO CLEAR GETMAIN AREA M2840
TWO56    DC    F'256'                  USED TO CLEAR GETMAIN AREA M2840
L80      DC    X'00500100'
LABTYPE  DC    CL8'LABTYPE='           INITT STATEMENT KEYWORD
AL       DC    CL2'AL'                 ASCII PARAMETER           S20038
SER      DC    CL4'SER='               INITT STATEMENT KEYWORD    M2840
DISP     DC    CL5'DISP='              INITT STATEMENT KEYWORD    M2840
OWNRN    DC    CL6'OWNER='             INITT STATEMENT KEYWORD    M2840
NUMBTAPE DC    CL9'NUMBTAPE='          INITT STATEMENT KEYWORD    M2840
REWIND   DC    CL6'REWIND'             INITT STATEMENT PARAMETER  M2840
LINCT    DC    CL8'LINECNT='           EXEC STATEMENT PARAMETER   M2840
INITT    DC    CL6'INITT '             COMMAND WORD CONSTANT      M2840
CS2      DC    A(IEHINIT2)             ADDRESS OF MSG CSECT      S20038
SIX10    DC    F'16'
EIGHT    DC    F'8'
ZERO     DC    D'0'
TEN      DC    F'10'                   EBCDIC OWNER PARM LENGTH   M2840
FOURTEEN DC    F'14'                   ASCII OWNER PARM LENGTH   S20038
FIVE     DC    H'40'                   DDNAMEADDR, SYSIN DISP     M2840
SIX      DC    H'48'                   DDNAMEADDR, SYSPRINT DISP  M2840
FOUR1    DC    X'0004'                 PAGECNT LENGTH            S20038
MAXCT    DC    H'10'                   MAX LNG OF LINECNT PARM   S20038
SYSPRINT DC    CL8'SYSPRINT'           UTILITY OUTPUT DSN         M2840
SYSIN    DC    CL8'SYSIN'              CONTROL DATA SET DSN       M2840
C        DC    F'12'                   TO CHECK RETURN CODE       M2840
FOUR     DC    F'4'                    TO CHECK RETURN CODE       M2840
FTHREE   DC    F'3'                    TO CHECK NUMBTAPE PARM LNG M2840
VLM      DC    CL4'VOL1'               STANDARD VOLUME LABEL ID   M2840
HEADER1  DC    CL5'HDR10'              STARDARD HDR1 ID           M2840
RWND     DC    X'C00004'
UNLD     DC    X'C00006'
*                                                                S20038
* THE HYPHEN,PERIOD,ALPHAMERICS AND NATIONAL ARE ACCEPTABLE    @ZA16491
* CHARACTERS IN THE MAKEUP OF THE SERIAL NUMBER. THE FOLLOWING @ZA16491
* TABLE IS IN ORDER BY THE BINARY SEQUENCE OF THE EBCDIC CODE. @ZA16491
* ZEROES INDICATE ACCEPTABLE CHARACTERS.  HENCE, THE TABLE     @ZA16491
* COULD BE MODIFIED BY MAINTAINING THIS PRACTICE.              @ZA16491
TABLE    DC    75X'01'                 NOT ACCEPTABLE          @ZA16491
         DC    X'00'                   '.' ACCEPTABLE          @ZA16491
         DC    15X'01'                 NOT ACCEPTABLE          @ZA16491
         DC    X'00'                   '$' ACCEPTABLE          @ZA16491
         DC    4X'01'                  NOT ACCEPTABLE          @ZA16491
         DC    X'00'                   '-' ACCEPTABLE          @ZA16491
         DC    26X'01'                 NOT ACCEPTABLE          @ZA16491
         DC    X'00'                   '#' ACCEPTABLE          @ZA16491
         DC    X'00'                   '@' ACCEPTABLE          @ZA16491
         DC    68X'01'                 NOT ACCEPTABLE          @ZA16491
         DC    9X'00'                  ALPHABETIC CHARACTER       M2840
         DC    7X'01'                  NOT ACCEPTABLE             M2840
         DC    9X'00'                  ALPHABETIC CHARACTER       M2840
         DC    8X'01'                  NOT ACCEPTABLE             M2840
         DC    8X'00'                  ALPHABETIC CHARACTER       M2840
         DC    6X'01'                  NOT ACCEPTABLE             M2840
         DC    10X'00'                 NUMERIC CHARACTER          M2840
         DC    6X'01'                  NOT ACCEPTABLE             M2840
*
*
         ORG   *+200                   MAINTENANCE AREA           M2840
*
*
IEHINIT2 CSECT
*        THIS CSECT CONTAINS ALL THE MESSAGES THAT THE IEHINITT  S20038
*        MODULE SENDS TO THE PRINT MODULE                        S20038
*                                                                S20038
ERR1     DC    CL33'IEH601I INVALID CONTROL STATEMENT'           S20038
ERR2     DC    CL23'IEH602I INVALID KEYWORD'                      M6029
ERR3     DC    CL31'IEH603I INVALID PARAMETER VALUE'             S20038
ERR4     DC    CL41'IEH604I OPERATOR SUPPRESSED VOLUME LABEL '
ERR5     DC    CL37'IEH605I INVALID DEVICE ALLOCATED ON  '
ERR6     DC    CL32'IEH606I PERMANENT I/O ERROR ON  '
ERR7     DC    CL35'IEH607I ALLOCATED DEVICES EXHAUSTED'         S20038
ERR8     DC    CL43'IEH608I I/O ERROR ON SYSIN. JOB TERMINATED.' S20038
ERR9     DC    CL43'IEH609I INVALID DEVICE SPECIFIED FOR ASCII ' S20038
ERR9C    DC    CL8'LABELING'                                     S20038
ERRA     DC    CL41'IEH610I INVALID PARM OR PARM LIST PASSED '   S20038
ERRAC    DC    CL14'TO IEHINITT.'                                 M2840
ERRB     DC    CL43'IEH611I INVALID DENSITY SPECIFIED, DEFAULT ' S20038
ERRBC    DC    CL10'VALUE USED'                                   M2840
X        DS    0F
*                                                                S20038
*                                                                S20038
* JOB FILE CONTROL BLOCK - SYMBOLIC NAMES                        S20038
*                        - USED TO OBTAIN DENSITY SETTING        S20038
*                                                                S20038
JFCB     DSECT
         IEFJFCBN
*                                                                S20038
* UNIT CONTROL BLOCK - SYMBOLIC NAMES                            S20038
*                    - USED TO DETERMINE TAPE MODE SETTING       S20038
*                                                                S20038
UCB      DSECT
         IEFUCBOB
*                                                                S20038
* WORKING STORAGE -                                              S20038
* THIS CONTROL MODULE OBTAINS CORE STROAGE FOR A COMMON WORKING  S20038
* AREA SHARED BY IEHINITT, IEHSCAN, IEHPRNT FOR THE PURPOSE OF   S20038
* PASSING SWITCH SETTINGS, READ AND PRINT DCB'S, MESSAGES, AND   S20038
* PARAMETERS, AS WELL AS HOLDING THE CONTROL CARD AND TAPE LABEL S20038
* IMAGES, AND SAVE AREAS.                                        S20038
*                                                                S20038
WORK     DSECT 0F
FIRST    DS    0F
*
*                  THE FOLLOWING AREA DESCRIBES THE VOLUME        M2840
*                       LABEL (80 BYTES) AND DUMMY HDR1           M2840
*                       RECORD (80 BYTES).  THESE AREAS           M2840
*                       MUST BE CONTIGUOUS                        M2840
*
LABEL    DS    0CL80                   MUST BE                   S20038
VOL1     DS    CL4                     CONSTANT VOL1 FIELD        M2840
SERIAL   DS    CL6                     VOLUME SERIAL FIELD        M2840
SECURITY DS    CL1                     VOLUME SECURITY FIELD      M2840
         DS    CL69                    NOT REFERENCED             M2840
DUMMYHDR DS    CL80                    CONTIGUOUS                S20038
*
*                  THE FOLLOWING AREA DESCRIBES THE LIST          M2840
*                       PASSED TO THE LABEL SVC.  THE AREA        M2840
*                       MUST BE CONTIGUOUS AND MUST BE            M2840
*                       ALIGNED ON A FULLWORD BOUNDARY            M2840
*
SVCLST   DS    0CL20                   FIVE WORD PARM LIST        M2840
SVCRUNOP DS    CL3                     REWIND/REWIND-UNLOAD       M2840
SVCUCB   DS    CL1                     UCB INDICATOR              M2840
SVCDDPTR DS    CL4                     POINTER TO DDNAME          M2840
SVCLBLPT DS    CL4                     PTR TO LABEL/DUMMY HEADER  M2840
         DS    CL8
*
HOMEJFCB DS    CL176                   INPUT AREA FOR RDJFCB      M2840
*
CARD     DS    CL80                    SCANNED CARD INPUT AREA    M2840
*
DISP12   DS    F                       SAVE FOR START NEXT SCAN   M2840
PARMPTR  DS    F                       PTR TO START PASSED PARM   M2840
LNGPARM  DS    F                       LENGTH OF PASSED PARAM     M2840
COMTAB   DS    0F
READAD   DS    F                       ADDRESS OF READ DCB        M2840
PRINTAD  DS    F                       ADDRESS OF PRINT DCB       M2840
JFLSTAD  DS    F                       ADDR OF HOMEJFCB (READIN)  M2840
AGO      DS    F                       ADDR DUMMY DCB FOR RDJFCB  M2840
JFCBLST  DS    F
*                                                                 M2840
*                  MESSAGE LINE, INCLUDING PAGE AND LINE COUNT    M2840
*                                                                 M2840
MSGWTR   DS    0CL128                                             M2840
CC       DS    CL1                     CARRIAGE CONTROL (ASA)     M2840
MSGTEXT  DS    CL120                   PRINT LINE                 M2840
MFILL1   DS    CL1                     NOT REFERENCED             M2840
PAGENUM  DS    CL2                     STARTING PAGE NUMBER       M2840
LINETOT  DS    CL2                     TOTAL LINES PER PAGE       M2840
LINELEFT DS    CL2                     LINES NOT YET PRINTED      M2840
*                                                                 M2840
SAVE     DS    18F                     IEHINITT SAVE AREA         M2840
GPSAVE   DS    18F                     IEHSCAN SAVE AREA          M2840
SAVEX    DS    18F                     IEHPRNT SAVE AREA          M2840
SAVEY    DS    2F
SAVEZ    DS    1F
MYSAVE   EQU   SAVEY                   3 FULLWORD SAVE AREA       M2840
TCB      DS    1F                      TCB/UCB PTR SAVE AREA      M2840
*                                                                 M2840
*                  PRINT DCB                                      M2840
*                                                                 M2840
PRINT    DS    0CL96                                              M2840
PFILL1   DS    CL40                    NOT REFERENCED             M2840
PDDNAME  DS    CL8                     DDNAME BEFORE OPEN         M2840
POPENFLG DS    CL1                     OPEN FLAG                  M2840
PFILL2   DS    CL8                     NOT REFERENCED             M2840
PSYNAD   DS    CL3                     PRINTER SYNAD ADDRESS      M2840
PFILL3   DS    CL2                     NOT REFERENCED             M2840
PBLKSZ   DS    CL2                     BLOCKSIZE                  M2840
PFILL4   DS    CL18                    NOT REFERENCED             M2840
PLRECL   DS    CL2                     RECORD LENGTH              M2840
PFILL5   DS    CL12                    NOT REFERENCED             M2840
*                                                                 M2840
*                  READ DCB                                       M2840
*                                                                 M2840
READ     DS    0CL96                                              M2840
RFILL1   DS    CL33                    NOT REFERENCED             M2840
REODAD   DS    CL3                     END OF DATA ADDRESS        M2840
RFILL2   DS    CL4                     NOT REFERENCED             M2840
RDDNAME  DS    CL8                     DDNAME BEFORE OPEN         M2840
ROPENFLG DS    CL1                     OPEN FLAG                  M2840
RFILL3   DS    CL8                     NOT REFERENCED             M2840
RSYNAD   DS    CL3                     SYNAD ADDRESS              M2840
RFILL4   DS    CL36                    NOT REFERENCED             M2840
*
*                  THE AREAS HERE AND THERE ARE WORK AREAS USED   M2840
*                       FOR PACKING, UNPACKING, CONVERTING TO     M2840
*                       DECIMAL, CONVERTING TO BIANRY, FIELD      M2840
*                       ADJUSTMENT, AND OTHER WORK PURPOSES.      M2840
*
D        DS    0D
HERE     DS    CL10
THERE    DS    CL12                                               10432
*
VALFLGS  DS    CL1                     VALID FLAGS               A46764
VALCOMP  DS    CL1                     DENSITY WORK AREA         A46764
*
*                  LABEL DCB
*                       NOTE: THE ONLY FIELDS THAT APPLY TO
*                       THE DCB ARE GO, DDNAME, AND DCBFLAGS
*                       THESE FIELDS MUST BE AT +36, +40, AND
*                       +48 RESPECTIVELY.
*
*                       THE REMAINDER OF THE FIELDS ARE WORK AREA
*
DISP5    DS    CL1                     IEHSCAN SWITCH BYTE        M2840
DISP8    DS    CL1                     IEHSCAN SWITCH BYTE        M2840
DISP10   DS    CL1                     IEHSCAN CURRENT DELIM      M2840
DISP11   DS    CL1                     IEHSCAN PREVIOUS DELIM     M2840
LBLDCB   DS    0CL96                   DUMMY DCB
SWITCHRD DS    CL1                     IEHSCAN PASSBACK INDICATOR M2840
PARMLNG  DS    CL1                     PARTIAL PARM LENGTH HOLD   M2840
SWITCHAL DS    CL1                     SWITCH FOR ASCII LABEL    S20038
KEY      DS    CL1
OWNRLNG  DS    F                       HOLD FOR OWNER LENGTH     S20038
OWNRHOLD DS    CL14                    HOLD FOR OWNER PARM       S20038
DFLG     DS    X                                                   BS0H
DFLAG    DS    X                                                   BS0H
LASTDD   DS    CL8                     SAVE PREVIOUS DDNAME.      15673
RC       DS    CL1                     RETURN CODE SAVE AREA      M2840
SWITCH2  DS    CL1
NUMTPS   DS    CL1
UCBNUM   DS    CL1
GO       DC    F'0'                    SAVE AREA FOR EXLIST
DDNAM    DS    CL8                     SAVE AREA FOR DDNAME       M2840
         DC    F'0'                    SAVE AREA FOR DCBFLG,DCBMACR
PARMKEEP DS    CL44                    PARAMETER SAVE AREA        M2840
*
UCBAREA  DS    CL176                   UCB PTRS FROM DD CARD     A23445
PARMERR  DS    X                       LINECNT & PAGECNT ERROR    M2840
LAST     DS    0H
         END
