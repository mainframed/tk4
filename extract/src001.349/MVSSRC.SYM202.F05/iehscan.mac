 TITLE 'IEHSCAN-IEHINITT SCAN MODULE'                              1581
 TITLE 'IEHSCAN-IEHINITT SCAN MODULE'                              1581
*TITLE 'IEHSCAN' SCAN CARD IMAGE FOR PARAMETERS SPECIFIED             *
*STATUS: CHANGE LEVEL 000                                             *
*FUNCTION/OPERATION: TO SCAN AN 80 BYTE AREA IN CORE AND PASS BACK A  *
* TITLE:
*    'IEHSCAN' SCAN CARD IMAGE FOR PARAMETERS SPECIFIED
* STATUS:
*    CHANGE LEVEL 000
* FUNCTION/OPERATION:
*    TO SCAN AN 80 BYTE AREA IN CORE AND PASS BACK A POINTER
*    TO THE PARAMETER, ITS LENGTH AND AN INDICATION AS TO WHAT
*    IT IS
* ENTRY POINTS:
*    THE ONLY ENTRY POINT IS IEHSCAN TO SCAN THE 80 BYTE
*    CARD IMAGE
* INPUT:
*    REGISTER 13 POINTS TO THE WORK AREA,
*    REGISTER 14 POINTS TO THE RETURN ADDRESS OF THE CALLING ROUTINE,
*    REGISTER 15 POINTS TO THE ENTRY POINT
* OUTPUT:
*    REGISTER 1 POINTS TO THE ITEM SCANNED,
*    REGISTER 2 CONTAINS ITS LENGTH,
*    REGISTER 3 POINTS TO THE START OF THE NEXT SCAN.            S20038
*    THE FLAG BYTE 'SWITCHRD' INDICATES SYNTAX ERROR,
*    BYPASS, END OF FILE, INITIAL ENTRY, COMMAND WORD, KEYWORD,
*    OR PARAMETER.
*    ANOTHER BIT IN THE WORK AREA INDICATES DDNAME
* EXTERNAL REFERENCES:
*    IEHPRNT  - PRINT ROUTINE. PRINT OUT EACH CARD IMAGE SCANNED
* EXITS, NORMAL:
*    CONTROL IS RETURNED TO THE CALLER
* EXITS, ERROR:
*    THE ERROR BIT IS SET BEFORE CONTROL IS RETURNED
* TABLES/WORK AREAS:
*    WORK AREA IS SET UP IN CALLING ROUTINE AND PASSED
*    TO SCAN
* ATTRIBUTES:
*    REENTRANT
* CHARACTER CODE DEPENDENCY -                                    S20038
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL       S20038
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS       S20038
*    EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME. THE CODING HAS S20038
*    BEEN ARRANGED SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS,S20038
*    BY REASSEMBLY, WILL RESULT IN A CORRECT MODULE FOR THE NEW  S20038
*    DEFINITIONS.                                                S20038
* NOTES:
*    THE MICROFICHE FLAG 'M2840' REPRESENTS A GENERAL CLEANUP OF  M2840
*    ALL IEHINITT MODULES, WITH SPECIAL EMPHASIS ON REMOVAL OF    M2840
*    DEAD AND REDUNDANT CODE FROM THE MODULE IEHSCAN.  THE FLAG   M2840
*    ALSO REPRESENTS PARAMETERIZATION OF ABSOLUTE VALUES FOR      M2840
*    CROSS-REFERENCE PURPOSES, AND THE ADDITION OF COMMENTS       M2840
*    TO A NUMBER OF SOURCE STATEMENTS.                            M2840
         EJECT
IEHSCAN  CSECT
*A025500-025584,121100-121300                                  @ZA16491
*D038200-038400                                                @ZA16474
*2558068200                                                        3660
* 136000                                                         S20038
         USING IEHCDSCN,SCANBASE       ESTABLISH ADDRESSIBILITY   M2840
         USING WORK,WORKREG            ADDR OF COMMON WORK AREA   M2840
IEHCDSCN SAVE  (14,12),T,IEHSCAN-OZ16491 SAVE REGISTERS        @ZA16491
         LR    SCANBASE,R15            LOAD BASE REGISTER         M2840
         ST    R13,GPSAVE+4            SAVE CALLER'S REG 13       M2840
         MVC   REODAD,EODA             INSERT EODAD ROUTINE       M2840
         MVC   RSYNAD,SYNAD            INSERT SYNAD IN READ DCB   M2840
         LA    COL71REG,COL71          ADDR OF COLUMN 71 OF CARD  M2840
         L     NEXTCOL,DISP12          SAVED START OF NEXT SCAN   M2840
         NI    SWITCHRD,BIT0OFF        RESET ERROR BIT            M2840
         TM    SWITCHRD,BIT3           TEST FIRST ENTRY BIT
         BC    OFF,KIRSCD              FIRST ENTRY OR NEW SET     M2840
         TM    SWITCHRD,BIT1           TEST BYPASS BIT
         BC    ON,KBYPO1               JUST READ AND PRINT CARDS  M2840
         TM    DISP5,BIT4              TEST READ CARD SWITCH
         BC    ON,KGTCD                READ AND PROCESS CARD      M2840
         TM    SWITCHRD,BIT7           COMMENT BIT ON            SM5216
         BNO   KPFOL                   CONTINUE CHECKING         SM5216
         OI    SWITCHRD,BIT1           SET BYPASS                SM5216
         NI    SWITCHRD,X'FF'-BIT6     SET OFF PARAM FOLLOW BIT  SM5216
         B     KGTCD                   READ AND PRINT CARDS      SM5216
KPFOL    TM    DISP5,BIT2              TEST PARAM FOLLOW SWITCH
         BC    ON,SCANR                SCAN FOR KEYWORD OR PARAM  M2840
         TM    DISP5,BIT1              TEST CONTINUE SWITCH
         BC    ON,KCONT                SCAN FOR COMMAND WORD      M2840
         TM    DISP10,BIT5             IS NAME BIT ON?            M2840
         BO    GETCMD                  YES, HAVE NAME, GET CMD    M2840
         EJECT
*
*   THIS ROUTINE CHECKS FOR A NAME AND SETS THE POINTER TO         1581
*   THE FIRST BLANK COLUMN                                         1581
*                                                                  1581
         LA    NEXTCOL,COL2            LOAD ADDRESS OF BEGIN COL  M2840
KNAME    CLI   0(NEXTCOL),BLANKCOL     IS NEXT COLUMN BLANK?      M2840
         BE    HAVENAME                YES, END OF DDNAME, BRANCH M2840
         CR    NEXTCOL,COL71REG        NEXT COLUMN COLUMN 71?     M2840
         BE    KINVALD                 YES, SET UP AS ERROR       M2840
         LA    NEXTCOL,BUMP1(I0,NEXTCOL) INCREMENT TO NEXT COLUMN M2840
         B     KNAME                   BRANCH BACK TO CHECK FOR   M2840
*                                         END OF DDNAME           M2840
HAVENAME LA    STARTCOL,CARD           CALCULATE LENGTH OF DDNAME M2840
         LR    LENGTH,NEXTCOL          ADDRESS OF FIRST BLANK COL M2840
         SR    LENGTH,STARTCOL         CALCULATE LENGTH           M2840
         OI    DISP10,BIT5             TURN ON NAME BIT
         B     KPASS                   RETURN DDNAME TO CALLER    M2840
*
GETCMD   MVI   DISP10,NULL             CLEAR CURRENT DELIM (NAME) M2840
         OI    DISP5,BIT5              SET COMMAND CONTROL SWITCH
         OI    SWITCHRD,BIT4           SET COMMAND BIT
*
*                  THIS SECTION ADJUSTS THE POINTER TO THE FIRST CHAR
*                     OF COMMAND, IF NO COMMAND FOUND COL 71 SET BYPASS
*                     BIT
STARTCMD CLI   0(NEXTCOL),BLANKCOL     IS COLUMN BLANK            M2840
         BNE   SCAN                    NO, FIRST CHAR OF CMD WORD M2840
         CR    NEXTCOL,COL71REG        IS THIS COLUMN 71          M2840
         BE    KINVALD                 YES, SET UP AS ERROR       M2840
         LA    NEXTCOL,BUMP1(I0,NEXTCOL)  ADJUST PTR TO NEXT COL  M2840
         B     STARTCMD                EXAMINE NEXT COLUMN        M2840
         EJECT
KCONT    LA    NEXTCOL,COL16           SET PTR TO CONTINUE COLUMN M2840
*
*                  RESET SWITCHRD CONTROLS, RESET EXIT CONTROLS SAVE
*                     LAST DELIMITER AND CLEAR DELIMITER INDICATOR
*
SCANR    NI    SWITCHRD,RESET1         RESET COMMAND BIT
SCAN     NI    SWITCHRD,RESET2         RESET ALL BITS BUT ERROR AND
*                                         FIRST ENTRY
         NI    DISP5,BIT8              RESET ALL SWITCHES BUT COMMAND
*                                         CONTROL SWITCH
SCANPP   MVC   DISP11,DISP10           SAVE CURRENT IN LAST DELIM M2840
         NI    DISP10,NULL             CLEAR CURRENT DELIMITER    M2840
*
*                  THIS ROUTINE SCANS CARD UNTIL DELIMITER IS FOUND
*                     SAVES ADDRESS OF FIRST CHAR, COMPUTES LENGTH,
*                     SET BIT IN EXIT CONTROL IF DELIM IS IN END  COL
*                     OR BEFORE END COL
*
         SR    LENGTH,LENGTH           SET LENGTH = ZERO          M2840
         LR    STARTCOL,NEXTCOL        START SCAN WHERE LEFT OFF  M2840
KOMPAR   CLI   0(NEXTCOL),KSPLT        IS CHAR A DELIMITER        M2840
         BC    LOW,KFIND               YES, BRANCH                M2840
KOMPARA  LA    LENGTH,BUMP1(I0,LENGTH) ADD ONE TO LENGTH          M2840
         CR    NEXTCOL,COL71REG        IS THIS COLUMN 71?         M2840
         BE    KPART                   YES, PARTIAL PARM, BRANCH  M2840
KOMPARS  LA    NEXTCOL,BUMP1(I0,NEXTCOL)  ADJUST PTR TO NEXT COL  M2840
         B     KOMPAR                  KEEP LOOKING FOR DELIMETER M2840
*
*                  KFIND CHECKS FOR DELIMITER OF PERIOD OR MINUS IF
*                     PRESENT RETURNS TO SCAN ROUTINE, TESTS END COLUMN
*                     IF DELIMITER IS IN END COL SET CC2 BIT IF BEFORE
*                     END COL SET CC1 BIT AND GOES TO TEST DELIMITER
*                     ROUTINE
*
KFIND    CLI   0(NEXTCOL),PERIOD       IS DELIMETER A PERIOD?     M2840
         BE    KOMPARA                 IGNORE, GO LOOK FOR DELIM  M2840
         CLI   0(NEXTCOL),MINUS        IS DELIMITER MINUS SIGN?   M2840
         BE    KOMPARA                 IGNORE, GO LOOK FOR DELIM  M2840
         CLI   0(NEXTCOL),DOLLAR      IS DELIMITER DOLLAR SIGN @ZA16491
         BE    KOMPARA               IGNORE, GO LOOK FOR DELIM @ZA16491
         CLI   0(NEXTCOL),NUMB         IS DELIMITER NUMB SIGN  @ZA16491
         BE    KOMPARA               IGNORE, GO LOOK FOR DELIM @ZA16491
         CLI   0(NEXTCOL),AT           IS DELIMITER AT SIGN    @ZA16491
         BE    KOMPARA               IGNORE, GO LOOK FOR DELIM @ZA16491
         CR    NEXTCOL,COL71REG        IS THIS COLUMN 71?         M2840
         BC    NO,TESTPAR              NO, GO CHECK LENGTH        M2840
         TM    DISP5,BIT3              IS PARTIAL PARAM SWITCH ON
         BO    KINVALD                 IF ON LENGTH IS TOO LONG   M2840
*
*                  TESTPAR CHECKS FOR PRESENCE OF PARTIAL PARAMETER
*
TESTPAR  TM    DISP5,BIT3              IS PARTIAL PARAMETER SWITCH ON
         BO    KPPAR                   YES, GO HANDLE PART PARAM  M2840
*
*                  DELIM CHECKS FOR VALID DELIMETERS AND BRANCHES TO
*                     ROUTINE TO SET CONTROL SWITCHES
*                     IF INVALID DELIMITER IS FOUND BRANCH TO KINVAL
*
DELIM    CLI   0(NEXTCOL),EQUAL        DELIMETER EQUAL SIGN?      M2840
         BE    KEY1                    GO HANDLE VALUE AS KEYWORD M2840
         CLI   0(NEXTCOL),COMMA        DELIMETER A COMMA?         M2840
         BE    PARAMC                  GO HANDLE VALUE AS PARAM   M2840
         CLI   0(NEXTCOL),APOST        DELIMETER APOSTROPHE?      M2840
         BE    PARMAPT                 GO HANDLE AS OWNER         M2840
         CLI   0(NEXTCOL),BLANKCOL     DELIMETER A BLANK          M2840
         BE    PARBLANK                GO HANDLE VALUE AS PARAM   M2840
*                                                                 M2840
*                  INVALID DELIMETER FOUND                        M2840
*                     SET 'ERROR' FOR IEHINITT (MESSAGE IS        M2840
*                     'INVALID CONTROL STATEMENT')                M2840
*                                                                 M2840
*
KINVALD  OI    SWITCHRD,BIT0+BIT1      SET ERROR BIT & BYPASS     M2840
         B     KPASS                   RETURN ERROR TO CALLER     M2840
         EJECT
*
*                  KIRSCO IS THE FIRST ENTRY ROUTINE WHICH CLEARS
*                     CONTROL SWITCHES TO ZERO FOR FIRST READ
*                     GOES TO READ A CARD ROUTINE
*
KIRSCD   OI    SWITCHRD,BIT3           SET FIRST ENTRY SWITCH
         SR    NEXTCOL,NEXTCOL         CLEAR LAST COL PTR AND     M2840
         ST    NEXTCOL,DISP5           DISP5,DISP8,DISP10,DISP11  M2840
         MVI   DISP8,FOXFOX            INDICATE FIRST ENTRY
*
*                  KGTCD READS A CARD INTO INPUT AREA IF LAST CARD
*                     HAS BEEN READ GO TO KEOF IF NOT GO TO KGTGDR
*
KGTCD    L     R1,READAD               GET ADDRESS OF SYSIN DCB   M2840
         LA    R1,0(I0,R1)             CLEAR HIGH-ORDER BYTE      M2840
         LA    R0,CARD                 LOAD ADDRESS OF INPUT AREA
         LA    R13,GPSAVE              POINT AT IEHSCAN SAVE AREA M2840
         GET   (R1),(R0)               READ A CARD
         CLI   DISP8,FOXFOX            IS THIS THE FIRST CARD     M2840
         BNE   PRNT                    BRANCH ON NO               M2840
         MVI   DISP8,NULL              RESET FIRST ENTRY BYTE     M2840
PRNT     BAL   LINKREG,KPRNT           BRANCH TO PRINT OUT CARD   M2840
*
*                  KGTCDR
*                      TEST BYPASS BIT IF ON
*                     GO TO KBYPO IF OFF GO TO ROUTINE TO TEST ENTRY
*                     CONTROL SWITCH
*
KGTCDR   MVI   DISP8,NULL
         TM    SWITCHRD,BIT1           IS BYPASS SWITCH ON        M2840
         BO    KBYPO1                  JUST READ AND PRINT CARDS  M2840
         B     KPFOL                   HOUSEKEEP, LOOK FOR DELIM  M2840
         EJECT
*
KEY1     OI    SWITCHRD,BIT5           SET KEYWORD BIT
         OI    DISP10,BIT0             SET DELIM EQUAL SIGN BIT
         B     DECID1                  CHECK FOR VALIDITY         M2840
*
PARAMC   OI    SWITCHRD,BIT6           SET PARAMETER BIT
         OI    DISP10,BIT1             SET DELIM COMMA BIT
*
DECID1   LTR   LENGTH,LENGTH           IS LENGTH EQUAL ZERO       M2840
         BC    8,KINVALD               NO KEYWORD OR VALUE, ERROR M2840
         OI    DISP5,BIT2              SET PARM FOLLOWS SWITCH
         CR    NEXTCOL,COL71REG        DELIMITER IN COLUMN 71     M2840
         BNE   KPASS                   DELIMITER OKAY, RETURN     M2840
         CLI   COL72,BLANKCOL          IS COLUMN 72 BLANK         M2840
         BE    KINVALD                 SHOULD CONTINUE, ERROR     M2840
         OI    DISP5,CONT              SET READ CARD AND CONTINUE M2840
         B     KPASS                   RETURN KEYWORD OR PARAM    M2840
*
*                  PARBLANK IS ENTERED IF DELIMITER WAS A BLANK
*                     CHECK FOR PRESENCE OF COMMAND WORD IF COMMAND
*                     ADJUST POINTER TO NEXT NON BLANK COLUMN
*                     CHECK LENGTH IF NOT ZERO SET BYPAS SWITCH
*                     IF ZERO AND PRECEEDED BY COMMA READ NEXT CARD
*
PARBLANK TM    DISP5,BIT5              IS COMMAND CONTROL SWITCH ON
         BO    KPCMD                   BRANCH IF YES             M2840
         OI    SWITCHRD,BIT6           SET PARMETER BIT
         OI    DISP10,BIT4             SET BLANK DELIMETER BIT
*
*                  DECID2 CHECK THE EXIT CONTROL SWITCH AND DELIMITERS
*                     INDICATOR TO SET CORRECT ENTRY SWITCHES AND
*                     INFORMATION FOR USER PROGRAM
*
DECID2   LTR   LENGTH,LENGTH           IS LENGTH EQUAL ZERO       M2840
         BC    7,NOTZERO               NOT ZERO, BRANCH           M2840
         TM    DISP11,BIT1             PREVIOUS DELIM COMMA       M2840
         BNO   KINVALD                 NOT COMMA, ERROR           M2840
         CLI   COL72,BLANKCOL          COLUMN 72 BLANK            M2840
         BE    KINVALD                 SHOULD CONTINUE, ERROR     M2840
         OI    DISP5,CONT              SET READ CARD AND CONTINUE M2840
         B     KGTCD                   READ ANOTHER CARD         SM5216
NOTZERO  CLI   COL72,BLANKCOL          ANY CONTINUATION          SM5216
         BNE   SETREAD                 NO, SET UP TO RETURN      SM5216
         OI    SWITCHRD,BIT1           SET BYPASS                SM5216
         B     KPASS                   RETURN PARAMETER          SM5216
KBYPO1   CLI   COL72,BLANKCOL          IS COLUMN 72 BLANK        SM5216
         BNE   KGTCD                   NO, KEEP READING CARDS    SM5216
         B     KPASS                   RETURN PARAMETER           M2840
SETREAD  OI    SWITCHRD,BIT7           SET ON COMMENT BIT        SM5216
         B     KPASS                   RETURN PARAMETER          SM5216
         EJECT
*
*
*
KPRNT    TM    POPENFLG,BIT3           IS PRINT DCB OPEN, IF NOT  M2840
         BCR   8,LINKREG               THEN SKIP PRINTING         M2840
         MVI   MSGTEXT,BLANKCOL        SET UP BLANK CLEAR CHAR    M2840
         MVC   MSGTEXT+D1(L119),MSGTEXT  CLEAR BUFFER TO BLANKS   M2840
         MVC   MSGTEXT(L'CARD),CARD    MOVE CARD IMAGE            M2840
         ST    NEXTCOL,SAVEZ           SAVE LAST COL SCANNED      M2840
         LA    NEXTCOL,MSGWTR          POINTER TO PRINT LINE      M2840
         LA    R13,GPSAVE              PTR TO IEHSCAN SAVE AREA   M2840
         ST    R12,8(R13)              SAVE PTR TO START OF WORK  M2840
         LA    R12,COMTAB              LOAD PTR WITHIN WORK       M2840
         LINK  EP=IEHPRNT              PRINT CARD JUST READ       M2840
         L     R12,8(R13)              RESTORE PTR TO WORK START  M2840
         L     NEXTCOL,SAVEZ           RESTORE LAST COL SCANNED   M2840
         TM    SWITCH2,BIT7            IS PERM ABORT BIT ON       M2840
         BO    KINVALD                 SET UP FOR ERROR           M2840
         BR    LINKREG                 RETURN TO CALLING ROUTINE  M2840
*
         EJECT
*
*
KPART    TM    DISP5,BIT3              IS PARTIAL PARM SWITCH ON
         BO    KINVALD                 TOO LONG, ERROR            M2840
KPTERR   OI    DISP5,BIT3              SET PARTIAL PARM BIT
         CLI   COL72,BLANKCOL          IS COLUMN 72 BLANK         M2840
         BE    KLAST                   YES, BLANK, BRANCH         M2840
         MVI   PARMKEEP,BLANKCOL       CLEAR FIRST BYTE           M2840
         MVC   PARMKEEP+D1(L43),PARMKEEP   CLEAR REMAINDER        M2840
         STC   LENGTH,PARMLNG          LENGTH OF PARTIAL PARM     M2840
         BCTR  LENGTH,REG0             ADJUST LENGTH FOR MOVE     M2840
         EX    LENGTH,MOVEP            MOVE PARTIAL PARM TO SAVE  M2840
         OI    DISP5,BIT1
         B     KGTCD                   GO READ ANOTHER CARD       M2840
KLAST    OI    SWITCHRD,BIT6           SET PARM BIT
KBYPS    OI    SWITCHRD,BIT1           SET BYPASS SWITCH
         B     KPASS                   RETURN PARAM TO CALLER     M2840
*
*
KPPAR    SR    PPWRKREG,PPWRKREG       CLEAR REG                  M2840
         IC    PPWRKREG,PARMLNG        PICK UP PARTIAL LENGTH     M2840
         AR    PPWRKREG,LENGTH         ADD SECOND PART LENGTH     M2840
         C     PPWRKREG,FOUR4          IS TOTAL GREATER THAN 44   M2840
         BH    KINVALD                 YES, TOO LONG, ERROR       M2840
         LTR   LENGTH,LENGTH           SECOND HALF LENGTH = 0     M2840
         BC    8,KOMPL                 TOTAL PARAM IN FIRST HALF  M2840
         SR    PPWRKREG,PPWRKREG       CLEAR WORK REGISTER        M2840
         IC    PPWRKREG,PARMLNG        GET PARTIAL PARM LENGTH    M2840
         LA    WRKREG,PARMKEEP         ADDR OF PARTIAL SAVE AREA  M2840
         AR    PPWRKREG,WRKREG         ADDR OF START SECOND HALF  M2840
         BCTR  LENGTH,REG0             DECREASE LENGTH BY 1       M2840
         EX    LENGTH,MOVPP            EXECUTE MOVE INSTRUCTION   M2840
         LA    LENGTH,BUMP1(I0,LENGTH) RESTORE ORIGINAL LENGTH    M2840
KOMPL    SR    PPWRKREG,PPWRKREG       CLEAR WORK REGISTER        M2840
         IC    PPWRKREG,PARMLNG        GET FIRST PART OF LENGTH   M2840
         AR    LENGTH,PPWRKREG         ADD PART LENGTH TO LENGTH  M2840
         LA    STARTCOL,PARMKEEP       PICK UP ADDR OF SAVE AREA  M2840
         MVC   DISP11(1),DISP10        SAVE LAST DELIM INDICATOR
         NI    DISP10,RESET0           CLEAR CURRENT DELIM FLAG   M2840
         NI    DISP5,RESET6            RESET PARTIAL PARM SWITCH
         B     DELIM                   GO CHECK FOR VALID DELIM   M2840
         EJECT
*
*
KPASS    LA    NEXTCOL,BUMP1(I0,NEXTCOL)  ADJUST PTR TO NEXT COL  M2840
KPASS1   NI    DISP5,X'EB'
         ST    STARTCOL,PARMPTR        FIRST CHAR OF THIS SCAN    M2840
         ST    NEXTCOL,DISP12          START OF NEXT SCAN         M2840
         ST    LENGTH,LNGPARM          LENGTH OF PARAMETER        M2840
KPASS2   L     R13,GPSAVE+4                                        9837
         RETURN (14,12),T              RETURN TO CALLER           M2840
*
*
KPCMD    LA    NEXTCOL,BUMP1(I0,NEXTCOL)  ADJUST PTR TO START OF NEXT
         CLI   0(NEXTCOL),BLANKCOL     THIS CHARACTER A BLANK     M2840
         BNE   KPCMA                   END OF BLANKS AFTER CMD    M2840
         CR    NEXTCOL,COL71REG        ALL BLANKS TO COL 71       M2840
         BE    KINVALD                 NO PARAM IN CARD, ERROR    M2840
         B     KPCMD                   KEEP LOOKING FOR NON-BLANK M2840
*
*
KPCMA    BCTR  NEXTCOL,REG0            POINT AT LAST BLANK        M2840
PRFOL    OI    DISP5,BIT2              SET PARM FOLLOW TO ENTRY CONTROL
         B     KPASS                   RETURN TO CALLER           M2840
*
         EJECT
*
*
*
*                  THIS ROUTINE CHECKS FOR A SECOND APOSTROPHY AS A
*                     DELIMETER FOR THE OWNER FIELD
*
PARMAPT  CLI   KEY,BIT2                WAS KEYWORD OWNER          M2840
         BNE   KINVALD                 INVALID APOSTROPHES, ERROR M2840
         OI    DISP11,BIT5             SET FIRST TIME BIT         M2840
         LA    OWNRREG,OWNRHOLD        PTR=START OF OWNER HOLD    M2840
         SR    BYTECTR,BYTECTR         ZERO OUT OWNER BYTE CNTR   M2840
ONWARD   CR    NEXTCOL,COL71REG        IS THIS COLUMN 71?         M2840
         BC    4,EVRONWRD              BRANCH IF NO               M2840
         CLI   COL72,BLANKCOL          IS CARD CONTINUED          M2840
         BE    KINVALD                 BRANCH FOR ERROR IF NO     M2840
         L     R1,READAD               GET THE NEXT CARD TO FINISH
         LA    R1,0(I0,R1)                PICKING UP THE OWNER FIELD
         LA    R0,CARD                 CARD WORK AREA             M2840
         LA    R13,GPSAVE              PTR TO IEHSCAN SAVE AREA   M2840
         GET   (R1),(R0)               READ A RECORD              M2840
         BAL   LINKREG,KPRNT           PRINT OUT  NEW CARD IMAGE  M2840
         LA    NEXTCOL,COL15           START TO SCAN NEW CARD     M2840
EVRONWRD LA    NEXTCOL,BUMP1(I0,NEXTCOL)  INCREMENT TO NEXT CHAR  M2840
         CLI   0(NEXTCOL),APOST        THIS CHAR AN APOSTROPHE    M2840
         BE    PAIRS                   BRANCH IF YES              M2840
MOVEONE  MVC   0(1,OWNRREG),0(NEXTCOL) MOVE A CHAR TO OWNER HOLD  M2840
         NI    DISP11,FOXBAKER         TURN OFF FIRST TIME BIT    M2840
         LA    OWNRREG,BUMP1(I0,OWNRREG) INCREMENT OWNER HOLD PTR M2840
         LA    BYTECTR,BUMP1(I0,BYTECTR) INCREMENT OWNER BYTE CNT M2840
         LA    OWNFULL,OWNRHOLD+14     IS OWNER HOLD FULL         M2840
         CR    OWNRREG,OWNFULL                                    M2840
         BC    2,KINVALD               BRANCH ON YES              M2840
         B     ONWARD                  GO EXAMINE NEXT CHAR       M2840
PAIRS    TM    DISP11,BIT5             IS THIS THE FIRST TIME     M2840
         BZ    TWOAPST                 BRANCH IF NO               M2840
         NI    DISP11,FOXBAKER         TURN OFF FIRST TIME BIT    M2840
         LA    NEXTCOL,BUMP1(I0,NEXTCOL)  POINT AT NEXT COL       M2840
         TM    0(NEXTCOL),APOST        ERROR IF ONLY TWO APOSTROPHIES
         BC    12,KINVALD                 AT BEGINNING OF OWNER NAME
         B     MOVEONE                 MOVE APOSTROPHE TO HOLD    M2840
TWOAPST  LA    NEXTCOL,BUMP1(I0,NEXTCOL)  POINT AT NEXT COL       M2840
         CLI   0(NEXTCOL),APOST        SECOND APOSTROPHE          M2840
         BE    MOVEONE                 END OF OWNER NAME FIELD    M2840
         NI    KEY,NULL                TURN OFF OWNER NAME BIT    M2840
         OI    SWITCHAL,OWNRSW         FLAG OWNER PARM SPECIFIED S20038
         ST    BYTECTR,OWNRLNG         SAVE OWNER PARM LENGTH     M2840
         CLI   0(NEXTCOL),COMMA        DELIMITER A COMMA          M2840
         BE    SETCOM                  YES, BRANCH                M2840
         CLI   0(NEXTCOL),BLANKCOL     DELIMITER A BLANK          M2840
         BE    SETEND                  YES, END OF SCAN           M2840
         B     KINVALD                 SET ERROR INDICATOR        M2840
*
SETCOM   OI    DISP10,BIT1             SCAN FOR KEYWORD           M2840
KPAFOL   LA    NEXTCOL,BUMP1(I0,NEXTCOL)  POINT AT NEXT COL       M2840
         B     SCANR
*
SETEND   OI    SWITCHRD,BIT1           END OF SCAN, SET BYPASS    M2840
         CLI   COL72,BLANKCOL          IS COLUMN 72 BLANK?
         BE    KPASS1                  YES, END OF SCAN
         B     KGTCD                   READ ANOTHER CARD
*
*
KEOF     MVI   SWITCHRD,BIT2           SET END OF FILE BIT ONLY   M2840
         B     KPASS                   RETURN TO CALLING PROGRAM  M2840
*
*
*
*
KSYNAD   OI    DISP11,BIT7             INDICATE SYNAD WAS ENTERED  9837
         MVI   RC,RCODE16              SET RETURN CODE OF 16      M2840
         B     KPASS2                                              9837
*
*
MOVEP    MVC   PARMKEEP(1),0(STARTCOL)
MOVPP    MVC   0(1,PPWRKREG),0(STARTCOL)
EODA     DC    AL3(KEOF)
FOUR4    DC    F'44'
SYNAD    DC    AL3(KSYNAD)             SYNAD FOR SYSIN DCB         9837
*                                                                S20038
         ORG   *+200                   MAINTENANCE AREA FOR PGM  S20038
         EJECT
*                                                                S20038
* REGISTER ALLOCATION, INSTRUCTION MASK AND BIT SETTINGS         S20038
*                                                                S20038
EODAD    EQU   33
REG0     EQU   0
R0       EQU   0
R1       EQU   1
STARTCOL EQU   1
LENGTH   EQU   2
R3       EQU   3
NEXTCOL  EQU   3                       PTR TO NEXT COL OF SCAN    M2840
OWNFULL  EQU   4
PPWRKREG EQU   4                       PARTIAL PARAMETER WORK REG M2840
R5       EQU   5
COL71REG EQU   5                       ADDR OF COL 71 OF CARD     M2840
BYTECTR  EQU   6
OWNRREG  EQU   7
WRKREG   EQU   8
R9       EQU   9
LINKREG  EQU   9                       LINK TO PRINT SUBROUTINE   M2840
WORKREG  EQU   10                      PTR TO GETMAIN WORK AREA   M2840
SCANBASE EQU   11                      BASE REGISTER FOR IEHSCAN  M2840
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
BUMP1    EQU   1                       INCREMENT REG WITH LD ADDR M2840
I0       EQU   0                       INDEX REG 0 FOR LD ADDR    M2840
BIT0     EQU   X'80'                   SET ERROR, CC0
BIT1     EQU   X'40'                   SET BYPASS,CONT,CC1
BIT2     EQU   X'20'                   SET EOF, PARM FOLLOW, CC2
BIT3     EQU   X'10'                   SET FIRST ENTRY, PARTIAL PARM
BIT4     EQU   X'08'                   SET COMMAND,READ CARD COL 72 B
BIT5     EQU   X'04'                   SET KEYWORD,CSWITCH,COL 72 NB
BIT6     EQU   X'02'                   SET PARM LNG GTR 0
BIT7     EQU   X'01'                   SET PARM LNG = 0
BIT8     EQU   X'14'
BIT0OFF  EQU   X'7F'                   RESET BIT 0                M2840
RESET0   EQU   X'00'
RESET1   EQU   X'F7'                   RESET COMMAND,RDCD,COL 72 BLANK
RESET2   EQU   X'98'
RESET6   EQU   X'EF'
RESET10  EQU   X'0C'
NULL     EQU   X'00'                   RESET SINGLE BYTE SWITCH   M2840
CONT     EQU   X'48'
ON       EQU   1
OFF      EQU   8
YES      EQU   8
NO       EQU   7
ZERO     EQU   8
SAME     EQU   8
KSPLT    EQU   X'7F'
LOW      EQU   4
EQUAL    EQU   X'7E'
COMMA    EQU   X'6B'
BLANKCOL EQU   X'40'
APOST    EQU   X'7D'
PERIOD   EQU   X'4B'
MINUS    EQU   X'60'
DOLLAR   EQU   X'5B'                   '$' ACCEPTABLE          @ZA16491
NUMB     EQU   X'7B'                   '#' ACCEPTABLE          @ZA16491
AT       EQU   X'7C'                   '@' ACCEPTABLE          @ZA16491
MSKE     EQU   X'42'
OWNRSW   EQU   X'40'                   OWNER PARM SPECIFIED      S20038
RCODE16  EQU   X'10'                   RETURN CODE OF 16          M2840
FOXBAKER EQU   X'FB'                   RESET OWNER FIRST TIME SWITCH
FOXFOX   EQU   X'FF'                   FIRST ENTRY (OR NEW SET)   M2840
D1       EQU   1                       DISPLACEMENT               M2840
L43      EQU   43                      DISPLACEMENT               M2840
L119     EQU   119                     LENGTH                     M2840
         EJECT
*                                                                S20038
* WORKING STORAGE FOR SCAN MODULE IS A COMMON AREA SHARED BY THE S20038
* CONTROL MODULE AND THE PRINT MODULE.                           S20038
*                                                                S20038
         SPACE 3
WORK     DSECT 0F
FIRST    DS    0F
*
*                  THE FOLLOWING AREA DESCRIBES THE VOLUME        M2840
*                       LABEL (80 BYTES) AND DUMMY HDR1           M2840
*                       RECORD (80 BYTES).  THESE AREAS           M2840
*                       MUST BE CONTIGUOUS                        M2840
*
LABEL    DS    0CL80                   MUST BE                   S20038
VOL1     DS    CL4                     CONSTANT VOL1 FIELD        M2840
SERIAL   DS    CL6                     VOLUME SERIAL FIELD        M2840
SECURITY DS    CL1                     VOLUME SECURITY FIELD      M2840
         DS    CL69                    NOT REFERENCED             M2840
DUMMYHDR DS    CL80                    CONTIGUOUS                S20038
*
*                  THE FOLLOWING AREA DESCRIBES THE LIST          M2840
*                       PASSED TO THE LABEL SVC.  THE AREA        M2840
*                       MUST BE CONTIGUOUS AND MUST BE            M2840
*                       ALIGNED ON A FULLWORD BOUNDARY            M2840
*
SVCLST   DS    0CL20                   FIVE WORD PARM LIST        M2840
SVCRUNOP DS    CL3                     REWIND/REWIND-UNLOAD       M2840
SVCUCB   DS    CL1                     UCB INDICATOR              M2840
SVCDDPTR DS    CL4                     POINTER TO DDNAME          M2840
SVCLBLPT DS    CL4                     PTR TO LABEL/DUMMY HEADER  M2840
         DS    CL8
*
HOMEJFCB DS    CL176                   INPUT AREA FOR RDJFCB      M2840
*
CARD     DS    CL80                    SCANNED CARD INPUT AREA    M2840
COL2     EQU   CARD+1                  POSITION 2 OF CDS          M2840
COL15    EQU   CARD+14                 POSITION 15 OF CDS         M2840
COL16    EQU   CARD+15                 POSITION 16 OF CDS         M2840
COL71    EQU   CARD+70                 POSITION 71 OF CDS         M2840
COL72    EQU   CARD+71                 POSITION 72 OF CDS         M2840
*
DISP12   DS    F                       SAVE FOR START NEXT SCAN   M2840
PARMPTR  DS    F                       PTR TO START PASSED PARM   M2840
LNGPARM  DS    F                       LENGTH OF PASSED PARAM     M2840
COMTAB   DS    0F
READAD   DS    F                       ADDRESS OF READ DCB        M2840
PRINTAD  DS    F                       ADDRESS OF PRINT DCB       M2840
JFLSTAD  DS    F                       ADDR OF HOMEJFCB (READIN)  M2840
AGO      DS    F                       ADDR DUMMY DCB FOR RDJFCB  M2840
JFCBLST  DS    F
*                                                                 M2840
*                  MESSAGE LINE, INCLUDING PAGE AND LINE COUNT    M2840
*                                                                 M2840
MSGWTR   DS    0CL128                                             M2840
CC       DS    CL1                     CARRIAGE CONTROL (ASA)     M2840
MSGTEXT  DS    CL120                   PRINT LINE                 M2840
MFILL1   DS    CL1                     NOT REFERENCED             M2840
PAGENUM  DS    CL2                     STARTING PAGE NUMBER       M2840
LINETOT  DS    CL2                     TOTAL LINES PER PAGE       M2840
LINELEFT DS    CL2                     LINES NOT YET PRINTED      M2840
*                                                                 M2840
SAVE     DS    18F                     IEHINITT SAVE AREA         M2840
GPSAVE   DS    18F                     IEHSCAN SAVE AREA          M2840
SAVEX    DS    18F                     IEHPRNT SAVE AREA          M2840
SAVEY    DS    2F
SAVEZ    DS    1F
MYSAVE   EQU   SAVEY                   3 FULLWORD SAVE AREA       M2840
TCB      DS    1F                      TCB/UCB PTR SAVE AREA      M2840
*                                                                 M2840
*                  PRINT DCB                                      M2840
*                                                                 M2840
PRINT    DS    0CL96                                              M2840
PFILL1   DS    CL40                    NOT REFERENCED             M2840
PDDNAME  DS    CL8                     DDNAME BEFORE OPEN         M2840
POPENFLG DS    CL1                     OPEN FLAG                  M2840
PFILL2   DS    CL8                     NOT REFERENCED             M2840
PSYNAD   DS    CL3                     PRINTER SYNAD ADDRESS      M2840
PFILL3   DS    CL2                     NOT REFERENCED             M2840
PBLKSZ   DS    CL2                     BLOCKSIZE                  M2840
PFILL4   DS    CL18                    NOT REFERENCED             M2840
PLRECL   DS    CL2                     RECORD LENGTH              M2840
PFILL5   DS    CL12                    NOT REFERENCED             M2840
*                                                                 M2840
*                  READ DCB                                       M2840
*                                                                 M2840
READ     DS    0CL96                                              M2840
RFILL1   DS    CL33                    NOT REFERENCED             M2840
REODAD   DS    CL3                     END OF DATA ADDRESS        M2840
RFILL2   DS    CL4                     NOT REFERENCED             M2840
RDDNAME  DS    CL8                     DDNAME BEFORE OPEN         M2840
ROPENFLG DS    CL1                     OPEN FLAG                  M2840
RFILL3   DS    CL8                     NOT REFERENCED             M2840
RSYNAD   DS    CL3                     SYNAD ADDRESS              M2840
RFILL4   DS    CL36                    NOT REFERENCED             M2840
*
*                  THE AREAS HERE AND THERE ARE WORK AREAS USED   M2840
*                       FOR PACKING, UNPACKING, CONVERTING TO     M2840
*                       DECIMAL, CONVERTING TO BIANRY, FIELD      M2840
*                       ADJUSTMENT, AND OTHER WORK PURPOSES.      M2840
*
D        DS    0D
HERE     DS    CL10
THERE    DS    CL12                                               10432
*
VALFLGS  DS    CL1                     VALID FLAGS               A46764
VALCOMP  DS    CL1                     DENSITY WORK AREA         A46764
DISP5    DS    CL1                     IEHSCAN SWITCH BYTE
DISP8    DS    CL1                     IEHSCAN SWITCH BYTE
DISP10   DS    CL1                     IEHSCAN CURRENT DELIM
DISP11   DS    CL1                     IEHSCAN PREVIOUS DELIM
*
*                  LABEL DCB
*                       NOTE: THE ONLY FIELDS THAT APPLY TO
*                       THE DCB ARE GO, DDNAME, AND DCBFLAGS
*                       THESE FIELDS MUST BE AT +36, +40, AND
*                       +48 RESPECTIVELY.
*
*                       THE REMAINDER OF THE FIELDS ARE WORK AREA
*
LBLDCB   DS    0CL96                   DUMMY DCB
SWITCHRD DS    CL1                     IEHSCAN PASSBACK INDICATOR M2840
PARMLNG  DS    CL1                     PARTIAL PARM LENGTH HOLD   M2840
SWITCHAL DS    CL1                     SWITCH FOR ASCII LABEL    S20038
KEY      DS    CL1
OWNRLNG  DS    F                       HOLD FOR OWNER LENGTH     S20038
OWNRHOLD DS    CL14                    HOLD FOR OWNER PARM       S20038
DFLG     DS    X                                                   BS0H
DFLAG    DS    X                                                   BS0H
LASTDD   DS    CL8                     SAVE PREVIOUS DDNAME.      15673
RC       DS    CL1                     RETURN CODE SAVE AREA      M2840
SWITCH2  DS    CL1
NUMTPS   DS    CL1
UCBNUM   DS    CL1
GO       DC    F'0'                    SAVE AREA FOR EXLIST
DDNAM    DS    CL8                     SAVE AREA FOR DDNAME       M2840
         DC    F'0'                    SAVE AREA FOR DCBFLG,DCBMACR
PARMKEEP DS    CL44                    PARAMETER SAVE AREA        M2840
*
UCBAREA  DS    CL176                   UCB PTRS FROM DD CARD     A23445
PARMERR  DS    X                       LINECNT & PAGECNT ERROR    M2840
LAST     DS    0H
         END
