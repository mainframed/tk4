* TITLE:
*    'IEHPRNT'  - MESSAGE WRITER
* STATUS:
*    CHANGE LEVEL 000
* FUNCTION/OPERATION:
*    TO PUT A SPECIFIED MESSAGE OUT ON THE PRINTER, WRITE
*    HEADINGS AND PAGE NUMBERS. THE ROUTINE KEEP TRACK OF THE
*    CURRENT PAGE NUMBER AND THE NUMBER OF LINES LEFT BEFORE
*    A NEW PAGE IS TO BE STARTED
* ENTRY POINTS:
*    IEHPRINT IS THE ONLY ENTRY POINT.
* INPUT:
*    REGISTER 3 CONTAINS THE ADDRESS OF THE MESSAGE MINUS ONE,
*    REGISTER 12 CONTAINS THE ADDRESS OF A COMMUNICATIONS AREA,
*    REGISTER 13 CONTAINS THE ADDRESS OF A SAVE AREA, AND
*    REGISTER 14 CONTAINS THE RETURN ADDRESS
* OUTPUT:
*    THE REGISTERS ARE IN THE SAME CONDITION AS FOR INPUT
* EXTERNAL ROUTINES:
*    PUT - TO WRITE MESSAGE TO SYSPRINT DATA SET
* EXITS-NORMAL:
*    CONTROL IS RETURNED TO CALLER AFTER THE MESSAGE HAS
*    BEEN PUT OUT ON THE PRINTER
* EXITS-ERROR:
*    ERROR BIT IS TURNED ON BEFORE CONTROL IS RETURNED TO THE
*    CALLER
* TABLES/WORK AREAS:
*    TABLES CONSIST OF MESSAGES TO BE PUT OUT, AND THE
*    AREAS WHERE PAGE NUMBER AND LINES LEFT ON THIS PAGE ARE KEPT
* ATTRIBUTES:
*    REENTRANT
         EJECT
IEHPRNT  CSECT
IEHPRINT SAVE  (14,12),T
         USING IEHPRINT,PRNTBASE       BASE REGISTER FOR IEHPRNT  M2840
         USING WORK,WORKREG            PTR TO SHARED DSECT        M2840
         ST    R13,SAVEX+DSP4          SAVE CALLER'S REGISTER 13  M2840
         LR    PRNTBASE,R15            LOAD BASE                  M2840
         TM    POPENFLG,BADOPEN        WAS OPEN SUCCESSFUL        M2840
         BZ    RETURN                  RETURN TO CALLING ROUTINE  M2840
         TM    DFLG,BADOPEN            SYSPRINT BLKSIZE ERROR     M2840
         BO    RETURN                  RETURN TO CALLING ROUTINE  M2840
         LA    MSGBASE,MSGWTR          GET ADDRESS OF MSG TEXT    M2840
         TM    CC,ALLONES              IS THIS THE INITIAL ENTRY  M2840
         BZ    FRSTNTRY                YES, CONTAINS BINARY ZEROS M2840
         BAL   LINKBODY,PRINTIT        GO PRINT DETAIL LINE       M2840
         SPACE 3
RETURN   L     R13,SAVEX+DSP4          RESTORE CALLER'S REG 13    M2840
         RETURN (14,12),T,RC=0         RETURN TO CALLER           M2840
         SPACE 3
*
PRINTIT  L     DCBPTR,PRINTAD          PICK UP SYSPRINT DCB PTR   M2840
         LA    DCBPTR,0(I0,DCBPTR)     CLEAR FIRST BYTE           M2840
         LA    R13,SAVEX               POINT AT IEHPRNT SAVE AREA M2840
         PUT   (DCBPTR),(MSGBASE)      WRITE MESSAGE              M2840
*                                                                 M2840
         LH    R7,LINELEFT             GET LINES LEFT ON PAGE     M2840
         BCT   R7,NOTHDR               REDUCE BY ONE              M2840
         SPACE 3
*
* TAKE BRANCH UNTIL LINES LEFT TO PRINT BECOMES ZERO              M2840
*
         LH    R7,LINETOT              GET TOTAL LINES PER PAGE   M2840
         STH   R7,LINELEFT             REFRESH LINES LEFT COUNT   M2840
         BAL   LINKHEAD,HEADER         BRANCH TO PRINT HEADER     M2840
         BR    LINKBODY                RETURN TO CALLING ROUTINE  M2840
*
NOTHDR   STH   R7,LINELEFT             STORE DECREMENTED COUNT    M2840
         MVI   CC,BLANKCOL             SET SINGLE SPACE CODE      M2840
         BR    LINKBODY                RETURN TO CALLING ROUTINE  M2840
*
*
HEADER   MVI   CC,BLANKCOL             CLEAR FIRST CHAR OF BUFFER M2840
         MVC   MSGTEXT(L'MSGTEXT),CC   CLEAR REST OF BUFFER       M2840
         MVC   MSGTEXT+PAGEDISP(L'PAGE),PAGE  PUT 'PAGE' INTO BUF M2840
         LH    R6,PAGENUM              GET CURRENT PAGE NUMBER    M2840
         CVD   R6,HERE                 CONVERT PAGE NUMBER FROM   M2840
         UNPK  NUMB(L4),HERE+D5(L3)    BINARY TO DECIMAL          M2840
         OI    NUMB+D3,FOXZERO         SET VALID SIGN BIT         M2840
         MVC   MSGTEXT(L'TOPLINE),TOPLINE MOVE HEADING LINE       M2840
         MVI   CC,SKPTOCH1             BEGIN NEW PAGE             M2840
         L     DCBPTR,PRINTAD          ADDR OF DCB FOR SYSPRINT   M2840
         LA    DCBPTR,0(I0,DCBPTR)     CLEAR FIRST BYTE           M2840
         LA    R13,SAVEX               POINT AT IEHPRNT SAVE AREA M2840
         PUT   (DCBPTR),(MSGBASE)      PRINT HEADER               M2840
         MVI   CC,SPACE3               TRIPLE SPACE BEFORE PRINT  M2840
         LH    R6,LINELEFT             GET LINES LEFT TO PRINT    M2840
         LA    R5,ABS2                 LOAD VALUE OF '2'          M2840
         SR    R6,R5                   SUBTRACT 2 FROM LINES LEFT M2840
         STH   R6,LINELEFT             RESTORE CURRENT LINES LEFT M2840
         LH    R6,PAGENUM              GET CURRENT PAGE NUMBER    M2840
         LA    R6,BUMP1(I0,R6)         ADD ONE TO PAGE NUMBER     M2840
         STH   R6,PAGENUM              STORE UPDATED PAGE NUMBER  M2840
         BR    LINKHEAD                RETURN TO CALLING ROUTINE  M2840
*
FRSTNTRY MVC   PSYNAD,SINAD            MOVE SYNAD INTO PRINT DCB  M2840
         BAL   LINKHEAD,HEADER         GO PRINT HEADING, NEW PAGE M2840
         B     RETURN                  GO RETURN TO CALLER        M2840
*
*
WTOPER   OI    SWITCH2,BIT7            SET PRMABORT BIT           M2840
         L     R13,SAVEX+DSP4          RESTORE CALLER'S REG 13    M2840
         RETURN (14,12),T,RC=4
         EJECT
* REGISTER EQUATES
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
MSGBASE  EQU   3                       PTR TO MSG PORTION OF WORK M2840
DCBPTR   EQU   4                       POINTER TO SYSPRINT DCB    M2840
R5       EQU   5
R6       EQU   6
R7       EQU   7
LINKHEAD EQU   8                       LINK TO PRINT HEADER LINE  M2840
LINKBODY EQU   9                       LINK TO PRINT DETAIL LINE  M2840
WORKREG  EQU   10                      PTR TO GETMAIN WORK AREA   M2840
PRNTBASE EQU   11                      BASE REGISTER FOR IEHPRNT  M2840
COMBASE  EQU   12                      WORK DSECT SUBSECTION PTR  M2840
R13      EQU   13
R14      EQU   14
R15      EQU   15                      AT LINK, HAS ADDR OF EP    M2840
         SPACE 3
Z        DS    0F
PAGE     DC    CL4'PAGE'
TOPLINE  DC    CL38'SYSTEM SUPPORT UTILITIES      IEHINITT'       M2840
Y        DS    0D
EIGHT    DC    F'8'
SINAD    DC    AL3(WTOPER)
         SPACE 3
D3       EQU   3                       DISPLACEMENT VALUE         M2840
DSP4     EQU   4                       DISPLACEMENT VALUE         M2840
D5       EQU   5                       DISPLACEMENT VALUE         M2840
L3       EQU   3                       LENGTH ATTRIBUTE           M2840
L4       EQU   4                       LENGTH ATTRIBUTE           M2840
ALLONES  EQU   X'FF'                   TEST MASK FOR BINARY ZEROS M2840
BLANKCOL EQU   X'40'                   TO CLEAR PRINT AREA        M2840
BADOPEN  EQU   X'10'                   SYSPRINT ERROR FLAG        M2840
PAGEDISP EQU   98                      LOCATION OF PAGE CONSTANT  M2840
SKPTOCH1 EQU   X'F1'                   ASA IMMEDIATE SKIP TO CH 1 M2840
SPACE3   EQU   X'60'                   ASA SPACE 3 BEFORE PRINT   M2840
ABS2     EQU   2                       TO DECREMENT LINES LEFT    M2840
BUMP1    EQU   1                       TO INCREMENT REGISTERS     M2840
FOXZERO  EQU   X'F0'                   MASK TO CHECK FOR NUMERICS M2840
BIT7     EQU   X'01'                   SET BIT FOR PERM ABORT     M2840
I0       EQU   0                       INDEX VALUE OF ZERO FOR LA M2840
*
*
*                 THE FOLLOWING DSECT REPRESENTS THAT PORTION     M2840
*                       OF THE COMMON WORK AREA OBTAINED BY       M2840
*                       IEHINITT THAT IS USED BY IEHPRNT          M2840
*
*
WORK     DSECT 0F
FIRST    DS    0F
*
*                  THE FOLLOWING AREA DESCRIBES THE VOLUME        M2840
*                       LABEL (80 BYTES) AND DUMMY HDR1           M2840
*                       RECORD (80 BYTES).  THESE AREAS           M2840
*                       MUST BE CONTIGUOUS                        M2840
*
LABEL    DS    0CL80                   MUST BE                   S20038
VOL1     DS    CL4                     CONSTANT VOL1 FIELD        M2840
SERIAL   DS    CL6                     VOLUME SERIAL FIELD        M2840
SECURITY DS    CL1                     VOLUME SECURITY FIELD      M2840
         DS    CL69                    NOT REFERENCED             M2840
DUMMYHDR DS    CL80                    CONTIGUOUS                S20038
*
*                  THE FOLLOWING AREA DESCRIBES THE LIST          M2840
*                       PASSED TO THE LABEL SVC.  THE AREA        M2840
*                       MUST BE CONTIGUOUS AND MUST BE            M2840
*                       ALIGNED ON A FULLWORD BOUNDARY            M2840
*
SVCLST   DS    0CL20                   FIVE WORD PARM LIST        M2840
SVCRUNOP DS    CL3                     REWIND/REWIND-UNLOAD       M2840
SVCUCB   DS    CL1                     UCB INDICATOR              M2840
SVCDDPTR DS    CL4                     POINTER TO DDNAME          M2840
SVCLBLPT DS    CL4                     PTR TO LABEL/DUMMY HEADER  M2840
         DS    CL8
*
HOMEJFCB DS    CL176                   INPUT AREA FOR RDJFCB      M2840
*
CARD     DS    CL80                    SCANNED CARD INPUT AREA    M2840
COL2     EQU   CARD+1                  POSITION 2 OF CDS          M2840
COL15    EQU   CARD+14                 POSITION 15 OF CDS         M2840
COL16    EQU   CARD+15                 POSITION 16 OF CDS         M2840
COL71    EQU   CARD+70                 POSITION 71 OF CDS         M2840
COL72    EQU   CARD+71                 POSITION 72 OF CDS         M2840
*
DISP12   DS    F                       SAVE FOR START NEXT SCAN   M2840
PARMPTR  DS    F                       PTR TO START PASSED PARM   M2840
LNGPARM  DS    F                       LENGTH OF PASSED PARAM     M2840
COMTAB   DS    0F
READAD   DS    F                       ADDRESS OF READ DCB        M2840
PRINTAD  DS    F                       ADDRESS OF PRINT DCB       M2840
JFLSTAD  DS    F                       ADDR OF HOMEJFCB (READIN)  M2840
AGO      DS    F                       ADDR DUMMY DCB FOR RDJFCB  M2840
JFCBLST  DS    F
*                                                                 M2840
*                  MESSAGE LINE, INCLUDING PAGE AND LINE COUNT    M2840
*                                                                 M2840
MSGWTR   DS    0CL128                                             M2840
CC       DS    CL1                     CARRIAGE CONTROL (ASA)     M2840
MSGTEXT  DS    CL120                   PRINT LINE                 M2840
MFILL1   DS    CL1                     NOT REFERENCED             M2840
PAGENUM  DS    CL2                     STARTING PAGE NUMBER       M2840
LINETOT  DS    CL2                     TOTAL LINES PER PAGE       M2840
LINELEFT DS    CL2                     LINES NOT YET PRINTED      M2840
NUMB     EQU   MSGWTR+106              LOCATION OF PAGE NUMBER    M2840
*                                                                 M2840
SAVE     DS    18F                     IEHINITT SAVE AREA         M2840
GPSAVE   DS    18F                     IEHSCAN SAVE AREA          M2840
SAVEX    DS    18F                     IEHPRNT SAVE AREA          M2840
SAVEY    DS    2F
SAVEZ    DS    1F
MYSAVE   EQU   SAVEY                   3 FULLWORD SAVE AREA       M2840
TCB      DS    1F                      TCB/UCB PTR SAVE AREA      M2840
*                                                                 M2840
*                  PRINT DCB                                      M2840
*                                                                 M2840
PRINT    DS    0CL96                                              M2840
PFILL1   DS    CL40                    NOT REFERENCED             M2840
PDDNAME  DS    CL8                     DDNAME BEFORE OPEN         M2840
POPENFLG DS    CL1                     OPEN FLAG                  M2840
PFILL2   DS    CL8                     NOT REFERENCED             M2840
PSYNAD   DS    CL3                     PRINTER SYNAD ADDRESS      M2840
PFILL3   DS    CL2                     NOT REFERENCED             M2840
PBLKSZ   DS    CL2                     BLOCKSIZE                  M2840
PFILL4   DS    CL18                    NOT REFERENCED             M2840
PLRECL   DS    CL2                     RECORD LENGTH              M2840
PFILL5   DS    CL12                    NOT REFERENCED             M2840
*                                                                 M2840
*                  READ DCB                                       M2840
*                                                                 M2840
READ     DS    0CL96                                              M2840
RFILL1   DS    CL33                    NOT REFERENCED             M2840
REODAD   DS    CL3                     END OF DATA ADDRESS        M2840
RFILL2   DS    CL4                     NOT REFERENCED             M2840
RDDNAME  DS    CL8                     DDNAME BEFORE OPEN         M2840
ROPENFLG DS    CL1                     OPEN FLAG                  M2840
RFILL3   DS    CL8                     NOT REFERENCED             M2840
RSYNAD   DS    CL3                     SYNAD ADDRESS              M2840
RFILL4   DS    CL36                    NOT REFERENCED             M2840
*
*                  THE AREAS HERE AND THERE ARE WORK AREAS USED   M2840
*                       FOR PACKING, UNPACKING, CONVERTING TO     M2840
*                       DECIMAL, CONVERTING TO BIANRY, FIELD      M2840
*                       ADJUSTMENT, AND OTHER WORK PURPOSES.      M2840
*
D        DS    0D
HERE     DS    CL10
THERE    DS    CL12                                               10432
VALFLGS  DS    CL1                     VALID FLAGS                M2840
VALCOMP  DS    CL1                     DENSITY WORK AREA          M2840
DISP5    DS    CL1                     IEHSCAN SWITCH BYTE        M2840
DISP8    DS    CL1                     IEHSCAN SWITCH BYTE        M2840
DISP10   DS    CL1                     IEHSCAN CURRENT DELIM      M2840
DISP11   DS    CL1                     IEHSCAN PREVIOUS DELIM     M2840
*
*                  LABEL DCB
*                       NOTE: THE ONLY FIELDS THAT APPLY TO
*                       THE DCB ARE GO, DDNAME, AND DCBFLAGS
*                       THESE FIELDS MUST BE AT +36, +40, AND
*                       +48 RESPECTIVELY.
*
*                       THE REMAINDER OF THE FIELDS ARE WORK AREA
*
LBLDCB   DS    0CL96                   DUMMY DCB                  M2840
SWITCHRD DS    CL1                     IEHSCAN PASSBACK INDICATOR M2840
PARMLNG  DS    CL1                     PARTIAL PARM LENGTH HOLD   M2840
SWITCHAL DS    CL1                     SWITCH FOR ASCII LABEL    S20038
KEY      DS    CL1
OWNRLNG  DS    CL4                     HOLD FOR OWNER LENGTH     S20038
OWNRHOLD DS    CL14                    HOLD FOR OWNER PARM       S20038
DFLG     DS    X                                                   BS0H
DFLAG    DS    X                                                   BS0H
LASTDD   DS    CL8                     SAVE PREVIOUS DDNAME.      15673
RC       DS    CL1                     RETURN CODE SAVE AREA      M2840
SWITCH2  DS    CL1
NUMTPS   DS    CL1
UCBNUM   DS    CL1
GO       DC    F'0'                    SAVE AREA FOR EXLIST
DDNAM    DS    CL8                     SAVE AREA FOR DDNAME       M2840
         DC    F'0'                    SAVE AREA FOR DCBFLG,DCBMACR
PARMKEEP DS    CL44                    PARAMETER SAVE AREA        M2840
*
UCBAREA  DS    CL176                   UCB PTRS FROM DD CARD     A23445
PARMERR  DS    X                       LINECNT & PAGECNT ERROR    M2840
LAST     DS    0H
         END
