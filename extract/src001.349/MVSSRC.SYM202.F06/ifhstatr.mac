IFH0     TITLE 'IFHSTATR - O/S VOLUME STATISTICS PRINT UTILITY'
         SPACE 3
         SPACE 3
***********************************************************************
*                                                                     *
*TITLE: 'IFHSTATR' - O/S VOLUME STATISTICS PRINT UTILITY              *
*                                                                     *
*STATUS: CHANGE LEVEL 2                                               *
*                                                                     *
*FUNCTION: THIS PROGRAM READS,FORMATS AND PRINTS TYPE 15 RECORDS      *
*   FROM A SYS1.MAN DATA SET.                                         *
*                                                                     *
*ENTRY: IFHSTATR                                                      *
*                                                                     *
*INPUT: SYS1.MAN DATA SET                                             *
*                                                                     *
*OUTPUT: 121 CHARACTER RECORDS WITH USASI CARRIAGE CONTROL CHARACTERS *
*   IN THE FIRST BYTE ARE WRITTEN TO SYSUT2.                          *
*                                                                     *
*EXITS-NORMAL: RETURN TO SUPERVISOR                                   *
*                                                                     *
*EXITS-ERROR: NONE                                                    *
*                                                                     *
*ATTRIBUTES: NONE                                                     *
*                                                                     *
***********************************************************************
         EJECT
IFHSTATR CSECT
*A099000                                                         A32893
*C024500                                                         A32893
*A041100                                                         A32896
*C137500,040000,040500,175000                                    A32896
*D148500                                                         A32896
*A 034100,035500-035800,098100,176700-176940                     A40940
*C 034500                                                        A40940
*D 035500                                                        A40940
*C 041100,137500                                                 A53648
*A 148500                                                        A53648
*C 029000,090000                                                YA01697
*A 083020,112100                                               @ZA13723
         SPACE
***********************************************************************
*                      REGISTER EQUATES AND USAGE                     *
***********************************************************************
         SPACE
R0       EQU   0                       NOT USED
R1       EQU   1                       WORK
R2       EQU   2                       WORK
R3       EQU   3                       WORK
R4       EQU   4                       WORK
R5       EQU   5                       WORK
R6       EQU   6                       WORK
R7       EQU   7                       WORK
R8       EQU   8                       WORK
R9       EQU   9                       WORK
R10      EQU   10                      LINE COUNT
R11      EQU   11                      RETURN REG. FOR INTERNAL PROC.
R12      EQU   12                      BASE
R13      EQU   13                      SAVE AREA POINTER
R14      EQU   14                      RETURN REGISTER
R15      EQU   15                      USED FOR A RETURN CODE    A32893
         SPACE 3
***********************************************************************
*                      CONSTANT EQUATES AND USAGE                     *
***********************************************************************
         SPACE 2
BLANK    EQU   C' '                    USED FOR ZERO SUPPRESSION
ZERO     EQU   C'0'                    USED FOR TESTING FOR ZEROS
TYPE15   EQU   X'15'                   RECORD TYPE
CODE200  EQU   X'03'               MASK FOR TESTING FOR 200 BPI YM07126
CODE556  EQU   X'40'                   MASK FOR TESTING FOR 556 BPI
CODE800  EQU   X'80'                   MASK FOR TESTING FOR 800 BPI
CODE1600 EQU   X'C3'                   SETMODE FOR 1600 BPI     YM07124
CODE6250 EQU   X'D3'                   SETMODE FOR 6250 BPI     YM07124
ONE      EQU   1
TWO      EQU   2
FOUR     EQU   4
SIXTEEN  EQU   16
TWENTY   EQU   20
HUNDRED  EQU   100
         EJECT
         STM   R14,R12,12(R13)         SAVE REGISTERS            A40940
         BALR  R12,0                   ESTABLISH BASE            A40940
         USING *,R12
         ST    R13,SAVE+4                                        A40940
         LA    R15,SAVE                                          A40940
         ST    R15,8(R13)                                        A40940
         LR    R13,R15                                           A40940
         SPACE
         OPEN  (SYSUT1,(INPUT),SYSUT2,(OUTPUT))
         TIME  DEC                     GET CURRENT DATE IN REG 1
         ST    R1,WORK                 STORE IN WORK AREA
         UNPK  WORK+8(5),WORK+1(3)     UNPACK CURRENT DATE
         MVC   CDATE(2),WORK+8         MOVE YEAR TO HEADING
         MVC   CDATE+3(3),WORK+10      MOVE DAY TO HEADING AFTER SLASH
NEWPAGE  L     R10,FIFTY               NUMBER OF DATA LINES PER PAGE
GETDATA  GET   SYSUT1                  READ A RECORD             A32896
         CLI   5(R1),TYPE15            TYPE '15' RECORD ?        A32896
         BNE   GETDATA                 NO, READ ANOTHER RECORD
         MVC   INAREA(RCRDEND-INAREA),0(R1) MOVE RCRD TO WRK     A53648
         CL    R10,FIFTY               IS THIS THE 1ST LINE ON PAGE ?
         BNE   CONVERT                 NO, DON'T PRINT HEADING
HEADING  MVI   DATAFLAG,TYPE15         INDICATE AT LEAST ONE RCD FOUND
         PUT   SYSUT2,LINE1            PRINT FIRST LINE OF HEADING
         PUT   SYSUT2,LINE2            PRINT SECOND LINE OF HEADING
         PUT   SYSUT2,LINE3            PRINT THIRD LINE OF HEADING
         PUT   SYSUT2,LINE4            PRINT FOURTH LINE OF HEADING
         EJECT
***********************************************************************
*     MOVE DATA FROM INPUT AREA TO PRINT AREA, MAKING NECESSARY       *
*     CONVERSIONS FOR PRINTING THE DATA.                              *
***********************************************************************
         SPACE
CONVERT  MVC   VOLID(6),IVOLID         MOVE VOLUME-SERIAL TO OUT AREA
         SPACE
         UNPK  WORK(8),IDATE(5)        UNPACK DATE
         MVC   YEAR(2),WORK+1          MOVE DATE
         MVC   DAY(3),WORK+3                     TO OUT AREA
         SPACE
         MVC   CPUID(2),ICPUID         MOVE CPU ID TO OUT AREA
         SPACE
         MVC   MODNO(2),IMODNO         MOVE CPU MODEL NO. TO OUT AREA
         SPACE
         MVC   WORK(4),ITIME           MOVE TIME TO WORD BOUNDRY
         SR    R4,R4                   CLEAR EVEN REGISTER
         L     R5,WORK                 LOAD ODD REGISTER WITH TIME
         LA    R6,HUNDRED              DIVISOR FOR DIVIDE OPERATION
         DR    R4,R6                   ELEMINATE HUNDREDTHS OF SECONDS
         SR    R4,R4                   CLEAR EVEN REGISTER
         D     R4,SIXTY                SEC IN REG 4, MIN IN REG 5
         CVD   R4,WORK                 CONVERT SECONDS
         MVC   WORK+10(2),WORK+6       SAVE PACKED SECONDS -XXXXSSS-
         SR    R4,R4                   CLEAR REGISTER
         D     R4,SIXTY                MIN IN REG 4, HOURS IN REG 5
         CVD   R4,WORK                 CONVERT MINUTES
         MVO   WORK+8(2),WORK+6(2)     MM+XSSS-
         MVO   WORK+8(3),WORK+8(1)     000MMSS-
         CVD   R5,WORK                 CONVERT HOURS
         L     R5,WORK+4               PACKED DECIMAL HOURS IN REG 5
         SRL   R5,FOUR                 ELIMINATE SIGN FROM HOURS
         SLL   R5,TWENTY               SHIFT TO HOURS POSITION
         O     R5,WORK+8               0HHMMSS- IN REG 5
         ST    R5,WORK+20              STORE CONVERTED TIME
         UNPK  WORK(8),WORK+20(5)      UNPACK TIME
         MVC   HOURS(2),WORK           MOVE
         MVC   MINUTES(2),WORK+2            TIME TO
         MVC   SECONDS(2),WORK+4                    OUT AREA
         SPACE
         UNPK  WORK(4),IADDRESS(3)     MAKE CHANNEL/UNIT PRINTABLE
         TR    WORK(3),TRTABLE
         MVC   ADDRESS(3),WORK         MOVE CHAN & UNIT TO OUT AREA
         SPACE
         LA    R1,ITREAD               LOAD ADDRESS OF COUNTER
         BAL   R11,ONEBYTE             BRANCH TO CONVERT 1 BYTE COUNTER
         MVC   TREAD(3),WORK           MOVE CONVERTED COUNTER TO OUTPUT
         EJECT
         LA    R1,ITWRITE              LOAD ADDRESS OF COUNTER
         BAL   R11,ONEBYTE             BRANCH TO CONVERT 1 BYTE COUNTER
         MVC   TWRITE(3),WORK          MOVE CONVERTED COUNTER TO OUTPUT
         SPACE
         LA    R1,ISIO                 LOAD ADDRESS OF COUNTER
         BAL   R11,TWOBYTE             BRANCH TO CONVERT 2 BYTE COUNTER
         MVC   SIO(5),WORK             MOVE CONVERTED COUNTER TO OUTPUT
         SPACE
         LA    R1,IPREAD               LOAD ADDRESS OF COUNTER
         BAL   R11,ONEBYTE             BRANCH TO CONVERT 1 BYTE COUNTER
         MVC   PREAD(3),WORK           MOVE CONVERTED COUNTER TO OUTPUT
         SPACE
         LA    R1,IPWRITE              LOAD ADDRESS OF COUNTER
         BAL   R11,ONEBYTE             BRANCH TO CONVERT 1 BYTE COUNTER
         MVC   PWRITE(3),WORK          MOVE CONVERTED COUNTER TO OUTPUT
         SPACE
         LA    R1,INOISE               LOAD ADDRESS OF COUNTER
         BAL   R11,ONEBYTE             BRANCH TO CONVERT 1 BYTE COUNTER
         MVC   NOISE(3),WORK           MOVE CONVERTED COUNT           R
         SPACE
         LA    R1,IERASE               LOAD ADDRESS OF COUNTER
         BAL   R11,TWOBYTE             BRANCH TO CONVERT 2 BYTE COUNTER
         MVC   ERASE(5),WORK           MOVE CONVERTED COUNTER TO OUTPUT
         SPACE
         LA    R1,ICLEANER             LOAD ADDRESS OF COUNTER
         BAL   R11,TWOBYTE             BRANCH TO CONVERT 2 BYTE COUNTER
         MVC   CLEANER(5),WORK         MOVE CONVERTED COUNTER TO OUTPUT
         SPACE
         MVC   DENSITY1,DENBLNK        CLEAR DENSITY FIELD     @ZA13723
BPI6250  CLI   IDENSITY,CODE6250       IS DENSITY = 6250 BPI ?  YM07124
         BNE   BPI1600                 NO, BRANCH               YM07124
         MVC   DENSITY(4),DEN6250      YES-MOVE DEN TO PRINT    YM07124
         B     CONVBLK                                          YM07124
BPI1600  CLI   IDENSITY,CODE1600       IS DENSITY = 1600 BPI ?  YM07124
         BNE   BPI800                  NO, BRANCH               YM07124
         MVC   DENSITY(4),DEN1600      YES, MOVE DENSITY TO PRINT AREA
         B     CONVBLK
BPI800   TM    IDENSITY,CODE800        IS DENSITY = 800 BPI ?
         BNO   BPI556                  NO, BRANCH
         MVC   DENSITY(4),DEN800       YES, MOVE DENSITY TO PRINT AREA
         B     CONVBLK
BPI556   TM    IDENSITY,CODE556        IS DENSITY = 556 BPI ?
         BNO   BPI200                  NO, BRANCH
         MVC   DENSITY(4),DEN556       YES, MOVE DENSITY TO PRINT AREA
         B     CONVBLK
BPI200   TM    IDENSITY,CODE200        IS DENSITY = 200 BPI ?
         BNO   BPI0000                 NO,BRANCH                YM07126
         MVC   DENSITY(4),DEN200       YES, MOVE DENSITY TO PRINT AREA
         B     CONVBLK
BPI0000  MVC   DENSITY1,DEN0000        DENSITY IS UNKNOWN       YM07126
         SPACE
CONVBLK  LA    R1,IBLOCK               LOAD ADDRESS OF COUNTER
         BAL   R11,TWOBYTE             BRANCH TO CONVERT 2 BYTE COUNTER
         MVC   BLOCK(5),WORK           MOVE CONVERTED COUNTER TO OUTPUT
         EJECT
PUTDATA  PUT   SYSUT2,DATALINE         PRINT 1 LINE OF OUTPUT
         BCT   R10,GETDATA             BRANCH UNTIL PAGE IS FULL
         B     NEWPAGE                 START NEW PAGE
FINISHED CLI   DATAFLAG,TYPE15         WERE THERE ANY TYPE 15 RECORDS ?
         BE    DONE                    YES, NORMAL EOJ
         PUT   SYSUT2,ERRLINE          NO, PUT OUT ERROR MSG.
DONE     CLOSE (SYSUT1,,SYSUT2)
         L     R13,SAVE+4                                        A40940
         LM    R14,R12,12(R13)         RESTORE REGISTERS
         SR    R15,R15                 SET RETURN CODE OF ZERO   A32893
         BR    R14                     RETURN TO SUPERVISOR
ONEBYTE  LA    R2,TWO                  NO. DECIMAL DIGITS - 1
         SR    R3,R3                   CLEAR WORK REGISTER
         IC    R3,0(0,R1)              LOAD COUNTER INTO REGISTER
         CVD   R3,WORK                 CONVERT TO DECIMAL
         UNPK  WORK(5),WORK+6(3)       UNPACK COUNTER + 1 BYTE
         B     SUPPRESS
TWOBYTE  LA    R2,FOUR                 NO. DECIMAL DIGITS - 1
         MVC   WORK(2),0(R1)           MOVE COUNTER TO WORD BOUNDRY
         L     R3,WORK                 LOAD 2 BYTE COUNTER
         SRL   R3,SIXTEEN              SHIFT TO LOW ORDER POSITION
         CVD   R3,WORK+8               CONVERT TO DECIMAL
         UNPK  WORK(7),WORK+13(4)      UNPACK COUNTER + 1 BYTE
SUPPRESS LA    R5,WORK                 LOAD BASE REG FOR CLI & MVI
LOOP     CLI   0(R5),ZERO              IS THIS A LEADING ZERO ?
         BCR   7,R11                   RETURN TO CALLER ON 1ST NON-ZERO
         MVI   0(R5),BLANK             BLANK OUT LEADING ZEROS
         LA    R5,1(R5)                INCREMENT BASE REG TO NEXT DIGIT
         BCT   R2,LOOP                 BRANCH IF ALL DIGITS NOT CHECKED
         BR    R11                     RETURN TO CALLER
         EJECT
***********************************************************************
*                            CONSTANTS                                *
***********************************************************************
         SPACE
FIFTY    DC    F'50'                   NUMBER OF DATA LINES PER PAGE
DENBLNK  DC    CL7'       '            CLEAR DENSITY FIELD     @ZA13723
DEN0000  DC    CL7'UNKNOWN'            DENSITY NOT AVAILABLE    YM07126
DEN200   DC    C' 200'                 200 BPI
DEN556   DC    C' 556'                 556 BPI
DEN800   DC    C' 800'                 800 BPI
DEN1600  DC    C'1600'                 1600 BPI
DEN6250  DC    C'6250'                 6250 BPI                 YM07124
WORK     DS    4D                      WORK AREA FOR CONVERTING
TRTABLE  EQU   *-X'F0'
         DC    C'0123456789ABCDEF'     TRANSLATE TABLE
SIXTY    DC    F'60'                   DIVISOR FOR TIME CALCULATIONS
DATAFLAG DC    X'00'                   INDICATOR FOR TYPE 15 RECORDS
***********************************************************************
*                  HEADINGS FOR OUTPUT PAGE                           *
***********************************************************************
         SPACE 1
LINE1    DS    0CL121
         DC    CL1'1'                  CARRIAGE CONTROL FOR A NEW PAGE
         DC    CL40' '
         DC    CL40'MAGNETIC TAPE ERROR STATISTICS BY VOLUME'
         DC    CL34' '
CDATE    DC    CL6'  /   '             CURRENT DATE IN THE FORM YY/DDD
         SPACE 1
LINE2    DS    0CL121
         DC    CL1'0'                  CARRIAGE CONTROL TO SKIP 2 LINE
         DC    CL40'VOLUME          CPU  MOD    TIME    CHAN'
         DC    CL40'NEL  TEMP   TEMP   PERM  PERM   NOISE   '
         DC    CL40'ERASE  CLEANER   USAGE    TAPE    BLOCK '
         SPACE 1
LINE3    DS    0CL121
         DC    CL1' '                  CARRIAGE CONTROL TO SKIP 1 LINE
         DC    CL40'SERIAL   DATE    ID   NO   OF DAY   / UN'
         DC    CL40'IT   READ   WRITE  READ  WRITE  BLOCKS  '
         DC    CL40' GAPS  ACTIONS  (SIO''S)  DENSITY  LENGTH'
         SPACE 1
LINE4    DS    0CL121
         DC    CL1'+'                  CARRIAGE CONTROL TO OVERPRINT
         DC    CL40'______  ______  ___  ___  ________  ____'
         DC    CL40'___  ____   _____  ____  _____  ______  '
         DC    CL40'_____  _______  _______  _______  ______'
         SPACE
ERRLINE  DS    0CL121
         DC    CL1'-'                  CARRIAGE CONTROL TO SKIP 3 LINES
         DC    CL40' '
         DC    CL40'            NO ESV RECORDS FOUND        '
         DC    CL40' '
         EJECT
***********************************************************************
*            WORK AREA FOR STORING 1 LOGICAL INPUT RECORD             *
***********************************************************************
         SPACE
INAREA   DS    0C                      FORMAT OF TYPE '15' RCD   A53648
IRECLEN  DS    CL2                     LENGTH OF DATA RECORD
SKIP1    DS    CL3                     IGNORE 3 BYTE ZERO FIELD
IRECTYPE DS    CL1                     TYPE OF DATA RECORD
ITIME    DS    CL4                     TIME FROM GETTIME BINARY MACRO
IDATE    DS    CL4                     DATE IN THE FORM 00YYDDDX
ICPUID   DS    CL2                     CPU IDENTIFICATION
IMODNO   DS    CL2                     MODEL NUMBER OF CPU
SKIP2    DS    CL2                     IGNORE 2 BYTE FIELD
IVOLID   DS    CL6                     VOLUME-SERIAL NUMBER
IADDRESS DS    CL2                     CHANNEL & UNIT IN THE FORM 0CUU
SKIP3    DS    CL4                     IGNORE 4 BYTE UCB TYPE FIELD
ITREAD   DS    CL1                     TEMPORARY READ ERROR COUNTER
ITWRITE  DS    CL1                     TEMPORARY WRITE ERROR COUNTER
ISIO     DS    CL2                     START I/O COUNTER
IPREAD   DS    CL1                     PERMANENT READ ERROR COUNTER
IPWRITE  DS    CL1                     PERMANENT WRITE ERROR COUNTER
INOISE   DS    CL1                     NOISE BLOCK COUNTER
IERASE   DS    CL2                     ERASE GAP COUNTER
ICLEANER DS    CL2                     CLEANER ACTION COUNTER
IDENSITY DS    CL1                     DENSITY OF TAPE IN ERROR
IBLOCK   DS    CL2                     BLOCK-SIZE OF TAPE IN ERROR
RCRDEND  EQU   *                       END OF RECORD             A53648
         EJECT
         SPACE 3
***********************************************************************
*            WORK AREA FOR CONSTRUCTION OF 1 LINE OF OUTPUT           *
***********************************************************************
         SPACE
DATALINE DS    0CL121                  FORMAT OF 1 LINE OF OUTPUT
CONTROL  DC    CL1' '                  CARRIAGE CONTROL FOR SINGLE SP.
VOLID    DS    CL6                     VOLUME-SERIAL NUMBER
         DC    CL2' '                  SPACING
YEAR     DS    CL2                     *
SLASH    DC    CL1'/'                  DATE IN THE FORM YY/DDD
DAY      DS    CL3                     *
         DC    CL3' '                  SPACING
CPUID    DS    CL2                     CPU IDENTIFICATION
         DC    CL3' '                  SPACING
MODNO    DS    CL2                     CPU MODEL NUMBER
         DC    CL2' '                  SPACING
HOURS    DS    CL2                     *
COLON1   DC    CL1':'                  *
MINUTES  DS    CL2                     TIME IN THE FORM HH:MM:SS
COLON2   DC    CL1':'                  *
SECONDS  DS    CL2                     *
         DC    CL4' '                  SPACING
ADDRESS  DS    CL3                     CHANNEL & UNIT IN THE FORM CUU
         DC    CL5' '                  SPACING
TREAD    DS    CL3                     TEMPORARY READ ERROR COUNTER
         DC    CL4' '                  SPACING
TWRITE   DS    CL3                     TEMPORARY WRITE ERROR COUNTER
         DC    CL4' '                  SPACING
PREAD    DS    CL3                     PERMANENT EAD ERROR COUNTER
         DC    CL3' '                  SPACING
PWRITE   DS    CL3                     PERMANENT WRITE ERROR COUNTER
         DC    CL4' '                  SPACING
NOISE    DS    CL3                     NOISE BLOCK COUNTER
         DC    CL4' '                  SPACING
ERASE    DS    CL5                     ERASE GAP COUNTER
         DC    CL3' '                  SPACING
CLEANER  DS    CL5                     CLEANER ACTION COUNTER
         DC    CL3' '                  SPACING
SIO      DS    CL5                     START I/O COUNTER
         DC    CL4' '                  SPACING
DENSITY1 DS    0CL7                     UNKNOWN DENSITY FIELD   YM07126
         DS    CL2' '                   SPACING                 YM07126
DENSITY  DS    CL4                     DENSITY OF TAPE HAVING ERROR
         DC    CL4' '                  SPACING
BLOCK    DS    CL5                     BLOCK SIZE OF TAPE HAVING ERROR
         DC    CL1' '                  SPACING
         EJECT
***********************************************************************
*                                    DCB'S                            *
***********************************************************************
         SPACE
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,EODAD=FINISHED,EROPT=SKP,        X
               MACRF=(GL),RECFM=VBS,BFTEK=A                      A32896
         SPACE 3
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(PM),RECFM=AFB,            X
               LRECL=121,BLKSIZE=121
         SPACE 2
***********************************************************************
*                IFHSTATR'S GENERAL PURPOSE SAVE AREA                 *
***********************************************************************
         SPACE 2
SAVE     DS    18F                                               A40940
         SPACE 2
         END
