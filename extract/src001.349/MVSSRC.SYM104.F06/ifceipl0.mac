*  LINE MACRO     LINE MACRO     LINE MACRO     LINE MACRO            *
         MACRO
&NAME    LINE  &DATA,&LABEL,&SKIP=
.*                            THIS MACRO ESTABLISHES THE MARGINS OR
.*                                 COLUMN SETTINGS FOR EACH LINE. IT
.*                                 GENERATES NECESSARY CARRIAGE CONTROL
.*                                 ENTRIES. LASTLY IT DETERMINES THE
.*                                 NUMBER OF ENTRIES ASSOCIATED WITH
.*                                 THE PREVIOUS LINE AND STORES THAT #
.*                                 INTO THE APPROPRIATE BYTE.
.*                            &DATA(1) - CHARACTER POSITION TO BEGIN
.*                                 DATA.
.*                            &DATA(2) - # OF SPACES BETWEEN SUCCESSIVE
.*                                 DATA COLUMN STARTING CHARACTERS.
.*                            &LABEL(1) - CHAR. POS. TO BEGIN LABELS.
.*                            &LABEL(2) - # SPACES BETWEEN LINE'S LABEL
.*                                 STARTING CHARACTERS.
.*                            &SKIP= - # OF LINES TO ADVANCE CARRIAGE
.*                                 TO BEGIN NEXT PRINTED LINE.
.*                            NEW ENTRIES ARE GENERATED ONLY WHEN
.*                                 NECESSARY. OMISSION OF ITEMS MEANS
.*                                 THEY HAVE NOT CHANGED SINCE PREVIOUS
.*                                 SETTINGS. A LINE MACRO IS REQUIRED
.*                                 FOR EACH PRINTED LINE. IF NO SETTING
.*                                 CHANGES ARE REQUIRED ALL OPERANDS
.*                                 ARE OMITTED IN WHICH CASE ONLY THE
.*                                 # OF ENTRIES FOR THE PREVIOUS LINE
.*                                 IS COMPUTED. THIS MACRO MUST BE THE
.*                                 FIRST MACRO INSTRUCTION ISSUED FOR
.*                                 THE SUBJECT LINE
         GBLA  &DB,&DT,&LB,&LT,&NRENTS
         GBLA  &LASTCC,&INITIAL
         GBLC  &NRENTSX
         GBLC  &CARRCON
         GBLB  &TEST
         LCLA  &S
&NAME    DS    0C
         AIF   (&INITIAL EQ 0).FIRSTLN
         LINEND
         AGO   .NTFIRST
.FIRSTLN ANOP
&NRENTSX SETC  'N&SYSNDX'
&INITIAL SETA  1
N&SYSNDX EQU   *
.NTFIRST ANOP
         DC    AL1(0)
         AIF   ('&DATA(1)' EQ '').CKDTABS
         AIF   (&DATA(1) EQ &DB).CKDTABS
&DB      SETA  &DATA(1)
         AGO   .AA
.CKDTABS AIF   ('&DATA(2)' EQ '').NCDATA
         AIF   (&DATA(2) EQ &DT).NCDATA
         AGO   .CC
.AA      AIF   ('&DATA(2)' EQ '').BB
         AIF   (&DATA(2) EQ &DT).BB
.CC      ANOP
&DT      SETA  &DATA(2)
.BB      DC    XL2'FFFE'
         DC    FL1'&DB'
         DC    FL1'&DT'
&NRENTS  SETA  &NRENTS+1
.NCDATA  ANOP
         AIF   ('&LABEL(1)' EQ '').CKLTABS
         AIF   (&LABEL(1) EQ &LB).CKLTABS
&LB      SETA  &LABEL(1)
         AGO   .AAA
.CKLTABS AIF   ('&LABEL(2)' EQ '').NCLABEL
         AIF   (&LABEL(2) EQ &LT).NCLABEL
         AGO   .CCC
.AAA     AIF   ('&LABEL(2)' EQ '').BBB
         AIF   (&LABEL(2) EQ &LT).BBB
.CCC     ANOP
&LT      SETA  &LABEL(2)
.BBB     DC    XL2'FFFF'
         DC    FL1'&LB'
         DC    FL1'&LT'
&NRENTS  SETA  &NRENTS+1
.NCLABEL ANOP
         AIF   ('&SKIP' NE '').CONT
         AIF   (&TEST EQ 0).NOCHANG
&S       SETA  &LASTCC
         AGO   .DEFAULT
.CONT    ANOP
         AIF   (&TEST EQ 0).AB
&S       SETA  &SKIP
.DEFAULT ANOP
         ORG   &CARRCON+3
         DC    AL.3(0),AL.2(&S),AL.3(1)
         ORG
&LASTCC  SETA  &S
&TEST    SETB  0
         AGO   .DD
.AB      AIF   (&SKIP EQ &LASTCC).NOCHANG
A&SYSNDX DC    XL2'FFFB'
         DC    AL.3(0),AL.2(&SKIP),AL.3(1)
         DC    FL1'9'
&LASTCC  SETA  &SKIP
&TEST    SETB  1
&CARRCON SETC  'A&SYSNDX'
&NRENTS  SETA  &NRENTS+1
.DD      ANOP
.NOCHANG ANOP
         MEND
         EJECT
         SPACE 2
*  ROUTINE MACRO     ROUTINE MACRO     ROUTINE MACRO     ROUTINE MACRO*
         MACRO
&NAME    ROUTINE &LOGITEM,&RNAME
         GBLC  &DSECT,&CSECT
         GBLC  &ITEM(1000)
         GBLA  &SHIFT(1000),&ITEMNBR
         GBLA  &NRENTS
         LCLA  &ISHIFT,&NDX
.LOOP    ANOP
&NDX     SETA  &NDX+1
         AIF   ('&ITEM(&NDX)' EQ '&LOGITEM').FOUND
         AIF   (&NDX LT &ITEMNBR).LOOP
         MNOTE *,'LOG ITEM NOT DEFINED'
         MEXIT
.FOUND   ANOP
&ISHIFT  SETA  &SHIFT(&NDX)
&NAME    DC    AL.12(&LOGITEM-&DSECT),XL.4'D'
         DC    AL.4(&ISHIFT),AL.12(&RNAME-&CSECT)
&NRENTS  SETA  &NRENTS+1
         MEND
*  DSGEN MACRO     DSGEN MACRO     DSGEN MACRO     DSGEN MACRO        *
         SPACE 2
         MACRO
&NAME    DSGEN &X1,&X2,&X3,&X4,&X5,&X6,&X7,&X8,&X9,&X10,&X11,&X12,&X13,X
               &X14,&X15,&X16,&X17,&X18,&X19,&X20,&X21,&X22,&X23,&X24, X
               &X25,&X26,&X27,&X28,&X29,&X30,&X31,&X32
.*                            THIS MACRO GENERATES ONE DSECT WORD
.*                                 FORMATTED FOR THE SUBJECT LOG RECORD
.*                                 EACH OPERAND HAS TWO SUB LISTS. THE
.*                                 FIRST IS THE NAME OF THE LOG ITEM
.*                                 WHILE THE SECOND IS THE # OF BITS TO
.*                                 ALLOCATE FOR THIS ITEM. ITEMS MUST
.*                                 BE DEFINED CONTIGUOUSLY WITHOUT GAPS
.*                                 UNUSED BIT GROUPS MUST BE GIVEN
.*                                 DUMMY NAMES. THE NAMES PROVIDED FOR
.*                                 THE LOG ITEMS MAY THEN BE USED IN
.*                                 THE CONVERSION MACRO INSTRUCTIONS.
         GBLC &ITEM(1000)
         GBLA &ITEMNBR,&BITS(1000),&SHIFT(1000)
         LCLC  &SYMBOL
         LCLA  &OP,&COUNT,&BYTE
.LOOP    ANOP
&OP      SETA  &OP+1
&ITEMNBR SETA  &ITEMNBR+1
&SYMBOL  SETC  '&SYSLIST(&OP,1)'
         AIF   ('&SYSLIST(&OP,2)' NE '').OK1
         MNOTE *,'NULL BIT COUNT FIELD'
         MEXIT
.OK1     ANOP
         AIF   ('&SYMBOL' NE '').OK
         MNOTE 'LOG ITEM SYMBOL NOT PROVIDED'
         AGO   .CNT
.OK      ANOP
         AIF   ('&SYMBOL'(1,1) EQ '''').CNT                *TEMP*
&SYMBOL  EQU   *+&BYTE
&ITEM(&ITEMNBR)  SETC '&SYSLIST(&OP,1)'
&BITS(&ITEMNBR) SETA &SYSLIST(&OP,2)
&SHIFT(&ITEMNBR) SETA &COUNT-&BYTE*8
.CNT     ANOP
&COUNT   SETA  &COUNT+&SYSLIST(&OP,2)
&BYTE    SETA  &COUNT/8
         AIF (&OP LT N'&SYSLIST).LOOP
         AIF   (&COUNT NE 32).NOT32
&NAME    DS    4C
         MEXIT
.NOT32   AIF   (&COUNT NE 64).ERR
&NAME    DS    8C
         MEXIT
.ERR     MNOTE '**BIT COUNT FIELDS DO NOT TOTAL 32 OR 64**'
         MEND
         MACRO
         LINEND
.*                            THIS MACRO IS USED ONLY BY THE LINE
.*                                 MACRO TO SET THE NUMBER OF ENTRIES
.*                                 ASSOCIATED WITH EACH LINE.
         GBLA  &NRENTS
         GBLC &NRENTSX
         ORG   &NRENTSX
         DC    FL1'&NRENTS'
         ORG
&NRENTS  SETA  0
B&SYSNDX EQU   *
&NRENTSX SETC  'B&SYSNDX'
         MEND
         SPACE 2
         EJECT
*  LABEL MACRO     LABEL MACRO     LABEL MACRO     LABEL MACRO        *
         SPACE 2
         MACRO
&NAME    LABEL &LABEL1,&LABEL2,&LABEL3,&LABEL4,&LABEL5,&LABEL6,        C
               &LABEL7,&LABEL8
.*                            THIS MACRO ALLOWS UP TO ACHT LABELS TO
.*                                 BE SPECIFIED FOR A GIVEN LINE. THE
.*                                 LABELS SHOULD BE ENCLOSED IN QUOTA-
.*                                 TION MARKS. THE COLUMN AND LINE END
.*                                 CONTROLS WILL BE INSERTED BY THE
.*                                 MACRO. OMITTED OPERANDS MEAN NO
.*                                 LABEL REQUIRED FOR RESPECTIVE
.*                                 COLUMN. THIS MACRO WILL ALSO ALERT
.*                                 THE PROGRAMMER TO EXACTLY WHERE THE
.*                                 TABLE MUST BE BROKEN IF NECESSARY
.*                                 TO AVOID EXCEEDING THE MAXIMUM LIMIT
         GBLC &LABELS(2000)
         GBLC  &LBL(100)
         GBLA  &LBLCTR(100)
         GBLA  &LABELNR
         GBLA  &NRENTS,&LPTR
         LCLC  &MARGIN,&TACK
         LCLC  &TEXT
         LCLA  &OP,&K,&START,&NRCHAR,&REMAIN
&MARGIN  SETC  '*'
         AIF   ('&NAME' EQ '').LOOPOUT
&LPTR    SETA  &LPTR+1
&LBL(&LPTR) SETC '&NAME'
&LBLCTR(&LPTR) SETA &LABELNR+1
.LOOPOUT ANOP
&OP      SETA  &OP+1
&K       SETA  K'&SYSLIST(&OP)
         AIF (&K GE 2).NORM
&K       SETA  2
.NORM    ANOP
&START   SETA  2
         AIF (&OP LT N'&SYSLIST).NOTLAST
&MARGIN  SETC  '$'
.NOTLAST ANOP
&NRCHAR  SETA  8
&TACK    SETC  ''
.LOOPIN  ANOP
&REMAIN  SETA  &K-&START
         AIF (&REMAIN GE 8).MORE
&TACK    SETC  '&MARGIN'
&NRCHAR  SETA  &REMAIN
.MORE    ANOP
&TEXT    SETC  ''
         AIF (&REMAIN EQ 0).ENDTEXT
&TEXT    SETC  '&SYSLIST(&OP)'(&START,&NRCHAR)
.ENDTEXT ANOP
&LABELNR SETA  &LABELNR+1
&LABELS(&LABELNR) SETC '&TEXT'.'&TACK'
&START   SETA  &START+8
         AIF ('&TACK' EQ '').LOOPIN
         AIF ('&TACK' EQ '*').LOOPOUT
         MEND
         SPACE 2
         MACRO
         LSTART
.*                            THIS MACRO IS ISSUED WHEN THE LABEL AREA
.*                                 OF THE TABLE IS TO BEGIN.
         GBLC &LABELS(2000)
         GBLA  &LABELNR
         GBLC  &LBL(100)
         GBLA  &LBLCTR(100)
         LCLA  &NDX,&CTR
         LINEND
&CTR     SETA  1
.LOOP    ANOP
&NDX     SETA  &NDX+1
         AIF   (&LBLCTR(&CTR) NE &NDX).NOLBL
&LBL(&CTR) DC  C'&LABELS(&NDX)'
&CTR     SETA  &CTR+1
         AGO   .LOOPCTL
.NOLBL   ANOP
         DC    C'&LABELS(&NDX)'
.LOOPCTL AIF   (&NDX LT &LABELNR).LOOP
         MEND
         SPACE 2
*  BIN MACRO     BIN MACRO     BIN MACRO     BIN MACRO     BIN MACRO  *
         SPACE 2
         MACRO
         BIN   &LOGITEM,&SKIP$OR
.*                                 &LOGITEM - NAME OF ITEM IN LOG
.*                                      RECORD DSECT
.*                                 &SKIP$OR - NBR OF SPACES TO SKIP ON
.*                                 MULTIPLE PRINT OR NBR OF SPACES
.*                                      TO BACK UP FROM COLUMN FOR
.*                                      START OF PRINTING. THE LETTER
.*                                      B PRECEEDS THE NUMBER (B5) IF
.*                                      THE LATTER IS DESIRED. OMISSION
.*                                      OF THIS OPERAND FORCES OPTION 2
.*                                      WITH BACKUP SPACING EQUAL TO
.*                                 THE NUMBER OF BITS IN THE ITEM
.*                                 DEFINITION (VIA MACRO 'DSGEN').
.*                            A LIMIT OF 15 BITS IS IMPOSED FOR THE
.*                                 TOTAL NUMBER OF BITS TO PRINT.
         GBLC &ITEM(1000)
         GBLA &ITEMNBR,&BITS(1000),&SHIFT(1000),&NRENTS
         GBLC  &DSECT
         LCLB  &B
         LCLA  &IBITS,&NDX,&ISHIFT,&K
         LCLA  &TEMP,&T
         LCLC  &S
.LOOP    ANOP
&NDX     SETA  &NDX+1
         AIF   ('&ITEM(&NDX)' EQ '&LOGITEM').FOUND
         AIF   (&NDX LT &ITEMNBR).LOOP
         MNOTE *,'YOU BLEW IT'
         ROUTINE DUM,SKIPPER
         MEXIT
.FOUND   ANOP
&ISHIFT  SETA  &SHIFT(&NDX)
&IBITS   SETA  &BITS(&NDX)
&T       SETA  &IBITS-1
&B       SETB  0
         AIF   ('&SKIP$OR' EQ '').BACKUP
&K       SETA  K'&SKIP$OR
&B       SETB  ('&SKIP$OR'(1,1) EQ 'B')
&S       SETC  '&SKIP$OR'(&B+1,&K-&B)
&T       SETA  &S
.BACKUP  ANOP
&NRENTS  SETA  &NRENTS+1
&TEMP    SETA  &ISHIFT
A&SYSNDX DC    AL.12(&LOGITEM-&DSECT),XL.4'&TEMP'
         AIF (&IBITS EQ 1 AND &B EQ 0).OUT
         ORG   A&SYSNDX
&B       SETB  1
         DC    AL.12(&LOGITEM-&DSECT),XL.4'C'
         DC    BL.1'&B',AL.7(&T),AL.4(&IBITS),FL.4'&ISHIFT'
.OUT     ANOP
         MEND
         EJECT
*  HEX MACRO     HEX MACRO     HEX MACRO    HEXN MACRO     HEX MACRO  *
         SPACE 2
         MACRO
         HEX   &LOGITEM,&SKIP$OR,&BYTES
.*                            THIS MACRO FACILITATES GENERATION OF THE
.*                                 TABLE ENTRIES SPECIFYING HEX TYPE
.*                                 CONVERSIONS.
.*                            &LOGITEM - NAME OF DATA ITEM TO BE
.*                                 CONVERTED
.*                            &SKIP$OR - NUMBER OF SPACES TO SKIP ON
.*                                 MULTIPLE PRINT OR NBR OF SPACES TO
.*                                 BACK UP FROM COLUMN FOR START OF
.*                                 PRINTING. THE LETTER B PRECEEDS THE
.*                                 NUMBER (B8) IF THE LATTER OPTION IS
.*                                 DESIRED. OMISSION OF THIS OPERAND
.*                                 FORCES OPTION 2 WITH BACKUP SPACING
.*                                 EQUAL TO THE NUMBER OF BYTES SPECI-
.*                                 FIED BY THE NEXT OPERAND (TWICE THIS
.*                                 NUMBER IF SUFFIX IS EXPLICITLY OR
.*                                 IMPLICITLY B).
.*                            &BYTES - NUMBER OF TOTAL INPUT BYTES TO
.*                                 PROCESS. THIS NUMBER IS SUFFIXED
.*                                 WITH AN L,R, OR B TO STIPULATE
.*                                 PRINTING OF THE LEFT HALF, RIGHT
.*                                 HALF, OR BOTH HALVES OF THE BYTE
.*                                 RESPECTIVELY. OMISSION OF THIS
.*                                 OPERAND DEFAULTS TO B WITH THE NUM-
.*                                 BER OF BYTES EQUAL TO THE ITEM LNGTH
.*                            A NEW ENTRY IS GENERATED ONLY WHEN NECES-
.*                                 SARY. THIS MACRO MUST BE THE FIRST
.*                                 MACRO INSTRUCTION ISSUED FOR THE
.*                                 SUBJECT LINE(S).
         GBLC &ITEM(1000),&DSECT
         GBLA &ITEMNBR,&BITS(1000),&SHIFT(1000),&NRENTS
         LCLB  &B
         LCLA  &IBITS,&NDX,&ISHIFT,&K,&T,&CODE,&BYTESP
         LCLC  &S,&LETTER
.LOOP    ANOP
&NDX     SETA  &NDX+1
         AIF   ('&ITEM(&NDX)' EQ '&LOGITEM').FOUND
         AIF   (&NDX LT &ITEMNBR).LOOP
         MNOTE *,'LOG ITEM NOT DEFINED'
         MEXIT
.FOUND   ANOP
&ISHIFT  SETA  &SHIFT(&NDX)
&IBITS   SETA  &BITS(&NDX)
&CODE    SETA  1
&K       SETA  K'&BYTES
         AIF   (&IBITS-(&IBITS/4)*4 EQ 0).OK
         MNOTE *,'BIT LENGTH SPECIFICATION INCONSISTENT WITH CONTROL'
         MEXIT
.OK      AIF   ('&BYTES' EQ '').USEDEFN
&LETTER  SETC  '&BYTES'(&K,1)
&S       SETC  '&BYTES'(1,&K-1)
&BYTESP  SETA  &S
         AIF   ('&LETTER' EQ 'B').BOTH
         AIF   (&ISHIFT EQ 0 OR &ISHIFT EQ 4).OK2
.ERR     MNOTE *,'DATA NOT ALLIGNED ON HEX OR BYTE BOUNDARY'
         MEXIT
.USEDEFN ANOP
&BYTESP  SETA  &IBITS/8
.BOTH    AIF   (&ISHIFT NE 0).ERR
&CODE    SETA  3
&T       SETA  &BYTESP*2-1
&S       SETC  '&T'
.OK2     AIF   (&ISHIFT EQ 0).L
&CODE    SETA  2
.L       ANOP
&B       SETB  1
         AIF   ('&SKIP$OR' EQ '').BACKUP
&K       SETA  K'&SKIP$OR
&B       SETB  ('&SKIP$OR'(1,1) EQ 'B')
&S       SETC  '&SKIP$OR'(&B+1,&K-&B)
.BACKUP  ANOP
&NRENTS  SETA  &NRENTS+1
         DC    AL.12(&LOGITEM-&DSECT),XL.4'A'
         DC    BL.1'&B',FL.7'&S',FL.4'&BYTESP',FL.4'&CODE'
.OUT     ANOP
         MEND
         GBLC  &DSECT,&CSECT
&CSECT   SETC  'IFCEIPL'
&DSECT   SETC  'XXXX'
IFCEIPL  CSECT
XXXX     DSECT
******** DSGEN ENTRIES *******
         DSGEN (CLASRC,8),(SYSREL,8),(SWITCH1,8),(SWITCH2,8)
         DSGEN (SPARE1,32)
         DSGEN (DAY,32)
         DSGEN (HOUR,32)
         DSGEN (SPARE2,8),(CPUSERL,24)
         DSGEN (CPUID,16),(DUM,16)
HEX24    DSGEN (SSID,8),(CAUADR,24),(IPLRCODE,16),(CHMAP,16)
         DSGEN (CHTAB07,32),(CHTAB8F,32)
         DSGEN (HSA,32),(SPARE99,32)
         DSGEN (OUTDATE,32),(OUTTIME,32)                       @YL02021
         ORG   HEX24                                           @YL02021
         DSGEN (DUM1,16),(COUNT,16)                            @YL02021
IFCEIPL  CSECT
         USING *,R9
         USING XXXX,R2
         DC    A(ENTRIES)
         DC    CL8'IFCEIPL '
XR9PTR   DC    F'0'
XLABELP  DC    F'0'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU     15
L0       EQU   0
L1       EQU   1
L2       EQU   2
L3       EQU   3
L4       EQU   4
L5       EQU   5
L6       EQU   6
L7       EQU   7
L8       EQU   8
L9       EQU   9
L10      EQU   10
L13      EQU   13
L14      EQU   14
L20      EQU   20
L21      EQU   21
L23      EQU   23
L24      EQU   24
L26      EQU   26
L27      EQU   27
L28      EQU   28
L30      EQU   30
L32      EQU   32
L35      EQU   35
L48      EQU   48
L51      EQU   51
L64      EQU   64
X01      EQU   X'01'
X02      EQU   X'02'
X03      EQU   X'03'
X04      EQU   X'04'
X05      EQU   X'05'
X06      EQU   X'06'
X07      EQU   X'07'
X10      EQU   X'10'
X20      EQU   X'20'
X30      EQU   X'30'
X40      EQU   X'40'
X50      EQU   X'50'
X60      EQU   X'60'
X70      EQU   X'70'
X80      EQU   X'80'
X90      EQU   X'90'
X91      EQU   X'91'
X81      EQU   X'81'                    SYS TERM RCD TYPE      @YL02021
WORK     DC    D'0'
XSAVE    DC    5F'0'
ZEROES   DC    C'000000'
BLANKS   DC    C'  '
NONE     DC    C'NONE  '
UNIVER   DC    C'UNIVERSAL'
UNATT    DC    C'UNATT '
SEL      DC    C'SEL   '
MPX      DC    C'MPX   '
BLKMPX   DC    C'BLKMPX'
C2860    DC    C'2860  '
C2870    DC    C'2870  '
C2880    DC    C'2880  '
IFA      DC    C'IFA   '                                       @YA04044
TEST     DC    XL1'00'
IBMPP    DC    C'IBM PROGRAMMING PRODUCT'
IBMSCP   DC    C'IBM SYSTEM CONTROL PROGRAM'
TP       DC    C'TELPROCESSING'
MICROCR  DC    C'MICR/OCR'
CARDPT   DC    C'CARD/PRINT'
OTHER    DC    C'(OTHER)'
GXDPAU   DC    C'GRAPHIX/DISPLAY/AUDIO'
TAPE     DC    C'TAPE'
DA       DC    C'DIRECT ACCESS'
NULL     DC    C'NULL'
PROCES   DC    C'PROCESSOR'
TABLE    DC    C'NM NORMAL                                          '
         DC    C'IE IBM HARDWARE PROGRAMMING PROBLEM -CE/SE NOT REQ.'
         DC    C'IM IBM HARDWARE PROGRAMMING PROBLEM -CE/SE REQ.    '
         DC    C'ME MEDIA                                           '
         DC    C'UN UNKNOWN                                         '
         DC    C'OP OPERATIONAL                                     '
         DC    C'UP USER PROGRAM                                    '
         DC    C'EN ENVIRONMENTAL                                   '
         DC    C'CE CE/SE HAS THE SYSTEM                            '
         DC    C'DF DEFAULT -U-                                     '
XFF      DC    C'FF  THE IPL REASON CODE IS INVALID                 '
EODOUT   DC    X'0018'                  LENGHT OF EOD RECORD
         DC    X'0018'                  LENGHT OF EOD HEADER
         DC    X'0000'
IPLOUT   DC    X'0038'                  LENGTH OF IPL RECORD   @YM6452P
         DC    X'0018'                  LENGHT OF IPL HEADER
         DC    X'0024'
SYSOUT   EQU   *                                               @YL02021
         DC    X'0000'                  LENGTH OF SYS TERM RECORD
         DC    X'0018'                  LENGTH OF SYS TERM HEADER
         DC    X'0000'                                         @YL02021
RELTAB   DS    0C                                              @L03976
         DC    C'OS  '                                         @L03976
         DC    C'DOS '                                         @L03976
         DC    C'VS 1'                                         @L03976
         DC    C'V370'                                         @L03976
         DC    C'VS 2'                                         @L03976
         DC    C'    '                                         @L03976
         DC    C'    '                                         @L03976
         DC    C'    '                                         @L03976
CONVREL  STM   R5,R8,XSAVE              SAVE WORK REGS
         SR    R6,R6                    CLEAR R6
         IC    R6,L0(R4)                PICK UP REL & SYS VALUES
         SRDL  R6,L5                    SAVE REL NO IN R7
        SLL   R6,L2                    MULTIPLY SYS VALUE BY 4
         L     R8,XLABELP               PICK UP LABEL POINTER
         LA    R6,RELTAB(R6)            INDEX INTO TABLE OF SYSTEMS
         MVC   L0(L4,R8),L0(R6)         MOVE SYSTEM NAME INTO LABEL
         SR    R6,R6                    CLEAR R6
         SLDL  R6,L5                    RETREIVE REL NO.
         LTR   R6,R6                    IS IT ZERO
         BZ    NOREL                    BR-YES
         CVD   R6,WORK                  CONVERT IT
         UNPK  L0(L2,R3),WORK+L6(L2)    UNPACK IT INTO PRINT BUFF
         MVZ   L1(L1,R3),L0(R3)         MOVE ON PROPER ZONE
         B     RETURN                   EXIT
NOREL    MVC   L0(L4,R3),NONE           MOVE IN NONE FOR REL
         B     RETURN
ENGLISH  STM   R5,R8,XSAVE              SAVE REGS
         LA    R5,TABLE                 GET ADDRESS OF TABLE
AGAIN99  CLC   L0(L2,R5),L0(R4)         SEE IF FIRST TWO BYTE SAME
         BE    PRTIT                    IF YES PRINT IT
         CLC   L0(L2,R5),XFF
         BE    PRTIT                    ERROR RETURN
         LA    R5,L51(R5)
         B     AGAIN99
PRTIT    MVC   L0(L48,R3),L3(R5)
         B     RETURN
SETDATE  UNPK  WORK(L8),L1(L3,R4)       UNPACK THE DATE
         MVC   L0(L3,R3),WORK+L5        MOVE DAY
         MVC   L6(L2,R3),WORK+L3        MOVE YEAR
         BR    R14                      GO BACK
RETURN   LM    R5,R8,XSAVE
SKIPPER  BR    R14
CHTABPT  STM   R5,R8,XSAVE              SAVE REGS
         LA    R8,L8                    GET INDEX(8) IN 8
         SRDL  R6,L64                   CLEAR 6&7
         MVC   WORK(L4),L0(R4)
         L     R7,WORK                  GET IN 8 ENTRIES
AGAIN    SLDL  R6,L4                    GET     4BITS IN R6
         STC   R6,TEST                  PUT IN TEST PLACE
         CLI   TEST,X'0A'               IFA ?                  @YA04044
         BNE   NEW2880                  NO   TRY  2880         @YA04044
         MVC   L0(L6,R3),IFA            YES  PUT IN IFA        @YA04044
         B     OVER3                                           @YA04044
NEW2880  CLI   TEST,X'08'               NEW TYPE 2880          @YA04044
         BNE   NOTTHIS                  NO  TRY   OTHER        @YA04044
         MVC   L0(L6,R3),C2880          YES  SAY  2880         @YA04044
         B     OVER3                                           @YA04044
NOTTHIS  EQU   *                                               @YA04044
         TM    TEST,X07                 IS IT 2880
         BO    Y21                      BR-YES
         B     N21                      BR-NO
Y21      MVC   L0(L6,R3),C2880          MOVE IN 2880
         B     OVER3                    BRANCH TO DO NEXT ENTRY
N21      TM    TEST,X06                 IS IT 2870
         BO    Y22                      BR-YES
         B     N22                      BR-NO
Y22      MVC   L0(L6,R3),C2870          MOVE IN 2870
         B     OVER3                    BRANCH TO DO NEXT ENTRY
N22      TM    TEST,X05                 IS IT 2860
         BO    Y23                      BR-YES
         B     N23                      BR-NO
Y23      MVC   L0(L6,R3),C2860          MOVE IN 2860
         B     OVER3                    BRANCH TO DO NEXT ENTRY
N23      TM    TEST,X03                 IS IT BLOCK MPX
         BO    Y24                      BR-YES
         B     N24                      BR-NO
Y24      MVC   L0(L6,R3),BLKMPX         MOVE IN BLKMPX
         B     OVER3                    BRANCH TO DO NEXT ENTRY
N24      TM    TEST,X02                 IS IT SELECTOR CHANNEL
         BO    Y25                      BR-YES
         B     N25                      BR-NO
Y25      MVC   L0(L6,R3),SEL            MOVE IN SEL
         B     OVER3                    BRANCH TO DO NEXT ENTRY
N25      TM    TEST,X01                 IS IT MPX
         BO    Y26                      BR-YES
         B     N26                      BR-NO
Y26      MVC   L0(L6,R3),MPX            MOVE IN MPX
         B     OVER3                    BRANCH TO DO NEXT ENTRY
N26      MVC   L0(L6,R3),UNATT          MOVE IN UNATT
OVER3    LA    R3,L10(R3)               MOVE R3 TO NEXT PRINT POSITION
         BCT   R8,AGAIN                 GET ANOTHER ENTRY
         B     RETURN                   GO BACK-FINISHED
CPUPT    STM   R5,R8,XSAVE              SAVE REGS
         LA    R15,L30                  GET 20 IN 15
         SR    R3,R15                   GET POINTER RIGHT
         LR    R5,R3                    GET POINTER IN 5
         LA    R15,L3                   GET 3 IN 15
         SR    R5,R15                   GO BACK FURTHER
         CLC   L0(L4,R5),ZEROES         SEE IF ALL ZEROES
         BE    UNIVERP
         CLC   L0(L2,R5),ZEROES         SEE IF LEADING ZEROES
         BE    OVER                     IF OFF-BRANCH
         CLI   0(R5),X'00'                                     @ZM43111
         BNE   RETURN                                          @ZM43111
        MVC   L0(L1,R5),BLANKS         PUT IN ONLY 1 BLANK
         B     RETURN                   RETURN
OVER     MVC   L0(L2,R5),BLANKS         IF ON PUT IN 2 BLANKS
         B     RETURN                   RETURN
UNIVERP  MVC   L0(L9,R5),UNIVER         IF ALL OFF PUT IN MSG
         B     RETURN
SERCK    STM   R5,R8,XSAVE              SAVE REGS
         LR    R5,R3                    GET POINTER IN 5
         LA    R15,L35                  GET 35 IN 25
         SR    R5,R15                   BUMP POINTER BACK
         CLC   L0(L6,R5),ZEROES         IS IT ZEROES
         BE    OVER1                    IF ZEROES-BR
         B     RETURN                   IF OFF RETURN
OVER1    MVC   L0(L6,R5),NONE           IF NO PUT IN NONE
         B     RETURN                   GO BACK
IPLCODE  MVC   L0(L2,R3),L0(R4)
         BR    R14
SSNAME   STM   R5,R8,XSAVE         SAVE REGS
         TM    L0(R4),X91          IS IT IBM PP
         BO    NO71
         B     NO7                 BR NO
NO71     MVC   L0(L23,R3),IBMPP    MOVE IN MSG
         B     RETURN
NO7      TM    L0(R4),X90          IS IT IBM SCP
         BO    NO81
         B     NO8                 BR-NO
NO81     MVC   L0(L26,R3),IBMSCP   MOVE IN MSG
         B     RETURN
NO8      TM    L0(R4),X70               IS IT TP
         BO    NO91                     BR-YES
         B     NO9                      BR-NO
NO91     MVC   L0(L13,R3),TP            MOVE IN MSG
         B     RETURN                   GO BACK
NO9      TM    L0(R4),X60               IS IT MICR/OBR
         BO    NO101                    BR-YES
         B     NO10                     BR-NO
NO101    MVC   L0(L8,R3),MICROCR        MOVE IN MSG
         B     RETURN                   GO BACK
NO10     TM    L0(R4),X50               IS IT CARO/PRINT
         BO    NO111                    BR-YES
         B     NO11                     BR-NO
NO111    MVC   L0(L10,R3),CARDPT        MOVE IN MSG
         B     RETURN                   GO BACK
NO11     TM    L0(R4),X30               IS IT OTHER
         BO    NO121                    BR-YES
         B     NO12                     BR-NO
NO121    MVC   L0(L7,R3),OTHER          MOVE IN MSG
         B     RETURN                   GO BACK
NO12     TM    L0(R4),X80               IS IT CX/DP/AVDIO
         BO    NO131                     BR-YES
         B     NO13                      BR-NO
NO131    MVC   L0(L21,R3),GXDPAU        MOVE IN MSG
         B     RETURN                   GO BACK
NO13     TM    L0(R4),X40               IS IT TAPE
         BO    NO141                    BR-YES
         B     NO14                     BR-NO
NO141    MVC   L0(L4,R3),TAPE           MOVE IN MSG
         B     RETURN                   GO BACK
NO14     TM    L0(R4),X20               IS IT DIRECT RECESS
         BO    NO151                    BR-YES
         B     NO15                     BR-NO
NO151    MVC   L0(L13,R3),DA            MOVE IN MSG
         B     RETURN                   GO BACK
NO15     TM    L0(R4),X10               IS IT PROCESSOR
         BO    NO161                    BR-YES
         MVC   L0(L4,R3),NULL           NULL MSG
         B     RETURN                   GO BACK
NO161    MVC   L0(L9,R3),PROCES         MOVE IN MSG
         B     RETURN                   GO BACK
EODREC   STM   R5,R8,XSAVE              SAVE REGS
         MVC   QUIT(L6),IPLOUT          PUT IN IPL DUMP PARMS
         CLI   L0(R4),X50               IS IT IPL RECORD ?     @YL02021
         BE    RETURN                   YES, BRANCH            @YL02021
         MVC   QUIT(L6),SYSOUT          GET SYS TERM REC DUMP PARMS
         CLI   L0(R4),X81               IS IT REALLY SYS TERM REC ?
         BE    NEXTONE                  YES, BRANCH            @YL02021
         MVC   QUIT(L6),EODOUT          PUT IN EOD DUMP PARMS
NEXTONE  EQU   *                                               @YL02021
         LA    R5,XXXY-L4               POINT TO END LINE
         ST    R5,XR9PTR                STORE POINTER
         LA    R5,YYYY                  POINT OT END LABEL
         ST    R5,XLABELP               STORE POINTER
         B     RETURN
SYSHEAD  EQU   *                                               @YL02021
         CLI   L0(R4),X81               SYS TERM RECOR         @YL02021
         BNE   DELETE                   NO, BRANCH             @YL02021
         MVC   SYSOUT(L2),COUNT         YES, MOVE COUNT INTO DUMP DC'S
         BR    R14                      RETURN                 @YL02021
IPLHEAD  CLI   L0(R4),X50               IS IT IPL RECORD ?     @YL02021
         BNE   DELETE                   NO, BRANCH             @YL02021
         BR    R14
EODHEAD  CLI   L0(R4),X80               IS IT EOD RECORD ?     @YL02021
         BCR   8,R14                    YES, BRANCH            @YL02021
DELETE   SR    R15,R15                  SET 15 SO NO LINE IS PRINTED
         BR    R14
CKOUT    TM    SWITCH2,X80              Q/ IS OUTAGE PRESENT   @YM6452P
         BNO   NEXTONE                  NO--EXIT               @YM6452P
         B     RETURN                   YES GO PRINT IT        @YM6452P
ENTRIES  DS    0F
         DC    XL4'00010001'
         DC    XL4'00010001'
         DC    A(LABELS)
         DC    H'0'
         DC    X'01'
         DC    X'000B8B00'
         LABEL ''
         LINE  (48,1),(1,38),SKIP=3
         ROUTINE CLASRC,EODHEAD
         LABEL 'EOD RECORD EDIT AND PRINTING SECTION'
         LINE
         ROUTINE CLASRC,IPLHEAD
         LABEL 'IPL RECORD EDIT AND PRINTING SECTION'
         LINE  ,                                               @YL02021
         ROUTINE CLASRC,SYSHEAD                                @YL02021
         LABEL 'SYSTEM TERMINATION RECORD EDIT AND PRINT SECTION'
         LINE  ,(7,39),SKIP=1
         LABEL 'DAY  YEAR','HH MM SS TH'
         LINE  (7,39),(1,39),SKIP=2
         ROUTINE DAY,SETDATE
         HEX   HOUR,1,4B
         LABEL 'DATE -','TIME -'
         LINE  (14,30),(3,18)
         HEX   CPUID
         ROUTINE CPUID,CPUPT
         HEX   CPUSERL
         ROUTINE CPUSERL,SERCK
         LABEL 'MODEL -','CPU SERIAL NO. -'
         LINE       (16,1),(4,1)
         ROUTINE   SYSREL,CONVREL
         LABEL      'XXXX REL.'
         LINE       (14,30),(3,18),SKIP=1
         ROUTINE CLASRC,EODREC
         LABEL ' '
         LINE  ,(1,),SKIP=1
         LABEL '--CHANNEL TYPE--'
         LINE  ,,SKIP=2
         LABEL '   CHANNELS 0-14'
         LINE  (1,5)
         ROUTINE CHTAB07,CHTABPT
         LABEL ''
         LINE ,(70,),SKIP=3
         ROUTINE CHTAB8F,CHTABPT
         LABEL '         '
         LINE  (29,),(11,40),SKIP=2
         ROUTINE IPLRCODE,IPLCODE
         ROUTINE IPLRCODE,ENGLISH
         LABEL 'IPL REASON CODE -'
         LINE  (27,41)
         HEX   SSID
         ROUTINE SSID,SSNAME
         LABEL 'SUBSYSTEM ID -','SUBSYSTEM NAME -'
         LINE  (43,),,SKIP=3
         HEX   HSA
         LABEL 'HIGHEST STORAGE ADDRESS'
         LINE  ,(7,39),SKIP=1                                  @YL02021
         ROUTINE SWITCH2,CKOUT                                 @YM6452P
         LABEL ''                                              @YM6452P
         LINE  ,                                               @YM6452P
         LABEL 'LAST ACTIVITY INFORMATION :'                   @YL02021
         LINE  ,(7,39),SKIP=1                                  @YL02021
         LABEL 'DAY  YEAR','HH MM SS TH'                       @YL02021
         LINE  (7,39),(1,39),SKIP=2                            @YL02021
         ROUTINE OUTDATE,SETDATE                               @YL02021
         HEX   OUTTIME,1,4B                                    @YL02021
         LABEL 'DATE -','TIME -'                               @YL02021
         LINE  ,(1,),SKIP=1
         LABEL 'END OF IPL RECORD'
XXXY     LINE (1,1),(1,1),SKIP=1
         ROUTINE CLASRC,SKIPPER
YYYY     LABEL ''
         LABEL ' '
**       END   OF   LINE  LABELS    **
         DC    X'FF'
QUIT     DC    XL2'0000'                THESE ARE PARAMETERS
         DC    XL2'0000'                FOR THE HEX DUMP AND ARE
         DC    XL2'0000'                MODIFIED FOR VARIOUS RECORDS
LABELS   DS    0F
         LSTART
         END
