         TITLE 'I F C E M E R 4 - I F C E M E R 4 - I F C E M E R 4'
*  DSGEN MACRO     DSGEN MACRO     DSGEN MACRO     DSGEN MACRO        *
         SPACE 2
         MACRO
&NAME    DSGEN &X1,&X2,&X3,&X4,&X5,&X6,&X7,&X8,&X9,&X10,&X11,&X12,&X13,X
               &X14,&X15,&X16,&X17,&X18,&X19,&X20,&X21,&X22,&X23,&X24, X
               &X25,&X26,&X27,&X28,&X29,&X30,&X31,&X32
.*                            THIS MACRO GENERATES ONE DSECT WORD
.*                                 FORMATTED FOR THE SUBJECT LOG RECORD
.*                                 EACH OPERAND HAS TWO SUB LISTS. THE
.*                                 FIRST IS THE NAME OF THE LOG ITEM
.*                                 WHILE THE SECOND IS THE # OF BITS TO
.*                                 ALLOCATE FOR THIS ITEM. ITEMS MUST
.*                                 BE DEFINED CONTIGUOUSLY WITHOUT GAPS
.*                                 UNUSED BIT GROUPS MUST BE GIVEN
.*                                 DUMMY NAMES. THE NAMES PROVIDED FOR
.*                                 THE LOG ITEMS MAY THEN BE USED IN
.*                                 THE CONVERSION MACRO INSTRUCTIONS.
         GBLC &ITEM(1000)
         GBLA &ITEMNBR,&BITS(1000),&SHIFT(1000)
         LCLC  &SYMBOL
         LCLA  &OP,&COUNT,&BYTE
.LOOP    ANOP
&OP      SETA  &OP+1
&ITEMNBR SETA  &ITEMNBR+1
&SYMBOL  SETC  '&SYSLIST(&OP,1)'
         AIF   ('&SYSLIST(&OP,2)' NE '').OK1
         MNOTE *,'NULL BIT COUNT FIELD'
         MEXIT
.OK1     ANOP
         AIF   ('&SYMBOL' NE '').OK
         MNOTE 'LOG ITEM SYMBOL NOT PROVIDED'
         AGO   .CNT
.OK      ANOP
         AIF   ('&SYMBOL'(1,1) EQ '''').CNT                *TEMP*
&SYMBOL  EQU   *+&BYTE
&ITEM(&ITEMNBR)  SETC '&SYSLIST(&OP,1)'
&BITS(&ITEMNBR) SETA &SYSLIST(&OP,2)
&SHIFT(&ITEMNBR) SETA &COUNT-&BYTE*8
.CNT     ANOP
&COUNT   SETA  &COUNT+&SYSLIST(&OP,2)
&BYTE    SETA  &COUNT/8
         AIF (&OP LT N'&SYSLIST).LOOP
         AIF   (&COUNT NE 32).NOT32
&NAME    DS    4C
         MEXIT
.NOT32   AIF   (&COUNT NE 64).ERR
&NAME    DS    8C
         MEXIT
.ERR     MNOTE '**BIT COUNT FIELDS DO NOT TOTAL 32 OR 64**'
         MEND
         EJECT
*  LINE MACRO     LINE MACRO     LINE MACRO     LINE MACRO            *
         SPACE 2
         MACRO
&NAME    LINE  &DATA,&LABEL,&SKIP=
.*                            THIS MACRO ESTABLISHES THE MARGINS OR
.*                                 COLUMN SETTINGS FOR EACH LINE. IT
.*                                 GENERATES NECESSARY CARRIAGE CONTROL
.*                                 ENTRIES. LASTLY IT DETERMINES THE
.*                                 NUMBER OF ENTRIES ASSOCIATED WITH
.*                                 THE PREVIOUS LINE AND STORES THAT #
.*                                 INTO THE APPROPRIATE BYTE.
.*                            &DATA(1) - CHARACTER POSITION TO BEGIN
.*                                 DATA.
.*                            &DATA(2) - # OF SPACES BETWEEN SUCCESSIVE
.*                                 DATA COLUMN STARTING CHARACTERS.
.*                            &LABEL(1) - CHAR. POS. TO BEGIN LABELS.
.*                            &LABEL(2) - # SPACES BETWEEN LINE'S LABEL
.*                                 STARTING CHARACTERS.
.*                            &SKIP= - # OF LINES TO ADVANCE CARRIAGE
.*                                 TO BEGIN NEXT PRINTED LINE.
.*                            NEW ENTRIES ARE GENERATED ONLY WHEN
.*                                 NECESSARY. OMISSION OF ITEMS MEANS
.*                                 THEY HAVE NOT CHANGED SINCE PREVIOUS
.*                                 SETTINGS. A LINE MACRO IS REQUIRED
.*                                 FOR EACH PRINTED LINE. IF NO SETTING
.*                                 CHANGES ARE REQUIRED ALL OPERANDS
.*                                 ARE OMITTED IN WHICH CASE ONLY THE
.*                                 # OF ENTRIES FOR THE PREVIOUS LINE
.*                                 IS COMPUTED. THIS MACRO MUST BE THE
.*                                 FIRST MACRO INSTRUCTION ISSUED FOR
.*                                 THE SUBJECT LINE
         GBLA  &DB,&DT,&LB,&LT,&NRENTS
         GBLA  &LASTCC,&INITIAL
         GBLC  &NRENTSX
         GBLC  &CARRCON
         GBLB  &TEST
         GBLB  &DIAGTST,&DIAGSET,&DIAGLIN
         LCLA  &S
         AIF   (&INITIAL EQ 0 OR &DIAGTST EQ 0).DIAGSET
         AIF   (&DIAGSET EQ 0).DIAGSET
         MNOTE '**NO LABEL ASSOCIATED WITH LAST SET**'
***** THE FOLLOWING GENERATED FOR TEST PURPOSES *****
*        LABEL '%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%'
               LABEL '%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%'
.DIAGSET ANOP
&DIAGSET SETB  1
&DIAGLIN SETB  1
&NAME    DS    0C
         AIF   (&INITIAL EQ 0).FIRSTLN
         LINEND
         AGO   .NTFIRST
.FIRSTLN ANOP
&NRENTSX SETC  'N&SYSNDX'
&INITIAL SETA  1
N&SYSNDX EQU   *
.NTFIRST ANOP
         DC    AL1(0)
         AIF   ('&DATA(1)' EQ '').CKDTABS
         AIF   (&DATA(1) EQ &DB).CKDTABS
&DB      SETA  &DATA(1)
         AGO   .AA
.CKDTABS AIF   ('&DATA(2)' EQ '').NCDATA
         AIF   (&DATA(2) EQ &DT).NCDATA
         AGO   .CC
.AA      AIF   ('&DATA(2)' EQ '').BB
         AIF   (&DATA(2) EQ &DT).BB
.CC      ANOP
&DT      SETA  &DATA(2)
.BB      DC    XL2'FFFE'
         DC    FL1'&DB'
         DC    FL1'&DT'
&NRENTS  SETA  &NRENTS+1
.NCDATA  ANOP
         AIF   ('&LABEL(1)' EQ '').CKLTABS
         AIF   (&LABEL(1) EQ &LB).CKLTABS
&LB      SETA  &LABEL(1)
         AGO   .AAA
.CKLTABS AIF   ('&LABEL(2)' EQ '').NCLABEL
         AIF   (&LABEL(2) EQ &LT).NCLABEL
         AGO   .CCC
.AAA     AIF   ('&LABEL(2)' EQ '').BBB
         AIF   (&LABEL(2) EQ &LT).BBB
.CCC     ANOP
&LT      SETA  &LABEL(2)
.BBB     DC    XL2'FFFF'
         DC    FL1'&LB'
         DC    FL1'&LT'
&NRENTS  SETA  &NRENTS+1
.NCLABEL ANOP
         AIF   ('&SKIP' NE '').CONT
         AIF   (&TEST EQ 0).NOCHANG
         AGO   .DEFALTA
.CONT    ANOP
         AIF   (&TEST EQ 0).AB
         AIF   ('&SKIP' EQ 'E').AB
&S       SETA  &SKIP
.DEFAULT ANOP
         AIF   (&SKIP EQ &LASTCC).DEFALTA
         ORG   &CARRCON+3
         DC    AL.3(0),AL.2(&S),AL.3(1)
         ORG
&LASTCC  SETA  &S
.DEFALTA ANOP
&TEST    SETB  0
         AGO   .DD
.AB      AIF   ('&SKIP' NE 'E').ABB
A&SYSNDX DC    X'FFFB8B00'              EJECT
         AGO   .ABC
.ABB     AIF   (&SKIP EQ &LASTCC).NOCHANG
A&SYSNDX DC    XL2'FFFB'
         DC    AL.3(0),AL.2(&SKIP),AL.3(1)
         DC    AL.3(0),AL.2(&SKIP),AL.3(1)
&LASTCC  SETA  &SKIP
&TEST    SETB  1
.ABC     ANOP
&CARRCON SETC  'A&SYSNDX'
&NRENTS  SETA  &NRENTS+1
.DD      ANOP
.NOCHANG ANOP
         MEND
         EJECT
*  BIN MACRO     BIN MACRO     BIN MACRO     BIN MACRO     BIN MACRO  *
         SPACE 2
         MACRO
         BIN
.*                                 &SYSLIST(&OP,1) - NAME OF LOG ITEM
.*                                      RECORD DSECT
.*                                 $SYSLIST(&OP,2) - NBR OF SPACES
.*                                      TO SKIP ON MULTIPLE
.*                                      PRINT OR NBR OF SPACES
.*                                      TO BACK UP FROM COLUMN FOR
.*                                      START OF PRINTING. THE LETTER
.*                                      B PRECEEDS THE NUMBER (B5) IF
.*                                      THE LATTER IS DESIRED. OMISSION
.*                                      OF THIS OPERAND FORCES OPTION 2
.*                                      WITH BACKUP SPACING EQUAL TO
.*                                 THE NUMBER OF BITS IN THE ITEM
.*                                 DEFINITION (VIA MACRO 'DSGEN').
.*                            A LIMIT OF 15 BITS IS IMPOSED FOR THE
.*                                 TOTAL NUMBER OF BITS TO PRINT.
         GBLA  &INITIAL
         GBLB  &DIAGTST,&DIAGSET,&DIAGLIN
         GBLC  &ITEM(1000),&DSECT
         GBLA  &BITS(1000),&ITEMNBR,&SHIFT(1000),&NRENTS
         LCLB  &B,&SUB
         LCLA  &IBITS,&NDX,&ISHIFT,&K
         LCLA  &TEMP,&T,&OP
         LCLC  &S
         ACTR  500000
         AIF   (&DIAGTST EQ 0).NOMSG
         AIF   (&INITIAL EQ 0).NOMSG
         AIF   (&DIAGLIN EQ 1).NOMSG
         MNOTE '**NO LINE ASSOCIATED WITH THIS DATA**'
*****THE FOLLOWING GENERATED FOR TEST PURPOSES*****
*        LINE
         LINE
.NOMSG   ANOP
         AIF   (K'&SYSLIST(1) EQ K'&SYSLIST(1,1)).LOOP
&SUB     SETB  1
.LOOP    ANOP
&OP      SETA  &OP+1
.LOOP1   ANOP
&NDX     SETA  &NDX+1
         AIF   ('&ITEM(&NDX)' EQ '&SYSLIST(&OP,1)').FOUND
         AIF   (&NDX LT &ITEMNBR).LOOP1
         MNOTE '**LOG ITEM NOT DEFINED**'
         MEXIT
.FOUND   ANOP
&ISHIFT  SETA  &SHIFT(&NDX)
&IBITS   SETA  &BITS(&NDX)
         AIF   (&SUB EQ 1).SUB00
         AIF   ('&SYSLIST(3)' EQ '').SETT
&IBITS   SETA  &SYSLIST(3)
         AGO   .SETT
.SUB00   AIF   ('&SYSLIST(&OP,3)' EQ '').SETT
&IBITS   SETA  &SYSLIST(&OP,3)
.SETT    ANOP
&T       SETA  &IBITS-1
&B       SETB  0
         AIF   (&IBITS GT 0 AND &IBITS LT 16).SUB0
         MNOTE '**NO. OF BITS OUTSIDE RANGE OF 1-15**'
         MEXIT
.SUB0    AIF   (&SUB EQ 1).SUBA
         AIF   ('&SYSLIST(2)' EQ '').BACKUP
&K       SETA  K'&SYSLIST(2)
&B       SETB  ('&SYSLIST(2)'(1,1) EQ 'B')
&S       SETC  '&SYSLIST(2)'(&B+1,&K-&B)
         AGO   .SUBB
.SUBA    ANOP
         AIF   ('&SYSLIST(&OP,2)' EQ '').BACKUP
&K       SETA  K'&SYSLIST(&OP,2)
&B       SETB  ('&SYSLIST(&OP,2)'(1,1) EQ 'B')
&S       SETC  '&SYSLIST(&OP,2)'(&B+1,&K-&B)
.SUBB    ANOP
&T       SETA  &S
.BACKUP  ANOP
&NRENTS  SETA  &NRENTS+1
&TEMP    SETA  &ISHIFT
         AIF   (&IBITS NE 1 OR &B NE 0).BIN2
         DC    AL.12(&SYSLIST(&OP,1)-&DSECT),XL.4'&TEMP'
         AGO   .OUT
.BIN2    AIF   (&SUB EQ 1).BIN2A
         AIF   ('&SYSLIST(2)' NE '').BIN3
         AGO   .BIN2B
.BIN2A   AIF   ('&SYSLIST(&OP,2)' NE '').BIN3
.BIN2B   ANOP
&B       SETB  1
.BIN3    ANOP
         DC    AL.12(&SYSLIST(&OP,1)-&DSECT),XL.4'C'
         DC    BL.1'&B',AL.7(&T),AL.4(&IBITS),FL.4'&ISHIFT'
.OUT     ANOP
&NDX     SETA  0
         AIF   (&SUB EQ 0).OUTA
         AIF   (&OP LT N'&SYSLIST).LOOP
.OUTA    ANOP
         MEND
         EJECT
*  HEX MACRO     HEX MACRO     HEX MACRO    HEXN MACRO     HEX MACRO  *
         SPACE 2
         MACRO
         HEX   &LOGITEM,&SKIP$OR,&BYTES
.*                            THIS MACRO FACILITATES GENERATION OF THE
.*                                 TABLE ENTRIES SPECIFYING HEX TYPE
.*                                 CONVERSIONS.
.*                            &LOGITEM - NAME OF DATA ITEM TO BE
.*                                 CONVERTED
.*                            &SKIP$OR - NUMBER OF SPACES TO SKIP ON
.*                                 MULTIPLE PRINT OR NBR OF SPACES TO
.*                                 BACK UP FROM COLUMN FOR START OF
.*                                 PRINTING. THE LETTER B PRECEEDS THE
.*                                 NUMBER (B8) IF THE LATTER OPTION IS
.*                                 DESIRED. OMISSION OF THIS OPERAND
.*                                 FORCES OPTION 2 WITH BACKUP SPACING
.*                                 EQUAL TO THE NUMBER OF BYTES SPECI-
.*                                 FIED BY THE NEXT OPERAND (TWICE THIS
.*                                 NUMBER IF SUFFIX IS EXPLICITLY OR
.*                                 IMPLICITLY B).
.*                            &BYTES - NUMBER OF TOTAL INPUT BYTES TO
.*                                 PROCESS. THIS NUMBER IS SUFFIXED
.*                                 WITH AN L,R, OR B TO STIPULATE
.*                                 PRINTING OF THE LEFT HALF, RIGHT
.*                                 HALF, OR BOTH HALVES OF THE BYTE
.*                                 RESPECTIVELY. OMISSION OF THIS
.*                                 OPERAND DEFAULTS TO B WITH THE NUM-
.*                                 BER OF BYTES EQUAL TO THE ITEM LNGTH
.*                            A NEW ENTRY IS GENERATED ONLY WHEN NECES-
.*                                 SARY. THIS MACRO MUST BE THE FIRST
.*                                 MACRO INSTRUCTION ISSUED FOR THE
.*                                 SUBJECT LINE(S).
         GBLC &ITEM(1000),&DSECT
         GBLA &ITEMNBR,&BITS(1000),&SHIFT(1000),&NRENTS
         LCLB  &B
         LCLA  &IBITS,&NDX,&ISHIFT,&K,&T,&CODE,&BYTESP
         LCLC  &S,&LETTER
.LOOP    ANOP
&NDX     SETA  &NDX+1
         AIF   ('&ITEM(&NDX)' EQ '&LOGITEM').FOUND
         AIF   (&NDX LT &ITEMNBR).LOOP
         MNOTE *,'LOG ITEM NOT DEFINED'
         MEXIT
.FOUND   ANOP
&ISHIFT  SETA  &SHIFT(&NDX)
&IBITS   SETA  &BITS(&NDX)
&CODE    SETA  1
&K       SETA  K'&BYTES
         AIF   (&IBITS-(&IBITS/4)*4 EQ 0).OK
         MNOTE *,'BIT LENGTH SPECIFICATION INCONSISTENT WITH CONTROL'
         MEXIT
.OK      AIF   ('&BYTES' EQ '').USEDEFN
&LETTER  SETC  '&BYTES'(&K,1)
&S       SETC  '&BYTES'(1,&K-1)
&BYTESP  SETA  &S
         AIF   ('&LETTER' EQ 'B').BOTH
         AIF   (&ISHIFT EQ 0 OR &ISHIFT EQ 4).OK2
.ERR     MNOTE *,'DATA NOT ALLIGNED ON HEX OR BYTE BOUNDARY'
         MEXIT
.USEDEFN ANOP
&BYTESP  SETA  &IBITS/8
.BOTH    AIF   (&ISHIFT NE 0).ERR
&CODE    SETA  3
&T       SETA  &BYTESP*2-1
&S       SETC  '&T'
.OK2     AIF   (&ISHIFT EQ 0).L
&CODE    SETA  2
.L       ANOP
&B       SETB  1
         AIF   ('&SKIP$OR' EQ '').BACKUP
&K       SETA  K'&SKIP$OR
&B       SETB  ('&SKIP$OR'(1,1) EQ 'B')
&S       SETC  '&SKIP$OR'(&B+1,&K-&B)
.BACKUP  ANOP
&NRENTS  SETA  &NRENTS+1
         DC    AL.12(&LOGITEM-&DSECT),XL.4'A'
         DC    BL.1'&B',FL.7'&S',FL.4'&BYTESP',FL.4'&CODE'
.OUT     ANOP
         MEND
         SPACE 2
*  ROUTINE MACRO     ROUTINE MACRO     ROUTINE MACRO     ROUTINE MACRO*
         SPACE 2
         MACRO
&NAME    ROUTINE &LOGITEM,&RNAME
         GBLC  &DSECT,&CSECT
         GBLC  &ITEM(1000)
         GBLA  &SHIFT(1000),&ITEMNBR
         GBLA  &NRENTS
         LCLA  &ISHIFT,&NDX
.LOOP    ANOP
&NDX     SETA  &NDX+1
         AIF   ('&ITEM(&NDX)' EQ '&LOGITEM').FOUND
         AIF   (&NDX LT &ITEMNBR).LOOP
         MNOTE *,'LOG ITEM NOT DEFINED'
         MEXIT
.FOUND   ANOP
&ISHIFT  SETA  &SHIFT(&NDX)
&NAME    DC    AL.12(&LOGITEM-&DSECT),XL.4'D'
         DC    AL.4(&ISHIFT),AL.12(&RNAME-&CSECT)
&NRENTS  SETA  &NRENTS+1
         MEND
         EJECT
*  LABEL MACRO     LABEL MACRO     LABEL MACRO     LABEL MACRO        *
         SPACE 2
         MACRO
&NAME    LABEL &LABEL1,&LABEL2,&LABEL3,&LABEL4,&LABEL5,&LABEL6,        C
               &LABEL7,&LABEL8
.*                                 THIS MACRO ALLOWS LABELS TO
.*                                 BE SPECIFIED FOR A GIVEN LINE. THE
.*                                 LABELS SHOULD BE ENCLOSED IN QUOTA-
.*                                 TION MARKS. THE COLUMN AND LINE END
.*                                 CONTROLS WILL BE INSERTED BY THE
.*                                 MACRO. OMITTED OPERANDS MEAN NO
.*                                 LABEL REQUIRED FOR RESPECTIVE
.*                                 COLUMN.
         GBLC &LABELS(2000)
         GBLC  &LBL(100)
         GBLA  &LBLCTR(100)
         GBLA  &LABELNR
         GBLA  &NRENTS,&LPTR,&INITIAL
         GBLB  &DIAGTST,&DIAGSET,&DIAGLIN
         LCLC  &MARGIN,&TACK
         LCLC  &TEXT
         LCLA  &OP,&K,&START,&NRCHAR,&REMAIN
         AIF   (&INITIAL EQ 0).DIAGCLR
         AIF   (&DIAGTST EQ 0).DIAGCLR
         AIF   (&DIAGLIN EQ 1).DIAGCLR
         MNOTE '**NO LINE ASSOCIATED WITH PREVIOUS LABEL**'
***** THE FOLLOWING GENERATED FOR TEST PURPOSES *****
*        LINE
         LINE
.DIAGCLR ANOP
&DIAGSET SETB  0
&DIAGLIN SETB  0
&MARGIN  SETC  '*'
         AIF   ('&NAME' EQ '').LOOPOUT
&LPTR    SETA  &LPTR+1
&LBL(&LPTR) SETC '&NAME'
&LBLCTR(&LPTR) SETA &LABELNR+1
.LOOPOUT ANOP
&OP      SETA  &OP+1
&K       SETA  K'&SYSLIST(&OP)
         AIF (&K GE 2).NORM
&K       SETA  2
.NORM    ANOP
&START   SETA  2
         AIF (&OP LT N'&SYSLIST).NOTLAST
&MARGIN  SETC  '$'
.NOTLAST ANOP
&NRCHAR  SETA  8
&TACK    SETC  ''
.LOOPIN  ANOP
&REMAIN  SETA  &K-&START
         AIF (&REMAIN GE 8).MORE
&TACK    SETC  '&MARGIN'
&NRCHAR  SETA  &REMAIN
.MORE    ANOP
&TEXT    SETC  ''
         AIF (&REMAIN EQ 0).ENDTEXT
&TEXT    SETC  '&SYSLIST(&OP)'(&START,&NRCHAR)
.ENDTEXT ANOP
&LABELNR SETA  &LABELNR+1
&LABELS(&LABELNR) SETC '&TEXT'.'&TACK'
&START   SETA  &START+8
         AIF ('&TACK' EQ '').LOOPIN
         AIF ('&TACK' EQ '*').LOOPOUT
         MEND
         EJECT
         MACRO
         LSTART
.*                            THIS MACRO IS ISSUED WHEN THE LABEL AREA
.*                                 OF THE TABLE IS TO BEGIN.
         GBLA  &LABELNR
         GBLC  &LBL(100)
         GBLC &LABELS(2000)
         GBLA  &LBLCTR(100)
         GBLB  &DIAGTST,&DIAGSET,&DIAGLIN
         LCLA  &NDX,&CTR
         AIF   (&DIAGTST EQ 0).NODIAG
         AIF   (&DIAGSET EQ 0).NODIAG
         MNOTE '**NO LABEL ASSOCIATED WITH LAST SET**'
***** THE FOLLOWING GENERATED FOR TEST PURPOSES *****
*        LABEL '%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%'
               LABEL '%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%'
.NODIAG  ANOP
         LINEND
&CTR     SETA  1
.LOOP    ANOP
&NDX     SETA  &NDX+1
         AIF   (&LBLCTR(&CTR) NE &NDX).NOLBL
&LBL(&CTR) DC  C'&LABELS(&NDX)'
&CTR     SETA  &CTR+1
         AGO   .LOOPCTL
.NOLBL   ANOP
         DC    C'&LABELS(&NDX)'
.LOOPCTL AIF   (&NDX LT &LABELNR).LOOP
         MEND
         SPACE 3
         MACRO
         LINEND
.*                            THIS MACRO IS USED ONLY BY THE LINE
.*                                 MACRO TO SET THE NUMBER OF ENTRIES
.*                                 ASSOCIATED WITH EACH LINE.
         GBLA  &NRENTS
         GBLC &NRENTSX
         ORG   &NRENTSX
         DC    FL1'&NRENTS'
         ORG
&NRENTS  SETA  0
B&SYSNDX EQU   *
&NRENTSX SETC  'B&SYSNDX'
         MEND
         EJECT
         GBLC  &DSECT,&CSECT
IFCEMER4 CSECT
&CSECT   SETC  'IFCEMER4'
&DSECT   SETC  'MER4'
*
*        SENSE FORMAT 4
*
MER4     DSECT
         DSGEN (CLASRC,8),(SYSREL,8),(SWITCHES,16)
         DSGEN (SPARE1,32)
         DSGEN (DAY,32)
         DSGEN (HOUR,32)
         DSGEN (SPARE2,8),(CPUSERL,24)
         DSGEN (CPUID,16),(SPARE3,16)
         DSGEN (JOBID1,32)
         DSGEN (JOBID2,32)
         DSGEN (FCCWCC,8),(FCCWDA,24)
         DSGEN (FCCWFL,8),(FCCW,8),(FCCWCT,16)
         DSGEN (CSWUS1,8),(CSWUS2,24)
         DSGEN (CSWUS3,8),(CSWUS4,8),(CSWUS5,16)
         DSGEN (DEVDPD,8),(SECCUA,24)                            S21021
         DSGEN (DEVTYP1,16),(DEVTYP2,16)
         DSGEN (SPARE4,8),(CUA,24)
         DSGEN (IORET,16),(SBCNT,16)                           @Y30LPDS
         DSGEN (VOLLAB1,8),(VOLLAB2,16),(VOLLAB3,8)
         DSGEN (VOLLAB4,8),(VOLLAB5,8),(SPARE5,16)
         DSGEN (LSKAD1,8),(LSKAD2,16),(LSKAD3,8)
         DSGEN (LSKAD4,8),(LSKAD5,16),(LSKAD6,8)
         DSGEN (AHA1,32)
         DSGEN (AHA2,16),(SPARE6,16)
         DSGEN (SNB00,1),(SNB01,1),(SNB02,1),(SNB03,1),(SNB04,1),      C
               (SNB05,1),(SNB06,1),(SNB07,1),(SNB10,1),(SNB11,1),      C
               (SNB12,1),(SNB13,1),(SNB14,1),(SNB15,1),(SNB16,1),      C
               (SNB17,1),(SNB20,1),(SNB21,1),(SNB22,1),(SNB23,1),      C
               (SNB24,1),(SNB25,1),(SNB26,1),(SNB27,1),(SNB30,1),      C
               (SNB31,1),(SNB32,1),(SNB33,1),(SNB34,1),(SNB35,1),      C
               (SNB36,1),(SNB37,1)
         DSGEN (SNB40,1),(SNB41,1),(SNB42,1),(SNB43,1),(SNB44,1),      C
               (SNB45,1),(SNB46,1),(SNB47,1),(SNB50,1),(SNB51,1),      C
               (SNB52,1),(SNB53,1),(SNB54,1),(SNB55,1),(SNB56,1),      C
               (SNB57,1),(SNB60,1),(SNB61,1),(SNB62,1),(SNB63,1),      C
               (SNB64,1),(SNB65,1),(SNB66,1),(SNB67,1),(SNB70,1),      C
               (SNB71,1),(SNB72,1),(SNB73,1),(SNB74,1),(SNB75,1),      C
               (SNB76,1),(SNB77,1)
         DSGEN (SNB80,1),(SNB81,1),(SNB82,1),(SNB83,1),(SNB84,1),      C
               (SNB85,1),(SNB86,1),(SNB87,1),(SNB90,1),(SNB91,1),      C
               (SNB92,1),(SNB93,1),(SNB94,1),(SNB95,1),(SNB96,1),      C
               (SNB97,1),(SNB100,1),(SNB101,1),(SNB102,1),(SNB103,1),  C
               (SNB104,1),(SNB105,1),(SNB106,1),(SNB107,1),(SNB110,1), C
               (SNB111,1),(SNB112,1),(SNB113,1),(SNB114,1),(SNB115,1), C
               (SNB116,1),(SNB117,1)
         DSGEN (SNB120,1),(SNB121,1),(SNB122,1),(SNB123,1),(SNB124,1), C
               (SNB125,1),(SNB126,1),(SNB127,1),(SNB130,1),(SNB131,1), C
               (SNB132,1),(SNB133,1),(SNB134,1),(SNB135,1),(SNB136,1), C
               (SNB137,1),(SNB140,1),(SNB141,1),(SNB142,1),(SNB143,1), C
               (SNB144,1),(SNB145,1),(SNB146,1),(SNB147,1),(SNB150,1), C
               (SNB151,1),(SNB152,1),(SNB153,1),(SNB154,1),(SNB155,1), C
               (SNB156,1),(SNB157,1)
         DSGEN (SNB160,1),(SNB161,1),(SNB162,1),(SNB163,1),(SNB164,1), C
               (SNB165,1),(SNB166,1),(SNB167,1),(SNB170,1),(SNB171,1), C
               (SNB172,1),(SNB173,1),(SNB174,1),(SNB175,1),(SNB176,1), C
               (SNB177,1),(SNB180,1),(SNB181,1),(SNB182,1),(SNB183,1), C
               (SNB184,1),(SNB185,1),(SNB186,1),(SNB187,1),(SNB190,1), C
               (SNB191,1),(SNB192,1),(SNB193,1),(SNB194,1),(SNB195,1), C
               (SNB196,1),(SNB197,1)
         DSGEN (SNB200,1),(SNB201,1),(SNB202,1),(SNB203,1),(SNB204,1), C
               (SNB205,1),(SNB206,1),(SNB207,1),(SNB210,1),(SNB211,1), C
               (SNB212,1),(SNB213,1),(SNB214,1),(SNB215,1),(SNB216,1), C
               (SNB217,1),(SNB220,1),(SNB221,1),(SNB222,1),(SNB223,1), C
               (SNB224,1),(SNB225,1),(SNB226,1),(SNB227,1),(SNB230,1), C
               (SNB231,1),(SNB232,1),(SNB233,1),(SNB234,1),(SNB235,1), C
               (SNB236,1),(SNB237,1)
***********************************************************************
*                                                                     *
*              THIS ENTIRE MODULE IS IN SUPPORT OF  S20201            *
*              EXCEPT WHERE OTHERWISE INDICATED                       *
*                                                                     *
***********************************************************************
IFCEMER4 CSECT
         USING *,R9
         USING MER4,R2                                         @Y30LPDS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
L0       EQU   0
L1       EQU   1
L2       EQU   2
L3       EQU   3
L4       EQU   4
L5       EQU   5
L6       EQU   6
L7       EQU   7
L8       EQU   8
L9       EQU   9
L10      EQU   10
L11      EQU   11
L12      EQU   12
L13      EQU   13
L14      EQU   14
L15      EQU   15
L16      EQU   16
L17      EQU   17
L18      EQU   18
L19      EQU   19
L20      EQU   20
L23      EQU   23
L24      EQU   24
L26      EQU   26
H0F      EQU   X'0F'
HE0      EQU   X'E0'
H80      EQU   X'80'
H40      EQU   X'40'
H20      EQU   X'20'
H10      EQU   X'10'
H21      EQU   X'21'
H23      EQU   X'23'
H25      EQU   X'25'
H29      EQU   X'29'
CLRSGN   EQU   X'F0'
SS1CNT   EQU   28                      # OF SENSE BYTES IF 3330
*                                       IS ATTACHED TO 3850    @Y30LPDS
HFF      EQU   X'FF'                                           @Y30LPDS
EMU      EQU   X'08'                 EMULATION BIT
         EJECT                                                 @Y30LPDS
         DC    A(ENTRIES)
         DC    CL8'IFCEMER4'
XR9PTR   DC    F'0'
XLABELP  DC    F'0'
*
*        SET HEX -PRINTS 1 CHAR HEX
*
SETHEX   STM   R5,R8,XSAVE             SAVE REGS
         LA    R8,CHARS                CHARACTER TABLE POINTER
         IC    R7,L0(R4)               PICK UP CHARACTER
         SLL   R7,L24                  MOVE TO HI R7
         SR    R6,R6                   CLEAR REGS
         SLDL  R6,L4                   MOVE FOUR BITS
         AR    R6,R8                   ADD OFFSET AND POINTER
         MVC   L0(L1,R3),L0(R6)        MOVE CHARACTER TO LINE
         SR    R6,R6                   CLEAR AGAIN
         SLDL  R6,L4                   MOVE LAST 4 BITS
         AR    R6,R8                   COMPUTE OFFSET
         MVC   L1(L1,R3),L0(R6)        MOVE LAST CHARACTER
         B     RETURN
*
*        TEST TO SEE IF RECORD IS IN AR-149 FORMAT
*
SETCONV  TM    L0(R4),HEX02            HAS RECORD BEEN CONVERTED KEY32
         BCR   ON,R14                  YES RETURN
         SR    R15,R15                 NO CLEAR R15 FOR NO PRINT OF
         BR    R14                     LINE
ON       EQU   1
HEX02    EQU   X'02'
*
*        NO OP RETURN
*
SKIPPER  BR    R14
*
*        TO ADD SPACES ON LINE
*
SPACER2  LA    R3,L4(R3)
         BR    14
*
*        RESTORE REGISTERS USED BY ROUTINES IN THIS MODULE
*
RETURN   LM    R5,R8,XSAVE             RESTORE REGS
         BR    R14                     RETURN
*
*        SET THE DATE READY FOR PRINT
*
SETDATE  UNPK  XSAVE(L8),L1(L3,R4)     UNPACK THE DATE
         MVC   L0(L3,R3),XSAVE+L5      MOVE DAY
         MVC   L6(L2,R3),XSAVE+L3      MOVE YEAR
         BR    R14
*
*        SET THE DEVICE TYPE READY FOR PRINT
*
SETDEVT  STM   R5,R8,XSAVE             SAVE REGS
         LA    R5,L1                    LOAD NO. OF POSSIBLE DEVICE TPS
         LA    R6,DEVTABLE             POIT TO DEVICE TYPES
SETDEVLP CLC   L0(L2,R4),L0(R6)        COMPARE TYPES
         BE    SDEVFND                 FOUND ONE SO BRANCH
         LA    R6,L6(R6)               MOVE POINTER TO LOOK AT NEXT T
         BCT   R5,SETDEVLP             BRANCH IF THERE ARE MORE
         MVC   L0(L4,R3),NONE          NO DEV TYPE MOVE IN NONE
         B     RETURN
SDEVFND  MVC   L0(L4,R3),L2(R6)        MOVE DEV NAME TO PRINT LINE
         B     RETURN
*
*        CHECK FOR CPU AND SERIAL NUMBER
*
SETCPUNO STM   R5,R8,XSAVE             SAVE REGS
         SR    R5,R5                   CLEAR R5
         IC    R5,L0(R4)               PICK UP MODEL NO
         SLL   R5,L8                   IN TO
         IC    R5,L1(R4)               R5
         LTR   R5,R5                   TEST FOR ZERO
         BNZ   RETURN                  NONE ZERO BR
         LR    R5,R3                   PUT LINE ADDRESS IN R5
         MVC   L0(L9,R5),UNIVERS       PUT IN UNIVERSAL
         MVC   L11(L6,R5),NONE         PUT IN NONE
         B     RETURN
*
*        SET PROGRAM NAME TO PRINT LINE
*
SETPGMN  MVC   L0(L8,R3),L0(R4)        MOVE 8 CHARS TO LINE
         BR    R14                     RETURN
*
*        SET BINARY WITH R4 POINTING TO THE CHAR
*
SETBIN   STM   R5,R8,XSAVE             SAVE REGS
         IC    R7,L0(R4)               PICK UP CHAR TO PRINT
         SLL   R7,L24                  MOVE IT TO HI OF R7
         LR    R8,R3                   PRINT POS POINTER TO REG8
         LA    R5,L8                   LOOP COUNT
SETBINA  SR    R6,R6                   CLEAR REG 6
         SLDL  R6,L1                   MOVE BIT TO R6
         STC   R6,L0(R8)               PUT IT INTO PRINT LINE
         MVZ   L0(L1,R8),CHAR0
         LA    R8,L1(R8)               MOVE POINTER TO NEXT POS
         BCT   R5,SETBINA              LOOP TILL 8 PRINTED
         B     RETURN
SETBIN1  STM   R5,R8,XSAVE             SAVE REGS
         IC    R7,L0(R4)               PICK UP CHAR TO PRINT
         SLL   R7,L24                  MOVE IT TO HI OF R7
         LR    R8,R3                   PRINT POS POINTER TO REG 8
         SR    R6,R6                   CLEAR REG 6
         SLDL  R6,L1                   MOVE BIT TO REG 6
         STC   R6,L11(R8)
         OC    L11(L1,R8),CHAR0
         B     RETURN                  RETURN
SETBIN2  STM   R5,R8,XSAVE             SAVE REGS                XL03130
         IC    R7,L0(R4)               INSERT CHARACTER         XL03130
         SLL   R7,L24                  SHIFT TO HIGH-ORDER BYTE XL03130
         LR    R8,R3                   LOAD ADDR OF PRINT POS.  XL03130
         LA    R5,L8                   LOAD LOOP COUNT          XL03130
         S     R8,ELEVEN               SUBTRACT FROM PRINT POS  XL03130
         B     SETBINA                 BRANCH TO CONTINUE       XL03130
*        CONVERT COUNTERS FROM HEX TO DECIMAL READY FOR PRINT
*
CONV1    STM   R5,R8,XSAVE             SAVE REGISTERS
         XC    READYP(L8),READYP       CLEAR UNPACK BUFFER
         SR    R6,R6                   ZERO REG
         LH    R6,L0(R4)               LOAD DATA TO BE CONVERTED
         CVD   R6,READYP               CONVERT ERROR COUNTER TO DECI
         UNPK  READYP(L5),READYP+L5(L3)  UNPACK DECI COUNT
         OI    READYP+L4,CLRSGN        CLEAR SIGN
         MVC   L0(L4,R3),READYP+L1     MOVE COUNTER READY TO PRINT
         B     RETURN
CHK3340  CLC   L0(L2,R4),DEV3340       IS THIS A 3340           XL03130
         BE    CHKGOOD               YES - CONTINUE
         CLC   L0(L2,R4),DEV3350     IS THIS IS 3350?
         BE    CHKGOOD               YES, BRANCH
         TM    SNB24,EMU             THIS IS AN EMULATED DEVICE?
         BNO   SKIPPER               NO, EXIT
CHKGOOD  STM   R5,R8,XSAVE           STORE REGS
         LA    R5,LAB3340-1            LOAD ADDR OF 3340 LABELS XL03130
         ST    R5,XLABELP              STORE ADDR FOR EREP0     XL03130
         LA    R5,LIN3340-4            LOAD ADDR OFFRST LINE    XL03130
         ST    R5,XR9PTR               STORE LINE ADDR          XL03130
         SR    R15,R15                 DON'T PRINT THIS LINE    XL03130
         B     RETURN                  RETURN                   XL03130
         SPACE 2
*                                                              @Y30LPDS
*      THE FOLLOWING CHECKS THE RECORD FOR EXTRA SENSE(28 BYTES).
*        IF THE RECORD DOES CONTAIN SS1(3850) SENSE,THIS ROUTINE
*        WILL SET PTRS TO PASS CONTROL TO IFCEMER0.            @Y30LPDS
*                                                              @Y30LPDS
*        IF RECORD IS REGULAR 3330,SET PTRS TO END NOW         @Y30LPDS
*                                                              @Y30LPDS
ENDNOW   STM   R5,R8,XSAVE             SAVE REGS               @Y30LPDS
         CLI   SBCNT+1,SS1CNT          EXTRA 3850 SENSE BYTES? @Y30LPDS
         BE    PTMER0                  YES - BR TO LOAD MER0   @Y30LPDS
         SPACE 1                                               @Y30LPDS
         L     R5,ENDLINE              LOAD ADDR OF LAST LINE   XL03130
         ST    R5,XR9PTR               STORE ADDR FOR EREP0     XL03130
         L     R5,ENDLABEL             LOAD ADDR OF LAST LABEL  XL03130
         ST    R5,XLABELP              STORE ADDR FOR EREP0     XL03130
         SR    R15,R15                 DONT PRINT THIS LINE     XL03130
         B     RETURN                  RETURN                   XL03130
         SPACE 1                                               @Y30LPDS
PTMER0   DS    0H                                              @Y30LPDS
         LA    R5,FEMER0-L4            LOAD PARM & MOD NAME ADDR
         ST    R5,XR9PTR               STORE FOR EREP0         @Y30LPDS
         SR    R15,R15                 DONT PRINT THIS LINE    @Y30LPDS
         MVI   CLASRC,HFF              SET INDICATOR FOR MER0  @Y30LPDS
         B     RETURN                  BR TO RETURN            @Y30LPDS
         SPACE
         DS    0F
XSAVE    DC    5F'0'
NONE     DC    CL6'NONE  '
CHARS    DC    C'0123456789ABCDEF'
CHAR0    DC    C'0'
ELEVEN   DC    F'11'
ENDLABEL DC    A(LASTLABL-1)
ENDLINE  DC    A(LASTLINE-4)
UNIVERS  DC    C'UNIVERSAL'
DEVTABLE DC    X'2009'
         DC    C'3330'
DEV3340  DC    X'200A'                 DEVICE TYPE CODE         XL03130
         DC    C'3340'                           FOR WINCHESTER XL03130
DEV3350  DC    X'200B'                 DEVICE TYPE CODE        @Z40RIVS
         DC    C'3350'                           FOR MADRID    @Z40RIVS
         SPACE 2                                               @Y30LPDS
FEMER0   DC    X'FE'                   CODE TO EREP0 TO CALL MOD
         DC    CL8'IFCEMER0'           MOD TO CALL             @Y30LPDS
         SPACE 2                                               @Y30LPDS
         DS    0D
READYP   DC    CL8' '
ENTRIES  DS    0F
         DC    XL4'00010001'       FORMAT 1
         DC    XL4'00010001'       FORMAT 2
         DC    A(LABELS)           LABEL POINTER
         DC    H'0'                DISP
*
*        SKIP TO CHANNEL 1
*
         DC    X'01'
         DC    X'000B0B00'
*
         LINE  (12,15),(3,15),SKIP=1
         ROUTINE   SNB80,SETHEX
         ROUTINE   SNB90,SETHEX
         ROUTINE   SNB100,SETHEX
         ROUTINE   SNB110,SETHEX
         ROUTINE   SNB120,SETHEX
         ROUTINE   SNB130,SETHEX
         ROUTINE   SNB140,SETHEX
         ROUTINE   SNB150,SETHEX
         LABEL 'BYTE 8','BYTE 9','BYTE 10','BYTE 11','BYTE 12','BYTE 13C
               ','BYTE 14','BYTE 15'
         LINE  ,(1,1),SKIP=2
         ROUTINE   DEVTYP2,CHK3340                              XL03130
         LABEL '-----------------------CCHHR READ FROM COUNT-----------C
               ------------------  SECTOR NUMBER  ACCESS OFFSET  RETRY X
               COUNT'
         LINE  (13,15),(1,15),SKIP=1
         BIN   SNB80
         BIN   SNB90
         BIN   SNB100
         BIN   SNB110
         BIN   SNB120
         BIN   SNB130
         BIN   SNB140
         BIN   SNB150
         LABEL 'CYL  -','CYL 128','HEAD -','HEAD -','RECORD -','SECTOR C
               -','REVERSE','RETRY -'
         LINE  (13,15),(1,15)
         BIN   SNB81
         BIN   SNB91
         BIN   SNB101
         BIN   SNB111
         BIN   SNB121
         BIN   SNB131
         BIN   SNB141
         BIN   SNB151
         LABEL 'CYL  -','CYL  64','HEAD -','HEAD -','RECORD 64','SECTORC
                64','OFFSET -','RETRY -'
         LINE  (13,15),(1,15)
         BIN   SNB82
         BIN   SNB92
         BIN   SNB102
         BIN   SNB112
         BIN   SNB122
         BIN   SNB132
         BIN   SNB142
         BIN   SNB152
         LABEL 'CYL  -','CYL  32','HEAD -','HEAD -','RECORD 32','SECTORC
                32','OFFSET 32','RETRY -'
         LINE  (13,15),(1,15)
         BIN   SNB83
         BIN   SNB93
         BIN   SNB103
         BIN   SNB113
         BIN   SNB123
         BIN   SNB133
         BIN   SNB143
         BIN   SNB153
         LABEL 'CYL  -','CYL  16','HEAD -','HEAD 16','RECORD 16','SECTOC
               R 16','OFFSET 16','RETRY 16'
         LINE  (13,15),(1,15)
         BIN   SNB84
         BIN   SNB94
         BIN   SNB104
         BIN   SNB114
         BIN   SNB124
         BIN   SNB134
         BIN   SNB144
         BIN   SNB154
         LABEL 'CYL  -','CYL   8','HEAD -','HEAD  8','RECORD  8','SECTOC
               R  8','OFFSET  8','RETRY  8'
         LINE  (13,15),(1,15)
         BIN   SNB85
         BIN   SNB95
         BIN   SNB105
         BIN   SNB115
         BIN   SNB125
         BIN   SNB135
         BIN   SNB145
         BIN   SNB155
         LABEL 'CYL  -','CYL   4','HEAD -','HEAD  4','RECORD  4','SECTOC
               R  4','OFFSET  4','RETRY  4'
         LINE  (13,15),(1,15)
         BIN   SNB86
         BIN   SNB96
         BIN   SNB106
         BIN   SNB116
         BIN   SNB126
         BIN   SNB136
         BIN   SNB146
         BIN   SNB156
         LABEL 'CYL 512','CYL   2','HEAD - ','HEAD  2','RECORD  2','SECC
               TOR  2','OFFSET  2','RETRY  2'                   XL03145
         LINE  (13,15),(1,15),SKIP=3
         BIN   SNB87
         BIN   SNB97
         BIN   SNB107
         BIN   SNB117
         BIN   SNB127
         BIN   SNB137
         BIN   SNB147
         BIN   SNB157
         LABEL 'CYL 256','CYL   1','HEAD -','HEAD  1','RECORD  1','SECTX
               OR  1','OFFSET  1','RETRY  1'
         LINE  (12,15),(3,15),SKIP=1
         ROUTINE   SNB160,SETHEX
         ROUTINE   SNB170,SETHEX
         ROUTINE   SNB180,SETHEX
         ROUTINE   SNB190,SETHEX
         ROUTINE   SNB200,SETHEX
         ROUTINE   SNB210,SETHEX
         ROUTINE   SNB220,SETHEX
         ROUTINE   SNB230,SETHEX
         LABEL 'BYTE 16','BYTE 17','BYTE 18','BYTE 19','BYTE 20','BYTE C
                21','BYTE 22','BYTE 23'
         LINE  ,(1,90),SKIP=2
         LABEL 'SOURCE ID','-----ERROR SYMPTOM CODE----'
         LINE  (2,15),(1,15),SKIP=1
         ROUTINE   SNB160,SETBIN1
         ROUTINE   SNB170,SETBIN
         ROUTINE   SNB180,SETBIN
         ROUTINE   SNB190,SETBIN
         ROUTINE   SNB200,SETBIN
         ROUTINE   SNB210,SETBIN
         ROUTINE   SNB220,SETBIN
         ROUTINE   SNB230,SETBIN
         LABEL 'CONTRL UNIT'
         LINE  (13,1),(1,1)
         BIN   SNB161
         LABEL 'CONTRL UNIT'
         LINE  (13,1),(1,1)
         BIN   SNB162
         LABEL 'DRIVE 3/6'
         LINE
         BIN   SNB163
         LABEL 'DRIVE 3/6'
         LINE
         BIN   SNB164
         LABEL 'DRIVE 3/6'
         LINE
         BIN   SNB165
         LABEL 'DRIVE 3/6'
         LINE
         BIN   SNB166
         LABEL 'DRIVE 3/6'
         LINE  (13,1),(1,1),SKIP=3
         BIN   SNB167
         LABEL 'DRIVE 3/6'                                      XL03130
         LINE  ,,SKIP=1                                         XL03130
         ROUTINE   DEVTYP2,ENDNOW                               XL03130
         LABEL ' '                                              XL03130
         EJECT
*                                                          *
*    W I N C H E S T E R ( 3 3 4 0 )  E D I T  T A B E L   *
*                                                          *
         SPACE
LIN3340  LINE  (12,15),(1,1),SKIP=2                             XL03130
         ROUTINE   SPARE1,SKIPPER                               XL03130
LAB3340  LABEL '-----------------------CCHHR READ FROM COUNT-----------C
               ------------------  SECTOR NUMBER '              XL03130
         LINE  (13,15),(1,15),SKIP=1                            XL03130
         BIN   (SNB80),(SNB90),(SNB100),(SNB110),(SNB120)       XL03130
         BIN   SNB130                                           XL03130
         ROUTINE   SNB140,SETBIN2                               XL03130
         ROUTINE   SNB150,SETBIN2                               XL03130
         LABEL 'CYL   - ','CYL 128','HEAD -','HEAD  - ','RECORD  - ','SC
               ECTOR  - '                                       XL03130
         LINE  (13,15),(1,15)                                   XL03130
         BIN   (SNB81),(SNB91),(SNB101),(SNB111),(SNB121)       XL03130
         BIN   SNB131                                           XL03130
         LABEL 'CYL   - ','CYL  64','HEAD -','HEAD  - ','RECORD  64','SC
               ECTOR  - '                                       XL03130
         LINE  (13,15),(1,15)                                   XL03130
         BIN   (SNB82),(SNB92),(SNB102),(SNB112),(SNB122)       XL03130
         BIN   SNB132                                           XL03130
         LABEL 'CYL   - ','CYL  32','HEAD -','HEAD  - ','RECORD  32','SC
               ECTOR  32'                                       XL03130
         LINE  (13,15),(1,15)                                   XL03130
         BIN   (SNB83),(SNB93),(SNB103),(SNB113),(SNB123)       XL03130
         BIN   SNB133                                           XL03130
         LABEL 'CYL   - ','CYL  16','HEAD -','HEAD  16','RECORD  16','SC
               ECTOR  16'                                       XL03130
         LINE  (13,15),(1,15)                                   XL03130
         BIN   (SNB84),(SNB94),(SNB104),(SNB114),(SNB124)       XL03130
         BIN   SNB134                                           XL03130
         LABEL 'CYL   - ','CYL   8','HEAD -','HEAD   8','RECORD   8','SC
               ECTOR   8'                                       XL03130
         LINE  (13,15),(1,15)                                   XL03130
         BIN   (SNB85),(SNB95),(SNB105),(SNB115),(SNB125)       XL03130
         BIN   SNB135                                           XL03130
         LABEL 'CYL   - ','CYL   4','HEAD -','HEAD   4','RECORD   4','SC
               ECTOR   4'                                       XL03130
         LINE  (13,15),(1,15)                                   XL03130
         BIN   (SNB86),(SNB96),(SNB106),(SNB116),(SNB126)       XL03130
         BIN   SNB136                                           XL03130
         LABEL 'CYL  512','CYL   2','HEAD -','HEAD   2','RECORD   2','SC
               ECTOR   2'                                       XL03130
         LINE  (13,15),(1,15),SKIP=3                            XL03130
         BIN   (SNB87),(SNB97),(SNB107),(SNB117),(SNB127)       XL03130
         BIN   SNB137                                           XL03130
         LABEL 'CYL  256','CYL   1','HEAD -','HEAD   1','RECORD   1','SC
               ECTOR   1'                                       XL03130
         LINE  (12,15),(3,15),SKIP=1                            XL03130
         ROUTINE   SNB160,SETHEX                                XL03130
         ROUTINE   SNB170,SETHEX                                XL03130
         ROUTINE   SNB180,SETHEX                                XL03130
         ROUTINE   SNB190,SETHEX                                XL03130
         ROUTINE   SNB200,SETHEX                                XL03130
         ROUTINE   SNB210,SETHEX                                XL03130
         ROUTINE   SNB220,SETHEX                                XL03130
         ROUTINE   SNB230,SETHEX                                XL03130
         LABEL 'BYTE 16','BYTE 17','BYTE 18','BYTE 19','BYTE 20','BYTE C
               21','BYTE 22','BYTE 23'                          XL03130
         LINE  ,(91,26),SKIP=2                                  XL03130
         ROUTINE   SPARE1,SKIPPER                               XL03130
         LABEL '-----ERROR SYMPTOM CODE----'                    XL03130
         LINE  (2,15),(1,1),SKIP=3                              XL03130
         ROUTINE   SNB160,SETBIN                                XL03130
         ROUTINE   SNB170,SETBIN                                XL03130
         ROUTINE   SNB180,SETBIN                                XL03130
         ROUTINE   SNB190,SETBIN                                XL03130
         ROUTINE   SNB200,SETBIN                                XL03130
         ROUTINE   SNB210,SETBIN                                XL03130
         ROUTINE   SNB220,SETBIN                                XL03130
         ROUTINE   SNB230,SETBIN                                XL03130
         LABEL ' '                                              XL03130
LASTLINE LINE  ,,SKIP=1                                         XL03130
         ROUTINE   SPARE1,SKIPPER                               XL03130
LASTLABL LABEL ' '                                              XL03130
         DC    X'FF'                                            XL03130
         DC    XL2'0070'                                         S21021
         DC    XL2'0018'
         DC    XL2'0000'
LABELS   DS    0F
         DC    C'$'
         LSTART
         END
