         MACRO
&NAME    VOLSTAT &DCB,&TFLAG
         LCLA  &CTR
         LCLC  &FLAG,&REG
         AIF   ('&TFLAG' EQ 'E' OR '&TFLAG' EQ 'C').GO
         IHBERMAC 180,TFLAG,C
&FLAG    SETC  'C'
         AGO   .CHECK
.GO      ANOP
&FLAG    SETC  '&TFLAG'
.CHECK   AIF   ('&DCB'(1,2) EQ '()' OR '&DCB' EQ '').ERR
&CTR     SETA  1
         AIF   ('&DCB'(1,1) EQ '(').PAREN
         LA    0,&DCB SET DCB PARAMETER.
         AGO   .TEST
.PAREN   AIF   ('&DCB'(&CTR+2,1) EQ ',').ERR
         AIF   ('&DCB'(&CTR+2,1) EQ ')').LOAD
&CTR     SETA  &CTR+1
         AGO   .PAREN
.LOAD    ANOP
&REG     SETC  '&DCB'(2,&CTR)
         LR    0,&REG SET DCB PARAMETER.
.TEST    ANOP
         AIF   ('&FLAG' EQ 'E').EOV
         SR    1,1 SET CLOSE FLAG.
         AGO   .SVC
.EOV     ANOP
         LA    1,99(0,0) SET EOV FLAG.                         @YM02296
.SVC     ANOP
         SVC   91 ISSUE VOLUME STATISTICS SVC.
         MEXIT
.ERR     IHBERMAC 190
         MEND
