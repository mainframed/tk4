CTRL  TITLE 'HEWLCTRL - LOADER CONTROL'
***********************************************************************
* STATUS - CHANGE LEVEL 20                                            *
*FUNCTION/OPERATION: HEWLCTRL CONTROLS LINKAGES TO AND FROM THE       *
*    PROCESSING PORTION OF THE LOADER AND LINKAGES TO AND FROM THE    *
*    OBJECT PROGRAM.                                                  *
*ENTRY POINTS:                                                        *
*        HEWLCTRL(HEWLDRGO,LOADER)--VIA EXEC JCL STATEMENT            *
*                                 --VIA LINK,ATTACH,XCTL,OR LOAD/BALR *
*INPUT: REGISTER 1 MUST POINT TO A VALID PARAMETER LIST ADDRESS       *
*    EVENTHOUGH NO PARAMETERS ARE SPECIFIED                           *
*OUTPUT:NONE                                                          *
*EXTERNAL ROUTINES:                                                   *
*        HEWLOADR - TO LOAD P/P                                       *
*        OBJECT PROGRAM- FOR EXECUTION                                *
*EXITS-NORMAL:RETURN TO CALLING PROGRAM VIA REGISTER 14 AFTER OBJECT  *
*    PROGRAM EXECUTION                                                *
*     -ERROR: RETURN TO CALLING PROGRAM VIA REGISTER 14 WITH ERROR    *
*    SEVERITY CODE RIGHT ADJUSTED IN REGISTER 15                      *
*TABLES/WORK AREAS: NONE                                              *
*ATTRIBUTES: READ ONLY,REUSABLE,REENTRANT                             *
*NOTES: NONE                                                          *
***********************************************************************
HEWLCTRL CSECT
*A286400,809300                                                  A40641
*                                                                A43867
*                                                                A44270
*                                                               SA53170
*                                                               SA53276
*                                                                YM2848
*                                                               SA49491
*D059000-059100                                                  YM3526
*                                                                YM3465
*                                                               YA00303
BRANCH   EQU   15
RETURN   EQU   14                  RETURN ADDRESS REGISTER
SAVEREG  EQU   13                  ADDRESS REGISTER SAVE AREA
BASE     EQU   12                  BASE REGISTER
PGMPARM  EQU   11                  OBJ. PROGRAM PARAMETER LIST ADDRESS
SAVEPARM EQU   10                  PARAMETER REGISTER SAVE AREA
FREEADDR EQU   9                   ADDRESS OF STORAGE TO BE FREED
R8       EQU   8                   WORK REGISTER
CT       EQU   7                       PARMETER LENGTH
TEMPSAVE EQU   6                   ADDR OF CALLING PROGRAM SAVE AREA
SAVECT   EQU   5                   SAVE OBJ. PROGRAM PARM LENGTH
SAVEPTR  EQU   4                   SAVE PTR. TO OBJ. PROG. PARMS
SAVEEP   EQU   3                   SAVE OBJECT PROGRAM ENTRY POINT
LDRPARM  EQU   2                   ADDRESS OF LOADER PARAMETERS
PARM     EQU   1                   ADDRESS OF OBJ. PROGRAM PARAMETERS
ZERO     EQU   0                   LOAD AND FREEMAIN REGISTER
EMVT     EQU   X'10'               MVT FLAG IN CVT
ABBIT    EQU   29                  ABEND BIT IN TCB              A40641
ABMASK   EQU   X'80'               FLAG TO TEST ABEND BIT        A40641
         SAVE  (14,12),T,*
         BALR  BASE,0              ESTABLISH ADDRESSIBILITY
         USING *,BASE
         LR    SAVEPARM,PARM       SAVE PARAMETER REGISTER
         LR    TEMPSAVE,SAVEREG    SAVE ADDR.OF CALLING PGM. SAVE AREA
         L     SAVEPTR,0(PARM)          GET PTR TO PARM LIST    SA53170
         LH    CT,0(SAVEPTR)            GET LENGTH OF PARM LIST SA53170
         LA    CT,CTRLEN(CT)            ADD IN FIXED LENGTH     SA53170
         GETMAIN R,LV=(CT)              GET FOR SA, ATTCH PARM  SA53170
*                                       LIST AND PRM LIST FOR PPSA53170
         LR    FREEADDR,PARM       SAVE ADDR FOR FREEMAIN
         USING CTRSTOR,FREEADDR
         ST    ZERO,GETLEN              SAVE LENGTH OF GETMAIN  SA53170
         MVI   GETLEN,X'00'             ZERO HIGH ORDER BYTE     YM2848
         MVI   CTRFLG,X'00'             ZERO OUT FLAG BYTE       A44270
         ST    SAVEREG,4(PARM)     FORWARD AND BACKWARD CHAIN SAVEAREAS
         ST    PARM,8(SAVEREG)
         LR    SAVEREG,PARM            ESTABLISH SAVE REG
*********                                                     *********
*           TEST FOR VS1 OR VS2                                       *
*********                                                     *********
         MVC   CTRNAM(8),NAMLOAD       INVOKE VIA IEWLOAD
         LA    R8,CVTPTR               GET ADDR OF PTR TO CVT
         L     R8,0(R8)                GET ADDR OF CVT
         USING CVTDSECT,R8             ESTABLISH ADDR FOR CVT
         TM    CVTDCB,CVT4MS1          IS THIS VS2 SYSTEM
         BO    GETLDR                  YES. BRANCH
         MVI   CTRNAM+7,C'R'           NO. INVOKE VIA IEWLOADR
********                                                       ********
*               LOAD THE PROCESSING PORTION OF THE LOADER             *
********                                                       ********
GETLDR   LOAD  EPLOC=CTRNAM        LOAD PROCESSING PART OF LOADER
********                                                       ********
*                 SET  UP  PARAMETERS FOR  THE  LOADER                *
********                                                       ********
         LR    PARM,SAVEPARM       RESTORE PARM REGISTER
         L     SAVEPTR,0(PARM)
         LH    CT,0(SAVEPTR)       GET PARAMETER COUNT
         LR    SAVECT,CT           SAVE FOR RESIDUAL CT. CALC.
         STH   CT,CTRBYT           SAVE RESIDUAL COUNT           A44270
         LTR   CT,CT               ARE THERE ANY PARAMETERS
         BZ    PROCESS             GO TO LOAD PROGRAM
LOOP     CLI   2(SAVEPTR),C'/'     IS THIS END OF LOADER PARMS.
         BE    PRMSET1             YES                           A44270
         LA    SAVEPTR,1(SAVEPTR)  UPDATE PARM POINTER
         BCT   SAVECT,LOOP         CONTINUE LOOP
         B     PRMSETUP            PREPARE TO LOAD PROGRAM       A44270
PRMSET1  OI    CTRFLG,X'01'        LOADED PROGRAM PARMS          A44270
PRMSETUP L     LDRPARM,0(PARM)     GET PARM ADDR
         SR    CT,SAVECT           CALC RESIDUAL COUNT
         STH   CT,0(LDRPARM)       RESET COUNT FOR LOADER
********                                                       ********
*             GO TO PROCESSING PORTION OF LOADER FOR LOADING          *
********                                                       ********
PROCESS  LR    BRANCH,ZERO         GET ADDRESS OF LOADER
         BALR  RETURN,BRANCH
         LR    PGMPARM,PARM        SAVE ADDRESS OF PARAMETERS
         LR    SAVEEP,ZERO         SAVE OBJECT PROGRAM ENTRY POINT
         LR    CT,BRANCH           SAVE RETURN CODE
********                                                       ********
*          FREE CORE OCCUPIED BY PROCESSING PORTION OF THE LOADER     *
********                                                       ********
         DELETE EPLOC=CTRNAM       FREE SPACE OCCUPIED BY LOADER
         LR    BRANCH,CT           RESTORE RETURN CODE
         CH    BRANCH,FOUR         IS CONDITION CODE GT 4
         BNH   PARMSET             NO.
         CLI   CTRNAM+7,C'R'       YES. DID WE INVOKE BY HEWLOADR
         BNE   CTRLRTRN            NO.
         B     FR2                 YES.
********                                                       ********
*                 SET UP PARAMETERS FOR LOADED PROGRAM                *
********                                                       ********
PARMSET  EQU   *
         LR    PARM,SAVEPARM       RESTORE PARM REGISTER
         LA    LDRPARM,PPARMSAC         POINT TO START OF PARM  SA53170
*                                       AREA PP WILL USE        SA53170
         O     LDRPARM,PPARMSOR         SHOW LAST WORD AND       YM3465
         ST    LDRPARM,PPARMS           STORE IN PARM LIST       YM3465
         BCTR  SAVECT,0                 FORGET SLASH             YM3465
         STH   SAVECT,PPARMSAC          STORE LENGTH FOR PP      YM3465
         LA    CT,255                   MAXIMUM LENGTH OF MOVE
         LTR   SAVECT,SAVECT            ANY PARMS FOR OBJ. PROGRAM
         BP    CTSETUP                  YES
         XC    0(2,LDRPARM),0(LDRPARM)  SET PARM CT EQ 0
         B     EXECUTE                  GO TO EXECUTE OBJ. PROGRAM
CTSETUP  CH    SAVECT,CONT256           ARE THERE MORE THAN 255 BYTES
         BNL   MOVEPARM                 YES
         LR    CT,SAVECT                NO, USE ACTUAL COUNT
         BCTR  CT,0                     DECR COUNT FOR EX INST.
MOVEPARM EX    CT,MOVE                  MOVE PARAMETERS
         SH    SAVECT,CONT256           DECR CT BY BYTES MOVED
         LTR   SAVECT,SAVECT            MORE PARAMETERS TO MOVE
         BNH   EXECUTE                  NO
         AH    LDRPARM,CONT256          YES,UPDATE DESTINATION PTR
         AH    SAVEPTR,CONT256          UPDATE ORIGINATION PTR
         B     CTSETUP                  GO TO CONTINUE MOVE
********                                                       ********
*                        BRANCH TO LOADED PROGRAM                    *
********                                                       ********
EXECUTE  EQU   *
         CLI   CTRNAM+7,C'R'           DID WE INVOKE VIA IEWLOADR
         BNE   LINK                    NO - ATTACH PROGRAM
         LR    BRANCH,SAVEEP           YES. BRANCH TO LOADER. GET EP
         LA    PARM,PPARMS              GET ADDRESS OF PARM LIST SM4390
*                                       FOR PROBLEM PROGRAM INTO SM4390
*                                       REGISTER ONE             SM4390
         BALR  RETURN,BRANCH           EXECUTE OBJECT PROGRAM
         LR    CT,BRANCH               SAVE OBJECT PROGRAM RETURN CODE
EX1      L     PARM,8(PGMPARM)         GET START OF FIRST EXTENT OF
*                                      COMPILER-LOADED TEXT
         LTR   PARM,PARM               IS THERE ANY?
         BZ    FR2                     NO.
         L     ZERO,12(PGMPARM)        YES.  GET LENGTH
         FREEMAIN R,LV=(0),A=(1)       FREE IT
FR2      EQU   *
         L     PARM,0(PGMPARM)         GET START OF PROGRAM
         L     ZERO,4(PGMPARM)         GET LENGTH
         FREEMAIN R,LV=(0),A=(1)       FREE PROGRAM
         B     CTRLRTRN                BRANCH TO FREE SAVE AREA
********                                                        ******
*                     ATTACH PROBLEM PROGRAM                          *
********                                                        *******
LINK     EQU   *
         MVC   CTRATTCH(CTRATTLN),ATTCH  MOVE ATTACH LIST
         ST    PGMPARM,CTRATTCH        STORE PTR TO NAME
         SR    R8,R8                   ZERO OUT
         ST    R8,CTRECB               ECB.
         LA    PARM,PPARMS              GET ADDRESS OF PARM LIST SM4390
*                                       FOR PROBLEM PROGRAM INTO SM4390
*                                       REGISTER ONE             SM4390
         ATTACH ECB=CTRECB,MF=(E,(1)),SF=(E,CTRATTCH),SHSPV=78  SA53276
         ST    PARM,CTRTCB             SAVE TCB POINTER
         WAIT ECB=CTRECB               WAIT FOR P/P TO FINISH
         NI    CTRECB,X'3F'            ISOLATE RETURN OR COMP CODE
         L     CT,CTRECB               AND SAVE IT
         L     PARM,CTRTCB         PICK UP TCB ADDRESS           A40641
         TM    ABBIT(PARM),ABMASK  HAS USER PGM ABENDED          A40641
         BZ    DETACH              NO                            A40641
         WTO   'IEW1991 ERROR - USER PROGRAM HAS ABNORMALLY TERMINATED'*
               ,ROUTCDE=(2,11)                                   A40641
         MVC   INITEXTR(EXTRLEN),EXTR  MOVE PARM LIST            A43867
         LA    R8,INITEXAD         GET ADDR FOR POINTER          A43867
         ST    R8,INITEXTR         PUT IN LIST                   A43867
         EXTRACT MF=(E,INITEXTR)   POINT TO LIST FORM            A43867
         L     R8,INITEXAD         GET IT                        A43867
         LTR   R8,R8               WAS IT DEFINED                A43867
         BZ    AB1                 NO,BRANCH                     A43867
         TM    0(R8),TCBTSTSK      YES, IT IS TSO                A43867
         BO    DETACH              YES                           A43867
AB1      L     PARM,CTRTCB         PICK UP TCB ADDRESS           A43867
         L     PARM,16(PARM)                                     A43867
         LA    PARM,0(PARM)        SUPPRESS DUMP (M2702)
         SVC   13                                                A43867
DETACH   EQU *                                                   A40641
         DETACH CTRTCB            DETACH P/P
********                                                       ********
*          FREE SAVE AREA AND ATTCH PARAM LIST
********                                                       ********
CTRLRTN  EQU   *
CTRLRTRN EQU   *
         TM    CTRFLG,X'01'        LOADED PROG PARMS?            A44270
         BZ    CT1                NO                             A44270
         NI    0(SAVEPARM),X'7F'  TURN OFF LAST PARM FLAG        A44270
CT1      L     PARM,0(SAVEPARM)                                  A44270
         MVC   0(2,PARM),CTRBYT   RESTORE RESIDUAL COUNT         A44270
         LR    PARM,FREEADDR       ADDR OF STORAGE TO BE FREED   A44270
         L     ZERO,GETLEN              GET LENGTH OF GOTTEN     YM2848
*                                       CORE                    SA53170
         FREEMAIN R,LV=(0),A=(1)        FREE SA AND PARM LIST    YM2848
         LR    BRANCH,CT           RESTORE OBJECT PROG RETURN CODE
         LR    SAVEREG,TEMPSAVE    RESTORE CALLING PGM SAVE AREA ADDR.
         RETURN (14,12),T,RC=(15)
         DS    0F                       ALIGN
PPARMSOR DC    X'80000000'              MASK TO OR HIGH ORDER BITYM3465
FOUR     DC    H'4'
CONT256  DC    H'256'                   DECR LENGTH FOR PARM. MOVES
MOVE     MVC   2(1,LDRPARM),3(SAVEPTR)  MOVE PARM. FIELD
NAMLOAD  DC    CL8'HEWLOAD '            NAME OF PROCESSING ROUTINES
ATTCH    ATTACH SF=L               ATTACH PARAM LIST
EXTR     EXTRACT EXTR,'S',FIELDS=(TSO),MF=L                      A43867
TCBTSTSK EQU   X'80'               TSO FLAG                      A43867
         EJECT
******                                                          *******
*              DSECT FOR CVT
******                                                          *******
CVTDSECT DSECT
         CVT
         EJECT
******                                                          *******
*              DSECT FOR CTR'S DYNAMIC STORAGE                        *
******                                                          *******
CTRSTOR  DSECT
CTRSA    DS    18F                 SAVE AREA
CTRATTCH ATTACH ECB=CTRECB,SF=L
CTRATTLN EQU   *-CTRATTCH          LENGTH OF ATTCH PARAM LIST
CTRECB   DS    F                   ECB FOR P/P TASK
CTRTCB   DS    F                   TCB FOR P/P
CTRNAM   DS    CL8                 NAME BY WHICH TO INVOKE LOADER
CTRFLG   DS    X
CTRBYT   DC    H'0'               FOR RESIDUAL COUNT             A44270
INITEXTR EXTRACT INITEXAD,'S',FIELDS=(TSO),MF=L                  A43867
EXTRLEN  EQU   *-INITEXTR                                        A43867
INITEXAD DS    F                   EXTRACT WILL PUT ADDR OF      A43867
*                                  TSO FIELD HERE
GETLEN   DS    F                        LENGTH OF GETMAIN WILL  SA53170
*                                       BE SAVED HERE           SA53170
         DS    0D                       ALIGN FOR PP PARM LIST  SA53170
CTRLEN   EQU   *-CTRSTOR+6            CTR WILL GETMAIN FOR THIS YA00303
*                                       VALUE PLUS THE LENGTH   SA53170
*                                       OF PARM LIST PASSED TO  SA53170
*                                       LOADER BY THE INVOKING  SA53170
*                                       PROGRAM                 SA53170
PPARMS EQU     *                        PARMS FOR PP WILL BE    SA53170
*                                       MOVED HERE FOR ALIGNMENTSA53170
PPARMSAC EQU   *+4                      ACTUAL POSITION OF START YM3465
*                                       OF PARM LIST FOR LOADED SA53170
*                                       PROGRAM                 SA53170
*                                       PARM LIST IS ONE         YM3465
*                                       FULLWORD POINTER TO      YM3465
*                                       LIST CONSISTING OF A     YM3465
*                                       HALFWORD LENGTH          YM3465
*                                       FOLLOWED IMMEDIATELY     YM3465
*                                       BY THE PARMS             YM3465
         END
