         MACRO
         TBLGEN &OPTBL,&SQVAL,&TNAME,&TID,&LENG,&BREG
         GBLA  &LN,&ENDC,&NDX
         GBLC  &TNM,&REP,&REPA,&BLK
.*       GENERATES TABLES IN X1A. ONE WITH OFFSET AND ONE WITH
.*       VALUES AT THE CORRESPONDING OFFSETS.  SEE MODULE X1A.
&TNM     SETC  '&TNAME'                 NAME TO BE GEN'ED FOR TABLE
&LN      SETA  K'&TNAME
         AIF   (&LN LE 8).TGBGN
         MNOTE '&TNM EXCEEDS 8 CHARACTERS - IT HAS K'&TNAME CHARACTERS'
         MEXIT
.TGBGN   ANOP
&LN      SETA  &LENG                    LENGTH ATTR FOR EACH TBL ENTRY
&TNM     EQU   *
&BLK     SETC  '  '
&NDX     SETA  0
&ENDC    SETA  N'&OPTBL
         AIF   ('&TID' EQ 'SEQT').SEQTBL
.* THE DEFAULT ID IS OP CODE TABLE ID
.OCTBL   ANOP
&NDX     SETA  &NDX+1
&REP     SETC  '&SYSLIST(1,&NDX)'
         AIF   (&NDX GE &ENDC).OUT1
         ORG   &TNM.+JT&REP.*&LN
         DC    AL&LN.(&REP.-&BREG.)
         AGO   .OCTBL
.* STATEMENT SEQUENCING TABLE
.SEQTBL  ANOP
&NDX     SETA  &NDX+1
&REP     SETC  '&SYSLIST(1,&NDX)'
&REPA    SETC  '&SYSLIST(2,&NDX)'
         AIF   (&NDX GE &ENDC).OUT2
         ORG   &TNM.+JT&REP.*&LN
         DC    AL&LN.(&REPA.)
         AGO   .SEQTBL
.OUT1    ANOP
         DC    AL&LN.(&REP.-&BREG.)
         AGO   .EXIT
.OUT2    ANOP
DMMY     EQU   *
         DC    AL&LN.(&REPA.)
.EXIT    ANOP
         DS    0H
         MEND
