         MACRO
&LABEL   JGENIN &LIST,&LENGTH
         LCLA  &K,&C,&CT,&LEN
         LCLB  &SW,&SW1
         LCLC  &LET,&GEN(8)
.* GENERATES INTERNALLY CODED TEXT.
.* ACCEPTS ALPHA, NUMBERIC, AND ALL SPECIAL CHARACTERS EXCEPT
.* AMPERSAND AND QUOTE.
&K       SETA  K'&LIST
&C       SETA  1
         AIF   ('&LIST'(1,1) NE '''').M1
&C       SETA  2
&SW      SETB  1
.M1      AIF   ('&LENGTH' NE '').M2
&SW1     SETB  1
&LEN     SETA  0
.*  SW =0 - NO QUOTE
.*  SW =1 - QUOTE
.*  SW1=0 - LENGTH
.*  SW1=1 - NO LENGTH
.M2      AIF   (&SW).M3                 BRANCH QUOTE
         AIF   (&SW1).M5                BRANCH NO QUOTE, NO LENGTH
.*   NO QUOTE  LENGTH
         AIF   (&LENGTH GT &K).M2A
&K       SETA  &LENGTH
&LEN     SETA  0
         AGO   .M5
.M2A     ANOP
&LEN     SETA  &LENGTH-&K
         AGO   .M5
.*   QUOTE
.M3      AIF   (&SW1).M4                BRANCH QUOTE NO LENGTH
.*   QUOTE LENGTH
&K       SETA  &K-2
         AIF   (&LENGTH GT &K).M3A
&K       SETA  &LENGTH+1
&LEN     SETA  0
         AGO   .M5
.M3A     ANOP
&LEN     SETA  &LENGTH-&K
&K       SETA  &K+1
         AGO   .M5
.*   QUOTE  NO LENGTH
.M4      ANOP
&K       SETA  &K-1
.M5      AIF   ('&LABEL' EQ '').L2
&LABEL   DC    0CL(&K-&C+1+&LEN)' '
.L2      ANOP
&CT      SETA  1
.L3      AIF   ('&LIST'(&C,1) EQ '$').L3DOLR      DOLLAR
         AIF   ('&LIST'(&C,1) EQ '#').L3POUND     POUND
         AIF   ('&LIST'(&C,1) EQ '@').L3AT        AT
         AIF   ('&LIST'(&C,1) EQ '=').L3EQ        EQUAL
         AIF   ('&LIST'(&C,1) EQ '(').L3LP        LEFT PARN.
         AIF   ('&LIST'(&C,1) EQ '+').L3PLUS      PLUS
         AIF   ('&LIST'(&C,1) EQ '-').L3MIN       MINUS
         AIF   ('&LIST'(&C,1) EQ '*').L3AST       ASTER
         AIF   ('&LIST'(&C,1) EQ '/').L3SLASH     SLASH
         AIF   ('&LIST'(&C,1) EQ ')').L3RP        RIGHT PARN.
         AIF   ('&LIST'(&C,1) EQ ',').L3C         COMMA
         AIF   ('&LIST'(&C,1) EQ ' ').L3B         BLANK
         AIF   ('&LIST'(&C,1) EQ '.').L3P         PERIOD
&LET     SETC  'J'.'&LIST'(&C,1)
.L6      AIF   (&CT EQ 1).L7
&LET     SETC  ','.'&LET'
.L7      ANOP
&GEN(&CT) SETC '&LET'
&C       SETA  &C+1
         AIF   (&C GT &K).L9
&CT      SETA  &CT+1
         AIF   (&CT LT 9).L3
         DC    AL1(&GEN(1)&GEN(2)&GEN(3)&GEN(4)&GEN(5)&GEN(6)&GEN(7)&GEX
               N(8))
&CT      SETA  1
.L8      ANOP
&GEN(&CT) SETC ''
&CT      SETA  &CT+1
         AIF   (&CT LT 9).L8
         AGO   .L2
.L3DOLR  ANOP
&LET     SETC  'JDOLLAR'
         AGO   .L6
.L3POUND ANOP
&LET     SETC  'JPOUND'
         AGO   .L6
.L3AT    ANOP
&LET     SETC  'JAT'
         AGO   .L6
.L3EQ    ANOP
&LET     SETC  'JEQUAL'
         AGO   .L6
.L3LP    ANOP
&LET     SETC  'JLPARN'
         AGO   .L6
.L3PLUS  ANOP
&LET     SETC  'JPLUS'
         AGO   .L6
.L3MIN   ANOP
&LET     SETC  'JMINUS'
         AGO   .L6
.L3AST   ANOP
&LET     SETC  'JASTER'
         AGO   .L6
.L3SLASH ANOP
&LET     SETC  'JSLASH'
         AGO   .L6
.L3RP    ANOP
&LET     SETC  'JRPARN'
         AGO   .L6
.L3C     ANOP
&LET     SETC  'JCOMMA'
         AGO   .L6
.L3B     ANOP
&LET     SETC  'JBLANK'
         AGO   .L6
.L3P     ANOP
&LET     SETC  'JPERIOD'
         AGO   .L6
.L9      DC    AL1(&GEN(1)&GEN(2)&GEN(3)&GEN(4)&GEN(5)&GEN(6)&GEN(7)&GEX
               N(8))
         AIF   (&LEN EQ 0).L10
         DC    &LEN.AL1(JBLANK)
.L10     MEND
