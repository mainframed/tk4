* /* START OF SPECIFICATIONS ***                                      *
*01* PROCESSOR = ASSEM;                                               *
**** END OF SPECIFICATIONS ***/                                       *
 TITLE 'IEHLIST1-IEHLIST ROOT,CONTROL CARD ANALYZER,PRINTER'
***********************************************************************
* MODULE NAME--IEHLIST1                                        YL026VC*
*                                                              YL026VC*
* DESCRIPTIVE NAME--IEHLIST ROOT,CONTROL CARD ANALYZER, PRINTERYL026VC*
*                                                              YL026VC*
* COPYRIGHT--NONE                                              YL026VC*
*                                                                     *
* STATUS--VS2 REL2 PROGRAM MODIFICATION SEPTEMBER 1975         @Z40CSJH
*        VS1 RELEASE 5.0 DELETEIONS/CHANGES                    @Z30RSAG
*         RACF CHANGES                                         @Z40CSJH
*A 595300,684100,726900,733500                                 @Z30RSAG
*                                                                     *
* FUNCTION--THIS PROGRAM PRODUCES OUTPUT THAT DESCRIBES THE CONTENTS  *
*    OF A VTOC, OR PDS, OR AN OS CATALOG(SYSCTLG) DATASET.     @Z40CSJH
*   THIS IS ACCOMPLISHED BY                                    @Z40CSJH
*    OPENING THE DESIRED DATA SET, READING IT VIA THE BSAM ACCESS     *
*    METHOD, FORMATTING THE INPUT, AND WRITING OUTPUT ON SYSPRINT     *
*    LISTVTOC OPTIONS:                                                *
*    1.DUMP - THIS OPTION CREATES A HEXADECIMAL LISTING  OF ALL THE   *
*    DATA SET CONTROL BLOCKS IN THE VTOC.  THE PROCESSING OF THE VTOC *
*    IS ACCOMPLISHED IN IEHLISTS.  PARTIAL OR ENTIRE VTOC REQUESTS    *
*    ARE HANDLED IN THIS CSECT.                                       *
*                                                                     *
*    2.FORMAT - THIS OPTION GENERATES A DECIMAL LISTING OF THE DSCBS  *
*    IN THE VTOC.  ALL FIELDS WHICH ARE CONVERTED ARE LABELED.  IN    *
*    SOME CASES ABBREVIATIONS ARE USED TO LESSEN THE GENERATED OUT-   *
*    PUT.  ALL PROCESSING OF THE VTOC AND GENERATION OF THE SYSOUT    *
*    DATA SET IS ACCOMPLISHED IN IEHLISTD.                            *
*                                                                     *
*    3.DEFAULT - THE DEFAULT OPTION IS AN ABBREVIATION OF THE FORMAT  *
*    OPTION.  THE OUTPUT GENERATED LISTS VOLUME SERIAL,VOLUME SE-     *
*    QUENCE NO, CREATION AND PURGE DATES, FILE TYPE, DATA SET NAME,   *
*    FILE SECURITY, AND EXTENTS USED.  ALL REQUESTS ARE PROCESSED     *
*    IN IEHLISTR.                                                     *
*                                                                     *
* ENTRY POINTS--THE PROGRAM IS ONLY ENTERED AT THE FIRST CSECT OF THE *
*         ROOT SEGMENT (IE: IEHLISTR)                                 *
*                                                                     *
* INPUT--THE ONLY INPUT TO THIS PROGRAM IS INPUT CONTROL STATEMENTS   *
*    WHICH ARE FOUND ON A DEVICE DEFINED BY THE SYSIN DD CARD         *
*                                                                     *
* OUTPUT--A LISTING OF INFORMATION RELATIVE TO THE INPUT REQUESTS     *
*    IS WRITTEN ON THE OUTPUT DEVICE DEFINED BY THE SYSPRINT DD CARD  *
*                                                                     *
* EXTERNAL REFERENCES--IEHLISTC,WHICH SCANS INPUT CARDS. IEHVOLMT,    *
*         WHICH HANDLES VOLUME MOUNTING. IEHLISTM, WHICH CONTAINS ALL *
*         MESSAGES                                                    *
*                                                                     *
* EXITS,NORMAL--RETURN MACRO IS ISSUED WITH RETURN CODE OF 0          *
*                                                                     *
* EXITS,ERROR--RETURN MACRO IS ISSUED WITH FOLLOWING RETURN CODES:    *
*         RETURN CODE OF 8 - I/O ERROR OR OTHER ERROR ON DATA SET     *
*         RETURN CODE OF 12- I/O ERROR ON SYSIN                       *
*         RETURN CODE OF 16- UNCORRECTABLE ERROR WITH TERMINATION     *
*                                                                     *
* TABLES/WORKAREAS--1) TABLE OF COMMAND WORDS 2)TABLE OF KEYWORDS     *
*    3) TABLE OF DATA SET NAMES FROM INPUT CONTROL CARDS              *
*    ALL WORK AREAS ARE IN ROOT SEGMENT (IEHLISTR)                    *
*                                                                     *
* ATTRIBUTES--REUSABLE                                                *
*                                                                     *
* NOTES--OTHER MODULES USED ARE DEVNAMET, WHICH CONTAINS DEVICE NAME  *
*    TABLE AND DEVMASKT, WHICH CONTAINS DEVICE MASK TABLE             *
*                                                                     *
* CHANGE ACTIVITY--FOLLOWS THIS PROLOG                         YL026VC*
*                                                                     *
**********************************************************************
*                                                                     *
*       RELEASE 20
*A 592800-594000,595400-596800,597600-600200,687600-701800,      A29716
*A   725000-726200,727000-727400,729600-732200                   A29716
*A 169600-170000                                                 A29736
*A 559400,721600-721800                                          A32555
*A 545800-549400,554200,721000,721400                            A34989
*A 678400-678600                                                 A38019
*A 649200-649400                                                 PM0405
*A 059800                                                        A38584
*A 187800                                                        A38611
*       RELEASE 21
*A 585200,585600                                                 A38621
*A 063400                                                        A40921
*A 444800-445000                                                 A42343
*A 202400-202600                                                 A42351
*       RELEASE OS/VS-1 AND 2
*A 466300,473803-473929,474032-474128,474600,476200,480600,      XM4744
*A   481200-481600,482400-482600,483500,484300,485200-485400,    XM4744
*A   486000-486400,487200-487400,488600-488700,489630,491200,    XM4744
*A   497650-497750                                               XM4744
*A 473930-474008,474152,496500                                   YM2247
*A 036984,582700-582720                                         YA00065
*A 581100,581700-581780,585400,734100-734160                    YA00066
*A 581120-581140,593300-593399,597500-597588,734180-734184      YA00067
*C 583400,592800,649200                                         YA00067
*A 36984-36985,582620-582698                                    YA00068
*A 036988,474500                                                YA00062
*A 182300-182320                                                YL026VC
*A 413900-414260,415700-415720,548800,549300-549388             YL026VC
*C 147000,192600,194000,366400-368200,521800-523600             YL026VC
*D 147200-147400,148600,192200-192400,193600-193800,            YL026VC
*D 234400-234600,235400-237400                                  YL026VC
*A 593385-593386                                                YL026VC
*A 596900-596920,684700-684760,726900,733500-733520             YL026VC
*C 667800                                                        YM5559
*A 532700-532720,667900-667920                                   YM5559
*C 216800                                                      @YA01685
*A 216700                                                      @YA01685
*A 707500,710900,745900                                        @YA02531
*A 36803-36806,364203,425403,519603                             YM06989
*D563200-563600                                                 YM08143
*A563700-563792                                                 YM08143
*C 061600,062400,063000                                        @ZA01245
*D 063200-063600                                               @ZA01245
*C 676600                                                      @ZA01652
*C218800,224400                                                @ZM30153
*C219000                                                       @ZM30161
*C 563792                                                      @ZA01480
*A 640100-640120                                               @ZA07324
*A 500,1500-1700,146900-146920,148500                          @Z40CSJH
*A 192500-192600,193900-193920,234700-234720                   @Z40CSJH
*A 235700-237580,254550-254890                                 @Z40CSJH
*A 427300-427380                        @XA15411=@YA13995=(ORG)@ZA11934
*C 427400,433800                        @XA15411=@YA13995=(ORG)@ZA11934
*A 267460                                                      @ZA18372
*             SU60 CHANGES/DELETIONS                           @G60ASBJ
*A 544210-544300                                               @G60ASBJ
*C 540000,541000,542200,545400,545700-546000,546400,546800     @G60ASBJ
*C 547200,547600,547800,548100,551400,551700,552000,552300     @G60ASBJ
*C 719600,719800,720000,720200,720400                          @G60ASBJ
*C 497880                               (ORG)@XA19271=@YA19145=@ZA25524
*C 522602-522604                        (ORG)@XA20229=@YA19937=@ZA28588
*C 522985,522986               @SA80149=@XA21270=@YA20405=(ORG)@ZA30116
*C 125800,125900,126100,126300 @SA80150=@XA21288=@YA20423=(ORG)@ZA28980
*C 129400,129600,129800        @SA80150=@XA21288=@YA20423=(ORG)@ZA28980
*C 522082,522083,522203,522204 @SA80150=@XA21288=@YA20423=(ORG)@ZA28980
**********************************************************************
*                                                              @G60ASBJ
*
*        REG,DISPLACEMENT, AND MASK EQUATES ARE ALL FOUND HERE
*
*
**********************************************************************
*
PARAREG1 EQU   0
KEEPRET  EQU   0
PARAREG2 EQU   1
SCANADD  EQU   1         CONTROL CARD SCAN PARAMETER   ADDRESS
MSGLEN   EQU   1
MSGRET   EQU   1
SCANLEN  EQU   2         CONTROL CARD SCAN PARAMETER   LENGTH
WORK1    EQU   2
WORK2    EQU   3
HLFWD    EQU   3                                                   VF0I
WORK3    EQU   4
JOB      EQU   4
ONEBYTE  EQU   5                                                   VF0I
WORK4    EQU   5
ELEMLEN  EQU   5
KEEPC    EQU   5
WORK5    EQU   6
BLOCPNT  EQU   6
LOCPNT   EQU   6
UNPKDSCB EQU   6
EMPTYCNT EQU   7
WORK6    EQU   7
NAMEPTR  EQU   7
TOBE     EQU   7
UNPKPR   EQU   7
COUNT    EQU   7
TABPNT   EQU   8
TTRPTR   EQU   8
TABEND   EQU   9
COMEBACK EQU   9
RECCNT   EQU   10
BASEREG2 EQU   11
BASEREG1 EQU   12
SAVEREG  EQU   13
RETURN   EQU   14
MSGFROM  EQU   14
BRANCHTO EQU   15
ECREG    EQU   15
LOCERR   EQU   15
MSGTO    EQU   15
STRIP2   EQU   X'1F'
ERRORCD  EQU   X'80'
BYPASS   EQU   X'40'
COMMAND  EQU   X'08'
KEYSW    EQU   X'04'
PARAM    EQU   X'02'
LC       EQU   X'20'
DATESW   EQU   X'01'
DUMPSW   EQU   X'02'
NODESW   EQU   X'04'
DSNAMESW EQU   X'08'
OPENSW   EQU   X'10'
SECURITY EQU   X'50'                                           @Z40RSSA
PSWD     EQU   X'10'                                           @Z40RSSA
RACF     EQU   X'40'                                           @Z40RSSA
*                                                              @Z40RSSA
*   SECURITY INDICES INTO MESSAGE TABLE                        @Z40RSSA
IBOTH    EQU   336                                             @Z40RSSA
INONE    EQU   340                                             @Z40RSSA
IPASS    EQU   344                                             @Z40RSSA
IRACF    EQU   348                                             @Z40RSSA
***********************************************************************
         SPACE 1
TYPESAM  EQU   X'40'
TYPEISAM EQU   X'80'
TYPEDAM  EQU   X'20'
TYPEPDS  EQU   X'02'
FIRST5   EQU   X'80'
FORMAT1  EQU   X'F1'
FORMAT4  EQU   X'F4'
FORMAT5  EQU   X'F5'
SLASH    EQU    X'61'                                              DT0I
INDISIN  EQU   34
INDISPR  EQU   42
INDISVOL EQU   82
ASACC    EQU   NODESW                                              VF0I
BLOCKED  EQU   OPENSW                                              VF01
CHAINCK  EQU   TYPEDAM                                             VF0I
DATACHK  EQU   TYPESAM                                             VF0I
FMTSW    EQU   X'01'                                               VF0I
FORMAT2  EQU   X'F2'                                               VF0I
FORMAT3  EQU   X'F3'                                               VF0I
FORMAT6  EQU   X'F6'                                               VF0I
MACHCC   EQU   TYPEPDS                                             VF0I
STND     EQU   DSNAMESW                                            VF0I
TALX     EQU   DUMPSW                                              VF0I
TCONT    EQU   DSNAMESW                                            VF0I
TCYL     EQU   X'C0'                                               VF0I
TMXIG    EQU   NODESW                                              VF0I
TPFIXED  EQU   TYPEISAM                                            VF0I
TPVAR    EQU   TYPESAM                                             VF0I
TREC     EQU   TYPESAM                                             VF0I
TRKOFLO  EQU   TYPEDAM                                             VF0I
TTRK     EQU   TYPEISAM                                            VF0I
WRITECK  EQU   TYPEISAM                                            VF0I
IND2      EQU   87                                              YM06989
SHR       EQU   X'08'                                           YM06989
**        EQUATES FOR LISTPDS CODE
ALIASMSK  EQU   X'04'                   PDSFMTSW-ALIAS ONLY
ARRMSK    EQU   X'20'                   PDSFMTSW-ALIAS,RENT,REUS MASK
SCATMSK   EQU   X'10'                   PDSFMTSW-SCATTER LOAD
OFFMSK    EQU   X'C3'                   PDSFMTSW-BITS OFF
MEMMSK    EQU   X'24'                   PDSFMTSW-04 & 20
HWORDMSK  EQU   X'1F'                   PDS LENGTH
AOSLE     EQU   X'80'                   AOS LINK-EDIT BIT
SCATBIT   EQU   X'04'                   SCATTER LOADED
SSIBIT    EQU   X'10'                   SSI FOR AOS L/E
APFBIT    EQU   X'08'                   PRESENCE OF APF
SIZEBIT   EQU   X'01'                   SIZE OF APF FIELD AND SW
*
BLANK     EQU   X'40'                   BLANK-EBCDIC
COMMA     EQU   X'6B'                   COMMA-EBCDIC
TRBLANK   EQU   X'EF'                   TRANS BLANK
TRCOMMA   EQU   X'EE'                   TRANS COMMA
SIGN      EQU   X'F0'                   POS SIGN
RTTRLL    EQU   X'00'                   RECORD # OF TTRLL       YA00068
ALOCBLK   EQU   X'80'                   ALLOC MSK FOR TRK & CYL YA00068
OSBIT     EQU   X'FB'                   OS L/E BIT MASK         YA00062
**********************************************************************
**
**                          IEHLISTR
**
**********************************************************************
IEHLISTR CSECT
         SAVE  (14,12),T,IEHLIST1-OZ28980                      @ZA28980
         BALR  BASEREG1,PARAREG1            ESTABLISH ADDRESSABILITY
         USING *,BASEREG1
IEHBAS01 ST    13,SAVE13          SAVE REG 13                      2564
         LR    WORK3,PARAREG2           SAVE REGISTER 1            I276
         LOAD  EP=DEVNAMET              LOAD DEVICE NAME TABLE     I276
         LR    PARAREG2,WORK3           RESTORE REGISTER 1         I276
         ST    KEEPRET,DEVTABAD         SAVE PTR TO TABLE          I276
         L     15,INVEECON             LOAD ADDR OF INVOLKER CONTROL
         BR    15                           SECTION AND BRANCH
PEKING   STM   WORK2,WORK3,TAIPEI      -* SAVE DATA IN TAIPEI.     VF01
         LA    WORK2,UNPKFLD           -* WORK2 POINTS TO THE DATA VF01
*                                         TO BE PROCESSED.         VF01
         LA    WORK3,5                 -* NUMBER OF BYTES TO BE    VF01
*                                         PROCESSED.               VF01
TRY      CLI   0(WORK2),X'F0'          -* IS THIS BYTE A ZERO?     VF01
         BNE   OUT                     -* NO,GET OUT OF THIS ROUTINVF01
         BCTR  WORK3,0                 -* SUBTRACT THE COUNTER BY 1VF01
         LTR   WORK3,WORK3             -* TEST COUNTER AGAIN.      VF01
         BZ    OUT                     -* NO MORE BYTES TO SEARCH. VF01
         MVI   0(WORK2),X'40'          -* OVERLAY WITH BLANK.      VF01
         LA    WORK2,1(WORK2)          -* INCRESE PTR TO NEXT BYTE.VF01
         B     TRY                     -* TRY OVER AGAIN.          VF01
OUT      LM    WORK2,WORK3,TAIPEI      -* RESTORE THE CONTAINS.    VF01
         BR    RETURN                  -* BRANCH TO CALLING RTN.   VF01
TAIPEI   DS    2F                      -* FOR SAVING REGISTERS.    VF01
NO7      DC    F'9'                    -* COUNTER FOR AVOIDING.    VF01
NO12     DC    F'16'                   -* SPREAD DATA ON PAGES.    VF01
TU       DS    1X                      -* FETCH BYTE FOR TESTING . VF01
INVEECON DC    V(IEHLISTI)             VCON FOR IEHLISTI
*********************************************************************
**
**                           IEHLISTI
**
*********************************************************************
IEHLISTI CSECT
         BALR  BASEREG2,0              ESTABLISH ADDRESSABILITY
         USING *,BASEREG2
***********************************************************************
**
**       THIS SECTION OF THE PROGRAM SEES TO IT THAT THE INPUT CONTROL
**       CARDS ARE READ AND THE TABLES AND SWITCHES ARECORRECTLY SET
**
***********************************************************************
**
         LR    5,1
         MVI   SWITCHRD,X'00'                RESET SWITCHRD
         LA    TABEND,TABLE+440             SET POINTER TO TABLE END
         BAL   RETURN,CLPR                  GO TO CLEAR PRINT AREA
         MVI   ZLINEPER,X'3A'               SET LINES PER PAGE
         MVI   ZLINECNT,X'99'               SET UP FOR SKIP
         MVI   RETCODE,X'00'
         MVI   ZSW,X'00'                    RESET MSG WRITER SWITCH
         MVI   RONSW,X'80'             RESET VOLUME MNT SW
         MVI   MODLJFCB+124,X'40'
         MVC   MODLJFCB+125(19),MODLJFCB+124
         MVC   GENLST(16),DDNAME1           SET UP DDNAMES
         MVC   RONTAB(4),RONTAB1
         LA    MSGTO,ZSAVEHD
         LA    MSGFROM,132
         BAL   MSGRET,MVMSG                 MOVE MESSAGE
         LR    1,5
**********************************************************************
**
**       THIS ROUTINE CHECKS FOR & MOVES INVOKE PARAMETERS
**         IT IS WRITTEN TO GO IN LINE WITH A PROGRAMS INITIAL CODE,
**         IS NOT REINTRANT,AND IS NOT TRANSPARENT (IE BOMBS REGS 2-7)
**
**********************************************************************
         L     2,0(1)                  LOAD ADDR OF PARM FIELD
         LTR   2,2                      CHECK IF LAST PARAM
         BM    INLDDONE                 IF SO ---
         L     3,4(1)                  LOAD ADDR OF DD NAMES
         LTR   3,3                      CHECK IF LAST PARAM
         BM    INLDDONE                 IF SO ---
         L     4,8(1)                  LOAD ADDR OF HEAD INFO
INLDDONE EQU   *
         BAL   14,INLNCNT                    GO TO SCAN FOR LINECOUNT
         LTR   2,2                           CHECK FOR LAST ENTRY
         BM    INDONE                        IF SO ----
         BAL   14,INGETDD                    GO TO GET DDNAMES
         L     3,4(1)                  RESTORE R3 DD NAMES       A38584
         LTR   3,3                           CHECK FOR LAST ENTRY
         BM    INDONE                        IF SO ----
         BAL   14,INGETHD                    GO TO GET HEADDER
         B     INDONE
INLNCNT  LH    5,0(2)                        GET LENGTH OF PARAMETER
         LTR   5,5                           CHECK IF = TO O
         BCR   8,14                          IF SO ----
         CLI   1(2),X'0A'              IMBEDDED CHARS?            M5844
         BCR   7,14                    YES,DEFAULT             @ZA01245
         AR    5,2                     ADDR OF PARM END MINUS 2   M5844
         LA    5,0(5)                  CLEAR HIGH ORDER BYTE      M5844
         LA    WORK6,2(WORK4)          ADDR OF PARM END           M5844
         LA    6,8                    SET REG TO LAST POSSIBLE @ZA01245
         SR    5,6                             POSITION OF KEYWORD
INCOMP1  CLC   INNAME(8),0(5)                COMPARE FOR KEYWORD
         BCR   7,14                    NOT EQU, DEFAULT        @ZA01245
INCOMP2  LA    WORK5,8                 LEN OF KEYWORD             M5844
         AR    WORK4,WORK5             ADDR OF NO                 M5844
         SR    WORK6,WORK4             LEN OF NO TO USE IN EX     M5844
         LA    WORK5,2                 MAX NO OF BYTES            M5844
         CR    WORK5,WORK6             IS LENGTH 2                M5844
         BCR   7,14                    IF NOT,RETURN              M5844
         CLI   0(WORK4),X'F9'          IS FIRST BYTE TOO HIGH     M5844
         BCR   2,14                    YES,RETURN                 M5844
         CLI   0(WORK4),X'F0'          IS FIRST BYTE TOO LOW      M5844
         BCR   4,14                    YES,RETURN                 M5844
         CLI   1(WORK4),X'F9'          IS SECOND BYTE TOO HIGH    M5844
         BCR   2,14                    YES,RETURN                 M5844
         CLI   1(WORK4),X'F0'          IS SECOND BYTE TOO LOW     M5844
         BCR   4,14                    YES,RETURN                 M5844
         PACK  INCON(8),0(2,5)         PACK LINECNT               M5844
         CVB   WORK5,INCON             MAKE BINARY                M5844
         STC   WORK5,INLINCNT          STORE IN SAVEAREA          M5844
         BR    14
INGETDD  LH    5,0(3)                        LOAD LEN OF PARAM
         LA    6,INDISIN                     LOAD DISP OF DDNAME SYSIN
         CR    6,5                           CHECK LIST LEN
         BH    ININNONE                      IN CANT INCLUDE ----
         CLI   INDISIN(3),X'00'             CHECK FOR NO DDNAME
         BE    ININNONE                     IF NOT ------
         MVC   INSYSIN(8),INDISIN(3)         IF DD NAME IN LIST -- SAVE
ININNONE LA    6,INDISPR                     LOAD DISP OF DDNAME SYSPR
         CR    6,5                           CHECK LIST LEN
         BCR   2,14                          IF CANT INCLUDE ----
         CLI   INDISPR(3),X'00'             CHECK FOR NO DDNAME
         BE    INCHKVOL                     IF NANE ------
         MVC   INSYSPR(8),INDISPR(3)         IF DD NAME IN LIST -- SAVE
INCHKVOL LA    6,INDISVOL                   CHECK IF LIST LONG ENOUGH
         CR    6,5                          TO INCLUDE VOLS NOT TO
         BCR   2,14                         DISMOUNT    ----   IF SO
         MVC   INDISVOL-16(16,3),INSYSIN    BRANCH  OTHERWISE SAVE
         LA    3,INDISVOL-16(3)             CALC ADDR OF VOL LIST
         ST    3,RONTAB
         SRL   5,3                          CALC NO OF DDNAMES
         LA    3,8
         SR    5,3                          SUB 8
         STC   5,RONTAB
         BR    14
INGETHD  LH    5,0(4)                        LOAD PARAM LEN
         LTR   5,5                           CHECH LEN = 0
         BCR   8,14                          IF SO ---- RETURN
         LA    7,7
         NR    5,7                          INSURE NO LESS THAN 7 DIG
         SR    7,7           SET REGISTER TO ZERO                  0783
         LA    4,2(4)                       POINT REG AT NO
INBLA2   IC    7,0(4)                       INSERT DIGIT OF NO
         BCT   5,INBLA1
         ST    7,CONVERT
         MVC   ZPGNO(4),CONVERT             MOVE NO TO HEADDER
         BR    14                           RETURN
INBLA1   LA    4,1(4)                       POINT TO NEXT DIGIT
         SLL   7,8           MOVE ONE BYTE A TIME                  0783
         B     INBLA2
         DS    0D
INNAME   DC    CL8'LINECNT='                 NAME OF KEY WORD TO SCAN
INCON    DS    8X                            WORK SPACE TO CONVERT
INDONE   EQU   *                       COME HERE AT END TO CONTIN
         MVC   PRINTOUT+40(8),GENLST+8      MOVE DDNAME TO DCB
         MVC   CARDIN+40(8),GENLST
         MVI   DFLG,X'00'                                          BS0H
         MVI   DFLAG,X'00'                                         BS0H
         OPEN  (PRINTOUT,OUTPUT)
         TM    PRINTOUT+48,X'10'    TEST FOR OPENABILITY
         BZ    NOOPEN1                 IF NOT OPENED ---
         TM    DFLG,X'10'               CHECK FOR BLKSIZE ERROR    BS0H
         BZ    NOFLG                    IF NONE -- BRANCH          BS0H
         MVI   RETCODE,X'10'            SET RETURN CODE TO 16      BS0H
         B     NOFREE                  BRANCH TO CLOSE DCB         BS0H
NOFLG    EQU   *                       NO ERROR,OPEN
         OPEN  (CARDIN)
         TM    CARDIN+48,X'10'           TEST FOR OPENABILITY
         BZ    NOOPEN2                 IF NOT OPENED ---
         TM    DFLAG,X'10'              CHECK FOR BLKSIZE ERROR    BS0H
         BC    1,INVBLK                 IF ERROR --                BS0H
         B     PBEGIN
INVBLK   LA    MSGFROM,148                                         BS0H
         LA    MSGTO,0                                             BS0H
         BAL   MSGRET,MVMSG             MOVE THE MESSAGE           BS0H
         BAL   RETURN,LINEPR    PRINT MSG - INVALID DCB PARAMETER  BS0H
         MVI   RETCODE,X'10'            SET RETURN CODE TO 16      BS0H
         B     NOFREE3                  CLOSE SYSIN                BS0H
NOOPEN1  MVI   RETCODE,X'10'           SET RETURN CODE TO 16
         B     NOFREE2
NOOPEN2  LA    MSGFROM,104
         LA    MSGTO,0
         BAL   MSGRET,MVMSG
         BAL   RETURN,LINEPR    PRINT MSG - NO SYSIN DD CARD
         MVI   RETCODE,X'10'           SET RETURN CODE TO 16
         B     NOFREE
PEXIT    LH    WORK4,PRINTOUT+62        LOAD BLKSIZE               BS0H
         OR    WORK4,WORK4                                         BS0H
         BC    8,DCBSIZ                 IF NONE SPECIFIED --       BS0H
         LA    WORK5,121                CHECK FOR MODULAR BLKSIZE  BS0H
         SR    WORK3,WORK3                                         BS0H
         DR    WORK3,WORK5                                         BS0H
         LTR   WORK3,WORK3              CHECK FOR MODULAR BLKSIZE  BS0H
         BCR   8,RETURN                 IF MODULAR --              BS0H
         MVI   DFLG,X'10'          SET ERROR FLAG -- PRINTOUT DCB  BS0H
         MVC   PRINTOUT+62(2),PRINTOUT+82  MOVE LRECL TO BLKSIZE   BS0H
         RETURN
DCBSIZ   MVI   PRINTOUT+63,X'79'        INSERT BLKSIZE -- 121      BS0H
         RETURN
CEXIT    LH    WORK4,CARDIN+62          LOAD BLKSIZE               BS0H
         OR    WORK4,WORK4                                         BS0H
         BC    8,DCBSZ                  IF NONE SPECIFIED --       BS0H
         LA    WORK5,80                 CHECK FOR MODULAR BLKSIZE  BS0H
         SR    WORK3,WORK3                                         BS0H
         DR    WORK3,WORK5                                         BS0H
         LTR   WORK3,WORK3              CHECK FOR MODULAR BLKSIZE  BS0H
         BCR   8,RETURN                 IF MODULAR --              BS0H
         MVI   DFLAG,X'10'         SET ERROR FLAG -- CARDIN DCB    BS0H
         MVC   CARDIN+62(2),CARDIN+82      MOVE LRECL TO BLKSIZE   BS0H
         RETURN
DCBSZ    MVI   CARDIN+63,X'50'          INSERT BLKSIZE -- 80       BS0H
         RETURN
IEHLISTR CSECT
**********************************************************************
**
**       INVALID CONTROL STATEMENTS ARE TAKEN CARE OF HERE
**
**********************************************************************
PERRPR   LA    MSGFROM,0
         LA    MSGTO,0
         CLI   RETCODE,X'08'           IS HIGHER RETURN CODE SET?
         BH    *+8                     BRANCH IF YES TO LET IT STAND
         MVI   RETCODE,X'08'           SET RETURN CODE TO 8
         BAL   MSGRET,MVMSG                 GO TO MOVE MESSAGE
         MVC   0(80,MSGTO),SWITCHRD+1       MOVE CARD TO PRINT
         BAL   RETURN,LINEPR       GO TO PRINT A LINE
         OI    SWITCHRD,BYPASS               TURN ON BYPASS SWITCH
         TM    SWITCHRD,LC                  TEST FOR LAST CARD
         BO    PTERM                        BRANCH IF LAST CARD
         B     PBEGIN                        CLOSE
PCLOSE   TM    SW,OPENSW                    IS WORKIN OPEN---
         MVI   ZSW,X'01'
         BZ    PBEGIN                       IF NOT BRANCH
         CLOSE (WORKIN)
         B     PBEGIN
**********************************************************************
**
**       THIS SECTION HANDLES ALL SYNSD EXITS ---- IT PRINTS A MESSAGE,
**       SETS THE APPROPRIATE RETURN CODE, AND TAKES THE CORROSPONDING
**       ACTION
**
**********************************************************************
WORKERR1 EQU   *                  EP FOR SYNAD EXIT              A24122
         USING WORKERR1,15        USE EP FOR BASE - ONE INSTR    A24122
         L     BASEREG1,ADDRCONT  RE-LOAD ORGINAL BASE REG       A24122
         DROP  15                       FOR CONTINUED            A24122
         USING IEHBAS01,BASEREG1             ADDRESSABILITY      A24122
WORKERR  EQU   *                  EP FOR I/O ERR FROM I/O MACROS A24122
         MVI   RETCODE,X'0C'      SET RETURN CODE TO 12          A24122
         LA    MSGTO,0
         LA    MSGFROM,144
         BAL   MSGRET,MVMSG
         BAL   RETURN,LINEPR                PRINT ERROR MSG
         B     PCLOSE
SYSINERR EQU   *                                                   2564
         USING SYSINERR,15        USE ENTRY PT FOR BASE-ONE INSTR  2564
         L     BASEREG1,ADDRCONT  RE-LOAD ORGINAL BASE REG FOR     2564
         DROP  15                                                  2564
         USING IEHBAS01,BASEREG1        CONTINUED ADDRESSABILITY   2564
         MVI   RETCODE,X'10'      SET RETURN CODE TO 16            2564
         LA    MSGTO,0
         LA    MSGFROM,140     LOAD ERROR MSG. ADCON DISPLACEMENT
         BAL   MSGRET,MVMSG
         MVC   0(8,15),INDD       MOVE INPUT DDNAME TO PR          2564
         BAL   RETURN,LINEPR                PRINT IO ERR MSG
         B     PTERM
ADDRCONT DC    A(IEHBAS01)        BASE ADDR OF IEHROOT CSECT       2564
INDD     DC    C' SYSIN  '                                         2564
**********************************************************************
**
**       THIS ROUTINE RECEIVES CONTROL AT END OF JOB
**
***********************************************************************
PTERM    EQU   *                       END OF JOB
         CLOSE  CARDIN                      CLOSE INPUT
         FREEPOOL  CARDIN
         CLOSE (WORKIN)
         TM    RONSW,X'10'             HAS THE VOL MOUNT ROUTINE
         BZ    NOFREE                     GOTTEN CORE
         L     1,RONTAB+4              SET UP PARAMETERS TO FREE
         LA    1,0(1)                       CORE USED BY THE VOLUME
         LH    0,0(1)                             MOUNTING ROUTINE
         FREEMAIN R,LV=(0),A=(1)
         DELETE EP=DEVMASKT             DELETE DEVICE MASK TABLE   I276
         DELETE EP=DEVNAMET       DELETE DEVICE NAME TABLE         I276
NOFREE   EQU   *                       CLOSE SYSPRINT
         CLOSE  PRINTOUT
         FREEPOOL  PRINTOUT
NOFREE2  EQU   *                       READY TO GET OUT
         L     13,SAVE13
         SR    15,15
         IC    15,RETCODE                   SET RETURN CODE
         RETURN  (14,12),RC=(15)
NOFREE3  EQU   *                       CLOSE INPUT
         CLOSE CARDIN                                              BS0H
         FREEPOOL CARDIN                                           BS0H
         B     NOFREE                   CLOSE SYSPRINT             BS0H
**********************************************************************
**
**       THIS IS A GENERALIZED MESSAGE WRITER  IT WILL COUNT LINES
**       AND PAGES  --  SAVE THE HEADDER --  AND SKIP TO ONE
**
**********************************************************************
LINEPR   ST    RETURN,ZSAVE14               SAVE RETURN ADDR
         STM   WORK2,WORK3,ZSAVE34     SAVE WORK REG CONTENTS    A27416
         TM    ZSW,X'03'                    TEST FOR TRIPLE SPACE  I282
         BO    ZSET3                        YES, GO SET CARR CNTL  I282
         TM    ZSW,X'02'                    TEST FOR DOUBLE SPACE
         BO    ZSET2                        IF SO BRANCH
         MVI   PRINT-1,X'40'           SET CARR CODE TO 1 SPACE
ZTEST1   TM    ZSW,X'01'                    TEST FOR SKIP
         BO    ZSETSK                       IF SO -- BRANCH
ZTESTO   CLC   ZLINEPER(1),ZLINECNT         IS PAGE FULL           I282
         BL    ZSETSK                       IF SO -- BRANCH
ZSETZPR  LA    ZPR,PRINT-1                  LOAD ADDR OF PRINT AREA
         BAL   RETURN,ZWRITE                GO TO PRINT
         NI    ZSW,X'00'                    RESET SW
         IC    ZPR,ZLINECNT                 LOAD LINES ON PAGE
         LA    ZPR,1(ZPR)                   INCREMENT BY ONE
         STC   ZPR,ZLINECNT                 STORE NEW LINECOUNT
         LM    WORK2,WORK3,ZSAVE34     RESTORE WORK REGISTERS    A27416
         L     RETURN,ZSAVE14               LOAD RETURN ADDR
CLPR     MVI   PRINT,X'40'                  CLEAR PRINT AREA
         MVC   PRINT+1(119),PRINT           CLEAR PRINT AREA
         BR    RETURN
ZSET3    MVI   PRINT-1,X'60'           SET CARR CODE TO 3 SPACE    I282
         IC    ZPR,ZLINECNT                 CURR NO. LINES/PAGE    I282
         LA    ZPR,2(ZPR)                   ADD TWO LINES          I282
         STC   ZPR,ZLINECNT                 UPDATE CUR LINE COUNT  I282
         B     ZTESTO                       GO CHECK FOR END OF PP I282
ZSET2    MVI   PRINT-1,X'F0'           SET CARR CODE TO 2 SPACE
         IC    ZPR,ZLINECNT                                        VF0I
         LA    ZPR,1(ZPR)                                          VF0I
         STC   ZPR,ZLINECNT                                        VF0I
         B     ZTEST1
ZSETSK   LA    ZPR,ZCTRLHD                  SET REG TO HEADDER
         MVI   ZCTRLHD,X'F1'           SET CARR CODE TO SKIP TO 1
         BAL   RETURN,ZWRITE                PRINT BLANKS AND SKIP
         MVI   ZLINECNT,X'04'               RESET LINECOUNT
         PACK  CONVERT(8),ZPGNO(4)          PACK PAGE NUMBER
         CVB   ZPR,CONVERT                  MAKE BINARY
         LA    ZPR,1(ZPR)                   INCREMENT BY ONE
         CVD   ZPR,CONVERT                  MAKE DECIMAL
         UNPK  ZPGNO(4),CONVERT(8)          UNPACK
         LA    WORK2,ZPGNO   -* ADDRESS OF PAGE NUMBER INFORMATION.VF01
         LA    WORK3,4       -* NO.OF DIGIT OT BE CONVERTED.       VF01
         BAL   RETURN,TRY    -* GO TO RTN TO RID OF LEADING ZERO   VF01
         OI    ZPGNO+3,X'F0'                CLEAR SIGN
         MVI   PRINT-1,X'60'           SET CODE FOR 3 SPACE
         B     ZSETZPR
         DS    0F
ZSAVE14  DS    4X
ZSAVE34  DS    D        SAVE AREA FOR WORK REGISTERS             A27416
ZCTRLHD  DS    1X
ZSAVEHD  DS    120X
ZLINEPER DS    1X
ZLINECNT DS    1X
ZSW      DS    1X
ZPR      EQU   3
ZPGNO    EQU   ZSAVEHD+101
ZWRITE   ST    RETURN,STORRET               SAVE RETURN ADDR
         LA    SAVEREG,SAVEAREA             LOAD ADDR OF REG SAVE AREA
         PUT   PRINTOUT,(ZPR)
         L     RETURN,STORRET               LOAD RETURN ADDR
         BR    RETURN                       RETURN
*******************************************************************VF0I
**                                                                 VF0I
**                  THIS SECTION HANDLES END OF DATA SUMMARY       VF0I
**                                                                 VF0I
*******************************************************************VF0I
DEODAD   NI    WORKIN+44,X'00'          RESET ERROR BITS IN DCB    VF0I
DRDFORM5 NOP   0(0)                                                VF0I
         LA    PARAREG1,FORM5TTR                                   VF0I
         BAL   RETURN,DOPOINT                                      VF0I
DREAD5   BAL   WORK2,DREAD                                         VF0I
         SR    WORK5,WORK5                                         VF0I
         LR    WORK4,WORK5                                         VF0I
DSCAN    LA    WORK3,READIN+4                                      VF0I
DADDCYL  MVC   CONVERT(8),CLEAR         CLEAR CONVERT              VF0I
         MVC   CONVERT+7(1),4(WORK3)    READY SPACE INFO FOR SUMMRYVF0I
         A     WORK4,CONVERT+4          ADD CYLINDERS              VF0I
         MVC   CONVERT+6(2),2(WORK3)                               VF0I
         A     WORK5,CONVERT+4          ADD  TRACKS                VF0I
         LA    WORK3,5(WORK3)                                      VF0I
         C     WORK3,RDPOS45                                       VF0I
         BE    DADD                                                VF0I
DCHKEND  C     WORK3,RDPOS136                                      VF0I
         BE    DENDDSCB                                            VF0I
         B     DADDCYL                                             VF0I
DADD     LA    WORK3,1(WORK3)                                      VF0I
         B     DADDCYL                                             VF0I
DENDDSCB CLC   CLEAR(5),0(WORK3)        CHECK -MULTIPLE FORMAT 5'S VF0I
         BE    DFINAL                                              VF0I
         IC    WORK2,4(WORK3)           PICK UP  RECORD ADDRESS    VF0I
         BCTR  WORK2,0                  DECREMENT BY ONE           VF0I
         STC   WORK2,4(WORK3)           RESTORE IN POINTER         VF0I
         MVC   WORKIN+8(5),0(WORK3)     MOVE CHAIN ADDR TO DCB     VF0I
         BAL   WORK2,DREAD                                         VF0I
         B     DSCAN                                               VF0I
DFINAL   EQU   *                       PRINT SUMMARY
         LA    MSGFROM,48                                          VF0I
         LA    MSGTO,0                                             VF0I
         BAL   MSGRET,MVMSG                                        VF0I
         CVD   WORK4,CONVERT       MOVE AVAIL TRKS TO PRINT    @ZA28980
         UNPK  PRINT+37(5),CONVERT+5(3)                        @ZA28980
         LA    WORK2,PRINT+37     -* ADR OF AVAIL TRACK TO PRINT.  VF01
         LA    WORK3,5       -* NO.OF DIGIT TO BE PROCESSED.   @ZA28980
         BAL   RETURN,TRY    -* TO GET RID OF LEADING ZEROS.       VF01
         OI    PRINT+41,X'F0' -* OVERLAP.                      @ZA28980
         CVD   WORK5,CONVERT  -* CONVERT TO DECIMAL FOR PRINT.     VF01
         UNPK  PRINT+11(4),CONVERT+5(3)                            VF0I
         LA    WORK2,PRINT+11 -* ADR OF AVAIL CYLINDER TO PRINT.   VF01
         LA    WORK3,4       -*NO OF DIGITS TO BE PROCESSED.       VF01
         BAL   RETURN,TRY    -* GO TO GET RID OF LEADING ZEROS RTN.VF01
         OI    PRINT+14,X'F0' -* OVERLAP.                          VF01
         BAL   RETURN,LINEPR  -* PRINT A SUMMARY INFORMATION.      VF01
         LA    MSGFROM,52                                          VF0I
         LA    MSGTO,0                                             VF0I
         BAL   MSGRET,MVMSG                                        VF0I
         LH    WORK3,VOIDDSCB+2   MOVE AVAILABLE DSCBS TO PRINT    VF0I
         CVD   WORK3,CONVERT                                       VF0I
         UNPK  PRINT+11(5),CONVERT+5(3)                        @ZA28980
         LA    WORK2,PRINT+11 -* ADR OF AVAIL DSCB FOR PRINT.      VF01
         LA    WORK3,5       -* NO.OF DIGITS TO BE PROCESSED.  @ZA28980
         BAL   RETURN,TRY    -* GO TO GET RID OF LEADING ZEROS RTN.VF01
         OI    PRINT+15,X'F0' -* OVERLAP.                      @ZA28980
         BAL   RETURN,LINEPR  -* PRINT ANOTHER SUMMARY INFORMATION.VF01
         B     PCLOSE                                              VF0I
DREAD    NOP   0(0)                                                VF0I
         LA    SAVEREG,SAVEAREA                                    VF0I
         READ  DSCBA,SF,WORKIN,READIN                              VF0I
         CHECK DSCBA                                               VF0I
         OI    READIN+44,X'F0'                                     VF0I
         BR    WORK2                                               VF0I
**********************************************************************
**
**       THIS IS A GENERALIZED MESSAGE MOVER  ITS PARAMETERS ARE
**       REG MSGTO=ADDR OF CORE TO RECEIVE MSG
**                 IF 0  PRINT AREA WILL RECEIVE
**       REG MSGFROM= INDEX FACTOR OF CORRECT MSG ADCON
**       REG MSGRET= RETURN ADDR
**
**********************************************************************
MVMSG    LR    KEEPRET,MSGRET           SAVE RETURN ADDRESS
         L     MSGLEN,IEHMSGAD              GET ADDR OF MSG CSECT
         AR    MSGFROM,MSGLEN               ADD TO DISP OF MSG ADCON
         L     MSGFROM,0(MSGFROM)           GET ADDR OF MSG
         SR    MSGLEN,MSGLEN
         IC    MSGLEN,0(MSGFROM)        PUT LENGTH OF MESSAGE IN MSGLEN
         BCTR  MSGLEN,0                     DOWN LENGTH BY ONE
         LTR   MSGTO,MSGTO                  TEST FOR -- TO -- ADDR
         BZ    MSGTOSET                 BRANCH TO SET MSGTO
MSGEX    EX    MSGLEN,MSGMOVE               MOVE MESSAGE
         AR    MSGTO,MSGLEN             ADD LENGTH TO MSGTO
         LR    MSGRET,KEEPRET               LOAD RETURN ADDR
         BR    MSGRET                   RETURN
MSGTOSET LA    MSGTO,PRINT                  SET T/ ADDR TO PRINT AREA
         B     MSGEX
MSGMOVE  MVC   0(1,MSGTO),1(MSGFROM)
**********************************************************************
**       THIS IS A GENERAL POINT ROUTINE  IT IS USED WHENEVER A
**       POINT IS TO BE ISSUED    IT IS LINKED TO BY A ----BAL 14,15
**       REGISTER  --  PARAREG 1  --  WILL CONTAIN THE ADDR OF THE TTR
**********************************************************************
DOPOINT  ST    14,PNTSTOR                   STORE RETURN ADDR
         LR    1,PARAREG1
         MVC   CONVERT(3),0(1)
         MVI   CONVERT+3,X'00'
         POINT   WORKIN,CONVERT
         L     14,PNTSTOR                   LOAD RETURN ADDR
         BR    14                           RETURN
**********************************************************************
**
**       THIS SECTION CONTAINS TABLES AND CONSTANTS REQUIRED BY AND FOR
**       THE VOLUME MOUNTING ROUTINE
**
**********************************************************************
         DS    0F
PNTSTOR  DS    4X
         DS    0F
RONTAB   DC    AL1(2)
         DC    AL3(GENLST)
         DC    A(1)
         DC    A(1)
DEVCODE  DS    4X
VOLID    DS    6X
RTA      DS    2X
UNIT     DC     5X'00'                                             DT0I
GENLST   DS    16X
DDNAME1  DC    CL8'SYSIN'
DDNAME2  DC    CL8'SYSPRINT'
RONTAB1  DC    AL1(2)
         DC    AL3(GENLST)
RONSEECT DC    V(IEHMVESF)
DEVTABAD DC    F'0'               ADDRESS OF DEVICE NAME TABLE     I276
**********************************************************************
**
**       THIS SECTION OF THE ROOT SEGMENT HANDLES THE PASSING OF
**       CONTROL FROM ONE SEGMENT TO ANOTHER
**
**********************************************************************
RONGO    BALR  14,15                   LINK TO VOLUME MNT RTN
         L     15,RONVEECO
         BR    15
RONVEECO DC    V(RONBACK)
PBEGIN   L     15,PVEECON
         BR    15
RSTART   L     15,RVEECON
         BR    15
QSTART   L     15,QVEECON                                      @Z40CSJH
         BR    15                                              @Z40CSJH
SSTART   L     15,SVEECON                                       YL026VC
         BR    15
DSTART   L     15,DVEECON                                          VF0I
         BR    15                                                  VF0I
DVEECON  DC    V(IEHLISTD)             VCON FOR IEHLISTD
PVEECON  DC    V(IEHLISTP)             VCON FOR IEHLISTP
QVEECON  DC    V(IEHLISTQ)             VCON FOR IEHLISTQ       @Z40CSJH
RVEECON  DC    V(IEHLISTU)             VCON FOR IEHLISTU
SVEECON  DC    V(IEHLISTS)             VCON FOR IEHLISTS
         DS    0D
**********************************************************************
**
**       THIS SECTION CONTAINS THE WORK AREAS AND DCB NECESSARY FOR
**       READING SYSIN AND SCANNING THE CONTROL CARDS
**
**********************************************************************
SWITCHRD DS    192X
CDDECB   DS    24X
SAVEAREA DS    72X
CARDIN   DCB   DSORG=PS,                                               -
               MACRF=(GM),                                             -
               RECFM=FB,                                               -
               LRECL=80,                                               -
               EXLST=DCBEXITC,                                     BS0H*
               BFTEK=S,                                                -
               BUFNO=1,                                                -
               SYNAD=SYSINERR
         DS    0D
**********************************************************************
**
**       HERE IS THE SYSPRINT DCB
**
**********************************************************************
PRINTOUT DCB   DSORG=PS,                                               -
               MACRF=(PM),                                             -
               RECFM=FBA,                                              -
               LRECL=121,                                              -
               EXLST=DCBEXITP,                                     BS0H*
               BFTEK=S,                                                -
               BUFNO=1
**********************************************************************
**
**       HERE IS THE DCB USED FOR READING A VTOC, OR PDS        YL026VC
**
**********************************************************************
WORKIN   DCB   DDNAME=WORKDD,                                          -
               DSORG=PS,                                               -
               EXLST=EXIT,                                             -
               RECFM=F,                                                -
               SYNAD=WORKERR1,                                   A24122-
               MACRF=RP,                                               -
               BLKSIZE=256
SETEO3   DC    AL3(SEODAD)
SETEO2   DC    AL3(REODAD)
SETEO4   DC    AL3(DEODAD)                                         VF0I
SETEO1   DC    AL3(1)
SETBLK1  DC    AL2(96)
SETBLK2  DC    AL2(256)
SETKEY1  DC    AL1(8)
SETKEY2  DC    AL1(44)
**********************************************************************
**
**       THIS SECTION CONTAINS CONSTANTS AND TABLES THAT ARE NOT RESET
**
**********************************************************************
         DS     0F
OBLIST1  CAMLST  SEARCH,OBTAININ,VOLID,DSCBIN
         DS    0F                                                  VF0I
OBLIST3  CAMLST SEEK,TRACK,VOLID,HOLDAREA                          VF0I
         DS    0F
FF       DC    8X'FF'
DCBEXITC DC    X'85'                                               BS0H
         DC    AL3(CEXIT)                                          BS0H
DCBEXITP DC    X'85'                                               BS0H
         DC    AL3(PEXIT)                                          BS0H
CLEAR    DC    X'0000000000000000'
SW       DC    CL1'0'
*                     THE VALUES THAT 'FMT' CAN ASSUME ARE AS      VF0I
*                     FOLLOWS -                                    VF0I
*                        1. X'01' - SIGNIFIES 'FORMAT' OPTION      VF0I
*                                   HAS BEEN SPECIFIED.            VF0I
*                        2. X'02' - SIGNIFIES THAT LL FIELD OF     VF0I
*                                   TTRLL IS NEGATIVE AND THAT     VF0I
*                                   WARNING MESSAGE IEH109I SHOULD VF0I
*                                   BE GENERATED.                  VF0I
*                        3. X'04' - SIGNIFIES THAT THE DEVICE IS   VF0I
*                                   A 2321 DATACELL.               VF0I
*                        4. X'08' - SPECIFIES THAT HALFWORD BEING  VF0I
*                                   CONVERTED IS A HH ADDRESS AND  VF0I
*                                   MUST BE TRANSPOSED BEFORE      VF0I
*                                   CONVERSION IF DEVICE IS 2321.  VF0I
FMT      DC    CL1'0'                                              VF0I
RDPOS45  DC    A(READIN+44)                                        VF0I
RDPOS136 DC    A(READIN+135)                                       VF0I
DSCBRET  DC    F'0'                                                VF0I
FORM5TTR DC    F'0'                                                VF0I
VOIDDSCB DC    F'0'                                                VF0I
STRIP    DC    X'1F'
FIVE     DC    F'5'
RETCODE  DS    1X
RDCARDAD DC    V(IEHLISTC)             VCON FOR IEHLISTC
PRINTEND DC    A(PRINT+120)
TABAD    DC    A(TABLE)
PRPOS110 DC    A(PRINT+110)
IEHMSGAD DC    V(IEHLISTM)             VCON FOR IEHLISTM
         EXTRN IEHLISTM
         DS    0F
         DC    X'6B'              TRANSLATE X'EE' TO A COMMA       I282
         DC    X'40'
TRANTAB  DC    X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
CATNAME  DC    CL8'SYSCTLG'
MODLJFCB DC    13F'0'                                            A29736
         DC    X'08000000'                                       A29736
         DC    30F'0'                                            A29736
EXIT     DC    X'87'
         DC    AL3(MODLJFCB)
***********************************************************************
**
**       CONSTANTS  ---------  RESET TO ZEROS
**
***********************************************************************
*
         DS    0D
DFLG     DS    X                                                   BS0H
DFLAG    DS    X                                                   BS0H
KEY      DS    1X
VOLSW    DS     1X
COMMSW   DS    1X
DATE     DS    5X
         DS    0D
DEVTYPE  DS    8X
CONVERT  DS    8X
STORRET  DS    4X
PDSSPACE DS    8X
SAVE13   DS    F
         DS    0D
         DS    7X
CONTROL  DC     X'09'
PRINT    DS    120X
TABLE    DS    44X
         DS    8X
         DS    0D                      LOCATE WORK AREA ALIGNED
CATIN    DS    440X
         DS    0D                                                  VF0I
         DS    4X                                                  VF0I
OBTAININ DS    8X
BLOCKIN  DS    36X
DSCBIN   DS    500X
SRDSW    DS    1X
ZEROS    EQU   CLEAR
TRANSLAT EQU   TRANTAB-240
DSYSUCB  EQU   X'0C'                         DISPLACE OF SYS RES UCB AD
DVOLID   EQU   X'0C'                         DISPLACE OF VOL ID IN UCB
LOCIN    EQU   CATIN
READIN   EQU   OBTAININ
VCBIN    EQU   CATIN
INDXNAME EQU   DSCBIN+48   NOTE-- INDXNAME MUST BE LOCATED       A27713
*                                    48 BYTES PAST DSCBIN        A27713
STOREG   EQU   INDXNAME+44
QSTOREG  EQU   INDXNAME+84
LOCTAB   EQU   CATIN+256
DCBDDNAM EQU   WORKIN+40
DCBBLKSI EQU   WORKIN+62
KEYLE    EQU   WORKIN+16
DCBEODAD EQU   WORKIN+33
INHEAD   EQU   ZSAVEHD                       HEADDER GOES HERE
INSYSPR  EQU   GENLST+8                      SYSPRINT DD NAME GOES HERE
INSYSIN  EQU   GENLST                        SYSIN DD NAME GOES HERE
INLINCNT EQU   ZLINEPER                      ONE BYTE BINARY GOES HERE
MAX      DC    A(PRINT+100)                                        VF0I
UND      DC    C'UN'                                               VF0I
CPS      DC    C'PS'                                               VF0I
CIS      DC    C'IS'                                               VF0I
CDA      DC    C'DA'                                               VF0I
CPO      DC    C'PO'                                               VF0I
CVS      DC    C'VS'                                            YL026VC
TYPEVSAM DC    X'0008'                                          YL026VC
HOLDAREA EQU   DSCBIN+144                                          VF0I
PHOLD    EQU   DSCBIN+350               LENGTH OF  28 BYTES        VF0I
XHOLD    EQU   DSCBIN+400               LENGTH OF  10 BYTES        VF0I
UNPKFLD  EQU   DSCBIN+420               LENGTH OF   5 BYTES        VF0I
WK2321   EQU   DSCBIN+440              LENGTH OF   2 BYTES         VF0I
HLD2321  EQU   DSCBIN+450               LENGTH OF   1 BYTE         VF0I
PRINT1   DS    1X                                                  VF0I
SAVECCHH DS    5X                                                  VF0I
TRACK    DS    5X                                                  VF0I
FMT1END  DC    A(HOLDAREA+20)                                      VF0I
HOLDEND  DC   A(HOLDAREA+130)                                      VF0I
**********************************************************************
**
**                           IEHLISTP
**
**********************************************************************
IEHLISTP CSECT
**********************************************************************
**
**       HERE IS WHERE THE INPUT CARDS ARE READ, TABLES BUILT,
**                   AND SWITCHES SET
**
**********************************************************************
         BALR  BASEREG2,0
         USING *,BASEREG2
         LA    TABPNT,TABLE                 LOAD ADDR OF TABLE
         MVI   VOLSW,X'00'                  RESET VOLMUME SPEC SW
         XC    UNIT(5),UNIT            ZERO FIELD FOR UNITADDR   A38611
         MVI   SW,X'00'                      SET SWITCHES OFF
         MVI   FMT,X'00'                                           VF0I
         MVI   TABLE,X'40'                  CLEAR TABLE
         MVC   TABLE+1(250),TABLE
         MVC   TABLE+251(250),TABLE+250
         MVC   TABLE+501(250),TABLE+500
         MVI   KEY,X'00'                    RESET KEY SW
         MVI   OBTAININ,X'00'               CLEAR WORK AREA
         MVC   OBTAININ+1(250),OBTAININ     CLEAR WORK AREA
         L     SAVEREG,KENS                 LOAD ADDR OF CARD READ
         L     BRANCHTO,RDCARDAD            WORK AREA AND ADDR OF SCAN
         BALR  RETURN,BRANCHTO               LINK TO CONTROL CARD SCAN
         TM    SWITCHRD,ERRORCD            TEST FOR ERROR RETURN
         BO    PERRPR                       IF SO BRANCH
         TM    SWITCHRD,LC                   TEST FOR LAST CARD
         BO    PTERM                        IF SO --- EOJ
         TM    SWITCHRD,COMMAND              TEST FOR COMMAND SCANNED
         BZ    PERRPR
         TM    SWITCHRD,KEYSW+PARAM          TEST FOR NO KEY OR PARAM
         BO    PERRPR
         BM    PERRPR
         CLC   0(8,SCANADD),COMMTAB IS COMMAND = LISTCTLG      @Z40CSJH
         BE    PCAT                                            @Z40CSJH
         CLC   0(8,SCANADD),COMMTAB+8 IS COMMAND = LISTVTOC    @Z40CSJH
         BE    PVTOC
         CLC   0(8,SCANADD),COMMTAB+16                   LISTPDS
         BE    PPDS
         B     PERRPR                        IF NONE GO TO ERROR
PCAT     MVI   COMMSW,X'01'                  SET SW TO 1       @Z40CSJH
         B     PON                                             @Z40CSJH
PVTOC    MVI   COMMSW,X'02'                  SET SW TO 2        YL026VC
         B     PON
PPDS     MVI   COMMSW,X'03'                            3
PON      TM    SWITCHRD,BYPASS              ARE ALL PARAMETERS PASSED
         BO    PWORKIN                      IF SO BRANCH
         L     SAVEREG,KENS                 IF NOT --- LOAD REGISTERS
         L     BRANCHTO,RDCARDAD            AND RETURN TO CARD CCANNER
         BALR  RETURN,BRANCHTO
         TM    SWITCHRD,ERRORCD+COMMAND      TEST FOR ERROR OR COMMAND
         BO    PERRPR
         BM    PERRPR
         CLI   KEY,X'FF'                    WAS LAST KEY = VOL
         BE    PDEVICE                      IF SO BRANCH
         LTR   SCANLEN,SCANLEN              TEST FOR PARAM LEN = 0
         BE    PBYPASS
         TM    SWITCHRD,KEYSW                TEST FOR KEY
         BO    PKEY
         TM    SWITCHRD,PARAM                TEST FOR PARAMETER
         BO    PPARAM
PBYPASS  TM    SWITCHRD,BYPASS               TEST FOR LAST PARAMETER
         BO    PWORKIN
         B     PON
PKEY     CLC   0(4,SCANADD),KEYS             CHECK KEY FOR
         BE    PDATEKEY                                    DATE
         CLC   0(6,SCANADD),KEYS+7
         BE    POPTKEY                                     OPTION
         CLC   0(4,SCANADD),KEYS+14
         BE    PNODEKEY                                    NODE
         CLC   0(3,SCANADD),KEYS+21
         BE    PDSNKEY                                     DSNAME
         CLC   0(3,SCANADD),KEYS+28
         BE    PVOLKEY
         B     PERRPR                       IF INVALID KEY
PDATEKEY MVI   KEY,X'04'                     FOR KEY=DATE
         OI    SW,DATESW
         CLI   COMMSW,X'02'            IS THIS LISTVTOC        @YM04802
         BNE   PERRPR                  NO -INVALID PARAM       @YM04802
         B     PBYPASS
POPTKEY  MVI   KEY,X'08'                     FOR KEY=OPTION
         B     PBYPASS
PVOLKEY  MVI   KEY,X'FF'
         B     PBYPASS
PNODEKEY MVI   KEY,X'0C'                     FOR KEY=NODE
         OI    SW,NODESW
         CLI   COMMSW,X'01'                                      A42351
         BNE   PERRPR                  MUST BE LISTCTLG          A42351
         B     PBYPASS
PDSNKEY  MVI   KEY,X'0C'                    FOR KEY = DSNAME    YL026VC
         OI    SW,DSNAMESW
         B     PBYPASS
PDEVICE  NOP    0(0)
         TM    SWITCHRD,KEYSW               TEST FOR KEY = VOL
         BZ    PERRPR
         MVI   KEY,X'10'                    RESET KEY SW FOR VOL ID
         BCTR  SCANLEN,0                    REDUCE LENGTH OF PARAMETER
         MVI   DEVTYPE,X'40'
         MVC   DEVTYPE+1(7),DEVTYPE
         EX      SCANLEN,PMVDEV             MOVE DEVICE TYPE TO SAVE
         B     PBYPASS
PMVDEV   MVC   DEVTYPE(1),0(SCANADD)        MOVE DEVICE TYPE TO SAVE
PPARAM   LR    WORK2,SCANADD
         AR    WORK2,SCANLEN
         CLI   0(WORK2),C'('
         BE    PERRPR
         BCTR  SCANLEN,0
         SR    WORK2,WORK2
         IC    WORK2,KEY                     PUT KEY IN REG
         B     PBRANCH(WORK2)                BRANCH TO INDEXED BRANCH
PBRANCH  B     POPT
         B     PDATE                                   KEY=4
         B     PERRPR
         B     PBLDTAB                                 KEY=12
         B     PVOL
PDATE    LA    WORK2,4
         CR    WORK2,SCANLEN                 CHECH DATE LENGTH
         BNE   PERRPR                       GO TO ERROR IF WRONG LEN
         MVC   DATE(5),0(SCANADD)             MOVE DATE
         MVI   KEY,X'00'                     RESET KEY
         B     PBYPASS
POPT     LA    WORK2,2                       CHECK PARAM LENGTH    8173
         CR    WORK2,SCANLEN                   IF 3 CHARACTERS,    8173
         BE    PKEY                            CHECK FOR VOL PARAM 8173
         LA    WORK2,3                                             8173
         CR    WORK2,SCANLEN
         BNE   POPT1
         CLC   OPTION(4),0(SCANADD)
         BE    POPDUMP
POPT1    LA    WORK2,5
         CR    WORK2,SCANLEN
         BNE   PERRPR
         CLC   OPTION+4(6),0(SCANADD)              = FORMAT
         BE    POPLIST
         B     PERRPR                        ERROR IF BAD OPTION
POPDUMP  OI    SW,DUMPSW                     TURN ON DUMP SW
         B     *+8                                                 VF0I
POPLIST  MVI   FMT,X'01'                                           VF0I
         MVI   KEY,X'00'                                           VF0I
         B     PBYPASS
PBLDTAB  LA    WORK2,43
         CR    WORK2,SCANLEN                 CHECK PARAMETER LENGTH
         BL    PERRPR                        LENGTH OVER 44 BYTES
         EX    SCANLEN,PTABLE                EXECUTE MOVE DSNAME - NODE
         TM    SWITCHRD+129,X'FF'      IS THERE A PARAN COUNT      8173
         BZ    PRESET                  NO,RESET KEY                8173
         TM    SWITCHRD,X'01'          IS THERE A PARAN IN SWITCHRD8173
         BZ    PBLDMOR                 NO,MORE PDS NAMES TO FOLLOW 8173
PRESET   MVI   KEY,X'00'               INSERT LAST PDS IN TABLE    8173
PBLDMOR  LA    TABPNT,44(TABPNT)       UP TABLE POINTER
         CR    TABPNT,TABEND                 CHECK IF TABLE FULL
         BNE   PBYPASS                      IF SO BRANCH
         MVI   KEY,X'00'                     IF FULL RESET KEY
         B     PBYPASS
PTABLE   MVC   0(0,TABPNT),0(SCANADD)        MOVE DSNAME - NODE TO TAB
PVOL     LA    WORK2,5                      CHECK LEN OF VOL ID
         CR    WORK2,SCANLEN
         BL    PERRPR                  CHECK LEN OVER 6
         MVI   VOLID,X'40'            CLEAR VOLID FIELD        @YA01685
         MVC   VOLID+1(5),VOLID                                @YA01685
         EX    SCANLEN,PMVID
         MVI   KEY,X'00'                    RESET KEY
         MVI   VOLSW,X'FF'                  TURN ON VOL SPEC SW
         B     PBYPASS
PMVID    MVC   VOLID(1),0(SCANADD)      MOVE VOL ID TO SAVE AREA
PWORKIN  CLI   VOLSW,X'FF'                  WAS VOL SPEC IN CTRL CD
         BE    PFINDVOL                     IF SO ------
         L     WORK2,CVTPTR            IF VOL NOT SPEC IN
         L     WORK2,40(WORK2)              CARD  FIND SYSRES UCB AND
VON      ICM   WORK3,3,0(WORK2)        GET VOLID AND DEVICE    @ZM30153
         N     WORK3,FOXES             ZERO LEFT HALF OF REG   @ZM30153
         C     WORK3,FOXES                  CODE               @ZM30161
         BE    PDEVNONE
         LTR   WORK3,WORK3
         BZ    NOTAUCB
         TM    18(WORK3),X'20'         TEST FOR D.A.
         BZ    NOTAUCB
         TM    3(WORK3),X'02'
         BO    VFIND
NOTAUCB  EQU   *
         LA    WORK2,2(WORK2)
         B     VON
VFIND    MVC   VOLID(6),28(WORK3)           MOVE VOL ID TO SAVE
         MVC   DEVCODE(4),16(WORK3)
         B     PCALLRON
PFINDVOL L     WORK2,DEVTABAD               GET ADDR OF DEVICE NAME TAB
         L     WORK3,0(WORK2)               GET NUMBER OF ENTRIES
         LA    WORK2,4(WORK2)               UP POINTER TO FIRST ENTRY
PDEVCOMP CLC   DEVTYPE(8),0(WORK2)          COMPARE
         BE    PDEVFIND                     BRANCH ON FIND
         LA    WORK2,12(WORK2)              UP POINTER TO NEXT ELEMENT
         BCT   WORK3,PDEVCOMP               CONTINUE SEARCH IF NOT END
*                                                                  DT0I
* IF NO ENTRY IS FOUND IN THE DEVICE NAME TABLE CHECK THE          DT0I
* UCB'S TO SEE IF IT WAS A UNIT ADDR                               DT0I
*                                                                  DT0I
         L     WORK2,CVTPTR            GET PTR TO UCB LUT
         L      WORK2,40(WORK2)                                    DT0I
UCB      ICM   WORK3,3,0(WORK2)        GET UCB PTR             @ZM30153
         N     WORK3,FOXES             ZERO LEFT HALF OF REG   @ZM30153
         TM    0(WORK2),X'FF'          TEST IF END OF UCB LUT      DT0I
         BC    12,NOTEND                                           DT0I
         TM    1(WORK2),X'FF'                                      DT0I
         BC     1,PDEVNONE         BRANCH IF END -- THIS INDICATES DT0I
*                                  DEVICE NOT FOUND                DT0I
NOTEND   LTR    WORK3,WORK3        TEST FOR ZERO ENTRY             DT0I
         BZ     NEXTPTR            BRANCH ON YES                   DT0I
         CLC   13(3,WORK3),DEVTYPE   IS THIS THE CORRECT DEV TYPE  DT0I
         BE     UNITFND            BRANCH ON YES                   DT0I
NEXTPTR  LA     WORK2,2(WORK2)     POINT TO NEXT UCBLUT  ENTRY     DT0I
         B      UCB                                                DT0I
UNITFND  MVC    DEVCODE(4),16(WORK3)   MOVE IN DEV TYPE FROM UCB   DT0I
         CLI    DEVTYPE+3,SLASH    IS THIS A 2321 BIN NUMB         DT0I
         BE     MOVE5              YES                             DT0I
         MVC    UNIT(3),13(WORK3)  MOVE IN UNIT NAME FROM UCB      DT0I
         B      PCALLRON                                           DT0I
MOVE5    MVC    UNIT(5),DEVTYPE       MOVE IN DEVICE TYPE FORM     DT0I
*                                     REQUEST                      DT0I
         B      PCALLRON                                           DT0I
PDEVFIND MVC   DEVCODE(4),8(WORK2)          SAVE DEVICE CODE
         B     PCALLRON
PDEVNONE LA    MSGTO,0                      PRINT ERROR MSG
         LA    MSGFROM,136                      IF BAD DEVICE OR
         CLI   RETCODE,X'08'           IS HIGHER RETURN CODE SET?
         BH    *+8                     BRANCH IF YES TO LET IT STAND
         MVI   RETCODE,X'08'           SET RETURN CODE TO 8
         BAL   MSGRET,MVMSG                     VOL ID
         BAL   RETURN,LINEPR
         B     PCLOSE
PCALLRON LA    1,RONTAB                     SET UP REGISTERS FOR LINK
         LA    13,DSCBIN                LOAD ADDR OF REG SAVE AREA
         L     15,RONSEECT
         CLI   DEVCODE+3,X'05'         IS DEVICE 2321
         BNE   RONGO
         OI    FMT,X'04'               DEVICE IS 2321 SET BIT 5 ON VF0I
         B     RONGO
RONBACK  EQU   *
         ENTRY RONBACK
         LA    13,SAVEAREA             RESET REG SAVE AREA ADDR
         TM    RONSW,X'01'                  TEST FOR VOLUME MOUNTED
         BZ    PDEVNONE                     IF NOT BRANCH
         NI    RONSW,X'FE'
         L     WORK2,RONTAB+4               GET ADDR OF ADDR OF DDNAME
         AH    WORK2,RTA
         L     WORK2,0(WORK2)               LOAD ADDR OF DDNAME
         MVC   DCBDDNAM(8),0(WORK2)         MOVE DDNAME TO SAVE
         B     PHEAD
PHEAD    NOP   0(0)
         MVI   ZSW,X'01'
         CLI   COMMSW,X'01'                  GO TO             @Z40CSJH
         BE    PHDCAT            PRINT CATALOG HEAD            @Z40CSJH
         CLI   COMMSW,X'03'
         BE    PHDPDS                              PRINT PDS HEAD
         B     PHDVTOC                             PRINT VTOC HEAD
PHDCAT   LA    MSGFROM,20                                      @Z40CSJH
         LA    MSGTO,0                                         @Z40CSJH
         BAL   MSGRET,MVMSG                                    @Z40CSJH
         LA    MSGTO,1(MSGTO)                                  @Z40CSJH
         MVC   0(6,15),VOLID                                   @Z40CSJH
         BAL   RETURN,LINEPR       GO TO PRINT A LINE          @Z40CSJH
         LA    MSGFROM,60                                      @Z40CSJH
         LA    MSGTO,0                                         @Z40CSJH
         BAL   MSGRET,MVMSG                                    @Z40CSJH
         BAL   RETURN,LINEPR                                   @Z40CSJH
         B    QSTART                                           @Z40CSJH
PHDPDS   TM    SW,DSNAMESW
         BZ    PERRPR
         LA    MSGFROM,28
         LA    MSGTO,0
         BAL   MSGRET,MVMSG
         LA    MSGTO,1(MSGTO)
         MVC   0(6,15),VOLID
         BAL   RETURN,LINEPR       GO TO PRINT A LINE
         LA    TABPNT,TABLE
         B     SSTART                       GO TO PDS ROUTINE
PHDVTOC  LA    MSGFROM,24
         LA    MSGTO,0
         BAL   MSGRET,MVMSG
         LA    MSGTO,1(MSGTO)
         MVC   0(6,15),VOLID
         BAL   RETURN,LINEPR       GO TO PRINT A LINE
         TM    SW,DUMPSW                     TEST OPTION
         BO    PHDDUMP                       IF DUMP
         TM    FMT,FMTSW                                           VF0I
         BO    DSTART                                              VF0I
         B     PHDLIST                       IF LIST
PHDDUMP  TM    FMT,FMTSW               PREVENT DUMP AND FORMAT
         BO    PERRPR                     ON SAME CARD
         LA    MSGFROM,36
         LA    MSGTO,0
         BAL   MSGRET,MVMSG
         BAL   RETURN,LINEPR       GO TO PRINT A LINE
         LA    MSGFROM,40
         LA    MSGTO,0
         BAL   MSGRET,MVMSG
         BAL   RETURN,LINEPR       GO TO PRINT A LINE
         LA    MSGFROM,44
         LA    MSGTO,0
         BAL   MSGRET,MVMSG
         BAL   RETURN,LINEPR       GO TO PRINT A LINE
         B     RSTART                        GO TO VTOC PROGRAM
PHDLIST  LA    MSGFROM,32
         LA    MSGTO,0
         BAL   MSGRET,MVMSG
         BAL   RETURN,LINEPR       GO TO PRINT A LINE
         B     RSTART              GO TO VTOC SECTION
         DS    0F                      ALIGN TO FULLWORD       @ZM30153
FOXES    DC    X'0000FFFF'             ZEROS LEFT HALF OF REG  @ZM30153
RONSW    EQU   RONTAB+4
KENS     DC    A(SWITCHRD)
COMMTAB  DC    CL8'LISTCTLG'
         DC    CL8'LISTVTOC'
         DC    CL8'LISTPDS'
OPTION   DC    CL10'DUMPFORMAT'
KEYS     DC    CL6'DATE'
         DC    X'04'
         DC    CL6'OPTION'
         DC    X'08'
         DC    CL6'NODE'
         DC    X'0C'
         DC    CL6'DSN'
         DC    X'0C'
         DC    CL6'VOL'
         DC    X'10'
* COMMUNICATION VECTOR TABLE DEFINITION
CVT      DSECT
         CVT   SYS=MIN
**********************************************************************
**
**                           IEHLISTQ
**
**********************************************************************
IEHLISTQ CSECT
***********************************************************************
**
**               THIS SECTION HANDLES THE SCANNING OF GENERAL
**               INFORMATION FROM THE CATALOG
**
**                                                             @Z40CSJH
**     THE ENTIRE CSECT IEHLISTQ WAS REACTIVATED               @Z40CSJH
**     FOR EXTENDED MVS CVOL SUPPORT IN ORDER TO LIST AN OS    @Z40CSJH
**     SYSCTLG DATASET, RESIDING ON A CONTROL                  @Z40CSJH
**     VOLUME (CVOL). THE FEATURE IS INITIATED                 @Z40CSJH
**     VIA THE LISTCTLG CONTROL FUNCTION                       @Z40CSJH
**                                                             @Z40CSJH
***********************************************************************
**
         BALR  BASEREG2,0                   ESTABLISH ADDRESSABILITY
         USING *,BASEREG2
         MVI   OBTAININ,X'40'               CLEAR WORK AREA
         MVC   OBTAININ+1(43),OBTAININ
         MVC   OBTAININ(8),CATNAME          MOVE  -- SYSCTLG -- TO
         OBTAIN  OBLIST1                    WORK AREA AND OBTAIN DSCB
         B     QTEST(ECREG)                 TEST RETURN
QTEST    B     QOPEN                        CATALOG IS ON VOLUME
         B     QERR                         EITHER NO CATALOG OR PERM
         B     QERR                            I/O ERROE WILL CAUSE
         B     WORKERR                      PERM I/O ERROR
QERR     LA    MSGTO,0                         ERROR MESSAGE
         LA    MSGFROM,8
         CLI   RETCODE,X'08'           IS HIGHER RETURN CODE SET?
         BH    *+8                     BRANCH IF YES TO LET IT STAND
         MVI   RETCODE,X'08'           SET RETURN CODE TO 8
         BAL   MSGRET,MVMSG                 MOVE ERROR MESSAGE
         BAL   RETURN,LINEPR                PRINT ERROR MSG
         B     PCLOSE
QOPEN    MVC   MODLJFCB(44),OBTAININ        SET UP JFCB FOR OPEN
         CLI   DSCBIN+56,X'FF'    CHECK FOR CATALOG FORMATTED
         BNE   LERRLOC          IF NOT ----
         MVI   MODLJFCB+117,X'01'
         MVI   MODLJFCB+44,X'40'
         MVI   MODLJFCB+IND2,SHR            SET SHARE BIT      @AZ18372
         MVC   MODLJFCB+118(6),VOLID
         MVC   DCBBLKSI(2),SETBLK2          SET BLOCKSIZE IN DCB
         MVC   KEYLE(1),SETKEY1             SET KEYLEN IN DCB
         MVC   DCBEODAD(3),SETEO1           SET EODAD ADDR IN DCB
         OI    SW,OPENSW                    SET OPEN SWITCH ON
         OPEN  (WORKIN),TYPE=J              OPEN DCB FOR INPUT
         BAL   RETURN,QCATREAD              GO TO READ CATALOG
         LA    BLOCPNT,CATIN                SET REG TO READ IN AREA
         LA    WORK2,24                     LOAD REG WITH NO. OF BYTES
         LNR   WORK2,WORK2                    TO SKIP AND INVERT
         AH    WORK2,CATIN                  ADD NO. OF BYTES USED
         ST    WORK2,ENTRYCNT               STORE IN SAVE AREA
         LA    BLOCPNT,24(BLOCPNT)          UP POIMTER PAST FIRST
QCHECK1  C     BLOCPNT,CATEND                COMP POINTER TO END ADDR.
         BH    QREAD1                       BRANCH IF PAST LAST ENTRY
         L     WORK2,ENTRYCNT               LOAD NUMBER OF BYTES TO
         LTR   WORK2,WORK2                    BE SCANNED ADD CHECK IF D
         BZ    QREAD1                       DONE
         CLC   FF(8),0(BLOCPNT)              CHECK IF AT NAME=FFFFFFFF
         BE    QEND1
         SR    WORK2,WORK2
         IC    WORK2,11(BLOCPNT)             PUT C INTO REG
         LA    WORK3,31
         NR    WORK2,WORK3                  STRIP HI THREE BITS
         SLL   WORK2,2                      MULT BY 4
         LA    WORK3,20                 LOAD REG WITH 4 TIMES 5    UC0H
         CR    WORK2,WORK3                   CHECK C LARGER THAN 4
         BH    QPRDS1                        IF POINTER TO DATA SET
         B     QBRANCH1(WORK2)               BRANCH TO INDEXED BRANCH
QBRANCH1 B     QSCAN1                        IF C=0  INITIAL,LAST,INDEX
         B     QPRDS1                           C=1  VCB   POINTER
         B     QSCAN1                           C=2  INDEX POINTER
         B     QPRCVOL                          C=3  CVOL  POINTER
         B     QPRCVOL                       C=4  ALIAS ENTRY      UC0H
         B     QPRCVOL                       C=5 NEW CVOL POINTER  UC0H
QSCAN1   SRL   WORK2,1                      DIVID BY 2
         LA    WORK2,12(WORK2)              ADD USER DATE LEN TO
         AR    BLOCPNT,WORK2                 ENTRY LEN -- UP POINTER
         L     WORK3,ENTRYCNT               SUB BYTES SCANNED FROM
         SR    WORK3,WORK2                     BYTE COUNT
         ST    WORK3,ENTRYCNT               SAVE DIFFERANCE
         B     QCHECK1
QREAD1   EQU   *                       ANOTHER BLOCK NEEDED
         BAL   RETURN,QCATREAD              GO TO READ CATALOG
         L     BLOCPNT,CATINAD               SET BLOCPNT TO START
         LH    WORK2,CATIN                  LOAD NO. OF BYTES TO
         BCTR  WORK2,0                         SCAN AND REDUCE BY 2
         BCTR  WORK2,0
         ST    WORK2,ENTRYCNT
         LA    BLOCPNT,2(BLOCPNT)           UP POINTER TO FIRST ENTRY
         B     QCHECK1
QEND1    CLC   CLEAR(4),8(BLOCPNT)          CHECK IF TTR = 000
         BE    QHEAD                         IF SO DONE WITH SCAN1
         MVC   KEEPTTR(3),8(BLOCPNT)        IF NOT MOVE TTR
         LA    PARAREG1,KEEPTTR             LOAD THE ADDR OF THE TTR
         BAL   RETURN,DOPOINT                 BRANCH TO POINT ROUTINE
         B     QREAD1
QPRCVOL  B     QPRGEN
QPRDS1   MVC   INDXNAME(8),0(BLOCPNT)       ON DATD SET ENTRY MOVE
         MVC   PRINT(8),INDXNAME               NAME TO PRINT
         LA    MSGTO,PRINT+26
         LA    MSGFROM,128
         BAL   MSGRET,MVMSG                 MOVE DESCRIPTION TO PRINT
         BAL   RETURN,LPRDATA               GO TO VOL ID SCAN
         B     QSCAN1
**********************************************************************
**
**         THIS SECTION PRINTS A CATALOG HEADDER AFTER THE GENERAL
**         INFORMATION IS PRINTER
**
**********************************************************************
QHEAD    TM    SW,NODESW
         BO    QHEAD2                       BRANCH IF NODE SPECIFIED
         LA    MSGFROM,64
         LA    MSGTO,PRINT+13
         BAL   MSGRET,MVMSG                 MOVE ENTIRE PRINT HEADDER
         BAL   RETURN,LINEPR
         B     QALL
QHEAD2   L     TABPNT,TABAD
         LA    MSGFROM,56
         LA    MSGTO,0
         BAL   MSGRET,MVMSG                 MOVE PARTIAL PRINT HEADDER
         MVC   1(44,MSGTO),0(TABPNT)
         BAL   RETURN,LINEPR                GO TO PRINT HEADDER
         MVC   INDXNAME(44),0(TABPNT)
         MVC   LOCTAB(3),PRINT              SET  TTR FIELD TO BLANKS
         BAL   WORK3,QLOCATE                LINK TO SCAR ROUTINE
         B     PCLOSE
***********************************************************************
**
**              FOR ENTIRE CATALOG PRINTS THIS SECTION SCANS HIGH
**              LEVEL NODE POINTS AND HANDS THEM TO QLOCATE
**
***********************************************************************
QALL     EQU   *
         LA    WORK2,256                    SET UP TTR = 1
         ST    WORK2,KEEPTTR                   FOR POINT
         LA    PARAREG1,KEEPTTR             LOAD THE ADDR OF THE TTR
         BAL   RETURN,DOPOINT                 BRANCH TO POINT ROUTINE
         BAL   RETURN,QCATREAD              GO TO READ CATALOG
         LA    BLOCPNT,CATIN                SET REG FOR SCAN
         LA    WORK2,24
         LNR   WORK2,WORK2                  CALC NO. OF BYTES TO SCAN
         AH    WORK2,CATIN                  MINUS BYTES IN FIRST ENTRY
         ST    WORK2,ENTRYCNT               AND SAVE
         LA    BLOCPNT,24(BLOCPNT)          UP POINTER TO FIRST ENTRY
QCHECK2  C     BLOCPNT,CATEND                COMP IF AT END OF BLOCK
         MVI   INDXNAME,X'40'
         MVC   INDXNAME+1(43),INDXNAME
         BH    QREAD2
         L     WORK2,ENTRYCNT               CHECK IF BYTES TO SCAN = 0
         LTR   WORK2,WORK2
         BZ    QREAD2
         CLC   FF(8),0(BLOCPNT)              CHECK IF AT NAME=FFFF
         BE    QEND2
         SR    WORK2,WORK2
         IC    WORK2,11(BLOCPNT)             PUT C IN REG
         LA    WORK3,31                     STRIP HI 3 BITS
         NR    WORK2,WORK3
         SLL   WORK2,2                      MULT BY 4
         LA    WORK3,16
         CR    WORK2,WORK3                   COMPARE C BIGGER THAN 4
         BH    QSCAN2
         B     QBRANCH2(WORK2)               BRANCH TO INDEXED BRANCH
QBRANCH2 B     QFIND                         IF C=0
         B     QSCAN2                           C=1
         B     QFIND                            C=2
         B     QSCAN2                           C=3
         B     QSCAN2                           C=4
QFIND    MVC   INDXNAME(8),0(BLOCPNT)       MOVE NAME TO SAVE AREA
         NOTE  WORKIN                       GET TTR OF LAST READ RECORD
         ST    PARAREG2,KEEPTTR
         MVC   PRINT(44),INDXNAME           MOVE NAME TO PRINT
         MVC   LOCTAB(4),8(BLOCPNT)         SAVE TTR OF INDEX
         BAL   WORK3,QLOCATE                LINK TO SCANNER
         LA    PARAREG1,KEEPTTR             SET UP ADDR OF TTR
         BAL   RETURN,DOPOINT                 BRANCH TO POINT ROUTINE
         BAL   RETURN,QCATREAD              REREAD INDEX BLOCK
         B     QSCAN2
QSCAN2   SRL   WORK2,1                      DIVID BY 2
         LA    WORK2,12(WORK2)              SET REG TO LEN OF ENTRY
         AR    BLOCPNT,WORK2                 UPDATE BLOCPNT TO NEXT
         L     WORK3,ENTRYCNT               RECALC NO. OF BYTES TO SCAN
         SR    WORK3,WORK2
         ST    WORK3,ENTRYCNT                  AND SAVE
         B     QCHECK2
QREAD2   EQU   *
         BAL   RETURN,QCATREAD              READ CATALOG
         L     BLOCPNT,CATINAD               RESET BLOCK POINTER
         LH    WORK2,CATIN                  GET NO. OF BYTES TO SCAN
         BCTR  WORK2,0                         AND REDUCE BY 2
         BCTR  WORK2,0
         ST    WORK2,ENTRYCNT                  AND SAVE
         LA    BLOCPNT,2(BLOCPNT)           UP POINTER TO FIRST ENTRY
         B     QCHECK2
QEND2    CLC   CLEAR(4),8(BLOCPNT)          COMPARE FOR TTR
         BE    PCLOSE                       IF NONE -----
         MVC   KEEPTTR(3),8(BLOCPNT)        IF ONE  ---- SAVE
         LA    PARAREG1,KEEPTTR             SET UP ADDR OF TTR
         BAL   RETURN,DOPOINT                 BRANCH TO POINT ROUTINE
         B     QREAD2
***********************************************************************
**
**             WHEN HANDED A NODEPOINT THIS ROUTINE WILL SCAN OFF
**             ALL DATA SET POINTERS
**
***********************************************************************
**
QLOCATE  STM   0,9,QSTOREG                  SAVE REGISTERS
         LA    LOCPNT,LOCIN+20              SET UP REG TO START SCAN
         LA    TTRPTR,LOCTAB                SET REG TO TTR TABLE
         LA    NAMEPTR,INDXNAME             SET REG TO START OF NAME
LUPPTR2  CLI   0(NAMEPTR),X'40'
         BE    LUPON2                       THIS ROUTINE UPS THE REG TO
         LA    NAMEPTR,1(NAMEPTR)              THE FIRST BLANK FOLLOWIN
         B     LUPPTR2                         THE NAME
LUPON2   EQU   *                       CHECK FOR TTR
         CLC   LOCTAB(3),PRINT              WAS A TTR OF NODE PASSED
         BE    LFIRSTLO                       IF NOT -----
         B     LFIRSTRD                       IF SO -----
LFIRSTLO MVC   DSCBIN(44),INDXNAME
         LOCATE  LOCLIST2                   LOCATE SPEC NODE
         CLC   VOLID(6),LOCIN+259      WAS NODE POINT FOUND VIA A CVOL
         BNE   LERRLOC                   POINTER ---- IF SO BRANCH
         B     LBRANCH(LOCERR)
LBRANCH  B     LERRLOC
         B     LERRLOC                       =4
         B     LERRLOC                       =8
         B     LFINDX                        =12
         B     LERRLOC                       =16
         B     LERRLOC                      =20
         B     WORKERR
LFINDX   EQU   *                       SAVE TTR OF THIS INDEX
         MVC   0(3,TTRPTR),LOCIN+14
LFIRSTRD EQU   *                       USE TTR TO READ
         LA    PARAREG1,LOCTAB              LOAD THE ADDR OF THE TTR
         BAL   RETURN,DOPOINT                 BRANCH TO POINT ROUTINE
LREAD1   BAL   RETURN,QCATREAD              READ INDEX BLOCK
LREAD3     LA    WORK2,20
         LNR   WORK2,WORK2
         AH    WORK2,CATIN                  CALC NO. OF BYTES TO
         ST    WORK2,QENTRCNT                 SCAN AND SAVE
         B     LCHKELEM
LCHKELEM C     LOCPNT,LOCINEND               CHECK FOR POINTER AT END
         BH    LREAD                                             A28823
         L     WORK2,QENTRCNT               CHECK NO. OF BYTES TO
         LTR   WORK2,WORK2                      SCAN = 0
         BZ    LREAD
         CLC   FF(8),0(LOCPNT)              COMP FOR LAST RECORD
         BE    LFF
         SR    KEEPC,KEEPC
         IC    KEEPC,11(LOCPNT)             PUT '"' CHAR IN REG
         LA    JOB,31
         NR    KEEPC,JOB                    STRIP HI 3 BITS
         SLL   KEEPC,2                      MULT BY 4
         LR    WORK2,KEEPC                    TO USE AS INDEX FACTOR
         LA    JOB,16
         CR    KEEPC,JOB                     COMPARE TO C
         BH    LDSPRINT                      IF C = 5 OR MORE
         B     LCHKC(KEEPC)
LCHKC    B     LINDEX                        IF C = O
         B     LDSPRINT                         C = 1
         B     LINDEX
         B     LERRLOC
         B     LERRLOC
LDSPRINT MVI   0(NAMEPTR),C'.'              PUT . IN NAME
         MVC   1(8,NAMEPTR),0(LOCPNT)       ADD NEW NAME
         TM    2(NAMEPTR),X'F0'        IS THIS A GDG NAME
         BZ    GDG                      IF SO ----
         B     GDGON
GDG      XC    2(4,NAMEPTR),FF          MAKE GDG NAME PRINTABLE
GDGON    EQU   *
        MVC   PRINT(44),INDXNAME            MOVE NAME TO PRINT
         BAL   RETURN,LPRDATA               PRINT IT
         MVC   0(9,NAMEPTR),PRINT           STRIP OFF . AND NAME
         SRL   KEEPC,1                      DIVID C BY 2
         LA    KEEPC,12(KEEPC)              CALC SIZE OF ERTRY
         AR    LOCPNT,KEEPC                 UP POINTER TO NEXT ENTRY
         L     WORK2,QENTRCNT
         SR    WORK2,KEEPC                  RECALC NO. OF BYTES TO SCAN
         ST    WORK2,QENTRCNT                  AND SAVE
         B     LCHKELEM
LINDEX   EQU   *                       ADD NAME
         MVC   12(4,TTRPTR),8(LOCPNT)       MOVE TTR OF NEXT INDEX
         MVI   0(NAMEPTR),C'.'                TO SAVE AND PUT . IN NAME
         MVC   1(8,NAMEPTR),0(LOCPNT)       ADD INDEX NAME
LUPPTR   CLI   0(NAMEPTR),X'40'
         BE    LUPON
         LA    NAMEPTR,1(NAMEPTR)           UP NAME POINTER PAST NAME
         B     LUPPTR
LUPON    EQU   *                       GET NEXT ENTRY
         SRL   KEEPC,1
         AR    LOCPNT,KEEPC
         LA    LOCPNT,12(LOCPNT)            UP POINTER TO NEXT ENTRY
         ST    LOCPNT,4(TTRPTR)               TO SCAN AND SAVE ADDR
         LA    WORK2,12
         LNR   WORK2,WORK2                  RECALC NO. OF BYTES TO SCAN
         A     WORK2,QENTRCNT
         STH   WORK2,8(TTRPTR)
         LA    TTRPTR,12(TTRPTR)            UP POINTER TO TTR TABLE
         LR    PARAREG1,TTRPTR              LOAD THE ADDR OF THE TTR
         BAL   RETURN,DOPOINT                 BRANCH TO POINT ROUTINE
         LA    LOCPNT,LOCIN+20              SET POINTER TO FIRST ENTRY
         B     LREAD1                          OF LOWER INDEX
LPAST    EQU   *                       CALC NO BYTES TO SCAN
LSET     LA    LOCPNT,LOCIN+2               UP POINTER PAST BYTE COUNT
         LH    WORK2,CATIN
         BCTR  WORK2,0                      CALC AND SAVE THE NO.
         BCTR  WORK2,0                        OF BYTES TO SAAN IN
         ST    WORK2,QENTRCNT                 THIS INDEX
         B     LCHKELEM
LREAD    EQU   *                       GET ANOTHER BLOCK
         BAL   RETURN,QCATREAD              READ CATALOG BLOCK
         NOTE  WORKIN                       GET TTR OF LAST READ RECORD
         ST    PARAREG2,0(TTRPTR)           SANE TTR
         B     LPAST
         LR    PARAREG1,TTRPTR              LOAD THE ADDR OF THE TTR
         BAL   RETURN,DOPOINT                 BRANCH TO POINT ROUTINE
         B     LSET
LFF      CLC   ZEROS(3),8(LOCPNT)            CHECK TTR FOR ZEROS
         BE    LEND                           IF SO ----
         B     LCONT
LCONT    EQU   *                       POINT TO TTR
         MVC   LOCIN(4),8(LOCPNT)           MOVE TTR TO POINT LOCATION
         LA    PARAREG1,LOCIN               LOAD THE ADDR OF THE TTR
         BAL   RETURN,DOPOINT                 BRANCH TO POINT ROUTINE
         B     LREAD
LEND     C     TTRPTR,LOCTABAD               CHECK FOR END OF SCAN
         BE    QLEAVE
         LA    JOB,12                       IF NOT -- BACK UP POINTER
         SR    TTRPTR,JOB                     TO TTR TABLE
         LH    WORK2,8(TTRPTR)              LOAD NO. OF BYTES TO SCAN
         ST    WORK2,QENTRCNT                  AND SAVE
         LR    PARAREG1,TTRPTR              LOAD THE ADDR OF THE TTR
         BAL   RETURN,DOPOINT                 BRANCH TO POINT ROUTINE
LDNPTR   CLI   0(NAMEPTR),C'.'
         BE    LDNON
         BCTR  NAMEPTR,0                    BACK UP POINTER TO DSNAME
         B     LDNPTR
LDNON    MVI   0(NAMEPTR),X'40'             STRIP OFF NAME OF INDEX
         MVC   1(9,NAMEPTR),0(NAMEPTR)         JUST SCANNED
         L     LOCPNT,4(TTRPTR)
         BAL   RETURN,QCATREAD              REREAD BLOCK
         B     LCHKELEM
LERRLOC  LA    MSGTO,0
         LA    MSGFROM,68                   MOVE ERROR MSG TO PRINT
         CLI   RETCODE,X'08'           IS HIGHER RETURN CODE SET?
         BH    *+8                     BRANCH IF YES TO LET IT STAND
         MVI   RETCODE,X'08'           SET RETURN CODE TO 8
         BAL   MSGRET,MVMSG
         BAL   RETURN,LINEPR
         B     PCLOSE
QLEAVE   LM    0,9,QSTOREG                  RESTORE REGISTERS
         BR    WORK3                           AND RETURN
*        *************************************************************
*        **
*        ** THIS SECTION OF THE PROGRAM WILL BE LINKED TO WHENEVER
*        ** A DATA SET POINTER IS FOUND WITHIN THE CATALOG. THESE
*        ** POINTERS CAN BE OF TWO TYPES ---(1) A STANDARD DATA SET
*        ** POINTER WITH UP TO FIVE VOLUME ID.S AND(2) A VCB POINTER
*        **   THE ONLY PARAMETER PASSED TO THIS ROUTINE WILL BE THE
*        ** CONTENTS OF REGISTER BLOCPNT OR LOCPNT WHICH POINTS TO
*        ** THE CATALOG POINTER WHICH POINTS TO VOLUMES OR VCB'S.
*        **   THE NAME OF THE DATA SET BEING INVESTIGATED WILL BE
*        ** AT LOCATION 'INDXNAME'
*        **
*        *************************************************************
LPRDATA  ST    14,STOREG+28                  SAVE REGISTERS
         STM   3,9,STOREG
         SR    WORK3,WORK3                   CHECK TO SEE IF THIS IS
         IC    WORK3,11(LOCPNT)                A VCB POINTER
         LA    WORK4,31
         NR    WORK3,WORK4                  STRIP HI 3 BITS
         LA    WORK4,1
         CR    WORK3,WORK4
         BE    QVCBPRNT                     IF VCB PTR ----
         B     QDSPRNT                      IF NON VCB PTR ----
QEND     ST    WORK6,LOCIN
         LA    PARAREG1,LOCIN               LOAD THE ADDR OF THE TTR
         BAL   RETURN,DOPOINT                 BRANCH TO POINT ROUTINE
         BAL   RETURN,QCATREAD
QEND3    LM    3,9,STOREG                   RESTORE REG
         L     14,STOREG+28
         BR    14                           RETURN
QDSPRNT  EQU   *                       GET CATLG INFO
         LA    WORK3,14(LOCPNT)             SET POINTER TO DEVICE PTR
QREDO    MVC   PRINT+50(6),4(WORK3)         MOVE VOL ID
         SR    WORK4,WORK4
         IC    WORK4,10(WORK3)              PUT FILE SEQ IN REG
         SLL   WORK4,8
         IC    WORK4,11(WORK3)
         CVD   WORK4,CONVERT            CONVERT AND MOVE FILE SEQ
         L     WORK4,CONVERT+4
         SRL   WORK4,4
         ST    WORK4,CONVERT+4
         UNPK  PRINT+60(7),CONVERT+2(7)     MOVE FILE SEQ TO PRINT
         MVI   PRINT+66,X'40'
         UNPK  CONVERT(5),0(3,WORK3)       UNPACK DEV TYPE
         UNPK  CONVERT+4(5),2(3,WORK3)
         TR    CONVERT(8),TRANSLAT          TRANSLATE TO HEX
         MVC   PRINT+70(8),CONVERT          MOVE DEV TYPE TO PRINT
         LA    WORK4,24
         SR    WORK2,WORK4
         BAL   RETURN,LINEPR                PRINT LINE
         CR    WORK2,WORK4                  CHECK FOR END OF VOL PTRS
         BL    QEND3
         LA    WORK3,12(WORK3)              UP PTR TO NEXT ENTRY
         B     QREDO
QVCBPRNT EQU   *                       GET BLOCK OF VCB'S
         NOTE  WORKIN                       GET TTR OF LAST READ RECORD
         LR    WORK6,PARAREG2               SAVE TTR OF NOTED RECORD
         MVC   LOCTTR(4),8(BLOCPNT)
         LA    PARAREG1,LOCTTR
         BAL   RETURN,DOPOINT
         BAL   RETURN,QCATREAD              READ BLOCK OF CATALOG
         B     QLOCFIND
QLOCFIND LH    WORK2,VCBIN              PUT COUNT OF VOLUME PTRS IN REG
         LA    WORK3,VCBIN+2            POINT REG TO VOLUME POINTER
QREDOING SR    WORK4,WORK4
         MVC   PRINT+50(6),4(WORK3)    MOVE VOL ID TO PRINT
         SR    WORK4,WORK4
         IC    WORK4,10(WORK3)
         SLL   WORK4,8                      PICK UP FILE SEQ
         IC    WORK4,11(WORK3)                  AND
         CVD   WORK4,CONVERT                CONVERT TO DEC
         L     WORK4,CONVERT+4
         SRL   WORK4,4
         ST    WORK4,CONVERT+4
         UNPK  PRINT+60(7),CONVERT+2(7)     MOVE TO PRINT AREA
         MVI   PRINT+66,X'40'
         UNPK  CONVERT(5),0(3,WORK3)
         UNPK  CONVERT+4(5),2(3,WORK3)      PUT DEV CODE IN HEX FORM AN
         TR    CONVERT(8),TRANSLAT             MOVE IT TO PRINE AREA
         MVC   PRINT+70(8),CONVERT
         LA    WORK4,1
         SR    WORK2,WORK4
         BAL   RETURN,LINEPR                PRINT LINE
         LTR   WORK2,WORK2                  CHECK FOR END OF VCB
         BZ    QENDVCB
         LA    WORK4,VCBIN+230
         CR    WORK4,WORK3
         BE    QENDVCB
         LA    WORK3,12(WORK3)
         B     QREDOING
QENDVCB  CLC   VCBIN+252(3),CLEAR
         BE    QEND
         MVC   LOCIN(4),VCBIN+252       MOVE TTR OF NEXT VCB
         LA    PARAREG1,LOCIN               LOAD THE ADDR OF THE TTR
         BAL   RETURN,DOPOINT                 BRANCH TO POINT ROUTINE
         BAL   RETURN,QCATREAD              READ NEXT VCB
         B     QLOCFIND
**********************************************************************
**
**        THIS SECTION HANDLES PRINTING OF GENERAL CATALOG
**        INFORMATION FOR CVOL AND ALIAS POINTERS
**
**********************************************************************
**
QPRGEN   LA    WORK3,16                 CHECK ENTRY TYPE           UC0H
         CR    WORK2,WORK3
         BE    QALIAS                   BRANCH IF ALIAS POINTER    UC0H
QCVOL    LA    MSGFROM,120
         LA    MSGTO,PRINT+26
         BAL   MSGRET,MVMSG                 MOVE CVOL MSG TO PRINT
         MVC   PRINT+1(8),0(BLOCPNT)    MOVE INDEX NAME TO PRINT   UC0H
         LA    WORK3,20                                            UC0H
         CR    WORK2,WORK3              IS THIS A NEW CVOL POINTER UC0H
         BE    QNEWCVOL                 YES, BRANCH                UC0H
         MVC   PRINT+50(6),12(BLOCPNT)  NO, MOVE VOL ID TO PRINT   UC0H
         B     QGENPR
QNEWCVOL LA    WORK3,12(BLOCPNT)        GET POINTER TO DEVICE TYPE UC0H
         UNPK  CONVERT(5),0(3,WORK3)    UNPACK DEVICE TYPE         UC0H
         UNPK  CONVERT+4(5),2(3,WORK3)                             UC0H
         TR    CONVERT(8),TRANSLAT      TRANSLATE TO HEX           UC0H
         MVC   PRINT+70(8),CONVERT      MOVE DEV TYPE TO PRINT     UC0H
         MVC   PRINT+50(6),16(BLOCPNT)  MOVE VOL ID TO PRINT       UC0H
         B     QGENPR                                              UC0H
QALIAS   LA    MSGFROM,124
         LA    MSGTO,PRINT+26               MOVE ALIAS MSG TO PRINT
         BAL   MSGRET,MVMSG
         MVC   PRINT+1(8),0(BLOCPNT)
         MVC   PRINT+80(8),12(BLOCPNT)
QGENPR   BAL   RETURN,LINEPR                PRINT LINE
         B     QSCAN1
**********************************************************************
**
**       THIS SECTION CONTAINS THE READ AND CHECK MACROS
**        FOR READING THE CATALOG
**
**********************************************************************
**
QCATREAD ST    RETURN,STORRET               SAVE REG
         LA    SAVEREG,SAVEAREA
         READ  CATDECB,SF,WORKIN,CATIN-8       READ CATALOG
         CHECK CATDECB
         L     RETURN,STORRET               RESTORE REG
         BR    14                             AND RETURN
LOCLIST2 CAMLST NAME,DSCBIN,VOLID,LOCIN
LOCINEND DC    A(LOCIN+244)
LOCTABAD DC    A(LOCTAB)
CATINAD  DC    A(CATIN)
CATEND   DC    A(CATIN+244)
ENTRYCNT DC    F'0'
QENTRCNT DC    F'0'
LOCTTR   DC    F'0'
KEEPTTR  DC    F'0'
**********************************************************************
**
**                           IEHLISTU
**
**********************************************************************
IEHLISTU CSECT
***********************************************************************
**
**       HERE IT IS DECIDED WEATHER AN ENTIRE OR PARTIALPRINTOUT
**       OF THE VTOC HAS BEEN REQUESTED
**
***********************************************************************
**
         BALR  BASEREG2,0                   ESTABLISH ARRRESSABILITY
         USING *,BASEREG2
         TM    SW,DSNAMESW                   TEST IF ENTIRE VTOC PRINT
         BO    RPARTIAL                     IF NOT ------
         B     RENTIRE                      IF SO -----
***********************************************************************
**
**       REQUESTS FOR PARTIAL VTOC PRINTOUTS ARE HANDLED HERE
**
***********************************************************************
**
RPARTIAL LA    TABPNT,TABLE                 SET REG TO START OF
ROBTAIN  MVC   OBTAININ(44),0(TABPNT)       DSNAME TABLE  MOVE NAME
         LA    SAVEREG,DSCBIN
         OBTAIN OBLIST1                     OBTAIN DSCB
         B     RTEST(ECREG)
RTEST    B     RLINK1                        OK RETURN
         B     RERRD                         VOL NOT MOUNTED
         B     RERRD                         DSCB NOT FOUND
         B     WORKERR                      PERM I/O ERROR
RERRD    LA    MSGFROM,12
         LA    MSGTO,0
         CLI   RETCODE,X'08'           IS HIGHER RETURN CODE SET?
         BH    *+8                     BRANCH IF YES TO LET IT STAND
         MVI   RETCODE,X'08'           SET RETURN CODE TO 8
         BAL   MSGRET,MVMSG                 MOVE ERROR MSG TO PRINT
         MVC   0(44,15),0(TABPNT)           MOVE DSNAME TO PRINT
         BAL   RETURN,LINEPR       GO TO PRINT A LINE
         B     RBACK
RLINK1   BAL   RETURN,RPRDSCB                LINK TO PRINT DSCB
         CLC   ZEROS(5),DSCBIN+91            POINTER TO FMT 2 OR 3 VF0I
         BE    RBACK                                               VF0I
         TM    SW,DUMPSW                     TEST FOR DUMP OPTION  VF0I
         BZ    RBACK                                               VF0I
         MVC   TRACK(5),DSCBIN+91            LOAD CCHHR INTO TRACK VF0I
         OBTAIN OBLIST3                SEEK FORMAT 2 OR 3 DSCB     VF0I
         B     DFMERR2(ECREG)                                      VF0I
DFMERR2  B     DPRFMT2                                             VF0I
         B     RERRD                                               VF0I
         B     RERRD                                               VF0I
         B     WORKERR                                             VF0I
DPRFMT2  MVC   READIN(140),HOLDAREA    MOVE DSCB TO READIN AREA    VF0I
         MVC   DSCBIN+96(5),TRACK       ADDR OF THIS DSCB TO INPUT VF0I
         B     RLINK1                        PRINT  DSCB           VF0I
RBACK    LA    TABPNT,44(TABPNT)             UPDATE TABLE POINTER
         CR    TABPNT,TABEND                 COMPARE FOR TABLE END
         BNL   PCLOSE                       IF DONE ----
         CLC   0(44,TABPNT),0(TABEND)       CHECK IF NO MORE NAMES
         BE    PCLOSE                       IF SO ------
         B    ROBTAIN                        IF NOT RETURN TO OBTAIN
***********************************************************************
**
**       REQUESTS FOR ENTIRE VTOC PRINTOUTS ANE HANDLED HERE
**
***********************************************************************
**
RENTIRE  EQU   *                       SET UP JFCB FOR OPEN
         MVI   MODLJFCB,X'04'
         MVC   MODLJFCB+1(43),MODLJFCB
         MVI   MODLJFCB+44,X'40'
         MVI   MODLJFCB+IND2,SHR            SET SHARE BIT       YM06989
         MVI   MODLJFCB+117,X'01'
         MVC   MODLJFCB+118(6),VOLID
         MVC   DCBBLKSI(2),SETBLK1          SET BLOCKSIZE IN DCB
         MVC   KEYLE(1),SETKEY2             SET KEYLEN IN DCB
         MVC   DCBEODAD(3),SETEO2           SET EODAD ADDR IN DCB
         OI    SW,OPENSW                    SET OPEN SWITCH ON
         OPEN  (WORKIN),TYPE=J              OPEN DCB FOR INPUT
         L     WORK2,WORKIN+44         GET ADDR OF DEB           A25709
         L     WORK2,32(WORK2)         GET UCB ADDR              A25709
         USING UCBD,WORK2                                        A25709
*        CLI   UCBTBYT4,X'05'          IS DEVICE 2321           YL026VC
*        BC    7,NOT2321A         NO,GO CHECK TTR OF VTOC START YL026VC
*SUBUCBA CLC   VOLID(6),DCELVOLI  YES, FIND SUB UCB FOR THIS VOLYL026VC
*        BE    THISUCBA           THIS IS THE SUB UCB, CHECK TTRYL026VC
*        LA    WORK2,16(WORK2)    POINT TO NEXT 2321 SUB UCB    YL026VC
*        BNE   SUBUCBA            NOT THIS SUB UCB, CHECK NEXT  YL026VC
*THISUCBACLI   DCELVTOC+2,X'01'   DOES VTOC START ON RECORD ONE YL026VC
*        BE    RGOREAD            YES, GO DO READ, BYPASS POINT YL026VC
*        MVC   FORM5TTR(3),DCELVTOC    NO, TTR OF VTOC FOR POINTYL026VC
*        B     SETUPA                  GO SET UP TO DO POINT    YL026VC
NOT2321A CLI   SRTEFSCT+2,X'01'   VTOC START ON OTHER THAN RCD 1 A27506
         BE    RGOREAD                 NO, PROCEED NORMALLY      A25709
         MVC   FORM5TTR(3),SRTEFSCT    YES                       A25709
SETUPA   LA    PARAREG1,FORM5TTR  SET UP ADDR OF TTR FOR POINT   A27506
         BAL   RETURN,DOPOINT          GO TO ISSUE POINT         A25709
         DROP  WORK2                                             A25709
RGOREAD  BAL   WORK2,RREAD              LINK TO THE READ MACRO
         MVC   TABLE(44),READIN
         CLI   READIN+44,FORMAT4        CHECK THE DSCB FOR FORMAT 4
         BE    RFORM4
         CLI   READIN+44,FORMAT5        CHECK FOR FORMAT 5
         BE    RFORM5
RCHKDUMP TM    SW,DUMPSW                TEST FOR DUMP SWITCH ON
         BO    RLINK
         CLI   READIN+44,FORMAT1        CHECK FOR FROMAT 1
         BE    RLINK
         B     RGOREAD                  RETURN TO WHERE WE LINK TO READ
RFORM4   TM    SW,DUMPSW                TEST FOR DUMP SWITCH OFF
         BZ    RSPACE
         BAL   RETURN,RPRDSCB               GO TO PRINT DSCB
         B     RSPACE
RSPACE   MVC   VOIDDSCB+2(2),READIN+50  SAVE THE NUMBER OF EMPTY DSCBS
         B     RGOREAD
RFORM5   TM    SW,FIRST5                    IS THIS THE FIRST FORMAT 5
         BO    RCHKDUMP                     IF SO   -----
         NOTE  WORKIN                       GET TTR OF LAST READ RECORD
         ST    PARAREG2,FORM5TTR            STORE NOTED TTR
         OI    SW,FIRST5                    TURN ON FIRST FORMAT 5 SW
         B     RCHKDUMP
RLINK    CLI   READIN+44,FORMAT1            CHECK FOR FORMAT ZERO
         BL    RGOREAD                      IF SO ---
         BAL   RETURN,RPRDSCB               IF NOT -- LINK TO PRINT
         B     RGOREAD
**********************************************************************
**
**       THIS SECTION CALCULATES AND PRINTS THE SPACE INFORMATION
**       ABOUT THE VTOC AND VOLUME
**
**********************************************************************
**
REODAD   NI    WORKIN+44,X'00'              RESET ERROR BITS IN DCB
         LA    PARAREG1,FORM5TTR            LOAD THE ADDR OF THE TTR
         BAL   RETURN,DOPOINT                 BRANCH TO POINT ROUTINE
RREAD5   BAL   WORK2,RREAD
         SR    WORK5,WORK5              CLEAR TRACK COUNT REG
         SR    WORK4,WORK4              CLEAR CYLINDER COUNT REG
RSCAN    LA    WORK3,READIN+4
RADDCYL  MVC   CONVERT(8),CLEAR         CLEAR CONVERT
         MVC   CONVERT+7(1),4(WORK3)        READY SP ACE INFO FOR SUMMA
         A     WORK4,CONVERT+4
         MVC   CONVERT+6(2),2(WORK3)
         A     WORK5,CONVERT+4          ADD UP TRACK COUNT
         LA    WORK3,5(WORK3)           UP DSCB POINTER
         C     WORK3,RDPOS45            CHECK IF AT POSITION 45
         BE    RADD                     IF SO GO TO RADD
RCHKEND  C     WORK3,RDPOS136           CHECK IF AT POSITION 136
         BE    RENDDSCB
         B     RADDCYL
RADD     LA    WORK3,1(WORK3)           SKIP OVER FORMAT 5 ID
         B     RCHKEND
RENDDSCB CLC   CLEAR(5),0(WORK3)        CHECK FOR CHAIN ADDRESS
         BE    RFINAL                   IF NONE GO TO
         IC    WORK2,4(WORK3)          PICK UP 'R' OF CHAIN ADDR
         BCTR  WORK2,0                 DECREMENT BY ONE
         STC   WORK2,4(WORK3)          RESTORE IN CHAIN AADR
         MVC   WORKIN+8(5),0(WORK3)    MOVE CHAIN ADDR TO DCB
         BAL   WORK2,RREAD              READ NEXT DSCB
         B     RSCAN
RFINAL   EQU   *                       PRINT STATISTICS
         LA    MSGFROM,48
         LA    MSGTO,0                      MOVE SPACE MSG TO PRINT
         BAL   MSGRET,MVMSG
         CVD   WORK4,CONVERT
         UNPK PRINT+37(5),CONVERT+5(3) MOVE AVAIL TRKS TO PRNT @ZA28980
         OI    PRINT+41,X'F0'                                  @ZA28980
         CVD   WORK5,CONVERT
         UNPK PRINT+11(4),CONVERT+5(3) MOVE AVAIL CYL.             6547
         OI    PRINT+14,X'F0'
         BAL   RETURN,LINEPR                GO TO PRINT LINE
         LA    MSGFROM,52
         LA    MSGTO,0                      MOVE EMPTY DSCB MSG TO PRIN
         BAL   MSGRET,MVMSG
         LH    WORK3,VOIDDSCB+2
         CVD   WORK3,CONVERT
         UNPK  PRINT+11(5),CONVERT+5(3) MOVE NO. OF EMPTY DSCB @ZA28980
         OI    PRINT+15,X'F0'                                  @ZA28980
         BAL   RETURN,LINEPR                GO TO PRINT LINE
         B     PCLOSE
***********************************************************************
**
**       THIS ROUTINE WILL FORMAT AND PRINT THE DSCBS THAT HAVE BEEN
**       READ BY THE ENTIRE OR PARTIAL VTOC DEFLONK ROUTINES
**
***********************************************************************
**
RPRDSCB  SR    WORK2,WORK2
         ST    RETURN,SAVERET               SAVE RETURN ADDR
         TM    SW,DUMPSW                     TEST FOR DUMP OPTION  VF0I
         BO    RHEX                                                VF0I
         IC    WORK2,DSCBIN+12               LOAD BINARY YEAR
         CVD   WORK2,CONVERT
         L     WORK2,CONVERT+4              AND SAVE
         SRL   WORK2,4
         ST    WORK2,CONVERT+4
         MVC   DSCBDATE+4(1),CONVERT+7
         SR    WORK2,WORK2                   CLEAR WORK2
         IC    WORK2,DSCBIN+13               LOAD BINARY DAY
         SLL   WORK2,8
         IC    WORK2,DSCBIN+14
         CVD   WORK2,CONVERT                CONVERT TO DECIMAL
         L     WORK2,CONVERT+4              AND SAVE
         SLL   WORK2,4
         ST    WORK2,CONVERT+4
         MVC   CONVERT+7(1),DSCBDATE+4      COMBINE YEAR AND DAY
         UNPK  DSCBDATE(6),CONVERT+3(6)     UNPACK AND SAVE
         TM    SW,DATESW                    TEST FOT DATE OPTION
         BO    RTSTDATE                     IF SO ------
         B     RDUMP
RTSTDATE MVC   PRINT(2),DSCBDATE+3          MOVE SPEC DATE
         MVC   PRINT+2(3),DSCBDATE
         MVC   PRINT+10(2),DATE+3           MOVE DATE IN DSCB
         MVC   PRINT+12(3),DATE
         CLC   PRINT(5),PRINT+10            COMPARE DATES
         BL    RFLAG                        IF PURGE PAST -----
         B     RDUMP
RFLAG    MVI   PRINT+62,C'*'              MOVE FLAG TO PRINT      10569
RDUMP    MVC   PRINT(44),0(TABPNT)           MOVE NAME TO PRINT    VF0I
         B     REDIT
RHEX     CLI   READIN+44,FORMAT1            TEST FOR FORMAT ONE
         BNE   ROTHER                       IF NOT ---
         MVC   PRINT(44),0(TABPNT)          IF SO MOVE NAME TO PRINT
         B     RHEX3
ROTHER   LA    MSGFROM,72                   MOVE FORMAT MSG TO PRINT
         LA    MSGTO,0
         BAL   MSGRET,MVMSG
         MVC   PRINT+8(1),READIN+44         MOVE FORMAT I.D.
         B     RHEX2
RHEX2    LA    UNPKPR,PRINT+32
         LA    UNPKDSCB,READIN
RUNPK0   UNPK  0(13,UNPKPR),0(7,UNPKDSCB)   VIA THE UNPACK ANDTRANSLATE
         LA    UNPKPR,12(UNPKPR)              INSTRUCTIONS PUT KEY IN
         LA    UNPKDSCB,6(UNPKDSCB)         HEX FORM AND PRINT
         C     UNPKPR,PRINTEND
         BL    RUNPK0
         TR    PRINT+32(88),TRANSLAT
         B     RHEX3
RHEX3    BAL   RETURN,LINEPR
         LA    UNPKPR,PRINT+10
         LA    UNPKDSCB,DSCBIN
RUNPK1   UNPK  0(13,UNPKPR),0(7,UNPKDSCB)   VIA THE UNPACK AND TRANSLAT
         LA    UNPKPR,12(UNPKPR)              INSTRUCTIONS PUT THE FIRS
         LA    UNPKDSCB,6(UNPKDSCB)           PORTION OF THE DSCB IN
         C     UNPKPR,PRINTEND                HEX AND PRINT
         BL    RUNPK1
         TR    PRINT+10(110),TRANSLAT
         BAL   RETURN,LINEPR       GO TO PRINT A LINE
         LA    UNPKPR,PRINT+10
         LA    UNPKDSCB,DSCBIN+55
RUNPK2   UNPK  0(13,UNPKPR),0(7,UNPKDSCB)   DO SAME FOR LAST PART
         LA    UNPKPR,12(UNPKPR)              OF DSCB
         LA    UNPKDSCB,6(UNPKDSCB)
         C     UNPKPR,PRINTEND
         BL    RUNPK2
         TR    PRINT+10(82),TRANSLAT
         MVI   PRINT+92,X'00'
         MVC   PRINT+93(28),PRINT+92
         UNPK  PRINT+103(11),WORKIN+8(6)    MOVE DSCB ADDR TO PRINT2213
         TM    SW,DSNAMESW                  TEST FOR PARTIAL VTOC  7456
         BO    PARTIAL                      OBTAIN MACRO HAS DSCB  7456
         B     ENTIRE                                              7456
PARTIAL  UNPK  PRINT+103(11),DSCBIN+96(6)   MOVE DSCB ADDRESS      7456
ENTIRE   TR    PRINT+103(11),TRANSLAT                              7456
         MVI   PRINT+113,X'00'
         BAL   RETURN,LINEPR       GO TO PRINT A LINE
         L     RETURN,SAVERET               LOAD RETURN ADDR
         BR    RETURN                       RETURN
REDIT    MVC   PRINT+55(5),DSCBDATE
         SR    WORK2,WORK2
         IC    WORK2,DSCBIN+9               LOAD CREATION YEAR
         CVD   WORK2,CONVERT                 CONVERT TO DECIMAL
         L     WORK2,CONVERT+4
         SRL   WORK2,4
         ST    WORK2,CONVERT+4
         MVC   PRINT+51(1),CONVERT+7        MOVE YEAR TO PRINT
         SR    WORK2,WORK2
         IC    WORK2,DSCBIN+10              LOAD CREATION DAY
         SLL   WORK2,8
         IC    WORK2,DSCBIN+11
         CVD   WORK2,CONVERT                 CONVERT TO DECIMAL
         L     WORK2,CONVERT+4
         SLL   WORK2,4
         ST    WORK2,CONVERT+4
         MVC   CONVERT+7(1),PRINT+51        MOVE DAY TO PRINT
         UNPK  PRINT+47(6),CONVERT+3(6)
         MVI   PRINT+52,X'40'
         SR    WORK2,WORK2
         IC    WORK2,DSCBIN+15              LOAD NO. OF EXTENTS
         CVD   WORK2,CONVERT                 CONVERT TO DECIMAL
         UNPK  PRINT+78(5),CONVERT+3(5)     PUT IN PRINT AREA
         OI     PRINT+82,X'F0'
         MVC   PRINT+88(6),DSCBIN+1         MOVE FILE SERIAL TO
         SR    WORK2,WORK2                     PRINT
         IC    WORK2,DSCBIN+7               LOAD VOL SEQ
         SLL   WORK2,8
         IC    WORK2,DSCBIN+8
         CVD   WORK2,CONVERT                 CONVERT TO DECIMAL
         UNPK  PRINT+101(5),CONVERT+3(5)    MOVE VOL SEQ TO PRINT
         OI    PRINT+105,X'F0'
         LA    MSGTO,PRINT+110              TARGET             @Z40RSSA
         LA    MSGFROM,IBOTH                RACF/PWD           @Z40RSSA
         TM    DSCBIN+49,SECURITY           TEST SECURITY      @Z40RSSA
         BO    MSMSG                        RACF AND PASSWORD PROTECTED
         LA    MSGFROM,INONE                NONE               @Z40RSSA
         BZ    MSMSG                        NO PROTECTION      @Z40RSSA
*        RACF OR PASSWORD PROTECTED (NOT BOTH)                 @Z40RSSA
         LA    MSGFROM,IPASS                PWD                @Z40RSSA
         TM    DSCBIN+49,PSWD               PSWD BIT ONLY      @Z40RSSA
         BO    MSMSG                        PWD                @Z40RSSA
         LA    MSGFROM,IRACF                RACF               @Z40RSSA
MSMSG    BAL   MSGRET,MVMSG                                    @Z40RSSA
         B     RTYPE
RTYPE    TM    DSCBIN+38,TYPESAM             TEST FILE TYPE
         BO    RSAM                         = SAM
         TM    DSCBIN+38,TYPEISAM
         BO    RISAM                        = ISAM
         TM    DSCBIN+38,TYPEDAM
         BO    RDAM                         = DAM
         TM    DSCBIN+38,TYPEPDS
         BO    RPDS                         = PDS
         CLC   DSCBIN+38(2),TYPEVSAM                            YL026VC
         BE    RVSAM                        = VSAM              YL026VC
         B     RNOT                         = NOT DEFINED
RSAM     LA    MSGFROM,84
         B     RPRINT
RISAM    LA    MSGFROM,88
         B     RPRINT
RDAM     LA    MSGFROM,92
         B     RPRINT
RPDS     LA    MSGFROM,96
         B     RPRINT
RVSAM    LA    MSGFROM,332                                      YL026VC
         B     RPRINT                                           YL026VC
RNOT     LA    MSGFROM,100
RPRINT   LA    MSGTO,PRINT+64
         BAL   MSGRET,MVMSG
         BAL   RETURN,LINEPR
         L     RETURN,SAVERET
         BR    RETURN
**********************************************************************
**
**       THIS SECTION CONTAINS THE READ AND CHECK MACROS USED TO
**       READ THE VTOC
**
**********************************************************************
**
RREAD    EQU   *                      READ
         LA    SAVEREG,SAVEAREA             LOAD SAVE AREA ADDR
         READ  DSCBDECB,SF,WORKIN,READIN
         CHECK DSCBDECB
         OI    READIN+44,X'F0'
         BR    WORK2                        RETURN
SAVERET  DC    F'0'
DSCBDATE DS    6X
UCBD     DSECT ,                                                 A25709
         IEFUCBOB  ,                                             A25709
**********************************************************************
**
**                           IEHLISTS
**
**********************************************************************
IEHLISTS CSECT
***********************************************************************
**
**       THIS ROUTINE HANDLES ALL REQUESTS FOR PDS PRINTOUTS
**
***********************************************************************
**
         BALR  BASEREG2,0                   ESTABLISH ADDRESSABILITY
         USING *,BASEREG2
         USING PDS,BLOCPNT
         L     TABPNT,TABAD                 PUT ADDR OF NAME IN REG
SRESTART MVC   OBTAININ(44),0(TABPNT)       MOVE NAME TO WORK AREA
         OBTAIN  OBLIST1                    OBTAIN DSCB
         B     SBRANCH(ECREG)                CHECK RETURN FROM OBTAIN
SBRANCH  B     SCHKPDS                       IF OK RETURN
         B     SERRD                         IF VOL NOT MOUNTED
         B     SERRD                         IF NO SUCH DSNAME
         B     WORKERR                      PERM I/O ERROR
SCHKPDS  TM    DSCBIN+38,X'02'    CHECK IF PDS
         BZ    SERRH      IF NOT A PDS
         MVC   MODLJFCB(44),0(TABPNT)       PREPARE JFCB FOR OPEN
         MVI   MODLJFCB+44,X'40'
         MVI   MODLJFCB+IND2,SHR            SET SHARE BIT       YM06989
         MVI   MODLJFCB+117,X'01'
         MVC   MODLJFCB+118(6),VOLID
         MVC   DCBBLKSI(2),SETBLK2          SET BLOCKSIZE IN DCB
         MVC   KEYLE(1),SETKEY1             SET KEYLEN IN DCB
         MVC   DCBEODAD(3),SETEO3       SET EODAD ADDR IN DCB
         MVI   SRDSW,X'00'              RESET LAST MEMBER NAME PASSED
         SR    RECCNT,RECCNT            ZERO OUT RECORD COUNTER
         OI    SW,OPENSW                    TURN ON OPEN SWITCH
         OPEN  (WORKIN),TYPE=J              OPEN DCB FOR INPUT
         TM    WORKIN+48,X'10'         IS DCB OPEN?            @ZA11934
         BO    DSOPENOK                YES,GO ON               @ZA11934
         NI    SW,X'EF'                NO,RESET OPEN SW        @ZA11934
         LA    MSGFROM,352             ISSUE ERRORMESSAGE      @ZA11934
         B     OPENERR                 GO ISSUE MSG NOTOPEN    @ZA11934
DSOPENOK MVC   PRINT(44),0(TABPNT)     MOVE DSNAME TO PRINT    @ZA11934
         BAL   RETURN,LINEPR                GO TO PRINT
         TM    FMT,X'01'          IS IT PDS FORMAT OPTION          I282
         BZ    DMPOPT             NO, GO PRINT DUMP SUB HEADING    I282
         MVI   PDSFMTSW,X'C0'     YES, SET FMT OPTION, FIRST ENTRY I282
         B     CONTINUE           BY-PASS PRINT OF DUMP HEADING    I282
DMPOPT   MVI   ZSW,X'02'          SPACE BEFORE PRINT SUB HEADING   I282
         LA    MSGTO,0
         LA    MSGFROM,116                  MOVE SUB HEAD TO PRINT
         BAL   MSGRET,MVMSG
         BAL   RETURN,LINEPR                PRINT SUB HEAD
CONTINUE SR    WORK2,WORK2                                         I282
         MVC   PDSSPACE(1),DSCBIN+16        SAVE LAST BLK SPACE INFO
SREAD1   SR    TOBE,TOBE
SREAD    EQU   *                      READ PDS
         READ  PDSDECB,SF,WORKIN,OBTAININ        READ PDS
         CHECK PDSDECB
         LA    RECCNT,1(RECCNT)         BUMP RECORD COUNTER
         CLI   SRDSW,X'00'              HAS LAST MEMBER BEEN PRINTED
         BE    TESTOPT            NO, GO TEST OPTION REQUESTED     I282
         LA    EMPTYCNT,1(EMPTYCNT)     BUMP ENPTY RECORD CNT BY ONE
         B     SREAD
SERRD    LA    MSGFROM,12
         LA    MSGTO,0
         CLI   RETCODE,X'08'           IS HIGHER RETURN CODE SET?
         BH    *+8                     BRANCH IF YES TO LET IT STAND
         MVI   RETCODE,X'08'           SET RETURN CODE TO 8
         BAL   MSGRET,MVMSG                 MOVE NO DATA SET FOUND ERR
         MVC   0(44,15),0(TABPNT)           MOVE DSNAME
         BAL   RETURN,LINEPR                GO TO PRINT
         B     SUPDATE
SERRH    LA    MSGFROM,16                   MOVE ERROR MSG FOR DATA SET
OPENERR  LA    MSGTO,0                                         @ZA11934
         CLI   RETCODE,X'08'           IS HIGHER RETURN CODE SET?
         BH    *+8                     BRANCH IF YES TO LET IT STAND
         MVI   RETCODE,X'08'           SET RETURN CODE TO 8
         BAL   MSGRET,MVMSG
         MVC   0(44,15),0(TABPNT)      MOVE DSNAME TO PRINT
         BAL   RETURN,LINEPR                GO TO PRINT
         B     SUPDATE
TESTOPT  TM    PDSFMTSW,X'C0'     FORMAT OPTION REQUESTED          I282
         BM    SECNDENT           YES, NOT FIRST ENTRY             I282
         BO    FIRSTENT           YES, FIRST ENTRY                 I282
SSCAN    SR    TOBE,TOBE          NO, DUMP OPTION - CONTINUE       I282
         LH    TOBE,BLOCKIN  NO. OF USEFUL BYTES IN DIRECTORY BLK 11386
         LA    BLOCPNT,BLOCKIN              SET REG TO START SCAN
         LA    ELEMLEN,2                     LOAD REG WITH 2
SRESCAN  SR    TOBE,ELEMLEN                  SUB ELEMENT LEN. FROM TOBE
         AR    BLOCPNT,ELEMLEN               AND UP BLOCKPNT
         SR    WORK2,WORK2
         CR    WORK2,TOBE                   CHECK FOR END OF BLOCK
         BNL   SREAD1
         CLC   0(8,BLOCPNT),FF              CHECK FOR LAST RECORD
         BE    SDONE
TEMPDUMP ST    RETURN,SRETURN     SAVE RETURN REG                  I282
         MVC   PRINT+10(8),0(BLOCPNT) MOVE MEMBER NAME             I282
         UNPK  PRINT+20(9),8(5,BLOCPNT)     PUT TTRC IN PRINT AREA
         MVI   PRINT+28,X'40'
         TR    PRINT+20(8),TRANSLAT         PUT TTRC IN HEX
         SR    WORK2,WORK2
         IC    WORK2,11(BLOCPNT)             LOAD 'C' INTO REG
         SR    WORK3,WORK3
         IC    WORK3,STRIP                  STRIP HI 3 BITS
         NR    WORK2,WORK3
         AR    WORK2,WORK2                   DOUBLE COUNT OF HALFWORDS
         LTR   WORK2,WORK2                  CHECK FOR NO USER DATA
         BE    SNOUSE
         MVI   PRINT+30,X'EF'               PAD PRINT AREA FOR TRANSLAT
         MVC   PRINT+31(87),PRINT+30                               1264
         LR    WORK1,WORK2
         LA    WORK3,PRINT+30
         LA    ELEMLEN,12(BLOCPNT)
SUNPKPDS UNPK  0(11,WORK3),0(6,ELEMLEN)     UNPK USER DATA
         MVI   10(WORK3),X'EF'                 TO PRINT AREA
         LA    WORK3,11(WORK3)
         LA    ELEMLEN,5(ELEMLEN)
         S     WORK1,FIVE
         BZ    STRAN
         BM    STOFAR
         C     WORK3,PRPOS110               CHECK FOR END OF PRINT LINE
         BL    SUNPKPDS
STRAN    TR    PRINT+30(88),TRANSLAT     PUT IN HEX FORM           1264
         BAL   RETURN,LINEPR                GO TO PRINT
         LTR   WORK1,WORK1                  CHECK FOR DONE
         BZ    SNOUSE2
         BM    SNOUSE2
         LA    WORK3,PRINT+30               SET POINTER TO CONTINUE
          MVI   PRINT+30,X'EF'                                    42343
          MVC   PRINT+31(87),PRINT+30                             42343
         B     SUNPKPDS
SNOUSE   BAL   RETURN,LINEPR                PRINT NAME IF NO USER DATA
SNOUSE2  LA    ELEMLEN,12
         AR    ELEMLEN,WORK2                UP POINTER TO NEXT ENTRY
         TM    PDSFMTSW,X'80'     FORMAT WITH < 34 BYTES           I282
         BO    GOFORMAT           GO CONTINUE FORMAT OPTION        I282
         B     SRESCAN
GOFORMAT L     RETURN,SRETURN     RESTORE RETURN REG               I282
         BR    RETURN             CONTINUE FORMAT OPTION           I282
STOFAR   AR    WORK1,WORK1
         AR    WORK3,WORK1                  CHECK FOR UNPK TO FAR AND I
         BCTR  WORK3,0                      IF SO  BLANK OUT EXCESS
         MVI   0(WORK3),X'EF'
         LPR   WORK1,WORK1             LOAD OVERFLOW POSITIVE      6715
         BCTR  WORK1,0                                             6715
         EX    WORK1,CLEANUP           MOVE EF TO OVERFLOW         6715
         SR    WORK1,WORK1             INDICATE DONE               6715
         B     STRAN
CLEANUP  MVC   1(1,WORK3),0(WORK3)                                 6715
SFINAL   EQU   *                      CLOSE PDS
         MVI   ZSW,X'01'
         NI    SW,X'7F'                     RESET OPEN SWITCH
         CLOSE (WORKIN)                     CLOSE PDS
SUPDATE  LA    TABPNT,44(TABPNT)             UPDATE TABLE POINTER
         CR    TABPNT,TABEND                CHECK FOR END OF TABLE
         BE    PCLOSE                       IF SO ----
         CLC   0(44,TABPNT),0(TABEND)       CHECK FOR NO MORE NAMES
         BE    PCLOSE                       IF NONE -----
         B     SRESTART
SDONE    MVI   SRDSW,X'FF'              SET LAST MEMBER PASSED SW
         MVI   PDSFMTSW,X'00'     TURN PDS FORMAT SWITCH OFF       I282
         B     SREAD1
SEODAD  BAL    RETURN,LINEPR
         LA    MSGFROM,4
         LA    MSGTO,0
         BAL   MSGRET,MVMSG
         CVD   RECCNT,CONVERT
         UNPK  PRINT+7(5),CONVERT+5(3)  DIRECTORY BLOCKS ALLOCATED 9734
         OI    PRINT+11,X'F0'                                      9734
         CVD   EMPTYCNT,CONVERT
         UNPK  PRINT+53(5),CONVERT+5(3) DIRECTORY BLOCKS UNUSED    9734
         OI    PRINT+57,X'F0'                                      9734
         BAL   RETURN,LINEPR
         B     SFINAL
**
**********************************************************************
**
**    THIS SECTION OF CSECT IEHLISTS HANDLES LISTPDS FORMAT REQUESTS
**
**********************************************************************
**
*        THIS ROUTINE IS ENTERED AT THE BEGINNING OF EACH PDS.
*        A CHECK FOR PDS CREATED BY THE LINKAGE EDITOR IS MADE.
*        PDS FORMAT HEADINGS ARE PRINTED.
*
FIRSTENT NI    PDSFMTSW,X'BF'     YES, TURN OFF FIRST ENTRY SWITCH I282
         LA    MSGFROM,260        INDEX FOR FIRST HEADING LINE     I282
         LA    WORK3,260          INDEX IN WORK REGISTER           I282
         LA    WORK1,13           THIRTEEN LINES IN HEADING        I282
         LA    WORK2,SP+12        END ADDR OF CARR CNTRL CHAR CONS I282
HEADPRIN MVC   ZSW(1),0(WORK2)    NUMBER OF SPACES PIOR TO PRINT   I282
         LA    MSGTO,0            PDS FORMAT HEADING TO PRINTER    I282
         BAL   MSGRET,MVMSG       MOVE LINE TO PRINT BUFFER        I282
         BAL   RETURN,LINEPR      PRINT HEADING LINE               I282
         LA    MSGFROM,4(WORK3)   INDEX FOR NEXT HEADING LINE      I282
         LR    WORK3,MSGFROM      SAVE INDEX FOR NEXT HEADING      I282
         BCTR  WORK2,0            POINT TO NEXT CARR CNTRL CHAR    I282
         BCT   WORK1,HEADPRIN     PRINT THIRTEEN LINES             I282
         BAL   RETURN,LINEPR      SPACE BETWEEN COL HEAD,MEMBERS   I282
*
*        THIS  ROUTINE IS ENTERED AT THE BEGINNING OF EACH DIRECTORY
*        ENTRY.  CHECKS ARE MADE FOR AN ALIAS NAME AND THE POSSIBLE
*        NEED FOR A SECOND LINE.
*
SECNDENT LA    BLOCPNT,BLOCKIN    ADDR OF BEG OF DIRECTORY RECORD  I282
         LA    BLOCPNT,2(BLOCPNT) ADDR OF BEG OF FIRST ENTRY       I282
         LH    ELEMLEN,BLOCKIN    NO. OF USED BYTES IN DIR RCD     I282
         BCTR  ELEMLEN,0          NO. OF USED BYTES                I282
         BCTR  ELEMLEN,0                     MINUS COUNT FIELD     I282
FMTENTRY SR    WORK1,WORK1        CLEAR REGISTER FOR COMPARE       I282
         CR    WORK1,ELEMLEN      END OF DIRECTORY RECORD          I282
         BNL   SREAD              YES, GO READ ANOTHER DIR RCD     I282
         CLC   0(8,BLOCPNT),FF    LAST MEMBER BEEN PROCESSED       I282
         BE    SDONE         YES, GO CALCULATE END INFORMATON      I282
         CLC   ZLINECNT,ZLINEPER       IS IT END OF PAGE TIME     M5844
         BL    ONWARD             NO, GO FORMAT DIRECTORY ENTRY    I282
         LA    MSGTO,0            YES, PRINT FMT SUB HEADING       I282
         LA    MSGFROM,304        INDEX FOR FIRST FMT SUB HEADING  I282
         BAL   MSGRET,MVMSG       MOVE TO PRINT BUFFER             I282
         MVI   ZSW,X'01'          SET CARR CODE TO SKIP TO NEXT PP I282
         BAL   RETURN,LINEPR      PRINT FIRST FMT SUB HEADING LINE I282
         LA    MSGTO,0            SECOND LINE TO PRINT BUFFER      I282
         LA    MSGFROM,308        SECOND LINE INDEX FACTOR         I282
         BAL   MSGRET,MVMSG       MOVE TO PRINT BUFFER             I282
         BAL   RETURN,LINEPR      PRINT SECND FMT SUB HEADING LINE I282
         BAL   RETURN,LINEPR      SPACE BETWEEN COL HEAD,MEMBERS   I282
ONWARD   SR    WORK2,WORK2        CLEAR  REG FOR IC INSTRUCTION    I282
         IC    WORK2,11(BLOCPNT)  INSERT LENGTH BYTE               I282
         SR    WORK3,WORK3        CLEAR REG FOR IC INSTRUCTION     I282
         IC    WORK3,STRIP        INSERT AND FIELD FOR NR INSTR    I282
         NR    WORK2,WORK3        STRIP OFF NON LENGTH BITS        I282
         L     WORK3,ELEVEN       LOAD CONSTANT FOR CR INSTRUCTION I282
         CR    WORK2,WORK3       COMPARE FOR ENTRIES < 34 BYTES    I282
         BL    DOFMTDMP           DO DUMP OPTION FOR THIS ENTRY    I282
CONFMTOP TM    11(BLOCPNT),X'80'  NO, IS MEMBER AN ALIAS           I282
         BZ    MNAME              NO, GO PUT NAME IN PRINT LINE    I282
         OI    PDSFMTSW,ALIASMSK       TURN ON ALIAS BIT-AOS     XM4744
         TM    20(BLOCPNT),X'C0'  YES, IS MEMBER ALSO RENT OR REUS I282
         BZ    MASTERIC           NO, GO MOVE IN ASTERISK          I282
         TM    20(BLOCPNT),X'04'  ALSO SCATTER LOAD                I282
         BZ    SETBIT             NO, GO SET ALIAS AND RENT-REUS   I282
         OI    PDSFMTSW,X'08'     SET SCAT, ALIAS AND RENT OR REUS I282
SETBIT   OI    PDSFMTSW,X'20'     SET MEMBER ALIAS AND RENT-REUS   I282
MASTERIC MVI   PRINT+9,C'*'       REFLECT NAME IS AN ALIAS         I282
*
*        THIS ROUTINE BUILDS THE STANDARD FIELDS OF THE DIRECTORY
*        ENTRY IN THE PRINT LINE.
*
MNAME    MVC   PRINT+1(8),0(BLOCPNT)        MEMBER NAME            I282
         UNPK  PRINT+15(7),27(4,BLOCPNT)    ENTRY POINT            I282
         MVI   PRINT+21,X'EF'               EXTRA BYTE TR-BLANK    I282
         TR    PRINT+15(7),TRANSLAT         MAKE EP PRINTABLE      I282
         UNPK  PRINT+24(5),20(3,BLOCPNT)    ATTRIBUTES             I282
         MVI   PRINT+28,X'EF'               EXTRA BYTE TR-BLANK    I282
         TR    PRINT+24(5),TRANSLAT         MAKE ATTR PRINTABLE    I282
         UNPK  PRINT+31(7),8(4,BLOCPNT)     TTR OF BEG OF MEMBER   I282
         MVI   PRINT+37,X'EF'               EXTRA BYTE TR-BLANK    I282
         TR    PRINT+31(7),TRANSLAT         MAKE TTR PRINTABLE     I282
         UNPK  PRINT+39(7),12(4,BLOCPNT)    TTR OF FIRST BLOCK TXT I282
         MVI   PRINT+45,X'EF'               EXTRA BYTE TR-BLANK    I282
         TR    PRINT+39(7),TRANSLAT         MAKE TTR PRINTABLE     I282
         XC    CONVERT(8),CONVERT           SET D WORD TO ZEROES   I282
         MVC   CONVERT+1(3),22(BLOCPNT)     ALIGN CONTIG STORAGE   I282
         L     WORK1,CONVERT                INTO REG               I282
         CVD   WORK1,CONVERT                     AND CVD           I282
         UNPK  PRINT+48(8),CONVERT+4(4)     MAKE PRINTABLE         I282
         OI    PRINT+55,X'F0'                    INCLUDING SIGN    I282
         MVC   CONVERT+6(2),25(BLOCPNT)     ALIGN LEN FIRST TEXT   I282
         LH    WORK1,CONVERT+6              INTO REG               I282
         CVD   WORK1,CONVERT                     AND CVD           I282
         UNPK  PRINT+60(5),CONVERT+5(3)     MAKE PRINTABLE         I282
         OI    PRINT+64,X'F0'                    INCLUDING SIGN    I282
         UNPK  PRINT+69(7),30(4,BLOCPNT)    ORGIN FIRST TEXT BLOCK I282
         MVI   PRINT+75,X'EF'               EXTRA BYTE TR-BLANK    I282
         TR    PRINT+69(7),TRANSLAT         MAKE PRINTABLE         I282
*                                                                XM4744
*  CALCULATE THE LENGTH OF THE COMPLETE ENTRY                    XM4744
*                                                                XM4744
         NI    BITS,HWORDMSK           TURN HI 3 BITS OFF        XM4744
         SR    WORK3,WORK3             ZERO WORK3                XM4744
         IC    WORK3,BITS              NO OF HALFWORDS           XM4744
         AR    WORK3,WORK3             NO OF BYTES OF USER DATA  XM4744
         LA    WORK3,12(WORK3)         TOTAL LENGTH OF ENTRY     XM4744
*                                                                XM4744
*  CHECK FOR AOS L/E CREATED PDS BY CHECKING ORG-FIRST-TEXT      XM4744
*  FIELD.  IF AOS CREATED,THEN CHECK AOS ATTRIBUTES.             XM4744
*                                                                XM4744
         TM    30(BLOCPNT),AOSLE       AOS L/E CREATED?          XM4744
         BZ    NOTAOS                  NOT AOS,BRANCH            XM4744
         MVC   PRINT+89(6),PRINT+69    MOVE AOS ATTR TO NEW FLD  XM4744
         MVI   PRINT+69,BLANK          BLANK ORG-FST-TXT         XM4744
         MVC   PRINT+70(5),PRINT+69     FIELD                    XM4744
*                                                                XM4744
*  LENGTH CALCULATIONS FOR AOS PDS                               XM4744
*                                                                XM4744
         LA    WORK1,END-BEG(BLOCPNT)  MIN PDS LENGTH            XM4744
         TM    PDSFMTSW,ALIASMSK       IS MEMBER AN ALIAS?       XM4744
         BZ    NOMEM                   NO,BRANCH                 XM4744
         LA    WORK1,10(WORK1)         ADD LENGTH MEM+TTR        XM4744
NOMEM    TM    ATTR,SCATBIT            IS MEMBER SCATTER LOADED? XM4744
         BZ    NOTSCTR                 NO,BRANCH TO CHECK SSI    XM4744
         LA    WORK1,8(WORK1)          ADD SCTR INFO LENGTH      XM4744
         OI    PDSFMTSW,SCATMSK        SET SCTR LOADED BIT       XM4744
*                                                                XM4744
*  CHECK FOR SSI                                                 XM4744
*                                                                XM4744
NOTSCTR  TM    AOSATTR,SSIBIT          SSI PRESENT?              XM4744
         BZ    NOSSIH                  NO,CONTINUE               XM4744
         UNPK  PRINT+79(9),0(5,WORK1)  SSI TO PRINT LINE         XM4744
         MVI   PRINT+87,TRBLANK        MAKE BLANK                XM4744
         TR    PRINT+79(9),TRANSLAT    MAKE SSI PRINTABLE        XM4744
         LA    WORK1,4(WORK1)          FOR APF TEST              XM4744
         B     APF                     TEST FOR APF              XM4744
NOSSIH   LA    MSGTO,PRINT+79          MOVE ABSENT IN            XM4744
         LA    MSGFROM,316             MOVE ABSENT               XM4744
         BAL   MSGRET,MVMSG            TO PRINT                  XM4744
APF      TM    AOSATTR,APFBIT          APF PRESENT?              XM4744
         BZ    OTHERINF                NO,BRANCH                 XM4744
*                                                                YM2247
* CHECK FOR SSI & ALIAS - WHEN NOT PRESENT REPOSITION POINTER TO APF
*
         TM    AOSATTR,SSIBIT          SSI PRESENT?              YM2247
         BO    LECNT                   YES, BRANCH               YM2247
         TM    PDSFMTSW,ALIASMSK       ALIAS?                    YM2247
         BO    LECNT                   YES,BRANCH                YM2247
         SH    WORK1,DEADBYTE          SUBT 1 BYTE TO REPOSITION YM2247
LECNT    CLI   0(WORK1),SIZEBIT        IS IT CORRECT SIZE?       YM2247
         BL    OTHERINF                NO, GET OUT             @ZA28588
         CLI   1(WORK1),SIZEBIT        AUTH REQD?              @ZA28588
         BL    NOAPF                   PRINT 'NO'              @ZA28588
         MVC   PRINT+97(3),YES         PRINT 'YES'               XM4744
         B     OTHERINF                ANYTHING ELSE?            XM4744
NOAPF    MVC   PRINT+97(2),NO          PRINT 'NO'                XM4744
         B     OTHERINF                                          YM2247
*
*        THIS ROUTINE CREATES THE SSI INFO FIELD IN THE PRINT LINE.
*
NOTAOS   NI    PDSFMTSW,OSBIT          TURN OFF ALIAS BIT - AOS YA00062
         LA    WORK1,END-BEG           MINIMUM PDS ENTRY LENGTH  XM4744
         TM    PDSFMTSW,X'20'     IS MEMBER ALIAS AND RENT OR REUS I282
         BZ    NOTARR             NO, BYPASS ADDITION OF THAT LEN  I282
         LA    WORK1,10(WORK1)    YES, ADD ALIAS, RENT/REUS LENGTH I282
NOTARR   TM    20(BLOCPNT),X'04'  IS MEMBER SCATTER LOADED         I282
         BZ    NOTSCATR           NO, BYPASS ADDITION OF THAT LEN  I282
         LA    WORK1,8(WORK1)     YES, ADD SCATTER INFO LEN        I282
         OI    PDSFMTSW,X'10'     SET SCATTER LOADED BIT           I282
NOTSCATR CR    WORK1,WORK3             IS SSI PRESENT            XM4744
         BE    NOSSI              NO, GO PUT ABSENT IN PRINT LINE  I282
         LA    WORK1,0(WORK1,BLOCPNT)       ADDRESS OF SSI         I282
         UNPK  PRINT+79(9),0(5,WORK1)       SSI TO PRINT LINE      I282
         MVI   PRINT+87,X'EF'               EXTRA BYTE TR-BLANK    I282
         TR    PRINT+79(9),TRANSLAT         MAKE SSI PRINTABLE     I282
         B     OTHERINF           GO CHECK FOR OTHER INFORMATION   I282
NOSSI    LA    MSGTO,PRINT+79     MOVE                             I282
         LA    MSGFROM,316             ABSENT TO                   I282
         BAL   MSGRET,MVMSG                     PRINT LINE         I282
*
*        THIS  ROUTINE HANDLES THE OTHER INFORMATION FIELD.
*
OTHERINF TM    20(BLOCPNT),X'24'  IS MEMBER IN OVLY OR SCAT LOAD   I282
         BZ    PRINTLNE           NO, GO PRINT LINE                I282
         TM    20(BLOCPNT),X'20'  IS MEMBER IN OVLY                I282
         BO    OVLYINFO           YES, GO CREATE OVLY INFO         I282
         LA    MSGTO,PRINT+103         NO,MOVE SCTR=             XM4744
         LA    MSGFROM,320                       TO                I282
         BAL   MSGRET,MVMSG                         PRINT LINE     I282
         UNPK  PRINT+108(7),TTRNOTE     SCTR TTR                 XM4744
         MVI   PRINT+114,TRCOMMA       COMMA                     XM4744
         TR    PRINT+108(7),TRANSLAT   MAKE TTR PRINTABLE        XM4744
         MVC   PDSFMTWK(8),33(BLOCPNT)      ALIGN OTHER SCAT INFO  I282
         LH    WORK1,PDSFMTWK               SCATTER LIST SIZE      I282
         CVD   WORK1,CONVERT                  CONVERT TO DECIMAL   I282
         UNPK  PRINT+115(5),CONVERT+5(3)    MAKE PRINTABLE       XM4744
         OI    PRINT+119,SIGN          INCLUDING SIGN            XM4744
         BAL   RETURN,LINEPR                PRINT FIRST LINE     XM4744
         LH    WORK1,PDSFMTWK+2             TRANSLATION TABLE SIZE I282
         CVD   WORK1,CONVERT                  CONVERT TO DECIMAL   I282
         UNPK  PRINT+108(5),CONVERT+5(3)      MAKE PRINTABLE       I282
         OI    PRINT+112,X'F0'                   INCLUDING SIGN    I282
         MVI   PRINT+113,COMMA              COMMA                XM4744
         LH    WORK1,PDSFMTWK+4             ESDID OF 1ST TXT CSECT I282
         CVD   WORK1,CONVERT                  CONVERT TO DECIMAL   I282
         UNPK  PRINT+114(2),CONVERT+6(2)      MAKE PRINTABLE       I282
         OI    PRINT+115,X'F0'                   INCLUDING SIGN    I282
         MVI   PRINT+116,COMMA              COMMA                XM4744
         LH    WORK1,PDSFMTWK+6             ESDID OF E.P. CSECT    I282
         CVD   WORK1,CONVERT                  CONVERT TO DECIMAL   I282
         UNPK  PRINT+117(2),CONVERT+6(2)      MAKE PRINTABLE       I282
         OI    PRINT+118,X'F0'                   INCLUDING SIGN    I282
         TM    PDSFMTSW,MEMMSK         IS TRUE NAME NEEDED?      XM4744
         BZ    PRINTLNE                NO,PRINT LINE             XM4744
         B     NEXTLINE                YES,GET TRUE NAME         XM4744
OVLYINFO LA    MSGTO,PRINT+103         OVLY=                     XM4744
         LA    MSGFROM,324                       TO                I282
         BAL   MSGRET,MVMSG                        PRINT LINE      I282
         UNPK  PRINT+108(7),TTRNOTE         OVERLAY TTR          XM4744
         MVI   PRINT+114,TRCOMMA            COMMA                XM4744
         TR    PRINT+108(7),TRANSLAT        MAKE TTR PRINTABLE   XM4744
         SR    WORK1,WORK1                  CLEAR REG FOR IC INSTR I282
         IC    WORK1,19(BLOCPNT)            NO. ENTR IN NOTE LIST  I282
         CVD   WORK1,CONVERT                NO. ENTRIES IN DEC     I282
         UNPK  PRINT+115(3),CONVERT+6(2)    MAKE PRINTABLE     @ZA30116
         OI    PRINT+117,SIGN          SIGN                    @ZA30116
*
*        A POSSIBLE SECOND LINE AND END OF A DIRECTORY ENTRY PROCESSING
*        IS DONE HERE.
*
PRINTLNE BAL   RETURN,LINEPR      GO PRINT FIRST LINE              I282
NEXTLINE TM    PDSFMTSW,MEMMSK         SHOULD OS OR AOS ALIAS    XM4744
*                                      BE PRINTED?               XM4744
         BZ    ENDENTRY           NO, GO END ENTRY                 I282
         LA    WORK1,33(BLOCPNT)  MIMUMN ADDR OF TRUE EP           I282
         TM    PDSFMTSW,X'10'     IS MEMBER SCATTER LOADED         I282
         BZ    MTRUE              NO, GO MOVE TRUE NAME TO PRINT   I282
         LA    WORK1,8(WORK1)     YES, ADD SCAT INFO LEN           I282
*                                                                XM4744
*        A MEMBER AND TTR IS PRESENT FOR AN ALIAS FOR AOS IF THE
*        PDSFMTSW = X'04'  -- A MEMBER IS PRESENT FOR AN ALIAS,
*        RENT,REUS ATTRIBUTE FOR OS IF THE PDSFMTSW = X'20'
*
MTRUE    MVC   PRINT+3(8),3(WORK1)          TRUE NAME              I282
         UNPK  PRINT+15(7),0(4,WORK1)       TRUE ENTRY POINT       I282
         MVI   PRINT+21,X'EF'               EXTRA BYTE TR-BLANK    I282
         TR    PRINT+15(7),TRANSLAT         MAKE PRINTABLE         I282
         BAL   RETURN,LINEPR      GO PRINT SECOND LINE             I282
ENDENTRY AR    BLOCPNT,WORK3      POINT TO NEXT ENTRY IN DIR RCD   I282
         SR    ELEMLEN,WORK3      REMAING BYTES IN DIRECTORY RCD   I282
         NI    PDSFMTSW,OFFMSK         RESET ENTRY CONTENTS BITS XM4744
         B     FMTENTRY           GO FORMAT NEXT ENTRY             I282
*
*        THIS ROUTINE IS ENTERED WHEN THE PDS FORMAT IS REQUESTED FOR
*        A PDS NOT CREATED BY THE LINKAGE EDITOR.
*
DOFMTDMP LA    MSGTO,0            MESSAGE TO PRINTER               I282
         LA    MSGFROM,312        ERROR MESSAGE INDEX FACTOR       I282
         BAL   MSGRET,MVMSG       MOVE MESSAGE TO PRINT BUFFER     I282
         MVI   ZSW,X'02'          SPACE BEFORE PRINTING MESSAGE    I282
         BAL   RETURN,LINEPR      PRINT MESSAGE                    I282
         ST    ELEMLEN,SELEMLEN   SAVE ELEMLEN INDICATOR           I282
         BAL   RETURN,TEMPDUMP    GO DO DUMP OPTION FOR ENTRY      I282
         L     ELEMLEN,SELEMLEN   RESTORE ELEMLEN INDICATOR        I282
         NI    11(BLOCPNT),X'1F'  TURN OFF NON LENGTH BITS         I282
         SR    WORK3,WORK3        CLEAR REG FOR IC INSTRUCTION     I282
         IC    WORK3,11(BLOCPNT)  LEN USER DATA FIELD IN HALFWORDS I282
         AR    WORK3,WORK3        LEN USER DATA FIELD IN BYTES     I282
         LA    WORK3,12(WORK3)    LEN OF COMPLETE DIRECTORY ENTRY  I282
         B     ENDENTRY           GO TO NORMAL END OF ENTRY        I282
*
*        CONSTANTS AND WORK AREAS FOR PDS FORMAT OPTION
*
PDSFMTWK DS    D                  WORK AREA FOR SCATTER LOAD
SELEMLEN DS    F                  SAVE AREA FOR ELEMLEN            I282
SRETURN  DS    F                  SAVE AREA FOR RETURN             I282
ELEVEN   DC    XL4'0B'                                             I282
DEADBYTE DC    X'0001'                 LENGTH OF DEAD BYTE       YM2247
PDSFMTSW DC    X'00'     MEANING OF BITS                           I282
*                  BIT 0     FORMAT OPTION REQUESTED               I282
*                  BIT 1     FIRST ENTRY FOR A PDS                 I282
*                  BIT 2     MEMBER IS ALIAS AND RENT OR REUS      I282
*                  BIT 3     SCATTER LOADED                        I282
*                  BIT 4     SCAT, ALIAS AND RENT OR REUS          I282
*                  BIT 5     ALIAS USE ONLY FOR AOS              XM4744
YES      DC    CL3'YES'                APF                       XM4744
NO       DC    CL2'NO'                 APF                       XM4744
SP       DC    X'00030000000203030202000202'     SPACE CHAR, HEAD  I282
*
*        PDS DSECT FOR L/E CREATED ENTRIES
*
PDS      DSECT
BEG      DS    0X                      START
NAME     DS    8X                      NAME
TTRBEG   DS    3X                      BEGINNING TTR
BITS     DS    1X                      INDICATOR FLAGS
TTRTXT   DS    4X                      TTR FIRST TXT
TTRNOTE  DS    XL4                     TTR NOTE/SCATTER        @ZA25524
ATTR     DS    2X                      ATTRIBUTES
STORAG   DS    3X                      CONTIGUOUS STORAGE
FTXTL    DS    2X                      LENGTH FIRST TXT
EPA      DS    3X                      ENTRY POINT ADDR
FSTXT    DS    3X                      FIRST TEXT BLOCK
         ORG   FSTXT                   AOS L/E ATTR
AOSATTR  DS    3X                      AOS L/E ATTR
DUM1     DS    1X                      DUMMY LENGTH
END      DS    0X                      END OF MIN PDS
*        LOAD MODULE-ALIAS RENT,REUS-OS
*                    ALIAS ONLY-AOS
         ORG   DUM1                    ACTUAL START POINT
TTR      DS    3X                      TTR OF MEMBER
MEMBER   DS    8X                      MEMBER NAME
*******************************************************************VF0I
**                                                                 VF0I
**                           IEHLISTD
**
*******************************************************************VF0I
IEHLISTD CSECT
*******************************************************************VF0I
**                                                                 VF0I
**                  REQUESTS FOR FORMATTED-DSCBS ARE HANDLED HERE  VF0I
**                                                                 VF0I
*******************************************************************VF0I
         BALR  BASEREG2,0                                          VF0I
         USING *,BASEREG2,RECCNT       -* MUTIPLE BASE REGISTERS   VFOI
BASE     L     RECCNT,BASEADR     -* LOAD RIGHT ADDRESS TO REG.    VFOI
         B     SET           -* SKIP BASEADR.                      VFOI
BASEADR  DC    A(BASE+4096)       -* FOR SETTING MULTI-BASE.       VFOI
SET      TM    FMT,X'04'          -* IS DEVICE 2321?               VFOI
         BZ    CANTON                                              VF01
         LA    MSGFROM,256              DEV2321 MESSAGE            VF0I
         LA    MSGTO,0                                             VF0I
         BAL   MSGRET,MVMSG                                        VF0I
         MVI   ZSW,X'02'                                           VF0I
         BAL   RETURN,LINEPR                                       VF0I
         B     CANTON          GO SAVE DSNAME FOR PARTIAL FMT PRNT VF0I
NOTAP    LA    MSGFROM,160   -* UNABLE TO CALCULATE SPACE MSG.     VF01
         LA    MSGTO,PRINT+32 -* STARTING OF PRINTING POSITION.    VF01
         BAL   MSGRET,MVMSG   -* MOVE MSG TO PRINT AREA.           VF01
         BAL   RETURN,LINEPR  -* TO PRINT A LINE ROUTINE.          VF01
COME     MVI   ZSW,X'02'     -* SET UP SWITCH TO SPACE A LINE.     VF01
         LA    MSGFROM,240   -* ONE MORE BLANK LINE.               VF01
         LA    MSGTO,0       -* PRINT POSITION 0.                  VF01
         BAL   MSGRET,MVMSG  -* MOVE MSG TO PRINT AREA.            VF01
         BAL   RETURN,LINEPR -* TO PRINT A LINE ROUTINE.           VF01
NOCOME   L     RETURN,SAVE1  -* PICK UP DATA.                      VF01
         BR    RETURN        -* BACK TO THE CALLING ROUTINE.       VF01
XZ       A     WORK1,NO7     -* CHECK THE NO.OF LINE LEFT.         VF01
         B     STC           -* BRACH TO COMPARISON.               VF01
BER      MVI   PRINT+47,X'40' -* PAD THE REST OF LINE WITH BLANKS. VF01
         MVC   PRINT+48(54),PRINT+47   -*                          VF01
         B     UNION         -* RETURN TO MAIN STREAM.             VF01
NARD     MVI   PRINT+75,X'40' -* BLANK THE FIRST BYTE.             VF01
         MVC   PRINT+76(26),PRINT+75  -* PAD THE REST OF LINE BLANKVF01
         B     UNION         -* RETURN TO MAIN STREAM.             VF01
XYZ      LA    RETURN,ABC    -* FORCE TO BRANCH TO ABC.            VF01
         ST    RETURN,ZSAVE14     -* STORE ADR OF ABC IN ZSAVE14.  VF01
         B     ZSETSK        -* SET SKIP PAGE ROUTINE.             VF01
CANTON   MVC   HONGKONG(44),TABLE  -* DSNAME IS SAVED.             VF01
         B     DENTIRE       -* TO DENTIRE RTN TO HAVE NECE. INFO. VF01
*******************************************************************VF0I
**                                                                 VF0I
**                  PARTIAL FORMATTED DSCB PRINT OUTS HANDLED HERE VF0I
**                                                                 VF0I
*******************************************************************VF0I
DPARTIAL LA    TABPNT,TABLE             SET REG TO START OF        VF0I
         MVC   TABLE(44),HONGKONG  -* DSNAME IS UNTOUCHED.         VF01
DOBTAIN  MVC   OBTAININ(44),0(TABPNT)   DSNAME TABLE   MOVE NAME   VF0I
         LA    SAVEREG,DSCBIN                                      VF0I
         OBTAIN OBLIST1                                            VF0I
         B     DTEST(ECREG)                                        VF0I
DTEST    B     DLINK1                                              VF0I
         B     DERRD                                               VF0I
         B     DERRD                                               VF0I
         B     WORKERR                                             VF0I
DERRD    LA    MSGFROM,12                                          VF0I
         LA    MSGTO,0                                             VF0I
         CLI   RETCODE,X'08'            IS HIGHER RETURN CODE SET  VF0I
         BH    *+8                                                 VF0I
         MVI   RETCODE,X'08'                                       VF0I
         BAL   MSGRET,MVMSG                                        VF0I
         MVC   0(44,15),0(TABPNT)                                  VF0I
         BAL   RETURN,LINEPR                                       VF0I
         B     DBACK                                               VF0I
DLINK1   MVC   WORKIN+8(5),READIN+140                              VF0I
         BAL   RETURN,DFORMAT1                                     VF0I
DOBTAIN2 CLC   ZEROS(5),DSCBIN+91  POINTER TO FMT 2 OR DSCB VALID  VF0I
         BE    DBACK                                               VF0I
         MVC   TRACK(5),DSCBIN+91       LOAD CCHHR INTO TRACK      VF0I
         MVC   WORKIN+8(5),TRACK                                   VF0I
         OBTAIN OBLIST3                 SEEK FORMAT 2 OR 3 DSCB    VF0I
         B     DFMERR(ECREG)                                       VF0I
DFMERR   B     DPRFMT                                              VF0I
         B     DERRD                                               VF0I
         B     DERRD                                               VF0I
         B     WORKERR                                             VF0I
DPRFMT   MVC   READIN(140),HOLDAREA     MOVE DSCB TO READIN AREA   VF0I
         CLI   READIN+44,FORMAT2                                   VF0I
         BNE   DCHECK3                  NO                         VF0I
         BAL   RETURN,DFORMAT2          YES PRINT FORMAT 2         VF0I
         B     DOBTAIN2      SEE IF FORMAT 3 POINTER - VALID       VF0I
DCHECK3  CLI   READIN+44,FORMAT3        IS DSCB FORMAT 3           VF0I
         BNE   DERRD                                               VF0I
         BAL   RETURN,DFORMAT3          YES - PRINT FORMAT 3       VF0I
         TM    SAVE3+5,X'02'
         BO    DOBTAIN2
DBACK    LA    TABPNT,44(TABPNT)        UPDATE TABLE POINTER       VF0I
         CR    TABPNT,TABEND            COMPARE FOR TABLE END      VF0I
         BNL   PCLOSE                                              VF0I
         CLC   0(44,TABPNT),0(TABEND)                              VF0I
         BE    PCLOSE                                              VF0I
         B     DOBTAIN                                             VF0I
*******************************************************************VF0I
**                                                                 VF0I
**             REQUESTS FOR ALL DSCBS TO BE FORMATTED HANDLED HERE VF0I
**                                                                 VF0I
*******************************************************************VF0I
DENTIRE  EQU   *                       SET UP JFCB FOR OPEN
         MVI   MODLJFCB,X'04'                                      VF0I
         MVC   MODLJFCB+1(43),MODLJFCB                             VF0I
         MVI   MODLJFCB+44,X'40'                                   VF0I
         MVI   MODLJFCB+IND2,SHR            SET SHARE BIT       YM06989
         MVI   MODLJFCB+117,X'01'                                  VF0I
         MVC   MODLJFCB+118(6),VOLID                               VF0I
         MVC   DCBBLKSI(2),SETBLK1      SET BLOCKSIZE              VF0I
         MVC   KEYLE(1),SETKEY2         SET KEYLEN                 VF0I
         MVC   DCBEODAD(3),SETEO4                                  VF0I
         OI    SW,OPENSW                                           VF0I
         OPEN  (WORKIN),TYPE=J                                     VF0I
         L     WORK2,WORKIN+44         GET ADDR OF DEB           A25709
         L     WORK2,32(WORK2)         GET UCB ADDR              A25709
         USING UCBD,WORK2                                        A25709
*        CLI   UCBTBYT4,X'05'          IS DEVICE 2321           YL026VC
*        BC    7,NOT2321B         NO,GO CHECK TTR OF VTOC START YL026VC
*SUBUCBB CLC   VOLID(6),DCELVOLI  YES, FIND SUB UCB FOR THIS VOLYL026VC
*        BE    THISUCBB           THIS IS THE SUB UCB, CHECK TTRYL026VC
*        LA    WORK2,16(WORK2)    POINT TO NEXT 2321 SUB UCB    YL026VC
*        BNE   SUBUCBB            NOT THIS SUB UCB, CHECK NEXT  YL026VC
*THISUCBBCLI   DCELVTOC+2,X'01'   DOES VTOC START ON RECORD ONE YL026VC
*        BE    DGOREAD            YES, GO DO READ, BYPASS POINT YL026VC
*        MVC   FORM5TTR(3),DCELVTOC    NO, TTR OF VTOC FOR POINTYL026VC
*        B     SETUPB                  GO SET UP TO DO POINT    YL026VC
NOT2321B CLI   SRTEFSCT+2,X'01'   VTOC START ON OTHER THAN RCD 1 A27506
         BE    DGOREAD                 NO, PROCEED NORMALLY      A25709
         MVC   FORM5TTR(3),SRTEFSCT    YES                       A25709
SETUPB   LA    PARAREG1,FORM5TTR  SET UP ADDR OF TTR FOR POINT   A27506
         BAL   RETURN,DOPOINT          GO TO ISSUE POINT         A25709
         DROP  WORK2                                             A25709
DGOREAD  BAL   WORK2,DREAD              LINK TO READ MACRO         VF0I
         MVC   TABLE(44),READIN                                    VF0I
         CLI   READIN+44,FORMAT4        CHECK IF FORMAT 4 DSCB     VF0I
         BE    DFORM4                                              VF0I
         CLI   READIN+44,FORMAT5        CHECK FOR FORMAT 5 DSCB    VF0I
         BE    DFORM5                                              VF0I
         CLI   READIN+44,FORMAT1        CHECK FOR FORMAT 1 DSCB    VF0I
         BNE   DGOREAD                                             VF0I
         BAL   RETURN,DFORMAT1          PRINT FORMAT 1 DSCB        VF0I
HUPEI    MVC   SAVECCHH(5),WORKIN+8          -*                    VFOI
DGET23   CLC   ZEROS(5),READIN+135     CHECK IF ANY  RELATED DSCBS VF0I
         BE    EXIT2              NO                             A29293
         SR    WORK2,WORK2        READ EITHER THE FORMAT 2 OR      VF0I
         IC    WORK2,READIN+139   FORMAT 3 DSCBS & PRINT THEM      VF0I
         BCTR  WORK2,0                                             VF0I
         STC   WORK2,READIN+139                                    VF0I
         MVC   WORKIN+8(5),READIN+135                              VF0I
         BAL   WORK2,DREAD                                         VF0I
         CLI   READIN+44,FORMAT2                                   VF0I
         BNE   CHK23                                               VF0I
         BAL   RETURN,DFORMAT2                                     VF0I
         B     DGET23                                              VF0I
CHK23    CLI   READIN+44,FORMAT3                                   VF0I
        BNE   EXIT1                                                VF0I
         BAL   RETURN,DFORMAT3                                     VF0I
EXIT1    TM    SAVE3+5,X'02'
         BO    DGET23
EXIT2    MVC   WORKIN+8(5),SAVECCHH                              A29293
         B     DGOREAD                                             VF0I
DFORM4   MVC   VOIDDSCB+2(2),READIN+50  SAVE NO. AVAILABLE DSCB    VF0I
         BAL   RETURN,DFORMAT4                                     VF0I
         B     DGOREAD                                             VF0I
DFORM5   TM    SW,FIRST5                                           VF0I
         BO    DGOREAD                                             VF0I
         NOTE  WORKIN                                              VF0I
         ST    PARAREG2,FORM5TTR        SAVE ADDR OF FORMAT 5      VF0I
         OI    SW,FIRST5                                           VF0I
         MVC   SAVECCHH(5),WORKIN+8 SAVE CURRENT READ ADDR IN DCB  VF0I
         BAL   RETURN,DFMT56                                       VF0I
*                                       THIS TEST ALSO USED FOR  YM5559
*                                       MORE THAN ONE FORMAT6    YM5559
REPEAT   CLC   ZEROS(5),READIN+135 TEST IF MORE THAN ONE FORMAT 5  VF0I
         BE    EXIT5                                               VF0I
         SR    WORK2,WORK2                                         VF0I
         IC    WORK2,READIN+139         RECORD ADDR OF NEXT FORMAT VF0I
         BCTR  WORK2,0                  REDUCE BY 1                VF0I
         STC   WORK2,READIN+139                                    VF0I
         MVC   WORKIN+8(5),READIN+135   PUT ADDR IN DCB            VF0I
         BAL   WORK2,DREAD                                         VF0I
         BAL   RETURN,DFMT56                                       VF0I
         B     REPEAT                                              VF0I
EXIT5    MVC   WORKIN+8(5),SAVECCHH     RESTORE ORIGINAL DCB ADDR  VF0I
         B     DGOREAD                                             VF0I
DFORMAT1 ST    RETURN,SAVE1                                        VF0I
         TM    READIN+82,X'80'         -* IS IT A ISAM DATA SET.   VFOI
         BZ    TENN               -* NO,GO TO NORMAL CASE.         VFOI
         CLI   READIN+59,X'03'        -* ISAM WITH FMT 3?          VFOI
         BH    HARBIN        -* THIS DATA SET HAS FMT 1,2,3.       VFOI
TENN     IC    WORK1,ZLINECNT      -* CHECK NO OF PAGES.           VFOI
         L     WORK2,DSCBIN+92     -* TEST NEXT DSCB POINTER.      VF01
         LTR   WORK2,WORK2    -* IS MORE DSCB TO BE PROCESSED.     VF01
         BC    8,XZ           -* NO.GO TO COMPARE A SMALLER COUNTERVF01
         A     WORK1,NO12     -* ADD THIS COUNTER BY 6.           VF01
STC      STC   WORK1,TU       -* STORE THIS NO.TO A SPOT.          VF01
         CLC   ZLINEPER(1),TU -* COMPARE WHICH IS LOWER.           VF01
         BL    XYZ            -* LOWER GO TO XYZ.                  VF01
ABC      LA    MSGFROM,152    -* PRINT A LINE IN FMT 1A.           VF01
         LA    MSGTO,PRINT    -* PRINT POSITION IS ZERO.           VF01
         BAL   MSGRET,MVMSG                                        VF0I
         MVI   ZSW,X'02'                                           VF0I
         BAL   RETURN,LINEPR                                       VF0I
         SR    ONEBYTE,ONEBYTE                                     VF0I
         MVC   PRINT(44),OBTAININ       DSNAME                     VF0I
         MVI   PRINT+46,C'1'            IDENTIFICATION TO PRINT    VF0I
         MVC   PRINT+49(6),DSCBIN+1     VOLUME SERIAL NO.          VF0I
         IC    ONEBYTE,DSCBIN+15        NO. OF EXTENTS             VF0I
         BAL   RETURN,CONVB             CONVERT ONE BYTE           VF0I
         MVC   PRINT+88(2),UNPKFLD+3    NO. OF EXTENTS OT PRINT@G60ASBJ
         IC    ONEBYTE,DSCBIN+12        EXPIRATION YEAR            VF0I
         CVD   ONEBYTE,CONVERT  -* CONVERT TO DECIMAL.             VF01
         UNPK  UNPKFLD(5),CONVERT+5(3)  -* UNPACK FOR PRINTABL.    VF01
         OI    UNPKFLD+4,X'F0'    -* OVERLAP WRONG INFORMATION.    VF01
         MVC   PRINT+75(2),UNPKFLD+3    EXPIRATION YEAR TO PRIN@G60ASBJ
         MVC   XHOLD(2),DSCBIN+13       EXPIRATION DATE            VF0I
         LH    HLFWD,XHOLD    -* LOAD EXP DATE.                    VF01
         CVD   HLFWD,CONVERT  -* CONVERT TO DECIAML.               VF01
         UNPK  UNPKFLD(5),CONVERT+5(3)  -* UNPACK FOR PRINTABLE.   VF01
         OI    UNPKFLD+4,X'F0'    -* OVERLAP WRONG INFORMATION.    VF01
         MVC   PRINT+72(3),UNPKFLD+2    EXPIRATION DAY TO PRINT@G60ASBJ
         IC    ONEBYTE,DSCBIN+9         CREATION YEAR              VF0I
         CVD   ONEBYTE,CONVERT  -* CONVERT TO DECIMAL.             VF01
         UNPK  UNPKFLD(5),CONVERT+5(3)  -* UNPACK FOR PRINTABLE.   VF01
         OI    UNPKFLD+4,X'F0'    -* OVERLAP WRONG INFORMATION.    VF01
         MVC   PRINT+68(2),UNPKFLD+3    -*CREATION YEAR TO PRINT.  VF01
         LH    HLFWD,DSCBIN+10         CREATION DATE               VF0I
         CVD   HLFWD,CONVERT      -* CONVERT TO DECIMAL.           VF01
         UNPK  UNPKFLD(5),CONVERT+5(3)  -* UNPACK FOR PRINTABLE.   VF01
         OI    UNPKFLD+4,X'F0'     -* OVERLAP THE WRONG INFORMATIONVF01
         MVC   PRINT+65(3),UNPKFLD+2    -* CREATION DAY TO PRINT.  VF01
         IC    ONEBYTE,DSCBIN+31        LAST REFERENCED YEAR   @G60ASBJ
         CVD   ONEBYTE,CONVERT          CONVERT TO DECIMAL     @G60ASBJ
         UNPK  UNPKFLD(5),CONVERT+5(3)  UNPACK LAST REF YEAR   @G60ASBJ
         OI    UNPKFLD+4,X'F0'          CONVERT SIGN BITS      @G60ASBJ
         MVC   PRINT+82(2),UNPKFLD+3    LAST REF YEAR TO PRINT @G60ASBJ
         LH    HLFWD,DSCBIN+32          LAST REFERENCED DAY    @G60ASBJ
         CVD   HLFWD,CONVERT            CONVERT TO DECIMAL     @G60ASBJ
         UNPK  UNPKFLD(5),CONVERT+5(3)  UNPACK LAST REF DAY    @G60ASBJ
         OI    UNPKFLD+4,X'F0'          CONVERT SIGN BITS      @G60ASBJ
         MVC   PRINT+79(3),UNPKFLD+2    LAST REF DAY TO PRINT  @G60ASBJ
         MVC   XHOLD(2),DSCBIN+7        VOLUME SEQUENCE NO.        VF0I
         BAL   RETURN,CONVCA                                       VF0I
         MVC   PRINT+58(4),UNPKFLD+1    VOLUME SEQ TO PRINT        VF0I
         LH    HLFWD,DSCBIN+42         BLOCK SIZE                  VF0I
         BAL   RETURN,CONVCH                                       VF0I
         MVC   PRINT+114(5),UNPKFLD     BLOCK SIZE TO PRINT    @G60ASBJ
         TM    DSCBIN+38,TYPESAM        PHYSICAL SEQUENTIAL X'40'  VF0I
         BZ    MVIS                    NO,CHECK ISAM             A34989
         MVC   PRINT+95(2),CPS         MOVE PS TO LINE         @G60ASBJ
CHKOPTCD UNPK  PRINT+108(3),DSCBIN+41(2)                       @G60ASBJ
         MVI   PRINT+110,X'EF'                                 @G60ASBJ
         TR    PRINT+108(3),TRANSLAT                           @G60ASBJ
         B     CHKMVE                  CHECK UNMOVABLE           A34989
MVIS     TM    DSCBIN+38,TYPEISAM      INDEXED SEQ         X'80' A34989
         BZ    MVDA                    NO,CHECK DA               A34989
         MVC   PRINT+95(2),CIS         MOVE IS TO LINE         @G60ASBJ
         B     CHKOPTCD                                          A34989
MVDA     TM    DSCBIN+38,TYPEDAM       DIRECT ACCESS       X'20' A34989
         BZ    MVPO                    NO,CHECK PO               A34989
         MVC   PRINT+95(2),CDA         MOVE DA TO LINE         @G60ASBJ
         B     CHKOPTCD                                          A34989
MVPO     TM    DSCBIN+38,TYPEPDS       PARTITIONED         X'02' A34989
         BZ    MVVS                    NO, CHECK VSAM           YL026VC
         MVC   PRINT+95(2),CPO                                 @G60ASBJ
         B     CHKOPTCD                CHECK OPTCD               A34989
MVVS     CLC   DSCBIN+38(2),TYPEVSAM   VSAM             X'0008' YL026VC
         BNE   UNDEF                   NO, CHECK UNDEF          YL026VC
         MVC   PRINT+95(2),CVS         MOVE VS TO PRINT LINE   @G60ASBJ
         B     CHKOPTCD                                         YL026VC
UNDEF    MVC   PRINT+95(2),UND         UNDEFINED ORG           @G60ASBJ
CHKMVE   TM    DSCBIN+38,X'01'          DATA SET UNMOVABLE         VF0I
         BZ    CHKRFM                                              VF0I
         MVI   PRINT+97,C'U'                                   @G60ASBJ
CHKRFM   TM    DSCBIN+40,TCYL      -* UNDEFINED RECORD FORMAT.     VF01
         BO    MVEU                                                VF0I
         TM    DSCBIN+40,TPVAR          VARIABLE FORMAT            VF0I
         BO    MVEV                                                VF0I
         TM    DSCBIN+40,TPFIXED        FIXED LENGTH FORMAT        VF0I
         BO    MVEF                                                VF0I
         MVI   PRINT+101,C'*'     ERROR CONDITION INVALID FORMA@G60ASBJ
CHKTOFL  TM    DSCBIN+40,TRKOFLO        TRACK OVERFLOW             VF0I
         BZ    *+8                                                 VF0I
         MVI   PRINT+103,C'T'                                  @G60ASBJ
         TM    DSCBIN+40,BLOCKED        BLOCKED  FORMAT  HEX 10    VF0I
         BZ    *+8                                                 VF0I
         MVI   PRINT+102,C'B'                                  @G60ASBJ
         TM    DSCBIN+40,STND  NO TRUNCATED BLKS OR UNFILLED TRKS  VF0I
         BZ    *+8                                                 VF0I
         MVI   PRINT+104,C'S'                                  @G60ASBJ
         TM    DSCBIN+40,ASACC     ASA CONTROL CHARACTER  HEX 04   VF0I
         BO    MVEA                YES                             VF0I
         TM    DSCBIN+40,MACHCC  MACHINE CONTROL CHARACTER HEX 02  VF0I
         BO    MVEM                YES                             VF0I
PRINTLN  BAL   RETURN,LINEPR           PRINT FIRST LINE          A34989
         LA    MSGFROM,156              FMT1B                      VF0I
         LA    MSGTO,PRINT+12                                      VF0I
         BAL   MSGRET,MVMSG                                        VF0I
         MVI   ZSW,X'02'                                           VF0I
         BAL   RETURN,LINEPR                                       VF0I
         LH    HLFWD,DSCBIN+44         RECORD LENGTH               VF0I
         BAL   RETURN,CONVCH                                       VF0I
         MVC   PRINT+12(5),UNPKFLD                                 VF0I
         CLC   ZEROS(1),DSCBIN+46       KEY LENGTH ZERO            VF0I
         BE    BYPSS                                               VF0I
         IC    ONEBYTE,DSCBIN+46        KEY LENGTH                 VF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   PRINT+20(3),UNPKFLD+2                               VF0I
BYPSS    CLC   ZEROS(5),DSCBIN+54       TTRLL EQUAL TO ZERO        VF0I
         BE    BYPASS1                  YES                        VF0I
         LH    HLFWD,DSCBIN+54                                     VF0I
         BAL   RETURN,CONVCH                                       VF0I
         MVC   PRINT+55(5),UNPKFLD      RELATIVE TRACK TO PRINT    VF0I
         IC    ONEBYTE,DSCBIN+56                                   VF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   PRINT+61(3),UNPKFLD+2 BLOCK NO. ON THAT TRACK TO PRTVF0I
         MVC   XHOLD(2),DSCBIN+57       BYTES REMAINING ON TRACK   VF0I
         LH    HLFWD,XHOLD                                         VF0I
         LTR   HLFWD,HLFWD                                         VF0I
         BM    ERRLL                    IS LL NEGATIVE             VF0I
CONVLL   BAL   RETURN,CONVCH           CONVERT HALFWORD          A32555
         MVC   PRINT+65(5),UNPKFLD                                 VF0I
BYPASS1  CLI   DSCBIN+16,X'00'     BYTES USED IN LAST PDS ENTRY = 0VF0I
         BE    BYPASS2             YES                             VF0I
         IC    ONEBYTE,DSCBIN+16                                   VF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   PRINT+80(3),UNPKFLD+2                               VF0I
BYPASS2  CLC   ZEROS(5),DSCBIN+91  POINTER TO FORMAT 203 DSCB = 0  VF0I
         BE    BYPASS3                                             VF0I
         MVC   XHOLD(2),DSCBIN+91 MOVE IN CYLINDER ADDR NEXT DSCB  VF0I
         BAL   RETURN,CONVA                                        VF0I
         MVC   PRINT+94(3),UNPKFLD+2                               VF0I
         MVC   XHOLD(2),DSCBIN+93 MOVE IN HEAD ADDR OF POINTER     VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVA                                        VF0I
         MVC   PRINT+98(3),UNPKFLD+2                               VF0I
         IC    ONEBYTE,DSCBIN+95        RECORD NUMBER              VF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   PRINT+102(3),UNPKFLD+2                              VF0I
BYPASS3  MVC   CONVERT(4),DSCBIN+50     ALIGN                    YM8143
         L     WORK2,CONVERT            SECONDARY ALLOCATION     YM8143
         LA    WORK2,0(WORK2)           CLEAR HIGH BYTE          YM8143
         CVD   WORK2,CONVERT            CONVERT IT.              YM8143
         UNPK  PRINT+42(7),CONVERT+4(4) UNPACK INTO PRINTLINE    YM8143
         LA    WORK2,PRINT+42           ADDR OF FIELD            YM8143
         LA    WORK3,7                  NO. OF DIGITS TO PROCESS YM8143
         BAL   RETURN,TRY               GET RID OF LEADING ZEROS YM8143
         OI    PRINT+48,X'F0'           OVERLAP                @ZA01480
         BAL   RETURN,CONVDSCB          CONVERT ADDR OF THE DSCB   VF0I
         MVC   PRINT+108(11),PHOLD                                 VF0I
         TM    DSCBIN+50,TCYL                                      VF0I
         BNO   *+12                                                VF0I
         LA    MSGFROM,184              ALLOC CYLINDER FMT1I       VF0I
         B     PRT                                                 VF0I
         TM    DSCBIN+50,TREC                                      VF0I
         BZ    *+12                                                VF0I
         LA    MSGFROM,176              ALLOC RECORDS  FMT1G       VF0I
         B     PRT                                                 VF0I
         TM    DSCBIN+50,TTRK                                      VF0I
         BZ    *+12                                                VF0I
         LA    MSGFROM,180              ALLOC TRACKS   FMT1H       VF0I
         B     PRT                                                 VF0I
         LA    MSGFROM,172              ALLOC ABSTR    FMT1F       VF0I
PRT      LA    MSGTO,PRINT+28                                      VF0I
         BAL   MSGRET,MVMSG                                        VF0I
         TM    DSCBIN+50,TCONT                                     VF0I
         BZ    *+12                                                VF0I
         LA    MSGFROM,188              ALLOC CONTIG   FMT1J       VF0I
         B     PRT1                                                VF0I
         TM    DSCBIN+50,TMXIG                                     VF0I
         BZ    *+12                                                VF0I
         LA    MSGFROM,192              ALLOC MXIG     FMT1K       VF0I
         B     PRT1                                                VF0I
         TM    DSCBIN+50,TALX                                      VF0I
         BZ    *+12                                                VF0I
         LA    MSGFROM,196              ALLOC ALX      FMT1L       VF0I
         B     PRT1                                                VF0I
         TM    DSCBIN+50,X'01'                                     VF0I
         BZ    *+16                                                VF0I
         LA    MSGFROM,200              ALLOC ROUND    FMT1M       VF0I
PRT1     LA    MSGTO,PRINT+34                                      VF0I
         BAL   MSGRET,MVMSG                                        VF0I
         BAL   RETURN,LINEPR                                       VF0I
         LA    MSGFROM,164              FMT1D                      VF0I
         LA    MSGTO,PRINT+12                                      VF0I
         BAL   MSGRET,MVMSG                                        VF0I
         MVI   ZSW,X'02'                                           VF0I
         CLI   DSCBIN+15,X'01'     -* THE NO.OF EXTENT IS 1?       VF01
         BE    BER                 -* YES,SO BLANK THE REST OF HEADVF01
         CLI   DSCBIN+15,X'02'     -* THE NO.OF EXTENT IS 2?       VF01
         BE    NARD                -* YES,BLANK THE REST OF HEADINGVF01
UNION    BAL   RETURN,LINEPR       -* GO TO PRINT A LINE.          VF01
         MVC   HOLDAREA(30),DSCBIN+61   MOVE EXTENTS TO HOLD       VF0I
         MVI   PHOLD,X'40'              BLANK OUT PHOLD
         MVC   PHOLD+1(22),PHOLD
         SR    WORK1,WORK1                                         VF0I
         SR    WORK3,WORK3                                         VF0I
         LA    WORK1,HOLDAREA                                      VF0I
         LA    WORK3,PRINT+21                                      VF0I
         SR    WORK2,WORK2        -* CLEAR REGISTER WORK2.         VF01
         ST    WORK2,SAVL         -* CLEAR COUNTER FOR CYLINDER.   VF01
         ST    WORK2,SAVT         -* CLEAR COUNTER FOR TRACK.      VF01
GETEXT   CLI   0(WORK1),X'00'                                      VF0I
         BE    WRTEXT                                              VF0I
         MVC   XHOLD(10),0(WORK1)                                  VF0I
         IC    ONEBYTE,XHOLD+1          NO. OF EXTENT              VF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   PHOLD(2),UNPKFLD+3                                  VF0I
         LH    HLFWD,XHOLD+2           LOW LIMIT CYL ADDR          VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PHOLD+5(3),UNPKFLD+2                                VF0I
         LH    HLFWD,XHOLD+4           LOW LIMIT HEAD ADDR         VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PHOLD+9(3),UNPKFLD+2                                VF0I
         LH    HLFWD,XHOLD+6           HIGH LIMIT CYL ADDR         VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PHOLD+17(3),UNPKFLD+2                               VF0I
         LH    HLFWD,XHOLD+8           HIGH LIMIT HEAD ADDR        VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PHOLD+21(3),UNPKFLD+2                               VF0I
         MVC   0(24,WORK3),PHOLD                                   VF0I
         LA    WORK3,28(WORK3)          INCREASE PRINT ADDR        VF0I
INCRS    LA    WORK1,10(WORK1)                                     VF0I
         BAL   RETURN,MYRTN       -* GO TO RTN FOR CALCULATION.    VF01
         C     WORK1,FMT1END            HAS END OF HOLDAREA BEEN   VF0I
         BH    WRTEXT                                              VF0I
         B     GETEXT                                              VF0I
WRTEXT   BAL   RETURN,LINEPR                                       VF0I
EXITF1   MVC   ORG(1),DSCBIN+38   -* SAVE DATA SET ORGANIZATION.   VF01
         MVC   CTB(1),DSCBIN+50   -* SAVE MARK FOR CYL,RECORD,TRK. VF01
         MVC   SAVERS+2(2),DSCBIN+54   -* REL ADR OF TRK.          VF01
         MVC   SAVER+2(2),DSCBIN+42    -* BLOCK LENGTH.            VF01
         MVC   SA+2(2),DSCBIN+57  -* NO.OF BYTES REMAINING ON TRACKVF01
         MVC   TTRLL(5),DSCBIN+54    SAVE FOR SPACE CALCULATION YA00066
         MVC   EXTNO(1),DSCBIN+61    SAVE # OF EXTENTS          YA00067
         MVC   KEYLENTH(1),DSCBIN+46 SAVE THE KEY LENGTH        YA00067
         L     WORK2,DSCBIN+92    -* IS ANY OTHER FORMAT?          VF01
         LTR   WORK2,WORK2                                         VF01
         BC    7,NOCOME           -* YES,MORE TO COME.             VF01
*                                                               YA00066
*  BEGINNING OF SPACE CALCULATIONS - DSCBIN MAY CONTAIN EITHER F1
*  OR F3 MODIFIED.  VARIABLE TTRLL CONTAINS THE PROPER BLOCK PTR.
*                                                               YA00066
MORE     TM    SAVE3+5,X'01'         -* MORE TO BE READ.           VFOI
         BO TAICHUNG             -* GO TO SET SWITCH.              VFOI
NEWYORK  L     WORK2,SAVL          -* ACCESS THE NO OF CYLINDERS.  VFOI
         M     WORK1,HANKOW+4     -* CONVERT IN TERM OF TRACKS.    VF01
         A     WORK2,SAVT         -* ADD UP THE NO.OF TRK IN TRK CTVF01
*                                                               YA00068
*  SAVT SHOULD BE DECREMENTED BY ONE FOR PROPER BLOCK CALCULATIONS    *
*  FOR THE FOLLOWING CONDITIONS:                                      *
*     1.  SPACE ALLOCATED IN BLOCKS (RECS)                            *
*     2.  SPACE ALLOCATED IN TRACKS OR CYLINDERS WHERE R OF TTRLL     *
*         IS NON-ZERO                                                 *
*                                                               YA00068
         TM    CTB,ALOCBLK           SP ALLOC IN TRKS OR CYLS?  YA00068
         BO    TRACKT                YES - BRANCH TO TRACK TEST YA00068
         B     SUBBLK                SPACE ALLOCATED IN BLOCKS  YA00068
TRACKT   CLI   TTRLL+2,RTTRLL        CHECK FOR EMPTY TRACK.     YA00065
         BE    SUBTRK                                           YA00065
SUBBLK   BCTR  WORK2,0            -* MINUS THE TOTAL NO TRK BY 1.  VF01
SUBTRK   S     WORK2,SAVERS       -* MINUS THE NO OF TRK USED.     VF01
         ST    WORK2,SAV          -* SAVE THE RESULT.              VF01
         CLI   EXTNO,X'00'         ARE THERE ANY EXTENTS?       YA00067
         BNE   TESTORG             YES, GO TEST ORGANIZATION     A27725
         LA    MSGFROM,328         NO, GIVE NO EXTENTS MESSAGE   A27725
         LA    MSGTO,PRINT+32      MESG POSITION IN PRINT BUFFER A27725
         BAL   MSGRET,MVMSG        MOVE MESG TO PRINT BUFFER     A27725
         BAL   RETURN,LINEPR       PRINT NO EXTENTS MESSAGE      A27725
         B     COME                GO FORMAT NEXT DSCB           A27725
TESTORG  TM    ORG,X'42'           TEST ORGANIZATION OF DATASET  A27725
         BC    8,NOTAP            -* MUST BE PS OR PO ORGANIZATION.VF01
         XC    CONST1(5),CONST1        ZERO FIELD FOR COMPARE    A38621
         CLC   TTRLL(5),CONST1         LAST BLOCK PTR APPLY?    YA00066
         BE    NOTAP                   NO,UNABLE TO CALCULATE    A38621
         LA    MSGFROM,204         ABLE TO CALCULATE MESSAGE       VF0I
         LA    MSGTO,PRINT+32     -* PRINT POSITION.               VF01
         BAL   MSGRET,MVMSG       -* MOVE MSG TO PRINT POSITION.   VF01
         TM    CTB,X'C0'     -* IS THIS A CYLINDER SPECIFIED?      VF01
         BC    1,CYL         -* YES,GO TO CYLINDER CAL. ROUTINE.   VF01
         TM    CTB,X'40'     -* IS IT A TRK SPECIFIED?             VF01
         BC    1,BLK         -* YES,SO GO TO BLK CALCULATION RTN.  VF01
         MVC   XHOLD(2),SAV+2  -* THEN THIS IS TRACK SPECIFIED.    VF01
         LH    HLFWD,XHOLD   -* FIT FOR CONVERTION ROUTINE.        VF01
         BAL   RETURN,CONVCA  -* GO TO RTN.                        VF01
         MVC   PRINT+67(5),UNPKFLD   -* MOVE DATA TO PRINT.        VF01
         MVC   PRINT+73(15),MTRK     -* MOVE CONSTANT TO PRINT.    VF01
         BAL   RETURN,LINEPR -* GO TO PRINT RTN.                   VF01
         B     COME                                                VF01
CYL      SR    WORK1,WORK1   -* CLEAN REG BEFORE DEVIDING DATA.    VF01
         L     WORK2,SAV     -* SAV CONTAINS THE NO OF TRACKS.     VF01
         D     WORK1,HANKOW+4  -* OBTAIN UNIT IN TERM OF CYL AND TKVF01
         ST    WORK2,SAV     -* CYLINDERS.                         VF01
         MVC   XHOLD(2),SAV+2  -* CONVERT TO PRINTABLE.            VF01
         LH    HLFWD,XHOLD   -* FIT FOR CONVERTION RTN.            VF01
         BAL   RETURN,CONVCA -* GO TO RTN.                         VF01
         MVC   PRINT+67(5),UNPKFLD  -* MOVE DATA TO PRINT AREA.    VF01
         MVC   PRINT+73(17),MCYL    -* MOVE CONSTANT TO PRINT.     VF01
         ST    WORK1,SAV     -* THE REMAIDER IS TRACKS.            VF01
         MVC   XHOLD(2),SAV+2  -* LOAD TO AREA SO RTN CAN HANDLE.  VF01
         LH    HLFWD,XHOLD     -* FIT FOR CONVERTION.              VF01
         BAL   RETURN,CONVCA   -* GO TO CONVT  RTN.                VF01
         MVC   PRINT+91(4),MPLUS  -* MOVE CONSTANT FOR MSG PRINT.  VF01
         MVC   PRINT+95(5),UNPKFLD  -* NUMBER OF TRACKS.           VF01
         MVC   PRINT+101(15),MTRK  -* MOVE CONSTANT TO MSG PRINT   VF01
         BAL   RETURN,LINEPR       -* GO TO PRINT A LINE.          VF01
         B     COME                -* PROCEED TO NEXT.             VF01
BLK      SR    WORK1,WORK1        -* CLEAR REG. PRIOR TO DIVISION. VF01
         C     WORK1,SAVER         IS BLKSIZE ZERO                M0128
         BE    NOTAP               YES-PRNT UNABLE TO CALC MESG   M0128
         IC    WORK1,KEYLENTH          KEY LENGTH OF DATA SET   YA00067
         STH   WORK1,KEYLE1            SAVE THE KEY LENGTH       A29716
         L     WORK2,HANKOW+8          BYTES PER TRACK           A29716
         C     WORK2,B14568            IS DEVICE A 2305-1?      YA00067
         BE    CALCBLK                 BRANCH IF YES            YA00067
         C     WORK2,B14858            IS DEVICE A 2305-2?      YA00067
         BE    CALCBLK                 BRANCH IF YES            YA00067
         C     WORK2,B13165            IS DEVICE A 3330?        YA00067
         BE    CALCBLK                 BRANCH IF YES            YA00067
         C     WORK2,B8368             IS DEVICE 3340?          YL026VC
         BE    CALCBLK                 BRANCH IF YES            YL026VC
         SH    WORK2,C53               SUB DEV CONST FR TRKSIZE YA00067
         SR    WORK2,WORK1             SUB KEYLENGTH FR TRKSIZE YA00067
         C     WORK2,SAVER             IS BLOCKSIZE > TRACKSIZE YA00067
         BL    NOTAP                   PRINT UNABLE TO CALC MSG YA00067
         AH    WORK2,C53               ADD DEV CONST TO TRKSIZE YA00067
         AR    WORK2,WORK1             ADD KEYLENGTH TO TRKSIZE YA00067
DEVICE   C     WORK2,B7294             DEVICE 2314               A29716
         BE    UN2314                  DEVICE IS A 2314          A29716
         C     WORK2,B3625             DEVICE 2311               A29716
         BE    UN2311                  DEVICE IS A 2311          A29716
         C     WORK2,B14568            IS DEVICE 2305-1
         BE    UN23051                 IF YES,BRANCH
         C     WORK2,B14858            IS DEVICE 2305-2
         BE    UN23052                 IF YES,BRANCH
         C     WORK2,B13165            IS DEVICE 3330
         BE    UN3330                  IF YES,BRANCH
         C     WORK2,B19254            IS IT 3350?             @Z30RSAG
         BE    UN3350                    BRANCH IF YES         @Z30RSAG
         C     WORK2,B4984             IS DEVICE A 2302          A29716
         BE    UN2311                  SAME BLOCK SPACE AS 2311  A29716
         C     WORK2,B4892             IS DEVICE A 2303          A29716
         BE    UN2303                  IF SO BRANCH TO BLK CALC. A29716
         C     WORK2,B2000             IS A DATA CELL SPECIFIED  A29716
         BE    UN2321                  IF SO BRANCH              A29716
         C     WORK2,B20483            IS DEVICE A 2301          A29716
         BE    UN2301                  IF SO --- BRANCH          A29716
         C     WORK2,B8368             IS DEVICE 3340           YL026VC
         BE    UN3340                  IF SO BRANCH             YL026VC
         MVI   PRINT,X'40'             BLANK PRINT LINE
         MVC   PRINT+1(119),PRINT      MAKE VOLUME INDEPENDENT
         B     COME                    GO TO NEXT DSCB
CALCBLK  SH    WORK2,C634              SUB DEV CONST FR TRKSIZE YA00067
         SR    WORK2,WORK1             SUB KEYLENGTH FR TRKSIZE YA00067
         C     WORK2,SAVER             IS BLOCKSIZE > TRACKSIZE YA00067
         BL    NOTAP                   PRINT UNABLE TO CALC MSG YA00067
         AH    WORK2,C634              ADD DEV CONST TO TRKSIZE YA00067
         AR    WORK2,WORK1             ADD KEYLENGTH TO TRKSIZE YA00067
         B     DEVICE                  BR TO DEVICE DETERMINE   YA00067
BLKLEFT  SR    WORK2,WORK1             SUB LAST RECORD LEN. FROM A29716
         SR    WORK1,WORK1             TR. CAPACITY AND DIVIDE BYA29716
         DR    WORK1,WORK3             LEN. OF EACH PHYSICAL REC.A29716
         LA    WORK2,1(WORK2)          TOTAL RECORDS PER TRACK   A29716
         LR    WORK5,WORK2             SAVE RECORDS PER TRACK    A29716
         SR    WORK1,WORK1             ZERO THE REGISTER         A29716
         M     WORK1,SAV               EMPTY BLKS ON EMPTY TRKS  A29716
         LH    WORK4,SA+2              EMPTY BYTES ON TRACK      A29716
         SR    WORK3,WORK3             ZERO MULTIPLY REG.        A29716
         MR    WORK3,WORK5             CALCULATE THE             A29716
         D     WORK3,HANKOW+8          UNUSED BLOCKS ON THE      A29716
*                                      PARTIALLY USED TRACK      A29716
         AR    WORK2,WORK4             TOTAL EMPTY BLOCKS        A29716
         ST    WORK2,SAV               STORE THE EMPTY BLOCK CNT.A29716
         MVC   XHOLD(2),SAV+2      -* CONVERT TO DECIMAL.          VF01
         LH    HLFWD,XHOLD          -* FULL THE RTN.REQUIREMENT.   VF01
         BAL   RETURN,CONVCA       -* GO TO RTN.                   VF01
         MVC   PRINT+77(5),UNPKFLD M-* MOVE DATA TO PRINT AREA.    VF01
         MVC   PRINT+83(15),MBLK   -* MOVE CONSTANT FOR PRINT.     VF01
         BAL   RETURN,LINEPR       -* GO TO PRINT A LINE.          VF01
         B     COME                -* PROCEED TO NEXT.             VF01
DFORMAT2 ST    RETURN,SAVE1                                        VF0I
         TM    SAVE3+5,X'01'       -* 1ST TIME ENTRY?              VFOI
         BO    NOCOME             -* FIRST TIME ENTRY.             VFOI
NANKING  MVI   ZSW,X'02'            -* SET A BLANK LINE PRINT      VFOI
         LA    MSGFROM,236              FMT2A                      VF0I
         LA    MSGTO,PRINT+5                                       VF0I
         BAL   MSGRET,MVMSG                                        VF0I
         BAL   RETURN,LINEPR                                       VF0I
         SR    ONEBYTE,ONEBYTE          INITIALIZE REGISTER 4      VF0I
         IC    ONEBYTE,READIN+1      2MIND - ADDR 2ND LEVEL MASTER VF0I
         BAL   RETURN,CONVB             INDEX                      VF0I
         MVC   PRINT+5(3),UNPKFLD+2     M - EXTENT                 VF0I
         LH    HLFWD,READIN+2                                      VF0I
         BAL   RETURN,CONVCH                                       VF0I
         MVC   PRINT+9(1),UNPKFLD+4     B - BIN                    VF0I
         LH    HLFWD,READIN+4                                      VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PRINT+11(3),UNPKFLD+2    C -CYLINDER                VF0I
         LH    HLFWD,READIN+6                                      VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PRINT+15(3),UNPKFLD+2    H - HEAD                   VF0I
         IC    ONEBYTE,READIN+13     3MIND - ADDR 3RD LEVEL MASTER VF0I
         BAL   RETURN,CONVB             INDEX                      VF0I
         MVC   PRINT+20(3),UNPKFLD+2    M - EXTENT                 VF0I
         LH    HLFWD,READIN+14                                     VF0I
         BAL   RETURN,CONVCH                                       VF0I
         MVC   PRINT+24(1),UNPKFLD+4    B - BIN                    VF0I
         LH    HLFWD,READIN+16                                     VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PRINT+26(3),UNPKFLD+2    C - CYLINDER               VF0I
         LH    HLFWD,READIN+18                                     VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PRINT+30(3),UNPKFLD+2    H - HEAD                   VF0I
         LH    HLFWD,READIN+8           L2MEN -CCHHR OF LAST ACTIVEVF0I
         BAL   RETURN,CONVH             INDEX ENTRY IN THE SECOND  VF0I
         MVC   PRINT+35(3),UNPKFLD+2    LEVEL MASTER INDEX         VF0I
         LH    HLFWD,READIN+10                                     VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PRINT+39(3),UNPKFLD+2    H - HEAD                   VF0I
         IC    ONEBYTE,READIN+12                                   VF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   PRINT+43(3),UNPKFLD+2    R - RECORD                 VF0I
         LH    HLFWD,READIN+20       L3MIN - CCHHR OF LAST ACTIVE  VF0I
         BAL   RETURN,CONVH             ENTRY IN THE THIRD LEVEL   VF0I
         MVC   PRINT+48(3),UNPKFLD+2    MASTER INDEX               VF0I
         LH    HLFWD,READIN+22                                     VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PRINT+52(3),UNPKFLD+2    H - HEAD                   VF0I
         IC    ONEBYTE,READIN+24                                   VF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   PRINT+56(3),UNPKFLD+2    R - RECORD                 VF0I
         IC    ONEBYTE,READIN+72    CYLAD - ADDR OF FIRST TRACK OF VF0I
         BAL   RETURN,CONVB             CYLINDER INDEX - MBBCCHH   VF0I
         MVC   PRINT+61(3),UNPKFLD+2    M - EXTENT                 VF0I
         MVC   XHOLD(2),READIN+73                                  VF0I
         BAL   RETURN,CONVCA       -* GO TO CONVERTION ROUTINE.    VF01
         MVC   PRINT+65(1),UNPKFLD+4 -* B-BIN.                     VF01
         MVC   XHOLD(2),READIN+75                                  VF0I
         BAL   RETURN,CONVA                                        VF0I
         MVC   PRINT+67(3),UNPKFLD+2    C - CYLINDER               VF0I
         MVC   XHOLD(2),READIN+77                                  VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVA                                        VF0I
         MVC   PRINT+71(3),UNPKFLD+2    H - HEAD                   VF0I
         IC    ONEBYTE,READIN+79    ADLIN - ADDR OF FIRST TRACK OF VF0I
         BAL   RETURN,CONVB             LOWEST LEVEL MASTER INDEX  VF0I
         MVC   PRINT+76(3),UNPKFLD+2    MBBCCHH                    VF0I
         LH    HLFWD,READIN+80                                     VF0I
         BAL   RETURN,CONVCH                                       VF0I
         MVC   PRINT+80(1),UNPKFLD+4 -* B-BIN.                     VF01
         LH    HLFWD,READIN+82                                     VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PRINT+82(3),UNPKFLD+2    C - CYLINDER               VF0I
         LH    HLFWD,READIN+84                                     VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PRINT+86(3),UNPKFLD+2    H - HEAD                   VF0I
         IC    ONEBYTE,READIN+86    ADHIN - ADDR OF FIRST TRACK OF VF0I
         BAL   RETURN,CONVB             HIGHEST LEVEL MASTER INDEX VF0I
         MVC   PRINT+91(3),UNPKFLD+2    MBBCCHH                    VF0I
         MVC   XHOLD(2),READIN+87                                  VF0I
         BAL   RETURN,CONVCA                                       VF0I
         MVC   PRINT+95(1),UNPKFLD+4 -* B-BIN.                     VF01
         MVC   XHOLD(2),READIN+89                                  VF0I
         BAL   RETURN,CONVA                                        VF0I
         MVC   PRINT+97(3),UNPKFLD+2    C - CYLINDER               VF0I
         MVC   XHOLD(2),READIN+91                                  VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVA                                        VF0I
         MVC   PRINT+101(3),UNPKFLD+2   H - HEAD                   VF0I
         LH    HLFWD,READIN+64          NOBYT - NO. BYTES NEEDED TOVF0I
         BAL   RETURN,CONVCH            HOLD HIGHEST LEVEL INDEX INVF0I
         MVC   PRINT+106(5),UNPKFLD     MAIN STORAGE               VF0I
         IC    ONEBYTE,READIN+66        NOTRK - NO, TRACKS OCCUPIEDVF0I
         BAL   RETURN,CONVB             BY HIGHEST LEVEL INDEX     VF0I
         MVC   PRINT+114(3),UNPKFLD+2                              VF0I
         BAL   RETURN,LINEPR         PRINT FIRST LINE OF FORMAT 2  VF0I
         LA    MSGFROM,244              FMT2C                      VF0I
         LA    MSGTO,PRINT+5                                       VF0I
         MVI   ZSW,X'02'                                           VF0I
         BAL   MSGRET,MVMSG                                        VF0I
         BAL   RETURN,LINEPR                                       VF0I
         MVC   XHOLD(2),READIN+101   LTRAD - CCHHR OF LAST NORMAL  VF0I
         BAL   RETURN,CONVA          ENTRY IN TRACK INDEX ON LAST  VF0I
         MVC   PRINT+5(3),UNPKFLD+2     CYLINDER                   VF0I
         MVC   XHOLD(2),READIN+103                                 VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVA                                        VF0I
         MVC   PRINT+9(3),UNPKFLD+2     H - HEAD                   VF0I
         IC    ONEBYTE,READIN+105                                  VF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   PRINT+13(3),UNPKFLD+2    R - RECORD                 VF0I
         LH    HLFWD,READIN+106         LCYAD - CCHHR OF LAST INDEXVF0I
         BAL   RETURN,CONVH             ENTRY IN CYLINDER INDEX    VF0I
         MVC   PRINT+18(3),UNPKFLD+2                               VF0I
         LH    HLFWD,READIN+108                                    VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PRINT+22(3),UNPKFLD+2    H - HEAD                   VF0I
         IC    ONEBYTE,READIN+110                                  VF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   PRINT+26(3),UNPKFLD+2    R - RECORD                 VF0I
         MVC   XHOLD(2),READIN+111      LMSAD - CCHHR OF LAST INDEXVF0I
         BAL   RETURN,CONVA             ENTRY IN THE MASTER INDEX  VF0I
         MVC   PRINT+31(3),UNPKFLD+2    C - CYLINDER               VF0I
         MVC   XHOLD(2),READIN+113                                 VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVA                                        VF0I
         MVC   PRINT+35(3),UNPKFLD+2    H - HEAD                   VF0I
         IC    ONEBYTE,READIN+115                                  VF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   PRINT+39(3),UNPKFLD+2    R - RECORD                 VF0I
         IC    ONEBYTE,READIN+93     LPRAD - MBBCCHHR OF LAST RCD  VF0I
         BAL   RETURN,CONVB             IN THE PRIME DATA AREA     VF0I
         MVC   PRINT+44(3),UNPKFLD+2    M - EXTENT                 VF0I
         LH    HLFWD,READIN+94                                     VF0I
         BAL   RETURN,CONVCH                                       VF0I
         MVC   PRINT+48(1),UNPKFLD+4    B - BIN                    VF0I
         LH    HLFWD,READIN+96                                     VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PRINT+50(3),UNPKFLD+2    C - CYLINDER               VF0I
         LH    HLFWD,READIN+98                                     VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PRINT+54(3),UNPKFLD+2    H - HEAD                   VF0I
         IC    ONEBYTE,READIN+100                                  VF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   PRINT+58(3),UNPKFLD+2    R - RECORD                 VF0I
         IC    ONEBYTE,READIN+45        NOLEV - NO. OF INDEX LEVELSVF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   PRINT+63(3),UNPKFLD+2                               VF0I
         IC    ONEBYTE,READIN+52        CYLOV - NO. OF TRACKS OF   VF0I
         BAL   RETURN,CONVB             CYL OVERFLOW ON EACH       VF0I
         MVC   PRINT+70(3),UNPKFLD+2    CYLINDER                   VF0I
         MVC   XHOLD(2),READIN+59    TAGDT - NO. RCDS TAGGED FOR   VF0I
         BAL   RETURN,CONVCA            DELETION                   VF0I
         MVC   PRINT+76(3),UNPKFLD                                 VF0I
         MVC   XHOLD(4),READIN+67       PRCTR - NO. RCDS IN PRIME  VF0I
         L     WORK2,XHOLD              DATA AREA                  VF0I
         CVD   WORK2,CONVERT                                       VF0I
         UNPK  PRINT+79(9),CONVERT+3(5)  -* FOR PRINTABLE.         VF02
         LA    WORK3,9             -* NO OF BYTES PROCESSED.       VF01
         LA    WORK2,PRINT+79      -* ADDRESS OF DATA TO BE PROCESSVF01
         BAL   RETURN,TRY          -* GO TO GET  RID  OF ZERO.     VF01
         OI    PRINT+87,X'F0'      -* FOR PRINTABLE.               VF01
         LH    HLFWD,READIN+128         OVRCT - NO. RECORDS IN     VF0I
         BAL   RETURN,CONVCH            OVERFLOW AREA              VF0I
         MVC   PRINT+94(5),UNPKFLD                                 VF0I
         LH    HLFWD,READIN+130         RORG1 - NO. CYL OVERFLOW   VF0I
         BAL   RETURN,CONVCH            AREAS THAT ARE FULL        VF0I
         MVC   PRINT+101(5),UNPKFLD                                VF0I
         CLC   CLEAR(5),READIN+135                                 VF0I
         BE    WRITE2                                              VF0I
         MVC   XHOLD(2),READIN+135      CCHHR OF POINTR TO FRMAT 3 VF0I
         BAL   RETURN,CONVA             DSCB IF APPLICABLE         VF0I
         MVC   PRINT+108(3),UNPKFLD+2   C - CYLINDER               VF0I
         MVC   XHOLD(2),READIN+137                                 VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVA                                        VF0I
         MVC   PRINT+112(3),UNPKFLD+2   H - HEAD                   VF0I
         IC    ONEBYTE,READIN+139                                  VF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   PRINT+116(3),UNPKFLD+2   R - RECORD                 VF0I
         TM    SAVE3+5,X'02'     -* FORMAT 3 EXIST?                VFOI
         BO    HUNAN              -* YES,GO TO SET OFF THE SWITCH. VFOI
WRITE2   BAL   RETURN,LINEPR       -* GO TO PRINT A LINE.          VF01
         B     MORE                -* PROCEED TO NEXT.             VF01
DFORMAT3 ST    RETURN,SAVE1                                        VF0I
         TM    SAVE3+5,X'02'     -* FORMAT 3 EXIST?            @ZA07324
         BO    HUNAN          -* YES,GO TO SET OFF THE SWITCH. @ZA07324
         SR    ONEBYTE,ONEBYTE                                     VF0I
         MVC   HOLDAREA(40),READIN+4                               VF0I
         MVC   HOLDAREA+40(90),READIN+45                           VF0I
         MVI   PHOLD,X'40'              BLANK OUT PHOLD
         MVC   PHOLD+1(22),PHOLD
         SR    WORK1,WORK1                                         VF0I
         LR    WORK3,WORK1                                         VF0I
         LA    WORK1,HOLDAREA                                      VF0I
         LA    WORK3,PRINT+21      -* ADDRESS TO PROPER PRINT POSITVF01
         MVI   PRINT1,X'00'                                        VF0I
NEXTEXT  CLI   1(WORK1),X'00'                                      VF0I
         BE    CHKWRT                                              VF0I
         C     WORK3,MAX                PRINT LINE FILLED          VF0I
         BH    PRNTEXT                  YES                        VF0I
CONTPRT  MVI   PRINT1,X'01'             SET VALID EXTENT PRINT SW  VF0I
         MVC   XHOLD(10),0(WORK1)                                  VF0I
         IC    ONEBYTE,XHOLD+1          NO. OF EXTENT              VF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   PHOLD(2),UNPKFLD+3                                  VF0I
         LH    HLFWD,XHOLD+2           LOW LIMIT CYL ADDR          VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PHOLD+5(3),UNPKFLD+2                                VF0I
         LH    HLFWD,XHOLD+4           LOW LIMIT HEAD ADDR         VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PHOLD+9(3),UNPKFLD+2                                VF0I
         LH    HLFWD,XHOLD+6           HIGH LIMIT CYL ADDR         VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PHOLD+17(3),UNPKFLD+2                               VF0I
         LH    HLFWD,XHOLD+8           HIGH LIMIT HEAD ADDR        VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PHOLD+21(3),UNPKFLD+2                               VF0I
         MVC   0(24,WORK3),PHOLD        MOVE EXTENTS TO PRINT AREA VF0I
         LA    WORK3,28(WORK3)                                     VF0I
         BAL   RETURN,MYRTN        -* TO SPACE CALCULATION ROUTINE.VF01
         CLI   XHOLD+1,X'0F'       -* IS THIS THE LAST ONE IN FM3  VF01
         BNL   CHKWRT
UPCOMP   C     WORK1,HOLDEND            PROCESSED ALL EXTENTS      VF0I
         BNL   CHKWRT                                              VF0I
         LA    WORK1,10(WORK1)          UP EXTENT POINTER          VF0I
         B     NEXTEXT                  GET NEXT EXTENT            VF0I
CHKWRT   CLI   PRINT1,X'01'   ANY TO BE PRINTED IN PRINT LINE      VF0I
         BNE   CONVADD                  NO                         VF0I
         BAL   RETURN,LINEPR                                       VF0I
CONVADD  MVI   EXTNO,X'04'             EXTENTS ARE PRESENT      YA00067
         B     MORE                    EMPTY SPACE CALC          PM0405
PRNTEXT  BAL   RETURN,LINEPR            PRINT LINE                 VF0I
         MVI   PRINT1,X'00'             RESET PRINT SWITCH         VF0I
         LA    WORK3,PRINT+21      -* SET THE PRINT POSITION.      VF01
         B     CONTPRT                  RETURN TO CONVERT RTN      VF0I
DFORMAT4 ST    RETURN,SAVE4A                                       VF0I
         MVC   HANKOW+2(2),DSCBIN+18  -* SAVE THE NUMBER OF CYLINDEVF01
         MVC   HANKOW+6(2),DSCBIN+20  -* SAVE THE NO OF TRACKS.    VF01
         MVC   HANKOW+10(2),DSCBIN+22  -* SAVE THE NO OF BYTES.    VF01
         TM    SW,DSNAMESW                                         VF01
         BO    DPARTIAL                                            VF01
         LA    MSGFROM,212              FMT4A                      VF0I
         LA    MSGTO,PRINT         -* SET UP THE PRINT POSITION.   VF01
         BAL   MSGRET,MVMSG                                        VF0I
         MVI   ZSW,X'02'                                           VF0I
         BAL   RETURN,LINEPR                                       VF0I
         LA    MSGFROM,216              FMT4B                      VF0I
         LA    MSGTO,PRINT+11
         BAL   MSGRET,MVMSG                                        VF0I
         BAL   RETURN,LINEPR                                       VF0I
         UNPK  PRINT+11(3),DSCBIN+14(2) VTOC INDICATORS
         MVI   PRINT+13,X'EF'
         TR    PRINT+11(3),TRANSLAT
         SR    ONEBYTE,ONEBYTE                                     VF0I
         LH    HLFWD,DSCBIN+6          NO. AVAILABLE DSCS          VF0I
         BAL   RETURN,CONVCH                                       VF0I
         MVC   PRINT+16(5),UNPKFLD                                 VF0I
         IC    ONEBYTE,DSCBIN+30        MAX NO. DSCB PER TRACK     VF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   PRINT+27(3),UNPKFLD+2                               VF0I
         IC    ONEBYTE,DSCBIN+31   MAX NO. FULL DRCTRY BLKS PER TRKVF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   PRINT+38(3),UNPKFLD+2                               VF0I
         LH    HLFWD,DSCBIN+12         NO. ALT TRACKS AVAILABLE    VF0I
         BAL   RETURN,CONVCH                                       VF0I
         MVC   PRINT+47(5),UNPKFLD                                 VF0I
         LH    HLFWD,DSCBIN+8          CYL ADDR OF NEXT ALT TRACK  VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PRINT+56(3),UNPKFLD+2                               VF0I
         LH    HLFWD,DSCBIN+10         HEAD ADDR OF NEXT ALT TRACK VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PRINT+60(3),UNPKFLD+2                               VF0I
         MVC   XHOLD(2),DSCBIN+1     CYL ADDR OF LAST ACTIVE FMT1  VF0I
         BAL   RETURN,CONVA                                        VF0I
         MVC   PRINT+79(3),UNPKFLD+2                               VF0I
         MVC   XHOLD(2),DSCBIN+3  HEAD ADDR OF LAST ACTIVE FMT1    VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVA                                        VF0I
         MVC   PRINT+83(3),UNPKFLD+2                               VF0I
         IC    ONEBYTE,DSCBIN+5   RECORD ADDR OF LAST ACTIVE FMT1  VF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   PRINT+87(3),UNPKFLD+2                               VF0I
         BAL   RETURN,CONVDSCB          CONVERT ADDR OF THE DSCB   VF0I
         MVC   PRINT+109(11),PHOLD                                 VF0I
         MVC   XHOLD(2),DSCBIN+63       LOW CYL OF VTOC EXTENT     VF0I
         BAL   RETURN,CONVA                                        VF0I
         MVC   PRINT+92(3),UNPKFLD+2                               VF0I
         MVC   XHOLD(2),DSCBIN+65       LOW HEAD OF VTOC EXTENT    VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVA                                        VF0I
         MVC   PRINT+96(3),UNPKFLD+2                               VF0I
         MVC   XHOLD(2),DSCBIN+67       HIGH CYL OF VTOC EXTENT    VF0I
         BAL   RETURN,CONVA                                        VF0I
         MVC   PRINT+100(3),UNPKFLD+2                              VF0I
         MVC   XHOLD(2),DSCBIN+69       HIGH HEAD OF VTOC EXTENT   VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVA                                        VF0I
         MVC   PRINT+104(3),UNPKFLD+2                              VF0I
         CLC   CLEAR(5),DSCBIN+56       POINTER TO FORMAT 6 DSCB   VF0I
         BE    PRT4                                                VF0I
         LH    HLFWD,DSCBIN+56         CYL OF  FMT 6 DSCB          VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PRINT+66(3),UNPKFLD+2                               VF0I
         LH    HLFWD,DSCBIN+58         HEAD ADDR OF FMT 6 DSCB     VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PRINT+70(3),UNPKFLD+2                               VF0I
         IC    ONEBYTE,DSCBIN+60        RECORD ADDR OF FMT 6 DSCB  VF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   PRINT+74(3),UNPKFLD+2                               VF0I
PRT4     BAL   RETURN,LINEPR       -* GO TO PRINT A LINE.          VF01
         MVC   SAVECCHH(5),WORKIN+8     SAVE ADDR OF THIS RECORD   VF0I
DCHAIN   CLC   CLEAR(5),READIN+100                                 VF0I
         BE    GETOUT                                              VF0I
         SR    WORK2,WORK2                                         VF0I
         IC    WORK2,READIN+104         PUT RECORD ADDR IN REG 3   VF0I
         BCTR  WORK2,0                  DECREMENT REGISTER BY 1    VF0I
         STC   WORK2,READIN+104                                    VF0I
         MVC   WORKIN+8(5),READIN+100                              VF0I
         BAL   WORK2,DREAD                                         VF0I
         BAL   RETURN,DFMT56                                       VF0I
         B     REPEAT                   USE TEST FOR MULTIPLE    YM5559
*                                       FORMAT5 TO ALSO TEST     YM5559
*                                       FOR MULTIPLE FORMAT6     YM5559
GETOUT   MVC   WORKIN+8(5),SAVECCHH                                VF0I
         L     RETURN,SAVE4A                                       VF0I
         BR    RETURN                                              VF0I
DFMT56   ST    RETURN,SAVE1                                        VF0I
         LA    MSGFROM,72                                          VF0I
         LA    MSGTO,0                                             VF0I
         BAL   MSGRET,MVMSG                                        VF0I
         MVC   PRINT+8(1),READIN+44                                VF0I
         CLI   READIN+44,FORMAT6                                   VF0I
         BE    *+12                                                VF0I
         LA    MSGFROM,220              FMT5A                      VF0I
         B     *+8                                                 VF0I
         LA    MSGFROM,232              FMT6A                      VF0I
         LA    MSGTO,PRINT+19                                      VF0I
         BAL   MSGRET,MVMSG                                        VF0I
         MVI   ZSW,X'02'                                           VF0I
         BAL   RETURN,LINEPR                                       VF0I
         LA    MSGFROM,224              FMT5B                      VF0I
         LA    MSGTO,PRINT+6                                       VF0I
         BAL   MSGRET,MVMSG                                        VF0I
         BAL   RETURN,LINEPR                                       VF0I
         LA    MSGFROM,228              FMT5C                      VF0I
         LA    MSGTO,PRINT+6                                       VF0I
         BAL   MSGRET,MVMSG                                        VF0I
         BAL   RETURN,LINEPR                                       VF0I
         SR    ONEBYTE,ONEBYTE                                     VF0I
         MVC   HOLDAREA(40),READIN+4                               VF0I
         MVC   HOLDAREA+40(90),READIN+45                           VF0I
         MVI   PHOLD,X'40'              BLANK OUT PHOLD
         MVC   PHOLD+1(22),PHOLD
         SR    WORK1,WORK1              INITIALIZE REGISTERS TO 0  VF0I
         LR    WORK3,WORK1                                         VF0I
         LA    WORK1,HOLDAREA                                      VF0I
         LA    WORK3,PRINT+5                                       VF0I
         MVI   PRINT1,X'00'                                        VF0I
NXTEXT5  CLC   ZEROS(5),0(WORK1)                                   VF0I
         BE    CHKWRT5                                             VF0I
         C     WORK3,MAX                END OF PRINT LINE REACHED  VF0I
         BH    PRTEXT5                  YES - PRINT LINE           VF0I
CONTPT5  MVI   PRINT1,X'01'                                        VF0I
         MVC   XHOLD(5),0(WORK1)                                   VF0I
         LH    HLFWD,XHOLD             RELATIVE TRACK ADDR         VF0I
         BAL   RETURN,CONVCH                                       VF0I
         MVC   PHOLD(5),UNPKFLD                                @ZA01652
         LH    HLFWD,XHOLD+2           NO. OF FULL CYLS            VF0I
         BAL   RETURN,CONVCH            FMT5 = FULL CYL AVAILABLE  VF0I
         MVC   PHOLD+8(3),UNPKFLD+2    FMT6 # FULL CYL BEING SHAREDVF0I
         IC    ONEBYTE,XHOLD+4                                     VF0I
         BAL   RETURN,CONVB             FMT5 = # OF ADDITIONAL TRKSVF0I
         MVC   PHOLD+13(3),UNPKFLD+2 FMT6 = # DATA SETS SHARING EXTVF0I
         MVC   0(16,WORK3),PHOLD        MOVE TO PRINT AREA         VF0I
         LA    WORK3,19(WORK3)                                     VF0I
UPADD    LA    WORK1,5(WORK1)                                    A38019
         C     WORK1,HOLDEND                                     A38019
         BNL   CHKWRT5                                             VF0I
         B     NXTEXT5                  GET NEXT EXTENT            VF0I
CHKWRT5  CLI   PRINT1,X'01'   ANY EXTENTS LEFT IN PRINT            VF0I
         BNE   CONV5          NO                                   VF0I
         BAL   RETURN,LINEPR  YES - PRINT THEM                     VF0I
CONV5    LA    MSGFROM,208              FMT3B                      VF0I
         MVI   ZSW,X'02'           -* PRINT A BLANK LINK.          VF01
         LA    MSGTO,PRINT+17                                      VF0I
         BAL   MSGRET,MVMSG                                        VF0I
         BAL   RETURN,CONVDSCB          CONVERT ADDR OF THE DSCB   VF0I
         MVC   PRINT+29(11),PHOLD                                  VF0I
         BAL   RETURN,LINEPR                                       VF0I
         MVI   ZSW,X'02'           -* PRINT A BLANK LINK.          VF01
         LA   MSGFROM,240          -* PRINT A MSG.                 VF01
         LA    MSGTO,0             -* SET UP PRINT POSITION.       VF01
         BAL   MSGRET,MVMSG        -* MOVE MSG TO PRINT AREA.      VF01
         BAL   RETURN,LINEPR       -* GO TO PRINT A LINE.          VF01
         L     RETURN,SAVE1                                        VF0I
         BR    RETURN                                              VF0I
UN23051  MVC   CONST1(2),C634          MOVE FORMULA CONST
         MVC   CONST2(2),C634          MOVE FORMULA CONST
         MVC   CONST3(2),C432          MOVE FORMULA CONST
         B     CMNCAL                  B TO DO BLKS LEFT CALC
UN23052  MVC   CONST1(2),C289          MOVE FORMULA CONSTANT
         MVC   CONST2(2),C289          MOVE FORMULA CONSTANT
         MVC   CONST3(2),C198          MOVE FORMULA CONSTANT
         B     CMNCAL                  B TO DO BLKS LEFT CALC
UN3350   MVC   CONST1(2),C267          MOVE FORMULA CONSTANT   @Z30RSAG
         MVC   CONST2(2),C267          MOVE FORMULA CONSTANT   @Z30RSAG
         MVC   CONST3(2),C185          MOVE FORMULA CONSTANT   @Z30RSAG
         B     CMNCAL                                          @Z30RSAG
UN3330   MVC   CONST1(2),C193          MOVE FORMULA CONSTANT
         MVC   CONST2(2),C193          MOVE FORMULA CONSTANT
         MVC   CONST3(2),C135          MOVE FORMULA CONSTANT
         B     CMNCAL                  B TO DO BLKS LEFT CALC   YL026VC
UN3340   MVC   CONST1(2),C242          MOVE FORMULA CONSTANT    YL026VC
         MVC   CONST2(2),C242          MOVE FORMULA CONSTANT    YL026VC
         MVC   CONST3(2),C167          MOVE FORMULA CONSTANT    YL026VC
CMNCAL   L     WORK4,C1000             MULTIPLYINF FACTOR
         LTR   WORK2,WORK1             IS THE KEY LENGTH 0
         BC    8,NOKEYH                BRANCH TO KEY EQUAL 0
         A     WORK2,SAVER             ADD BLOCKSIZE TO KEY LEN.
         BAL   RETURN,BLKCAL           GO TO BLOCK CALC
         AH    WORK1,KEYLE1            ADD KEY LENGTH
         AH    WORK1,CONST1            ADD FORMULA CONSTANT
         AH    WORK3,CONST2            ADD FORMULA CONSTANT
         B     BLKLEFT
NOKEYH   L     WORK2,SAVER             LOAD BLOCKSIZE
         BAL   RETURN,BLKCAL           GO TO BLK CALC
         AH    WORK3,CONST3            ADD FORMULA CONSTANT
         AH    WORK1,CONST3            LENGTH LAST RECORD
         B     BLKLEFT                 GO TO REMAINING BLK CALC
UN2314   L     WORK4,C1043             MULTIPLYING FACTOR        A29716
         LTR   WORK2,WORK1             IS THE KEY LENGTH 0       A29716
         BC    8,NOKEYA                BRANCH TO KEY EQUAL 0     A29716
         A     WORK2,SAVER             ADD BLOCKSIZE TO KEY LEN. A29716
         BAL   RETURN,BLKCAL           GO TO BLOCK CALCULATION   A29716
         AH    WORK1,KEYLE1            ADD KEY LEN.              A29716
         AH    WORK1,C45               ADD FORMULA CONSTANT      A29716
         AH    WORK3,C146              ADD FORMULA CONSTANT      A29716
         B     BLKLEFT                 GO TO REMAINING BLK CALC  A29716
NOKEYA   L     WORK2,SAVER             LOAD BLOCKSIZE            A29716
         BAL   RETURN,BLKCAL           GO TO BLK. CALC.          A29716
         AH    WORK3,C101              ADD FORMULA CONSTANT      A29716
         B     BLKLEFT                 GO TO REMAINING BLK CALC  A29716
UN2311   L     WORK4,C1049             MULTIPLYING FACTOR        A29716
         LTR   WORK2,WORK1             IS THE KEY LENGTH 0       A29716
         BC    8,NOKEYB                BRANCH TO KEY EQUAL 0     A29716
         A     WORK2,SAVER             ADD BLOCKSIZE TO KEY LEN. A29716
         BAL   RETURN,BLKCAL           GO TO BLOCK CALCULATION   A29716
         AH    WORK1,KEYLE1            ADD KEY LEN.              A29716
         AH    WORK1,C20               ADD FORMULA CONSTANT      A29716
         AH    WORK3,C81               ADD FORMULA CONSTANT      A29716
         B     BLKLEFT                 GO TO REMAINING BLK CALC  A29716
NOKEYB   L     WORK2,SAVER             LOAD BLOCKSIZE            A29716
         BAL   RETURN,BLKCAL                                     A29716
         AH    WORK3,C61               ADD FORMULA CONSTANT      A29716
         B     BLKLEFT                 GO TO REMAINING BLK CALC  A29716
UN2303   L     WORK4,C1000             MULTIPLYING FACTOR        A29716
         LTR   WORK2,WORK1             IS THE KEY LENGTH 0       A29716
         BC    8,NOKEYC                BRANCH TO KEY EQUAL 0     A29716
         A     WORK2,SAVER             ADD BLOCKSIZE TO KEY LEN. A29716
         BAL   RETURN,BLKCAL           GO TO BLOCK CALCULATION   A29716
         AH    WORK1,KEYLE1            ADD KEY LEN.              A29716
         AH    WORK1,C38               ADD FORMULA CONSTANT      A29716
         AH    WORK3,C146              ADD FORMULA CONSTANT      A29716
         B     BLKLEFT                 GO TO REMAINING BLK CALC  A29716
NOKEYC   L     WORK2,SAVER             LOAD BLOCKSIZE            A29716
         BAL   RETURN,BLKCAL                                     A29716
         AH    WORK3,C108              ADD FORMULA CONSTANT      A29716
         B     BLKLEFT                 GO TO REMAINING BLK CALC  A29716
UN2301   L     WORK4,C1000             MULTIPLYING FACTOR        A29716
         LTR   WORK2,WORK1             IS THE KEY LENGTH 0       A29716
         BC    8,NOKEYD                BRANCH TO KEY EQUAL 0     A29716
         A     WORK2,SAVER             ADD BLOCKSIZE TO KEY LEN. A29716
         BAL   RETURN,BLKCAL           GO TO BLOCK CALCULATION   A29716
         AH    WORK1,KEYLE1            ADD KEY LEN.              A29716
         AH    WORK1,C53               ADD FORMULA CONSTANT      A29716
         AH   WORK3,C186               ADD FORMULA CONSTANT      A29716
         B     BLKLEFT                 GO TO REMAINING BLK CALC  A29716
NOKEYD   L     WORK2,SAVER             LOAD BLOCKSIZE            A29716
         BAL   RETURN,BLKCAL                                     A29716
         AH    WORK3,C133              ADD FORMULA CONSTANT      A29716
         B     BLKLEFT                 GO TO REMAINING BLK CALC  A29716
UN2321   L     WORK4,C1049             MULTIPLYING FACTOR        A29716
         LTR   WORK2,WORK1             IS THE KEY LENGTH 0       A29716
         BC    8,NOKEYE                BRANCH TO KEY EQUAL 0     A29716
         A     WORK2,SAVER             ADD BLOCKSIZE TO KEY LEN. A29716
         BAL   RETURN,BLKCAL           GO TO BLOCK CALCULATION   A29716
         AH    WORK1,KEYLE1            ADD KEY LEN.              A29716
         AH    WORK1,C16               ADD FORMULA CONSTANT      A29716
        AH    WORK3,C100              ADD FORMULA CONSTANT       A29716
         B     BLKLEFT                 GO TO REMAINING BLK CALC  A29716
NOKEYE   L     WORK2,SAVER             LOAD BLOCKSIZE            A29716
         BAL   RETURN,BLKCAL                                     A29716
         AH    WORK3,C84               ADD FORMULA CONSTANT      A29716
         B     BLKLEFT                 GO TO REMAINING BLK CALC  A29716
BLKCAL   SR    WORK1,WORK1             ZERO REGISTER FOR DIVIDE  A29716
         MR    WORK1,WORK4             MULT BY FORMULA CONSTANT  A29716
         D     WORK1,C1000             MOVE DECIMAL 3 DIGITS     A29716
         LR    WORK3,WORK2             SAVE VALUE                A29716
         L     WORK2,HANKOW+8          LOAD TRACK SIZE           A29716
         L     WORK1,SAVER             LOAD BLOCKSIZE            A29716
         BR    RETURN                                            A29716
PRTEXT5  BAL   RETURN,LINEPR                                       VF0I
         MVI   PRINT1,X'00'             RESET PRINT SWITCH         VF0I
         LA    WORK3,PRINT+5            RESET PRINT ADDR           VF0I
         B     CONTPT5                  RETURN                     VF0I
*  CONVB IS LINKED TO WHENEVER ONE BYTE OF DATA REQUIRES CONVERSIONVF0I
CONVB    ST    RETURN,SAVE2       -* SAVE RETURN ADDRESS.          VF01
         CVD   ONEBYTE,CONVERT                                     VF0I
         UNPK  UNPKFLD(5),CONVERT+5(3)                             VF0I
         TM    FMT,X'0C'              IS IT HEAD ADDR FOR 2321     VF0I
         BO    BYPASSSP               BYPASS SUPRESSION OF ZEROES  VF0I
         BAL   RETURN,PEKING       -* GO TO GET RID LEADING ZERO   VF01
BYPASSSP OI    UNPKFLD+4,X'F0'                                     VF0I
         L     RETURN,SAVE2       -* RELOAD RETURN ADDRESS.        VF01
         BR    RETURN                                              VF0I
*                                                                  VF0I
*   CONVCA IS LINKED TO WHEN                                       VF0I
*        1.  A HALFWORD OF DATA, NOT ON A HALFWORD BUNDRY, REQUIRESVF0I
*            CONVERSION AND                                        VF0I
*        2.  IS NOT PART OF AN ADDRESS AND THERFORE REQUIRES       VF0I
*            NO SPECIAL HANDLING  IF THE DEVICE IS A 2321          VF0I
*   CONVCH IS LINKED TO WHEN                                       VF0I
*        1. A HALFWORD OF DATE, ON A HALFWORD BOUNDARY REQUIRES    VF0I
*            CONVERSION AND                                        VF0I
*        2.  IS NOT PART OF AN ADDRESS AND THEREFORE REQUIRES      VF0I
*            NO SPECIAL HANDLING IF THE DEVICE IS A 2321.          VF0I
CONVCA   ST    RETURN,RET                                          VF0I
         LH    HLFWD,XHOLD                                         VF0I
CONVCH   ST    RETURN,RET                                          VF0I
         N     HLFWD,HBCLEAR      CLEAR THE 2 HIGH ORDER BYTES @YA02531
         CVD   HLFWD,CONVERT                                       VF0I
         UNPK  UNPKFLD(5),CONVERT+5(3)                             VF0I
         BAL   RETURN,PEKING       -* GO TO GET RID LEADING ZERO   VF01
         OI    UNPKFLD+4,X'F0'                                     VF0I
         L     RETURN,RET                                          VF0I
         BR    RETURN                                              VF0I
*   CONVA IS LINKED TO WHEN                                        VF0I
*        1.  HALFWORD OF DATA, NOT ON PROPER BOUNDARY ALIGNMENT    VF0I
*            REQUIRES CONVERSION AND                               VF0I
*        2.  REQUIRES SPECIAL HANDLING IF DEVICE IS 2321           VF0I
*   CONVH IS LINKED TO WHEN                                        VF0I
*        1.  HALFWORD OF DATA, ON PROPER BOUNDARY ALIGNMENT        VF0I
*            REQUIRES CONVERSION AND                               VF0I
*        2.  DATA REQUIRES SPECIAL HANDLING IF DEVICE IS 2321      VF0I
CONVA    ST    RETURN,RET                                          VF0I
         LH    HLFWD,XHOLD                                         VF0I
CONVH    ST    RETURN,RET                                          VF0I
         N     HLFWD,HBCLEAR      CLEAR THE 2 HIGH ORDER BYTES @YA02531
         TM    FMT,X'04'                IS DEVICE 2321             VF0I
         BZ    DREGCONV                                            VF0I
         TM    FMT,X'08'                                           VF0I
         BO    REVERSE            TRANSPOSE THE 2 BYTES IN HLFWD   VF0I
         STH   HLFWD,WK2321                                        VF0I
C2321A   IC    ONEBYTE,WK2321     PICK UP SUBCELL OR TRACK ADDR    VF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   HLD2321(2),UNPKFLD+3 SAVE SUBCEL/TRK ADDR FOR 2321  VF0I
         IC    ONEBYTE,WK2321+1                                    VF0I
         BAL   RETURN,CONVB                                        VF0I
         TM    FMT,X'08'          TRANSPOSE UNPKFLD FOR PRINTING   VF0I
         BO    REVERSEA                                            VF0I
         MVC   UNPKFLD+2(2),HLD2321 COMBINE SUBCELL-STRIP FOR PRINTVF0I
C2321B   NI    FMT,X'F7'                                           VF0I
         B     CONVH1                                              VF0I
DREGCONV CVD   HLFWD,CONVERT                                       VF0I
         UNPK  UNPKFLD(5),CONVERT+5(3)                             VF0I
         BAL   RETURN,PEKING       -* GO TO GET RID LEADING ZERO   VF01
         OI    UNPKFLD+4,X'F0'                                     VF0I
CONVH1   L     RETURN,RET                                          VF0I
         BR    RETURN                                              VF0I
REVERSE  STC   HLFWD,WK2321            INVERT THE 2 BYTES OF HLFWD VF0I
         SRL   HLFWD,8                 SO ROUTINE CAN BE USED TO   VF0I
         STC   HLFWD,WK2321+1          CONVERT 2321 ADDRESSES      VF0I
         B     C2321A                                              VF0I
REVERSEA MVC   UNPKFLD+2(1),UNPKFLD+4   REARRANGE CHARS FOR PRINT  VF0I
         MVC   UNPKFLD+3(2),HLD2321     COMBINE CYLINDER AND TRACK VF0I
         B     C2321B                                              VF0I
CONVDSCB ST    RETURN,DSCBRET                                      VF0I
         MVI   PHOLD,X'40'              BLANK OUT PHOLD
         MVC   PHOLD+1(10),PHOLD
         LH    HLFWD,WORKIN+8           CONVERT CYLINDER ADDR      VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PHOLD(3),UNPKFLD+2                                  VF0I
         LH    HLFWD,WORKIN+10          CONVERT HEAD ADDR          VF0I
         OI    FMT,X'08'                                           VF0I
         BAL   RETURN,CONVH                                        VF0I
         MVC   PHOLD+4(3),UNPKFLD+2                                VF0I
         IC    ONEBYTE,WORKIN+12                                   VF0I
         BAL   RETURN,CONVB                                        VF0I
         MVC   PHOLD+8(3),UNPKFLD+2                                VF0I
         L     RETURN,DSCBRET                                      VF0I
         BR    RETURN                                              VF0I
MVEU     MVI   PRINT+101,C'U'           UNDEFINED RECORD FORMAT@G60ASBJ
         B     CHKTOFL                                             VF0I
MVEV     MVI   PRINT+101,C'V'                                  @G60ASBJ
         B     CHKTOFL                                             VF0I
MVEF     MVI   PRINT+101,C'F'                                  @G60ASBJ
         B     CHKTOFL                                             VF0I
MVEA     MVI   PRINT+105,C'A'                                  @G60ASBJ
         B     PRINTLN                 BRANCH TO PRINT LINE      A34989
MVEM     MVI   PRINT+105,C'M'                                  @G60ASBJ
         B     PRINTLN                 BRANCH TO PRINT LINE      A34989
ERRLL    SR    HLFWD,HLFWD             MAKE LL ZERO              A32555
         MVC   DSCBIN+57(2),ZEROS      PUT '0000'X IN DSCB-LL    A32555
         B     CONVLL                                              VF0I
TAICHUNG MVC   SAVE4(5),READIN+135   -* SAVE ADR OF FMT 3          VFOI
         MVC   READIN+135(5),SAVE3    -* FORCE NEXT TO READ FMT2.  VFOI
         NI    SAVE3+5,X'00'        -* CLEAR SWITCH.               VFOI
         OI    SAVE3+5,X'02' -* SWITCH TO NEXT DATA SET ADDRESS.   VFOI
         B     NOCOME             -* GO BACK TO REREAD.            VFOI
HUNAN    MVC   READIN+135(5),SAVE4  -* PTR TO NXT DATA SET         VFOI
         NI    SAVE3+5,X'00'       -* CLEAR SWITH.                 VFOI
         B     WRITE2              -* GO TO PRINT A LINE RTN.      VFOI
RET      DC    F'0'                                                VF0I
SAVE1    DC    F'0'                                                VF0I
SAVE2    DC    F'0'                                                VF01
SAVE3    DC    F'0'                                                VFOI
         DC    X'0000'                                             VFOI
SAVE4    DS    5X                                                  VFOI
KEYLE1   DC    H'0'                    KEY LENGTH OF DATA SET    A29716
B7294    DC    F'7294'                 BYTES PER TRK.--2314      A29716
B3625    DC    F'3625'                 B.P.T. ON DEVICE 2311     A29716
B4984    DC    F'4984'                 B.P.T. ON DEVICE 2302     A29716
B4892    DC    F'4892'                 B.P.T. ON DEVICE 2303     A29716
B2000    DC    F'2000'                 B.P.T. ON DATA CELL       A29716
B20483   DC    F'20483'                B.P.T. ON DEVICE 2301     A29716
B14568   DC    F'14568'                B.P.T. ON DEVICE 2305-1
B14858   DC    F'14858'                B.P.T. ON DEVICE 2305-2
B13165   DC    F'13165'                B.P.T. ON DEVICE 3330
B8368    DC    F'8368'                 B.P.T. ON DEVICE 3340    YL026VC
B19254   DC    F'19254'                B.P.T. ON DEVICE 3350   @Z30RSPC
C1049    DC    F'1049'                 FORMULA CONSTANT FOR 2311 A29716
C1043    DC    F'1043'                 FORMULA CONSTANT FOR 2314 A29716
C1000    DC    F'1000'                 FORMULA CONSTANT FOR 2303 A29716
***********************************************************************
*                                                                     *
*  THE FOLLOWINF HALFWORD CONSTANTS ARE CONSTANTS USED BY THE         *
*  FORMULAS FOR THE DIFFERENT DEVICES: 2314,2311,2301,2302,           *
*  2303, AND 2321,TO DETERMINE THE NUMBER OF BLOCKS ON ONE TRACK      *
*                                                                     *
***********************************************************************
CONST1   DC    H'0'                    USED FOR DEVICES HAVING
CONST2   DC    H'0'                    RECS/TRACK OF
CONST3   DC    H'0'                    (KL+DL+CONST)
C61      DC    H'61'                                             A29716
C101     DC    H'101'                                            A29716
C108     DC    H'108'                                            A29716
C133     DC    H'133'                                            A29716
C84      DC    H'84'                                             A29716
C20      DC    H'20'                                             A29716
C45      DC    H'45'                                             A29716
C38      DC    H'38'                                             A29716
C53      DC    H'53'                                             A29716
C16      DC    H'16'                                             A29716
C81      DC    H'81'                                             A29716
C146    DC    H'146'                                             A29716
C186     DC    H'186'                                            A29716
C100     DC    H'100'                                            A29716
C634     DC    H'634'                  FORMULA CONSTANT 2305-1
C432     DC    H'432'                  FORMULA CONSTANT 2305-1
C289     DC    H'289'                  FORMULA CONSTANT 2305-2
C198     DC    H'198'                  FORMULA CONSTANT 2305-2
C193     DC    H'193'                  FORMULA CONSTANT 3330
C135     DC    H'135'                  FORMULA CONSTANT 3330
C242     DC    H'242'                  FORMULA CONSTANT 3340    YL026VC
C167     DC    H'167'                  FORMULA CONSTANT 3340    YL026VC
C267     DC    H'267'                  FORMULA CONSTANT 3350   @Z30RSPC
C185     DC    H'185'                  FORMULA CONSTANT 3350   @Z30RSPC
HARBIN   OI    SAVE3+5,X'01'        -* SET SWITCH FOR FMT 1,2,3.   VFOI
         MVC   SAVE3(5),READIN+135   -* SAVE ADR OF FMT 2.         VFOI
         B     TENN          -* GO TO READ FMT 1.                  VFOI
*                                                               YA00066
*                    SPACE CALCULATIONS                         YA00066
*                                                               YA00066
TTRLL    DC    CL5'0'                SPACE CALC. WORK AREA      YA00066
EXTNO    DC    H'0'                  NUMBER OF EXTENTS          YA00067
KEYLENTH DC    H'0'                  KEY LENGTH OF D/S          YA00067
SAVE4A   DC    F'0'                                                VF0I
SAVL     DC    F'0'                    -* FOR SAVING CYLINDER ONLY.VF01
SAVT     DC    F'0'                    -* FOR SAVING NO OF TRACK.  VF01
HONGKONG DS    44X                -* FOR DSNAME SAVING.            VF01
SA       DC    F'0'               -*                               VF01
SAVERS   DC    F'0'                                                VF01
HANKOW   DC    F'0'                                                VF01
         DC    F'0'                                                VF01
         DC    F'0'                                                VF01
AREA     DC    F'0'                                                VF01
         DC    F'0'                                                VF01
SAV      DC    F'0'                                                VF01
MBLK     DC    C'EMPTY BLOCK(S).' -* MESSAGE FILLER.               VF01
MPLUS    DC    C'PLUS'            -* MESSAGE FILLER.               VF01
MCYL     DC    C'EMPTY CYLINDER(S)'    -* MESSAGE FILLER.          VF01
MTRK     DC    C'EMPTY TRACK(S).' -* MESSAGE FILLER.               VF01
ORG      DC    X'00'                                               VF01
CTB      DC    X'00'                                               VF01
SAVER    DC    F'0'                                                VF01
MYRTN    STM   KEEPRET,MSGTO,SAVE5     -* SAVE ALL REGISTERS       VFOI
         MVC   MBBCCHHR+3(4),XHOLD+2   -* MOVE CCHH FOR CONVERSION VFOI
         L    MSGRET,WORKIN+44    -* INFO FROM DCB                 VFOI
         LA    WORK1,MBBCCHHR     -* DATA AREA POINTED BY WORK1.   VFOI
         LA    WORK3,SAVE5   -* CONVERSION RTN NOT USES REG3.      VFOI
         ST    RECCNT,40(WORK3)   -* STORE UPDATED BASE REGISTER.  VFOI
         ST    BASEREG2,44(WORK3) -* STORE UPDATED BASE REGISTER.  VFOI
         L     MSGTO,16           -* LOAD THE CVT.                 VFOI
         L     MSGTO,32(MSGTO)         -* ACCESS THE CONVERSION RTNVFOI
         BALR  RETURN,MSGTO  -* BRANCH TO CONVERSION RTN.          VFOI
         L     RECCNT,40(WORK3)   -* RETURN THE BASE REGISTER.     VFOI
         L     BASEREG2,44(WORK3) -* RETURN THE BASE REGISTER.     VFOI
         L     BASEREG1,48(WORK3) -* RETURN THE BASE REGISTER.     VFOI
         SRL   KEEPRET,16         -* NO. OF TT IS RIGHT JUSTIFIED. VFOI
         ST    KEEPRET,SAVE6      -* SAVE NO. OF LOW CCHH.         VFOI
         MVC   MBBCCHHR+3(4),XHOLD+6   -* MOVE CCHH FOR CONVERSION VFOI
         L    MSGRET,WORKIN+44    -* INFO FROM DCB                 VFOI
         LA    WORK1,MBBCCHHR     -* DATA AREA POINTED BY WORK1.   VFOI
         ST    RECCNT,40(WORK3)   -* STORE UPDATED BASE REGISTER.  VFOI
         ST    BASEREG2,44(WORK3) -* STORE UPDATED BASE REGISTER.  VFOI
         L     MSGTO,16           -* LOAD THE CVT.                 VFOI
         L     MSGTO,32(MSGTO)         -* ACCESS THE CONVERSION RTNVFOI
         BALR  RETURN,MSGTO  -* BRANCH TO CONVERSION RTN.          VFOI
         L     RECCNT,40(WORK3)   -* RETURN THE BASE REGISTER.     VFOI
         L     BASEREG2,44(WORK3) -* RETURN THE BASE REGISTER.     VFOI
         L     BASEREG1,48(WORK3) -* RETURN THE BASE REGISTER.     VFOI
         SRL   KEEPRET,16         -* NO. OF TT IS RIGHT JUSTIFIED. VFOI
         S     KEEPRET,SAVE6      -* SUBTRACT LOW TT FROM THE HIGH.VFOI
         LR   WORK2,KEEPRET       -* LOAD NEW REG FOR INCREMENT.   VFOI
         LA    WORK2,1(WORK2)     -* INCREMENT BY ONE.             VFOI
         A     WORK2,SAVT         -* ADD TO TRACK COUNTER.         VFOI
         ST    WORK2,SAVT         -* STORE TRACK COUNTER.          VFOI
         ST    RECCNT,40(WORK3)   -* STORE UPDATED BASE REGISTER.  VFOI
         ST    BASEREG2,44(WORK3) -* STORE UPDATED BASE REGISTER.  VFOI
         LM    KEEPRET,MSGTO,SAVE5     -* RETURN THE CONTENT OF REGVFOI
         BR    RETURN             -* BACK WHERE IT BELONGS.        VFOI
SAVE5    DS    16F                -* FOR SAVING REGISTERS.         VFOI
MBBCCHHR DC    F'0'               -* WORK AREA FOR CONVERSION RTN. VFOI
         DC    F'0'               -* WORK AREA FOR CONVERSION RTN. VFOI
SAVE6    DC    F'0'                -*                              VFOI
HBCLEAR  DC    X'0000FFFF'                                     @YA02531
         END   ,                                                   VF0I
