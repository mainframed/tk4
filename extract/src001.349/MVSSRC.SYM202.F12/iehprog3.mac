   TITLE  'IEHPROGM - IEHPROG3  MESSAGE WRITER MODULE'
***********************************************************************
*                                                                     *
* MODULE NAME -- IEHPROG3                                      YL026VC*
*                                                              YL026VC*
* DESCRIPTIVE NAME -- IEHPROGM MESSAGE WRITER MODULE           YL026VC*
*                                                              YL026VC*
* COPYRIGHT -- NONE                                            YL026VC*
*                                                              YL026VC*
* STATUS -- 'IEHPROG3'  OS/VS2 PROLOGUE MODIFICATION           YL026VC*
*    NOVEMBER 1972                                             YL026VC*
*                                                              YL026VC*
* FUNCTION -- THIS MODULE IS THE GENERALIZED MESSAGE WRITER FOR       *
*    IEHPROGM.                                                        *
*                                                                     *
* ENTRY POINTS --                                                     *
*         IEHPROG3 IS THE ONLY ENTRY POINT FOR THIS MODULE.  IT IS    *
*    USED WHETHER DIAGNOSTIC MESSAGES OR INFORMATIONAL MESSAGES       *
*    ARE TO BE PRINTED.                                               *
*                                                                     *
* INPUT --INPUT TO IEHPROG3 IS TWOFOLD.  FIRST, REGISTER 0 WILL       *
*    CONTAIN ALL INFORMATION AS TO WHETHER THE MESSAGE TO BE          *
*    PRINTED IS AN INFORMATIONAL MESSAGE OR A DIAGNOSTIC MESSAGE,     *
*    AND SECONDLY, IF THE MESSAGE IS INFORMATIONAL THE MESSAGE        *
*    TEXT WILL RESIDE IN AN AREA IN A COMMUNICATION REGION.  IF THE   *
*    MESSAGE IS IN THIS COMMUNICATION AREA, REGISTER 0 WILL CONTAIN   *
*    INFORMATION TO DETERMINE WHERE THE MESSAGE IS LOCATED.           *
*                                                                     *
* OUTPUT -- THE OUTPUT FROM THIS PART OF IEHPROGM IS THE WRITING      *
*    OR PRINTING OF THE MESSAGE REQUESTS FROM THE OTHER MODULES       *
*    OF IEHPROGM.                                                     *
*                                                                     *
* EXTERNAL REFERENCES -- THIS ROUTINE HAS NO EXTERNAL ROUTINES.       *
*                                                                     *
* EXITS, NORMAL -- THIS MODULE DOES A BRANCH ON REGISTER 15 TO        *
*    RETURN TO ITS CALLING MODULE.                                    *
*                                                                     *
* EXITS, ERROR -- THIS MODULE DOES A BRANCH ON REGISTER 15 TO         *
*    RETURN TO ITS CALLING MODULE.                                    *
*                                                                     *
* TABLES/WORK AREA --                                                 *
*    DSECT IS A COMMON COMMUNICATIONS AND WORK AREA POINTED TO BY     *
*    REGISTER 12 AND DESCRIBED BY A DSECT NAMED DSECT AND BEGINNING   *
*    IEHECHAR.                                                        *
*    MSGADS IS A TABLE THAT IS SPLIT INTO TWO GENERAL PARTS.  THE     *
*    FIRST PART IS A TABLE OF ADDRESS CONSTANTS OF THE MESSAGES       *
*    THEMSELVES.  THE MESSAGES THEN MAKE UP THE SECOND PART OF THE    *
*    TABLE.  ALSO INTEGRATED WITHIN THE TABLE OF ADCONS ARE           *
*    INSTRUCTIONS WHICH ARE EXECUTED IF THE MESSAGE REQUESTS          *
*    ARE FOR INFORMATIONAL MESSAGES.                                  *
*                                                                     *
* ATTRIBUTES -- THIS MODULE OPERATES AS ONE LEG OF AN OVERLAY         *
*    STRUCTURE.                                                       *
*                                                                     *
* CHARACTER CODE DEPENDENCY -- THIS MODULE MUST BE ASSEMBLED IN       *
*    EBCDIC AND RUN IN EBCDIC TO INSURE COLLATING SEQUENCE AND        *
*    INTERNAL REPRESENTATION OF CERTAIN CHARACTERS.                   *
*                                                                     *
* NOTES -- APPROXIMATELY 3400 BYTES OF STORAGE ARE REQUIRED FOR       *
*    CODE.                                                            *
*                                                                     *
* CHANGE ACTIVITY -- FOLLOWS PROLOGUE                          YL026VC*
*                                                              YL026VC*
***********************************************************************
IEHPROG3 CSECT                                                    M2838
*             VS2 SU32 DELETIONS                               @G32DSFS
*D129740,241890-241910,241930-241960                           @G32DSFS
*                                                              @G32DSFS
*C234000-234100                                                 YL026VC
*A87240                                                          YM5077
*A45000,70366-70426                                              A57261
*A44500,70354-70370                                              YM5076
*C400,14800                                                       M2838
*A44000,70310-70370                                              A50977
*                                                                  I276
*1051070200                                                        BS0H
*2353                                                              DT0I
*                                                                A23466
*                                                                A25564
*                                                                A24020
*                                                                A28562
*                                                                A28552
*A1600-1940,2600,27100-27200                                    YL026VC
*C2000-2500                                                     YL026VC
*A 198500                                                       YA01707
*C 198500,199500                                                YA01707
*A 1981000                                                     @ZA01675
*D 199000                                                      @ZA01675
*C234000-234100                                                @VS30028
*                                                              @Z40RSDS
*                   *****     REGISTER  USAGE     *****
ZERO     EQU   0        ----------------------------------------
R1       EQU   12
R2       EQU   2
R3       EQU   3             REGISTERS ZERO THROUGH
R4       EQU   4             TEN ARE WORK REGISTERS
R5       EQU   5
R6       EQU   6             REGESTERS ELEVEN AND
R7       EQU   7             TWELVE ARE BASE REGISTERS
R8       EQU   8             FOR THE PROGRAN AND THE
R9       EQU   9             DUMMY SECTION RESPECTIVELY
R10      EQU   10
RB       EQU   11            THIRTEEN POINTS TO A REGISTER
RBD      EQU   12
SAVEREG  EQU   13            SAVE AREA
RETURN   EQU   14
LINKREG  EQU   15            FIFTEEN AND FOURTEEN ARE LINK
PARAMREG EQU   0             AND RETURN REGISTERS RESPECTIVELY
LISTREG  EQU   1        -----------------------------------------
*
         USING IEHECHAR,R1
         SAVE  (14,12)            SAVE REGISTERS                  M2838
         ST    PARAMREG,IEHERSAV  SAVE PARAMETER LIST             M2838
         BALR  RB,0                ESTABLISH ADDRESSABILITY
         USING *,RB
         ST    SAVEREG,SAVEREG3+4 KEEP OLD SAVE AREA              M2838
         LA    SAVEREG,SAVEREG3   ESTABLISH NEW SAVE AREA         M2838
         TM    IEHERSAV+1,X'02'
         BZ    NOSEARCH
         LA    R2,MSGOUT+110       FIND  DISPLACEMENT NEEDED
         LA    R3,112
         CLI   0(R2),C' '          TO  PLACE THIS MESSAGE
         BNE   *+10
         BCTR  R2,0                2 BYTES ABOVE THE LAST
         BCT   R3,*-10
         STC   R3,IEHERSAV+2       CHARACTER IN THE OUTPUT AREA
NOSEARCH EQU   *
         LA    R4,255
         NR    R4,PARAMREG
         LA    R5,MSGADS
        SLA   R4,2          MSG NUM. TIMES 4 AND SET CONDITION CODE
         BZ    WRITENOW                MSG IS IN OUT AREA WRITE IT
         AR    R4,R5
         LA    R8,WTO              BE PREPARED FOR A WTO
         L     6,0(R4)             IF WE DO NOT HAVE AN ADCON
         TM    0(R4),X'41'         FOR THE MESSAGE
         BZ    *+8                 EXECUTE THE LOAD ADDRESS
         EX    0,0(R4)             TO GET IT.
         SR    R4,R4               CLEAR REGISTER 4
         IC    R4,0(R6)            PUT MESSAGE LENGTH INTO IT
         SR    R7,R7
         IC    R7,IEHERSAV+2
         LA    R7,MSGOUT(R7)
         EX    R4,MOVEMSG
         TM    IEHERSAV+1,X'01'   SHOULD WE WRITE THIS MESSAGE ?
         BO    MSGEXIT            NO --- BRANCH
WRITENOW LA    R2,MSGOUT-1
         MVI   MSGOUT-1,C' '       SET ASA FOR SINGLE SPACE
         EX    0,WRITEIT
         MVC   MSGOUT(121),MSGOUT-1     CLEAR THE BUFFER
         LH    R3,LINECNT
         LA    R3,1(R3)
         CH    R3,MAXLINES
         BH    EJECT
         STH   R3,LINECNT
         B     CLEARMSG
WRITEIT  BAL   R5,*+4
         PUT   UTOUTDCB,(R2)
         BR    R5
EJECT    EQU   *
         LA    R2,WRITEHDR
         EX    0,WRITEIT           WRITE HEADER AND SPACE FORMS
         MVI   LINECNT+1,X'04'     RESET LINE COUNT TO 4
         PACK  DBLWORD(8),PAGENUM(4)
         CVB   R5,DBLWORD
         LA    R5,1(R5)
         CVD   R5,DBLWORD
         UNPK  PAGENUM(4),DBLWORD(8)
         OI    PAGENUM+3,X'F0'
         B     UPTHREE             GO SPACE THE FORMS
SPACE    LH    R3,LINECNT
         LA    R3,3(R3)            IF A TRIPPLE SPACE WILL
         CH    R3,MAXLINES         CAUSE AN OVERFLOW
         BNL   EJECT               GO TO THE EJECT ROUTINE
         STH   R3,LINECNT               --OTHERWIZE--
UPTHREE  MVI   MSGOUT-1,C'-'       SET ASA FOR TRIPLE SPACE
         LA    R2,MSGOUT-1
         EX    0,WRITEIT           GIVE THE SPACE COMMAND
         B     CLEARMSG            TAKE THE EXIT
WTO      LH    LISTREG,0(LISTREG)  GET DISPLACEMENT TO PROPER WTO
         LA    LISTREG,MSGADS(LISTREG)  GET ADDRESS OF PROPER WTO
         WTO   MF=(E,(1))               WRITE TO THE OPERATOR
         B     MSGEXIT                  ......EXIT......
CLEARMSG MVI   MSGOUT,C' '
         MVC   MSGOUT+1(119),MSGOUT    CLEAR THE OUTPUT AREA
MSGEXIT  L     SAVEREG,SAVEREG3+4 RETRIEVE OLD SAVE AREA          M2838
         LM    14,12,12(13)       RESTORE REGS                    M2838
         L     PARAMREG,IEHERSAV  RESTORE PARAMETERS              M2838
         L     15,DECB2+4               GET RETURN ADDRESS
         BR    15
MOVEMSG  MVC   0(1,R7),1(R6)
         DS    0F
MSGADS   EQU   *-4
         DC    A(MSG3)        FOR A 1 WRITE MSG 3
         DC    A(MSG2)
         DC    A(MSG3)
         DC    A(MSG4)
*              THESE INSERTS TO ALLOW GETTING DYNAMIC MESSAGES
         LA    R6,FNSA
         LA    R6,KWSA
         LA    R6,PARAM2-1
         LA    R6,PARAM4-1
         DC    A(MSG9)
         DC    A(MSG3)        FOR A10 WRITE MSG 3
         DC    A(MSG11)
         DC    A(MSG12)
         DC    A(0)           NO SUCH MESSAGE
         DC    AL4(MSG14)
         DC    AL4(MSG15)
         DC    AL4(MSG16)
         DC    AL4(MSG17)
         DC    A(0)           NO SUCH MESSAGE
        DC    AL4(MSG19)
         DC    A(MSG20)
         DC    A(MSG21)
         DC    A(MSG22)
         DC    A(MSG23)
         DC    A(MSG24)
         DC    A(MSG25)
         DC    A(MSG26)
         DC    A(MSG27)
         DC    A(MSG28)
         DC    A(MSG29)
         DC    A(MSG30)
         DC    A(MSG31)
         DC    A(MSG32)
         DC    A(MSG33)
         DC    A(MSG34)
         DC    A(MSG35)
         DC    A(MSG36)
         DC    A(MSG37)
         DC    A(MSG38)
         DC    A(MSG39)
         DC    A(MSG40)
         DC    A(MSG41)
         DC    A(MSG42)
         DC    A(MSG43)
         DC    A(MSG44)
         DC    A(MSG45)
         DC    A(MSG46)
         DC    A(MSG47)
         BAL   6,EJECT                 FORCE A SKIP TO ONE
         BAL   6,SPACE                 TRIPLE SPACE
         DC    A(MSG50)
         BALR  LISTREG,R8          51 WRITE ON CONSOLE....WTO1
         DC    AL2(WTO1-MSGADS)
         BALR  LISTREG,R8          52 WRITE ON CONSOLE....WTO2
         DC    AL2(WTO2-MSGADS)
         DC    A(MSG53)                 I/O ERROR ON SYSIN
         DC    A(MSG54)
         DC    A(MSG55)                                            BS0H
         DC    A(MSG56)                                            BS0H
         DC    A(MSG57)                                          A23466
         DC    A(MSG58)                                          A24020
         DC    A(MSG59)                                          A28552
*                                                                S21046
*   THE FOLLOWING ADDRESSES REFER TO THE DIAGNOSTIC MESSAGES     S21046
*   PRODUCED THROUGH ERROR CONDITIONS OCCURING IN PROTECT        S21046
*                                                                S21046
         DC    A(MSG60)                                          S21046
         DC    A(MSG61)                                          S21046
         DC    A(MSG62)                                          S21046
         DC    A(MSG63)                                          S21046
         DC    A(MSG64)                                          S21046
         DC    A(MSG65)                                          S21046
         DC    A(MSG66)                                          S21046
         DC    A(MSG67)                                          S21046
*                                                                A31333
*   THE FOLLOWING MESSAGE NUMBERS REFER TO A DIAGNOSTIC MESSAGE  A31333
*   PRODUCED BY A RETURN CODE FROM CATALOG                       A31333
*                                                                A31333
         DC    A(MSG68)                                          A31333
         DC    A(MSG69)                                          A31333
*  THE FOLLOWING TWO MESSAGES ARE A RESULT OF ERROR CONDITIONS   A33374
* IN THE STOW ROUTINE.                                           A33374
         DC    A(MSG70)                DATA SET NOT OPEN         A33374
         DC    A(MSG71)                GETMAIN FAILED IN STOW    A33374
         DC    A(MSG72)           SCRATCH VTOC FAILED TO FIND DS A50977
         DC    A(MSG73)           PDS DIRECTORY FULL             YM5076
         DC    A(MSG74)           ZERO VOL CNT OR BAD LIST       A57261
*                                                              @VS30028
* THE FOLLOWING MESSAGES ARE A RESULT OF ERROR CONDITIONS      @VS30028
* IN SCRATCH OR RENAME.                                        @VS30028
*                                                              @VS30028
         DC    A(MSG75)                DATA SET OPEN           @VS30028
         DC    A(MSG76)                VOLUME NOT MOUNTED      @VS30028
         DC    A(MSG77)                INVALID AUTHORIZATION   @Z40RSDS*
                                       TO SECURITY PROFILE     @Z40RSRJ
         DC    A(MSG78)                UNABLE TO RENAME        @Z40RSRJ*
                                       SECURITY PROFILE        @Z40RSRJ
         EJECT                                                 @VS30028
MSG2     DC    AL1(MSG3-MSG2-2)
         DC    C' IEH201I  INVALID REQUEST..'
         DC    CL19'. STATEMENT IGNORED'
MSG3     DC    AL1(MSG4-MSG3-2)
         DC    C'IEH202I  INVALID KEYWORD '
         DC    CL27'OR CONTROL STATEMENT SYNTAX'
MSG4     DC    AL1(MSG5-MSG4-2)
         DC    CL30'AN UNDEFINED ERROR CODE HAS BE'
         DC    CL11'EN RETURNED'
MSG5     EQU   *
MSG9     DC    AL1(MSG10-MSG9-2)
         DC    CL32'NORMAL END OF TASK RETURNED FROM'
MSG10    EQU   *
MSG11    DC    AL1(MSG12-MSG11-2)
         DC    C' IEH203I  '
         DC    CL30'THE SYSCTLG DATA SET IS NOT AV'
         DC    CL23'AILABLE OR FORMS A LOOP'
MSG12    DC    AL1(MSG13-MSG12-2)
         DC    C' IEH204I  '
         DC    CL31'STATUS OF THE REQUESTED TASK CA'
         DC    CL19'N NOT BE DETERMINED'
MSG13    EQU   *
MSG14    DC    AL1(MSG15-MSG14-2)
         DC    C' IEH205I  '
         DC    CL30'INFORMATION IN CONTROL STATMEN'
         DC    CL14'T IS REDUNDANT'
MSG15    DC    AL1(MSG16-MSG15-2)
         DC    CL14'NOT SUFFICIENT'
MSG16    DC    AL1(MSG17-MSG16-2)
         DC    CL30'THE REQUESTED PROGRAM FUNCTION'
         DC    CL20' HAS NOT BEEN CALLED'
MSG17    DC    AL1(MSG18-MSG17-2)
         DC    C' ... UNUSUAL END'
MSG18    EQU   *
MSG19    DC    AL1(MSG20-MSG19-2)
         DC    C' IEH206I  '
         DC    C'CVOL IS NOT DIRECT ACCESS'
MSG20    DC    AL1(MSG21-MSG20-2)
         DC    C' IEH207I  '
         DC    CL26'STATUS OF USERS REQUEST TO'
MSG21    DC    AL1(MSG22-MSG21-2)
         DC    CL10'DATA SET'
MSG22    DC    AL1(MSG23-MSG22-2)
         DC    CL30'VOLUME I.D.     ACTION TAKEN  '
         DC    CL32'   REASON FOR TAKING THIS ACTION'
         DC    CL25'                    ERROR'
MSG23    DC    AL1(MSG24-MSG23-2)
         DC    CL13'USERS REQUEST'
MSG24    DC    AL1(MSG25-MSG24-2)
         DC    C'DATA SET OR MEMBER NOT FOUND'
MSG25    DC    AL1(MSG26-MSG25-2)
         DC    CL30'CORRECT PASSWORD NOT AVAILABLE'
MSG26    DC    AL1(MSG27-MSG26-2)
         DC    CL26'PURGE DATE HAS NOT EXPIRED'
MSG27    DC    AL1(MSG28-MSG27-2)
         DC    CL25'THE SPECIFIED NEW NAME IS'
         DC    CL16' ALREADY PRESENT'
MSG28    DC    AL1(MSG29-MSG28-2)
         DC    CL4'NONE'
MSG29    DC    AL1(MSG30-MSG29-2)
         DC    CL31'PERMANENT I/O ERROR ENCOUNTERED'
MSG30    DC    AL1(MSG31-MSG30-2)
         DC    CL43'END OF LISTING OF DATA SETS TO BE SCRATCHED' A25564
         DC    CL11' OR RENAMED'                                 A25564
MSG31    DC    AL1(MSG32-MSG31-2)
         DC    CL11'UTILITY END'
MSG32    DC    AL1(MSG33-MSG32-2)
         DC    CL27'DATA SET NAME'
MSG33    DC    AL1(MSG34-MSG33-2)
         DC    CL30'SCRATCH THE VOLUME TABLE OF CO'
         DC    CL6'NTENTS'
MSG34    DC    AL1(MSG35-MSG34-2)
         DC    CL19'END OF SCRATCH VTOC'
MSG35    DC    AL1(MSG36-MSG35-2)
         DC    C' IEH208I  LIST TRUNCATED TO '
         DC    CL25'1 VOLUME FOR SCRATCH VTOC'
MSG36    DC    AL1(MSG37-MSG36-2)
         DC    C' IEH209I  '
         DC    CL26'STATUS OF USERS REQUEST TO'
MSG37    DC    AL1(MSG38-MSG37-2)
         DC    C' IEH210I  YOUR REQUEST CANNOT'
         DC    CL16' BE SERVICED....'
MSG38    DC    AL1(MSG39-MSG38-2)
         DC    CL23'IS A CATALOGED DATA SET'
MSG39    DC    AL1(MSG40-MSG39-2)
         DC    CL32'THE INDEX SPECIFIED IS NOT EMPTY'
         DC    CL16' OR HAS AN ALIAS'
MSG40    DC    AL1(MSG41-MSG40-2)
         DC    CL29'NO SPACE AVAILIBLE TO SYSCTLG'
MSG41    DC    AL1(MSG42-MSG41-2)
         DC    CL28'IS NOT A VALID DATA SET NAME'
MSG42    DC    AL1(MSG43-MSG42-2)
         DC    CL27'WITHIN THE SYSCTLG DATA SET'
MSG43    DC    AL1(MSG44-MSG43-2)
         DC    CL27'IS CURRENTLY AN INDEX LEVEL'
MSG44    DC    AL1(MSG45-MSG44-2)
         DC    CL32'THE IMPLIED INDEX STRUCTURE DOES'
         DC    CL10' NOT EXIST'
MSG45    DC    AL1(MSG46-MSG45-2)
         DC    CL29'A DATA SET EXISTS WITHIN THE '
         DC    CL29'SPECIFIED LIST OF INDEX NAMES'
MSG46    DC    AL1(MSG47-MSG46-2)
         DC    CL24'A SYNTAX ERROR EXISTS IN'
MSG47    DC    AL1(MSG48-MSG47-2)
         DC    CL27'IS NOT A CATALOGED DATA SET'
MSG48    EQU   *
MSG50    DC    AL1(MSG51-MSG50-2)
         DC    C' IEH211I  '
         DC    CL30'REQUIRED VOLUME COULD NOT BE M'
         DC    CL6'OUNTED'
MSG51    EQU   *
WTO1     WTO  'IEH002I SYNAD EXIT FROM SYSPRINT- TASK TERMINATED',MF=L
WTO2     WTO  'IEH005I PROGRAM INTERRUPT ... TASK TERMINATED',MF=L
MSG53    DC    AL1(MSG54-MSG53-2)
         DC    C' IEH212I  I/O ERROR ON THE SY'
         DC    C'SIN DATA SET --- JOB TERMINATED'
MSG54    DC    AL1(MSG55-MSG54-2)
         DC    CL30'IT IS INVALID --OR-- IT IS NOT'
         DC    CL28' PROPER WITHIN YOUR PRESENT '
         DC    CL17'CATALOG STRUCTURE'
MSG55    DC    AL1(MSG56-MSG55-2)                                  BS0H
         DC    C' IEH213I  '                                       BS0H
         DC    CL32' JOB TERMINATED -- INVALID BLOCK'              BS0H
         DC    CL28'SIZE SPECIFIED IN SYSIN DCB '                  BS0H
MSG56    DC    AL1(MSG57-MSG56-2)                                  BS0H
         DC    C' IEH216I  '                                     A28562
         DC    CL32' SYSIN CANNOT BE OPENED -- CHECK'              BS0H
         DC    CL14' SYSIN DD CARD'                                BS0H
MSG57    DC    AL1(MSG58-MSG57-2)                                A23466
         DC    C' IEH214I  '                                     A23466
         DC    CL38' CONTINUATION CARD EXPECTED---REQUESTS'      A23466
         DC    CL19' CANNOT BE SERVICED'                         A23466
MSG58    DC    AL1(MSG59-MSG58-2)                                A24020
         DC    C' IEH215I  '                                     A24020
         DC    CL42' SYNTAX ERROR ENCOUNTERED IN NAME FIELD OF'  A24020
         DC    CL42' CONTROL STATEMENT-PROCESSING IS CONTINUED'  A24020
MSG59    DC    AL1(MSG60-MSG59-2)      LENGTH OF ENTRY           A28552
         DC    C' IEH217I  '                                   @ZA01675
         DC    CL42'ERROR ENCOUNTERED IN A NAME, INDEX, ALIAS,' YA01707
         DC    CL42' OR MEMBER FIELD OF THE CONTROL STATEMENT ' YA01707
         DC    CL18'.. REQUEST IGNORED'                         YA01707
*                                                                S21046
*   MESSAGE 60 IS PRODUCED BY A RETURN CODE FROM PROTECT         S21046
*                                                                S21046
MSG60    DC    AL1(MSG61-MSG60-2)                                S21046
         DC    C' IEH223I '                                      S21046
         DC    CL37' THE PASSWORD DATA SET DOES NOT EXIST'       S21046
*                                                                S21046
*   MESSAGE 61 IS PRODUCED BY A RETURN CODE FROM PROTECT         S21046
*                                                                S21046
MSG61    DC    AL1(MSG62-MSG61-2)                                S21046
         DC    C' IEH224I '                                      S21046
         DC    CL39'WARNING ... UNABLE TO ALTER PROTECTION '     S21046
         DC    CL24'STATUS OF TAPE DATA SETS'                    S21046
*                                                                S21046
*   MESSAGE 62 IS PRODUCED BY A RETURN CODE FROM PROTECT         S21046
*                                                                S21046
MSG62    DC    AL1(MSG63-MSG62-2)                                S21046
         DC    C' IEH225I '                                      S21046
         DC    CL31' DUPLICATE ENTRY EXISTS IN THE '             S21046
         DC    CL17'PASSWORD DATA SET'                           S21046
*                                                                S21046
*   MESSAGE 63 IS PRODUCED BY A RETURN CODE FROM PROTECT         S21046
*                                                                S21046
MSG63    DC    AL1(MSG64-MSG63-2)                                S21046
         DC    C' IEH218I '                                      S21046
         DC    CL41' JOB TERMINATED --- FIVE INVALID PASSWORD'   S21046
         DC    CL15'S WERE SUPPLIED'                             S21046
*                                                                S21046
*   MESSAGE 64 IS PRODUCED BY A RETURN CODE FROM PROTECT         S21046
*                                                                S21046
MSG64    DC    AL1(MSG65-MSG64-2)                                S21046
         DC    C' IEH222I '                                      S21046
         DC    CL37'UNABLE TO ALTER PROTECTION STATUS OF '       S21046
         DC    CL8'DATA SET'                                     S21046
*                                                                S21046
*   MESSAGE 65 IS PRODUCED BY A RETURN CODE FROM PROTECT         S21046
*                                                                S21046
MSG65    DC    AL1(MSG66-MSG65-2)                                S21046
         DC    C' IEH219I '                                      S21046
         DC    CL31' I/O ERROR IN PASSWORD DATA SET'             S21046
*                                                                S21046
*   MESSAGE 66 IS PRODUCED BY A RETURN CODE FROM PROTECT         S21046
*                                                                S21046
MSG66    DC    AL1(MSG67-MSG66-2)                                S21046
         DC    C' IEH221I '                                      S21046
         DC    CL31' THE PASSWORD DATA SET IS FULL'              S21046
*                                                                S21046
*   MESSAGE 67 IS PRODUCED BY A RETURN CODE FROM PROTECT         S21046
*                                                                S21046
MSG67    DC    AL1(MSG68-MSG67-2)                                S21046
         DC    C' IEH229I '                                      S21046
         DC    CL32' INVALID PARAMETER IN PARM FIELD'            S21046
         DC    CL13' OF EXEC CARD'                               S21046
*                                                                A31333
*   MESSAGE 68 IS PRODUCED BY A RETURN CODE FROM CATALOG         A31333
*                                                                A31333
MSG68    DC   AL1(MSG69-MSG68-2)                                 A31333
         DC    C' IEH230I '                                      A31333
*                                                                A31333
*   MESSAGE 69 IS PRODUCED BY A RETURN CODE FROM CATALOG         A31333
*                                                                A31333
         DC    C' VTOC NOT CONVERTED FROM DOS TO OS '            A31333
MSG69    DC    AL1(MSG70-MSG69-2)                                A31333
         DC    C'          DATA SET NOT CATALOGUED OR INDEX'     A31333
         DC    C' NOT BUILT'                                     A31333
*  VTOC OR DIRECTORY OF PDS COULD NOT BE OPENED                  A33374
MSG70    EQU   *                                                 A33374
         DC    AL1(MSG71-MSG70-2)                                A33374
         DC    C'VTOC OR DIRECTORY OPEN FAILED'                @VS30028
*  CONDITIONAL GETMAIN FAILED IN STOW                            A33374
MSG71    EQU   *                                                 A33374
         DC    AL1(MSG72-MSG71-2)                                A33374
         DC    C'INSUFFICIENT CORE, CONDITIONAL GETMAIN FAILED'  A33374
*     SCRATCH FAILED TO FIND A VTOC ENTRY DURING SCRATCH  VTOC   A50977
MSG72    DC    AL1(MSG73-MSG72-2)                                A50977
         DC    CL37'ERROR IN VTOC ENTRY-UNABLE TO SCRATCH'       A50977
*     STOW FOUND A FULL PDS DIRECTORY                            YM5076
MSG73    DC    AL1(MSG74-MSG73-2)                                YM5076
         DC    CL21'PDS DIRECTORY IS FULL'                       YM5076
*        VOLUME COUNT IS ZERO OR LIST NOT IN REGION              A57261
MSG74    EQU   *                                                 A57261
         DC    AL1(MSG75-MSG74-2)                                A57261
         DC    C'VOL COUNT IS ZERO OR LIST NOT IN REGION'        A57261
*                                                              @VS30028
*   MESSAGE 75 IS PRODUCED BY A RETURN CODE FROM SCRATCH OR    @VS30028
*   RENAME (IF THE DATA SET TO BE SCRATCHED/RENAMED WAS OPEN)  @VS30028
*   AND IS PRINTED AS PART OF MESSAGE IEH207I OR IEH209I.      @VS30028
*                                                              @VS30028
MSG75    EQU   *                                                 A57261
         DC    AL1(MSG76-MSG75-2)      MESSAGE LENGTH - 1      @VS30028
         DC    C'DATA SET WAS OPEN'    MESSAGE TEXT            @VS30028
*                                                              @VS30028
*   MESSAGE 76 IS PRODUCED BY A RETURN CODE FROM SCRATCH       @VS30028
*   WHEN THE VOLUME COULD NOT BE VERIFIED AS MOUNTED AND       @VS30028
*   IS PRINTED AS PART OF MESSAGE IEH209I.                     @VS30028
*                                                              @VS30028
MSG76    EQU   *                       MESSAGE NUMBER 76       @VS30028
         DC    AL1(MSG77-MSG76-2)      MESSAGE LENGTH - 1      @VS30028
         DC    C'VOLUME NOT MOUNTED'   MESSAGE TEXT            @VS30028
*                                                              @Z40RSDS
*   MESSAGE 77 IS PRODUCED BY A RETURN CODE FROM SCRATCH       @Z40RSDS
*   AND IS PRINTED AS PART OF MESSAGE IEH207I OR IEH209I.      @Z40RSDS
*                                                              @Z40RSDS
MSG77    EQU   *                       MESSAGE NUMBER 77       @VS30028
         DC    AL1(MSG78-MSG77-2)      MESSAGE LENGTH - 1      @Z40RSDS
         DC    C'INVALID AUTHORIZATION TO SECURITY PROFILE'    @Z40RSRJ
*                                                              @Z40RSDS
*   MESSAGE 78 IS PRODUCED BY A RETURN CODE FROM RENAME        @Z40RSDS
*   AND IS PRINTED AS PART OF MESSAGE IEH207I.                 @Z40RSDS
*                                                              @Z40RSDS
MSG78    EQU   *                       MESSAGE NUMBER 78       @Z40RSDS
         DC    AL1(MSG80-MSG78-2)      MESSAGE LENGTH - 1      @G32DSFS
         DC    C'UNABLE TO RENAME SECURITY PROFILE'  MSG TEXT  @Z40RSRJ
*                                                              @Z40RSDS
*   MESSAGE 79 HAS BEEN DELETED                                @G32DSFS
*                                                              @G32DSFS
MSG80    EQU   *                       MESSAGE NUMBER 80       @G32DSFS
         EJECT                                                 @VS30028
DSECT    DSECT
IEHECHAR DS    C         CHARACTER TO BE SCANNED FOR
IEHECOND DS    C         CONDITION CODE TO STOP ON
IEHECODE DS    C
IEHEARGL DS    C
IEHESTT  DS    F
IEHESTP  DS    F
IEHEFND  DS    F
IEHETBL  DS    F
IEHEFUN  DS    F
IEHESCNX DS    3F
IEHEMAC1 DS    7F                 PARAMETER LIST                 S21046
IEHEMAC2 DS    6F
IEHESTIN DS    F
IEHENDIN DS    F
IEHECCNT DS    2C
IEHESW1  DS    C
DUPSW    DS    2C                 SWITCHES FOR TEST OF DUPLICATE S21046
*                                 PARAMETERS                     S21046
PPARAMSW DS    CL2
IEHESW5  DS    C
IEHESW6  DS    C
IEHECALL DS    F
         DS    0D
DBLWORD  DS    F
FULLWORD DS    F
         DS    0D
DECB1    DS    24X
DECB2    DS    24X
VOLISTAD DS    F
MATRIXIU DS    F
VDTIU    DS    F
VOLPAREN DS    FL2
VSRCM    DS    2X
VDSA     DS    X
DFLG     DS    X                                                   BS0H
DFLAG    DS    X                                                   BS0H
         DS    0F
FLSTADDR DS    F
FNSA     DS    12X
KWSA     DS    12X
SCANRET  DS    F
MARESAR  DS    72X
IEHESPC  DS    XL20
         DS    XL20
IEHERSAV DS    64X
PASSUNIT DS    5X                                                  DT0I
MOUNTSW  DS    X                                                   DT0I
TABLEAD  DS    F                  ADDRESS OF DEVICE NAME TABLE     I276
IEHESW2  DS    X                  PROTECT SWITCHES               S21046
*                                 BIT 1=1 IF WE ARE TO PRINT     S21046
*                                        CONTROL CARDS           S21046
*                                 BIT 3=1 INDICATES FIRST QUOTE  S21046
*                                        IN SCAN FOR DATA        S21046
*                                        OPERATION IS FOUND      S21046
*                                 BIT 4= 1 IF WTOR SETUP IS DONE S21046
*                                 BIT 6=1 IF FIRST PARM ENTRY    S21046
*                                 WAS VALID                      S21046
*                                 BIT 7=1 IF INVALID PARM ENTRY  S21046
*                                 MSG HAS BEEN PRINTED           S21046
PASWDCT1 DS    1X                 NUMBER OF PROMPTS FOR PASWORD1 S21046
PASWDCT3 DS    1X                 NUMBER OF PROMPTS FOR CPASWORD S21046
TOTALPS  DS    1X                 TOTAL NO OF OPERATOR REPLIES   S21046
WTORECB  DS   1F                  EVENT CONTROL BLOCK FOR WTOR   S21046
TIOTADRS DS    1F                 TIOT ADDRESS FOR WTOR          S21046
CCNAME   DS    8C                 NAME OFF CONTROL CARD          S21046
WTORLIST WTOR  'IEH220A JOBNAMES, STEPNAME, CARDNAME, REPLY WITH PASSWOX
               RD',,8,WTORECB,ROUTCDE=(9),DESC=2,MF=L            S21046
EXTRACTO EXTRACT ,'S',FIELDS=(TIOT),MF=L                         S21046
DCBAREA  DS    0F
UTINDCB  DCB   DSORG=PS,MACRF=(GM)
CODELIST DC    C'.,=()   '        SPECIAL CHARACTERS CODED BY SCAN
MAXLINES DC    FL2'45'             MAXIMUM LINES PER PAGE
LINECNT  DC    FL2'0'              LINE COUNT FIELD
SKIP21   DC    X'8B'               SKIP TO 1 COMMAND
WRITEHDR DC    X'19'               SPACE 3 AFTER PRINT
HEADER   DC    CL30'MODIFY....VERSION BETA-1......'
         DC    CL30'LAST UPDATED....JULY 11  10 PM'
         DC    CL30'                              '
         DC    CL30'               PAGE 001       '
PAGENUM  EQU   HEADER+110
DDNAMES  DS    3CL8                     FOR THREE DD NAMES
UTOUTDCB DCB   DSORG=PS,MACRF=(PM)
         DS    2F
IEHESAT  DS    150X
READBASE DC    F'0'
PARAM1   DS    44X
         DS    F              FOR  LENGTH OF  PARAM2
PARAM2   DS    44X
         DS    F                   FOR LENGTH OF PARAM3
PARAM3   DS    44X
PARAM5   DS    2F                 CPASWORD PARAMETER             S21046
PARAM6   DS    151F               VOL IDS PARAMETER              S21046
PARAM7   DS    2F                 PASSWORD2 PARAMETER            S21046
PARAM8   DS    80X                DATA PARAMETER                 S21046
DUMMY    DS    1F                 TEMPORARY STORAGE FIELD        S21046
PROTCTAD DS    1F                 ADDRESS OF WHERE TO PUT NEXT   S21046
*                                 VOL ID IN PARAM6 FOR PROTECT   S21046
FINUSE   DS    F                   ADDR OF DC LIST IN USE
DCSAVEAD DS    F                   SAVE AREA FOR ADDR OF DC LIST
INPUT    DS    40F                 TWO CARD INPUT AREA
MSGOUT   DS    30F                 MSG OUTPUT AREA
         DS    F
         DS    0D
PARAM4   DS    151F                FOR A VOLUME LIST
EOCORE   EQU   *-12
         ORG   PARAM4+20
VTOCDCB2 DS    0F
DCB4VTOC DCB   DDNAME=VTOC,                                            .
               DSORG=PS,                                               .
               MACRF=R,                                                .
               KEYLEN=44,                                              .
               BLKSIZE=1,                                              .
               RECFM=U,                                                .
               EODAD=VTOCEND,                                          .
               SYNAD=VTOCERR,                                          .
               EXLST=VTOCEND
JFCB     DS    24D
*        THE FOLLOWING ORG STATEMENT IS FLAGGED                  YM5077
         ORG
SAVEREG2 DS    72X                SECONDARY SAVE AREA             M2838
SAVEREG3 DS    72X                THIRD LEVEL SAVE AREA           M2838
         ORG
VTOCSW   EQU   JFCB+50
VTOCDECB EQU   JFCB+60
JFCBVOL  EQU   JFCB+118
EXLST2   EQU   DCB4VTOC+36
FORMATID EQU   PARAM2+44
BLANK    EQU   64
READTOO  EQU   INPUT+72
COL16    EQU   READTOO+15
COL72    EQU   READTOO+71
MIDDLE   EQU   READTOO-1
WARE2PUT EQU   IEHEMAC1-4
SYSINEND EQU   *
VTOCEND  EQU   *
VTOCERR  EQU   *
         END
