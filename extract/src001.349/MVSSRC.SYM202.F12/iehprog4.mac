   TITLE  'IEHPROGM  -  IEHPROG4  AUXILIARY PARAMETER ANALYZER'
**********************************************************************
*                                                                     *
* MODULE NAME -- IEHPROG4                                      YL026VC*
*                                                              YL026VC*
* DESCRIPTIVE NAME -- IEHPROGM AUXILIARY PARAMETER ANALYZER    YL026VC*
*                                                              YL026VC*
* COPYRIGHT -- NONE                                            YL026VC*
*                                                              YL026VC*
* STATUS -- 'IEHPROG4' OS/VS2 PROLOGUE MODIFICATION            YL026VC*
*    NOVEMBER 1972                                             YL026VC*
*                                                              YL026VC*
* FUNCTION -- THIS MODULE TESTS THE INVOKER PARAMETER LISTS AND       *
*    OPENS THE INPUT AND OUTPUT DCB'S USED BY IEHPROGM.               *
*                                                                     *
* ENTRY POINTS --                                                     *
*         IEHPROG4 IS THE ONLY ENTRY POINT TO THE MODULE 'IEHPROG4'.  *
*                                                                     *
* INPUT -- INPUT TO IEHPROG4 CONSISTS OF THE STANDARD INVOKER LIST    *
*    POINTED TO BY REGISTER 1 AND THE COMMON COMMUNICATIONS AND WORK  *
*    AREA POINTED TO BY REGISTER 12 AND DESCRIBED BY THE DSECT        *
*    NAMED DSECT.                                                     *
*                                                                     *
* OUTPUT -- OUTPUT OF IEHPROG4 CONSISTS OF MODIFICATIONS TO THE       *
*    COMMUNICATIONS AREA THAT WAS INPUT TO THE MODULE.                *
*                                                                     *
* EXTERNAL REFERENCES -- THIS ROUTINE HAS NO EXTERNAL ROUTINES.       *
*                                                                     *
* EXITS, NORMAL -- THIS MODULE DOES A BRANCH ON REGISTER 15 TO THE    *
*    POINT OF CALL IN 'IEHPROG1'.                                     *
*                                                                     *
* EXITS, ERROR -- THIS MODULE DOES A BRANCH ON REGISTER 15 TO THE     *
*    POINT OF CALL IN 'IEHPROG1'.                                     *
*                                                                     *
* TABLES/WORK AREA --                                                 *
*    DSECT IS A COMMON COMMUNICATIONS AND WORK AREA POINTED TO BY     *
*    REGISTER 12 AND DESCRIBED BY A DSECT NAMED DSECT AND BEGINNING   *
*    IEHECHAR.                                                        *
*                                                                     *
* ATTRIBUTES -- THIS MODULE OPERATES AS ONE LEG OF AN OVERLAY         *
*    STRUCTURE.                                                       *
*                                                                     *
* CHARACTER CODE DEPENDENCY -- THIS MODULE MUST BE ASSEMBLED IN       *
*    EBCDIC AND RUN IN EBCDIC TO INSURE COLLATING SEQUENCE AND        *
*    INTERNAL REPRESENTATION OF CERTAIN CHARACTERS.                   *
*                                                                     *
* NOTES -- APPROXIMATELY 1000 BYTES OF STORAGE ARE REQUIRED FOR       *
*    CODE.                                                            *
*                                                                     *
* CHANGE ACTIVITY -- FOLLOWS PROLOGUE                          YL026VC*
*                                                              YL026VC*
**********************************************************************
IEHPROG4 CSECT                                                    M2838
*A49220                                                          YM5077
*C200,10600                                                       M2838
*                                                                  I276
*1056029000-029200,031800-032000                                   BS0H
*7064018200,018600                                               PTM732
*2480                                                              DT0I
*A700-784,1000,8500-8520                                        YL026VC
*C800-900                                                       YL026VC
*C57800
REG1     EQU   1
REG2     EQU   2
REG3     EQU   3
REG4     EQU   4
REG5     EQU   5
REG6     EQU   6
REG7     EQU   7
GOTO     EQU   8
MOCANAR  EQU   9
REG10    EQU   10
NULL     EQU   0                  USED AS NULL REGISTER IN BCTR  S21046
BASE     EQU   11                 BASE REGISTER                  S21046
DBASE    EQU   12                 DSECT BASE REGISTER            S21046
SAVEREG  EQU   13                 SAVE AREA POINTER               M2838
RETURN   EQU   14
REG15    EQU   15                                                  BS0H
BITON    EQU   X'80'              TURNS ON THE PRINT BIT         S21046
BITOFF   EQU   X'7F'              TURNS OFF THE PRINT BIT        S21046
ONEGOOD  EQU   X'04'              1ST PARAMETER GOOD             S21046
MSGPRINT EQU   X'02'              INDICATES TO IEHPROG1 THAT THE S21046
*                                 PARM ERROR MSG IS TO BE        S21046
*                                 PRINTED. MSG NUMBER 67         S21046
MSGOFF   EQU   X'FD'              USED TO TURN OFF THE MSG BIT   S21046
ZERO     EQU   C'0'               CHARACTER ZERO                 S21046
NINE     EQU   C'9'               CHARACTER NINE                 S21046
PRINTLNG EQU   5                  LENGTH OF 'PRINT'              S21046
NPRNTLNG EQU   7                  LENGTH OF 'NOPRINT'            S21046
NAMLNGTH EQU   8                  LENGTH OF 'LINECNT='           S21046
MOVE8    EQU   8                  MOVE THE OPEN LIST             S21046
CNTLNGTH EQU   10                 LENGTH OF LINE COUNT PARM      S21046
OFFSET0  EQU   0                  OFFSET OF 0                    S21046
OFFSET1  EQU   1                  OFFSET FOR MSGOUT              S21046
OFFSET2  EQU   2                  OFFSET FOR RETURN CODE         S21046
INCRMNT1 EQU   1                  MOVE POINTER UP ONE            S21046
INCRMNT2 EQU   2                  MOVE POINTER UP TWO            S21046
INCRMNT3 EQU   3                  INCREMENT OF THREE             S21046
COMMA    EQU   C','               CHECK FOR COMMA                S21046
AA       EQU   34
         SAVE  (14,12)        SAVE REGS FOR MAIN PROG
         BALR  BASE,0             SET UP ADDRESSABILITY          S21046
         USING *,BASE                                            S21046
         USING IEHECHAR,DBASE                                    S21046
         ST    SAVEREG,SAVEREG2+4 KEEP OLD SAVE AREA              M2838
         LA    SAVEREG,SAVEREG2   ESTABLISH NEW SAVE AREA         M2838
         MVI   CTRLWORD,X'00'     RESET SWITCHES FOR VOLUME MOUNTING
         MVC   DCBAREA(256),DCBSTART    PUT THE DCB'S
         MVC   DCBAREA+250(DCBSTOP-DCBSTART-250),DCBSTART+250
         OI    IEHESW2,BITON      TURN ON THE PRINT BIT          S21046
         NI    IEHESW2,MSGOFF     TURN OFF THE MSG BIT           S21046
         LTR   REG10,REG1    DOES A PARAMETER LIST EXIST
         L     REG1,0(REG10)
         BZ    FLEE               NO .... BRANCH
         LA    GOTO,GOTOTAB   SET ADDRESS OF ROUTINE TO BE ENTERED
TSTCOUNT LH    REG2,0(REG1)
         LTR   REG2,REG2      TEST COUNT FIELD FOR ZERO COUNT
         BCR   7,GOTO         BRANCH IF NOT ZERO
TST4LAST TM   0(REG10),X'80'  IS THIS THE LAST ITEM
        BO    FLEE   GO BACK TO CALLING RTN
         LA    REG10,4(REG10)    POINT AT NEXT ENTRY IN THE LIST
         L     REG1,0(REG10)
         LA    GOTO,4(GOTO)   POINT TO NEXT ENTRY IN GOTOTAB
         B     TSTCOUNT
GOTOTAB  B     PARAMFLD       PROCESS THE PARAMETER FIELD
         B     DD             PROCESS THE DD FIELD
         B     HEADLINE       PROCESS THE HEADER
         B     FLEE           RETURN ... WE SHOULD HAVE STOPED. BEFORE
*                                                                S21046
*      INTERPRET THE PARM FIELD OF THE EXEC CARD OR THE          S21046
*      PARAM FIELD OF THE LINK OR ATTACH MACRO. 'PRINT',         S21046
*      'NOPRINT' AND 'LINECNT=XX' ARE THE ONLY LEGAL PARAMETERS  S21046
*      IF ANYTHING ELSE IS PRESENT OR IF THESE ARE INVALID       S21046
*      DEFAULT TO 'PRINT' AND 'LINECNT=45' RESPECTIVLY AND ISSUE S21046
*      AN ERROR MESSAGE WITH A RETURN CODE OF 4, DO NOT          S21046
*      TERMINATE THE PROGRAM.                                    S21046
*                                                                S21046
PARAMFLD EQU   *                  CHECK PARAMETER FIELD          S21046
         LA    REG1,INCRMNT2(REG1) STEP UP PARM ADDRESS          S21046
CHCKLNGH CLC   OFFSET0(NAMLNGTH,REG1),COUNTFLD                   S21046
*                                 IS 'LINECNT=' PRESENT          S21046
         BE    COUNTFND           GO CHECK NUMBERS GIVEN         S21046
CHCKPRNT CLC   OFFSET0(PRINTLNG,REG1),PRINT                      S21046
*                                 IS 'PRINT' PRESENT             S21046
         BE    NEXTONE            GO SET PRINT LENGTH            S21046
CHCKNPRT CLC   OFFSET0(NPRNTLNG,REG1),NOPRINT                    S21046
*                                 IS 'NOPRINT' PRESENT           S21046
         BNE   PRINTERR           1ST PARAMETER INVALID          S21046
         NI    IEHESW2,BITOFF     TURN OFF THE PRINT BIT         S21046
         LA    REG4,NPRNTLNG      GET LENGTH OF 'NOPRINT'        S21046
CHCKNEXT SR    REG2,REG4          UPDATE LENGTH COUNT OF PARM    S21046
         BC    8,TST4LAST         IF COUNT IS NOW ZERO GO CHECK  S21046
*                                 ALTERNATE DD ENTRIES           S21046
         OI    IEHESW2,ONEGOOD    INDICATE ONE GOOD PARAMETER    S21046
         LA    REG1,OFFSET0(REG4,REG1)                           S21046
*                                 UPDATE POINTER TO DELIMITER    S21046
         CLI   OFFSET0(REG1),COMMA                               S21046
*                                 IS IT DELIMITED BY A COMMA     S21046
         BNE   PRINTERR           IF NOT PRINT MESSAGE           S21046
         BCTR  REG2,NULL          DECREMENT COUNT BY ONE         S21046
         LA    REG1,INCRMNT1(REG1)                               S21046
*                                 UPDATE PARAMETER ADDRESS       S21046
         B     CHCKLNGH           GO CHECK FOR 'LINECNT=XX'      S21046
*                                                                S21046
*         TURN ON THE PARM ERROR SWITCH                          S21046
*                                                                S21046
PRINTERR TM    IEHESW2,MSGPRINT   IS THE BIT ALREADY ON          S21046
         BC    1,TST4LAST         GO CHECK DD                    S21046
         OI    IEHESW2,MSGPRINT   TURN ON THE PRINT BIT          S21046
         TM    IEHESW2,ONEGOOD    IS THIS 2ND PARAMETER          S21046
         BC    1,TST4LAST         GO CHECK DD                    S21046
CHCKCOMA CLI   OFFSET0(REG1),COMMA IS A COMMA PRESENT            S21046
         BE    UPONE              SET UP TO CHECK NEXT PARM      S21046
         LA    REG1,INCRMNT1(REG1) UP ADDRESS BY ONE             S21046
         BCT   REG2,CHCKCOMA      DECREMENT COUNT                S21046
         B     TST4LAST           SECOND PARAMETER IS INVALID    S21046
*                                 BUT THE MESSGE BIT IS ON       S21046
UPONE    LA    REG1,INCRMNT1(REG1) POINT AT 1ST CHARACTER        S21046
         B     CHCKLNGH           REPEAT SCAN                    S21046
NEXTONE  LA    REG4,PRINTLNG      GET LENGTH OF 'PRINT'          S21046
         B     CHCKNEXT           GO CHECK NEXT PARAMETER        S21046
*                                                                S21046
*      'LINECNT=XX' IS PRESENT                                   S21046
*                                                                S21046
COUNTFND EQU   *                                                 S21046
         LA    REG1,NAMLNGTH(REG1) POINT TO LINECNT NUMBER       S21046
*                                                                S21046
*        CHECK TO BE SURE THESE ARE EBCDIC NUMERALS              S21046
*                                                                S21046
         CLI   OFFSET0(REG1),ZERO IS NUMBER LESS THAN ZERO       S21046
         BL    PRINTERR           INVALID ENTRY                  S21046
         CLI   INCRMNT1(REG1),ZERO IS 2ND NUMBER LESS THAN 0     S21046
         BL    PRINTERR           INVALID ENTRY                  S21046
         CLI   OFFSET0(REG1),NINE IS NUMBER GREATER THAN 9       S21046
         BH    PRINTERR           INVALID ENTRY                  S21046
         CLI   INCRMNT1(REG1),NINE IS NUMBER GREATER THAN 9      S21046
         BH    PRINTERR           INVALID ENTRY                  S21046
         LA    REG3,INCRMNT1      PREPARE FOR EXECUTE            S21046
         EX    REG3,PACK          PACK THE LINE COUNT            S21046
         CVB   REG4,DBLWORD                                      S21046
         STH   REG4,MAXLINES      MOVE NUMBER OF LINES           S21046
         LA    REG4,CNTLNGTH      TOTAL LENGTH OF LINE COUNT     S21046
         SR    REG2,REG4          DECREASE PARM COUNT            S21046
         BC    8,TST4LAST         IF ZERO GO CHECK DD ENTRIES    S21046
         BCTR  REG2,NULL          DECREMENT COUNT BY 1           S21046
         OI    IEHESW2,ONEGOOD    INDICATE ONE GOOD PARAMETER    S21046
         CLI   INCRMNT2(REG1),COMMA                              S21046
*                                 IS IT DELIMITED BY A COMMA     S21046
         BNE   PRINTERR           IF NOT GO PRINT ERROR MESSAGE  S21046
         LA    REG1,INCRMNT3(REG1) POINT AT FIRST CHARACTER      S21046
         B     CHCKPRNT           GO CHECK FOR 'PRINT','NOPRINT' S21046
PACK     PACK  DBLWORD(NAMLNGTH),OFFSET0(INCRMNT1,REG1)          S21046
*                                                                S21046
*    PICK UP ALTERNATE NAMES FOR SYSIN AND SYSPRINT ,IF GIVEN    S21046
*                                                                S21046
DD       EQU   *
         MVC   DBLWORD(16),AA(REG1)     INSURING ALLIGNMENT
         LM    REG3,REG6,DBLWORD    REGS 3 - 6 GET SYSIN, SYSPRINT
         LA    REG7,AA
         CR    REG2,REG7           LENGTH INCLUDE SYSIN  ?
         BNH   NODD                NO .... BRANCH
         LTR   REG3,REG3           SYSIN PRESENT  ?
         BZ    *+12                NO .... BRANCH
         STM   REG3,REG4,UTINDCB+40
         OI    CTRLWORD,X'02'      INDICATE SYSIN  WAS PRESENT
         LA    REG7,AA+8
         CR    REG2,REG7           LENGTH INCLUDE SYSOUT ?
         BNH   NODD                NO .... BRANCH
         LTR   REG5,REG5           SYSOUT PRESENT  ?
         BZ    *+12                NO .... BRANCH
         STM   REG5,REG6,UTOUTDCB+40
         OI    CTRLWORD,X'01'      INDICATE SYSPRINT WAS PRESENT
NODD     OI    CTRLWORD,X'04'      INFORM VOLMOUNT OF NEW DD LIST
         B     TST4LAST            GO LOOK FOR HEADER
*                                                                S21046
*   PICK UP STARTING PAGE NUMBER IF GIVEN                        S21046
*                                                                S21046
HEADLINE EQU   *
         MVC   PAGENUM(4),2(REG1)  PICK UP PAGE NUMBER
         B     TST4LAST
*                                                                S21046
*   INITIALIZE BLOCKSIZE ERROR FLAGS , OPEN IN AND OUT DCB'S     S21046
*  AND RETURN TO PROG1                                           S21046
*                                                                S21046
FLEE     EQU   *                  RETURN TO PROG1
         MVC   DBLWORD(MOVE8),OPENLIST
         MVI   DFLG,X'00'               INITIALIZE BLKSIZE         BS0H
         MVI   DFLAG,X'00'              ERROR FLAGS                BS0H
         OPEN  (UTINDCB,(INPUT),UTOUTDCB,(OUTPUT)),MF=(E,DBLWORD)
         L     SAVEREG,SAVEREG2+4 RETRIEVE OLD SAVE AREA          M2838
         LM    14,12,12(13)             RESTORE THE REGISTERS
         L     15,DECB2+4               GET RETURN V TYPE ADCON
         BR    15                       BE GONE
*                                                                  BS0H
*                   DCB  EXIT  ROUTINES                            BS0H
*                                                                  BS0H
DCBEXIT1 EQU   *                  CHECK LRECL AND BLKSIZE
         LH    REG5,UTINDCB+62          LOAD BLKSIZE               BS0H
         LTR   REG5,REG5                                           BS0H
         BC    8,BLKSZ                  IF NONE SPECIFIED --       BS0H
         SR    REG4,REG4                                           BS0H
         LA    REG3,80                  CHECK FOR MODULAR BLKSIZE  BS0H
         DR    REG4,REG3                                           BS0H
         LTR   REG4,REG4                                           BS0H
         BCR   8,RETURN           IF OK RETURN TO CALLER - OPEN    BS0H
         MVI   DFLAG,X'10'             IF NOT MODULAR - SET FLAG   BS0H
         MVC   UTINDCB+62(2),UTINDCB+82    MOVE LRECL TO BLKSIZE   BS0H
         RETURN
BLKSZ    MVI   UTINDCB+63,X'50'         INSERT BLKSIZE -- 80       BS0H
         RETURN
DCBEXIT2 EQU   *                  CHECK LRECL AND BLKSIZE
         LH    REG5,UTOUTDCB+62         LOAD BLKSIZE               BS0H
         LTR   REG5,REG5                                           BS0H
         BC    8,BLKSIZ                 IF NONE SPECIFIED --       BS0H
         SR    REG4,REG4                                           BS0H
         LA    REG3,121                                            BS0H
         DR    REG4,REG3                CHECK FOR MODULAR BLKSIZE  BS0H
         LTR   REG4,REG4                                           BS0H
         BCR   8,RETURN           IF OK RETURN TO CALLER - OPEN    BS0H
         MVI   DFLG,X'10'               IF NOT MODULAR - SET FLAG  BS0H
         MVC   UTOUTDCB+62(2),UTOUTDCB+82  MOVE LRECL TO BLKSIZE   BS0H
         RETURN
BLKSIZ   MVI   UTOUTDCB+63,X'79'        INSERT BLKSIZE -- 121      BS0H
         RETURN
OPENLIST OPEN  (,(INPUT),,(OUTPUT)),MF=L
COUNTFLD DC    CL8'LINECNT='
PRINT    DC    CL5'PRINT'              SHOULD INPUT BE PRINTED   S21046
NOPRINT  DC    CL7'NOPRINT'       SUPPRESS PRINTING CONTROL CARD S21046
INEXIT   DC    X'85'                                               BS0H
         DC    AL3(DCBEXIT1)                                       BS0H
OUTEXIT  DC    X'85'                                               BS0H
         DC    AL3(DCBEXIT2)                                       BS0H
         EXTRN IEHPROGD           SYSIN END ROUTINE               M2838
         EXTRN IEHPROGE           SYNAD ROUTINE                   M2838
DCBSTART DS    0F            LABLE FIRST ITEM IN THE DCB
         DCB   DSORG=PS,MACRF=(GM),                                    .
               DDNAME=SYSIN,RECFM=FB,                                  .
               LRECL=80,EXLST=INEXIT,BUFNO=1,                      BS0H*
               EODAD=IEHPROGD,SYNAD=IEHPROGE                      M2838
SPECIALS DC    C'.,=()   '
         DC    FL2'45'             MAXIMUM LINES PER PAGE      @YA02534
         DC    FL2'0'              LINE COUNT FIELD
         DC    C'01'               SKIP TO ONE COMMAND IN ASA
         DC    CL30'SYSTEM SUPPORT UTILITIES ---- '
         DC    CL30'IEHPROGM                      '
         DC    CL30'                              '
         DC    CL30'               PAGE 0001      '
         DC    CL8'SYSIN'
         DC    CL8'SYSPRINT'
         DC    CL8'SYSUT1'
         DCB   DSORG=PS,MACRF=(PM),                                    .
               DDNAME=SYSPRINT,RECFM=FBA,LRECL=121,EXLST=OUTEXIT,  BS0H*
               EODAD=IEHPROGE,SYNAD=IEHPROGE,BUFNO=1              M2838
DCBSTOP  EQU   *-1                LABLE LAST ITEM IN THE DCB
DSECT    DSECT
IEHECHAR DS    C         CHARACTER TO BE SCANNED FOR
IEHECOND DS    C         CONDITION CODE TO STOP ON
IEHECODE DS    C
IEHEARGL DS    C
IEHESTT  DS    F
IEHESTP  DS    F
IEHEFND  DS    F
IEHETBL  DS    F
IEHEFUN  DS    F
IEHESCNX DS    3F
IEHEMAC1 DS    7F                 PARAMETER LIST                 S21046
IEHEMAC2 DS    6F
IEHESTIN DS    F
IEHENDIN DS    F
IEHECCNT DS    2C
IEHESW1  DS    C
DUPSW    DS    2C                 SWITCHES FOR TEST OF DUPLICATE S21046
*                                 PARAMETERS                     S21046
PPARAMSW DS    CL2
IEHESW5  DS    C
IEHESW6  DS    C
IEHECALL DS    F
         DS    0D
DBLWORD  DS    F
FULLWORD DS    F
         DS    0D
DECB1    DS    24X
DECB2    DS    24X
VOLISTAD DS    F
MATRIXIU DS    F
VDTIU    DS    F
VOLPAREN DS    FL2
VSRCM    DS    2X
VDSA     DS    X
DFLG     DS    X                                                   BS0H
DFLAG    DS    X                                                   BS0H
         DS    0F
FLSTADDR DS    F
FNSA     DS    12X
KWSA     DS    12X
SCANRET  DS    F
MARESAR  DS    72X
IEHESPC  DS    XL20
         DS    XL20
IEHERSAV DS    64X
PASSUNIT DS    5X                                                  DT0I
MOUNTSW  DS    X                                                   DT0I
TABLEAD  DS    F                  ADDRESS OF DEVICE NAME TABLE     I276
IEHESW2  DS    X                  PROTECT SWITCHES               S21046
*                                 BIT 1=1 IF WE ARE TO PRINT     S21046
*                                        CONTROL CARDS           S21046
*                                 BIT 2=1 IF THE DCB'S ARE       S21046
*                                         OPENED DUE TO PARM     S21046
*                                 BIT 3=1 INDICATES FIRST QUOTE  S21046
*                                        IN SCAN FOR DATA        S21046
*                                        OPERATION IS FOUND      S21046
*                                 BIT 4= 1 IF WTOR SETUP IS DONE S21046
*                                 BIT 6=1 IF FIRST PARM ENTRY    S21046
*                                 WAS VALID                      S21046
*                                 BIT 7=1 IF INVALID PARM ENTRY  S21046
*                                 MSG HAS BEEN PRINTED           S21046
PASWDCT1 DS    1X                 NUMBER OF PROMPTS FOR PASWORD1 S21046
PASWDCT3 DS    1X                 NUMBER OF PROMPTS FOR CPASWORD S21046
TOTALPS  DS    1X                 TOTAL NO OF OPERATOR REPLIES   S21046
WTORECB  DS    1F                 EVENT CONTROL BLOCK FOR WTOR   S21046
TIOTADRS DS    1F                 TIOT ADDRESS FOR WTOR          S21046
CCNAME   DS    8C                 NAME OFF CONTROL CARD          S21046
WTORLIST WTOR  'IEH220A JOBNAMES, STEPNAME, CARDNAME, REPLY WITH PASSWOX
               RD',,8,WTORECB,ROUTCDE=(9),DESC=2,MF=L            S21046
EXTRACTO EXTRACT ,'S',FIELDS=(TIOT),MF=L                         S21046
DCBAREA  DS    0F
UTINDCB  DCB   DSORG=PS,MACRF=(GM)
CODELIST DC    C'.,=()   '        SPECIAL CHARACTERS CODED BY SCAN
MAXLINES DC    FL2'45'             MAXIMUM LINES PER PAGE
LINECNT  DC    FL2'0'              LINE COUNT FIELD
SKIP21   DC    X'8B'               SKIP TO 1 COMMAND
WRITEHDR DC    X'19'               SPACE 3 AFTER PRINT
HEADER   DC    CL30'MODIFY....VERSION BETA-1......'
         DC    CL30'LAST UPDATED....JULY 11  10 PM'
         DC    CL30'                              '
         DC    CL30'               PAGE 001       '
PAGENUM  EQU   HEADER+110
DDNAMES  DS    3CL8                     FOR THREE DD NAMES
UTOUTDCB DCB   DSORG=PS,MACRF=(PM)
         DS    2F
IEHESAT  DS    150X
READBASE DC    F'0'
ERRFLD   DS    0CL8               FOR POSTING OF ERROR CODES     S21046
PARAM1   DS    44X
         DS    F              FOR  LENGTH OF  PARAM2
PARAM2   DS    44X
         DS    F                   FOR LENGTH OF PARAM3
PARAM3   DS    44X
PARAM5   DS    2F                 CPASWORD PARAMETER             S21046
PARAM6   DS    151F               VOL IDS PARAMETER              S21046
PARAM7   DS    2F                 PASSWORD2 PARAMETER            S21046
PARAM8   DS    80X                DATA PARAMETER                 S21046
DUMMY    DS    1F                 TEMPORARY STORAGE FIELD        S21046
PROTCTAD DS    1F                 ADDRESS OF WHERE TO PUT NEXT   S21046
*                                 VOL ID IN PARAM6 FOR PROTECT   S21046
FINUSE   DS    F                   ADDR OF DC LIST IN USE
DCSAVEAD DS    F                   SAVE AREA FOR ADDR OF DC LIST
INPUT    DS    40F                 TWO CARD INPUT AREA
MSGOUT   DS    30F                 MSG OUTPUT AREA
         DS    F
         DS    0D
PARAM4   DS    151F                FOR A VOLUME LIST
EOCORE   EQU   *-12
         ORG   PARAM4+20
VTOCDCB2 DS    0F
DCB4VTOC DCB   DDNAME=VTOC,                                            .
               DSORG=PS,                                               .
               MACRF=R,                                                .
               KEYLEN=44,                                              .
               BLKSIZE=1,                                              .
               RECFM=U,                                                .
               EODAD=VTOCEND,                                          .
               SYNAD=VTOCERR,                                          .
               EXLST=VTOCEND
JFCB     DS    24D
*        THE FOLLOWING ORG STATEMENT IF FLAGGED                  YM5077
         ORG
SAVEREG2 DS    72X                SECONDARY SAVE AREA             M2838
SAVEREG3 DS    72X                THIRD LEVEL SAVE AREA           M2838
         ORG
VTOCSW   EQU   JFCB+50
VTOCDECB EQU   JFCB+60
JFCBVOL  EQU   JFCB+118
EXLST2   EQU   DCB4VTOC+36
FORMATID EQU   PARAM2+44
BLANK    EQU   64
READTOO  EQU   INPUT+72
COL16    EQU   READTOO+15
COL72    EQU   READTOO+71
MIDDLE   EQU   READTOO-1
WARE2PUT EQU   IEHEMAC1-4
CTRLWORD EQU   IEHESAT
VTOCEND  EQU   *
VTOCERR  EQU   *
VTOCDCB  EQU   *
         END
