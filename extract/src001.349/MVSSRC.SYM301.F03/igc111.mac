JEOB     TITLE 'IGC111 - JOB ENTRY SUBSYSTEM ACCESS METHOD SVC ROUTINE'
IGC111   START 0                   SVC 111 -- JES ACCESS METHOD SVC
         EJECT
***********************************************************************
*                                                                     *
* MODULE NAME -- IGC111                                               *
*                                                                     *
* DESCRIPTIVE NAME -- SVC 111                                         *
*                                                                     *
* COPYRIGHT -- NONE                                                   *
*                                                                     *
* STATUS -- RELEASE 1, LEVEL 0                                        *
*                                                                     *
* FUNCTION -- PROVIDES AN EXIT TO AN ACCESS METHOD END-OF-BLOCK       *
*             ROUTINE IN KEY 0, SUPERVISOR STATE, WITH NO LOCKS.      *
*             THIS ROUTINE IS USED BY THE JOB ENTRY SUBSYSTEM ACCESS  *
*             METHODS TO MOVE DATA TO AND FROM PROTECTED AREAS OF     *
*             MAIN STORAGE.  SECURITY IS ASSURED BY REQUIRING THAT    *
*             THE EXIT ADDRESS BE PROVIDED THROUGH A VALID DATA       *
*             EXTENT BLOCK (DEB).  INVALID CALLERS WILL BE TERMINATED *
*             WITH A SYSTEM 36F ABEND CODE.                           *
*                                                                     *
* NOTES -- SEE BELOW                                                  *
*                                                                     *
*    DEPENDENCIES -- NONE                                             *
*                                                                     *
*    RESTRICTIONS -- NONE                                             *
*                                                                     *
*    REGISTER CONVENTIONS -- R0 = ENTRY CODE, UNUSED BY IGC111        *
*                            R1 = ADDRESS OF RPL                      *
*                            R2 = ADDRESS OF ASSOCIATED DEB           *
*                            R3 = ADDRESS OF CVT                      *
*                            R4 = ADDRESS OF CALLER'S TCB             *
*                            R5 = ADDRESS OF CURRENT RB               *
*                            R6 = ADDRESS OF IGC111 ENTRY POINT       *
*                            R7 = ADDRESS OF ASCB                     *
*                            R8 = ADDRESS OF ASSOCIATED ACB           *
*                                                                     *
*    PATCH LABEL -- NONE                                              *
*                                                                     *
* MODULE TYPE -- SUPERVISOR                                           *
*                                                                     *
*    PROCESSOR -- ASSEMBLER F OR ASSEMBLER H                          *
*                                                                     *
*    MODULE SIZE -- 88 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES -- RE-ENTERABLE                                       *
*                                                                     *
* ENTRY POINT -- IGC111                                               *
*                                                                     *
*    PURPOSE -- SEE FUNCTION                                          *
*                                                                     *
*    LINKAGE -- SVC 111                                               *
*                                                                     *
* INPUT -- R0 = ENTRY CODE                                            *
*          R1 = IF POSITIVE, ADDRESS OF RPL                           *
*               IF NEGATIVE, ADDRESS OF ACB                           *
*          R3 = ADDRESS OF CVT                                        *
*          R4 = ADDRESS OF CALLER'S TCB                               *
*          R5 = ADDRESS OF CURRENT RB                                 *
*          R6 = ADDRESS OF IGC111 ENTRY POINT                         *
*          R7 = ADDRESS OF ASCB                                       *
*                                                                     *
* OUTPUT -- SAME AS INPUT, PLUS -                                     *
*           R2  = ADDRESS OF ASSOCIATED DEB                           *
*           R15 = ADDRESS OF END-OF-BLOCK ROUTINE                     *
*                                                                     *
* EXIT-NORMAL -- TO END-OF-BLOCK ROUTINE SPECIFIED IN THE SECOND WORD *
*                OF AN EXIT LIST POINTED TO BY THE DEBAPPAD FIELD OF  *
*                THE DEB.  ENTRY TO THE END-OF-BLOCK ROUTINE IS MADE  *
*                IN KEY ZERO, SUPERVISOR STATE, WITH NO LOCKS.        *
*                                                                     *
* EXIT-ERROR -- IF RPL/ACB ADDRESS IS ZERO OR IF DEB IS INVALID,      *
*               EXIT IS MADE THROUGH ABEND (SVC 13) WITH AN ABEND     *
*               CODE OF SYSTEM 36F                                    *
*                                                                     *
* EXTERNAL REFERENCES -- SEE BELOW                                    *
*                                                                     *
*    ROUTINES -- DEB CHECK ROUTINE SPECIFIED BY CVTDEBVR ENTRY IN CVT *
*                END-OF-BLOCK ROUTINE SPECIFIED THROUGH DEB           *
*                                                                     *
*                SETLOCK ROUTINE                                      *
*                                                                     *
*    DATA AREAS -- NONE                                               *
*                                                                     *
*    CONTROL BLOCKS -- REQUEST PARAMETER LIST (RPL)                   *
*                      ACCESS METHOD CONTROL BLOCK (ACB)              *
*                      DATA EXTENT BLOCK (DEB)                        *
*                      COMMUNICATIONS VECTOR TABLE (CVT)              *
*                      CVT EXTENSION                                  *
*                      REQUEST BLOCK (RB)                             *
*                                                                     *
* TABLES -- NONE                                                      *
*                                                                     *
* MACROS -- IHAPSA                                                    *
*           CVT                                                       *
*           IKJRB                                                     *
*           IFGRPL                                                    *
*           IFGACB                                                    *
*           IEZDEB                                                    *
*           SETLOCK                                                   *
*                                                                     *
* CHANGE ACTIVITY -- NONE                                             *
*                                                                     *
***********************************************************************
         TITLE 'SYMBOLIC REGISTER DEFINITIONS'
         SPACE 5
*
*                                  SYMBOLIC REGISTER DEFINITIONS
*
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE 'PREFIXED SAVE AREA (PSA) DSECT'
         IHAPSA DSECT=YES
         TITLE 'COMMUNICATION VECTOR TABLE (CVT) DSECT'
         CVT   LIST=YES,DSECT=YES
         TITLE 'REQUEST BLOCK (RB) DSECT'
         IKJRB
         TITLE 'REQUEST PARAMETER LIST (RPL) DSECT'
         IFGRPL
         TITLE 'ACCESS METHOD CONTROL BLOCK (ACB) DSECT'
         IFGACB
         TITLE 'DATA EXTENT BLOCK (DEB) DSECT'
         IEZDEB LIST=YES
         TITLE 'IGC111 - JOB ENTRY SUBSYSTEM ACCESS METHOD SVC ROUTINE'
IGC111   CSECT                     SVC 111 -- JES ACCESS METHOD SVC
         USING IFGRPL,R1           ESTABLISH RPL ADDRESSABILITY
         USING DEBBASIC,R2         ESTABLISH DEB ADDRESSABILITY
         USING CVTMAP,R3           ESTABLISH CVT ADDRESSABILITY
         USING RBBASIC,R5          ESTABLISH RB ADDRESSABILITY
         USING IGC111,R6           ESTABLISH LOCAL ADDRESSABILITY
         USING IFGACB,R8           ESTABLISH ACB ADDRESSABILITY
         USING CVTXTNT2,R15        ESTABLISH CVT EXTENT ADDRESSABILITY
         SPACE 3
         LCR   R8,R1               TEST ADDRESS OF RPL/ACB
         BZ    IGCERROR            ILLEGAL IF ZERO
         BP    IGCACB              BRANCH IF ARGUMENT IS ACB
         L     R8,RPLDACB          GET ADDRESS OF ACB FROM RPL
IGCACB   L     R2,ACBDEB-1         GET ADDRESS OF DEB FROM ACB
         TM    RBOPSW+1,X'81'      TEST CALLER'S KEY AND STATE
         BNO   IGCNOCHK            BYPASS DEB CHECK IF CALLER IS IN
*                                   SUPERVISOR KEY OR STATE
         SPACE 2
*                                  PREPARE TO CHECK DEB
IGCLOCK  SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,  GET LOCAL LOCK        C
               RELATED=(TCBDEB,IGC111(IGCUNLOK))
         LR    R8,R1               SAVE RPL ADDRESS
         LR    R1,R2               SET R1 TO POINT TO DEB
         LR    R10,R4              SET R10 TO POINT TO CURRENT TCB
         L     R15,CVTEXT2         GET ADDRESS OF CVT EXTENSION
         L     R15,CVTDEBVR        GET ADDRESS OF DEBCHK BRANCH ENTRY
         BALR  R14,R15             CHECK THE DEB
         B     IGCDEBOK            +0 - DEB IS VALID
*                                  +4 - DEB IS INVALID
         SPACE 2
IGCERROR L     R1,IGCABEND         SET ABEND CODE SYSTEM 36F
         SVC   13                  ABEND CALLER
         SPACE 3
IGCDEBOK LR    R1,R8               RESTORE RPL ADDRESS
IGCUNLOK SETLOCK RELEASE,TYPE=LOCAL,  RELEASE LOCAL LOCK               C
               RELATED=(TCBDEB,IGC111(IGCLOCK))
IGCNOCHK L     R15,DEBAPPAD        GET ADDRESS OF EXIT LIST
         L     R15,4(,R15)         GET ADDRESS OF EOB ROUTINE
         BR    R15                  AND EXIT
         SPACE 3
IGCABEND DC    X'8036F000'         INVALID DEB ABEND CODE
         END
