         TITLE 'IGE0904A - BTAM ERP CONTROL LOAD TWO MODULE'
IGE0904A CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0904A                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM ERP CONTROL, LOAD 2                                         *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS MODULE IS ENTERED FOR EQUIPMENT CHECK ERRORS. IT DETERMINES *
*    IF A SPECIAL ENTRY IS REQUIRED FOR EXECUTING DISABLE. IF         *
*    REQUIRED THE FAILING CHANNEL PROGRAM IS RETRIED.                 *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (28 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        292 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD.  *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0904A                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        SETS INDICATOR IN DECERRST FOR SHOULD NOT OCCUR ERROR        *
*        ALTERED CHANNEL PROGRAM                                      *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        NONE                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YM05652K (1/25/74)                                               *
*    ZA14045  (09/28/76)                                              *
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         L     UCBREG,IOSUCB       BASE UCB
         DROP  ERREG5
         L     CCWREG,IOBFL3       POINT TO CCW
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       ADJUST TO FAILING CCW
         CLI   IOBERRCT+L1,MASK15   SPECIAL ENTRY
         BE    ERRTOVR5            YES
         OC    EWASTUP(L1),IOBSENS0   PUT SENSE DATA INTO TABLE
         CLI   IOBERRCT,ERTRYCT    RETRY COUNT 2
         BE    ERRTOVR             YES POST COMPLETE
         IC    ERREG1,IOBERRCT     PICK UP RETRY COUNT
         LA    ERREG1,L1(ERREG1)   UPDATE COUNT
         STC   ERREG1,IOBERRCT     STORE IN COUNT FIELD
         ST    CCWREG,IOBSTART     START PTR TO FAILING CCW
         B     RETRY               EXIT
ERRTOVR  TM    IOBCSW+L3,CSWASCB   SHOULD NOT OCCUR ERROR
         BNZ   ERRTOVR1            YES SNO MSG REQ
         TM    IOBCSW+L3,CSWUCK    UNIT CHECK
         BZ    ERRTOVR1            NO INDICATE SNO ERROR
         TM    IOBSENS0,CSWCRBO    CMD REJ OR BUSS OUT
         BNZ   ERRTOVR2            YES-BYPASS SNO MESSAGE
ERRTOVR1 OI    DECERRST(DECBRG),CSWSNO   INDICATE SNO ERROR
ERRTOVR2 TM    IOBSIOCC,IOBCCODE     SIO COND CODE 1
         BZ    ERRTOVR3            NO CONTINUE
         LA    ERREG1,ERRCN08      BUMP CONSTANT
         MVC   IOBCSW(L3),IOBSTART+L1   RESET CSW
ERRTOVR4 L     ERREG2,IOBFL3       FIRST WORD OF CSW
         LA    ERREG2,ZERO(ERREG2)   CLEAR OUT FLAG BYTE
         AR    ERREG2,ERREG1       ADJUST TO FAILING CCW
         ST    ERREG2,EWAISAVE     STORE IN EWA
         MVC   IOBCSW(L3),EWAISAVE+L1   STORE IN CSW
         NI    IOBSIOCC,L255-IOBCCODE   TURN OF SIO COND CODE
         NI    IOSCC,L255-IOBCCODE   DO SAME IN IOSB
ERRTOVR3 MVI   IOBERRCT+L1,TERMPASS   INDICATE TERM ID BYPASS
         OI    IOSFLB,IOSLOG       INDICATE LOG OUT
         LH    NAMEREG,ERLOD5      ADDRESS OF  POST ROUTINE
XCTLOUT  EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE
         DROP  ERRETR
         BR    ERRETR              XCTL
ERRTOVR5 TM    UCBTYP+L1(UCBREG),SWLINE   DISABLE REQUIRED
         BZ    ERRTOVR3            NO, WTO
         MVC   IOBERINF+L8(L8),ZERO(CCWREG)   SAVE FAILING CCW
         MVC   IOBERINF(L1),IOBSENS0   SAVE SENSE DATA
         MVC   IOBERINF+L1(L7),IOBCSW   SAVE CSW DATA
         MVI   IOBERRCT+L1,DISABIND   INDICATE DISABLE EXECUTED
         OI    IOSFLB,IOSLOG       INDICATE LOG OUT
         MVC   IOBERCCW(L8),DISABCMD   GENERATE CCW
         LA    ERREG1,IOBERCCW     CHANNEL PROGRAM START
         ST    ERREG1,IOBSTART     MAKE IOBSTART = IOBERCCW    @ZA14045
RETRY    EQU   *
         MVI   EWAIND1,EWAISVCF    SVC 15/SVC 3                YM05652K
         L     NAMEREG,TRANSRTN    LOAD NAME
         B     XCTLOUT             XCTL
         EJECT
**********
*
*        CONSTANTS
*
**********
         DS    0D
DISABCMD DC    X'2F00000020300001'
TRANSRTN DC    F'9042'             NAME OF BTAM TRANSLATE ROUTINE
ERRCN8   DC    H'8'
ERLOD5   DC    H'5041'
PACHAREA DC    7C'904A'            PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
IOBCCODE EQU   X'10'               SIO CC = 1
ZERO     EQU   0                   LENGTH AND DISPLACEMENT
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
CSWASCB  EQU   X'F0'
CSWCRBO  EQU   X'A0'
CSWSNO   EQU   X'40'
CSWUCK   EQU   X'02'               UNIT CHECK
DECERRST EQU   29
DISABIND EQU   X'80'
ERRCN08  EQU   8
ERTRYCT  EQU   X'02'
L255     EQU   255                 AND MASK
MASK15   EQU   X'0F'
SWLINE   EQU   X'90'
TERMPASS EQU   X'40'
UCBTYP   EQU   16                  DEVICE TYPE CODE
**********
*
*        REGISTER DEFINITION
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
ERREG2   EQU   2                   WORK REG
EWAREG   EQU   3                   ERP WORKAREA BASE
CCWREG   EQU   4                   CCW ADDRESS REG
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE
BASEREG  EQU   8                   PROGRAM BASE
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG4   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  LINK REG
ERREG5   EQU   15                  WORK REG/LINK REG
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
