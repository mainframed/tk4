         TITLE 'IECTSVC - BTAM LOCAL 3270 SUPPORTING SVC LOGIC'
IECTSVC  CSECT
*
*
*        MAINTENANCE ACTIVITY
*
*        YA02130
*        YA02174
*        YA02128
*        YA02451
*        YA03231
*        YA03253
*
*        ZA00537  (11/25/74)        ZA02332  (11/25/74)
*        ZA03587  (03/19/75)        AZ08050  (01/29/76)
*        ZA14028  (09/14/76)
*
         SPACE 5
         ENTRY IECTRDIL,IECTATRL,IECTCHSL,IECTCHAL,IECTRSTL
         EJECT
* STATUS- CHANGE LEVEL 000
*
* FUNCTION- 1. TEST FOR AN OUTSTANDING ATTENTION ON A LINE GROUP AND
*    QUEUE THE READ INITIAL IF NO ATTENTION EXISTS (IECTRDIL).
*
*         2. RESET ATTENTION STATUS FLAG FOR A DEVICE AND UPDATE THE
*    LINE GROUP'S ATTENTION COUNT (IECTATRL).
*
*         3. SET THE SKIP FLAG FOR A DEVICE TO INHIBIT PERFORMING READ
*    INITIAL OPERATIONS ON THAT DEVICE (IECTCHSL).
*
*         4. RESET THE SKIP FLAG FOR A DEVICE TO PERMIT PERFORMING READ
*    INITIAL OPERATIONS ON THAT DEVICE (IECTCHAL).
*
*         5. DEQUEUE A QUEUED READ INITIAL OPERATION (IECTRSTL).
*
* ENTRY POINTS- 1. IECTRDIL FROM SVC 116 IF THE ROUTING CODE WAS 0.
*
*         2. IECTATRL FROM SVC 116 IF THE ROUTING CODE WAS 1.
*
*         3. IECTCHSL FROM SVC 116 IF THE ROUTING CODE WAS 2.
*
*         4. IECTCHAL FROM SVC 116 IF THE ROUTING CODE WAS 3.
*
*         5. IECTRSTL FROM SVC 116 IF THE ROUTING CODE WAS 4.
*
* INPUT-  1. REGISTER 15 CONTAINS THE ROUTING CODE ON ENTRY TO SVC 116.
*         2. REGISTER  0 CONTAINS THE ADDRESS OF THE DECB FOR IECTRSTL
*    AND ZERO IN ALL OTHER CASES
*         3. REGISTER  1 CONTAINS THE ADDRESS OF THE UCB FOR THE RLN
*    SPECIFIED OR IMPLIED
*         4. REGISTER 14 CONTAINS THE ADDRESS OF THE TYPE 1 SVC EXIT
*    ROUTINE.
*
* OUTPUT- 1. UPDATED ATTENTION FLAGS IN THE UCB'S FOR THE DEVICE AND
*    LINE GROUP.
*         2. A RETURN CODE IN REGISTER 15.
*
* EXITS,NORMAL- BR 14 TO THE TYPE 1 SVC EXIT ROUTINE.
*
* EXITS,ERROR-  BR 14 TO THE TYPE 1 SVC EXIT ROUTINE.
*
* TABLES/WORK AREAS- NONE
*
* ATTRIBUTES- NUCLEUS RESIDENT, DISABLED, SUPERVISOR PROTECT KEY, READ
*    ONLY, TYPE 1 SVC INITIATED FUNCTION.
*
         EJECT
         EJECT
* CHARACTER CODE DEPENDENCY- N/A
*
* NOTES- THIS ROUTINE MANIPULATES ATTENTION HANDLING FLAGS IN THE UCB'S
*    FOR LOCAL 3270 DEVICES SUPPORTED UNDER BTAM.  USER REGISTERS 2-14
*    ARE SAVED BY SVC FLIH AND RESTORED ON RETURN TO THE USER.
*
         EJECT
*/*IECTSVC: CHART */
*/* HEADER
*/*LOGIC MODULE FOR ESR ROUTING CODES 0 THROUGH 4
*/*BTAM 3270 ATTENTION HANDLING SUPPORTING SVC                      */
*/*IECTRDIL: E ENTRY FROM ESR */
*/* M ESTABLISH ADDRESSABILITY AND SAVE RETURN ADDR */
*/* S UCBCHK: CHECK UCB ADDRESS FOR VALIDITY */
*/* D (YES,,NO,RC8) UCB VALID? */
*/* D (YES,,NO,RC0) ATTENTION COUNT IS ZERO? */
*/* D (YES,,NO,CONTRDTI) BTAM READY PROCESSING ? */
*/* D (YES,RDYPROC,NO,) READY IN LINE GROUP ? */
*/*CONTRDTI: P SET READ INITIAL PENDING FLAG */
*/* P (,RC4) SAVE RLN OF INITIALIZED IOB */
*/*RDYPROC: P SET READ INITIAL PENDING FLAG */
*/* P (,RCC) SAVE RLN IN MASTER, SET READY NOT DONE IN MASTER */
*/*IECTATRL: E ENTRY FROM ESR */
*/* M ESTABLISH ADDRESSABILITY AND SAVE RETURN ADDR */
*/* S UCBCHK: CHECK UCB ADDRESS FOR VALIDITY */
*/* D (YES,,NO,RC4) UCB VALID? */
*/* D (YE>,,NO,RDYCHKT) ATTN FLAG SET ? */
*/* D (YES,RDYCHKT,NO,) PROCESSING READY ? */
*/* P RESET THE ATTENTION FLAG */
*/* D (NO,,YES,RC0) SKIP FLAG SET? */
*/* P (,RC0) DECREMENT ATTENTION COUNT FOR LINE GROUP BY 1 */
*/*RDYCHKT: D (YES,,NO,RC4) USING BTAM'S READY ROUTINE ? */
*/*RDYCHK: D (YES,,NO,RC0) READY NOT DONE FLAG ON ? */
*/* L (,RC0) INVOKE POST TO RESETPL THE READ */
*/*IECTCHSL: E ENTRY FROM ESR */
*/* M ESTABLISH ADDRESSABILITY AND SAVE RETURN ADDR */
*/* S UCBCHK: CHECK UCB ADDRESS FOR VALIDITY */
*/* D (YES,,NO,RC8) UCB VALID? */
*/* D (NO,,YES,RC0) SKIP FLAG SET? */
*/* P SET THE SKIP FLAG */
*/* D (YES,,NO,RC0) ATTENTION FLAG SET */
*/* P (,RC0) DECREMENT ATTENTION COUNT FOR LINE GROUP BY 1 */
*/*IECTCHAL: E ENTRY FROM ESR */
*/* M ESTABLISH ADDRESSABILITY AND SAVE RETURN ADDR */
*/* S UCBCHK: CHECK UCB ADDRESS FOR VALIDITY */
*/* D (YES,,NO,RC8) UCB VALID? */
*/* D (YES,,NO,RC0) SKIP FLAG SET? */
*/* P RESET THE SKIP FLAG */
*/* D (YES,,NO,RC0) ATTENTION FLAG SET? */
*/* D (NO,,YES,SCHEDIQE) READ INITIAL PENDING? */
*/* P (,RC0) INCREMENT ATTENTION COUNT FOR LINE GROUP BY 1 */
*/*SCHEDIQE: P RESET READ INITIAL PENDING AND ATTENTION FLAGS */
*/* P OBTAIN AND INITIALIZE IQE */
*/* L (,RC0) INVOKE AEE2 TO SCHEDULE READ */
*/*IECTRSTL: E ENTRY FROM ESR */
*/* M ESTABLISH ADDRESSABILITY AND SAVE RETURN ADDR */
*/* S UCBCHK: CHECK UCB ADDRESS FOR VALIDITY */
*/* D (0,,4,RC8,8,RCC) TEST RETURN CODE */
*/*RDTITST: D (YES,,NO,RC4) READ INITIAL PENDING? */
*/* P RESET READ INITIAL PENDING FLAG */
*/* P CLEAR INITIALIZED RLN FIELD */
*/* L (,RC0) POST USER'S ECB COMPLETE WITH X'48' CODE */
*/*RC0: P SET RETURN CODE OF 0 */
*/* R BR TO TYPE I SVC EXIT RTN */
*/*RC4: P SET RETURN CODE OF 4 */
*/* R BR TO TYPE I SVC EXIT RTN */
*/*RC8: P SET RETURN CODE OF 8 */
*/* R BR TO TYPE I SVC EXIT RTN */
*/*RCC: P SET RETURN CODE OF X'0C' */
*/* R BR TO TYPE I SVC EXIT RTN */
*/*IECTSVC: END */
*/*UCBCHK: CHART */
*/* HEADER
*/*IECTSVC UCB VALIDITY CHECK SUBROUTINE                            */
*/*UCBCHK: E ENTRY */
*/* M ESTABLISH ADDRESSABILITY FOR SUBROUTINE */
*/* M ESTABLISH ADDRESSABILITY FOR DEVICE UCB */
*/* D (YES,,NO,VCRC08) UCBID = X'FF'? */
*/* D (YES,,NO,VCRC04) IS DEVICE A 3270 DEVICE? */
*/* D (YES,,NO,VCRC08) IRB ADDRESS INITIALIZED? */
*/* D (NO,,YES,RLNISONE) DEVICE UCB = MASTER UCB? */
*/* M OBTAIN ADDRESS OF MASTER UCB FROM DEVICE UCB */
*/*CHKMSTR: D (YES,,NO,VCRC08) UCBID OF MASTER UCB = X'FF'? */
*/* D (YES,,NO,VCRC04) IS MASTER DEVICE A 3270 DEVICE? */
*/* D (YES,,NO,VCRC08) IRB ADDR IN MASTER UCB NON-ZERO? */
*/* D (YES,,NO,VCRC08) BOTH UCB IRB ADDRESSES THE SAME? */
*/* D (YES,,NO,VCRC08) RLN IN MASTER UCB = 1? */
*/*VCRC00:  P SET RETURN CODE OF ZERO */
*/* R RETURN */
*/*VCRC04: P SET RETURN CODE OF FOUR */
*/* R RETURN */
*/*VCRC08: P SET RETURN CODE OF EIGHT */
*/* R RETURN */
*/*RLNISONE: P (,VCRC00) SET MASTER UCB = DEVICE UCB */
*/*UCBCHK: END */
         EJECT
PARMREG2 EQU   0                    SECOND PARAMETER REGISTER
PARMREG1 EQU   1                    FIRST PARAMETER REGISTER
CVTREG   EQU   3                    POINTER TO CVT
TCBREG   EQU   4                   POINTER TO TCB
RTNREG   EQU   14                   RETURN POINT REGISTER
EPREG    EQU   15                   ENTRY POINT REGISTER
*
BASEREG  EQU   5                    BASE REGISTER FOR MAIN ROUTINE
SUBASREG EQU   10                   BASE REGISTER FOR SUB-ROUTINE
UCBREG   EQU   9                    BASE REGISTER FOR DEVICE UCB
MSTREG   EQU   8                    BASE REGISTER FOR MASTER UCB
IRBREG   EQU   7                    POINTER TO IRB
IQEREG   EQU   6                    POINTER TO IQE
*
SAVEREG  EQU   12                   SAVE REGISTER FOR REGISTER 14
WORKREG  EQU   2                    WORK REGISTER
POSTCODE EQU   10                  COMPLETION CODE REG FOR POST
POSTECB  EQU   11                  ECB ADDR REG FOR POST
POSTTCB  EQU   12                  TCB ADDR REG FOR POST
ONEBYTE  EQU   8                   EIGHT BITS IN A BYTE     LD @ZA00537
*
DVC3277  EQU   X'09'                DEVICE TYPE FOR 3277 DISPLAY
DVC3286  EQU   X'0B'               DEVICE TYPE FOR 3286 PRINTER
*              X'0A'               DEVICE TYPE FOR 3284 PRINTER
READY    EQU   X'20'               HAS COME READY FLAG         @YA02128
LINERDY  EQU   X'01'               MTR DEVICE IN GROUP READY   @YA02128
RDTIACTV EQU   X'08'               READ INIT REQ OUTSTANDING    YA01033
RDYNDONE EQU   X'04'               INDICATE READY NOT DONE     @YA02128
RLN1     EQU   X'01'                RLN FOR RLN 1 OF LINE GROUP
IGG019UP EQU   X'10'               USING BTAM EXIT ROUT        @YA02128
*                                   (CVT0PT01)
ZERO     EQU   X'00'                COMPARE VALUE OF 0
ONE      EQU   X'01'                INCREMENT VALUE OF 1
FOUR     EQU   X'04'                RETURN CODE OF 4
EIGHT    EQU   X'08'                RETURN CODE OF 8
TWELVE   EQU   X'0C'                RETURN CODE OF X'0C'
X58      EQU   X'58'                DISPLACEMENT TI 1ST IOB  LD YA02451
SIXTEEN  EQU   X'10'               RETURN CODE X'10'           @YA02128
         EJECT
         USING UCB,UCBREG           ADDRESSABILITY FOR DEVICE UCB
         USING MSTRUCB,MSTREG       AND MASTER UCB
         USING RBBASIC,IRBREG                                 LD Y02947
         USING CVT,CVTREG                                     LD Y02947
         USING IQESECT,IQEREG                                 LD Y02947
         SPACE 5
*        READ INITIAL ATTENTION TEST
         SPACE 3
IECTRDIL LR    SAVEREG,RTNREG       SAVE RETURN ADDRESS
         BALR  BASEREG,0            ESTABLISH BASE REGISTER
         USING *,BASEREG            AND ADDRESSABILITY
         B     BEGIN1              BRANCH AROUND ID         LD @ZA02332
         DC    C' IECTSVC'          MODULE IDENTIFIER          @YA02128
         DC    C'** MVS *'                                     @ZA02332
         DC    C'&SYSDATE'         DATE LAST ASSEMBLY       LD @ZA02332
PATCH1   DC    20X'00'             PATCH AREA FOR IECTRDIL  LD @ZA02332
BEGIN1   DS    0F                                           LD @ZA02332
*
         BAL   RTNREG,UCBCHK        CHECK UCB ADDRESS FOR VALIDITY
         LTR   EPREG,EPREG          UCB VALID
         BNZ   RC8                  NO, RETURN WITH RETURN CODE OF 8
*
         OI    MTRGCB,RDTIACTV     READ INIT REQ OUTSTANDING    YA01033
         CLI   MTRATNCT,ZERO        IS ATTENTION COUNT ZERO
         BNE   RC0                  NO, RETURN WITH RETURN CODE OF 0
*
         TM    UCBGRAF,IGG019UP    BTAM READY PROCESSING ?     @YA02128
         BNO   CONTRDTI            NO, DO READ TI              @YA02128
         TM    MTRGRAF,LINERDY     COME READY ?                @YA02128
         BO    RDYPROC             YES, PROCESS READY          @YA02128
CONTRDTI EQU   *                   COMPLETE RDTI               @YA02128
         OI    MTRGCB,UCBRIPND      SET READ INIT.PEND. FLAG  LD Y02947
         MVC   MTRINRLN(ONE),UCBRLN  MOVE RLN OF INITIALIZED LINE
         B     RC4                  RETURN WITH RETURN CODE OF 4
RDYPROC  EQU   *                   DEVICE READY                @YA02128
         OI    MTRGCB,UCBRIPND     SET PENDING FLAG            @YA02128
         MVC   MTRINRLN(ONE),UCBRLN MOVE RLN OF INIT LINE      @YA02128
         OI    MTRGRAF,RDYNDONE    SET NOT DONE IN UCB         @YA02128
         B     RCC                 GIVE RETURN 0C              @YA02128
         DROP  BASEREG
         EJECT
*
*        RESET ATTENTION FLAG
         SPACE 3
IECTATRL LR    SAVEREG,RTNREG       SAVE RETURN ADDRESS
         BALR  BASEREG,0            ESTABLISH BASE REGISTER
         USING *,BASEREG            AND ADDRESSABILITY
ATRLBASE EQU   *                   ALLOW ADDRESSABILITY     L5 @ZA03587
         B     BEGIN2              BRANCH AROUND PATCHAREA  LD @ZA02332
PATCH2   DC    20X'00'             PATCH AREA FOR IECTATRL  LD @ZA02332
BEGIN2   DS    0F                                           LD @ZA02332
*
         BAL   RTNREG,UCBCHK        CHECK UCB ADDRESS FOR VALIDITY
         LTR   EPREG,EPREG          UCB VALID
         BNZ   RC4                  NO, GIVE RETURN CODE OF 4
*
         TM    UCBGCB,UCBATRCD      ATTENTION FLAG SET        LD Y02947
         BZ    RDYCHKT              NO, SEE IF READY           @YA02128
         TM    MTRGRAF,RDYNDONE    READY NOT DONE FLAG ?       @YA02128
         BO    RDYCHKT             YES, PROCESS READY          @YA02128
         XI    UCBGCB,UCBATRCD      YES, RESET THE ATT. FLAG  LD Y02947
*
         TM    UCBGCB,UCBSKPFG      SKIP FLAG SET             LD Y02947
         BO    RC0                  YES, GIVE RETURN CODE OF 0
         SR    WORKREG,WORKREG
         IC    WORKREG,MTRATNCT     DECREMENT ATTENTION COUNT
         BCTR  WORKREG,0            FOR LINE GROUP BY 1
         STC   WORKREG,MTRATNCT
         B     RC0                  GIVE RETURN CODE OF 0
RDYCHKT  EQU   *                   SEE IF BTAM                 @YA02128
         TM    UCBGRAF,IGG019UP    USE BTAMS ?                 @YA02128
         BNO   RC0                 NO, GIVE RETURN CODE 0      @YA02128
RDYCHK   EQU   *                   CHECK FOR READY             @YA02128
         TM    MTRGRAF,RDYNDONE    NOT DONE FLAG ?             @YA02128
         BZ    RC0                 NO, GIVE RETURN CODE 0      @YA02128
         LTR   PARMREG2,PARMREG2   DECB ADDR IN REG ?       L5 @ZA03567
         BZ    RC8                 IF NO,SET RET CODE TO 8  L5 @ZA03567
         LR    SUBASREG,BASEREG    ESTABLISH NEW BASE          @YA02128
         DROP  BASEREG             RELEASE OLD BASE            @YA02128
         USING ATRLBASE,SUBASREG   AND USE NEW BASE            @YA02128
         LA    BASEREG,RSTLBASE   ESTABLISH NEW ADDRESSABILITY @YA02128
         B     POST48              GO POST READ                @YA02128
         DROP  SUBASREG            DROP ADDRESSABILITY         @YA02128
         EJECT
*
*        INHIBIT READ INITIALS ON A DEVICE
         SPACE 3
IECTCHSL LR    SAVEREG,RTNREG       SAVE RETURN ADDRESS
         BALR  BASEREG,0            ESTABLISH BASE REGISTER
         USING *,BASEREG            AND ADDRESSABILITY
         B     BEGIN3              BR AROUND PATCH AREA     LD @ZA02332
PATCH3   DC    20X'00'             PATCH AREA FOR IECTCHSL  LD @ZA02332
BEGIN3   DS    0F                                           LD @ZA02332
*
         BAL   RTNREG,UCBCHK        CHECK UCB ADDRESS FOR VALIDITY
         LTR   EPREG,EPREG          UCB VALID
         BNZ   RC8                  NO, GIVE RETURN CODE OF 8
*
         TM    UCBGCB,UCBSKPFG      SKIP FLAG SET             LD Y02947
         BO    RC0                  YES, GIVE RETURN CODE OF 0
         OI    UCBGCB,UCBSKPFG      NO, SET THE SKIP FLAG     LD Y02947
*
         TM    UCBGCB,UCBATRCD      ATTENTION FLAG SET        LD Y02947
         BZ    RC0                  NO, GIVE RETURN CODE OF 0
         SR    WORKREG,WORKREG
         IC    WORKREG,MTRATNCT     DECREMENT ATTENTION COUNT
         BCTR  WORKREG,0            FOR LINE GROUP BY 1
         STC   WORKREG,MTRATNCT
         B     RC0                  GIVE RETURN CODE OF 0
         DROP  BASEREG
         EJECT
*
*        PERMIT READ INITIALS ON A DEVICE
         SPACE 3
IECTCHAL LR    SAVEREG,RTNREG       SAVE RETURN ADDRESS
         BALR  BASEREG,0            ESTABLISH BASE REGISTER
         USING *,BASEREG            AND ADDRESSABILITY
         B     BEGIN4              BR AROUND PATCH AREA     LD @ZA02332
PATCH4   DC    20X'00'             PATCH AREA FOR IECTCHAL  LD @ZA02332
BEGIN4   DS    0F                                           LD @ZA02332
*
         BAL   RTNREG,UCBCHK        CHECK UCB ADDRESS FOR VALIDITY
         LTR   EPREG,EPREG          UCB VALID
         BNZ   RC8                  NO, GIVE RETURN CODE OF 8
*
         TM    UCBGCB,UCBSKPFG      SKIP FLAG SET             LD Y02947
         BZ    RC0                  NO, GIVE RETURN CODE OF 0
         XI    UCBGCB,UCBSKPFG      YES, RESET THE SKIP FLAG  LD Y02947
*
         TM    UCBGCB,UCBATRCD      ATTENTION FLAG SET        LD Y02947
         BZ    RC0                  NO, GIVE RETURN CODE OF 0
*
         TM    MTRGCB,UCBRIPND      READ INITIAL PENDING      LD Y02947
         BO    SCHEDIQE             YES, SCHEDULE 2ND LEVEL ATTN. RTN
*                                   TO DO READ
         SR    WORKREG,WORKREG      NO, INCREMENT ATTENTION COUNT
         IC    WORKREG,MTRATNCT     FOR LINE GROUP BY 1
         LA    WORKREG,ONE(WORKREG)
         STC   WORKREG,MTRATNCT
         B     RC0                  GIVE RETURN CODE OF ZERO
*
SCHEDIQE XI    MTRGCB,UCBRIPND      RESET RD INIT.PEND. FLAG  LD Y02947
         XI    UCBGCB,UCBATRCD      RESET ATTENTION FLAG      LD Y02947
         L     IQEREG,RBNEXAV       GET ADDRESS OF IQE        LD Y02947
         XC    RBNEXAV(FOUR),RBNEXAV  CLEAR IQE LINK          LD Y02947
         ST    UCBREG,IQEPARAM      STORE UCB ADDRESS IN IQE  LD Y02947
         LNR   PARMREG1,IQEREG      PARM = COMPLEMENT OF IQE ADDR
         L     EPREG,CVT0EF00       EP = AEE2                 LD Y02947
         BALR  RTNREG,EPREG         SCHEDULE IQE
         B     RC0                  GIVE RETURN CODE OF 0
         EJECT
*
*        DEQUEUE PENDING READ INITIAL
         SPACE 3
IECTRSTL LR    SAVEREG,RTNREG       SAVE RETURN ADDRESS
         DROP  BASEREG
         BALR  BASEREG,0            ESTABLISH BASE REGISTER
         USING *,BASEREG            AND ADDRESSABILITY
RSTLBASE EQU   *                   ALLOW ADDRESSABILITY     L5 @ZA03587
         B     BEGIN5              BR AROUND PATCH AREA     LD @ZA02332
PATCH5   DC    20X'00'             PATCH AREA FOR IECTRSTL  LD @ZA02332
BEGIN5   DS    0F                                           LD @ZA02332
*
         BAL   RTNREG,UCBCHK        CHECK UCB ADDRESS FOR VALIDITY
         B     RCTABLE(EPREG)       TEST RETURN CODE
RCTABLE  B     RDTITST             GO TEST READ INITIAL     LD @ZA02332
         B     RC8                  4, INVALID DVC- GIVE RC OF 8
         B     RCC                  8, INVALID UCB- GIVE RC OF X'0C'
*
*              14 LINES DELETED FOR ----->                  LD @ZA02332
*
RDTITST  TM    MTRGCB,RDTIACTV     READ INIT REQ OUTSTANDING?   YA01033
         BZ    RC10                RC=16                       @ZA14028
         TM    MTRGCB,UCBRIPND     READ INIT PENDING?         LD Y02947
         BZ    RC4                  NO, GIVE RETURN CODE OF 4
*                                   (OPERATION IN PROGRESS)
POST48   EQU   *                                             LD YA02174
         NI    MTRGCB,X'FF'-RDTIACTV-UCBRIPND     YES,        LD Y02947
*                                  RESET READ INIT PENDING      YA01033
*                                  AND OUTSTANDING FLAGS        YA01033
         TM    MTRGRAF,IGG019UP    BTAM READY ?                @YA02128
         BNO   CONTPOST            NO, DON'T DO READY CHECK    @YA02128
         TM    MTRGRAF,RDYNDONE    READY PROCESSING            @YA02128
         BNO   CONTPOST            NO, DON'T RESET FLAGS       @YA02128
         NI    MTRGRAF,X'FB'       TURN OFF READY NOT DONE     @YA02128
         NI    UCBGRAF,X'DB'       TURN OFF READY FLAG         @YA02128
         L     EPREG,MTRDEBAD      GET DEB ADDRESS             @YA02128
         LA    EPREG,0(EPREG)      CLEAR HIGH BYTE             @YA02128
         XR    POSTECB,POSTECB     CLEAR RLN REG               @YA02128
         IC    POSTECB,16(EPREG)   GET NUMBER EXTENTS          @YA02128
         XR    POSTCODE,POSTCODE   CLEAR INDEX REGISTER        @YA02128
READYLP  EQU   *                   CHECK UCB'S FOR READY       @YA02128
         L     PARMREG1,32(POSTCODE,EPREG) POINT TO UCB        @YA02128
         TM    28(PARMREG1),READY  COME READY                  @YA02128
         BO    CONTPOST            YES, DON'T TURN OFF         @YA02128
*                                  LINE READY IN MASTER        @YA02128
         LA    POSTCODE,4(POSTCODE) BUMP INDEX                 @YA02128
         BCT   POSTECB,READYLP     CHECK IF MORE TO DO         @YA02128
         NI    MTRGRAF,X'FE'       TURN OFF LINE READY         @YA02128
CONTPOST EQU   *                   CONTINUE THE POST           @YA02128
*
         MVI   MTRINRLN,ZERO        CLEAR INITIALIZED RLN
*        SAVE CONTENTS OF REG 12 ACROSS CALL TO POST
*
         LR    PARMREG1,SAVEREG    REG 1 IS SAVED BY POST
*
*        SET UP LINKAGE TO POST -- NON-STANDARD
*
         L     POSTCODE,COMPCODE   REG 10 = COMPLETION CODE
         LR    POSTECB,PARMREG2    REG 11 = ECB ADDR         LD YA03231
         LA    POSTECB,ZERO(POSTECB) CLEAR HIGH ORDER BYTE   LD YA03231
         LR    POSTTCB,TCBREG      REG 12 = TCB ADDR
         L     EPREG,CVT0PT01      EP = POST                  LD Y02947
         BALR  RTNREG,EPREG        POST USER'S ECB COMPLETE
         LR    SAVEREG,PARMREG1    RESTORE REG 12
         B     RC0                  GIVE RETURN CODE OF 0
         DROP  BASEREG
         EJECT
*
*        RETURN CODES SUBROUTINE
*
RC0      SR    EPREG,EPREG          SET REGISTER 15 = 0
         LR    RTNREG,SAVEREG       EXIT RTN ADDRESS IN 14
         BR    RTNREG               BRANCH TO TYPE 1 EXIT RTN.
*
RC4      LA    EPREG,FOUR           SET RC = 4
         LR    RTNREG,SAVEREG       EXIT RTN ADDR TO REG 14
         BR    RTNREG               BRANCH TO TYPE 1 EXIT RTN.
*
RC8      LA    EPREG,EIGHT          SET RC = 8
         LR    RTNREG,SAVEREG       EXIT RTN ADDR TO REG 14
         BR    RTNREG               BRANCH TO TYPE 1 EXIT RTN.
*
RCC      LA    EPREG,TWELVE         SET RC = X'0C'
         LR    RTNREG,SAVEREG       EXIT RTN ADDR TO REG 14
         BR    RTNREG               BRANCH TO TYPE 1 EXIT RTN.
*
RC10     EQU   *                                               @ZA14028
         LR    WORKREG,PARMREG2     LOAD DECB ADDR             @ZA14028
         SLL   WORKREG,ONEBYTE      DECB ADDR IN HI BYTE       @ZA14028
         LA    EPREG,SIXTEEN        RC=X'10'                   @ZA14028
         OR    EPREG,WORKREG        DECB ADDR IN REG 15        @ZA14028
         LR    RTNREG,SAVEREG       EXIT ROUT ADDR IN REG 14   @ZA14028
         BR    RTNREG               BR TYPE1 EXIT ROUT         @ZA14028
         EJECT
*
*        UCB VALIDITY CHECK ROUTINE
         SPACE 3
UCBCHK   BALR  SUBASREG,0           ADDRESSABILITY FOR
         USING *,SUBASREG           VALIDITY SUBROUTINE
         LR    UCBREG,PARMREG1      ADDRESSABILITY FOR UCB
*
         CLI   UCBID,UCBSTND        UCBID = X'FF'             LD Y02947
         BNE   VCRC08               NO, GIVE RETURN CODE OF 8
*
         CLI   UCBTBYT3,UCB3DISP    IS DEVICE A GRAPHICS DEV. LD Y02947
         BNE   VCRC04               NO, GIVE RETURN CODE OF 4
         CLI   UCBTBYT4,DVC3277    DEVICE 3277 DISPLAY        LD Y02947
         BL    VCRC04              NO, GIVE RETURN CODE OF 4
         CLI   UCBTBYT4,DVC3286    DEV. ANY OTHER 3270 DEVICE LD Y02947
         BH    VCRC04              NO, GIVE RETURN CODE OF 4
*
CHKIRB   L     IRBREG,UCBIRB        GET IRB ADDRESS
         LA    IRBREG,0(IRBREG)    CLEAR GRAF FLAGS            @YA02128
         LTR   IRBREG,IRBREG        IS IT ZERO
         BZ    VCRC08               YES, GIVE RETURN CODE OF 8
         CLI   UCBRLN,RLN1          DEVICE UCB = MASTER UCB
         BE    RLNISONE             YES, SET MASTER UCB BASE
         L     MSTREG,UCBCTLNK      GET ADDRESS OF MASTER UCB
*
CHKMSTR  CLI   MTRUCBID,UCBSTND     UCBID OF MASTER UCB       LD Y02947
         BNE   VCRC08               NO, GIVE RETURN CODE OF 8
*
         CLI   MTRDVCLS,UCB3DISP    DEVICE A GRAPHICS DEVICE  LD Y02947
         BNE   VCRC04               NO, GIVE RETURN CODE OF 4
         CLI   MTRUNTYP,DVC3277     DEVICE 3277 DISPLAY
         BL    VCRC04              NO, GIVE RETURN CODE OF 4
         CLI   MTRUNTYP,DVC3286    DEVICE ANY OTHER 3270 DEVICE
         BH    VCRC04              NO, GIVE RETURN CODE OF 4
*
CHKIRB1  L     WORKREG,MTRIRB       GET IRB ADDRESS
         LA    WORKREG,0(WORKREG)  CLEAR GRAF FLAGS            @YA02128
         LTR   WORKREG,WORKREG      IS IT ZERO
         BZ    VCRC08               YES, GIVE RETURN CODE OF 8
         CR    WORKREG,IRBREG       EQUAL TO ADDRESS FROM DEVICE UCB
         BNE   VCRC08               NO, GIVE RETURN CODE OF 8
         CLI   MTRRLN,RLN1          RLN = 1
         BNE   VCRC08               NO, GIVE RETURN CODE OF 8
*
VCRC00   SR    EPREG,EPREG          YES, GIVE RETURN CODE OF 0
         BR    RTNREG               AND RETURN
*        INVALID DEVICE TYPE
VCRC04   LA    EPREG,FOUR           GIVE RETURN CODE OF 4
         BR    RTNREG               AND RETURN
*        INVALID CONTROL BLOCK
VCRC08   LA    EPREG,EIGHT          GIVE RETURN CODE OF 8
         BR    RTNREG              AND RETURN
*
RLNISONE LR    MSTREG,UCBREG        SET MASTER UCB = DEVICE UCB
         B     VCRC00               MASTER UCB OK. GIVE RETURN CODE 0
*
         DS    0F
COMPCODE DC    X'48000000'         COMPLETION CODE FOR RESETPL
*
IN       EQU   8
         EJECT
CVT      DSECT
         CVT                                          LD Y02947
         EJECT
         DCBD  DSORG=BX                                      LD YA02451
         EJECT
         IECTDEBX                                            LD YA02451
         EJECT
         IECTIOBX                                            LD YA02451
         EJECT
         IHAIQE
         EJECT
         IHARB
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
MSTRUCB  DSECT
MTRUCBID EQU   MSTRUCB+(UCBID-UCBOB)
MTRDVCLS EQU   MSTRUCB+(UCBDVCLS-UCBOB)
MTRUNTYP EQU   MSTRUCB+(UCBUNTYP-UCBOB)
MTRATNCT EQU   MSTRUCB+(UCBATNCT-UCBOB)
MTRGCB   EQU   MSTRUCB+(UCBGCB-UCBOB)
MTRIRB   EQU   MSTRUCB+(UCBIRB-UCBOB)
MTRGRAF  EQU   MTRIRB
MTRINRLN EQU   MSTRUCB+(UCBINRLN-UCBOB)
MTRRLN   EQU   MSTRUCB+(UCBRLN-UCBOB)
MTRDEBAD EQU   MTRRLN
         SPACE 5
         END
