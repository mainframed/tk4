         TITLE ' IGC1206F - LOCAL TEST CONTROL '
IGC1206F CSECT
***********************************************************************
*               IGC1206F - LOCAL TEST CONTROL                         *
***********************************************************************
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000           NOVEMBER 11, 1971                     *
*                                                                     *
* FUNCTION:                                                           *
*    THIS MODULE SETS UP THE CHANNEL PROGRAM FOR LOCAL 3270 ON-LINE   *
*    TEST AND SELECTS THE APPROPRIATE SECOND SVC 66 (ACCORDING TO     *
*    THE VALUE OF XX) AND XCTL'S TO IT.                               *
*                                                                     *
* ENTRY:                                                              *
*         TO THE FIRST EXECUTABLE INSTRUCTION VIA XCTL FROM           *
*         IGC0006F.                                                   *
*                                                                     *
* INPUT:                                                              *
*    REGISTER 1 - ADDRESS OF THE IOB.                                 *
*    IOBERINF - ADDRESS OF THE START OF THE RFT IN THE USER'S         *
*    BUFFER.                                                          *
*                                                                     *
* OUTPUT:                                                             *
*                                                                     *
*    REGISTER 4 - BINARY XX VALUE                                     *
*    IOBERINF+4 (1 BYTE) - BINARY XX VALUE                            *
*    IOBERINF+5 (1 BYTE) - BINARY YY VALUE                            *
*    IOBWORK+2 (1 BYTE) - BINARY YY VALUE (COUNTER)                   *
*    IOBCPA (8 BYTES) - ERASE/WRITE CCW WITH ADDRESS OF NEXT BYTE     *
*    IN USER'S BUFFER PAST THE RFT. THE FLAGS AND COUNT ARE ZERO.     *
*    IOBSTART - ADDRESS OF IOBCPA                                     *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*         XCTL (TO NEXT ON-LINE TEST SVC 66 LOAD)                     *
*                                                                     *
* EXITS, NORMAL:                                                      *
*         XCTL TO IGC0E06F (FOR TESTS 23 AND 24)                      *
*         XCTL TO IGC0F06F (FOR TESTS 26 AND 27)                      *
*         XCTL TO IGC1306F (FOR TEST 28)                              *
*         XCTL TO IGC1406F (FOR TEST 25)                              *
*                                                                     *
* EXITS, ERROR:                                                       *
*         NONE                                                        *
*                                                                     *
* TABLES/WORK AREAS:                                                  *
*         NONE                                                        *
*                                                                     *
* ATTRIBUTES:                                                         *
*    TRANSIENT, RE-ENTRANT.                                           *
*                                                                     *
* CHARACTER CODE DEPENDENCY:                                          *
*    NONE                                                             *
*                                                                     *
***********************************************************************
       EJECT
*/*IGC1206F: CHART*/
*/* E IGC1206F*/
*/* P CONVERT XX AND YY VALUES TO BINARY AND STORE IN IOB*/
*/* P GET ADDRESS OF DATA AND SET UP ERASE/WRITE CCW*/
*/* P PUT CCW ADDRESS IN IOBSTART*/
*/* P GET ID OF NEXT MODULE*/
*/* R XCTL TO NEXT LOAD*/
*/*IGC1206F: END*/
       EJECT
         SPACE 2
*        ESTABLISH ADDRESSABILITY
         SPACE
         BALR  BASEREG,0           SET UP BASE REG
         USING *,BASEREG
         USING IECTIOB,IOBREG
         USING RFTMSG,RFTREG
         B     GETRFT              BRANCH AROUND ID USED TO
         DC    C'IGC1206F'         FIND MODULE IN DUMP
         SPACE 2
*        CONVERT XX AND YY VALUES TO BINARY AND STORE IN
*        APPROPRIATE FIELDS OF IOB
         SPACE
GETRFT   L     RFTREG,IOBERINF     GET RFT ADDR
         PACK  IOBERINF(8),TESTNUMB CONVERT TEST NUMBER (XX)
         CVB   BINXXREG,IOBERINF   TO BINARY
         PACK  IOBERINF(8),PASSCNT CONVERT NUMBER OF TESTS
         CVB   WORKREG1,IOBERINF   (YY) TO BINARY
         ST    RFTREG,IOBERINF     PUT RFT ADDR BACK
         STC   BINXXREG,IOBERINF+4 PUT BINARY XX AND YY VALUES
         STC   WORKREG1,IOBERINF+5 IN IOBERINF
         STC   WORKREG1,IOBWORK+2  PUT YY VALUE IN COUNTER
*                                  USED BY IGG019PI
         SPACE 2
*        GET ADDRESS OF DATA AND SET UP ERASE/WRITE CCW
         SPACE
         LA    DATADDR,DATA(RFTREG)
         ST    DATADDR,IOBCPA      PUT DATA ADDR IN CCW
         MVI   IOBCPA,EWCMD        PUT ERASE/WRITE CMD IN CCW
*                                  IN CCW
         XC    IOBCPA+4(4),IOBCPA+4 ZERO OUT FLAGS AND COUNT
*                                  IN CCW
         SPACE 2
*        PUT CCW ADDRESS IN IOBSTART
         SPACE
         LA    WORKREG1,IOBCPA     GET ADDR OF CHAN PGM AREA
         ST    WORKREG1,IOBSTART   STORE IN IOBSTART
         SPACE 2
*        GET ID OF NEXT MODULE AND XCTL TO IT
         SPACE
         CLI   IOBERINF+4,TWENTY8  IS IT TEST # 28 ?
         BE    USE1306F            YES, SET UP FOR IGC1306F
         CLI   IOBERINF+4,TWENTY5  IS IT TEST # 25 ?
         BE    USE1406F            YES, SET UP FOR IGC1406F
         BL    USE0E06F            TESTS 23&24, SET UP FOR
*                                  IGC0E06F
         MVI   IOBERCCW+3,C'0'     TESTS 26 & 27, SET UP TO LOAD
         MVI   IOBERCCW+4,C'F'     IGC0F06F
         B     LOADIT              GO XCTL
USE0E06F EQU   *
         MVI   IOBERCCW+3,C'0'     TESTS 23 & 24, SET UP TO LOAD
         MVI   IOBERCCW+4,C'E'     IGC0E06F
         B     LOADIT              IGC0E06F
USE1306F EQU   *
         MVI   IOBERCCW+4,C'3'     TEST 28, SET UP TO LOAD
         B     LOADIT              IGC1306F
USE1406F EQU   *
         MVI   IOBERCCW+4,C'4'     TEST 25, LOAD IGC1406F
LOADIT   EQU   *
         LA    WORKREG1,IOBERCCW   GET ADDR OF LOAD NAME
         XCTL  EPLOC=(3)
         SPACE 2
*        REGISTER EQUATES
         SPACE
IOBREG   EQU   1                   ADDR OF IOB
RFTREG   EQU   2                   ADDR OF RFT MSG
WORKREG1 EQU   3                   WORK REG
BINXXREG EQU   4                   BINARY VALUE OF XX
DATADDR  EQU   5
BASEREG  EQU   12                  BASEREG
         SPACE 2
*        MISCELLANEOUS EQUATES
         SPACE
DATA     EQU   10                  DISP OF DATA PAST START OF
*                                  RFT IN USER'S BUFFER
EWCMD    EQU   X'05'               ERASE/WRITE COMMAND
TWENTY8  EQU   X'1C'               TEST # 28
TWENTY5  EQU   X'19'               TEST # 25
         SPACE 2
*        STORAGE
         SPACE
ID0E     DC    C'IGC0E06F'         ID FOR TESTS 23 & 24
ID0F     DC    C'IGC0F06F'         ID FOR TESTS 26 & 27
ID13     DC    C'IGC1306F'         ID FOR TEST 28
ID14     DC    C'IGC1406F'         ID FOR TEST 25
         SPACE 2
*        DSECTS
RFTMSG   DSECT
         DS    0F
SOHPERC  DS    CL2
TESTNUMB DS    CL2
PASSCNT  DS    CL2
ADBYTCNT DS    CL1
CUUADDR  DS    CL3
         SPACE 2
         IECTIOBX
         END
