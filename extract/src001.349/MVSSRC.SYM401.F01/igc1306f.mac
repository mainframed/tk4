         TITLE ' IGC1306F - 3270 LOCAL/REMOTE EBCDIC TEST MODULE '
IGC1306F CSECT
***********************************************************************
*             3270 LOCAL/REMOTE EBCDIC TEST MODULE                    *
***********************************************************************
*                                                                     *
* STATUS:                                                             *
*                                                                     *
*        ZA02332  (11/20/74)                                          *
*                                                                     *
***********************************************************************
*                                                                     *
* FUNCTION:                                                           *
*    THIS MODULE BUILDS PREDEFINED EBCDIC ON-LINE TEST MESSAGES       *
*    FOR THE 3270 DISPLAY SYSTEM - LOCAL OR REMOTE.                   *
*                                                                     *
* ENTRY:                                                              *
*         ENTRY IS TO THE FIRST EXECUTABLE INSTRUCTION VIA XCTL       *
*         FROM IGC0D06F(REMOTE) OR IGC1206F(LOCAL).                   *
*                                                                     *
* INPUT:                                                              *
*    REG 1 - ADDRESS OF THE IOB                                       *
*    REG 4 - TEST TYPE XX VALUE                                       *
*    IOBERINF - ADDRESS OF THE RFT                                    *
*    ON-LINE TEST CHANNEL PROGRAM IN IOB, BUILT BY IGC0D06F           *
*    (REMOTE) OR IGC1206F (LOCAL).                                    *
*                                                                     *
* OUTPUT:                                                             *
*    LOCAL--                                                          *
*                                                                     *
*    TEST MESSAGE (MINUS LINE CONTROL) BUILT IN USER'S AREA.          *
*    COUNT INSERTED IN ERASE/WRITE CCW.                               *
*    IF THE USER'S BUFFER IS NOT LONG ENOUGH (300) BYTES OR           *
*    THE TEST MESSAGE NUMBER IS INVALID, AN ERROR FLAG (IOBINCAM,     *
*    BYTE 0, BIT 6) IS TURNED ON, AND A NOP REPLACES THE ERASE/       *
*    WRITE CCW. THIS WILL CAUSE IGG019PI TO CANCEL THE TEST BY        *
*    RE-ISSUING THE USER'S READ INITIAL.                              *
*                                                                     *
*    REMOTE--                                                         *
*                                                                     *
*    TEST MESSAGE BUILT IN USER'S AREA.                               *
*    COUNT IN WRITE TEXT CCW.                                         *
*    IF AN ERROR IS FOUND, THE ERROR FLAG (SEE LOCAL) IS TURNED       *
*    ON TO CAUSE IGG019MR TO CANCEL THE TEST.                         *
*                                                                     *
*    TESTS BUILT BY THIS MODULE:                                      *
*                                                                     *
*    TEST 28 - 3270 NL/EOM PRINTER EBCDIC
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*         SVC 0 - EXECUTE CHANNEL PROGRAM                             *
*                                                                     *
* EXITS, NORMAL:                                                      *
*         SVC 3 - RETURN TO IOS                                       *
*                                                                     *
* EXITS, ERROR:                                                       *
*         NONE                                                        *
*                                                                     *
* TABLES/WORK AREAS:                                                  *
*         NONE                                                        *
*                                                                     *
* ATTRIBUTES:                                                         *
*    RE-ENTRANT                                                       *
*                                                                     *
* CHARACTER CODE DEPENDENCY:                                          *
*    NONE                                                             *
*                                                                     *
***********************************************************************
       EJECT
*/*IGC1306F: CHART*/
*/* E IGC1306F*/
*/* D (YES,,NO,SETERR) IS BUFFER LONG ENOUGH (300 BYTES)*/
*/* D (YES,,NO,SETERR) IS TEST NUMBER VALID (28)*/
*/* P GET MESSAGE LENGTH AND ADDRESS*/
*/*TESTLCL: D (YES,LOCAL,NO,REMOTE) IS DEVICE A LOCAL 3270*/
*/*LOCAL: D (NO,,YES,ISSUENOP) IS ERROR FLAG ON*/
*/* P POINT MESSAGE ADDRESS PAST STX, ESC, AND CMD CODE*/
*/* P STORE ADDR OF START OF RFT IN CCW*/
*/* P (,MOVEIN) GET ADDRESS OF START OF CCW*/
*/*REMOTE: D (NO,,YES,REMOTERR) IS ERROR FLAG ON*/
*/* P OVERLAY TRANSPARENT CCW*/
*/* P GET ADDR OF CCW*/
*/* P STORE ADDR OF START OF RFT IN CCW*/
*/*MOVEIN: P (,STARTEST) MOVE MESSAGE INTO USER'S BUFFER*/
*/*SETERR: M (,TESTLCL) TURN ON ERROR FLAG*/
*/*ISSUENOP: P SET COUNT TO ONE*/
*/* P (,STARTEST) MOVE NOP COMMAND INTO CCW*/
*/*REMOTERR: P SET COUNT TO ONE*/
*/*STARTEST: P STORE COUNT IN CCW*/
*/* P SAVE THE ECB*/
*/* L SVC 0*/
*/* P RESTORE THE ECB*/
*/* R SVC 3*/
*/*IGC1306F: END*/
       EJECT
         SPACE 2
*        ESTABLISH ADDRESSABILITY
         SPACE
         BALR  BASEREG,0           SET UP BASE REG
         USING *,BASEREG
         USING IECTIOB,IOBREG      IOB DSECT
         USING IECTDECB,DECBREG    DECB DSECT
         USING IHADCB,DCBREG       DCB DSECT
         USING IECTDEB,DEBREG      DEB DSECT
         USING UCBDSECT,UCBREG     UCB DSECT
         B     SETUP               BRANCH AROUND ID USED TO
         DC    C'IGC1306F'         FIND MODULE IN DUMP
         DC    C' 4322'            DATE LAST CHANGE         LD @ZA02332
         DC    C'&SYSDATE'         DATE LAST ASSEMBLY       LD @ZA02332
         DS    0H                                           LD @ZA02332
PATCH    DC    48X'00'             PATCH AREA               LD @ZA02332
         DS    0H                                           LD @ZA02332
SETUP    EQU   *
         L     DECBREG,IOBECBPT    GET DECB ADDR
         L     DCBREG,IOBDCBPT     GET DCB ADDR
         L     DEBREG,DCBDEBAD     GET DEB ADDR
         L     DEBREG,28(DEBREG)   GO PAST PREFIX
         L     RFTREG,IOBERINF     GET ADDR OF RFT
         SPACE 2
*        VALIDITY CHECK BUFFER LENGTH AND TEST NUMBER. IF
*        INVALID, SET UP NOP CCW AND TURN ON ERROR FLAG.
         SPACE
         CLC   DECLNGTH(2),MINBUF  IS BUFFER LONG ENOUGH ?
         BL    SETERR              NO, SET UP FOR ERROR
         C     TESTREG,TEST28      IS IT TEST # 28 ?
         BNE   SETERR              NO, SET UP FOR ERROR
         SPACE 2
*        GET TEST MESSAGE ADDRESS AND LENGTH
         SPACE
GETINFO  LR    WORKREG1,TESTREG    GET TEST #
         S     WORKREG1,TEST28     SUBTRACT 28 TO GET INDEX
*                                  (28 IS LOWEST TEST NUMBER
*                                  THIS MODULE WILL HANDLE)
         SLL   WORKREG1,2          MULTIPLY BY 4
         L     COUNTREG,COUNTAB(WORKREG1) GET MSG LENGTH
         B     ADDRTAB(WORKREG1)   GET MSG ADDR
         B     TESTLCL
         SPACE 2
*        MESSAGE ADDRESS AND LENGTH TABLES - (TEST MESSAGE
*        NUMBER - 28) X 4 = THE ARGUMENT USED TO OBTAIN VALUES
*        FROM THE TABLE.
         SPACE
         DS    0F
COUNTAB  EQU   *                   MESSAGE LENGTH IN BYTES
         DC    A(TST28SZ)          TEST 28
         SPACE 2
ADDRTAB  EQU   *                   ADDRESS OF TEST MESSAGE
         B     TEST28AD            00   GET TEST 28 ADDR
TEST28AD LA    ADDREG,IJLBOT28     GET ADDR
         SPACE 2
*        TEST FOR LOCAL OR REMOTE
         SPACE
TESTLCL  EQU   *
         SR    UCBREG,UCBREG       CLEAR REG FOR IC
         IC    UCBREG,IOBUCBX      GET INDEX TO UCB
         SLL   UCBREG,2            MULTIPLY BY FOUR
         L     UCBREG,DEBUCBAD(UCBREG)  GET ADDR OF UCB
         CLI   DVCLASS,GRAPHIC     IS DVC A GRAPHIC ?
         BNE   REMOTE              NO, SET UP FOR REMOTE
         CLI   UNITYPE,A3277       IS DVC A 3277 ?
         BE    LOCAL               YES, SET UP FOR LOCAL
         CLI   UNITYPE,A3284       IS DVC A 3284 ?
         BE    LOCAL               YES, SET UP FOR LOCAL
         CLI   UNITYPE,A3286       IS DVC A 3286 ?
         BNE   REMOTE              NO, SET UP FOR REMOTE
*        FOR LOCAL DEVICES, STRIP OFF STX, ESC, CMD CODE, AND
*        ETX OF MESSAGE
         SPACE
LOCAL    EQU   *
          LA    CCWREG,IOBCPA       GET ADDR OF CCW
         TM    IOBINCAM,ERFLAG     IS ERROR FLAG ON ?
         BO    ISSUENOP            YES, GO ISSUE NOP
         LA    WORKREG2,4          SUBTRACT 4 FROM LENGTH
         SR    COUNTREG,WORKREG2   OF MESSAGE
         LA    ADDREG,3(ADDREG)    POINT MESSAGE ADDRESS PAST
*                                  STX, ESC, CMD CODE
         LA    RFTREG,10(RFTREG)   ADJUST PTR PAST RFT
         B     MOVEIN              GO SET UP MSG
         SPACE 2
*        FOR REMOTE, LEAVE MSG ADDRESS AND LENGTH ALONE
         SPACE
REMOTE   EQU   *
         LA    WORKREG2,IOBCPA     GET ADDR OF IOBCPA            S99245
         ST    WORKREG2,IOBSTART   AND STORE IN IOBSTART         S99245
         LA    CCWREG,IOBCPA+40    GET ADDR OF CCW
         TM    IOBINCAM,ERFLAG     IS ERROR FLAG ON ?
         BO    REMOTERR            YES, ISSUE CCW
         MVC   IOBCPA+48(8),IOBCPA+56 OVERLAY TRANS CCW
         CLI   NVALUE(RFTREG),NOADDR   IS THIS A DIAL RFT?       S99245
         BNE   BSC3RFT             NO,FOLLOW BSC3 PROCEDURE      S99245
         LA    RFTREG,CHARS7(RFTREG)   ADJUST PTR PAST RFT       S99245
         B     MOVEIN              GO SET UP MSG                 S99245
BSC3RFT  EQU   *                                                 S99245
         LA    RFTREG,11(RFTREG)   ADJUST PTR PAST RFT
         SPACE 2
*        MOVE TEST MSG INTO USER'S BUFFER
         SPACE
MOVEIN   EQU   *
         SR    WORKREG2,WORKREG2   CLEAR REG
         C     COUNTREG,MAXLNGTH   IS MSG LNGTH OVER 256 ?
         BNH   LNGTHOK             NO, BRANCH
         LR    WORKREG2,COUNTREG   GET EXCESS OVER 256
         S     WORKREG2,MAXLNGTH
         L     COUNTREG,MAXLNGTH   SET COUNT TO 256
LNGTHOK  EQU   *
         BCTR  COUNTREG,0          SUBTRACT 1 FROM LNGTH FOR
*                                  EXECUTE
         EX    COUNTREG,MSGMOVE    MOVE TEST MSG INTO USER'S
*                                  BUFFER
         LTR   WORKREG2,WORKREG2   ANY MORE TO MOVE ?
         BZ    STARTEST            NO, BRANCH
         A     RFTREG,MAXLNGTH     BUMP ADDRESS PAST 256
         A     ADDREG,MAXLNGTH     CHARS JUST MOVED
         BCTR  WORKREG2,0          SUBTRACT 1 FOR EXECUTE
         EX    WORKREG2,MSGMOVE    MOVE REMAINDER OF TEXT
         B     STARTEST            BRANCH AROUND EXECUTE
MSGMOVE  MVC   0(0,RFTREG),0(ADDREG) MOVE TEST MSG TO
*                                  USER'S BUFFER, USING LENGTH
*                                  SPECIFIED IN COUNTREG
       SPACE 2
*      IF AN ERROR WAS FOUND, TURN ON ERROR FLAG AND GO GET
*      ADDRESS OF CCW.
       SPACE
SETERR   EQU   *
         OI    IOBINCAM,ERFLAG     TURN ON ERROR FLAG
         B     TESTLCL             GO SEE IF LOCAL
       SPACE 2
*      FOR ERROR IN LOCAL, SET UP A NOP CCW
       SPACE
ISSUENOP EQU   *
         LA    COUNTREG,1          SET COUNT TO ONE
         MVI   0(CCWREG),NOP       MOVE IN NOP COMMAND
         B     STARTEST            GO ISSUE NOP
       SPACE 2
*      FOR REMOTE ERROR, SET COUNT IN WRITE TEXT CCW TO ONE
       SPACE
REMOTERR EQU   *
         LA    COUNTREG,1          SET COUNT TO ONE
         B     STARTEST            DO TEST
         SPACE 2
*        MOVE COUNT INTO CCW AND START TEST
         SPACE
STARTEST EQU   *
         LA    COUNTREG,1(COUNTREG) RESTORE COUNT AFTER EX
         STH   COUNTREG,6(CCWREG)  STORE COUNT IN CCW
*
*              1 LINE DELETED FOR ----->                    LD @ZA02332
*
         LR    WORKREG1,IOBREG     SAVE IOB ADDR            LD @ZA02332
         L     PARMREG,GETPARM     SET UP FOR GETMAIN       LD @ZA02332
         GETMAIN R,LV=(0)          GETMAIN FOR ECB          LD @ZA02332
         LR    WORKREG2,IOBREG     SAVE ECB ADDR            LD @ZA02332
         ST    DECBREG,DISP4(WORKREG2)  SAVE DECB ADDR      LD @ZA02332
         MVI   DISP4(WORKREG2),FOXFOX   FLAG AS DUMMY ECB   LD @ZA02332
         LR    IOBREG,WORKREG1     RESTORE IOB REG          LD @ZA02332
         STCM  WORKREG2,MASK,IOBECBPT+DISP1  POINT IOB...   LD @ZA02332
*                                  ...TO DUMMY              LD @ZA02332
         SVC   0                   DO TEST
*
*              1 LINE DELETED FOR ----->                    LD @ZA02332
*
         STCM  DECBREG,MASK,IOBECBPT+DISP1  REPOINT IOB...  LD @ZA02332
*                                  ...TO DECB               LD @ZA02332
         L     PARMREG,GETPARM     SET UP FOR FREEMAIN      LD @ZA02332
         FREEMAIN R,LV=(0),A=(WORKREG2)  FREE DUMMY ECB     LD @ZA02332
         SVC   3                   RETURN
         SPACE 2
         SPACE
         SPACE 2
*        REGISTER EQUATES
         SPACE
PARMREG  EQU   0                   PARAMETER REGISTER       LD @ZA02332
IOBREG   EQU   1                   ADDR OF IOB
DECBREG  EQU   2                   ADDR OF DECB
DCBREG   EQU   3                   ADDR OF DCB
TESTREG  EQU   4                   TEST #
WORKREG1 EQU   5                   WORK REG
WORKREG2 EQU   6                   WORK REG
DEBREG   EQU   7                   ADDR OF DEB
UCBREG   EQU   8                   ADDR OF UCB
ADDREG   EQU   9                   TEST MSG ADDRESS
COUNTREG EQU   10                  TEST MSG LENGTH
CCWREG   EQU   11                  ADDR OF CCW TO WRITE TEST MSG
BASEREG  EQU   12
RFTREG   EQU   13                  ADDR OF RFT
         SPACE 2
*        MISCELLANEOUS EQUATES
         SPACE
A3286    EQU   X'0B'               3286 DEVICE TYPE
A3284    EQU   X'0A'               3284 DEVICE TYPE
A3277    EQU   X'09'               3277 DEVICE TYPE
GRAPHIC  EQU   X'10'               GRAPHICS DEVICE TYPE
NOP      EQU   X'03'               NOP COMMAND CODE
ERFLAG   EQU   X'02'               ERROR FLAG
NVALUE   EQU   6                   DISP INTO RFT OF N VALUE      S99245
NOADDR   EQU   X'00'               IF N=0, IT IS A DIAL RFT      S99245
CHARS7   EQU   7                   LENGTH OF STANDARD DIAL RFT   S99245
FOXFOX   EQU   X'FF'               BYTE OF ALL ONES         LD @ZA02332
MASK     EQU   7                   MASK FOR STCM            LD @ZA02332
DISP1    EQU   1                   DISPLACEMENT             LD @ZA02332
DISP4    EQU   4                   DISPLACEMENT             LD @ZA02332
         SPACE 2
*        STORAGE
         SPACE
         DS    0H                                           LD @ZA02332
GETPARM  DC    X'FA000008'         SUBPOOL # AND LENGTH     LD @ZA02332
MINBUF   DC    X'012C'             MINIMUM BUFFER LENGTH
TEST28   DC    F'28'               ACCEPTABLE TEST NUMBER
MAXLNGTH DC    F'256'              MAX # OF CHARS FOR MVC
IJLBOT28 EQU   *
         DC    X'0227F5'           STX-ESC-EWRT
         DC    X'4F'               WCC- RSTRE KYBRD/SND ALRM/STRT PRNT
         DC    C'NEW LINE FUNCTION CHECK'                   DS LINE 1
         DC    X'15'               NEW LINE
         DC    C'NEW LINE FUNCTION'                         DS LINE 2
         DC    X'15'               NEW LINE
         DC    C'NEW LINE'                                  DS LINE 3
         DC    X'15'               NEW LINE
         DC    C'NEW'                                       DS LINE 4
         DC    X'15'               NEW LINE
         DC    C'NEW LINE'                                  DS LINE 5
         DC    X'15'               NEW LINE
         DC    C'NEW LINE FUNCTION'                         DS LINE 6
         DC    X'15'               NEW LINE
         DC    C'NEW LINE FUNCTION CHECK'                   DS LINE 7
         DC    X'15'               NEW LINE
         DC    X'1D4C'             NON DISPLAY,NON PRINT,NON DETECTABLE
         DC    C'THIS DATA NOT VISIBLE/EOM AND N/L IGNORED'
         DC    X'1519'             NEW LINE EOM (IGNORED)
        DC    X'1D40'             SF/NORMAL INT/ UNPROTECTED
         DC    C'END-OF-MESSAGE-TERMINATES PRINT'
         DC    X'19'               END OF MESSAGE
         DC    X'15'               NEW LINE                 DS LINE 8
         DC    C'2ND EOM LINE NOT ON PRINTER OUTPUT'
         DC    X'19'               END OF MESSAGE
         DC    X'03'               ETX
TST28SZ  EQU   *-IJLBOT28          LENGTH OF TEST 28
         SPACE 2
*        DSECTS
         SPACE
         IECTIOBX
         EJECT
         IECTDECB
         EJECT
         DCBD  DSORG=BX,DEVD=BS
         EJECT
         IECTDEBX
UCBDSECT DSECT
         DS    0F
         ORG   UCBDSECT+13
CHARADDR DS    CL3                 UNIT ADDR (EBCDIC)
         ORG   UCBDSECT+18
DVCLASS  DS    CL1                 DEVICE CLASS
UNITYPE  DS    CL1                 UNIT TYPE
         ORG   UCBDSECT+36
RLN      DS    CL1                 RELATIVE LINE NUMBER
         END
