         TITLE 'IGE0604C - BSC ERP LOST DATA ROUTINE'
IGE0604C CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0604C                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM LOST DATA ERP                                               *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS MODULE IS ENTERED FOR LOST DATA ERRORS. DETERMINES          *
*    WHEN THE LOST DATA ERROR OCCURRED WITHIN THE CHANNEL             *
*    PROGRAM AND THEN ATTEMPTS THE APPROPRIATE RECOVERY ACTION.       *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (52 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        534 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD.  *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0604C                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        POST OPERATION COMPLETE                                      *
*        ALTERED CHANNEL PROGRAM                                      *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        NONE                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YA02160  (11/5/73)                                               *
*    YM05652K (1/25/74)                                               *
*    YA03249  (2/15/74)                                               *
*    ZA00504  (5/9/74)                                                *
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         B     BEGIN               BRANCH AROUND ID            @ZA00504
         DC    C' IGE0604C '       MODULE ID                   @ZA00504
         DC    C' 4129 '           DATE LAST CHANGE            @ZA00504
         DC    CL8'&SYSDATE'       DATE LAST ASSEMBLY          @ZA00504
BEGIN    DS    0H                                              @ZA00504
         USING RQE,ERREG5
         USING IECTDECB,DECBRG
         USING IHADCB,DCBREG
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         DROP  ERREG5
         L     CCWREG,IOBFL3       POINT TO IOB CSW
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       ADDRESS OF FAILING CCW IN IOB
         MVC   IOBWORK(L1),TPOPCODE(CCWREG)   INITIALIZE WORK AREA
         NI    IOBWORK,L255-HIBIT   CLEAR UNUSED BITS
         CLI   ZERO(CCWREG),CCWDIAL   ERROR ON A DIAL
         BNE   BSCLD01             NO CHECK FURTHER
         LA    ERREG4,CCWDISAB     INDICATE DISABLE REQUIRED
         LA    ERRLNK,CHAINCDE     COMMAND CHAIN, SLI FLAGS
         LA    ERREG5,CCWTIC       2ND COMMAND
BSCLD00A SR    ERREG2,ERREG2       NO DATA ADR REQUIRED
BSCLD00  EQU   *
         ST    ERREG2,IOBERCCW     SET UP CHAN PROG
         STC   ERREG4,IOBERCCW     PUT IN COMMAND
         ST    ERREG4,IOBERCCW+L4   COUNT FIELD FIRST COMMAND
         STC   ERRLNK,IOBERCCW+L4   SET CCW FLAGS
         ST    CCWREG,IOBERCCW+L8   PUT IN TIC ADDRESS
         STC   ERREG5,IOBERCCW+L8   SET SECOND COMMAND
         ST    ERREG4,IOBERCCW+L12   COUNT FIELD 2ND COMMAND
         LA    CCWREG,IOBERCCW     NEW CHAN PROG START
         CLI   IOBWORK+L1,ZERO     INTERNAL ROUTINE SPECIFIED
         BE    BSCLD00C            NO
         MVI   IOBERRCT+L1,RTRNSPCD   SET SPEC RTN INDICATOR
BSCLD00C EQU   *
         ST    CCWREG,IOBSTART     STORE IN POINTER LOCATION
         B     BSCLD06             CHECK FOR LERB
BSCLD01  CLI   ZERO(CCWREG),CCWREAD   ERROR ON A READ COMMAND
         BE    BSCLD02             YES CHECK FURTHER
         CLI   ZERO(CCWREG),CCWPREP   ERROR ON PREPARE COMMAND
         BE    BSCLD06             YES CHECK FOR LERB
         OI    DECERRST,DECSNO     SET SHOULD NOT OCCUR
BSCLD05C EQU   *
         MVI   IOBERRCT,ERRMAXCT   SET COUNT TO MAXIMUM
         MVI   IOBERRCT+L1,RTRNCDE5   SET SPECIAL RETURN INDICATOR
BSCLD01A LA    ERREG5,CCWNOP       SET COMMAND FOR NO OPERATION
BSCLD01B LA    ERREG4,CCWREAD      SET COMMAND FOR READ
         LA    ERRLNK,CHAINCD1     COMMAND CHAIN, SLI, SKIP FLAGS
         B     BSCLD00A            GO COMPLETE CHAN PROGRAM
BSCLD02  CLI   IOBWORK,TPOPRENQ    ERROR ON READ ENQ
         BNE   BSCLD02E            NO, CHECK FURTHER
         CLI   DECTYPE+L1,WRRESET   WRITE RESET CHAN PROGRAM
         BNE   BSCLD00C            NO, CONTINUE
         MVI   IOBERRCT,ERRMAXCT   YES, SET NO RETRIES
         B     BSCLD07             POST ERROR CONDITION
BSCLD02E CLI   IOBWORK,TPOPRIDR    READ ID RESPONSE
         BE    BSCLD02C            YES, GENERATE CHAN PROG
         CLI   IOBWORK,TPOPRIDE    THIS READ ENQ WITH ID
         BNE   BSCLD02B            NO, CHECK FURTHER
         B     BSCLD00C            RETRY FAILING CCW
BSCLD02C MVI   IOBWORK+L1,RTRNCDE3   INDICATE INTERNAL ROUTINE
         B     BSCLD02A            CONTINUE CH.PROG. GENERATN
BSCLD02B EQU   *
         CLI   IOBWORK,TPOPRREN    ERROR ON READ RESP ENQ
         BNE   BSCLD04             NO CHECK FURTHER
BSCLD02D SH    CCWREG,ERRCN8       SET CCW TO WRITE ENQ COMMAND
         LA    ERREG5,CCWTIC       SET 2ND COMMAND FOR TIC
         B     BSCLD01B            GO COMPLETE CHAN PROGRAM
BSCLD02A LA    ERREG5,CCWNOP       SECOND COMMAND
         LA    ERRLNK,CHAINCDE     CHAINING FLAGS CC,SLI
         LA    ERREG2,IOBWORK+L2   CONSTANTS FOR CHAN
         LA    ERREG4,CCWREAD      PROG GENERATION ROUTINE
         B     BSCLD00             GENERATE CHANNEL PROGRAM
BSCLD04  CLI   IOBWORK,TPOPDTXT
         BNE   BSCLD04A
         MVC   IOBWORK+L2(L1),DECTYPE+L1   OPERATION TYPE CODE
         NI    IOBWORK+L2,MASK1    ZERO OUT UNUSED HALF
         TM    DCBBFTEK,DYNBUF     DYNAMIC BUFFERING USED
         BO    BSCLD05C            YES, POST WITH ERROR
         CLI   IOBWORK+L2,DECCONV   CONVERSATIONAL WRITE
         BL    BSCLD05A            NO CHECK FOR POLL
         SH    CCWREG,ERRCN8       BACK UP TO RD RESP TO TXT
BSCLD05  MVI   IOBWORK+L1,RTRNCDE2   INDICATE INTERNAL ROUTINE
         B     BSCLD02A            COMPLETE CHANNEL PROG
BSCLD04A EQU   *
         CLI   IOBWORK,TPOPRRTX    THIS READ RESP TO TEXT
         BNE   BSCLD100
         MVI   IOBWORK+L1,RTRNCDE3   SPEC RETURN 3O45
         B     BSCLD01A            GENERATE CHAN PROGRAM
BSCLD100 CLI   IOBWORK,TPOPRRAD    READ RESP TO ADDRESSING
         BE    BSCLD08             YES, SET UP RETRY
         OI    DECERRST,DECSNO     SET SHOULD NOT OCCUR
         CLI   L5(CCWREG),ZERO     ERP CCW
         BNE   BSCLD05C            NO SET PER ERROR
         L     CCWREG,IOBERCCW+L8   YES PICK UP RESTART ADDRESS
         LA    ERREG5,CCWTIC       SET COMMAND FOR TIC
         B     BSCLD01B            GO FINISH CHANNEL PROGRAM
BSCLD05A CLI   IOBCPA,CCWAPOLL     THIS AN AUTOPOLL DEVICE
         BNE   BSCLD05             NO, SET UP CHAN PROG
         LR    ERREG1,CCWREG       POINT TO CURRENT CCW
         SH    ERREG1,ERRCN8       BACK UP ONE
         L     ERREG1,ZERO(ERREG1)   GET AREA ADDRESS
         LA    ERREG1,L1(ERREG1)   BUMP BY ONE (INDEX BYTE)
         C     ERREG1,ZERO(CCWREG)   HAS COUNT BEEN ADJUSTED
         BE    BSCLD05             YES, CONTINUE
         STCM  ERREG1,L7,L1(CCWREG)   NO, RESTORE READ         @ZA00504
         LH    ERREG1,L6(CCWREG)   PICK UP COUNT
         LA    ERREG1,L1(ERREG1)   BUMP BY ONE
         STH   ERREG1,L6(CCWREG)   STORE
         LA    ERREG2,DCBBSNAK     GET NAK                      YA03249
         LA    ERREG4,CCWWRITE     GET OP TYPE                  YA03249
         LA    ERRLNK,CHAINCDE     GET CCW FLAGS                YA03249
         LA    ERREG5,CCWTIC       GET TIC OP TYPE              YA03249
         MVI   IOBWORK+L1,RTRNCDE2   SET SPECIAL RETURN         YA03249
         B     BSCLD00             GENERATE CHAN PROG           YA03249
BSCLD08  LA    CCWREG,IOBCPA       START OF CHAN PROG
         B     BSCLD00C            GO STORE IN IOBSTART
BSCLD06  EQU   *
BSCLD06A CLI   IOBERRCT,ERRMAXCT   ANY RETRIES LEFT
         BNL   BSCLD07             NO SET PERM ERROR
         IC    ERREG1,IOBERRCT     PICK UP LAST COUNT
         LA    ERREG1,L1(ERREG1)   BUMP OLD COUNT
         STC   ERREG1,IOBERRCT     STORE NEW COUNT
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
RETRY    EQU   *
         L     NAMEREG,TRANSRTN    LOAD NAME
         B     XCTLOUT             XCTL
BSCLD07  NI    IOSFLA,L255-IOSERR   SET PERM ERROR INDICATOR
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
         LA    NAMEREG,ERLOD25
XCTLOUT  EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE
         DROP  ERRETR
         BR    ERRETR              XCTL
         EJECT
**********
*
*        CONSTANTS
*
**********
TRANSRTN DC    F'9042'             NAME OF BTAM TRANSLATE ROUTINE
ERRCN8   DC    H'8'
PACHAREA DC    13C'604C'           PATCHAREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
CCWDIAL  EQU   X'29'
CCWWRITE EQU   X'01'               WRITE OP TYPE                YA03249
CCWDISAB EQU   47
CCWNOP   EQU   3
TPOPDTXT EQU   X'11'
TPOPRRAD EQU   X'06'               READ RESP TO ADDRESSING
TPOPRIDE EQU   X'03'
TPOPRRTX EQU   X'25'
CHAINCDE EQU   X'60'
TPOPRIDR EQU   X'07'
RTRNSPCD EQU   X'14'
RTRNCDE5 EQU   X'1A'
RTRNCDE3 EQU   X'83'
RTRNCDE2 EQU   X'82'
MASK1    EQU   X'0F'
CHAINCD1 EQU   X'70'
TPOPCODE EQU   5
L255     EQU   X'FF'               AND MASK
DECCONV  EQU   X'0D'
CCWREAD  EQU   X'02'
HIBIT    EQU   X'C0'
DECSNO   EQU   X'40'
ERRMAXCT EQU   7
ZERO     EQU   X'00'               LENGTH AND DISPLACEMENT
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT     @ZA00504
L8       EQU   8                   LENGTH AND DISPLACEMENT
L12      EQU   12                  LENGTH AND DISPLACEMENT
TPOPRENQ EQU   X'0B'
TPOPRREN EQU   X'0C'
DYNBUF   EQU   X'08'
CCWAPOLL EQU   X'09'
CCWPREP  EQU   X'06'
CCWTIC   EQU   X'08'
ERLOD25  EQU   2043
WRRESET  EQU   X'0A'
**********
*
*        REGISTER DEFINITION
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
ERREG2   EQU   2                   WORK REG
EWAREG   EQU   3                   ERP WORKAREA BASE
CCWREG   EQU   4                   CCW ADDRESS REG
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE
BASEREG  EQU   8                   PROGRAM BASE
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG4   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  LINK REG
ERREG5   EQU   15                  LINK REG/WORK REG
         EJECT
         DCBD  DSORG=BX,DEVD=BS              DCB DESECT MACRO
         EJECT
         IECTDECB
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
