         TITLE 'IECTLOPN - LOPEN MACRO SUBROUTINE'
IECTLOPN CSECT
*                                                              @YA02128
*        FOLLOWING FIXES APPLIED:                              @YA02128
*              OY02128     OCT 1973                            @YA02128
*              OY02471     OCT 1973                            @YA02471
*                                                              @YA02128
* C303300-303600                                             LD YA03254
* A136500,C487000,A544500-547920,A642600-643100,             LD YA03257
* A806500-808500                                             LD YA03257
* C704600                                                    LD YA03956
*        ZA00535  (11/15/74)
*        ZA02300  (11/15/74)        ZA02301  (11/15/74)
*        AZ04194  (09/05/75)
*        AZ08076 (02/02/76)
*        AZ11421 (06/09/76)
*        AZ11458 (07/21/76)         AZ12130  (07/21/76)
*        AZ12159 (08/19/76)         AZ12162  (08/19/76)
*        AZ12163 (08/19/76)         AZ15416  (11/12/76)
*        AZ16559 (01/03/77)         AZ17715  (03/01/77)
*        AZ18590 (05/12/77)
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                                                                     *
*FUNCTION/OPERATION-THIS ROUTINE WILL RE-ISSUE SAD,SET MODE OR ENABLE
*   COMMANDS TO A CONTROL UNIT TYPE 2701, 2702, 2703. THIS IS AN      *
*   ATTEMPT TO RECOVER FROM A FAILURE AT OPEN TIME.                   *
*                                                                     *
*                                                                     *
*ENTRY- WILL BE TO THE FIRST BYTE OF THE MODULE. NORMAL ENTRY WILL BE *
*   FROM A 'LOPEN' MACRO IN THE USER CSECT.                           *
*                                                                     *
*                                                                     *
*INPUT-GENERAL REGESTER 1 WILL CONTAIN A POINTER TO A DECB.           *
*                                                                     *
* OUTPUT -          RETURN CODE IN REGISTER FIFTEEN                   *
*                                                                     *
*                                                                     *
*EXTERNAL ROUTINES- NONE                                              *
*                                                                     *
*                                                                     *
*EXIT-VIA A 'RETURN' WITH RETURN CODES                                *
*  X'00' = NORMAL EXIT                                                *
*  X'04' = COMMAND(S) NOT COMPLETED SUCCESSFULY                       *
*  X'08' = ERROR-IOB HAS BUSY FLAG ON                                 *
*  X'0C' = ERROR-DECRLN IS GREATER THAN NUMBER OF EXTENTS             *
*  X'10' = ERROR-DCB HAS NOT BEEN OPENED                              *
*  X'14' = OLTEP IS EXECUTING                                         *
*  X'20' = LOCAL DEVICE OTHER THAN 3270                               *
*                                                                     *
* ATTRIBUTES -      THIS ROUTINE IS REENTRANT                         *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE
R1       EQU   1                                             LD YA03257
RDCB     EQU   2
RIOB     EQU   3                        BASE FOR IOB
RCTL     EQU   4                        CONTROL SWITCH
RPTR     EQU   5                        POINTER TO NEXT CCW
RWKE     EQU   6                        WORK REG (EVEN)
RWKO     EQU   RWKE+1                   WORK REG (ODD)
RUCB     EQU   8                        BASE FOR UCB
RDECB    EQU   9
RDEB     EQU   10
RBASE    EQU   12
RL3270   EQU   11                   REG 11=LOCAL/REMOTE TEST   @ZA04194
RCREG    EQU   15                    RETURN CODE REG           @ZA04194
         SPACE
         SAVE  (14,12)                                         @ZA15416
         SPACE
         USING IECTIOB,RIOB
         USING UCBSTART,RUCB
         USING IHADCB,RDCB
         USING IECTDECB,RDECB           BASE FOR DECB
         USING IECTDEB+36,RDEB
         SPACE
         BALR  RBASE,0
         USING *,RBASE                  BASE REGISTER
         B     CONTINUE                BRANCH AROUND ID        @YA02128
         DC    CL8'IECTLOPN'             MODULE ID             @YA02128
         DC    XL3'010593'               PTF NUMBER            @YA18590
         DC    CL8'&SYSDATE'       DATE LAST ASSEMBLY
         DS    0F                                              @ZA11458
PATCH    DC    40X'00'             PATCH AREA                  @ZA11458
CONTINUE DS    0H                                              @YA02128
         SPACE
         SR    RCREG,RCREG              CLEAR RETURN CODE      @ZA04194
         SR    RL3270,RL3270    CLEAR REG 11 FO 3270 TEST      @ZA04194
*                              ZERO MEANS REMOTE, NON-ZERO     @ZA04194
*                               MEANS LOCAL                    @ZA04194
         LR    RDECB,1
         SPACE
         L     RDCB,DECDCBAD            BASE FOR DCB
         L     RDEB,DCBDEBAD            BASE FOR DEB               1850
         TM    DCBOFLGS,OPENFLG         TEST FOR OPEN
         BZ    EXIT10
         SR    RWKO,RWKO                CLEAR WORK REG
         SR    RWKE,RWKE
         CLC   DECRLN(1),DEBNMEXT       IS RLN A POSSIBILITY?
         BH    EXITC                     NO - ERROR EXIT
         IC    RWKO,DECRLN              GET RLN
         IC    RWKE,DCBEIOBX            IOB LENGTH
         MR    RWKE,RWKE                 X RELATIVE LINE NUMBER
         L     RIOB,DCBIOBAD             + START
         AR    RIOB,RWKO                BASE FOR IOB
         SR    RUCB,RUCB               CLEAR REG FOR UCB BASE
         IC    RUCB,IOBUCBX            FIND UCB INDEX
         SLL   RUCB,2                  MULTIPLY BY FOUR TO GET OFFSET
         L     RUCB,DEBUCBAD(RUCB)     LOAD UCB ADDRESS FROM DEB LIST
         CLI   UCBDEVTP+3,TYPE3        TEST FOR 2260 ON TYPE 3 ADAPTER
         BE    EXIT0                   IF SO, EXIT (LOPEN INEFFECTIVE)
         SPACE
         TM    IOBINCAM,BUSY            IS IOB BUSY FLAG SET
         BO    EXIT8               YES, SET BUSY RC AND EXIT
         CLI   UCBDEVTP+2,GRAPHICS TEST FOR GRAPHICS DEVICE CLASS
         BNE   CHKTCU              IF NOT, TEST TCU
         CLI   UCBDEVTP+3,DSPL3277 TEST FOR 3277 DISPLAY
         BE    DVC3270             YES, SET UP ERASE WRITE
         CLI   UCBDEVTP+3,PRT3284  TEST FOR 3284 PRINTER
         BE    DVC3270             YES, SET UP ERASE/WRITE
         CLI   UCBDEVTP+3,PRT3286  TEST FOR 3286 PRINTER
         BE    DVC3270             YES, SET UP ERASE/WRITE     @ZA16559
         CLI   UCBDEVTP+2,GRAPHICS   STILL GRAPICS             @ZA12163
         BE    EXIT20              YES SET ERROR CODE          @ZA12163
DVC3270  EQU   *
         LA    RL3270,ONE         MAKE NON-ZERO TO INDICATE    @ZA04194
*                                  LOCAL DEVICE                @ZA04194
         TM    UCBGCB,OLTEP        IS OLTEP EXECUTING ?        @ZA04194
         BO    TSTOLTEP           BRANCH IF YES                @ZA04194
         TM    UCBGRAF,IGG019UP     USING BTAMS ?              @YA02128
         BNO   CONTLOPN             NO, DON'T CHECK FLAGS      @YA02128
         TM    UCBGRAF,RDYNDONE     READY PROCESSING NOW       @YA02128
         BO    EXIT4                YES, OPEN FAILED           @YA02128
         TM    UCBGRAF,NOTRDY       DEVICE NOT READY ?         @YA02128
         BO    EXIT4                YES, OPEN FAILED           @YA02128
CONTLOPN EQU   *                                               @YA02128
         MVI   DECTYPE+1,WTERASE       DECTYPE FOR WRITE/ERASE @YA02471
         OI    IOBINCAM,BUSY       LOCAL 3270 DVC - TURN ON BUSY BIT
         LA    RPTR,IOBCPA         SET POINTER TO IOBCPA
         ST    RPTR,IOBSTART       STORE IN IOBSTART
         MVC   0(CCWLNGTH,RPTR),ERASEWRT MOVE IN ERASE/WRITE CMD
         B     EXCP1               ISSUE EXCP
TSTOLTEP EQU   *
         LA    RCREG,ERR14            SET RETURN CODE 14       @ZA04194
         B     OUT
CHKTCU   EQU   *                                               @ZA04194
*             THREE LINES OF CODE DELETED BY--------------->   @ZA04194
* TWO INSTRUCTIONS REMOVED BY APAR OZ12130                     @ZA12130
         OI    IOBINCAM,BUSY
         SPACE
         LA    RPTR,IOBCPA              SET POINTER
         ST    RPTR,IOBSTART
         MVC   0(8,RPTR),DISABLE       MOVE IN DISABLE COMMAND     000H
         LA    RPTR,8(RPTR)                                        000H
         TM    UCBDEVTP+3,CU0103       TEST FOR 2701 OR 2703
         BO    SMODETST                IF SO, DON'T SET UP SAD COMMAND
         SPACE
SETADD   SR    RWKO,RWKO                CLEAR WORK REG
         IC    RWKO,UCBDEVTP+1          SET COMMAND CODE
         N     RWKO,ANDMASK
         IC    RWKO,SADCODE(RWKO)       INSERT ACTUAL COMMAND CODE
         STC   RWKO,0(RPTR)             STORE IN CCW
         MVC   1(7,RPTR),CCWCONST       SET REMAINDER OF SAD COMMAND
         LA    RPTR,8(RPTR)             INCREMENT RPTR FOR 2ND CCW
         SPACE
SMODETST TM    UCBDEVTP+3,CU0103BS      TEST FOR 2701/3 BSC
         BNO   ENABLTST                   NOT 2701/3 BSC
         SPACE  2
         LA    RCTL,DCBXMODE           GET ADDRESS OF MODE CONTROL
         ST    RCTL,0(RPTR)                BYTE AND STOREIN CH. PGM.
         MVI   0(RPTR),MODECMCD         INSERT COMMAND CODE
         MVC   4(4,RPTR),CCWCONST+3
         LA    RPTR,8(RPTR)             INCREMENT RPTR FOR NEXT CCW
         SPACE  2
ENABLTST TM    UCBDEVTP+1,AUTO         TEST FOR AUTO-CALL, AUTO-ANSWER
         BNE   EXCP                    IF SO, DON'T ENABLE
         MVC   0(8,RPTR),ENABLE        MOVE ENABLE COMMAND TO CH. PGM.
         B     EXCP1
         SPACE
EXCP     SH    RPTR,EIGHT               POINT TO LAST CCW   LD @ZA02301
         NI    4(RPTR),CCOFF            TURN OFF COMMAND CHAIN
EXCP1    LR    R1,RIOB                  LOAD IOB ADDRESS       @ZA04194
         NI    IOBINCAM,FF-SADENER        CLEAR FAIL FLAG      @ZA04194
         LTR   RL3270,RL3270         LOCAL OR REMOTE DEVICE ?  @ZA04194
         BNZ   LBTAMECB             BRANCH FOR LOCAL DEV       @ZA04194
         ST    RDECB,IOBECBPT     PUT USER DECB IN IOB REMOTE  @ZA04194
EXCP2    EQU   *                                               @ZA04194
         EXCP  (1)
         MVI   IOBINCAM+1,X'00'        SET TIMER-COUNT BYTE TO ZERO
STIMER   STIMER  WAIT,BINTVL=TIME
         L     R1,IOBECBPT              LOAD POINTER TO ECB    @ZA04194
         TM    0(1),X'40'              IS OPERATION COMPLETE
         BO    COMPNORM                YES, CHECK COMPLETION CODE
         CLI   IOBINCAM+ONE,COUNT       IS TIME EXHAUSTED ?    @ZA04194
         BNL   EXIT4A                                          @ZA04194
         IC    RCTL,IOBINCAM+1         INCREMENT
         LA    RCTL,1(RCTL)                TIMER COUNT FOR
         STC   RCTL,IOBINCAM+1                   ANOTHER 1/2 SEC
         B     STIMER                                 AND REISSUE
LBTAMECB LA    RWKE,IOBERCCW     USE IT AS A BTAM ECB          @ZA04194
         XC    IOBERCCW(L4),IOBERCCW   CLEAR THE ECB           @ZA04194
         ST    RWKE,IOBECBPT    PUT BTAM DECB IN IOB LOCAL     @ZA04194
         SPACE 1
***********************************************************************
*         FOLLOWING CODE GETS WCC CHARACTER AND ITS ADDRESS    @ZA17715
*         WHICH MUST BE PASSED TO SIO/PAGE FIX APPENDAGE.      @ZA17715
***********************************************************************
         SPACE 1
         MVC   46(2,RIOB),LNGONE  MOVE LENGTH INTO BTAM DECB   @ZA17715
         L     RWKE,IOBCPA        GET ADDR OF WCC FROM CCW     @ZA17715
         LA    RWKE,0(RWKE)       CLEAR HIGH ORDER BYTE.       @ZA17715
         ST    RWKE,52(RIOB)      PUT WCC ADDR IN BTAM DECB    @ZA17715
         B    EXCP2                                            @ZA04194
COMPNORM EQU   *                                               @ZA04194
         MVI   IOBINCAM+1,X00      SET TIMER COUNT TO ZERO     @ZA04194
         LTR    RL3270,RL3270       LOCAL OR REMOTE DEVICE     @ZA04194
         BNZ   PUDECB             BRANCH FOR LOCAL DEVICE      @ZA04194
NORMCOMP EQU   *                                               @ZA04194
         CLI   ZERO(RDECB),NORM     IS COMPLETION CODE A 7F ?  @ZA08076
         BNE   EXIT4
         SPACE
         SPACE
*   GOOD RETURN
         SPACE
EXIT0    NI    IOBINCAM,FF-SADENER      CLEAR FAIL FLAG
         SR    RCREG,RCREG              CLEAR RETURN CODE REG  @ZA04194
         B     EXIT
PUDECB   EQU   *                                               @ZA04194
         TM    0(RDECB),X'80'        USER ECB WAITING?         @ZA18590
         BO    EXIT4                 YES WAITING, DON'T POST   @ZA18590
         XC    0(1,RDECB),0(RDECB)   CLEAR USERS ECB COMP CODE @ZA18590
         L     RL3270,IOBERCCW       GET COMPLETION CODE       @ZA04194
*                                       FROM BTAM ECB          @ZA04194
         N     RL3270,CLEAR         CLEAR BITS 8 THRU 31       @ZA04194
         POST (RDECB),(RL3270)        POST USER ECB            @ZA04194
         B     NORMCOMP                                        @ZA04194
         SPACE
         SPACE
*   CONTROL UNIT NOT OPERATIONAL
         SPACE
*              FOUR LINES OF CODE DELETED BY APAR ---------->  @ZA04194
EXIT4A   EQU   *                                               @ZA04194
         LTR   RL3270,RL3270        LOCAL OR REMOTE DEVICE     @ZA04194
         BNZ   EXIT4                BRANCH FOR LOCAL DEVICE    @ZA04194
         LR    R1,RUCB           LOAD UCB ADDRESS IN REG1      @ZA04194
         LA    R1,ZERO(R1)       CLEAR HIGH ORDER BYTE         @ZA04194
         SVC   33                ISSUE IOHALT                  @ZA04194
         SPACE   1
*    ERROR------- OPEN NOT SUCCESSFUL                          @ZA04194
*              ELEVEN LINES OF CODE DELETED BY APAR-------->   @ZA04194
EXIT4    OI    IOBINCAM,SADENER         SET FAIL FLAG          @ZA04194
         MVI   IOBINCAM+ONE,X00    SET TIMER TO ZERO           @ZA04194
         LR    R1,RDECB           PUT DECB ADDRESS IN REG 1    @ZA04194
         LA    RCREG,ERR4               SET RETURN CODE        @ZA12163
         B     EXIT                                            @ZA04194
         SPACE
         SPACE
*   ERROR -- IOB BUSY
         SPACE
EXIT8    LA    RCREG,ERR8               SET RETURN CODE        @ZA12163
         B     OUT
         SPACE
         SPACE
*   ERROR -- DECRLN NOT VALID
         SPACE
EXITC    LA    RCREG,ERRC               SET RETURN CODE        @ZA12163
         B     OUT
         SPACE 2
*   ERROR -- DCB NOT OPENED
         SPACE
EXIT10   LA    RCREG,ERR10              SET RETURN CODE        @ZA12163
         B     OUT
         SPACE
EXIT20   LA    RCREG,ERR20       SET RETURN CODE X'20'         @ZA12163
         B     OUT                                             @ZA12163
         SPACE
EXIT     NI    IOBINCAM,FF-BUSY
         LR    R1,RDECB            GET USER ECB                @ZA18590
         ST    R1,IOBECBPT         RESTORE USER IN IOB         @ZA18590
OUT      RETURN (14,12),,RC=(15)        RESTORE REGISTERS      @ZA15416
         EJECT
*
*        EQUATIONS  AND CONSTANTS
*
X00      EQU   X'00'                                           @ZA04194
GRAPHICS EQU   X'10'               GRAPHICS DEVICE CLASS
DSPL3277 EQU   X'09'               DVC TYPE - 3277 DISPLAY
PRT3284  EQU   X'0A'               DVC TYPE - 3284 PRINTER
PRT3286  EQU   X'0B'               DVC TYPE - 3286 PRINTER
IGG019UP EQU   X'10'               USING BTAMS READY           @YA02128
RDYNDONE EQU   X'04'               READY BEING PROCESSED       @YA02128
NOTRDY   EQU   X'02'               DEVICE NOT READY            @YA02128
OLTEP    EQU   X'80'               OLTEP EXECUTING FLAG
WTERASE  EQU   X'0E'     DECB OPTION TYPE FOR 3270 WRITE/ERASE  YA02471
COUNT    EQU   X'08'                                           @ZA11421
ONE      EQU   1                                               @ZA04194
L4       EQU   4                                               @ZA04194
DVCLASS  EQU   2                   DISP OF DVC CLASS IN UCBDEVTP
ADAPTYPE EQU   3                   DISP OF ADAPTER TYPE IN UCBDEVTP
CCWLNGTH EQU   8                   LENGTH OF A CCW
ZERO     EQU   0                                             LD YA03257
SIXTEEN  EQU   16                                            LD YA03257
         SPACE
OPENFLG  EQU   X'10'
*       ONE LINE OF CODE DELETED BY APAP----------------->     @ZA12159
NORM     EQU   X'7F'
*       ONE LINE OF CODE DELETED BY APAP----------------->     @ZA12159
SADENER  EQU   X'80'
BUSY     EQU   X'40'
CU0103BS EQU   X'92'
MODECMCD EQU   X'23'
CU0103   EQU   X'02'
CCOFF    EQU   X'BF'
FF       EQU   X'FF'
TYPE3    EQU   X'82'
AUTO     EQU   X'90'
LNGONE   DC    H'1'                LENGTH OF X'0001' FOR WCC   @ZA17715
WCC      DC    X'C3'               WCC WITH RESET MODIFIED DATA AND
XA0      EQU   X'A0'                                        LD @ZA00535
*                                   RESET KEYBOARD TAGS ON
EIGHT    DC    H'8'                                         LD @ZA02301
         DS    0F
*        ONE LINE OF CODE DELETED BY APAR --------------->     @ZA12159
CLEAR    DC    X'FF000000'                                     @ZA04194
ERASEWRT DC    X'05'               ERASE WRITE CCW WITH -
WCCADDR  DC    AL3(WCC)                 ADDR OF WCC CHAR
         DC    X'00A40001'              COUNT OF ONE         LD YA03956
ANDMASK  DC    X'00000003'
TIME     DC    F'50'
CCWCONST DC    X'00000060240001'
DISABLE  DC   X'2F00000060010001'                                  000H
SADCODE  DC    X'13'                    SAD 0 * TABLE INDEXING @ZA04194
         DC    X'17'                    SAD 1 *   DO NOT       @ZA04194
         DC    X'1B'                    SAD 2 *   CHANGE       @ZA04194
         DC    X'1F'                    SAD 3 *  SEQUENCING    @ZA04194
         SPACE
ENABLE   DC    X'2700000020A40001'                          LD @ZA02301
         SPACE
ERR4     EQU   X'04'                                           @ZA12163
ERR8     EQU   X'08'                                           @ZA12163
ERRC     EQU   X'0C'                                           @ZA12163
ERR10    EQU   X'10'                                           @ZA12163
ERR14    EQU   X'14'
ERR20    EQU   X'20'                                           @ZA12163
*        FIVE LINES OF CODE DELETED BY APAR ------------>      @ZA12159
         EJECT
         DCBD  DSORG=BX,DEVD=BS
         EJECT
         IECTIOBX
         EJECT
         IECTDECB
         EJECT
         IECTDEBX
         EJECT
UCBSTART DSECT
UCBDEVTP EQU   UCBSTART+16
UCBGCB   EQU   UCBSTART+27         3270 ATTENTION CONTROL BYTE
UCBGRAF  EQU   UCBSTART+28        GRAPHICS STATUS FLAGS         YA02128
         END
