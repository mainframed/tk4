 TITLE 'IGG019LP - START I/O AND PAGE FIX APPENDAGES'
***********************************************************************
*                                                                     *
* MODULE NAME: IGG019LP   (OS/VS)                                     *
*                                                                     *
* DESCRIPTIVE NAME: BTAM START I/O AND PAGE FIX APPENDAGE             *
*                                                                     *
* COPYRIGHT: NONE                                                     *
*                                                                     *
* STATUS: RELEASE 2                                                   *
*                                                                     *
* FUNCTIONS:        1. START I/O APPENDAGE                            *
*                   THIS IS A NOP IN THAT CONTROL IS                  *
*                   IMMEDIATELY RETURNED TO IOS VIA REGISTER          *
*                   14. THE 'EXTEND PARAMETER LIST' INTERFACE         *
*                   (WHICH WAS A LIST OF CCW STRINGS FOR              *
*                   AUTOPOLL AND/OR DYNAMIC BUFFERING                 *
*                   THAT IOS SHOULD FIX AND TRANSLATE)                *
*                   IS NO LONGER NEEDED.                              *
*                                                                     *
*                   2. PAGE FIX APPENDAGE                             *
*                   CONSTRUCTS THE 'PAGE FIX PARAMETER LIST'          *
*                   TO CONTAIN THE BUFFER CONTROL BLOCK, THE          *
*                   BUFFER POOL, AND BOTH INDIRECT ADDRESSING         *
*                   LISTS AND THEN USES THE BRANCH ENTRY              *
*                   INTERFACE OF PAGE SERVICES TO FIX THE             *
*                   ABOVE AREAS IN REAL STORAGE FOR THE               *
*                   DURATION OF THE I/O REQUEST.                      *
*                                                                     *
* ENTRY POINTS:     SIOPF01  -  ENTRY POINT TO SIO APPENDAGE          *
*                                                                     *
*                   SIOPF02  -  ENTRY POINT TO PAGE FIX APPENDAGE     *
*                                                                     *
* INPUT:            1. START I/O APPENDAGE                            *
*                                                                     *
*                   REGISTER  2  -  IOB ADDRESS                       *
*                   REGISTER  3 -  DEB ADDRESS                        *
*                   REGISTER  4 -  DCB ADDRESS                        *
*                   REGISTER  7 -  UCB ADDRESS                        *
*                   REGISTER 14 -  RETURN ADDRESS                     *
*                                                                     *
*                   PAGE FIX APPENDAGE  -                             *
*                                                                     *
*                   REGISTER  2  -  IOB ADDRESS                       *
*                   REGISTER  3  -  DEB ADDRESS                       *
*                   REGISTER  4  -  DCB ADDRESS                       *
*                   REGISTER  7  -  UCB ADDRESS                       *
*                   REGISTER 14  -  RETURN ADDRESS                    *
*                                                                     *
* OUTPUT:           START I/O APPENDAGE  -                            *
*                                                                     *
*                   REGISTER 14  -  RETURN ADDRESS                    *
*                                                                     *
*                   PAGE FIX APPENDAGE  -                             *
*                                                                     *
*                   REGISTER 9  -  ADDRESS OF VIRTUAL SUBAREA         *
*                                   LIST                              *
*                                                                     *
*                    FORMAT OF VIRTUAL SUBAREA LIST ENTRIES:          *
*                                                                     *
*                   ************************************              *
*                   *         *                        *              *
*                   *    0    *        ADDR1           *              *
*                   *         *                        *              *
*                   ************************************              *
*                   *         *                        *              *
*                   *    0    *        ADDR2           *              *
*                   *         *                        *              *
*                   ************************************              *
*                                                                     *
*                   EACH ENTRY IS ONE DOUBLEWORD.  BITS 8 THRU 31     *
*                   OF AN ENTRY (ADDR1) CONTAIN THE STARTING          *
*                   ADDRESS OF AN AREA TO BE FIXED. BITS 40 THRU 63   *
*                   (ADDR2) CONTAIN THE ENDING ADDRESS PLUS           *
*                   ONE OF THE AREA TO BE FIXED. BIT 1 IS ON          *
*                   IN THE FIRST ENTRY TO INDICATE THAT THE           *
*                   AREAS ARE TO BE FIXED. BIT 32 IS ON IN            *
*                   THE LAST ENTRY TO INDICATE THAT IT IS             *
*                   THE LAST.                                         *
*                                                                     *
* EXTERNAL ROUTINES: NONE                                             *
*                                                                     *
* EXITS:            START I/O APPENDAGE - CONTROL IS RETURNED         *
*                   TO IOS VIA THE ADDRESS IN REGISTER 14.            *
*                                                                     *
*                   PAGE FIX APPENDAGE  -  CONTROL IS RETURNED TO     *
*                   IOS VIA THE ADDRESS IN REGISTER 14.               *
*                                                                     *
* OPERATION:        TO BE COMPLETED                                   *
*                                                                     *
*                                                                     *
* ATTRIBUTES:       THE START I/O APPENDAGE IS REENTERANT,            *
*                   ENABLED, AND OPERATES IN SUPERVISOR               *
*                   STATE WITH THE LOCAL LOCK HELD BY IOS.            *
*                                                                     *
*                   THE PAGE FIX APPENDAGE IS REENTRANT,              *
*                   ENABLED, AND OPERATES IN SUPERVISOR               *
*                   STATE WITH THE LOCAL LOCK HELD BY IOS.            *
*                                                                     *
* CHANGE ACTIVITY AS FOLLOWS:                                         *
*                                              PTMS                   *
*                                          YM5663  (12/11/73) PROLOG  *
*                                                                     *
***********************************************************************
         EJECT
IGG019LP START
         SPACE 5
ZEROREG  EQU   0
RQEREG   EQU   1
IOBREG   EQU   2
DEBREG   EQU   3
DCBREG   EQU   4
TCBREG   EQU   5               TCB ADDR                          Y02947
VSLREG   EQU   6                VIRTUAL SUBAREA LIST             Y02947
*                                          ADDRESS               Y02947
UCBREG   EQU   7
DBEXTREG EQU   8                ADDRESS OF DYNAMIC BUFFERING     Y02947
*                                EXTENSION IN THE IOB            Y02947
PARMREG  EQU   9                ADDRESS OF VIRTUAL SUBAREA       Y02947
*                                LIST ENTRY                      Y02947
WREGA    EQU   10               STARTING ADDRESS OF AREA         Y02947
*                                TO BE FIXED                     Y02947
WREGB    EQU   11               ENDING ADDRESS + 1 OF AREA       Y02947
*                                TO BE FIXED                     Y02947
BASEREG  EQU   12               BASE REGISTER                    Y02947
SAVEREG  EQU   13               SAVE AREA ADDRESS                Y02947
RETREG   EQU   14
BRREG    EQU   15              BRANCH REGISTER                   Y02947
         USING *,BASEREG
         USING IECTDEB+36,DEBREG
         USING IECTIOB,IOBREG
         USING IHADCB,DCBREG
         EJECT
SIOPF01  EQU   *                   ENTRY TO SIO APPENDAGE        Y02947
         BR    RETREG              RETURN                        Y02947
         CNOP  0,4                                               Y02947
         SPACE 2
SIOPF02  EQU   *                  ENTRY TO PAGE FIX APPENDAGE
         STM   RETREG,BASEREG,DISP0(SAVEREG) SAVE REGISTERS      Y02947
         LR    BASEREG,BRREG      ESTABLISH ADDRESSABILITY       Y02947
         B     AROUND             BRANCH AROUND ID               Y02947
         DC    C'IGG019LP'                                       Y02947
         DC    C',VS2/R2, DEC  73'                               Y02947
         DS    0F                                                Y02947
PATCH    DC    XL40'00'            PATCH AREA                    Y02947
AROUND   L     TCBREG,DEBTCBAD-ONE     LOAD TCB ADDR             Y02947
         USING TCB,TCBREG              ESTABLISH TCB ADDR        Y02947
         TM    TCBFLGS6,TCBRV          VIRTUAL = REAL            Y02947
         BO    EXIT                    YES, EXIT                 Y02947
CKOLT    TM    IOBINCAM,ONLTT          ONLINE TEST IN CONTROL
         BO    EXIT
CKDB0    TM    DCBBFTEK,DYNBUF         DYNAMIC BUFFERING
         BNO   EXIT
*                                                                Y02947
*       TO REACH THIS POINT, WE ARE RUNNING VIRTUAL = VIRTUAL    Y02947
*       AND DYNAMIC BUFFERING IS SPECIFIED IN THE DCB. A         Y02947
*       VIRTUAL SUBAREA LIST WILL BE CREATED WHICH WILL          Y02947
*       CONTAIN THE AREAS TO BE FIXED FOR THIS I/O REQUEST.      Y02947
*       THEY ARE:                                                Y02947
*          1. BUFFER CONTROL BLOCK                               Y02947
*          2. THE ENTIRE BUFFER POOL                             Y02947
*               THE BCB AND THE BUFFER POOL MUST BE IN           Y02947
*               CONTIGUOUS CORE LOCATIONS                        Y02947
*          3. BOTH INDIRECT ADDRESSING LISTS  WHICH ALSO MUST    Y02947
*               BE IN CONTIGUOUS CORE LOCATIONS                  Y02947
*                                                                Y02947
         SR    VSLREG,VSLREG       CLEAR REGISTER                Y02947
         IC    VSLREG,DCBEIOBX     IOB SIZE                      Y02947
         LR    DBEXTREG,IOBREG     IOB ADDRESS                   Y02947
         AR    DBEXTREG,VSLREG     POINT TO END OF IOB           Y02947
         SH    DBEXTREG,HALF8      POINT TO DYN BUF EXT          Y02947
         L     VSLREG,DISP4(DBEXTREG)  LOAD VSL ADDR             Y02947
         LA    PARMREG,DISP8(VSLREG) POINT TO VSL ENTRIES        Y02947
         SPACE 1
BUFPOOL  L     WREGA,DCBBUFCB          BCB ADDRESS
         LA    WREGA,0(WREGA)          CLEAR HIGH BYTE
         SR    WREGB,WREGB             CLEAR IC REG
         IC    WREGB,DCBBUFNO          NUMBER OF BUFFERS
         MH    WREGB,DCBBUFL           TOTAL SIZE OF BUFFERS
         LA    WREGB,7(WREGA,WREGB)    ENDING ADDRESS
         ST    WREGA,DISP0(PARMREG) BUFFER START ADDRESS         Y02947
         OI    DISP0(PARMREG),PGFIX INDICATE PAGE FIX FUNCTION   Y02947
         ST    WREGB,DISP4(PARMREG) BUFFER END ADDR + 1          Y02947
         L     WREGA,DISP0(VSLREG) 1ST IDAL ADDR                 Y02947
         LA    WREGA,DISP0(WREGA)  CLEAR HIGH ORDER BYTE         Y02947
         LR    WREGB,WREGA         LOAD 1ST IDAL ADDR            Y02947
         AH    WREGB,THREE20       ADD LENGTH OF BOTH IDALS      Y02947
         ST    WREGA,DISP8(PARMREG) 1ST IDAL START ADDR          Y02947
         ST    WREGB,DISP12(PARMREG) 2ND IDAL END ADDR + 1       Y02947
         OI    DISP12(PARMREG),LASTVSL SET LAST VSL ENTRY        Y02947
         L     WREGA,SIXT          LOAD CVT ADDR                 Y02947
         USING CVT,WREGA           ESTABLISH CVT ADDR            Y02947
         L     BRREG,CVTVPSIB      LOAD PAGE SERVICES ADDR       Y02947
         DROP  WREGA               DROP CVT ADDR                 Y02947
         MVI   DISP0(DBEXTREG),ON  SET COMP CODE = X'00'         Y02947
         EJECT
*                                                                Y02947
*      FOR THE BRANCH INTERFACE TO PAGE SERVICES (PGFIX), THE    Y02947
*      FOLLOWING REGISTERS MUST CONTAIN:                         Y02947
*              REG0 = ECB ADDRESS                                Y02947
*              REG1 = VIRTUAL SUBAREA LIST ADDRESS               Y02947
*              REG4 = TCB ADDRESS                                Y02947
*                                                                Y02947
         LR    ZEROREG,DBEXTREG    LOAD ECB ADDR                 Y02947
         LR    RQEREG,PARMREG      LOAD VSL ENTRY ADDR           Y02947
         O     RQEREG,INDLIST1     SET LIST FLAG                 Y02947
         LR    DCBREG,TCBREG       LOAD TCB ADDR                 Y02947
         BALR  RETREG,BRREG        LINK TO PAGE SERVICES         Y02947
EXIT     EQU   *
         LM    RETREG,BASEREG,DISP0(SAVEREG) RESTORE REGISTERS   Y02947
         BR    RETREG                  RETURN
         EJECT
         DS    0F                                                Y02947
INDLIST1 DC    X'80000000'
HALF8    DC    H'8'
THREE20  DC    H'320'              LENGTH OF 2 IDALS             Y02947
ON       EQU   X'00'                                             Y02947
SIXT     EQU   X'10'               CVT ADDRESS                   Y02947
PGFIX    EQU   X'40'               PAGE FIX FLAG                 Y02947
LASTVSL  EQU   X'80'               LAST VSL ENTRY FLAG           Y02947
DYNBUF   EQU   X'08'
ONLTT    EQU   X'01'
ONE      EQU   1                   CONSTANT OF 1                 Y02947
DISP0    EQU   0                   DISPLACEMENT OF 0             Y02947
DISP3    EQU   3                   DISPLACEMENT OF 3             Y02947
DISP4    EQU   4                   DISPLACEMENT OF 4             Y02947
DISP8    EQU   8                   DISPLACEMENT OF 8             Y02947
DISP12   EQU   12                  DISPLACEMENT OF 12            Y02947
         EJECT
         IECTIOBX
WORKBYTE EQU   IOBCSW+7
         EJECT
         IECTDEBX
         EJECT
         DCBD  DSORG=BX,DEVD=BS
         EJECT                                                   Y02947
         IKJTCB DSECT=YES,LIST=YES                               Y02947
         EJECT                                                   Y02947
CVT      DSECT                                                   Y02947
         CVT   LIST=NO                                           Y02947
         END
