         TITLE 'IGE0404C - BSC ERP TIME OUT LOAD 2'
IGE0404C CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0404C                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM BSC TIME OUT ERP LOAD 2                                     *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS RECOVERS FROM TIME OUT ERRORS THAT OCCUR DURING             *
*    BSC OPERATION. THE FAILING CCW IS FOUND AND THE CORRECT          *
*    PROCEDURE IS INITIATED.                                          *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (92 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD.  *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0404C                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        SETS INDICATOR IN DECERRST FOR SHOULD NOT OCCUR ERROR        *
*        ALTERED CHANNEL PROGRAM                                      *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        NONE                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YA01231  (IN BASE)                                               *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YA02436  (11/5/73)                                               *
*    YA02160  (11/5/73)                                               *
*    YM05652K (1/25/74)                                               *
*    YA03261  (2/5/74)                                                *
*    YA03262  (2/5/74)                                                *
*    YA03249  (2/15/74)                                               *
*    ZA00504  (5/9/74)                                                *
*    AZ03990  (04/24/75)                                              *
*    ZA04863  (10/03/75)               ZA11456  (07/06/76)            *
*    ZA17702  (02/15/77)                                              *
*    ZA26097  (10/26/77)                                              *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         B     BEGIN               BRANCH AROUND ID            @ZA00504
         DC    C' IGE0404C '       MODULE ID                   @ZA00504
         DC    C' UZ15202'         PTF NUMBER                  @ZA26097
         DC    CL8'&SYSDATE'       DATE LAST ASSEMBLY          @ZA00504
BEGIN    DS    0H                                              @ZA00504
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         USING IECTDECB,DECBRG
         USING IHADCB,DCBREG
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         L     UCBREG,IOSUCB       BASE UCB
         DROP  ERREG5
         L     CCWREG,IOBFL3       GET IOB CSW
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       ADJUST TO FAILING CCW IN IOB
BSCTO0AA TM    IOBWORK,TPOPPTXT    ERROR OCCUR PRIOR TEXT
         BZ    BSCTO02             YES, CONTINUE FLOW
         CLI   ZERO(CCWREG),CCWREAD   ERROR OCCUR ON A READ
         BNE   BSCTO11             NO, CHECK SPECIAL ERRORS
         TM    IOBWORK,TPOPDTXT    ERROR OCCUR DURING TEXT
         BO    BSCTO14             YES, CHECK FURTHER
         CLI   IOBWORK,TPOPRRTX    ERROR ON RESPONSE TO TEXT
         BE    BSCTOT01            YES, SET UP WRITE ENQ
BSCTO00  OI    DECERRST,DECSNO     INDICATE SHOULD NOT OCCUR
BSCTO01  MVI   IOBERRCT,ERRMAXCT   SET FOR NO RETRIES
         B     BSCTOT07            CHECK FOR LERB
BSCTO02  CLI   ZERO(CCWREG),CCWDIAL   ERROR ON A DIAL COMMAND
         BE    BSCTOT0A            YES, SET UP DISABLE
         CLI   IOBWORK,TPOPRENQ    ERROR ON A READ ENQ
         BE    BSCTO21X            YES, CHECK OPERATION TYPE
         CLI   IOBWORK,TPREADID    THIS READ ID CCW
         BE    BSCTO22X            YES, CHECK FURTHER
         B     BSCTO04
BSCTO21X CLI   DECTYPE+L1,READINIT   THIS A READ INITIAL
         BNE   BSCTO02A            NO, CONTINUE CHECKING
         TM    L17(UCBREG),X90     ARE WE ON DIAL LINE
         BNZ   BSCTOT07            YES, RESTART WIRH READ ENQ
         B     BSCTO03             NO, ADJUST TO PREPARE
BSCTO02A CLI   DECTYPE+L1,RDINITEN   THIS A READ INITIAL ENQ
         BE    BSCTO03             YES, REWRITE EOT'S
         CLI   DECTYPE+L1,WRENQCP    RD ENQ CP
         BNE   BSCTOT07            NO, CHECK FURTHER            YA03261
         TM    L35(DCBREG),RJEMASK   RJE REQUESTING NO RETRIES
         BZ    BSCTOT07            CHECK FOR LERB
         B     BSCTO01             SET NO RETRIES
BSCTOT0B EQU   *
         CLI   DECTYPE+L1,WRRESET    WRITE RESET CHAN PROGRAM
         BE    BSCTO01             YES, SET NO RETRIES
         B     BSCTOT07            NO, SET CPA TO FAILURE CM
BSCTO03  SH    CCWREG,ERRCN8       BACK UP ONE CCW
         B     BSCTOT07            CONTINUE FLOW
BSCTO22X SH    CCWREG,ERRCN8       BACK UP ONE CCW
         CLI   L5(CCWREG),TPWRITID   THIS WRITE ID CCW
         BE    BSCTOT07            YES, RESTART HERE
         LA    CCWREG,L8(CCWREG)   STEP TO NEXT CCW
         B     BSCTOT07
BSCTO04  CLI   IOBWORK,TPOPRREN    ERROR ON A READ RESP TO ENQ
         BNE   BSCTO2              NO CONTINUE
         B     BSCTO03             RETRY
BSCTO10  LA    NAMEREG,ERLOD12     ADDRESS OF LERB ROUTINE
XCTLOUT  EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUITNE
         DROP  ERRETR
         BR    ERRETR              EXIT VIA LERB ROUTINE
BSCTO11  CLI   ZERO(CCWREG),CCWWRITE    ERROR ON A WRITE
         BE    BSCTO12             YES, SET UP WRITE CHAN PROG
         CLI   ZERO(CCWREG),CCWDISAB    ERROR ON A DISABLE
         BNE   BSCTO00             NO, SET SHOULD NOT OCCUR
         B     BSCTOT07            YES, SET UP TO RETRY
BSCTO12  CLI   L13(CCWREG),WENDRSP   ERROR DURING WR TRANS TXT
         BNE   BSCTO00             NO, POST SNO
         MVC   IOBWORK+L2,DCBBSDLE   PREPARE TO WRITE DLE ENQ
         MVC   IOBWORK+L3,DCBBSENQ
         LA    ERREG2,IOBWORK+L2   ADDRESS OF DLE ENQ
         LA    ERREG4,CCWREAD      COUNT OF 2
         ST    ERREG4,IOBERCCW+L4   STORE IN CCW
         LA    ERREG4,CCWWRITE     SET WRITE COMMAND
         LA    ERRLNK,CCWTIC       SET TIC COMMAND
         B     BSCTO04A            COMPLETE CHANNEL PROGRAM
BSCTO14  TM    DCBBFTEK,DYNBUF     DYNAMIC BUFFERING USED
         BO    BSCTOT09            YES, POST WITH ERROR
         CLI   IOBCPA,CCWAPOLL     THIS A POLL OPERATION
         BNE   BSCTOT07            NO, BRANCH                  @ZA17702
         SH    CCWREG,ERRCN8       BACK UP ONE CCW
         L     ERREG5,ZERO(CCWREG)   PICK UP AREA ADDRESS
         LA    ERREG5,L1(ERREG5)   INCREMENT ADDRESS BY ONE
         CLM   ERREG5,L7,L9(CCWREG) IS ADDR & COUNT ADJUSTED   @ZA11456
         BE    BSCTOTCA            SET UP WRITE NAK AND TIC    @ZA11456
         STCM  ERREG5,L7,L9(CCWREG)   NO, STORE IN READ        @ZA00504
         LH    ERREG5,L14(CCWREG)   PICK UP COUNT
         LA    ERREG5,L1(ERREG5)   INCREMENT COUNT BY ONE
         STH   ERREG5,L14(CCWREG)   STORE IN CCW
BSCTOTCA AH    CCWREG,ERRCN8       READJUST POINTER            @ZA11456
         LA    ERREG4,CCWWRITE     WRITE OP TYPE                YA03249
         LA    ERREG2,DCBBSNAK     NAK                          YA03249
         LA    ERRLNK,CCWTIC       TIC OP TYPE                  YA03249
BSCTO142 MVI   IOBWORK+L1,RTRNCDE2   INDICATE INTERNAL ROUTINE  YA03249
         B     BSCTOT04            CONTINUE CHAN PROG GEN       YA03249
BSCTOT07 ST    CCWREG,IOBSTART     SET CHAN PROG ADDRESS
BSCTOTB7 TM    IOBSENS0,LERBDCID   LINE ERROR RECORD REQ
         BZ    BSCTOTA7            NO, CONTINUE FLOW
         TM    DCBERROP,DCBLERBI   IS LERB REQUIRED
         BNO   BSCTOTA7            NO, CONTINUE FLOW
         TM    DECTYPE,NOLERB      SUPPRESS LERB
         BO    BSCTOTA7            YES, DON'T SET LERB
         MVI   IOBINCAM+L1,LERBTOID   TIME OUT LERB REQUIRED
BSCTOTA7 CLI   IOBERRCT,ERRMAXCT   ARE RETRIES EXHAUSTED
         BNL   BSCTOT09            YES, SET PERM ERROR
         IC    ERREG5,IOBERRCT     PICK UP RETRY COUNT
         LA    ERREG5,L1(ERREG5)   INCREMENT BY ONE
         STC   ERREG5,IOBERRCT     STORE NEW COUNT
BSCTO26  EQU   *
         CLI   IOBINCAM+L1,LERBTOID     LERB REQUIRED           YA02436
         BE    BSCTO10             YES, EXIT VIA LERB           YA02436
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
         L     NAMEREG,TRANSRTN    LOAD NAME OF 904B           YM02594K
         B     XCTLOUT             XCTL
BSCTOT09 NI    IOSFLA,L255-IOSERR   INDICATE PERMANENT ERROR
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
         L     ERREG5,DECENTRY     GET ADDR OF TERMINAL LIST
         CLI   ZERO(ERREG5),SWLST   IS THIS A SWLST
         BNE   BSCTO21D
         CLI   DECTYPE+L1,WRTCON   IS THIS WRITE CONNECT
         BE    BSCTOSW3
*                                  THE FOLLOWING DELETED FOR    YA03261
***      CLI   IOBCPA+L21,X07      READ ID (WORLD TRADE)
***      BE    BSCTO21
         CLI   L5(CCWREG),X07      IS THIS READ ID ENQ          YA03262
         BNE   BSCTO21D
BSCTO21  EQU   *
         LA    CCWREG,L32(CCWREG)   POINT TO DISABLE CCW
BSCTODIS EQU   *
         MVC   IOBERCCW(L16),ZERO(CCWREG)   MOVE DISABLE AND TIC CCWS
         LA    CCWREG,IOBERCCW     POINT TO ERROR CCW AREA
         MVI   IOBERRCT,ZERO       ZERO OUT RETRY COUNT
         B     BSCTO21E
BSCTOSW3 EQU   *
         MVC   IOBERCCW(L16),DLEOTCCW   WRITE DLE EOT , DISABLE
         LA    ERREG5,DCBBSDLE     GET ADDRESS OF DLE EOT
         ST    ERREG5,IOBERCCW     STORE ADDRESS IN CCW
         MVI   IOBERCCW,CCWWRITE   MOVE IN WRITE COMMAND
         LA    CCWREG,IOBERCCW
BSCTO21C EQU   *
         MVC   IOBERCCW+L16(L1),IOBSENS0   MOVE SENSE INFO TO AREA
         MVC   IOBERCCW+L17(L7),IOBCSW   MOVE CSW TO AREA
         MVI   IOBERRCT+L1,RTRNSPCD   SET SPECIAL RETURN CODE
         MVI   IOBWORK+L1,RTNCD8C   SET BRANCH OFFSET
BSCTO21E ST    CCWREG,IOBSTART     STORE IN IOBSTART
         OI    IOSFLA,IOSERR       TURN ON ERP IN CONTROL
         OI    IOBFLAG1,IOBERRTN   ALSO IN IOB
         B     BSCTO26             EXECUTE CHAN PROGRAM
BSCTO21D EQU   *
         CLI   IOBINCAM+L1,LERBTOID   LERB REQUIRED             YA02436
         BE    BSCTO10             YES, EXIT VIA LERB           YA02436
         LH    NAMEREG,ERLOD35     ADDRESS OF ERROR POST ROUTINE
         B     XCTLOUT             EXIT
BSCTOT01 LA    ERREG2,DCBBSENQ     ADDRESS OF ENQ CHARACTER
         MVI   IOBWORK+L1,RTNCDE6
BSCTOT02 LA    ERREG4,CCWWRITE     SET WRITE COMMAND
         LA    ERRLNK,CCWTIC       SET TIC COMMAND
BSCTOT04 ST    ERREG4,IOBERCCW+L4   SET COUNT IN CCW
BSCTO04A ST    ERREG2,IOBERCCW     STORE ADDRESS IN CCW
         STC   ERREG4,IOBERCCW     SET COMMAND IN CCW
         CLI   IOBWORK+L1,ZERO     SPECIAL RETURN REQUIRED
         BE    BSCTOTA6            NO, DON'T SET CODE
         MVI   IOBERRCT+L1,RTRNSPCD   SET SPECIAL RETURN CODE
BSCTOTA6 ST    CCWREG,IOBERCCW+L8   STORE TIC ADDRESS
         STC   ERRLNK,IOBERCCW+L8   SET COMMAND IN CCW
         ST    ERREG4,IOBERCCW+L12   SET COUNT IN CCW           YA02160
         MVI   IOBERCCW+L4,CHAINCDE   SET COMMND CHN & SLI FLAGS
         LA    CCWREG,IOBERCCW     CHANNEL PROGRAM ADDRESS
         B     BSCTOT07            RETRY ERROR
BSCTOT2A LA    ERRLNK,CCWNOP
         LA    ERREG2,IOBWORK+L2
         LA    ERREG4,CCWREAD
         MVI   IOBWORK+L1,RTRNCDE1  SET SPECIAL RET CD = 81    @ZA11456
         B     BSCTOT04
BSCTO2   CLI   IOBWORK,TPOPRRPL    READ RESPONSE TO POLLING
         BNE   BSCTO23             NO CHECK FURTHER
         TM    DCBBFTEK,DYNBUF     DYNAMIC BUFFERING           @ZA03990
         BNO   BSCT02A             NO, CHECK RETRY             @ZA03990
         MVC   L9(L3,CCWREG),L1(CCWREG)   PUT INPUT            @ZA03990
*                     AREA ADDR IN RD TEXT CCW (FROM RD INDEX) @ZA03990
*                        FOR MB TO RELEASE BUFFERS. THIS MVC   @ZA03990
*                        IS GOOD FOR BOTH OPEN AND WRAP LIST.  @ZA03990
         B     BSCTOT09         POST WITH ERROR                @ZA03990
BSCT02A  EQU   *
         L     ERREG2,DECAREA      POINT TO BUFFER              YA03249
         CLC   ZERO(L1,ERREG2),IOBDCBPT   INDEX EQUAL           YA03249
         BE    INDXEQUL            YES                          YA03249
         MVC   IOBDCBPT(L1),ZERO(ERREG2)   SET NEW INDEX        YA03249
         MVI   IOBERRCT,ZERO       SET COUNT TO ZERO            YA03249
INDXEQUL EQU   *                                                YA03249
         L     ERREG5,IOBCPA+L8    USER POLLING ENTRY
         MVC   IOBCPA+L9(L3),DECENTRY+L1   FAILING ADDR TO CHAN PROG
         L     ERREG2,IOBCPA+L8    PICK UP FAILING ADDR
         SR    ERREG5,ERREG2       DIFF BETWEEN USERS AND FAILED
         AH    ERREG5,IOBCPA+L14   ADD TOTAL
         STH   ERREG5,IOBCPA+L14   STORE NEW COUNT
         L     ERREG5,ZERO(CCWREG)   GET DATA ADDRESS           YA03249
         LA    ERREG5,L1(ERREG5)   BUMP BY ONE                  YA03249
         STCM  ERREG5,L7,L9(CCWREG)   STORE IN NEXT CCW        @ZA00504
         LH    ERREG5,L14(CCWREG)   GET COUNT                   YA03249
         LA    ERREG5,L1(ERREG5)   BUMP BY ONE                  YA03249
         STH   ERREG5,L14(CCWREG)   RESET COUNT                 YA03249
         MVI   IOBWORK+L1,RTRNCDE2   SET SPECIAL RETURN         YA03249
         LA    ERREG2,DCBBSNAK     NAK                          YA03249
         LA    CCWREG,L8(CCWREG)   START ON NEXT CCW            YA03249
         B     BSCTOT02            BUILD CHANNEL PROGRAM        YA03249
BSCTO23  CLI   IOBWORK,TPOPRRAD    READ RESP TO ADDRESSING
         BE    BSCTO24             YES SET UP RETRY
         CLI   ZERO(CCWREG),CCWPREP   IS CCW PREPARE
         BE    BSCTO231            YES, POST SNO
         CLI   IOBWORK,TPWRITRS    IS THIS WR RESP TO ENQ
         BE    BSCTO231            YES, POST SNO
         CLI   IOBWORK,ZERO        ERP CCW
         BNE   BSCTO231            NO, BRANCH                  @ZA26097
         L     CCWREG,IOBERCCW+L8   LOAD FAILING CCW
         B     BSCTOT07            RESTART WITH IT
BSCTO242 SH    CCWREG,ERRCN8       RESEND ID AND ENQ
         B     BSCTOT07            CONTINUE FLOW
BSCTO231 EQU   *
         OI    DECERRST,DECSNO     SET SHOULD NOT OCCUR
         B     BSCTOT09
BSCTO24  LA    CCWREG,IOBCPA       START OF CHANNEL PROG
         B     BSCTOT07
BSCTOT0A MVC   IOBERCCW(L8),DISABCCW   MOVE IN DISABLE
         LA    ERRLNK,CCWTIC       SET TIC COMMAND
         LA    ERREG4,CCWWRITE     SET COUNT TO ONE
         B     BSCTOTA6            SET UP CHANNEL PROGRAM
         EJECT
**********
*
*        CONSTANTS
*
**********
TRANSRTN DC    F'9042'             NAME OF BTAM TRANSLATE ROUTINE
DLEOTCCW DC    X'0100000060210002'
DISABCCW DC    X'2F00000020010001'   DISABLE CCW
ERLOD35  DC    H'2043'
ERRCN8   DC    H'8'
PACHAREA DC    23C'404C'           PATCHAREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
STATUP   EQU   254                 STATISTICS UPDATE ROUTINE NAME
TPOPRRPL EQU   X'0A'
TPOPRRAD EQU   X'06'
DECSNO   EQU   X'40'
DCBLERBI EQU   X'08'
LERBTOID EQU   X'03'
ERRMAXCT EQU   7
WRTCON   EQU   X'1C'               WRITE CONNECT
CCWDIAL  EQU   X'29'
WENDRSP  EQU   X'13'
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L9       EQU   9                   LENGTH AND DISPLACEMENT
L12      EQU   12                  LENGTH AND DISPLACEMENT
L13      EQU   13                  LENGTH AND DISPLACEMENT
L14      EQU   14                  LENGTH AND DISPLACEMENT
L16      EQU   16                  LENGTH AND DISPLACEMENT
L17      EQU   17                  LENGTH AND DISPLACEMENT
L21      EQU   21                  LENGTH AND DISPLACEMENT
L32      EQU   32                  LENGTH AND DISPLACEMENT
L35      EQU   35                  LENGTH AND DISPLACEMENT
ZERO     EQU   X'00'               LENGTH AND DISPLACEMENT
X07      EQU   X'07'               READ ID MASK
X87      EQU   X'87'               WRITE CONNECT MASK
X90      EQU   X'90'               DIAL MASK
ERLOD12  EQU   2042
NOLERB   EQU   X'40'
L255     EQU   X'FF'               AND MASK
CHAINCDE EQU   X'60'
RTRNSPCD EQU   X'14'
CCWTIC   EQU   X'08'
LERBDCID EQU   X'01'
RTRNCDE1 EQU   X'81'
RTRNCDE2 EQU   X'82'                                            YA03249
CCWAPOLL EQU   X'09'
DYNBUF   EQU   X'08'
CCWPREP  EQU   X'06'
CCWDISAB EQU   X'2F'
CCWWRITE EQU   X'01'
TPOPRREN EQU   X'0C'
RDINITEN EQU   X'19'
RTNCDE6  EQU   X'86'
TPOPRENQ EQU   X'0B'
READINIT EQU   X'01'
TPOPRRTX EQU   X'25'
TPOPDTXT EQU   X'11'
CCWREAD  EQU   X'02'
CCWNOP   EQU   X'03'
TPOPPTXT EQU   X'30'
TPREADID EQU   X'07'
WRRESET  EQU   X'0A'
WRENQCP  EQU   X'15'
RJEMASK  EQU   X'80'
TPWRITID EQU   X'03'
TPWRITRS EQU   7
SWLST    EQU   X'FF'
RTNCD8C  EQU   X'8C'
COUNT2   EQU   X'02'
**********
*
*        REGISTER EQUATES
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
ERREG2   EQU   2                   WORK REG
ERREG1   EQU   3                   WORK REG
CCWREG   EQU   4                   CCW ADDRESS REG
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE
BASEREG  EQU   8                   PROGRAM BASE
EWAREG   EQU   9                   ERP WORKAREA BASE
IOBRG    EQU   10                  IOB BASE
ERREG4   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  LINK REG
ERREG5   EQU   15                  WORK REG/LINK REG
         EJECT
         IECTDECB
         EJECT
         DCBD  DSORG=BX,DEVD=BS
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
