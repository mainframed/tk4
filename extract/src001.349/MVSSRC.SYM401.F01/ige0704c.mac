         TITLE  'IGE0704C - BSC BUS OUT ERP ROUTINE'
IGE0704C CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0704C                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM BSC BUS OUT ERP                                             *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS ROUTINE RECOVERS FROM A BUS OUT ERROR WHICH OCCURRED DURING *
*    A BSC OPERATION. THE TYPE OF CCW ON WHICH THE ERROR OCCURRED     *
*    IS DETERMINED AND THE PROPER ERROR RECOVERY IS INITIATED.        *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (52 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        526 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD.  *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0704C                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        ALTERED CHANNEL PROGRAM                                      *
*        SETS INDICATOR IN DECERRST FOR SHOULD NOT OCCUR ERROR
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        NONE                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YM05652K (1/24/74)                                               *
*    ZA02812  (01/10/75)          AZ03976  (04/24/75)                 *
*    AZ13205  (09/23/76)                                              *
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         USING RQE,ERREG5
         USING IECTDECB,DECBRG
         USING IHADCB,DCBREG
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         B     BEGIN               BRANCH AROUND ID         LD @ZA02812
         DC    C'IGE0704C'         MODULE ID                LD @ZA02812
         DC    C'5010'             DATE LAST CHANGE         LD @ZA02812
         DC    CL8'&SYSDATE'       DATE LAST ASSEMBLY       LD @ZA02812
BEGIN    DS    0H                                           LD @ZA02812
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         L     UCBREG,IOSUCB       BASE UCB
         DROP  ERREG5
         L     CCWREG,IOBFL3       GET IOB CSW
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       POINT TO FAILING CCW IN IOB
         XC    IOBERCCW(L16),IOBERCCW   CLEAR ERP CCW AREA
         TM    IOBSENS0,BUSOUT     IS THIS A BUSOUT CHECK
         BO    BSCBO00B            YES BRANCH INTO ROUTINE
         CLI   ZERO(CCWREG),CCWREAD   DID ERROR OCCUR ON A READ
         BNE   BSCOR03             NO, GO SET SHOULD NOT OCCUR
         CLI   IOBWORK,TPOPRENQ    IS THIS READ ENQUIRY
         BE    BSCOR06             YES, GO TO RESTART CHAN.PROG.
         CLI   IOBWORK,TPRDRENQ    IS THIS READ RESP TO ENQ
         BE    BSCOR04             YES, RESTART W/ PREVIOUS CCW
         CLI   IOBWORK,TPOPRREN    IS THIS READ RESP TO TEXT
         BE    BSCOR05             YES, RESTART W/ READ ENQ CCW
         CLI   IOBWORK,TPOPRTXT    IS THIS READ TEXT
         BNE   BSCOR03             NO, GO SET SHOULD NOT OCCUR
         CLI   L5(DECBRG),DECCONV   CONVERSATIONAL WRITE ?
         BNL   BSCCCC
         TM    L4(DECBRG),AUTOPOLL USING AUTOPOLL ?         LD @ZA02812
         BZ    TSTDB               NO, CHECK DYN. BUFF.     LD @ZA02812
         CLI   L5(DECBRG),DECTYP1  IS READ INIT ?           LD @ZA02812
         BNE   TSTDB               NO, CHECK DYN. BUFF.     LD @ZA02812
         LR    ERREG2,CCWREG       GET READ TXT ADDR           @ZA03976
         SH    ERREG2,EIGHT        BACK UP TO RD INDEX         @ZA03976
         L     ERREG4,ZERO(ERREG2) LOAD DATA ADDR              @ZA03976
         AH    ERREG4,ONE           ADD ONE                    @ZA03976
         C     ERREG4,ZERO(CCWREG)  ADDR PREV ADJUSTED?        @ZA03976
         BE    TSTDB                YES, DONT ADJUST AGAIN     @ZA03976
*        ONE LINE OF CODE DELETED BY APAR ---------------->    @ZA13205
         ST    ERREG4,ZERO(CCWREG) RESTORE CORRECT DATA...  LD @ZA02812
*                                  ...ADDR IN READ TXT.     LD @ZA02812
         LH    ERREG4,L6(CCWREG)   GET CCW BYTE COUNT       LD @ZA02812
         LA    ERREG4,L1(ERREG4)   ADD ONE                  LD @ZA02812
         STH   ERREG4,L6(CCWREG)   RESTORE COUNT TO CCW     LD @ZA02812
TSTDB    TM    DCBBFTEK,BSCDYBUF   WAS DYNAMIC BUFFERING EMPLOYED
         BO    BSCOR02             YES, INDICATE PERMANENT ERROR
BSCOR00  LA    ERREG4,DCBBSNAK     ADDRESS OF NAK SPECIAL CHAR.
         ST    ERREG4,IOBERCCW     STORE ADDRESS IN ERP CCW AREA
BSCOR00A MVI   IOBERCCW,OPCODWRT   COMMAND CODE INTO ERP CCW
         MVI   IOBERCCW+L4,FLAG
         MVI   IOBERCCW+L7,COUNTONE   COUNT OF 1 FOR WRITE NAK CCW
         ST    CCWREG,IOBERCCW+L8   READ TEXT CCW ADDRESS IN TIC
         MVI   IOBERCCW+L8,BSCTICCC   TIC COMMAND CODE INTO ERP CCW
         LA    ERREG4,IOBERCCW     GET ADDRESS OF ERP CHANNEL PROG
         ST    ERREG4,IOBSTART     STORE ADDRESS IN IOB FIELD
BSCOR01  CLI   IOBERRCT,ERRMAX     RETRY COUNT AT MAXIMUM
         BNL   BSCOR02             INDICATE PERMANENT ERROR
         IC    ERREG4,IOBERRCT     UPDATE RETRY COUNT
         LA    ERREG4,L1(ERREG4)   TO NEW COUNT
         STC   ERREG4,IOBERRCT     STORE IN COUNT FIELD
         B     RETRY               ISSUE ERROR EXCP
BSCOR02  NI    IOSFLA,L255-IOSERR  INDICATE PERMANENT ERROR
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
         LA    NAMEREG,ERLOD25     GET ERROR ROUTINE ADDRESS
XCTLOUT  EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE
         DROP  ERRETR
         BR    ERRETR              XCTL
BSCOR03  OI    DECERRST,DECBSNO    SET SHOULD-NOT-OCCUR FLAG
         B     BSCOR02             INDICATE PERMANENT ERROR & EXIT
BSCOR04  SH    CCWREG,ERRCN8       BACK UP TO PREVIOUS CCW
BSCOR06  ST    CCWREG,IOBSTART     STORE CPA ADDRESS IN DCB FIELD
         B     BSCOR01
BSCCCC   SH    CCWREG,ERRCN8       BACK UP TO RD RESP TO TEXT
         B     TSTDB
BSCOR05  EQU   *
         LA    ERREG4,DCBBSENQ     GET ADDRESS OF ENQ CHARACTER
         ST    ERREG4,IOBERCCW     STORE ENQ ADDRESS IN CCW
         MVI   IOBERCCW+L5,TPOPWENQ   MOVE IN WRITE TQ TP CODE
         B     BSCOR00A            GO FINISH CCW AND EXCP
BSCBO00B EQU   *
         CLI   ZERO(CCWREG),CCWREAD   WAS CCW FOR A READ OPERATION
         BE    BSCBO02             CHK. CT. AND RETRY IF POSSIBLE
         CLI   ZERO(CCWREG),CCWDIAL   CCW FOR A DIAL COMMAND
         BE    BSCBO03             DISABLE LINE AND DIAL AGAIN
         CLI   ZERO(CCWREG),CCWPOLL   CCW FOR A POLL COMMAND
         BE    BSCBO04             RE-EXECUTE CHANNEL PROGRAM
         CLI   ZERO(CCWREG),CCWRITE   CCW FOR A WRITE COMMAND
         BNE   BSCBO00             NO, RETRY FAILING COMMAND
         TM    TPOPCODE(CCWREG),PRIORTXT   CCW PRIOR TO TEXT TRANSFER
         BZ    BSCBO05             YES, CHECK FOR WRITE EOT CCW
         TM    TPOPCODE(CCWREG),DURINTXT   CCW DURING TEXT TRANSFER
         BO    BSCBO02             YES, RETRY FAILING CCW IF CAN
         TM    TPOPCODE(CCWREG),LASTCOMD   CCW LAST ONE IN CHAN.PROGRAM
         BZ    BSCBO06             NO, RESTART WITH NEXT CCW
BSCBO00  ST    CCWREG,IOBSTART     ADDR. OF FAILING CCW IN IOBSTAR
BSCBO00A CLI   IOBERRCT,ERRMAX     RETRY COUNT AT MAXIMUM
         BNL   BSCBO01             YES, GO INDICATE A PERM. ERROR
         IC    ERREG4,IOBERRCT     NO, UPDATE RETRY COUNT BY ONE
         LA    ERREG4,L1(ERREG4)   AND ISSUE EXCP TO RESTART
         STC   ERREG4,IOBERRCT     WITH THE FAILING CCW
RETRY    EQU   *
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
         L     NAMEREG,TRANSRTN    LOAD NAME
         B     XCTLOUT             XCTL
BSCBO02  CLC   COUNT(L2,CCWREG),IOBCSW+L5   RESIDUAL COUNT = ORIGINAL
         BE    BSCBO00             YES, RETRY FAILING COMMAND
BSCBO01  NI    IOSFLA,L255-IOSERR  SET PERMANENT ERROR FLAGS
         NI    IOBFLAG1,L255-IOBERRTN   ALSO ON IOB
         LA    NAMEREG,ERLOD25     ADDRESS OF ERROR POST
         B     XCTLOUT             BRANCH TO XCTL ROUTINE
BSCBOSNO OI    DECERRST,DECBSNO    SET PERMANENT ERROR FLAG
         B     BSCBO01             GO TO ERRPOST VIA XCTL ROUTINE
BSCBO03  XC    IOBERCCW(L12),IOBERCCW   CLEAR OUT NEEDED CCW AREA
         MVI   IOBERCCW,DISABLE    MOVE DISABLE COMMAND CODE
         MVI   IOBERCCW+L5,TPDISAB   DSABLE TP CODE IN CCW
         MVI   IOBERCCW+L7,DISABCNT   COUTN IN CCW
         MVI   IOBERCCW+L4,FLAGS   CC & SLI FLAGS INTO CCW
         ST    CCWREG,IOBERCCW+L8   USER CCW ADDR IN TIC COMMAND
         MVI   IOBERCCW+L8,TICCOMMD   TIC COMMAND CODE IN CCW
BSCBO04  LA    ERREG2,IOBCPA       UPDATE IOBSTART TO POINT TO
         ST    ERREG2,IOBSTART     START OF USER CHANNEL PROG.
         L     ERREG4,IOBCPA+L8    ADDR. OF USER POLLING ENTRY
         MVC   IOBCPA+L9(L3),DECENTRY+L1   FAILING ENTRY ADDR TO CCW
         L     ERREG2,IOBCPA+L8    ADDR. OF FAILING POLL CCW
         SR    ERREG4,ERREG2       COUNT BETWEEN USER AND FAILED
         AH    ERREG4,IOBCPA+L14   ADD TOTAL COUNT FROM USER ENTRY
         STH   ERREG4,IOBCPA+L14   STORE NEW COUNT IN POLL CCW
         B     BSCBO00A            GO EXECUTE
BSCBO05  CLI   IOBWORK,WRITEOT     WRITE EOT CCW
         BE    BSCBO00
BSCBO06  LA    CCWREG,NEXTCCW(CCWREG)   ADDRESS OF NEXT CCW
         ST    CCWREG,IOBSTART
         B     BSCBO00A
         EJECT
**********
*
*        CONSTANTS
*
**********
TRANSRTN DC    F'9042'             NAME OF BTAM TRANSLATE ROUTINE
EIGHT    EQU   *                                               @ZA03976
ERRCN8   DC    H'08'               NO BYTES IN ONE CCW
ONE      DC    H'01'                                           @ZA03976
PACHAREA DC    13C'704C'           PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
TPOPCODE EQU   5                   LOCATION OF TP CODE IN CCW
L255     EQU   X'FF'               AND MASK
DECCONV  EQU   X'0D'               CONVERSATIONAL WRITE
CCWREAD  EQU   X'02'               COMMAND CODE FOR READ CCW
CCWDIAL  EQU   X'29'               COMMAND CODE FOR DIAL
CCWPOLL  EQU   X'09'               COMMAND CODE FOR POLL
CCWRITE  EQU   X'01'               COMMAND CODE FOR WRITE
PRIORTXT EQU   X'F0'               MASK TO SEE IF CCW PRIOR TO TXT
DURINTXT EQU   X'10'               CCW DURING TEXT TRANSFER
LASTCOMD EQU   X'80'               LAST CCW IN CHANNEL PROGRAM
ERRMAX   EQU   7                   MAXIMUM NO RETRIES ALLOWED
COUNT    EQU   6
DISABLE  EQU   X'27'               COMMAND CODE FOR DISABLE CCW
TPDISAB  EQU   X'01'               TP CODE FOR DISABLE CCW
DISABCNT EQU   X'01'               COUNT FOR DISABLE CCW
FLAGS    EQU   X'60'               FLAGS FOR DISABLE CCW
TICCOMMD EQU   X'08'               COMMAND CODE FOR TIC COMMAND
WRITEOT  EQU   X'02'               TP CODE FOR WRITE EOT
NEXTCCW  EQU   X'08'
ERLOD25  EQU   2043
BUSOUT   EQU   X'20'               BUS OUT BIT IN SENSE BYTE
DECBSNO  EQU   X'40'               SHOULD NOT OCCUR FLAG IN DECB
TPOPRENQ EQU   X'0B'               TP CODE FOR READ ENQ
TPRDRENQ EQU   X'0C'               TP CODE FOR READ-RESPONSE-ENQ
TPOPRREN EQU   X'25'               TP OP CODE FOR RD RESP TO TEXT
TPOPRTXT EQU   X'11'               TP CODE FOR READ TEXT
BSCDYBUF EQU   X'08'               DYNAMIC BUFFERING FLAG
ZERO     EQU   X'00'               LENGTH AND DISPLACEMENT
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L9       EQU   9                   LENGTH AND DISPLACEMENT
L12      EQU   12                  LENGTH AND DISPLACEMENT
L14      EQU   14                  LENGTH AND DISPLACEMENT
L16      EQU   16                  LENGTH AND DISPLACEMENT
COUNTONE EQU   X'01'               COUNT FOR WRITE NAK & ENQ CCWS
OPCODWRT EQU   X'01'               OP-CODE FOR A WRITE COMMAND
BSCTICCC EQU   X'08'               OP-CODE FOR TIC COMMAND
TPOPWENQ EQU   X'03'
FLAG     EQU   X'60'
AUTOPOLL EQU   X'80'               AUTOPOLL FLG IN DECB     LD @ZA02812
DECTYP1  EQU   X'01'               READ INIT. OP TYPE...    LD @ZA02812*
                                   ...IN DECB               LD @ZA02812
**********
*
*        REGISTER DEFINITION
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
ERREG2   EQU   2                   WORK REG
EWAREG   EQU   3                   ERP WORKAREA BASE
CCWREG   EQU   4                   CCW ADDRESS REG
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE
BASEREG  EQU   8                   PROGRAM BASE
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG4   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  LINK REG
ERREG5   EQU   15                  LINK REG/WORK REG
         EJECT
         IECTDECB
         EJECT
         DCBD  DSORG=BX,DEVD=BS
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
