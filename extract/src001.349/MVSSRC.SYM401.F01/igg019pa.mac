         TITLE 'IGG019PA - LOCAL 3270 CE/XCE APPENDAGE '
IGG019PA CSECT
*
***********************************************************************
*                                                                     *
* C348000,C352000-354000,A356500-357500,C358000,D362000,      LD Y02947
* D364000,C386000,C394000,C470000,C500000,C506000,A506500,    LD Y02947
* C520000,C522800,C554000,C562000,C566000,C580600,C592500,    LD Y02947
* C626000,A658500,D674000,D682000,D700000,D707000-709000,     LD Y02947
* D710000,D714500,A739500,D744000                             LD Y02947
* A372500-373700                                             LD YA02143
* A593500,A730500,A738100                                    LD YA02166
* A564500-565700,A725500-725100                              LD YA02167
* D520000-522000,A522720-522792,D522800-522830,C556000,      LD YA02451
* A586500,C592000,C593000,A593600                            LD YA02451
* A587000-587500,A694500                                     LD YA02471
* A386500,D391000-432000,A391000-394500                      LD YA03957
*        APARS FIXED                                                  *
*                                                                     *
*        ZA02332  (11/21/74)                                          *
*        ZA02342  (12/19/74)                                          *
*        ZA06371  (11/04/75)                                          *
*        ZA27950  (11/11/75)                                          *
*                                                                     *
***********************************************************************
*                   LOCAL 3270 CHANNEL END - ABNORMAL                 *
*                         END APPENDAGE                               *
***********************************************************************
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS MODULE WILL BE LOADED BY THE BTAM OPEN EXECUTOR WHEN THE    *
*    APPLICATION PROGRAM OPENS A LINE GROUP DCB THAT CONTAINS A LOCAL *
*    3270 DEVICE. THE ENTRY POINT ADDRESS WILL BE PLACED INTO THE DEB *
*    ASSOCIATED WITH THE LINE GROUP. WHEN IOS IS READY TO DO CHANNEL  *
*    END OR ABNORMAL CHANNEL END PROCESSING, IT WILL GET THE ADDRESS  *
*    AND GIVE IT CONTROL.                                             *
*                                                                     *
*    THIS MODULE HAS THE FOLLOWING RESPONSIBILITIES:                  *
*    1) CONTROL BLOCK CLEAN-UP AT THE END OF AN I/O REQUEST INITIATED *
*    BY THE BTAM READ/WRITE ROUTINE (IGG019MA) FOR THE APPLICATION    *
*    PROGRAM.                                                         *
*    2) RETRY CHANNEL PROGRAMS THAT WERE NOT RETRYABLE BY THE LOCAL   *
*    DEVICE ERP'S (IGE0010E, IGE0110E).                               *
*    3) RECOGNIZE REQUEST-FOR-TEST MESSAGES AND THE ON-LINE TEST IN   *
*    PROGRESS CONDITION AND SCHEDULE THE LOCAL ON-LINE TEST CONTROL   *
*    MODULE (IGG019PI).                                               *
*    4) RECOGNIZE THE OLTEP EXECUTING CONDITION AND SET THE OLTEP     *
*    IN CONTROL FLAG.                                                 *
*                                                                     *
* ENTRY:                                                              *
*         TO THE FIRST EXECUTABLE INSTRUCTION.                        *
*                                                                     *
* INPUT:                                                              *
*    REGISTER 1 - ADDRESS OF THE RQE.                                 *
*    REGISTER 2 - ADDRESS OF THE IOB.                                 *
*    REGISTER 3 - ADDRESS OF THE DEB.                                 *
*    REGISTER 4 - ADDRESS OF THE DCB.                                 *
*    REGISTER 6 - UNIT ADDRESS OF TARGET DEVICE OF I/O REQUEST.       *
*    REGISTER 7 - ADDRESS OF UCB.                                     *
*    REGISTER 14 - RETURN ADDRESS TO IOS.                             *
*    REGISTER 15 - ENTRY POINT ADDRESS.                               *
*    DECAREA - CONTAINS THE ADDRESS OF THE REQUEST-FOR-TEST MESSAGE   *
*    IF AN RFT WAS RECEIVED. THE 3270 RFT HAS THE FOLLOWING FORMAT:   *
*          +++++++++++++++++++++++++++++++++++++  SOH%/STX = HEADER   *
*          +     +     +     +     +     +     +  XX = TEST MSG NUMBER*
*    +0    + SOH +  %  +  /  + STX +  X  +  X  +  YY = NUMBER OF TIMES*
*          +     +     +     +     +     +     +       TEST MSG TO BE *
*          +++++++++++++++++++++++++++++++++++++       SENT           *
*          +     +     +     +     +     +     +  N = 3 (# OF CHARS IN*
*    +6    +  Y  +  Y  +  N  +  C  +  U  +  U  +       ADDRESS)       *
*          +     +     +     +     +     +     +  CUU = ADDRESS OF    *
*          +++++++++++++++++++++++++++++++++++++       TARGET DEVICE  *
*                                                                     *
* OUTPUT:                                                             *
*    ALL REGISTERS ARE THE SAME AS WHEN THE CE/XCE APPENDAGE WAS      *
*    ENTERED.                                                         *
*                                                                     *
*    CONTROL BLOCKS:                                                  *
*    DECCOUNT = RESIDUAL COUNT FROM IOBCSW                            *
*    DECCSWST = STATUS BITS FROM IOBCSW                               *
*    DECSENS0 = SENSE INFORMATION FROM IOBSENS0 (IF ABNORMAL CHANNEL  *
*    AND UNIT CHECK)                                                  *
*                                                                     *
*    NORMAL COMPLETION (IOBFLAG1, BITS 2 & 5 OFF) : IOBERRCT =0,      *
*    BUSY BIT OF IOBINCAM TURNED OFF (IF A READ INITIAL, THE BUSY BIT *
*    IN EVERY IOB IS TURNED OFF).                                     *
*                                                                     *
*    PERMANENT ERROR (IOBFLAG1, BIT 2 OFF AND BIT 5 ON; AND IOBERRCT, *
*    BYTE 2, BITS 5,6,&7 = 011) : SAME OUTPUT AS NORMAL COMPLETION.   *
*                                                                     *
*    USER CORRECTABLE ERROR (IOBFLAG1, BIT 2 OFF AND BIT 5 ON; AND    *
*    IOBERRCT, BYTE 2, BITS 5,6,&7 = 110) : SAME OUTPUT AS NORMAL     *
*    COMPLETION.                                                      *
*                                                                     *
*    INTERCEPT CONDITION (IOBFLAG1, BIT 5 ON AND IOBECBCC = X'44') :  *
*    SAME OUTPUT AS NORMAL COMPLETION.                                *
*                                                                     *
*    RECONSTRUCTION REQUIRED (IOBFLAG1, BIT 5 ON; AND IOBERRCT, BYTE  *
*    2, BITS 5,6,&7 = 010) : SAME OUTPUT AS NORMAL COMPLETION.        *
*                                                                     *
*    RETRY CHANNEL PROGRAM (IOBFLAG1, BIT 2 ON; AND IOBERRCT, BYTE 2, *
*    BITS 5,6,&7 = 111) : THE FOLLOWING FIELDS ARE RESET TO ZERO --   *
*    IOBFLAG1, IOBFLAG2, IOBSENS0, IOBSENS1, IOBFLAG3, IOBCSW.        *
*    IOBECBCC IS RESET TO X'7F' AND IOBERRCT, BYTE 2, IS SET TO X'05'.*
*                                                                     *
*    RETRY FAILURE (IOBFLAG1, BIT 5 ON; AND IOBERRCT, BYTE 2, BITS    *
*    5,6,&7 = 101) : SAME OUTPUT AS NORMAL COMPLETION.                *
*                                                                     *
*    OLTEP EXECUTING (IOBINCAM, BYTE 1 = X'02') : BUSY BIT OF IOB     *
*    PASSED BY IOS TURNED ON; DECFLAGS, BIT 7 (OLTEP IN CONTROL)      *
*    TURNED ON.                                                       *
*                                                                     *
*    REQUEST FOR TEST (FIRST 3 BYTES OF USER AREA = SOH%/) : CONVERT  *
*    TO STANDARD OS BTAM RFT --                                       *
*    +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++    *
*    +     +     +     +     +     +     +     +     +     +     +    *
*    + SOH +  %  +  X  +  X  +  Y  +  Y  +  N  +  C  +  U  +  U  +    *
*    +     +     +     +     +     +     +     +     +     +     +    *
*    +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++    *
*    AND SCHEDULE ON-LINE TEST.                                       *
* PUT ADDR OF RFT IN IOBERINF, AMD SCHEDULE ON-LINE TEST.             *
*    ON-LINE TEST IN PROGRESS (IOBINCAM, BYTE 1, BIT 7 ON) : SCHEDULE *
*    ON-LINE TEST.                                                    *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*         NONE                                                        *
*                                                                     *
* EXITS, NORMAL:                                                      *
*         POST COMPLETE OR ERP REQUIRED - RETURN TO IOS.              *
*         RETRY CHANNEL PROGRAM - RETURN TO IOS+8.                    *
*                                                                     *
* EXITS, ERROR:                                                       *
*         NONE                                                        *
*                                                                     *
* TABLES/WORK AREAS:                                                  *
*         NONE                                                        *
*                                                                     *
* ATTRIBUTES:                                                         *
*    RESIDENT, ENABLED, AND SERIALLY REUSABLE.                        *
*                                                                     *
* CHARACTER CODE DEPENDENCY:                                          *
*    NONE                                                             *
*                                                                     *
***********************************************************************
         EJECT
*/*IGG019PA: CHART*/
*/* E IGG019PA*/
*/*LOADECB: D (YES,,NO,RETURN) IS DECB VALID*/
*/*DECBOK: P RESIDUAL COUNT INTO DECCOUNT*/
*/* P MOVE STATUS BITS INTO DECCSWST*/
*/* D (YES,,NO,TESTXCE) IOB CC X'7E'*/
*/* M (,RETURN) SET IOB EXCEPTION*/
*/*TESTXCE: D (YES,,NO,TESTABNL) IS ONLINE TEST RUNNING*/
*/* M (,SCHEDOLT) RESET IOB ERROR FLAG AND SCHEDULE OLT CONTROL*/
*/*TESTABNL: D (YES,,NO,TSTRDTI) IS IT ABNORMAL CHANNEL END*/
*/* D (YES,,NO,TSTREADY) FIRST ENTRY TO CHE/ABE*/
*/* D (NO,TESTIOB,YES,,) UNIT CHECK IN CSW STATUS*/
*/* P MOVE SENSE INFO INTO DECSENS0*/
*/*NOTUC: D (NO,,YES,TESTIOB) HAS ERP BEEN ENTERED*/
*/* D (YES,,NO,NORETRY) CHANNEL RETRY BY CE/XCE APPENDAGE*/
*/* M (,TSTRDTI) SET PERMANENT ERROR COMPLETION CODE*/
*/*NORETRY: M TURN ON ERP HAS BEEN ENTERED BIT*/
*/* R RETURN TO IOS*/
*/*TESTIOB: D (NO,,YES,TSTRDTI) WAS THE IOB INTERCEPTED*/
*/* D (YES,,NO,TSTRECON) IS ERROR PERMANENT*/
*/* D (YES,,NO,TSTRDTI) OPERATIONAL SIO*/
*/* P (,TSTRDTI) SET SIOCC=3 ERROR FLAG*/
*/*TSTREADY: D (YES,,NO,RETURN) UNIT CHECK STATUS*/
*/* D (YES,,NO,RETURN) INTERVENTION REQUIRED*/
*/* D (YES,,NO,RETURN) READYQ AVAILABLE*/
*/* P (,RETURN) SET DVC NOT RDY & READYQ*/
*/*TSTRECON: D (NO,,YES,RECON) IS RECONSTRUCT REQUIRED*/
*/* D (YES,,NO,TSTRDTI) SHOULD REQUEST BE RETRIED*/
*/* M RESET IOBFLAG1 FIELD TO ZERO*/
*/* M RESET IOBFLAG2 FIELD TO ZERO*/
*/* M RESET IOBFLAG3 FIELD TO ZERO*/
*/* M RESET IOBSENS0 AND IOBSENS1 TO ZERO*/
*/* M RESET IOBCSW TO ZERO*/
*/* M SET IOBERRCT TO SHOW CE/XCE RETRY*/
*/* M RESET IOBECBCC TO X'7F'*/
*/* R RETURN TO IOS+8*/
*/* RECON: M SET RECONSTRUCTION REQUIRED FLAG*/
*/*TSTRDTI: M RESET IOBERRCT FIELD TO ZERO*/
*/* D (NO,,YES,ALLIOBS) WAS COMMAND A READ INITIAL*/
*/* P GET ADDR OF MASTER UCB*/
*/* D (NO,,YES,TSTOLTEP) READ INITIAL PENDING ON LINE GROUP*/
*/* M (,TSTOLTEP) TURN OFF BUSY BIT IN IOB PASSED BY IOS*/
*/*ALLIOBS: P TURN OFF BUSY BIT IN ALL IOB'S*/
*/*TSTOLTEP: D (YES,,NO,TSTOLT) IS OLTEP EXECUTING*/
*/* M TURN ON BUSY BIT IN IOB PASSED BY IOS*/
*/* M SET OLTEP IN CONTROL FLAG*/
*/*RESTREG: R RETURN TO IOS*/
*/*TSTOLT: D (NO,,YES,SCHEDOLT) IS OLT IN PROGRESS*/
*/* D (YES,,NO,RESTREG) SOH%/ MESSAGE*/
*/* P CONVERT TO BTAM RFT*/
*/*SCHEDOLT: P SCHEDULE ON-LINE TEST*/
*/* R RETURN TO IOS+4*/
*/*IGG019PA: END*/
         EJECT
         SPACE 2
*        ESTABLISH ADDRESSABILITY
         SPACE
         USING IOBSTDRD,IOBREG                                LD Y02947
         USING IHADCB,DCBREG
         USING DECB,ECBREG                                    LD Y02947
         USING DEB,DEBREG                                     LD Y02947
         USING *,BASEREG
         STM   ZEROREG,BRREG,ZEROREG(SAVEREG)  SAVE REG       LD Y02947
         LR    BASEREG,BRREG       SET UP BASE REG
         B     BEGIN                 BRANCH AROUND ID         LD Y02947
         DC    AL1(8)                                         LD Y02947
         DC    CL8'IGG019PA'         MODULE ID                LD Y02947
         DC    C'  4322'             DATE LAST CHANCHE      LD @ZA02332
         DC    C' UZ15414'         PTF NUMBER                  @ZA27950
         DS    0H                                           LD @ZA02332
PATCH    DC    10F'0'              PATCH AREA
         SPACE 2
BEGIN    DS    0H
*        ADDRESS CONTROL BLOCKS
         SPACE
LOADECB  L     ECBREG,IOBECBPT     LOAD ADDR OF DECB
         LA    ECBREG,ZERO(ECBREG)  CLEAR HI BYTE              @ZA06371
         LTR   ECBREG,ECBREG       IOBECBPT ZEROES ?           @ZA06371
         BZ    RETURN               YES RETURN                 @ZA06371
         CLI   DECTYPE,ALLON       IS THIS USER'S DECB       LD YA02143
         BNE   DECBOK              YES, CONTINUE             LD YA02143
         L     ECBREG,FOUR(ECBREG) NO, GET @ REAL DECB       LD YA02143
         LA    ECBREG,ZEROS(ECBREG) CLEAR 1ST. BYTE          LD YA02143
DECBOK   EQU   *                   CONTINUE                  LD YA02143
         L     DEBREG,28(DEBREG)   LOAD ADDR OF START OF DEB
         SPACE 2
*        SAVE RESIDUAL COUNT AND STATUS
         SPACE
         MVC   DECCOUNT(2),IOBCSW+5 MOVE INTO DECB - RESIDUAL
         MVC   DECCSWST(2),IOBCSW+3 COUNT AND STATUS BITS OF
*                                   LAST CCW EXECUTED
         CLI   IOBECBCC,X'7E'      IOB COMPLETION CODE A '7E'
         BNE   TESTXCE             NO , GO CHECK FOR ABNORMAL CE
         OI    IOBFLAG1,IOBIOERR   TURN ON IOB EXCEPTION      LD Y02947
         B     RETURN              GO TO ERP'S               LD YA03957
         SPACE 2
*        ABNORMAL CHANNEL END
         SPACE 2
TESTXCE  DS    0H                  CHECK FOR ABNORMAL ENTRY  LD YA03957
         TM    IOBINCAM,OLT        IS OLT IN PROGRESS       LD @ZA02332
         BZ    TESTABNL            NO, GO TEST IOB EXCEPT.  LD @ZA02332
         NI    IOBFLAG1,ALLON-IOBIOERR TURN OFF IOBIOERR... LD @ZA02332
*                                  ...IF IT IS ON           LD @ZA02332
         B     SCHEDOLT            BRANCH TO SCHE IGG019PI  LD @ZA02332
TESTABNL EQU   *                                            LD @ZA02332
         TM    IOBFLAG1,IOBIOERR   ABNORMAL ENTRY            LD YA03957
         BNO   TSTRDTI             NO, GOOD COMPLETION       LD YA03957
         CLI   IOBECBCC,NORMAL     FIRST ENTRY               LD YA03957
         BE    TSTREADY            YES, SEE IF INT. REQ.       @ZA27950
         TM    IOBSTBYT,UNITCHK    UNIT CHECK?               LD YA03957
         BNO   TESTIOB             NO, DON'T UPDATE DECB    LD @ZA02342
         MVC   DECSENS0(ONE),IOBSENS0 YES, UPDATE DECB       LD YA03957
         SPACE 2
*        INTERCEPTED IOB
         SPACE
TESTIOB  EQU   *
         CLI   IOBECBCC,INTRCEPT   WAS THE IOB INTERCEPTED
         BE    TSTRDTI             YES, TEST FOR RD INIT & CONTINUE
         SPACE 2
*        TEST FOR PERMANENT ERROR FOUND BY ERP
         SPACE
         TM    IOBERRCT+1,USERCORR IS ERROR USER CORRECTABLE
         BO    TSTRECON            YES, TEST FOR RECONSTRUCTION
         TM    IOBERRCT+1,USERETRY IS ERROR PERMANENT OR USER RETRYABLE
         BNO   TSTRECON            NO, TEST FOR RECONSTRUCTION
         SPACE 2
*        TEST FOR OPERATIONAL SIO
         SPACE
         TM    IOBSIOCC,OPERSIO    TEST FOR OPERATIONAL SIO
         BNO   TSTRDTI             BRANCH IF OPERATIONAL
         OI    DECERRST,DECDIAGN   SET SIOCC=3 ERROR FL AG    LD Y02947
         B     TSTRDTI             GO TEST FOR READ INITIAL
         SPACE 2
*        TEST FOR READYQ AND DEVICE NOT READY                  @ZA27950
         SPACE 2
TSTREADY TM    IOBSTBYT,UNITCHK    UNIT CHECK STATUS           @ZA27950
         BZ    RETURN              NO, NORMAL ERP PROCESSING   @ZA27950
         USING UCB,UCBREG
         CLI   IOBSENS0,INTVREQ    INTERVENTION REQUIRED?      @ZA27950
         BNE   RETURN              NO, GO TO IOS               @ZA27950
         TM    DCBRDYQ,DCBRDYIQ    READYQ AVAILABLE            @ZA27950
         BZ    RETURN              NO, NORMAL ERP PROCESSING   @ZA27950
         OI    UCBGRAF,UCBDWNR     SET DVC NOT RDY & READYQ    @ZA27950
         B     RETURN              RETURN TO IOS FOR ERP       @ZA27950
         SPACE 2
*        USER CORRECTABLE ERROR - CHECK FOR RECONSTRUCTION OR RETRY
         SPACE
TSTRECON EQU   *
         TM    IOBERRCT+1,NORECON  IS RECONSTRUCTION REQUIRED
         BNO   RECON               YES, SET RECON REQUIRED BIT
         TM    IOBERRCT+1,RTRYREQ  SHOULD REQUEST BE RETRIED
         BM    TSTRDTI             NO, TEST FOR READ INITIAL
         XC    IOBFLAG1(TOSENS1),IOBFLAG1   RESET IOBFLAG1, IOBFLAG2,
*                                           IOBSENS0, IOBSENS1 TO ZERO
         XC    IOBFLAG3(TOCSW),IOBFLAG3     RESET IOBFLAG3, IOBCSW TO 0
         MVI   IOBFLAG1,IOBCMDCH+IOBUNREL                    LD YA03272
         MVI   IOBERRCT+1,RETRY    SET IOBERRCT TO SHOW CE/XCE RETRY
         MVI   IOBECBCC,NORMAL     RESET IOBECBCC TO X'7F'
         LM    ZEROREG,BRREG,ZEROREG(SAVEREG) RESTORE REGS    LD Y02947
         B     8(RETREG)           RETURN TO IOS+8
RECON    EQU   *
         MVI   DECERRST,DECDIAGN   SET RECONSTRUCTION         LD Y02947
*                                      REQUIRED FLAG          LD Y02947
         SPACE 2
*        CHECK FOR READ INITIAL - IF SO, TUR OFF ALL BUSY BITS. IF NOT,
*        TURN OFF BUSY BIT OF IOB PASSED BY IOS
         SPACE 2
TSTRDTI  EQU   *
         XC    IOBERRCT(2),IOBERRCT RESET IOBERRCT TO ZERO
         USING UCB,UCBREG               ADDRESSABILITY
         CLI   UCBRLN,MASTER       IS UCB MASTER UCB FOR LINE
         BE    RLNISONE            YES, BRANCH
         L     UCBMSTR,UCBCTLNK    GET ADDR OF MASTER UCB
         B     BSYTST
RLNISONE LR    UCBMSTR,UCBREG      UCB IS MASTER UCB
         USING MSTRUCB,UCBMSTR     ADDRESSABILITY
BSYTST   EQU   *                                             LD YA02451
*
*              2 LINES DELETED FOR ----->                   LD @ZA02332
*
         CLI   DECTYPE2,RDTI       IS OPERATION RD INITIAL   LD YA02451
         BE    TESTRFT             YES, TEST FOR RFT         LD YA02451
         TM    MTRGCB,RDTIACTV     READ INIT REQ OUTSTANDING?   YA01033
         BO    TSTOLTEP            YES, LEAVE BUSY BIT ON
         NI    IOBINCAM,BUSY       TURN OFF BUSY BIT OF IOB PASSED BY
*                                   IOS
         B     TSTOLTEP            GO TEST FOR OLTEP EXECUTING
ALLIOBS  L     WREG,DCBIOBAD       GET INITIAL IOB BASE
         SR    ICREG,ICREG         CLEAR REG
         IC    ICREG,DEBNMEXT      LOOP COUNT
         SR    ICSREG,ICSREG       CLEAR REG
         IC    ICSREG,DCBEIOBX     = INCREMENT
         LA    BRREG,DEBSUCBA-FOUR GET BEGINNING OF UCB LIST  LD Y02947
NEXTIOB  LA    WREG,0(ICSREG,WREG) GET ADDR OF IOB
         LA    BRREG,FOUR(BRREG)                                YA01031
         L     UCBMSTR,0(BRREG)    GET UCB ADR                  YA01031
         NI    MTRGCB,ALLON-RDTIACTV   SIGNAL END OF READ INIT  YA01045
         TM    6(UCBMSTR),X'E0'    OUTSTANDING I/O?             YA01031
         BNZ   ALLIOBLP                                         YA01031
         NI    INCAM(WREG),BUSY    TURN OF IOB
ALLIOBLP BCT   ICREG,NEXTIOB       GO TO NEXT IOB               YA01031
         SPACE 2
*        TEST FOR OLTEP EXECUTING
         SPACE
TSTOLTEP EQU   *
         TM    UCBGCB,UCBOLTEP     IS OLTEP EXECUTING         LD Y02947
         BNO   RESTREG             NO, RETURN                LD YA02451
         OI    DECFLAGS,DECSTXNQ   TURN ON OLTEP IN CTROL BIT LD Y02947
RESTREG  EQU   *
         CLI   DECTYPE2,RDTI       READ INITIAL ?            LD YA02167
         BE    RETURN              YES, RETURN               LD YA02167
         MVC   LOCALRLN(ONE,ECBREG),RLN(ECBREG)              LD YA02167
*                                  MOVE RLN INTO DECPOLPT    LD YA02167
RETURN   EQU   *                                             LD YA02167
         LM    ZEROREG,BRREG,ZEROREG(SAVEREG) RESTORE REGS    LD Y02947
         BR    RETREG              RETURN TO IOS
         SPACE 2
*        TEST FOR RFT
         SPACE
TESTRFT  EQU   *                                             LD YA02451
         TM    IOBECBCC,X20        PERM. ERROR ?             LD YA02471
         BZ    ALLIOBS             YES, DO NOT CHECK BUFFER  LD YA02471
         L     WREG,DECAREA        LOAD ADDR OF AREA
         CLC   0(3,WREG),SOHMSG    ARE FIRST 3 CHARS SOH%/
         BNE   ALLIOBS             NO, BRANCH                LD YA02451
         TM    DCBERROP,DCBERPT    TERMINAL TESTS SPECIFIED?  LD Y02947
         BZ    ALLIOBS             NO, BRANCH                LD YA02451
         CLC   DECLNGTH(CNTLNGTH),MINBUFF BUFF LONG ENOUGH   LD YA02166
         BL    ALLIOBS             NO, BRANCH                LD YA02451
         MVC   2(10,WREG),4(WREG)  CONVERT TO BTAM RFT
         ST    WREG,IOBERINF            SAVE ADDR OF AREA
         SPACE 2
*        SCHEDULE ON LINE TEST
         SPACE
SCHEDOLT EQU   *
         L     UCBREG,DEBOFLGS     GET ADDR OF PTR TO IRB
         L     UCBREG,0(UCBREG)    GET OLT IRB ADDR
         L     ICREG,96(UCBREG)    LOAD 1ST IQE ADDR
         MVC   96(4,UCBREG),0(ICREG) DEQUEUE
         XC    0(4,ICREG),0(ICREG) ZERO LINK FIELD
         ST    IOBREG,4(ICREG)     STORE IOB ADDR AS PARM
         LA    ICREG,0(ICREG)      CLEAR HIGH ORDER BYTE
         LCR   IQEREG,ICREG             COMPLEMENT ADDR & PUT IN REG1
         L     BRREG,SIXT          LOAD CVT ADDR
         L     BRREG,CVT0EF00(BRREG)  LOAD ASYNCH RT ADDR
         BALR  RETREG,BRREG        LINK TO SCHEDULE ROUTINE
         LM    ZEROREG,BRREG,ZEROREG(SAVEREG) RESTORE REGS    LD Y02947
         B     4(RETREG)           RETURN TO IOS+4
         SPACE 2
*        REGISTER EQUATES
         SPACE
ZERO     EQU   0                                               @ZA06371
ZEROREG  EQU   0
RQEREG   EQU   1                   ADDR OF RQE ASSOC WITH I/O REQUEST
IOBREG   EQU   2                   ADDR OF IOB
DEBREG   EQU   3                   ADDR OF DEB
DCBREG   EQU   4                   ADDR OF DCB
ECBREG   EQU   5                   ECB ADDR
UNITADRG EQU   6                   UNIT ADDR OF DVC I/O REQUEST WAS FOR
UCBREG   EQU   7                   UCB ADDR
ICSREG   EQU   8                   WORK REG
ICREG    EQU   9                   WORK REGISTER
WREG     EQU   10                  WORK REG
UCBMSTR  EQU   11                  ADDR OF MASTER UCB
BASEREG  EQU   12                  BASE REGISTER
SAVEREG  EQU   13                  SAVEAREA REG               LD Y02947
RETREG   EQU   14                  RETURN ADDR TO IOS
BRREG    EQU   15                  ENTRY POINT ADDRESS
IQEREG   EQU   1                        ADDR OF IQE
         SPACE
         SPACE
*        CONTROL BLOCK BIT SETTINGS
         SPACE
ZEROS    EQU   X'00'               BYTE OF ZEROS
ALLON    EQU   X'FF'               BYTE OF ONES                 YA01033
RECONSTR EQU   X'02'               RECONSTRUCT BUFFER IMAGE BIT
INTRCEPT EQU   X'44'               COMPLETION CODE - IOB INTERCEPTED
OPERSIO  EQU   X'30'               OPERATIONAL SIO CODE
RETRY    EQU   X'05'               RETRY CHAN PGM SETTING
ERP      EQU   X'02'               ERP HAS BEEN ENTERED BIT
ERROR    EQU   X'41'               PERMANENT ERROR COMPLETION CODE
USERCORR EQU   X'04'               USER CORRECTABLE BIT
USERETRY EQU   X'01'               PERMANENT OR USER RETRYABLE
*                                   ERROR BIT
X20      EQU   X'20'               IOB PERM. ERROR BIT       LD YA02471
NORECON  EQU   X'05'               RECONSTRUCTION NOT REQUIRED SETTING
RTRYREQ  EQU   X'07'               RETRY CHAN PGM SETTING
NORMAL   EQU   X'7F'               NORMAL COMPLETION CODE
RDTI     EQU   X'01'               VALUE OF READ INITIAL
BUSY     EQU   X'FF'-X'40'         BUSY BIT
MASTER   EQU   X'01'               MASTER UCB BIT
RDTIACTV EQU   X'08'               READ INIT OUTSTANDING FLG    YA01033
SIXT     EQU   X'10'               CVT ADDR
OLT      EQU   X'01'               OLT IN PROGRESS BIT
*                                  IE. TERM TESTS SPECIFIED     YA00811
UNITCHK  EQU   X'02'               UNIT CHECK BIT            LD YA03957
INTVREQ  EQU   X'40'               INTERVENTION REQUIRED       @ZA27950
         SPACE
         SPACE
*        DISPLACEMENTS EQUATES
         SPACE
TOSENS1  EQU   4                   LENGTH OF ZERO-OUT FOR RETRY
FOUR     EQU   4                   DISP INTO IOS                YA01031
ONE      EQU   1                                             LD YA02167
LOCALRLN EQU   39                  DSP OF DECPOLPT LOCAL RLN LD YA02167
RLN      EQU   25                  DSP OF DECB RLN FIELD     LD YA02167
TOCSW    EQU   8                   LENGTH OF ZERO-OUT
INCAM    EQU   28                  DISP OF IOBINCAM
CVT0EF00 EQU   4                   OFFSET TO ASYNCH SCHEDULER
CNTLNGTH EQU   2                                             LD YA02166
         SPACE 2
*        STORAGE
         SPACE
SOHMSG   DC    X'016C61'           SOH HEADER SEQUENCE
MINBUFF  DC    X'012C'             MIN BUFF LENGTH FOR RFT   LD YA02166
         EJECT
         DCBD DSORG=BX,DEVD=BS
         EJECT
         IEZDEB LIST=YES
         EJECT
         IHADECB
         EJECT
         IEZIOB
         EJECT
UCB      DSECT
         IEFUCBOB LIST=YES
         EJECT
MSTRUCB  DSECT
         ORG   MSTRUCB+24
         DS    H
MTRATNCT DS    X
MTRGCB   DS    X
         DS    2F
MTRRLN   DS    X
         SPACE 5
         END
