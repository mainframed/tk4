         TITLE 'IGE0504A - BTAM ERP ERROR POST MODULE'
IGE0504A CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0504A                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM ERP ERROR POST                                              *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    PERFORMS ERROR MESSAGE WRITING AND MESSAGE CONSTRUCTION FOR ALL  *
*    BTAM ERP ROUTINES.  CHECKS RESULTS OF DIAGNOSTIC WRITE/READ      *
*    2701 ADAPTER UNIT. POSTS COMPLETE WITH ERROR.                    *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (84 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        840 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE,                 *
*        LOCAL LOCK HELD ACCROSS POST CALL                            *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0504A                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        SET DECERRST FOR ERROR STATUS, ENABLE FAILED, DIAG RD/WRT    *
*        FAILED, DISABLE FAILED.                                      *
*        ALTERED CHANNEL PROGRAM.                                     *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        SETLOCK                                                      *
*        POST (IEA0PT01)                                              *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*        PSA          (MAPPED BY IHAPSA)                              *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    SETLOCK                                                          *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YA02437  (11/1/73)                                               *
*    YA02455  (11/1/73)                                               *
*    YM05652K (1/25/74)                                               *
*    ZA02844 (03/03/75)          ZA03591 (03/24/75)                   *
*    AZ04855 (09/24/75)          AZ07659  (11/25/75)                  *
*    AZ10051 (05/17/76)                                               *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         B     START
         DC    C'IGE0504A'       EYE CATCHER
         DC    C'** MVS *'
         DC    C'&SYSDATE'      DATE LAST ASSEMBLED
         DS    0H
START    L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         L     UCBREG,IOSUCB       BASE UCB
         DROP  ERREG5
         L     CCWREG,IOBFL3       ADDRESS OF CCW
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       ADJUST TO FAILING CCW
         XC    IOBRESTR(L4),IOBRESTR   CLR XTENDED MSG AREA
         MVI   IOBSENS1,ZERO
         MVI   IOSSNS+L1,FOXFOX
         SR    ERREG1,ERREG1       INITIALIZE WORK
         LA    ERRLNK,L1
         IC    ERREG1,IOBERRCT+L1   PICK UP ENTRY INDEX
         SRL   ERREG1,L4           ADJUST INDEX VALUE
         B     ERRPST1(ERREG1)     BRANCH VIA BRANCH TQBLE
ERRPST1  B     ERRPST              NORMAL POST
         B     ERRPST              TERM ID BYPASS
         B     ERRPST6             DISABLE RESTORE
         NI    L5(CCWREG),MASK2    SETUP TO DETERMINE FAILING CCW
         IC    ERREG1,L5(CCWREG)   INDEX VALUE
         B     ERRPST2(ERREG1)     ENTRY TO ERROR ROUTINE
ERRPST2  B     ERRPST3             DISABLE FAILED
         B     ERRPST4             DIAG WRITE FAILED
         B     ERRPST5             DIAG READ FAILED
         CLI   IOBCSW+L3,CSWCDE    CHAN AND DEV END ONLY
         BNE   ERRPST21            NO INDICATE ERROR
         TM    IOBCSW+L4,FOXFOX    CHECK SECOND STATUS BYTE
         BZ    ERRPST22            NO ERROR DETECTED
ERRPST21 OI    DECERRST(DECBRG),ERRENB   INDICATE ENABLE FAILED
ERRPST22 EQU   *
         MVC   IOBSENS0(L1),IOBERINF   RESTORE SENSE BYTE
         MVC   IOBCSW(L7),IOBERINF+L1   RESTORE 7 LOW
*                                  ORDER BYTES OF CHANNEL STATUS WORD
         L     CCWREG,IOBFL3       RESTORE CCW POINTER
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       ADJUST TO FAILING CCW
         MVC   ZERO(L8,CCWREG),IOBERINF+L8   RESTORE FAILING CCW
ERRPST   TM    UCBTYP+L1(UCBREG),SWLINE   IS THIS A SWITCHED LINE
         BNZ   ERRPST03            YES NO TERM ID'S REQUIRED
         TM    DECTYPE+L1(DECBRG),READ   IS THIS READ OPERATION
         BO    RDTYPE              YES, BRANCH
         L     ERREG4,DECADRPT(DECBRG)   ADDRESS OF CHARS
         B     ERRDATA
RDTYPE   TM    UCBTYP+L1(UCBREG),APOLL   IS OPERATION AUTOPOLL
         BO    POLTYPE             YES, BRANCH
         L     ERREG4,DECPOLPT(DECBRG)   ADDRESS OF POLL CHAR
         B     ERRDATA
POLTYPE  L     ERREG4,DECENTRY(DECBRG)   ADDRESS OF POLL CHAR
ERRDATA  MVC   IOBWARK(L4),UCBTYP(UCBREG)   MOVE DEVICE DATA
         NI    IOBWARK+L3,MASK3    CLEAR UNWANTED BITS
         NI    IOBWARK,MASK2       CLEAR UNWANTED BITS
         CLI   IOBWARK+L3,ADAPT5   CHECK FOR TWX ADAPTER
         BL    ERRPST11            NO, NOT TWX OR ABOVE
ERRPST05 CLI   IOBWARK+L3,ADAPT8   THIS A 2260
         BNE   ERRPST03            NO, NO ID NEEDED
         CLI   IOBCPA+L13,X03      IS THIS WR ADDR/POLL CCW
         BNE   ERRPST02            NO, USE DECENTRY ADDR
         L     ERREG4,IOBCPA+L8    PICK UP ADDRESS FROM CCW
         B     ERRPST02            MOVE ID TO MESSAGE
ERRPST11 CLI   IOBERRCT+L1,X40     SUPPRESS ID IN MESSAGE
         BE    ERRPST03            YES, NO ID IN MESSAGE
         CLI   IOBWARK,X09         IS THIS 2740B
         BE    A2740               YES, TREAT AS 2740
         CLI   IOBWARK,MODTYP40    THIS A 2740
         BNE   ERRPST04            NO CONTINUE CHECKING
A2740    EQU   *
         TM    IOBWARK+L1,STACTRL   DOES 2740 HAVE STATION CONTROL
         BZ    ERRPST03            NO TERM ID NOT REQUIRED
ERRPST01 BCTR  ERRLNK,ZERO         REDUCE REG FOR MOVE
ERRPST02 EX    ERRLNK,ERRMOVE      MOVE TERM ID INTO MSG
ERRPST03 OI    IOBFLAG2,IOBRDHA0   INDICATE EXTENDED MSG TO WTO
         MVC   IOBRESTR(L1),L5(CCWREG)   PUT TP OP CODE IN MSG
         L     DCBREG,IOBDCBPT     PICK UP POINTER TO DCB
         L     ERREG2,IOBSTART     CLEAR 1ST BYTE OF REG
         CR    CCWREG,ERREG2       IS THIS FIRST CCW
         BNE   ERRPST06            NO, BRANCH
         TM    IOBRESTR,X21        IS THIS WRITE RESET
         BO    ERRPST07            YES, BRANCH
ERRPST06 EQU   *
         LA    CCWREG,IOBCPA       POINT TO FIRST CCW
         TM    DCBBFTEK(DCBREG),X08   DYNAMIC BUFFERING
         BNO   ERRWTO              NO, NORMAL POST
         TM    IOBERINF+L13,X30    ERROR PRIOR TO TEXT
         BNZ   ERRWTO              NO, DON'T POST BUFFER
         TM    IOBRESTR,X11        IS TP OP CODE RD/WR TEXT
         BO    ERRWTO              YES, DON'T POST BUFFER
ERRPST07 SR    ERREG2,ERREG2       CLEAR REGISTER
         LA    IOBRG,ZERO(IOBRG)   CLEAR HI-ORDER BYTE
         IC    ERREG2,DCBIOBSZ(DCBREG)   PICK UP IOB SIZE
         AR    ERREG2,IOBRG        CALCULATE IOB END
ERRPST9B CR    ERREG2,CCWREG       ARE WE IN IOB
         BNH   ERRWTO              NO, NORMAL POST
         TM    L5(CCWREG),X11      IS THIS READ OR WRITE DATA
         BO    ERRPST9A            YES, POST THE BUFFER
         TM    L5(CCWREG),LASTCCW  IS THIS THE LAST CCW?       @ZA07659
         BO    ERRWTO              YES, DON'T POST THE BUFFER  @ZA07659
         LA    CCWREG,L8(CCWREG)   POINT TO NEXT CCW
         B     ERRPST9B            CHECK IT
ERRPST9A L     ERREG4,ZERO(CCWREG)   PICK UP DATA ADDRESS
         N     ERREG4,MASKDB       AND TO FULL WORD BOUNDARY
         SH    ERREG4,ERRCN4       BACK UP TO LINK FIELD
         LTR   ERREG4,ERREG4       TEST FOR ZERO ADDRESS
         BNP   ERRWTO              BRANCH IF YES
         TM    ZERO(ERREG4),X40    IS BUFFER POSTED
         BO    ERRWTO              YES, DON'T POST AGAIN
         STM   ERREG3,ERREG5,EWAIAREA   SAVE REGS 0-15
GET      SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,RELATED=(LOCAL,IGE0504A(X
               REL))               GET LOCAL LOCK
         TM    DECSDECB(DECBRG),X'80'  DECB WAITING?        L5 @ZA02844
         BO    REL                     YES,DONT POST        L5 @ZA02844
         L     ERREG4,EWAIAREA+L44   GET BFR ADDRESS        L5 @ZA02844
         L     IOBRG,0(ERREG4)         SET NXT BFR ADDR     L5 @ZA02844
         LA    IOBRG,0(IOBRG)          CLR HI BYTE          L5 @ZA02844
         O     IOBRG,POSTCODE      INSERT COMP CODE
         LA    ERRLNK,L1           GET A ONE
         SLL   ERRLNK,L31          MOVE TO SIGN POSITION
         OR    ERREG4,ERRLNK       POST WITH ECB
         SR    ERRLNK,ERRLNK       MEMORY RELATED POST
         L     NAMEREG,IOSSRB      POINT TO SRB
         USING SRB,NAMEREG
         L     NAMEREG,SRBASCB     POINT TO ASCB
         DROP  NAMEREG
         L     ERREG5,CVTPTR       POINT TO CVT
         USING CVTMAP,ERREG5
         L     ERREG5,CVT0PT01     ADDRESS OF BRANCH ENTRY
         DROP  ERREG5
         BALR  ERRETR,ERREG5       LINK TO POST BUFFER
REL      SETLOCK RELEASE,TYPE=LOCAL,RELATED=(LOCAL,IGE0504A(GET))
*                                  GET RID OF LOCAL LOCK
         LM    ERREG3,ERREG5,EWAIAREA   RESTORE REGS 0-15
ERRWTO   EQU   *
         OI    IOSFLB,IOSLOG       INDICATE OBR REQUIRED    L5 @ZA03591
         MVI   EWAIND1,EWAIWTOL    TELL 904B TO GO TO IOS WTO  YM05652K
         L     NAMEREG,TRANSRTN    NAME OF TRANSLATE RTN       YM02594K
XCTLOUT  EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE
         CLI   DECERRST(DECBRG),SNO     SHOULD NOT OCCUR       @ZA10051
         BNER  ERRETR              NO, GO TO XCTL              @ZA10051
         TM    IOBSENS0,LDORDC     LOST DATA OR DATA CHECK     @ZA10051
         BZR   ERRETR              NO, GO TO XCTL              @ZA10051
         NI    IOSFLA,L255-IOSERR TURN OFF ERP IN CNTL IN IOSB @ZA10051
         NI    IOBFLAG1,L255-IOBERRTN    ALSO IN IOB           @ZA10051
         DROP  ERRETR
         BR    ERRETR              XCTL
ERRPST04 CLI   IOBWARK+L3,ADAPT2   THIS A 1030
         BE    ERRPST01            YES REDUCE MOVE COUNT
         B     ERRPST02            NO MOVE COUNT CORRECT
ERRPST5  MVC   IOBWARK+L3(L1),UCBTYP+L3(UCBREG)   ADAPTER TYPE BYTE
         NI    IOBWARK+L3,MASK3   CLEAR UNWANTED BITS
         CLI   IOBWARK+L3,ADAPT4   CHECK FOR TELEGRAPH ADAPTER -
*                                 TYPE I
         BNE   ERRPST52           NO ALL ERRORS VALID
         CLI   IOBCSW+L3,CSWCDEE   CHECK FOR CHANNEL END - DEVICE
*                                  END - UNIT EXCEPTION STATUS
         BNE   ERRPST4             NO INDICATE FAILURE
ERRPST51 TM    IOBCSW+L4,FOXFOX    ANY OTHER ERRORS
         BNZ   ERRPST4             INDICATE DIAG W/R FAILED
         B     ERRPST41            NORMAL COMPLETION
ERRPST52 TM    L4(CCWREG),CHAINCDE   THIS COMMAND CHAINED
         BO    ERRPST4             INDICATE FAILURE
         TM    IOBCSW+L3,CSWCDE    CHAN AND DEV END
         BC    12,ERRPST4          INDICATE FAILURE
         TM    IOBCSW+L3,L255-CSWCDE   ANY ERRORS
         BZ    ERRPST51            NO ERRORS CHECK FURTHER
ERRPST4  OI    DECERRST(DECBRG),ERRDIAG   INDICATE DIAG W/R FAILED
ERRPST41 MVC   IOBSENS1(L1),IOBSENS0   SAVE DIAGNOSTIC
*                                  WRITE/READ SENSE BYTE IN SECOND BYTE
         MVI   IOBSENS0,ZERO       CLEAR FIRST SENSE BYTE
         TM    IOBCPA+L20,CHAINCDE   ENABLE REQUIRED
         BC   12,ERRPST22          NO GO TO WTO AND POST MSG
         LA    ERREG1,IOBCPA+L24   ADDRESS OF CCW
ERRPST42 ST    ERREG1,IOBSTART     LOCATION OF POINTER TO CCW
RETRY    EQU   *
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
         L     NAMEREG,TRANSRTN    LOAD NAME
         B     XCTLOUT             XCTL
ERRPST3  OI    DECERRST(DECBRG),ERRENB   INDICATE DISABLE FAILED
         NI    DECERRST(DECBRG),L255-DISABLOK   TURN OFF DISABLE SW
         LA    ERREG1,IOBCPA+L8    ADDR OF DIAG WRITE CCW
         B     ERRPST42            EXECUTE DIAG WRITE
ERRPST6  CLI   IOBCPA,RSET2848     2848 RESET CHANNEL PROG
         BE    ERRPST7             YES  BRANCH
         NI    IOSFLA,L255-IOSERR TRN OFF ERP IN CNTRL IN IOSB @ZA04855
         NI    IOBFLAG1,L255-IOBERRTN    ALSO IN IOB           @ZA04855
         CLI   IOBCSW+L3,CSWCDE    CHAN AND DEVICE END ONLY
         BE    ERRPST22            YES CONTINUE FLOW
         NI    DECERRST(DECBRG),L255-DISABLOK   INCICATE DISABLE FAIL
         OI    DECERRST(DECBRG),ERRENB   MAKE IT POSITIVE
         B     ERRPST22            POST OPERATION COMPLETE
ERRPST7  CLI   ZERO(CCWREG),CCWBREAK   INTERRUPT CAUSED BY BRK CCW
         BNE   ERRPST71            NO  CAUSED BY READ SKIP
         MVC   IOBSENS0(L1),IOBERINF+L8   RESTORE SENSE
         MVC   IOBCSW(L7),IOBERINF+L9   RESTORE CSW
         MVC   IOBCPA(L16),IOBERCCW
         L     ERREG4,DECENTRY(DECBRG)   ADDR OF TERM ID/POLL CHARS
         B     ERRPST02            POST WITH ERROR
ERRPST71 MVC   IOBSTART+L1(L3),IOBCSW   SET FOR RESTART
         B     RETRY               EXIT CODE
         EJECT
**********
*
*        EXECUTED INSTRUCTIONS
*
**********
ERRMOVE  EQU   *
         MVC   IOBRSTRB+L1(ZERO),ZERO(ERREG4)   MOVE RESTART ADDR
**********
*
*        CONSTANTS
*
**********
TRANSRTN DC    F'9042'             BTAM TRANSLATE ROUTINE NAME
POSTCODE DC    X'41000000'
MASKDB   DC    X'00FFFFFC'
ERRCN4   DC    H'4'
ERRCN8   DC    H'8'
PACHAREA DC    21C'0000'           PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
DCBIOBSZ EQU   X'24'               IOB SIZE IN DCB
CCWBREAK EQU   X'41'
RSET2848 EQU   X'42'
ADAPT2   EQU   X'20'
DCBBFTEK EQU   X'20'
LASTCCW  EQU   X'80'               FLAG FOR LAST CCW           @ZA07659
ADAPT4   EQU   X'40'
SNO      EQU   X'20'           SHOULD NOT OCCUR BIT            @ZA10051
LDORDC   EQU   X'0A'       LOST DATA OR DATA CHECK             @ZA10051
DECTYPE  EQU   4
DECADRPT EQU   32
DECPOLPT EQU   36
READ     EQU   X'01'
APOLL    EQU   X'40'
ADAPT5   EQU   X'50'
ADAPT8   EQU   X'80'
CHAINCDE EQU   X'60'
CSWCDE   EQU   X'0C'               CHANNEL AND DEVICE END
CSWCDEE  EQU   X'0D'
DECENTRY EQU   X'14'
DECSDECB EQU   0                                            L5 @ZA02844
DECERRST EQU   X'1D'
DISABLOK EQU   X'08'
ERRDIAG  EQU   X'10'
ERRENB   EQU   X'20'
EXCPER   EQU   15                  EXCP ERROR ROUTINE
L255     EQU   255                 AND MASK
FOXFOX   EQU   X'FF'
X09      EQU   X'09'
X03      EQU   X'03'
X40      EQU   X'40'
X21      EQU   X'21'
X08      EQU   X'08'
X30      EQU   X'30'
X11      EQU   X'11'
ZERO     EQU   0                   LENGTH AND DISPLACEMENT
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L9       EQU   9                   LENGTH AND DISPLACEMENT
L13      EQU   13                  LENGTH AND DISPLACEMENT
L16      EQU   16                  LENGTH AND DISPLACEMENT
L20      EQU   20                  LENGTH AND DISPLACEMENT
L24      EQU   24                  LENGTH AND DISPLACEMENT
L31      EQU   31                  LENGTH AND DISPLACEMENT
L44      EQU   44                  LENGTH AND DISPLACEMENT
MASK2    EQU   X'0F'
MASK3    EQU   X'F0'
MODTYP40 EQU   X'04'
STACTRL  EQU   X'08'
SWLINE   EQU   X'90'
UCBTYP   EQU   16                  DEVICE TYPE CODE
**********
*
*        REGISTER DEFINITION
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
ERREG2   EQU   2                   WORK REG
EWAREG   EQU   3                   ERP WORKAREA BASE
CCWREG   EQU   4                   CCW ADDRESS REG
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE
BASEREG  EQU   8                   PROGRAM BASE
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE/PARAMETER REG
ERREG4   EQU   11                  WORK REG/PARAMETER REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  RETURN REG
ERREG5   EQU   15                  LINK REG
         EJECT
         IHAPSA
         EJECT
         IECDRQE
         EJECT
         IEZIOB
IOBWARK  EQU   IOBWORK+3                                       YM05652K
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IHASRB
         EJECT
         IECDIOSB
         END
