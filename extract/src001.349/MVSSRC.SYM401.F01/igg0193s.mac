 TITLE 'IGG0193S  BTAM LINE GROUP OPEN EXECUTOR - LOAD 4'
***********************************************************************
*                                                                     *
* MODULE NAME: IGG0193S   (OS/VS)                                     *
*                                                                     *
* DESCRIPTIVE NAME: BTAM LINE GROUP OPEN EXECUTOR - LOAD 4            *
*                                                                     *
* COPYRIGHT: NONE                                                     *
*                                                                     *
* STATUS: RELEASE 2                                                   *
*                                                                     *
* FUNCTION/OPERATION: THIS ROUTINE IS BROUGHT INTO CORE AND GIVEN     *
*  CONTROL WHEN OPEN EXECUTOR LOAD3 FOR TELECOMMUNICATIONS ISSUES     *
*  'XCTL' TO IGG0193S.                                                *
*  THIS ROUTINE LOADS THE REQUIRED OPEN TELECOMMUNICATION MODULES,AND *
*  STORES THE ID OF THE MODULES AND A COUNT OF MODULES LOADED, INTO   *
*  THE DEB AND SETS UP TERMINAL TEST MASK IF REQUIRED.                *
*                                                                     *
* ENTRY POINT: ENTRY WILL BE TO THE FIRST BYTE OF THE MODULE BY AN    *
*  'XCTL' TO IGG0193S THE FOURTH BTAM LOAD.                           *
*                                                                     *
* INPUT:  THE REGISTERS 5,6,7,8 ARE THE INPUT AS FOLLOWS,             *
*       5 -   ADDRESS OF THE FIRST ENTRY IN THE COPIED DCB PARAMETER  *
*             LIST,                                                   *
*       6 -   BEGINNING OF THE WHERE-TO-GO TABLE                      *
*       7 -   ADDRESS OF THE CURRENT ENTRY IN THE COPIED DCB          *
*             PARAMETER LIST,                                         *
*       8 -   ADDRESS OF THE CURRENT ENTRY IN THE WHERE-TO-GO TABLE   *
*                                                                     *
* OUTPUT: THE REGISTERS 7,8 WILL BE POSITIONED TO THE NEXT UNFINISHED *
*  ENTRY, AND THE REQUIRED BTAM MODULES WILL BE LOADED INTO CORE.     *
*                                                                     *
* EXTERNAL ROUTINES: NONE                                             *
*                                                                     *
* EXIT-NORMAL: THIS ROUTINE TERMINATES BY LOCATING THE NEXT NON-ZERO  *
*  ENTRY IN THE WHERE-TO-GO TABLE AND EXECUTING AN XCTL TO THAT       *
*  ROUTINE.                                                           *
*                                                                     *
* EXIT-ERRORS:                                                        *
*     -ERRORS-  THIS ROUTINE EXITS VIA ABEND FOR THE FOLLOWING        *
*   ERROR CONDITIONS-                                                 *
*   ABEND CODE ERROR CONDITION                                        *
*   00097000   THE CURRENT OPEN REQUIRES AN ADDITIONAL ENTRY IN       *
*              THE DEVICE I/O DIRECTORY.                              *
*                                                                     *
* TABLES/WORK AREAS: 1.DCB PARAMETER LIST CONTAINS THE ADDRESS OF EACH*
*  DCB SPECIFIED IN THE OPEN MACRO INSTRUCTION. 2. THE WHERE-TO-GO    *
*  TABLE CONTAINS THE ID AND TTR OF THE ROUTINE NEEDED TO PROCESS THE *
*  DCB WHICH CORRESPONDS TO THIS ENTRY. THE CHNIDTBL CONTAINS THE     *
*  IDS NEEDED FOR WHERE-TO-GO TABLE AND LOAD OF REQUIRED OPEN MODULES.*
*                                                                     *
* ATTRIBUTES: THIS ROUTINE IS EXECUTED IN THE DATA MANAGEMENT KEY OF  *
*       5 AS ENABLED, PRIVILEGED, AND REENTRANT.                      *
*                                                                     *
* NOTES:  THIS ROUTINE COMPLETES THE OPEN PROCESSING FOR              *
*  TELECOMMUNICATION SO THE WHERE-TO-GO TABLE ENTRY WILL BE SET TO 0. *
*  THE COPIED DCB WILL BE COPIED TO THE USER'S DCB TO REFLECT ALL     *
*  CHANGES MADE BY THE BTAM EXECUTORS.                                *
*                                                                     *
* CHANGE ACTIVITY AS FOLLOWS:                                         *
*            APARS                           PTMS                     *
*       YA02171  (10/5/73)               YM4057  (10/3/73) NO CODE    *
*       ZA00531  (10/18/74)              YM4072  (10/24/73)           *
*       ZA00541  (11/18/74)              YM4091  (11/5/73)            *
*       ZA02349  (02/21/75)              YM5661  (12/11/73)           *
*       ZA03594  (10/03/75)              YM5663  (12/11/73) PROLOG    *
*       ZA06376  (10/03/75)              YM5668  (12/14/73) IECRES    *
*       ZA08768  (04/15/76)              YM5674  (1/3/73)             *
*       ZA11422  (06/24/76)                                           *
*       ZA11446  (10/05/76)                                           *
*       ZA14073  (03/22/77)                                           *
*       ZA17710  (04/28/77)                                           *
*       ZA33143  (07/23/77)                                           *
***********************************************************************
         EJECT
IGG0193S CSECT
         SPACE 5
RE       EQU   0                       WORK/PARAMREG
RF       EQU   1                       WORK/PARAMREG
RDCB     EQU   2                       COPIED DCB ADDRESS        Y02947
ENDADR   EQU   2                       END ADDR FOR FIX LIST     Y02947
ASCBFLAG EQU   RDCB                                         L5 @ZA02349
RBASE    EQU   3                       BASE REGISTER
RCORE    EQU   4                       IOB ADDRESS
PSAREG   EQU   4                       ADDR OF PSA            LD YM7707
ASCBREG  EQU   4                       ADDR OF ASCB           LD YM7707
RPAR     EQU   5                       START OF COPIED DCB       Y02947
*                                      PARAMETER LIST            Y02947
RWTG     EQU   6                       START OF WTG
RPARC    EQU   7                       CURRENT ENTRY IN COPIED   Y02947
*                                      DCB PARAMETER LIST        Y02947
RWTGC    EQU   8                       CURRENT ENTRY IN WTG TABLE
RTIOT    EQU   9                       TIOT ADDRESS
RUCB     EQU   10                      CURRENT UCB
RDEB     EQU   11                      DEB ADDRESS
RB       EQU   12                      WORK REG
ECBREG   EQU   12                      ADDR OF ECB            LD YM7707
TCBREG   EQU   13                      TCB ADDRESS            LD YM7707
RC       EQU   13                      WORK REG
RD       EQU   14                      WORK/PARAMREG
RJ       EQU   15                      WORK/PARAMREG
*    *    *    *    *    *    *    *    *    *    *    *    *    *    *
         USING IHADCB,RDCB
         USING IECTDEB,RDEB
         USING WTG,RWTG                 WHERE-TO-GO TABLE        Y02947
*                                           ADDRESSABILITY       Y02947
         EJECT
         BALR  RBASE,0
         USING *,RBASE
         SPACE 4                                                 Y02947
         B     AROUND                   BRANCH AROUND ID         Y02947
         DC    CL8'IGG0193S'            OPEN ID                  Y02947
         DC    CL8'** MVS *'
         DC    CL8'&SYSDATE'         DATE LAST ASSEMBLY        @ZA00541
         DC    XL3'019825'             CURRENT PTF NUMBER
PATCH    DC    XL50'00'                 PATCH AREA               Y02947
         DS    0H
         SPACE 4                                                 Y02947
AROUND   L     RDCB,0(RPARC)            COPIED DCB ADDRESS       Y02947
         L     RDEB,DCBDEBAD            LOAD DEB ADDRESS           000D
         L     RDEB,28(RDEB)            LOAD DEB START FOR DSECT   000D
         LA    RB,DEBUCBAD             LOAD UCB ADDRESS          A30780
         SR    RUCB,RUCB               CLEAR REGISTER            A30780
         IC    RUCB,DEBNMEXT           LOAD NO EXTENTS(UCB'S)    A30780
         SLL   RUCB,2                                            A30780
         AR    RUCB,RB                 ADD UCB SECTION SIZE      A30780
         SR    RTIOT,RTIOT             CLEAR REGISTER            A30780
         STCM  RUCB,SEVEN,DCBREAD+DISP1      SAVE ACCESS METHOD  Y02947
*                                            ADDRESS             Y02947
         IC    RTIOT,DEBAMLNG          LOAD ACCESS METHOD SIZE   A30780
         AR    RUCB,RTIOT              ADD TO GET SUB-ID AREA    A30780
         SR    RTIOT,RTIOT              CLEAR REGISTER             000E
         L     RCORE,DEBIRBAD-1         GET ADDRESS OF POINTERS    000E
         TM    DCBERROP,TRMTST          TEST FOR ONLINE-TEST       000D
         BO    ONLTST1                  IF SO, BRANCH TO SET UP    000E
         SPACE 1
***********************************************************************
*                                                                     *
*              2740 CODES                                             *
*        C-CHECKING                                                   *
*        D-AUTODIAL                                                   *
*        A-AUTO ANSWER                                                *
*        S-STATION CONTROL                                            *
*        P-AUTO POLL                                                  *
*        T-TRANSMIT CONTROL                                           *
*                                                                     *
***********************************************************************
         SPACE 1
TST2741  CLI   DCBDEVTP,INTR1      2741 ?                   LD @ZA00541
         BE    INTERUPT                                            000I
         CLI   DCBDEVTP,INTR2      2741 ?                   LD @ZA00541
         BNE   GRAPHICS            NO BRANCH                   @ZA33143
*              FOUR LINES OF CODE DELETED BY OZ33143           @ZA33143
INTERUPT LA    RB,BREAK41                                          000I
         BAL   RC,LOADROUT                                         000I
         BAL   RD,BLDIRB                                           000I
         SPACE 1                                                 Y02947
         MODESET EXTKEY=ZERO           KEY OF ZERO               Y02947
         SPACE 1                                                 Y02947
         ST    RF,8(RCORE)                                         000I
         SPACE 1                                                 Y02947
         MODESET EXTKEY=DATAMGT        DATAMGT KEY               Y02947
         SPACE 2
GRAPHICS LA    RJ,DEBUCBAD         GET UCB TABLE ADDRESS           000E
         L     RJ,0(RJ)            GET UCB ADDRESS                 000E
         TM    19(RJ),X'90'        THIS DEVICE BISYNCH             000E
         BNO   LOADPCI             IF NOT, LOAD PCI APPEND         000E
         TM    DCBXCODE,GRAPHMSK   TEST FOR 1130/2250              000E
         BO    GRAPH1                   IF SO, BRANCH TO SET UP    000E
         B     LOADPCI                  GO LOAD PCI APPENDAGE      000E
INITIQE  SRL   RTIOT,1                  REDUCE TO NMBR OF EXTENTS  000E
         SPACE 1                                                 Y02947
         MODESET EXTKEY=ZERO            KEY OF ZERO              Y02947
         SPACE 1                                                 Y02947
         LA    RB,96(RF)                LOAD ADRS OF IQE POINTER   000D
         LA    RC,4(RB)                 START IQE'S IN NEXT WORD   000D
         L     RE,DEBTCBAD-1            LOAD ADDRESS OF TCB
IQEINIT  ST    RC,0(RB)                 STORE ADDRESS OF NEXT IQE  000D
         ST    RF,8(RC)                 STORE IRB ADDRESS IN IQE   000D
         ST    RE,12(RC)                STORE TCB ADDRESS
         MVI   12(RC),X'00'             ZERO OUT HIGH BYTE        23883
         LR    RB,RC                    SAVE ADDRESS OF THIS IQE   000D
         LA    RC,16(RC)                STEP TO NEXT               000D
         BCT   RTIOT,IQEINIT            DECREMENT AND TEST         000D
         ST    RTIOT,0(RB)              ZERO LAST POINTER          000D
         SPACE 1                                                 Y02947
         MODESET EXTKEY=DATAMGT         DATAMGT KEY              Y02947
         SPACE 1                                                 Y02947
         BR    RJ                       BRANCH                     000E
ONLTST1  IC    RTIOT,DCBDEVTP           LOAD DEVICE TYPE           000D
         LM    RB,RC,ONLTTMSK           LOAD ONLTST MASK           000D
         SLDL  RB,0(RTIOT)              SHIFT TO DEVICE BIT        000D
         LTR   RB,RB                    TEST FOR ONLTST SUPPORT    000D
         BM    TSTOK                    IF BIT=1, BRANCH           000D
         NI    DCBERROP,MASKALL-TRMTST  TURN OFF TEST BIT          000D
         B     TST2741                  GO TEST FOR 2741
TSTOK    LA    RB,ONLTST                LOAD ADDRESS OF MODULE NAME000D
         BAL   RC,LOADROUT              LINK TO LOAD ROUTINE       000D
         BAL   RD,BLDIRB
         SPACE 1                                                 Y02947
         MODESET EXTKEY=ZERO           KEY OF ZERO               Y02947
         SPACE 1                                                 Y02947
         ST    RF,0(RCORE)              STORE IRB ADRS IN QUEUE
         SPACE 1                                                 Y02947
         MODESET EXTKEY=DATAMGT        DATAMGT KEY               Y02947
         SPACE 1                                                 Y02947
         B     TST2741
         SPACE 2
BLDIRB   EQU   *
         IC    RTIOT,DEBNMEXT      LOAD NUMBER OF EXTENTS          000E
         SLL   RTIOT,2                  MULTIPLY BY EIGHT          000D
         LA    RTIOT,1(RTIOT)           ADD 1 FOR SIZE OF WORKAREA 000D
         CIRB  EP=(RE),KEY=SUPR,MODE=SUPR,STAB=(RE),WKAREA=(RTIOT) 000D
         BAL   RJ,INITIQE          LINK TO IQE INIT                000E
         BR    RD
GRAPH1   EQU   *                                               @ZA06376
         SPACE 1                                               @ZA06376
         MODESET EXTKEY=ZERO      SET KEY OF ZERO              @ZA06376
         SPACE 1                                               @ZA06376
         L     RE,12(RCORE)             GET IRB ROUTINE ADDRESS    000E
         SPACE 1                                               @ZA06376
         MODESET  EXTKEY=DATAMGT                               @ZA06376
         SPACE 1                                               @ZA06376
         IC    RTIOT,DEBNMEXT      LOAD NUMBER OFEXTENTS           000E
         SLL   RTIOT,2             MULTIPLY BY EIGHT               000E
         LA    RTIOT,1(RTIOT)      ADD 1 FOR WKAREA SIZE           000E
         CIRB  EP=(RE),KEY=SUPR,MODE=SUPR,STAB=(RE),WKAREA=(RTIOT),    X
               SVAREA=YES                                          000E
         BAL   RJ,INITIQE          LINK TO INITIALIZE IQE          000E
         SPACE 1                                                 Y02947
         MODESET EXTKEY=ZERO           KEY OF ZERO               Y02947
         SPACE 1                                                 Y02947
         ST    RF,4(RCORE)         STORE IRB ADDRESS               000E
         SPACE 1                                                 Y02947
         MODESET EXTKEY=DATAMGT        DATAMGT KEY               Y02947
         SPACE 1                                                 Y02947
LOADPCI  TM    DCBBFTEK,DYNBUF          TEST FOR DYNAMIC BUFFERING 000E
         BNO   TESTBPM                 NO, DON'T LOAD PCI APP    YM4072
*             4 LINES DELETED BY THE FOLLOWING PTM               YM4072
LOADPCI2 EQU   *
         LA    RB,PCIAPP                LOAD ADDRESS OF IDTTRL ENTRY.
         BAL   RC,LOADROUT              BRANCH TO LOAD PCI APPENDAGE.
         ST    RE,DEBPCIA               STORE ADDRESS IN APPENDAGE
*                                  TABLE.
*        B     LOADBPMR             DELETED BY FOLLOWING APAR   YA02171
TESTBPM  CLI   DEBAMLNG,X'00'           TEST FOR AN ACCESS METHOD
*    SECTION.  IF NOT, DO NOT LOAD BUFFER ROUTINE.
         BZ    LOADCEAP
LOADBPMR LA    RB,BPMR                  POINT TO IDTTRL TABLE AND
         BAL   RC,LOADROUT         BRANCH TO LOAD BUFFER POOL
*                                  MANAGEMENT ROUTINE.
         L     RTIOT,DCBREAD           LOAD ACCESS METHOD ADRS   A30780
         EJECT
         ST    RE,0(RTIOT)              STORE ADDRESS OF BUFFER POOL
*    MANAGEMENT ROUTINE IN FIRST WORD OF ACCESS METHOD SECTION OF DEB.
LOADCEAP EQU   *
         LA    RB,CHENDAPP             ID AND TTRL FLD NEEDED FOR LOAD
         BAL   RC,LOADROUT
         ST    RE,DEBCEA                STORE ADDRESS OF CHANNEL END -
         ST    RE,DEBXCEA          ABNORMAL END APPENDAGE IN APPENDAGE
*                                  TABLE.
         CLI   DCBDEVTP,DEVWTTA         IS THE WORLD TRADE TELETYPE000G
         BNE   OTHERDEV                 BRANCH IF NOT              000G
         LA    RB,CEAPPWT               LOAD ADDRESS OF IDTTRL ENTRY00G
         BAL   RC,LOADROUT              BRANCH TO LOAD CEAPP FOR WTTA.G
*
*        THE PCI APPENDAGE (IGG019MC) AND THE CHANNEL END
*        APPENDAGE (IGG019MB) ADDRESSES MUST BE MOVED INTO
*        THE 3RD AND 4TH WORDS IF IGG019PD. OFFSET X'0C'
*        AND X'10' RESPECTIVELY.
*
         USING FORCORE,RJ    WORKAREA ADDRESSABILITY           @ZA03594
         L     RJ,DISP4(RWTGC)         WORK AREA ADDRESS       @ZA03594
         SPACE 1                                               @ZA03594
         MODESET EXTKEY=ZERO          SET SUPERVISOR KEY       @ZA14073
         SPACE 1                                               @ZA03594
         DROP  RJ                    DROP WORK AREA ADDRESS    @ZA03594
         MVC   TWELVE(EIGHT,RB),DEBPCIA  MOVE PCI AND CE    LD @ZA00531
*                                  APP. ADDRESSES INTO WTTA LD @ZA00531
*                                  CE APPENDAGE.            LD @ZA00531
         SPACE 1
         MODESET  EXTKEY=DATAMGT   BACK TO DATA MGT KEY        @ZA11422
         SPACE 1
         ST    RE,DEBPCIA               STORE CEAPP ADDR IN APP. TABLEG
         MVC   DEBCEA(8),DEBPCIA       CH END AND ABN END ADDR
OTHERDEV EQU   *                                                   000G
         LA    RB,SIOAP                LOAD START I/O APPENDAGE
         BAL   RC,LOADROUT
         ST    RE,DEBSIOA              PUT ADDRESS IN DEB
         OI    DEBSIOA,PGFXBIT     FLAG PAGE FIX APP IN SIO       3876
         LA    RB,READWR               ID AND TTRL FLD NEEDED FOR LOAD
         BAL   RC,LOADROUT             RETURN LINKAGE ESTABLISHED
*        PROTECT DCBOFLGS FIELD FOR STORE OF READ/WRITE ADDRESS
*     WHICH DESTROYES HIGH ORDER BYTE-DCBOFLGS.
         STCM  RE,SEVEN,DCBREAD+DISP1      ADDRESS OF READ/WRITE Y02947
*                                          MODULE                Y02947
         SPACE 2                                                   000M
         USING FORCORE,RD               WORKAREA ADDRESSABILITY  Y02947
         L     RD,4(RWTGC)              ADDRESSABILITY FOR WORK ARE000M
         SPACE 1                                                   000M
         LR    RCORE,0                 RCORE USED FOR LOCATION OF VD
         AH    RCORE,VDOFFSET          LOCATE VECTOR DIRECTORY
         SR    RTIOT,RTIOT             REG USED FOR DEVICE CODE
         IC    RTIOT,DISP0(RD)  GET DEVICE CODE STORED           Y02947
*                                         BY MODULE IGG0194N       000M
         STC   RTIOT,DCBDEVTP          STORE IN DCB
         SR    RC,RC         USE LINKREG AS TEMP. COUNT
*                                 USED AS A DEVICE COUNT.
         SPACE 1                                                 Y02947
RWVDLP   MODESET EXTKEY=ZERO           KEY OF ZERO               Y02947
         SPACE 1                                                 Y02947
         NC    0(4,RCORE),0(RCORE)     TEST FOR ZERO FIELD
         BNZ   VD1                     BRANCH IF NOT ZERO FIELD    000A
         MVI   3(RCORE),X'FF'          MAKE FIELD NON ZERO       A28605
RWVD1    EQU   *                                                 Y02947
         SPACE 1                                                 Y02947
         MODESET EXTKEY=DATAMGT        DATAMGT KEY               Y02947
         SPACE 1                                                 Y02947
         LA    RB,WORKTTRL(RD)          RETREIVE I/O MODULE      Y02947
*                                       ID. (IT WAS PLACED IN    Y02947
*                                       THE WORKAREA BY OPEN     Y02947
*                                       LOAD 2 (IGG0194N))       Y02947
         STC   RC,DCBDEVTP   STORE DEVICE COUNT
         BAL   RC,LOADROUT
         DROP  RD                                                  000M
         SPACE 1                                                 Y02947
NEXTSP10 MODESET EXTKEY=ZERO           KEY OF ZERO               Y02947
         SPACE 1                                                 Y02947
         ST    0,0(RCORE)              STORE VECTOR LIST ADDRESS
         STC   RTIOT,0(RCORE)          STORE DEVICE CD IN HIGH ORDER
         SPACE 1                                                 Y02947
         MODESET EXTKEY=DATAMGT        DATAMGT KEY               Y02947
         SPACE 2
ENDLOD4  XC    0(2,RWTGC),0(RWTGC)      CLEAR ID TO INDICATE
*                                       COMPLETION
         SPACE 2                                                 Y02947
***********************************************************************
*        SYSEVENT REQSWAP AND SYSEVENT DONTSWAP, AND THEIR     @ZA17710
*        RELATED SETLOCK OBTAIN/RELEASE AND REQUIRED GETMAIN/  @ZA17710
*        FREEMAINS, HAVE BEEN REMOVED AND REPLACED BY SYSEVENT @ZA17710
*        TRANSWAP MACRO.  TRANSWAP WILL BE ISSUED FOR EACH     @ZA17710
*        BTAM DCB IF RUNNING V=V.  IF THERE IS NO PREVIOUS     @ZA17710
*        OUTSTANDING TRANSWAP AGAINST THE ADDRESS SPACE, THE   @ZA17710
*        ADDRESS SPACE WIL BE SWAPPED OUT AND THEN BACK INTO   @ZA17710
*        THE PREFERRED AREA OF STORAGE, AND THE ADDRESS SPACE  @ZA17710
*        WILL BE MADE NON-SWAPPABLE.  ISSUING THE TRANSWAP     @ZA17710
*        MACRO ELIMINATES THE NECESSITY OF MANIPULATING THE    @ZA17710
*        ASCBNSWP FLAG AS PREVIOUSLY DONE.                     @ZA17710
***********************************************************************
         SPACE 2
         SR    PSAREG,PSAREG       CLEAR REG                  LD YM7707
         USING PSA,PSAREG          PSA ADDRESSABILITY         LD YM7707
         L     TCBREG,PSATOLD      TCB ADDRESS                LD YM7707
**************** ONE LINE OF CODE REMOVED BY APAR OZ17710      @ZA17710
         DROP  PSAREG                                         LD YM7707
         USING TCB,TCBREG      TCB ADDRESSABILITY              @ZA11446
         TM    TCBFLGS6,TCBRV  VIRTURE = REAL JOB?             @ZA11446
         BO    REALIOB    YES, DONT ISSUE TRANSWAP MACRO       @ZA17710
         DROP  TCBREG                                          @ZA11446
         SPACE 1
***********************************************************************
*      FIFTY LINES OF CODE HAVE BEEN DELETED AND FIFTEEN       @ZA17710
*      LINES HAVE BEEN ADDED BY APAR OZ17710, IN ORDER TO      @ZA17710
*      SUPPORT THE TRANSWAP MACRO.                             @ZA17710
***********************************************************************
         SPACE 1                                               @ZA17710
TRANSWAP EQU   *                                               @ZA17710
         USING FORCORE,RJ          WORKAREA ADDRESSABILITY     @ZA17710
         L     RJ,DISP4(RWTGC)     WORK AREA ADDRESS           @ZA17710
         XC    DXECB(4),DXECB      CLEAR ECB                   @ZA17710
         LA    RF,DXECB            GET ADDR ECB FOR TRANSWAP   @ZA17710
         SPACE 1
         SYSEVENT TRANSWAP         ISSUE TRANSWAP              @ZA17710
         SPACE 1
         N     RF,ZEROFF           MAKE SURE ONLY RC IN REG 1  @ZA17710
         LTR   RF,RF               IS RETURN CODE ZERO?        @ZA17710
         BNZ   REALIOB             RC IS NOT ZERO, TRANSWAP    @ZA17710
*                               WAS SUCCESSFUL, DONT WAIT      @ZA17710
         L      RJ,DISP4(RWTGC)       WORK AREA ADDRESS        @ZA17710
         LA    RF,DXECB            GET ADDR ECB FOR WAIT       @ZA17710
         SPACE 1
         WAIT  ECB=(1)             WAIT FOR ECB TO BE POSTED   @ZA17710
         SPACE 1
         L     RJ,DISP4(RWTGC)       WORK AREA ADDRESS         @ZA17710
         CLC   DXECB,ECBPOST       WAS ECB POSTED BY TRANSWAP  @ZA17710
         BE    TRANSWAP         NO, RETRY..OTHERWISE CONTINUE  @ZA17710
         DROP  RJ                                              @ZA17710
****************************END OF TRANSWAP CODE************** @ZA17710
         SPACE 2                                                 YM5674
REALIOB  L     RDCB,DISP0(RPARC)      GET COPY DCB ADDRESS     @ZA11446
         L     RCORE,4(RWTGC)           ADDR OF OPEN WORK AREA   Y02947
         SPACE 2                                                 Y02947
*    UPDATE THE USER'S DCB TO REFLECT ALL CHANGES MADE BY BTAM        *
*    OPEN EXECUTORS ON THE COPIED DCB.                                *
         L     RJ,WTGPREFX                                       Y02947
         STM   RE,RD,IECREGSV-IECPREFX(RJ)  SAVE REGS            Y02947
         IECRES INIT,DCBCOPY=FRWKAR                              Y02947
         LR    RJ,RWTG                  REG SAVEAREA ADDR        YM5668
         LM    RE,RD,IECREGSV-IECPREFX(RJ)  RESTORE REGS         Y02947
         SPACE 2                                                 Y02947
         USING FORCORE,RJ               WORKAREA ADDRESSABILITY  Y02947
         L     RJ,DISP4(RWTGC)          WORKAREA ADDRESS         Y02947
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     INCREMENT CURRENT WTG REGISTER.
         LA    RPARC,PLOFF(0,RPARC)     INCREMENT CURRENT PARAMETER
*                                  LIST REGISTER TO NEXT ENTRY.
         CLC   0(2,RWTGC),AMIDCNST      IF THE NEXT WTG ENTRY CALLS FOR
         BCR   8,RBASE
         CLC   0(2,RWTGC),OPIDCNST      IF NOT, TEST FOR END OF TABLE.
         BNE   RELOOP              NOT END OF TABLE, RETURN TO CHECK
*                                  NEXT ENTRY.
         LR    RPARC,RPAR               REINITIALIZE CURRENT WTG REG
         LA    RWTGC,WAOFF(0,RWTG) AND CURRENT PARAMETER LIST REG.
ZCHECK   CLI   0(RWTGC),X'00'
         BNE   XCTLRTNE            IF NOT ZERO, GO TO TRANSFER CONTROL.
         LA    RWTGC,WGOFF(0,RWTGC) IF ZERO, GET NEXT ENTRY AND RETURN
         LA    RPARC,PLOFF(0,RPARC) TO ZCHECK.
         B     ZCHECK
*    *    *    *    *    *    *    *    *    *    *    *    *    *    *
XCTLRTNE EQU   *
         LA    RJ,DXCCW12           POINT TO DOUBLE WORD LIST.   Y02947
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID TO NAME FIELD.
         MVC   14(3,RWTG),2(RWTGC)      MOVE TTR TO WTG TABLE.
         DROP  RJ                 DROP WORKAREA ADDRESS          Y02947
         L     RC,SIXT            CVT ADDRESS                    Y02947
         USING CVT,RC             CVT ADDRESSABILITY             Y02947
         L     RC,CVTSVDCB        SYS1.SVCLIB DCB                Y02947
         DROP  RC                 DROP CVT ADDRESS               Y02947
         XCTL  DE=(RWTG),SF=(E,(15)),DCB=(RC)                    Y02947
         EJECT
LOADROUT EQU   *                                                 Y02947
         MVC   6(2,RWTG),0(RB)         MOVE ID OF LAST 2 BYTES OF NAME
         MVC   14(3,RWTG),2(RB)        MOVE TTR FROM TABLE
LOAD     LR    RE,RWTG                  POINT TO PARAMETER LIST.
         USING CVT,RF                   CVT ADDRESSABILITY       Y02947
         L     RF,SIXT                  CVT ADDRESS              Y02947
         L     RF,CVTSVDCB              SYS1.SVCLIB DCB          Y02947
         DROP  RF                       DROP CVT ADDRESS         Y02947
         CLC   DISP6(LEN2,RWTG),READWR  READ/WRITE ROUTINE       YM4057
         BE    LOADMA                   YES, LOAD FROM SVCLIB    YM4057
         CLC   DISP6(LEN2,RWTG),CHENDAPP CE/ABE APPENDAGE        YM4057
         BE    LOADMB                   YES, LOAD FROM SVCLIB    YM4057
         CLC   DISP6(LEN2,RWTG),PCIAPP  PCI APPENDAGE?           Y02947
         BE    LOADMC                   YES, LOAD FROM SVCLIB    YM4057
         CLC   DISP6(LEN2,RWTG),CEAPPWT WTTA CE APPENDAGE        YM4057
         BE    LOADPD                   YES, LOAD FROM SVCLIB    YM4057
         CLC   DISP6(LEN2,RWTG),ONLTST  ON LINE TEST CONTROL MOD YM4091
         BE    LOADMR                   YES, LOAD FROM SVCLIB    YM4091
         CLC   DISP6(LEN2,RWTG),BPMR    BUFFER MANAGEMENT MOD    YM5661
         BE    LOADMS                   YES, LOAD FROM SVCLIB    YM5661
         SPACE 1                                                 Y02947
         LOAD  DE=(0),DCB=(1)
*
MOVEID   EQU   *                                                 Y02947
         MVC   0(2,RUCB),0(RB)         MOVE ID TO DEB
         LA    RUCB,2(0,RUCB)           UPDATE POINTER TO SUBROUTINE
*                                  ID SECTION IN DEB.
         IC    RD,DEBNMSUB              GET NMBR ROUTINES LOADED   000E
         LA    RD,1(RD)                 ADD ONE                    000E
         STC   RD,DEBNMSUB              STORE COUNT IN DEB         000E
PAGEFIXR EQU   *                   AOS PAGE FIX FOR APPENDS       3876
         LR    RB,RE               TEMP SAVE EP ADDRESS           3876
         SLL   RF,3                CONVERT DOUBLEWORDS TO BYTES   3876
         LA    RF,PAGESIZE(RF,RB)  BUMP BY PAGE SIZE              3876
         N     RE,PAGEBDRY         PUT ON PAGE BOUNDRY            3876
         SLR   RF,RE               REMOVE EXTRA BYTES             3876
         SRL   RF,PAGESHFT         GET NUMBER OF PAGES            3876
         SLL   RF,24               POSITION FOR OR INTO ADDRESS   3876
         LR    RE,RB               RESTORE ADDRESS                3876
         OR    RE,RF               PUT PAGE FIX COUNT IN          3876
         BR    RC                       RETURN.
LOADMA   LOAD  EPLOC=IGG019MA,DCB=(1)                            YM4057
         B     MOVEID                   MOVE IN THE ID           YM4057
         SPACE 1                                                 YM4057
LOADMB   LOAD  EPLOC=IGG019MB,DCB=(1)                            YM4057
         B     MOVEID                   MOVE IN THE ID           YM4057
         SPACE 1                                                 YM4057
LOADMC   LOAD  EPLOC=IGG019MC,DCB=(1)                            YM4057
         B     MOVEID                   MOVE IN THE ID           Y02947
         SPACE 1                                                 YM4057
LOADPD   LOAD  EPLOC=IGG019PD,DCB=(1)                            YM4057
         B     MOVEID                   MOVE IN THE ID           YM4057
         SPACE 1                                                 YM4091
LOADMR   LOAD  EPLOC=IGG019MR,DCB=(1)   LOAD IGG019MR            YM4091
         B     MOVEID                   MOVE IN THE ID           YM4091
         SPACE 1                                                 YM5661
LOADMS   LOAD  EPLOC=IGG019MS,DCB=(1)   LOAD IGG019MS            YM5661
         B     MOVEID                   MOVE IN THE ID           YM5661
         SPACE 1                                                 YM4057
VD1      CLC   DCBDEVTP(1),0(RCORE)  TEST FOR EQUAL DEVICE CODE: DCBDV
         BE    RWVD1         VS. VECTOR DIREC. DEVICE TYPE.
*                                  DEVICE CODE AND VECTOR DIRECTORY TBL
         SPACE 1                                                 Y02947
         MODESET EXTKEY=DATAMGT        DATAMGT KEY               Y02947
         SPACE 1                                                 Y02947
         CLI   0(RCORE),X'FF'          TEST END OF TABLE           000A
         BE    VDOVFLER                                            000A
         LA    RCORE,4(0,RCORE)        BUMP TO GET NEXT VD FIELD
         AH    RC,ONE        BUMP DEVICE COUNT
*                                 VECTOR LIST FIELD NOT ZERO OR EQUAL
*                                 TO DEVICE CODE TO LOAD PROGRAM.
         B     RWVDLP
VDOVFLER L     1,ERR6                  VECTOR DIRECTOR OVERFLOW    000A
         ABEND (1)                                                 000A
         CNOP  0,4
         EJECT
ONLTTMSK DS    0F                                                  000D
       DC    BL.51'111111111111111111111111111000000011111111111111111'
*      1050        *****                                           000J
*      2740             ******************                         000J
*      1060                               **                       000J
*      1030                                 **                     000J
*      83B3                                   **                   000J
*      115A                                     **                 000J
*      TWX                                        ***              000J
*      2260/2848                                     *             000J
*      S360/1130/2780/2020                            ********     000J
*      2741                                                   **   000J
*      2740/2760                                                ** 000J
*      1050X                                                      **
*      2740X                                                        **
         DS    0F
ZEROFF   DC    X'000000FF'                                    LD YM7707
XFF      EQU   X'FF'                                          LD YM7707
PAGEBDRY DC    X'00FFF800'         AOS2 PAGE BOUNDRY MASK       AS23648
PAGESHFT EQU   11                  AOS2 PAGE SHIFT COUNT        AS23648
PAGESIZE EQU   2047                AOS2 PAGE SIZE - 1           AS23648
ERR6     DC    X'80097000'                                         000A
TRMTST   EQU   X'10'                    TERMINAL TEST BIT IN ERROP
         SPACE
WAOFF    EQU   32                       OFFSET OF FIRST WTG ENTRY FROM
*                                  START OF WTG.
PLOFF    EQU   4                        OFFSET OF DCB ENTRIES.
WGOFF    EQU   8                        OFFSET OF WTG ENTRIES.
OPIDCNST DC    C'0S'                    ID OF LAST OPEN LOAD.
AMIDCNST DC    C'3S'                    ID OF THIS MODULE.         000D
WORKTTRL EQU   2                                                 Y02947
DYNBUF   EQU   X'08'                    CODE FOR BFTEK=D IN THE DCB,
DEVWTTA  EQU   X'2A'                    WTTA OFFSET                000G
ONE      DC    H'01'
VDOFFSET DC    H'12'
GRAPHMSK EQU   X'40'                                               000E
MASKALL  EQU   X'FF'                    ALL ONES                   000D
XFE      EQU   X'FE'                                           @ZA08768
INTR1    EQU   X'2B'
INTR2    EQU   X'2C'
INTR3    EQU   X'16'                                        LD @ZA00541
INTR4    EQU   X'10'                                        LD @ZA00541
PGFXBIT  EQU   X'C0'
DISP0    EQU   0                  ZERO DISPLACEMENT              Y02947
DISP1    EQU   1                  VALUE OF ONE                   Y02947
DISP4    EQU   4                  DISPLACEMENT OF 4              Y02947
DISP6    EQU   6                  DISPLACEMENT OF 6              Y02947
DISP28   EQU   28                 DISPLACEMENT OF 28             Y02947
LEN2     EQU   2                  LENGTH OF 2                    Y02947
SIXT     EQU   16                 OFFSET TO CVT                  Y02947
FOUR     EQU   4                                              LD YM7707
SEVEN    EQU   7                  MASK FOR STCM INSTRUCTION      Y02947
*                                 (3 BYTES)                      Y02947
EIGHT    EQU   8                   DISPLACEMENT OF 8        LD @ZA00531
TWELVE   EQU   12                  DISPLACEMENT OF 12       LD @ZA00531
TWENTY   EQU   20                                              @ZA11446
         SPACE 2
IGG019MA DC    C'IGG019MA'              READ/WRITE MODULE        YM4057
IGG019MB DC    C'IGG019MB'              CE/ABE APPENDAGE         YM4057
IGG019MC DC    C'IGG019MC'              PCI MOD NAME             Y02947
IGG019PD DC    C'IGG019PD'              WTTA CE APPENDAGE        YM4057
IGG019MR DC    C'IGG019MR'              ON LINE TEST MODULE      YM4091
IGG019MS DC    C'IGG019MS'              BUFFER MANAGEMENT MODULE YM5661
PCIAPP   DC    C'MC'                   PCI APPENDAGE
         DC    XL4'0'                  TTRL FIELD
ONLTST   DC    C'MR'                    ON-LINE TEST CONTROL MOD   000D
         DC    XL4'0'                        TTRL FIELD            000D
BPMR     DC    C'MS'                   BUFFER POOL MANAGEMENT ROUTINE
         DC    XL4'0'                  TTRL FIELD
CHENDAPP DC    C'MB'                   CHANNEL END/ABNORMAL END
*                                 APPENDAGE
         DC    XL4'0'                  TTRL FIELD
CEAPPWT  DC    C'PD'                    CHANNEL END/ABNORMAL END   000G
*                                  APPENDAGE FOR WTTA.             000G
         DC    XL4'0'                   TTRL FIELD                 000G
READWR   DC    C'MA'                   READ/WRITE ROUTINE
         DC    XL4'0'                  TTRL FIELD
BREAK41  DC    C'PK',XL4'0'            2741 BREAK MODULE
SIOAP    DC    C'LP',XL4'0'            SIO APPENDAGE
SUBPOOL  DC    X'FA000000'             SUBPOOL 250             @ZA11446
ECBPOST  DC    X'40000004'    POST CODE FOR ECB FROM TRANSWAP  @ZA17710
         EJECT                                                   Y02947
         IHAASCB
         EJECT
         DCBD  DSORG=BX,DEVD=BS                                    000E
         EJECT
        IECTDEBX
         EJECT                                                     000M
         IHAPSA                                                  Y02947
         EJECT                                                     000M
         IECDSECS MAIN,WTG,PREFX,CVT,EXPAND=YES                  Y20947
         EJECT
         IKJTCB DSECT=YES
         END
