         TITLE 'IGC0B06F - 2760 ON-LINE TERMINAL TEST MODULE'
IGC0B06F CSECT
*  STATUS -        CHANGE LEVEL 000       05-01-69
         SPACE 2
*  FUNCTION -      PERFORM ONLINE TERMINAL TESTS FOR THE IBM 2760
*                    OPTICAL IMAGE UNIT
         SPACE 2
*  ENTRY POINT -    ENTRY IS TO THE FIRST EXECUTABLE INSTRUCTION.
*                   MODULE IS REQUESTED VIA AN SVC 66 FROM THE
*                   MODULE IGC0006F WHEN A TYPE 10 OR 11 2740 TEST
*                   IS REQUESTED.
         SPACE 2
*  INPUT -          REGISTER 1 - ADDRESS OF THE IOB FOR LINE REQUESTING
*                                    THE TEST.
*                   IOBERCCW (IOB+40)
*                             +0 - NAME OF THIS MODULE
*                             +8 - ADDRESS OF REQUEST MESSAGE
*                            +12 - ADDRESS OF PARAMETER LIST
*                                      IN IGG019MR.
*                   PARAMETER LIST -
*                             +0 ADDRESS OF PATTERN AREA
*                             +4 ADDRESS OF NL AND IDLES
         SPACE 2
*  EXTERNAL ROUTINES - NONE
         SPACE 2
*  EXIT -           A BRANCH ON REGISTER 14 RETURNS CONTROL TO
*                     IGG019MR.  IGG019MR THEN EXECUTES THE CHANNEL
*                     PROGRAM BUILT BY THIS MODULE.
         SPACE 1
*          ERROR EXIT - SAME AS ABOVE EXCEPT THAT BIT 7 OF IOBINCAM
*                       (IOB+28) IS SET TO 0 TO INDICATE TO IGG019MR
*                       THAT THE CHANNEL PROGRAM IS NOT TO BE EXECUTED.
         SPACE 2
*  ATTRIBUTES - THIS ROUTINE IS REENTRANT
         SPACE 2
*  COMMENTS
*              BASIC CCWS ARE MOVED INTO THE IOBCPA FIELD IN GROUPS
*              ACCORDING TO THE FUNCTION TO BE PERFORMED.  THESE CCWS
*              HAVE THE REQUIRED COMMAND CODES, FLAGS, AND IN MOST
*              INSTANCES, DATA COUNT.
*
*              WHEN A DATA ADDRESS IS TO BE INSERTED INTO THE CCW, THE
*              FOLLOWING METHOD IS USED:
*
*              1.  LOAD THE DATA ADDRESS INTO A REGISTED.
*
*              2.  ADD THE FIRST WORD OF THE REQUIRED CCW TO THE
*                  REGISTER.  NOTE THAT THE COMMAND CODE WILL BE
*                  CARRIED ALONG AND WILL BE IN THE HIGH
*                  ORDER BYTE OF THE REGISTER.
*
*              3.  STORE THE REGISTER AT THE FIRST BYTE OF THE
*                  CCW.  THE COMMAND CODE WILL NOT BE CHANGED.
         EJECT
IOBREG   EQU   1                        POINTER TO IOB
DECBREG  EQU   2                        POINTER TO DECB
PARMREG  EQU   3                        PARAMETERS PASSED FROM ONLINE
*                                         TEST CONTROL
AREAREG  EQU   4                        POINTER TO REQUEST MESSAGE
OIUREG   EQU   5                        POINTER TO 2760 CONTROL CHARS
WORKREG  EQU   6                        WORK REGISTER
BASEREG  EQU   12                       BASE FOR THIS MODULE
RETREG   EQU   14
TESTCODE EQU 5                          OFFSET INTO MSG TO TEST CODE
NOENTRY  EQU   X'BF'                    TURNS OFF REENTRY FLAG
CC       EQU   X'40'                    CHAIN COMMAND FLAG
REENTRY  EQU   X'40'                    REENTRY CODE IN DECONLTT
EOTOFF   EQU   21                       OFFSET TO EOT CHARS IN PARM
*                                            LIST
DIALBITS EQU   X'90'                    A CALL AANSR FEATURE BITS
READTI   EQU   1                        READ INITIAL OPTYPE
READTV   EQU   5                        READ CONVERSATIONAL OPTYPE
TCO      EQU   X'0E'                    INVITATIONAL OPTICAL TYPE
         BALR  BASEREG,0
         USING *,BASEREG
         USING  IECTDECB,DECBREG
         USING IECTIOB,IOBREG
         L     DECBREG,IOBECBPT         POINTER TO DECB
         L     PARMREG,IOBERCCW+12      POINTER TO PARAMETER LIST
         L     AREAREG,IOBERCCW+8       INPUT AREA
         TM    DECONLTT,REENTRY         FIRST ENTRY FOR THIS TEST
         BO    SECOND                   BRANCH IF NOT
         CLI   DECTYPE+1,TCO            TEST ENTERED ON WRITE TCO
         BNE   VALIDITY                 BRANCH IF NO
         TM    FEATURES,DIALBITS        IS THIS A DIAL LINE
         BNZ   DIAL                     BRANCH IF YES
         MVI   DECTYPE+1,READTI         CONSTRUCT READ TI AFTER TEST
VALIDITY TM    FEATURES,X'2C'           IS DEVICE 2740 CHECKING,OIU
         BNO   NOGOOD                   BRANCH IF NOT
         MVC   IOBWORK+3(4),DPREFIXO    2760 CONTROL CHARS TO IOB
         MVC   IOBCPA(40),TIOCCWS       MOVE CCWS TO WRITE TO 2760
*                                         INTO IOBCPA
         LA    WORKREG,IOBWORK+3        POINT TO 2760 CONTROL CHARS
         A     WORKREG,IOBCPA           ADD COMMAND AND OFFSET TO
*                                         CONTROL CHARACTER ADDRESS
         ST    WORKREG,IOBCPA           WRITE EOA PREFIX O
         SPACE 2
         LR    WORKREG,AREAREG          DATA AREA
         LA    WORKREG,8(WORKREG)       BUMP TO FRAME CHANGE MESSAGE
         A     WORKREG,IOBCPA+8
         ST    WORKREG,IOBCPA+8         WRITE FRAME CHANGE
         SPACE 2
         LA    WORKREG,IOBWORK+6        CIRCLE B
         A     WORKREG,IOBCPA+16
         ST    WORKREG,IOBCPA+16        WRITE EOB
         SPACE 2
         LA    WORKREG,DECRESPN+1       RESPONSE AREA
         A     WORKREG,IOBCPA+24
         ST    WORKREG,IOBCPA+24        READ RESPONSE TO EOB
         SPACE 2
         L     WORKREG,4(PARMREG)       POINTER TO CRIDLES
         LA    WORKREG,EOTOFF(WORKREG)  POINTER TO EOT
         A     WORKREG,IOBCPA+32
         ST    WORKREG,IOBCPA+32        WRITE CIRCLE C
         SPACE 2
         CLC   TESTCODE(2,AREAREG),ELEVEN IS THIS SCAN POINT TEST
         BNE   GOODEXIT                 BRANCH IF NO
         SPACE 2
         OI    IOBCPA+36,CC             TURN ON CHAIN COMMAND FLAG IN
*                                         WRITE CIRCLE C CCW
         MVC   IOBCPA+40(16),RESPCCWS   CCWS TO READ OPERATORS
*                                         RESPONSE MESSAGE
         A     AREAREG,IOBCPA+48
         ST    AREAREG,IOBCPA+48        DATA AREA IN READ DATA CCW
         LH    WORKREG,DECLNGTH         LENGTH OF DATA AREA
         STH   WORKREG,IOBCPA+54        READ DATA CCW
         SPACE 2
         OI    DECONLTT,REENTRY         TURN ON REENTRY FLAG
         SPACE 2
GOODEXIT EQU   *
         OI    IOBINCAM,X'01'           INDICATE TEST IS TO BE EXECUTED
EXIT     EQU   *
         BR    RETREG
NOGOOD   EQU   *                        INVALID TEST EXIT
         NI    IOBINCAM,X'FE'           INDICATE TEST NOT TO BE
*                                          EXECUTED
         B     EXIT
         SPACE   2
DIAL     MVI   DECTYPE+1,READTV         CONSTRUCT READ TV AFTER TEST
         B     VALIDITY                 CHECK FOR VALID FEATURES
         EJECT
SECOND   EQU   *
         NI    DECONLTT,NOENTRY         TURN OFF REENTRY FLAG
         CLC   0(2,AREAREG),PREFIXO     PROBE FROM 2760
         BNE   NOGOOD                   BRANCH IF NO
         MVC   IOBCPA(24),WTICCWS       MOVE CCW BASES INTO IOB
         SPACE 2
         L     WORKREG,4(PARMREG)       POINTER TO CRIDLES FROM
*                                       PARAMETER LIST
         A     WORKREG,IOBCPA
         ST    WORKREG,IOBCPA           WRITE CIRCLE D PAD
*                                             NEW LINE PAD
         SPACE 2
         LA    AREAREG,2(AREAREG)       BUMP PAST LEADING CONTROL CHARS
         A     AREAREG,IOBCPA+8
         ST    AREAREG,IOBCPA+8         DATA AREA
         LH    WORKREG,DECLNGTH         LENGTH OF DATA AREA
         SH    WORKREG,IOBCSW+6         SUBTRACT RESIDUAL COUNT
         SH    WORKREG,THREE            EXCLUDE PRE,O,EOT
         STH   WORKREG,IOBCPA+14        WRITE DATA TO 2740
         SPACE 2
         L     WORKREG,4(PARMREG)       POINTER TO CONTROL CHARS FROM
*                                         PARAMETER LIST
         LA    WORKREG,EOTOFF(WORKREG)  GET TO EOT
         A     WORKREG,IOBCPA+16
         ST    WORKREG,IOBCPA+16        WRITE EOT
         B     GOODEXIT
          EJECT
         DS    0D
TIOCCWS  DC    X'0100000080000003'      WR D PRE O
         DC    X'0100000080000003'      WR FRAME CHANGE
         DC    X'0100000060000001'      WR EOB
         DC    X'0200000060000001'      RD RESPONSE
         DC    X'0100000020000003'      WR EOT
         SPACE 2
RESPCCWS DC    X'0600000060000001'      PREPARE
         DC    X'0200000020000000'      RD DATA
         SPACE 2
WTICCWS  DC    X'0100000080000014'      WR EOA NL PADS
         DC    X'0100000080000000'      WR DATA
         DC    X'0100000020000003'      WR EOT
         SPACE 2
*      THESE THREE CONSTANTS MUST BE KEPT TOGETHER
DPREFIXO DC    X'16'                    EOA
PREFIXO  DC    X'3E4C'
EOB      DC    X'3D'                    EOB CHARACTER
THREE    DC    H'3'
ELEVEN   DC    X'0202'
         EJECT
         IECTDECB
         EJECT
         IECTIOBX
FEATURES EQU  IOBWORK+3
         END
