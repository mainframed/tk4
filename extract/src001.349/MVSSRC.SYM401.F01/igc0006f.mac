         TITLE  'IGC0006F  -  ON-LIN TEST CONTROL MODULE    '
IGC0006F CSECT
         SPACE 3
*
* C070500,A070600                                             LD YM4058
* A054100,054200,061100,061300,061400                       L5 @ZA02806
* A072600-072900,073020,085100-085400,085600                L5 @ZA02806
* A097100-097300,097600                                     L5 @ZA02806
*
         SPACE 3
***********************************************************************
*
*
*      THIS MODULE HAS BEEN RE-WRITTEN FOR OS/VS2-REL2 (MVM)
*
*
***********************************************************************
*                                                                     *
* STATUS -           CHANGE LEVEL 002,  1AUG 1969                  000M
*                                                                     *
*FUNCTION -         SERVICES BTAM START-STOP ON LINE TEST REQUESTS    *
*                   VALIDATES REQUEST FOR TEST MESSAGES AND XCTL'S    *
*                     TO APPROPRIATE DEVICE DEPENDENT LOAD.           *
*                                                                     *
*ENTRY -            ENTRY IS TO THE FIRST EXECUTABLE INSTRUCTION.     *
*                                                                     *
* INPUT -           REGISTER 1 - ADDRESS OF THE IOB.                  *
*                   ADDRESS OF THE RFT MESSAGE IN IOBERINF INSERTED   *
*                     BY IGG019MB (CHANNEL END).                      *
*                   ADDRESS OF THE PARAMETER LIST IN IOBERINF+4       *
*                     INSERTED BY IGG019MR (TERMINAL TEST CONTROL     *
*                       MODULE).                                      *
*                                                                     *
* OUTPUT -            REGISTER 1 - ADDRESS OF IOB                     *
*                                                                     *
* EXTERNAL ROUTINES - EXECUTE CHANNEL PROGRAM ROUTINE                 *
*                                                                     *
* EXIT -            XCTL TO APPROPRIATE SUBSEQUENT LOAD.              *
*                                                                     *
* IN-LINE ROUTINES- THIS FIRST SVC LOAD DETERMINES BY TESTING THE UCB *
*                   WHETHER THE LINE IN TEST IS BI-SYNC OR START-STOP.*
*                   IF START-STOP, IT MOVES ADAPTER TYPE AND MODEL    *
*                   CODE INTO THE DCB, USES CODE AS INDEX TO TYPE OF  *
*                   TERMINAL AND TEST, GETS I/O AREA FROM THE IOB,    *
*                   AND PROCESSES TEST REQUESTS WITH THE FOLLOWING    *
*                   SUBROUTINES.                                      *
*                                                                     *
*                     1. DISPTEST - THIS SUBROUTINE IS USED IF REQUEST*
*                        IS FOR A 2848. ALL POSSIBILITIES OF ILLEGAL  *
*                        TEST REQUESTS ARE CHECKED. CONTROL UNIT AND  *
*                        DISPLAY STATION SELECTION ADDRESSES ARE      *
*                        CHECKED FOR VALIDITY. IF NO ERRORS, XCTL TO  *
*                        MODULE IGC0506F.                             *
*                     2. CHK1050 - MOVES IN ADDRESSING CHARACTERS,    *
*                        MOVES UCB FEATURES FIELD TO DCB, CHECKS FOR  *
*                        COMPONENT SELECTION SPECIFIED IN RFT, CHECKS *
*                        FOR ILLEGAL COMPONENT, CHECKS FIRST TEST     *
*                        SELECTION CODE = 00. IF THESE CONDITIONS ARE *
*                        MET, GOES TO COMCHK, IF NOT, RESTART.        *
*                                                                     *
*                     3. CHK1060 - CHECKS SELECTION CODE, COMPONENT   *
*                        SELECTION, AND FIRST ADDRESS POSITION. IF    *
*                        THESE ARE VALID, GOES TO COMCHK, IF NOT,     *
*                        RESTARTS.                                    *
*                                                                     *
*                     4. CHK1030 - TESTS SELECTION CODE. IF VALID,    *
*                        GOES TO COMCHK, IF NOT, RESTARTS.            *
*                                                                     *
*                     5. CHK2740 - MOVES UCB FEATURES FIELD TO IOB,   *
*                        CHECKS FOR 2760 TEST, TESTS SELECTION CODE.  *
*                        GOES TO COMCHK IF VALID, RESTARTS IF NOT.    *
*                                                                     *
*                     6. CHKNUM - CHECKS 1030 MANUAL ENTRY OR BADGE   *
*                        READER. CHECKS TEST SELECTION CODE AND FIRST *
*                        ADDRESS POSITION. IF VALID, GOES TO COMCHK,  *
*                        IF NOT, RESTARTS.                            *
*                                                                     *
*                     7. COMCHK - CHECKS TEST SELECTION CODE FURTHER. *
*                        IF STILL VALID, GOES TO XCTL. IF NOT,        *
*                        RESTARTS.                                    *
*                                                                     *
*                     8. RESTART - RETURN TO USER VIA REGISTER 14     *
*                        WITH ON-LINE TEST FLAG IN IOBINCAM TURNED    *
*                        OFF.                                         *
*                                                                     *
*                     9. CHK2740C - MOVES UCB FEATURES FIELD TO IOB,  *
*                        CHECKS SELECTION CODES AND GOES TO RESTART   *
*                        IF CODE IS 0, GREATER THAN 7, OR A SPACE     *
*                        CHARACTER. OTHERWISE, GOES TO XCTL.          *
*                                                                     *
*                    10. CHK2741C - SAME AS CHK2740C.                 *
*                                                                     *
*                    11. CHK2741P - SAME AS CHK2740.                  *
*                                                                     *
*                   IF THE TEST IS FOR BI-SYNC, THIS ROUTINE XCTL'S   *
*                   TO IGC0D06F.                                      *
*                                                                     *
*                   IF THE TEST IS FOR A LOCAL 3270, THIS             *
*                   ROUTINE XCTL'S TO IGC1206F                        *
*                                                                     *
* ATTRIBUTES - THIS ROUTINE IS REENTRANT.                             *
*                                                                     *
***********************************************************************
         EJECT
IOBREG   EQU   1
DEBREG   EQU   2
PARREG   EQU   3
TYPEREG  EQU   4
BASEREG  EQU   6
IOREG    EQU   7
TESTREG  EQU   8
CNTREG   EQU   9
ADREG    EQU   10
TCBREG   EQU   10                                           L5 @ZA02806
RTCD     EQU   15                                           L5 @ZA02806
DCBREG   EQU   11
DECBREG  EQU   12
UCBREG   EQU   13
RTNREG   EQU   14
         SPACE 4
         BALR  BASEREG,ZERO
         USING *,BASEREG
         USING IOBSTDRD,IOBREG        *
         USING DEB,DEBREG             * DSECTS FOR CONTROL BLOCKS
         USING IHADCB,DCBREG          *
         USING DECB,DECBREG
         USING UCB,UCBREG
         B     AROUND
         DC    CL8'IGC0006F'
         DC    XL2'5052'
         DC    C'&SYSDATE'
         DC    S(*)
PATCH    DC    20F'0'
         DS    0F
AROUND   EQU   *
         L     DECBREG,IOBECBPT
         L     DCBREG,IOBDCBPT          LOAD DCB ADDRESS
         L     DEBREG,DCBDEBAD          LOAD POINTER TO DEB
         SH    DEBREG,H36              ADJUST FOR DSECT
         LR    TCBREG,IOBREG           SAVE IOB ADDR        L5 @ZA02806
         LR    CNTREG,RTNREG           SAVE RETURN REG      L5 @ZA02806
         DEBCHK (DCBREG),TYPE=VERIFY,AM=TAM                 L5 @ZA02806
         LTR   RTCD,RTCD               CHECK RET CODE       L5 @ZA02806
         BNZ   ABEND                   ABEND WITH 16E       L5 @ZA02806
         LR    IOBREG,TCBREG           RESTORE IOBADDR      L5 @ZA02806
         LR    RTNREG,CNTREG           RESTORE RETURM REG   L5 @ZA02806
         TM    DECONLTT,DECRCVMS        IS REQUEST FOR REENTRY
         BO    RE2760                   BRANCH IF YES
         SR    UCBREG,UCBREG            CLEAR REGISTER
         IC    UCBREG,IOBUCBX           GET INDEX TO UCB FOR THIS LINE
         SLL   UCBREG,TWO               MULTIPLY BY FOUR
         L     UCBREG,DEBSUCBA          GET UCB ADDRESS FOR THIS LINE
         TM    UCBTBYT4,BISYNC          IS THIS A BI-SYNC LINE
         BO    BISNTST
         CLI   UCBTBYT3,UCB3DISP        IS DVC A GRAPHIC ?
         BNE   NOTGRAPH                 NO,CAN'T BE A 3270 DEVICE
         CLI   UCBTBYT4,UCB3211         IS IT A 3277 ?
         BE    PATH3270                 YES, XCTL TO IGC1206F
         CLI   UCBTBYT4,A3284           IS IT A 3284 ?
         BE    PATH3270                 YES, XCTL
         CLI   UCBTBYT4,A3286           IS IT A 3286 ?
         BE    PATH3270                 YES, XCTL
NOTGRAPH EQU   *
         MVN   DCBERROP+TWO(ONE),UCBTBYT1    MOVE MODEL CODE INTO DCB
         MVZ   DCBERROP+TWO(ONE),UCBTBYT4    MOVE ADAPTER.... LD YM4058
*                                            TYPE INTO DCB    LD YM4058
         L     IOREG,IOBERINF           GET I/O AREA ADDR FROM IOBLIST
         CLI   SEVEN(IOREG),FIFTY6      BRANCH IF REQUEST
         BE    DISPTEST                 FOR 2848
         SR    TYPEREG,TYPEREG          CLEAR THE TYPE REGISTER
         L     TCBREG,DEBNMSUB         GET TCB ADDR         L5 @ZA02806
         USING TCB,TCBREG              BASE FOR DSECT       L5 @ZA02806
         MODESET EXTKEY=TCB,WORKREG=15 GO TO USER KEY       L5 @ZA02806
         DROP  TCBREG
         IC    TYPEREG,SEVEN(IOREG)     GET TYPECODE FROM INPUT AREA
         MODESET EXTKEY=ZERO                                L5 @ZA02806
         N     TYPEREG,BADBITS          TURN OFF UNWANTED BITS
         SRL   TYPEREG,ONE              DIVIDE BY TWO
         BZ    RESTART                  BRANCH IF TYPE CODE EQUAL ZERO
         IC    TESTREG,LOADSEL-ONE(TYPEREG) GET MODULE NUMBER FOR XCTL
         STC   TESTREG,IOBERCCW+FOUR    STORE MODULE ID NUMBER IN NAME
         LA    TESTREG,MODADAPT-ONE(TYPEREG)    GET ADDR - ADAPTER...
*                                               AND MOD CODE
         CLI   DCBERROP+TWO,TYP1050     IS TYPE A 1050
         BE    VAL1050                                             000M
         CLI   DCBERROP+TWO,TYP1050X
         BE    VAL1050
         CLI   DCBERROP+TWO,TTY1050X
         BE    VAL1050
         CLI   DCBERROP+TWO,TYP2740X
         BE    VAL2740
         CLI   DCBERROP+TWO,NINETEEN    2S THIS 2740B
         BNE   CHECKVAL                 NO
         MVI   DCBERROP+TWO,FOURTEEN    YES, TREAT AS 2740
CHECKVAL EQU   *
         CLC   DCBERROP+TWO(ONE),ZERO(TESTREG)  COMPARE FOR VALIDITY
         BNE   RESTART
         CLI   SEVEN(IOREG),MAX         BRANCH IF TYPE CODE IS
         BH    RESTART                  GREATER THAN 5
         SPACE 2
         SLL   TYPEREG,TWO              MULTIPLY BY FOUR
         L     TCBREG,DEBNMSUB         GET TCB ADDR         L5 @ZA02806
         USING TCB,TCBREG              BASE FOR DSECT       L5 @ZA02806
         MODESET EXTKEY=TCB,WORKREG=15  GO TO USER KEY      L5 @ZA02806
         DROP  TCBREG                                       L5 @ZA02806
         MVC   IOBWORK(TWO),EIGHT(IOREG)    MOVE IN ADDRESS CHARACTERS
         MODESET EXTKEY=ZERO                                L5 @ZA02806
         B     BRANSEL-FOUR(TYPEREG)    BRANCH TO GET MODULE TYPE
         SPACE 2
*
*                             SPECIAL VALIDITY TESTS
*
VAL1050  CLI   SEVEN(IOREG),P2          TEST 1050 ON IBM TELL ADAPT
         BE    CHK1050
         B     RESTART
         SPACE 1
VAL2740  CLI   SEVEN(IOREG),P4          TEST 2740X
         BE    CHK2740
         B     RESTART
         SPACE 2
BRANSEL  B     CHK1030                  CHECK 1030 REQUEST
         B     CHK1050                  CHECK 1050 REQUEST
         B     CHK1060                  CHECK 1060 REQUEST
         B     CHK2740                  CHECK 2740 REQUEST
         B     CHKNUM                   CHECK 1030 M.E. OR BADGE
         B     RESTART                  RESERVED 2848/2260
         B     RESTART                  RESERVED
         B     CHK2740C
         B     CHK2741C
         B     CHK2741P
         SPACE 2
CHK1050  EQU   *
         L     TCBREG,DEBNMSUB         GET TCB ADDRR        L5 @ZA02806
         USING TCB,TCBREG              BASE FOR TCB DSECT   L5 @ZA02806
         MODESET EXTKEY=TCB,WORKREG=15  GO TO USER KEY      L5 @ZA02806
         DROP TCBREG                                        L5 @ZA02806
         MVC   IOBWORK(TWO),EIGHT(IOREG)  MOVE IN ADDRESSING CHARACTERS
         MODESET EXTKEY=ZERO                                L5 @ZA02806
         MVC   DCBERROP+TWO(ONE),UCBTBYT2  MOVE FEATURES FIELD FROM UCB
         CLI   NINE(IOREG),TWO          BRANCH IF
         BL    RESTART                  NO COMPONENT SELECTION
         CLI   NINE(IOREG),FIFTEEN      BRANCH IF
         BH    RESTART                  ILLEGAL COMPONENT
FIRSTSEL CLI   FIVE(IOREG),FIFTEEN      BRANCH IF FIRST TEST
         BNE   RESTART                  SELECTION CODE NOT ZERO
         B     COMCHK                   GO TO COMMON CHECKS
         SPACE 2
CHK1060  EQU   *
         CLI   SIX(IOREG),X0B           BRANCH IF
         BE    LOADIT                   COMPARE TEST
         CLI   FIVE(IOREG),FIFTEEN      BRANCH IF FIRST TEST
         BNE   RESTART                  SELECTION CODE NOT ZERO
         CLI   TEN(IOREG),TWO           BRANCH IF COMPONENT
         BE    CHKADDR                  SELECTION OF 1
         CLI   TEN(IOREG),SEVEN         BRANCH IF COMPONENT
         BE    CHKADDR                  SELECTION OF 3
         B     RESTART                  BRANCH IF ILLEGAL COMPONENT
CHKADDR  CLI   EIGHT(IOREG),FIFTEEN     BRANCH IF FIRST ADDRESS
         BE    COMCHK                   POSITION IS ZERO
         CLI   EIGHT(IOREG),TWO         BRANCH IF FIRST ADDRESS
         BE    COMCHK                   POSITION IS 1
         CLI   EIGHT(IOREG),FOUR
         BNE   RESTART
         SPACE 2
COMCHK   CLI   SIX(IOREG),FIFTEEN       BRANCH IF
         BE    RESTART                  TEST CODE OF ZERO
         CLI   SIX(IOREG),X0E           BRANCH IF TEST SELECTION
         BH    RESTART                  GREATER THAN SEVEN
         CLI   SIX(IOREG),ONE           BRANCH IF
         BE    RESTART                  TEST CODE IS BLANK
         B     LOADIT
         SPACE 2
CHK1030  CLI   FIVE(IOREG),TWENTY       BRANCH IF FIRST TEST
         BNE   RESTART                  SELECTION CODE NOT ZERO
         CLI   SIX(IOREG),TWENTY        BRANCH IF TEST
         BE    RESTART                  CODE OF ZERO
         B     COMCHK+EIGHT
         SPACE 3
CHK2741P EQU   *
         SPACE 2
CHK2740  MVC   IOBWORK+THREE(ONE),UCBTBYT2    MOVE FEATURES FIELD
         CLC   FIVE(TWO,IOREG),SCANTEST SCAN POINT TEST
         BE    RE2760                   BRANCH IF YES
         CLC   FIVE(TWO,IOREG),FRAMETST FRAME CHANGE TEST
         BNE   FIRSTSEL                 BRANCH IF NO
         SPACE 2
RE2760   MVI   IOBERCCW+FOUR,MOD2760    CODE FOR 2760 TEST MODULE
         B     LOADIT
         SPACE 2
CHK2740C EQU   *
CHK2741C EQU   *
         MVC   IOBWORK+THREE(ONE),UCBTBYT2   MOVE FEATURES FIELD
         CLI   FIVE(IOREG),C0           BRANCH IF FIRST TEST SELECTION
         BNE   RESTART                  CODE NOT ZERO
         CLI   SIX(IOREG),C8            BRANCH IF INVALID CODE
         BE    RESTART                  *          THAT IS  0,
         CLI   SIX(IOREG),C4            *          GREATER THAN 7,
         BH    RESTART                  *          OR A SPACE CHARACTER
         CLI   SIX(IOREG),SP            *
         BE    RESTART                  *
         B     LOADIT
         SPACE 3
CHKNUM   CLI   FIVE(IOREG),TWENTY        BRANCH IF
         BNE   RESTART                  TEST SELECT 1 NOT ZERO
         CLI   SIX(IOREG),TWENTY        BRANCH IF
         BE    RESTART                  TEST CODE OF ZERO
         CLI   EIGHT(IOREG),TWENTY      BRANCH IF FIRST ADDRESS
         BE    COMCHK+EIGHT             POSITION IS ZERO
         CLI   EIGHT(IOREG),TWO         BRANCH IF FIRST ADDRESS
         BE    COMCHK+EIGHT             POSITION IS ONE
         CLI   EIGHT(IOREG),FOUR        BRANCH IF FIRST ADDRESS
         BNE   RESTART                  POSITION IS NOT TWO
         B     COMCHK+EIGHT
         SPACE 2
LOADSEL  DC    CL1'1'                   1030 TEST SEL. MODULE NUMBER
         DC    CL1'2'                   1050 TEST SEL. MODULE NUMBER
         DC    CL1'3'                   1060 TEST SEL. MOD. NUMBER
         DC    CL1'4'                   2740 TEST SEL. MOD. NUMBER
         DC    CL1'1'                   1030 M.E. OR BADGE
         DC    CL1'5'                   2848 TEST SEL. MOD. NUMBER
         DC    CL1'F'                   NCS
         DC    CL1'C'                   2740C TEST SEL. MODULE NUMB000M
         DC    CL1'9'                   2741C TEST SEL. MODULE NUMBER
         DC    CL1'A'                   2741P TEST SEL. MODULE NUMBER
         SPACE 2
MODADAPT DC    X'21'
         DC    X'11'
         DC    X'12'
         DC    X'14'
         DC    X'21'
         DC    X'81'
         DC    X'00'
         DC    X'13'
         DC    X'15'
         DC    X'16'
         SPACE 2
DISPTEST MVI   IOBERCCW+FOUR,CHAR5      STORE MODULE ID IN NAME
         CLI   FIVE(IOREG),FIFTY        BRANCH IF FIRST
         BNE   RESTART                  TEST POSITION NOT ZERO
         CLI   SIX(IOREG),FIFTY9        BRANCH IF
         BH    RESTART                  1.ILLEGAL TEST REQUESTED
         BE    LOADIT                   2.ADDRESS RETURN TEST
         CLI   SIX(IOREG),FIFTY1        BRANCH IF
         BL    RESTART                  ILLEGAL TEST REQUESTED
         CLI   SIX(IOREG),FIFTY7        BRANCH IF
         BE    RESTART                  ILLEGAL TEST REQUESTED
         LA    CNTREG,FOUR              LOAD POSITION CNT TO BE CHECKED
         LA    ADREG,EIGHT(IOREG)       LOAD ADDR OF DC&DS SELECTION
TESTHDR  CLI   ZERO(ADREG),FIFTY        BRANCH IF
         BL    RESTART                  ILLEGAL SELECTION
         CLI   ZERO(ADREG),FIFTY9       BRANCH IF
         BH    RESTART                  ILLEGAL SELECTION
         LA    ADREG,ONE(ADREG)         POINT TO NEXT POSITION
         BCT   CNTREG,TESTHDR           GO CHECK NEXT POSITION
         CLI   TEN(IOREG),FIFTY2        BRANCH IF
         BNH   LOADIT
RESTART  NI    IOBINCAM,ONLTT           INDICATE UNSUCCESSFUL
         BR    RTNREG                   RETURN
         SPACE 2
PATH3270 EQU   *
         MVI   IOBERCCW+THREE,CHAR1     SET UP TO LOAD IGC1206F
         MVI   IOBERCCW+FOUR,CHAR2
         B     LOADIT                   GO XCTL
         SPACE 2
BISNTST  MVI   IOBERCCW+FOUR,CHARD      SET UP TO LOAD DO6F   BSC
LOADIT   LA    PARREG,IOBERCCW          GET ADDRESS OF MODULE NAME
         XCTL  EPLOC=(3)                LINK TO NEXT LOAD
ABEND    ABEND DEBCHK                  DEBCHK ABEND         L5 @ZA02806
         SPACE 4
P0       EQU   X'15'
MAX      EQU   P0
DEBCHK   EQU   X'16E'                 ABEND CODE FOR DEBCHK L5 @ZA02806
P2       EQU   X'04'
P4       EQU   X'08'
SP       EQU   X'01'                    SPACE CHARACTER
C0       EQU   X'13'                    CORRS 0
C4       EQU   X'10'                    CORRS 4
C8       EQU   X'0E'                    CORRS 8
         SPACE 4
BADBITS  DC    0F'0',X'0000001E'
SCANTEST DC    X'0202'                   TYPE 11 TEST
FRAMETST DC    X'0215'                  TYPE 10 TEST
ZERO2740 DC    X'1515'                  ZEROS IN 2740 CODE
         SPACE 4
ONLTT    EQU   X'00'
TYP1050  EQU   X'31'
TYP1050X EQU   X'17'
TTY1050X EQU   X'32'
TYP2740X EQU   X'18'
BISYNC   EQU   X'90'
MOD2760  EQU   X'C2'                    CODE FOR 2760 TEST MODULE
         SPACE 4
H36      DC    H'36'
A3284    EQU   X'0A'                    3284 DEVICE TYPE
A3286    EQU   X'0B'                    3286 DEVICE TYPE
DVCLASS  EQU   18                       DEVICE CLASS IN UCB
UNITYPE  EQU   19                       UNIT TYPE IN UCB
CHAR1    EQU   C'1'                     USED TO LOAD IGC1206F IF A
CHAR2    EQU   C'2'                     LOCAL 3270 DVC
CHAR5    EQU   C'5'
CHARD    EQU   C'D'
         SPACE 4
ZERO     EQU   X'00'
ONE      EQU   X'01'
TWO      EQU   X'02'
THREE    EQU   X'03'
FOUR     EQU   X'04'
FIVE     EQU   X'05'
SIX      EQU   X'06'
SEVEN    EQU   X'07'
EIGHT    EQU   X'08'
NINE     EQU   X'09'
TEN      EQU   X'0A'
FOURTEEN EQU   X'14'
FIFTEEN  EQU   X'15'
NINETEEN EQU   X'19'
TWENTY   EQU   X'20'
THIRTY3  EQU   X'33'
FIFTY    EQU   X'50'
FIFTY1   EQU   X'51'
FIFTY2   EQU   X'52'
FIFTY6   EQU   X'56'
FIFTY7   EQU   X'57'
FIFTY9   EQU   X'59'
X0B      EQU   X'0B'
X0E      EQU   X'0E'
         EJECT
         DCBD  DSORG=BX,DEVD=BS
         EJECT
         IHADECB
         EJECT
         IEZDEB LIST=YES
         EJECT
         IEZIOB
         EJECT
         IKJTCB DSECT=YES,LIST=YES                          L5 @ZA02806
         EJECT
UCB      DSECT
         IEFUCBOB LIST=YES
         SPACE 5
         END
