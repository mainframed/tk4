         TITLE 'IGG019PD - CHANNEL END/ABNORMAL END APPENDAGE WTTA'
IGG019PD CSECT
         SPACE 2
* STATUS -          CHANGE LEVEL VS2/R3    OCT  1974
         SPACE 2
* FUNCTION -        SERVICE BTAM DEPENDENT WTTA I/O INTERRUPTS
*                   AND PCI INTERRUPTIONS.
*                        1. GIVE CONTROL TO THE PCI APPENDAGE
*                        2. PROCESS THE LAST CHARACTER OF THE
*                           MESSAGE AND GIVE CONTROL TO THE
*                           NORMAL CHANNEL END APPENDAGE.
*                        3. PROVIDE CHANNEL PROGRAM RESTARTS
         SPACE 2
* ENTRY POINT -     ENTRY IS TO 1ST EXECUTABLE INSTRUCTION FROM IOS
         SPACE 2
* INPUT -           REGISTER  2 - ADDRESS OF THE IOB
*                   REGISTER  4 - ADDRESS OF THE DCB
*                   REGISTER  7 - ADDRESS OF THE UCB
*                   REGISTER 14 - RETURN ADDRESS
         SPACE 2
* OUTPUT -          1.  GIVE CONTROL TO THE PCI APPENDAGE
*                   2.  GIVE CONTROL TO THE NORMAL CHANNEL END APP.
*                   3.  CHANNEL PROGRAM RESTARTS
         SPACE 2
* EXTERNAL ROUTINES
*                   NONE
         SPACE 2
* EXIT -            RETURN TO IOS VIA REGISTER 14 + 8 FOR RESTART OF CP
*                                                 + 0 WHEN THERE IS PCI
*                   INTERRUPT WITH SPECIAL CONDITIONS
*                   GIVE CONTROL TO THE NORMAL CE APPENDAGE
*                   GIVE CONTROL TO THE PCI APPENDAGE
         SPACE 2
* IN LINE ROUTINES -THE TP OP CODE IS USED AS AN INDEX TO BRANCH TO
*                        THE APPROPRIATE ROUTINE WITHIN THE CHANNEL
*                        END APPENDAGE. THE SEQUENCE OF PROCESSING
*                        IS INITIALIZATION AND CHECKING FOR ANY
*                        ERRORS, TP OP CODE ROUTINE, AND THEN EXITING.
*                        THE VARIOUS ROUTINES ARE -
*                        1. TP01 - A PREPARE COMMAND HAS BEEN HALTED
*                            BY A HALT I/O AS A RESULT OF A RESETPL
*                            MACRO INSTRUCTION.
*                        2. HIOREAD - A READ COMMAND HAS BEEN HALTED
*                            BY A HALT I/O AS A RESULT OF A RESETPL
*                            MACRO INSTRUCTION.
*                        3. TP07 - THE RECEIVED AND EXPECTED TERMINAL
*                            IDENTIFICATION ARE COMPARED.
*                            IF THE COMPARISON IS CORRECT, THE NEXT
*                            READ CCW IS STARTED, OTHERWISE THE OPERA-
*                            TION IS STOPPED AND THE CONTROL WILL
*                            RETURN TO THE USER.
*                        4. TP11 - A MESSAGE HAS BEEN RECEIVED OR SENT
*                        5. TPXX - CONTROL IS GIVEN TO THE NORMAL
*                            CHANNEL END APPENDAGE.
         SPACE 2
* ATTRIBUTES -      THIS ROUTINE IS REENTRANT, ENABLED, RUNS IN
*                   SUPERVISOR STATE WITH THE LOCAL LOCK HELD
*                   BY IOS.
*
*
* CHANGE ACTIVITY
*
*        ZA00533  (10/21/74)
*        ZA05411  (10/09/75)
*        ZA26199  (01/03/78)
*        ZA26279  (01/03/78)
*
*
         EJECT
IOBREG   EQU   2                        INPUT/OUTPUT BLOCK ADDRESS
SAVE13   EQU   3                        REG 13 SAVE              YM4055
DCBREG   EQU   4                        DATA CONTROL BLOCK ADDRESS
UCBREG   EQU   7                        UNIT CONTROL BLOCK ADDRESS
WREG     EQU   8                        ADDRESS WORK REGISTER
W2REG    EQU   9                        ADDRESS WORK REGISTER
ECBREG   EQU   10                       EVENT CONTROL BLOCK ADDRESS
ICREG    EQU   11                       ADDRESS WORK REGISTER
BASEREG  EQU   12                       BASE REGISTER
CCWREG   EQU   13                       CHANNEL COMMAND WORD ADDRESS
SAVEREG  EQU   13                       SAVE AREA ADDRESS        YM4055
RETREG   EQU   14                       RETURN ADDRESS
BRREG    EQU   15                       ENTRY POINT ADDRESS
         USING IECTDECB,ECBREG
         USING IECTIOB,IOBREG
         USING IHADCB,DCBREG
         USING *,BASEREG
         STM   RETREG,W2REG,DISP0(SAVEREG) SAVE REGISTERS        YM4055
         LR    BASEREG,BRREG            LOAD BASE REGISTER     @ZA05411
         B     AROUND0             BRANCH AROUND ADCONS     LD @ZA00533
         DS    0F                  FULL WORD ALIGNMENT      LD @ZA00533
*
*        THE FOLLOWING TWO ADCONS MUST REMAIN AT OFFSETS X'0C' AND
*        X'10' SINCE BTAM OPEN (IGG0193S) MOVES IN THE ADDRESSES OF
*        THE PCI APPN AND THE CE APPN.
*
MCPCIAPP DC    A(0)                     PCI APPENDAGE ADDRESS
MBENDAPP DC    A(0)                     CHANNEL END APPENDAGE ADDRESS
AROUND0  EQU   *                                            LD @ZA00533
         LR    SAVE13,SAVEREG           SAVE SAVEAREA ADDRESS  @ZA05411
         B     AROUND                   BRANCH AROUND MODULE ID  YM4055
         DC    C'IGG019PD'              MODULE ID                YM4055
         DC    C'00016610'              PTF NUMBER
         DS    0F                                                YM4055
         DC    XL60'00'                 PATCH AREA               YM4055
AROUND   EQU   *                                                 YM4055
         L     CCWREG,IOBCSW            LOAD CCW ADDRESS + 8     YM4055
         LA    CCWREG,DISP0(CCWREG)     CLEAR HIGH ORDER BYTE    YM4055
         SH    CCWREG,EIGHT             POINT TO CCW             YM4055
         CLC   IOBCSW+DISP4(LEN2),STATUS     PCI ALONE           YM4055
         BE    PCIAPP                   BRANCH IF YES
         TM    IOBCSW+DISP5,PCI         IS PCI BIT PRESENT       YM4055
         BNO   CEAPPWT                  IF NOT, BRANCH TO CE.APP WTTA
         TM    IOBCSW+DISP5,PC          PROGRAM CHECK            YM4055
         BO    PCIAPP                   BRANCH IF YES
         CLI   0(CCWREG),X'01'          IS A COMMAND WRITE
         BNE   WTAPP00                  BRANCH IF NOT
         CLC   6(2,CCWREG),H1           IS CCW COUNT EQUAL AT 1
         BE    CEAPPWT                  BRANCH IF YES
WTAPP00  EQU   *
         NI    IOBCSW+DISP5,MASKALL-PCI-IL    TURN OFF PCI AND   YM4055
*                                       IL FLAGS                 YM4055
         CLC   IOBCSW+DISP6(LEN2),DISP6(CCWREG)   TEST FOR       YM4055
*                                      RESIDUAL CNT = CCW CNT    YM4055
         BNE   EXIT                     BRANCH IF UNEQUAL
         TM    5(CCWREG),X'40'          IS THE FIRST PCI
         BO    EXIT                     BRANCH IF YES
         XC    IOBCSW+DISP6(LEN2),IOBCSW+DISP6  CLEAR RESIDUAL   YM4055
*                                                 COUNT          YM4055
         L     CCWREG,8(CCWREG)         LOAD THE PREVIOUS BUFFER ADDR
         LA    CCWREG,8(CCWREG)         INCREMENT THIS ADDR BY 8
         STCM  CCWREG,SEVEN,IOBCSW+DISP1 STORE IT IN THE CSW     YM4055
         B     EXIT
PCIAPP   EQU   *                                               @ZA26279
         LR    SAVEREG,SAVE13           RESTORE SAVEAREA ADD   @ZA26279
         LM    RETREG,W2REG,DISP0(SAVEREG) RESTORE REGISTERS   @ZA26279
         L     BRREG,MCPCIAPP           LOAD PCI APPENDAGE ADD @ZA26279
         BR    BRREG                    BRANCH TO PCI APPENDAGE
         EJECT
CEAPPWT  L     ECBREG,IOBECBPT          LOAD ADDRESS OF DECB
         L     CCWREG,IOBCSW            LOAD CCW ADDRESS
         SR    ICREG,ICREG              CLEAR ICREG REGISTER
         TM    IOBFLAG1,IOBEXE          IS ERP IN CONTROL
         BO    EXIT                     BRANCH IF YES
         SH    CCWREG,EIGHT             SUBTRACT 8 TO LAST CCW
         BP    WTAPP01
         TM    IOBSIOCC,X'30'           TEST FOR OPERATIONAL SIO
         BO    SIOCC1                   IF NOT,BRANCH
         OI    DECERRST,SNO             SET ON SHOULD NOT OCCUR BIT
         B     CSWADR
WTAPP01  EQU   *
         TM    IOBCSW+4,X'F0'           TEST FOR ERROR IN CSW STATUS
         BNZ   SIOCC                    BRANCH IF YES
         TM    IOBCSW+5,X'3F'           TEST CSW CHANNEL STATUS
         BZ    NORMAL                   BRANCH IF NO ERRORS
SIOCC    TM    IOBSIOCC,X'30'           TEST SIO COMPLETION CODE
         BNO   SIOCC10                  BRANCH IF OPERATIONAL SIO
SIOCC1   OI    DECERRST,X'80'           SET ON BIT 0 ERROR STATUS
         B     CSWADR
SIOCC10  TM    IOBSIOCC,X'10'           TEST FOR CSW STORED
         BZ    TPXX                     BRANCH IF NOT
CSWADR   MVC   IOBCSW+1(3),IOBSTART+1   MOVE START ADDRESS
         L     WREG,IOBCSW              LOAD IT,
         LA    WREG,8(WREG)             ADD 8
         ST    WREG,IOBCSW              AND STORE
         L     CCWREG,IOBSTART          LOAD START ADDRESS
         B     TPXX
NORMAL   MVC   TPCODE,5(CCWREG)         MOVE TPOP CODE
         NI    TPCODE,TPMSK             SET OFF BITS 0-1 OF TPOP CODE
         CLI   TPCODE,X'04'             DETERMINE BRANCH TABLE FOR
         BE    TPRDWR                   NORMAL ENTRIES-DEPENDS UPON OF
         CLI   TPCODE,X'07'             IS READ TERMINAL ID
         BE    TPTERMID                 BRANCH IF YES
         CLI   TPCODE,X'01'             THE TP CODE
         BE    PREPASNS
         CLI   TPCODE,X'11'
         BE    TPRDWR
         CLI   TPCODE,TPBRK             IS A WRITE BREAK
         BE    TPBREAK                  BRANCH IF YES
         B     TPXX
TPBREAK  LA    ICREG,4(ICREG)           ADD 4 TO ICREG
TPRDWR   LA    ICREG,4(ICREG)           ADD 4 TO ICREG
TPTERMID LA    ICREG,4(ICREG)           ADD 4 TO ICREG
PREPASNS LA    ICREG,0(ICREG)           ICREG KEEPS ITS VALUE
         B     TPOP(ICREG)
TPOP     B     TP01                     PREPARE-SENSE     01
         B     TP07                     READ TERM. ID.    07
         B     TP11                     READ/WRITE TEXT   11
         B     TP23                     WRITE BREAK       23
         EJECT
TP01     CLI   IOBECBPT,CCHIO           IS LINE STOPPED BY HALT I/O
         BNE   TP01A                    BRANCH IF NOT
         NI    IOBFLAG1,MASKALL-IOBEX   SET OFF IOB EXCEPTION FLAG
         CLI   IOBCSW+4,CEDEUE          TEST FOR CE-DE UNIT EXCEPTION
         BNE   REXITA                   BRANCH TO RESTART
         OI    DECFLAGS,EOTWTFLG        SET ON EOT FLAG
         NI    DECRESPN,MASKALL-MASKALL RESET DECRESPN
         B     TP11G
TP01A    TM    IOBSENS0,TIMOUT+INTREQ   IS TIME OUT OR INTERVENTION
*                                       REQUIRED PRESENT
         BM    TP11G
         B     TP11E                    SHOULD NOT OCCUR
         SPACE 3
HIOREAD  EQU   *                                                 YM4055
         L     ICREG,0(CCWREG)          LOAD BUFFER OR AREA ADDRESS
         AH    ICREG,6(CCWREG)          ADD CCW COUNT
         SH    ICREG,0(CCWREG)          SUBTRACT RESIDUAL COUNT
         ST    ICREG,0(CCWREG)          AND STORE IN AREA CCW ADDRESS
         MVC   6(2,CCWREG),IOBCSW+6     MOVE RESIDUAL CNT AS CCW CNT
         B     REXIT                    BRANCH TO RESTART
         SPACE 3
TP07     NI    IOBFLAG1,MASKALL-IOBEX   SET OFF IOB EXCEPTION FLAG
         LH    ICREG,6(CCWREG)          LOAD IDENT COUNT
         LA    ICREG,1(ICREG)           INCREMENT BY 1
         L     WREG,0(CCWREG)           LOAD ADDR OF IDENT CHAR
         LA    W2REG,0(WREG,ICREG)      LOAD ADDR OF COMPARE CHAR
         BCTR  ICREG,0                  DECREMENT COUNT BY 1
         BCTR  ICREG,0                  DECREMENT COUNT BY 1
         EX    ICREG,IDCMP              COMPARE IDENTIFICATION
         BE    IDGOOD                   IF GOOD, BRANCH
         OI    DECFLAGS,NOCOMPAR        SET FLAG FOR BAD COMPARISON
         B     TPXX
IDGOOD   NI    DECFLAGS,MASKALL-NOCOMPAR RESET FLAG FOR BAD COMPARISON
         B     REXITA                   EXECUTE NEXT CCW
IDCMP    CLC   0(0,WREG),0(W2REG)       COMPARE IDENT INSTRUCTION
         EJECT
TP11     NI    IOBFLAG1,MASKALL-IOBEX   SET OFF IOB EXCEPTION FLAG
         CLI   0(CCWREG),X'01'          IS COMMAND WRITE
         BE    WRITE                    BRANCH IF YES
         CLC   6(2,CCWREG),IOBCSW+6     TEST FOR CCW CNT=RESIDUAL COUNT
         BE    TP11C                    BRANCH IF YES
         L     ICREG,0(CCWREG)          LOAD BUFFER OR AREA ADDRESS
         LA    ICREG,0(ICREG)           CLEAR TOP REGISTER
         ST    ICREG,SAVEAREA           SAVE LAST BUFFER ADDRESS
         AH    ICREG,6(CCWREG)          ADD CCW COUNT
         SH    ICREG,IOBCSW+6           SUBTRACT RESIDUAL COUNT
         BCTR  ICREG,0                  DECREMENT BY 1
         CLI   0(ICREG),WRU             TEST FOR LAST CHAR.=WRU
         BNE   TP11B                    BRANCH IF NOT
         TM    DCBBQFLG,IAMYES          TEST FOR IAM=YES IN DCB
         BNO   TP11B                    BRANCH IF NOT
         OI    DECFLAGS,WRUWTFLG        SET ON WRU FLAG
TP11A    LH    WREG,IOBCSW+6            LOAD RESIDUAL COUNT
         LA    WREG,1(WREG)             INCREMENT IT BY 1
         CH    WREG,6(CCWREG)           TEST FOR RESIDUAL CNT=CCW CNT
         BNE   TP11A1                   BRANCH IF INEQUAL
         L     ICREG,DECAREA            LOAD FIRST BUFFER ADDRESS
         TM    DCBBFTEK,DYNBF           IS DYNAMIC BUFFERING SPECIFIED
         BZ    TP11A1                   BRANCH IF NOT
         LA    ICREG,4(ICREG)           POINT ON DATA AREA ADDRESS
         C     ICREG,SAVEAREA           IS LAST BUFFER THE 1ST BUFFER
         BE    TP11A1                   BRANCH IF YES
         L     ICREG,SAVEAREA           LOAD THE LAST BUFFER ADDRESS
         MVI   0(ICREG),X'1F'           MOVE A LETTER SHIFT AS 1ST CHAR
         BCTR  WREG,0                   DECREMENT RESIDUAL CNT BY 1
TP11A1   EQU   *
         STH   WREG,IOBCSW+6            AND STORE IN RESIDUAL COUNT
         B     TP11C
TP11B    CLC   0(1,ICREG),DCBWTEOM      TEST FOR LAST CHAR.=EOM
         BE    TP11A                    BRANCH IF YES
         CLC   0(1,ICREG),DCBWTEOT      TEST FOR LAST CHAR.=EOT
         BNE   TP11C                    BRANCH IF NOT
         OI    DECFLAGS,EOTWTFLG        SET ON FLAG
         B     TP11A
TP11C    TM    IOBCSW+4,UC              TEST FOR UNIT CHECK
         BNO   TPXX                     BRANCH IF NOT
         TM    IOBSENS0,TIMOUT          TEST FOR TIME OUT BIT IN SENSE
         BNO   TP11D                    BRANCH IF NOT
         OI    DECFLAGS,EOTWTFLG        SET ON EOT FLAG
         B     TPXX
TP11D    TM    IOBSENS0,LOSTDATA        TEST FOR LOST DATA BIT IN SNS
         BNO   TP11D1                   BRANCH IF BIT ABSENT
         NI    IOBSENS0,MASKALL-LOSTDATA TURN OFF LOST DATA BIT
         B     TPXX
TP11D1   TM    IOBSENS0,DATACHK+OVERRUN IS DATA CHECK OR OVERRUN BIT
         BM    TP11E+4                  BRANCH IF YES
TP11E    OI    DECERRST,SNO             SET ON SHOULD NOT OCCUR BIT
         OI    IOBFLAG1,IOBEX           SET ON IOB EXCEPTION FLAG
         B     TPXX
         EJECT
WRITE    TM    DECFLAGS,X'02'           THRESH RETURN AFTER CONTENTION
         BNO   WRITE1                   BRANCH IF NOT
         MVI   IOBECBPT,CCNORM          YES, SET NORMAL COMPLETION
         NI    DECFLAGS,X'FD'           RESET TEMPORARY FLAG
         B     TPXX                     AND EXIT
WRITE1   CLI   IOBECBPT,CCNORM          TEST FOR NORMAL COMPLETION
         BNE   TPXX                     BRANCH IF NOT
         TM    IOBCSW+4,UE              TEST FOR UNIT EXCEPTION
         BNO   TP11F                    BRANCH IF NOT
         B     TP11I
TP11F    TM    IOBCSW+4,UC              TEST FOR UNIT CHECK
         BNO   TPXX                     BRANCH IF NOT
         TM    IOBSENS0,DATACHK         TEST FOR DATA CHECK IN SENSE
         BNO   TP11E                    BRANCH IF NOT
         B     TP11I
TP11G    EQU   *
         TM    DCBERROP,LERCNT          ARE LINE ERROR COUNTS SPECIFIED
         BZ    TPXX                     IF NOT, EXIT
         SR    ICREG,ICREG              CLEAR ICREG REGISTER
         IC    ICREG,IOBUCBX            LOAD LINE NUMBER
         LA    W2REG,20                 MULTIPLY BY SIZE OF ERROR BLOCK
         MR    WREG,ICREG
         A     W2REG,DCBLERB            ADD ERROR BLOCK START ADDRESS
         CLI   IOBECBPT,CCHIO           IS LINE STOPPED BY HALT I/O
         BE    TP11H                    BRANCH IF YES
         IC    ICREG,CDC(W2REG)         LOAD DATA CHECK COUNTER
         LA    ICREG,1(ICREG)           INCREMENT BY 1
         STC   ICREG,CDC(W2REG)         AND STORE IT IN DATA CHECK CNT
         IC    WREG,CDCT(W2REG)         LOAD THRESHHOLD DATA CHECK CNT
         CR    ICREG,WREG               COMPARE THESE COUNTS
         BNE   TPXX
         OI    IOBFLAG1,IOBEX           SET ON IOB EXCEPTION FLAG
         BCTR  ICREG,0                  DECREMENT BY 1
         STC   ICREG,CDC(W2REG)         AND STORE IT IN DATA CHECK CNT
         OI    DECFLAGS,X'02'           SET TEMPORARY FLAG
         B     TPXX
TP11H    IC    ICREG,CNT(W2REG)         LOAD TRANSMISSION COUNT
         BCTR  ICREG,0                  DECREMENT BY 1
         STC   ICREG,CNT(W2REG)         UPDATE TRANSMISSION COUNT
         SPACE 3
TPXX     LR    SAVEREG,SAVE13               RESTORE SAVEAREA   @ZA26199
         LM    RETREG,W2REG,DISP0(SAVEREG)  RESTORE REGISTERS  @ZA26199
         L     BRREG,MBENDAPP           LOAD NORMAL END APPENDAGE ADDR
         BR    BRREG                    AND BRANCH
         EJECT
TP11I    EQU   *
         MVC   IOBERINF(1),IOBSENS0     SAVE SENSE DATA
         MVC   IOBERINF+1(7),IOBCSW+1   SAVE CSW DATA
         MVC   IOBERINF+8(8),0(CCWREG)  SAVE FAILING CCW
         LA    CCWREG,IOBCPA            LOAD CHANNEL PROGRAM ADDRESS
         MVC   0(8,CCWREG),WRITEBRK     MOVE WRITE BREAK CCW
         B     REXIT
         SPACE 3
TP23     EQU   *
         TM    IOBCSW+4,UC              TEST FOR UNIT CHECK
         BO    TP11E                    BRANCH IF PRESENT
         MVC   IOBSENS0(1),IOBERINF     RESTORE SENSE DATA
         MVC   IOBCSW+1(7),IOBERINF+1   RESTORE CSW DATA
         L     CCWREG,IOBCSW            PICK UP CCW POINTER
         SH    CCWREG,EIGHT             ADJUST TO FAILING CCW
         MVC   0(8,CCWREG),IOBERINF+8   RESTORE FAILING CCW
         OI    DECFLAGS,CONTFLG         SET ON CONTENTION FLAG
         TM    IOBCSW+4,UC              TEST FOR UNIT CHECK
         BO    TP11G                    BRANCH IF YES
         B     TPXX
         EJECT
REXITA   LA    CCWREG,8(CCWREG)         UPDATE CCW POINTER
REXIT    ST    CCWREG,IOBSTART          STORE CCW START ADDRESS
         NI    IOBFLAG1,MASKALL-IOBEX   SET OFF IOB EXCEPTION FLAG
         LR    SAVEREG,SAVE13           RESTORE SAVE AREA ADDR   YM4055
         LM    RETREG,W2REG,DISP0(SAVEREG)  RESTORE REGISTERS    YM4055
         B     8(RETREG)                RETURN TO IOS+8
         SPACE 3
EXIT     LR    SAVEREG,SAVE13           RESTORE SAVE AREA ADDR   YM4055
         LM    RETREG,W2REG,DISP0(SAVEREG)  RESTORE REGISTERS    YM4055
         BR    RETREG                   RETURN TO IOS
         EJECT
WRITEBRK DC    XL8'0D00000020230004'
SAVEAREA DC    F'0'
EIGHT    DC    H'8'
H1       DC    H'1'
TPCODE   DC    X'00'
STATUS   DC    XL2'0080'
PCI      EQU   X'80'
IL       EQU   X'40'                    INCORRECT LENGTH
PC       EQU   X'20'
UE       EQU   X'01'
UC       EQU   X'02'
CEDE     EQU   X'0C'
CEDEUE   EQU   X'0D'
CEDEUC   EQU   X'0E'
COMREJ   EQU   X'80'
INTREQ   EQU   X'40'
BUSOUT   EQU   X'20'
EQUCHK   EQU   X'10'
DATACHK  EQU   X'08'
OVERRUN  EQU   X'04'
LOSTDATA EQU   X'02'
TIMOUT   EQU   X'01'
DYNBF    EQU   X'08'
CNT      EQU   10                       NUMBER OF TRANSMISSIONS
CDC      EQU   11
CDCT     EQU   15
LERCNT   EQU   X'08'
WRU      EQU   X'32'
EOTWTFLG EQU   X'04'
WRUWTFLG EQU   X'01'
CONTFLG  EQU   X'10'
TPBRK    EQU   X'23'
NOCOMPAR EQU   X'10'
HIOFLAG  EQU   X'80'
MASKALL  EQU   X'FF'
IAMYES   EQU   X'20'
SNO      EQU   X'40'
CCNORM   EQU   X'7F'                    NORMAL COMPLETION CODE
CCHIO    EQU   X'48'
IOBEX    EQU   X'04'
IOBEXE   EQU   X'24'
TPMSK    EQU   X'3F'
DISP0    EQU   0                        DISPLACEMENT OF 0        YM4055
DISP1    EQU   1                        DISPLACEMENT OF 1        YM4055
DISP4    EQU   4                        DISPLACEMENT OF 4        YM4055
DISP5    EQU   5                        DISPLACEMENT OF 5        YM4055
DISP6    EQU   6                        DISPLACEMENT OF 6        YM4055
DISP40   EQU   40                       DISPLACEMENT OF 40       YM4055
LEN2     EQU   2                        LENGTH OF 2              YM4055
SEVEN    EQU   7                        LENGTH FOR STCM INSTR.   YM4055
         CNOP  0,8
         IECTDECB
         IECTIOBX
         DCBD  DSORG=BX,DEVD=WT
         END
