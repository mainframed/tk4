         TITLE 'IECTTRNS - TRANSLATE MACRO SUBROUTINE'
IECTTRNS CSECT
*00010006001000,006200,010800,011200,011800,012200                 000C
*
* STATUS            CHANGE LEVEL  001   NOVEMBER 14, 1967
*
*******************************************************************
*
*              FOLLOWING FIXES APPLIED
*        AZ04193  (09/05/75)
*        AZ12135  (07/21/76)
*
********************************************************************
*
* FUNCTION -        TRANSLATE STORAGE BYTES FROM EBCDIC TO TELECOMMUN-
*                   ICATION DEVICE TRANSMISSION CODES
         SPACE 2
* ENTRY POINT -     ENTRY IS TO 1ST EXECUTABLE INSTRUCTION FROM IOS
         SPACE 2
* INPUT -           REGISTER  1 - ADDRESS OF PARAMETER LIST
*                   REGISTER 14 - RETURN ADDRESS
         SPACE 2
* OUTPUT -          TRANSLATED DATA IN AREAS SPECIFIED BY PARAMETER
*                   LIST
         SPACE 2
* IN LINE ROUTINES -LOAD THE PARAMETERS, DCB ADRS, TRANSLATE TABLE
*                   ADRS AND AREA ADRS, AND   DECREMENT THE COUNT BY
*                   256 UNTIL LESS THAN 256 BYTES REMAIN TO BE
*                   TRANSLATED.  TRANSLATE 256 BYTES WITH EACH DECRE-
*                   MENT.  WHEN LESS THAN 256 BYTES REMAIN ADD BACK
*                   255 AND TRANSLATE THE REMAINING BYTES.  WHEN
*                   DYNAMIC BUFFERING IS SPECIFIED THE COUNT CAN BE
*                   OBTAINED FROM THE DCB FOR THE FIRST BUFFER.  A
*                   CHAIN OF BUFFERS IS ASSUMMED AND THE LAST BUFFER
*                   IS DETERMINED BY LOOKING FOR A FULL WORD OF ZEROS
*                   AT THE START OF THE BUFFER (AREA).
         EJECT
         USING *,15
         B     START
         DC    C'IECTTRNS'
         DC    CL8'&SYSDATE'
         DS    0F
PATCH    DC    16C'TRNS'             PATCH AREA
START    STM   DREG,NREG,28(SREG)       SAVE REGISTERS             000C
         LM    DREG,AREG,0(PREG)        LOAD PARAMTER LIST INTO REGS
         LA    CREG,255                 LOAD MAXIMUM COUNT
         LR    LREG,0                   LOAD GIVEN COUNT
         LTR   ZEROREG,ZEROREG         TEST COUNT REGISTER       A38564
         BZ    TRNS0Z                  IF ZERO EXIT              A38564
         BNM   TRNS0A                  IF NOT 'S' CODE,BRANCH    A38564
         LTR   DREG,DREG               TEST DCB ADDRESS          A38564
         BZ    TRNS0Z                  'S' CODE NEEDS DCB ADDR   A38564
         B     TRNS0D                  GO LOAD COUNT FROM DCB    A38564
TRNS0A   EQU   *                                               @ZA04193
         LTR   DREG,DREG            DCB ADDRESS PRESENT ?      @ZA04193
         BZ    TRNS0H                 BRANCH IF NO             @ZA04193
         TM    DCBBFTEK(DREG),DYNBFR    DYNAMIC BUFFERING?      YA01032
         BO    TRNS0E              YES, BRANCH                  YA01032
TRNS0H   EQU   *                                               @ZA04193
         SH    AREG,K4             NET ADR TO START OF AREA     YA01032
TRNS0F   LR    NREG,AREG           SAVE ADDR OF START OF AREA      000C
TRNS0B   SH    LREG,K256           SUB MAX COUNT
         BNP   TRNS0C                   IF NEG OR ZERO, LAST TRNS  000A
         EX    CREG,TRNS0T              TRANSLATE
         AH    AREG,K256                UPDATE ADDRESS             000A
         B     TRNS0B                   LOOP
TRNS0C   AR    LREG,CREG                RESTORE COUNT
         EX    LREG,TRNS0T              TRANSLATE
         LTR   ZEROREG,ZEROREG          'S' CODE?               YA01032
         BM    TRNS0G                   YES, CONTINUE           YA01032
         LTR   DREG,DREG                TEST FOR DCB ADDR      @ZA12135
         BZ    TRNS0Z                   NO, EXIT               @ZA12135
         TM    DCBBFTEK(DREG),DYNBFR    DYNAMIC BUFFERING?      YA01032
         BZ    TRNS0Z                   NO, EXIT                YA01032
TRNS0G   EQU   *                                                YA01032
         CLC   1(3,NREG),K0             IS THIS LAST BUFFER        000C
         BE    TRNS0Z                   IF SO EXIT
         L     AREG,0(NREG)             LOAD ADDRESS OF NEXT BUFFER000C
TRNS0D   LH    LREG,DCBBUFL(DREG)       LOAD COUNT FROM DCB
TRNS0E   SH    LREG,K4                  ADJUST FOR LINK FIELD
         B     TRNS0F                   LOOP                       000C
TRNS0T   TR    4(0,AREG),0(TREG)        TRNSLATE INSTRUCTION
TRNS0Z   LM    DREG,NREG,28(SREG)       RESTORE REGISTERS          000C
         BR    RETREG                   RETURN
         SPACE 5
K0       DC    F'0'
K4       DC    H'4'
K256     DC    H'256'                   MAXIMUM COUNT CONSTANT
DYNBFR   EQU   X'08'                    DYNAMIC BUFFERING IF ON
DCBBUFL  EQU   24                       OFFSET TO BUFFER LENGTH
DCBBFTEK EQU   32
ZEROREG  EQU   0                       LENGTH REGISTER           A38564
         SPACE 5
PREG     EQU   1                        PARAMETER REGISTER
DREG     EQU   2                        DCB ADDRESS
TREG     EQU   3                        TRANSLATE TABLE ADDRESS
AREG     EQU   4                        AREA ADDRESS
CREG     EQU   5                        MAXIMUM COUNT
LREG     EQU   6                        TRANSLATE COUNT
NREG     EQU   7                        AREA START ADDRESS         000C
SREG     EQU   13                       SAVE AREA ADDRESS IN USER'S PRG
RETREG   EQU   14                       RETURN REGISTER
         END
