 TITLE 'IGG0193Q  BTAM LINE GROUP OPEN EXECUTOR - LOAD 3'
***********************************************************************
*                                                                     *
* MODULE NAME: IGG0193Q  (OS/VS)                                      *
*                                                                     *
* DESCRIPTIVE NAME: BTAM LINE GROUP OPEN EXECUTOR - LOAD 3            *
*                                                                     *
* COPYRIGHT: NONE                                                     *
*                                                                     *
* STATUS: RELEASE 2                                                   *
*                                                                     *
* FUNCTION/OPERATION: THIS ROUTINE GETS MAIN STORAGE FOR AND INITIAL- *
*   IZES AN IOB. IT COMPLETES ANALYSIS OF DEVICE TYPE INFORMATION     *
*   PROVIDED IN THE UNIT CONTROL BLOCK TO DETERMINE WHAT TYPE OF      *
*   TERMINAL DEVICES ARE ATTACHED TO THE GROUP OF LINES THAT MAKE UP  *
*   THIS LINE GROUP. IT THEN INITIALIZES THE LINE BY EXECUTING THE    *
*   APPROPIATE SET ADDRESS, ENABLE, OR SET MODE COMMAND.              *
*                                                                     *
*                                                                     *
* ENTRY POINT: ENTRY WILL BE TO THE FIRST BYTE OF THE MODULE BY AN    *
*  'XCTL' TO IGG0193Q THE THIRD  BTAM LOAD. CONTROL WILL BE TRANSFERED*
*   FROM LOAD 2. IT MAY ALSO BE RE-ENTERED FROM THE RELOOP PORTION OF *
*   THIS ROUTINE IF IT DETERMINES THAT THERE IS ANOTHER DATA CONTROL  *
*   BLOCK TO BE OPENED.                                               *
*                                                                     *
* INPUT:  THE REGISTERS 5, 6, 7, AND 8 ARE THE INPUT, AS FOLLOWS,     *
*   5- THE ADDRESS OF THE FIRST ENTRY IN THE COPIED DCB PARAMETER LIST*
*   6- THE ADDRESS OF THE BEGINNING OF THE WHERE-TO-GO TABLE,         *
*   7- THE ADDRESS OF THE CURRENT ENTRY IN THE COPIED DCB PARAMETER   *
*      LIST,                                                          *
*   8- THE ADDRESS OF THE CURRENT EXECUTOR'S ENTRY IN THE WHERE-TO-GO *
*      TABLE.                                                         *
*                                                                     *
*                                                                     *
* OUTPUT: THE REGISTERS 7 AND 8 WILL BE POSITIONED AT THE NEXT ENTRIES*
*   IN THE DCB PARAMETER LIST AND THE WHERE-TO TABLE. ONE IOB PER LINE*
*   WILL HAVE BEEN CONSTRUCTED IN SUBPOOL 250 IN THE USER'S KEY.      *
*   IF DYNAMIC BUFFERING WAS SPECIFIED AND VIRTUAL=VIRTUAL, ONE       *
*   VIRTUAL SUBAREA LIST AND TWO INDIRECT ADDRESSING LISTS WILL       *
*   BE OBTAINED FROM SUBPOOL 250 IN THE USER'S KEY FOR EACH IOB       *
*   IN THE LINE GROUP. DCB FIELDS (DEVTP, IOBAD, EIOBX,               *
*   IFLGS, DEBAD, AND READ/WRITE WILL HAVE BEEN INITIALIZED.          *
*   THIS EXECUTOR'S ENTRY (OR ENTRIES) IN THE WHERE-TO-               *
*   GO TABLE WILL HAVE BEEN UPDATED TO IDENTIFY LOAD 4 (IGG0193S).    *
*                                                                     *
* EXTERNAL ROUTINES: NONE                                             *
*                                                                     *
* EXITS-NORMAL:  THIS ROUTINE EXITS VIA AN XCTL TO THE MODULE         *
*   IDENTIFIED BY THE NEXT NON-ZERO ENTRY IN THE WHERE-TO-GO TABLE,   *
*   NAMELY IGG0193S (BTAM OPEN EXECUTOR - LOAD 4).                    *
*                                                                     *
* EXIT-ERRORS:                                                        *
*     -ERRORS-  THIS ROUTINE EXITS VIA ABEND FOR THE FOLLOWING        *
*   ERROR CONDITIONS-                                                 *
*   ABEND CODE ERROR CONDITION                                        *
*   00098000    DUAL COMMUNICATION INTERFACE B OR DUAL CODE FEATURE   *
*               B WAS NOT SPECIFIED IN THE UCB.                       *
*                                                                     *
* TABLES/WORK AREAS: 1.DCB PARAMETER LIST CONTAINS THE ADDRESS OF EACH*
*  DCB SPECIFIED IN THE OPEN MACRO INSTRUCTION. 2. THE WHERE-TO-GO    *
*  TABLE CONTAINS THE ID AND TTR OF THE ROUTINE NEEDED TO PROCESS THE *
*  DCB WHICH CORRESPONDS TO THIS ENTRY.                               *
*                                                                     *
* NOTES: FOR SECURITY AND INTEGRITY PURPOSES, OPEN PROCESSING WILL    *
*  BE ONLY ON THE COPIED DCB AND COPIED DCB PARAMETER LIST. BTAM      *
*  OPEN LOADS MUST KEEP THE USER DCB AND THE COPIED DCB               *
*  SYNCHRONIZED. UPON COMPLETION OF THIS MODULE, CONTROL IS PASSED    *
*  TO THE FOURTH OPEN MODULE THROUGH THE WHERE-TO-GO TABLE ENTRY.     *
*  BEFORE ISSUEING THE I/O TO INITIALIZE THE LINE, THE COPIED DCB     *
*  WILL BE MAPPED TO THE USER'S DCB.                                  *
*                                                                     *
* CHANGE ACTIVITY AS FOLLOWS:                                         *
*            APARS                           PTMS                     *
*       YA01249  (10/1/73)               YM4084  (10/29/73)           *
*       YA01260  (10/1/73)               YM5663  (12/12/73) PROLOG    *
*       YA02168  (N/A)                   YM5668  (12/14/73) IECRES    *
*       YA02172  (10/5/73)                                            *
*       ZA00535  (11/15/74)        ZA00544  (11/15/74)                *
*       ZA03207  (03/24/75)        ZA03589  (03/20/75)                *
*       AZ04187  (10/06/75)        AZ04865  (10/14/75)                *
*       AZ06399  (12/19/75)                                           *
*       AZ07636  (11/10/75)                                           *
*       AZ08051  (01/29/76)                                           *
*       AZ09356  (04/05/76) - DELETED APARS AZ04187 & AZ04865
*       AZ11430  (06/09/76)
*       AZ13169  (09/03/76)                                           *
*                                                                     *
***********************************************************************
         EJECT
IGG0193Q CSECT
         SPACE 5
RE       EQU   0                       WORK/PARAMREG
RF       EQU   1                       WORK/PARAMREG
RDCB     EQU   2                       COPIED DCB ADDRESS        Y02947
RBASE    EQU   3                       BASE REGISTER
RCORE    EQU   4                       IOB ADDRESS
RPAR     EQU   5                       START OF COPIED DCB       Y02947
*                                      PARAMETER LIST            Y02947
RWTG     EQU   6                       START OF WTG
RPARC    EQU   7                       CURRENT ENTRY IN COPIED   Y02947
*                                      DCB PARAMETER LIST        Y02947
RWTGC    EQU   8                       CURRENT ENTRY IN WTG TABLE
RTIOT    EQU   9                       TIOT ADDRESS
VSLREG   EQU   RTIOT                   VIRTUAL SUBAREA LIST      Y20947
*                                      ADDRESS                   Y02947
RUCB     EQU   10                      CURRENT UCB
IOBNUM   EQU   RUCB                    NUMBER OF IOB'S           Y02947
RDEB     EQU   11                      DEB ADDRESS
IDALREG  EQU   RDEB                    INDIRECT ADDRESSING LIST  Y02947
RB       EQU   12                      WORK REG
RC       EQU   13                      WORK REG
RD       EQU   14                      WORK/PARAMREG
RJ       EQU   15                      WORK/PARAMREG
TCBREG   EQU   RJ                      TCB ADDRESS               Y02947
         SPACE 3
         USING IECTIOB,RCORE
         USING IHADCB,RDCB
         USING IECTDEB,RDEB
         BALR  RBASE,0
         USING *,RBASE
         SPACE 4                                                 Y02947
         B     AROUND                   BRANCH AROUND ID         Y02947
         DC    C'IGG0193Q'              OPEN ID                  Y02947
         DC    C'** MVS *'                                       Y02947
         DC    C'&SYSDATE'         DATE LAST ASSEMBLY       LD @ZA00544
PATCH    DC    XL90'00'                PATCH AREA                Y02947
         DS    0H
         SPACE 4                                                 Y02947
AROUND   L     RDCB,0(RPARC)           ESTABLISH COPIED DCB ADDR Y02947
         L     RDEB,DCBDEBAD            ACCESS POINTER TO DEB    Y02947
         L     RDEB,28(RDEB)            LOAD DEB START FOR DSECT   000D
         L     RUCB,DEBUCBAD           GET UCB ADDRESS             000M
         TM    19(RUCB),X'90'          IS THIS A BSC DEVICE        000M
         BNO   TSTRETRY                                            000M
         MVC   92(4,RDCB),DCBBSTSX      SAVE IRB ENTRY POINT       000M
         SR    RF,RF               CLEAR REGISTER                  000M
         TM    DCBXCODE,USASCII    ASCII CODE TO BE USED           000M
         BO    ASCII               IF SO, BRANCH                   000M
         TM    DCBXCODE,TRANSCD    SIX BIT CODE TO BE USED         000M
         BO    SIXBIT              IF SO, BRANCH                   000M
         LA    RF,TBLOFFST(RF)     ADD OFFSET TO CORRECT TABLE     000M
SIXBIT   LA    RF,TBLOFFST(RF)     ADD OFFSET TO CORRECT TABLE     000M
ASCII    LA    RF,CHARTBL(RF)      GET ADDRESS OF CHAR TABLE       000M
         MVC   DCBBSRSV(TBLOFFST),0(RF)   MOVE IN CONTROL CHARS    000M
         EJECT                                                     000M
TSTRETRY EQU   *                                                   000M
         SR    RTIOT,RTIOT
         IC    RTIOT,DCBDEVTP           LOAD DEVICE OFFSET
         LM    RE,RF,RETRYMSK           GET RETRY MASK
         SLDL  RE,0(RTIOT)              POSITION BIT               4113
         LTR   RE,RE                    TEST SIGN BIT
         BM    IOBST1                   RETRYS ARE POSIBLE
         NI    DCBERROP,ALLON-RDRETRY
         EJECT
IOBST1   SR    RC,RC                                               000D
         IC    RC,SZTABLE(RTIOT)        SET NORMAL SIZE            000D
         TM    DCBBFTEK,DYNBUF    BIT 4 ON INDICATES DYN BUFFERING
         BZ    SETSIZE                  IF NOT, BRANCH             000D
         LA    RC,24(RC)                ADD 24 FOR 3 CCW'S         000D
         SPACE 1                                                 Y02947
* 6 LINES OF CODE DELETED BY APAR ---------------------------- @ZA13169
         SPACE 1                                                 Y02947
SETSIZE  LA    RC,64(RC)                ADD BASIC IOB SIZE         000D
         TM    19(RUCB),X'90'      BSC DEVICE?                  YA01260
         BO    YESBSC              YES, CONTINUE                YA01260
         LA    RC,L32(RC)           NO, EXTEND IOB FOR         @ZA06399
*                                  START STOP SPECIAL CHARS     YA01260
YESBSC   EQU   *                                                YA01260
         STC   RC,DCBEIOBX              STORE IOB SIZE IN IOB      000D
         LA    RF,4(RC)                 INCREASE IOB SIZE BY 4 @ZA03207
*    FOR THE ERP EXTENSION. THERE IS ONE ERP EXT ENTRY PER IOB @ZA03207
         SR    RB,RB                    CLEAR REGISTER             000D
         IC    RB,DEBNMEXT              LOAD NUMBER OF IOB'S       000D
         MR    RE,RB                    MULTIPLY SIZE BY NO OF IOB 000:
         LA    RF,4(RF)                 ADD 4 MORE SINCE THE   @ZA03207
*                      0TH ENTRY OF THE ERP EXTENSION WILL     @ZA03207
*                      CONTAIN THE ADDR OF THE TRANSLATOR.     @ZA03207
*                      ALL OTHER ENTRIES CORRESPOND TO THEIR   @ZA03207
*                      RLN'S.                                  @ZA03207
         LR    RE,RF                    LOAD PRODUCT INTO REG 0    000D
         O     RE,IOBPOOL               OR IN SUBPOOL NUMBER       000E
         L     RJ,4(RWTGC)              ADDRESS OF OPEN WORKAREA Y02947
         USING WORKAREA,RJ              WORKAREA ADDRESSABILITY  Y02947
         SPACE 1                                                 Y02947
         MODESET KEYADDR=DXUKEY,WORKREG=14   ASSUME USER KEY     Y02947
         SPACE 1                                                 Y02947
*    GET CORE FOR IOB'S FROM SUBPOOL 250                         Y02947
         GETMAIN R,LV=(0)               GET CORE FOR THE IOB'S     000D
         SPACE 1                                                 Y02947
         LR    RCORE,RF                 LOAD THE ADDRESS OF THE IOB000D
         SR    RCORE,RC                 SUB IOB SIZE               000D
         SPACE 1                                                 Y02947
         MODESET EXTKEY=DATAMGT         BACK TO DATA MGT KEY     Y02947
         SPACE 1                                                 Y02947
         ST    RCORE,DCBIOBAD           STORE IOB ADRS MINUS SIZE  000D
         STC   RTIOT,DCBDEVTP           STORE DEVICE TYPE          000D
         L     RJ,DISP4(RWTGC)          RESTORE WORKAREA ADDR    Y02947
         SPACE 1                                                 Y02947
         MODESET KEYADDR=DXUKEY,WORKREG=14  ASSUME USER KEY      Y02947
         SPACE 1                                                 Y02947
         SR    RD,RD                    RESET REGISTER FOR RLN CNT 000D
         BCTR  RC,0                     SUBTRACT 1 FOR EXECUTE INST000D
STARTLP  LA    RCORE,1(RC,RCORE)        STEP IOB ADDRESS           000D
         EX    RC,CLEAR                 CLEAR IOB                  000D
         MVI   IOBFLAG1,X'C2'           SET BITS 0,1 AND 6 FOR BTAM000D
         LA    RF,IOBCPA                LOAD THE CHANNEL PROGRAM AD000D
         ST    RF,IOBSTART              STORE IN IOBSTART          000D
         MVC   IOBDCBPT(4),DXUDCBAD     USER'S DCB ADDR TO IOB   Y02947
         STC   RD,IOBUCBX               STORE RELATIVE LINE NUMBER 000D
         LA    RD,1(RD)                 ADD 1                      000D
         BCT   RB,STARTLP               DECREMENT & TEST IOB COUNT 000D
         LA    RCORE,1(RC,RCORE)        ADDR OF ERP EXT OR     @ZA03207
*                            ADDR OF END OF IOB'S              @ZA03207
         IC    RB,DEBNMEXT              GET NUMBER OF IOB'S    @ZA03207
         LA    RB,1(RB)                 THIS ENTRY IS FOR THE  @ZA03207
*                           ADDR OF THE TRANSLATOR (IECVTCCW)  @ZA03207
         SLL   RB,2                     MULTIPLY BY 4, THE     @ZA03207
*                      LENGTH OF EACH ENTRY IN THE ERP EXT     @ZA03207
         BCTR  RB,0                     DECREMENT BY 1 FOR     @ZA03207
*                      THE EXECUTE INSTRUCTION                 @ZA03207
         EX    RB,CLEAR                 CLEAR ALL ENTRIES IN   @ZA03207
*                      THE ERP EXTENSION                       @ZA03207
* 79 LINES OF CODE DELETED BY APAR --------------------------- @ZA13169
NODBEXT  EQU   *                                                 Y02947
         LA    RUCB,DEBUCBAD            GET ADRS OF UCB LIST       000E
         L     RUCB,0(RUCB)             GET ADRS OF UCB            000E
         CLI   DCBDEVTP,INTR1                                      000I
         BE    IRBPNT                                              000I
         CLI   DCBDEVTP,INTR2                                      000I
         BE    IRBPNT                                              000I
         TM    19(RUCB),X'90'           THIS A BISYNCH DEVICE      000E
         BNO   TSTONLT                  IF NOT, BRANCH             000E
         TM    DCBXCODE,GRAPHMSK        1130/2250 GRAPHICS         000E
         BO    IRBPNT                   IF SO, IRB POINTERS NEEDED 000E
TSTONLT  TM    DCBERROP,TRMTST          ONLINE TEST REQUESTED      000E
         BNO   NOPNTRS                  NO POINTERS NEEDED         000E
         SPACE 1                                                 Y02947
IRBPNT   MODESET EXTKEY=ZERO           KEY OF ZERO               Y02947
         SPACE 1                                                 Y02947
         LA    RE,16                    INDICATE NUMBER OF BYTES   000E
         O     RE,IOBPOOL               OR IN SUBPOOL NUMBER       000E
         SPACE 1                                                 Y02947
*    GET CORE FOR IRB'S FROM SUBPOOL 250                         Y02947
         GETMAIN  R,LV=(0)              GET CORE FOR POINTERS      000E
         SPACE 1                                                 Y02947
         XC    0(12,RF),0(RF)           CLEAR AREA                 000E
* 4 LINES OF CODE DELETED BY APAR ---------------------------- @ZA13169
         SPACE 1                                                 Y02947
         MODESET EXTKEY=DATAMGT         BACK TO DATA MGT KEY     Y02947
         SPACE 1                                                 Y02947
         ST    RF,DEBIRBAD-1            STORE ADDRESS IN DEB       000E
         L     RJ,DISP4(RWTGC)          WORKAREA ADDRESS         Y02947
         USING WORKAREA,RJ              WORKAREA ADDRESSABILITY  Y02947
         SPACE 1                                                 Y02947
         MODESET KEYADDR=DXUKEY,WORKREG=14  ASSUME USER KEY      Y02947
         SPACE 1                                                 Y02947
         DROP  RJ                       DROP WORKAREA ADDRESS    Y02947
         EJECT
*                   INITIALIZE THE LINES WITH A SET MODE, SAD AND/OR
*                   AN ENABLE COMMAND AS APPROPRIATE
         SPACE 3
NOPNTRS  LA    RD,DEBUCBAD              POINT TO TABLE OF UCB      000E
*                                  ADDRESSES.
         L     RCORE,DCBIOBAD           CALCULATE ADDRESS OF FIRST IOB.
         SR    RB,RB
         IC    RB,DCBEIOBX
         AR    RCORE,RB
         SR    RC,RC                    LOAD NUMBER OF EXTENTS TO BE
         IC    RC,DEBNMEXT         USED AS A LOOP COUNTER.
*    *    *    *    *    *    *    *    *    *    *    *    *    *    *
SADTEST  L     RUCB,0(RD)               LOAD UCB ADDRESS.
         TM    19(RUCB),X'90'           TEST FOR BXC (VIA ADAPTER TYPE(
         BNO   NOTBSC                   IF NOT BSC, BRANCH
CHKTCU   TM    19(RUCB),X'03'           TEST FOR 2703 T C U        000D
         BO SETMODE                     IF SO, NO FURTHER TESTS
         TM    DCBXMODE,DATAB           TEST FOR DATA INTERFACE B
         BZ    TESTCODE                 IF NOT SPECIFIED GO TEST CODE
         TM    17(RUCB),DATAB           IF SOK IS IT LEGAL
         BZ    AB7                      NO, ABEND              @ZA07636
TESTCODE TM    DCBXMODE,CODEB           TEST FOR TRANSMISSION CODE B
         BZ    SETMODE                  IF NOT SPECIFIED GO SET MODE
         TM    17(RUCB),CODEBUCB        IF SO, IS IT LEGAL
         BZ    AB7                      IF NOT, ABEND
SETMODE  L     RJ,IOBDCBPT             GET DCB ADDR FROM IOB     YM4084
         LA    RJ,DCBXMODE-IHADCB(RJ)  GET ADDRESS OF MODE BYTE  YM4084
*                                      IN USER DCB               YM4084
         MVC   IOBCPA+8(8),ENABLE      MOVE IN SETMODE/SAD CCW   A26896
         MVI   IOBCPA,DISABLE          MOVE IN COMMAND CODE      A26896
         MVC   IOBCPA+1(7),ENABLE+1    COMPLETE CCW              A26896
         OI    IOBCPA+4,CMDCHAIN       CHAIN DISABLE TO SET MODE A26896
         TM    19(RUCB),X'90'          TEST FOR BSC              A26896
         BNO   SADRTNE                 IF NOT, BRANCH            A26896
         ST    RJ,IOBCPA+8             STORE MODE IN CCW         A26896
         MVI   IOBCPA+8,SETMODCM       MOVE IN COMMAND CODE      A26896
         B     TESTAA
AB7      L     1,ERR7
         ABEND (1)
NOTBSC   TM    19(RUCB),X'01'           TEST FOR 2702/3
         BZ    ENABCTL
         TM    19(RUCB),X'0C'
         BZ    SETMODE                                           A26896
ENABCTL  MVC   IOBCPA(8),ENABLE         SET ENABLE CCW IN CHAN PGA30747
         TM    17(RUCB),X'90'           AUTO CALL OR AUTO ANSR   A30747
         BNZ   SETNOP                   BRANCH IF EITHER         A30747
         CLI   19(RUCB),X'82'           TEST FOR 2701 W/ TYPE 3 ADPTR
         BNE   EXCP                     NO, BRANCH               A30747
SETNOP   MVI   IOBCPA,X'03'             MOVE IN NOP COMMAND CODE A30747
         B     EXCP
SADRTNE  SR    RDCB,RDCB                CLEAR REGISTER AND SET UP
         IC    RDCB,17(RUCB)       SAD COMMAND CODE.
         N     RDCB,ANDMASK
         IC    RTIOT,SADCCODE(RDCB)     INSERT ACTUAL COMMAND CODE AND
         STC   RTIOT,IOBCPA+8          STORE IN IOB              A26896
TESTAA   TM    17(RUCB),X'90'           TEST FOR AUTO-CALL OR ANSWER
         BNZ   EXCP                     IF EITHER PRESENT, DO NOT ENABL
         MVC   IOBCPA+16(8),ENABLE     MOVE IN ENABLE COMMAND    A26896
         OI    IOBCPA+12,CMDCHAIN      CHAIN SETMODE TO ENABLE   A26896
*                                  TO SAD COMMAND.
EXCP     EQU   *                                                 Y02947
         LA    RE,SIXTY                 MESSAGE AREA LENGTH      Y02947
         O     RE,POOLNUM               SUBPOOL 250              Y02947
         SPACE 1                                                 Y02947
         GETMAIN R,LV=(0)               GET CORE FOR MSG AREA    Y02947
         SPACE 1                                                 Y02947
         LR    RTIOT,RF                 LOAD BASE FOR MSG AREA   Y02947
         USING REPLYLTH,RTIOT           MSG AREA ADDRESSABILITY  Y02947
         ST    RTIOT,IOBCPA+32         ST ADD OF AREA IN IOB     A28619
         MVI   IOBCPA+32,X'FF'         SET FLAG FOR MSGWTR       A29575
         MVI   REPLYLTH,ZERO           SET REPLY LENGTH FIELD    Y02947
*                                      TO ZERO. THE ERP'S WILL   Y02947
*                                      CHANGE THIS FIELD TO      Y02947
*                                      REPLY LENGTH IF WE MUST   Y02947
*                                      ISSUE WTOR                Y02947
         SPACE 1                                                 Y02947
         MODESET EXTKEY=DATAMGT        DATAMGT KEY               Y02947
         LR    RF,RCORE                SAVE IOB ADDRESS          Y02947
         L     RCORE,DISP4(RWTGC)      WORK AREA ADDRESS         Y02947
         SPACE 1                                                 Y02947
         USING WTG,RWTG                 WTG ADDRESSABILITY       Y02947
         L     RJ,WTGPREFX              WTG PREFIX ADDRESS       Y02947
         STM   RE,RD,IECREGSV-IECPREFX(RJ) SAVE REGS             Y02947
         IECRES INIT,DCBCOPY=FRWKAR                              Y02947
         LR    RJ,RWTG                 REG SAVEAREA ADDR         YM5668
         LM    RE,RD,IECREGSV-IECPREFX(RJ) RESTORE REGS          Y02947
         LR    RCORE,RF                RESTORE IOB ADDRESS       Y02947
         DROP  RWTG                     DROP WTG ADDRESS         Y02947
         USING WORKAREA,RJ              WORKAREA ADDRESSABILITY  Y02947
         L     RJ,4(RWTGC)              WORKAREA ADDRESS         Y02947
         SPACE 1                                                 Y02947
         MODESET KEYADDR=DXUKEY,WORKREG=1     USER KEY           Y02947
         SPACE 1                                                 Y02947
         DROP  RJ                       DROP WORKAREA ADDRESS    Y02947
RETRY    EQU   *                                                 Y02947
         LA    RJ,IOBERCCW                                       Y02947
         ST    RJ,IOBECBPT              STORE ECB ADDRESS IN IOB Y02947
         LA    1,IOBFLAG1               LOAD IOB ADDRESS.
         EXCP  (1)
         MVI   IOBINCAM+1,X'00'         SET BYTE TO ZERO           000E
         MVC   IOBCPA+36(TIMELNG),TIME  MOVE TIME VALUE        @ZA11430
         SPACE 1                                                 Y02947
STIMER   STIMER  WAIT,BINTVL=IOBCPA+36 WAIT ONE HALF SECOND LD @ZA11430
         SPACE 1                                                 Y02947
         L     1,IOBECBPT               LOAD POINTER TO IOB        000E
         TM    IOBFLAG1,X'24'           IS ERP IN CONTROL
         BO    STIMER                   WAIT FOR ERP TO COMPLETE
         TM    0(1),X'7F'               IS ECB POSTED              000E
         BNZ   POSTED                   BRANCH IF YES              000E
         CLI   IOBINCAM+1,X'0D'        HAS COUNT REACHED MAX 7   A26896
         BNL   PURGE                   YES, PURGE I/O       LD @ZA00535
         IC    RF,IOBINCAM+1            GET TIMER COUNT            000E
         LA    RF,1(RF)                 UPDATE COUNT               000E
         STC   RF,IOBINCAM+1            STORE TIMER COUNT          000E
         B     STIMER                                              000E
POSTED   CLI   0(1),X'7F'               TEST FOR NORMAL COMPLETION 000E
         BE    NORMALCP                                          A29575
         CLI   REPLYLTH,ZERO           ERP HAD CONTROL?          Y02947
         BE    SETERR                  NO, MSGWTOR NOT NEEDED    A27612
WTOR     EQU   *                                                 Y02947
         LR    RF,RTIOT                MSG AREA ADDRESS ADDR     Y02947
         MVI   REPLY+DISP8,ZERO        SET COMPLETION FLAG OFF   Y02947
*                                      THE 3RD WORD OF THE REPLY Y02947
*                                      AREA IS THE ECB           Y02947
         SPACE 3                                                 Y02947
         WTOR  MF=(E,(1))              WRITE CONTROL UNIT NOT OP A27612
         SPACE 3                                                 Y02947
         WAIT  ECB=REPLY+DISP8         WAIT FOR REPLY            Y02947
         SPACE 1                                                 Y02947
         CLC   REPLY(4),CONT           RETRY REQUESTED           A27612
         BE    RETRY                                             A27612
         CLC   REPLY(4),POST           LINE IDLE REQUESTED       A27612
         BNE   WTOR                    INVALID RESPONSE, RETRY MEA27612
         B     SETERR                                       LD @ZA00535
*
PURGE    EQU   *                                            LD @ZA00535
         MODESET EXTKEY=DATAMGT    DATA MANAGEMENT KEY      LD @ZA00535
         L     RJ,DISP4(RWTGC)     OPEN WORKAREA ADDR       LD @ZA00535
         USING WORKAREA,RJ         WORKAREA ADDRESSABILITY  LD @ZA00535
         XC    DXCCW(TWELVE),DXCCW CLEAR PURGE PARM LIST    LD @ZA00535
         LA    RDEB,HEX24(RDEB)        POINT TO DEB LOC'0'  L5 @ZA03589
         ST    RDEB,DXCCW          DEB ADDR IN PARM LIST    LD @ZA00535
         MVI   DXCCW,PURGEOPT      HALT I/O OPTION          LD @ZA00535
         PURGE DXCCW                                        LD @ZA00535
         L     RJ,DISP4(RWTGC)     OPEN WORKAREA ADDR       LD @ZA00535
         L     RDEB,28(RDEB)       LOAD DEB START FOR DSECT L5 @ZA03589
         MODESET KEYADDR=DXUKEY,WORKREG=15  USER'S KEY      LD @ZA00535
SETERR   OI    IOBINCAM,SADENER         SET ERROR FLAG IN IOB      000E
NORMALCP EQU   *                                                 Y02947
         LR    RF,RTIOT                MSG AREA ADDRESS          Y02947
         LA    RE,SIXTY                MSG AREA LENGTH           Y02947
         O     RE,POOLNUM              SUBPOOL 250               Y02947
         SPACE 1                                                 Y02947
         FREEMAIN R,LV=(0),A=(1)       FREE MSG AREA             Y02947
         SPACE 1                                                 Y02947
         XC    IOBCPA+32(4),IOBCPA+32  CLEAR WORKAREA IN IOB     A28619
SADLPCHK MVI   IOBINCAM+1,X'00'                                  A29575
         LA    RCORE,0(RB,RCORE)                                   000E
         LA    RD,4(RD)                 GET ADDRESS OF NEXT ENTRY IN
*                                  TABLE OF UCB ADDRESSES.
         BCT   RC,SADTEST               RETURN TO SADTEST IF ALL LINES
*                                  HAVE NOT YET BEEN INITIALIZED.
         DROP  RTIOT                   DROP MSG AREA ADDRESS     Y02947
         SPACE 3
         EJECT
ENDLOD2  EQU   *                                                 Y02947
         SPACE 1                                                 Y02947
         MODESET EXTKEY=DATAMGT         BACK TO DATA MGT KEY     Y02947
         SPACE 1                                                 Y02947
         MVC   0(5,RWTGC),BTAMLOD4      SET UP ID FOR LOAD 4 OF  Y02947
*                                       BTAM EXECUTOR.           Y02947
         L     RJ,4(RWTGC)
         USING WORKAREA,RJ              WORKAREA ADDRESSABILITY  Y02947
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     INCREMENT CURRENT WTG REGISTER.
         LA    RPARC,PLOFF(0,RPARC)     INCREMENT CURRENT PARAMETER
*                                  LIST REGISTER TO NEXT ENTRY.
         CLC   0(2,RWTGC),AMIDCNST      IF THE NEXT WTG ENTRY CALLS FOR
         BCR   8,RBASE             THIS EXECUTOR, RETURN TO BEGINNING.
         CLC   0(2,RWTGC),OPIDCNST      IF NOT, TEST FOR END OF TABLE.
         BNE   RELOOP              NOT END OF TABLE, RETURN TO CHECK
*                                  NEXT ENTRY.
         LR    RPARC,RPAR               REINITIALIZE CURRENT WTG REG
         LA    RWTGC,WAOFF(0,RWTG) AND CURRENT PARAMETER LIST REG.
ZCHECK   CLI   0(RWTGC),X'00'
         BNE   XCTLRTNE            IF NOT ZERO, GO TO TRANSFER CONTROL
         LA    RWTGC,WGOFF(0,RWTGC) IF ZERO, GET NEXT ENTRY AND RETURN
         LA    RPARC,PLOFF(0,RPARC) TO ZCHECK.
         B     ZCHECK
         SPACE 3
XCTLRTNE EQU   *
         LA    RJ,DXCCW12               ADDR DOUBLE WORD LIST    Y02947
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID TO NAME FIELD.
         MVC   14(3,RWTG),2(RWTGC)      MOVE TTR TO WTG TABLE.
         DROP  RJ                       DROP WORKAREA ADDRESS    Y02947
         L     RC,SIXT                  CVT ADDRESS              Y02947
         USING CVT,RC                   CVT ADDRESSABILITY       Y02947
         L     RC,CVTSVDCB              SYS1.SVCLIB DCB          Y02947
         DROP  RC                       DROP CVT ADDRESS         Y02947
         XCTL  DE=(RWTG),SF=(E,(15)),DCB=(RC)                    Y02947
         EJECT
SZTABLE  EQU   *
         SPACE 1
         DC    AL1(S8)   00        1050
         DC    AL1(S9)   01             P
         DC    AL1(SB)   02             AD
         DC    AL1(SA)   03             A
         DC    AL1(SB)   04             D
         SPACE 1
         DC    AL1(S5)   05        2740
         DC    AL1(S8)   06             AD
         DC    AL1(S6)   07             A
         DC    AL1(S7)   08             D
         DC    AL1(S8)   09             ACDX
         DC    AL1(S8)   0A             ACX
         DC    AL1(S8)   0B             CDX
         DC    AL1(S8)   0C             ADX
         DC    AL1(S8)   0D             AX
         DC    AL1(S8)   0E             DX
         DC    AL1(S8)   0F             ACD
         DC    AL1(S6)   10             AC
         DC    AL1(S8)   11             CD
         DC    AL1(S9)   12             CPS
         DC    AL1(S8)   13             CS
         DC    AL1(S8)   14             PS
         DC    AL1(S7)   15             S
         DC    AL1(S6)   16             C
         SPACE 1
         DC    AL1(S7)   17        1060
         DC    AL1(S9)   18             P
         SPACE 1
         DC    AL1(S8)   19        1030
         DC    AL1(S9)   1A             P
         SPACE 1
         DC    AL1(S6)   1B        83B3
         DC    AL1(S0)   1C             TT
         SPACE 1
         DC    AL1(S5)   1D        115A
         DC    AL1(S0)   1E             TT
         SPACE 1
         DC    AL1(S7)   1F        TWX  A
         DC    AL1(S7)   20             AD
         DC    AL1(S7)   21             D
         SPACE 1
         DC    AL1(S8)   22        2848
         SPACE 1
         DC    AL1(S0)   23
         DC    AL1(S0)   24
         SPACE 1
         DC    AL1(S9)   25        BSC1
         SPACE 1
         DC    AL1(SC)   26        BSC2      (A, AD, OR D)       S99245
         SPACE 1
         DC    AL1(SA)   27        BSC3      (P)
         SPACE 1
         DC    AL1(S0)   28
         DC    AL1(S0)   29
         SPACE 1
         DC    AL1(S8)   2A        WTTA
         SPACE 1
         DC    AL1(S6)   2B        2741
         DC    AL1(S6)   2C             A
         SPACE 1
         DC    AL1(S8)   2D        2740 C OIU
         DC    AL1(S8)   2E             CD OIU
         SPACE 1
         DC    AL1(S8)   2F        1050X
         DC    AL1(SB)   30             A,D,AD
         SPACE 1
         DC    AL1(S8)   31        2740X
         DC    AL1(S8)   32             A,D,AD
         SPACE 4
S0       EQU   0
S5       EQU   8*5
S6       EQU   8*6
S7       EQU   8*7
S8       EQU   8*8
S9       EQU   8*9
SA       EQU   8*10
SB       EQU   8*11
SC       EQU   8*12          DVC DEPENDENT I/O LENGTH - BSC2     S99245
         EJECT                                                     000M
CHARTBL  EQU   *                                                   000M
*              USASCII CONTROL CHARACTERS                          000M
         DC    X'103F1002'         DLE WBT, DLE STX                000M
         DC    X'10031030'         DLE ETX, ACK 0                  000M
         DC    X'10310515'         ACK 1, INQ, NAK                 000M
         DC    X'17100416'         ETB, DLE, EOT, SYN              000M
         DC    X'10170125'         DLE ETB, SOH %
         DC    X'103B103C'         DLE SAK, DLE RVI                000M
TBLOFFST EQU   *-CHARTBL                                           000M
*              TRANSCODE CONTROL CHARACTERS                        000M
         DC    X'1F401F0A'         DLE WBT, DLE STX                000M
         DC    X'1F2E1F20'         DLE ETX, ACK 0                  000M
         DC    X'1F232D3D'         ACK 1, INQ, NAK                 000M
         DC    X'0F1F1E3A'         ETB, DLE, EOT, SYN              000M
         DC    X'1F0F002C'         DLE ETB,SOH %
         DC    X'1F291F32'         DLE SAK, DLE RVI                000M
*              EBCDIC CONTROL CHARACTERS                           000M
         DC    X'107F1002'         DLE WBT, DLE STX                000M
         DC    X'10031070'         DLE ETX, ACK 0                  000M
         DC    X'10612D3D'         ACK 1, INQ, NAK                 000M
         DC    X'26103732'         ETB, DLE, EOT, SYN              000M
         DC    X'1026016C'         DLE ETB, SOH %
         DC    X'106B107C'         DLE SAK, DLE RVI                000M
USASCII  EQU   X'14'                                               000M
TRANSCD  EQU   X'28'                                               000M
TIMELNG  EQU   4                                               @ZA08051
         SPACE 4
         EJECT
TIME     DC    F'50'                                               000E
EIGHT    DC    F'8'                    LENGTH OF IOB DYNAMIC     Y02947
*                                      BUFFERING EXTENSION       Y02947
RPGFIX   DC    X'40000000'             REGISTER PGFIX FLAG FOR   Y02947
*                                      PAGE SERVICES             Y02947
POST     DC    C'POST'                                           A27612
CONT     DC    C'CONT'                                           A27612
POOLNUM  DC    X'FA000000'       SUBPOOL 250                     Y02947
ERR4     DC    0F'0',X'80094000'        NON-SUPPORTED FEATURE      000I
ERR7     DC    X'80098000'       ABEND FOR DUAL INTERFACE ERROR YA01249
*     ERR8                          DELETED BY FOLLOWING APAR   YA02172
ANDMASK  DC    X'00000003'
ENABLE   DC    X'2700000020240001'
IOBPOOL  DC    X'FA000000'              SUBPOOL NUMBER FOR IOB
SADCCODE DC    X'13'                    SADZER CODE
         DC    X'17'                    SADONE CODE
         DC    X'1B'                    SADTWO CODE
         DC    X'1F'                    SADTHREE CODE
RETRYMSK DS    0F             READ RETRY MASK
   DC  BL.51'111110000111000111110010000000000010000001000111111'  000M
*        1050*****                                                 000K
*        2740     ******************                               000K
*        1060                       **                             000K
*        1030                         **                           000K
*        TEL1                           ****                       000K
*        TEL2                               ***                    000K
*        2260                                  *                   000K
*        S360                                   **                 000K
*        1130/2780                                ***              000K
*        2780 MULTIPOINT                             *             000K
*        2020 MULTIPOINT                              *            000K
*        WTTA                                          *           000K
*        2741                                           **         000K
*        2740 OIU                                         **       000K
*        1050X                                              **     000M
*        2740X                                                **   000M
         SPACE
CLEAR    XC    0(1,RCORE),0(RCORE)
OPIDCNST DC    C'0S'                    ID OF LAST OPEN LOAD.
AMIDCNST DC    C'3Q'                    ID OF THIS MODULE.
MASK1    EQU   X'05'                                               000I
MASK     EQU   X'03'                    UCB MODEL BITS FOR 2780    000C
MPOFFSET EQU   X'26'                    OFFSET FOR 1130/2780 MPT   000C
DISP0    EQU   0                        DISPLACEMENT OF 0        Y02947
DISP4    EQU   4                        DISPLACEMENT OF 4        Y02947
DISP8    EQU   8                        DISPLACEMENT OF 8        Y02947
DISP160  EQU   160                      LENGTH OF ONE IDAL       Y02947
         SPACE 2
CODEBUCB EQU   X'04'                    TRANSMISSION CODE B
CODEB    EQU   X'08'                    TRANSMISSION CODE B
DATAB    EQU   X'20'                    DATA INTERFACE B
SETMODCM EQU   X'23'                    SET MODE COMMAND
DISABLE  EQU   X'2F'                                             A26896
WAOFF    EQU   32                       OFFSET OF FIRST WTG ENTRY FROM
*                                  START OF WTG.
PLOFF    EQU   4                        OFFSET OF DCB ENTRIES.
WGOFF    EQU   8                        OFFSET OF WTG ENTRIES.
SADENER  EQU   X'80'         FLAG FOR ERROR ON SAD OR ENABLE
ALLON    EQU   X'FF'
RDRETRY  EQU   X'02'
CMDCHAIN EQU   X'40'
DYNBUF   EQU   X'08'                    CODE FOR BFTEK=D IN THE DCB,
GRAPHMSK EQU   X'40'                                               000E
ONLT     EQU   X'10'                    ON LINE TEST BIT           000D
TRMTST   EQU   X'10'                                               000E
TYP25    EQU   X'25'
TYP26    EQU   X'26'
TYP27    EQU   X'27'
TYP2B    EQU   X'2B'                   2741 OFFSET                 000I
INTR1    EQU   X'2B'                                               000I
INTR2    EQU   X'2C'                                               000I
FEA      EQU   X'10'                    AUTO-ANSWER FEATURE        000I
M2020    EQU   X'04'
ZERO     EQU   X'00'
X90      EQU   X'90'                                           @ZA04187
L19      EQU   19                                              @ZA04187
HEX24    EQU   36                                           L5 @ZA03589
PURGEOPT EQU   X'A0'               OPTION BYTE FOR PURGE    LD @ZA00535
ONE      EQU   1                    VALUE OF 1                   Y02947
L2       EQU   2                                               @ZA04865
LEN4     EQU   4                    LENGTH OF 4                  Y02947
TWELVE   EQU   12                                           LD @ZA00535
SIXT     EQU   16                   CVT ADDRESS                  Y02947
TWENTY8  EQU   28                   VALUE OF 28                  Y02947
THIRTY2  EQU   32                   VSL LENGTH                   Y02947
L32      EQU   32                                              @ZA06399
IDALLEN  EQU   320                  LENGTH OF 2 IDAL'S           Y02947
ONEBIT   EQU   1                    MULTIPLY BY 2                Y02947
FIVEBITS EQU   5                    MULTIPLY BY 32               Y02947
SIXTY    EQU   X'60'                MESSAGE AREA LENGTH          Y02947
         SPACE 4                                                   000I
BTAMLOD4 DC    C'3S'                   ID FOR FOURTH OPEN LOAD   Y02947
         DC    X'00000000'             TTR AND L
         EJECT
         DCBD  DSORG=BX,DEVD=BS
         EJECT                                                   Y02947
        IECTIOBX
         EJECT
        IECTDEBX
         EJECT                                                   Y02947
WORKAREA DSECT                                                   Y02947
         IECDSECT IOB=NO                                         Y02947
         EJECT                                                   Y02947
         IECDSECS WTG,PREFX,EXPAND=YES                           Y02947
         EJECT                                                   Y02947
CVT      DSECT                                                   Y02947
         CVT   LIST=NO                                           Y02947
         EJECT                                                   Y02947
         IKJTCB DSECT=YES,LIST=YES                               Y02947
         EJECT                                                   Y02947
         IHAPSA                                                  Y20947
         END
