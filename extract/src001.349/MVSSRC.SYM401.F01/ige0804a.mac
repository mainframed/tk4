         TITLE 'IGE0804A - BTAM ERP STATUS CHECK MODULE'
IGE0804A CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0804A                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM STATUS CHECK ERP                                            *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    CHECKS FOR THE FOLLOWING CINDITIONS AND XCTLS TO THE             *
*    PROPER ROUTINE.                                                  *
*        1. CHAIN CHECK                                               *
*        2. PROGRAM CHECK                                             *
*        3. PROTECTION CHECK                                          *
*        4. UNIT EXCEPTION                                            *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (12 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        130 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD.  *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0804A                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        SETS INDICATOR IN DECERRST FOR NOT SHOULD NOT OCCUR ERROR    *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        NONE                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM05652K  (1/25/74)                                              *
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         L     ERREG5,IOSUSE       BASE RQE
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     UCBREG,IOSUCB       BASE UCB
         DROP  ERREG5
         TM    IOBSTBYT+L1,CSWCHAIN   CHAINING CHECK
         BO    ERRSCR06            YES
         TM    IOBSTBYT+L1,CSWPROG   PROGRAM CHECK
         BO    ERRSCR06            YES
         TM    IOBSTBYT+L1,CSWPROT   PROTECTION CHECK
         BO    ERRSCR06            YES
         TM    IOBSTBYT,CSWUE      UNIT EXCEPTION
         BNO   ERRSCR05            NO, NONE OF THE ABOVE
ERRSCR04 LA    NAMEREG,ERLOD13     ADDRESS OF UNIT EXCEPTION RTN
         B     ERRLINK1            EXIT ROUTINE
ERRSCR05 OI    DECERRST(DECBRG),CSWSNO   SET SHOULD NOT OCCUR ERROR
ERRSCR06 TM    UCBTYP+L1(UCBREG),SWLINE   IS A DISABLE REQUIRED
         BZ    ERRSCR07            NO GO TO OBR ROUTINE
         MVI   IOBERRCT+L1,MASK15   SPECIAL ENTRY INDICATOR
         L     NAMEREG,ERLOD9      ADDRESS OF DISABLE ROUTINE
ERRLINK1 EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE
         DROP  ERRETR
         BR    ERRETR              XCTL
ERRSCR07 OI    IOSFLB,IOSLOG       INDICATE OUTBOARD RECORDING
         LH    NAMEREG,ERLOD5      ADDRESS OF POST ROUTINE
         B     ERRLINK1            EXIT
         EJECT
**********
*
*        CONSTANTS
*
**********
ERLOD9   DC    F'9041'
ERLOD5   DC    H'5041'
PACHAREA DC    3C'804A'            PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
CSWCHAIN EQU   X'01'               CHAINING CHECK
CSWPROG  EQU   X'20'               PROGRAM CHECK
CSWPROT  EQU   X'10'               PROTECTION CHECK
CSWUE    EQU   X'01'               UNIT EXCEPTION
L1       EQU   1                   LENGTH AND DISPLACEMENT
L255     EQU   255                 AND MASK
DECERRST EQU   29
CSWSNO   EQU   X'40'
UCBTYP   EQU   16
SWLINE   EQU   X'90'
MASK15   EQU   X'0F'
ERLOD13  EQU   3042
**********
*
*        REGISTER DEFINITION
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
ERREG2   EQU   2                   WORK REG
EWAREG   EQU   3                   ERP WORKAREA BASE
CCWREG   EQU   4                   CCW ADDRESS REG
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE
BASEREG  EQU   8                   PROGRAM BASE
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG4   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  LINK REG
ERREG5   EQU   15                  LINK REG/WORK REG
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
