         TITLE ' IGC1106F - 3270 REMOTE ASCII TEST MODULE '
IGC1106F CSECT
***********************************************************************
*                  3270 REMOTE ASCII TEST MODULE                      *
***********************************************************************
*                                                                     *
* STATUS:                                                             *
*                                                                     *
*        ZA02332  (11/20/74)                                          *
*        AZ09327  (03/11/76)                                          *
*                                                                     *
***********************************************************************
*                                                                     *
* FUNCTION:                                                           *
*    THIS MODULE BUILDS PREDEFINED ASCII ON-LINE TEST MESSAGES        *
*    FOR THE REMOTE 3270 DISPLAY SYSTEM.                              *
*                                                                     *
* ENTRY:                                                              *
*         ENTRY IS TO THE FIRST EXECUTABLE INSTRUCTION VIA XCTL       *
*         FROM IGC0D06F.                                              *
*                                                                     *
* INPUT:                                                              *
*    REG 1 - ADDRESS OF THE IOB                                       *
*    REG 4 - TEST TYPE XX VALUE                                       *
*    IOBERINF - ADDRESS OF THE RFT                                    *
*    ON-LINE TEST CHANNEL PROGRAM IN IOB, BUILT BY IGC0D06F.          *
*                                                                     *
*                                                                     *
* OUTPUT:                                                             *
*    TEST MESSAGE BUILT IN USER'S AREA.                               *
*    COUNT IN WRITE TEXT CCW.                                         *
*    IF THE USER'S BUFFER IS NOT LONG ENOUGH (300 BYTES) OR THE       *
*    TEST MESSAGE NUMBER IS INVALID, AN ERROR FLAG (IOBINCAM,         *
*    BYTE 0, BIT 6) IS TURNED ON. THIS WILL CAUSE IGG019MR TO         *
*    CANCEL THE TEST                                                  *
*    TESTS BUILT BY THIS MODULE:                                      *
*    TEST 31 - 3270 MODEL 2 ALIGN ASCII                               *
*    TEST 33 - 3270 BASIC PRINTER ASCII                               *
*    TEST 34 - 3270 NL/EOM PRINTER ASCII                              *
* EXTERNAL REFERENCES:                                                *
*         SVC 0 - EXECUTE CHANNEL PROGRAM                             *
*                                                                     *
* EXITS, NORMAL:                                                      *
*     SVC 3 - RETURN TO IOS                                           *
*                                                                     *
* EXITS, ERROR:                                                       *
*         NONE                                                        *
*                                                                     *
* TABLES/WORK AREAS:                                                  *
*         NONE                                                        *
*                                                                     *
* ATTRIBUTES:                                                         *
*    RE-ENTRANT                                                       *
*                                                                     *
* CHARACTER CODE DEPENDENCY:                                          *
*    NONE:                                                            *
*                                                                     *
***********************************************************************
         EJECT
*/*IGC1106F: CHART*/
*/* E IGC1106F*/
*/* P OVERLAY TRANSPARENT CCW*/
*/* P STORE ADDR OF START OF RFT IN CCW*/
*/* D (YES,,NO,SETERR) IS BUFFER LONG ENOUGH*/
*/* D (YES,,NO,SETERR) IS TEST NUMBER VALID (31,33, OR 34)*/
*/* P GET MESSAGE LENGTH AND ADDRESS*/
*/* P (,STARTEST) MOVE TEST MESSAGE INTO USER'S BUFFER*/
*/*SETERR: M TURN ON ERROR FLAG*/
*/* P SET COUNT TO ONE*/
*/*STARTEST: P STORE COUNT IN CCW*/
*/* P SAVE THE ECB*/
*/* L SVC 0*/
*/* P RESTORE THE ECB*/
*/* R SVC 3*/
*/*IGC1106F: END*/
         EJECT
         SPACE 2
*        ESTABLISH ADDRESSABILITY
         SPACE
         BALR  BASEREG,ZEROREG     SET UP BASE REG
         USING *,BASEREG
         USING IECTIOB,IOBREG      IOB DSECT
         USING IECTDECB,DECBREG    DECB DSECT
         USING IHADCB,DCBREG       DCB DSECT
         USING IECTDEB,DEBREG      DEB DSECT
         USING UCBDSECT,UCBREG     UCB DSECT
         B     SETUP               BRANCH AROUND ID USED TO
         DC    C'IGC1106F'         FIND MODULE IN DUMP
         DC    C' 4322'            DATE LAST CHANGE         LD @ZA02332
         DC    C'&SYSDATE'         DATE LAST ASSEMBLY       LD @ZA02332
         DS    0H                                           LD @ZA02332
PATCH    DC    48X'00'             PATCH AREA               LD @ZA02332
         DS    0H                                           LD @ZA02332
SETUP    EQU   *
         L     DECBREG,IOBECBPT    GET DECB ADDR
         L     DCBREG,IOBDCBPT     GET DCB ADDR
         L     DEBREG,DCBDEBAD     GET DEB ADDR
         L     DEBREG,PREFIX(DEBREG) GO PAST PREFIX
         SPACE 2
*        SET UP CHANNEL PROGRAM
         SPACE
         MVC   IOBCPA+48(CCWLNG),IOBCPA+56 OVERLAY TRANS CCW
         LA    CCWREG,IOBCPA+40    GET ADDR OF CCW
         SPACE 2
*        VALIDITY CHECK BUFFER LENGTH AND TEST NUMBER. IF
*        INVALID, SET UP NOP CCW AND TURN ON ERROR FLAG.
         SPACE
         CLC   DECLNGTH(BUFLNG),MINBUF IS BUFFER LONG ENOUGH
         BL    SETERR              NO, SET UP FOR ERROR
         C     TESTREG,TEST31      IS IT TEST 31 ?
         BE    GETINFO             YES, GET MSG ADDR & COUNT
         C     TESTREG,TEST33      IS IT TEST 33 ?
         BE    GETINFO             YES, GET MSG ADDR & COUNT
         C     TESTREG,TEST34      IS IT TEST 34 ?
         BNE   SETERR              NO, SET UP FOR ERROR
         SPACE 2
*        GET TEST MESSAGE ADDRESS AND LENGTH
         SPACE
GETINFO  LR    WORKREG1,TESTREG    GET TEST #
         S     WORKREG1,TEST31     SUBTRACT 31 TO GET INDEX
*                                  (31 IS LOWEST TEST NUMBER
*                                  THIS MODULE WILL HANDLE)
         SLL   WORKREG1,2          MULTIPLY BY 4
         L     COUNTREG,COUNTAB(WORKREG1) GET MSG LENGTH
         B     ADDRTAB(WORKREG1)   GET MSG ADDR
         SPACE 2
*        MESSAGE ADDRESS AND LENGTH TABLES - (TEST MESSAGE
*        NUMBER - 31) X 4 = THE ARGUMENT USED TO OBTAIN VALUES
*        FROM THE TABLE.
         SPACE
         DS    0F
COUNTAB  EQU   *                   MESSAGE LENGTH IN BYTES
         DC    A(TST31SZ)          TEST 31
         DC    A(0)                UNUSED
         DC    A(TST33SZ)          TEST 33
         DC    A(TST34SZ)          TEST 34
         SPACE 2
ADDRTAB  EQU   *                   ADDRESS OF TEST MESSAGE
         B     TEST31AD            00   GET TEST 31 ADDR
         B     SETERR              04   UNUSED
         B     TEST33AD            08   GET TEST 33 ADDR
         B     TEST34AD            0C   GET TEST 34 ADDR
TEST31AD LA    ADDREG,IJLBOT31     GET MSG ADDR
         B     MOVEIN              CONTINUE
TEST33AD LA    ADDREG,IJLBOT33     GET MSG ADDR
         B     MOVEIN              CONTINUE
TEST34AD LA    ADDREG,IJLBOT34     GET MSG ADDR
         SPACE 2
*        MOVE TEST MSG INTO USER'S BUFFER
         SPACE
MOVEIN   EQU   *
         L     RFTREG,IOBERINF     GET ADDR OF RFT
         LA    WORKREG2,IOBCPA     GET ADDR OF IOBCPA            S99245
         ST    WORKREG2,IOBSTART   AND STORE IN IOBSTART         S99245
         CLI   NVALUE(RFTREG),NOADDR   IS THIS A DIAL RFT?       S99245
         BNE   BSC3RFT             NO,FOLLOW BSC3 PROCEDURE      S99245
         LA    RFTREG,CHARS7(RFTREG)   ADJUST PTR PAST RFT       S99245
         B     TSTLNG              GO CHECK MSG LENGTH           S99245
BSC3RFT  EQU   *                                                 S99245
         LA    RFTREG,CHARS11(RFTREG) ADJUST PTR PAST RFT
TSTLNG   EQU   *                                                 S99245
         SR    WORKREG2,WORKREG2   CLEAR REG
         C     COUNTREG,MAXLNGTH   IS MSG LNGTH OVER 256 ?
         BNH   LNGTHOK             NO, BRANCH
         LR    WORKREG2,COUNTREG   GET EXCESS OVER 256
         S     WORKREG2,MAXLNGTH
         L     COUNTREG,MAXLNGTH   SET COUNT TO 256
LNGTHOK  EQU   *
         BCTR  COUNTREG,ZEROREG    SUBTRACT ONE FROM LENGTH FOR
*                                  EXECUTE
         EX    COUNTREG,MSGMOVE    MOVE TEST MSG TO USER'S
*                                  BUFFER
         LTR   WORKREG2,WORKREG2   ANY MORE TO MOVE ?
         BZ    STARTEST            NO, BRANCH
         A     RFTREG,MAXLNGTH     BUMP ADDRESS PAST 256
         A     ADDREG,MAXLNGTH     CHARS JUST MOVED
         AR    COUNTREG,WORKREG2   ADD LENGTH TO COUNTREG       YA01228
         BCTR  WORKREG2,ZEROREG    SUBTRACT ONE FOR EX
         EX    WORKREG2,MSGMOVE    MOVE REMAINDER OF TEXT
         B     STARTEST            BRANCH AROUND EXECUTE
MSGMOVE  MVC   0(FILLIN,RFTREG),0(ADDREG) MOVE TEST MSG TO
*                                  USER'S BUFFER, USING LENGTH
*                                  SPECIFIED IN COUNTREG
SETERR   EQU   *
         OI    IOBINCAM,ERFLAG     TURN ON ERROR FLAG
         LA    COUNTREG,ONECHAR    SET COUNT TO ONE
         B     STARTEST            DO TEST
         SPACE 2
*        MOVE COUNT INTO CCW AND START TEST
         SPACE
STARTEST EQU   *
         LA    COUNTREG,ONECHAR(COUNTREG) RESTORE COUNT
         STH   COUNTREG,BYTECNT(CCWREG) STORE COUNT OF CCW
*
*              1 LINE DELETED FOR ----->                    LD @ZA02332
*
         LR    WORKREG1,IOBREG     SAVE IOB ADDR            LD @ZA02332
         L     PARMREG,GETPARM     SET UP FOR GETMAIN       LD @ZA02332
         GETMAIN R,LV=(0)          GETMAIN FOR ECB          LD @ZA02332
         LR    WORKREG2,IOBREG     SAVE ECB ADDR            LD @ZA02332
         ST    DECBREG,DISP4(WORKREG2)  SAVE DECB ADDR      LD @ZA02332
         MVI   DISP4(WORKREG2),FOXFOX   FLAG AS DUMMY ECB   LD @ZA02332
         LR    IOBREG,WORKREG1     RESTORE IOB REG          LD @ZA02332
         STCM  WORKREG2,MASK,IOBECBPT+DISP1  POINT IOB...   LD @ZA02332
*                                  ...DUMMY                 LD @ZA02332
         SVC   EXCP                DO TEST
*
*              1 LINE DELETED FOR ----->                    LD @ZA02332
*
         STCM  DECBREG,MASK,IOBECBPT+DISP1  POINT IOB...    LD @ZA02332
*                                  ...TO DECB               LD @ZA02332
         L     PARMREG,GETPARM     SET UP FOR FREEMAIN      LD @ZA02332
         FREEMAIN R,LV=(0),A=(WORKREG2)  FREE DUMMY ECB     LD @ZA02332
         SVC   RETURN              RETURN
         SPACE 2
*        IF AN ERROR WAS FOUND, TURN ON ERROR FLAG, GET CCW
*        ADDR, AND ISSUE NOP CCW
         SPACE
         SPACE 2
*        REGISTER EQUATES
         SPACE
PARMREG  EQU   0                   PARAMETER REGISTER       LD @ZA02332
IOBREG   EQU   1                   ADDR OF IOB
DECBREG  EQU   2                   ADDR OF DECB
DCBREG   EQU   3                   ADDR OF DCB
TESTREG  EQU   4                   TEST #
WORKREG1 EQU   5                   WORK REG
WORKREG2 EQU   6                   WORK REG
DEBREG   EQU   7                   ADDR OF DEB
UCBREG   EQU   8                   ADDR OF UCB
ADDREG   EQU   9                   TEST MSG ADDRESS
COUNTREG EQU   10                  TEST MSG LENGTH
CCWREG   EQU   11                  ADDR OF CCW TO WRITE TEST MSG
BASEREG  EQU   12
RFTREG   EQU   13                  ADDR OF RFT
ZEROREG  EQU   0                   USED IN BCTR FOR NO BRANCH
         SPACE 2
*        MISCELLANEOUS EQUATES
         SPACE
ERFLAG   EQU   X'02'               ERROR FLAG
PREFIX   EQU   28                  DISP OF START OF DEB
CCWLNG   EQU    8                  LENGTH OF A CCW
BUFLNG   EQU   2                   LENGHT OF DECLNGTH FIELD
CHARS11  EQU   11                  LENGHT OF STD RFT
FILLIN   EQU   0                   LENTH FILLED IN EX INSTR
ONECHAR   EQU   1                  ONE CHARACTER
BYTECNT   EQU   6                  DISP IN CCW OF BYTE COUNT
EXCP     EQU   0                   EXCP SVC NUMBER
RETURN   EQU   3                   RETURN SVC #
NVALUE   EQU   6                   DISP INTO RFT OF N VALUE      S99245
NOADDR   EQU   X'00'               IF N=0, IT IS A DIAL RFT      S99245
CHARS7   EQU   7                   LENGTH OF STANDARD DIAL RFT   S99245
FOXFOX   EQU   X'FF'               BYE OF ALL ONES          LD @ZA02332
MASK     EQU   7                   MASK FOR STCM            LD @ZA02332
DISP1    EQU   1                   DISPLACEMENT             LD @ZA02332
DISP4    EQU   4                   DISPLACEMENT             LD @ZA02332
         SPACE 2
*        STORAGE
         SPACE
         DS    0F                  FULLWORD BOUNDARY        LD @ZA02332
GETPARM  DC    X'FA000008'         SUBPOOL # AND LENGTH     LD @ZA02332
MINBUF   DC    X'012C'             MINIMUM BUFFER LENGTH
TEST31   DC    F'31'               ACCEPTABLE TEST NUMBER
TEST33   DC    F'33'               ACCEPTABLE TEST NUMBER       YA02054
TEST34   DC    F'34'               ACCEPTABLE TEST NUMBER
MAXLNGTH DC    F'256'              MAX # OF CHARS FOR MVC
IJLBOT31 EQU   *
         DC    X'021B3522'         STX-ESC-EWRT-WCC
         DC    X'14412645'         RA 80,E
         DC    X'48'               H
         DC    X'11425E4848'       SBA 159,HH                  @ZA09327
         DC    X'11433F4848'       SBA 239,HH
         DC    X'1144224848'       SBA 319,HH
         DC    X'1146214848'       SBA 399,HH
         DC    X'11475E4848'       SBA 479,HH                  @ZA09327
         DC    X'114756'           SBA 485
         DC    X'544553542050415454' TEST PATT
         DC    X'45524E20464F5220'            ERN FOR
         DC    X'333237352D322F33'                    3275-2/3
         DC    X'3237372D32'                                  277-2
         DC    X'11483F4848'       SBA 559,HH
         DC    X'11483D'           SBA 574
         DC    X'414C49474E4D454E54' ALIGNMENT
         DC    X'1149224848'       SBA 639,HH
         DC    X'112E214848'       SBA 719,HH
         DC    X'113C5E4848'       SBA 799,HH
         DC    X'113C582A'         SBA 807,*
         DC    X'1D20'             SF 40
         DC    X'13'               INC
         DC    X'554E50524F544543' UNPROTEC
         DC    X'5445442041524541'         TED AREA
         DC    X'20202020'         (4 SPACES)
         DC    X'1D2D2A'           SF 60,*
         DC    X'11283F4848'       SBA 879,HH
         DC    X'112B504F4F'       SBA 919,OO
         DC    X'112B224848'       SBA 959,HH
         DC    X'1121584F4F'       SBA 999,OO
         DC    X'1126214848'       SBA 1039,HH
         DC    X'114A5E4848'       SBA 1119,HH
         DC    X'114B3F4848'       SBA 1199,HH
         DC    X'114C224848'       SBA 1279,HH
         DC    X'114E214848'       SBA 1359,HH
         DC    X'114F5E4848'       SBA 1439,HH
         DC    X'11503F4848'       SBA 1519,HH
         DC    X'1151224848'       SBA 1599,HH
         DC    X'115D214848'       SBA 1679,HH
         DC    X'11245E4848'       SBA 1759,HH
         DC    X'112A3F48'         SBA 1839,H
         DC    X'14202045'         RA 0,E
         DC    X'03'               ETX
TST31SZ  EQU   *-IJLBOT31          LENGTH OF TEST 31
IJLBOT33 EQU   *
         DC    X'021B35'           STX-ESC-EWRT
         DC    X'21'               WCC-STRT PRNT/SND ALRM/RST KEYBRD
         DC    X'404142434445464748494A4B4C4D4E4F'  ASCII
         DC    X'112058'           SBA  39
         DC    X'1D2D'             SF 60
         DC    X'505152535455565758595A5B5C5D5E5F'    UNIVERSAL
         DC    X'114121'           SBA  79
         DC    X'1D20'             SF  40
         DC    X'606162636465666768696A6B6C6D6E6F'      CHARACTER
         DC    X'114137'           SBA  119
         DC    X'1D2D'             SF 60
         DC    X'707172737475767778797A7B7C7D7E7F'      SET
         DC    X'11425E'           SBA 159
         DC    X'1D20'             SF  40
         DC    X'303132333435363738393A3B3C3D3E3F'  FOR
         DC    X'114347'           SBA  199
         DC    X'1D2D'             SF 60
         DC    X'202122232425262728292A2B2C2D2E2F'    ANR-3270
         DC    X'11433F'           SBA  239
         DC    X'1D20'             SF   40
         DC    X'4E2F4C20434845484B20'  NEW LINE CHAR TO FOLLOW
         DC    X'0A0A0A0A0A'
         DC    X'114450'           SBA  279
         DC    X'1D2D'             SF  60
         DC    X'454F4D20434845434B20'  EOM CHECK
         DC    X'19'
         DC    X'114621'           SBA  399
         DC    X'1D20'             SF   40
         DC    X'13'               INC
         DC    X'03'               ETX
TST33SZ  EQU   *-IJLBOT33          LENGTH OF TEST 33
IJLBOT34 EQU   *
         DC    X'021B35'           STX-ESC-EWRT
         DC    X'214E4557204C'     WCC- NEW L
         DC    X'494E452046554E43'           INE FUNC
         DC    X'54494F4E20434845'                   TION CHE
         DC    X'434B0A4E4557204C' CK-NL-NEW L
         DC    X'494E452046554E43'            INE FUNC
         DC    X'54494F4E0A4E4557'                     TION-NL-NEW
         DC    X'204C494E450A4E45'  LINE-NL-NE
         DC    X'570A4E4557204C49'            W-NL-NEW LI
         DC    X'4E450A4E4557204C'                       NE-NL-NEW L
         DC    X'494E452046554E43' INE FUNC
         DC    X'54494F4E0A4E4557'         TION-NL-NEW
         DC    X'204C494E45204655'  LINE FU
         DC    X'4E4354494F4E2043'         NCTION C
         DC    X'4845434B0A'                       HECK-NL
         DC    X'1D3C'             SF 4C
         DC    X'544849532044415441' THIS DATA
         DC    X'204E4F5420564953'             NOT VIS
         DC    X'49424C452F454F4D' IBLE/EOM
         DC    X'20414E44204E2F4C'          AND N/L
         DC    X'2049474E4F524544'  IGNORED
         DC    X'0A19'             NL-EOM
         DC    X'1D20'             SF 40
         DC    X'454E442D4F462D4D' END-OF-M
         DC    X'4553534147452D54'         ESSAGE-T
         DC    X'45524D494E415445'                 ERMINATE
         DC    X'53205052494E5419' S PRINT-EOM
         DC    X'0A324E4420454F4D' NL-2ND EOM
         DC    X'204C494E45204E4F'  LONE NO
         DC    X'54204F4E20505249'         T ON PRI
         DC    X'4E544552204F55'                   NTER OU
         DC    X'5450555419'       TPUT-EOM
         DC    X'03'               ETX
TST34SZ  EQU   *-IJLBOT34          LENGTH OF TEST 34
         SPACE 2
*        DSECTS
         SPACE
         IECTIOBX
         EJECT
         IECTDECB
         EJECT
         DCBD  DSORG=BX,DEVD=BS
         EJECT
         IECTDEBX
UCBDSECT DSECT
         DS    0F
         ORG   UCBDSECT+13         UCBNAME (UNIT NAME)
CHARADDR DS    CL3                 UNIT ADDR (EBCDIC)
         ORG   UCBDSECT+18         UCBTYP+2
DVCLASS  DS    CL1                 DEVICE CLASS
UNITYPE  DS    CL1                 UNIT TYPE
         ORG   UCBDSECT+36         UCBRLN
RLN      DS    CL1                 RELATIVE LINE NUMBER
      END
