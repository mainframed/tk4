         TITLE ' IGC1406F - 3270 LOCAL/REMOTE EBCDIC TEST MODULE '
IGC1406F CSECT
*                                                                     *
***********************************************************************
*                                                                     *
*        MAINTENANCE ACTIVITY                                         *
*                                                                     *
*        ZA02332  (11/20/74)                                          *
*        ZA03972  (04/24/75)                                          *
*        ZA26729  (10/03/77)                                          *
*                                                                     *
***********************************************************************
         EJECT
*/*IGC1406F: CHART*/
*/* E IGC1406F*/
*/* D (YES,,NO,SETERR) IS BUFFER LONG ENOUGH (300 BYTES)*/
*/* D (YES,,NO,SETERR) IS TEST NUMBER VALID (25)*/
*/* P GET MESSAGE LENGTH AND ADDRESS*/
*/*TESTLCL: D (YES,LOCAL,NO,REMOTE) IS DEVICE A LOCAL 3270*/
*/*TESTLCL: D (NO,,YES,TSTLCONT) IS DVC 3277 AND MODEL2*/
*/*M SET ON IOB ERROR FLAG FOR A 3277 THAT IS NOT A MODEL2*/
*/*TSTLCONT: D (YES,LOCAL,NO,REMOTE) IS DEVICE A LOCAL 3270*/
*/*LOCAL: D (NO,,YES,ISSUENOP) IS ERROR FLAG ON*/
*/* P POINT MESSAGE ADDRESS PAST STX, ESC, AND CMD CODE*/
*/* P STORE ADDR OF START OF RFT IN CCW*/
*/* P (,MOVEIN) GET ADDRESS OF START OF CCW*/
*/*REMOTE: D (NO,,YES,REMOTERR) IS ERROR FLAG ON*/
*/* P OVERLAY TRANSPARENT CCW*/
*/* P GET ADDR OF CCW*/
*/* P STORE ADDR OF START OF RFT IN CCW*/
*/*MOVEIN: P (,STARTEST) MOVE MESSAGE INTO USER'S BUFFER*/
*/*SETERR: M (,TESTLCL) TURN ON ERROR FLAG*/
*/*ISSUENOP: P SET COUNT TO ONE*/
*/* P (,STARTEST) MOVE NOP COMMAND INTO CCW*/
*/*REMOTERR: P SET COUNT TO ONE*/
*/*STARTEST: P STORE COUNT IN CCW*/
*/* P SAVE THE ECB*/
*/* L SVC 0*/
*/* P RESTORE THE ECB*/
*/* R SVC 3*/
*/*IGC1406F: END*/
         SPACE 2
*        ESTABLISH ADDRESSABILITY
         SPACE
         BALR  BASEREG,0           SET UP BASE REG
         USING *,BASEREG
         USING IECTIOB,IOBREG      IOB DSECT
         USING IECTDECB,DECBREG    DECB DSECT
         USING IHADCB,DCBREG       DCB DSECT
         USING IECTDEB,DEBREG      DEB DSECT
         USING UCBDSECT,UCBREG     UCB DSECT
         B     SETUP               BRANCH AROUND ID USED TO
         DC    C'IGC1406F'         FIND MODULE IN DUMP
         DC    XL3'014779'         PTF NUMBER
         DC    C'&SYSDATE'         DATE LAST ASSEMBLY       LD @ZA02332
         DS    0H                                           LD @ZA02332
PATCH    DC    48X'00'             PATCH AREA               LD @ZA02332
         DS    0H                                           LD @ZA02332
SETUP    EQU   *
         L     DECBREG,IOBECBPT    GET DECB ADDR
         L     DCBREG,IOBDCBPT     GET DCB ADDR
         L     DEBREG,DCBDEBAD     GET DEB ADDR
         L     DEBREG,28(DEBREG)   GO PAST PREFIX
         L     RFTREG,IOBERINF     GET ADDR OF RFT
         SPACE 2
*        VALIDITY CHECK BUFFER LENGTH AND TEST NUMBER. IF
*        INVALID, SET UP NOP CCW AND TURN ON ERROR FLAG.
         SPACE
         CLC   DECLNGTH(2),MINBUF  IS BUFFER LONG ENOUGH ?
         BL    SETERR              NO, SET UP FOR ERROR
         C     TESTREG,TEST25      IS IT TEST # 25 ?
         BNE   SETERR              NO, SET UP FOR ERROR
         SPACE 2
*        GET TEST MESSAGE ADDRESS AND LENGTH
         SPACE
GETINFO  LR    WORKREG1,TESTREG    GET TEST #
         S     WORKREG1,TEST25     SUBTRACT 25 TO GET INDEX
*                                  (25 IS LOWEST TEST NUMBER
*                                  THIS MODULE WILL HANDLE)
         SLL   WORKREG1,2          MULTIPLY BY 4
         L     COUNTREG,COUNTAB(WORKREG1) GET MSG LENGTH
         B     ADDRTAB(WORKREG1)   GET MSG ADDR
         SPACE 2
*        MESSAGE ADDRESS AND LENGTH TABLES - (TEST MESSAGE
*        NUMBER - 25) X 4 = THE ARGUMENT USED TO OBTAIN VALUES
*        FROM THE TABLE.
         SPACE
         DS    0F
COUNTAB  EQU   *                   MESSAGE LENGTH IN BYTES
         DC    A(TST25SZ)          TEST 25
         SPACE 2
ADDRTAB  EQU   *                   ADDRESS OF TEST MESSAGE
         B     TEST25AD            00   GET TEST 25 ADDR
TEST25AD LA    ADDREG,IJLBOT25     GET ADDR
         SPACE 2
*        TEST FOR LOCAL OR REMOTE
         SPACE
TESTLCL  EQU   *
         SR    UCBREG,UCBREG       CLEAR REG FOR IC
         IC    UCBREG,IOBUCBX      GET INDEX TO UCB
         SLL   UCBREG,2            MULTIPLY BY FOUR
         L     UCBREG,DEBUCBAD(UCBREG)  GET ADDR OF UCB
         CLI   UNITYPE,A3277           IS DEVICE A 3277        @ZA03972
         BNE   TSTLCONT        NO, BRANCH TO CONTINUE TEST     @ZA03972
         TM    MODTYPE,MOD2        IS 3277 A MODEL2            @ZA03972
         BO    TSTLCONT        YES, BRANCH TO CONTINUE         @ZA03972
         OI    IOBINCAM,ERFLAG SET ON ERROR FLAG FOR OTHER MOD @ZA03972
TSTLCONT EQU   *                TEST LOCAL CONTINUE            @ZA03972
         CLI   DVCLASS,GRAPHIC     IS DVC A GRAPHIC ?
         BNE   REMOTE              NO, SET UP FOR REMOTE
         CLI   UNITYPE,A3277       IS DVC A 3277 ?
         BE    LOCAL               YES, SET UP FOR LOCAL
         CLI   UNITYPE,A3284       IS DVC A 3284 ?
         BE    LOCAL               YES, SET UP FOR LOCAL
         CLI   UNITYPE,A3286       IS DVC A 3286 ?
         BNE   REMOTE              NO, SET UP FOR REMOTE
*        FOR LOCAL DEVICES, STRIP OFF STX, ESC, CMD CODE, AND
*        ETX OF MESSAGE
         SPACE
LOCAL    EQU   *
         LA    CCWREG,IOBCPA       GET ADDR OF CCW
         TM    IOBINCAM,ERFLAG     IS ERROR FLAG ON ?
         BO    ISSUENOP            YES, GO ISSUE NOP
         LA    WORKREG2,4          SUBTRACT 4 FROM LENGTH
         SR    COUNTREG,WORKREG2   OF MESSAGE
         LA    ADDREG,3(ADDREG)    POINT MESSAGE ADDRESS PAST
*                                  STX, ESC, CMD CODE
         LA    RFTREG,10(RFTREG)   ADJUST PTR PAST RFT
         B     MOVEIN              GO SET UP MSG
         SPACE 2
*        FOR REMOTE, LEAVE MSG ADDRESS AND LENGTH ALONE
         SPACE
REMOTE   EQU   *
         LA    WORKREG2,IOBCPA     GET ADDR OF IOBCPA            S99245
         ST    WORKREG2,IOBSTART   AND STORE IN IOBSTART         S99245
         LA    CCWREG,IOBCPA+40    GET ADDR OF CCW
         TM    IOBINCAM,ERFLAG     IS ERROR FLAG ON ?
         BO    REMOTERR            YES, ISSUE CCW
         MVC   IOBCPA+48(8),IOBCPA+56 OVERLAY TRANS CCW
         CLI   NVALUE(RFTREG),NOADDR   IS THIS A DIAL RFT?       S99245
         BNE   BSC3RFT             NO,FOLLOW BSC3 PROCEDURE      S99245
         LA    RFTREG,CHARS7(RFTREG)   ADJUST PTR PAST RFT       S99245
         B     MOVEIN              GO SET UP MSG                 S99245
BSC3RFT  EQU   *                                                 S99245
         LA    RFTREG,11(RFTREG)   ADJUST PTR PAST RFT
         SPACE 2
*        MOVE TEST MSG INTO USER'S BUFFER
         SPACE
MOVEIN   EQU   *
         SR    WORKREG2,WORKREG2   CLEAR REG
         C     COUNTREG,MAXLNGTH   IS MSG LNGTH OVER 256 ?
         BNH   LNGTHOK             NO, BRANCH
         LR    WORKREG2,COUNTREG   GET EXCESS OVER 256
         S     WORKREG2,MAXLNGTH
         L     COUNTREG,MAXLNGTH   SET COUNT TO 256
LNGTHOK  EQU   *
         BCTR  COUNTREG,0          SUBTRACT 1 FROM LNGTH FOR
*                                  EXECUTE
         EX    COUNTREG,MSGMOVE    MOVE TEST MSG INTO USER'S
*                                  BUFFER
         LTR   WORKREG2,WORKREG2   ANY MORE TO MOVE ?
         BZ    STARTEST            NO, BRANCH
         A     RFTREG,MAXLNGTH     BUMP ADDRESS PAST 256
         A     ADDREG,MAXLNGTH     CHARS JUST MOVED
         BCTR  WORKREG2,0          SUBTRACT 1 FOR EXECUTE
         EX    WORKREG2,MSGMOVE    MOVE REMAINDER OF TEXT
         B     STARTEST            BRANCH AROUND EXECUTE
MSGMOVE  MVC   0(0,RFTREG),0(ADDREG) MOVE TEST MSG TO
*                                  USER'S BUFFER, USING LENGTH
*                                  SPECIFIED IN COUNTREG
SETERR   EQU   *
         OI    IOBINCAM,ERFLAG     TURN ON ERROR FLAG
         B     TESTLCL             GO SEE IF LOCAL
ISSUENOP EQU   *
         LA    COUNTREG,1          SET COUNT TO ONE
         MVI   0(CCWREG),NOP       MOVE IN NOP COMMAND
         B     STARTEST            GO ISSUE NOP
REMOTERR EQU   *
         LA    COUNTREG,1          SET COUNT TO ONE
         B     STARTEST            DO TEST
         SPACE 2
*        MOVE COUNT INTO CCW AND START TEST
         SPACE
STARTEST EQU   *
         LA    COUNTREG,1(COUNTREG) RESTORE COUNT AFTER EX
         STH   COUNTREG,6(CCWREG)  STORE COUNT IN CCW
*
*              1 LINE DELETED FOR ----->                    LD @ZA02332
*
         LR    WORKREG1,IOBREG     SAVE IOB ADDR            LD @ZA02332
         L     PARMREG,GETPARM     SET UP FOR GETMAIN       LD @ZA02332
         GETMAIN R,LV=(0)          GETMAIN FOR ECB          LD @ZA02332
         LR    WORKREG2,IOBREG     SAVE ECB ADDR            LD @ZA02332
         ST    DECBREG,DISP4(WORKREG2)  SAVE DECB ADDR      LD @ZA02332
         MVI   DISP4(WORKREG2),FOXFOX   FLAG AS DUMMY ECB   LD @ZA02332
         LR    IOBREG,WORKREG1     RESTORE IOB REG          LD @ZA02332
         STCM  WORKREG2,MASK,IOBECBPT+DISP1  POINT IOB...   LD @ZA02332
*                                  ...TO DUMMY              LD @ZA02332
         SVC   0                   DO TEST
*
*              1 LINE DELETED FOR ----->                    LD @ZA02332
*
         STCM  DECBREG,MASK,IOBECBPT+DISP1  REPOINT IOB...  LD @ZA02332
*                                  ...TO DECB               LD @ZA02332
         L     PARMREG,GETPARM     SET UP FOR FREEMAIN      LD @ZA02332
         FREEMAIN R,LV=(0),A=(WORKREG2)  FREE DUMMY ECB     LD @ZA02332
         SVC   3                   RETURN
         SPACE 2
*        IF AN ERROR WAS FOUND, TURN ON ERROR FLAG, GET CCW
*        ADDR, AND ISSUE NOP CCW
         SPACE
         SPACE 2
*        REGISTER EQUATES
         SPACE
PARMREG  EQU   0                   PARAMETER REGISTER       LD @ZA02332
IOBREG   EQU   1                   ADDR OF IOB
DECBREG  EQU   2                   ADDR OF DECB
DCBREG   EQU   3                   ADDR OF DCB
TESTREG  EQU   4                   TEST # 4
WORKREG1 EQU   5                   WORK REG
WORKREG2 EQU   6                   WORK REG
DEBREG   EQU   7                   ADDR OF DEB
UCBREG   EQU   8                   ADDR OF UCB
ADDREG   EQU   9                   TEST MSG ADDRESS
COUNTREG EQU   10                  TEST MSG LENGTH
CCWREG   EQU   11                  ADDR OF CCW TO WRITE TEST MSG
BASEREG  EQU   12
RFTREG   EQU   13                  ADDR OF RFT
         SPACE 2
*        MISCELLANEOUS EQUATES
         SPACE
A3286    EQU   X'0B'               3286 DEVICE TYPE
A3284    EQU   X'0A'               3284 DEVICE TYPE
A3277    EQU   X'09'               3277 DEVICE TYPE
MOD2     EQU   X'12'               MODEL2 3277                 @ZA03972
GRAPHIC  EQU   X'10'               GRAPHICS DEVICE TYPE
NOP      EQU   X'03'               NOP COMMAND CODE
ERFLAG   EQU   X'02'               ERROR FLAG
NVALUE   EQU   6                   DISP INTO RFT OF N VALUE      S99245
NOADDR   EQU   X'00'               IF N=0, IT IS DIAL RFT        S99245
CHARS7   EQU   7                   LENGTH OF STANDARD DIAL RFT   S99245
FOXFOX   EQU   X'FF'               BYTE OF ALL ONES         LD @ZA02332
MASK     EQU   7                   MASK FOR STCM            LD @ZA02332
DISP1    EQU   1                   DISPLACEMENT             LD @ZA02332
DISP4    EQU   4                   DISPLACEMENT             LD @ZA02332
         SPACE 2
*        STORAGE
         SPACE
         DS    0F                  FULLWORD BOUNDARY        LD @ZA02332
GETPARM  DC    X'FA000008'         SUBPOOL # AND LENGTH     LD @ZA02332
MINBUF   DC    X'012C'             MINIMUM BUFFER LENGTH
TEST25   DC    F'25'               ACCEPTABLE TEST NUMBER
MAXLNGTH DC    F'256'              MAX # OF CHARS FOR MVC
IJLBOT25 EQU   *
         DC    X'0227F5'           STX-ESC-EWRT
         DC    X'7F'               WCC-SOUND ALARM/RESTORE KEYBOARD/
*                                      START PRINT/80 CHARS PER LINE
         DC    X'3CC150'           REPEAT TO ADDR
         DC    C'E'                80 E CHARACTERS
         DC    C'H'                CHAR 81=H
         DC    X'11C25F'           SBA 159
         DC    C'HH'
         DC    X'11C36F'           SBA 239
         DC    C'HH'
         DC    X'11C47F'           SBA 319
         DC    C'HH'
         DC    X'11C64F'           SBA 399
         DC    C'HH'
         DC    X'11C75F'           SBA 479                     @ZA26729
         DC    C'HH'
         DC    X'11C7E5'           SBA   485
         DC    C'TEST PATTERN FOR 3275-2/3277-2'
         DC    X'11C86F'           SBA 559
         DC    C'HH'
         DC    X'11C87E'           SBA 574
         DC    C'ALIGNMENT'
         DC    X'11C97F'           SBA 639
         DC    C'HH'
         DC    X'114B4F'           SBA 719
         DC    C'HH'
         DC    X'114C5F'           SBA 799
         DC    C'HH'
         DC    X'114CE7'           SBA 807
         DC    C'*'                ASTERISK
         DC    X'1D40'             UNPROTECTED,NORMAL INTENSITY
         DC    X'13'               INSERT CURSOR
         DC    C'UNPROTECTED AREA    '
         DC    X'1D60'             PROTECTED,NORMAL INTENSITY
         DC    C'*'
         DC    X'114D6F'           SBA 879
         DC    C'HH'
         DC    X'114ED7'           SBA 919
         DC    C'OO'               TWO LETTER O'S
         DC    X'114E7F'           SBA 959
         DC    C'HH'
         DC    X'114FE7'           SBA 999
         DC    C'OO'               TWO LETTER O'S
         DC    X'11504F'           SBA 1039
         DC    C'HH'
         DC    X'11D15F'           SBA 1119
         DC    C'HH'
         DC    X'11D26F'           SBA 1199
         DC    C'HH'
         DC    X'11D37F'           SBA 1279
         DC    C'HH'
         DC    X'11D54F'           SBA 1359
         DC    C'HH'
         DC    X'11D65F'           SBA 1439
         DC    C'HH'
         DC    X'11D76F'           SBA 1519
         DC    C'HH'
         DC    X'11D87F'           SBA 1599
         DC    C'HH'
         DC    X'115A4F'           SBA 1679
         DC    C'HH'
         DC    X'115B5F'           SBA 1759
         DC    C'HH'
         DC    X'115C6F'           SBA 1839
         DC    C'H'
         DC    X'3C4040'           LAST LINE = 80
         DC    C'E'                CHARS OF E'S
         DC    X'03'               ETX
TST25SZ  EQU   *-IJLBOT25          LENGTH OF TEST 25
         SPACE 2
*        DSECTS
         SPACE
         IECTIOBX
         EJECT
         IECTDECB
         EJECT
         DCBD  DSORG=BX,DEVD=BS
         EJECT
         IECTDEBX
UCBDSECT DSECT
         DS    0F
         ORG   UCBDSECT+13
CHARADDR DS    CL3                 UNIT ADDR (EBCDIC)
MODTYPE  DS    CL1                 MODEL TYPE                  @ZA03972
         ORG   UCBDSECT+18
DVCLASS  DS    CL1                 DEVICE CLASS
UNITYPE  DS    CL1                 UNIT TYPE
         ORG   UCBDSECT+36
RLN      DS    CL1                 RELATIVE LINE NUMBER
         END
