         TITLE 'IGE0104A - BTAM ERROR DATA CHECK LOAD ONE'
IGE0104A CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0104A                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM ERROR DATA CHECK LOAD ONE                                   *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS MODULE IS ENTERED ON THE OCCURRENCE OF A DATA CHECK ERROR.  *
*    A CHECK IS MADE TO DETERMINE IF THE ERROR OCCURRED BEFORE,       *
*    DURING, OR AFTER TEXT TRANSFER.  IF REQUIRED, A CHANNEL          *
*    PROGRAM IS CONSTRUCTED AND EXECUTED.  LINKAGE IS PROVIDED        *
*    TO DATA CHECK LOAD TWO AND THE DIAGNOSTIC WRITE/READ ROUTINES.   *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (60 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        614 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD   *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0104A                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        CREATES NEW CHANNEL PROGRAM FOR RECOVERY                     *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        NONE                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    READ                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YM05652K (1/24/74)                                               *
*    ZA00504  (5/9/74)                                                *
*    ZA26418  (10/11/77)                                              *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG6      SET BASE REG
         B     BEGIN                                           @ZA00504
         DC    C' IGE0104A '       MODULE NAME                 @ZA00504
         DC    XL3'014830'         PTF NUMBER                  @ZA26418
         DC    CL8'&SYSDATE'       DATE LAST ASSEMBLY          @ZA00504
BEGIN    DS    0H                                              @ZA00504
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         DROP  ERREG5
         L     CCWREG,IOBFL3       ADDRESS OF CCW
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       ADJUST TO FAILING CCW
         MVC   IOBWORK(L1),L5(CCWREG)   MOVE TP OP CODE TO WORK AREA
         NI    IOBWORK,L255-CCWMASK1   MASK OUT UNUSED BITS
         MVC   IOBWORK+L1(L1),DECTYPE+L1(DECBRG)   OPERATION TYPE
         NI    IOBWORK+L1,L255-HIBIT   MASK OUT RESET BIT
         L     UCBREG,IOSUCB       BASE UCB
         MVC   IOBWORK+L2(L1),UCBTYP+L3(UCBREG)   ADAPTER TYPE
         NI    IOBWORK+L2,MASK3    MASK OUT UNUSED HALF BYTE
         MVC   IOBWORK+L3(L1),UCBTYP+L3(UCBREG)   CONTROL UNIT TYPE
         NI    IOBWORK+L3,MASK15   MASK OUT UNUSED HALF BYTE
         CLI   ZERO(CCWREG),CCWAPOLL   POLL COMMAND
         BE    ERRDCD09            YES CALL IN SECOND LOAD
         CLI   ZERO(CCWREG),CCWINCMD   INHIBIT COMMAND
         BE    READ                YES
         CLI   ZERO(CCWREG),CCWRDCMD   READ COMMAND
         BNE   ERRDCD09            NO CALL IN SECOND LOAD
READ     EQU   *
         TM    IOBWORK,CCWPTXFR    READ PRIOR TO TEXT TRANSFER
         BZ    ERRDCD09            YES CALL IN SECOND LOAD
         TM    DCBBFTEK(DCBREG),X08   IS DYNAMIC BUFFERING SPECIFIED
         BZ    ERRDCD14            NO, TEST FOR DATA TRANS
         NI    IOSFLA,L255-IOSERR   TURN ERP CONTROL OFF
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
         B     ERRDCD01            POST ERROR
ERRDCD14 TM    IOBWORK,CCWTXTDT    DURING TEXT TRANSFER
         BZ    ERRDCD00            NO CHECK FOR READ RESPONSE TEXT
         TM    DCBERROP(DCBREG),DCBRDTRY   READ RETRY SPECIFIED
         BZ    ERRDCD01            NO CHECK FOR LERB
         CLI   IOBERRCT,ERTRYCT    MORE RETRIES NECESSARY
         BNL   ERRDCD08            NO SET TO CALL SECOND LOAD
         CLI   IOBWORK+L1,DECRDRPT   OPERATION TYPE READ REPEAT
         BE    ERRDCD03            YES RE-EXECUTE CHANNEL PROGRAM
         CLI   IOBWORK+L1,DECWRTTV   OPERATION WRITE TTV
         BNE   ERRDCD13            NO
         LA    ERREG2,IOBCPA       POINT TO FIRST CCW
NEXTCCW  EQU   *
         LA    ERREG2,L8(ERREG2)   POINT TO NEXT CCW
         CLI   L5(ERREG2),RESET    FIND READ TV PART OF WRITE TTV
         BNE   NEXTCCW             NO
         ST    ERREG2,IOBSTART     SET RESTART ADDRESS
         B     ERRDCD03            EXECUTE CHANNEL PROGRAM
ERRDCD13 EQU   *
         NI    DECTYPE+L1(DECBRG),HIBIT   CLEAR LOW ORDER 6 BITS OF
*                                  TYPE
         OI    DECTYPE+L1(DECBRG),DECRDRPT   SET OPERATION TYPE
*                                  TO READ TP
ERRDCD10 TM    DECTYPE(DECBRG),AUTOPOLL   IS AN AUTOPOLL OPERATION
         BZ    ERRDCD07            IN PROGRESS.  IF NOT, BRANCH.
         L     ERREG4,DECAREA(DECBRG)   LOAD MESSAGE AREA ADDRESS
         MVC   DECPOLPT(L1,DECBRG),ZERO(ERREG4)   AND SAVE INDEX BYTE.
ERRDCD07 EQU   *
         LR    ERREG2,IOSBREG      SAVE IOSB POINTER
         OI    IOBINCAM,ERPBIT     ERP IN CONTROL BIT
         LA    NAMEREG,EWAIAREA    REGISTER SAVE AREA ADDRESS
         LR    IOSBREG,DECBRG      DECB ADDRESS IN PARAMETER REG
         READ  (1),T,MF=E          CONSTRUCT CHANNEL PROGRAM
         LR    IOSBREG,ERREG2      RESTORE IOSB BASE
         NI    IOBINCAM,L255-ERPBIT   TURN OFF ERP BIT
ERRDCD03 IC    ERREG2,IOBERRCT     RETRY COUNT UPDATE
         LA    ERREG2,L1(ERREG2)   BUMP COUNT BY ONE
         STC   ERREG2,IOBERRCT     STORE BACK
         MVI   IOBSENS0,ZERO       CLEAR SENSE BYTE
         TM    DCBERROP(DCBREG),DCBLERB   IS LERB REQUIRED
         BO    ERRDCD04            YES
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
         L     NAMEREG,TRANSRTN    LOAD NAME
         B     ERRDCD05            XCTL
ERRDCD04 LA    NAMEREG,ERLOD12     ADDRESS OF  LERB ROUTINE
         MVI   IOBERRCT-L1,LERBDC   INDICATE DATA CHECK ERROR
ERRDCD05 EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVT,ERRETR
         L     ERRETR,CVTXTLER     XCTL ROUTINE NAME
         BR    ERRETR              XCTL
         DROP  ERRETR
ERRDCD00 CLI   IOBWORK,CCWRRTXT    READ RESPONSE TO TEXT
         BNE   ERRDCD01            NO CHECK FOR LINE ERROR RECORDS
         TM    DCBERROP(DCBREG),CCWRTRT   LINE ERROR RECORDING REQUIRED
         BO    ERRDCD06            YES CHECK COUNT
ERRDCD01 TM    DCBERROP(DCBREG),DCBLERB   LINE ERROR RECORDING REQUIRED
         BO    ERRDCD02            YES SET UP FOR LERB
         LH    NAMEREG,ERLOD5      ADDRESS OF ERROR POST
         NI    IOSFLA,L255-IOSERR   INDICATE PERM ERROR        @ZA26418
         NI    IOBFLAG1,L255-IOBERRTN    IOB ALSO              @ZA26418
         B     ERRDCD05            BRANCH TO POST ROUTINE
ERRDCD02 MVI   IOBERRCT-L1,LERBDC   INDICATE DATA CHECK TO LERB
         NI    IOSFLA,L255-IOSERR   INDICATE PERM ERROR
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
         B     ERRDCD04            EXIT TO LERB PRINT ROUTINE
ERRDCD06 CLI   IOBERRCT,ERTRYCT    RETRY COUNT AT MAXIMUM
         BNL   ERRDCD08            YES, CALL IN SECOND LOAD
         CLI   IOBWORK+L1,CCWWRTCT   OPERATION TYPE WRITE CONTINUE
         BE    ERRDCD03            YES RETRY CHAN PROG
         CLI   IOBWORK+L1,TTV      OPERATION TYPE WRITE TTV
         BE    ERRDCD03            YES, RETRY CHANNEL PROGRAM
         L     UCBREG,IOSUCB       BASE UCB
         TM    UCBTYP+L3(UCBREG),NOTADAP1   IS THIS ADAPTER TYPE I
         BNZ   ERRDCD12            NO, BRANCH
         TM    UCBTYP(UCBREG),NOT2740   IS THIS 2740
         BNZ   ERRDCD12            IF NOT BRANCH
         TM    UCBTYP+L1(UCBREG),VERIMAT   IS THIS 2760
         BNO   ERRDCD12            IF NOT, BRANCH
         CLI   IOBWORK+L1,TIO      WRITE TIO
         BE    ERRDCD11            BRANCH IF YES
         CLI   IOBWORK+L1,TCO      WRITE TCO
         BE    ERRDCD11            BRANCH IF YES
         CLI   IOBWORK+L1,TVO      WRITE TVO
         BE    ERRDCD11            BRANCH IF YES
ERRDCD12 CLI   IOBWORK+L1,TCO      IS THIS A WRITE ERASE?
         BE    ERRDCD03            YES RETRY CHANNEL PROGRAM
         NI    DECTYPE+L1(DECBRG),HIBIT   CLEAR OPERATION TYPE CODE
         OI    DECTYPE+L1(DECBRG),CCWRTRT   OPERATION WRITE CONTINUE
         B     ERRDCD07            CONSTRUCT CHANNEL PROGRAM
ERRDCD08 MVI   IOBERRCT+L1,MASK3   INDICATE LERB CHECK ENTRY
ERRDCD09 LA    NAMEREG,ERLOD10     NAME OF DISABLE CHECK ROUTINE
         B     ERRDCD05            XCTL
ERRDCD11 EQU   *
         SH    CCWREG,TWENTY4      BACK UP TO WR D PRE O CCW
         CLI   L7(CCWREG),L2       CHECK TO SEE IF A DATA CHECK ERROR
*                                  HAS ALREADY BEEN HANDLED ON
*                                  THIS OPERATION
         BE    ERRDCD03            IF COUNT IS TWO, BRANCH BECAUSE
*                                  BECAUSE CCW HAS ALREADY BEEN ALTERED
         ST    CCWREG,IOBSTART     RESTART ON THIS ONE
         L     ERREG2,ZERO(CCWREG)   DATA ADDRESS
         LA    ERREG2,L1(ERREG2)   BUMP PAST EOA
         STCM  ERREG2,L7,L1(CCWREG)   STORE IN CCW             @ZA00504
         L     ERREG2,L4(CCWREG)   DATA LENGTH
         BCTR  ERREG2,ZERO         SUBTRACT 1
         ST    ERREG2,L4(CCWREG)   STORE IN CCW
         B     ERRDCD03            UPDATE RETRY COUNT
         EJECT
**********
*
*        CONSTANTS
*
**********
TRANSRTN DC    F'9042'             BTAM TRANSLATE ROUTINE NAME
ERRCN8   DC    H'8'                ARITHMETIC
ERLOD5   DC    H'5041'             XCTL NAME OF ERROR POST
TWENTY4  DC    H'24'               SIZE OF 3 CCWS
PACHAREA DC    15C'104A'           PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
ERPBIT   EQU   X'08'
NOTADAP1 EQU   X'E0'
NOT2740  EQU   X'0B'
VERIMAT  EQU   X'0C'
CCWAPOLL EQU   X'09'
CCWMASK1 EQU   X'C0'
CCWPTXFR EQU   X'30'
CCWINCMD EQU   X'0A'
CCWRDCMD EQU   X'02'
CCWRRTXT EQU   X'20'
CCWRTRT  EQU   X'04'
CCWTXTDT EQU   X'10'
CCWWRTCT EQU   X'04'
DECWRTTV EQU   X'0F'
DCBBFTEK EQU   X'20'
DCBERROP EQU   33
DCBLERB  EQU   X'08'
DCBRDTRY EQU   X'02'
DECRDRPT EQU   X'07'
DECTYPE  EQU   4
DECAREA  EQU   12
DECPOLPT EQU   36
AUTOPOLL EQU   X'80'
UCBTYP   EQU   16
ERLOD10  EQU   42
ERLOD12  EQU   2042
ERTRYCT  EQU   X'02'
HIBIT    EQU   X'C0'
LERBDC   EQU   X'01'
ZERO     EQU   0                   LENGTH AND DISPLACEMENT
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L255     EQU   255                 AND MASK
X08      EQU   X'08'
MASK3    EQU   X'F0'
MASK15   EQU   X'0F'
RESET    EQU   X'02'
TCO      EQU   14                  OPERATION TYPE FOR WRITE TCO
TIO      EQU   12                  OPERATION TYPE FOR WRITE TIO
TTV      EQU   15                  OPERATION TYPE FOR WRITE TTV
TVO      EQU   18                  OPERATION TYPE FOR WRITE TVO
**********
*
*        REGISTER DEFINITION
*
**********
ERREG1   EQU   0                   WORK REG/PARAMETER REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
ERREG2   EQU   2                   WORK REG
ERREG3   EQU   3                   WORK REG
CCWREG   EQU   4                   CCW ADDRESS REG
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE/WORK REG
BASEREG  EQU   8                   PROGRAM BASE
EWAREG   EQU   9                   ERP WORKAREA BASE
IOBRG    EQU   10                  IOB BASE
ERREG4   EQU   11                  WORK REG
ERREG5   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  LINK REG
ERREG6   EQU   15                  LINK REG,WORK REG
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
