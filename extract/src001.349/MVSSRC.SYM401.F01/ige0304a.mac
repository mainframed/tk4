         TITLE 'IGE0304A - BTAM ERP INTERVENTION REQUIRED MODULE'
IGE0304A CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0304A                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM INTERVENTION REQUIRED ERP                                   *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    IF REQUIRED, RETRIES CHANNEL PROGRAM THAT FAILED BECAUSE OF      *
*    AN INTERVENTION REQUIRED CONDITION. DETERMINES IF LINE ERROR     *
*    RECORDING IS REQUIRED AND EXITS EITHER TO LERB OR POST ROUTINE.  *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (68 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        692 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD   *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0304A                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        SET DECERRST FOR SHOULD NOT OCCUR ERROR.                     *
*        SET DECERRST FOR DISABLE REQUIRED.                           *
*        ALTERED CHANNEL PROGRAM.                                     *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        NONE                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YA02435  (11/5/73)                                               *
*    YA02436  (11/5/73)                                               *
*    YM05652K (1/25/74)                                               *
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         L     UCBREG,IOSUCB       BASE UCB
         DROP  ERREG5
         L     CCWREG,IOBFL3       ADDRESS OF CCW
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       ADJUST TO FAILING CCW
         CLI   IOBERRCT+L1,MASK3   SPECIAL ENTRY FOR DISABLE
         BE    ERRTRY03            YES CHECK FOR DISABLE
         CLI   IOBERRCT+L1,X01     SPECIAL RETURN FOR WR EOT
         BE    ERRTRY04            YES, BRANCH
         MVC   IOBWORK(L1),L5(CCWREG)   TP OP CODE TO WORK AREA
         NI    IOBWORK,L255-CCWMASK1   MASK OUT UNUSED BITS
         CLI   ZERO(CCWREG),CCWAPOLL   THIS AUTOPOLL
         BE    ERRIRI02            YES SET UP POLL ADDRESS
         CLI   ZERO(CCWREG),CCWRDCMD   READ COMMAND
         BE    ERRIRR00            YES CHECK WHEN
         CLI   ZERO(CCWREG),CCWINHCD   INHIBIT COMMAND
         BE    ERRIRR00            TREAT SAME AS READ
         CLI   ZERO(CCWREG),CCWDIAL   DIAL COMMAND
         BE    ERRIRI00            YES RETRY AT START
         CLI   ZERO(CCWREG),CCWPREP   PREPARE COMMAND
         BE    ERRIRI03            YES, CHECK ADAPTER TYPE
         CLI   ZERO(CCWREG),CCWWRT   WRITE COMMAND
         BE    ERRIRWT1            YES SET UP TO FINI
         CLI   ZERO(CCWREG),CCWBRK   BREAK COMMAND
         BE    ERRIRWT1            YES SET UP TO FINI
         OI    DECERRST(DECBRG),CSWSNO   SHOULD NOT OCCUR
         B     ERRTRY01            SET UP TO POST
ERRIRWT1 MVI   IOBERRCT,ERTRYCT    SET RETRY COUNT TO MAXIMUM
         B     ERRTOD03            CHECK FOR LINE ERROR RECORDING
ERRIRWT0 CLI   IOBWORK,CCWRRPL     READ RESPONCE TO POLLING
         BNE   ERRIRI01            NO, CHECK FOR READ INDEX
         CLI   DECBTYPE(DECBRG),TYPEWTTV   IS READ RESP IN WRITE TTV
         BNE   ERRIRI00            NO, SET UP START OF CHAN PROG
         LA    ERREG4,IOBCPA+L16   SET UP START OF READ
         ST    ERREG4,IOBSTART     SET RESTART ADDRESS
         B     ERRTOD03            CHECK FOR LINE ERROR RECORDING
ERRIRI01 CLI   IOBWORK,CCWRDIX     THIS READ RESP TO INDEX
         BNE   ERRIRI00            NO, SET UP START OF CHAN PROG
ERRIRI02 EQU   *
         L     ERREG1,IOBCPA+L8    LOAD ENTRY ADDRESS FROM PREVIOUS
*                                  POLL (WITH POLL COMMAND CODE
*                                  IN HIGH ORDER BYTE).
         MVC   IOBCPA+L9(L3),DECENTRY+L1(DECBRG)   MOVE IN ADDRESS
*                                  OF LAST ENTRY POLLED (TO BE
*                                  USED AS RESTART ADDRESS).
         L     ERREG4,IOBCPA+L8    LOAD ENTRY ADDRESS FOR NEXT POLL
*                                  (WITH POLL COMMAND CODE IN
*                                  HIGH ORDER BYTE).
         SR    ERREG1,ERREG4       SUBTRACT NEW POLL ADDRESS
*                                  FROM PREVIOUS POLL ADDRESS.
         AH    ERREG1,IOBCPA+L14   IF THE NEW POLL ADDRESS WERE
*                                  LESS THAN THE PREVIOUS POLL ADDRESS
*                                  THE EFFECT IS TO ADD THE ABSOLUTE
*                                  DIFFERENCE TO THE PREVIOUS COUNT;
*                                  OTHERWISE THE EFFECT IS TO SUBTRACT.
         STH   ERREG1,IOBCPA+L14   STORE ADJUSTED COUNT IN
*                                  POLL COMMAND.
ERRIRI00 LA    ERREG4,IOBCPA       RESOLVE START OF CHAN PROGRAM
         ST    ERREG4,IOBSTART     STORE CPA POINTER
         B     ERRTOD03            CHECK FOR LINE ERROR RECORDING
ERRIRI03 EQU   *
         MVC   IOBWORK+L1(L1),UCBTYP+L3(UCBREG)   MOVE ADAPTER
         NI    IOBWORK+L1,MASK3    CLEAR USED BITS
         CLI   IOBWORK+L1,X10      IBM TYPE 1 ADAPTER
         BNE   ERRIRI00            NO, BRANCH
         TM    UCBTYP(UCBREG),X04   TEST
         BZ    ERRIRI00            FOR
         TM    UCBTYP(UCBREG),X03   2741
         BNM   ERRIRI00            NOT 2741
         TM    UCBTYP+L1(UCBREG),X90   SWITCHED LINE
         BNZ   ERRIRI00            YES, BRANCH
         MVC   IOBERINF+L8(L8),ZERO(CCWREG)   SAVE CCW
         MVC   IOBERINF(L1),IOBSENS0   SAVE SENSE STATUS
         MVC   IOBERINF+L1(L7),IOBCSW   SAVE CSW
         MVI   IOBWORK+L2,X1F      MOVE IN EOT
         LA    ERREG1,IOBWORK+L2   GET ADDRESS OF EOT
         ST    ERREG1,IOBERCCW     STORE IN CCW
         LA    ERREG1,CCWWRT       GET COMMAND
         STC   ERREG1,IOBERCCW     STORE COMMAND
         ST    ERREG1,IOBERCCW+L4  STORE COUNT
         LA    ERREG1,IOBERCCW     LOAD CCW ADDRESS
         ST    ERREG1,IOBSTART     STORE CCW ADDRESS
         MVI   IOBERRCT+L1,X01     MOVE IN SPECIAL RETURN
         B     ERRTOD03            UPDATE RETRY COUNT
ERRTRY04 EQU   *
         NI    IOBERRCT+L1,XFE     CLEAR SPECIAL RETURN
         CLI   IOBERRCT,ERTRYCT    RETRY REQUIRED
         BNL   ERRTRY05            NO, BRANCH
         LA    ERREG4,IOBCPA
         ST    ERREG4,IOBSTART
         B     ERRTRY06            RESTART FAILING CCW
ERRTRY05 EQU   *
         MVC   IOBSENS0(L1),IOBERINF   RESTORE SENSE
         MVC   IOBCSW(L7),IOBERINF+L1   RESTORE CSW
         B     ERRPERM1            POST WITH ERROR
ERRIRR00 TM    IOBWORK,CCWPTXFR    PRIOR TO TEXT TRANSFER
         BZ    ERRIRWT0            YES, CHECK FOR RESPONCE TO POLL
         CLI   IOBWORK,CCWRRTXT    READ RESPONSE TO TEXT
         BNE   ERRTSTX             NO CHECK FOR READ TEXT       YA02435
         TM    DCBERROP(DCBREG),DCBRDTRY   TEXT READ RETRIES REQUIRED
         BZ    ERRTOT06            NO CHECK FOR LINE ERROR UPDATE
         MVC   IOBWORK+L1(L1),DECTYPE+L1(DECBRG)   OP TYPE TO WORK AREA
         NI    IOBWORK+L1,L255-HIBIT   CLEAR UNUSED BIT
         CLI   IOBWORK+L1,CCWWRTCT   OPERATION A WRITE CONTINUE
         BNE   ERRTOD03            CHECK FOR LINE ERROR UPDATE
ERRTOT06 TM    DCBERROP(DCBREG),DCBLERB   LINE ERROR RECORDING REQUIRED
         BZ    ERRTOT04            NO POST COMPLETE WITH ERROR
         MVI   IOBERRCT-L1,LERBIR   INDICATE LERB RECORDING
         NI    IOSFLA,L255-IOSEX-IOSERR   INDICATE POST NO WTO
         NI    IOBFLAG1,L255-IOBIOERR-IOBERRTN   ALSO IN IOB
         LA    NAMEREG,ERLOD12     ADDRESS OF LERB ROUTINE
         B     ERRLINK1
ERRTSTX  EQU   *                                                YA02435
         CLI   IOBWORK,CCWRDTXT    READ TEXT                    YA02435
         BNE   ERRTOT06            NO                           YA02435
         TM    DCBERROP(DCBREG),DCBLERB   LERB REQUIRED         YA02435
         BZ    ERRPERM1            NO, POST COMPLETE            YA02435
         MVI   IOBERRCT-L1,LERBIR   INT. REQ. LERB              YA02435
         B     ERRPERM1            EXIT                         YA02435
ERRTOD03 TM    DCBERROP(DCBREG),DCBLERB   LINE ERROR RECORDING REQUIRED
         BZ    ERRCTT01            NO CONTINUE FLOW
         MVI   IOBERRCT-L1,LERBIR   INDICATE INT REQ LERB
ERRCTT01 CLI   IOBERRCT,ERTRYCT    RETRY REQUIRED
         BL    ERRTRY02            YES SET UP FOR RETRY
         CLI   ZERO(CCWREG),CCWDISAB   DISABLE EXECUTED
         BE    ERRPERM1            YES POST COMPLETE WITH ERROR
ERRTRY01 TM    UCBTYP+L1(UCBREG),SWLINE   DISABLE REQUIRED
         BZ    ERRPERM1            NO POST COMPLETE
ERRTRY03 EQU   *
         MVC   IOBERINF+L8(L8),ZERO(CCWREG)   SAVE CCW
         MVC   IOBERINF(L1),IOBSENS0   SAVE SENSE DATA
         MVC   IOBERINF+L1(L7),IOBCSW   SAVE CSW DATA
         MVI   IOBERRCT+L1,DISABIND   INDICATE DISABLE REQUIRED
         LA    ERREG1,IOBERCCW     CHANNEL PROG ADDRESS
         ST    ERREG1,IOBSTART     CHAN PROG POINTER
         OI    DECERRST(DECBRG),DISABLOK   INDICATE DISABLE EXECUTED
         MVC   IOBERCCW(L8),DISABCMD   GENERATE DISABLE CCW
ERRTRY02 IC    ERREG1,IOBERRCT     ERROR RETRY COUNT
         LA    ERREG1,L1(ERREG1)   BUMP BY ONE
         STC   ERREG1,IOBERRCT     RESTORE IN COUNT FIELD
         CLI   IOBERRCT-L1,LERBIR    LERB REQUIRED              YA02436
         BE    ERLERBTO            YES, EXIT TO LERB ROUTINE    YA02436
ERRTRY06 EQU   *
         MVI   IOBSENS0,ZERO       CLEAR SENSE BYTE
RETRY    EQU   *
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
         L     NAMEREG,TRANSRTN    LOAD NAME
         B     ERRLINK1            XCTL
ERRPERM1 EQU   *
         NI    IOSFLA,L255-IOSERR
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
         CLI   IOBERRCT-L1,LERBIR    LERB REQUIRED              YA02436
         BE    ERLERBTO            YES, EXIT                    YA02436
         LH    NAMEREG,ERLOD5      ADDRESS OF POST ROUTINE
ERRLINK1 EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE
         BR    ERRETR              XCTL
         DROP  ERRETR
ERLERBTO LA    NAMEREG,ERLOD12     ADDRESS OF LERB ROUTINE
         B     ERRLINK1
ERRTOT04 NI    IOSFLA,L255-IOSERR   INDICATE PERM ERROR
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
         B     RETRY              GO EXCP
         EJECT
**********
*
*        CONSTANTS
*
**********
         DS    0D
DISABCMD DC    X'2F00000020300001'
TRANSRTN DC    F'9042'             BTAM TRANSLATOR ROUTINE NAME
ERRCN8   DC    H'8'                DECIMAL 8
ERLOD5   DC    H'5041'
PACHAREA DC    17C'304A'           PATCHAREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
CCWAPOLL EQU   X'09'
CCWBRK   EQU   X'0D'
CCWDIAL  EQU   X'29'
CCWDISAB EQU   X'2F'
CCWINHCD EQU   X'0A'
CCWMASK1 EQU   X'C0'
CCWPREP  EQU   X'06'
CCWPTXFR EQU   X'30'
CCWRDCMD EQU   X'02'
CCWRDIX  EQU   X'0A'
CCWRRPL  EQU   X'05'
CCWRRTXT EQU   X'20'
CCWWRT   EQU   X'01'
CCWWRTCT EQU   X'04'
CSWSNO   EQU   X'40'
DCBERROP EQU   33
DCBLERB  EQU   X'08'
DCBRDTRY EQU   X'02'
DECBTYPE EQU   X'04'
DECENTRY EQU   20
DECERRST EQU   29
DECTYPE  EQU   4
DISABIND EQU   X'80'
DISABLOK EQU   X'08'
ERLOD12  EQU   2042
ERTRYCT  EQU   X'02'
HIBIT    EQU   X'C0'
LERBIR   EQU   X'02'
L1       EQU   1                    LENGTH AND DISPLACEMENT
L2       EQU   2                    LENGTH AND DISPLACEMENT
L3       EQU   3                    LENGTH AND DISPLACEMENT
L4       EQU   4                    LENGTH AND DISPLACEMENT
L5       EQU   5                    LENGTH AND DISPLACEMENT
L6       EQU   6                    LENGTH AND DISPLACEMENT
L7       EQU   7                    LENGTH AND DISPLACEMENT
L8       EQU   8                    LENGTH AND DISPLACEMENT
L9       EQU   9                    LENGTH AND DISPLACEMENT
L14      EQU   14                   LENGTH AND DISPLACEMENT
L16      EQU   16                   LENGTH AND DISPLACEMENT
L255     EQU   255                  AND MASK
MASK3    EQU   X'F0'
ZERO     EQU   X'00'                LENGTH AND DISPLACEMENT
SWLINE   EQU   X'90'
TYPEWTTV EQU   X'0F'
UCBTYP   EQU   16                   DEVICE TYPE CODE
XFE      EQU   X'FE'
X1F      EQU   X'1F'
X90      EQU   X'90'
X03      EQU   X'03'
X04      EQU   X'04'
X10      EQU   X'10'
X01      EQU   X'01'
CCWRDTXT EQU   X'11'               TP OP CODE FOR READ TEXT     YA02435
**********
*
*        REGISTER DEFINITION
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
EWAREG   EQU   2                   ERP WORKAREA BASE
BASEREG  EQU   3                   PROGRAM BASE
CCWREG   EQU   4                   CCW ADDRESS REG
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE
ERREG2   EQU   8                   WORK REG
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG4   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  RETURN REG
ERREG5   EQU   15                  WORK REG
         EJECT
         IECDRQE
         EJECT
         IEZIOB
IOBCMD3  EQU   IOBCPA+L16
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
