         TITLE 'IGE0904B - BTAM ERP TRANSLATE MODULE'
IGE0904B CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0904B                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM ERROR RECOVERY TRANSLATE PROCEDURE                          *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 001                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    MAKE CORRESPONDING CHANGES TO REAL CCWS AFTER VIRTUAL ONES       *
*    HAVE BEEN ALTERED.                                               *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA                                                         *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, LOCAL LOCKS     *
*        HELD AROUND TRANSLATOR CALLS                                 *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0904B                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        REAL CHANNEL PROGRAM FOR RETRY.                              *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    SUPERVISOR VIA SVC 3                                             *
*    LINKAGE TO TP RECORDER (IGE0625F)                                *
*    LINKAGE TO STAT. UPDATE ROUTINE (IGE0025D)                       *
*    LINKAGE TO WTO ROUTINE (IGE0025C)                                *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        ESTAE                                                        *
*        SETLOCK                                                      *
*        SETRP                                                        *
*        GETMAIN                                                      *
*        FREEMAIN                                                     *
*        SVC 15 ERROR EXCP                                            *
*        IOS CCW TRANSLATOR                                           *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*        TCCW         (MAPPED BY IECDTCCW)                            *
*        SDWA         (MAPPED BY IHASDWA)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*    ERP EXTENSION TO IOB                                             *
*                                                                     *
* MACROS:                                                             *
*    FREEMAIN                                                         *
*    GETMAIN                                                          *
*    ESATE                                                            *
*    SETLOCK                                                          *
*    SETRP                                                            *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)   (COMPLETE REWRITE)                          *
*    YM04094K (11/9/73)                                               *
*    YM05673K (1/7/74)                                                *
*    YM05652K (1/25/74)                                               *
*    YM05680K (1/25/74)                                               *
*    YM05697K (2/12/74)                                               *
*    YM07715K (4/10/74)                                               *
*    ZA03207 (03/24/75)                                               *
*    ZA04156 (04/29/75)          AZ04166 (08/25/75)                   *
*    AZ04853 (08/27/75)          AZ04874 (09/23/75)
*    AZ07662 (11/25/75)          AZ07668 (01/23/76)                   *
*    AZ08475 (01/14/76)          AZ08084 (06/21/76)
*    AZ12160  (08/06/76)         AZ13203 (09/08/76)                   *
*    AZ14034  (09/14/76)         AZ14061 (10/11/76)                   *
*    AZ14136  (10/26/76)         AZ15404 (11/23/76)                   *
*    AZ17698  (04/04/77)         AZ20113 (09/14/77)                   *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      BASE MODULE
         USING RQE,RQEREG
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         B     BEGIN               BRANCH AROUND ID            @ZA12160
         DC    C'IGE0904B'         MODULE ID                   @ZA12160
         DC    CL8'&SYSDATE'       ASSEMBLY DATE               @ZA12160
         DC    C'UZ14366'                                      @ZA20133
         DS    0H                                              @ZA12160
BEGIN    LR    IOSBREG,PARMREG     BASE IOSB                   @ZA12160
         L     RQEREG,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORK AREA
         L     IOBRG,RQEIOB        BASE IOB
         LA    IOBRG,ZERO(IOBRG) CLEAR HI ORDER BYTE           @ZA03207
         MVC   EWAIEXIT(L16),EXITADDR   MOVE ESTAE LIST TO ERP WA
         LA    PARMREG,EWAIEXIT    POINT TO ESTAE LIST
*            ONE LINE REMOVED BY------------------------------>@ZA15404
         MVI   IOBSENS1,ZERO       CLEAR UNUSED SENSE BYTE     YM04094K
         MVC   IOSSNS(L2),IOBSENS0   MOVE SENSE TO IOSB
*
*                                  THE FOLLOWING MOVE IS FOR THE WHOLE
*                                  CSW IN AN ATTEMPT TO FAKE EXCP
*                                  COMPATIBILITY INTERFACE IOB MAPPING
*                                  ON FINAL EXIT TO APPENDAGE. THIS
*                                  WILL HAVE TO BE CHANGED WHEN EXCP
*                                  STARTS TO VALIDITY CHECK THE CSW
*                                  CONTENTS.
*
         MVC   IOSCSW(L7),IOBIOCSW   MOVE CSW COMPLETELY
START    EQU   *
         L     ERREG1,RQEUCB       SET UP UCB ADDRESSABILITY   @ZA08084
         TM    L19(ERREG1),NINETY  BISYNC DEVICE               @ZA08084
         BO    START1              YES,CHECK V=R               @ZA08084
         L     ERREG1,IOBECBPT     SET UP DECB ADDRESSABILITY  @ZA08084
         CLI   L5(ERREG1),READTI   IS THIS A READ INITIAL      @ZA08084
         BE    START0              BRANCH, YES                 @ZA20113
         CLI   L5(ERREG1),READTIR  IS THIS READ TIR            @ZA20113
         BNE   START1              NO,CHECK V=R                @ZA08084
START0   L     ERREG1,IOBSTART     GET CCW START ADDRESS PTR   @ZA20113
         CLI   L16(ERREG1),L8      IS THIS A TIC               @ZA08084
         BNE   START1              NO,CHECK V=R                @ZA08084
         CLI   L22(ERREG1),L2      SPEC. ERROR COUNT=02        @ZA08084
         BE    START1              YES,CHECK V=R               @ZA08084
         CLI   L22(ERREG1),ZERO    SPEC. ERROR COUNT=0         @ZA08084
         MVI   L22(ERREG1),L1      SET TO 1                    @ZA08084
         BE    EXCPCOND            YES,CHECK FOR EXCP. COND.   @ZA08084
         MVI   L22(ERREG1),L2      SET TO 2                    @ZA08084
EXCPCOND TM    IOBFLAG1,L4         EXCEPTIONAL CONDITION ON    @ZA08084
         BNO   START1              NO,CHECK V=R                @ZA08084
         TM    IOBFLAG1,L2         ERP IN CONTROL              @ZA08084
         BNO   START1              NO,CHECK V=R                @ZA08084
         CLI   EWAIND1,EWAIWTOL    WTO BIT ON                  @ZA08084
         BE    START1              YES,CHECK V=R               @ZA08084
         NI    IOSFLA,L255-IOSEX-IOSERR  SET NORM COMP IN IOSB @ZA08084
         NI    IOBFLAG1,L255-IOSEX-IOSERR  ALSO IN IOB         @ZA08084
         MVI   IOBWORK+L1,SPMBFLAG SET SPEC FLAG FOR MB        @ZA08084
         MVI   EWAIND1,EWAISVCF    SET UP FOR SVC 15           @ZA08084
START1   TM    RQETYPE,RQE1TO1        DOES V EQUAL R ?         @ZA08084
         BO    RESET1                 YES SKIP TRANSLATION     @ZA04166
         TM    IOBFLAG1,IOBERRTN+IOBIOERR   IS THIS A RETRY SITUATION
         BO    RETRY               YES
         TM    EWAIND0,EWAIPMAP    ANY PARALLEL MAPPING DONE
         BNO   RESET               NO
         L     PARMREG,EWAITCCW    ADDR OF TCCW             L5 @ZA04156
         BAL   ERREG7,CHANGE1      DO THE UNFIX             L5 @ZA04156
RESET    EQU   *
         BAL   ERREG1,ERPEXT  GET ADDR OF CURR EXT ENTRY    L5 @ZA04156
         CLC   ZERO(L4,ERRETR),FULL0  ENTRY EXIST?          L5 @ZA04156
         BE    RESET1                 NO,NOTHING TO UNFIX   L5 @ZA04156
         L     PARMREG,ZERO(ERRETR)   ADDR OF TCCW          L5 @ZA04156
         BAL   ERREG7,CHANGE1         UNFIX IT              L5 @ZA04156
RESET1   EQU   *                                            L5 @ZA04156
*        ONE LINE OF CODE DELETED BY APAR ---------->          @ZA07668
         L     ERREG4,IOBSTART     GET RESTART CCW ADDRESS     @ZA13203
         TM    IOBFLAG1,IOBERRTN+IOBIOERR  RETRY SITUATION?    @ZA13203
         BO    STOREIT                     YES                 @ZA13203
         XC    EWAITCCW(L4),EWAITCCW CLEAR OUR TCCW ADDRESS    @ZA13203
         L     ERREG4,EWAIOVST     GET ORIGINAL IOSVST
STOREIT  ST    ERREG4,IOSVST       UPDATE IOSVST FIELD         @ZA13203
         LRA   ERREG4,ZERO(ERREG4)   GET REAL EQUIVALENT
         ST    ERREG4,IOSRST       UPDATE IOSRST FIELD
CLEANUP  EQU   *
*              ONE LINE REMOVED BY---------------------------->@ZA15404
         LR    PARMREG,IOSBREG     RESTORE IOSB AS PARAMETER
*
*                                  EXIT TO IOS WTO MUST BE MADE BEFORE
*                                  EXIT TO STAT UPDATE. WTO GOES TO
*                                  STAT UPDATE, BUT STAT UPDATE DOES
*                                  NOT GO TO WTO.
*                                  ERROR LOAD 15 IS AN IOS MODULE.
*                                  EXIT TO IGE0004C MUST BE MADE AS
*                                  IF A NEW ENTRY CONDITION BUT
*                                  MUST BE SELF CONTAINED.
*
*                                  PREPARE FOR POSSIBLE OPERATOR MSG
         OI    EWAFLG1,EWADDMSG    TP SIZED MESSAGE
         CLI   EWAIND1,EWAILD15   EXIT TO LOAD 15 TP RECORDER  @ZA14136
         BE    EXIT15              YES                         @ZA14136
         MVC   EWAITRAN(L2),IOBSENS0   MOVE SENSE FOR MESSAGE  @ZA12160
         MVC   EWTCTID(L2),IOBRESTR+L2  MOVE TERM ID FOR MSG   @ZA08475
*        ONE LINE OF CODE DELETED BY APAR ------------------>  @ZA14034
         CLI   EWAIND1,EWAIWTOL    EXIT TO IOS WTO             YM05652K
         BE    WTOEXIT             YES                         YM05652K
         CLI   EWAIND1,EWAISTAT    EXIT TO STAT UPDATE         YM05652K
         BNE   IOSEXIT             NO, GO TO SVC 15 AND SVC 3  YM05652K
*                                  EWASTUP ALREADY SET BY OTHER LOADS
         LA    NAMEREG,STATUP      LOAD ROUTINE NAME
         B     XCTLOUT             XCTL                        @ZA07662
EXIT15   EQU   *
         MVI   EWAIND1,ZERO         RESET FLAG                 YM05652K
         OI    EWAFLG2,EWAMDR       MDR RECORDING REQUESTED    YM05673K
         NI    IOSFLB,L255-IOSLOG   NO OBR WANTED              YM05673K
         MVC   EWAIBUFF(L4),IOBERCCW   MOVE POINTER TO EWA     YM05673K
         LH    NAMEREG,ERLOD15     GET MODULE NAME
         B     XCTLOUT             GO TO IT                    @ZA07662
WTOEXIT  EQU   *
         MVC   EWTCTPL(L2),IOBRESTR  MOVE IN TP OP CODE AND    @ZA14034
*                               MOVE IN DECFLAGS FOR MESG      @ZA14034
         L     NAMEREG,IOSUCB      SET UP UCB ADDRESSIBILITY   @ZA14061
         MVC   EWACHA(L2),L4(NAMEREG) MOVE UNIT ADDR FOR MESG. @ZA14061
         MVI   EWAIND1,ZERO        RESET FLAG                  YM05652K
         LA    NAMEREG,WTORTN      GET NAME OF IOS WTO
XCTLOUT1 NI    IOSFLA,L255-IOSERR  TURN OFF IOSB ERP IN CNTRL  @ZA04874
         NI    IOBFLAG1,L255-IOBERRTN    ALSO IN IOB           @ZA04874
XCTLOUT  EQU   *
         USING CVTMAP,ERRETR
         L     ERRETR,CVTPTR       POINT TO CVT
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE
         BR    ERRETR              GO TO IT
         DROP  ERRETR
IOSEXIT  EQU   *
         CLI   EWAIND1,EWAISVCF    SVC 15 FLAG SET             YM05652K
         BNE   IOSEXIT1            NO, FORCE PERM ERROR        YM05652K
*                                  THIS IS AN ERP ERROR        YM05652K
IOSEXIT2 EQU   *                                               YM05652K
         MVI   EWAIND1,ZERO        CLEAR FLAG                  YM05652K
         SVC   15                  ERROR EXCP
         SVC   3                   RETURN TO IOS
IOSEXIT1 EQU   *                                               YM05652K
         NI    IOSFLA,L255-IOSERR  TURN OFF IOSB ERP IN CNTRL  @ZA04874
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB            @ZA04874
         B     IOSEXIT2            ISSUE SVC                   YM05652K
RETRY    EQU   *
         TM    EWAIND0,EWAIPMAP    PARALLEL MAPPING DONE
         BNO   CHANGE              NO,NOTHING TO UNFIX      L5 @ZA04156
         L     PARMREG,EWAITCCW    ADDR OF TCCW             L5 @ZA04156
         BAL   ERREG7,CHANGE1      DO UNFIX                 L5 @ZA04156
CHANGE   EQU   *
         BAL   ERREG1,ERPEXT  GET ADR OF CURR EXT ENTRY     L5 @ZA04156
         CLC   ZERO(L4,ERRETR),FULL0         ENTRY EXIST?   L5 @ZA04156
         BE    CHANGE3             NO,NOTHING TO UNFIX      L5 @ZA04156
         L     PARMREG,ZERO(ERRETR)          ADR OF TCCW    L5 @ZA04156
         BAL   ERREG7,CHANGE1      DO UNFIX                 L5 @ZA04156
CHANGE3  EQU   *                                            L5 @ZA04156
         OI    EWAIND0,EWAIGPRO    GETMAIN IN PROCESS
         L     ERREG3,GET480       LD PARMS                 L5 @ZA04156
         GETMAIN    R,LV=(0)                                L5 @ZA04156
         NI    EWAIND0,L255-EWAIGPRO   RESET FLAG
         ST    PARMREG,EWAITCCW    STORE NEW TCCW
         USING TCCW,PARMREG
         XC    ZERO(L160,PARMREG),ZERO(PARMREG)   ZERO TCCW
         LA    ERREG7,L160(PARMREG)   POINT TO BEB
         XC    ZERO(L160,ERREG7),ZERO(ERREG7)   CLEAR BEB
         ST    ERREG7,TCCWBEB      STORE BEB ADDR IN TCCW
         LA    ERREG7,L160(ERREG7)   POINT TO FIXLIST
         XC    ZERO(L160,ERREG7),ZERO(ERREG7)   CLEAR FIXLIST
         ST    ERREG7,TCCWFIX      STORE FIXLIST ADDR IN TCCW
         MVC   TCCWUCB(L3),IOSUCB+L1   UCB ADDR TO TCCW
         MVC   TCCWTCB(L4),RQETCB   TCB ADDR TO TCCW
         OI    EWAIND0,EWAIPMAP    MAPPING DONE
         L     ERREG7,IOBSTART     TRANSLATE FROM IOBSTART , THIS WILL
*                                  BE IOBERCCW IF BUILT
         LA    ERREG7,ZERO(ERREG7)   CLEAR OUT FLAGS
         ST    ERREG7,TCCWFVC      FIRST VIRT CCW ADDR TO TCCW
         LR    ERREG3,ERREG7       SET AS PARAMETER
         SR    ERREG6,ERREG6       CLEAR REGISTER              @ZA04853
*            2 LINES DELETED BY THE FOLLOWING APAR             @ZA03207
RETCCW   EQU   *
GETLOCK2 SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                        X
               RELATED=(ERP,IGE0904B(FREELOC2))
         CH    ERREG6,HALF12       MORE CORE NEED ?            @ZA04853
         BE    RETXLAT        YES BRANCH TO TRANSLATOR         @ZA04853
         MVI   TCCWOPTN,TCCWXLAT TRANSLATE AND FIX             @ZA03207
RETXLAT  L     ERREG5,EWAITRAN     POINT TO TRANSLATOR         @ZA04853
         BALR  ERRETR,ERREG5       GO TO HIM
         LR    ERREG6,ERREG5       SAVE RETURN CODE            @ZA04853
         BAL   ERREG1,ERPEXT     GET ADDR OF CURRENT ERP       @ZA03207
*                                EXT ENTRY                     @ZA03207
         MVC   ZERO(L4,ERRETR),EWAITCCW  PUT TCCW ADDR INTO    @ZA03207
*                           ERP EXT ENTRY FOR THIS IOB         @ZA03207
FREELOC2 SETLOCK RELEASE,TYPE=LOCAL,RELATED=(ERP,IGE0904B(GETLOCK2))
         CH    ERREG6,HALF12       RETURN FOR MORE CORE        @ZA03207
         BE    CHANGE0             YES, DO GETMAIN
         L     NAMEREG,TCCWFRC     GET FIRST REAL CCW ADDR     YM05697K
         LRA   NAMEREG,ZERO(NAMEREG)    REAL ADDR OF REAL CCW  YM05697K
         ST    NAMEREG,IOSRST      STORE 1ST REAL CCW ADDRESS  YM05697K
         B     CLEANUP             EXIT
         DROP  PARMREG
CHANGE0  EQU   *
         OI    EWAIND0,EWAIGPRO    GETMAIN IN PROCESS
         L     ERREG3,GET160       LD PARMS                 L5 @ZA04156
         GETMAIN  R,LV=(0)                                  L5 @ZA04156
         NI    EWAIND0,L255-EWAIGPRO   RESET FLAG
         XC    ZERO(L160,PARMREG),ZERO(PARMREG)   CLEAR CORE
         LR    ERREG3,PARMREG      PUT IN PARAMETER REG
         L     PARMREG,EWAITCCW    POINT TO TCCW
         B     RETCCW              GO TO TRANSLATOR AGAIN
CHANGE1  EQU   *
GETLOCK3 SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                        X
               RELATED=(ERP,IGE0904B(FREELOC3))
         SR    ERREG3,ERREG3       CLEAR OUT PARM REG
         USING TCCW,PARMREG
         MVI   TCCWOPTN,TCCWUNFX   CLEAR OPTIONS FIELD         @ZA03207
         DROP  PARMREG
         L     ERREG5,EWAITRAN     GET ADDRESS OF TRANSLATOR
         BALR  ERRETR,ERREG5       GO TO HIM
         BAL   ERREG1,ERPEXT     GET ADDR OF CURRENT ERP       @ZA03207
*                                EXTENSION ENTRY               @ZA03207
         XC    ZERO(L4,ERRETR),ZERO(ERRETR) CLEAR OUT THE      @ZA03207
*                           ERP EXT ENTRY (TCCW ADDR)          @ZA03207
FREELOC3 SETLOCK RELEASE,TYPE=LOCAL,RELATED=(ERP,IGE0904B(GETLOCK3))
CHANGE2  EQU   *
         LTR   PARMREG,PARMREG     END OF CHAIN
         BCR   8,ERREG7            YES,RETURN               L5 @ZA04156
         L     ERREG2,ZERO(PARMREG)   SAVE LINK POINTER
         BAL   ERRETR,FREERTN      FREEMAIN ONE BLOCK
         LR    PARMREG,ERREG2      RESET CHAIN BASE
         B     CHANGE2             TEST FOR END
FREERTN  EQU   *
         LR    ERREG6,ERRETR       SAVE RETURN ADDRESS
         OI    EWAIND0,EWAIFPRO    FREEMAIN IN PROGRESS
         L     ERREG3,GET160       LD PARMS                 L5 @ZA04156
         FREEMAIN  R,LV=(0),A=(1)                           L5 @ZA04156
         NI    EWAIND0,L255-EWAIFPRO   RESET FLAG
         BR    ERREG6              RETURN TO CALLER
*
*       THIS SUBROUTINE WILL LOCATE THE ADDRESS OF THE CURRENT
*       ERP EXTENSION ENTRY AND PASS IT BACK TO THE CALLER IN
*       REG ERRETR.
*
ERPEXT   L     NAMEREG,IOBDCBPT  GET DCB ADDR                  @ZA03207
         USING IHADCB,NAMEREG    DCB DSECT ADDRESSABILITY      @ZA03207
         L     ERRETR,DCBIOBAD   GET 1ST IOB ADDR MINUS LENGTH @ZA03207
         SR    ERREG2,ERREG2     CLEAR REG FOR IOB SIZE        @ZA03207
         IC    ERREG2,DCBEIOBX   GET IOB SIZE                  @ZA03207
         SR    ERREG5,ERREG5     IOB RLN INDICATOR             @ZA03207
NOTFOUND LA    ERRETR,ZERO(ERREG2,ERRETR)  IOB ADDR            @ZA03207
         LA    ERREG5,L1(ERREG5)  INCREMENT RLN INDICATOR      @ZA03207
         CR    ERRETR,IOBRG      IS THIS THE IOB FOR ERP       @ZA03207
         BNE   NOTFOUND          NO, ADDR NEXT IOB             @ZA03207
         SLL   ERREG5,L2         MULT INDEX BY 4 TO GET        @ZA03207
*                                DISPLACEMENT INTO ERP EXT     @ZA03207
         L     ERRETR,DCBIOBAD   1ST IOB ADDR MINUS LENGTH     @ZA03207
         L     NAMEREG,DCBDEBAD  ADDR OF DEB                   @ZA03207
         DROP  NAMEREG           DROP DCB ADDRESSABILITY       @ZA03207
         USING DEBBASIC,NAMEREG  DEB DSECT ADDRESSABILITY      @ZA03207
         IC    NAMEREG,DEBNMEXT  NUMBER OF IOB'S               @ZA03207
         DROP  NAMEREG           DROP DEB ADDRESSABILITY       @ZA03207
         N     NAMEREG,FULL255   CLEAR BYTES 0,1, AND 2        @ZA03207
NEXT     LA    ERRETR,ZERO(ERREG2,ERRETR) IOB ADDR             @ZA03207
         BCT   NAMEREG,NEXT      FALL THRU WHEN ERRETR POINTS  @ZA03207
*                                TO THE LAST IOB               @ZA03207
         LA    ERRETR,ZERO(ERREG2,ERRETR) ADDR ERP EXT         @ZA03207
         MVC   0(4,ERRETR),EWAITRAN       ADDR OF TRANSLATOR   @ZA03207
*                                  IN 0TH ENTRY OF ERP EXT     @ZA03207
         AR    ERRETR,ERREG5     ADDR CURRENT ENTRY IN THE     @ZA03207
*                                ERP EXTENSION                 @ZA03207
         BR    ERREG1            RETURN TO CALLER              @ZA03207
*
*
*
EXITEXIT EQU   *
         DROP  BASEREG
         BALR  BASEREG,ZERO
         USING *,BASEREG
         LR    ERREG7,ERRETR       SAVE RETURN ADDRESS
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(ERP,IGE0904B(GETLOCK))
*                                  PROCEED WITH ABEND
         SETRP
         LR    ERRETR,ERREG7       RESET RETURN REG FOR RTM
         BR    ERREG7              RETURN TO TASK RECOVERY
         EJECT
**********
*
*        CONSTANTS
*
**********
EXITADDR ESTAE EXITEXIT,MF=L
ERLOD15  DC    H'6256'             TP RECORDER
HALF12   DC    H'12'               CONSTANT FOR RETURN CODE CHECK
FULL255  DC    F'255'            FULL WORD OF 255              @ZA03207
GET160   DC    X'FD0000A0'                                  L5 @ZA17698
GET480   DC    X'FD0001E0'                                  L5 @ZA17698
FULL0    DC    F'0'                                         L5 @ZA04156
PACHAREA DC    40C'904B'           PATCH AREA FOR MAINTENANCE
         SPACE 2
**********
*
*        MISCELLANEOUS EQUATES
*
**********
STATUP   EQU   254                 STATISTICS UPDATE ROUTINE NAME
WTORTN   EQU   253                 NAME OF IOS WTO ROUTINE
ERLOD00  EQU   43                  NAME OF 004C ERP
SPMBFLAG EQU   X'9F'                                           @ZA08084
READTI   EQU   X'01'                                           @ZA08084
READTIR  EQU   X'81'                                           @ZA20113
NINETY   EQU   X'90'                                           @ZA08084
*
*
* SUBPOOL WAS 245 BUT NEED SOMETHING TCB RELATED, NOT RCT RELATED
*
*
L255     EQU   255                 AND MASK
ZERO     EQU   0                   LENGTH AND DISPLACEMENT
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L16      EQU   16                  LENGTH AND DISPLACEMENT
L19      EQU   19                                              @ZA08084
L22      EQU   22                                              @ZA08084
L160     EQU   160                 LENGTH AND DISPLACEMENT
         SPACE 2
**********
*
*        REGISTER DEFINITION
*
**********
ERREG3   EQU   0                   WORK REG
PARMREG  EQU   1                   PARAMETER REG
ERREG2   EQU   2                   WORK REG
EWAREG   EQU   3                   ERP WORKAREA BASE
ERREG7   EQU   4                   CCW ADDRESS REG
IOSBREG  EQU   5                   IOSB BASE
RQEREG   EQU   6                   RQE BASE
ERREG6   EQU   7                   WORK REG
BASEREG  EQU   8                   PROGRAM BASE
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG4   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  LINK REG/RETURN REG
ERREG5   EQU   15                  LINK REG/WORK REG
         EJECT                                                 @ZA03207
         DCBD DSORG=BX,DEVD=BS                                 @ZA03207
         EJECT                                                 @ZA03207
         IEZDEB LIST=YES                                       @ZA03207
         EJECT
         IHAPSA
         EJECT
         IHASDWA
         EJECT
         IECDTCCW
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA  EWTC                                        @ZA08475
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAIBUFF EQU   EWAIAREA+X'18'      MDR RECORD POINTER AT X'48' YM05673K
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
