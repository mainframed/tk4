         TITLE 'IGE0604B - BTAM ERP INTERVENTION MESSAGE WRITER'
IGE0604B CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0604B                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM INTERVENTION REQUIRED MESSAGE WRITER ERP                    *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    WRITES MESSAGES TO SYSTEM OPERATOR CONSOLE INDICATING THAT A     *
*    NOT OPERATIONAL START IO HAS OCCURRED.                           *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (28 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        296 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD   *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0604B                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        SETS INDICATOR IN DECERRST FOR NOT OPERATIONAL SIO           *
*        ISSUE MESSAGE TO SYSTEM OPERATOR                             *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        WTO (SVC 35)                                                 *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    WTO                                                              *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YM05652K (1/25/74)                                               *
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         L     UCBREG,IOSUCB       BASE UCB
         DROP  ERREG5
         LR    ERREG4,IOSBREG      SAVE IOSB POINTER
         CLI   IOBINCAM+L1,L255    SPECIAL RETURN
         BE    ERPWTO11            YES POST WITH ERROR
         CLI   L96(IOBRG),L255     IS SIO IN OPEN
         BNE   ERPWTO10            NO, ISSUE JUST WTO
         L     MSGREG,L96(IOBRG)   LOAD DSECT BASE FOR OPEN WTOR
         USING MSGWTOR,MSGREG
         MVC   REPLYAD(M1LENGTH),MSGAREA2   MOVE MSG TO GETMAINED AREA
         MVC   MESSAGE+L8(L3),UCBNAME(UCBREG)   MOVE IN UNIT NAME
         MVC   REPLYAD+M1LENGTH(M2LENGTH),MSGAREA5   MOVE END OF MSG
         MVI   MESSAGE+L6,CA       CORRECT MESSAGE NUMBER
         MVI   MESSAGE+L5,C4       CORRECT MESSAGE NUMBER
         LA    ERREG5,REPLY        INITIALIZE REPLY ADDR
         ST    ERREG5,REPLYAD
         MVI   REPLYAD,L4          REPLY LENGTH
         LA    ERREG5,ECB          INITIALIZE ECB ADDR
         ST    ERREG5,ECBAD
         MVI   MSGLN+L1,M3LENGTH   CORRECT MESSAGE LENGTH
         NI    IOSFLA,L255-IOSERR
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
         B     OPENOUT             RETURN TO OPEN
ERPWTO10 LA    IOSBREG,EWAIAREA    PARAMETER FOR WTO
         SR    ERREG3,ERREG3       CLEAR REG 0
         MVC   EWAIAREA(MSGLNG),MSGAREA   MOVE PARA LIST
         MVC   TEXTIN(L3),UCBNAME(UCBREG)   LINE ADDR TO MESSAGE
         WTO   MF=(E,(1))          WRITE CU NOT OPERATIONAL
         MVI   IOBERINF+L12,ZERO
         MVI   IOBINCAM+L1,L255    SPECIAL RETURN
         B     ERPWTO20            CONTINUE FLOW
ERPWTO11 EQU   *
         MVI   IOBINCAM+L1,ZERO    CLEAR SPECIAL RETURN
         OI    DECERRST(DECBRG),SIOCC3   INDICATE NOT OPERATIONAL SIO
         NI    IOSFLA,L255-IOSERR
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
ERPWTO20 NI    UCBBSYBT(UCBREG),L255-HIBIT
OPENOUT  EQU   *
         LR    IOSBREG,ERREG4      RESTORE IOSB POINTER
         MVI   EWAIND1,EWAISVCF    SVC 15/SVC 3                YM05652K
         L     NAMEREG,TRANSRTN    LOAD NAME
EREXCTL  EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     XCTL ROUTINE ADDRESS
         BR    ERRETR              EXIT VIA XCTL
         DROP  ERRETR
         EJECT
**********
*
*        CONSTANTS
*
**********
TRANSRTN DC    F'9042'            NAME OF BTAM TRANSLATE ROUTINE
MSGAREA2 DC    AL1(4)             REPLY LENGTH
         DC    AL3(1)             REPLY ADDRESS
MSGAREA3 DC    A(1)               ECB ADDRESS
MSGAREA  DC    AL2(MSGAREA4-*)    MESSAGE LENGTH
         DC    X'8000'            MCS FLAGS
         DC    C'IEC809I '        MESSAGE
MSGAREA1 DC    C'    '
         DC    C'CONTROL UNIT NOT OPERATIONAL.'
MSGAREA4 DC    X'1000'
         DC    X'0140'
MSGAREA5 DC    C' REPLY CONT OR POST  '
MSGAREA6 DC    X'2000'
         DC    X'8140'
MSGLNG   EQU   *-MSGAREA           LENGTH OF WTO PARAMETER LIST
M3LENGTH EQU   MSGAREA6-MSGAREA-4
M1LENGTH EQU   MSGAREA4-MSGAREA2
M2LENGTH EQU   MSGAREA6+4-MSGAREA5
ERRCN8   DC    H'8'                CONSTANT
PACHAREA DC    7C'604B'            PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
UCBNAME  EQU   13
L255     EQU   X'FF'               AND MASK
HIBIT    EQU   X'80'
UCBBSYBT EQU   6
DECERRST EQU   29
SIOCC3   EQU   X'80'
ZERO     EQU   X'00'               LENGTH AND DISPLACEMENT
CA       EQU   C'A'                CHARACTER A
C4       EQU   C'4'                CHARACTER 4
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L12      EQU   12                  LENGTH AND DISPLACEMENT
L96      EQU   96                  LENGTH AND DISPLACEMENT
**********
*
*        REGISTER DEFINITIONS
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
ERREG2   EQU   2                   WORK REG
EWAREG   EQU   3                   ERP WORKAREA BASE
MSGREG   EQU   4                   WTOR PARAMETER LIST BASE
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE
BASEREG  EQU   8                   PROGRAM BASE
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG4   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  LINK REG
ERREG5   EQU   15                  WORK REG/LINK REG
         EJECT
MSGWTOR  DSECT
*                                  WORKAREA FIELDS FOR OPEN WTOR
REPLYAD  DS    A
ECBAD    DS    A
MSGLN    DS    H
MCSFLAGS DS    BL2
MESSAGE  DS    CL68
DSRTCDS  DS    XL4
REPLY    DS    2F
ECB      DS    F
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
TEXTIN   EQU   EWAIAREA+L12        TEXT INSERT
         EJECT
         IECDIOSB
         END
