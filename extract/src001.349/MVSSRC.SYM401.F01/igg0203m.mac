 TITLE 'IGG0203M - BTAM LINE GROUP CLOSE EXECUTOR'
IGG0203M CSECT
*                                                              @YA02128
*        FOLLOWING CHANGES APPLIED:                            @YA02128
*                                                              @YA02128
*
*
* D061000-061500,C062000-062500                              LD YA02147
* A085600-085988,A259000,A276200                             LD YA02169
* A076020                                                     LD YM7707
* D096100-096200                                            LD @ZA00520
* C036500                                                   L5 @ZA02840
* A036600-036800                                            L5 @ZA02840
*
*        YA03971  (1/3/74)
*        ZA00545  (11/15/74)
*        ZA00546  (11/15/74)
*        ZA00547  (11/15/74)
*        ZA02840  (02/25/75)
*        ZA03207  (03/24/75)          ZA03557  (03/10/75)
*        ZA03561  (03/10/75)          ZA03973  (04/29/75)
*        ZA04076  (04/17/75)          ZA04082  (04/17/75)
*        AZ07660  (12/05/75)          AZ08049  (01/26/76)             *
*        AZ09347  (04/08/76)          AZ10045  (06/07/76)             *
*        AZ14146  (11/01/76)          AZ17707  (02/15/77)
*
***********************************************************************
*                                                                     *
* STATUS -          CHANGE LEVEL 003    8/16/74                       *
*                                                                     *
* FUNCTION -       THE CLOSE EXECUTOR LOGICALLY DETACHES A COMMUNI-   *
*                  CATION LINE GROUP FROM THE SYSTEM. THE ROUTINE     *
*                  PERFORMS THE FOLLOWING FUNCTIONS -                 *
*                                                                     *
*                    1. HALTS ANY OUTSTANDING ENABLE COMMANDS.        *
*                                                                     *
*                    2. STOPS ACTIVITY ON THE LINES.                  *
*                                                                     *
*                    3. DISABLES THE LINES.                           *
*                                                                     *
*                    4. FREES THE STORAGE OBTAINED BY OPEN FOR        *
*                                                                     *
*                       INPUT/OUTPUT BLOCKS.                          *
*                                                                     *
*                    5. REINITIALIZES DCB FIELDS.                     *
*                    6. FREES THE STORAGE OCCUPIED BY THE BUFFER      *
*                       POOL IF A BUFFER POOL WAS OBTAINED BY THE     *
*                       OPEN EXECUTOR.                                *
*                                                                     *
* ENTRY POINT -    ENTRY IS TO THE FIRST EXECUTABLE INSTRUCTION.      *
*                                                                     *
* INPUT -          REGISTER 5 - START OF PARAMETER LIST               *
*                                                                     *
*                  REGISTER 6 - START OF WHERE-TO-GO TABLE            *
*                                                                     *
*                  REGISTER 7 - CURRENT ENTRY IN PARAMETER LIST       *
*                                                                     *
*                  REGISTER 8 - CURRENT ENTRY IN WHERE-TO-GO TABLE    *
*                                                                     *
* OUTPUT -         NONE                                               *
*                                                                     *
* EXIT -           CONTROL IS GIVEN TO THE SYSTEM CLOSE ROUTINE.      *
*                                                                     *
* ATTRIBUTES -     THIS ROUTINE IS EXECUTED IN THE TRANSIENT AREA     *
*                  AS ENABLED AND REENTRANT.                          *
*                                                                     *
***********************************************************************
         EJECT
         USING IECTIOB,RCORE
         USING IHADCB,RDCB
         USING IECTDEB,RDEB
         BALR  RBASE,0
         USING *,RBASE
         B     BEGIN
         DC    C'IGG0203M'
         DC    C'** MVS *'
         DC    C'&SYSDATE'         ASSEMBLY DATE            LD @ZA00545
         DC    C'UZ06852'          PTF NUMBER
         DC    S(*)
PATCH    DC    20F'0'
         DS    0H
BEGIN    EQU   *
*                                      INITIALIZATION OF REGISTERS FOR
*                                      DEB,DCB AND IOB
         L     RDCB,0(RPARC,0)         ACCESS POINTER TO DCB
         L     RDEB,DCBDEBAD           ACCESS POINTER TO DEB
         L     RJ,4(RWTGC)    LOAD ADDRESS OF DCB WORK AREA.   @ZA09347
         USING WORKAREA,RJ                                     @ZA09347
         ST    RDEB,DXCCW          SAVE DEB ADDR FOR PURGE     @ZA09347
         L     RDEB,28(RDEB)           POINT TO APPENDAGE TABLE OF DEB
*                                 OR TRUE START OF DEB FOR DSECT.
         L     RCORE,DCBIOBAD           GET IOB ADDRESS MINUS SIZE.
         MVC   DEBSIOA+1(THREE),DEBEOEA+1     SET           L5 @ZA02840
         MVC   DEBPCIA+1(THREE),DEBEOEA+1       APPENDAGE   L5 @ZA02840
         MVC   DEBCEA+1(THREE),DEBEOEA+1          ADDRESS   L5 @ZA02840
         MVC   DEBXCEA+1(THREE),DEBEOEA+1          TO IOS   L5 @ZA02840
         NI    DEBSIOA,NOPGFIX          TURN OFF PAGE FIX      @ZA02840
*                        APPENDAGE FLAG REQUIRED FLAG          @ZA02840
         TM    DCBDEVTP,X'FF'           TEST FOR STRAM             000C
         BC    14,NOSTRAM               IF NOT, BRANCH             000C
         MVC   0(5,RWTGC),STRAMID       MOVE IN STRAM ID           000C
         B     STRAMOUT                                            000C
NOSTRAM  SR    RB,RB          CLEAR REGISTER
         IC    RB,DCBEIOBX    GET IOB  SIZE
         SR    RC,RC
         IC    RC,DEBNMEXT              GET NUMBER OF EXTENTS.
         L     RTIOT,DEBTCBAD-1
         TM    TCBFLG(RTIOT),ABCLOSE   WAS CLOSE INITIATED BY ABEND?
         BO    DISABLER            IF SO, GO DIRECTLY TO DISABLE
*    ROUTINE, AS ABEND WILL HAVE PURGED ALL OUTSTANDING I/O REQUESTS.
*    *    *    *    *    *    *    *    *    *    *    *    *    *    *
         SPACE 2
*    *    *    *    *    *    *    *    *    *    *    *    *    *    *
         SPACE 1
         AR    RCORE,RB                 INITIALIZE IOB REGISTER  A25673
*         TWO LINES OF CODE DELETED BY APAR ------------->     @ZA09347
         MODESET KEYADDR=DXUKEY,WORKREG=1   ASSUME USER KEY      Y02947
         OI    IOBFLAG2,RESETPL     TERMINATE POLLING            Y02947
         MODESET EXTKEY=DATAMGT    GET TO KEY 5
         L     RB,DXCCW       SAVE ORGINAL DEB ADDRESS         @ZA09347
         LA    RF,DXCCW         LOAD ADDRESS OF AREA TO BE USED  Y02947
*                                  AS PARAMETER LIST FOR PURGE.
         LA    RTIOT,DEBNMSUB           LOAD ADDRESS OF DEB ORIGIN.
         ST    RTIOT,DXCCW   STORE DED ADDRESS IN PARM LIST    @ZA09347
         MVI   0(RF),PURGEOPT  MOVE PARAMETER LIST OPTION BYTE @ZA09347
*                             TO PARAMETER LIST AREA.
*         ONE LINE OF CODE DELETED BY APAR -------------->     @ZA09347
         L     RTIOT,DEBTCBAD-1
         LA    RTIOT,0(RTIOT)           CLEAR HIGH ORDER BYTE OF TCB
*    ADDRESS SO THAT IT WILL NOT LOOK LIKE A COMPLETION CODE.
         ST    RTIOT,4(RF)              STORE TCB ADDRESS IN PARAMETER
*                                  LIST.
         LA    RTIOT,DEBSYSPG-1         STORE ADDRESS OF DEB SYSTEM
         ST    RTIOT,8(RF)         PURGE FIELD IN PARAMETER LIST.
         LA    RTIOT,4(RF)              LOAD ADDRESS OF TCB FIELD IN
*                                  PARAMETER LIST TO BE USED AS ECB.
         LA    RC,DXCCW+12        LOAD ADDRESS OF CCW AREA PLUS  Y02947
*    12 IN DCB WORK AREA FOR USE AS A REGISTER SAVE AREA.
         MODESET KEYADDR=DXUKEY,WORKREG=10  ASSUME USER KEY
         SVC   16                       ISSUE PURGE SVC.
         MODESET EXTKEY=DATAMGT    GET TO KEY 5                @ZA09347
         L     RJ,4(RWTGC)   LOAD ADDRESS OF DCB WORK AREA     @ZA09347
         LA    RJ,0(RJ)      CLEAR HIGH ORDER BYTE             @ZA09347
         ST    RB,DXCCW      RESTORE ORIGINAL DEB ADDRESS      @ZA09347
         MODESET KEYADDR=DXUKEY,WORKREG=10  ASSUME USER KEY    @ZA09347
         SR    RC,RC
         IC    RC,DEBNMEXT              RE-INITIALIZE NUMBER OF
*                                  EXTENTS REGISTER.
*    *    *    *    *    *    *    *    *    *    *    *    *    *    *
*    THE FOLLOWING SECTION OF CODE ISSUES DISABLE COMMANDS TO ALL
*    COMMUNICATION LINES IN THE LINE GROUP.
         SPACE 1
DISABLER EQU   *
         L     RUCB,DEBUCBAD       GET FIRST UCB ADDRESS
         CLI   UCBCLASS(RUCB),GRPHCDVC  IS DEVICE 3270
         BNE   ENDANR              NO, SKIP ANR CLEAN-UP AND   @ZA03207
*                        SEE IF ANY PAGES ARE TO BE FREED      @ZA03207
*
         L     RF,ATNIRBAD(RUCB)   GET ADDRESS OF IRB
         LA    RF,ZERO(RF)         CLEAR HI ORDER BYTE       LD YA03240
         L     RB,RDYQ(RUCB)       GET ADDR OF READYQ IRB    LD YA03240
         LA    RB,ZERO(RB)         CLEAR HI ORDER BYTE       LD YA03240
         SR    RC,RC               CLEAR REG                 LD YA03240
         SR    RD,RD               CLEAR REG                 LD YA03240
         IC    RD,DEBNMEXT         GET NUM. OF DEVICES       LD YA03240
         MODESET EXTKEY=ZERO       RESUME KEY ZERO           LD YA03240
CLIRBPT  IC    RDCB,UCBGCB(RUCB)   SAVE GCB                  LD YA03240
         XC    UCBATTN(ANRATNXT,RUCB),UCBATTN(RUCB)          LD YA03240
*                                  CLEAR READYQ IRB PTR      LD YA03240
         STC   RDCB,UCBGCB(RUCB)   RESTORE GCB               LD YA03240
         LA    RC,FOUR(RC)         BUMP TO NEXT UCB          LD YA03240
         L     RUCB,DEBUCBAD(RC)   POINT TO NEXT UCB         LD YA03240
         BCT   RD,CLIRBPT          LOOP TO CLEAR IRB PTR     LD YA03240
         LR    RUCB,RB             RDY Q IRB ADDRESS        L5 @ZA04076
         LTR   RF,RF               IS ATN IRB ADDR ZERO?       @ZA03557
         BZ    TSTRDYQ             YES, GO TEST READYQ         @ZA03557
         BAL   RDCB,IRBFREE        GO FREE THE IRB          L5 @ZA04076
TSTRDYQ  LTR   RF,RUCB             IS RDY Q IRB ADDR ZERO?  L5 @ZA04076
         BZ    NORDYQP             YES,COMTINUE             L5 @ZA04076
         BAL   RDCB,IRBFREE        GO FREE THE IRB          L5 @ZA04076
         B     NORDYQP             CONTINUE                 L5 @ZA04076
IRBFREE  EQU   *                                            L5 @ZA04076
         SR    RB,RB               CLEAR REG                 LD YA03240
         IC    RB,NINE(RF)         GET READYQ IRB SIZE       LD YA03240
         SLL   RB,THREE            CHANGE TO BYTES           LD YA03240
         LA    RE,ZERO(RB)         PUT IN PARM               LD YA03240
         O     RE,IRBPOOL          OR IN SUBPOOL NUMBER      LD YA03240
         LA    RD,TWO              NUMBER OF IQES           L5 @ZA04076
         L     RC,NXTAVAL(RF)      ADDR AVAIL. READYQ IQE    LD YA03240
IQELOOP  LA    RC,ZERO(RC)         CLEAR HI ORDER BYTE       LD YA03240
         LTR   RC,RC               IQE ADDR AVAILABLE        LD YA03240
         BZ    CVTFREE             NO,BRANCH                 LD YA03240
         L     RC,ZERO(RC)         GET NEXT IQE PRT          LD YA03240
         BCT   RD,IQELOOP                                    LD YA03240
         SH    RF,HEADER           ALLOW FOR 20 BYTE PREFIX L5 @ZA04082
         FREEMAIN R,LV=(0),A=(1)   FREE IRB                  LD YA03240
         BR    RDCB                RETURN TO NSI             LD YA03240
CVTFREE  EQU   *                                             LD YA03240
         SR    RC,RC               CLEAR REG                 LD YA03240
         L     RC,CVTPTR           GET CVT ADDRESS           LD YA03240
         USING CVT,RC              CVT ADDRESSABILITY        LD YA03240
         LA    RC,CVTEXIT          GET ADDR OF SVC 3         LD YA03240
         DROP  RC                                            LD YA03240
         ST    RC,RBEP(RF)         PLACE SVC INTO EP         LD YA03240
         XC    RBOPSW+ONE(THREE,RF),RBOPSW+ONE(RF)           LD YA03240
         OC    RBOPSW+ONE(THREE,RF),RBEP+ONE(RF)             LD YA03240
         OI    RBSTAB(RF),TWO      RB CAN BE FREED           LD YA03240
         BR    RDCB                RETURN TO NSI             LD YA03240
NORDYQP  EQU   *                                             LD YA03240
         L     RDCB,ZERO(RPARC)    GET DCB ADDRESS           LD YA03240
         SR    RC,RC              CLEAR THE INDEX REG          @YA02128
         IC    RC,DEBNMEXT        RESTORE NUMBER EXTENTS       @YA02128
         L     RUCB,DEBUCBAD      RESTORE FIRST UCB POINTER    @YA02128
         SR    RF,RF               INITIALIZE INDEX
*         TWO LINES OF CODE DELETED BY APAR ----------->       @ZA09347
         MODESET EXTKEY=DATAMGT    ASSUME DATAMGT KEY         LD YM5674
         B     ENDANR1             BRANCH AROUND SYSEVENT MAC LD YM5674
ENDANR   EQU   *                   END OF 3270 CODE
         MODESET EXTKEY=DATAMGT         ASSUME DATA MGT KEY   LD Y02947
         DROP  RCORE                                           @AZ17707
         L     TCBREG,DEBTCBAD-1       SET TCB ADDRESSABILITY  @AZ17707
         USING TCB,TCBREG              TCB ADDRESSABILITY      @AZ17707
         TM    TCBFLGS6,TCBRV          VIRTUAL=REAL JOB?       @AZ17707
         BO    ENDANR1                 YES, DON'T ISSUE OKSWAP @AZ17707
         DROP  TCBREG                                          @AZ17707
         SR    RE,RE               CLEAR REG                  LD YM7707
         SYSEVENT OKSWAP           MAKE MEMORY....            LD YM5674
*                                      SWAPPABLE AGAIN.       LD YM5674
ENDANR1  EQU   *                                              LD YM5674
         USING IECTIOB,RCORE        RESET IOB ADDRESSABILITY   @ZA17707
         SR    RB,RB               GET NUMBER OF DEVICES       @ZA09347
         IC    RB,DCBEIOBX       GET  IOBSIZE                  @ZA09347
         L     RDCB,0(RPARC)            RE-INITIALIZE DCB REGISTER.
         L     RCORE,DCBIOBAD           GET IOB ADDRESS MINUS SIZE.
         OI    DCBIFLGS,X'0C'           INDICATE NO ERP IN CLOSE
         L     RJ,FOUR(RWTGC)       WORKAREA ADDRESSABILITY   LD YM5674
         MODESET KEYADDR=DXUKEY,WORKREG=14   ASSUME USER KEY     Y02947
         LR    RTIOT,RC                 LOAD NUMBER OF EXTENTS TO COUNT
*                                  REGISTER.
         L     RDCB,DXUDCBAD       POINT TO USER DCB
         OI    DCBIFLGS,X'0C'      INDICATE NO ERP TO CLOSE
         LA    RDCB,IOBERCCW(RB)        LOAD ECB ADDRESS.     LD YM5655
EXCPLOOP AR    RCORE,RB                 INITIALIZE IOB REGISTER TO
*                                  POINT AT NEXT IOB.
         SR    RD,RD
         IC    RD,IOBUCBX
         SLL   RD,2
         TM    IOBINCAM,X'80'          SAD/ENABLE FAILURE        A30762
         BO    SADOFF                  YES, AVOID DISABLE        A30762
         L     RUCB,DEBUCBAD(RD)        LOAD UCB ADDRESS.
         CLI   UCBCLASS(RUCB),GRPHCDVC  IS DEVICE 3270
         BE    CLEARDVC                 YES, CLEAR DEVICE    LD YA03971
         CLI   UCBTYP+3(RUCB),IBM32701  TEST FOR IBM TYPE III ADAPTER
         BE    LOOPTEST            ON A 2701 CONTROL UNIT.
         TM    UCBFL1(RUCB),XE8    DEV NOT READY OR BUSY    LD @ZA00546
         BM    LOOPTEST            EITHER, BYPASS EXCP      LD @ZA00546
         ST    RDCB,IOBECBPT            STORE ECB ADDRESS IN IOB.
         XC    IOBERCCW(4),IOBERCCW     CLEAR ECB              @ZA03207
         MVC   IOBCPA(8),DISABLE
         MVC   IOBCPA+8(8),IONOP                                   000E
         LA    RF,IOBCPA                SET UP START OF CHANNEL
         ST    RF,IOBSTART         PROGRAM ADDRESS.
         L     RF,DEBTCBAD-ONE      GET TCB ADDRESS          LD YA03237
         TM    TCBFLG(RF),ABCLOSE    ABEND ISSUED CLOSE?     LD YA03237
         LR    RF,RCORE
         BZ    ISSUEXCP                 NO, ISSUE EXCP       LD YA02169
REPSTEST LA    RE,LOOPCNT               LOAD LOOP COUNT        @ZA03973
REPTTEST EQU   *                                             LD YA02169
         BCT   RE,TESTUCB                                    LD YA02169
         B     SADOFF         TURN OFF SAD/ENAB EHWN CNT=0   LD YA02169
TESTUCB  EQU   *                                             LD YA02169
         TM    UCBFL1(RUCB),XE8         IS LINE HUNG?        LD YA02169
         BC    FIVE,REPTTEST                                 LD YA02169
ISSUEXCP EQU   *                                             LD YA02169
         EXCP  (1)
         LR    RF,RDCB                  LOAD ECB ADDRESS FOR WAIT.
         MVI   IOBINCAM+1,X'00'    SET TIMER COUNT TO ZERO     @ZA14146
STIMER   STIMER WAIT,BINTVL=TIME        WAIT 1/2 SECOND     LD @ZA14146
         TM    0(2),X'7F'               IS ECB POSTED       LD @ZA14146
         BNZ   SADOFF                   BRANCH IF YES          @ZA14146
         CLI   IOBINCAM+1,X'0D'         IS COUNT MAX 7         @ZA14146
         BNL   GOPURGE                  YES, GO PURGE          @ZA14146
         IC    RF,IOBINCAM+1            STORE NEW STIMER COUNT @ZA14146
         LA    RF,1(RF)                 UPDATE COUNT BY ONE    @ZA14146
         STC   RF,IOBINCAM+1            STORE NEW TIMER COUNT  @ZA14146
         B     STIMER                   REISSUE TIMER 1/2 SEC. @ZA14146
GOPURGE  EQU   *                                               @ZA10045
         STC   RTIOT,IOBCPA+SIXTEEN YES, SAVE COUNT REG     LD @ZA00546
         BAL   RD,PURGE2           BAL TO PURGE SUBRTN      LD @ZA00546
         SR    RTIOT,RTIOT         CLEAR REG                LD @ZA00546
         IC    RTIOT,IOBCPA+SIXTEEN RESTORE COUNT REG       LD @ZA00546
*                       THREE LINES DELETED BY---------------->@ZA14146
*                                                                     *
*        2 LINES OF CODE REMOVED FOR OZ00520                          *
*                                                                     *
SADOFF   NI    IOBINCAM,X'7F'          TURN OFF SAD/ENABLE       A30762
LOOPTEST EQU   *
         BCT   RTIOT,EXCPLOOP
         B     FREEIOBS                 GO TO FREE IOB'S     LD YA03971
*    *    *    *    *    *    *    *    *    *    *    *    *    *    *
         SPACE 1
PURGE2   EQU   *                                            LD @ZA00546
         MODESET EXTKEY=DATAMGT    DATA MANAGEMENT KEY      LD @ZA00546
         L     RJ,FOUR(RWTGC)      WORKAREA ADDRESSABILITY  LD @ZA00546
         XC    DXCCW+L4(L8),DXCCW+L4    CLEAR PARM LIST        @ZA09347
*         ONE LINE OF CODE DELETED BY APAR ------------->      @ZA09347
         MVI   DXCCW,PURGOPTN      PURGE OPTION             LD @ZA00546
         PURGE DXCCW               ISSUE PURGE/HALT I/O     LD @ZA00546
         L     RJ,FOUR(RWTGC)      WORKAREA ADDRESSABILITY  LD @ZA00546
         MODESET KEYADDR=DXUKEY,WORKREG=15                  LD @ZA00546
         BR    RD                  RETURN TO CALLER         LD @ZA00546
         SPACE 1
*    *    *    *    *    *    *    *    *    *    *    *    *    *    *
*        THIS SECTION WILL CLEAR THE 3270'S SCREEN
         SPACE 1
CLEARDVC EQU   *                                             LD YA03971
         ST    RDCB,IOBECBPT            STORE ECB ADDR...    LD YA03971
*                                       IN IOB               LD YA03971
         XC    IOBERCCW(4),IOBERCCW     CLEAR ECB              @ZA03207
         MVC   IOBCPA+FOUR(FIVE),CLEARCCW  MOVE ER/WR CCW    LD YA03971
*                                       AND WCC TO IOB       LD YA03971
         LA    RF,IOBCPA+EIGHT          ADDR OF DATA AREA
         ST    RF,IOBCPA                STORE IT IN CCW      LD YA03971
         MVI   IOBCPA,X05               OP CODE FOR CCW      LD YA03971
         LA    RF,IOBCPA                ADDR OF CCW          LD YA03971
         ST    RF,IOBSTART              FOR I/O              LD YA03971
         LR    RF,RCORE                 ADDR OF IOB          LD YA03971
         B     REPSTEST                 ISSUE EXCP             @ZA03973
         SPACE 1
*
*    *    *    *    *    *    *    *    *    *    *    *    *    *    *
         SPACE 2
*    *    *    *    *    *    *    *    *    *    *    *    *    *    *
*    THE FOLLOWING SECTION OF CODE FREES THE CORE USED FOR IOB'S.
         SPACE 1
FREEIOBS EQU   *
         L     RDCB,0(RPARC)            RE-INITIALIZE DCB REGISTER.
*
*             15 LINES DELETED BY THE FOLLOWING APAR           @ZA03207
*
CONTINUE EQU   *                                              LD Y02947
         L     RUCB,DEBUCBAD          UCB ADDR                 @ZA03207
         CLI   UCBCLASS(RUCB),GRPHCDVC LOCAL 3270 DEVICE       @ZA03207
         BNE   FREETCCW               NO, CHECK ERP EXT        @ZA03207
RETURN   EQU   *                                               @ZA03207
         L     RF,DCBIOBAD              GET IOB ADDRESS MINUS SIZE.
         AR    RF,RB                    ADD SIZE TO GET TRUE IOB ADDR.
         L     RUCB,DEBUCBAD          UCB ADDR                 @ZA03207
         CLI   UCBCLASS(RUCB),GRPHCDVC LOCAL 3270 DEVICE       @ZA03207
         BE    NOEXT                  YES, NO ERP EXT          @ZA03207
         LA    RB,4(RB)               LENGTH OF ERP EXT ENTRY  @ZA03207
*                          FOR REMOTES ONLY                    @ZA03207
NOEXT    EQU   *                                               @ZA03207
         MR    RB,RB                    MULTIPLY TO GET TOTAL SIZE.
         CLI   UCBCLASS(RUCB),GRPHCDVC LOCAL 3270 DEVICE       @ZA03207
         BE    NOEXT1                 YES, NO ERP EXT          @ZA03207
         LA    RC,4(RC)                 ADD 4 MORE FOR THE 0TH @ZA03207
*                                       IN THE ERP EXT         @ZA03207
NOEXT1   EQU   *                                               @ZA03207
         LA    RE,0(RC)                 CLEAR HIGH ORDER BYTE.
         O     RE,IOBPOOL               PLACE SUBPOOL NUMBER IN HIGH
*                                  ORDER BYTE.
         FREEMAIN R,LV=(0),A=(1)       FREEMAIN FOR IOB
         L     RB,DEBIRBAD-1            GET ADRS OF PNTRS TO IRBS  000E
         LA    RB,0(RB)                 CLEAR HI-ORDER BYTE        000E
         LTR   RB,RB                    WAS ADDRESS ZERO           000E
         BZ    BUFPOOL                  IF SO, BRANCH              000E
         MODESET EXTKEY=DATAMGT         BACK TO DATA MGT KEY     Y02947
         XC    DEBIRBAD(3),DEBIRBAD     CLEAR ADDRESS FIELD        000E
         MODESET EXTKEY=ZERO                                  LD YM4059
         L     RC,0(RB)                 GET ADDRESS OF ONLT IRB    000E
         MODESET EXTKEY=DATAMGT                               LD YM4059
         LTR   RC,RC                    IS ADDRESS PRESENT         000E
         BZ    NOTONLT                  IF NOT, BRANCH             000E
         LA    RDCB,NOTONLT        RETURN ADDR FOR SUBRTN   LD @ZA00547
         SR    RE,RE               CLEAR REG                LD @ZA00547
         L     RJ,FOUR(RWTGC)      WORKAREA ADDRESSABILITY  LD @ZA00547
         MODESET KEYADDR=DXUKEY,WORKREG=15   USER'S KEY     LD @ZA00547
         IC    RE,DEBNMEXT         NO. DEVICES TIMES 2...   LD @ZA00547
         AR    RE,RE               ...IS TOTAL NO. IQES     LD @ZA00547
         MODESET EXTKEY=ZERO       KEY ZERO                 LD @ZA00547
         L     RJ,NXTAVAL(RC)      GET 1'ST FREE IQE        LD @ZA00547
         LR    RF,RC               PRIME REG FOR SUBRTN     LD @ZA00547
ONLTLOOP EQU   *                                            LD @ZA00547
         LA    RJ,ZERO(RJ)         CLEAR HI-ORDER BYTE      LD @ZA00547
         LTR   RJ,RJ               IQE ADDR AVAILABLE       LD @ZA00547
         BZ    CVTFREE             NO, LET EXIT FREE IRB.   LD @ZA00547
         L     RJ,ZERO(RJ)         GET NEXT IQE ADDRESS.    LD @ZA00547
         BCT   RE,ONLTLOOP         LOOP TO CHECK ALL IQES.  LD @ZA00547
         MODESET EXTKEY=DATAMGT    DATA MANAGEMENT KEY      LD @ZA00547
         LR    RC,RF               RESTORE IRB ADDRESS      LD @ZA00547
         BAL   RDCB,FREEIRB             GO FREE CORE               000E
NOTONLT  EQU   *                                              LD YM4059
         MODESET EXTKEY=ZERO                                  LD YM4059
         L     RC,4(RB)                 GET ADDRESS OF PTOP IRB    000E
         MODESET EXTKEY=DATAMGT                               LD YM4059
         LTR   RC,RC                    IS ADDRESS PRESENT         000E
         BZ    NOTPTOP                  IF NOT BRANCH              000E
         L     R2,0(RC)                 GET ADR OF SAVE     LD @ZA00545
         LA    R2,0(R2)                 CLEAR HIGH BYTE     LD @ZA00545
         LTR   R2,R2                    SAVE AREA EXIST?    LD @ZA00545
         BZ    NOPTOPSV                 NO, PROCESS IRB         YA01234
         LA    RJ,72                    SAVE AREA SIZE          YA01234
         O     RJ,IOBPOOL               USER SUBPOOL            YA01234
         LR    RF,R2                    ADDR FOR FREEMAIN   LD @ZA00545
         LR    RE,RJ                    SUBPOOL & SIZE          YA01234
         FREEMAIN R,LV=(0),A=(1)        FREE SAVE AREA          YA01234
NOPTOPSV EQU   *                        PROCESS THE IRB         YA01234
         BAL   RDCB,FREEIRB             GO FREE CORE               000E
NOTPTOP  EQU  *                                                  Y02947
         L     RJ,4(RWTGC)               ADDR OF OPEN WORK AREA  Y02947
         MODESET KEYADDR=DXUKEY,WORKREG=13   ASSUME USER KEY     Y02947
         LR    RF,RB                    ADDRESS OF PNTRS IN REG 1Y02947
         LA    RE,16                    LNGTH 16 BYTES             000E
         O     RE,IOBPOOL               OR IN SUBPOOL NUMBER       000E
         FREEMAIN  R,LV=(0),A=(1)       FREE IRB PNTRS
         B     BUFPOOL                                             000E
FREEIRB  SR    RJ,RJ                    CLEAR REGISTER             000E
         IC    RJ,DEBNMEXT              GET NUMBER OF EXTENTS      000E
         SLL   RJ,2                     MULTIPLY BY 8              000E
         LA    RJ,1(RJ)                 ADD ONE                    000E
         SLL   RJ,3                     MULTIPLY BY EIGHT          000E
         LA    RJ,96(RJ)                ADD IRB SIZE               000E
         O     RJ,IRBPOOL               OR IN SUBPOOL NUMBER       000E
         LR    RE,RJ                    PUT COUNT IN REG 0         000E
         LR    RF,RC                    PUT ADDRESS IN REG 1       000E
         FREEMAIN R,LV=(0),A=(1)        FREE IRB                   000E
         BR    RDCB                                                000E
*    *    *    *    *    *    *    *    *    *    *    *    *    *    *
         SPACE 2
*    *    *    *    *    *    *    *    *    *    *    *    *    *    *
*    THE FOLLOWING SECTION OF CODE FREES THE CORE USED FOR A BUFFER
*    POOL, IF THE BUFFER POOL WAS CREATED BY OPEN.
         SPACE 1
BUFPOOL  L     RJ,4(RWTGC)                                       Y02947
         L     RDCB,DXUDCBAD            POINT TO USER'S DCB      Y02947
         TM    DCBMACRF,X'04'           TEST WHETHER BUFFER POOL SHOULD
         BZ    DCBINIT             BE FREED.  IF NOT, BRANCH.
         MODESET EXTKEY=DATAMGT
         L     RDCB,ZERO(RPARC)    COPY DCB ADDRESS
         NI    DCBMACRF,X'FB'           CLEAR BIT THAT INDICATES BUFFER
*                                  POOL SHOULD BE FREED BY CLOSE.
         L     RF,DCBBUFCB              LOAD BUFFER CONTROL BLOCK
*                                  ADDRESS.
         LA    RF,0(RF)                ZERO OUT HI BYTE          A32442
         LH    RB,DCBBUFL
         SR    RC,RC
         IC    RC,DCBBUFNO
         MR    RB,RB
         LA    RE,8(RC)                 LOAD BUFFER POOL SIZE TO
*                                  PARAMETER LIST REGISTER.
         O     RE,IOBPOOL               PLACE SUBPOOL NUMBER IN HIGH
*    ORDER BYTE OF PARAMETER LIST REGISTER.
*
         MODESET KEYADDR=DXUKEY,WORKREG=13   ASSUME USER KEY  LD Y02947
*
         FREEMAIN R,LV=(0),A=(1)
*
         MODESET EXTKEY=DATAMGT                               LD Y02947
*
         MVC   DCBBUFCB+1(3),CLEARMSK+1 RE-INITIALIZE BUFFER CONTROL
*                                  BLOCK ADDRESS FIELD IN DCB.
*    *    *    *    *    *    *    *    *    *    *    *    *    *    *
         SPACE 2
*    *    *    *    *    *    *    *    *    *    *    *    *    *    *
*    THE FOLLOWING SECTION OF CODE RE-INITIALIZES DCB FIELDS.
*     IF LOCAL 3270 IOB SIZE CHANGES FROM X'58', THE           @ZA08049
*      FOLLOWING (CLI) MUST BE CHANGED.                        @ZA08049
         SPACE 1
DCBINIT  EQU   *
         CLI   DCBEIOBX,L3270      IF NOT L3270, THERE WAS NO  @ZA08049
         BNE   NOCLRLRB          READY Q, SO DON'T CLEAR LERB  @ZA08049
         TM    DCBLERB,IGG019UP  READYQ SPEC BTAM'S ROUTINE    @ZA08049
         BNO   NOCLRLRB        NO, DON'T CLEAR ADDRESS         @ZA08049
         NI    DCBLERB,ALLON-IGG019UP CLEAR READYQ FLAG        @ZA08049
         XC    DCBLERB+L1(L3),DCBLERB+L1 CLEAR ADDRESS         @ZA08049
*                                       READYQ  IS  RESET      @ZA08049
*              TWO LINES OF CODE DELETED BY APAR -------->     @ZA07660
NOCLRLRB MVC   DCBIOBAD(L4),CLEARMSK    REINIT IOB ADDRESS     @ZA08049
*                                         FIELD IN DCB         @ZA08049
         XC    DCBEIOBX(L1),DCBEIOBX  REINIT DCB'S IOB SIZE    @ZA08049
         XC    DCBREAD+L1(L3),DCBREAD+L1   REINIT R/W ADDRESS  @ZA08049
         XC    DCBIFLGS(L1),DCBIFLGS  REINIT IOS ERROR FLAGS   @ZA08049
         IC    RD,DCBMACRF                                       Y02947
         L     RJ,FOUR(RWTGC)      CLOSE WORK AREA
         L     RDCB,DXUDCBAD       USER DCB ADDRESS
         MODESET KEYADDR=DXUKEY,WORKREG=13   USER KEY
         STC   RD,DCBMACRF          (NOT MAPPED BY SYS CLOSE)    Y02947
         L     RDCB,0(RPARC)           POINT TO COPY DCB
         MODESET EXTKEY=DATAMGT                               LD Y02947
         XC    0(2,RWTGC),0(RWTGC)     CLEAR ID TO INDICATE COMPLETION
*                                  OF EXECUTOR.
STRAMOUT EQU   *
         USING WTG,RWTG
         LR    RF,RCORE                 SAVE IOB ADDRESS      LD Y02947
         L     RCORE,4(RWTGC)           WORK ADREA ADDRESS    LD Y02947
         L     RJ,WTGPREFX              WTG PREFIX ADDRESS    LD Y02947
         STM   RE,RD,IECREGSV-IECPREFX(RJ)  SAVE REGS         LD Y02947
         IECRES INIT,DCBCOPY=FRWKAR                           LD Y02947
         LR    RJ,RWTG                  REG SAVEAREA ADDR        YM5668
         LM    RE,RD,IECREGSV-IECPREFX(RJ)  RESTORE REGS      LD Y02947
         L     RJ,4(RWTGC)              ADDR OPEN WORK AREA   LD Y02947
         LR    RCORE,RF                 RESTORE IOBADDRESS    LD Y02947
RELOOP   LA    RWTGC,WGOFF(0,RWTGC)     INCREMENT CURRENT WTG REGISTER.
         LA    RPARC,PLOFF(0,RPARC)     INCREMENT CURRENT PARAMETER
*                                  LIST REGISTER TO NEXT ENTRY.
         CLC   0(2,RWTGC),AMIDCNST     IF THE NEXT WTG ENTRY CALLS FOR
         BCR   8,RBASE             BTAM CLOSE, REEXECUTE THIS EXECUTOR.
         CLC   0(2,RWTGC),CLOSEIDL
         BL    RELOOP
         CLC   0(2,RWTGC),CLOSEIDH
         BH    RELOOP
         LR    RPARC,RPAR               REINITIALIZE CURRENT WTG REG
         LA    RWTGC,WAOFF(0,RWTG) AND CURRENT PARAMETER LIST REG.
ZCHECK   CLI   0(RWTGC),X'00'     TEST FOR ZERO ENTRY
         BNE   XCTLRTNE            IF NOT ZERO, GO TO TRANSFER CONTROL.
         LA    RWTGC,WGOFF(0,RWTGC) IF ZERO, GET NEXT ENTRY AND RETURN
         LA    RPARC,PLOFF(0,RPARC) TO ZCHECK.
         B     ZCHECK
*    *    *    *    *    *    *    *    *    *    *    *    *    *    *
XCTLRTNE EQU   *
         LA    RJ,DXCCW12               POINT TO DOUBLE WORD LIST.
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID TO NAME FIELD.
         MVC   14(3,RWTG),2(RWTGC)      MOVE TTR TO WTG TABLE.
         XCTL  DE=(RWTG),SF=(E,(15))
         EJECT
*                                                                     *
*                                                                     *
*        THIS ROUTINE WILL UNFIX ALL PAGES THAT WERE FIXED (BUT       *
*        NOT FREED) BY THE ERROR RECOVERY PROCEDURES. (REMOTE ONLY)   *
*                                                                     *
*                                                                     *
*        SUBROUTINE REGISTER DEFINITION                               *
*                                                                     *
*                                                                     *
         SPACE 3
GETMNIN  EQU   0                   INPUT FOR GETMAIN          LD Y02947
ECBREG   EQU   0                   ECB REGISTER FOR PGFREE    LD Y02947
*        1 LINE DELETED BY THE FOLLOWING APAR                  @ZA03207
GETMNOUT EQU   1                   OUTPUT FROM GETMAIN        LD Y02947
FREELIST EQU   1                   START ADDRES FOR PGFREE     @ZA03207
ENDADDR  EQU   2                   END ADDRESS FOR PGFREE     LD Y02947
TCBREG   EQU   4                   TCB REGISTER               LD Y02947
IOBADR   EQU   5                   ADDRESS OF IOB'S           LD Y02947
IOBSIZE  EQU   6                   SIZE OF IOB'S              LD Y02947
NUMIOBS  EQU   7                   NUMBER OF IOB'S             @ZA03207
WORKREG  EQU   8                   WORK REGISTER              LD Y02947
TCCW     EQU   9                   TCCW ADDR (ERP'S)           @ZA03207
SAVEREG  EQU   10                  SAVEAREA REGISTER          LD Y02947
CVTREG   EQU   11                  CVT ADDRESS                LD Y02947
RTNREG   EQU   14                  RETURN REGISTER            LD Y02947
LINKREG  EQU   15                  LINK REGISTER              LD Y02947
         SPACE 5
FREETCCW EQU   *                                               @ZA03207
         MODESET EXTKEY=DATAMGT    DATA MANAGEMENT KEY 0F 5    @ZA03207
         L     GETMNIN,SAVEAREA    GETMAIN BYTES AND SUBPOOL  LD Y02947
         GETMAIN R,LV=(0)          GETMAIN FOR SAVEAREA       LD Y02947
         STM   GETMNIN,LINKREG,ZERO(GETMNOUT) SAVE REGS       LD Y02947
         LR    SAVEREG,GETMNOUT    SAVE ADDRES OF GETMAINT    LD Y02947
         L     IOBADR,DCBIOBAD     IOBADDR MINUS SIZE         LD Y02947
         SR    IOBSIZE,IOBSIZE     CLEAR REGISTER 6           LD Y02947
         IC    IOBSIZE,DCBEIOBX    SIZE OF IOB'S              LD Y02947
         LA    IOBADR,ZERO(IOBSIZE,IOBADR) ADDR OF 1ST IOB     @ZA03207
         SR    NUMIOBS,NUMIOBS        CLEAR REG                @ZA03207
         IC    NUMIOBS,DEBNMEXT       NUMBER OF IOB'S          @ZA03207
         L     LINKREG,4(WORKREG)     WORKAREA ADDR            @ZA03207
         MODESET KEYADDR=DXUKEY,WORKREG=14   GET USER KEY      @ZA03207
         LR    WORKREG,IOBADR         ADDR OF 1ST IOB          @ZA03207
NEXT     LA    WORKREG,ZERO(IOBSIZE,WORKREG) ADDR NEXT IOB     @ZA03207
         BCT   NUMIOBS,NEXT           WHEN WE FALL THRU,       @ZA03207
*                     WORKREG POINTS TO ERP EXT                @ZA03207
         L     TCBREG,0(WORKREG)        ADDR OF TRANSLATOR...  @ZA03207
*                  PUT THERE BY IGE0904B...                    @ZA03207
         LA    TCBREG,0(TCBREG)         CLEAR HIGH ORDER BYTE  @ZA03207
         LA    WORKREG,4(WORKREG)       POINT TO 1ST ENTRY IN  @ZA03207
*                        ERP EXT CORRESPONDING TO RLN 1        @ZA03207
         MODESET EXTKEY=DATAMGT       DATA MANAGEMENT KEY      @ZA03207
         IC    NUMIOBS,DEBNMEXT       NUMBER OF IOB'S          @ZA03207
         MODESET KEYADDR=DXUKEY,WORKREG=14   GET USER KEY      @ZA03207
AGAIN    L     RTNREG,0(WORKREG)      GET ENTRY CONTENTS       @ZA03207
         LA    RTNREG,0(RTNREG)       CLEAR HI ORDER BYTE      @ZA03207
         LTR   RTNREG,RTNREG          ENTRY = 0 MEANS NOTHING  @ZA03207
*                                     NEEDS TO BE UNFIXED      @ZA03207
         BNZ   UNFIX                  UNFIX NECESSARY PAGES    @ZA03207
CHECK    LA    WORKREG,4(WORKREG)     ADDR NEXT ERP EXT ENTRY  @ZA03207
         BCT   NUMIOBS,AGAIN          FINISHED WHEN FALL THRU  @ZA03207
         B     DONE                   ALL PAGES FREED          @ZA03207
UNFIX    EQU   *                                               @ZA03207
*                                                             LD Y02947
*                                                             LD Y02947
*        FOR BRANCH ENTRY TO THE TRANSLATOR (IECVTCCW)         @ZA03207
*        WE MUST BE IN KEY 0, WITH A LOCAL LOCK HELD.         LD Y02947
*          REG0 = ZERO                                         @ZA03207
*          REG1 = ADDR OF TCCW                                 @XA03207
*          REG14 = RETURN ADDR                                 @ZA03207
*          REG15 = TRANSLATOR (IECVTCCW) ENTRY ADDR            @ZA03207
*                                                             LD Y02947
*                                                             LD Y02947
         SPACE 3
         MODESET EXTKEY=ZERO       GET KEY 0                  LD Y02947
         SPACE 3
GETLOCK  SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,RELATED=(PGFREE,        *
               IGG0203,(RELLOCK))                             LD Y02947
         SPACE 3
         SR    ECBREG,ECBREG          ECB ADDR FOR IECVTCCW    @ZA03207
         L     TCCW,ZERO(WORKREG)     TCCW ADDR FROM ERP EXT   @ZA03207
         LR    LINKREG,TCBREG         ADDR OF IECVTCCW         @ZA03207
         MVI   4(TCCW),X'08'        UNFIX OPTION OF TRANSLATOR @ZA03207
         LR    FREELIST,TCCW            REG1 = TCCW ADDR       @ZA03207
         BALR  RTNREG,LINKREG      LINK TO PAGE SERVICES      LD Y02947
         SPACE 3
RELLOCK  SETLOCK RELEASE,TYPE=LOCAL,RELATED=(PGFREE,IGG0203M,(GETLOCK))
         SPACE 3
NEXTBLK  LTR   FREELIST,FREELIST      END OF CHAIN             @ZA03207
         BZ    RESET                  YES, CLEAR ERP EXT ENTRY @ZA03207
         L     GETMNIN,TCCWPOOL    SUBPOOL AND LENGTH          @ZA03207
         L     TCCW,0(FREELIST)       SAVE LINK POINTER        @ZA03207
         FREEMAIN R,LV=(0),A=(1)     FREE TCCW                 @ZA03207
         LR    FREELIST,TCCW          RESET CHAIN BASE         @ZA03207
         B     NEXTBLK                                         @ZA03207
RESET    EQU   *                                               @ZA03207
         L     LINKREG,X20(SAVEREG)   RESTORE REGISTER 8      LD Y02947
         L     LINKREG,FOUR(LINKREG)  RESTORE WORKAREA ADDR   LD Y02947
*                                                             LD Y02947
*                                                             LD Y02947
*        RETURN TO USER KEY                                   LD Y02947
*                                                             LD Y02947
*                                                             LD Y02947
         SPACE 2
         MODESET KEYADDR=DXUKEY,WORKREG=14                    LD Y02947
         SPACE 2
*                                                             LD Y02947
         XC    ZERO(4,WORKREG),ZERO(WORKREG) CLEAR TCCW ADDR   @ZA03207
*                              AFTER FREEING                   @ZA03207
         B     CHECK                  SEE IF ANY MORE          @ZA03207
DONE     EQU   *                                               @ZA03207
         SPACE 2
         MODESET EXTKEY=DATAMGT       DATA MANAGEMENT KEY      @ZA03207
         L     GETMNIN,SAVEAREA    SET PARAMETERS FOR FREE    LD Y02947
         LR    GETMNOUT,SAVEREG    THE SAVEAREA               LD Y02947
         LM    ENDADDR,LINKREG,EIGHT(SAVEREG) RESTORE REGS    LD Y02947
         FREEMAIN R,LV=(0),A=(1)   FREE SAVEAREA              LD Y02947
         L     RJ,4(RWTGC)         WORKAREA ADDRESS            @ZA03207
         MODESET KEYADDR=DXUKEY,WORKREG=15     USER KEY        @ZA03207
         B     RETURN                 RETURN TO MAINSTREAM     @ZA03207
         EJECT
**********
*
*        CONSTANTS
*
**********
         DS    0F                                             LD Y02947
SAVEAREA DC    X'E6000040'         SAVEAREA SUBPOOL AND LNGTH  @ZA03207
IOBPOOL  DC    X'FA000000'              SUBPOOL NUMBER OF 250 FOR IOB'S
IRBPOOL  DC    X'FD000000'                                         000E
TCCWPOOL DC    X'FF0000A0'         SUBPOOL AND LENGTH OF TCCW  @ZA03207
CLEARMSK DC    X'00000001'
DISABLE  DC    X'2F00000060240001'      DISABLE COMMAND.           000E
IONOP    DC    X'0300000020240001'      I/O NOP COMMAND            000E
FULL0    DC    F'0'                   FULL WORD OF ZEROS       @ZA03207
LIST     DC    X'80000000'            INDICATES A LIST ,       @ZA03207
*                        RATHER THAN REGISTERS                 @ZA03207
STRAMID  DC    C'3W'                                               000C
         DC    X'00000000'                                         000C
CLOSEIDL DC    C'0B'           LOW RANGE OF ID'S OF LAST LOAD OF CLOSE
CLOSEIDH DC    C'0G'           HIGH RANGE OF ID'S OF LAST LOAD OF CLOSE
AMIDCNST DC    C'3M'              ID OF THIS EXECUTOR
CLEARCCW DC    X'20240001'              CCW FOR ERASE/WRITE  LD YA03971
         DC    X'C3'                    WRITE CONTRL CHAR.   LD YA03971
HEADER   DC    H'32'               HEADER SIZE FOR IRBS     L5 @ZA04082
TIME     DC    F'50'
         EJECT
**********
*
*        REGISTER DEFINITIONS
*
**********
RE       EQU   0                       WORK/PARAMREG
RF       EQU   1                       WORK/PARAMREG
RTCB     EQU   1                       TCB ADDRESS            LD Y02947
RDCB     EQU   2                       DCB ADDRESS
R2       EQU   RDCB                    WORK REGISTER        LD @ZA00545
RBASE    EQU   3                       BASE REGISTER
RCORE    EQU   4                       IOB ADDRESS
RPAR     EQU   5                       START OF PARAMETER LIST
RWTG     EQU   6                       START OF WTG
RPARC    EQU   7                       CURRENT ENTRY IN PARAMETER LIST
RWTGC    EQU   8                       CURRENT ENTRY IN WTG TABLE
RTIOT    EQU   9                       TIOT ADDRESS
RUCB     EQU   10                      CURRENT UCB
RDEB     EQU   11                      DEB ADDRESS
RB       EQU   12                      WORK REG
RC       EQU   13                      WORK REG
RD       EQU   14                      WORK/PARAMREG
RJ       EQU   15                      WORK/PARAMREG
**********
*
*        MISCELLANEOUS EQUATES
*
**********
TCCWFIX  EQU   12                     OFFSET IN TCCW OF FIX    @ZA03207
*                                           LIST               @ZA03207
PGFREE   EQU   X'20'                  PAGE FREE FLAG           @ZA03207
SBPOOL   EQU   255                    TCCW SUBPOOL             @ZA03207
L160     EQU   160                    TCCW LENGTH              @ZA03207
LEN4     EQU   4
LOOPCNT  EQU   4095                LOOP COUNT                LD YA02169
IRBSTAB  EQU   10                  DISPLACE FOR IRBSTAB FLAGS   YA01235
WAOFF    EQU   32                       OFFSET OF FIRST WTG ENTRY FROM
*                                  START OF WTG.
PLOFF    EQU   4                        OFFSET OF DCB ENTRIES.
WGOFF    EQU   8                        OFFSET OF WTG ENTRIES.
PURGEOPT EQU   X'E0'               OPTION BYTE FOR PURGE
PURGOPTN EQU   X'A0'               OPTION BYTE FOR PURGE    LD @ZA00546
ZERO     EQU   0                                              LD Y02947
ONE      EQU   1                                              LD Y02947
FIVE     EQU   5                                              LD Y02947
EIGHT    EQU   8                                              LD Y02947
IDALNGTH EQU   320                 LENGTH OF TWO IDAL'S       LD Y02947
TCBFLG   EQU   29
ABCLOSE  EQU   X'04'
UCBFL1   EQU   6
UCBTYP   EQU   16
ARMSEEK  EQU   X'02'
SWITCHED EQU   X'90'
IBM32701 EQU   X'82'
RESETPL  EQU   X'01'
BUSY     EQU   X'40'
CCPOLL   EQU   X'09'                                               000E
CCNOOP   EQU   X'03'                                               000E
CCTIC    EQU   X'08'                                               000E
COMCHAIN EQU   X'40'                                               000E
APOLL    EQU   X'40'                                               000E
TRMTST   EQU   X'10'
CCPREPAR EQU   X'06'
CCENABLE EQU   X'27'
CCWCCSLI EQU   X'60'
GRPHCDVC EQU   X'10'               GRAPHIC DEVICE CLASS
UCBCLASS EQU   18                  DISP TO DEVICE CLASS IN UCB
ANRATNXT EQU   14                  LENGTH OF ATTN HANDLING FIELDS
*                                       IN UCB EXTENSION
OLTEP    EQU   X'80'               UCB OLTEP FLAG
X20      EQU   X'20'               REG 8 DISP. IN SAVEAREA    LD Y02947
XE8      EQU   X'E8'               MASK FOR LINE HUNGED UP   LD YA02169
X05      EQU   X'05'                    OPCODE FOR ER/WR CCW LD YA03971
ALLON    EQU   X'FF'               MASK OF ALL ONE'S
ATNIRBSZ EQU   116                 SIZE OF ANR ATTN IRB & IQE
ATNIRBAD EQU   28                  DISP TO IRB ADDR IN UCB
UCBGCB   EQU   27                  DISP TO GCB IN UCB
UCBGRAF  EQU   28                 DISP TO GRAPHIC STATUS       @YA02128
RDYQ     EQU   32                 OFFSET TO READY IRB POINTER  @YA02128
IGG019UP EQU   X'01'              FLAG FOR DEFAULT ROUTINE     @YA02128
UCBATTN  EQU   26                  DISP TO ATTN FIELDS IN UCB
L1       EQU   1                                               @ZA08049
TWO      EQU   2                                             LD YA03240
L3       EQU   3                                               @ZA08049
THREE    EQU   3                                             LD YA03240
L4       EQU   4                                               @ZA08049
FOUR     EQU   4
L8       EQU   8                                               @ZA09347
NINE     EQU   9                                             LD YA03240
TWELVE   EQU   12                                           LD @ZA00546
SIXTEEN  EQU   16                  DISPLACEMENT             LD @ZA00546
NXTAVAL  EQU   X'60'               NEXT AVAILABLE IQE        LD YA03240
RBSTAB   EQU   11                  STATUS AND ATTRIBUTE BIT  LD YA03240
RBEP     EQU   12                  ENTRY POINT               LD YA03240
RBOPSW   EQU   20                  OLD PSW                   LD YA03240
NOPGFIX  EQU   X'7F'       NO PAGE FIX REQUIRED IN DEB AVT     @ZA02840
L3270    EQU   X'58'      SIZE OF LOCAL 3270 IOB               @ZA08049
TIMELNG  EQU   4
         EJECT
CVT      DSECT                                                LD Y02947
         CVT
         EJECT
         DCBD  DSORG=BX
         EJECT
         IECDSECS WTG,PREFX,EXPAND=YES                        LD Y02947
         EJECT
         IECTDEBX
         EJECT
         IECTIOBX
         EJECT
         IHAPSA
         EJECT
WORKAREA DSECT                                                   Y02947
         IECDSECT IOB=NO                                         Y02947
         EJECT
         IKJTCB DSECT=YES
         END
