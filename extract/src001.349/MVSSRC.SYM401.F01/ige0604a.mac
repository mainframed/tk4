         TITLE 'IGE0604A - BTAM ERR BUS OUT MODULE'
IGE0604A CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0604A                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM BUS OUT ERROR ERP                                           *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS MODULE IS ENTERED ON THE OCCURRENCE OF A BUS OUT ERROR.     *
*    CHECKS FOR AN OVERRUN ERROR AND XCTLS TO THE OVERRUN ROUTINE     *
*    IF NECESSARY.  THE CHANNEL PROGRAM IS RETRIED IF REQUIRED.       *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (24 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        236 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD   *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0604A                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        SET DECERRST FOR SHOULD NOT OCCUR ERROR.                     *
*        ALTERED CHANNEL PROGRAM.                                     *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        NONE                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YM05652K (1/24/74)                                               *
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         L     UCBREG,IOSUCB       BASE UCB
         DROP  ERREG5
         L     CCWREG,IOBFL3       ADDRESS OF CCW
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       ADJUST TO FAILING CCW
         MVC   IOBWORK(L1),DECTYPE+L1(DECBRG)   PICK UP DEV TYPE
         NI    IOBWORK,L255-HIBIT   CLEAR UNUSED BITS
         MVC   IOBWORK+L1(L1),TPCODE(CCWREG)   PICK UP TP OP CODE
         NI    IOBWORK+L1,L255-HIBIT   CLEAR UNUSED BITS
         TM    IOBSENS0,IOBSNBOC   THIS BUSSOUT ERROR
         BZ    ERROVR00            NO CHECK FOR OVERRUN
         CLI   ZERO(CCWREG),CCWAPOLL   THIS A POLL COMMAND
         BE    ERRBOC03            YES SET UP REPOLL FUNCTION
         CLI   ZERO(CCWREG),CCWWRT   THIS A WRITE COMMAND
         BNE   ERRBOC02            NO CHECK FURTHER
         TM    IOBWORK+L1,PTXTXFER   IS COMMAND PRIOR TO TEXT XFERS
         BZ    ERROVR01            YES RETRY CHAN PROGRAM
         TM    IOBWORK+L1,ATXTXFER   IS COMMAND AFTER TEXT XFER
         BZ    ERRBOC01            NO CHECK FURTHER
ERRSTORE EQU   *
         ST    CCWREG,IOBSTART     CPA START TO FAILING CCW
         B     ERROVR01            EXECUTE FAILING CCW
ERRBOC01 TM    DCBERROP(DCBREG),DCBWRTRY   WRITE RETRIES REQUESTED
         BZ    ERRBOC00            NO POST WITH ERROR
         TM    TPCODE(CCWREG),CCWLAST   THIS LAST CCW
         BO    ERROVR01            YES RE-EXECUTE CHAN PROG
         MVC   IOBWORK+L3(L1),TPCODE+L8(CCWREG)   PICK UP NEXT OP CODE
         NI    IOBWORK+L3,L255-HIBIT   CLEAR UNWANTED BITS
         CLI   IOBWORK+L3,CCWRRTXT   READ RESPONSE TO TEXT
         BNE   ERROVR01            NO RE-EXECUTE CHAN PROGRAM
         B     ERRSTORE            RESET IOBSTART
ERRBOC02 EQU   *
         CLI   ZERO(CCWREG),CCWDIAL   THIS A DIAL COMMAND
         BE    ERROVR01            YES RETRY CHANNEL PROGRAM
ERRBOSNO OI    DECERRST(DECBRG),CSWSNO   SET SHOULD NOT OCCUR ERROR
ERRBOC00 LH    NAMEREG,ERLOD5      ADDRESS OF POST ROUTINE
         OI    IOSFLB,IOSLOG       INDICATE LOG OUT
ERRLINK1 EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE
         DROP  ERRETR
         BR    ERRETR              XCTL
ERRCHK1  EQU   *
         TM    UCBTYP+L1(UCBREG),SWLINE   DISABLE REQUIRED
         BZ    ERRBOC00            NO POST ERROR
         MVI   IOBERRCT+L1,MASK15   SPECIAL ENTRY TO DISABLE ROUTINE
         L     NAMEREG,ERLOD9      ADDRESS OF DISABLE ROUTINE
         B     ERRLINK1            EXIT ROUTINE
ERRBOC03 LA    ERREG2,IOBCPA       START OF CHAN PROG
         ST    ERREG2,IOBSTART     STORE POINTER
         L     ERREG1,IOBCPA+L8    LOAD ENTRY ADDRESS FROM PREVIOUS
*                                  POLL (WITH COMMAND CODE IN HIGH
*                                  ORDER BYTE)
         MVC   IOBCPA+L9(L3),DECENTRY+L1(DECBRG)    MOVE IN ADDRESS
*                                  OF LAST ENTRY POLLED (TO BE USED
*                                  AS RESTART ADDRESS)
         L     ERREG4,IOBCPA+L8   LOAD ENTRY ADDRESS FOR NEXT POLL
*                                  (WITH POLL COMMAND CODE IN HIGH
*                                  ORDER BYTE)
         SR    ERREG1,ERREG4       SUBTRACT NEW POLL ADDRESS
*                                  FROM PREVIOUS POLL ADDRESS.
         AH    ERREG1,IOBCPA+L14   IF THE NEW POLL ADDRESS WERE LESS
*                                  THAN THE PREVIOUS POLL ADDRESS, THE
*                                  EFFECT IS TO ADD THE ABSOLUTE
*                                  DIFFERENCE TO THE PREVIOUS COUNT.
*                                  IF IT WERE GREATER, THE EFFECT IS
*                                  TO SUBTRACT.
         STH   ERREG1,IOBCPA+L14   STORE ADJUSTED COUNT IN
*                                  POLL COMMAND.
ERROVR01 CLI   IOBERRCT,ERTRYCT    RETRY REQUIRED
         BNL   ERRCHK1             NO CHECK FOR DISABLE
         IC    ERREG1,IOBERRCT     PICK UP RETRY COUNT
         LA    ERREG1,L1(ERREG1)   BUMP COUNT BY ONE
         STC   ERREG1,IOBERRCT     STORE NEW COUNT
         OC    EWASTUP(L1),IOBSENS0   RECORD TEMPORARY ERRORS
         MVI   IOBSENS0,ZERO       CLEAR SENSE BYTE
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
         L     NAMEREG,TRANSRTN    LOAD NAME
         B     ERRLINK1            XCTL
ERROVR00 LH    NAMEREG,ERLOD15     ADDRESS OF OVERRUN ROUTINE
         B     ERRLINK1            XCTL
         EJECT
**********
*
*        CONSTANTS
*
**********
TRANSRTN DC    F'9042'             NAME OF BTAM TRANSLATE ROUTINE
ERLOD9   DC    F'9041'
ERLOD5   DC    H'5041'
ERLOD15  DC    H'5042'
ERRCN8   DC    H'8'                DECIMAL 8
PACHAREA DC    6C'604A'            PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
ZERO     EQU   X'00'               LENGTH AND DISPLACEMENT
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L9       EQU   9                   LENGTH AND DISPLACEMENT
L14      EQU   14                  LENGTH AND DISPLACEMENT
ERTRYCT  EQU   X'02'
DCBERROP EQU   33
CCWRRTXT EQU   X'20'
HIBIT    EQU   X'C0'
L255     EQU   X'FF'               AND MASK
DECTYPE  EQU   4
PTXTXFER EQU   X'F0'
ATXTXFER EQU   X'20'
TPCODE   EQU   5
CCWLAST  EQU   X'80'
DCBWRTRY EQU   X'40'
CCWWRT   EQU   X'01'
MASK15   EQU   X'0F'
CCWDIAL  EQU   X'29'
CCWAPOLL EQU   X'09'
DECENTRY EQU   20
DECERRST EQU   29
UCBTYP   EQU   16
SWLINE   EQU   X'90'
IOBSNBOC EQU   X'20'
CSWSNO   EQU   X'40'
**********
*
*        REGISTER DEFINITION
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
ERREG2   EQU   2                   WORK REG
EWAREG   EQU   3                   ERP WORKAREA BASE
CCWREG   EQU   4                   CCW ADDRESS REG
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE
BASEREG  EQU   8                   PROGRAM BASE
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG4   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  RETURN REG
ERREG5   EQU   15                  LINK REG/WORK REG
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
