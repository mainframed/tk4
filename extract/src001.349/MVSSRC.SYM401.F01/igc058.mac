 TITLE 'IGC058 - BTAM SVC ROUTINE FOR DYNAMIC BUFFERING'
IGC058   CSECT
         SPACE 2
**********                                                   **********
*                                                                     *
*     THIS MODULE HAS BEEN PARTIALLY RE-WRITTEN FOR OS/VS2 REL 2      *
*                                                                     *
**********                                                   **********
         SPACE 2
* STATUS -          CHANGE LEVEL 000    JANUARY 25, 1967
         SPACE 2
*FUNCTION@ THIS ROUTINE PROVIDES ACCESS TO A DYNAMIC BUFFER MANAGEMENT*
*   ROUTINE LOADED AT OPEN                                            *
         SPACE 2
* ENTRY POINT -     ENTRY IS TO 1ST EXECUTABLE INSTRUCTION
         SPACE 2
* INPUT -           REGISTER 0 - REQUEST COUNT OR RELEASE ADDRESS
*                   REGISTER 1 - DCB ADDRESS
         SPACE 2
* OUTPUT -          REGISTER 15- 00-12 RETURN CODE FROM BUFFER ROUTINE
*                                16    NO BUFFER ROUTINE
         SPACE 2
*EXTERNAL ROUTINES@ BUFFER ROUTINE (IGG019MS)                         *
         SPACE 2
* EXIT -            RETURN IS TO SVC HANDLER VIA REGISTER 14
         SPACE 2
*ATTRIBUTES - THIS IS A TYPE I SVC ROUTINE.  IT IT IS RESIDENT AND    *
*             RUNS DISABLED IN SUPERVISOR MODE.                       *
*
**************** APAR FIXES *******************************************
***********************************************************************
*      ZA02831                    ZA02836
*      AZ08069  (02/02/76)
*
*
*
***********************************************************************
         EJECT
*
*                   REGISTER EQUATION
*
REGZERO  EQU   0
DCBREG   EQU   1                        ADDRESS OF DCB
DEBREGAD EQU   1                        DEB ADDR FOR DEBCHK   LD Y02947
AREAREG  EQU   1                        ADDR FROM GETMAIN     LD YM4077
SAVEREG  EQU   2
TESTREG  EQU   3
TCBREG   EQU   4                        TCB ADDRESS           LD YM4077
DEBREG   EQU   5                        ADDR OF DEB           LD YM4077
SAVE2REG EQU   6
ASCBREG  EQU   7                        ADDRESS OF ASCB       LD YM4077
CVTREG   EQU   8                        PTR TO CVT            LD Y02947
SAVREG15 EQU   8                                              LD YM4077
CVTEXTRG EQU   9                        PTR TO CVT EXTENSION  LD Y02947
TCBREG2  EQU   10                       PTR TO TCB            LD YM4077
FREEMREG EQU   11                       FREEMAIN ADDRESS      LD YM4077
BASEREG  EQU   12                       BASE REGISTER
SAVERG   EQU   13                       SAVEAREA REG          LD YM4077
RETREG   EQU   14                       RETURN REGISTER
BRREG    EQU   15                       BRANCH REGISTER
         SPACE 3
*
*                    MISCELLANEOUS EQUATIONS
*
ZERO     EQU   0                                              LD Y02947
TWO      EQU   2                                              LD Y02947
CRRNTTCB EQU   4                        CURRENT TCB PRT       LD YM4061
EIGHT    EQU   8                                              LD Y02947
CRNTASCB EQU   12                      CURRENT ASCB PRT       LD YM4077
DEBCHK   EQU   X'16E'                  ABEND CODE FOR DEBCHK  LD YM4061
TAM      EQU   X'10'                   BTAM ID              L5 @ZA02831
         EJECT
         BALR  BASEREG,ZERO                                   LD Y02947
         USING *,BASEREG
         USING IHADCB,DCBREG           ADDRESSABILITY FOR DCB LD Y02947
         USING DEB,DEBREG         ADDRESSABILITY FOR DEB    L5 @ZA02831
         B     AROUND
         DC    CL8'IGC058  '
         DC    C'** MVS *'
         DC    C'&SYSDATE'
         DC    S(*)
PATCH    DC    10F'0'
         DS    0F
AROUND   EQU   *
         LR    SAVEREG,RETREG          SAVE RETURN REGISTER
TESTDCB  EQU   *                                              LD Y02947
         SPACE 1
         TM    DCBOFLGS,DCBOFOPN       IS DCB OPEN            LD Y02947
         BNO   NOR                     NO, ABORT
*
*
*        SET PARM AND BRANCH TO DEB VALIDITY CHECK RTN                *
*        TO VERIFY THE DEB                                            *
*
*
         L     DEBREG,DCBDEBAD         DEB ADDR               LD Y02947
         SH    DEBREG,H36              ADJUST FOR DSECT     L5 @ZA02831
         CLI   DEBAMTYP,TAM            IS IT BTAM DEB?      L5 @ZA02831
         BNE   ABEND                   NO,ABEND             L5 @ZA02831
         LR    TESTREG,DCBREG          SAVE DCB ADDRESS           3866
         LR    SAVE2REG,REGZERO        SAVE REG 0             LD YM4061
         USING CVT,CVTREG          ADDRESSABILITY FOR CVT     LD YM4061
         USING CVTXTNT2,CVTEXTRG   ADDRESSABILITY FOR CVT EXT LD YM4061
         L     CVTREG,CVTPTR           CVT POINTER            LD YM4061
         L     CVTEXTRG,CVTEXT2        ADDRESS OF CVT EXT     LD YM4061
         L     TCBREG,CVTTCBP          PRT TO TCB'S ADDRS     LD YM4061
         L     ASCBREG,CRNTASCB(TCBREG) ADDRS OF CURRENT ASCB LD YM4077
         L     TCBREG,CRRNTTCB(TCBREG) ADDR OF TCB            LD YM4061
         L     BRREG,CVTDEBVR          ADDR OF BRANCH ENTRY.. LD YM4061
*                                      POINT TO DEB VALIDITY..LD YM4061
*                                      CHECK ROUTINE          LD YM4061
         SR    REGZERO,REGZERO         PARM REG0 = 0          LD YM4061
         LR    DEBREGAD,DEBREG         PARM REG1 = DEB ADDR   LD YM4051
         LA    DEBREGAD,36(DEBREGAD)   POINT TO DEB BASIC   L5 @ZA02831
         LR    TCBREG2,TCBREG          PARM REG10 = TCB ADDR  LD YM4077
         BALR  RETREG,BRREG            BRANCH TO DEBCHK RTN   LD YM4061
*                                      UPON RETURN FROM DEBVER;
*                                      DEB OK RETURN TO R14
*                                      DEB BAD RETURN T0 R14+4
*
*
         B     OKDEB                   CONTINUE             L5 @ZA02836
         B     ABEND                   DEB BAD,ABEND        L5 @ZA02836
OKDEB    LR    REGZERO,SAVE2REG        RESTORE REG 0        L5 @ZA02836
         LR    DCBREG,TESTREG          RESTORE DCBREG             3866
         SPACE 1
         CLI   DEBAMLNG,ZERO           IS THERE A BUFFER RTN  LD Y02947
         BE    NOR                     NO, ABORT              LD Y02947
         L     REGZERO,AREASIZE        SIZE FOR GETMAIN       LD YM4077
         GETMAIN R,LV=(0),BRANCH=YES                          LD YM4077
         LR    SAVERG,AREAREG       SET SAVEAREA REG FOR MS   LD YM4077
         LR    FREEMREG,AREAREG     SAVE ADDR FOR FREEMAIN    LD YM4077
         LR    REGZERO,SAVE2REG        RESTORE REG 0          LD YM4077
         LR    DCBREG,TESTREG          RESTORE DCBREG         LD YM4077
         SR    BRREG,BRREG             CLEAR REGISTER
         IC    BRREG,DEBNMEXT          NUMBER OF UCBS         LD Y02947
         SLL   BRREG,TWO               SIZE OF UCB TABLE      LD Y02947
         SPACE 1
         L     BRREG,DEBSUCBA(BRREG)   GET MS ADDRESS         LD Y02947
         SPACE 1
         BALR  RETREG,BRREG            LINK TO BUFFER ROUTINE
         SPACE 1
         L     REGZERO,AREASIZE     SET SIZE FOR FREEMAIN     LD YM4077
         LR    AREAREG,FREEMREG     SET ADDR FOR FREEMAIN     LD YM4077
         LR    SAVREG15,BRREG       SAVE REGISTER 15          LD YM4077
         FREEMAIN R,LV=(0),A=(1),BRANCH=YES                   LD YM4077
         LR    REGZERO,SAVE2REG        RESTORE REG 0          LD YM4077
         LR    DCBREG,TESTREG          RESTORE DCBREG         LD YM4077
         LR    BRREG,SAVREG15          RESTORE REGISTER 15    LD YM4077
RET      LR    RETREG,SAVEREG          RESTORE RETURN ADDRESS
         BR    RETREG
NOR      L     BRREG,NORTMSK           SET ERROR CODE
         B     RET
         SPACE 2
*
*         ABEND ROUTINE                                               *
*         THIS ROUTINE WILL ISSUE AN 16E ABEND WHEN THE DEB           *
*         IS NOT A VALID ONE                                          *
*
ABEND    ABEND DEBCHK,DUMP,STEP,SYSTEM                         @ZA08069
         EJECT
PAGEBOUN DS    0F
         DC    X'00FFF800'
NORTMSK  DC    X'10000000'              ERROR CODE FOR NO BUFFER ROUTIN
AREASIZE DC    X'FA00004C'     SUBPOOL AND SIZE FOR GETMAIN   LD YM4077
H36      DC    H'36'                                        L5 @ZA02831
         EJECT
CVT      DSECT
         CVT LIST=YES
         EJECT
         DCBD  DSORG=BX,DEVD=BS
         EJECT
         IEZDEB LIST=YES
         SPACE 3
         CNOP  0,8
         END
