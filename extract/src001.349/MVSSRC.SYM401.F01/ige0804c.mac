      TITLE 'IGE0804C - BSC ERP EQUIPMENT CHECK/COMMAND REJECT MODULE'
IGE0804C CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0804C                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM BSC EQUIPMENT CHECK/COMMAND REJECT ERP                      *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS ROUTINE ATTEMPTS RECOVERY OF EQUIPMENT CHECK AND CHANNEL    *
*    DATA CHECK ERRORS DURING OS/BTAM BINARY SYNCHRONOUS              *
*    COMMUNICATIONS OPERATION.                                        *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (60 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        606 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD.  *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0804C                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        ALTERED CHANNEL PROGRAM                                      *
*        SETS INDICATOR IN DECERRST FOR SHOULD NOT OCCUR ERROR        *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        NONE                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YA02142  (11/5/73)                                               *
*    YM05652K (1/25/74)                                               *
*    AZ08059  (01/26/76)   *                                          *
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         USING RQE,ERREG5
         USING IECTDECB,DECBRG
         USING IHADCB,DCBREG
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         B     AROUND
         DC    C'IGE0804C'
         DC    C'** MVS *'
         DC    C'&SYSDATE'
         DS    0F
AROUND   L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         L     UCBREG,IOSUCB        BASE UCB                   @ZA08059
         DROP  ERREG5
         L     CCWREG,IOBFL3       GET IOB CSW
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       ADJUST TO FAILING CCW IN IOB
         MVC   IOBWORK(L1),TPOPCODE(CCWREG)   INITIALIZE WORK AREA
         NI    IOBWORK,L255-HIBIT   CLEAR UNUSED BITS
         TM    IOBSENS0,CSWCDREJ   THIS CMD REJECT
         BO    BSCEC03             YES GET NEXT LOAD
         TM    IOBCSW+L4,CHNDATCK   CHANNEL DATA CHECK
         BO    BSCDC00             YES CONTINUE FLOW
         TM    IOBWORK,TPOPDTXT    ERROR DURING TEXT
         BNO   BSCEC00E            NO ALL SET FOR RETRY
         TM    DCBBFTEK,DYNBUF     THIS DYNAMIC BUFFERING
         BO    BSCEC02             YES SET PERM ERROR
         TM    IOBWORK,TPOPDTXT    ERROR ON WRITE DLE-ETX
         BO    BSCEC00D            YES, RETRY CCW
BSCEC00F CLI   IOBWORK+L4,ZERO     CHECK FOR RD OR SND ENQ
         BE    BSCEC01             READ ENQ
         LA    ERRLNK,CCWWRITE     SET TO GENERATE CHAN PROG
BSCEC00  LA    ERREG4,CCWTIC       2ND COMMAND CODE
BSCEC00A LA    ERREG2,DCBBSENQ     DATA AREA
BSCEC00B LA    ERREG1,L1           DATA COUNT
BSCEC00C ST    ERREG2,IOBERCCW     CONSTRUCT 1ST CCW DATA ADDR
         STC   ERRLNK,IOBERCCW     PUT IN COMMAND
         ST    ERREG1,IOBERCCW+L4   STORE COUNT
         MVI   IOBERCCW+L4,CHAINCDE   SET CCW FLAGS
         ST    CCWREG,IOBERCCW+L8   SET TO RETRY CCW ADDRESS
         STC   ERREG4,IOBERCCW+L8   SET TIC OR NOP COMMAND
         ST    ERREG1,IOBERINF+L4                               YA02142
         LA    CCWREG,IOBERCCW     NEW CCW ADDRESS
BSCEC00D ST    CCWREG,IOBSTART     SET CCW POINTER TO CHAN RESTART
BSCEC00E CLI   IOBERRCT,ERRMAXCT   RETRY COUNT AT MAX
         BNL   BSCEC02             YES SET PERM ERROR INDICATOR
         IC    ERREG1,IOBERRCT     PICK UP OLD COUNT
         LA    ERREG1,L1(ERREG1)   BUMP TO NEW COUNT
         STC   ERREG1,IOBERRCT     STORE NEW COUNT
         MVI   EWAIND1,EWAISVCF    EXIT TO SVC 15              YM05652K
         B     RETRY               ERROR EXCP
BSCEC01  CLI   ZERO(CCWREG),CCWREAD   THIS A READ FAILURE
         BNE   BSCEC01A            NO, SET UP READ ENQ
         CLI   IOBCPA,CCWAPOLL     POLLING OPERATION
         BNE   BSCEC01A            NO NORMAL READ
         L     ERREG1,IOBCPA+L48   READ TEXT CCW
         BCTR  ERREG1,ZERO         REDUCE BY ONE
         ST    ERREG1,IOBCPA+L48   RESTORE NEW AREA AND COMMAND
         LH    ERREG1,IOBCPA+L54   PICK UP COUNT
         LA    ERREG1,L1(ERREG1)   BUMP BY ONE
         STH   ERREG1,IOBCPA+L54   PUT NEW COUNT IN CCW
         LA    CCWREG,IOBCPA+L48   ADDRESS OF FAILING COMMAND
BSCEC01A LA    ERREG2,IOBWORK+L2   DATA AREA
BSCEC01B LA    ERREG4,CCWNOP       2ND COMMAND
         LA    ERRLNK,CCWREAD      GENERATE CHAN PROG 1ST COMMAND
         MVI   IOBWORK+L1,RTRNCDE0   SPECIAL ROUTIN IND
         MVI   IOBERRCT+L1,RTRNSPCD   SET FOR SPECIAL RETURN
         LA    ERREG1,L2           DATA COUNT
         B     BSCEC00C
BSCDC00  CLI   ZERO(CCWREG),CCWWRITE   ERROR ON A WRITE
         BNE   BSCDC01             NO CHECK FURTHER
         CLI   IOBWORK,TPOPDTXT    DURING TEXT
         BE    BSCEC02             YES SET PERM ERROR
         ST    CCWREG,IOBSTART     SET TO RETRY CHAN PROG
         B     BSCEC00E            CHECK RETRIES
BSCDC01  CLI   IOBWORK,TPOPDTXT    DURING TEXT
         BNE   BSCDC02             NO CHK FURTHER
         TM    DCBBFTEK,DYNBUF     DYNAMIC BUFFERING
         BO    BSCEC02             SET PERM ERROR
         LA    ERREG2,DCBBSNAK     ADDRESS OF NAK CHARS
         LA    ERREG4,CCWTIC       2ND COMMAND
         LA    ERRLNK,CCWWRITE     1ST COMMAND
         B     BSCEC00B            GENERATE CHAN PROGRAM
BSCDC02  CLI   IOBWORK,TPOPRENQ    READ ENQ
         BE    BSCEC00D            YES RETRY CHAN PROG
         B     BSCEC00F            CHECK SEND OR RCV ENQ
BSCEC02  NI    IOSFLA,L255-IOSERR   INDICATE PERM ERROR
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
         LA    NAMEREG,ERLOD25     ADDRESS OF ERROR POST
XCTLOUT  EQU   *
         MVC   EWACHA(L2),L4(UCBREG) MOVE UNIT ADDR TO WK AREA @ZA08059
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE
         DROP  ERRETR
         BR    ERRETR              XCTL
BSCEC03  SR    ERREG1,ERREG1       CLEAR LINKAGE REGISTER
         CLI   IOBERRCT+L1,NORMRET   SPECIAL RETURN
         BNE   BSCCR02             YES, CHK FOR GOOD WRT DLE
         CLI   ZERO(CCWREG),CCWRDCMD   READ COMMAND
         BNE   BSCCR03             NO,  INDICATE SNO ERR
         CLI   DECTPCOD,RDRESPOP   READ RESP AFTER TEXT
         BNE   BSCCR03             NO,  INDICATE SNO ERR
         MVC   IOBWORK+L2(L1),DCBBSDLE   PICK UP DLE CHAR
         MVC   IOBWORK+L3(L1),DCBBSENQ   PICK UP ENQ CHAR
         LA    ERREG2,IOBWORK+L2   ADDRESS OF AREA
         LA    ERRLNK,CCWWRTCD     FIRST COMMAND CODE
         LA    ERREG4,NOOP         SECOND COMMAND
         LA    ERREG1,L2           COUNT
         ST    ERREG2,IOBERCCW     SET UP AREA IN CCW
         STC   ERRLNK,IOBERCCW     PUT IN COMMAND
         STH   ERREG1,IOBERCCW+L6   PUT IN COUNT
         MVI   IOBERCCW+L4,CHAINCDE   SET CHAINING
         ST    CCWREG,IOBERCCW+L8   RESTART ADDRESS
         STC   ERREG4,IOBERCCW+L8   SECOND COMMAND
         ST    ERREG1,IOBERCCW+L12   COUNT IN SECOND CCW
         MVI   IOBERRCT+L1,SPECRET   SPECIAL RETURN SET
         CLI   IOBERRCT,ERRMAXCT   RETRIES EXHAUSTED
         BE    BSCCR03             YES,  SET PERM ERROR
         IC    ERREG1,IOBERRCT     UPDATE RETRY COUNT
         LA    ERREG1,L1(ERREG1)   TO NEW VALUE
         STC   ERREG1,IOBERRCT     STORE IN COUNT FIELD
         LA    CCWREG,IOBERCCW     CHAN START
         ST    CCWREG,IOBSTART     SET RESTART ADDRESS
         MVI   EWAIND1,EWAISVCF    EXIT TO SVC 15              YM05652K
         B     RETRY               ERROR EXCP
BSCCR03  NI    IOSFLA,L255-IOSERR   INDICATE PERM ERROR
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
         MVI   IOBERRCT,ERRMAXCT   INDICATE NO RETRIES
         LA    NAMEREG,ERLOD25     ADDRESS OF ERROR POST
         B     XCTLOUT             EXIT
BSCCR02  TM    IOBCSW+L3,L255-CSWCDE   ANY COND BUT CE-DE
         BM    BSCCR03             YES, PERMANENT ERROR
         TM    IOBCSW+L3,CSWCDE    CHAN END AND DEV END
         BNO   BSCCR03             NO,  PERMANENT ERROR
         CLI   IOBCSW+L4,ZERO      ANY STATUS IN BYTE 2
         BNE   BSCCR03             YES, PERMANENT ERROR
         MVC   IOBSTART+L1(L3),IOBERCCW+L9   WRT DLE-ENQ WAS GOOD
         MVI   IOBERRCT+L1,NORMRET   NORMAL RETURN, RETRY FAILING CCW
         MVI   EWAIND1,EWAISVCF    EXIT TO SVC 15              YM05652K
RETRY    EQU   *
         L     NAMEREG,TRANSRTN    LOAD NAME
         B     XCTLOUT             XCTL
         EJECT
**********
*
*        CONSTANTS
*
**********
TRANSRTN DC    F'9042'             NAME OF BTAM TRANSLATE ROUTINE
ERRCN8   DC    H'8'
PACHAREA DC    15C'804C'           PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
CSWCDREJ EQU   X'80'
RTRNCDE0 EQU   X'80'
CCWNOP   EQU   3
TPOPDTXT EQU   X'11'
CHAINCDE EQU   X'60'
CHNDATCK EQU   X'08'
TPOPCODE EQU   5
L255     EQU   X'FF'               AND MASK
CCWREAD  EQU   X'02'
HIBIT    EQU   X'C0'
ERRMAXCT EQU   7
ZERO     EQU   X'00'               LENGTH AND DISPLACEMENT
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L9       EQU   9                   LENGTH AND DISPLACEMENT
L12      EQU   12                  LENGTH AND DISPLACEMENT
L48      EQU   48                  LENGTH AND DISPLACEMENT
L54      EQU   54                  LENGTH AND DISPLACEMENT
TPOPRENQ EQU   X'0B'
DYNBUF   EQU   X'08'
CCWAPOLL EQU   X'09'
CCWWRITE EQU   X'01'
CCWTIC   EQU   X'08'
ERLOD25  EQU   2043
RTRNSPCD EQU   X'14'
CCWRDCMD EQU   X'02'               READ COMMAND CODE
CCWWRTCD EQU   X'01'               WRITE COMMAND CODE
CSWCDE   EQU   X'0C'               CHAN-DEVICE END
NOOP     EQU   X'03'
NORMRET  EQU   X'00'               NORMAL RETURN
RDRESPOP EQU   X'25'               BSC RD RESP AFTER TXT
SPECRET  EQU   X'04'               SPECIAL RETURN CMDREJ
**********
*
*        REGISTER DEFINITIONS
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
ERREG2   EQU   2                   WORK REG
EWAREG   EQU   3                   ERP WORKAREA BASE
CCWREG   EQU   4                   CCW ADDRESS REG
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE
BASEREG  EQU   8                   PROGRAM BASE
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG4   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  LINK REG
ERREG5   EQU   15                  LINK REG/WORK REG
         EJECT
         DCBD  DSORG=BX,DEVD=BS
         EJECT
         IECTDECB
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
