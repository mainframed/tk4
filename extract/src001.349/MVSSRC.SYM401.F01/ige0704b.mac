         TITLE '  IGE0704B - 3270 ERROR POST '
IGE0704B CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0704B                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM 3270 ERROR POST ERP                                         *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS MODULE IS AN ADDITIONAL LOAD OF THE BSC ERP AND IS XCTL'D   *
*    TO BY BSC ERP CONTROL (IGE0004C). IT FORMATS THE T-TYPE RECORD   *
*    USING INFORMATION IN THE ERROR STATUS MESSAGE (SOH%R MESSAGE)    *
*    FOR THE DEVICES OF THE REMOTE 3270 DISPLAY SYSTEM.               *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        TRANSLATE TABLE AND CODE TO PROCESS ASCII DATA ARE CHARACTER *
*        CODE DEPENDENT.                                              *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (36 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        366 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD   *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0704B                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        BSC3: IOBERINF+4: T-TYPE RECORD WITH THE FOLLOWING FORMAT:   *
*       +++++++++++++++++++++++++++++++++++++++++++++++++++++++       *
*       +        +        +        +        +        +        +       *
*    +0 +     BUFFER      +   NOT  +    XCTL INFO    + RECORD +       *
*       +      SIZE       +  USED  +        +        +   ID   +       *
*       +        +        +        +        +        +        +       *
*       +++++++++++++++++++++++++++++++++++++++++++++++++++++++       *
*       +        +        +        +        +        +        +       *
*    +6 +  CHANNEL/UNIT   +   CU   +   DS   +  STAT1 +  STAT2 +       *
*       +    ADDRESS      +        +        +        +        +       *
*       +        +        +        +        +        +        +       *
*       +++++++++++++++++++++++++++++++++++++++++++++++++++++++       *
*                                                                     *
*       BSC2: IOBERINF+0: MDR (T-TYPE) RECORD WITH THE FOLLOWING      *
*       FORMAT:                                                       *
*       +++++++++++++++++++++++++++++++++++++++++++++++++++++++       *
*       +        +        +        +        +        +        +       *
*    +0 +     BUFFER      + SUB-ID +    XCTL INFO    + RECORD +       *
*       +      SIZE       +  INFO  +        +        +   ID   +       *
*       +        +        +        +        +        +        +       *
*       +++++++++++++++++++++++++++++++++++++++++++++++++++++++       *
*       +        +        +        +        +        +        +       *
*    +6 +  CHANNEL/UNIT   +     T E R M I N A L     I D       +       *
*       +    ADDRESS      +        +        +        +        +       *
*       +        +        +        +        +        +        +       *
*       +++++++++++++++++++++++++++++++++++++++++++++++++++++++       *
*       +        +        +                                           *
*       +  STAT1 +  STAT2 +                                           *
*    +C +        +        +                                           *
*       +        +        +                                           *
*       +++++++++++++++++++                                           *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        NONE                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA                                         *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YM05652K (1/24/74)                                               *
*    YA03260  (2/4/74)                                                *
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         USING IHADCB,DCBREG
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DCBREG,IOBDCBPT     BASE DCB
         L     UCBREG,IOSUCB       BASE UCB
         DROP  ERREG5
         TM    UCBTYP+L1(UCBREG),BSC2   IS IT DIAL
         BO    BSC2RTN             YES, HANDLE
         LA    ERREG2,IOBERINF+L4   POINT TO T-RECORD
         ST    ERREG2,IOBERCCW     SAVE ADDR OF T-RECORD FOR ERREP
         NI    IOSFLB,L255-IOSLOG   NO RECORDING REQUIRED
         XC    ZERO(RCDLNGTH,ERREG2),ZERO(ERREG2)   CLEAR T RECORD
         MVI   BUFSIZE(ERREG2),SIZE   MOVE BUFFER SIZE INTO RECORD
         MVI   RECDID(ERREG2),ID   MOVE IN RECORD ID
         MVC   CUA(LINEADDR,ERREG2),UCBCUA(UCBREG)   MOVE LINE ADDR
         L     RMSGPTR,IOBERINF    GET ADDR OF SOH%R MESSAGE
         MVC   CU(TOSTAT2,ERREG2),RMSGCU(RMSGPTR)   PUT IN RECORD
         TM    DCBXCODE,ASCII      IS IT ASCII
         BNO   NOTRANS             NO, DON'T TRANSLATE
         LA    ERREG2,CU(ERREG2)   GET BEGIN ADDR OF 4 BYTES TO TRANS
         TR    ZERO(ADRLNGTH,ERREG2),ASCTAB   TRANSLATE TO EBCDIC
NOTRANS  EQU   *
         NI    IOBCSW+L3,UE        TURN OFF EXCEPTION BIT
         NI    IOBCSW+L4,L255-IL   ALSO INCORRECT LENGTH        YA03260
         NI    IOSFLA,L255-IOSERR-IOSEX   RESET ERP FLAGS
         NI    IOBFLAG1,L255-IOBERRTN-IOBIOERR   ALSO IN IOB
         OI    IOBINCAM,RMSGPROC   TURN ON BIT 5
         L     NAMEREG,TRANSRTN    ADDR OF TRANSLATE RTN       YM02594K
         MVI   EWAIND1,EWAILD15    SET FOR TPR EXIT            YM05652K
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE
         DROP  ERRETR
         BR    ERRETR              EXIT VIA XCTL ROUTINE
BSC2RTN  EQU   *
         L     RMSGPTR,IOBERINF    GET ADDR OF S/S MSG
         LA    ERREG2,IOBERINF     POINT TO MDR RECORD
         ST    ERREG2,IOBERCCW     SAVE ADDR OF MDR FOR EREP
         NI    IOSFLB,L255-IOSLOG   NO RECORDING REQUIRED
         XC    ZERO(MDRLNGTH,ERREG2),ZERO(ERREG2)   CLEAR MDR AREA
         MVI   BUFRSZ(ERREG2),BUFSZ   MOVE IN BUFFER SIZE
         MVI   RCDID(ERREG2),DIALID   MOVE IN RECORD ID
         MVI   SUBID(ERREG2),SUBIDINF   MOVE IN SUB-ID
         MVC   CHANUNIT(LINEADDR,ERREG2),UCBCUA(UCBREG)   LINE ADDR
         MVC   TERMID(IDLENGTH,ERREG2),IOBCPA+CPAEND   TERM ID
         MVC   SS1(STATLNG,ERREG2),STATUS(RMSGPTR)   SENSE+STATUS
         TM    DCBXCODE,ASCII      IS IT ASCII ?
         BNO   NOTRANS             NO, DON'T TRANSLATE
         LA    ERREG2,TERMID(ERREG2)   GET ADDR OF BYTES TO TRANS
         TR    ZERO(TRANSLNG,ERREG2),ASCTAB   TRANSLATE TERM ID
         B     NOTRANS             SET UP TO XCTL
         EJECT
**********
*
*        CONSTANTS
*
**********
         ORG   *-X'20'             SAVE 20 BYTES WITH ORG
ASCTAB   DS    0F
         ORG   *+X'20'             ORG TO START OF TABLE
*                EBCDIC EQUIVALENTS     ASCII
         DC    X'404F7F7B5B6C507D4D5D'  X'20' TO X'29'
         DC    X'5C4E6B604B61F0F1F2F3'  X'2A' TO X'33'
         DC    X'F4F5F6F7F8F97A5E4C7E'  X'34' TO X'3D'
         DC    X'6E6F7CC1C2C3C4C5C6C7'  X'3E' TO X'47'
         DC    X'C8C9D1D2D3D4D5D6D7D8'  X'48' TO X'51'
         DC    X'D9E2E3E4E5E6E7E8E94A'  X'52' TO X'5B'
         DC    X'6A5A5F6D'              X'5C' TO X'5F'
TRANSRTN DC    F'9042'             NAME OF BTAM TRANSLATE RTN  YM02594K
ERRCN8   DC    H'8'                CONSTANT
PACHAREA DC    9C'704B'            PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
IL       EQU   X'40'               INCORRECT LENGTH IN CSW      YA03260
L255     EQU   255                 AND MASK
ZERO     EQU   0                   LENGTH AND DISPLACEMENT
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L12      EQU   12                  LENGTH AND DISPLACEMENT
L14      EQU   14                  LENGTH AND DISPLACEMENT
SIZE     EQU   12                  SIZE OF RECORD
UCBCUA   EQU   4                   UCB DISP OF CUA
TOSTAT2  EQU   4                   LENGTH OF ADDR AND STATUS BYTES
RMSGCU   EQU   4                   CU DISP IN SOH%R MSG
LINEADDR EQU   2                   DISP OF LINE ADDR IN T RECORD
ADRLNGTH EQU   4                   LENGTH OF ADDR IN RECORD
UCBTYP   EQU   16                  DISP OF TYPE IN UCB
MDRLNGTH EQU   14                  LENGTH OF DIAL MDR RECORD
IDLENGTH EQU   4                   LENGTH OF TERMINAL ID
CPAEND   EQU   64                  DISP PAST IOBCPA OF TERM ID
STATUS   EQU   4                   DISP OF STATUS IN S/S MSG
STATLNG  EQU   2                   NO. OF STATUS BYTES
TRANSLNG EQU   6                   NO. OF BYTES TO TRANSLATE
ASCII    EQU   X'14'               ASCII SETTING
UE       EQU   X'FE'               UNIT EXCEPTION BIT
RMSGPROC EQU   X'04'               SOH%R MSG PROCESSED
BSC2     EQU   X'90'               BSC2 FEATURE IN UCB
DIALID   EQU   X'0B'               ID FOR DIAL IN MDR
SUBIDINF EQU   X'83'               SUB-ID INFO FOR DIAL - TELLS
*                                  EREP THERE'S A 4-BYTE TERM ID
BUFSZ    EQU   X'0E'               BUFFER LENGTH OF MDR
ID       EQU   3                   RECORD ID
*                                  T RECORD
BUFSIZE  EQU   1                   BUFFER SIZE + 1
RECDID   EQU   5                   RECORD ID
CUA      EQU   6                   CHANNEL/UNIT ADDR
CU       EQU   8                   CONTROL UNIT ADDR
RCDLNGTH EQU   12                  LENGTH OF T-RECORD
*                                  MDR RECORD
BUFRSZ   EQU   1                   BUFFER SIZE
SUBID    EQU   2                   SUB-ID INFO
RCDID    EQU   5                   RECORD ID
CHANUNIT EQU   6                   CUA
TERMID   EQU   8                   TERMINAL ID
SS1      EQU   12                  S/S1
**********
*
*        REGISTER DEFINITIONS
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE
ERREG2   EQU   2                   WORK REG
EWAREG   EQU   3                   ERP WORKAREA BASE
UCBREG   EQU   4                   UCB BASE
IOBRG    EQU   5                   IOB BASE
DCBREG   EQU   6                   DCB BASE
RMSGPTR  EQU   7                   SOH%R MSG ADDR
BASEREG  EQU   8                   PROGRAM BASE
ERREG1   EQU   9                   WORK REG
ERREG6   EQU   10                  WORK REGG
ERREG4   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  LINK REG
ERREG5   EQU   15                  WORK REG
         EJECT
         DCBD  DSORG=BX,DEVD=BS
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
