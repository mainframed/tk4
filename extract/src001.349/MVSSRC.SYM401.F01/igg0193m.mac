 TITLE 'IGG0193M  BTAM LINE GROUP OPEN EXECUTOR - LOAD 1'
***********************************************************************
*                                                                     *
* MODULE NAME: IGG0193M  (OS/VS)                                      *
*                                                                     *
* DESCRIPTIVE NAME: BTAM LINE GROUP OPEN EXECUTOR - LOAD 1            *
*                                                                     *
* COPYRIGHT: NONE                                                     *
*                                                                     *
* STATUS: RELEASE 2                                                   *
*                                                                     *
* FUNCTION/OPERATION: THIS ROUTINE GETS MAIN STORAGE FOR AND INITIAL- *
*   IZES A DATA EXTENT BLOCK.  IT ANALYZES DEVICE TYPE INFORMATION    *
*   PROVIDED IN THE UNIT CONTROL BLOCK TO DETERMINE WHAT TYPE OF      *
*   TERMINAL DEVICES ARE ATTACHED TO THE GROUP OF LINES THAT MAKE UP  *
*   THIS LINE GROUP.                                                  *
*                                                                     *
* ENTRY POINTS: THIS ROUTINE IS ENTERED AT IGG0193M VIA AN XCTL FROM  *
*   AN I/O SUPPORT MODULE OR FROM ANOTHER ACCESS METHOD'S OPEN        *
*   EXECUTOR.  IT MAY ALSO BE RE-ENTERED FROM THE RELOOP PORTION OF   *
*   THIS ROUTINE IF IT DETERMINES THAT THERE IS ANOTHER DATA CONTROL  *
*   BLOCK TO BE OPENED.                                               *
*                                                                     *
* INPUT: THE REGISTERS 5, 6, 7, AND 8 ARE THE INPUT, AS FOLLOWS,      *
*   5- THE ADDRESS OF THE FIRST ENTRY IN THE COPIED DCB PARAMETER LIST*
*   6- THE ADDRESS OF THE BEGINNING OF THE WHERE-TO-GO TABLE,         *
*   7- THE ADDRESS OF THE CURRENT ENTRY IN THE COPIED DCB PARAMETER   *
*      LIST,                                                          *
*   8- THE ADDRESS OF THE CURRENT EXECUTOR'S ENTRY IN THE WHERE-TO-GO *
*      TABLE.                                                         *
*                                                                     *
* OUTPUT:  THE REGISTERS 7 AND 8 WILL BE POSITIONED AT THE NEXT       *
*   ENTRIES IN THE COPIED DCB PARAMETER LIST AND THE WHERE-TO TABLE.  *
*   A DEB WILL HAVE BEEN CONSTRUCTED IN SUBPOOL 230.                  *
*  SOME OF THE DCB FIELDS WILL HAVE BEEN INITIALIZED.                 *
*  THIS EXECUTOR'S ENTRY (OR ENTRIES) IN THE WHERE - TO -             *
*   GO TABLE WILL HAVE BEEN UPDATED TO IDENTIFY LOAD 2 (IGG0194N -    *
*   FOR NON LOCAL 3270 DEVICES) OR (IGG0194P - FOR LOCAL 3270 DEVICES)*
*                                                                     *
* EXTERNAL ROUTINES:  NONE                                            *
*                                                                     *
* EXITS-NORMAL:  THIS ROUTINE EXITS VIA AN XCTL TO THE MODULE         *
*   IDENTIFIED BY THE NEXT NON-ZERO ENTRY IN THE WHERE-TO-GO TABLE,   *
*   NAMELY IGG0194N OR IGG0194P.                                      *
*                                                                     *
*     -ERRORS-  THIS ROUTINE EXITS VIA ABEND FOR THE FOLLOWING        *
*   ERROR CONDITIONS-                                                 *
*   ABEND CODE ERROR CONDITION                                        *
*   00090000    A DEVICE OTHER THAN A COMMUNICATIONS DEVICE HAS BEEN  *
*               ALLOCATED TO THIS DCB.                                *
*   00091000    AN INVALID OR UNSUPPORTED CONTROL UNIT HAS BEEN       *
*               SPECIFIED                                             *
*   00092000    AN INVALID OR UNSUPPORTED TYPE OF TERMINAL CONTROL    *
*               OR ADAPTER HAS BEEN SPECIFIED                         *
*   00093000    AN INVALID OR UNSUPPORTED TERMINAL TYPE HAS BEEN      *
*               SPECIFIED.                                            *
*   00094000    AN INVALID OR UNSUPPORTED OPTIONAL FEATURE (OR MODE OF*
*               OPERATION) HAS BEEN SPECIFIED.                        *
*   00095000    INCOMPATABLE DEVICES ARE DEFINED TO BE ON THE SAME    *
*               LINE GROUP.                                           *
*                                                                     *
* TABLES/WORK AREAS: THE WHERE-TO-GO TABLE CONTAINS THE ID AND TTR    *
*       OF THE OPEN EXECUTOR LOAD 2. THE DCB PARAMETER LIST CONTAINS  *
*       THE ADDRESS OF EACH DCB SPECIFIED IN THE OPEN MACRO. THE      *
*       COMMUNICATION VECTOR TABLE IS A RESIDENT VECTOR LIST USED     *
*       TO LOCATE THE TIOT, WHICH IS NEEDED IN DEB CONSTRUCTION.      *
*                                                                     *
* ATTRIBUTES: THIS ROUTINE IS EXECUTED IN THE DATA MANAGEMENT KEY OF  *
*       5 AS ENABLED, PRIVILEGED, AND REENTRANT.                      *
*                                                                     *
* NOTES: THIS ROUTINE INITIATES THE OPEN PROCESSING FOR               *
*  TELECOMMUNICATIONS. IT CONSTRUCTS AND INITIALIZES THE DEB          *
*  (INCLUDING THE UCB ADDRESSES IN THE DEB). FOR SECURITY AND         *
*  INTEGRITY PURPOSES, OPEN PROCESSING WILL BE ONLY ON THE COPIED     *
*  DCB AND COPIED DCB PARAMETER LIST. BTAM OPEN LOADS MUST KEEP       *
*  THE USER DCB AND THE COPIED DCB SYNCHRONIZED. THE LAST BTAM        *
*  OPEN LOAD WILL SYNCHRONIZE THE DCB'S. UPON COMPLETION OF THIS      *
*  MODULE, CONTROL IS PASSED TO THE SECOND OPEN MODULE THROUGH        *
*  THE WHERE-TO-GO TABLE ENTRY.                                       *
*                                                                     *
* CHANGE ACTIVITY AS FOLLOWS:                                         *
*            APARS                           PTMS                     *
*       YA02170  (10/5/73)               YM5663  (12/11/73) PROLOG    *
*       YA03251  (2/1/74)                                             *
*       AZ04858  (10/06/75)                                           *
*       AZ10016  (03/30/76)                                           *
*       AZ26035  (09/16/77)                                           *
*       AZ17697  (11/10/77)                                           *
***********************************************************************
         EJECT
*/*IGG0193M: CHART                                                   */
*/*HEADER
*/*CHART A1-A  BTAM OPEN EXECUTOR - LOAD 1 (IGG0193M; VS2 RELEASE 2) */
*/*IGG0193M: E IGG0193M */
*/*          P ESTABLISH COPIED DCB ADDRESSABILITY */
*/*          D (NO,,YES,BYPASS) IS THIS A STRAM DCB? */
*/*          P LOCATE DD ENTRY IN TIOT */
*/*DDCONCAT  P ACCUMULATE SPACE REQUIREMENTS FOR UCB ADDRESSES */
*/*          D (YES,ENDD,NO,) END OF TIOT? */
*/*          D (YES,DDCONCAT,NO,) NEXT DD ENTRY CONCATENATED? */
*/*ENDD      D (YES,DEBMAX,NO,) DYNAMIC BUFFERING SPECIFIED? */
*/*          D (YES,DEBMID,NO,) DOES BUFFER POOL EXIST? */
*/*          D (NO,DEBMIN,YES,) ARE ANY BUFFERS REQUESTED? */
*/*          D (YES,DEBMIN,NO,DEBMID) BUFFER LENGTH ZERO? */
*/*DEBMAX    P INCREMENT DEB SIZE BY 10 */
*/*DEBMID    P INCREMENT DEB SIZE BY 6 */
*/*DEBMIN    P INCREMENT DEB SIZE BY BASIC DEB SIZE OF 89 */
*/*          L SVC 10 - GETMAIN FOR DEB FROM SUBPOOL 230 */
*/*CLEARDEB  P CLEAR DEB AND INITIALIZE DEB AND COPIED DCB FIELDS */
*/*CHAIN     P PUT DEB ON TCB-DEB CHAIN */
*/*          D (YES,SETERP,NO,) IS ERP REQUESTED? */
*/*          P SET NO ERP FLAG IN DCB */
*/*SETERP    P PLACE USER'S DCB ADDRESS IN DEB */
*/*          L SVC 117 - DEBCHK,TYPE=ADD */
*/*          P PRIME REG1 WITH ABEND CODE 90 */
*/*UCBMOVE   P MOVE UCB ADDRESSES TO DEB */
*/*          P SET IOS APPENDAGES ADDR IN FIRST 20 BYTES OF DCB */
*/*          D (YES,ANRLOAD2,NO,) DEVICE CLASS = GRAPHICS? */
*/*NEXTUCB   D (NO,AB5,YES,) ALL DEVICES IN LINE GROUP COMPATIBLE? */
*/*TESTTCU   D (NO,AB0,YES,) TELECOMMUNICATIONS UCB? */
*/*          D (NO,AB1,YES,) VALID CONTROL UNIT? */
*/*          D (NO,AB2,YES,) VALID ADAPTER? */
*/*          D (NO,AB3,YES,) VALID TERMINAL? */
*/*          D (NO,AB4,YES,) VALID FEATURES? */
*/*ENDLOD1   P SAVE INDEX FOR DEVTAB IN DCB FOR LOAD2 */
*/*          P SET WTG TABLE FOR IGG0194N */
*/*IGG0193M: END */
         EJECT
IGG0193M CSECT
         SPACE 5
RPARM0   EQU   0                        PARAMETER REGISTER 0
RPARM1   EQU   1                        PARAMETER REGISTER 1
RW1      EQU   2                        WORK REGISTER 1
RW2      EQU   3                        WORK REGISTER 2
RW3      EQU   4                        WORK REGISTER 3
RPAR     EQU   5                        ADDRESS OF COPIED DCB    Y02947
*                                       PARAMETER LIST           Y02947
RWTG     EQU   6                        ADDRESS OF WTG TABLE
RPARC    EQU   7                        ADDRESS OF CURRENT       Y02947
*                                       COPIED DCB ENTRY         Y02947
RWTGC    EQU   8                        ADDRESS OF CURRENT WTG ENTRY
RTIOT    EQU   9                        ADDRESS OF TASK I/O TABLE
RDCB     EQU   10                       ADDRESS OF COPIED DCB    Y02947
RDEB     EQU   11                       ADDRESS OF DEB
RBASE    EQU   12                       BASE REGISTER
RSAVE    EQU   13                       ADDRESS OF TASK CONTROL BLOCK
RUCB     EQU   14                       NO. OF UCB'S/ADDRESS OF UCB
RSIZE    EQU   15                       SIZE OF DEB IN DOUBLE WORDS
         BALR  RBASE,0                  SET UP BASE REGISTER
         USING *,RBASE
         USING IHADCB,RDCB
         USING IECTDEB,RDEB
         SPACE 4
         B     AROUND                   BRANCH AROUND ID         Y02947
         DC    C'IGG0193M'              OPEN ID                  Y02947
         DC    C'UZ06851'               CURRENT PTF NUMBER     @ZA26035
         DS    0F                                                Y02947
PATCH    DC    XL50'00'                 PATCH AREA               Y02947
         SPACE 4                                                 Y02947
AROUND   L     RDCB,0(RPARC)            LOAD COPIED DCB ADDRESS  Y02947
*                                       IF THE SAME DCB OCCURS IN THE
*                                            PARAMATER LIST MORE THAN
*                                            ONCE, IT SHOULD BE PRO-
*                                            CESSED ONLY THE FIRST TIME
*       FOUR LINES DELETED BY THE FOLLOWING APAR                YA03251
         SPACE 1
         TM    DCBDEVTP,STR             TEST FOR STRAM
         BO    BYPASS                   IF SO, BRANCH OUT
         SPACE 1                                                 Y02947
         USING CVT,RSAVE                CVT ADDRESSABILITY       Y02947
         L     RSAVE,SIXT               LOCATE DDNAME VIA CVT,   Y02947
         L     RSAVE,CVTTCBP                 TASK CONTROL BLOCK, Y02947
         L     RSAVE,CURNTTCB(RSAVE)         AND CURRENT TASK CONTROL
         L     RTIOT,TCBTIO(RSAVE)           BLOCK.  LOAD ADRS OF TASK
         AH    RTIOT,DCBTIOT                 I/O TABLE, ADD OFFSET TO
*                                            LENGTH FIELD
         DROP RSAVE                    DROP CVT ADDRESS          Y02947
         EJECT                                                   Y02947
         LR    RW1,RTIOT                LOAD ADRS INTO WORK REGISTER
         SR    RW2,RW2                  RESET REGISTERS
         SR    RW3,RW3
         SR    RUCB,RUCB
         SPACE 1                                                 Y02947
         MODESET EXTKEY=ZERO            KEY ZERO                 Y02947
         SPACE 1                                                 Y02947
DDCONCAT LA    RW2,1(RW2)               ADD 1 FOR COUNT OF CONCAT DD'S
         IC    RW3,0(RW1)               LOAD TIOT LENGTH
         AR    RW1,RW3                  ADD TO START OF DD ENTRY
         SH    RW3,SIXTEEN              SUBTRACT SIZE OF DD INFORMATION
         AR    RUCB,RW3                 RESULT=NO OF DEVICES, ACCUMULAT
         NC    0(4,RW1),0(RW1)          TEST FOR ZERO END WORD
         BZ    ENDD                     IF ZERO BRANCH
         CLI   4(RW1),BLNK              TEST FOR CONCATENATED DD
         BE    DDCONCAT                 IF SO, LOOP
         SPACE 1                                                 Y02947
ENDD     EQU   *                                                 Y02947
         MODESET EXTKEY=DATAMGT         DATAMGT KEY              Y02947
         SPACE 1                                                 Y02947
         ST    RW2,DCBDEBAD            SAVE COUNT OF DD'S        A26886
         SR    RW2,RW2                                           A26886
         LR    RW3,RUCB
         TM    DCBBFTEK,DYNBUF          TEST FOR DYNAMIC BUFFERING
         BO    DEBMAX                   IF SPECIFIED BRANCH
         CLI   DCBBUFCB+3,X'01'         IS THERE A BUFFER POOL
         BNE   DEBMID                   IF SO, BRANCH (X'01'=NO BFR PL)
         CLI   DCBBUFNO,X'00'           ARE ANY BUFFERS REQUESTED
         BE    DEBMIN                   IF NOT, BRANCH
         CH    RW2,DCBBUFL              IF SO, TEST LENGTH
         BE    DEBMIN                   NO LENGTH IS SPECIFIED
         B     DEBMID                   LENGTH IS NOT ZERO
DEBMAX   LA    RW3,10(RW3)              INCREMENT SIZE
         LA    RW2,8(RW2)               INCREMENT TYPE
DEBMID   LA    RW3,6(RW3)               INCREMENT SIZE
         LA    RW2,4(RW2)               INCREMENT TYPE
DEBMIN   LA    RW3,89(RW3)              ADD BASIC DEB SIZE
         SRL   RW3,3                    COMPUTE DOUBLE WORDS IN DEB
         LR    RW1,RW3                  SAVE
         SLL   RW3,3                    COMPUTES BYTES IN DEB
         LR    RPARM0,RW3               LOAD GET MAIN PARAMETER REG
         O     RPARM0,DEBPOOL           OR IN SUBPOOL NUMBER
         SPACE 2                                                 Y02947
*   GET CORE FOR DEB'S FROM SUBPOOL 230                          Y02947
         GETMAIN R,LV=(0)               REQUEST STORAGE FOR DEB
         EJECT                                                   Y02947
         LR    RDEB,RPARM1              PLACE DEB ADDRESS IN DEB REG
         SR    RPARM1,RPARM1            CLEAR REG               YA02170
         B     CLEARS
CLEARDEB STC   RPARM1,0(RDEB,RW3)       ZERO DEB
CLEARS   BCT   RW3,CLEARDEB             DECREMENT AND TEST COUNT
         STC   RPARM1,0(RDEB)
         STC   RW1,DEBLNGTH             STORE DEB SIZE (IN DBL WORDS)
         SRL   RUCB,2
         STC   RUCB,DEBNMEXT            STORE UCB COUNT
         ST    RSAVE,DEBTCBAD-1         STORE TCB ADDRESS
         ST    RDEB,DEBAPPAD-1          STORE DEB ADDRESS
         MVI   DEBEXSCL,X'02'           CODE AS NOT DIRECT ACCESS DEVIC
         STC   RW2,DEBAMLNG             STORE ACCESS METHOD SIZE IN DEB
         SPACE 1                                                 Y02947
         MODESET EXTKEY=ZERO            KEY OF ZERO              Y02947
         SPACE 1                                                 Y02947
         LA    RW3,DEBTCBAD-1           LOAD DEB ADDRESS         Y02947
         L     RW2,TCBDEB(RSAVE)        ADDR OF 1ST DEB ON TCB   Y02947
CHAIN    STCM  RW2,SEVEN,DEBDEBAD       CHAIN TO OUR DEB         Y02947
         CS    RW2,RW3,TCBDEB(RSAVE)    PUT OUR DEB AT TOP OF    Y02947
         BNE   CHAIN                       CHAIN OR PICK UP NEW  Y02947
*                                          TOP IF CHANGED        Y02947
         SPACE 1                                                 Y02947
         MODESET EXTKEY=DATAMGT         DATAMGT KEY              Y02947
         SPACE 1                                                 Y02947
         L     RUCB,DCBDEBAD           GET COUNT OF DD'S         A26886
         ST    RW3,DCBDEBAD             STORE DEB ADDRESS IN     Y02947
*                                       COPIED DCB               Y02947
         TM    DCBERROP,ERP             TEST FOR ERROR RECOVERY PROCED.
         BZ    SETERP                   IF ZERO, ERP REQUESTED
         MVI   DCBIFLGS,X'0C'           IF ONE, SET NO ERP BITS
         SPACE 1                                                 Y02947
SETERP   EQU  *                                                  Y02947
*         THE USER'S DCB ADDRESS RATHER THAN THE ADDRESS OF THE  Y02947
*         COPIED DCB MUST BE PLACED IN THE DEB FOR THE DEBCHK    Y02947
*         MACRO                                                  Y02947
         L     RW1,4(RWTGC)             ADDR OF OPEN WORK AREA   Y02947
         USING WORKAREA,RW1             WORKAREA ADDRESSABILITY  Y02947
         MVC   DEBDCBMK(4),DXDEBXAD     ADDR OF DEB XTENTION   @ZA17697
         SPACE 1                                                 Y02947
         MODESET KEYADDR=DXUKEY,WORKREG=15    USER'S KEY         Y02947
         SPACE 1                                                 Y02947
         L     RW1,DXUDCBAD             GET ADDR OF USER'S DCB   Y02947
         DROP  RW1                      DROP WORKAREA ADDRESS    Y02947
         DROP  RDCB                     DROP COPY DCB ADDRESS    Y02947
         USING IHADCB,RW1               USER DCB ADDRESSABILITY  Y02947
         ST    RW3,DCBDEBAD             PLACE DEB ADDRESS IN     Y02947
*                                       USER DCB                 Y02947
         DROP  RW1                      DROP USER DCB ADDRESS    Y02947
         USING IHADCB,RDCB              COPY DCB ADDRESSABILITY  Y02947
         SPACE 1                                                 Y02947
         MODESET EXTKEY=DATAMGT         DATAMGT KEY              Y02947
         SPACE 1                                                 Y02947
         ST    RW1,DEBDCBAD-1           PLACE USER'S DCB         Y02947
*                                       ADDRESS IN DEB           Y02947
         MVC   DEBPROTG(1),TCBPKF(RSAVE) MOVE PROTECTION KEY TO DEB
         OI    DEBPROTG,X'0F'           OR IN DEB ID
         DEBCHK (RW1),AM=TAM,TYPE=ADD  ADD DEB TO SYSTEM TABLE   Y20947
         EJECT                                                   Y02947
         L     RPARM1,ABCODE            LOAD ABEND CODE         YA02170
         LA    RW3,DEBUCBAD             LOAD UCB ADDRESS
         SR    RW1,RW1                  RESET WORK REGISTER
UCBMOVE  IC    RW1,0(RTIOT)             LOAD UCB SIZE
         SH    RW1,SEVENTEN             SUBTRACT 16
         EX    RW1,UCBMVEX              MOVE UCB FROM TIOT TO DEB
         LA    RW3,1(RW1,RW3)           STEP ADDRESS
         LA    RTIOT,17(RW1,RTIOT)      STEP ADDRESS
         BCT   RUCB,UCBMOVE             DECREMENT COUNT OF UCB'S
         L     RUCB,DEBUCBAD            LOAD UCB ADDRESS
         USING CVT,RW1                  CVT ADDRESSABILITY       Y02947
         L     RW1,SIXT                 LOAD ADRS OF CVT         Y02947
         L     RW1,CVTXAPG          LOAD ADRS OF IOS RETURN ADRS Y02947
         DROP RW1                       DROP CVT ADDRESS         Y02947
         MVC   DEBEOEA(4),0(RW1)        MOVE IOS RETURN ADRS INTO DEB
         MVC   DEBSIOA(16),DEBEOEA      FOR EOEA, SIO, PCI, CE, & XCE
         CLI   UCBDVCLS(RUCB),GRAPHICS  GRAPHICS DEVICE CLASS IN UCB
         BE    ANRLOAD2            YES, GO TO SPECIAL LOAD FOR 3270
         LA    RW1,4                    SET 1ST WORK REG TO VALUE 4
         SR    RW2,RW2                  RESET REGISTER
         IC    RW2,DEBNMEXT             LOAD NO OF UCB'S
         BCTR  RW2,0                    DECREMENT BY 1
         SLL   RW2,2                    MULTIPLY BY 4, RESULT COUNT-1X4
         SR    RW3,RW3                  RESET WORK REGISTER
NEXTUCB  BXH   RW3,RW1,TESTTCU          INCREMENT & TEST COUNT
         MVC   0(4,RWTGC),16(RUCB)      MOVE 1ST UCB TO WORK AREA
         L     RSIZE,DEBUCBAD(RW3)      LOAD ADRS OF 2ND-NTH UCB
         XC    0(4,RWTGC),16(RSIZE)     X-OR 2ND-NTH UCB TO 1ST
         NC    0(4,RWTGC),HOLDMASK      AND OF VARIABLE BITS
         BNZ   AB5                      IF RESULT NON ZERO ERROR EXISTS
         B     NEXTUCB                  GO CHECK NEXT UCB
         SPACE 4
TESTTCU  EQU   *
         SR    RW1,RW1
         SR    RW2,RW2
         SR    RW3,RW3
         SR    RPARM0,RPARM0
         SPACE
         CLI   18(RUCB),TPDEVCCD        IS UCB FOR TELECOMMUNICATIONS
         BNE   AB0                      IF NOT, ABEND
         TM    19(RUCB),X'03'           TEST FOR VALID XMSN CTRL UNIT
         BZ    AB1                      IF NOT ABEND
         MVZ   DCBHOLD1(1),19(RUCB)     GET ADAPTER TYPE
         MVN   DCBHOLD1(1),16(RUCB)          MODEL TYPE
         IC    RW2,17(RUCB)             GET SPECIAL FEATURES
         SRL   RW2,2
         STC   RW2,DCBHOLD2
         SPACE 1
         LA    RW1,TYPETAB
         LA    RW2,SPFETAB
         B     SCAN1
         SPACE
SCAN     LA    RW3,1(RW3)
         LA    RW1,1(RW1)
         LA    RW2,1(RW2)
         SPACE 1
SCAN1    CLC   0(1,RW1),DCBHOLD1        SCAN FIRST FOR DEVICE TYPE
         BL    SCAN
         BH    ERROR
         SPACE
         LH    RPARM0,ABINDEX
         CLC   0(1,RW2),DCBHOLD2            THEN FOR FEATURES
         BNE   SCAN
         SPACE 4
ENDLOD1  STC   RW3,DCBDEVTP
         MVC   0(5,RWTGC),BTAMLOD2      MOVE BTAM LOAD 2 ID
         B     BYPASS1
         SPACE 1
DUPDCB   MVC   0(2,RWTGC),ZERO          NO FURTHER OPEN PROCESSING
         B     BYPASS1
ANRLOAD2 MVC   0(IDTTRSZ,RWTGC),ANRLD2ID  MOVE ID OF OPEN LOAD 2
*                                  FOR 3270 INTO WTG TABLE
         B     BYPASS1             GO TO END-OF-LOAD PROCESSING
*
         SPACE
BYPASS   MVC   0(5,RWTGC),STRAMID
BYPASS1  L     RSIZE,4(RWTGC)           LOAD ADDRESS OF WORK AREA
RELOOP   LA    RWTGC,WGOFF(RWTGC)       INCREMENT REGISTER TO    Y02947
*                                       NEXT WTG ENTRY           Y02947
         LA    RPARC,PLOFF(RPARC)       INCREMENT REGISTER TO    Y02947
*                                       NEXT COPIED DCB ADDRESS  Y02947
         CLC   0(2,RWTGC),AMIDCNST      IS THIS OPEN TO BE CALLED NEXT
         BE    0(RBASE)                 IF SO, RETURN TO BEGINNING
         CLC   0(2,RWTGC),OPIDCNST      TEST FOR END OF TABLE
         BNE   RELOOP                   IF NOT END, LOOP
         LR    RPARC,RPAR               RESTORE REGISTERS
         LA    RWTGC,WAOFF(RWTG)
ZCHECK   CLI   0(RWTGC),X'00'           IS IT ZERO
         BNE   XCTLRTNE                 IF NOT, GO EXIT
         LA    RWTGC,WGOFF(RWTGC)       INCREMENT REGISTER TO    Y02947
*                                       NEXT WTG ENTRY           Y02947
         LA    RPARC,PLOFF(RPARC)       INCREMENT REGISTER TO    Y02947
*                                       NEXT COPIED DCB ADDRESS  Y02947
         B     ZCHECK
XCTLRTNE EQU   *
         LR    RW1,RSIZE
         USING WORKAREA,RW1             WORKAREA ADDRESSABILITY  Y02947
         LA    RSIZE,DXCCW12 POINT TO DOUBLE WORK LIST
         DROP  RW1                      DROP WORKAREA ADDRESS    Y02947
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID TO NAME FIELD.
         MVC   14(3,RWTG),2(RWTGC)      MOVE TTR TO WTG TABLE.
         L     RW1,SIXT                 CVT ADDRESS              Y02947
         USING CVT,RW1                  CVT ADDRESSABILITY       Y02947
         L     RW1,CVTSVDCB             SYS1.SVCLIB DCB          Y02947
         DROP  RW1                      DROP CVT ADDRESSABILITY  Y02947
         XCTL  DE=(RWTG),SF=(E,(15)),DCB=(RW1)                   Y02947
         SPACE 4
AB5      AH    RPARM1,ABINDEX           ADD ONE TO ABEND CODE
AB4      AH    RPARM1,ABINDEX           ADD ONE TO ABEND CODE
AB3      AH    RPARM1,ABINDEX           ADD ONE TO ABEND CODE
AB2      AH    RPARM1,ABINDEX           ADD ONE TO ABEND CODE
AB1      AH    RPARM1,ABINDEX           ADD ONE TO ABEND CODE
AB0      ABEND (1)
         SPACE
*                                 ABEND S093/94
ERROR    AR    RPARM1,RPARM0                                     A35728
         B     AB3                                               A35728
         DS    0F
         SPACE
         EJECT
BLNK     EQU   X'40'                    EBCDIC BLANK CHARACTER
CURNTTCB EQU   4                        OFFSET CURRENT TASK CONTROL BLK
SIXT     EQU   16                       OFFSET OF CVT            Y02947
DYNBUF   EQU   X'08'                    DYNAMIC BUFFERING BIT
ERP      EQU   X'01'                    ERP MASK
GRAPHICS EQU   X'10'               DEVICE CLASS FOR 3270 DEVICES
IDTTRSZ  EQU   5                   LENGTH OF IDTTR
PLOFF    EQU   4                        OFFSET OF DCB ENTRIES
STR      EQU   X'FF'
TCBPKF   EQU   28
TCBTIO   EQU   12
TPDEVCCD EQU   X'40'
UCBDVCLS EQU   18                  DISP TO DEVICE CLASS FIELD IN UCB
WAOFF    EQU   32                       OFFSET OF FIRST WTG ENTRY FROM
WGOFF    EQU   8                        OFFSET OF WTG ENTRIES
OFF1C4   EQU   X'1C4'                   DCB PROCESSED BIT OFFSET  M4840
ON       EQU   X'80'                    DCB PROCESSED BIT         M4840
TCBDEB   EQU  8                         DISPLACEMENT INTO TCB    Y02947
*                                       FOR DEB ADDRESS          Y02947
SEVEN    EQU  7                         MASK FOR STCM            Y02947
*                                       INSTR (3 BYTES)          Y02947
         EJECT
TYPETAB  EQU   *              NOTE: THIS TABLE IS IN PARALLEL WITH
*                                       SPFETAB AND OFFSETAB
         DC    5X'11'         1050
         DC    2X'12'         1060
         DC    4X'13'         2740C
         DC   22X'14'         2740
         DC    2X'15'         2741P
         DC    3X'16'         2741C                            @ZA10016
         DC    4X'17'         1050X
         DC    4X'18'         2740X
         DC    1X'19'        2740B                                 00MC
         DC    2X'21'         1030
         DC    5X'31'         1050
         DC    4X'32'         1050X
         DC    1X'41'         83B3
         DC    1X'42'         115A
         DC    4X'51'         TWX                              @ZA26035
         DC    1X'61'         WTTA
         DC    1X'81'         2848
         DC    4X'95'         BSC1
         DC   12X'96'         BSC2
         DC    4X'97'         BSC3
         DC    X'FF'     END OF TYPETAB
         EJECT
SPFETAB  EQU   *              NOTE: THIS TABLE IS IN PARALLEL WITH
*                                       TYPETAB AND OFFSETAB
         SPACE
*                **               N/A   (6 BITS ONLY)
*                  *          D   AUTO DIAL
*                   *         P   AUTO POLL
*                    *        C   CHECKING
*                     *       A   AUTO ANSWER
*                      *      S   STATION CONTROL  (2740)
*                       *     T   TRANSMIT CONTROL (2740)
         DC    B'00000000'         1050
         DC    B'00000100'
         DC    B'00010000'
         DC    B'00100000'
         DC    B'00100100'
         DC    B'00000000'         1060
         DC    B'00010000'
         SPACE
         DC    B'00001000'         2740C
         DC    B'00001100'
         DC    B'00101000'
         DC    B'00101100'
         DC    B'00000000'         2740
         DC    B'00000010'
         DC    B'00000100'
         DC    B'00000101'
         DC    B'00001000'
         DC    B'00001010'
         DC    B'00001011'
         DC    B'00001100'
         DC    B'00001101'
         DC    B'00001111'
         DC    B'00010010'
         DC    B'00011010'
         DC    B'00100000'
         DC    B'00100001'
         DC    B'00100100'
         DC    B'00100101'
         DC    B'00101000'
         DC    B'00101001'
         DC    B'00101011'
         DC    B'00101100'
         DC    B'00101101'
         DC    B'00101111'
         SPACE
         DC    B'00000000'         2741'S (2)-PTTC
         DC    B'00000100'
         DC    B'00000000'         2741'S (2)-CORRS
         DC    B'00000010'         2741 AUTO ANSWER            @ZA04858
         DC    B'00000100'
         DC    B'00000000'         1050X
         DC    B'00000100'
         DC    B'00100000'
         DC    B'00100100'
         DC    B'00001000'         2740X
         DC    B'00001100'
         DC    B'00101000'
         DC    B'00101100'
         DC    B'00001000'                                         00MC
         DC    B'00000000'         1030'S (2)
         DC    B'00010000'
         DC    B'00000000'         1050'S (5)-IBM TELL
         DC    B'00010000'
         DC    B'00100100'
         DC    B'00000100'
         DC    B'00100000'
         DC    B'00000000'         1050X
         DC    B'00000100'
         DC    B'00100000'
         DC    B'00100100'
         DC    B'00000000'         83B3'S (1)
         DC    B'00000000'         115A'S (1)
         DC    B'00000000'         TWX'S  (4)                  @ZA26035
         DC    B'00100100'
         DC    B'00000100'
         DC    B'00100000'
         DC    B'00000000'         WTTA
         DC    B'00000000'         2848'S (1)
         SPACE
*                **               N/A   (6 BITS ONLY)
*                  *              AUTO DIAL   (BSC2)
*                   *             AUTO POLL   (BSC3)
*                    *            DUAL INTERFACE
*                     *           AUTO ANSWER (BSC2)
*                      *          NOT USED = 00
*                       *         DUAL CODE
         DC    B'00000000'         BSC1
         DC    B'00000001'
         DC    B'00001000'
         DC    B'00001001'
         DC    B'00000100'         BSC2
         DC    B'00000101'
         DC    B'00001100'
         DC    B'00001101'
         DC    B'00100000'
         DC    B'00100001'
         DC    B'00100100'
         DC    B'00100101'
         DC    B'00101000'
         DC    B'00101001'
         DC    B'00101100'
         DC    B'00101101'
         DC    B'00010000'         BSC3
         DC    B'00010001'
         DC    B'00011000'
         DC    B'00011001'
         SPACE 2
         SPACE 2
ABCODE   DC    0F'0',X'80090000'
DEBPOOL  DC    0F'0',X'E6000000'        SUBPOOL NUMBER 230       Y02947
ZERO     DC    X'0000'
SIXTEEN  DC    H'16'
SEVENTEN DC    H'17'
ABINDEX  DC    X'1000'
         SPACE 2
HOLDMASK DS    0F
         DC    BL.8'11111111'   0-3 IOSFLAGS, 4-7 MODEL CODE
         DC    BL.8'11111100'   8-13 FEATURES, 14-15 SAD COMMAND
         DC    BL.8'11111111'   16-23 DEVICE CLASS,
         DC    BL.8'11110000'   24-27 ADAPTER TYPE,
*                                    28-31 CONTROL UNIT
OPIDCNST DC    C'0S'                    ID OF LAST OPEN LOAD.
AMIDCNST DC    C'3M'                    ID OF THIS MODULE.
UCBMVEX  MVC   0(0,RW3),16(RTIOT)       UCB MOVE INSTRUCTION
         SPACE 4
BTAMLOD2 DC    C'4N'                   ID FOR SECOND OPEN LOAD MODULE
         DC    X'00000000'             TTR AND L
STRAMID  DC    C'3W'
         DC    X'00000000'
ANRLD2ID DC    C'4P',XL4'0'        IDTTR OFR OPEN LOAD 2 FOR 3270
         EJECT
         DCBD  DSORG=BX
         SPACE 2
DCBHOLD1 EQU   DCBREAD+2
DCBHOLD2 EQU   DCBHOLD1+1
         EJECT
        IECTDEBX
         EJECT                                                   Y02947
WORKAREA DSECT
         IECDSECT
         EJECT                                                   Y02947
CVT      DSECT                                                   Y02947
         CVT   LIST=NO                                           Y02947
         END
