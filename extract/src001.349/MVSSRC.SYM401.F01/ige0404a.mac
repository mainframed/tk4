         TITLE 'IGE0404A - BTAM ERP LOST DATA MODULE'
IGE0404A CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0404A                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM LOST DATA ERP                                               *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    IF REQUIRED, RETRIES CHANNEL PROGRAM THAT FAILED BECAUSE OF A    *
*    LOST DATA CONDITION.                                             *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (48 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        458 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD   *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0404A                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        SET DECERRST FOR SHOULD NOT OCCUR ERROR.                     *
*        ALTERED CHANNEL PROGRAM                                      *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        NONE                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YM05652K (1/24/74)                                               *
*    AZ04683 (08/26/75)
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         L     UCBREG,IOSUCB       BASE UCB
         DROP  ERREG5
         L     CCWREG,IOBFL3       ADDRESS OF CCW
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       ADJUST TO FAILING CCW
         MVC   IOBWORK(L1),TPCODE(CCWREG)   PUT TP OP CODE IN WORK
         NI    IOBWORK,L255-CCWMASK1   MASK OUT UNUSED BITS
         CLI   ZERO(CCWREG),CCWDIAL   THIS A DIAL COMMAND
         BNE   ERRLDC02            NO CHECK FOR READ
ERRLDC00 CLI   IOBERRCT,ERTRYCT    RETRY AGAIN
         BNL   ERRLDC04            NO CHECK FOR SWITCHED LINE
         IC    ERREG1,IOBERRCT     PICK UP RETRY COUNT
         LA    ERREG1,L1(ERREG1)   INCREMENT RETRY COUNT
ERRLDC01 STC   ERREG1,IOBERRCT     RETRY CHANNEL PROG
         MVI   IOBSENS0,ZERO       CLEAR SENSE BYTE
RETRY    EQU   *
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
         L     NAMEREG,TRANSRTN    LOAD NAME
         B     ERRLINK1            XCTL
ERRLDC02 CLI   ZERO(CCWREG),CCWRDCMD   THIS A READ COMMAND
         BE    ERRLDC03            YES CHECK IF READ TWX
         CLI   ZERO(CCWREG),CCWINHCD   THIS AN INHIBIT COMMAND
         BNE   ERRLDC05            NO INDICATE SHOULD NOT OCCUR
ERRLDC03 CLI   IOBWORK,CCWTWXID    THIS A READ TWX ID RESPONSE
         BE    ERRLDC00            YES CHECK FOR RETRY REQUIRED
         CLI   IOBWORK,CCWRRADR    THIS A READ RESPONSE TO ADDRESS
         BE    ERRLDC06            YES SET UP TO RETRY CHAN PROG
         MVC   IOBERINF(L1),IOBSENS0   SAVE PRIOR SENSE DATA
         MVC   IOBERINF+L1(L7),IOBCSW   SAVE PRIOR CSW DATA
         MVC   IOBERINF+L8(L8),ZERO(CCWREG)   SAVE FAILING CCW
         LA    ERREG1,IOBERCCW     ADDRESS OF DUMMY CCW
         ST    ERREG1,IOBSTART     POINTER TO CHAN PROGRAM
         CLI   IOBWORK,CCWRDTXT    THIS A READ TEXT
         BE    ERRLDC10            YES CHECK RESIDUAL COUNT
         CLI   IOBWORK,CCWRRTXT    THIS A READ RESPONSE TO TEXT
         BE    ERRLDC11            YES SET UP READ SKIP OPERATION
         CLI   IOBWORK,CCWRDSKP    THIS A READ SKIP
         BNE   ERRLDC05            INDICATE SHOULD NOT OCCUR
ERRLDC10 CLC   IOBCSW+L5(L2),ERRHLF00   RESIDUAL COUNT = 0
         BNE   ERRLDC05            NO INDICATE SHOULD NOT OCCUR
         MVC   IOBWORK+L1(L1),UCBTYP+L3(UCBREG)   ADAPTER TYPE TO WORK
         NI    IOBWORK+L1,MASK3    CLEAR UNWANTED BITS
         CLI   IOBWORK+L1,UCBTTYP1   THIS A TELETYPE ADAPTER 1
         BE    ERRLDC12            YES WRITE BREAK REQUIRED
         CLI   IOBWORK,CCWRDSKP    THIS A READ SKIP
         BE    ERRLDC04            YES, CHECK FOR SWITCH LINE
         CLI   IOBWORK+L1,ADAPT8   THIS A 2260
         BE    ERRLDC14            YES, ISSUE READ CLEAR
ERRLDC11 SR    ERREG1,ERREG1       CLEAR REGISTER
         ST    ERREG1,IOBERCCW     CLEAR DUMMY CCW
         MVI   IOBERCCW,CCWRDCMD   PUT READ COMMAND IN CCW
         MVI   IOBERCCW+L4,RDSKPIND   PUT SKIP INDICATOR IN CCW
         LA    ERREG5,L1           GET A ONE
         SR    ERREG1,ERREG5       GET READ COUNT
         STH   ERREG1,IOBERCCW+L6   PUT READ COUNT IN CCW
         MVI   IOBERCCW+L5,X30     SET TPOP CODE
         B     ERRLDC13            EXIT VIA ERROR EXCP ROUTINE
ERRLDC14 EQU   *
         MVC   IOBERCCW(L8),RDCL2848   SET UP RD CLEAR
         B     ERRLDC13            COMPLETE CHANNEL PROGRAM
ERRLDC12 ST    IOBRG,IOBERCCW      GENERATE LEGAL DATA ADDRESS
         MVI   IOBERCCW,WRTBKCMD   PUT WRITE BREAK COMMAND IN CCW
         LA    ERREG1,MASK15       BREAK COUNT
         ST    ERREG1,IOBERCCW+L4   PUT IN CCW AND CLEAR REST
         MVI   IOBERCCW+L4,SILLYBIT   SET SUPPRESS LENGTH INDICATOR
ERRLDC13 MVI   IOBERRCT+L1,ERRLDIND   INDICATE LOST DATA RETURN
         MVI   IOBSENS0,ZERO       CLEAR PREVIOUS SENSE DATA
         OC    EWASTUP(L1),IOBERINF   MOVE SENSE FOR STAT UPDATE
         B     RETRY               ERROR EXCP
ERRLDC04 TM    UCBTYP+L1(UCBREG),SWLINE   DISABLE REQUIRED
         BZ    ERRLDC09            NO GO TO ERROR POST
         MVI   IOBERRCT+L1,MASK3   INDICATE SPECIAL ENTRY
         LA    NAMEREG,ERLOD3      DISABLE CHECK ROUTINE ADDRESS
ERRLINK1 EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE
         DROP  ERRETR
         BR    ERRETR              XCTL
ERRLDC05 OI    DECERRST(DECBRG),CSWSNO   SET SHOULD NOT OCCUR ERROR
         NI    IOSFLA,L255-IOSERR  TURN OFF ERP IN CONTROL     @ZA04683
*                                      IN IOSB                 @ZA04683
         NI    IOBFLAG1,L255-IOBERRTN    ALSO IN IOB           @ZA04683
         B     ERRLDC04            CHECK FOR DISABLE REQUIRED
ERRLDC06 CLI   IOBCPA,CCWDISAB     FIRST CCW DISABLE COMMAND
         BE    ERRLDC08            YES SET CPA TO THIRD COMMAND
         LA    ERREG1,IOBCPA       SET CPA TO START OF CHAN PROG
ERRLDC07 ST    ERREG1,IOBSTART     STORE CPA POINTER
         B     ERRLDC00            CHECK FOR RETRY REQUIRED
ERRLDC08 LA    ERREG1,IOBCMD3      ADDRESS OF THIRD COMMAND
         B     ERRLDC07            CHECK FOR RETRY REQUIRED
ERRLDC09 LH    NAMEREG,ERLOD5      ADDRESS OF ERROR POST ROUTINE
         B     ERRLINK1
         EJECT
**********
*
*        CONSTANTS
*
**********
         DS    0D
RDCL2848 DC    X'42000001303003C0'     READ CLEAR
TRANSRTN DC    F'9042'             NAME OF BTAM TRANSLATE ROUTINE
ERRHLF00 DC    H'00'
ERRCN8   DC    H'8'
ERLOD5   DC    H'5041'
PACHAREA DC    12C'404A'           PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
ADAPT8   EQU   X'80'
CCWRDTXT EQU   X'11'
CCWRRTXT EQU   X'20'
CCWRDSKP EQU   X'12'
UCBTTYP1 EQU   X'40'
WRTBKCMD EQU   X'0D'
ERRLDIND EQU   X'08'
MASK15   EQU   X'0F'
UCBTYP   EQU   16
SWLINE   EQU   X'90'
CCWDIAL  EQU   X'29'
CCWDISAB EQU   X'2F'
CCWMASK1 EQU   X'C0'
CCWRDCMD EQU   X'02'
CCWRRADR EQU   X'06'
CCWINHCD EQU   X'0A'
TPCODE   EQU   5
RDSKPIND EQU   X'30'
SILLYBIT EQU   X'20'
ERTRYCT  EQU   X'02'
MASK3    EQU   X'F0'
ERLOD3   EQU   3041
L255     EQU   255                 AND MASK
ZERO     EQU   X'00'               LENGTH AND DISPLACEMENT
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L16      EQU   16                  LENGTH AND DISPLACEMENT
X30      EQU   X'30'               TP OP CODE
DECERRST EQU   29
CCWTWXID EQU   X'07'
CSWSNO   EQU   X'40'
**********
*
*        REGISTER DEFINITION
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
ERREG2   EQU   2                   WORK REG
EWAREG   EQU   3                   ERP WORKAREA BASE
CCWREG   EQU   4                   CCW ADDRESS REG
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE
BASEREG  EQU   8                   PROGRAM BASE
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG4   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  LINK REG/RETURN REG
ERREG5   EQU   15                  LINK REG/WORK REG
         EJECT
         IECDRQE
         EJECT
         IEZIOB
IOBCMD3  EQU   IOBCPA+L16
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
