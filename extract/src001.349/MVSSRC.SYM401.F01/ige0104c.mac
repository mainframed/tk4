         TITLE 'IGE0104C BSC DATA CHECK ERP ROUTINE'
IGE0104C CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0104C                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM BSC DATA CHECK ERP                                          *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS MODULE IS ENTERED FOR DATA CHECK ERRORS. IT DETERMINES      *
*    WHEN THE DATA CHECK OCCURRED AND THEN ATTEMPTS RECOVERY.         *
*    ALL READ TYPE CCW'S WILL RESULT IN A RECOVERY ATTEMPT EXCEPT     *
*    FOR A READ DURING TEXT TRANSFER WITH DYNAMIC BUFFERING.          *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (48 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        458 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD.  *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0104C                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        SETS INDICATOR IN DECERRST FOR FAILING LINE                  *
*        ALTERED CHANNEL PROGRAM                                      *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        NONE                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YA02436  (11/5/73)                                               *
*    YM05652K (1/25/74)                                               *
*    YA03249  (2/15/74)                                               *
*    YM07710  (4/2/74)                                                *
*    ZA00504  (5/9/74)                                                *
*    ZA14139  (10/28/76)                                              *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         B     BEGIN               BRANCH AROUND ID            @ZA00504
         DC    C' IGE0104C '       MODULE ID                   @ZA00504
         DC    C' 4129 '           DATE LAST CHANGE            @ZA00504
         DC    CL8'&SYSDATE'       DATE LAST ASSEMBLY          @ZA00504
         USING IECTDECB,DECBRG
         USING IHADCB,DCBREG
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         DS    0H
BEGIN    EQU   *
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         L     UCBREG,IOSUCB       BASE UCB
         DROP  ERREG5
         L     CCWREG,IOBFL3       POINT TO CSW IN IOB
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8          FAILING CCW IN IOB
         MVC   IOBWORK(L1),TPOPCODE(CCWREG)   INITIALIZE WORK AREA
         NI    IOBWORK,L255-HIBIT   CLEAR UNUSED BITS
         CLI   ZERO(CCWREG),CCWREAD   ERROR ON A READ
         BNE   BSCDCSNO            NO GO TO SNO ROUTINE
         TM    IOBWORK,TPOPPTXT    ERROR PRIOR TO TEXT
         BZ    BSCDC03             YES CHECK FURTHER
         TM    IOBWORK,TPOPDTXT    ERROR DURING TEXT
         BO    BSCDC04             YES CHECK BUFFER TECHNIQUE
         TM    IOBWORK,TPOPATXT    ERROR AFTER TEXT
         BO    BSCDC07             YES GO TO WRT ENQ ROUTINE
BSCDCSNO OI    DECERRST,DECSNO     INDICATE SNO
BSCDC00  MVI   IOBERRCT,ERRMAXCT   SET FOR NO RETRIES
BSCDC01  TM    DCBERROP,DCBLERBI   LERB REQUIRED
         BNO   BSCDC02             NO CHECK RETRIES
         MVI   IOBINCAM+L1,LERBDCID   NDICATE LERB DATA CHECK
BSCDC02  CLI   IOBERRCT,ERRMAXCT   RETRY COUNT AT MAX
         BNL   BSCDC08             YES GO TO PERM ERROR
         IC    ERREG1,IOBERRCT     ADJUST RETRY COUNT
         LA    ERREG1,L1(ERREG1)   FOR NEXT RETRY ATTEMPT
         STC   ERREG1,IOBERRCT     STORE IT BACK
         CLI   IOBINCAM+L1,LERBDCID   IS LERB REQUIRED          YA02436
         BE    BSCDC10             YES, GO TO LERB ROUTINE      YA02436
         MVI   EWAIND1,EWAISVCF    SVC 15/SVC 3                YM05652K
         L     NAMEREG,TRANSRTN    LOAD NAME
         B     XCTLOUT             XCTL
BSCDC03  CLI   IOBWORK,TPOPRENQ    READ ENQ
         BE    BSCDC06             YES SET TO RETRY
         CLI   IOBWORK,TPREADID    IS THIS READ ID CCW
         BE    BSCDC0A             YES, CHECK FURTHER
         CLI   IOBWORK,TPOPRREN    READ RESP ENQ
         BNE   BSCDCSNO            NO INDICATE SNO ERROR
         SH    CCWREG,ERRCN8       SET CCWREG FOR RETRY
         B     BSCDC06             GO TO RETRY
BSCDC0A  SH    CCWREG,ERRCN8       BACK UP ONE CCW
         CLI   L5(CCWREG),TPWRITID   THIS WIRTE ID CCW
         BE    BSCDC06            YES, RESTART WITH THIS CCW
         LA    CCWREG,L8(CCWREG)   BUMP BY ONE CCW
         B     BSCDC06            RESTART WITH READ ID CCW
BSCDC04  TM    DCBBFTEK,DYNBUF    DYNAMIC BUFFERING USED
         BO    BSCDC00            YES GO TO PERM ERROR ROUTINE
         CLI   IOBCPA,CCWAPOLL    THIS A MULTIPOINT READ INITIAL
         BNE   BSCDC05            NO CONTINUE FLOW
         LR    ERREG1,CCWREG      POINT TO CURRENT CCW
         SH    ERREG1,ERRCN8      BACK UP ONE
         L     ERREG1,ZERO(ERREG1)   GET AREA ADDRESS
         LA    ERREG1,L1(ERREG1)   BUMP BY ONE
         CLM   ERREG1,L7,L1(CCWREG)   HAS COUNT BEEN ADJUSTED  @ZA14139
         BE    BSCDC05             YES, CONTINUE
         STCM  ERREG1,L7,L1(CCWREG)   NO, RESTORE READ         @ZA00504
         LH    ERREG1,L6(CCWREG)   PICK UP COUNT
         LA    ERREG1,L1(ERREG1)   BUMP BY ONE
         STH   ERREG1,L6(CCWREG)   STORE
BSCDC05  TM    DCBXCODE,ITBMODE    ITB MODE
         BO    BSCDC11             YES, CHECK RESID COUNT
BSCDC12  LA    ERREG1,DCBBSNAK     ADDRESS OF NAK CHAR
         MVI   IOBWORK+L1,RTRNCDE2   SET SPECIAL RETURN BRA.  LD YM7710
         MVI   IOBERRCT+L1,RTRNSPCD   SET SPECIAL RETURN CODE LD YM7710
         MVI   IOBERCCW+L7,NAKCOUNT   SET COUNT FOR NAK
BSCDC051 SH    CCWREG,ERRCN8       BACK UP ONE CCW
         CLI   L5(CCWREG),TPRDRESP   IS THIS READ RESPONSE
         BE    BSCDC052            YES. RESTART WITH THIS CCW
         LA    CCWREG,L8(CCWREG)   NO, RESTART WITH TEXT CCW
BSCDC052 ST    CCWREG,IOBERCCW+L8   GENERATE TIC COMMAND
         ST    ERREG1,IOBERCCW     SET UP DATA FIELD ADDR
         MVI   IOBERCCW,CCWWRITE   MOVE IN WRITE COMMAND
         MVI   IOBERCCW+L4,CMDCHAIN   SET UP FOR COMMAND CHAINING
         MVI   IOBERCCW+L8,CCWTIC   MOVE IN TIC COMMAND
         LA    CCWREG,IOBERCCW     SET UP CHAN START ADDRESS
BSCDC06  ST    CCWREG,IOBSTART     STORE IN RESTART ADDRESS
         B     BSCDC01             CHECK RETRY COUNT
BSCDC07  LA    ERREG1,DCBBSENQ     ADDRESS OF ENQ CHAR
         ST    ERREG1,IOBERCCW     GENERATE CHAN PROG
         MVI   IOBERCCW+L7,ENQCOUNT   SET UP COUNT
         B     BSCDC051            CONTINUE GENERATION
BSCDC08  NI    IOBFLAG1,L255-IOBERRTN  INDICATE PERMANENT ERROR
         NI    IOSFLA,L255-IOSERR   ALSO IN IOSB
         CLI   IOBINCAM+L1,LERBDCID   LERB REQUIRED             YA02436
         BE    BSCDC10             YES EXIT VIA LERB            YA02436
         LA    NAMEREG,ERLOD25     ADDRESS OF ERROR POST
XCTLOUT  EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE
         DROP  ERRETR
         BR    ERRETR              EXIT VIA XCTL ROUTINE
BSCDC10  LA    NAMEREG,ERLOD12     ADDRESS OF LERB ROUTINE
         B     XCTLOUT             GO TO EXIT ROUTINE
BSCDC11  LH    UCBREG,IOBCSW+L5    GET RESIDUAL COUNT
         LA    UCBREG,L1(UCBREG)   ADD ONE
         CH    UCBREG,L6(CCWREG)   MORE THAN ONE CHARACTER
         BL    BSCDC00             YES, PERMANENT ERROR
         B     BSCDC12
         EJECT
**********
*
*        CONSTANTS
*
**********
TRANSRTN DC    F'9042'             NAME OF BTAM TRANSLATE ROUTINE
ERRCN8   DC    H'8'
PACHAREA DC    12C'104C'           PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
TPREADID EQU   X'07'
TPWRITID EQU   X'03'
TPRDRESP EQU   X'25'
TPOPDTXT EQU   X'11'
LERBDCID EQU   X'01'
TPOPCODE EQU   5
L255     EQU   X'FF'               AND MASK
CCWREAD  EQU   X'02'
TPOPPTXT EQU   X'30'
HIBIT    EQU   X'C0'
TPOPATXT EQU   X'20'
DECSNO   EQU   X'40'
ERRMAXCT EQU   7
DCBLERBI EQU   X'08'
ZERO     EQU   X'00'               LENGTH AND DISPLACEMENT
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
TPOPRENQ EQU   X'0B'
TPOPRREN EQU   X'0C'
DYNBUF   EQU   X'08'
CCWAPOLL EQU   X'09'
CCWWRITE EQU   X'01'
CMDCHAIN EQU   X'60'
NAKCOUNT EQU   1
ITBMODE  EQU   X'40'
RTRNCDE2 EQU   X'82'                                            YA03249
RTRNSPCD EQU   X'14'               SPECIAL RTN CODE           LD YM7710
CCWTIC   EQU   X'08'
ENQCOUNT EQU   X'01'
ERLOD25  EQU   2043
ERLOD12  EQU   2042
**********
*
*        REGISTER DEFINITION
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
EWAREG   EQU   2                   ERP WORKAREA BASE
ERREG4   EQU   3                   WORK REG
CCWREG   EQU   4                   CCW ADDRESS REG
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE
BASEREG  EQU   8                   PROGRAM BASE
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  LINK REG
ERREG5   EQU   15                  LINK REG/WORK REG
         EJECT
         DCBD  DSORG=BX,DEVD=BS
         EJECT
         IECTDECB
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
