         TITLE 'IGE0304B - BTAM ERP UNIT EXCEPTION MODULE'
IGE0304B CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0304B                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM UNIT EXCEPTION ERP                                          *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS MODULE RETRIES IF REQUIRED THE CHANNEL PROGRAM THAT         *
*    FAILED DUE TO A UNIT EXCEPTION CHECK.                            *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (76 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        776 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD   *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0304B                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        SET DECERRST FOR SHOULD NOT OCCUR ERROR.                     *
*        ALTERED CHANNEL PROGRAM.                                     *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        SVC 35 WTO                                                   *
*        WRITE                                                        *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    WTO                                                              *
*    WRITE                                                            *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YA01250  (IN BASE)                                               *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YM05652K (1/24/74)                                               *
*    YA03259  (2/4/74)                                                *
*    AZ04871  (10/06/75)                                              *
*    AZ04886  (10/06/75)                                              *
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         L     UCBREG,IOSUCB       BASE UCB
         DROP  ERREG5
         L     CCWREG,IOBFL3       ADDRESS OF CCW
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       ADJUST TO FAILING CCW
         MVC   IOBWORK(L1),L5(CCWREG)   TP OP CODE TO WORK AREA
         NI    IOBWORK,L255-CCWMASK1   CLEAR UNUSED BITS
         MVC   IOBWORK+L1(L1),UCBTYP+L3(UCBREG)   ADAPT TYPE TO
*                                  WORK AREA
         NI    IOBWORK+L1,MASK3    CLEAR UNUSED BITS
         CLI   ZERO(CCWREG),CCWWRT   THIS A WRITE COMMAND
         BE    ERRUEE06            YES CHECK FOR ADAPTER TYPE
         CLI   ZERO(CCWREG),CCWAPOLL   THIS AUTOPOLL
         BE    ERRUEE06            YES TREAT AS READ
         CLI   IOBWORK,CCWRRTXT    THIS A READ RESPONSE TO TEXT
         BNE   ERRUEE01            NO INDICATE SHOULD NO OCCUR
         CLI   IOBWORK+L1,IBMADPT3   IBM TYPE 3 ADAPTER(2260R)?
         BE    ERRUEE02            YES, POST WITH ERROR
         CLI   IOBWORK+L1,IBMADPT1   IBM TYPE 1 ADAPTER?
         BNE   ERRUEE01            IF NOT, BRANCH.
         TM    UCBTYP(UCBREG),X09   IS IT 2740B
         BO    A2740               YES, TREAT AS 2740
         TM    UCBTYP(UCBREG),L255-MASK3-MODL2740   MODEL CODE
         BNZ   ERRUEE01            OTHER THAN 2740 IF SO, BRANCH.
A2740    EQU   *
         TM    UCBTYP+L1(UCBREG),SCONTROL+CHECKING   2740 WITH
*                                  STATION CONTROL AND CHECKING
*                                  FEATURES. IF SO, A 2740 MOD2
*                                  HAS DETECTED A BUFFER OVERFLOW
*                                  PRINTER OVERFLOW OR INCORRECT
*                                  CASE ERROR CONDITION
         BO    ERRUEE03            GO CHECK FOR WRITE RETRIES
ERRUEE01 CLI   IOBWORK,X06         RD RESP TO ADDR CCW
         BNE   ERRSNO              NO, SHOULD NOT OCCUR
         TM    UCBTYP+L3(UCBREG),XE0   THIS TYPE I ADAPTER
         BNZ   ERRSNO              NO, SHOULD NOT OCCUR
         TM    UCBTYP(UCBREG),X09   IS IT 2740B
         BO    A2740B              YES, TREAT AS 2740
         TM    UCBTYP(UCBREG),X0B   THIS A 2740 TERMINAL
         BNZ   ERRSNO              NO, SHOULD NOT OCCUR
A2740B   EQU   *
         CLI   DECRESPN+L1(DECBRG),X40   THIS A CIRCLE N
         BNE   ERRCRY              NO, CHECK FOR CIRCLE Y
         MVI   ZERO(CCWREG),X03    CHANGE OPERATION TO NOP
         LR    ERREG4,CCWREG       SAVE CCW ADDRESS
         B     ERR27402
ERRCRY   CLI   DECRESPN+L1(DECBRG),X76   THIS A CIRCLE Y
         BNE   ERRSNO              NO, SHOULD NOT OCCUR
         NI    IOBSTBYT,XFE        SET UNIT EXCEPTION OFF
         LA    ERREG4,L8(CCWREG)   BUMP TO NEXT CCW
ERR27402 ST    ERREG4,IOBSTART     STORE ADDRESS RESTART
         MVC   EWAIAREA(LMSG),ERRMSG   MOVE PARAMETER LIST
         MVC   EWAICCC(L2),COMCODE   MOVE COMMAND CODE
         UNPK  EWAICCSW(L5),IOBSTBYT(L3)   UNPACK CSW
         UNPK  EWAICSNS(L5),IOBSENS0(L3)   UNPACK SENSE
         MVC   IOBREPOS(L1),L5(CCWREG)   MOVE IN TP OP CODE
         LR    ERREG4,CCWREG       SAVE CCW ADDRESS
LOOP     SH    ERREG4,ERRCN8       BACK UP ONE CCW
         CLI   L5(ERREG4),X03      WRITE ADDR CHAR CCW
         BNE   LOOP                NO BACK ONE MORE
         L     ERREG4,ZERO(ERREG4)   ADDRESS OF ADDR CHARS
         MVC   IOBRSTRB+L1(L1),ZERO(ERREG4)   MOVE IN ADDR CHARS
         MVC   IOBRSTRB+L2(L1),DECRESPN(DECBRG)   MOVE IN STATUS CO
         UNPK  EWAICTP(L9),IOBREPOS(L5)   UNPACK TPOP & ID
         TR    EWAICCC(L22),ERRTBL-L240   TRANSLATE
         MVC   EWAICUCB(L3),UCBNAME(UCBREG)   MOVE IN UNIT NAME
         MVI   EWAICC1,COMMA       SET UP
         MVI   EWAICC2,COMMA       COMMAS AND
         MVI   EWAICC3,COMMA       SPACES IN
         MVI   EWAICBL,BLANK       MESSAGE
         LR    ERREG2,IOSBREG      SAVE IOSB BASE
         SR    ERREG3,ERREG3       CLEAR PARAMETER REG
         WTO   MF=(E,EWAIAREA)
         LR    IOSBREG,ERREG2      RESTORE IOSB BASE
RETRY    EQU   *
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
         L     NAMEREG,TRANSRTN    LOAD NAME
         B     ERRLINK1            XCTL
ERRSNO   OI    DECERRST(DECBRG),CSWSNO   INDICATE SHOULD NOT OCCUR
ERRUEE02 OI    IOSFLB,IOSLOG       INDICATE OBR LOGOUT
         NI    IOSFLA,L255-IOSERR TURN OFF ERP IN CNTL IN IOSB @ZA04886
         NI    IOBFLAG1,L255-IOBERRTN      ALSO IN IOB         @ZA04886
         LH    NAMEREG,ERLOD5      ADDRESS OF ERROR POST
         MVC   EWACHA(L2),L4(UCBREG)  MOVE ADDR TO WORK AREA   @ZA04871
ERRLINK1 EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE
         BR    ERRETR              XCTL
         DROP  ERRETR
ERRUEE03 TM    DCBERROP(DCBREG),DCBWRTRY   WRITE RETRIES SPECIFIED
         BZ    ERRUEE02            NO POST COMPLETE WITH ERROR
         CLI   IOBERRCT,ERTRYCT    RETRY COUNT AT MAX
         BNL   ERRUEC01            YES CHECK FOR DISABLE REQUIRED
         IC    ERREG1,IOBERRCT     PICK UP RETRY COUNT
         LA    ERREG1,L1(ERREG1)   ADD 1 TO RETRY COUNT
         STC   ERREG1,IOBERRCT     STORE NEW COUNT
         MVC   IOBWORK+L2(L1),DECTYPE+L1(DECBRG)   OP TYPE TO WORKAREA
         NI    IOBWORK+L2,L255-HIBIT   CLEAR UNUSED BITS
         CLI   IOBWORK+L2,DECWINIT   OP TYPE WRITE INITIAL      YA03259
         BNE   ERRUEE02            NO, GO TO ERROR POST         YA03259
*                                  THE FOLLOWING DELETED FOR    YA03259
***      NI    DECTYPE+L1(DECBRG),CCWMASK1   CLEAR OP TYPE BITS
***      OI    DECTYPE+L1(DECBRG),DECWRCNT   INDICATE WRITE CONTINUE
ERRUEE04 EQU   *
***      LR    ERREG2,IOSBREG      SAVE IOSB BASE
***      OI    IOBBTAMF,X08        ERP FLAG FOR RD/WR
***      LA    NAMEREG,EWAIAREA-L12   ADDRESS OF REG SAVE AREA
***      LR    IOSBREG,DECBRG      DECB ADDRESS IN PARAMETER REG
***      WRITE (1),T,MF=E          CONSTRUCT CHANNEL PROGRAM
***      LR    IOSBREG,ERREG2      RESTORE IOSB BASE
***      NI    IOBBTAMF,L255-X08   ERP RD/WR FLAG OFF
ERRUEE05 MVI   IOBSENS0,ZERO       CLEAR SENSE BYTE
         B     RETRY               ERROR EXCP
ERRUEE06 EQU   *
         CLI   IOBWORK+L1,IBMADPT1   IBM TYPE 1 ADAPTER
         BNE   ERRUEC06            NO
         TM    UCBTYP(UCBREG),X04   TEST
         BZ    ERRUEC06            FOR
         TM    UCBTYP(UCBREG),X03   2741
         BNM   ERRUEC06            NOT 2741
         CLI   IOBERRCT,ERTRYCT    RETRY COUNT AT MAX
         BNL   ERRUEE02
         IC    ERREG1,IOBERRCT     PICK UP RETRY COUNT
         LA    ERREG1,L1(ERREG1)   ADD ONE
         STC   ERREG1,IOBERRCT     STORE
         ST    CCWREG,IOBSTART     RESTART AT WRITE
         B     ERRUEE05
ERRUEC06 EQU   *
         MVC   IOBERCCW(L8),ERRSKEL1   MOVE SKELETON CCW
         LA    ERREG1,IOBERCCW     ADDRESS OF DUMMY CCW
         ST    ERREG1,IOBSTART     STORE IN CHAN PROG POINTER LOC
         MVI   IOBERRCT+L1,UELINKID   INDICATE UNIT EXCEPTION RETURN
         CLI   IOBWORK+L1,TTYADPT1   IS THIS TELETYPE ADAPTER 1
         BE    ERRUEE07            YES SET UP WRITE BREAK
         MVI   IOBERCCW,CCWRDSKP   SET UP FOR A READ SKIP OPERATON
         OI    IOBERCCW+L4,CCWSKPID   INDICATE READ SKIP OPERATION
         B     ERRUEE08            BRANCH TO EXECUTE CHAN PROGRAM
ERRUEE07 ST    IOBRG,IOBERCCW      VALID DATA ADDRESS
         MVI   IOBERCCW,CCWWRTBK   PUT WRITE CMND IN CCW
         MVC   IOBERCCW+L6(L2),CCWBRKCT   PUT WRITE BREAK COUNT IN CCW
ERRUEE08 MVC   IOBERINF(L1),IOBSENS0   SAVE PREVIOUS SENSE DATA
         MVC   IOBERINF+L8(L8),ZERO(CCWREG)   SAVE FAILING CCW
         MVI   IOBSENS0,ZERO       CLEAR SENSE BYTE
         MVC   IOBERINF+L1(L7),IOBCSW   SAVE CSW DATA
         B     RETRY               ERROR EXCP
ERRUEC01 TM    UCBTYP+L1(UCBREG),SWLINE  DISABLE REQUIRED
         BZ    ERRUEE02            NO POST WITH ERROR
         MVI   IOBERRCT+L1,MASK15   SPECIAL DISABLE ENTRY INDICATOR
         L     NAMEREG,ERLOD9      ADDRESS OF DISABLE ROUTINE
         B     ERRLINK1            EXIT ROUTINE
         EJECT
**********
*
*        CONSTANTS
*
**********
         DS    0D
ERRMSG   WTO   'IEA000I  AAA,I/O ERR,BB,CCCC,DDEE,FFGGHHHH ',          X
               MF=L,ROUTCDE=(1,8,10)
LMSG     EQU   *-ERRMSG            LENGTH OF PARAMETER LIST
TRANSRTN DC    F'9042'             BTAM TRANSLATE ROUTINE NAME
ERLOD9   DC    F'9041'             BTAM DISABLE ROUTINE NAME
ERRSKEL1 DC    X'00000000203000FF'
ERRCN8   DC    H'8'                DECIMAL 8
ERLOD5   DC    H'5041'             BTAM ERROR POST ROUTINE NAME
CCWBRKCT DC    H'15'
ERRTBL   DC    C'0123456789ABCDEF'
COMCODE  DC    C'02'
PACHAREA DC    19C'304B'           PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
DECWINIT EQU   X'02'               WRITE INITIAL OP TYPE        YA03259
DECRESPN EQU   26
UCBNAME  EQU   13
CCWAPOLL EQU   X'09'
CCWWRT   EQU   X'01'
CCWWRTBK EQU   X'0D'
UELINKID EQU   X'04'
CCWMASK1 EQU   X'C0'
MASK3    EQU   X'F0'
CCWRRTXT EQU   X'20'
DCBERROP EQU   33
DCBWRTRY EQU   X'04'
ERTRYCT  EQU   X'02'
DECTYPE  EQU   4
HIBIT    EQU   X'C0'
DECWRCNT EQU   X'04'
COMMA    EQU   C','                CHARACTER COMMA
BLANK    EQU   C' '                CHARACTER BLANK
ZERO     EQU   X'00'               LENGTH AND DISPLACEMENT
L255     EQU   255                 AND MASK
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L9       EQU   9                   LENGTH AND DISPLACEMENT
L12      EQU   12                  LENGTH AND DISPLACEMENT
L22      EQU   22                  LENGTH AND DISPLACEMENT
L240     EQU   240                 LENGTH AND DISPLACEMENT
X09      EQU   X'09'               2740B MASK
X06      EQU   X'06'               READ RESPONSE MASK
XE0      EQU   X'E0'               TYPE 1 ADAPTER MASK
X0B      EQU   X'0B'               2740 TERMINAL MASK
X40      EQU   X'40'               CIRCLE N MASK
X03      EQU   X'03'               NOP
X76      EQU   X'76'               CIRCLE Y MASK
XFE      EQU   X'FE'               U/C AND MASK
X08      EQU   X'08'               READ/WRITE MASK
X04      EQU   X'04'               2741 MASK
IBMADPT1 EQU   X'10'
TTYADPT1 EQU   X'40'
IBMADPT3 EQU   X'80'
MODL2740 EQU   X'04'
SCONTROL EQU   X'08'
CHECKING EQU   X'20'
CCWRDSKP EQU   X'02'
CCWSKPID EQU   X'30'
DECERRST EQU   29
CSWSNO   EQU   X'40'
UCBTYP   EQU   16
SWLINE   EQU   X'90'
MASK15   EQU   X'0F'
**********
*
*        REGISTER DEFINITION
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
ERREG2   EQU   2                   WORK REG
BASEREG  EQU   3                   PROGRAM BASE
CCWREG   EQU   4                   CCW ADDRESS REG
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE
EWAREG   EQU   8                   ERP WORKAREA BASE
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG4   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  RETURN REG
ERREG5   EQU   15                  WORK REG
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
EWAICUCB EQU   EWAIAREA+13         UCB NAME
EWAICCC  EQU   EWAIAREA+25         CONDITION CODE
EWAICC1  EQU   EWAIAREA+27         FIRST COMMA
EWAICCSW EQU   EWAIAREA+28         CSW
EWAICC2  EQU   EWAIAREA+32         SECOND COMMA
EWAICSNS EQU   EWAIAREA+33         SENSE
EWAICC3  EQU   EWAIAREA+37         THIRD COMMA
EWAICTP  EQU   EWAIAREA+38         TP CODE
EWAICBL  EQU   EWAIAREA+46         BLANK
         EJECT
         IECDIOSB
         END
