         TITLE ' IGC1006F - 3270 REMOTE ASCII TEST MODULE '
IGC1006F CSECT
***********************************************************************
*                  3270 REMOTE ASCII TEST MODULE                      *
***********************************************************************
*                                                                     *
* STATUS:                                                             *
*                                                                     *
*        ZA02332  (11/20/74)                                          *
*                                                                     *
***********************************************************************
*                                                                     *
* FUNCTION:                                                           *
*    THIS MODULE BUILDS PREDEFINED ASCII ON-LINE TEST MESSAGES        *
*    FOR THE REMOTE 3270 DISPLAY SYSTEM.                              *
*                                                                     *
* ENTRY:                                                              *
*         ENTRY IS TO THE FIRST EXECUTABLE INSTRUCTION VIA XCTL       *
*         FROM IGC0D06F.                                              *
*                                                                     *
* INPUT:                                                              *
*    REG 1 - ADDRESS OF THE IOB                                       *
*    REG 4 - TEST TYPE XX VALUE                                       *
*    IOBERINF - ADDRESS OF THE RFT                                    *
*    ON-LINE TEST CHANNEL PROGRAM IN IOB, BUILT BY IGC0D06F.          *
*                                                                     *
*                                                                     *
* OUTPUT:                                                             *
*    TEST MESSAGE BUILT IN USER'S AREA.                               *
*    COUNT IN WRITE TEXT CCW.                                         *
*    IF THE USER'S BUFFER IS NOT LONG ENOUGH (300 BYTES) OR THE       *
*    TEST MESSAGE NUMBER IS INVALID, AN ERROR FLAG (IOBINCAM,         *
*    BYTE 0, BIT 6) IS TURNED ON. THIS WILL CAUSE IGG019MR TO         *
*    CANCEL THE TEST                                                  *
*    TESTS BUILT BY THIS MODULE:                                      *
*    TEST 29 - 3270 BASIC ASCII                                       *
*    TEST 30 - 3270 MODEL 1 ALIGN ASCII                               *
*    TEST 32 - 3270 ORDERS ASCII                                      *
* EXTERNAL REFERENCES:                                                *
*         SVC 0 - EXECUTE CHANNEL PROGRAM                             *
*                                                                     *
* EXITS, NORMAL:                                                      *
*     SVC 3 - RETURN TO IOS                                           *
*                                                                     *
* EXITS, ERROR:                                                       *
*         NONE                                                        *
*                                                                     *
* TABLES/WORK AREAS:                                                  *
*         NONE                                                        *
*                                                                     *
* ATTRIBUTES:                                                         *
*    RE-ENTRANT                                                       *
*                                                                     *
* CHARACTER CODE DEPENDENCY:                                          *
*    NONE:                                                            *
*                                                                     *
***********************************************************************
         EJECT
*/*IGC1006F: CHART*/
*/* E IGC1006F*/
*/* P OVERLAY TRANSPARENT CCW*/
*/* P STORE ADDR OF START OF RFT IN CCW*/
*/* D (YES,,NO,SETERR) IS BUFFER LONG ENOUGH*/
*/* D (YES,,NO,SETERR) IS TEST NUMBER VALID (29,30, OR 32)*/
*/* P GET MESSAGE LENGTH AND ADDRESS*/
*/* P (,STARTEST) MOVE TEST MESSAGE INTO USER'S BUFFER*/
*/*SETERR: M TURN ON ERROR FLAG*/
*/* P SET COUNT TO ONE*/
*/*STARTEST: P STORE COUNT IN CCW*/
*/* P SAVE THE ECB*/
*/* L SVC 0*/
*/* P RESTORE THE ECB*/
*/* R SVC 3*/
*/*IGC1006F: END*/
         EJECT
         SPACE 2
*        ESTABLISH ADDRESSABILITY
         SPACE
         BALR  BASEREG,ZEROREG     SET UP BASE REG
         USING *,BASEREG
         USING IECTIOB,IOBREG      IOB DSECT
         USING IECTDECB,DECBREG    DECB DSECT
         USING IHADCB,DCBREG       DCB DSECT
         USING IECTDEB,DEBREG      DEB DSECT
         USING UCBDSECT,UCBREG     UCB DSECT
         B     SETUP               BRANCH AROUND ID USED TO
         DC    C'IGC1006F'         FIND MODULE IN DUMP
         DC    C' 4322'            DATE LAST CHANGE         LD @ZA02332
         DC    C'&SYSDATE'         DATE LAST ASSEMBLY       LD @ZA02332
         DS    0H                                           LD @ZA02332
PATCH    DC    48X'00'             PATCH AREA               LD @ZA02332
         DS    0H                                           LD @ZA02332
SETUP    EQU   *
         L     DECBREG,IOBECBPT    GET DECB ADDR
         L     DCBREG,IOBDCBPT     GET DCB ADDR
         L     DEBREG,DCBDEBAD     GET DEB ADDR
         L     DEBREG,PREFIX(DEBREG) GO PAST PREFIX
         SPACE
         SPACE
*        SET UP CHANNEL PROGRAM
         SPACE
         MVC   IOBCPA+48(CCWLNG),IOBCPA+56 OVERLAY TRANS CCW
         LA    CCWREG,IOBCPA+40    GET ADDR OF CCW
         SPACE 2
*        VALIDITY CHECK BUFFER LENGTH AND TEST NUMBER. IF
*        INVALID, SET UP NOP CCW AND TURN ON ERROR FLAG.
         SPACE
         CLC   DECLNGTH(BUFLNG),MINBUF IS BUFFER LONG ENOUGH
         BL    SETERR              NO, SET UP FOR ERROR
         C     TESTREG,TEST29      IS IT TEST 29 ?
         BE    GETINFO             YES, GET MSG ADDR & COUNT
         C     TESTREG,TEST30      IS IT TEST 30 ?
         BE    GETINFO             YES, GET MSG ADDR & COUNT
         C     TESTREG,TEST32      IS IT TEST 32 ?
         BNE   SETERR              NO, SET UP FOR ERROR
         SPACE 2
*        GET TEST MESSAGE ADDRESS AND LENGTH
         SPACE
GETINFO  LR    WORKREG1,TESTREG    GET TEST #
         S     WORKREG1,TEST29     SUBTRACT 29 TO GET INDEX
*                                  (29 IS LOWEST TEST NUMBER
*                                  THIS MODULE WILL HANDLE)
         SLL   WORKREG1,2          MULTIPLY BY 4
         L     COUNTREG,COUNTAB(WORKREG1) GET MSG LENGTH
         B     ADDRTAB(WORKREG1)   GET MSG ADDR
         SPACE 2
*        MESSAGE ADDRESS AND LENGTH TABLES - (TEST MESSAGE
*        NUMBER - 29) X 4 = THE ARGUMENT USED TO OBTAIN VALUES
*        FROM THE TABLE.
         SPACE
         DS    0F
COUNTAB  EQU   *                   MESSAGE LENGTH IN BYTES
         DC    A(TST29SZ)          TEST 29
         DC    A(TST30SZ)          TEST 30
         DC    A(0)                UNUSED
         DC    A(TST32SZ)          TEST 32
         SPACE 2
ADDRTAB  EQU   *                   ADDRESS OF TEST MESSAGE
         B     TEST29AD            00   GET TEST 29 ADDR
         B     TEST30AD            04   GET TEST 30 ADDR
         B     SETERR              08   UNUSED
         B     TEST32AD            0C   GET TEST 32 ADDR
TEST29AD LA    ADDREG,IJLBOT29     GET MSG ADDR
         B     MOVEIN              CONTINUE
TEST30AD LA    ADDREG,IJLBOT30     GET MSG ADDR
         B     MOVEIN              CONTINUE
TEST32AD LA    ADDREG,IJLBOT32     GET MSG ADDR
         SPACE 2
         SPACE 2
*        MOVE TEST MSG INTO USER'S BUFFER
         SPACE
MOVEIN   EQU   *
         L     RFTREG,IOBERINF     GET ADDR OF RFT
         LA    WORKREG2,IOBCPA     STORE ADDR IOBCPA             S99245
         ST    WORKREG2,IOBSTART   INTO IOBSTART                 S99245
         CLI   NVALUE(RFTREG),NOADDR   IS THIS A DIAL RFT?       S99245
         BNE   BSC3RFT             NO,FOLLOW BSC3 PROCEDURE      S99245
         LA    RFTREG,CHARS7(RFTREG)   ADJUST PTR PAST RFT       S99245
         B     TSTLNG                                            S99245
BSC3RFT  EQU   *                                                 S99245
         LA    RFTREG,CHARS11(RFTREG) ADJUST PTE PAST RFT
TSTLNG   EQU   *                                                 S99245
         SR    WORKREG2,WORKREG2   CLEAR REG
         C     COUNTREG,MAXLNGTH   IS MSG LNGTH OVER 256 ?
         BNH   LNGTHOK             NO, BRANCH
         LR    WORKREG2,COUNTREG   GET EXCESS OVER 256
         S     WORKREG2,MAXLNGTH
         L     COUNTREG,MAXLNGTH   SET COUNT TO 256
LNGTHOK  EQU   *
         BCTR  COUNTREG,ZEROREG    SUBTRACT ONE FROM LNGTH FOR
*                                  EXECUTE
         EX    COUNTREG,MSGMOVE    MOVE TEST MSG TO USER'S
*                                  BUFFER
         LTR   WORKREG2,WORKREG2   ANY MORE TO MOVE ?
         BZ    STARTEST            NO, BRANCH
         A     RFTREG,MAXLNGTH     BUMP ADDRESS PAST 256
         A     ADDREG,MAXLNGTH     CHARS JUST MOVED
         BCTR  WORKREG2,ZEROREG    SUBTRACT ONE FOR EXECUTE
         EX    WORKREG2,MSGMOVE    MOVE REMAINDER OF TEXT
         B     STARTEST            BRANCH AROUND EXECUTE
MSGMOVE  MVC   0(FILLIN,RFTREG),0(ADDREG) MOVE TEST MSG TO
*                                  USER'S BUFFER, USING LENGTH
*                                  SPECIFIED IN COUNTREG
SETERR   EQU   *
         OI    IOBINCAM,ERFLAG     TURN ON ERROR FLAG
         LA    COUNTREG,ONECHAR    SET COUNT TO ONE
         B     STARTEST            DO TEST
         SPACE 2
*        MOVE COUNT INTO CCW AND START TEST
         SPACE
STARTEST EQU   *
         LA    COUNTREG,ONECHAR(COUNTREG) RESTORE COUNT
         STH   COUNTREG,BYTECNT(CCWREG) STORE COUNT IN CCW
*
*              1 LINE DELETED FOR ----->                    LD @ZA02332
*
         LR    WORKREG1,IOBREG     SAVE IOB ADDR            LD @ZA02332
         L     PARMREG,GETPARM     SET FOR GETMAIN          LD @ZA02332
         GETMAIN R,LV=(0)          GETMAIN FOR ECB          LD @ZA02332
         LR    WORKREG2,IOBREG     SAVE ECB ADDR            LD @ZA02332
         ST    DECBREG,DISP4(WORKREG2)  SAVE ECB ADDR       LD @ZA02332
         MVI   DISP4(WORKREG2),FOXFOX   FLAG AS DUMMY DECB  LD @ZA02332
         LR    IOBREG,WORKREG1     RESTORE IOB REG          LD @ZA02332
         STCM  WORKREG2,MASK,IOBECBPT+DISP1  POINT IOB...   LD @ZA02332
*                                  ...TO DUMMY              LD @ZA02332
         SVC   EXCP                DO TEST
*
*              1 LINE DELETED FOR ----->                    LD @ZA02332
*
         STCM  DECBREG,MASK,IOBECBPT+DISP1  REPOINT IOB...  LD @ZA02332
*                                  ...TO DECB               LD @ZA02332
         L     PARMREG,GETPARM     SET UP FOR FREEMAIN      LD @ZA02332
         FREEMAIN R,LV=(0),A=(WORKREG2)  FREEMAIN ECB       LD @ZA02332
         SVC   RETURN              RETURN
         SPACE 2
*        IF AN ERROR WAS FOUND, TURN ON ERROR FLAG, GET CCW
*        ADDR, AND ISSUE NOP CCW
         SPACE
         SPACE 2
*        REGISTER EQUATES
         SPACE
PARMREG  EQU   0                   PARAMETER REG            LD @ZA02332
IOBREG   EQU   1                   ADDR OF IOB
DECBREG  EQU   2                   ADDR OF DECB
DCBREG   EQU   3                   ADDR OF DCB
TESTREG  EQU   4                   TEST #
WORKREG1 EQU   5                   WORK REG
WORKREG2 EQU   6                   WORK REG
DEBREG   EQU   7                   ADDR OF DEB
UCBREG   EQU   8                   ADDR OF UCB
ADDREG   EQU   9                   TEST MSG ADDRESS
COUNTREG EQU   10                  TEST MSG LENGTH
CCWREG   EQU   11                  ADDR OF CCW TO WRITE TEST MSG
BASEREG  EQU   12
RFTREG   EQU   13                  ADDR OF RFT
ZEROREG  EQU   0                   IN BCTR FOR NO BRANCH
         SPACE 2
*        MISCELLANEOUS EQUATES
         SPACE
ERFLAG   EQU   X'02'               ERROR FLAG
PREFIX  EQU    28                  DISP OF START OF DEB
CCWLNG   EQU   8                   LENTH OF A CCW
BUFLNG   EQU   2                   LENGTH OF DECLNGTH FIELD
CHARS11  EQU   11                  LENGHT OF STD RFT - REMOTE
FILLIN   EQU   0                   LENGTH FILLED IN EX INSTR
ONECHAR  EQU   1                   ONE CHARACTER
BYTECNT  EQU   6                   DISP IN CCW OF BYTE COUNT
EXCP     EQU   0                   EXCP SVC #
RETURN   EQU   3                   RETURN SVC #
NVALUE   EQU   6                   DISP INTO RFT OF N VALUE      S99245
NOADDR   EQU   X'00'               IF N=0, IT IS A DIAL RFT      S99245
CHARS7   EQU   7                   LENGTH OF STANDARD DIAL RFT   S99245
FOXFOX   EQU   X'FF'               BYTE OF ALL ONES         LD @ZA02332
MASK     EQU   7                   MASK FOR STCM            LD @ZA02332
DISP1    EQU   1                   DISPLACEMENT             LD @ZA02332
DISP4    EQU   4                   DISPLACEMENT             LD @ZA02332
         SPACE 2
*        STORAGE
         SPACE
         DS    0F                  FULLWORD BOUNDARY        LD @ZA02332
GETPARM  DC    X'FA000008'         SUBPOOL # AND LENGTH     LD @ZA02332
MINBUF   DC    X'012C'             MINIMUM BUFFER LENGTH
TEST29   DC    F'29'               ACCEPTABLE TEST NUMBER
TEST30   DC    F'30'               ACCEPTABLE TEST NUMBER
TEST32   DC    F'32'               ACCEPTABLE TEST NUMBER
MAXLNGTH DC    F'256'              MAX # OF CHARS FOR MVC
IJLBOT29 EQU   *
         DC    X'021B355E'         STX-ESC-EWRT-WCC
         DC    X'114126'           SBA 80
         DC    X'1D2D'             SF 60
         DC    X'41424344454647'   ABCDEFG
         DC    X'48494A4B4C4D4E4F'         HIJKLMNO
         DC    X'5051525354555657' PQRSTUVW
         DC    X'58595A20'                 XYZ
         DC    X'1D25'             SF 6C
         DC    X'4E4F4E204449'     NON DI
         DC    X'53504C4159'              SPLAY
         DC    X'11422D'           SBA 160
         DC    X'1D20'             SF 40
         DC    X'13'               INC
         DC    X'434F50592041424F56' COPY ABOV
         DC    X'4520494E20544849'            E IN THI
         DC    X'53204C494E45'                        S LINE
         DC    X'20202020'         (FOUR SPACES)
         DC    X'1D20'             SF40
         DC    X'494E534552542043' INSERT C
         DC    X'4B0000'                   K-(2 NULLS)
         DC    X'1D2D'             SF 60
         DC    X'114330'           SBA 240
         DC    X'1D59'             SF E8
         DC    X'5B3C282B2126'     SPECIAL CHARS
         DC    X'5D242A295E2D2F3B' SPECIAL CHARS
         DC    X'255F3E3F3A234027' SPECIAL CHARS
         DC    X'3D22'             SPECIAL CHARS
         DC    X'1D38'             SF F8
         DC    X'3031323334353637' 01234567
         DC    X'38392C2E2D41'             89,.-A
         DC    X'114520'           SBA 320
         DC    X'1D48'             SF C8
         DC    X'434F50592041424F56' COPY ABOV
         DC    X'4520494E20544849'            E IN THI
         DC    X'53204C494E4520'                      S LINE
         DC    X'1D51'             SF D8
         DC    X'114559'           SBA 360
         DC    X'1D2D'             SF 60
         DC    X'114626'           SBA 400
         DC    X'1D55'             SF E4
         DC    X'3F53454C2050454E20' ?SEL PEN
         DC    X'54455354000000'              TEST
         DC    X'1D59'             SF E8
         DC    X'3E53454C205045'   >SEL PE
         DC    X'4E2054455354'            N TEST
         DC    X'03'               ETX
TST29SZ  EQU   *-IJLBOT29          LENGTH OF TEST 29
IJLBOT30 EQU   *
         DC    X'021B355E'         STX-ESC-EWRT-WCC
         DC    X'112020'           SBA 0
         DC    X'14205945'         RA 40,E
         DC    X'48'               H
         DC    X'1141214848'       SBA 79,HH
         DC    X'1141374848'       SBA 119,HH
         DC    X'11425E4848'       SBA 159,HH
         DC    X'1143474848'       SBA 199,HH
         DC    X'11433F4848'       SBA 239,HH
         DC    X'1144504848'       SBA 279,HH
         DC    X'1144224848'       SBA 319,HH
         DC    X'1145584848'       SBA 359,HH
         DC    X'1146214848'       SBA 399,HH
         DC    X'11463748'         SBA 439,H
         DC    X'1143244F4F'       SBA 219,OO
         DC    X'1144434F4F'       SBA 299,OO
         DC    X'114429'           SBA 285
         DC    X'5445535420504154' TEST PAT
         DC    X'5445524E20464F52'         TERN FOR
         DC    X'20333237372D312F'  3277-1/
         DC    X'333237352D31'             3275-1
         DC    X'114521'           SBA 335
         DC    X'414C49474E4D45'   ALIGNME
         DC    X'4E54'                    NT
         DC    X'1145302A'         SBA 368,*
         DC    X'1D20'             SF 40
         DC    X'13'               INC
         DC    X'554E50524F5445'   UNPROTE
         DC    X'435445442044415441'      CTED DATA
         DC    X'114646'           SBA 390
         DC    X'1D2D2A'           SF 60,*
         DC    X'114638'           SBA 440
         DC    X'14475E45'         RA 479,E
         DC    X'45'               E
         DC    X'03'               ETX
TST30SZ  EQU   *-IJLBOT30          LENGTH OF TEST 30
IJLBOT32 EQU   *
         DC    X'021B353F'         STX-ESC-EWRT-WCC
         DC    X'1D2D'             SF 60
         DC    X'464F5220434C55'   FOR CLU
         DC    X'53544552454420'          STERED
         DC    X'5052494E54455253'               PRINTERS
         DC    X'2044415441205348'  DATA SH
         DC    X'4F554C442053544152'       OULD STAR
         DC    X'54204E455854204C'                  T NEXT L
         DC    X'494E452048455245'                          INE HERE
         DC    X'202D2D2D2D2D2D3E'  ------>
         DC    X'434845434B494E472050'     CHECKING P
         DC    X'524F4752414D205441422F'             PROGRAM TAB/
         DC    X'455241534520554E' ERASE UN
         DC    X'50524F542E20544F204144'   PROT. TO AD
         DC    X'44522F4455502F464945'                DR/DUP/FIE
         DC    X'4C44204D4152'     LD MAR
         DC    X'4B204F5244455253'       K ORDERS
         DC    X'114330'           SBA 240
         DC    X'13'               INC
         DC    X'1D38'             SF F8
         DC    X'14452031'         RA 320,1
         DC    X'1D48'             SF 68
         DC    X'14462641'         RA 400,A
         DC    X'1D2D'             SF 60
         DC    X'14475E42'         RA 479,B
         DC    X'11445031'         SBA 279,1
         DC    X'09'               PT
         DC    X'114559'           SBA 360
         DC    X'124626'           EUA 400
         DC    X'11465B'           SBA 394
         DC    X'1C1E0A19'         DUP/FM/NL/EOM
         DC    X'03'               ETX
TST32SZ  EQU   *-IJLBOT32          LENGTH OF TEST 32
         SPACE 2
*        DSECTS
         SPACE
         IECTIOBX
         EJECT
         IECTDECB
         EJECT
         DCBD  DSORG=BX,DEVD=BS
         EJECT
         IECTDEBX
UCBDSECT DSECT
         DS    0F
         ORG   UCBDSECT+13         UCBNAME (UNIT NAME)
CHARADDR DS    CL3                 UNIT ADDR (EBCDIC)
         ORG   UCBDSECT+18         UCBTYP + 2
DVCLASS  DS    CL1                 DEVICE CLASS
UNITYPE  DS    CL1                 UNIT TYPE
         ORG   UCBDSECT+36         UCBRLN
RLN      DS    CL1                 RELATIVE LINE NUMBER
      END
