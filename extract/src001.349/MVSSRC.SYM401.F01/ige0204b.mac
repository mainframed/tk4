         TITLE 'IGE0204B - BTAM LINE ERROR RECORDING MODULE'
IGE0204B CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0204B                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM LINE ERROR RECORDING MODULE                                 *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS MODULE UPDATES THE FOLLOWING COUNTS IN THE LINE ERROR       *
*    RECORDING BLOCK POINTED TO BY THE DCB:                           *
*        1.  TRANSMISSION COUNT                                       *
*        2.  LINE ERROR COUNT                                         *
*        3.  TOTAL TRANSMISSIONS COUNT                                *
*        4.  DATA CHECK TOTAL COUNT                                   *
*        5.  INTERVENTION REQUIRED TOTAL COUNT                        *
*        6.  TIME OUT TOTAL COUNT                                     *
*    WRITES A MESSAGE TO THE OPERATOR CONSOLE AND PROVIDES            *
*    LINKAGE TO THE ERROR POST AND DIAGNOSTIC READ/WRITE ROUTINES.    *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (48 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        470 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD   *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0204B                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        SEE 'FUNCTION'                                               *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        WTO (SVC 35)                                                 *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    WTO (SVC 35)                                                     *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YM05652K (1/24/74)                                               *
*    YM05680K (1/24/74)                                               *
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      BASE PROGRAM
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DCBREG,IOBDCBPT     BASE DCB
         DROP  ERREG5
         L     CCWREG,IOBFL3       ADDRESS OF CCW
         LA    CCWREG,ZERO(CCWREG)   CLEAR FLAG BYTE
         SH    CCWREG,ERRCN8       ADJUST TO FAILING CCW
         LH    NAMEREG,ERLOD5      ERROR POST NAME
         L     ERREG1,DCBLERB(DCBREG)   ADDRESS OF TABLE
         LTR   ERREG1,ERREG1       CHECK FOR LERB ADDRESS
         BZ    ERRDIAG             NONE
         MVC   EWAIAREA(LMSG),MSGAREA   MOVE PARAMETER LIST TO EWA
         SR    ERREG2,ERREG2       CLEAR REGISTER
         IC    ERREG2,IOBUCBX      INDEX FACTOR TO TABLE ENTRY
         MH    ERREG2,ERRCN20      RESOLVE TABLE INDEX
         AR    ERREG2,ERREG1       RESOLVE FINAL ADDRESS
         SR    ERRLNK,ERRLNK       CLEAR IC REGISTER
         IC    ERRLNK,LERBTRCT(ERREG2)   TOTAL TRANS COUNT
         LA    ERRLNK,L1(ERRLNK)   UPDATE COUNT
         STC   ERRLNK,LERBTRCT(ERREG2)   STORE NEW COUNT
         SR    ERREG1,ERREG1       REPLACES 3 INSTRUCTIONS
         IC    ERREG1,IOBERRCT-L1   PICK UP TABLE OFFSET
         AH    ERREG1,ERRCN10      ADD STANDARD OFFSET
         AR    ERREG1,ERREG2       RESOLVE FINAL ADDRESS
         IC    ERRLNK,ZERO(ERREG1)   PICK UP ERROR LINE COUNT
         LA    ERRLNK,L1(ERRLNK)   UPDATE COUNT
         STC   ERRLNK,ZERO(ERREG1)   STORE NEW COUNT
         EX    ERRLNK,ERRCTCMP     COUNT EQUAL MAX ALLOWED
         BE    ERRLRB01            BYPASS NO WTO
         CLC   LERBTRCT(L1,ERREG2),LERBTRT(ERREG2) TRANS COUNT REACHED
         BNE   ERRDIAG             NO CHECK IF DIAG WR/RD REQUIRED
         OI    IOBERRCT-L1,NOPRINT   INDICATE NO WTO
ERRLRB01 EQU   *
         L     ERREG1,ZERO(ERREG2)   YES UPDATE ACCUMULATIVE COUNTER
         IC    ERRLNK,L10(ERREG2)   TOTAL TRANSMISSIONS COUNT
         AR    ERREG1,ERRLNK       UPDATED ACCUM TRANS COUNT
         ST    ERREG1,ZERO(ERREG2)   STORE NEW COUNT
         CVD   ERRLNK,EWAISAVE     GENERATE MESSAGE FORMAT
         UNPK  ERRMSG02+L7(L3),EWAISAVE+L6(L2)   CREATE PRINTABLE CHARS
         OI    ERRMSG02+L9,MASK16   REMOVE SIGN
         LH    ERREG1,L4(ERREG2)    DATA CHECK TOTAL ERROR COUNT
         IC    ERRLNK,L11(ERREG2)   DATA CHECK NEW COUNT
         AR    ERREG1,ERRLNK        NEW TOTAL COUNT
         STH   ERREG1,L4(ERREG2)    STORE UPDATED TOTAL COUNT
         CVD   ERRLNK,EWAISAVE      DECIMAL COUNT FOR WTO MESSAGE
         UNPK  ERRMSG03+L4(L3),EWAISAVE+L6(L2)   PRINT FORMAT INTO MSG
         OI    ERRMSG03+L6,MASK16   MESSAGE FORMAT COMPLETE
         LH    ERREG1,L6(ERREG2)   INTERVENTION REQUIRED TOTAL COUNT
         IC    ERRLNK,L12(ERREG2)   NEW COUNT
         AR    ERREG1,ERRLNK       NEW TOTAL COUNT
         STH   ERREG1,L6(ERREG2)   STORE NEW TOTAL
         CVD   ERRLNK,EWAISAVE     NEW COUNT TO DECIMAL
         UNPK  ERRMSG04+L4(L3),EWAISAVE+L6(L2)   FORMAT FOR OUTPUT MSG
         OI    ERRMSG04+L6,MASK16   REMOVE SIGN
         LH    ERREG1,L8(ERREG2)   TIME OUT TOTAL COUNT
         IC    ERRLNK,L13(ERREG2)   TIME OUT NEW COUNT
         AR    ERREG1,ERRLNK       NEW TOTAL COUNT
         STH   ERREG1,L8(ERREG2)   STORE NEW TOTAL
         TM    IOBERRCT-L1,NOPRINT   WTO REQUIRED
         BO    ERRLRB02            NO CONTINUE PROCESSING
         CVD   ERRLNK,EWAISAVE     DECIMAL COUNT FOR WTO
         UNPK  ERRMSG05+L4(L3),EWAISAVE+L6(L2)   FORMAT PRINTABLE CHARS
         OI    ERRMSG05+L6,MASK16   REMOVE SIGN
         L     UCBREG,IOSUCB       ADDRESS OF UCB
         N     UCBREG,UCBZIP       CLEAR LOW BIT
         MVC   ERRMSG01(L3),UCBNAME(UCBREG)   LINE ADDR TO MESSAGE
         SR    ERREG3,ERREG3       CLEAR PARAMETER REG
         LR    ERREG4,IOSBREG      SAVE BASE                   YM05680K
         LA    IOSBREG,EWAIAREA    POINTER TO MESSAGE
         WTO   MF=(E,(1))          WRITE MESSAGE
         LR    IOSBREG,ERREG4      RESTORE BASE                YM05680K
ERRLRB02 EQU   *
         XC    L10(L4,ERREG2),L10(ERREG2)   CLEAR NEW COUNT FIELDS
ERRDIAG  MVI   IOBERRCT-L1,ZERO    CLEAR LERB INDICATORS
         TM    IOBERRCT+L1,RTRNCDE   DIAG WRITE READ REQ
         BO    ERE5C1              YES GO TO DIAG ROUTINE
         TM    IOSFLA,IOSEX+IOSERR   DETERMINE EXIT
         BM    ERRLRB03
         OI    IOSFLA,IOSEX        SET ON IOB EXCEPTION FLAG
         OI    IOBFLAG1,IOBIOERR   ALSO IN IOB
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
         L     NAMEREG,TRANSRTN    LOAD NAME
         B     EXIT                XCTL
ERRLRB03 TM    UCBTYPE+L3(UCBREG),UCBBSC   THIS A BI-SYNC DEVICE
         BNO   EXIT                NO, GO TO ERROR POST ROUTINE
         LA    NAMEREG,ERLOD25     ADDRESS OF ERROR POST
         B     EXIT                EXIT
ERE5C1   LA    NAMEREG,ERLOD11     ADDRESS OF DIAG WRT/RD ROUTINE
EXIT     EQU   *
         L     COMREG,CVTPTR       ADDRESS OF CVT
         USING CVTMAP,COMREG
         L     ERRETR,CVTXTLER     XCTL ROUTINE ADDRESS
         BR    ERRETR              XCTL
         EJECT
**********
*
*        EXECUTED INSTRUCTIONS
*
**********
ERRCTCMP EQU   *
         CLI   L4(ERREG1),ZERO
**********
*
*        CONSTANTS
*
**********
TRANSRTN DC    F'9042'             NAME OF BTAM TRANSLATE ROUTINE
UCBZIP   DC    X'FFFFFFFE'         AND MASK, MUST BE ON WORD
MSGAREA  WTO   'IEC801I     THRESHOLD TRANS=    DC=    IR=    TO=    ',X
               MF=L,ROUTCDE=(8,10),DESC=(4)
LMSG     EQU   *-MSGAREA           LENGTH OF PARAMETER LIST
ERRCN8   DC    H'8'                DECIMAL 8
ERRCN10  DC    H'10'
ERLOD5   DC    H'5041'
ERRCN20  DC    H'20'
PACHAREA DC    12C'204B'           PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
DCBLERB  EQU   52
ERLOD11  EQU   1042
UCBBSC   EQU   X'90'
UCBTYPE  EQU   16
ERLOD25  EQU   2043
LERBTRCT EQU   10
LERBTRT  EQU   14
MASK16   EQU   X'F0'
ZERO     EQU   0
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L9       EQU   9                   LENGTH AND DISPLACEMENT
L10      EQU   10                  LENGTH AND DISPLACEMENT
L11      EQU   11                  LENGTH AND DISPLACEMENT
L12      EQU   12                  LENGTH AND DISPLACEMENT
L13      EQU   13                  LENGTH AND DISPLACEMENT
L14      EQU   14                  LENGTH AND DISPLACEMENT
L15      EQU   15                  LENGTH AND DISPLACEMENT
L16      EQU   16                  LENGTH AND DISPLACEMENT
L255     EQU   255                 AND MASK
NOPRINT  EQU   X'10'
RTRNCDE  EQU   X'C0'
UCBNAME  EQU   13
**********
*
*        REGISTER DEFINITION
*
**********
ERREG3   EQU   0                   WORK REG/PARAMETER REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
ERREG2   EQU   2                   WORK REG
BASEREG  EQU   3                   PROGRAM BASE
CCWREG   EQU   4                   CCW ADMRESS REG
EWAREG   EQU   5                   ERP WORKAREA BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE
COMREG   EQU   8                   CVT BASE
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG4   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  LINK REG
ERREG5   EQU   15                  LINK REG/WORK REG
         EJECT
         IECDRQE
         EJECT
         IEZIOB
IOBCMD3  EQU   IOBCPA+L16
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
ERRMSG01 EQU   EWAIAREA+12
ERRMSG02 EQU   EWAIAREA+25
ERRMSG03 EQU   EWAIAREA+35
ERRMSG04 EQU   EWAIAREA+42
ERRMSG05 EQU   EWAIAREA+49
         EJECT
         IECDIOSB
         END
