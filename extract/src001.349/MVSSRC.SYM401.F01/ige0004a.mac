         TITLE 'IGE0004A - BTAM ERP CONTROL MODULE'
IGE0004A CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0004A                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM ERROR RECOVERY CONTROL PROCEDURE                            *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 001                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    1. PROVIDES LINKAGE TO SUBSEQUENT LOADS OF EITHER BTAM           *
*       ACCESS METHODS ERROR ROUTINES                                 *
*    2. DETERMINES IF ENTRY IS DUE TO INITIAL ERROR OR AS             *
*       A RESULT OF ERROR ROUTINE CHANNEL PROGRAM EXECUTION           *
*    3. DETERMINES TYPE OF INITIAL ERROR ENCOUNTERED AND              *
*       LINKS TO PROPER ROUTINE VIA CVT XCTL ROUTINE                  *
*    4. GENERATES RETURN LINKAGE TO SUBSEQUENT LOADS OF               *
*       ERROR ROUTINES WHEN REQUIRED DUE TO ERROR ROUTINE             *
*       CHANNEL PROGRAM EXECUTION                                     *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (68 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, LOCK HELD       *
*        WHEN GOING TO TRANSLATOR.                                    *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0004A                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        NONE FOR THIS MODULE                                         *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    STATISTICS UPDATE ROUTINE (IGE0025D) VIA BRANCH ENTRY TO XCTL    *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        IECVTCCW (IOS CCW TRANSLATOR)                                *
*        IOS INTERPRETOR                                              *
*        IECVIOPM                                                     *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*        TCCW         (MAPPED BY IECDTCCW)                            *
*        IOCOM        (MAPPED BY IECDIOCM)                            *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* NOTES:                                                              *
*    IOBERRCT+1 DETERMINES THE TYPE OF EXIT TO BE PERFORMED           *
*    IOBERRCT+1=00    NORMAL EXIT DETERMINED BY ERROR CONDITION FOUND *
*    IOBERRCT+1=01    INTERVENTION REQUIRED RETURN                    *
*    IOBERRCT+1=0F    SPECIAL ENTRY FOR LOAD 2                        *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YA01230  (IN BASE)                                               *
*    YM02568K (8/6/73)                                                *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YA02441  (11/1/73)                                               *
*    YA02452  (11/1/73)                                               *
*    YM05652K (1/25/74)                                               *
*    YM08278 (4/9/74)                                                 *
*    ZA02815  (01/17/75)           ZM00634  (01/27/75)                *
*    ZA03585 (03/24/75)                                               *
*    AZ04166  (08/25/75)          AZ04683  (08/26/76)                 *
*    AZ04868  (08/27/75)          AZ04869  (08/27/75)                 *
*    AZ06395  (11/04/75)          AZ09345  (04/08/76)                 *
*    AZ09351  (04/08/76)  DELETES APAR AZ04869                        *
*    AZ10030  (04/12/76)           AZ12155  (08/04/76)                *
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASREG
         LR    BASREG,ERREG5       SET BASE REG
         B     BEGIN               BRANCH AROUND ID
         DC    C'IGE0004A'         MODULE ID
         DC    C'** MVS *'
         DC    CL8'&SYSDATE'
         DS    0H
BEGIN    EQU   *
         USING IOSB,IOSBREG
         USING RQE,ERREG5
         USING IOBSTDRD,IOBREG
         USING EWA,EWAREG
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBREG,RQEIOB       BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         L     ERREG1,CVTPTR       BASE CVT                    YM02594K
         USING CVTMAP,ERREG1                                   YM02594K
         L     ERREG1,CVTIXAVL     POINT TO IOCOM              YM02594K
         DROP  ERREG1                                          YM02594K
         USING IOCOM,ERREG1                                    YM02594K
         L     ERREG1,IOCTCCW      POINT TO TRANSLATE ROUTINE  YM02594K
         ST    ERREG1,EWAITRAN     SAVE FOR DURATION OF ERROR  YM02594K
         DROP  ERREG1                                          YM02594K
         CLI   IOSDVRID,IOSXCPID   EXCP I/O
         BNE   ERRUPDTE            NO, GET OUT
         TM    IOSFLA,IOSERR         FIRST ENTRY
         BO    NOTFIRST            NO
         XC    EWASTUP(L2),EWASTUP   CLEAR STAT UPDATE BYTES
         XC    EWAITCCW(L124),EWAITCCW   CLEAR EWA USER AREA
         MVC   EWAIOVST(L4),IOSVST   SAVE ADDRESS              YM02594K
****  ONE LINE OF CODE DELETED BY ---------------------->      @ZA06395
         B     FIRST               BRANCH AROUND IOBCSW MAP
NOTFIRST EQU   *
         L     CCWREG,IOSCC        LOAD CSW                    YM02594K
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT CONDITION CODE  YM02594K
*
*              ONE DELETED FOR ----->                       LD @ZM00634
*
         LR    ERREG3,CCWREG       SET PARAMETER REG           YM02594K
         LR    ERREG1,IOSBREG      SAVE IOSB BASE              YM02594K
         DROP  IOSBREG                                         YM02594K
         TM    EWAIND0,EWAIPMAP    PARALLEL MAPPING DONE       YM02594K
         BNO   HISTCCW             NO                          YM02594K
         L     IOSBREG,EWAITCCW    BASE MY TCCW                YM02594K
         B     GOTRANS             GO TO TRANSLATOR            YM02594K
HISTCCW  EQU   *                                               YM02594K
         L     IOSBREG,RQETCCW     GET IOS TCCW                YM02594K
GOTRANS  EQU   *                                               YM02594K
         TM    RQETYPE,RQE1TO1     DOES V EQUAL R ?            @ZA04166
         BZ    GETLOCK             NO GO MAP IOBCSW            @ZA04166
         USING IOSB,ERREG1          SET IOSB ADDRESSIBLITY     @ZA04166
         MVC   IOBCSW(L7),IOSCSW   MOVE CSW FROM IOSB TO IOB   @ZA04166
         DROP  ERREG1                                          @ZA04166
         LR    IOSBREG,ERREG1   REINITILIZE IOSB REG           @ZA10030
         B     FIRST               BRANCH AROUND IOBCSW MAP    @ZA04166
         DROP  ERREG5                                          YM02594K
         USING TCCW,IOSBREG                                    YM02594K
GETLOCK  SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                        *
               RELATED=(ERP,IGE0004A(FREELOCK))             LD @ZA02815
         MVI   TCCWOPTN,TCCWCSWX   SET FOR CSW BACK TRANS      @ZA12155
         L     ERREG5,EWAITRAN     POINT TO IECVTCCW           YM02594K
         BALR  ERRETR,ERREG5       GO TO TRANSLATOR            YM02594K
FREELOCK SETLOCK RELEASE,TYPE=LOCAL,                                   *
               RELATED=(ERP,IGE0004A(GETLOCK))              LD @ZA02815
         LR    CCWREG,ERREG3       LOAD FOR LOAD ADDR          YM02594K
*
*              ONE LINE DELETED FOR ----->                  LD @ZM00634
*
         ST    CCWREG,EWAISAVE     SET CCW ADDRESS IN AREA     YM02594K
         MVC   IOBCMDA(L3),EWAISAVE+L1   MOVE TO CSW           YM02594K
         DROP  IOSBREG                                         YM02594K
         LR    IOSBREG,ERREG1      REBASE IOSB                 YM02594K
         USING IOSB,IOSBREG                                    YM02594K
         MVC   IOBSTBYT(L4),IOSTATUS   MOVE STATUS AND COUNT   YM02594K
FIRST    EQU   *
         MVC   IOBSENS0(L2),IOSSNS   MOVE SENSE IOSB TO IOB
         MVC   IOBSIOCC(L1),IOSCC   MOVE SIO CC IOSB TO IOB
         MVC   IOBECBCC(L1),IOSCOD   MOVE ECB CC IOSB TO IOB
         SR    ERREG1,ERREG1       CLEAR LINKAGE REGISTER
         L     CCWREG,IOBFL3       ADDRESS OF CCW FROM IOB CSW
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT CC
         SH    CCWREG,ERRCN8       ADJUST TO FAILING CCW IN IOB
         OI    IOSFLB,IOSMSG       INDICATE I/O ERROR TO MSG WRITER
         TM    IOBFLAG2,IOBRESP    RESET POLL ISSUED            YA02441
         BNO   ERRCK05A            NO                           YA02452
         MVI   IOBERRCT,L2         SET RETRY COUNT TO ALLOW     YA02452
*                                  RESET TO COMPLETE            YA02452
ERRCK05A EQU   *                                                YA02452
         TM    IOSFLA,IOSERR       ERP ROUTINE IN CONTROL
         BNO   ERRCK02             NO CONTINUE PROCESSING
         OI    IOSFLA,IOSEX        SET ERP IN CONTROL
         OI    IOBFLAG1,IOBIOERR   ALSO IN IOB
         CLI   IOBERRCT+L1,ZERO    NORMAL RETRY
         BE    ERRCK01             YES, CONTINUE RETRY CHECKING
         CLI   IOBERRCT+L1,L1      SPECIAL RETURN FOR INT REQ
         BE    ERRM38              YES, BRANCH
         CLI   IOBERRCT+L1,MASK15   LINK ROUTINE SPECIFIED
         BL    EREXCTL1            YES RETURN VIA LINK MOCULE
         B     ERRWTO              RETURN VIA ERROR POST
ERRCK01  TM    IOBCSW+L3,CSWNORM1   NORMAL COMPLETION          YM02568K
         BNZ   ERRCK02             NO NORMAL PROCESSING
         TM    IOBCSW+L4,CSWNORM2   MORE NORMAL COMPLETION     YM02568K
         BNZ   ERRCK02             NO NORMAL PROCESSING
         TM    IOBCSW+L3,CSWUE     THIS UNIT EXCEPTION         YM02568K
         BZ    ERRUPDTE            NO STASTICSTICS UPDATE OR POST
         CLI   ZERO(CCWREG),CCWRDCMD   READ COMMAND
         BNE   ERRCK01A            IF NOT, CHECK FOR INHIBIT
         TM    TPCODE(CCWREG),CCWRRTXT   READ RESPONSE TO TEXT BIT
         BNO   ERRUPDTE            IF NOT SET, BRANCH TO DO SDR OR POST
         TM    TPCODE(CCWREG),L255-CCWRRTXT-HIBIT   ANY OTHER TP
         BNZ   ERRUPDTE            OP CODE BITS SET?  IF SO, BRANCH
ERRCK01A CLI   ZERO(CCWREG),CCWINHCD   INHIBIT COMMAND
         BE    ERRUPDTE            IF SO, BRANCH TO DO SDR OR POST
ERRCK02 EQU    *
         CLI   IOBECBCC,HIOISS     HAS HIO BEEN ISSUED
         BE    ERRSTA01            YES, RETURN INTERRUPT TO APP
         OI    IOSFLA,IOSERR+IOSEX   INDICATE ERP IN CONTROL
         OI    IOBFLAG1,IOBERRTN+IOBIOERR   ALSO IN IOB
         TM    IOBCSW+L4,ICORCC    INTERFACE OR CONTROL CHECK
         BZ    CHKMORE
         LA    ERREG1,L1
         B     ERRM43
CHKMORE  TM    IOBCSW+L4,CPC       CHANNEL PGM CHECK OCCUR
         BO    ERRM43              YES, GO TO STATUS CHECK RTN
         CLC   IOSSNS(L2),SCC3SNS   SIO CC3 SENSE
         BNE   CHKMORE1            NO
         TM    IOSCC,IOSCC3        SIO CC 3                    @ZA09345
         BO    ERRPST03            YES, BRANCH                 @ZA09345
CHKMORE1 EQU   *
         TM    IOSCC,IOSCC3        SIO CC NOT = 3
         BM    ERRSTA04            IF SIO COND CODE 1, GO TO CONTROL
*                                  LOAD 2 TO HANDLE INITIAL SELECTION
*                                  ERROR.
         TM    IOBCSW+L4,CSWCDC    CHANNEL DATA CHECK?
         BO    ERRM35              IF SO, GO TO SPECIAL ENTRY OF
*                                  CONTROL LOAD 2
         TM    IOBCSW+L3,CSWASCB   ATTEN, STATUS MOD, CTRL UNIT
*                                  END OR BUSY INDICATED
         BNZ   ERRSNO              ENTRY TO ERROR POST
         TM    IOBCSW+L3,CSWUCK    IS THIS UNIT CHECK
         BNO   ERRM43   STATUS CHECK ENTRY TO BRANCHING TABLE  @ZA12155
ERRSTA02 TM    IOBCSW+L3,CSWCDE    BOTH CHAN AND DEV END
         BO    ERRPST04            YES, EXAMINE SENSE BITS
         B     ERRSTA04            NO
ERRUPDTE NI    IOSFLA,L255-IOSERR-IOSEX   TURN OFF ERP FLAGS
         NI    IOBFLAG1,L255-IOBERRTN-IOBIOERR   ALSO IN IOB
         CLI   EWASTUP,ZERO        STAT UPDATE REQUIRED
         BE    RETRY               NO, POST COMPLETE           YM05652K
         TM    EWASTUP+L1,X01      CHANNEL CHECK OCCUR
         BNO   RETRY               NO                          YM05652K
         MVI   EWAIND1,EWAISTAT    GO TO STAT UPDATE           YM05652K
         OI    IOSFLB,IOSLOG       CAUSE UPDATE EXIT
ERRSTA01 EQU   *
         L     NAMEREG,TRANSRTN    LOAD NAME
EREXCTL  EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     XCTL ROUTINE ADDRESS
         BR    ERRETR              EXIT VIA XCTL
         DROP  ERRETR
RETRY    EQU   *                                               YM05652K
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
         B     ERRSTA01            EXIT TO 904B                YM05652K
ERRPST03 EQU   *
         L     UCBREG,IOSUCB       GET ADDR OF UCB            LD YM8278
         LA    UCBREG,ZERO(UCBREG) CLEAR HIGH ORDER BYTE      LD YM8278
         LR    ERREG1,IOSBREG      SAVE IOSB ADDR             LD YM8278
         MVC   EWAIEXIT(L16),EXITADDR MOVE ESTAE LIST         LD YM8278
         LA    PARMREG,EWAIEXIT    POINT TO ESTAE LIST        LD YM8278
         ESTAE MF=(E,(1))          SETUP RECOVERY RTN         LD YM8278
         L     ERREG3,SAVE         SUBPOOL AND LENGTH         LD YM8278
         GETMAIN R,LV=(0)          GET STORAGE FOR PARM WORD  LD YM8278
*                                  AND SAVEAREA               LD YM8278
         LA    NAMEREG,L4(PARMREG) ADDRESS OF SAVEAREA        LD YM8278
         ST    UCBREG,ZERO(PARMREG) INITIALIZE PARM WORD      LD YM8278
         LINK  EPLOC=IECVIOPM      LINK TO CHECK ALT PATH      @ZA09351
*                                            ALT. PATH
         LR    ERREG4,ERREG5       SAVE RETURN CODE           LD YM8278
         L     ERREG3,SAVE         SUBPOOL AND LENGTH         LD YM8278
         FREEMAIN R,LV=(0),A=(1)   FREE STORAGE FOR PARM      LD YM8278
*                                  AND SAVEAREA.              LD YM8278
         ESTAE 0                   CLEAR OUT RECOVERY EXIT     @ZA04868
         LR    IOSBREG,ERREG1      RESTORE IOSB REG           LD YM8278
         LTR   ERREG4,ERREG4       CHECK FOR 0 RTN CODE       LD YM8278
         BZ    RETRY               RC=0, RETRY ON NEW PATH    LD YM8278
         LH    NAMEREG,ERLOD16     ADDRESS OF INT REQ MSG WRITER
         B     EREXCTL             LINK TO MESSAGE WRITER
ERRPST04 EQU   *
         L     ERREG5,CVTPTR       POINT TO CVT
         USING CVTMAP,ERREG5
         L     ERREG5,CVTXITP      LOAD NAME INTERPRETER RTNE
         DROP  ERREG5
ERRM30   BALR  ERRETR,ERREG5       EXIT TO INTERPRETER
         DC    X'03'                         EQUIPMENT CHECK
         DC    AL1(ERRM35-ERRM30-2)
         DC    X'06'                         LOST DATA
         DC    AL1(ERRM36-ERRM30-4)
         DC    X'07'                         TIME OUT
         DC    AL1(ERRM37-ERRM30-6)
         DC    X'01'                         INTERVENTION REQUIRED
         DC    AL1(ERRM38-ERRM30-8)
         DC    X'02'                         BUS OUT CHECK
         DC    AL1(ERRM39-ERRM30-10)
         DC    X'04'                         DATA CHECK
         DC    AL1(ERRM40-ERRM30-12)
         DC    X'05'                         OVERRUN
         DC    AL1(ERRM39-ERRM30-14)
         DC    X'00'                         COMMAND REJECT
         DC    AL1(ERRM35-ERRM30-16)
         DC    X'2F'                         END OF TEST
         DC    AL1(ERRSNO-ERRM30-18)
ERRSNO   EQU   *
         OI    DECERRST(DECBRG),CSWSNO  INDICATE SHOULD NOT OCCUR ERROR
         NI    IOSFLA,L255-IOSERR  TURN OFF ERP IN CONTROL     @ZA04683
*                                    IN IOSB                   @ZA04683
         NI    IOBFLAG1,L255-IOBERRTN    ALSO IN IOB           @ZA04683
*                                  ENTRY FOR COMMAND REJECT
ERRM35   MVI   IOBERRCT+L1,MASK15   IND SPECIAL ENTRY LOAD 2
ERRSTA04 EQU   *
         TM    IOBSTBYT+L1,CSWCHK  CHAIN, PROG OR PROT CHECK?  @ZA12155
         BNZ   ERRSCR06            YES                         @ZA12155
         TM    IOBSTBYT,CSWUE      UNIT EXCEPTION
         BNO   ERRSCR05            NO, NONE OF THE ABOVE
ERRSCR04 LA    NAMEREG,ERLOD13     ADDRESS OF UNIT EXCEPTION RTN
         B     EREXCTL             EXIT ROUTINE
ERRSCR05 OI    DECERRST(DECBRG),CSWSNO  INDICATE SHOULD NOT OCCUR ERROR
         NI    IOSFLA,L255-IOSERR  TURN OFF ERP IN CONTROL     @ZA04683
*                                      IN IOSB                 @ZA04683
         NI    IOBFLAG1,L255-IOBERRTN      ALSO IOB            @ZA04683
ERRSCR06 EQU   *
         L     UCBREG,IOSUCB       POINT TO UCB
         TM    UCBTYP+L1(UCBREG),SWLINE  IS A DISABLE REQUIRED
         BZ    ERRSCR07            NO GO TO OBR ROUTINE
         MVI   IOBERRCT+L1,MASK15   SPECIAL ENTRY INDICATOR
         L     NAMEREG,ERLOD9      ADDRESS OF DISABLE ROUTINE
         B     EREXCTL             EXIT
ERRSCR07 OI    IOSFLB,IOSLOG       INDICATE STAT UPDATE REQUIRED
         LH    NAMEREG,ERLOD5      ADDRESS OF POST ROUTINE
         B     EREXCTL             EXIT
ERSTA4Q  EQU   *
         LA    ERREG1,ERRRTCON(ERREG1)   ENTRY FOR CDC, INITIAL
*                                  SELECTION ERRORS AND EQUIPMENT CHECK
ERRM43   LA    ERREG1,ERRRTCON(ERREG1)   ENTRY FOR STATUS CHECK
EREXCTL1 LA    ERREG1,ERRRTCON(ERREG1)   ENTRY FOR A READ-SKIP OR
*                                  BREAK WHICH WERE ISSUED TO RECOVER
*                                  FROM LOST DATA ON A READ
*                                  OR UNIT EXCEPTION ON A WRITE
*                                  ENTRY FOR BUS-OUT CHECK
ERRM39   LA    ERREG1,ERRRTCON(ERREG1)   ENTRY FOR OVERRUN
ERRWTO   LA    ERREG1,ERRRTCON(ERREG1)   ENTRY FOR ERROR POST
ERRM36   LA    ERREG1,ERRRTCON(ERREG1)   ENTRY FOR LOST DATA
ERRM38   LA    ERREG1,ERRRTCON(ERREG1)   ENTRY FOR INTERVENTION REQ
ERRM37   LA    ERREG1,ERRRTCON(ERREG1)   ENTRY FOR TIME OUT
ERRM40   AH    ERREG1,ERLOD1       ADD BTAM INTERFACE VALUE
         LR    NAMEREG,ERREG1      SET UP LINK REGISTER
         B     EREXCTL             EXIT VIA LINK MODULE
         EJECT
**********                                                    LD YM8278
*                                                             LD YM8278
*        ESTAE EXIT ROUTINE                                   LD YM8278
*                                                             LD YM8278
**********                                                    LD YM8278
         SPACE 2
EXITRTN  EQU   *                                              LD YM8278
         DROP  BASREG                                         LD YM8278
         BALR  BASREG,ZERO                                    LD YM8278
         USING *,BASREG            ROUTINE ADDRESSABILITY     LD YM8278
*              PROCEED WITH ABEND                             LD YM8278
         SETRP
         BR    ERRETR              RETURN TO TASK RECOVERY    LD YM8278
         EJECT
**********
*
*        CONSTANTS
*
**********
         SPACE 2
EXITADDR ESTAE EXITRTN,MF=L                                   LD YM8278
         SPACE 2
TRANSRTN DC    F'9042'             TRANSLATE ROUTINE NAME FOR XCTL
ERLOD9   DC    F'9041'             DISABLE ROUTINE NAME FOR XCTL
ERLOD5   DC    H'5041'             POST ROUTINE NAME FOR XCTL
ERLOD16  DC    H'6042'             INT REQ MSG WRITER NAME FOR XCTL
ERRCN8   DC    H'8'                CONSTANT FOR ARITHMETIC
ERLOD1   DC    H'1041'             BTAM INTERFACE VALUE
SCC3SNS  DC    X'10FE'             SENSE FOR SIOCC=3
         DS    0F
SAVE     DC    X'FF00004C'         SUBPOOL 255 AND LENGTH     LD YM8278
IECVIOPM DC    C'IECVIOPM'                                    LD YM8278
PACHAREA DC    17C'004A'           PATCH AREA FOR MAINTENANCE
**********
*
*        REGISTER DEFINITION
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE
PARMREG  EQU   IOSBREG             PARM REG FOR IECVIOPM      LD YM8278
ERREG2   EQU   2                   WORK REG/RQE BASE
EWAREG   EQU   3                   EWA BASE
CCWREG   EQU   4                   CCW POINTER
DECBRG   EQU   5                   DECB BASE
BASREG   EQU   6                   PROGRAM BASE
UCBREG   EQU   7                   UCB BASE/WORK REG
DCBREG   EQU   8                   DCB BASE
ERREG1   EQU   9                   WORK REG
IOBREG   EQU   10                  IOB BASE
ERREG4   EQU   11                  WORK REG
ERREG6   EQU   12                  WORK REG
NAMEREG  EQU   13                  LINK REG/WORK REG
ERRETR   EQU   14                  LINK REG/RETURN REG
ERREG5   EQU   15                  WORK REG/LINK REG
**********
*
*        MISCELLANEOUS EQUATES
*
**********
ZERO     EQU   0                   NUMBER AND DISPLACEMENT
L1       EQU   1                   NUMBER AND DISPLACEMENT
L2       EQU   2                   NUMBER AND DISPLACEMENT
L3       EQU   3                   NUMBER AND DISPLACEMENT
L4       EQU   4                   NUMBER AND DISPLACEMENT
L5       EQU   5                   NUMBER AND DISPLACEMENT
L6       EQU   6                   NUMBER AND DISPLACEMENT
L7       EQU   7                                               @ZA04166
L8       EQU   8                   LENGTH AND DISPLACEMENT
L16      EQU   16                                             LD YM8278
L124     EQU   124                 NUMBER AND DISPLACEMENT
L255     EQU   255                 NUMBER AND DISPLACEMENT
ERRRTCON EQU   1000                NUMBER USED TO CREATE BTAM LOAD NAME
ERLOD13  EQU   3042                UNIT EXCEPTION RTN NAME FOR XCTL
DECERRST EQU   29
HIBIT    EQU   X'C0'     END OF CHANNEL PROG AND 1ST TIME PCI BITS
MASK15   EQU   X'0F'
SWLINE   EQU   X'90'
TPCODE   EQU   5
UCBTYP   EQU   16
HIOISS   EQU   X'48'               HALT IO ISSUED
IOBRESP  EQU   X'01'               RESET POLL FLAG IN IOBFLAG2  YA02441
X01      EQU   X'01'               CHANNEL CHECK IN EWASTUP
**********
*
*        CCW AND CSW RELATED EQUATES
*
**********
CCWINHCD EQU   X'0A'               INHIBIT COMMAND
CCWRDCMD EQU   X'02'               READ COMMAND
CCWRRTXT EQU   X'20'               READ RESPONSE-TO-TEXT TP CODE
CSWASCB  EQU   X'F0'
CSWCDC   EQU   X'08'               CHANNEL DATA CHECK
CSWCDE   EQU   X'0C'               CHANNEL AND DEVICE END
CSWSNO   EQU   X'40'
CSWUCK   EQU   X'02'               UNIT CHECK
CSWUE    EQU   X'01'
CSWNORM1 EQU   X'F2'
CSWNORM2 EQU   X'BF'
ICORCC   EQU   X'06'               CONTROL CHECKS
CPC      EQU   X'20'               CHANNEL PROGRAM CHECK
CSWCHK   EQU   X'31'    CHAINING , PROG OR PROT CHECK          @ZA12155
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IHAPSA
         EJECT
         IECDIOCM
         EJECT
         IECDTCCW
         EJECT
         IECDIOSB
         EJECT
         IHASDWA
         END
