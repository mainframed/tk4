         TITLE 'IECTLERP - BTAM LINE ERROR PRINT ROUTINE'
IECTLERP CSECT
         SPACE 2
* STATUS -          CHANGE LEVEL 000    JANUARY 16, 1967
         SPACE 2
***********************************************************************
*                                                                     *
*        LINE ERROR RECORDING PRINT ROUTINE (LERPRT)                  *
*                                                                     *
* FUNCTION -        THIS ROUTINE PRINTS LINE ERROR DATA FOR THE       *
*                   FOLLOWING -                                       *
*                                                                     *
*                     1. DATA CHECK ERRORS                            *
*                     2. INTERVENTION REQUIRED ERRORS                 *
*                     3. TIME OUT ERRORS                              *
*                                                                     *
* ENTRY -           FIRST EXECUTABLE INSTRUCTION                      *
*                                                                     *
* INPUT -           1. REGISTER 0 CONTAINS THE RELATIVE LINE NUMBER.  *
*                      IF REGISTER 0=0,ALL LINES ASSOCIATED WITH THE  *
*                      LINE GROUP WILL BE PRINTED.                    *
*                   2. REGISTER 1 CONTAINS A POINTER TO THE ASSOCIATED*
*                      LINE GROUP DATA CONTROL BLOCK.                 *
*                                                                     *
* OUTPUT -          LINE ERROR MESSAGES TO THE OPERATOR CONSOLE       *
*                                                                     *
* EXTERNAL ROUTINE - SVC 35 - WRITE TO OPERATOR                       *
*                                                                     *
* EXIT -            RETURN TO USER VIA REGISTER 14                    *
*                                                                     *
* ATTRIBUTES -      THIS ROUTINE IS REENTRANT.                        *
*                                                                     *
***********************************************************************
         USING *,BASREG
         STM   LNKREG,CLRREG,12(SAVREG) SAVE PROBLEM PROG REGS
         SR    CLRREG,CLRREG           CLEAR REGISTER            A27630
         LTR   PARRG1,PARRG1           TEST REGISTER U FOR POS   A27630
         BP    NOCOMPL                 BRANCH IF YES             A27630
         LA    CLRREG,1                IF NO, CLEAR=YES          A27630
         LCR   PARRG1,PARRG1           CONVERT TO POSITIVE ADDRESS27630
NOCOMPL  TM    DCBERROP(PARRG1),DCBLECT LINE ERROR RECORDING SPECA27630
         BZ    LERPRT11                NO EXIT                   A27630
         L     DEBREG,DCBDEBAD(PARRG1)  ADDRESS OF DEB
         LA    PTRREG,DEBUCBAD-4(DEBREG) ADDRESS OF FIRST UCB POINTER-4
         SR    LERBRG,LERBRG            INITIALIZE LERB REGISTER
         LTR   RLNREG,PARRG0            CHECK FOR RELATIVE LINE PRINT
         BZ    LERPRT00                 LINE GROUP PRINT REQUIRED
         BCTR  RLNREG,PARRG0            ADJUST RLN FOR FACTORING
         LR    LERBRG,RLNREG            FACTOR TO MULTIPLY REG
         MH    LERBRG,LERPCN20          RESOLVE LERB INDEX
         SLL   RLNREG,2                 RESOLVE UCB INDEX
         LA    LNCTRG,1                 SET TO PRINT ONE LINE
         B     LERPRT01                 PRINT RELATIVE LINE
LERPRT00 SR    LNCTRG,LNCTRG            LINE GROUP PRINT ROUTINE SETUP
         IC    LNCTRG,DEBNMEXT(DEBREG)  NUMBER OF LINES TO BE PRINTED
LERPRT01 A     LERBRG,DCBLERB(PARRG1)   ADDRESS OF LERB BLOCKS
         SH    LERBRG,LERPCN20          ADJUST FOR PRINT ROUTINE
         AR    PTRREG,RLNREG            ADJUST UCB POINTER TO FIRST ENT
LERPRT02 AH    LERBRG,LERPCN20          STEP LERB POINTER TO NEXT ENTRY
         LA    PTRREG,4(PTRREG)         STEP UCB POINTER TO NEXT ENTRY
         L     WRKREG,0(LERBRG)         TOTAL NUMBER OF TRANSMISSIONS
         SR    CNTREG,CNTREG            INITIALIZE IC REGISTER
         IC    CNTREG,LERBTHCT(LERBRG)  PICK UP THRESHOLD COUNT
         AR    WRKREG,CNTREG            NEW COUNT
         ST    WRKREG,0(LERBRG)         UPDATE TOTAL COUNTER
         CVD   WRKREG,LERPDBWD
         UNPK  OUTMSG03+6(8),LERPDBWD+3(5) MOVE INTO MSG IN ZONE FORMAT
         OI    OUTMSG03+13,MASK3        MASK OUT SIGN
         LH    WRKREG,LERBDCTC(LERBRG)  PICK UP TOTAL DATA CHECK COUNT
         IC    CNTREG,LERBDCNC(LERBRG)  PICK UP DATA CHECK NEW COUNT
         AR    WRKREG,CNTREG            NEW TOTAL
         STH   WRKREG,LERBDCTC(LERBRG)  UPDATE TOTAL COUNTER
         CVD   WRKREG,LERPDBWD
         UNPK  OUTMSG04+3(5),LERPDBWD+5(3) MOVE MSG INTO  AREA W/ZONES
         OI    OUTMSG04+7,MASK3         MASK OUT SIGN
LERPRT04 LH    WRKREG,LERBIRTC(LERBRG)  PICK UP OLD TOTAL COUNT
         IC    CNTREG,LERBIRNC(LERBRG)  PICK UP NEW COUNT
         AR    WRKREG,CNTREG            NEW TOTAL
         STH   WRKREG,LERBIRTC(LERBRG)  UPDATE TOTAL COUNTER
         CVD   WRKREG,LERPDBWD
         UNPK  OUTMSG05+3(5),LERPDBWD+5(3) MOVE COUNT TO MSG WITH ZONES
         OI    OUTMSG05+7,MASK3         MASK OUT SIGN
LERPRT06 LH    WRKREG,LERBTOTC(LERBRG)  TIME OUT TOTAL COUNT
         IC    CNTREG,LERBTONC(LERBRG)  TIME OUT NEW COUNT
         AR    WRKREG,CNTREG            NEW TOTAL
         STH   WRKREG,LERBTOTC(LERBRG)  UPDATE TOTAL COUNTER
         CVD   WRKREG,LERPDBWD
         UNPK  OUTMSG06+3(5),LERPDBWD+5(3) PUT COUNT INTO MSG WITH ZONE
         OI    OUTMSG06+7,MASK3         MASK OUT SIGN
         XC    LERBTHCT(4,LERBRG),LERBTHCT(LERBRG) CLEAR INTERIM CTRS
         L     WRKREG,0(PTRREG)         ADDRESS OF UCB
         MVC   OUTMSG02(3),UCBNAME(WRKREG) MOVE IN LINE ADDRESS
         LR    WRKREG,BASREG            SAVE BASE
         LR    CNTREG,PARRG1            SAVE PARAMETER REGISTER
         LR    PARRG0,PARRG1           CONSOLE ID TO R0          A29589
         SRL   PARRG0,24               NOW TO LOW ORDER BYTE     A29589
         LTR   PARRG0,PARRG0           SEE IF MCS FLAGS SHOULD   A29589
*                                      BE CHANGED                A29589
         BZ    LERPRT12                NO                        A29589
         MVI   MSGAREA+2,X'40'         CHANGE MCS FLAGS SO THAT  A29589
*                                      LERB IS SENT TO REQUESTINGA29589
*                                      CONSOLE                   A29589
LERPRT12 EQU   *                                                 A29589
         LA    PARRG1,MSGAREA           WTO PARAMETER VALUE
         WTO   MF=(E,(1))               WRITE MESSAGE
         LR    BASREG,WRKREG            RESTORE BASE
         LR    PARRG1,CNTREG            RESTORE PARAMETER REG
LERPRT09 LTR   CLRREG,CLRREG            COUNTER CLEAR REQUIRED
         BZ    LERPRT10                 NO EXIT
         MVI   0(LERBRG),NONAME         READY TO CLEAR COUNTERS
         MVC   1(13,LERBRG),0(LERBRG)   CLEAR}
LERPRT10 BCT   LNCTRG,LERPRT02          BRANCH IF MORE LINES TO PROCESS
LERPRT11 EQU   *
         LM    LNKREG,CLRREG,12(SAVREG) RESTORE PROBLEM PROG REGISTERS
         BR    LNKREG                   RETURN
LERPDBWD DC    D'0'
MSGAREA  WTO   'IEC802I     LINE TOTALS TRANS=         DC=      IR=    X
                 TO=      ',MF=L,ROUTCDE=(8),DESC=(4)              000H
OUTMSG02 EQU   MSGAREA+12                                          000H
OUTMSG03 EQU   MSGAREA+28                                          000H
OUTMSG04 EQU   MSGAREA+43                                          000H
OUTMSG05 EQU   MSGAREA+52                                          000H
OUTMSG06 EQU   MSGAREA+61                                          000H
LERPCN20 DC    AL2(20)
COUNTSW  DC    X'00'
DCBDEBAD EQU   44
DCBERROP EQU   33
DCBLECT  EQU   X'08'
DEBUCBAD EQU   32
DEBNMEXT EQU   16
DCBLERB  EQU   52
LERBTHCT EQU   10
LERBDCTC EQU   4
UCBNAME  EQU   13
LERBDCNC EQU   11
LINEACT  EQU   X'10'
MASK3    EQU   X'F0'
LERBIRTC EQU   6
LERBIRNC EQU   12
LERBTOTC EQU   8
LERBTONC EQU   13
MASKALL  EQU   255
NONAME   EQU   0
PARRG0   EQU   0
PARRG1   EQU   1
DEBREG   EQU   2
LNCTRG   EQU   3
PTRREG   EQU   4
UCBREG   EQU   5
RLNREG   EQU   6
LERBRG   EQU   7
WRKREG   EQU   8
CNTREG   EQU   9
CLRREG   EQU   10
SAVREG   EQU   13
LNKREG   EQU   14
BASREG   EQU   15
         END
