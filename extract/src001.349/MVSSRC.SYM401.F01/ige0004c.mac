         TITLE 'IGE0004C - BSC ERP CONTROL MODULE'
IGE0004C CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0004C                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM BSC CONTROL ERP                                             *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 001                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS ROUTINE IS THE FIRST LOAD OF EACH ERP RETRY ATTEMPT.        *
*    IT ROUTINE PERFORMS THE FOLLOWING FUNCTIONS:                     *
*        * PROVIDES LINKAGE TO PROPER ERP ROUTINE FOR TYPE OF         *
*          ERROR ENCOUNTERED.                                         *
*        * DETERMINES IF ERP RETRIES ARE SUCCESSFUL.                  *
*        * MAINTAINS SOR TABLE FOR TEMPORARY ERRORS.                  *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (100 BYTES)                                             *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        980 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD.  *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0004C                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        ALTERED CHANNEL PROGRAM                                      *
*        SETS INDICATOR IN DECERRST FOR SHOULD NOT OCCUR ERROR        *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        IOS INTERPRETER                                              *
*        IECVTCCW (IOS CCW TRANSLATOR)                                *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*        TCCW         (MAPPED BY IECDTCCW)                            *
*        IOCOM        (MAPPED BY IECDIOCM)                            *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YM05652K (1/24/74)                                               *
*    YM05680K (1/24/74)                                               *
*    YM07711  (4/2/74)                                                *
*    YM08278 (4/16/74)                                                *
*    ZA02815  (01/17/75)                 ZM00634  (01/27/75)          *
*    AZ04166  (08/25/75)          AZ04681  (08/26/75)                 *
*    AZ04868  (08/27/75)          AZ04869  (08/27/75)                 *
*    AZ09345  (04/08/76)                                              *
*    AZ09351  (04/08/76)  DELETES APAR AZ04869                        *
*    AZ10030  (04/12/76)                                              *
*    AZ24270  (09/29/77)                                              *
*    AZ28893   12/20/77                                               *
*    AZ31667   04/06/78                                               *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         USING RQE,ERREG5
         USING IECTDECB,DECBRG
         USING IHADCB,DCBREG
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         B     BEGIN               BRANCH AROUND ID
         DC    C'IGE0004C'         MODULE ID
         DC    XL3'017983'         PTF NUMBER
         DC    CL8'&SYSDATE'
         DS    0H
BEGIN    EQU   *
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         L     ERREG1,CVTPTR       BASE CVT                    YM02594K
         USING CVTMAP,ERREG1                                   YM02594K
         L     ERREG1,CVTIXAVL     POINT TO IOCOM              YM02594K
         DROP  ERREG1                                          YM02594K
         USING IOCOM,ERREG1                                    YM02594K
         L     ERREG1,IOCTCCW      POINT TO TRANSLATE ROUTINE  YM02594K
         ST    ERREG1,EWAITRAN     SAVE FOR DURATION OF ERROR  YM02594K
         DROP  ERREG1                                          YM02594K
         CLI   IOSDVRID,IOSXCPID   EXCP I/O
         BNE   BSCUET09            NO, GET OUT
         CLI   EWAIND1,EWAILD00    DIRECT ENTRY FROM OTHER ERP YM05680K
         BE    ERRBSC1             YES                         YM05680K
         TM    IOSFLA,IOSERR       FIRST ENTRY
         BO    NOTFIRST            NO
         XC    EWASTUP(L2),EWASTUP   CLEAR STAT UPDATE DATA AREA
         XC    EWAITCCW(L124),EWAITCCW   CLEAR EWA USER AREA
         MVC   EWAIOVST(L4),IOSVST   SAVE ADDRESS              YM02594K
*        ONE LINE OF CODE DELETED BY-------------------->      @ZA04681
         B     FIRST               BRANCH AROUND IOBCSW MAP
NOTFIRST EQU   *
         L     CCWREG,IOSCC        LOAD CSW                    YM02594K
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT CONDITION CODE  YM02594K
*
*              ONE LINE DELETED FOR ----->                  LD @ZM00634
*
         LR    ERREG3,CCWREG       SET PARAMETER REG           YM02594K
         LR    ERREG1,IOSBREG      SAVE IOSB BASE              YM02594K
         DROP  IOSBREG                                         YM02594K
         TM    EWAIND0,EWAIPMAP    PARALLEL MAPPING DONE       YM02594K
         BNO   HISTCCW             NO                          YM02594K
         L     IOSBREG,EWAITCCW    BASE MY TCCW                YM02594K
         B     GOTRANS             GO TO TRANSLATOR            YM02594K
HISTCCW  EQU   *                                               YM02594K
         L     IOSBREG,RQETCCW     GET IOS TCCW                YM02594K
GOTRANS  EQU   *                                               YM02594K
         TM    RQETYPE,RQE1TO1     DOES V EQUAL R ?            @ZA04166
         BZ    GETLOCK             NO GO MAP IOBCSW            @ZA04166
         USING IOSB,ERREG1         SET IOSB ADDRESSIBLITY      @ZA04166
         MVC   IOBCSW(L7),IOSCSW  MOVE CSW FROM IOSB TO IOB    @ZA04166
         DROP  ERREG1                                          @ZA04166
         LR    IOSBREG,ERREG1   REINITILIZE IOSB REG           @ZA10030
         B     FIRST               BRANCH AROUND IOBCSW MAP    @ZA04166
         DROP  ERREG5                                          YM02594K
GETLOCK  SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                        *
               RELATED=(ERP,IGE0004C(FREELOCK))             LD @ZA02815
         USING TCCW,IOSBREG                                    YM02594K
         MVI   TCCWOPTN,ZERO       CLEAR OUT OPTION FIELD      YM02594K
         OI    TCCWOPTN,TCCWCSWX   SET FOR CSW BACK TRANS      YM02594K
         L     ERREG5,EWAITRAN     POINT TO IECVTCCW           YM02594K
         BALR  ERRETR,ERREG5       GO TO TRANSLATOR            YM02594K
FREELOCK SETLOCK RELEASE,TYPE=LOCAL,                                   *
               RELATED=(ERP,IGE0004C(GETLOCK))              LD @ZA02815
         LR    CCWREG,ERREG3       LOAD FOR LOAD ADDR          YM02594K
*              ONE LINE DELETED FOR ----->                  LD @ZM00634
         ST    CCWREG,EWAISAVE     SET CCW ADDRESS IN AREA     YM02594K
         MVC   IOBCMDA(L3),EWAISAVE+L1   MOVE TO CSW           YM02594K
         DROP  IOSBREG                                         YM02594K
         LR    IOSBREG,ERREG1      REBASE IOSB                 YM02594K
         USING IOSB,IOSBREG                                    YM02594K
         MVC   IOBSTBYT(L4),IOSTATUS   MOVE STATUS AND COUNT   YM02594K
FIRST    EQU   *
         MVC   IOBSENS0(L2),IOSSNS   MOVE SENSE IOSB TO IOB
         MVC   IOBSIOCC(L1),IOSCC   MOVE SIO CC IOSB TO IOB
         MVC   IOBECBCC(L1),IOSCOD   MOVE ECB CC IOSB TO IOB
ERRBSC1  EQU   *
         OI    IOSFLA,IOSEX        INSURE FLAG IS ON
         OI    IOBFLAG1,IOBIOERR   ALSO IN IOB
         SR    ERREG6,ERREG6       CLEAR WORK REGISTER
         LA    ERREG2,ERRRTCON     SET UP FOR INDEXING
         SR    ERREG1,ERREG1       CLEAR LINKAGE REGISTER
         SR    ERREG4,ERREG4       ZERO ERROR IN CONTROL
         TM    DCBOFLGS,X11        IS CLOSE IN PROCESS
         BO    BSCCT025            IF SO, BRANCH
         TM    IOBCSW+L4,CSWCCK    CHAN OR INTERFACE CTL CHECK
         BZ    NOTCHAN             NO CONTINUE                 @ZA31667
         BCT   ERREG1,BSCCT016     SUBTRACT 1 FROM REG 9       @ZA31667
NOTCHAN  L     CCWREG,IOBFL3       ADDRESS OF CCW              @ZA31667
         LA    CCWREG,ZERO(CCWREG)   CLEAR HI ORDER BYTE
         SH    CCWREG,ERRCN8       ADJUST TO FAILING CCW
         OI    IOSFLB,IOSMSG       INDICATE I/O ERR TO MSG WT
         MVC   IOBWORK(L1),TPOPCODE(CCWREG)   SETUP WORK AREA
         NI    IOBWORK,L255-HIBIT  CLEAR UNUSED BITS
         CLI   DECTYPE+1,RDINIT    READ INITIAL ?              @ZA28893
         BNE   ERRESTPL            NO, BRANCH                  @ZA28893
         TM    IOBFLAG2,RESETPL    RESETPL ISSUED ?            @ZA28893
         BNO   ERRESTPL            NO, BRANCH                  @ZA28893
         MVI   IOBERRCT,ERRMAXCT   SET RETRY TO MAX            @ZA28893
ERRESTPL EQU   *                                               @ZA28893
         TM    IOSFLA,IOSERR       ERP ROUTINE IN CONTROL
         BNO   BSCSAVE             NO,  SAVE SENSE, CSW INFO
         TM    IOBCSW+L3,CSWUCK    UNIT CHECK OCCUR
         BO    BSCCT003            YES CONTINUE FLOW
         STH   ERREG6,IOBSENS0     CLEAR SENSE INFO
BSCCT003 EQU   *
         IC    ERREG6,IOBERRCT+L1  BRANCH OFFSET TO LOADS
         LTR   ERREG6,ERREG6       ERROR RETURN
         BP    BSCCT008(ERREG6)    YES, GO THERE
         CLI   L5(CCWREG),ZERO     ANY TP OP CODE
         BNE   BSCCT002            YES
         TM    IOBCSW+L3,XF3       ANYTHING OTHER THAN CE/DE
         BNZ   BSCCT002            YES
         TM    IOBCSW+L3,CSWCDE    CHECK FOR BOTH CE AND DE
         BO    SETMAX              IF BOTH ON SET MAX RETRY
BSCCT002 OI    IOSFLA,IOSEX+IOSERR   INDICATE ERP IN CONTROL
         OI    IOBFLAG1,IOBERRTN+IOBIOERR   ALSO IN IOB
         CLC   IOSSNS(L2),SCC3SNS   SIO CC3 SENSE
         BNE   NOSIOCC3            NO
         TM    IOSCC,IOSCC3        SIO CC 3                    @ZA09345
         BO    BSCCT005            YES, BRANCH                 @ZA09345
NOSIOCC3 EQU   *
         TM    IOSCC,IOSCC1        SIO COND CODE 1
         BO    BSCCT016            YES
         TM    IOBCSW+L3,CSWASCB   ATTEN, STATUS MOD, CONTROL UNIT END
*                                  OR BUSY INDICATED
         BNZ   BSCCT008            YES, SNO & ERROR POST
         TM    IOSTSA,CSWUCK       UNIT CHECK
         BO    BSCCT009            YES
         TM    IOSTSB,CSWCHAIN     CHAINING CHECK
         BO    BSCS001             YES
         TM    IOSTSB,CSWPROG      PROGRAM CHECK
         BO    BSCS001             YES
         TM    IOSTSB,CSWPROT      PROTECTION CHECK
         BO    BSCS001             YES
         TM    IOSTSA,CSWUNITE     UNIT EXCEPTION
         BO    BSCUE0AA            YES
         TM    IOSTSB,CSWCDC       CHANNEL DATA CHECK
         BO    BSCCT016            YES
BSCS002  LTR   ERREG4,ERREG4       FIRST TIME THRU ERP (SET BY BSCSAVE)
         BZ    BSCCT004            NO, POST  NORMAL RETURN
         OI    DECERRST,DECSNO     SET SHOULD NOT OCCUR
BSCS001  OI    IOSFLB,IOSLOG       INDICATE OBR RECORDING
         B     BSCCT022            LINK TO ERROR POST
SETMAX   MVI   IOBERRCT,ERRMAXCT   SET RETRY TO MAX
         OI    DECERRST,DECSNO     SET SHOULD NOT OCCUR
         B     BSCCT022            LINK TO ERROR POST
BSCCT025 NI    IOSFLA,L255-IOSERR  TURN OFF IOB EXCEPTION
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
         B     RETRY               ERROR EXCP
BSCCT004 NI    IOSFLA,L255-IOSEX-IOSERR   TURN OFF ERP CONTROL INDS.
         NI    IOBFLAG1,L255-IOBERRTN-IOBIOERR   ALSO IN IOB
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
         NI    EWASTUP,FIRSTBIT    TEST AND SET X'80'
         BZ    RETRY               NO, POST COMPLETE
         MVI   IOBSENS0,ZERO
         OI    IOSFLB,IOSLOG       SET LOGOUT REQUIRED
         MVI   EWAIND1,EWAISTAT    STAT UPDATE EXIT            YM05652K
RETRY    EQU   *
         L     NAMEREG,TRANSRTN    LOAD NAME FOR 904B          YM02594K
         B     XCTLOUT             XCTL
BSCSAVE  MVC   IOBERCCW+L16(L1),IOBSENS0   MOVE SENSE INFO TO AREA
         MVC   IOBERCCW+L17(L7),IOBCSW   MOVE CSW TO AREA
         XC    IOBWORK+L1(L3),IOBWORK+L1   CLEAR WORK AREA
         XC    IOBERCCW(L8),IOBERCCW   CLEAR ERP CCW AREA
         LR    ERREG4,IOBRG
         B     BSCCT002            SET ERP IN CONTROL
BSCCT005 EQU   *                                              LD YM8278
         L     ERREG2,IOSUCB       GET ADDR OF UCB            LD YM8278
         LA    ERREG2,ZERO(ERREG2) CLEAR HIGH ORDER BYTE      LD YM8278
         LR    ERREG1,IOSBREG      SAVE IOSB ADDR             LD YM8278
         MVC   EWAIEXIT(L16),EXITADDR MOVE ESTAE LIST         LD YM8278
         LA    PARMREG,EWAIEXIT    POINT TO ESTAE LIST        LD YM8278
         ESTAE MF=(E,(1))          SETUP RECOVERY RTN         LD YM8278
         L     ERREG3,SAVE         SUBPOOL AND LENGTH         LD YM8278
*                                  GET STORAGE FOR PARM WORD  LD YM8278
         GETMAIN R,LV=(0)          AND SAVEAREA               LD YM8278
         LA    NAMEREG,L4(PARMREG) ADDRESS OF SAVEAREA        LD YM8278
         ST    ERREG2,ZERO(PARMREG) INITIALIZE PARM WORD      LD YM8278
         LINK  EPLOC=IECVIOPM      LINK TO CHECK ALT PATH      @ZA09351
*                                            ALT. PATH
         LR    ERREG4,ERREG5       SAVE RETURN CODE           LD YM8278
*                                  FREE STORAGE FOR PARM      LD YM8278
         L     ERREG3,SAVE         SUBPOOL AND LENGTH         LD YM8278
         FREEMAIN R,LV=(0),A=(1)   AND SAVEAREA               LD YM8278
         ESTAE 0                   CLEAR OUT RECOVERY RTN      @ZA04868
         LR    IOSBREG,ERREG1      RESTORE IOSB REG           LD YM8278
         LTR   ERREG4,ERREG4       CHECK FOR 0 RTN CODE       LD YM8278
         BZ    RETRY               RC=0, RETRY ON NEW PATH    LD YM8278
         LH    NAMEREG,ERLOD16     INT REQ MSG WRITER ADDRESS
         B     XCTLOUT             LINK TO MSG WRITER
BSCCT009 TM    IOBCSW+L3,CSWCDE    BOTH CHAN AND DEV END OFF
         BZ    BSCCT016            INITIAL SELECTION
BSCCT012 EQU   *
         L     ERREG5,CVTPTR       POINT TO CVT
         USING CVTMAP,ERREG5
         L     ERREG5,CVTXITP      POINT TO INTERPRETER
         DROP  ERREG5
BSCCT014 BALR  ERRETR,ERREG5       EXIT TO INTERPRETER RTNE
         DC    X'03'                         EQUIPMENT CHECK
         DC    AL1(BSCCT016-BSCCT014-2)
         DC    X'06'                         LOST DATA
         DC    AL1(BSCCT018-BSCCT014-4)
         DC    X'07'                         TIME OUT
         DC    AL1(BSCCT020-BSCCT014-6)
         DC    X'01'                         INTERVENTION REQUIRED
         DC    AL1(BSCCT021-BSCCT014-8)
         DC    X'02'                         BUS-OUT CHECK
         DC    AL1(BSCCT017-BSCCT014-10)
         DC    X'04'                         DATA CHECK
         DC    AL1(BSCCT023-BSCCT014-12)
         DC    X'05'                         OVERRUN
         DC    AL1(BSCCT017-BSCCT014-14)
         DC    X'00'                         COMMAND REJECT
         DC    AL1(BSCCT016-BSCCT014-16)
         DC    X'2F'                         END OF TEST
         DC    AL1(BSCCT008-BSCCT014-18)
BSCCT008 OI    DECERRST,CSWSNO     SET SHOULD NOT OCCUR ERR
BSCCT016 AR    ERREG1,ERREG2       ENTRY EQUIP CHECK
BSCCT017 AR    ERREG1,ERREG2       ENTRY BUSOUT OVERRUN
BSCCT018 AR    ERREG1,ERREG2       ENTRY LOST DATA
         NOP   ZERO                NO OPERATION INSTRUCTION   LD YM7711
         OC    EWASTUP(L1),IOBSENS0   SET FOR STAT UPDATE
BSCCT019 AR    ERREG1,ERREG2       ENTRY SPEC RETURN
BSCCT020 AR    ERREG1,ERREG2       ENTRY 2ND LOAD T/O RTN
BSCCT021 AR    ERREG1,ERREG2       ENTRY INTERVENTION REQ
BSCCT022 AR    ERREG1,ERREG2       ENTRY ERROR POST
BSCCT023 LA    NAMEREG,BSCLOD1(ERREG1)  ENTRY DATA CHECK
         B     XCTLOUT             LINK TO NEXT ERP MODULE
BSCUE0AA CLI   ZERO(CCWREG),CCWREAD   IS THIS READ OPERATION
         BNE   BSCUE0TP            NO
         LTR   ERREG4,ERREG4       FIRST TIME THRU ERP
         BZ    BSCEREAD            NO, CHECK TP-OP CODE
         CLI   IOBWORK,X11         READ TEXT
         BNE   BSCUE0A1            NO, LOAD UNIT EXCEPTION
         LH    ERREG4,L6(CCWREG)   PICK UP ORIG CT
         BCTR  ERREG4,ZERO         SUBTRACT ONE
         CH    ERREG4,IOBCSW+L5    THIS COUNT = RESIDUAL COUNT
         BNE   CHKSOHR             NO, CHK FOR SOH%R MSG
         L     ERREG4,ZERO(CCWREG)   ENQ POINTER
         CLC   ZERO(L1,ERREG4),DCBBSENQ   ENQ RECEIVED
         BNE   BSCUE0A1            NO, LOAD UNIT EXCEPTION
         TM    DCBBFTEK,DYNBUF     DYNAMIC BUFFER
         BO    BSCUET09            YES, POST WITH ERROR
         CLI   DECTYPE+L1,X13      RD CONT/GRAPHICS
         BE    BSCUETAB            YES, BRANCH
         CLI   DECTYPE+L1,X17      RD REPEAT/GRAPHICS
         BNE   BSCUE0A1            NO, LOAD UNIT EXCEPTION
BSCUETAB SH    CCWREG,ERRCN16      BACK UP TO WR GRAPHICS
         B     BSCUETA7            BRANCH TO EXECUTE
CHKSOHR  L     ERREG4,IOBERINF     GET ADDR OF INPUT AREA
         TM    DCBXCODE,ASCII      IS IT ASCII XMISSION CODE
         BO    USEASCII            YES, TEST FOR SOH%R IN ASCII
         CLC   ZERO(SOHRLNG,ERREG4),EBCSOHR   IS IT AN EBCDIC SOH%R
*                                  MESSAGE
         BNE   BSCUE0A1            NO, LOAD UNIT EXCEPTION
         ST    ERREG4,IOBERINF     SAVE ADDR OF SOH%R MSG
         B     XFERCTL             YES, SET UP TO TRANSFER
*                                  CONTROL TO ERROR POST
USEASCII CLC   ZERO(SOHRLNG,ERREG4),ASCSOHR   IS IT ASCII SOH%R MSG
         BNE   BSCUE0A1            NO, LOAD UNIT EXCEPTION
XFERCTL  L     NAMEREG,LOAD3270    LOAD ADDR OF 3270 ERROR POST
         B     XCTLOUT             EXIT VIA XCTL ROUTINE
BSCUE0A1 L     NAMEREG,ERLOD904
         B     XCTLOUT
BSCUE0TP CLI   IOBWORK,TPOPERP     THIS ERP WR CCW
         BNE   BSCUE0A1            NO, GO TO UNIT EXCEPTION
         L     ERREG4,ZERO(CCWREG)   GET WR CHAR
         CLC   ZERO(L1,ERREG4),DCBBSENQ   IS THIS ERP WR ENQ
         BNE   BSCUE02G            CHECK FOR NAK
         MVI   IOBWORK+L1,RTRNCD3   SET SP RETURN
         B     BSCUET2A            COMPLETE RD SKIP CHAN PROG
BSCUE02G CLC   ZERO(L1,ERREG4),DCBBSNAK   IS THIS ERP WR NAK
         BNE   BSCUE0A1            NO, GO TO UNIT EXCEPTION
         MVI   IOBWORK+L1,RTRNCD8   SET SP RETURN              @ZA24270
BSCUET2A LA    ERREG5,CCWNOP       SET 2ND COMMAND FOR NO-OP
BSCUE02B LA    ERREG2,IOBWORK+L2   DATA ADDRESS
         LA    ERREG4,CCWREAD      SET FIRST COMMAND FOR READ
BSCUET04 EQU   *
BSCUET05 ST    ERREG2,IOBERCCW     STORE DATA ADDR IN CHAN PROG
         STC   ERREG4,IOBERCCW     STORE COMMAND IN 1ST CCW
BSCUET06 ST    ERREG4,IOBERCCW+L4   PUT COUNT IN CCW
         CLI   IOBWORK+L1,ZERO
         BE    BSCUETA6
         MVI   IOBERRCT+L1,RTRNSPCD
BSCUETA6 EQU   *
         CLI   IOBWORK+L1,RTRNCD8   SPEC RETURN OF 88          @ZA24270
         BNE   BSCUE02H            BRANCH IF NOT
BSCUE02J MVI   IOBERCCW+L4,X70     COM CH, SLI, SKIP
         B     BSCUE02F
BSCUE02H CLI   IOBWORK+L1,RTRNCD3   SPEC RETURN OF 83
         BE    BSCUE02J            BRANCH IF YES
         ST    CCWREG,IOBERCCW+L8   STORE TIC ADDR
         MVI   IOBERCCW+L4,CHAINCDE   COM CH, SLI
BSCUE02F STC   ERREG5,IOBERCCW+L8   STORE COMMAND IN 2ND CCW
         ST    ERREG4,IOBERCCW+L12   PUT COUNT IN CCW
         LA    CCWREG,IOBERCCW     ADDR OF NEW CHAN PROG
         B     BSCUETA7            RETRY ERROR
BSCEREAD EQU   *
         CLI   L5(CCWREG),ZERO     IS THIS ERP CCW
         BNE   BSCCT004            NO, POST NORMAL
         L     CCWREG,IOBERCCW+L8   ADDRESS FAILING CCW
BSCUETA7 ST    CCWREG,IOBSTART
BSCUET08 CLI   IOBERRCT,ERRMAXCT   RETRIES EXHAUSTED
         BNL   BSCUET09            YES SET PERM ERROR
         IC    ERREG1,IOBERRCT     PICK UP RETRY COUNT
         LA    ERREG1,L1(ERREG1)   BUMP BY ONE
         STC   ERREG1,IOBERRCT     RESTORE NEW COUNT
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
         B     RETRY               SET FOR EXIT
BSCUET09 NI    IOSFLA,L255-IOSERR   INDICATE PERM ERROR
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
BSCUET0A LA    NAMEREG,BSCLOD2     ADDRESS OF ERROR POST
XCTLOUT  EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUITNE
         DROP  ERRETR
         BR    ERRETR              EXIT VIA XCTL ROUTINE
         EJECT
**********                                                    LD YM8278
*                                                             LD YM8278
*        ESTAE EXIT ROUTINE                                   LD YM8278
*                                                             LD YM8278
**********                                                    LD YM8278
         SPACE 2
EXITRTN  EQU   *                                              LD YM8278
         DROP  BASEREG                                        LD YM8278
         BALR  BASEREG,ZERO                                   LD YM8278
         USING *,BASEREG           ROUTINE ADDRESSABILITY     LD YM8278
*              PROCEED WITH ABEND                             LD YM8278
         SETRP                                                LD YM8278
         BR    ERRETR              RETURN TO TASK RECOVERY    LD YM8278
         EJECT
**********
*
*        CONSTANTS
*
**********
         SPACE 2
EXITADDR ESTAE EXITRTN,MF=L                                   LD YM8278
         SPACE 2
TRANSRTN DC    F'9042'             NAME OF BTAM TRANSLATE ROUTINE
ERLOD904 DC    F'9043'
LOAD3270 DC    F'7042'             3270 ERROR POST IDENT
ERRCN16  DC    H'16'
ERRCN8   DC    H'8'
ERLOD16  DC    H'6042'             WTOR ERP I.D.
SCC3SNS  DC    X'10FE'             SPECIAL SENSE FOR SIOCC=3
EBCSOHR  DC    X'016CD9'           SOH%R SEQUENCE - EBCDIC
ASCSOHR  DC    X'012552'           SOH%R SEQUENCE - ASCII
         DS    0F
SAVE     DC    X'FF00004C'         SUBPOOL 255 AND LENGTH     LD YM8278
IECVIOPM DC    C'IECVIOPM'                                    LD YM8278
PACHAREA DC    25C'004C'           PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
DYNBUF   EQU   X'08'               DYNAMIC BUFFERING
DECSNO   EQU   X'40'               SHOULD NOT OCCUR FLAG
L255     EQU   X'FF'               AND MASK
BSCLOD1  EQU   1043                BSC ERP BASE IDENT
BSCLOD2  EQU   2043                BSC ERROR POST IDENT
CSWCHAIN EQU   X'01'               CHAINING CHECK
CSWPROG  EQU   X'20'               PROGRAM CHECK
CSWPROT  EQU   X'10'               PROTECTION CHECK
CSWUNITE EQU   X'01'               UNIT EXCEPTION
CSWCDC   EQU   X'08'               CHANNEL DATA CHECK
CSWASCB  EQU   X'F0'               ATTEN,STATUS,CTL UNIT,BUSY
CSWCCK   EQU   6                   CHAN CTL AND INTF CTL CHECK
CCWREAD  EQU   2
RESETPL  EQU   X'01'                                           @ZA28893
RDINIT   EQU   X'01'                                           @ZA28893
CSWCDE   EQU   X'0C'               CHANNEL AND DEVICE END
CSWSNO   EQU   X'40'               SHOULD NOT OCCUR
CSWUCK   EQU   X'02'               UNIT CHECK
ERRRTCON EQU   1000                ERP LOAD MODULE INCREMENT
SIOCODE1 EQU   X'10'               START I/O CODE 1
ZERO     EQU   X'00'               LENGTH AND DISPLACEMENT
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L12      EQU   12                  LENGTH AND DISPLACEMENT
L16      EQU   16                  LENGTH AND DISPLACEMENT
L17      EQU   17                  LENGTH AND DISPLACEMENT
L124     EQU   124                 LENGTH AND DISPLACEMENT
XF3      EQU   X'F3'               ANYTHING OTHER THAN CE/DE
X11      EQU   X'11'               CLOSE FLAG
X13      EQU   X'13'               READ CONTINUE/GRAPHICS
X17      EQU   X'17'               READ REPEAT/GRAPHICS
X70      EQU   X'70'               ERROR CCW FLAGS
CCWNOP   EQU   3
CHAINCDE EQU   X'60'
RTRNSPCD EQU   X'14'
RTRNCD3  EQU   X'83'
RTRNCD8  EQU   X'88'                                           @ZA24270
TPOPERP  EQU   X'00'
WLR      EQU   X'40'               SLI FLAG
TPOPCODE EQU   5
HIBIT    EQU   X'C0'
ERRMAXCT EQU   X'07'
ASCII    EQU   X'14'               BIT SETTING FOR ASCII
FIRSTBIT EQU   X'7F'               FIRST BIT OF EWASTUP
SOHRLNG  EQU   3                   3 CHARS ARE NEEDED TO RECOGNIZE
*                                  SOH%R MSG
**********
*
*        REGISTER DEFINITIONS
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
PARMREG  EQU   IOSBREG             PARM REG FOR IECVIOPM      LD YM8278
ERREG2   EQU   2                   WORK REG
ERREG4   EQU   3                   WORK REG
CCWREG   EQU   4                   CCW ADDRESS REG
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
EWAREG   EQU   7                   ERP WORKAREA BASE
BASEREG  EQU   8                   PROGRAM BASE
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG6   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  LINK REG
ERREG5   EQU   15                  LINK REG/WORK REG
         EJECT
         IECTDECB
         EJECT
         DCBD  DSORG=BX,DEVD=BS              DCB DESECT MACRO
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IHAPSA
         EJECT
         IECDTCCW
         EJECT
         IECDIOCM
         EJECT
         IECDIOSB
         EJECT
         IHASDWA
         END
