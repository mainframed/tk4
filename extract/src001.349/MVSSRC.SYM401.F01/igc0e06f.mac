         TITLE ' IGC0E06F - 3270 LOCAL/REMOTE EBCDIC TEST MODULE '
IGC0E06F CSECT
***********************************************************************
*        MAINTENANCE ACTIVITY                                         *
*                                                                     *
*        ZA02332  (11/20/74)                                          *
*        ZA02334  (11/20/74)                                          *
*                                                                     *
***********************************************************************
*             3270 LOCAL/REMOTE EBCDIC TEST MODULE                    *
***********************************************************************
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000         NOVEMBER 5, 1971                        *
*                                                                     *
* FUNCTION:                                                           *
*    THIS MODULE BUILDS PREDEFINED EBCDIC ON-LINE TEST MESSAGES       *
*    FOR THE 3270 DISPLAY SYSTEM - LOCAL OR REMOTE.                   *
*                                                                     *
* ENTRY:                                                              *
*         ENTRY IS TO THE FIRST EXECUTABLE INSTRUCTION VIA XCTL       *
*         FROM IGC0D06F(REMOTE) OR IGC1206F(LOCAL).                   *
*                                                                     *
* INPUT:                                                              *
*    REG 1 - ADDRESS OF THE IOB                                       *
*    REG 4 - TEST TYPE XX VALUE                                       *
*    IOBERINF - ADDRESS OF THE RFT                                    *
*    ON-LINE TEST CHANNEL PROGRAM IN IOB, BUILT BY IGC0D06F           *
*    (REMOTE) OR IGC1206F (LOCAL).                                    *
*                                                                     *
* OUTPUT:                                                             *
*    LOCAL--                                                          *
*                                                                     *
*    TEST MESSAGE (MINUS LINE CONTROL) BUILT IN USER'S AREA.          *
*    COUNT INSERTED IN ERASE/WRITE CCW.                               *
*    IF THE USER'S BUFFER IS NOT LONG ENOUGH (300) BYTES OR           *
*    THE TEST MESSAGE NUMBER IS INVALID, AN ERROR FLAG (IOBINCAM,     *
*    BYTE 0, BIT 6) IS TURNED ON, AND A NOP REPLACES THE ERASE/       *
*    WRITE CCW. THIS WILL CAUSE IGG019PI TO CANCEL THE TEST BY        *
*    RE-ISSUING THE USER'S READ INITIAL.                              *
*                                                                     *
*    REMOTE--                                                         *
*                                                                     *
*    TEST MESSAGE BUILT IN USER'S AREA.                               *
*    COUNT IN WRITE TEXT CCW.                                         *
*    IF AN ERROR IS FOUND, THE ERROR FLAG (SEE LOCAL) IS TURNED       *
*    ON TO CAUSE IGG019MR TO CANCEL THE TEST.                         *
*                                                                     *
*    TESTS BUILT BY THIS MODULE:                                      *
*                                                                     *
*    TEST 23 - 3270 BASIC EBCDIC                                      *
*    TEST 24 - 3270 MODEL 1 ALIGN EBCDIC                              *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*         SVC 0 - EXECUTE CHANNEL PROGRAM                             *
*                                                                     *
* EXITS, NORMAL:                                                      *
*         SVC 3 - RETURN TO IOS                                       *
*                                                                     *
* EXITS, ERROR:                                                       *
*         NONE                                                        *
*                                                                     *
* TABLES/WORK AREAS:                                                  *
*         NONE                                                        *
*                                                                     *
* ATTRIBUTES:                                                         *
*    RE-ENTRANT                                                       *
*                                                                     *
* CHARACTER CODE DEPENDENCY:                                          *
*    NONE                                                             *
*                                                                     *
***********************************************************************
       EJECT
*/*IGC0E06F: CHART*/
*/* E IGC0E06F*/
*/* D (YES,,NO,SETERR) IS BUFFER LONG ENOUGH (300 BYTES)*/
*/* D (YES,,NO,SETERR) IS TEST NUMBER VALID (23 OR 24)*/
*/* P GET MESSAGE LENGTH AND ADDRESS*/
*/*TESTLCL: D (YES,LOCAL,NO,REMOTE) IS DEVICE A LOCAL 3270*/
*/*LOCAL: D (NO,,YES,ISSUENOP) IS ERROR FLAG ON*/
*/* P POINT MESSAGE ADDRESS PAST STX, ESC, AND CMD CODE*/
*/* P STORE ADDR OF START OF RFT IN CCW*/
*/* P (,MOVEIN) GET ADDRESS OF START OF CCW*/
*/*REMOTE: D (NO,,YES,REMOTERR) IS ERROR FLAG ON*/
*/* P OVERLAY TRANSPARENT CCW*/
*/* P GET ADDR OF CCW*/
*/* P STORE ADDR OF START OF RFT IN CCW*/
*/*MOVEIN: P (,STARTEST) MOVE MESSAGE INTO USER'S BUFFER*/
*/*SETERR: M (,TESTLCL) TURN ON ERROR FLAG*/
*/*ISSUENOP: P SET COUNT TO ONE*/
*/* P (,STARTEST) MOVE NOP COMMAND INTO CCW*/
*/*REMOTERR: P SET COUNT TO ONE*/
*/*STARTEST: P STORE COUNT IN CCW*/
*/* P SAVE THE ECB*/
*/* L SVC 0*/
*/* P RESTORE THE ECB*/
*/* R SVC 3*/
*/*IGC0E06F: END*/
       EJECT
         SPACE 2
*        ESTABLISH ADDRESSABILITY
         SPACE
         BALR  BASEREG,0           SET UP BASE REG
         USING *,BASEREG
         USING IECTIOB,IOBREG      IOB DSECT
         USING IECTDECB,DECBREG    DECB DSECT
         USING IHADCB,DCBREG       DCB DSECT
         USING IECTDEB,DEBREG      DEB DSECT
         USING UCBDSECT,UCBREG     UCB DSECT
         B     SETUP               BRANCH AROUND ID USED TO
         DC    C'IGC0E06F'         FIND MODULE IN DUMP
         DC    C' 4322'            DATE LAST CHANGE         LD @ZA02334
         DC    C'&SYSDATE'         DATE LAST ASSEMBLY       LD @ZA02334
         DS    0H                                           LD @ZA02334
PATCH    DC    40X'00'             PATCH AREA               LD @ZA02334
         DS    0H                                           LD @ZA02334
SETUP    EQU   *
         L     DECBREG,IOBECBPT    GET DECB ADDR
         L     DCBREG,IOBDCBPT     GET DCB ADDR
         L     DEBREG,DCBDEBAD     GET DEB ADDR
         L     DEBREG,PREFIX(DEBREG) GO PAST PREFIX
         L     RFTREG,IOBERINF     GET ADDR OF RFT
         SPACE 2
*        VALIDITY CHECK BUFFER LENGTH AND TEST NUMBER. IF
*        INVALID, SET UP NOP CCW AND TURN ON ERROR FLAG.
         SPACE
         CLC   DECLNGTH(LNGTH2),MINBUF IS BUFFER LONG ENOUGH
         BL    SETERR              NO, SET UP FOR ERROR
         C     TESTREG,TEST23      IS IT TEST # 23 ?
         BE    GETINFO             YES, GET MSG ADDR & COUNT
         C     TESTREG,TEST24      IS IT TEST # 24 ?
         BNE   SETERR              NO, SET UP FOR ERROR
         SPACE 2
*        GET TEST MESSAGE ADDRESS AND LENGTH
         SPACE
GETINFO  LR    WORKREG1,TESTREG    GET TEST #
         S     WORKREG1,TEST23     SUBTRACT 23 TO GET INDEX
*                                  (23 IS LOWEST TEST NUMBER
*                                  THIS MODULE WILL HANDLE)
         SLL   WORKREG1,2          MULTIPLY BY 4
         L     COUNTREG,COUNTAB(WORKREG1) GET MSG LENGTH
         B     ADDRTAB(WORKREG1)   GET MSG ADDR
         SPACE 2
*        MESSAGE ADDRESS AND LENGTH TABLES - (TEST MESSAGE
*        NUMBER - 23) X 4 = THE ARGUMENT USED TO OBTAIN VALUES
*        FROM THE TABLE.
         SPACE
         DS    0F
COUNTAB  EQU   *                   MESSAGE LENGTH IN BYTES
         DC    A(TST23SZ)          00   TEST # 23
         DC    A(TST24SZ)          04   TEST # 24
         SPACE 2
ADDRTAB  EQU   *                   ADDRESS OF TEST MESSAGE
         B     TEST23AD            00   GET TEST 23 ADDR
         B     TEST24AD            04   GET TEST 2J ADDR
TEST23AD LA    ADDREG,IJLBOT23     GET ADDR
         B     TESTLCL             CONTINUE
TEST24AD LA    ADDREG,IJLBOT24     GET ADDR
         SPACE 2
*        TEST FOR LOCAL OR REMOTE
         SPACE
TESTLCL  EQU   *
         SR    UCBREG,UCBREG       CLEAR REG FOR IC
         IC    UCBREG,IOBUCBX      GET INDEX TO UCB
         SLL   UCBREG,2            MULTIPLY BY FOUR
         L     UCBREG,DEBUCBAD(UCBREG)  GET ADDR OF UCB
         CLI   DVCLASS,GRAPHIC     IS DVC A GRAPHIC ?
         BNE   REMOTE              NO, SET UP FOR REMOTE
         CLI   UNITYPE,A3277       IS DVC A 3277 ?
         BE    LOCAL               YES, SET UP FOR LOCAL
         CLI   UNITYPE,A3284       IS DVC A 3284 ?
         BE    LOCAL               YES, SET UP FOR LOCAL
         CLI   UNITYPE,A3286       IS DVC A 3286 ?
         BNE   REMOTE              NO, SET UP FOR REMOTE
*        FOR LOCAL DEVICES, STRIP OFF STX, ESC, CMD CODE, AND
*        ETX OF MESSAGE
         SPACE
LOCAL    EQU   *
         LA    CCWREG,IOBCPA       GET ADDR OF CCW
         TM    IOBINCAM,ERFLAG     IS ERROR FLAG ON ?
         BO    ISSUENOP            YES, GO ISSUE NOP
         LA    WORKREG2,LCLNGTH    SUBTRACT 4 FROM LENGTH
         SR    COUNTREG,WORKREG2   OF MESSAGE
         LA    ADDREG,LINECTL(ADDREG) POINT MSG ADDR PAST
*                                  STX, ESC, CMD CODE
         LA    RFTREG,TENCHARS(RFTREG) ADJUST POINTER PAST RFT
         B     MOVEIN              GO SET UP MSG
         SPACE 2
*        FOR REMOTE, LEAVE MSG ADDRESS AND LENGTH ALONE
         SPACE
REMOTE   EQU   *
         LA    WORKREG2,IOBCPA     STORE ADDR OF IOBCPA          S99245
         ST    WORKREG2,IOBSTART   INTO IOBSTART                 S99245
         LA    CCWREG,IOBCPA+40    GET ADDR OF CCW
         TM    IOBINCAM,ERFLAG     IS ERROR FLAG ON ?
         BO    REMOTERR            YES, ISSUE CCW
         MVC   IOBCPA+48(CCWLNGTH),IOBCPA+56 OVERLAY TRANS CCW
         CLI   NVALUE(RFTREG),NOADDR   IS THIS A DIAL RFT?       S99245
         BNE   BSC3RFT             NO,FOLLOW BSC3 PROCEDURE      S99245
         LA    RFTREG,CHARS7(RFTREG)   ADJUST PTR PAST RFT       S99245
         B     MOVEIN              GO SET UP MSG                 S99245
BSC3RFT  EQU   *                                                 S99245
         LA    RFTREG,CHARS11(RFTREG) ADJUST PTR PAST RFT
         SPACE 2
*        MOVE TEST MSG INTO USER'S BUFFER
         SPACE
MOVEIN   EQU   *
         SR    WORKREG2,WORKREG2   CLEAR REG
         C     COUNTREG,MAXLNGTH   IS MSG LNGTH OVER 256 ?
         BNH   LNGTHOK             NO, BRANCH
         LR    WORKREG2,COUNTREG   GET EXCESS OVER 256
         S     WORKREG2,MAXLNGTH
         L     COUNTREG,MAXLNGTH   SET COUNT TO 256
LNGTHOK  EQU   *
         BCTR  COUNTREG,ZERO       SUBTRACT 1 FROM LNGTH FOR
*                                  EXECUTE
         EX    COUNTREG,MSGMOVE    MOVE TEST MSG INTO USER'S
*                                  BUFFER
         LTR   WORKREG2,WORKREG2   ANY MORE TO MOVE ?
         BZ    STARTEST            NO,BRANCH
         A     RFTREG,MAXLNGTH     BUMP ADDRESS PAST 256
         A     ADDREG,MAXLNGTH     CHARS JUST MOVED
         BCTR  COUNTREG,ZERO       SUBTRACT 1 FOR EXECUTE
         EX    WORKREG2,MSGMOVE    MOVE REMAINDER OF TEXT
         B     STARTEST            BRANCH AROUND EXECUTE
MSGMOVE  MVC   0(FILLIN,RFTREG),0(ADDREG) MOVE TEST MSG TO
*                                  USER'S BUFFER, USING LENGTH
*                                  SPECIFIED IN COUNTREG
       SPACE 2
*      IF AN ERROR WAS FOUND, TURN ON ERROR FLAG AND GO GET
*      ADDRESS OF CCW.
       SPACE
SETERR   EQU   *
         OI    IOBINCAM,ERFLAG     TURN ON ERROR FLAG
         B     TESTLCL             GO SEE IF LOCAL
       SPACE 2
*      FOR ERROR IN LOCAL, SET UP A NOP CCW
       SPACE
ISSUENOP EQU   *
         LA    COUNTREG,ONECHAR    SET COUNT TO ONE
         MVI   0(CCWREG),NOP       MOVE IN NOP COMMAND
         B     STARTEST            GO ISSUE NOP
       SPACE 2
*      FOR REMOTE ERROR, SET COUNT IN WRITE TEXT CCW TO ONE
       SPACE
REMOTERR EQU   *
         LA    COUNTREG,ONECHAR    SET COUNT TO ONE
         B     STARTEST            DO TEST
         SPACE 2
*        MOVE COUNT INTO CCW AND START TEST
         SPACE
STARTEST EQU   *
         LA    COUNTREG,ONECHAR(COUNTREG) RESTORE COUNT
         STH   COUNTREG,BYTECNT(CCWREG) STORE COUNT IN CCW
*
*              1 LINE DELETED FOR ----->                    LD @ZA02332
*
         LR    WORKREG1,IOBREG     SAVE IOB REG             LD @ZA02332
         L     PARMREG,GETPARM     SET SOBPOOL & LENGTH     LD @ZA02332
         GETMAIN R,LV=(0)          GETMAIN FOR ECB          LD @ZA02332
         LR    WORKREG2,IOBREG     SAVE ADDR OF ECB         LD @ZA02332
         ST    DECBREG,DISP4(WORKREG2)  SAVE ADDR OF DECB   LD @ZA02332
         MVI   DISP4(WORKREG2),FOXFOX  FLAG AS DUMMY ECB    LD @ZA02332
         LR    IOBREG,WORKREG1     RESTORE IOB REGISTER     LD @ZA02332
         STCM  WORKREG2,MASK,IOBECBPT+DISP1  POINT IOB...   LD @ZA02332
*                                  ...TO DUMMY              LD @ZA02332
         SVC   ZERO                EXCP                     LD @ZA02332
         STCM  DECBREG,MASK,IOBECBPT+DISP1  REPOINT IOB...  LD @ZA02332
*                                  ...TO DECB               LD @ZA02332
         L     PARMREG,GETPARM     SET FOR FREEMAIN         LD @ZA02332
         FREEMAIN R,LV=(0),A=(WORKREG2) FREE DUMMY ECB      LD @ZA02332
*
*              2 LINES DELETED FOR ----->                   LD @ZA02332
*
         SVC   RETURN              RETURN
         SPACE 2
         SPACE
         SPACE 2
*        REGISTER EQUATES
         SPACE
PARMREG  EQU   0                   PARAMETER REGISTER       LD @ZA02332
IOBREG   EQU   1                   ADDR OF IOB
DECBREG  EQU   2                   ADDR OF DECB
DCBREG   EQU   3                   ADDR OF DCB
TESTREG  EQU   4                   TEST #
WORKREG1 EQU   5                   WORK REG
WORKREG2 EQU   6                   WORK REG
DEBREG   EQU   7                   ADDR OF DEB
UCBREG   EQU   8                   ADDR OF UCB
ADDREG   EQU   9                   TEST MSG ADDRESS
COUNTREG EQU   10                  TEST MSG LENGTH
CCWREG   EQU   11                  ADDR OF CCW TO WRITE TEST MSG
BASEREG  EQU   12
RFTREG   EQU   13                  ADDR OF RFT IN USER'S
*                                  BUFFER
ZERO     EQU   0                   USED IN BCTR TO DECREMENT
         SPACE 2
*        MISCELLANEOUS EQUATES
         SPACE
A3286    EQU   X'0B'               3286 DEVICE TYPE
A3284    EQU   X'0A'               3284 DEVICE TYPE
A3277    EQU   X'09'               3277 DEVICE TYPE
GRAPHIC  EQU   X'10'               GRAPHICS DEVICE TYPE
NOP      EQU   X'03'               NOP COMMAND CODE
ERFLAG   EQU   X'02'               ERROR FLAG
TENCHARS EQU   10                  LENGTH OF STANDARD RFT - LOCAL
CHARS11  EQU   11                  LENGTH OF STANDARD RFT - REMOTE
PREFIX   EQU   28                  DISP IN DEB PAST PREFIX
LNGTH2   EQU   2                   LNGTH OF DECLNGTH FIELD
LCLNGTH  EQU   4                   NO. OF LINE CONTROL CHARS
LINECTL  EQU   3                   # OF LINE CONTROL CHARS
*                                   PRECEDING MSG
CCWLNGTH EQU   8                   LENGTH OF A CCW
FILLIN   EQU   0                   LENGTH FILLED IN EX INSTR
ONECHAR  EQU   1                   ONE CHARACTER
BYTECNT  EQU   6                   DISP IN CCW OF BYTE COUNT
EXCP     EQU   0                   EXCP SVC #
RETURN   EQU   3                   RETURN SVC #
NVALUE   EQU   6                   DISP INTO RFT OF N VALUE      S99245
NOADDR   EQU   X'00'               IF N=0, IT IS A DIAL RFT      S99245
CHARS7   EQU   7                   LENGTH OF STANDARD DIAL RFT   S99245
FOXFOX   EQU   X'FF'               BYTE OF ALL ONES         LD @ZA02332
MASK     EQU   7                   MASK FOR STCM            LD @ZA02332
DISP1    EQU   1                   DISPLACEMENT             LD @ZA02332
DISP4    EQU   4                   DISPLACEMENT             LD @ZA02332
         SPACE 2
*        STORAGE
         SPACE
         DS    0F                  FULLWORD BOUNDARY        LD @ZA02332
GETPARM  DC    X'FA000008'         SUBPOOL # AND LENGTH     LD @ZA02332
MINBUF   DC    X'012C'             MINIMUM BUFFER LENGTH
TEST23   DC    F'23'               ACCEBTABLE TEST NUMBER
TEST24   DC    F'24'               ACCEPTABLE TEST NUMBER
MAXLNGTH DC    F'256'              MAX # OF CHARS FOR MVC
IJLBOT23 EQU   *
         DC    X'0227F5'           STX-ESC-EWRT
         DC    X'5F'               WCC  SOUND ALARM/RESTORE KEYBORAD/
*                                       START PRINT/40 CHAR LINE
         DC    X'11C150'           SBA 80
         DC    X'1D60'             PROTECTED,NORMAL INTENSITY
         DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ '  BEGIN MESSAGE
         DC    X'1D6C'             PROTECTED,NON DISPLAY
         DC   C'NON DISPLAY'       FOUND IN NON-DISPLAY AREA
         DC    X'11C260'           SBA 160
         DC    X'1D40'             UNPROTECTED,NORMAL INTENSITY
         DC    X'13'               INSERT CURSOR
         DC   C'COPY ABOVE IN THIS LINE    '   INSTRUCTION TO CONSOLE
         DC    X'1D40'             NORMAL INT/ UNPROTECTED
         DC    C'INSERT CK'
         DC    X'0000'             2 NULL CHARS
         DC    X'1D60'             PROTECTED,NORMAL INTENSITY
         DC    X'11C3F0'           SBA 240
         DC    X'1DE8'             PROTECTED,HIGH INTENSITY
         DC    C'Ö<(+×&&!$*)^-/;%_>?:#@''="'  SPECIAL CHARS TEST
         DC    X'1DF8'             PROTECTED,HIGH INTENSITY
         DC    C'0123456789,.-A'    NUMERIC FIELD
         DC    X'11C540'           SBA  320
         DC    X'1DC8'             UNPROTECTED,HIGH INTENSITY
         DC    C'COPY ABOVE IN THIS LINE ' INSTRUCTIONS TO CONSOLE
         DC    X'1DD8'             NUMERIC,UNPROTECTED,HIGH INTENSITY
         DC    X'11C5E8'           SBA 360
         DC    X'1D60'             PROTECTED,NORMAL INTENSITY
         DC    X'11C650'           SBA 400
         DC    X'1DE4'             PROTECTED,NORMAL INTENSITY,SELECTOR
*                                                                PEN
         DC    C'?SEL PEN TEST'    DEFINE SEL PEN AREA
         DC    X'000000'           3 NULL CHARACTERS
         DC    X'1DE8'             PROTECTED,HIGH INTENSITY,SELECTOR
*                                                                PEN
         DC   C'>SEL PEN TEST'     DEFINE SEL PEN FIELD
         DC    X'03'               ETX
TST23SZ  EQU   *-IJLBOT23          LENGTH OF TEST 23
IJLBOT24 EQU   *
         DC    X'0227F55F'          STX-ESC-EWRT-WCC
         DC    X'114040'           SBA 0                    LD @ZA02334
         DC    X'3C40E8C5C8'        RA 40,E -H
         DC    X'11C14FC8C8'       SBA  79,HH
         DC    X'11C1F7C8C8'       SBA 119,HH
         DC    X'11C25FC8C8'       SBA 159,HH
         DC    X'11C3C7C8C8'       SBA 199,HH
         DC    X'11C36FC8C8'       SBA 239,HH
         DC    X'11C4D7C8C8'       SBA 279,HH
         DC    X'11C4BFC8C8'       SBA 319,HH               LD @ZA02334
         DC    X'11C5E7C8C8'       SBA 359,HH
         DC    X'11C64FC8C8'       SBA 399,HH
         DC    X'11C6F7C8'         SBA 439,H                LD @ZA02334
         DC    X'11C35BD6D6'       SBA 219,OO
         DC    X'11C4C3D6D6'       SBA 259,OO
         DC    X'11C45D'           SBA 285
         DC    C'TEST PATTERN FOR 3277-1/3275-1'   TEST MESSAGE
         DC    X'11C54F'           SBA 335
         DC    C'ALIGNMENT'
         DC    X'11C5F05C'         SBA 368,*
         DC    X'1D40'             SF  40
         DC    X'13'               INC
         DC   C'UNPROTECTED DATA'
         DC    X'11C6C6'           SBA 390
         DC    X'1DE05C'           SF 60,*                  LD @ZA02334
         DC    X'11C6F8'           440                      LD @ZA02334
         DC    X'3CC75FC5'         RA 479,E
         DC    C'E'                LAST E
         DC    X'03'               ETX
TST24SZ  EQU   *-IJLBOT24          LENGTH OF TEST 24
         EJECT
*        DSECTS
         SPACE
         IECTIOBX
         EJECT
         IECTDECB
         EJECT
         DCBD  DSORG=BX,DEVD=BS
         EJECT
         IECTDEBX
         EJECT
UCBDSECT DSECT
         DS    0F
         ORG   UCBDSECT+13
CHARADDR DS    CL3                 UNIT ADDR (EBCDIC)
         ORG   UCBDSECT+18
DVCLASS  DS    CL1                 DEVICE CLASS
UNITYPE  DS    CL1                 UNIT TYPE
         ORG   UCBDSECT+36
RLN      DS    CL1                 RELATIVE LINE NUMBER
      END
