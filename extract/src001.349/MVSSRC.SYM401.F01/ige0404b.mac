         TITLE 'IGE0404B - BTAM ERP RD/SKP WRT/BRK CHECK LOAD TWO'
IGE0404B CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0404B                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM RD/SKP AND WRT/BRK CHECK ERP LOAD TWO                       *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS MODULE DETERMINES IF A READ COMMAND WAS ISSUED A CHANNEL    *
*    PROGRAM IS CONSTRUCTED AND EXECUTED IF REQUIRED.                 *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (48 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        464 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD   *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0404B                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        SET DECERRST FOR SHOULD NOT OCCUR ERROR.                     *
*        ALTERED CHANNEL PROGRAM.                                     *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        READ                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    READ                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YM05652K (1/24/74)                                               *
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         L     UCBREG,IOSUCB       BASE UCB
         DROP  ERREG5
         L     CCWREG,IOBFL3       ADDRESS OF CCW
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       ADJUST TO FAILING CCW
ERRCHK11 CLI   ZERO(CCWREG),CCWRDCMD   WAS A READ ISSUED
         BNE   ERRCHK10            NO POST COMPLETE WITH ERROR
         TM    DCBERROP(DCBREG),DCBRDTRY   READ RETRIES REQUIRED
         BZ    ERRCHK10            NO POST COMPLETE WITH ERROR
         CLI   IOBCSW+L3,CSWCDE    CHANNEL END DEVICE END ONLY
         BNE   ERRCHK13            NO CHECK FURTHER
         CLI   IOBCSW+L4,ZERO      ANY OTHER ERROR
         BNE   ERRCHK10            YES POST COMPLETE WITH ERROR
ERRCHK14 MVC   IOBWORK+L1(L1),DECTYPE+L1(DECBRG)
         NI    IOBWORK+L1,L255-HIBIT   CLEAR UNUSED BITS
         TM    IOBWORK+L1,READ     IS OPERATION READ
         BZ    ERRCHK10            NO, POST WITH ERROR
         CLI   IOBWORK+L1,DECRDRPT   OPERATION TYPE READ REPEAT
         BNE   ERRCHK12            NO CONSTRUCT CHAN PROGRAM
         TM    L4(CCWREG),X10      IS THIS READ SKIP
         BNO   ERRCHK23            NO, START CHAN PROGRAM
         TM    IOBSENS0,X08        IS THERE DATA CHAINING
         BO    ERRCHK10            YES,POST WITH ERROR
ERRCHK23 EQU   *
         LA    ERREG2,IOBCPA       START OF CHAN PROGRAM
         B     ERRCHK04            EXECUTE CHAN PROGRAM
ERRCHK12 EQU   *
         L     ERREG4,DECAREA(DECBRG)   SAVE AREA ADDRESS
         LH    ERRLNK,DECLNGTH(DECBRG)   SAVE AREA LENGTH
         CLI   IOBWORK+L1,DECRDINT   OPERATION READ INITIAL
         BNE   ERRCHK20            NO, DON'T SET DECB
         TM    UCBTYP+L1(UCBREG),AUTOPOLL
         BNO   ERRCHK20
         CLI   IOBCPA,X09
         BNE   ERRCHK20
         TM    L5(CCWREG),X30
         BZ    ERRCHK10
         L     ERRLNK,DECAREA(DECBRG)
         MVC   DECPOLPT(L1,DECBRG),ZERO(ERRLNK)
ERRCHK22 LR    ERRLNK,ERREG4       SAVE AREA ADDRESS
         LA    ERRLNK,L1(ERRLNK)   ADD ONE TO ADDRESS
         ST    ERRLNK,DECAREA(DECBRG)   UPDATE DECB
         LH    ERRLNK,DECLNGTH(DECBRG)   PICK UP AREA LENGTH
         BCTR  ERRLNK,ZERO         REDUCE BY ONE
         STH   ERRLNK,DECLNGTH(DECBRG)   UPDATE DECB
         LA    ERRLNK,L1(ERRLNK)   RESTORE LENGTH
ERRCHK20 EQU   *
         LR    ERREG1,IOSBREG      SAVE IOSB BASE
         LA    NAMEREG,EWAIAREA-L12   ADDRESS OF REGISTER SAVE AREA
         LR    IOSBREG,DECBRG       DECB ADDRESS IN PARAMETER REG
         NI    DECTYPE+L1(DECBRG),HIBIT   CLEAR OP TYPE CODE
         OI    DECTYPE+L1(DECBRG),DECRDRPT   INDICATE READ REPEAT
         OI    IOBBTAMF,X08        SET ERP FLAG FOR RD/WR
         READ  (1),T,MF=E          CONSTRUCT CHAN PROGRAM
         LR    IOSBREG,ERREG1      RESTORE IOSB ABSE
         NI    IOBBTAMF,L255-X08   ERP RD/WR FLAG OFF
         ST    ERREG4,DECAREA(DECBRG)   RESET DECB
         STH   ERRLNK,DECLNGTH(DECBRG)   RESET DECB
         B     ERRCHK05            UPDATE RETRY COUNT
ERRCHK13 CLI   IOBCSW+L3,CSWCDEUC   CHAN AND DEV END WITH UNIT CHK.
         BNE   ERRCHK10            NO POST COMPLETE WITH ERROR
         TM    IOBSENS0,L255-CSWLDDCO   EXPECTED ERROR
         BNZ   ERRCHK10            NO POST COMPLETE WITH ERROR
         TM    IOBSENS0,CSWLD      LOST DATA ERROR
         BZ    ERRCHK14            NO CHECK FOR RETRIES
         CLC   L6(L2,CCWREG),ERRCN0   RESIDUAL COUNT 0
         BE    ERRCHK10            YES POST WITH ERROR
         B     ERRCHK14            NO CHECK FOR RETRIES
ERRCHK04 ST    ERREG2,IOBSTART     SET CPA TO FAILING CCW
ERRCHK05 CLI   IOBERRCT,ERTRYCT    MORE RETRIES NECESSARY
         BNL   ERRCHK07            NO CHECK FOR DISABLE REQUIRED
         IC    ERREG1,IOBERRCT     PICK UP RETRY COUNT
         LA    ERREG1,L1(ERREG1)   BUMP COUNT BY ONE
         STC   ERREG1,IOBERRCT     STORE NEW COUNT
         MVI   IOBERRCT+L1,ZERO    INDICATE NORMAL RETURN
         OC    EWASTUP(L1),IOBERINF   SET FOR STAT UPDATE
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
         L     NAMEREG,TRANSRTN    LOAD NAME
         B     ERRCHK08            XCTL
ERRCHK06 EQU   *
         TM    DCBBFTEK(DCBREG),DCBDYNBF   DYNAMIC BUFFERING SPECIFIED
         BO    ERRCHK10            POST WITH ERROR
         LA    ERREG2,IOBCPA       POINT TO START OF CHANNEL PGM
         B     ERRCHK04            RETRY CHANNEL PROGRAM
ERRCHK07 EQU   *
         TM    UCBTYP+L1(UCBREG),SWLINE   DISABLE REQUIRED
         BZ    ERRCHK10            NO, POST WITH ERROR
         LA    NAMEREG,ERLOD3      NAME OF DISABLE ROUTINE
ERRCHK08 EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE
         DROP  ERRETR
         BR    ERRETR              XCTL
ERRCHK19 EQU   *
         OI    DECERRST(DECBRG),DECERPER   SET ERROR IN ERP EXCP
ERRCHK09 EQU   *
         OI    DECERRST(DECBRG),CSWSNO   SET SHOULD NOT OCCUR
ERRCHK10 EQU   *
         MVI   IOBCSW+L3,CSWCDE    SET ENTRY TO POST
         MVI   IOBERRCT+L1,RTRNCDE   FAKE OUT POST ROUTINE
         OI    IOSFLB,IOSLOG       LOGOUT REQUIRED
         LH    NAMEREG,ERLOD5      ERROR POST NAME
         B     ERRCHK08            XCTL
         EJECT
**********
*
*        CONSTANTS
*
**********
ERRCN0   DC    F'0'
TRANSRTN DC    F'9042'             BTAM TRANSLATE ROUTINE NAME
ERRCN8   DC    H'8'
ERLOD5   DC    H'5041'
PACHAREA DC    12C'404B'           PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
AUTOPOLL EQU   X'40'
DECPOLPT EQU   36
READ     EQU   X'01'
CSWCDE   EQU   X'0C'
CSWCDEUC EQU   X'0E'
DCBBFTEK EQU   32
DECERPER EQU   X'20'
CSWSNO   EQU   X'40'
DECERRST EQU   29
DECAREA  EQU   12
DECLNGTH EQU   6
DECRDINT EQU   X'01'
HIBIT    EQU   X'C0'
RTRNCDE  EQU   X'80'
ERTRYCT  EQU   X'02'
DECTYPE  EQU   4
DCBDYNBF EQU   X'08'
UCBTYP   EQU   16
SWLINE   EQU   X'90'
ERLOD3   EQU   3041
CCWRDCMD EQU   X'02'
DCBERROP EQU   33
DCBRDTRY EQU   X'02'
DECRDRPT EQU   X'07'
CSWLDDCO EQU   X'0E'
CSWLD    EQU   X'02'
X10      EQU   X'10'
X08      EQU   X'08'
X09      EQU   X'09'
X30      EQU   X'30'
L255     EQU   255                 AND MASK
ZERO     EQU   0                   LENGTH AND DISPLACEMENT
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L12      EQU   12                  LENGTH AND DISPLACEMENT
**********
*
*        REGISTER DEFINITION
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
ERREG2   EQU   2                   WORK REG
BASEREG  EQU   3                   PROGRAM BASE
CCWREG   EQU   4                   CCW ADDRESS REG
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE
EWAREG   EQU   8                   ERP WORKAREA BASE
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG4   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  RETURN REG
ERREG5   EQU   15                  WORK REG
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
