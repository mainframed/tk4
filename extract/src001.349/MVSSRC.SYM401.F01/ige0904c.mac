         TITLE 'IGE0904C - BSC UNIT EXCEPTION ERP ROUTINE'
IGE0904C CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0904C                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM BSC UNIT EXCEPTION ERP                                      *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS ROUTINE ATTEMPTS RECOVERY FROM ALL ERRORS THAT DO NOT       *
*    NORMALLY SET AN ERROR INDICATION. IT ALSO CHECKS VALID ERRORS,   *
*    SUCH AS  RECOVERY FROM FAILURE TO END TRANSPARENCY, AND FROM     *
*    CONTENTION SITUATIONS.                                           *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (104 BYTES)                                             *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD.  *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0904C                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        ALTERED CHANNEL PROGRAM                                      *
*        SETS ERROR INDICATORS IN DECERRST                            *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004C.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD.                                                        *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        NONE                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YA01232  (IN BASE)                                               *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YA02454  (11/1/73)                                               *
*    YM05652K (1/25/74)                                               *
*    ZA00519  (7/15/74)                                               *
*    ZA02314  (11/18/74)                                              *
*    ZA02316  (11/18/74)                                              *
*    ZA02846  (03/03/75)                                              *
*    ZA02847  (03/03/75)                                              *
*    ZA02849  (03/03/75)                                              *
*    ZA03556  (03/05/75)                                              *
*    AZ03974  (04/24/75)          AZ03989  (04/24/75)                 *
*    AZ04185  (10/01/75)          AZ04866  (08/26/75)                 *
*    AZ06368  (09/09/75)          AZ06383  (10/01/75)                 *
*    AZ07662  (11/25/75)          AZ07674  (12/04/75)                 *
*    AZ11416  (06/08/76)          AZ11424  (06/08/76)                 *
*    AZ13172  (09/14/76)          AZ13180  (09/13/76)                 *
*    AZ13206  (09/13/76)          AZ13207  (09/23/76)                 *
*    AZ17702  (02/15/77)          AZ29189  (01/06/78)                 *
*    AZ29193  (01/06/78)                                              *
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         USING RQE,ERREG5
         USING IECTDECB,DECBRG
         USING IHADCB,DCBREG
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         B     BEGIN               BRANCH AROUND ID         LD @ZA00519
         DC    C'IGE0904C'         MODULE ID                LD @ZA00519
         DC    C' UZ16243'         PTF NUMBER
         DC    C'&SYSDATE'         ASSEMBLY DATE            LD @ZA00519
BEGIN    DS    0H                                           LD @ZA00519
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         L     UCBREG,IOSUCB       BASE UCB
         DROP  ERREG5
         L     CCWREG,IOBFL3       GET CSW FORM IOB
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       ADJUST TO FAILING CCW IN IOB
         CLI   ZERO(CCWREG),CCWWRITE   ERROR ON A WRITE OPERATION
         BE    BSCUE02             YES CHECK FURTHER
         CLI   ZERO(CCWREG),CCWAPOLL   ERROR ON A POLL
         BE    BSCUE01             YES SET NEW POLL
         CLI   ZERO(CCWREG),CCWREAD   FAILURE ON READ (CEA END ERROR)
         BNE   BSCUE00A            NO SET SNO AND PERM ERROR
         TM    DECFLAGS,RVIRCV     2 RVI'S RECEIVED IN ORDER
         BO    BSCUET0A            YES, POST IN ERROR
         CLC   DECRESPN(L1),DCBBSENQ  TEST FOR ENQ CHAR.       @ZA06383
         BNE   BSCCT003            IF NOT, BRANCH           LD @ZA02316
         CLI   L5(CCWREG),L6       TP-OP CODE OF 06         LD @ZA02316
         BNE   BSCCT003            IF NOT, BRANCH           LD @ZA02316
         SH    CCWREG,SIXTEEN      BACK UP 2 CCW'S          LD @ZA02316
         B     BSCUETA7            UPDATE IOBSTART AND...   LD @ZA02316
*                                  ...CHECK RETRIES         LD @ZA02316
BSCCT003 EQU   *                                            LD @ZA02316
         CLI   IOBWORK,TPOPRENQ    READ ENQ
         BNE   BSCUE000            NO CHECK FURTHER
         CLC   DECRESPN(L1),DCBBSEOT   EOT RECEIVED
         BE    BSCUET0A            YES POST WITH ERROR
         SH    CCWREG,ERRCN8       BACK UP ONE CCW
         B     BSCUETA7            RETRY PREPARE
BSCCT004 NI    IOSFLA,L255-IOSERR-IOSEX   TURN OFF ERP CONTROL INDS.
         NI    IOBFLAG1,L255-IOBERRTN-IOBIOERR   ALSO IN IOB
         MVI   IOBSENS0,ZERO
****           ONE LINE OF CODE DELETED BY APAR --------->     @ZA07662
         NI    EWASTUP,FIRSTBIT    TURN OFF AND TEST X'80'
         BZ    BSCUET10            POST OPERATION COMPLETE
         OI    IOSFLB,IOSLOG       SET STAT UPDATE NEEDED
         MVI   EWAIND1,EWAISTAT    EXIT TO STAT UPDATE         YM05652K
         B     STATRTN             EXIT TO STAT RTN            @ZA13180
BSCUE000 EQU   *
         CLI   IOBWORK,TPOPRREN    READ RESP TO ENQ
         BE    BSCUECON            YES CHECK FOR CONTENTION    @ZA13206
         CLI   IOBWORK,TPOPRTXT    READ TEXT
         BE    BSCUE03             YES CHECK FURTHER
         CLI   IOBWORK,TPOPRRID    RD ID SEQUENCE
         BNE   BSCUE0BB
         TM    DECFLAGS,INVALID    WAS INVALID ID
         BZ    BSCUEVID            IF NOT, BRANCH
         CLI   DECTYPE+L1,WRTCON   IS OPERATION WRITE TC
         BNE   BSCUERTC            NO, BRANCH
BSCUECCW EQU   *
         SH    CCWREG,ERRCN8       BACK UP ONE CCW
         B     BSCUETA7            RETRY WRITE ID ENQ
BSCUECON TM    DCBXCODE,DCBMASTR   MASTER STATION ?            @ZA13206
         BNO   BSCUECCW            YES - DO RETRY              @ZA13206
         TM    DECFLAGS,CONTETON   IS CONTENTION ON ?          @ZA13206
         BO    BSCUET09            YES, POST PERMANENT         @ZA13206
         B     BSCUECCW            NO, DO RETRY                @ZA13206
BSCUERTC EQU   *
         CLI   DECTYPE+L1,RCONTONE   IS THIS READ TCW
         BE    BSCUETA7
         CLI   DECTYPE+L1,RDCON    IS OPERATION READ TC
         BNE   BSCUE00A            NO, SET SHOULD NOT OCCUR
         B     BSCUETA7            RETRY READ ID ENQ
BSCUEVID EQU   *
         LH    ERREG2,L6(CCWREG)   PICK UP ORIGINAL COUNT
         SH    ERREG2,IOBCSW+L5    SUBTRACT RESIDUAL COUNT
         BCTR  ERREG2,ZERO
         A     ERREG2,ZERO(CCWREG)   POINT TO LAST CHARACTER
         CLC   ZERO(L1,ERREG2),DCBBSNAK   WAS NAK RECEIVED
         BNE   BSCUE00A            NO, SET SHOULD NOT OCCUR
         B     BSCUECCW
BSCUE0BB EQU   *
         CLI   IOBWORK,TPOPRRTX    READ RESP TO TEXT
         BNE   BSCUE00A            NO, SET SHOULD NOT OCCUR
         L     ERREG2,ZERO(CCWREG)   GET PTR TO RESPONSE
         CLC   ZERO(L1,ERREG2),DCBBSNAK   WAS RESPONLE NAK
         BE    BSCUET11            YES, CHECK DYN BUFFERING
         TM    L17(UCBREG),MULTIPNT   MULTIPOINT ?             @ZA11416
         BO    BSCUETQ               YES, CHECK ENQ            @ZA11416
         CLC   ZERO(L1,ERREG2),DCBBSEOT  EOT RECEIVED          @ZA11416
         BE    BSCUE00A                  YES,SET PERM ERROR    @ZA11416
BSCUETQ  CLC   ZERO(L1,ERREG2),DCBBSENQ WAS RESPONSE ENQ?      @ZA13172
         BNE   BSCUE01B            NO, GENERATE WRITE ENQ      @ZA13172
         CLI   DECTYPE+L1,WTTVX WR CONT CONVERSATIONAL TRANSP? @ZA13172
         BE    BSCUET11            SET UP TO RETRY WR TEXT     @ZA13172
         CLI   DECTYPE+L1,WRITTQ   IS THIS A WR INQUIRY?       @ZA13172
         BNE   BSCUE01B            NO, GENERATE WRITE ENQ      @ZA13172
         OI    DECFLAGS,SLVCONT    INDICATE CONTENTION
         MVC   IOBCSW(L7),IOBERCCW+L17   RESTORE FAILING DATA
         NI    IOSFLA,L255-IOSERR TURN OFF ERP IN CONTROL IN IOSB
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
***      ONE LINE OF CODE DELETED BY APAR----------------->    @ZA07662
         B     BSCUET10            EXIT VIA IOS
BSCUET11 TM    DCBBFTEK,DYNBUF     DYNAMIC BUFFERING
         BO    BSCUET0A            YES, POST WITH ERROR
         SH    CCWREG,ERRCN8       BACK UP ONE CCW
         CLI   TPOPCODE(CCWREG),TPOPRTXT   READ TEXT
         BE    BSCUETA7            RETRY WRITE  TEXT
         CLI   TPOPCODE(CCWREG),TPOPWENQ  WRITE ENQ ? IF SO    @ZA04866
*                                     DON'T BACK UP ANY MORE   @ZA04866
         BE    BSCUETA7            RETRY WRITE TEST            @ZA04866
         SH    CCWREG,ERRCN8       TRANSPART,  BACK ONE MORE
         B     BSCUETA7            RETRY WRITE TEXT
BSCUE01  LA    ERREG1,IOBCPA       START OF CHAN PROG
         CR    ERREG1,CCWREG       FAILING CCW FIRST ONE
         BE    BSCUERC8       INDICATE INTERNAL ROUTINE 88     @ZA13180
BSCUE01A EQU   *
         LA    CCWREG,IOBCPA       RESTART ADDRESS
         L     ERREG1,IOBCPA+L8    ADDR OF POLLING ENTRY
         MVC   IOBCPA+L9(L3),DECENTRY+L1   FAILING ENTRY ADDR
         L     ERREG3,IOBCPA+L8    SAME ADDR
         SR    ERREG1,ERREG3       DIFFERENCE
         AH    ERREG1,IOBCPA+L14   TOTAL COUNT
         STH   ERREG1,IOBCPA+L14   STORED IN CCW
BSCUETA7 ST    CCWREG,IOBSTART
BSCUET08 CLI   IOBERRCT,ERRMAXCT   RETRIES EXHAUSTED
         BNL   BSCUET09            YES SET PERM ERROR
         IC    ERREG1,IOBERRCT     PICK UP RETRY COUNT
         LA    ERREG1,L1(ERREG1)   BUMP BY ONE
         STC   ERREG1,IOBERRCT     RESTORE NEW COUNT
         NI    DECFLAGS,XBF        RESET BIT 1 IN DECB FLAGS
BSCUET10 EQU   *
         MVI   EWAIND1,EWAISVCF    SET SVC 15 RETRY FLAG       @ZA07662
STATRTN  L     NAMEREG,TRANSRTN    LOAD NAME                   @ZA13180
         B     XCTLOUT             XCTL
BSCUE00A OI    DECERRST,DECSNO     SET SHOULD NOT OCCUR
BSCUET09 NI    IOSFLA,L255-IOSERR  INDICATE PERM ERROR
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
         CLI   DECTYPE+L1,WRTCON   IS OPERATION WRITE TC
         BNE   BSCUENOW
         TM    DECFLAGS,IMPRESP    WAS THE RESPONSE A NAK
         BZ    BSCUEID
         MVI   IOBCPA+L5,CONNECT   INDICATE LINE CONNECTION
         B     BSCCT004            POST NORMALLY
BSCUEID  EQU   *
         TM    DECFLAGS,INVALID    WAS IT AN INVALID ID
         BZ    BSCUET0A
         MVC   IOBERCCW(L16),DLEOTCCW   WRITE DLE EOT , DISABLE
         LA    ERREG1,DCBBSDLE     GET ADDRESS OF DLE EOT
         ST    ERREG1,IOBERCCW     STORE ADDRESS IN CCW
         MVI   IOBERCCW,CCWWRITE   MOVE IN WRITE COMMAND
         B     BSCUERST
BSCUENOW EQU   *
         CLI   DECTYPE+L1,RCONTONE   IS OPERATION READ TCW
         BE    BSCUETCW
         CLI   DECTYPE+L1,RDCON    IS OPERATION READ TC
         BNE   BSCUET0A
BSCUETCW EQU   *
         TM    DECFLAGS,INVALID    WAS IT AN INVALID ID
         BZ    BSCUET0A
         MVC   IOBERCCW(L8),DISABCCW   MOVE IN DISABLE CCW
BSCUERST EQU   *
         LA    CCWREG,IOBERCCW
         ST    CCWREG,IOBSTART
         MVI   IOBERRCT,ZERO       SET ERROR COUNT TO ZERO
         OI    IOSFLA,IOSERR   TURN ON ERP IN CONTROL IN IOSB
         OI    IOBFLAG1,IOBERRTN   ALSO IN IOB
****           ONE LINE OF CODE DELETED BY APAR ------------>  @ZA07662
         B     BSCUET10
BSCUET0A LA    NAMEREG,BSCLOD2     ADDRESS OF ERROR POST
         MVC   EWACHA(L2),L4(UCBREG) MOVE UNIT ADDR TO WK AREA @ZA03989
XCTLOUT  EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE
         DROP  ERRETR
         BR    ERRETR              XCTL OUT
BSCUE01B MVI   IOBWORK+L1,RTRNCDE6   INDICATE INTERNAL ROUTINE
         LA    ERRLNK,DCBBSENQ     ADDRESS OF ENQ CHAR
BSCUET02 LA    ERREG2,CCWWRITE     1ST COMMAND
         LA    ERREG5,CCWTIC       2ND COMMAND
BSCUET05 ST    ERRLNK,IOBERCCW     STORE DATA ADDR IN CHAN PROG
         STC   ERREG2,IOBERCCW     STORE COMMAND IN 1ST CCW
         ST    ERREG2,IOBERCCW+L4   PUT COUNT IN CCW
         CLI   IOBWORK+L1,ZERO    HAS SPECIAL INTERNAL RETURN
*                                 CODE BEEN SET?
         BE    BSCUETA6       NO, DON'T SET FLAG FOR 004C
         MVI   IOBERRCT+L1,RTRNSPCD  SET BRANCHING FLAG FOR
*                                    SPECIAL RETURN MODULE
BSCUETA6 EQU   *
         ST    CCWREG,IOBERCCW+L8  STORE TIC ADDR
         MVI   IOBERCCW+L4,CHAINCDE   COM CH, SLI
         CLI   IOBWORK+L1,RTRNCDE8   SPEC RETURN OF 88
         BNE   BSCUE040           NO,DONT SET COM CH        L5 @ZA03556
         MVI   IOBERCCW+L4,X70     COM CH, SLI, SKIP
         B     BSCUE02F                BUILD 2ND RETRY CCW  L5 @ZA03556
BSCUE040 CLI   IOBWORK+L1,RTRNCDE0     SPEC RETURN OF 80    L5 @ZA03556
         BNE   BSCUE02F             NO,BUILD 2ND RETEY CCW  L5 @ZA03556
         CLI   IOBWORK,TPOPWREN  TP OP CODE OF 08(WT ENQ)   L5 @ZA03556
         BNE   BSCUE02F              NO,BUILD 2ND RETRY CCW L5 @ZA03556
         MVI   IOBERCCW+L8,CCWREAD  MAKE 2ND CCW A READ     L5 @ZA03556
         MVI   IOBERCCW+L12,SLISKIP  SUPRESS INCORRECT      L5 @ZA03556
*            LENGTH AND TRANSFER TO STORAGE ON 2ND CCW      L5 @ZA03556
         MVI   IOBERCCW+L4,CDSLI       DATA CHAING AND      L5 @ZA03556
*                                      SUPP LEN IN FST CCW  L5 @ZA03556
         MVI   IOBERCCW+L15,L255  MAKE 2ND CCW LENGTH          @ZA03556
*                                  FIELD EQU FF                @ZA03556
         B     BSCUE02G                GET RESTART ADDR     L5 @ZA03556
BSCUE02F STC   ERREG5,IOBERCCW+L8   STORE COMMAND IN 2ND CCW
         ST    ERREG2,IOBERCCW+L12   PUT COUNT IN CCW
BSCUE02G EQU   *                                            L5 @ZA03556
         LA    CCWREG,IOBERCCW     ADDR OF NEW CHAN PROG
         B     BSCUETA7            RETRY ERROR
BSCUE02  CLI   IOBWORK,TPOPWENQ    THIS WRITE ENQ
         BE    BSCUE02C            YES GENERATE RD ENQ
         CLI   IOBWORK,TPOPWREN    WRITE RESP TO ENQ
         BE    BSCUET2D            YES SET UP READ ENQ         @ZA13180
         CLI   IOBWORK,TPOPWRTT    WRITE WACK OR            LD @ZA00519*
                                   WRITE RESP TO TEXT       LD @ZA00519
         BL    BSCUET2D            IOBWORK = WRITE WACK     LD @ZA00519
         BE    BSCUERC8            IOBWORK = WRITE          LD @ZA00519*
                                   RESP TO TEXT             LD @ZA00519
         CLI   IOBWORK,TPOPDTXT    ERROR DURING TEXT
         BNE   BSCUE002            NO CHECK FURTHER
BSCUERC8 EQU   *
         MVI   IOBWORK+L1,RTRNCDE8   INDICATE INTERNAL ROUTINE
         B     BSCUET2A            CONTINUE GENERATION
*        SIXTEEN LINES OG CODE DELETED BY APAR ------------->  @ZA13180
BSCUET2D EQU   *
         MVI   IOBWORK+L1,RTRNCDE0   INDICATE INTERNAL ROUTINE
BSCUET2A LA    ERREG5,CCWNOP       SET 2ND COMMAND FOR NO-OP
         LA    ERRLNK,IOBWORK+L2   DATA ADDRESS
         LA    ERREG2,CCWREAD      SET IS COMMAND FOR READ
         B     BSCUET05            COMPLETE CHAN PROG
BSCUETRY OI    DECFLAGS,IMPRESP    IMPROPER RESPONSE
         SH    CCWREG,ERRCN8       BACK UP ONE CCW
         B     BSCUETA7            RETRY
BSCUE02C CLC   DECRESPN(L1),DCBBSNAK   NAK RECEIVED
         BE    BSCUETRY            YES, BRANCH TO IMPROPER
         CLC   DECRESPN(L2),DCBBSRVI   RVI RECEIVED
         BE    BSCUETRY            YES, BRANCH TO IMPROPER
         SH    CCWREG,ERRCN8       BACK UP ONE CCW
         CLI   L5(CCWREG),TPOPRDID   IS PREV CCW READ ID
         LA    CCWREG,L8(CCWREG)   RESET CCW REG
         BE    BSCUET2D            BRANCH IF READ ID PREV
         MVI   IOBWORK+L1,RTRNCDE4   SET SPECIAL RETURN CODE
         B     BSCUET2A            GENERATE CHAN PROG
BSCUE03  EQU   *
         TM    DCBBFTEK,DYNBUF     DYNAMIC BUFFERING ?         @Z29189
         BNO   BSCUE031            NO, BRANCH                  @Z29189
         OI    DECERRST,DECSNO     SET SHOULD NOT OCCUR        @Z29189
         NI    IOSFLA,L255-IOSERR  INDICATE PERM ERROR         @Z29189
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB            @Z29189
         B     BSCUET0A            POST PERM ERROR             @Z29189
BSCUE031 EQU   *                                               @Z29189
         LH    ERREG2,L6(CCWREG)   PICK UP ORIG CT             @Z29189
         BCTR  ERREG2,ZERO         SUBTRACT ONE
         CH    ERREG2,IOBCSW+L5    THIS COUNT=RESIDUAL COUNT
         BE    BSCUE03B            YES CHECK FOR ENQ
         L     ERREG1,IOBERINF
         CLC   ZERO(L2,ERREG1),DCBBSONL   SOH % MESSAGE RECEIVED?
         BE    BSCUET0A            YES, GO TO ERROR POST
         SH    ERREG2,IOBCSW+L5    FIND DIFFERENCE
         A     ERREG2,ZERO(CCWREG)   LAST CHAR ADDRESS         @ZA04185
         CLC   ZERO(L1,ERREG2),DCBBSENQ   THIS ENQ CHARACTER
         BE    BSCUE03I             INDICATE ENQ               @ZA04185
         CLC   ZERO(L1,ERREG2),DCBBSEOT IS LAST CHAR AN EOT    @ZA04185
         BNE   BSCUE00A            NO SET SNO                  @ZA04185
*              THE FOLLOWING TEST WILL DETERMINE IF GARBLED    @ZA04185
*              SYN CHARACTORS PRECEEDED THE EOT                @ZA04185
         TM    L17(UCBREG),X40      MULTI-POINT BSC3 LINE      @ZA04185
         BNO   BSCUE00A            SET SNO                     @ZA04185
         LA    ERRLNK,DECAREA      INPUT AREA ADDR             @ZA04185
         CLI   IOBCPA,CCWAPOLL     MULTI-POINT RD INIT         @ZA04185
         BNE   BSCUE03A              NO                        @ZA04185
         LA    ERRLNK,L1(ERRLNK)   SKIP OVER INDEX BYTE        @ZA04185
BSCUE03A CLC   ZERO(L1,ERRLNK),DCBBSSTX  FIRST BYTE STX        @ZA04185
         BE    BSCUE00A             YES, POST SNO              @ZA04185
         CLC   ZERO(L1,ERRLNK),DCBBSONL  FIRST BYTE SOH        @ZA04185
         BE    BSCUE00A             YES, POST SNO              @ZA04185
         CLC   ZERO(L1,ERRLNK),DCBBSRSV  FIRST BYTE DLE...     @ZA04185
*                             INDICATING TRANSPARENT MODE      @ZA04185
         BE    BSCUE00A             YES, POST SNO              @ZA04185
         CLI   IOBCPA,CCWAPOLL     MULTI-POINT RD TI           @ZA04185
         BE    BSCUE03D                                        @ZA04185
         MVI   IOBWORK+L1,RTRNCDED  INDICATE INTERNAL ROUTINE  @ZA04185
         B     BSCUE03G        WRITE NAK, TIC TO RD TXT        @ZA04185
BSCUE03D MVI   IOBWORK+L1,RTRNCDE2                             @ZA04185
         L     ERRLNK,ZERO(CCWREG)   FIND RD TXT               @ZA04185
         BCTR  ERRLNK,ZERO         DECREMENT BY ONE            @ZA04185
         STCM  ERRLNK,L7,L1(CCWREG) UPDATE INPUT AREA ADDR     @ZA04185
         LH    ERRLNK,L6(CCWREG)    ORGINAL LENGTH             @ZA04185
         LA    ERRLNK,L1(ERRLNK) ADJUST INPUT AREA ADDR BY ONE @ZA04185
         STH   ERRLNK,L6(CCWREG)    UPDATE LENGTH              @ZA04185
         B     BSCUE03G                                        @ZA04185
BSCUE03I EQU   *                                               @ZA04185
         OI    DECFLAGS,STXENQ     MSG ENDING IN ENQ RCVD   LD @ZA02314
         BCTR  ERREG2,ZERO         BACK UP ONE CHARACTER
         CLC   ZERO(L1,ERREG2),DCBBSSTX   IS THIS CHARACTER STX
         BNE   BSCUE03C            NO, NOT ABORT SEQUENCE
*        ONE LINE OF CODE DELETED BY APAR ------------->       @ZA13180
         MVI   IOBWORK+L1,RTRNCDE9   SET SPECIAL RETURN CODE
BSCUE03C EQU   *
         TM    DCBBFTEK,DYNBUF     DYNAMIC BUFFERING
         BO    BSCUET0A            YES POST WITH ERROR
         CLI   IOBCPA,CCWAPOLL         MULTIPOINT RD INIT?  L5 @ZA02846
         BNE   BSCUE03G                NO,WRITE NAK         L5 @ZA02846
         L     ERREG2,0(CCWREG)        RD TXT DATA ADDR     L5 @ZA02846
         SH    CCWREG,ERRCN8           BACK UP 1 CCW        L5 @ZA02846
         C     ERREG2,0(CCWREG)        ADDR  MODIFIED?      L5 @ZA02846
         BNE   BSCUE03H                NO                   L5 @ZA02846
         LA    CCWREG,8(CCWREG)        RESTART NEXT CCW     L5 @ZA02846
         B     BSCUE03G                                     L5 @ZA02846
BSCUE03H BCTR  ERREG2,0                BACK UP 1 CHAR       L5 @ZA02846
         B     BSCUE03J                SET UP NEW RD ADDR   L5 @ZA02846
BSCUE03G EQU   *                                            L5 @ZA02846
         LA    ERRLNK,DCBBSNAK     SET ADDR TO NAK
         B     BSCUET02            GO COMPLETE CHAN PROG
BSCUE03B L     ERREG2,ZERO(CCWREG)   ENQ POINTER
         CLC   ZERO(L1,ERREG2),DCBBSENQ   ENQ RECEIVED
         BE    BSCUE03E            YES,CONTINUE
         CLC   ZERO(L1,ERREG2),DCBBSEOT   EOT RECEIVED?
         BNE   BSCUE00A            NO ,POST WITH ERROR         @ZA13207
*        TWO LINES OF CODE DELETED BY APAR ---------------->   @ZA13207
         L     ERREG2,DECAREA      GET BUFFER ADDRESS          @Z29193
         CLC   0(1,ERREG2),DCBBSDLE IS FIRST CHAR DLE          @Z29193
         BE    BSCUE00A            YES, POST WITH ERROR        @Z29193
         L     ERREG2,ZERO(CCWREG) LOAD DATA ADDRESS           @Z29193
         MVC   IOBERCCW+L17(L7),IOBCSW  SAVE NEW CSW
         TM    DECFLAGS,STXENQ     ABORT SITUATION?
         BNO   BSCUE03F
         MVI   IOBECBCC,ERRCODE    SET 41 COMPLETION
BSCUE03F EQU   *
         CLI   IOBCPA,CCWAPOLL     MULTIPOINT READ INITIAL?
         BNE   BSCCT004            POST NORMAL                  YA02454
         SH    CCWREG,ERRCN8       BACK UP TO READ INDEX CCW
         C     ERREG2,ZERO(CCWREG)   HAS ADDRESS BEEN MODIFIED?
         BNE   BSCUE00A            NO, SET SHOULD NOT OCCUR
         LH    ERREG2,IOBERCCW+L22   GET RESIDUAL COUNT
         BCTR  ERREG2,ZERO         SUBTRACT FOR INDEX BYTE
         STH   ERREG2,IOBERCCW+L22   STORE CORRECTED COUNT
         B     BSCUET0A
BSCUE03E EQU   *
         TM    DCBBFTEK,DYNBUF     DYNAMIC BUFFERING
         BO    BSCUET0A            YES POST WITH ERROR
         SH    CCWREG,ERRCN8       BACK UP TO NEXT CCW
         CLI   L5(CCWREG),TPOPWRTX   THIS WRT RESP TO TEXT
         BNE   BSCUE03N             NO, CHECK CONV. WRITE      @ZA17702
         MVI   IOBWORK+L1,RTRNCDE1     SET RETURN CODE 81      @ZA17702
         MVI   IOBERRCT+L1,RTRNSPCD    SET SPECIAL RETURN CODE @ZA17702
         MVI   IOBWORK+L3,CON9A        SET INDICATOR           @ZA17702
         BE    BSCUETA7                                        @ZA17702
BSCUE03N CLI   L5(CCWREG),TPOPRRTX   THIS A CONV WRITE         @ZA17702
         BE    BSCUE03C            GEN WRT NAK CHAN PROGRAM
         CLI   IOBCPA,CCWAPOLL     POLLING SEQUENCE
         BNE   BSCUE00A            NO, SET SNO
         L     ERRLNK,DECAREA      GET I/O AREA ADDR           @ZA03974
         LA    ERRLNK,L1(ERRLNK)   PT TO INPUT AREA + ONE      @ZA03974
         LA    ERREG2,ZERO(ERREG2) CLEAR HI BYTE-THIS REG      @ZA03974
*                   CONTAINS CURRENT INPUT AREA ADDR FROM CCW  @ZA03974
         CR    ERRLNK,ERREG2       HAS READ TXT CCW BEEN       @ZA03974
*                              ALTERED (READ TI POLLING)       @ZA03974
         BNE   BSCUE03K            NO, CONTINUE NORMALLY       @ZA03974
         MVI   IOBWORK+L1,RTRNCDE2  SET INTERNAL CODE          @ZA03974
         MVI   IOBERCCW+L16,TIMEOUT  SET T.O. SENSE            @ZA03974
         NI    IOBERCCW+L20,XFE     TURN OFF UNIT EXCEPTION    @ZA03974
         OI    IOBERCCW+L20,UNITCHK SET U.C. STATUS            @ZA03974
         LA    CCWREG,L8(CCWREG)     POINT TO FAILING CCW      @ZA03974
         B     BSCUE03G              GO BUILD RETRY CHAN PGM   @ZA03974
BSCUE03K EQU   *
         BCTR  ERREG2,ZERO         ADDRESS OF BEGINNING TEXT
         CLC   ZERO(L1,ERREG2),DCBBSSTX   STX PRECEDING ENQ ?
         BNE   BSCUE00A            NO, SET SNO
         OI    DECFLAGS,STXENQ         INDICATE ENQ RECVD   L5 @ZA02846
         MVI   IOBWORK+1,RTRNCDE9      SET SPEC RETURN      L5 @ZA02846
BSCUE03J EQU   *                                            L5 @ZA02846
         STCM  ERREG2,L7,L9(CCWREG) SET UP NEW READ IN AREA    @ZA07674
         STCM  ERREG2,L7,L1(CCWREG) MODIFY AREA ADDRESS        @ZA07674
         LA    CCWREG,L8(CCWREG)   BACK TO PROPER CCW
         LH    ERREG2,L6(CCWREG)   INCRIMENT
         LA    ERREG2,L1(ERREG2)   COUNT
         STH   ERREG2,L6(CCWREG)   FOR READ
         B     BSCUE03G         SET SP RTN - GEN CHAN PROG  L5 @ZA02846
BSCUE002 CLI   IOBWORK,TPOPRSET    THIS A RESET OPERATION
         BNE   BSCUE00A            NO SET SNO ERROR
         CLI   L13(CCWREG),X8B     WRITE RESET CHAN PROGRAM
         BNE   BSCUE0X2            NO, CONTINUE
         LA    CCWREG,L8(CCWREG)   YES, RESTART AT NEXT CCW
         B     BSCUETA7
BSCUE0X2 MVI   IOBWORK+L1,RTRNCDE7   INDICATE INTERNAL ROUTINE
         B     BSCUET2A            GENERATE CHAN PROGRAM
         EJECT
**********
*
*        CONSTANTS
*
**********
TRANSRTN DC    F'9042'             NAME OF BTAM TRANSLATE ROUTINE
FOURTY   DC    AL4(40)                                      L5 @ZA02847
DLEOTCCW DC    X'0100000060210002'
DISABCCW DC    X'2F00000020010001'    DISABLE CCW
ERRCN8   DC    H'8'
SIXTEEN  DC    H'16'                                        LD @ZA02316
PACHAREA DC    26C'0000'           PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
TPOPRRTX EQU   X'25'
DECSNO   EQU   X'40'
XFE      EQU   X'FE'                                           @ZA03974
L255     EQU   X'FF'               AND MASK
BSCLOD2  EQU   2043                BSC ERROR POST IDENT
ERRCN2   EQU   8
FIRSTBIT EQU   X'7F'               AND MASK FOR X'80'
NORMCOND EQU   X'0D'               CE,DE,UE FLAGS ON
ZERO     EQU   X'00'               LENGTH AND DISPLACEMENT
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L9       EQU   9                   LENGTH AND DISPLACEMENT
L12      EQU   12                  LENGTH AND DISPLACEMENT
L13      EQU   13                  LENGTH AND DISPLACEMENT
L14      EQU   14                  LENGTH AND DISPLACEMENT
L15      EQU   15                  LENGTH AND DISPLACEMENT  L5 @ZA03556
L16      EQU   16                  LENGTH AND DISPLACEMENT
L17      EQU   17                  LENGTH AND DISPLACEMENT
L20      EQU   20                                              @ZA03974
L22      EQU   22                  LENGTH AND DISPLACEMENT
X40      EQU   X'40'                                           @ZA04185
X70      EQU   X'70'
X8B      EQU   X'8B'
XBF      EQU   X'BF'               DECB FLAGS AND MASK
WLR      EQU   X'40'               SLI FLAG
TIMEOUT  EQU   X'01'          TIME OUT SENSE BYTE              @ZA03974
UNITCHK  EQU   X'02'          UNIT CHECK STATUS                @ZA03974
TPOPWENQ EQU   X'03'
TPOPDTXT EQU   X'11'
CCWNOP   EQU   3
TPOPWRTX EQU   X'08'
TPOPCODE EQU   5
WRITTQ   EQU   X'16'
WTTVX    EQU   X'1F'                                           @ZA13172
SLVCONT  EQU   X'10'
CCWWRITE EQU   1
CCWAPOLL EQU   X'09'
TPOPWRTT EQU   X'02'               TP-OP WRITE RESP TO TEXT LD @ZA00519
TPOPWREN EQU   X'08'
CCWREAD  EQU   2
TPOPRENQ EQU   X'0B'
TPOPRREN EQU   X'0C'
TPOPRTXT EQU   X'11'
STXENQ   EQU   X'01'
IMPRESP  EQU   X'40'
CONNECT  EQU   X'DD'
TPOPRRID EQU   X'07'
WRTCON   EQU   X'1C'
RDCON    EQU   X'11'
RCONTONE EQU   X'1E'
INVALID  EQU   X'10'
ERRCODE  EQU   X'41'
ERRMAXCT EQU   X'07'
RTRNCDE1 EQU   X'81'          RETURN CODES                     @ZA17702
RTRNCDE2 EQU   X'82'                                           @ZA03974
RTRNCDE4 EQU   X'84'
RTRNCDE6 EQU   X'86'
RTRNCDE0 EQU   X'80'
RTRNCDE7 EQU   X'87'
RTRNCDE8 EQU   X'88'
RTRNCDE9 EQU   X'89'
RTRNCDED EQU   X'8D'                                           @ZA04185
TPOPRSET EQU   X'21'
TPOPRDID EQU   X'07'
RTRNSPCD EQU   X'14'
CHAINCDE EQU   X'60'
CCWTIC   EQU   X'08'
RVIRCV   EQU   X'42'               RVI RECEIVED ; DEFLAG SETTING
DYNBUF   EQU   X'08'
X08      EQU   X'08'
SIX      EQU   6       RD SKIP CCW LEN FIELD                L5 @ZA03556
CDSLI    EQU   X'A0'   DATA CHAINING AND SUPRESS            L5 @ZA03556
*                      INCORRECT LENGTH
SLISKIP  EQU   X'30'   SUPPRESS INCORRECT LENGTH AND        L5 @ZA03556
*                      TRANSFER TO STORAGE
RLN0A    EQU   X'0A'                   RELATIVE LINE TEN    L5 @ZA02847
MULTIPNT EQU   X'40'                                           @ZA11416
CONTETON EQU   X'10'       CONTENTION CHECK                    @ZA13206
DCBMASTR EQU   X'80'        REMOTE STATION                     @ZA13206
CON9A    EQU   X'9A'        SPECIAL CODE                       @ZA17702
**********
*
*        REGISTER DEFINITION
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
ERREG2   EQU   2                   WORK REG
EWAREG   EQU   3                   ERP WORKAREA BASE
CCWREG   EQU   4                   CCW ADDRESS REG
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE
BASEREG  EQU   8                   PROGRAM BASE
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG4   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  LINK REG
ERREG5   EQU   15                  LINK REG/WORK REG
         EJECT
         IECTDECB
         EJECT
         DCBD  DSORG=BX,DEVD=BS              DCB DESECT MACRO
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
