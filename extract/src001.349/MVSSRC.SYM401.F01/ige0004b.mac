         TITLE 'IGE0004B - BTAM ERROR DATA CHECK LOAD TWO'
IGE0004B CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0004B                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM ERROR DATA CHECK LOAD TWO                                   *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    1.ATTEMPT ERROR RECOVERY OF DATA CHECK ERRORS OCCURRING          *
*    DURING OR AFTER TEXT TRANSFERS. ERRORS THAT OCCUR AFTER TEXT     *
*    TRANSFER ARE RETRIED AUTOMATICALLY. ERRORS THAT OCCUR DURING     *
*    TEXT TRANSFER ARE RETRIED ONLY IF DECERROP PARAMETER             *
*    SPECIFIES READ RETRIES.                                          *
*    2.PROVIDE LINKAGE TO LINE ERROR RECORDING ROUTINE IF LERB        *
*    INDICATOR IS SPECIFIED IN DECERROP PARAMETER                     *
*    3.PROVIDE LINKAGE TO POST ROUTINE IF ERROR IS DETERMINED         *
*    PERMANENT OR IF DURING TEXT TRANSFER AND READ RETRIES            *
*    ARE NOT SPECIFIED                                                *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (48 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        500 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD   *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0004B                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        ALTERS EXISTING CCWS OR CREATES NEW ONES FOR RECOVERY        *
*        ALTERS DECERRST FOR SHOULD NOT OCCUR ERROR.                  *
*        ALTERS DECERRST FOR DISABLE REQUIRED.                        *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        NONE                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*        TCCW         (MAPPED BY IECDTCCW)                            *
*        IOCOM        (MAPPED BY IECDIOCM)                            *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YM05652K (1/24/74)                                               *
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG4      BASE PROGRAM
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         DROP  ERREG5
         L     CCWREG,IOBFL3       ADDRESS OF CCW
         LA    CCWREG,ZERO(CCWREG)   CLEAR FLAG BYTE
         SH    CCWREG,ERRCN8       ADJUST TO FAILING CCW
         CLI   IOBERRCT+L1,MASK3   ENTRY FROM 1ST LOAD FOR LERB CHECK
         BE    ERRDCD03            YES CHECK FOR LERB REQUIRED
         CLI   ZERO(CCWREG),CCWAPOLL   POLL COMMAND
         BE    ERRDCD12            YES TREAT AS READ
         CLI   ZERO(CCWREG),CCWINCMD   INHIBIT COMMAND
         BE    READ                YES
         CLI   ZERO(CCWREG),CCWRDCMD   READ COMMAND
         BNE   ERRDCD00             NO CHECK FOR WRITE
READ     EQU   *
         CLI   IOBWORK,CCWTWXID    READ RESPONSE TWX ID
         BE    ERRDCD01            YES INITIALIZE START
         CLI   IOBCPA,CCWDISAB     FIRST COMMAND A DISABLE
         BNE   ERRDCD01            NO INITIALIZE START
         LA    ERREG1,IOBCMD3      SET CPA TO THIRD CCW
         ST    ERREG1,IOBSTART     STORE IN POINTER
         B     ERRDCD03            CHECK FOR LINE ERROR RECORDING
ERRDCD00 CLI   ZERO(CCWREG),CCWWRT   WRITE COMMAND
         BE    ERRDCD02            YES
         OI    DECERRST(DECBRG),CSWSNO   SET SHOULD NOT OCCUR ERROR
         B     ERRDCD05            CHECK FOR SWITCHED LINE
ERRDCD02 EQU   *
         CLI   IOBWORK+L2,TTYADPT1   THIS TELETYPE ADAPTER 1
         BE    ERRDCD14            IF SO, BRANCH
         CLI   IOBWORK+L2,TTYADPT2   THIS TELETYPE ADAPTER 2
         BE    ERRDCD14            IF SO, BRANCH
         MVI   IOBERRCT,ERTRYCT    SET RETRY COUNT TO 2
         L     UCBREG,IOSUCB       POINT TO UCB
         TM    UCBTYP+L3(UCBREG),X90   IS A WTTA
         BNZ   ERRDCD03            BRANCH IF NOT
         TM    UCBTYP+L3(UCBREG),X60   IS A WTTA
         BO    ERRDCD16            BRANCH IF YES
         B     ERRDCD03            CHECK FOR LINE ERROR RECORDING
ERRDCD01 EQU   *
         CLI   IOBWORK,CCWRDIX     READ RESPONSE TO POLL ID
         BNE   ERRDCD13            NO ALL COMMANDS GOOD
ERRDCD12 EQU   *
         L     ERREG1,IOBCPA+L8    LOAD ENTRY ADDRESS FROM PREVIOUS
*                                  POLL (WITH POLL COMMAND CODE
*                                  IN HIGH ORDER BYTE)
         MVC   IOBCPA+L9(L3),DECENTRY+L1(DECBRG)   MOVE IN ADDRESS
*                                  OF LAST ENTRY POLLED (TO BE
*                                  USED AS RESTART ADDRESS)
         L     ERREG4,IOBCPA+L8    LOAD ENTRY ADDRESS FOR NEXT POLL
*                                  (WITH POLL COMMAND CODE IN
*                                  HIGH ORDER BYTE)
         SR    ERREG1,ERREG4       SUBTRACT NEW POLL ADDRESS
*                                  FROM PREVIOUS POLL ADDRESS.
         AH    ERREG1,IOBCPA+L14   IF THE NEW POLL ADDRESS
*                                  WERE LESS THAN THE PREVIOUS POLL
*                                  ADDRESS, THE EFFECT IS TO ADD THE
*                                  ABSOLUTE DIFFERENCE TO THE PREVIOUS
*                                  COUNT; IF IT WERE GREATER, THE
*                                  EFFECT IS TO SUBTRACT THE ABSOLUTE
*                                  DIFFERENCE FROM THE PREVIOUS COUNT
         STH   ERREG1,IOBCPA+L14   STORE ADJUSTED COUNT IN
*                                  POLL COMMAND.
ERRDCD13 EQU   *
         LA    ERREG4,IOBCPA       START OF CHANNEL PROGRAM
         ST    ERREG4,IOBSTART     STORE CPA IN POINTER
ERRDCD03 MVI   IOBERRCT+L1,NONAME   CLEAR SPECIAL ENTRY INDICATOR
         TM    DCBERROP(DCBREG),DCBLERB   LINE ERROR RECORDING REQUIRED
         BZ    ERRDCD04            NO CHECK RETRIES
         MVI   IOBERRCT-L1,LERBDC   INDICATE LERB DATA CHECK ERROR
ERRDCD04 CLI   IOBERRCT,ERTRYCT    RETRY REQUIRED
         BL    ERRDCD06            YES UPDATE RETRY COUNT
         TM    DCBBFTEK(DCBREG),DYNBUF   IS DYNAMIC BUF SPECIFIED?
         BO    ERRDCD05            YES, NO DIAG ISSUED
         CLI   IOBWORK+L3,CTRL2701   THIS A 2701 CONTROL UNIT
         BE    ERRDCD10            YES ISSUE DIAG WRITE/READ
ERRDCD05 EQU   *
         L     UCBREG,IOSUCB       BASE UCB
         TM    UCBTYP+L1(UCBREG),SWLINE   DISABLE REQUIRED
         BZ    ERRDCD07            NO INDICATE PERMANENT ERROR
         MVC   IOBERINF+L8(L8),ZERO(CCWREG)   SAVE FAILING CCW
         MVC   IOBERINF(L1),IOBSENS0   SAVE SENSE BYTE
         MVC   IOBERINF+L1(L7),IOBCSW   SAVE CSW DATA
         MVC   IOBERCCW(L8),DISABCMD   GENERATE DISABLE CCW
         LA    ERREG1,IOBERCCW     ADDRESS OF CCW
         ST    ERREG1,IOBSTART     CPA START IN POINTER
         MVI   IOBERRCT+L1,DISABIND   INDICATE DISABLE EXECUTED
         OI    DECERRST(DECBRG),DISABLOK   INDICATE DISABLE EXECUTED
ERRDCD06 IC    ERREG1,IOBERRCT     RETRY COUNT
         LA    ERREG1,L1(ERREG1)   BUMP COUNT
         STC   ERREG1,IOBERRCT     STORE NEW COUNT
         MVI   IOBSENS0,NONAME     CLEAR SENSE BYTE
         CLI   IOBERRCT-L1,NONAME  IS LERB SPECIFIED
         BNE   ERRDCD09            IF YES, BRANCH
RETRY    EQU   *
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
         L     NAMEREG,TRANSRTN    LOAD NAME
         B     ERRDCD08            XCTL
ERRDCD07 NI    IOSFLA,L255-IOSERR   INDICATE POST TO LERB
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
         CLI   IOBERRCT-L1,NONAME   LINE ERROR RECORDING
         BNE   ERRDCD09
         LH    NAMEREG,ERLOD5      NAME OF POST ROUTINE
ERRDCD08 L     ERRETR,CVTPTR       BASE CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE
         BR    ERRETR              XCTL
         DROP  ERRETR
ERRDCD09 LA    NAMEREG,ERLOD12     NAME OF LERB ROUTINE
         B     ERRDCD08            XCTL
ERRDCD10 CLI   IOBERRCT-L1,NONAME   LINE ERROR RECORDING REQUIRED
         BE    ERRDCD11            NO EXIT TO DIAG WRT/RD
         MVI   IOBERRCT+L1,RTRNCDE   INDICATE DIAG WR/RD REQ
         LA    NAMEREG,ERLOD12     NAME OF LERB ROUTINE
         B     ERRDCD08            XCTL
ERRDCD11 LA    NAMEREG,ERLOD11     NAME OF DIAG WR/RD ROUTINE
         B     ERRDCD08            XCTL
ERRDCD14 TM    L5(CCWREG),CCWTXTDT   DURING TEXT TRANSFER
         BZ    ERRDCD13            IF NOT, GO TO RETRY
         TM    DCBERROP(DCBREG),CCWRTRT   WRITE RETRIES SPECIFIED
         BO    ERRDCD13            IF SO, GO TO RETRY
ERRDCD16 EQU   *
         TM    DCBERROP(DCBREG),DCBLERB   LINE ERROR COUNTS
         BZ    ERRDCD15            IF NOT, POST
         MVI   IOBERRCT-L1,LERBDC   INDICATE LERB DATA CHECK
         NI    IOSFLA,L255-IOSERR-IOSEX   INDICATE POST WITH NO
*                                  ERROR MESSAGE
         NI    IOBFLAG1,L255-IOBERRTN-IOBIOERR   ALSO IN IOB
         B     ERRDCD09            GO TO LERB
ERRDCD15 NI    IOSFLA,L255-IOSERR   INDICATE PERM ERROR
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
         B     RETRY               RETRY EXCP AND RETURN
         EJECT
**********
*
*        CONSTANTS
*
**********
         DS    0D
DISABCMD DC    X'2F00000020300001'   DISABLE CCW
TRANSRTN DC    F'9042'             NAME OF BTAM TRANSLATE ROUTINE
ERLOD5   DC    H'5041'             NAME OF POST ROUTINE
ERRCN8   DC    H'8'                ARITHMETIC
PACHAREA DC    12C'ZAP'            PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
ZERO     EQU   0                   LENGTH AND DISPLACEMENT
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L9       EQU   9                   LENGTH AND DISPLACEMENT
L14      EQU   14                  LENGTH AND DISPLACEMENT
X90      EQU   X'90'               WTTA MASK
X60      EQU   X'60'               WTTA MASK
CCWAPOLL EQU   X'09'
CCWINCMD EQU   X'0A'
CCWDISAB EQU   X'2F'
CCWRDCMD EQU   X'02'
CCWRDIX  EQU   X'0A'
CCWRTRT  EQU   X'04'
CCWTXTDT EQU   X'10'
CCWTWXID EQU   X'07'
CCWWRT   EQU   X'01'
CSWSNO   EQU   X'40'
CTRL2701 EQU   X'02'
DCBERROP EQU   33
DCBBFTEK EQU   X'20'
DYNBUF   EQU   X'08'
DCBLERB  EQU   X'08'
DECENTRY EQU   20
DECERRST EQU   29
DISABIND EQU   X'80'
DISABLOK EQU   X'08'
ERLOD11  EQU   1042                NAME OF DIAG RD/WRT ROUTINE
ERLOD12  EQU   2042                NAME OF LERB ROUTINE
ERTRYCT  EQU   X'02'
LERBDC   EQU   X'01'
L255     EQU   255                 AND MASK
MASK3    EQU   X'F0'
NONAME   EQU   X'00'
RTRNCDE  EQU   X'C0'
SWLINE   EQU   X'90'
TTYADPT1 EQU   X'40'
TTYADPT2 EQU   X'50'
UCBTYP   EQU   16                  DEVICE TYPE CODE
**********
*
*        REGISTER DEFINITION
*
**********
ERREG2   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
EWAREG   EQU   2                   ERP WORKAREA BASE
BASEREG  EQU   3                   PROGRAM BASE
CCWREG   EQU   4                   CCW ADDRESS
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   WORK REG/UCB BASE
ERREG5   EQU   8                   WORK REG
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG3   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  RETURN REG
ERREG4   EQU   15                  LINK REG
         EJECT
         IECDRQE
         EJECT
         IEZIOB
IOBCMD3  EQU   IOBCPA+16
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
