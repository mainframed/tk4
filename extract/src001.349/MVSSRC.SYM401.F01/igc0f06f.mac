         TITLE ' IGC0F06F - 3270 LOCAL/REMOTE EBCDIC TEST MODULE '
IGC0F06F CSECT
         SPACE 3
***********************************************************************
*                                                                     *
*        MAINTENANCE ACTIVITY                                         *
*                                                                     *
*        ZA02332  (11/20/74)                                          *
*                                                                     *
***********************************************************************
         EJECT
*/*IGC0F06F: CHART*/
*/* E IGC0F06F*/
*/* D (YES,,NO,SETERR) IS BUFFER LONG ENOUGH (300 BYTES)*/
*/* D (YES,,NO,SETERR) IS TEST NUMBER VALID (26 OR 27)*/
*/* P GET MESSAGE LENGTH AND ADDRESS*/
*/*TESTLCL: D (YES,LOCAL,NO,REMOTE) IS DEVICE A LOCAL 3270*/
*/*LOCAL: D (NO,,YES,ISSUENOP) IS ERROR FLAG ON*/
*/* P POINT MESSAGE ADDRESS PAST STX, ESC, AND CMD CODE*/
*/* P STORE ADDR OF START OF RFT IN CCW*/
*/* P (,MOVEIN) GET ADDRESS OF START OF CCW*/
*/*REMOTE: D (NO,,YES,REMOTERR) IS ERROR FLAG ON*/
*/* P OVERLAY TRANSPARENT CCW*/
*/* P GET ADDR OF CCW*/
*/* P STORE ADDR OF START OF RFT IN CCW*/
*/*MOVEIN: P (,STARTEST) MOVE MESSAGE INTO USER'S BUFFER*/
*/*SETERR: M (,TESTLCL) TURN ON ERROR FLAG*/
*/*ISSUENOP: P SET COUNT TO ONE*/
*/* P (,STARTEST) MOVE NOP COMMAND INTO CCW*/
*/*REMOTERR: P SET COUNT TO ONE*/
*/*STARTEST: P STORE COUNT IN CCW*/
*/* P SAVE THE ECB*/
*/* L SVC 0*/
*/* P RESTORE THE ECB*/
*/* R SVC 3*/
*/*IGC0F06F: END*/
         SPACE 2
*        ESTABLISH ADDRESSABILITY
         SPACE
         BALR  BASEREG,0           SET UP BASE REG
         USING *,BASEREG
         USING IECTIOB,IOBREG      IOB DSECT
         USING IECTDECB,DECBREG    DECB DSECT
         USING IHADCB,DCBREG       DCB DSECT
         USING IECTDEB,DEBREG      DEB DSECT
         USING UCBDSECT,UCBREG     UCB DSECT
         B     SETUP               BRANCH AROUND ID USED TO
         DC    C'IGC0F06F'         FIND MODULE IN DUMP
         DC    C' 4322'            DATE LAST CHANGE         LD @ZA02332
         DC    C'&SYSDATE'         DATE LAST ASSEMBLY       LD @ZA02332
         DS    0H                                           LD @ZA02332
PATCH    DC    48X'00'             PATCH AREA               LD @ZA02332
         DS    0H                                           LD @ZA02332
SETUP    EQU   *
         L     DECBREG,IOBECBPT    GET DECB ADDR
         L     DCBREG,IOBDCBPT     GET DCB ADDR
         L     DEBREG,DCBDEBAD     GET DEB ADDR
         L     DEBREG,28(DEBREG)   GO PAST PREFIX
           L     RFTREG,IOBERINF     GET ADDR OF RFT
         SPACE 2
*        VALIDITY CHECK BUFFER LENGTH AND TEST NUMBER. IF
*        INVALID, SET UP NOP CCW AND TURN ON ERROR FLAG.
         SPACE
         CLC   DECLNGTH(2),MINBUF  IS BUFFER LONG ENOUGH ?
         BL    SETERR              NO, SET UP FOR ERROR
         C     TESTREG,TEST26      IS IT TEST # 26 ?
         BE    GETINFO             YES, GET MSG ADDR & COUNT
         C     TESTREG,TEST27      IS IT TEST # 27 ?
         BNE   SETERR              NO, SET UP FOR ERROR
         SPACE 2
*        GET TEST MESSAGE ADDRESS AND LENGTH
         SPACE
GETINFO  LR    WORKREG1,TESTREG    GET TEST #
         S     WORKREG1,TEST26     SUBTRACT 26 TO GET INDEX
*                                  (26 IS LOWEST TEST NUMBER
*                                  THIS MODULE WILL HANDLE)
         SLL   WORKREG1,2          MULTIPLY BY 4
         L     COUNTREG,COUNTAB(WORKREG1) GET MSG LENGTH
         B     ADDRTAB(WORKREG1)   GET MSG ADDR
         SPACE 2
*        MESSAGE ADDRESS AND LENGTH TABLES - (TEST MESSAGE
*        NUMBER - 26) X 4 = THE ARGUMENT USED TO OBTAIN VALUES
*        FROM THE TABLE.
         SPACE
         DS    0F
COUNTAB  EQU   *                   MESSAGE LENGTH IN BYTES
         DC    A(TST26SZ)          TEST 26
         DC    A(TST27SZ)          TEST 27
         SPACE 2
ADDRTAB  EQU   *                   ADDRESS OF TEST MESSAGE
         B     TEST26AD            00   GET TEST 26 ADDR
         B     TEST27AD            04   GET TEST 27 ADDR
TEST26AD LA    ADDREG,IJLBOT26     GET ADDR
         B     TESTLCL             CONTINUE
TEST27AD LA    ADDREG,IJLBOT27     GET ADDR
         SPACE 2
*        TEST FOR LOCAL OR REMOTE
         SPACE
TESTLCL  EQU   *
         SR    UCBREG,UCBREG       CLEAR REG FOR IC
         IC    UCBREG,IOBUCBX      GET INDEX TO UCB
         SLL   UCBREG,2            MULTIPLY BY FOUR
         L     UCBREG,DEBUCBAD(UCBREG)  GET ADDR OF UCB
         CLI   DVCLASS,GRAPHIC     IS DVC A GRAPHIC ?
         BNE   REMOTE              NO, SET UP FOR REMOTE
         CLI   UNITYPE,A3277       IS DVC A 3277 ?
         BE    LOCAL               YES, SET UP FOR LOCAL
         CLI   UNITYPE,A3284       IS DVC A 3284 ?
         BE    LOCAL               YES, SET UP FOR LOCAL
         CLI   UNITYPE,A3286       IS DVC A 3286 ?
         BNE   REMOTE              NO, SET UP FOR REMOTE
*        FOR LOCAL DEVICES, STRIP OFF STX, ESC, CMD CODE, AND
*        ETX OF MESSAGE
         SPACE
LOCAL    EQU   *
           LA    CCWREG,IOBCPA       GET ADDR OF CCW
         TM    IOBINCAM,ERFLAG     IS ERROR FLAG ON ?
         BO    ISSUENOP            YES, GO ISSUE NOP
         LA    WORKREG2,4          SUBTRACT 4 FROM LENGTH
         SR    COUNTREG,WORKREG2   OF MESSAGE
         LA    ADDREG,3(ADDREG)    POINT MESSAGE ADDRESS PAST
*                                  STX, ESC, CMD CODE
         LA    RFTREG,10(RFTREG)   ADJUST PTR PAST RFT
         B     MOVEIN              GO SET UP MSG
         SPACE 2
*        FOR REMOTE, LEAVE MSG ADDRESS AND LENGTH ALONE
         SPACE
REMOTE   EQU   *
         LA    WORKREG2,IOBCPA     GET ADDR OF IOBCPA            S99245
         ST    WORKREG2,IOBSTART   AND STORE IN IOBSTART         S99245
         LA    CCWREG,IOBCPA+40    GET ADDR OF CCW
         TM    IOBINCAM,ERFLAG     IS ERROR FLAG ON ?
         BO    REMOTERR            YES, ISSUE CCW
         MVC   IOBCPA+48(8),IOBCPA+56 OVERLAY TRANS CCW
         CLI   NVALUE(RFTREG),NOADDR   IS THIS A DIAL RFT?       S99245
         BNE   BSC3RFT             NO,FOLLOW BSC3 PROCEDURE      S99245
         LA    RFTREG,CHARS7(RFTREG)   ADJUST PTR PAST RFT       S99245
         B     MOVEIN              GO SET UP MSG                 S99245
BSC3RFT  EQU   *                                                 S99245
         LA    RFTREG,11(RFTREG)   ADJUST PTR PAST RFT
         SPACE 2
*        MOVE TEST MSG INTO USER'S BUFFER
         SPACE
MOVEIN   EQU   *
         SR    WORKREG2,WORKREG2   CLEAR REG
         C     COUNTREG,MAXLNGTH   IS MSG LNGTH OVER 256 ?
         BNH   LNGTHOK             NO, BRANCH
         LR    WORKREG2,COUNTREG   GET EXCESS OVER 256
         S     WORKREG2,MAXLNGTH
         L     COUNTREG,MAXLNGTH   SET COUNT TO 256
LNGTHOK  EQU   *
         BCTR  COUNTREG,0          SUBTRACT 1 FROM LNGTH FOR
*                                  EXECUTE
         EX    COUNTREG,MSGMOVE    MOVE TEST MSG INTO USER'S
*                                  BUFFER
         LTR   WORKREG2,WORKREG2   ANY MORE TO MOVE ?
         BZ    STARTEST            NO, BRANCH
         A     RFTREG,MAXLNGTH     BUMP ADDRESS PAST 256
         A     ADDREG,MAXLNGTH     CHARS JUST MOVED
         AR    COUNTREG,WORKREG2   ADD LENGTH TO COUNTREG       YA01228
         BCTR  WORKREG2,0          SUBTRACT 1 FOR EXECUTE
         EX    WORKREG2,MSGMOVE    MOVE REMAINDER OF TEXT
         B     STARTEST            BRANCH AROUND EXECUTE
MSGMOVE  MVC   0(0,RFTREG),0(ADDREG) MOVE TEST MSG TO
*                                  USER'S BUFFER, USING LENGTH
*                                  SPECIFIED IN COUNTREG
SETERR   EQU   *
         OI    IOBINCAM,ERFLAG     TURN ON ERROR FLAG
         B     TESTLCL             GO SEE IF LOCAL
ISSUENOP EQU   *
         LA    COUNTREG,1          SET COUNT TO ONE
         MVI   0(CCWREG),NOP       MOVE IN NOP COMMAND
         B     STARTEST            GO ISSUE NOP
REMOTERR EQU   *
         LA    COUNTREG,1          SET COUNT TO ONE
         B     STARTEST            DO TEST
         SPACE 2
*        MOVE COUNT INTO CCW AND START TEST
         SPACE
STARTEST EQU   *
         LA    COUNTREG,1(COUNTREG) RESTORE COUNT AFTER EX
         STH   COUNTREG,6(CCWREG)  STORE COUNT IN CCW
*
*              3 LINES DELETED FOR ----->                   LD @ZA02332
*
         LR    WORKREG1,IOBREG     SAVE IOB ADDR            LD @ZA02332
         L     PARMREG,GETPARM     SET FOR GETMAIN          LD @ZA02332
         GETMAIN R,LV=(0)          GETMAIN FOR ECB          LD @ZA02332
         LR    WORKREG2,IOBREG     SAVE ECB ADDR            LD @ZA02332
         ST    DECBREG,DISP4(WORKREG2)  SAVE DECB ADDR      LD @ZA02332
         MVI   DISP4(WORKREG2),FOXFOX  FLAG AS DUMMY ECB    LD @ZA02332
         LR    IOBREG,WORKREG1     RESTORE IOB REG          LD @ZA02332
         STCM  WORKREG2,MASK,IOBECBPT+DISP1  POINT IOB...   LD @ZA02332
*                                  ...TO DUMMY ECB          LD @ZA02332
         SVC   ZERO                EXCP                     LD @ZA02332
         STCM  DECBREG,MASK,IOBECBPT+DISP1  REPOINT IOB...  LD @ZA02332
*                                  ...TO DECB               LD @ZA02332
         L     PARMREG,GETPARM     SET UP FOR FREEMAIN      LD @ZA02332
         FREEMAIN R,LV=(0),A=(WORKREG2) FREE DUMMY ECB      LD @ZA02332
         SVC   3                   RETURN
         SPACE 2
*        IF AN ERROR WAS FOUND, TURN ON ERROR FLAG, GET CCW
*        ADDR, AND ISSUE NOP CCW
         SPACE
         SPACE 2
*        REGISTER EQUATES
         SPACE
PARMREG  EQU   0                   PARAMETER REGISTER       LD @ZA02332
IOBREG   EQU   1                   ADDR OF IOB
DECBREG  EQU   2                   ADDR OF DECB
DCBREG   EQU   3                   ADDR OF DCB
TESTREG  EQU   4                   TEST #
WORKREG1 EQU   5                   WORK REG
WORKREG2 EQU   6                   WORK REG
DEBREG   EQU   7                   ADDR OF DEB
UCBREG   EQU   8                   ADDR OF UCB
ADDREG   EQU   9                   TEST MSG ADDRESS
COUNTREG EQU   10                  TEST MSG LENGTH
CCWREG   EQU   11                  ADDR OF CCW TO WRITE TEST MSG
BASEREG  EQU   12
RFTREG   EQU   13                  ADDR OF RFT
         SPACE 2
*        MISCELLANEOUS EQUATES
         SPACE
A3286    EQU   X'0B'               3286 DEVICE TYPE
A3284    EQU   X'0A'               3284 DEVICE TYPE
A3277    EQU   X'09'               3277 DEVICE TYPE
GRAPHIC  EQU   X'10'               GRAPHICS DEVICE TYPE
NOP      EQU   X'03'               NOP COMMAND CODE
ERFLAG   EQU   X'02'               ERROR FLAG
NVALUE   EQU   6                   DISP INTO RFT OF N VALUE      S99245
NOADDR   EQU   X'00'               IF N=0, IT IS A DIAL RFT      S99245
CHARS7   EQU   7                   LENGTH OF STANDARD DIAL RFT   S99245
FOXFOX   EQU   X'FF'               BYTE OF ALL ONES         LD @ZA02332
MASK     EQU   7                   MASK FOR STCM            LD @ZA02332
DISP1    EQU   1                   DISPLACEMENT             LD @ZA02332
DISP4    EQU   4                   DISPLACEMENT             LD @ZA02332
ZERO     EQU   0                                            LD @ZA02332
         SPACE 2
*        STORAGE
         SPACE
         DS    0F                  FULLWORD BOUNDARY        LD @ZA02332
GETPARM  DC    X'FA000008'         SUBPOOL # AND LENGTH     LD @ZA02332
MINBUF   DC    X'012C'             MINIMUM BUFFER LENGTH
TEST26   DC    F'26'               ACCEPTABLE TEST NUMBER
TEST27   DC    F'27'               ACCEPTABLE TEST NUMBER
MAXLNGTH DC    F'256'              MAX # OF CHARS FOR MVC
IJLBOT26 EQU   *
         DC    X'0227F5'           STX-ESC-EWRT
         DC    X'6F'               WCC  START PRINT/64 CHARS PER LINE/
*                                       SOUND ALARM/RESTORE KEYBOARD
         DC    X'1D60'             SF/NORMAL INT/PROTECTED
         DC    C'FOR CLUSTERED PRINTERS DATA SHOULD'
         DC    C' START NEXT LINE HERE ------>'    NEW LINE AT CHAR 65
         DC    C'CHECKING PROGRAM TAB/ERASE UNPROT. TO '
         DC    C'ADDR/DUP/FIELD MARK ORDERS'
         DC    X'11C3F0'           SBA 240
         DC    X'13'               INSERT CURSOR
         DC    X'1DF8'             AUTOSKIP,PROTECTED,HIGH INTENSITY
         DC    X'3CC540F1'         RA 320 (1'S)
         DC    X'1DC8'             UNPROTECTED,HIGH INTENSITY
         DC    X'3CC650'           RA 400 (A'S)
         DC    C'A'                REPEATED CHAR
         DC    X'1D60'             PROTECTED,NORMAL INTENSITY
         DC    X'3CC75F'           RA   479  (B'S)
         DC    C'B'                REPEATED CHAR
         DC    X'11C4D7F1'         SBA 279,1
         DC    X'05'               PROGRAM TAB
         DC    X'11C5E8'           SBA 360
         DC    X'12C650'           EUA 400
         DC    X'11C64A'           SBA 394
         DC    X'1C1E1519'         DUP/FIELD MARK/NEW LINE/EOM
         DC    X'03'               ETX
TST26SZ  EQU   *-IJLBOT26          LENGTH OF TEST 26
IJLBOT27 EQU   *
         DC    X'0227F5'           STX-ESC-EWRT
* UNIVERSAL 3270 TEST PATTERN (RFT MESSAGE)
         DC    X'4F'     WCC   SOUND ALARM/RESTORE KEYBOARD/
*                              START PRINT/132 CHARCTRS PER LINE
         DC    X'8182838485868788898A8C8D8E8F'
         DC    X'909192939495969798999A9D9E9F'
         DC    X'1140E7'           SBA 39
         DC    X'1D60'             SF 60
         DC    X'A2A3A4A5A6A7A8A9AAACADAEAF'
         DC    X'BABBBCBDBEBF808B9B9CA0A1AB'
         DC    X'B0B1B2B3B475B6B7B879'
         DC    X'11C14F'           SBA 79
         DC    X'1D40'             SF 40
         DC    X'414243444546474849'
         DC    X'515253545556575859'
         DC    X'6263646566676869'
         DC    X'70717273747576777879'
         DC    X'11C1F7'           SBA 119
         DC    X'1D60'             SF 60
         DC    X'C1C2C3C4C5C6C7C8C9'
         DC    X'D1D2D3D4D5D6D7D8D9'
         DC    X'E2E3E4E5E6E7E8E9'
         DC    X'F0F1F2F3F4F5F6F7F8F9'
         DC    X'11C25F'           SBA 159
         DC    X'1D40'             SF 40
         DC    X'40506061C0D0E0E1'
         DC    X'11C3C7'           SBA 199
         DC    X'1D60'             SF 60
         DC    X'4A4B4C4D4E4F'
         DC    X'5A5B5C5D5E5F'
         DC    X'6A6B6C6D6E6F'
         DC    X'7A7B7C7D7E7F'
         DC    X'11C36F'           SBA 239
         DC    X'1D40'             SF 40
         DC    X'CACBCCCDCECF'
         DC    X'DADBDCDDDEDF'
         DC    X'EAEBECEDEEEF'
         DC    X'FAFBFCFDFEFF'
         DC    X'11C4D7'           SBA 279
         DC    X'1D60'             SF 60
         DC    C'N/L CHECK'
         DC    X'15B515B515'       5 NEW LINE MESSAGES
         DC    X'11C540'           SBA  320
         DC    C'EOM CHECK'
         DC    X'B919'             END OF MESSAGE (ALT. CODES)
         DC    X'11C64F'           SBA  399
         DC    X'1D40'             SF 40
         DC    X'13'               INC
         DC    X'03'               ETX
TST27SZ  EQU   *-IJLBOT27          LENGTH OF TEST 27
         EJECT
*        DSECTS
         SPACE
         IECTIOBX
         EJECT
         IECTDECB
         EJECT
         DCBD  DSORG=BX,DEVD=BS
         EJECT
         IECTDEBX
         EJECT
UCBDSECT DSECT
         DS    0F
         ORG   UCBDSECT+13
CHARADDR DS    CL3                 UNIT ADDR (EBCDIC)
         ORG   UCBDSECT+18
DVCLASS  DS    CL1                 DEVICE CLASS
UNITYPE  DS    CL1                 UNIT TYPE
         ORG   UCBDSECT+36
RLN      DS    CL1                 RELATIVE LINE NUMBER
         END
