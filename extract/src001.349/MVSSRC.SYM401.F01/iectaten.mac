         TITLE 'IECTATEN - BTAM LOCAL 3270 FIRST LEVEL ATTN RTN'
IECTATEN CSECT
         SPACE 3
*********
*
*      THIS MODULE HAS BEEN PARTIALLY RE-WRITTEN FOR OS/VS2-REL2
*
*********
*
*        YA02144                   YM4074
*        YA03953                   YM5698
*        YA03957                   YM6997
*        ZA02340  (11/22/74)
*
*********
         SPACE 3
* STATUS- CHANGE LEVEL 000
*
* FUNCTION-HANDLE ALL ATTENTION INTERRUPTS FOR LOCAL 3270 DEVICES IN
*    OS/BTAM.  SCHEDULE THE SECOND LEVEL ATTENTION RTN. (IGG019PG) IF
*    A READ IS TO BE STARTED.  UPDATE ATTENTION INFORMATION FOR THE
*    BTAM LINE GROUP.
*
* ENTRY POINT- THE ROUTINE IS ENTERED AT IECTATEN FROM THE I/O INTER-
*    RUPT SUPERVISOR WHENEVER ATTENTION STATUS OCCURS FROM A LOCAL 3270
*    DEVICE.
*
* INPUT- REGISTERS 7, 10, AND 14 ARE ESTABLISHED BY IOS
*    REGISTER  7 - THE ADDRESS OF THE UCB FOR THE DEVICE
*    REGISTER 10 - ADDRESS OF THE ENTRY POINT OF AEE2
*    REGISTER 14 - RETURN POINT ADDRESS
*
* OUTPUT- UPDATED ATTENTION FLAGS AND COUNT IN THE LINE GROUP'S UCB'S.
*    AN INITIALIZED IQE IF THE SECOND LEVEL ATTENTION RTN IS SCHEDULED.
*
* EXTERNAL REFERENCES- ASYNCHRONOUS EXIT EFFECTOR, STAGE 2 (AEE2)
*
* EXITS,NORMAL- RETURN TO IOS WITH ATTENTION INFORMATION UPDATED
*
* EXITS,ERROR- RETURN TO IOS WITHOUT UPDATING ATTENTION INFORMATION
*
* TABLES/WORK AREAS- NONE
*
* ATTRIBUTES- NUCLEUS RESIDENT, DISABLED, SUPERVISOR PROTECT KEY,
*    READ ONLY.
*
* CHARACTER CODE DEPENDENCY- N/A
*
* NOTES- THIS ROUTINES LOGS ATTENTION INTERRUPTS RECEIVED FROM LOCAL
*    3270 DEVICES ON OPEN BTAM LINE GROUPS.  IF AN OUTSTANDING READ
*    INITIAL EXISTS FOR THE LINE GROUP AND THE DEVICE IS ABLE TO
*    PROCESS READ INITIALS, THE SECOND LEVEL ATTENTION ROUTINE IS
*    SCHEDULED USING AEE2 TO START THE READ.  ATTENTION INFORMATION
*    IN THE LINE GROUP'S UCB'S IS UPDATED AS REQUIRED.
*
         EJECT
*/*IECTATEN: CHART */
*/* HEADER
*/*IECTATEN - BTAM FIRST LEVEL ATTENTION ROUTINE FOR 3270 DEVICES   */
*/* E ENTRY FROM IOS */
*/* M SAVE REGS AND ESTABLISH ADDRESSABILITY */
*/* D (YES,CHKTYPE,NO,) USER'S SPACE ADDRESSABLE? */
*/* P RESTORE IOS'S REGISTERS */
*/* R RETURN TO IOS+4 */
*/*CHKTYPE: D (YES,,NO,RTNTOIOS) IS DEVICE A 3270 DEVICE? */
*/*OPENCHK1: D (YES,CHKCSW,NO,RTNTOIOS) ATTN HANDLING IRB ADDRESS
*/*NON-ZERO? */
*/*RTN2IOS: P TURN OFF BUSY,NOT READY, AND CU BUSY IN UCBFL1 */
*/* P RESTORE IOS'S REGISTERS */
*/* R RETURN TO IOS+4 (INTERCEPT BIT UNCHANGED) */
*/*RTNTOIOS: P RESTORE IOS'S REGISTERS */
*/* R RETURN TO IOS */
*/*CHKREADY: D (NO,,YES,CHKFUTH) OPEN FAIL ON THIS DEVICE (UCBGRAF) ?
*/* */
*/* D (YES,FLGERCMP,NO,RTNTOIOS) DE WITH ERROR (UCBGCB) ? */
*/*CHKFUTH: D (YES,,NO,NFUTHCHK) I/O QUEUED FOR THIS DEVICE (UCBFL1) ?
*/**/
*/* D (YES,FLGERCMP,NO,RTNTOIOS) DE WITH ERROR (UCBGCB) ? */
*/*NFUTHCHK: P SET READY FLAG (UCBGRAF) */
*/* P GET IQE ADDRESS FROM IRB AND RECHAIN IQE'S */
*/* P GET ADDRESS OF MASTER UCB */
*/* D (YES,,NO,NOSETRDY) USING BTAM'S READY ROUTINE IGG019UP ? */
*/* P (,LNRDYSET) MARK LINE READY (MTRGRAF) */
*/*NOSETRDY: P TURN OFF LINE READY (MTRGRAF) */
*/*LNRDYSET: P SET RLN AND DEB ADDRESS AS IQE PARAMETERS */
*/* S AEE2:SCHEDULE IQE FOR READY ROUTINE */
*/* P TURN OFF OPEN FAIL FLAG FOR DEVICE (UCBGRAF) */
*/* D (YES,FLGERCMP,NO,RTN2IOS) DE WITH ERROR (UCBGCB) ? */
*/*FLAGRDY: P FLAG INTERUPT DURING OPEN (UCBGRAF) */
*/* D (YES,FLGERCMP,NO,RTNTOIOS) DE WITH ERROR (UCBGRAF) ? */
*/*CHKCSW: D (YES,OTHRSTS,NO,) ATTN, DE, AND/OR CUE ONLY STATUS? */
*/*FLGERROR: P SET IOB INTERCEPT FLAG IN UCBFL1 */
*/* D (YES,,NO,FLGERCMP) DEVICE END STATUS ? */
*/* P SET DE WITH ERROR (UCBGCB) */
*/* D (NO,,YES,OPENCHK) DEVICE PREVIOUSLY NOT READY (UCBGRAF) ? */
*/*FLGERCMP: P (,RTN2IOS) TURN OFF DE WITH ERROR (UCBGCB) */
*/*OTHRSTS: D (YES,PROCATTN,NO,) ATTN STATUS PRESENTED? */
*/* D (YES,,NO,RTNTOIOS) DEVICE END STATUS ? */
*/*OPENCHK: D (YES,,NO,NXTCHK) DEVICE READY (UCBGRAF) ? */
*/* D (YES,FLGERCMP,NO,RTNTOIOS) DE WITH ERROR (UCBGCB) ? */
*/*NXTCHK: D (NO,,YES,FLAGRDY) OPEN IN PROGRESS (UCBGRAF) ? */
*/* D (NO,,YES,CHKREADY) READYQ SPECIFIED IN DCB (UCBRDYQ) ? */
*/* D (YES,FLGERCMP,NO,RTNTOIOS) DE WITH ERROR (UCBGCB) ? */
*/*PROCATTN: D (NO,,YES,RTN2IOS) I/O OPERATION QUEUED FOR THIS DVC? */
*/*ACPTATN: D (NO,CHKRLN,YES,SETATNFL) SKIP FLAG ON (UCBGCB) ? */
*/*SETATNFL: P (,RTN2IOS) SET ATTENTION FLAG IN UCBGCB */
*/*CHKRLN: P OBTAIN ADDRESS OF MASTER UCB */
*/*RDTIPND: D (NO,,YES,NOSCHED) PROCESSING DEVICE READY (MTRGRAF) ? */
*/* D (NO,,YES,SCHEDIQE) READ INITIAL PENDING (MTRGCB) ? */
*/*NOSCHED: D (NO,,YES,RTN2IOS) ATTENTION FLAG ON (UCBGCB) ? */
*/* P (,SETATNFL) INCREMENT ATTENTION COUNT IN MASTER UCB BY 1
*/*(MTRATNCT) */
*/*SCHEDIQE: P GET IQE FROM RBNEXAV QUEUE IN IRB */
*/* P STORE UCB ADDRESS IN IQE */
*/* P SET RBNEXAV TO ZERO IN IRB */
*/* P SET READ INITIAL PENDING FLAG OFF (MTRGCB) */
*/* S (,RTN2IOS) AEE2:SCHEDULE 2ND LEVEL ATTN RTN */
*/*IECTATEN: END */
         EJECT
***********************************************************************
*
*         REGISTER DEFINITION
*
***********************************************************************
         SPACE 5
PARMREG  EQU   1                    PARAMETER REGISTER
AEEREG   EQU   12                   ADDRESS OF AEE2           LD Y02947
R12      EQU   12                                             LD Y02947
SAVEREG  EQU   13                   ADDRESS OF SAVE AREA
R13      EQU   13                                             LD Y02947
RTNREG   EQU   14                   RETURN POINT REGISTER
R14      EQU   14                                             LD Y02947
EPREG    EQU   15                   ENTRY POINT REGISTER
*
BASEREG  EQU   11                   BASE REG FOR ATT RTN      LD Y02947
IRBREG   EQU   9                    POINTER TO IRB
MSTREG   EQU   8                    BASE REGISTER FOR MASTER UCB
UCBREG   EQU   7                    BASE REGISTER FOR DEVICE UCB
IQEREG   EQU   6                    POINTER TO IQE
CVTREG   EQU   5                    POINTER TO CVT            LD Y02947
ASCBREG  EQU   5                    POINTER TO ASCB           LD Y02947
DEBREG   EQU   4                  ADDR OF DEB FROM MSTRUCB     @YA02128
IOSBREG  EQU   4                    POINTER TO OISB           LD Y02947
SRBREG   EQU   3                    POINTER TO SRB            LD Y02947
WORK2    EQU   SRBREG                                        LD YA02128
UCBEXTRG EQU   2                    POINTER TO UCB EXTENSION  LD YM0160
WORK1    EQU   2                    WORK REGISTER
         SPACE 5
***********************************************************************
*
*         EQUATIONS
*
***********************************************************************
         SPACE 5
ALLON    EQU   X'FF'                ONE BYTE MASK-ALL BITS ON
ATNSTAT  EQU   X'80'                ATTENTION STATUS
DESTAT   EQU   X'04'                DEVICE END STATUS
CUESTAT  EQU   X'20'                CONTROL UNIT END STATUS  LD YA02144
IQEPARAM EQU   4                    DISPLACEMENT TO IQEPARAM
RBNEXAV  EQU   96                   DISPLACEMENT TO IRB POINTER TO IQE
IOBNTCPT EQU   X'10'                UCB IOB INTERCEPT FLAG
DVC3277  EQU   X'09'                DEVICE TYPE FOR 3277 DISPLAY
DVC3286  EQU   X'0B'                DEVICE TYPE FOR 3286 PRINTER
IQELNGTH EQU   16                 LENGTH OF AN IQE             @YA02128
IQELINK  EQU   0                  DISPL. TO LK FILD IN IEQ   LD YA03953
*
DEERR    EQU   X'40'              DEVICE END PLUS ERROR        @YA02128
IGG019UP EQU   X'10'              USING BTAM'S READY ROUT      @YA02128
LINERDY  EQU   X'01'              MTR FLAG FOR READY IN GROUP  @YA02128
OPENFAIL EQU   X'02'              OPEN FAILED FOR DEVICE       @YA02128
OPENINIT EQU   X'80'              OPEN IN PROGRESS FLAG        @YA02128
OPENRDY  EQU   X'40'              DEV INTERRUPT DURING OPEN    @YA02128
READY    EQU   X'20'              GRAF FLAG READY              @YA02128
RDYNDONE EQU   X'04'              PROCESSING READY             @YA02128
RLN1     EQU   X'01'                RLN FOR RLN 1
ONE      EQU   1
TWO      EQU   2                                              LD Y02947
FOUR     EQU   4
ZERO     EQU   0                                              LD Y02947
A12      EQU   12                                             LD Y02947
X28      EQU   X'28'                                        LD @ZA02340
         EJECT
******
*              ENTRY CODE & ADDRESSABILITY
******
         SPACE 3
         STM   ZERO,EPREG,ZERO(R13) SAVE REGISTERS            LD Y02947
         BALR  BASEREG,ZERO         ESTABLISH ADDRSSBLTY FOR  LD Y02947
         USING *,BASEREG            ATTENTION RTN CSECT
         B     CONTIN               BRANCH AOUND MOD ID        @YA02128
         DC    CL8'IECTATEN'        MODULE ID                 LD Y02947
         DC    C' 4322'             DATE LAST CHANGE        LD @ZA02340
         DC    C'&SYSDATE'          DATE LAST ASSEMBLY      LD @ZA02340
PATCH    DC    10F'0'               PATCH AREA
CONTIN   DS    0H                                              @YA02128
         USING UCB,UCBREG           ESTABLISH ADDRESSABILITY FOR UCB
         USING IOSB,IOSBREG         ADDRESSABILITY FOR IOSB   LD Y02947
         SPACE 5
******
*         CHECK ADDRESSABILITY OF ATTN RTN.
*         IF RUNNING UNDER THE MASTER SCHEDULER ADDRESSABILITY
*         RETURN TO IOS+4 IN ORDER TO BE RESCHEDULED UNDER THE
*         USER ADDRESSABILITY.
******
         USING SRB,SRBREG                                     LD Y02947
         USING ASCB,ASCBREG                                   LD Y02947
         LR    IOSBREG,PARMREG      GET IOSB ADDRESS          LD Y02947
         L     SRBREG,IOSSRB        GET SRB ADDRESS           LD Y02947
         L     ASCBREG,SRBASCB      GET ADDRESS OF ASCB       LD Y02947
         L     UCBREG,IOSUCB        ADDRESS OF UCB            LD Y02947
         L     UCBEXTRG,UCBEXTPT    ADDR OF COMMON UCB EXT    LD YM0160
         USING UCBCMEXT,UCBEXTRG    ADDRESSABILITY FOR...     LD YM0160
*                                   COMMON UCB EXTENSION.     LD YM0160
         CLC   UCBASID(TWO),ZEROS   IS THIS AN...             LD YM6997
*                                   UNSOLICITED ATTENTION?    LD YM6997
         BE    RTNTOIOS             YES, RETURN               LD YM6997
*                                                             LD YM6997
         CLC   ASCBASID(TWO),UCBASID IS ATTN RTN RUNNING      LD Y02947
*                                   UNDER USER ADDRESSABILITY LD Y02947
         DROP  ASCBREG
         DROP  SRBREG
         BE    CONTINUE             BRANCH IF YES             LD Y02947
         MVC   IOSASID(TWO),UCBASID MOVE USER MEMORY ID TO    LD YM0160
*                                   IOSB FOR IOS.             LD YM0160
         LM    ZERO,EPREG,ZERO(R13) AND RETURN TO             LD Y02947
         B     FOUR(R14)            IOS+4                     LD Y02947
         SPACE 5
******
*         ADDRESS OF AEE STAGE 2 FROM CVT
******
CONTINUE EQU   *                                              LD Y02947
         USING CVT,CVTREG           ADDRESSABILITY FOR CVT    LD Y02947
         L     CVTREG,CVTPTR        ADDRESS OF CVT            LD Y02947
         L     AEEREG,CVT0EF00      ADDRESS OF AEE STAGE 2    LD Y02947
         DROP  CVTREG                                         LD Y02947
         SPACE 5
*              VALIDATE DEVICE
         SPACE 3
CHKTYPE  CLI   UCBTBYT3,UCB3DISP   GRPHS DEVICE CLASS IN UCB  LD Y02947
         BNE   RTNTOIOS             NO, RETURN TO IOS
         CLI   UCBTBYT4,DVC3277    IS DEVICE 3270 DEVICE      LD Y02947
         BL    RTNTOIOS            NO, RETURN TO IOS
         CLI   UCBTBYT4,DVC3286    MAYBE, CHECK FURTHER       LD Y02947
         BH    RTNTOIOS            NO, RETURN TO IOS
         SPACE 3
*              CHECK OPEN STATUS
         SPACE 3
OPENCHK1 L     IRBREG,UCBIRB        GET ADDRESS OF IRB         @YA02128
         LA    IRBREG,0(IRBREG)   CLEAR GRAF FLAGS             @YA02128
         LTR   IRBREG,IRBREG        IRB ADDR ESTABLISHED BY OPEN
         BZ    RTNTOIOS             NO, IGNORE ATTENTION    LD @ZA02340
         TM    UCBFL1,X28          RQE ADDR PRESENT         LD @ZA02340
         BZ    CHKCSW              NO, ASYNCHRONOUS         LD @ZA02340
*                                  YES, RETURN TO IOS...    LD @ZA02340
*                                  ...SINCE THIS IS...      LD @ZA02340
*                                  ...ENDING STATUS.        LD @ZA02340
         SPACE 3
*              EXIT CODE
         SPACE 3
RTNTOIOS EQU   *                                              LD Y02947
         LM    ZERO,EPREG,ZERO(R13) LOAD REGISTER             LD Y02947
         BR    RTNREG                                         LD Y02947
         SPACE 3
*                                 ASYNCHRONOUS READY           @YA02128
CHKREADY EQU   *                  READY QUEUE HAS BEEN         @YA02128
*                                 INITIALIZED                  @YA02128
         TM    UCBGRAF,OPENFAIL   DID OPEN FOR DEVICE FAIL ?   @YA02128
         BO    CHKFUTH            NO, RETURN TO IOS            @YA02128
         TM    UCBGCB,DEERR       DE WITH ERROR ?              @YA02128
         BO    FLGERCMP           YES, SET ERROR               @YA02128
         B     RTNTOIOS           NO, RETURN                   @YA02128
CHKFUTH  EQU   *                  CHECK FURTHER                @YA02128
*
*              6 LINES DELETED                               LD YA03957
*
         OI    UCBGRAF,READY      SET READY FLAG               @YA02128
         L     IQEREG,RBNEXAV(IRBREG) GET ADDR OF READY QUEUE  @YA02128
         MVC   RBNEXAV(FOUR,IRBREG),0(IQEREG) CHAIN NEXT IQE   @YA02128
         XC    0(FOUR,IQEREG),0(IQEREG) REMOVE IQE FROM CHAIN  @YA02128
         XR    WORK1,WORK1        CLEAR EVEN REG               @YA02128
         IC    WORK2,UCBRLN       PICK UP RLN OF THIS DEVICE   @YA02128
         CLI   UCBRLN,RLN1        IS THIS THE MASTER UCB ?     @YA02128
         BE    MSTADDOK           YES, USE THE ADDRESS         @YA02128
         L     MSTREG,UCBCTLNK    NO, GET ADDR OF MASTER UCB   @YA02128
         SPACE 3
         LA    MSTREG,0(MSTREG)   CLEAR THE HIGH BYTE          @YA02128
         B     MSTUCBOK           GO AND CHECK                 @YA02128
MSTADDOK EQU   *                  SET UP TO CHECK MASTER UCB   @YA02128
         LR    MSTREG,UCBREG      PICK UP MASTER UCB ADDR      @YA02128
MSTUCBOK EQU   *                  MSTREG CONTAINS ADDRESS      @YA02128
         USING MSTRUCB,MSTREG     TELL ASSEMBLER               @YA02128
         TM    MTRGRAF,IGG019UP   USING BTAM'S ?               @YA02128
         BNO   NOSETRDY           NO, DON'T SET LINERDY        @YA02128
         OI    MTRGRAF,LINERDY    INDICATE DEVICE READY        @YA02128
         B     LNRDYSET           BYPASS TURN OFF              @YA02128
NOSETRDY EQU   *                  TURN OFF MTR LINE READY      @YA02128
         NI    MTRGRAF,ALLON-LINERDY TURN OFF LINE READY       @YA02128
LNRDYSET EQU   *                  MTR GRAF FLAGS SET           @YA02128
         L     DEBREG,MTRDEBAD    GET DEB ADDRESS FOR GROUP    @YA02128
         DROP  MSTREG             DON'T NEED MASTER ANYMORE    @YA02128
         LA    DEBREG,0(DEBREG)   CLEAR HIGH BYTE              @YA02128
         SLL   WORK2,24           PUT RLN IN HIGH BYTE         @YA02128
         OR    DEBREG,WORK2       PUT RLN IN PARM              @YA02128
         ST    DEBREG,IQEPARAM(IQEREG) STORE RLN/DEBADDR       @YA02128
         LNR   PARMREG,IQEREG     PARM = COMPLEMENT OF         @YA02128
*                                 IQE ADDRESS                  @YA02128
         LR    EPREG,AEEREG       ENTRY POINT TO AEE2          @YA02128
         BALR  RTNREG,EPREG       GO TO AEE2 TO SCHEDULE       @YA02128
*                                 ASYNCHRONOUS DEVICE          @YA02128
*                                 READY ROUTINE                @YA02128
         NI    UCBGRAF,ALLON-OPENFAIL TURN OFF OPENFAIL FLAG   @YA02128
         TM    UCBGCB,DEERR       ERROR WITH DE STAT ?         @YA02128
         BO    FLGERCMP           YES, SET ERROR               @YA02128
         B     RTNTOIOS           RETURN TO IOS+4              @YA02128
FLAGRDY  EQU   *                  DEVICE INTERRUPT IN OPEN     @YA02128
         OI    UCBGRAF,OPENRDY    SET FLAG INT DURING OPEN     @YA02128
         TM    UCBGCB,DEERR       ERROR WITH DE STAT ?         @YA02128
         BO    FLGERCMP           YES, SET ERROR               @YA02128
         B     RTNTOIOS           RETURN TO IOS                @YA02128
*              CHECK CSW STATUS OTHER THAN ATTN,DE AND CUE   LD YA02144
CHKCSW   TM    IOSTSA,ALLON-ATNSTAT-DESTAT-CUESTAT           LD YA02144
         BZ    OTHRSTS              NO, CHECK FURTHER
FLGERROR EQU   *                                              LD YM5698
         TM    IOSTSA,DESTAT      DEVICE END ?                 @YA02128
         BNO   PROCATTN           SINCE ERROR, TRY TO...     LD YA03957
*                                 TO SCHE 2ND LEVEL SO...    LD YA03957
*                                 USER CAN BE AWARE OF.      LD YA03957
         OI    UCBGCB,DEERR       SET DE WITH ERROR FLAG       @YA02128
         TM    UCBGRAF,OPENFAIL   NOT PREVIOUSLY READY ?       @YA02128
         BO    OPENCHK            YES, PROCESS READY           @YA02128
FLGERCMP EQU   *                  RETURN WITH ERROR            @YA02128
         NI    UCBGCB,ALLON-DEERR TURN OFF DE WITH ERROR       @YA02128
         B     RTNTOIOS             RETURN TO IOS             LD Y02947
OTHRSTS  TM    IOSTSB,ALLON         ERROR STATUS IN BYTE2     LD Y02947
         BNZ   FLGERROR             YES, FLAG ERROR IN UCB
         TM    IOSTSA,ATNSTAT       ATTENTION STATUS ON       LD Y02947
         BO    PROCATTN           YES, GO PROCESS IT           @YA02128
         TM    IOSTSA,DESTAT      DEVICE END STATUS ?          @YA02128
         BZ    RTNTOIOS           NO, RETURN TO IOS            @YA02128
OPENCHK  EQU   *                  SEE IF OPEN IN PROGRESS,     @YA02128
*                                 OR IF OPEN COMPLETE          @YA02128
         TM    UCBGRAF,OPENRDY+READY UCB ALREADY READY         @YA02128
         BZ    NXTCHK             NO, CHECK FURTHER            @YA02128
         TM    UCBGCB,DEERR       ERROR STAT ?                 @YA02128
         BO    FLGERCMP           YES, SET ERROR               @YA02128
         B     RTNTOIOS           NO, RETURN                   @YA02128
NXTCHK   EQU   *                  CHECK FURTHER                @YA02128
         TM    UCBGRAF,OPENINIT   IS OPEN IN PROGRESS ?        @YA02128
         BO    FLAGRDY            YES, FLAG INT DURING OPEN    @YA02128
         L     IRBREG,UCBRDYQ     GET ADDRESS OF READY IRB     @YA02128
         LA    IRBREG,0(IRBREG)   CLEAR HIGH BYTE              @YA02128
         LTR   IRBREG,IRBREG      READY QUEUE INITIALIZED ?    @YA02128
         BNZ   CHKREADY           YES, SEE IF NEED SCHEDULE    @YA02128
         TM    UCBGCB,DEERR       ERROR WITH STAT ?            @YA02128
         BO    FLGERCMP           YES, SET ERROR               @YA02128
         B     RTNTOIOS           NO, RETURN                   @YA02128
*
PROCATTN EQU   *                  PROCESS THE ATTENTION        @YA02128
*
*              2 LINES DELETED                               LD YA03957
*
ACPTATN  TM    UCBGCB,UCBSKPFG      SKIP FLAG ON              LD Y02947
         BZ    CHKRLN               NO, CHECK FOR PENDING READ
SETATNFL OI    UCBGCB,UCBATRCD      SET ATT. FLAG IN GCB      LD Y02947
         B     RTNTOIOS             RETURN TO IOS             LD Y02947
*
CHKRLN   CLI   UCBRLN,RLN1          UCB RLN 1 FOR LINE GROUP
         BE    RLNISONE             YES, UCB = MASTER UCB
         L     MSTREG,UCBCTLNK      NO, GET ADDRESS OF MASTER UCB
         USING MSTRUCB,MSTREG       & ESTABLISH ADDRESSABILITY
*
RDTIPND  EQU   *                  CHECK STATUS OF UCB          @YA02128
         TM    MTRGRAF,RDYNDONE   PROCESSING READY ?           @YA02128
         BO    NOSCHED            YES, DON'T SCHEDULE          @YA02128
         TM    MTRGCB,UCBRIPND      READ INITIAL PENDING
         BO    SCHEDIQE             YES, SCHEDULE 2ND LEVEL ATTN RTN
NOSCHED  EQU   *                  WON'T SCHEDULE               @YA02128
         TM    UCBGCB,UCBATRCD      IF ATT. FLAG ALREADY ON,  LD Y02947
         BO    RTNTOIOS             RETURN TO IOS             LD Y02947
         SR    WORK1,WORK1
         IC    WORK1,MTRATNCT       INCREMENT ATTENTION COUNT IN
         LA    WORK1,ONE(WORK1)     MASTER UCB BY 1
         STC   WORK1,MTRATNCT
         B     SETATNFL             SET ATTENTION FLAG IN GCB
*              SCHEDULE 2ND LEVEL ATTENTION ROUTINE
SCHEDIQE L     IQEREG,RBNEXAV(IRBREG) GET ADDRESS OF IQE
         ST    UCBREG,IQEPARAM(IQEREG) STORE ADDRESS OF UCB IN IQE
         MVC   RBNEXAV(FOUR,IRBREG),IQELINK(IQEREG) UNCHAIN  LD YA03953
*                                   1ST AVAILABLE IQE        LD YA03953
         LNR   PARMREG,IQEREG       PARM = COMPLEMENT OF IQE ADDRESS
         LR    EPREG,AEEREG         EP = AEE2
         BALR  RTNREG,EPREG         GO TO AEE2 TO SCHEDULE IQE
         NI    MTRGCB,ALLON-UCBRIPND TURN RD PENDING FLAG OFF LD Y02947
         B     RTNTOIOS             RETURN TO IOS             LD Y02947
RLNISONE LR    MSTREG,UCBREG        TRANSFER UCB ADDRESS TO MASTER
         B     RDTIPND              UCB BASE AND CHECK FOR PENDING READ
*
ZEROS    DC    H'0'                 HALF WORD OF ZEROS       LD YA03953
*
         EJECT
ASCB     IHAASCB                                              LD Y02947
         EJECT
CVT      DSECT                                                LD Y02947
         CVT                                                  LD Y02947
         EJECT
         IECDIOSB                                             LD Y02947
         EJECT
SRB      IHASRB                                               LD Y02947
         EJECT
UCB      DSECT                                                LD Y02947
         IEFUCBOB                                             LD Y02947
         EJECT
MSTRUCB  DSECT
MTRATNCT EQU   MSTRUCB+(UCBATNCT-UCBOB)
MTRGCB   EQU   MSTRUCB+(UCBGCB-UCBOB)
MTRGRAF  EQU   MSTRUCB+(UCBGRAF-UCBOB)
MTRDEBAD EQU   MSTRUCB+(UCBRLN-UCBOB)
         SPACE 5
         END
