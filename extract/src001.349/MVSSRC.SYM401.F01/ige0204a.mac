         TITLE 'IGE0204A - BTAM ERP TIME OUT MODULE'
IGE0204A CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0204A                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM ERP TIME OUT                                                *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    IF REQUIRED, RETRIES CHANNEL PROGRAM THAT FAILED BECAUSE OF      *
*    A TIME OUT CONDITION.  DETERMINES IF LINE ERROR RECORDING IS     *
*    REQUIRED AND XCTLS EITHER TO DISABLE, LERB OR POST ROUTINE.      *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (68 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        692 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD   *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0204A                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        ALTERS EXISTING CCWS OR CREATES NEW ONES FOR RECOVERY        *
*        ALTERS DECERRST FOR SHOULD NOT OCCUR ERROR.                  *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        NONE                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YA01060  (IN BASE)                                               *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YA02436  (11/1/73)                                               *
*    YA02463  (11/1/73)                                               *
*    YM05652K (1/25/74)                                               *
*    AZ04683 (08/26/75)
*    AZ25078 (10/15/77)                                               *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         L     UCBREG,IOSUCB       BASE UCB
         DROP  ERREG5
         L     CCWREG,IOBFL3       ADDRESS OF CCW
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       ADJUST TO FAILING CCW
         MVC   IOBWORK(L1),L5(CCWREG)   TP OP CODE TO WORK AREA
         NI    IOBWORK,L255-CCWMASK1   MASK OUT UNUSED BITS
         LA    ERREG1,IOBCPA       ADDRESS OF CHAN PROG
         ST    ERREG1,IOBSTART     STORE IN PTR LOC
         CLI   ZERO(CCWREG),CCWAPOLL   THIS POLL COMMAND
         BE    ERRTOT02            UPDATE POLL COMMAND
         CLI   ZERO(CCWREG),CCWINCMD   INHIBIT COMMAND
         BE    READ                YES
         CLI   ZERO(CCWREG),CCWRDCMD    READ COMMAND
         BNE   ERRTOD00            NO CHECK FOR DISABLE OR PREPARE
READ     EQU   *
         TM    IOBWORK,CCWPTXFR    PRIOR TO TEXT TRANSFER
         BZ    ERRTOD06            YES, CHECK ADAPTER
         TM    DCBBFTEK(DCBREG),X08   DYNAMIC BUFFER SPECIFIED
         BZ    ERRTOT03            NO, CHECK FOR READ RESPONSE TEXT
         NI    IOSFLA,L255-IOSERR   TURN OFF ERP CONTROL
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
         B     ERRPERM1            POST PERMANENT ERROR
ERRTOD06 TM    UCBTYP+L3(UCBREG),T83B3A   IS THIS 83B3 ADAPTER
         BNZ   ERRTOT08            NO CONTINUE PROCESSING
         TM    UCBTYP(UCBREG),T83B3D   IS THIS 83B3
         BNO   ERRTOT08            NO CONTINUE PROCESSING
         CLI   IOBWORK,CCWRDRSP    READ RESPONSE TO ADDRESSING
         BE    ERRTOT09            YES SET UP RETRY
ERRTOT08 CLI   IOBWORK,CCWTWXID    THIS TWX ID RESPONSE
         BE    ERRTOD03            YES CHECK FOR LERB
ERRTOT00 CLI   IOBCPA,CCWDISAB     FIRST COMMAND DISABLE
         BNE   ERRTOT01            NO CHECK FOR POLL RESPONSE
ERRTOT09 LA    ERREG1,IOBCMD3      INITIALIZE CPA TO 3RD COMMND
         ST    ERREG1,IOBSTART     STORE CPA START IN POINTER
ERRTOT01 CLI   IOBWORK,CCWPOLL     READ RESPONSE TO POLLING
         BNE   ERRTOD04            NO, CHECK FOR AUTOPOLL
*                                  READ INDEX.
         CLC   IOBCSW+L5(L2),L6(CCWREG)   RESIDUAL COUNT = ORIG COUNT
         BE    ERRTOD03            YES CHECK FOR LERB
         B     ERRCTT01            NO CHECK FOR RETRY
ERRTOD04 CLI   L5(CCWREG),CCWRDIX   READ RESPONSE TO INDEX
         BNE   ERRTOD03            NO CHECK FOR LERB
ERRTOT02 EQU   *
         L     ERREG1,IOBCPA+L8    LOAD ENTRY ADDRESS FROM PREVIOUS
*                                  POLL (WITH POLL COMMAND CODE IN
*                                  HIGH ORDER BYTE).
         MVC   IOBCPA+L9(L3),DECENTRY+L1(DECBRG)   MOVE IN ADDRESS
*                                  OF LAST ENTRY POLLED (TO BE USED
*                                  AS RESTART ADDRESS).
         L     ERREG4,IOBCPA+L8    LOAD ENTRY ADDRESS FOR NEXT POLL
*                                  (WITH POLL COMMAND CODE IN HIGH
*                                  ORDER BYTE).
         SR    ERREG1,ERREG4       SUBTRACT NEW POLL ADDRESS
*                                  FROM PREVIOUS POLL ADDRESS.
         AH    ERREG1,IOBCPA+L14   IF THE NEW POLL ADDRESS
*                                  WERE LESS THAN THE PREVIOUS POLL
*                                  ADDRESS, THE EFFECT IS TO ADD THE
*                                  ABSOLUTE DIFFERENCE TO THE PREVIOUS
*                                  COUNT; IF IT WERE GREATER, THE
*                                  EFFECT IS TO SUBTRACT THE
*                                  ABSOLUTE DIFFERENCT FROM THE
*                                  PREVIOUS COUNT.
         STH   ERREG1,IOBCPA+L14   STORE ADJUSTED COUNT IN
*                                  POLL COMMAND.
         CLI   ZERO(CCWREG),CCWAPOLL   POLL COMMAND
         BE    ERRTOD03            YES CHECK FOR LERB
         B     ERRCTT01            NO CHECK FOR MORE RETRIES
ERRTOD00 CLI   ZERO(CCWREG),CCWDISAB   DISABLE COMMAND
         BE    ERRTOD02            YES  SET CPA TO START OF CHAN PROG
         CLI   ZERO(CCWREG),CCWENABL   ENABLE COMMAND
         BE    ERRFIX01            YES, CHECK FOR RESTART
         CLI   ZERO(CCWREG),CCWPREP   PREPARE COMMAND
         BE    ERRTOD02            YES  SET CPA TO START CHAN PROG
         CLI   ZERO(CCWREG),CCWDIAL   DIAL COMMAND
         BE    ERRCTT01            YES CHECK FOR LERB
         CLI   ZERO(CCWREG),CCWWRITE   WRITE COMMAND
         BNE   ERRTOSNO            NO, SHOULD NOT OCCUR
         MVC   IOBWORK(L1),UCBTYP+L3(UCBREG)   MOVE DEVTP TO WORK
         NI    IOBWORK,MASK3       AND OFF UNUSED BITS
         CLI   IOBWORK,IBMTYP3     IBM TYPE 3 ADAPTER
         BNE   ERRTOSNO            NO  SET SHOULD NOT OCCUR
         MVC   IOBERINF+L8(L1),IOBSENS0   SAVE SENSE BYTE
         MVC   IOBERINF+L9(L7),IOBCSW   SAVE 7 BYTE CSW
         MVC   IOBERCCW(L16),IOBCPA   MOVE 2 CCWS
         MVC   IOBCPA(L16),RSET2848   SET UP RESET CHAN PROG
         MVI   IOBERRCT+L1,RTRN2848   INDICATE 2848 RESET
         B     ERRTRY02            EXECUTE RESET CHAN PROG
ERRFIX01 SH    CCWREG,ERRCN8       BACK UP ONE CCW
         CLI   ZERO(CCWREG),CCWDISAB   DISABLE COMMAND
         BE    ERRTOD02            YES,RESTART CHAN PROG
ERRTOSNO OI    DECERRST(DECBRG),CSWSNO   INDICATE SHOULD NOT OCCUR
         NI    IOSFLA,L255-IOSERR  TURNOFF ERP IN CONTROL      @ZA04683
*                                      IN IOSB                 @ZA04683
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB            @ZA04683
         B     ERRTRY01            EXIT TO WRITE MESSAGE
ERRTOD02 EQU   *
         ST    CCWREG,IOBSTART     ADDR OF FAILING CCW IN POINTER
ERRTOD03 TM    DCBERROP(DCBREG),DCBLERB   LINE ERROR RECORDING REQUIRED
         BNO   ERRCTT01            NO CONTINUE FLOW
         MVI   IOBERRCT-L1,LERBTO   INDICATE TIME OUT LERB REQUIRED
ERRCTT01 CLI   DECTYPE+L1(DECBRG),TYPETTV   IS OP TYPE A WRITE TTV?
         BNE   ERRTOD05            NO, CHECK FOR RETRY
         LA    ERREG1,IOBCMD3      YES, POINT TO START OF POLLING
         ST    ERREG1,IOBSTART     STORE FOR RESTART
ERRTOD05 CLI   IOBERRCT,ERTRYCT    RETRY REQUIRED
         BL    ERRTRY02            YES SET UP FOR RETRY
         CLI   ZERO(CCWREG),CCWDISAB   DISABLE EXECUTED
         BE    ERRPERM1            YES POST COMPLETE WITH ERROR
ERRTRY01 TM    UCBTYP+L1(UCBREG),SWLINE   DISABLE REQUIRED
         BZ    ERRPERM1            NO POST COMPLETE
         MVI   IOBERRCT+L1,MASK3   SET MASK
         LA    NAMEREG,ERLOD3      ADDRESS OF DISABLE ROUTINE
         B     ERRLINK1            XCTL
ERRTRY02 IC    ERREG1,IOBERRCT     ERROR RETRY COUNT
         LA    ERREG1,L1(ERREG1)   BUMP BY ONE
         STC   ERREG1,IOBERRCT     RESTORE IN COUNT FIELD
         MVI   IOBSENS0,ZERO       CLEAR SENSE BYTE
         CLI   IOBERRCT-L1,LERBTO   LINE ERROR RECORDING REQ    YA02436
         BE    ERLERBTO            YES, EXIT TO LERB ROUTINE    YA02436
RETRY    EQU   *
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
         L     NAMEREG,TRANSRTN    LOAD NAME
         B     ERRLINK1             XCTL
ERRPERM1 CLI   IOBERRCT-L1,LERBTO   LINE ERROR RECORDING REQ    YA02436
         BE    ERLERBT1            YES EXIT                     YA02436
         NI    IOSFLA,L255-IOSERR   INDICATE ERROR POST IOSB   @ZA25078
         NI    IOBFLAG1,L255-IOBERRTN    ALSO IN IOB           @ZA25078
         LH    NAMEREG,ERLOD5      NAME OF POST ROUTINE
ERRLINK1 EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     GET BRANCH ENTRY TO XCTL
         BR    ERRETR              XCTL
         DROP  ERRETR
ERLERBT1 NI    IOSFLA,L255-IOSERR   INDICATE POST AFTER LERB
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
ERLERBTO LA    NAMEREG,ERLOD12     NAME OF LERB ROUTINE
         B     ERRLINK1            XCTL
ERRTOT03 TM    IOBWORK,CCWTXTDT    ERROR DURING TRANSFER
         BZ    ERRTOT07            NO CHECK FOR READ RESPONSE TO TXT
         CLI   IOBWORK,CCWRDTXT    CHECK FOR READ TEXT
         BNE   ERRPERM1            NO, CHECK FOR LERB
         TM    UCBTYP+L3(UCBREG),IBMTYP3   IS DEVICE 2260/2265  YA02463
         BO    ERRPERM1            YES, BRANCH                  YA02463
         NI    IOSFLA,L255-IOSERR   YES, INDICATE POST
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
         B     RETRY               EXIT TO SVC 15
ERRTOT07 CLI   IOBWORK,CCWRRTXT    READ RESPONSE TO TEXT
         BE    ERRTOT05            YES CONTINUE FLOW
         B     ERRPERM1            NO, INDICATE PERM ERROR
ERRTOT05 TM    DCBERROP(DCBREG),CCWRTRT   WRITE RETRY REQUIRED
         BZ    ERRTOT06            NO CONTINUE FLOW
         MVC   IOBWORK+L1(L1),DECTYPE+L1(DECBRG)   TYPE CDE TO WORK
         NI    IOBWORK+L1,L255-HIBIT   CLEAR UNUSED BITS
         CLI   IOBWORK+L1,CCWWRTCT   PREVIOUS CMD WRITE CONTINUE
         BNE   ERRTOT00            NO RESUME FLOW
ERRTOT06 TM    DCBERROP(DCBREG),DCBLERB   LINE ERROR RECORDING REQ
         BNO   ERRPERM1            NO POST COMPLETE WITH ERROR
         MVI   IOBERRCT-L1,LERBTO   INDICATE LERB RECORDING
         NI    IOSFLA,L255-IOSEX-IOSERR   INDICATE POST NO WTO
         NI    IOBFLAG1,L255-IOBERRTN-IOBIOERR   ALSO IN IOB
         LA    NAMEREG,ERLOD12     LINE ERROR RECORDING ROUTINE
         B     ERRLINK1            XCTL
         EJECT
**********
*
*        CONSTANTS
*
**********
         DS    0D
RSET2848 DC    X'42000001700003C0'  RESET CCWS
         DC    X'41000001200003E8'
TRANSRTN DC    F'9042'             XCTL NAME OF BTAM TRANSLATE ROUTINE
ERRCN8   DC    H'8'                DECIMAL 8
ERLOD5   DC    H'5041'
PACHAREA DC    17C'204A'           PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
IBMTYP3  EQU   X'80'
CCWWRITE EQU   X'01'
RTRN2848 EQU   X'80'
T83B3A   EQU   X'B0'
T83B3D   EQU   X'01'
CCWRDRSP EQU   X'06'
CCWAPOLL EQU   X'09'
CCWDIAL  EQU   X'29'
CCWDISAB EQU   X'2F'
CCWENABL EQU   X'27'
CCWMASK1 EQU   X'C0'
CCWPOLL  EQU   X'05'
CCWPREP  EQU   X'06'
CCWPTXFR EQU   X'30'
CCWRDCMD EQU   X'02'
CCWINCMD EQU   X'0A'
CCWRDIX  EQU   X'0A'
CCWRRTXT EQU   X'20'
CCWRTRT  EQU   X'04'
CCWTWXID EQU   X'07'
CCWTXTDT EQU   X'10'
CCWWRTCT EQU   X'04'
CSWSNO   EQU   X'40'
DCBBFTEK EQU   X'20'
DCBERROP EQU   33
DCBLERB  EQU   X'08'
DECENTRY EQU   20
DECERRST EQU   29
DECTYPE  EQU   4
ERLOD3   EQU   3041
ERLOD12  EQU   2042
ERTRYCT  EQU   X'02'
HIBIT    EQU   X'C0'
LERBTO   EQU   X'03'
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L9       EQU   9                   LENGTH AND DISPLACEMENT
L14      EQU   14                  LENGTH AND DISPLACEMENT
L16      EQU   16                  LENGTH AND DISPLACEMENT
X08      EQU   X'08'               DYNAMIC BUFFERING MASK
L255     EQU   255                 AND MASK
MASK3    EQU   X'F0'
SWLINE   EQU   X'90'
TYPETTV  EQU   X'0F'
UCBTYP   EQU   16                  DEVICE TYPE CODE
ZERO     EQU   0                   LENGTH AND DISPLACEMENT
CCWRDTXT EQU   X'11'
**********
*
*        REGISTER DEFINITION
*
**********
ERREG3   EQU   0                   WORK REG/PARAMETER REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
ERREG2   EQU   2                   WORK REG
EWAREG   EQU   3                   ERP WORKAREA BASE
CCWREG   EQU   4                   CCW ADDRESS REG
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE
BASEREG  EQU   8                   PROGRAM BASE
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG4   EQU   11                  LINK REG/WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  LINK REG
ERREG5   EQU   15                  LINK REG/WORK REG
         EJECT
         IECDRQE
         EJECT
         IEZIOB
IOBCMD3  EQU   IOBCPA+L16
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
