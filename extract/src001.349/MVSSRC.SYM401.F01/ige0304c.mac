         TITLE 'IGE0304C - BSC INTERVENTION REQUIRED ERP ROUTINE'
IGE0304C CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0304C                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM INTERVENTION REQUIRED ERP                                   *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS ROUTINE RECOVERS FROM INTERVENTION REQUIRED ERROR           *
*    CONDITIONS THAT OCCUR DURING BSC OPERATION. THE CCW THAT IS      *
*    IN ERROR IS DETECTED. THE PROPER ERROR RECOVERY PROCEDURE        *
*    FOR THE TYPE OF OPERATION BEING EXECUTED IS THEN INITIATED.      *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (48 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        470 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD.  *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0304C                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        SETS INDICATOR IN DECERRST FOR SHOULD NOT OCCUR ERROR        *
*        ALTERED CHANNEL PROGRAM                                      *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        NONE                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YA02142  (11/5/73)                                               *
*    YM05652K (1/25/74)                                               *
*    ZA00504  (5/9/74)                                                *
*    ZA00527  (9/4/74)                                                *
*    ZA14141  (10/28/76)                                              *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         B     BEGIN               BRANCH ARUOND ID            @ZA00504
         DC    C' IGE0304C '       MODULE ID                   @ZA00504
         DC    C' 4129 '           DATE LAST CHANGE            @ZA00504
         DC    CL8'&SYSDATE'       DATE LAST ASSEMBLY          @ZA00504
BEGIN    DS    0H                                              @ZA00504
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         USING IECTDECB,DECBRG          DECB DSECT BASE REGISTER
         USING IHADCB,DCBREG            DCB DSECT BASE REGISTER
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         L     UCBREG,IOSUCB       BASE UCB
         DROP  ERREG5
         L     CCWREG,IOBFL3       GET IOB CSW
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       BACK UP TO FAILING CCW IN IOB
         MVC   IOBWORK(L1),TPOPCODE(CCWREG)   TP CODE TO IOB WORK AREA
         NI    IOBWORK,L255-UNUSBIT  TURN OFF UNUSED HI-ORDER BITS
         TM    L17(UCBREG),X90     IS THIS DIAL LINE
         BNZ   BSCIR05             YES, DISABLE AND POST
         CLI   ZERO(CCWREG),CCWPREP   ERROR OCCUR ON A PREPARE CMD.
         BE    BSCIR06
         CLI   ZERO(CCWREG),CCWPOLL   ERROR OCCUR ON A POLL COMMAND
         BE    BSCIR07             UPDATE CHAN PROG. RESTART POLL
         CLI   IOBWORK,DURINTXT    ERROR OCCUR DURING TEXT
         BE    BSCIR001            YES CHECK BUFFERING TECH
         CLI   IOBWORK,ZERO        ERP CCW
         BE    BSCIR01             YES, RESTART CHANNEL PROG
         B     BSCIR06             RESTART FAILING CCW
BSCIR001 EQU   *
         CLI   ZERO(CCWREG),CCWREAD   ERROR OCCUR ON A READ COMMAND
         BNE   BSCIR08
         TM    DCBBFTEK,BSCDYBUF   IS DYNAMIC BUFFERING EMPLOYED
         BZ    BSCIR09
BSCIR00  MVI   IOBERRCT,ERRMAX     SET RETRY COUNT TO MAXIMUM
BSCIR01  TM    DCBERROP,LERBREQ    IS LERB REQUIRED
         BNO   BSCIR02             NO, SKIP INTV REQ
         MVI   IOBINCAM+L1,IRIND   TURN ON INTV REQ FLAG
BSCIR02  CLI   IOBERRCT,ERRMAX     IS RETRY COUNT AT MAXIMUM
         BE    BSCIR04             YES, GO INDICATE PERMANENT ERR.
         IC    ERREG1,IOBERRCT     NO, UPDATE
         LA    ERREG1,L1(ERREG1)   RETRY
         STC   ERREG1,IOBERRCT     COUNT
         CLI   IOBINCAM+L1,IRIND
         BE    BSCIR03
         MVI   EWAIND1,EWAISVCF    SVC 15/SVC 3                YM05652K
         L     NAMEREG,TRANSRTN    LOAD NAME OF 904B           YM02594K
         B     XCTLOUT             XCTL
BSCIR03  LA    NAMEREG,LERBADDR    GET LERB ROUTINE ADDR
XCTLOUT  EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE
         DROP  ERRETR
         BR    ERRETR              BRANCH INTO XCTL ROUTINE
BSCIR04  NI    IOSFLA,L255-IOSERR   TURN ON PERM. ERROR INDICATOR
         NI    IOBFLAG1,L255-IOBERRTN   ALSO IN IOB
         CLI   IOBINCAM+L1,IRIND   IS LERB REQUIRED
         BE    BSCIR03             GO, BRANCH TO LERB
         LA    NAMEREG,ERLOD25     ADDRESS OF ERROR POST
         B     XCTLOUT
BSCIR05  CLI   ZERO(CCWREG),CCWDIAL   THIS DIAL CCW
         BNE   BSCIR00             NO, POST WITH ERROR
         MVI   IOBERCCW,DISABLE    COMMAND CODE DISABLE
         MVI   IOBERCCW+L7,DISABCNT   MOVE COUNT INTO DISABLE CCW
         MVI   IOBERCCW+L4,SLICCFLG   SLI & CC FLAGS INTO CCW
         ST    CCWREG,IOBERCCW+L8   MOVE FAILING CMD ADDR INTO TIC
         MVI   IOBERCCW+L8,TICCOMMD   MOVE TIC COMMAND CODE INTO CCW
BSCIR05A LA    CCWREG,IOBERCCW
BSCIR06  ST    CCWREG,IOBSTART     SET CPA ADDR. TO PREPARE
         B     BSCIR01
BSCIR07  LA    ERREG2,IOBCPA       UPDATE IOBSTART TO POINT TO
         ST    ERREG2,IOBSTART     START OF USER CHANNEL PROGRAM
         L     ERREG1,IOBCPA+L8    ADDR OF USER POLLING ENTRY
         MVC   IOBCPA+L9(L3),DECENTRY+L1   MOVE FAILING ADDR TO CCW
         L     ERREG2,IOBCPA+L8    ADDR OF FAILING POLL CCW
         SR    ERREG1,ERREG2       COUNT BETWEEN USERS AND FAILED
         AH    ERREG1,IOBCPA+L14   ADD TOTAL COUNT FROM USER ENTRY
         STH   ERREG1,IOBCPA+L14   STORE NEW COUNT IN POOL CCW
         B     BSCIR01
BSCIR08  CLC   DECCOUNT(L2),L6(CCWREG)   RESIDUAL COUNT=ORIGINAL COUNT
         BNE   BSCIR00             NO, SET RETRY COUNT TO MAXIMUM
         B     BSCIR01             YES, CHECK FOR LERB REQUIRED
BSCIR09  CLI   IOBCPA,CCWPOLL      OPERATION READ TI WITH MULTIPOINT
         BNE   BSCIR09A
         LR    ERREG1,CCWREG       POINT TO CURRENT CCW
         SH    ERREG1,ERRCN8       BACK UP ONE
         L     ERREG1,ZERO(ERREG1)   GET AREA ADDRESS
         LA    ERREG1,L1(ERREG1)   BUMP BY ONE (INDEX BYTE) LD @ZA00527
         CLM   ERREG1,L7,L1(CCWREG)   HAS COUNT BEEN ADJUSTED  @ZA14141
         BE    BSCIR09A            YES, CONTINUE
         STCM  ERREG1,L7,L1(CCWREG)   NO, RESTORE READ      LD @ZA00504
         LH    ERREG1,L6(CCWREG)   PICK UP COUNT
         LA    ERREG1,L1(ERREG1)   BUMP BY ONE
         STH   ERREG1,L6(CCWREG)   STORE
BSCIR09A LA    ERREG1,IOBWORK+L2   ENQ-READ-IN ADDR FOR ERP
         ST    ERREG1,IOBERCCW     ENQ ADDRESS INTO ERP CCW
         MVI   IOBERCCW,CCWREAD    READ COMMAND CODE INTO ERP CCW
         MVI   IOBERCCW+L4,SLICCFLG   SLI AND CC FLAGS SET IN CCW
         MVI   IOBERCCW+L7,CCWCOUNT   COUNT FOR READ ENQ IN CCW
         ST    CCWREG,IOBERCCW+L8   AND STORE IN NOP COMMAND
         MVI   IOBERCCW+L8,NOPCC   MOVE NOP COMMAND CODE INTO CCW
         MVI   IOBWORK+L1,RTRNCDE1   SPEC RETURN CODE
         MVI   IOBERRCT+L1,RTRNSPCD   LINK OFFSET
         LA    ERREG1,L1           GET COUNT OF ONE             YA02142
         ST    ERREG1,IOBERINF+L4   STORE IT                    YA02142
         B     BSCIR05A
         EJECT
**********
*
*        CONSTANTS
*
**********
TRANSRTN DC    F'9042'             NAME OF BTAM TRANSLATE ROUTINE
ERRCN8   DC    H'8'
PACHAREA DC    12C'304C'           PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
ZERO     EQU   X'00'               LENGTH AND DISPLACEMENT
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L9       EQU   9                   LENGTH AND DISPLACEMENT
L14      EQU   14                  LENGTH AND DISPLACEMENT
L17      EQU   17                  LENGTH AND DISPLACEMENT
X90      EQU   X'90'               DIAL MASK
L255     EQU   X'FF'               AND MASK
UNUSBIT  EQU   X'C0'               HI-ORDER BITS OF TP CODE
CCWDIAL  EQU   X'29'               COMMAND CODE FOR DIAL
CCWPREP  EQU   X'06'               COMMAND CODE FOR PREPARE
CCWPOLL  EQU   X'09'               COMMAND CODE FOR POLL
DURINTXT EQU   X'11'               CCW DURING TEXT TRANSFER
CCWREAD  EQU   X'02'               COMMAND CODE FOR READ
BSCDYBUF EQU   X'08'               DYNAMIC BUFFERING FLAG
ERRMAX   EQU   7                   MAXIMUM RETRY COUNT IN ERP
LERBREQ  EQU   X'08'               FLAG IN DCB INDICATING LERB
IRIND    EQU   2
LERBADDR EQU   2042                LERB ROUTINE ADDR FOR XCTL
ERLOD25  EQU   2043
TPOPCODE EQU   5                   OFFSET TO TP CODE IN CCW
DISABLE  EQU   X'2F'               COMMAND CODE FOR DISABLE
DISABCNT EQU   X'01'               COUNT FOR DISABLE CCW
TICCOMMD EQU   X'08'               COMMAND CODE FOR TIC
NOPCC    EQU   X'03'
SLICCFLG EQU   X'60'               SLI & CC FLAGS
CCWCOUNT EQU   X'02'               COUNT FOR READ ENQ CCW IN ERP
RTRNCDE1 EQU   X'81'
RTRNSPCD EQU   X'14'
**********
*
*        REGISTER DEFINITION
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
EWAREG   EQU   2                   ERP WORKAREA BASE
ERREG4   EQU   3                   WORK REG
CCWREG   EQU   4                   CCW ADDRESS REG
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE
BASEREG  EQU   8                   PROGRAM BASE
ERREG2   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG1   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  LINK REG
ERREG5   EQU   15                  LINK REG/WORK REG
         EJECT
         IECTDECB
         EJECT
         DCBD  DSORG=BX,DEVD=BS
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
