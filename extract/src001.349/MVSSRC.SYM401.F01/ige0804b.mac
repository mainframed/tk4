         TITLE 'IGE0804B - BTAM ERP CHANNEL CHECK RECOVERY ROUTINE'
IGE0804B CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0804B                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM CHANNEL CHECK ERP                                           *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS ROUTINE IS ENTERED FROM IGE0004A FOR A CHANNEL CONTROL      *
*    CHECK, CHANNEL DATA CHECK, OR INTERFACE CONTROL CHECK. THE       *
*    ERPIB IS LOCATED AND ANALYZED.  IF POSSIBLE, RECOVERY IS         *
*    ATTEMPTED.  IF NOT, CONTROL IS PASSED TO THE MESSAGE WRITER      *
*    MESSAGE WRITER MODULE AND PERMANENT ERROR IS SET.                *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (64 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        630 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD.  *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0804B                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        ALTERED CHANNEL PROGRAM                                      *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        NONE                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YM05652K (1/24/74)                                               *
*    YA03263  (2/4/74)                                                *
*    ZA12164  (09/07/76)                                              *
*    ZA12165  (09/07/76)                                              *
*    ZA20121  (05/26/77)                                              *
*    ZA20123  (06/07/77)                                              *
*    ZA28820  (12/08/77)                                              *
*    ZA29881  (01/31/78)                                              *
*    ZA32894  (05/11/78)                                              *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     UCBREG,IOSUCB       BASE UCB
         DROP  ERREG5
         L     CCWREG,IOBFL3       POINT TO CCW
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       ADJUST TO FAILING CCW
         BP    OKCSW               CSW-CCW IS OK               @ZA29881
         L     CCWREG,IOBSTART     NO  GET GOOD CCW ADDR       @ZA29881
         STCM  CCWREG,7,IOBCSW     STORE IN IOBCSW             @ZA29881
         B     POST                POST                        @ZA29881
OKCSW    OI    EWASTUP+L1,X01      SET CH CHK FOR STAT UPDATE  @ZA29881
         MVC   IOBRESTR(L4),EWAERPIB+L4   SAVE PERTINENT DATA IN IOB
         XC    EWAERPIB+L1(L7),EWAERPIB+L1  CLEAR LAST 7 BYTES @ZA12165
         TM    IOBRESTR,RETRY1     IS NO-RETRY FLAG SET
         BO    POST                YES
         TM    IOBRESTR,CSWSTORD   VALID CSW STORED (SIO OR INTERRUPT)
         BNZ   CHKRETRY            YES, CHECK THE RETRY CODE
         TM    IOBRESTR,HALTIO     DID A HIO CAUSE INTERRUPT
         BZ    POST
         BAL   ERRLNK,CCWADD       VERIFY CCW ADDRESS
         B     FINDVAL             INVALID, TRY TO FIND VALID CCW
SETCCW   LA    CCWREG,L8(CCWREG)   ADD 8 TO INITIALIZE.
         ST    CCWREG,EWAISAVE     SET FOR CHANNEL END
         MVC   IOBCSW(L3),EWAISAVE+L1   STORE ADDR IN CSW
         MVC   IOBCSW+L3(L2),CEDEUX   SET CHANNEL END, DEVICE END,
*                                  UNIT EXCEPTION
         NI    IOSFLA,L255-IOSEX-IOSERR   INDICATE ERROR CORRECTED
         NI    IOBFLAG1,L255-IOBERRTN-IOBIOERR   ALSO IN IOB
         B     ERRCHK05            EXIT
FINDVAL  LA    CCWREG,IOBCPA
LOOPAGIN CLI   ZERO(CCWREG),X06    IS CCW A PREPARE
         BE    SETCCW              YES, GO SET UP FOR CHANNEL END
         CLI   ZERO(CCWREG),X27    IS CCW AN ENABLE
         BE    SETCCW              YES, GO SET UP FOR CHANNEL END
         CLI   ZERO(CCWREG),X0A    IS CCW AN INHIBIT
         BE    SETCCW              YES, GO SET UP FOR CHANNEL END
         TM    L5(CCWREG),CCWLAST   IS LAST CCW IN CHANNEL PGM
         BO    POST                YES
         LA    CCWREG,L8(CCWREG)   STEP POINTER TO NEXT CCW
         B     LOOPAGIN            LOOP THRU ONCE AGAIN
CHKRETRY BAL   ERRLNK,CCWADD       VERIFY CCW ADDRESS VALIDITY
         B     POST
         TM    IOBRESTR+L2,RETRYCDE   IS RETRY CODE VALID
         BZ    POST
         LA    ERREG2,L3           PLACE CONTROL UNIT TYPE IN
         N     ERREG2,UNITTYPE(UCBREG)   REGISTER
*                                            2701 = 2 = X'0010'
*                                            2702 = 1 = X'0001'
*                                            2703 = 3 = X'0011'
         BCTR  ERREG2,ZERO         DECREMENT BY ONE
         SLL   ERREG2,L2           MULTIPLY BY FOUR
*                                  REGISTER NOW CONTAINS FOR
*                                            2701 = 4
*                                            2702 = 0
*                                            2703 = 8
         LA    ERREG7,L7           INITIALIZE FOR RETRY CODE
         N     ERREG7,IOBRESTR     ONLY VALID RETRY CODE REMAINS
         LA    ERREG6,L1           SET UP INCREMENT FOR BXLE'S
         SR    ERREG4,ERREG4       INITIALIZE FIRST BXLE OPERAND
         MVI   IOBRESTR+L1,XC0
         NC    IOBRESTR+L1(L1),IOBRESTR+L3
         CLI   IOBRESTR+L1,X40     TERMINATION CODE = CONDITION CODE
*                                            X'00'            4 - LOW
*                                            X'01'            8 - EQUAL
*                                            X'02'            2 - HIGH
         BXLE  ERREG4,ERREG6,CODE001   BRANCH IF RETRY CODE NOT X'000'
         B     POST
CODE001  BXLE  ERREG4,ERREG6,CODE010   BRANCH IF RETRY CODE NOT X'001'
         BH    RETRY               BRANCH IF TERMINATION CODE IS 2
         BE    CK00A(ERREG2)       BRANCH TERMINATION CODE 1
         B     CK00B(ERREG2)       BRANCH TERMINATION CODE 2
CODE010  BXLE  ERREG4,ERREG6,CODE011   BRANCH IF RETRY CODE NOT X'010'
         BH    RETRY               BRANCH TERMINATION CODE 2
         BL    CK00B(ERREG2)       BRANCH TERMINATION CODE 0
         B     CK00C(ERREG2)       BRANCH TERMINATION CODE 1
CODE011  BXLE  ERREG4,ERREG6,CODE100   BRANCH IF RETRY CODE NOT X'011'
C02C011  BH    POST                REQUIRES A SIO TO ALLOW AN
*                                  EQUIPMENT CHECK INTERRUPT THEN
*                                  ATTEMPT RECOVERY.
         BE    LOSTD               BRANCH TERMINATION CODE 1
         B     *+L4(ERREG2)        DETERMINE CONTROL UNIT TYPE
         B     LOSTD               2702
         B     SNO                 2701
LOSTD    EQU   *                   2703
         CLI   ZERO(CCWREG),X29    FAILING CCW A DIAL
         BNE   CHKSNS              NO, CHECK FOR A READ
         B     RETRY               YES, BRANCH TO RETRY CH PGM
CODE100  BXLE  ERREG4,ERREG6,CODE101   BRANCH IF RETRY CODE NOT X'100'
         BE    SNO                 BRANCH IF TERMINATION CODE 1
         BH    RETRY               BRANCH IF TERMINATION CODE 2
         B     CK00B(ERREG2)       BRANCH IF TERMINATION CODE 0
CODE101  BXLE  ERREG4,ERREG6,POST   BRANCH IF RETRY CODE NOT X'101'
         LTR   ERREG2,ERREG2       IS CONTROL UNIT A 2702
         BNZ   CHKSNS              BRANCH IF NOT A 2702
         CLI   IOBRESTR+L1,X40     RESET THE CONDITION CODE
         B     C02C011             BRANCH, 2702 - USE CODE 011
CHKSNS   EQU   *
         CLI   IOBSENS0,ZERO       IS SENSE BYTE 0
         BNE   CHEQCK
         TM    IOBCSW+L4,L8        IS CHANNEL DATA CHECK
         BO    CKDIAL
         B     SNO                 ELSE, BRANCH TO SHOULD NOT OCCUR
CHEQCK   TM    IOBSENS0,X10        IS EQUIPMENT CHECK SET
         BO    CKDIAL
         CLI   0(CCWREG),X'29'     FAILING CCW DIAL?           @ZA28820
         BE    RETRY               YES, BR RETRY CH PGM        @ZA28820
         TM    IOBSENS0,X08        IS DATA CHECK SET
         BNO   CHKMORE             NO, BRANCH
         LA    NAMEREG,DATACHK     GET NAME OF DATA CHK ERP MOD
ERRLINK1 EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE
         DROP  ERRETR
         BR    ERRETR              XCTL FOR RETRIES
CHKMORE  TM    IOBSENS0,X01        IS TIME OUT SET
         BNO   SNO                 NO, GO TO SHOULD NOT OCCUR
         LA    NAMEREG,TIMEOUT     GET NAME OF TIME OUT ERP MOD
         B     ERRLINK1            XCTL FOR RETRIES
CK00C    B     RETRY               2702 ENTRY
         B     RETRY               2701 ENTRY
CK00A    B     SNO                 2702 ENTRY FROM CK00A BRANCH &
*                                  2701 ENTRY FROM CK00C BRANCH
         B     SNO                 2701 ENTRY FROM CK00A BRANCH &
*                                  2703 ENTRY FROM CK00C BRANCH
CK00B    B     RETRY               2703 ENTRY FROM CK00A BRANCH &
*                                  2702 ENTRY FROM CK00B BRANCH
         B     SNO                 2701 ENTRY
         B     RETRY               2703 ENTRY
SNO      L     ERRLNK,IOBECBPT     POINT TO DECB
         OI    L29(ERRLNK),CSWSNO   INDICATE SHOULD NOT OCCUR ERROR
CKDIAL   TM    L17(UCBREG),SWLINE   IS DISABLE REQUIRED
         BZ    POST                NO, GO TO ERROR POST
         MVI   IOBERRCT+L1,MASK3   INDICATE SPECIAL ENTRY
         LA    NAMEREG,ERLOD3      DISABLE CHECK ROUTINE ADDRESS
         B     ERRLINK1
POST     EQU   *
         OI    IOSFLB,IOSLOG       SET OBR RECORDING FLAG
         TM    L19(UCBREG),BISYNC   BSC DEVICE                  YA03263
         BNO   POSTA               NO, GO TO START STOP         YA03263
         MVI   IOBERRCT,X'07'      SET ERR RETRY COUNT TO 7     ZA20123
         MVC   IOBERCCW+L17(7),IOBCSW   SET ORIG CSW FOR 204C  @ZA32894
         LH    NAMEREG,ERLOD4      BSC ERROR POST               YA03263
         B     ERRLINK1            EXIT                         YA03263
POSTA    EQU   *                                                YA03263
         LH    NAMEREG,ERLOD5      ADDRESS OF ERROR POST ROUTINE
         B     ERRLINK1
RETRY    CLI   IOBERRCT,L2         HAVE WE TRIED THREE TIMES
         BE    CKDIAL
         SR    ERREG2,ERREG2
         IC    ERREG2,IOBERRCT     PICK UP RETRY COUNT
         LA    ERREG2,L1(ERREG2)   ADD ONE
         STC   ERREG2,IOBERRCT     STORE BACK                  @ZA20121
         CLI   ZERO(CCWREG),X27    IS FAILING CCW A DIAG, ENABLE,
*                                  OR DISABLE
         BL    EREXCP              NO, RESTART WITH FAILING CCW
FIRST    LA    CCWREG,IOBCPA       YES, RESTART WITH DISABLE
EREXCP   ST    CCWREG,IOBSTART     INITIALIZE START ADDRESS
ERRCHK05 EQU   *
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
         L     NAMEREG,TRANSRTN    LOAD NAME
         B     ERRLINK1            XCTL
**********
*        THE FOLLOWING SUBROUTINE CHECKS THE VALIDITY OF THE CCW
*        ADDRESS IN THE CSW. IF THE CCW ADDRESS IS VALID, THE ENDING
*        CCW ADDRESS+8 WILL BE PLACED IN CCWREG, RETURN IS TO NSI+4.
*        IF THE CCW ADDRESS IS INVALID, RETURN IS MADE TO NSI WITH
*        THE INVALID ADDRESS IN CCWREG.
**********
CCWADD   L     CCWREG,IOBSTART     LAST CCW
         TM    IOBRESTR,X80        DID A SIO STORE A CSW
         BO    L4(ERRLNK)          BRANCH YES TO VALID RETURN
         L     CCWREG,IOBFL3       CCW ADDRESS STORED IN CSW
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       BACK UP 8 BYTES
         TM    IOBRESTR+L2,L4      IS IT VALID
         BO    L4(ERRLNK)          BRANCH YES TO VALID RETURN  @ZA12164
         BR    ERRLNK              BRANCH NOT VALID
         EJECT
**********
*
*        CONSTANTS
*
**********
TRANSRTN DC    F'9042'             NAME OF BTAM TRANSLATE ROUTINE
ERLOD4   DC    H'2043'                                          YA03263
ERLOD5   DC    H'5041'
CEDEUX   DC    X'0D00'
ERRCN8   DC    H'8'
PACHAREA DC    16C'804B'           PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
RETRY1   EQU   1
CSWSTORD EQU   X'C0'
HALTIO   EQU   X'10'
CCWLAST  EQU   X'80'
RETRYCDE EQU   X'10'
UNITTYPE EQU   16
SWLINE   EQU   X'90'
MASK3    EQU   X'F0'
CSWSNO   EQU   X'40'
TIMEOUT  EQU   2041
DATACHK  EQU   1041
ERLOD3   EQU   3041
L255     EQU   255                 AND MASK
X06      EQU   X'06'               PREPARE MASK
X27      EQU   X'27'               ENABLE MASK
X0A      EQU   X'0A'               INHIBIT MASK
XC0      EQU   X'C0'               MASK
X40      EQU   X'40'               TERMINATION MASK
X29      EQU   X'29'               DIAL MASK
X10      EQU   X'10'               EQUIPMENT CHECK MASK
X08      EQU   X'08'               DATA CHECK MASK
X01      EQU   X'01'               TIMEOUT MASK
X80      EQU   X'80'               SIO STORE MASK
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L17      EQU   17                  LENGTH AND DISPLACEMENT
L19      EQU   19                  LENGTH AND DISPLACEMENT      YA03263
L29      EQU   29                  LENGTH AND DISPLACEMENT
ZERO     EQU   0                   LENGTH AND DISPLACEMENT
BISYNC   EQU   X'90'               UCB TYPE FOR BSC DEVICE      YA03263
**********
*
*        REGISTER DEFINITION
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
UCBREG   EQU   2                   UCB BASE
IOBRG    EQU   3                   IOB BASE
CCWREG   EQU   4                   CCW ADDRESS
ERREG1   EQU   5                   WORK REG
BASEREG  EQU   6                   PROGRAM BASE
EWAREG   EQU   7                   ERP WORKAREA BASE
ERREG2   EQU   8                   WORK REG
ERREG4   EQU   9                   WORK REG
ERREG6   EQU   10                  WORK REG
ERREG7   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  LINK REG
ERREG5   EQU   15                  LINK REG/WORK REG
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
