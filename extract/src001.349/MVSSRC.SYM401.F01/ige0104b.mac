         TITLE 'IGE0104B - BTAM ERP DIAGNOSTIC READ/WRITE MODULE'
IGE0104B CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0104B                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM DIAGNOSTIC READ/WRITE ERP                                   *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS MODULE GENERATES A DIAGNOSTIC WRITE/READ PROGRAM. IT SAVES  *
*    INFORMATION FROM THE CCW AND CSW. DISABLE IS EXECUTED            *
*    IF REQUIRED.                                                     *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (44 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        450 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE, NO LOCKS HELD   *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0104B                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        SETS INDICATOR IN DECERRST FOR DISABLE EXECUTED              *
*        CREATES NEW CHANNEL PROGRAM FOR RECOVERY                     *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        NONE                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    NONE                                                             *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YM05652K (1/24/74)                                               *
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         L     UCBREG,IOSUCB       BASE UCB
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         DROP  ERREG5
         L     CCWREG,IOBFL3       GET ADDR OF CSW
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       BACK UP ONE CCW
ERE5C1   EQU   *                   ISSUE DIAGNOSTIC WRITE/READ HERE
         MVC   IOBERINF(L1),IOBSENS0   SAVE SENSE BYTE
         MVC   IOBERINF+L1(L7),IOBCSW   SAVE 7 LOW ORDER BYTES OF
*                                  CHANNEL STATUS WORD
         MVC   IOBERINF+L8(L8),ZERO(CCWREG)   SAVE FAILING CCW
         XC    IOBSENS0(L2),IOBSENS0   CLEAR SENSE BYTES
         MVI   IOBERRCT+L1,RTRNCDE   INDICATE DIAG W/R
         LA    ERREG1,IOBCPA       START OF CHANNEL PROGRAM
         ST    ERREG1,IOBSTART     POINTER LOC FOR CCW
         MVC   IOBCPA(L32),ERRSKEL1   PUT CHAN PROG IN IOB
         TM    UCBTYP+L3(UCBREG),M2702   CHECK 2702/2703
         BO    T27023              BRANCH IF 2702 OR 2703
         MVC   IOBWORK(L1),UCBTYP+L3(UCBREG)   MOVE ADAPTER
         NI    IOBWORK,MASK3       TYPE TO IOBWORK CLEAR BITS
         CLI   IOBWORK,MOD2260     CHECK FOR TYPE III ADAPTER
         BNE   T27023              BRANCH IF NOT
         MVI   IOBCPA+L16,RDCMD2   DIAG READ CMND IN CCW
         B     T2701               MOVE TP OP CODE
T27023   MVI   IOBCPA+L16,RDCMD    DIAG READ COMMAND IN CCW
T2701    MVI   IOBCPA+L21,DIAGTPRD   MOVE IN TPOP CODE
         LA    ERRLNK,L8           SET UP TO
         LA    ERREG1,L4           GENERATE CORRECT
         LA    ERREG2,L24          DATA ADDRESS IN
         LA    ERREG4,IOBWORK      CCW'S FOR DIAG WR/RD
ERERPT   L     NAMEREG,IOBCPA(ERREG2)   FIRST WORD OF CCW
         AR    NAMEREG,ERREG4      ADD DATA ADDR
         ST    NAMEREG,IOBCPA(ERREG2)   STORE IN CCW
         SR    ERREG2,ERRLNK       DETERMINE NEXT CCW
         BCT   ERREG1,ERERPT       REPEAT IF MORE CCW'S
         MVC   IOBWORK(L1),UCBTYP+L3(UCBREG)   PICK UP DEV TYPE IND
         NI    IOBWORK,MASK3       CLEAR UNUSED BITS
         CLI   IOBWORK,MOD2260     IS THIS A 2260
         BNE   ERECK01             NO CHECK FOR SWITCHED LINE
         L     ERREG1,IOBSTART     PICK UP CPA POINTER
         LA    ERREG1,L8(ERREG1)   BUMP PAST DISABLE
         ST    ERREG1,IOBSTART     RESTORE POINTER
         B     ERECLNP             EXECUTE CHAN PROGRAM
ERECK01  EQU   *
         TM    UCBTYP+L1(UCBREG),SWLINE   THIS A SWITCHED LINE
         BNZ   ERECLNP             YES CHAN PROG IS COMPLETE
         MVI   IOBCPA+L20,CHAINCDE   CHAIN IN ENABLE COMMAND
ERECLNP  MVC   IOBWORK(L1),UCBTYP+L3(UCBREG)   DEVICE DATA TO IOBWORK
         NI    IOBWORK,MASK3       MASK OUT EXTRA BITS
         TM    IOBWORK,MASK4       CHECK FOR 2260,83B3 OR 115A
         BZ    EREMV01             YES SET UP FOR MOVE
         NI    IOBWORK,MASK5       CHECK FOR TWX ADAPTERS 1 OR 2
         BZ    EREMV02             ADAPTERS 1,2,3 IF BRANCH
         OI    IOBWORK,MASK6       TWX SPACIFIED
EREMV01  SR    ERREG1,ERREG1       CLEAR REGISTER
         IC    ERREG1,IOBWORK      PICK UP INDEX
         SRL   ERREG1,L3           ADJUST FOR CHAR COUNT
EREMV02  LA    ERREG1,ERRDATA1(ERREG1)   RESOLVE DATA ADDRESS
         MVC   IOBWORK(L7),ZERO(ERREG1)   MOVE DATA INTO BUFFER
         OI    IOBFLAG1,IOBCMDCH   INDICATE CHAINED CHAN PROG
         OI    IOSFLA,IOSCCHN      COMMAND CHAINED
         TM    UCBTYP+L1(UCBREG),SWLINE   THIS A SWITCHED LINE
         BZ    EXCP                NO, DON'T SET FLAG
         OI    DECERRST(DECBRG),DISABLOK   SET DISABLE EXECUTED
EXCP     EQU   *
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
         L     NAMEREG,TRANSRTN    LOAD ROUTINE NAME           YM02594K
         USING CVTMAP,ERRETR                                   YM02594K
         L     ERRETR,CVTPTR       POINT TO CVT                YM02594K
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE       YM02594K
         BR    ERRETR              GO TO IT                    YM02594K
         DROP  ERRETR                                          YM02594K
         EJECT
**********
*
*        CONSTANTS
*
**********
         DS    0D
ERRSKEL1 EQU   *
         DC    X'2F00000060300007'   DISABLE COMMAND
         DC    X'0500000060340007'   DIAG WRITE COMMAND
         DC    X'0000000020000007'   DIAG READ COMMAND
         DC    X'27000000203C0007'   ENABLE COMMAND
ERRDATA1 DC    X'16626467686B3D00'   MESSAGE FORMAT ONE 1050'> ETC
         DC    X'18130E121B051F00'   MESSAGE FORMAT TWO 115A, 83B3
         DC    X'02A1A2A3A3A40300'   MESSAGE FORMAT THREE 2260'S
         DC    X'8343C323A36321'     MESSAGE FORMAT FOUR TWX
TRANSRTN DC     F'9042'            NAME OF BTAM TRANSLATE RTN  YM02594K
ERRCN8   DC    H'8'                DECIMAL 8
PACHAREA DC    11C'104B'           PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
ZERO     EQU   0                   LENGTH AND DISPLACEMENT
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L9       EQU   9                   LENGTH AND DISPLACEMENT
L16      EQU   16                  LENGTH AND DISPLACEMENT
L20      EQU   20                  LENGTH AND DISPLACEMENT
L21      EQU   21                  LENGTH AND DISPLACEMENT
L24      EQU   24                  LENGTH AND DISPLACEMENT
L32      EQU   32                  LENGTH AND DISPLACEMENT
L255     EQU   255                 AND MASK
CHAINCDE EQU   X'60'
CHAININD EQU   X'40'
DIAGTPRD EQU   X'38'
DECERRST EQU   29
DISABLOK EQU   X'08'
UCBTYP   EQU   16                  DEVICE TYPE CODE
MASK3    EQU   X'F0'
MASK4    EQU   X'30'
MASK5    EQU   X'40'
MASK6    EQU   X'80'
MOD2260  EQU   X'80'
RDCMD    EQU   X'12'
RTRNCDE  EQU   X'C0'
SWLINE   EQU   X'90'
M2702    EQU   X'01'
RDCMD2   EQU   X'06'
**********
*
*        REGISTER DEFINITIONS
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE
ERREG2   EQU   2                   WORK REG
BASEREG  EQU   3                   PROGRAM BASE
CCWREG   EQU   4                   CCW ADDRESS
DECBRG   EQU   5                   DECB BASE
UCBREG   EQU   7                   UCB BASE
EWAREG   EQU   8                   ERP WORKAREA BASE
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG4   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG/WORK REG
ERRETR   EQU   14                  LINK REG
ERREG5   EQU   15                  WORK REG
         EJECT
         IECDRQE
         EJECT
         IEZIOB
IOBCMD3  EQU   IOBCPA+L16
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IECDIOSB
         END
