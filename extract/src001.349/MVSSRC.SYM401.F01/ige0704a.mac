         TITLE 'IGE0704A - READ/SKIP WRITE/BREAK CHECK MODULE'
IGE0704A CSECT
***********************************************************************
*                                                                     *
* MODULE NAME:                                                        *
*    IGE0704A                                                         *
*                                                                     *
* DESCRIPTIVE NAME:                                                   *
*    BTAM READ/SKIP WRITE/BREAK CHECK ERP                             *
*                                                                     *
* COPYRIGHT:                                                          *
*    NONE                                                             *
*                                                                     *
* STATUS:                                                             *
*    CHANGE LEVEL 000                                                 *
*                                                                     *
* FUNCTION:                                                           *
*    THIS MODULE DETERMINES IF A WRITE/BREAK WAS ISSUED. IF A LOST    *
*    DATA ERROR HAS OCCURRED EXIT IS MADE TO THE LOST DATA ROUTINE.   *
*    IF NECESSARY, THE CHANNEL PROGRAM IS RETRIED.                    *
*                                                                     *
* NOTES:                                                              *
*    DEPENDENCIES:                                                    *
*        NONE                                                         *
*    RESTRICTIONS:                                                    *
*        THIS ERP IS ONLY FOR DEVICES SUPPORTED BY BTAM.              *
*    REGISTER CONVENTIONS:                                            *
*        SEE 'REGISTER DEFINITION' BELOW                              *
*                                                                     *
* PATCHLABEL:                                                         *
*    PACHAREA (48 BYTES)                                              *
*                                                                     *
* MODULE TYPE:                                                        *
*    PROCEDURE                                                        *
*    PROCESSOR:                                                       *
*        BAL                                                          *
*    MODULE SIZE:                                                     *
*        474 BYTES                                                    *
*    ATTRIBUTES:                                                      *
*        REENTRANT, ENABLED, KEY 0, SUPERVISOR STATE,                 *
*        LOCAL LOCK HELD ACCROSS POST CALL                            *
*                                                                     *
* ENTRY POINT:                                                        *
*    IGE0704A                                                         *
*    PURPOSE:                                                         *
*        SEE 'FUNCTION'                                               *
*    LINKAGE:                                                         *
*        THIS MODULE IS SCHEDULED FOR EXECUTION BY THE INPUT/OUTPUT   *
*        SUPERVISOR. IT RUNS UNDER AN SIRB.                           *
*    INPUT:                                                           *
*        REG 1 = ADDRESS OF IOSB                                      *
*    OUTPUT:                                                          *
*        SETS INDICATOR FOR ERROR IN ERP EXCP.                        *
*        SETS INDICATOR IN DECERRST FOR NOT OPERATIONAL SIO           *
*        ALTERED CHANNEL PROGRAM                                      *
*                                                                     *
* EXITS-NORMAL:                                                       *
*    APPROPRIATE BTAM ERROR HANDLER VIA BRANCH ENTRY TO XCTL.         *
*        ALL LOADS START WITH IGE0. THE FIRST LOAD IS IGE0004A.       *
*        SUBSEQUENT LOADS ARE NUMBERED 1000 HIGHER THAN THE PREVIOUS  *
*        LOAD. THE ELEVENTH LOAD IS IGE0004B AND ALL SUBSEQUENT       *
*        LOADS ARE 1000 GREATER.                                      *
*        NO REGISTERS ARE SAVED BY THE ERROR ROUTINES AND ONLY        *
*        REGISTER 1 MAINTAINS TRANSPARENCY BETWEEN LOADS.             *
*                                                                     *
* EXITS-ERROR:                                                        *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES:                                                *
*    ROUTINES:                                                        *
*        POST                                                         *
*    DATA AREAS                                                       *
*        IOB (MAPPED BY IEZIOB)                                       *
*        VIRTUAL CHANNEL PROGRAM                                      *
*        ERP WORKAREA (MAPPED BY IECDERWA)                            *
*    CONTROL BLOCKS                                                   *
*        UCB                                                          *
*        DCB                                                          *
*        DECB                                                         *
*        IOSB         (MAPPED BY IECDIOSB)                            *
*        CVT          (MAPPED BY CVT)                                 *
*        IOB          (MAPPED BY IEZIOB)                              *
*        RQE          (MAPPED BY IECDRQE)                             *
*                                                                     *
* TABLES-WORKAREAS:                                                   *
*    ERP WORKAREA                                                     *
*    IOB CHANNEL PROGRAM AREA IN IOB BTAM EXTENSION                   *
*                                                                     *
* MACROS:                                                             *
*    SETLOCK                                                          *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*    YM02595K (9/20/73)                                               *
*    YM02594K (9/24/73)                                               *
*    YM05652K (1/24/74)                                               *
*                                                                     *
***********************************************************************
         EJECT
         USING *,BASEREG
         LR    BASEREG,ERREG5      SET BASE REG
         USING RQE,ERREG5
         USING IOSB,IOSBREG
         USING IOBSTDRD,IOBRG
         USING EWA,EWAREG
         L     ERREG5,IOSUSE       BASE RQE
         L     EWAREG,IOSERP       BASE ERP WORKAREA
         L     IOBRG,RQEIOB        BASE IOB
         L     DECBRG,IOBECBPT     BASE DECB
         L     DCBREG,IOBDCBPT     BASE DCB
         L     UCBREG,IOSUCB       BASE UCB
         DROP  ERREG5
         L     CCWREG,IOBFL3       POINT TO CCW
         LA    CCWREG,ZERO(CCWREG)   CLEAR OUT FLAG BYTE
         SH    CCWREG,ERRCN8       ADJUST TO FAILING CCW
         CLI   ZERO(CCWREG),CCWWRTBK   WAS A WRITE BREAK ISSUED
         BNE   ERRCHK03            NO DETERMINE ENTRY POINT
         TM    IOBCSW+L3,L255-CSWCDE   IS THIS NORMAL COMPLETION
         BNZ   ERRCHK01            NO DETERMINE FAILURE TYPE
         CLI   IOBCSW+L4,ZERO      EXAMINE SECOND STATUS BYTE
         BE    ERRCHK03            NO ERROR CONTINUE NORMAL OPER.
ERRCHK01 CLI   IOBCSW+L3,CSWCDEUC   ERROR UNIT CHECK WITH CHAN END
         BNE   ERRCHK19            NO INDICATE ERP AND SNO ERROR
         TM    IOBSENS0,IOBSNDC    DATA CHECK ERROR
         BO    ERRCHK02            YES CHECK XFER COUNT IN CCW
         TM    IOBSENS0,IOBSNBOC   BUSSOUT ERROR
         BO    ERRCHK03            NORMAL ERROR CONTINUE PROCESSNG
         B     ERRCHK19            INDICATE ERP AND SNO ERROR
ERRCHK02 CLI   L7(CCWREG),MASK15   FIRST WRITE BREAK
         BNE   ERRCHK19            NO INDICATE ERP AND SNO ERROR
         LH    ERREG1,L6(CCWREG)   PICK UP WRITE COUNT
         SLL   ERREG1,L1           DOUBLE WRITE COUNT
         STH   ERREG1,L6(CCWREG)   STORE NEW COUNT
         B     RETRY               GO TRANSLATE
ERRCHK03 CLI   IOBERRCT+L1,ERRLDIND   LOST DATA FAILURE
         BE    ERRCHK11            YES GO TO LOST DATA ERROR ENTRY
         L     ERREG2,IOBERINF     POINTER TO ORIGINAL FAILING CCW
         SH    ERREG2,ERRCN8       ADJUST TO REAL FAILING CCW
         MVC   IOBWORK(L1),TPCODE(ERREG2)   PICK UP TP OP CODE
         NI    IOBWORK,L255-HIBIT   CLEAR UNWANTED BITS
         TM    IOBWORK,CCWAFTXT    ERROR AFTER TEXT TRANSFER
         BZ    ERRCHK06            NO CHECK FOR DYNAMIC BUFFERING
ERRCHK04 ST    ERREG2,IOBSTART     SET CPA TO FAILING CCW
ERRCHK05 CLI   IOBERRCT,ERTRYCT    MORE RETRIES NECESSARY
         BNL   ERRCHK07            NO CHECK FOR DISABLE REQUIRED
         IC    ERREG1,IOBERRCT     PICK UP RETRY COUNT
         LA    ERREG1,L1(ERREG1)   BUMP COUNT BY ONE
         STC   ERREG1,IOBERRCT     STORE NEW COUNT
         MVI   IOBERRCT+L1,ZERO    INDICATE NORMAL RETURN
RETRY    EQU   *
         MVI   EWAIND1,EWAISVCF    SVC 15                      YM05652K
         L     NAMEREG,TRANSRTN    LOAD NAME
         B     ERRCHK08            XCTL
ERRCHK06 TM    DCBBFTEK(DCBREG),DCBDYNBF   DYNAMIC BUFFERING SPECIFIED
         BO    ERRCHK10            YES POST COMPLETE WITH ERROR
         LA    ERREG2,IOBCPA       SET CPA TO START OF CHAN PROG.
         B     ERRCHK04            RETRY CHAN PROGRAM
ERRCHK07 TM    UCBTYP+L1(UCBREG),SWLINE   DISABLE REQUIRED
         BZ    ERRCHK10            NO POST WITH ERROR
         LA    NAMEREG,ERLOD3      ADDRESS OF DISABLE ROUTINE
ERRCHK08 EQU   *
         L     ERRETR,CVTPTR       POINT TO CVT
         USING CVTMAP,ERRETR
         L     ERRETR,CVTXTLER     POINT TO XCTL ROUTINE
         DROP  ERRETR
         BR    ERRETR              XCTL
ERRCHK19 OI    DECERRST(DECBRG),DECERPER   INDICATE ERROR IN ERP EXCP
ERRCHK09 OI    DECERRST(DECBRG),CSWSNO   INDICATE SHOULD NOT OCCUR
ERRCHK10 MVC   IOBSENS0(L1),IOBERINF   RESTORE SENSE DATA
         MVC   IOBCSW(L7),IOBERINF+L1   RESTORE CSW DATA
         L     ERREG2,IOBFL3       PICK UP CCW POINTER
         LA    ERREG2,ZERO(ERREG2)   CLEAR OUT FLAG BYTE
         SH    ERREG2,ERRCN8       ADJUST TO FAILING CCW
         MVC   ZERO(L8,ERREG2),IOBERINF+L8   RESTORE FAILING CCW
         OI    IOSFLB,IOSLOG       INDICATE OBR REQUIRED
         LH    NAMEREG,ERLOD5      ADDRESS OF ERROR POST
         B     ERRCHK08            LINK TO POST ROUTINE
ERRCHK11 LA    NAMEREG,ERLOD14     ADDRESS OF LOST DATA CHECK RTNE
         B     ERRCHK08            XCTL
ERRCHK15 LA    CCWREG,IOBCPA+L24   POINT TO FOURTH CCW
         TM    L5(CCWREG),X11      READ OR WIRTE TEXT
         BNO   ERRCHK10            NO, DON'T POST BUFFER
         L     ERREG4,ZERO(CCWREG)   POINT TO BUFFER
         N     ERREG4,MASK         AND TO FULLWORD BOUNDARY
         SH    ERREG4,ERRCN4       POINT TO BUFFER LINK FIELD
         TM    ZERO(ERREG4),X80    IS WAIT BIT ON
         BNO   ERRCHK10            NO, DON'T POST BUFFER
         STM   ERREG3,ERREG5,EWAIAREA   SAVE REGS 0-15
GET      SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,RELATED=(LOCAL,IGE0704A(X
               REL))               GET LOCAL LOCK
         L     ERREG4,EWAIAREA+L44   RESTORE REG 11
         L     IOBRG,ZERO(ERREG4)   GET BUFFER CHAIN ADDRESS
         LA    IOBRG,ZERO(IOBRG)   CLEAR HIGH ORDER BYTE
         O     IOBRG,POSTCODE      OR IN COMPLETION CODE
         LA    ERRLNK,L1           GET A ONE
         SLL   ERRLNK,L31          MOVE TO SIGN POSITION
         OR    ERREG4,ERRLNK       POST WITH ECB
         SR    ERRLNK,ERRLNK       MEMORY RELAYED POST
         L     NAMEREG,IOSSRB      POINT TO SRB
         USING SRB,NAMEREG
         L     NAMEREG,SRBASCB     POINT TO ASCB
         DROP  NAMEREG
         L     ERREG5,CVTPTR       POINT TO CVT
         USING CVTMAP,ERREG5
         L     ERREG5,CVT0PT01     ADDRESS OF POST BARNCH ENTRY
         DROP  ERREG5
         BALR  ERRETR,ERREG5       LINK TO POST BUFFER
REL      SETLOCK RELEASE,TYPE=LOCAL,RELATED=(LOCAL,IGE0704A(GET))
         LM    ERREG3,ERREG5,EWAIAREA   RESTORE REGS 1-15
         B     ERRCHK10            CONTINUE PROCESSING
         EJECT
**********
*
*        CONSTANTS
*
**********
         DS    0F
POSTCODE DC    X'41000000'
MASK     DC    X'00FFFFFC'
TRANSRTN DC    F'9042'             NAME OF BTAM TRANSLATE ROUTINE
ERRCN4   DC    H'4'
ERRCN8   DC    H'8'
ERLOD5   DC    H'5041'
PACHAREA DC    12C'704A'           PATCH AREA FOR MAINTENANCE
**********
*
*        MISCELLANEOUS EQUATES
*
**********
CCWWRTBK EQU   X'0D'
L255     EQU   X'FF'               AND MASK
CSWCDE   EQU   X'0C'
ZERO     EQU   X'00'               LENGTH AND DISPLACEMENT
L1       EQU   1                   LENGTH AND DISPLACEMENT
L2       EQU   2                   LENGTH AND DISPLACEMENT
L3       EQU   3                   LENGTH AND DISPLACEMENT
L4       EQU   4                   LENGTH AND DISPLACEMENT
L5       EQU   5                   LENGTH AND DISPLACEMENT
L6       EQU   6                   LENGTH AND DISPLACEMENT
L7       EQU   7                   LENGTH AND DISPLACEMENT
L8       EQU   8                   LENGTH AND DISPLACEMENT
L9       EQU   9                   LENGTH AND DISPLACEMENT
L24      EQU   24                  LENGTH AND DISPLACEMENT
L31      EQU   31                  LENGTH AND DISPLACEMENT
L44      EQU   44                  LENGTH AND DISPLACEMENT
X11      EQU   X'11'
X80      EQU   X'80'
CSWCDEUC EQU   X'0E'
IOBSNDC  EQU   X'80'
IOBSNBOC EQU   X'20'
MASK15   EQU   X'0F'
ERRLDIND EQU   X'08'
DECERRST EQU   29
TPCODE   EQU   5
HIBIT    EQU   X'C0'
CCWAFTXT EQU   X'20'
ERTRYCT  EQU   X'02'
UCBTYP   EQU   16
DCBBFTEK EQU   32
DCBDYNBF EQU   X'08'
DECERPER EQU   X'20'
CSWSNO   EQU   X'40'
SWLINE   EQU   X'90'
ERLOD3   EQU   3041
ERLOD14  EQU   4042
**********
*
*        REGISTER DEFINITION
*
**********
ERREG3   EQU   0                   WORK REG
IOSBREG  EQU   1                   IOSB BASE/PARAMETER REG
ERREG2   EQU   2                   WORK REG
EWAREG   EQU   3                   ERP WORKAREA BASE
CCWREG   EQU   4                   CCW ADDRESS REG
DECBRG   EQU   5                   DECB BASE
DCBREG   EQU   6                   DCB BASE
UCBREG   EQU   7                   UCB BASE
BASEREG  EQU   8                   PROGRAM BASE
ERREG1   EQU   9                   WORK REG
IOBRG    EQU   10                  IOB BASE
ERREG4   EQU   11                  WORK REG
ERRLNK   EQU   12                  LINK REG
NAMEREG  EQU   13                  LINK REG
ERRETR   EQU   14                  LINK REG
ERREG5   EQU   15                  WORK REG/LINK REG
         EJECT
         IHAPSA
         EJECT
         IECDRQE
         EJECT
         IEZIOB
         EJECT
CVT      DSECT
         CVT
         EJECT
         IECDERWA
**********
*
*        THE FOLLOWING DEFINITIONS ARE LOCAL FOR BTAM ERPS ONLY.
*
**********
         ORG   EWAIERP
EWAITRAN DS    F                   ADDRESS OF IECVTCCW         YM02594K
EWAITCCW DS    F                   PARALLEL TCCW               YM02594K
EWAIOVST DS    F                   ORIGINAL IOSVST CONTENTS    YM02594K
EWAIND0  DS    B                   FLAG BYTE
EWAIVISR EQU   X'10'               V=R REQUEST
EWAIPMAP EQU   X'08'               PARALLEL MAPPING DONE       YM02594K
EWAIGPRO EQU   X'04'               GETMAIN IN PROCESS          YM02594K
EWAIFPRO EQU   X'02'               FREEMAIN IN PROCESS         YM02594K
EWAIND1  DS    B                   FLAG BYTE
EWAILD00 EQU   X'80'               EXIT TO 004C                YM02594K
EWAILD15 EQU   X'40'               EXIT TO LOAD 15             YM02594K
EWAIWTOL EQU   X'20'               EXIT TO IOS WTO             YM02594K
EWAISTAT EQU   X'10'               EXIT TO STAT UPDATE         YM05652K
EWAISVCF EQU   X'08'               SVC 15/SVC 3                YM05652K
EWAIND2  DS    B                   FLAG BYTE
EWAIND3  DS    B                   FLAG BYTE
EWAIAREA DS    20F                 SAVE AREA FOR MA OR AREA FOR WTO
EWAISAVE DS    2F                  TEMPORARY SAVE AREA
EWAIEXIT DS    4F                  ESATE PARAMETER LIST
*********  END OF BTAM LOCAL DEFINITIONS **********
         EJECT
         IHASRB
         EJECT
         IECDIOSB
         END
