         MACRO
         BOXRPF
         GBLB  &BOXSW
         AIF   (&BOXSW).LOWER
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
&BOXSW   SETB  1
         MEXIT
.LOWER   ANOP
*                                                                     *
*------------------------------------------ (C)-2003 SKYBIRD SYSTEMS -*
&BOXSW   SETB  0
         MEND
         MACRO
&NAME    RPFBEGIN
         GBLB  &REGSW
         AIF   (&REGSW).NOREG
         BOXRPF
*        SYMBOLIC REGISTER EQUATES.
         BOXRPF
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         BOXRPF
*        TERMINAL ATTENTION INTERUPT ELEMENT
         BOXRPF
         IKJTAIE
         BOXRPF
*        TSO ENVIRONMENT CONTROL TABLE
         BOXRPF
         IKJECT
         BOXRPF
*        COMMAND SCAN PARAMETER LIST.
         BOXRPF
         IKJCSPL
LCSPL    EQU   *-CSPL
         BOXRPF
*        COMMAND SCAN OUTPUT AREA.
         BOXRPF
         IKJCSOA
LCSOA    EQU   *-CSOA
         BOXRPF
*        CSPL-FLAGS INDICATING SYNTAX CHECK OR NOT.
         BOXRPF
CSPLFLGS DSECT
         DS    F
LCSPLFLG EQU   *-CSPLFLGS
         BOXRPF
*        RPF COMMON DATA AREA.
         BOXRPF
COMMAREA RPFCOMM
         BOXRPF
*        COMMAND PROCESSOR PARAMETER LIST.
         BOXRPF
         IKJCPPL
         BOXRPF
*        COMMUNICATIONS VECTOR TABLE.
         BOXRPF
         CVT DSECT=YES
         BOXRPF
*        SYSTEM DIAGNOSTIC WORK AREA
         BOXRPF
         IHASDWA
&REGSW   SETB  1
.NOREG   ANOP
         EJECT
&NAME    CSECT
         USING &NAME,R15
         B     *+100
         DC    CL8'&NAME'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    18F'-1'
         STM   R14,R12,12(R13)
         LA    R12,28(R15)
         ST    R13,4(R12)
         ST    R12,8(R13)
         LR    R13,R12
         DROP  R15
         USING &NAME+28,R13
         MEND
RPFTSO   TITLE 'RPF - TSO INTERFACE'
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*        MODULE - RPFTSO.                                             *
*                                                                     *
*        FUNCTION -                                                   *
*             THIS ROUTINE IS THE RPF-INTERFACE WITH TSO.             *
*             ROUTINE OPERATION:                                      *
*                                                                     *
*             ROUTINE INITIALIZATION -                                *
*                  1. PICK UP ADDRESS COMMON DATA AREA AND CPPL.      *
*                  2. ALLOCATE CSOA, CSPL AND CSOAFLGS IN SUB-        *
*                     POOL 1.                                         *
*                  3. INITIALIZE CSPL.                                *
*                  4. ESTABLISH ESTAE-ROUTINE.                        *
*                  5. ESTABLISH STAX-ROUTINE.                         *
*                                                                     *
*             COMMAND PROCESSING -                                    *
*                  1. If 'END' or '=n.n' entered, terminate        @rp2
*                  2. IF COMMAND IS NOT SUPPORTED, ISSUE ERROR-       *
*                     MESSAGE AND RETRY.                              *
*                  3. SET UP COMMAND-BUFFER AND COMPLETE CSPL.        *
*                  4. INVOKE IKJSCAN; IF NECESSARY ISSUE MESSAGE      *
*                     AND RETRY.                                      *
*                  5. COMPLETE CPPL AND ISSUE STLINENO                *
*                     TO ESTABLISH PROPER DISPLAY-AREA.               *
*                  6. CALL RPFTSOEX TO VALIDATE THE COMMAND.          *
*                  7. ATTACH APPROPRIATE COMMAND-PROCESSOR.           *
*                  8. DETACH COMMAND-PROCESSOR, RE-ESTABLISH FULL-    *
*                     SCREENMODE AND REPEAT PROCESSING.               *
*                                                                     *
*             ROUTINE TERMINATION -                                   *
*                  1. FREEMAIN WORKAREA.                              *
*                  2. CANCEL STAX-ROUTINE.                            *
*                  3. RE-ESTABLISH OLD LINELGTH AND SCREENLENGTH.     *
*                  4. CANCEL ESTAE-ROUTINE.                           *
*                  5. EXIT TO RPFMAIN.                                *
*                                                                     *
*        INPUT -                                                      *
*             - POINTER TO COMMON DATA-AREA.                          *
*             - POINTER TO CPPL.                                      *
*                                                                     *
*        OUTPUT -                                                     *
*             N.A.                                                    *
*                                                                     *
*        REGISTER USAGE -                                             *
*             R0 - R7   - WORK REGISTER.                              *
*             R8        - CSPL BASE REGISTER.                         *
*             R9        - CSOA BASE REGISTER.                         *
*             R10       - CPPL BASE REGISTER.                         *
*             R11       - COMMON DATA AREA BASE REGISTER.             *
*             R12       - NOT USED.                                   *
*             R13       - ROUTINE BASE REGISTER.                      *
*             R14       - LINKAGE REGISTER.                           *
*             R15       - ENTRY POINT/RETURN CODE REGISTER.           *
*                                                                     *
*        AUTHORS -                                                    *
*             A.W. VERMEER - SYSTEMS PROGRAMMING DEPT.                *
*             ROB PRINS    - SYSTEMS PROGRAMMING DEPT.                *
*                                                                     *
*        MODIFICATIONS:                                               *
*             06AUG02 RPR: BYPASS COMMAND CHECK, ALLOW ALL CMDS    @RP1
*             15feb03 RPr: Allow fast jump command with the '='    @rp2
*             04apr04 RPr: Leave fullscr on if 27x132 terminal     @rp3
*                                                                     *
*------------------------------------------ (C)-2003 SKYBIRD SYSTEMS -*
RPFTSO   RPFBEGIN
         BOXRPF
*        ROUTINE INITIALIZATION.
         BOXRPF
         BOXRPF
*        SET ADDRESSABILITY TO COMMON AND TSO-AREA'S.
         BOXRPF
         L     R11,0(R1)               ADDRESS OF COMMON DATA AREA.
         USING COMMAREA,R11
         XC    TSOCMD,TSOCMD           INIT CLEAR CMD IN PANEL
         MVC   SCR1TXT(2),=X'27F5'     DEFAULT WRITE ERASE
         CLC   COMMSIZE,=H'43'         3278/79 MODEL 4 SCREEN ?    @rp3
         BE    WRALT                   YES: GENERATE WRALT
         CLC   COMMSIZE,=H'32'         3278/79 MODEL 3 SCREEN ?    @rp3
         BNE   NOWRALT                 NO: USE WRITE ERASE
WRALT    MVC   SCR1TXT(2),=X'277E'     GENERATE WRITE ALTERNATE ERASE
NOWRALT  L     R10,COMMCPPL            ADDRESS OF CPPL.
         USING CPPL,R10                COMMAND PROCESSOR PARMLIST.
         MVC   ORGCBUF,CPPLCBUF        SAVE COMMAND BUFFER ADDRESS
         ST    R10,RPFCPPL             SAVE THIS ADDRESS.
         USING CSOA,R9                 COMMAND SCAN OUTPUT AREA.
         USING CSPL,R8                 COMMAND SCAN PARAMETER LIST.
         BOXRPF
*        ALLOCATE CSOA, CSPL AND CSPLFLGS IN SUBPOOL 1.
         BOXRPF
         LA    R0,LCSOA+LCSPL+LCSPLFLG
         GETMAIN RC,LV=(0),SP=1
         XC    0(LCSPL+LCSOA+LCSPLFLG,R1),0(R1)
         LR    R8,R1
         ST    R8,RPFCSPL              SAVE ADDRESS CSPL.
         LA    R9,LCSPL(R8)            GET ADDRESS OF CSOA.
         ST    R9,RPFCSOA              SAVE.
         LA    R1,LCSOA(R9)            GET ADDRESS OF CSPLFLGS
         ST    R1,RPFCSFLG             SAVE.
         BOXRPF
*        INITIALIZE COMMAND SCAN PARAMETER LIST.
         BOXRPF
         MVC   CSPLUPT,CPPLUPT         USER PROFILE TABLE.
         MVC   CSPLECT,CPPLECT         ENVIRONMENT CONTROL TABLE.
         MVC   CSPLFLG,RPFCSFLG        CSPL-FLAGS.
         MVC   CSPLOA,RPFCSOA          COMMAND SCAN OUTPUT AREA.
         MVC   CSPLCBUF,=A(CMMDBFFR)   COMMAND BUFFER.
         BOXRPF
*        ESTABLISH ESTAE-ROUTINE.
         BOXRPF
         LA    R2,RPFSTAE              GET ADDRESS OF ESTAE-ROUTINE.
         ESTAE (R2)
         BOXRPF
*        ESTABLISH STAX-ROUTINE.
         BOXRPF
         L     R2,=V(RPFSTAX)          GET ADDRESS OF STAX-ROUTINE.
         STAX  (R2),DEFER=NO
         BOXRPF
*        SAVE CURRENT LINELGTH AND SCREEN-LENGTH.
         BOXRPF
         BOXRPF
*        COMMAND PROCESSING.
         BOXRPF
         BOXRPF
*        SETUP TSO-SCREEN.
         BOXRPF
         STM   R0,R15,ESTAERUB+2       CURRENT REGISTER-CONTENTS.
         STM   8,13,SAVESTAE           SAVE THE REGISTERS FOR ABEND
TSO010   EQU   *
         ENTRY TSO010                  PROVIDE EXTERNAL ADDRESSABILITY.
         CALL  RPFTIME                 GET TIME OF DAY
         L     R2,=V(TIMEDISP)         LOAD TIME OF DAY
         MVC   SCR1LNE+71(8),2(R2)     MOVE TIME INTO SCREEN
         TPUT  SCR1TXT,SCR1LEN,FULLSCR
         BOXRPF
*        GET COMMAND INPUT FROM THE TERMINAL.
         BOXRPF
TSO020   EQU   *
         LA    R5,BUFFER               OUTPUT FORMATTED BUFFER
         L     R3,COMMSCR              LOAD SCREEN ADDRESS
         TGET  (R3),300,ASIS           READ THE SCREEN
         LR    R4,R1                   SAVE LENGTH
*        DO AN TGET ASIS TO OBTAIN THE AID BYTE
*        AFTER THE TGET ASIS REMOVE THE SBA'S FROM THE OUTPUT
         CLI   0(R3),X'F3'             PFK3 (END) PRESSED
         BE    TSO080                  YES: TERMINATE RPFTSO
         CLI   0(R3),X'C3'             PFK15 (END) PRESSED
         BE    TSO080                  YES: TERMINATE RPFTSO
         XR    R2,R2                    INIT OUTPUT LENGTH
         LR    R4,R1                   SAVE INPUT LENGTH
         SH    R4,=H'3'                SKIP AID AND CURSOR ADDRESS
         C     R4,=F'0'                REGISTER ZERO
         BNH   CONV099                 NOT HIGH: END OF ROUTINE
         LA    R3,3(R3)
         LA    R5,BUFFER               OUTPUT ADDRESS
         MVC   BUFFER(256),BLANKS      BLANK BUFFER FIRST          @rp1
*        INPUT SCREEN IS POINTED TO BY R3
*        OUTPUT SCREEN IS POINTED TO BY R5
*        THE NEXT SECTION WILL REMOVE ALL THE SBA'S
CONV001  CLI   0(R3),X'11'             SET BUFFER ADDRESS ?
         BE    CONV003                 YES: SKIP NEXT 3 BYTES INPUT
CONV002  MVC   0(1,R5),0(R3)           MOVE INPUT BYTE
         LA    R3,1(R3)                NEXT BYTE INPUT
         LA    R2,1(R2)                COUNT OUTPUT BYTES
         LA    R5,1(R5)                NEXT BYTE OUTPUT
         BCT   R4,CONV001              TEST NEXT BYTE
         B     CONV099                 END OF ROUTINE
CONV003  LA    R3,3(R3)                SKIP 3 BYTES INPUT
         BCTR  R4,0                    MINUS 1 BCT
         BCTR  R4,0                    MINUS 1 BCT
         C     R4,=F'0'                REGISTER ZERO ?
         BNH   CONV099                 YES: TERMINATE CONVERSION
         BCT   R4,CONV001              TEST NEXT BYTE
CONV099  LR    R1,R2                   OUTPUT LENGTH IN REGISTER 1
         OC    BUFFER(256),BLANKS      XLATE TO UPPERCASE
         LTR   R1,R1                   ANYTHING ENTERED?
         BZ    TSO010                  NO, GO BACK.
         CLI   BUFFER,X'6E'            RESHOW CODE?
         BE    TSO010                  YES, RESHOW SCREEN.
         BOXRPF
*        SET UP THE COMMAND BUFFER.
         BOXRPF
         LR    R3,R1                   SAVE LENGTH ENTERED.
         BCTR  R3,R0                   MINUS ONE FOR EXECUTE.
         XC    TSOCMD(255),TSOCMD      CLEAR SCREEN AREA
         EX    R3,TRTEX                EX TRT INSTRUCTION
         BZ    TSO010                  ZERO NO INPUT RECEIVED
         EX    R3,MVCEX                EX MOVE INSTRUCTION OF CMD
         BOXRPF
* AT THIS POINT REG1 CONTAINS THE ADDRESS OF THE FIRST NONBLANK
* CHARACTER IN THE FIELD 'BUFFER' (REG1 IS SET AT LABEL 'TRTEX')
         BOXRPF
         CLC   =C'END',0(R1)           WAS 'END' ENTERED?
         BE    TSO080                  YES, TERMINATE.
         CLI   0(R1),C'='              '=' command entered ?       @rp2
         BNE   TSO022                  No: continue                @rp2
         MVC   CMAINOPT(3),1(R1)       Move RPFMAIN option         @rp2
*                       The opt after the '=' is option from main  @rp2
         OI    INFCODE2,$RETURN        Return to RPFMAIN           @rp2
         B     TSO080                  Exit RPFTSO                 @rp2
TSO022   DS    0H                                                  @rp2
         CLC   =C'X ',0(R1)            'X' COMMAND (PCF) ENTERED ?
         BE    TSO055                  YES: COMMAND NOT SUPPORTED
         S     R1,=A(BUFFER)           COMPUTE OFFSET.
         STH   R1,BFFROFFS             STORE INTO COMMAND BUFFER.
         EX    R3,MVCINBUF             (MVC CMMDBFFR+4(0),BUFFER)
         LA    R3,5(R3)                LENGTH BUFFER + HEADER.
         STH   R3,BFFRLGTH             STORE INTO COMMAND BUFFER.
         BOXRPF
*        INVOKE IKJSCAN.
         BOXRPF
         CALLTSSR EP=IKJSCAN,MF=(E,(R8))
         BOXRPF
*        TEST THE RESULT OF COMMAND SCAN.
         BOXRPF
         L     R9,CSPLOA
         TM    CSOAFLG,VALDCMD1+VALDCMD2 VALID COMMAND?
         BNZ   TSO050                  VALID COMMAND-NAME SYNTAX.
         TM    CSOAFLG,CMDQMARK+EMPTSEPR
         BZ    TSO030
         CALL  MSGRTN,(1,0)            INVALID COMMAND INPUT.
         B     TSO020
         SPACE 2
TSO030   EQU   *
         TM    CSOAFLG,SYNTXERR        ANY SYNTAX ERRORS?
         BZ    TSO040                  NO.
         CALL  MSGRTN,(2,0)            COMMAND NAME SYNTAX ERROR.
         B     TSO020
         SPACE 2
TSO040   EQU   *
         CALL  MSGRTN,(3,0)            CMMND NOT SUPPORTED (IMPL.EXEC).
         BOXRPF
*        CHECK WHETHER COMMAND IS SUPPORTED.
         BOXRPF
TSO050   EQU   *
         L     R5,CSOACNM              Point to command name       @RP1
         LH    R6,CSOALNM              Point to command length     @RP1
         BCTR  R6,0                    Minus 1 for EX              @rp1
         MVC   INPCMD,BLANKS           Blank command name          @rp1
         EX    R6,CMDMVE               Move command name           @rp1
         CLC   =C'RPF',0(R5)           Recursive call to RPF?      @RP1
         BE    TSO055                  Yes: Error                  @RP1
         CLC   =C'LOGO',0(R5)          LOGON of LOGOFF?            @RP1
         BE    TSO055                  Yes: Error                  @RP1
         MVC   CMMDLOC,=A(INPCMD)      STORE NAME-POINTER          @RP1
         B     TSO060                  BYPASS CMDCHECK             @RP1
CMDMVE   MVC   INPCMD(0),0(5)          FOR EXECUTE ONLY.
TSO055   CALL  MSGRTN,(3,0)            COMMAND NOT SUPPORTED.
         B     TSO020
         BOXRPF
*        SET UP DISPLAY-AREA AND ATTACH PROCESSOR.
         BOXRPF
TSO060   EQU   *
         L     R2,CPPLECT              LOAD ENVIRONMENT CONTROL TABLE
         USING ECT,R2                  GET ADDRESSABILITY
         MVI   ECTSWS,X'00'            SET COMMAND SWITCH
         MVC   ECTPCMD,=CL8' '         BLANK COMMAND NAME INTO ECT
         L     R4,CSOACNM              LOAD SCANNED COMMAND NAME
         LH    R3,CSOALNM              LOAD COMMAND LENGTH
         BCTR  R3,0                    MINUS 1 FOR EX
         EX    R3,MVECECT              MOVE COMMAND INTO
         MVI   CMDAREA,C' '            BLANK CMDAREA IN COMMAREA
         MVC   CMDAREA+1(24),CMDAREA   BLANK CMDAREA COMPLETE
         EX    R3,MVECMDA              MOVE COMMAND NAME INTO CMDAREA
         XC    RTNCODE,RTNCODE         CLEAR RETURNCODE FIELD
         MVC   BLDLCMD,=CL8'RPFTSOEX'  Move name of exit
         BLDL  0,BLDLLIST              LOOK IF RPFTSOEX EXISTS
         LTR   15,15                   PRESENT ?
         BNZ   TSO061                  NO: BYPASS EXIT
         LINK  EP=RPFTSOEX,PARAM=((11)) TSO COMMAND VALIDATION EXIT
*
*        IF RTNCODE NE ZERO THEN REJECT THE COMMAND
*
         MVI   CMDAREA,C' '            BLANK CMDAREA IN COMMAREA
         MVC   CMDAREA+1(24),CMDAREA   BLANK CMDAREA COMPLETE
         CLC   RTNCODE(4),=F'0'        COMMAND ALLOWED ?
         BE    TSO061                  YES: CONTINUE
         CALL  MSGRTN,(3,0)            NO: ISSUE MESSAGE
         XC    RTNCODE,RTNCODE         CLEAR RTNCODE FIELD
         B     TSO020                  DISPLAY SCREEN
TSO061   DS    0H
         LH    R4,CMMDBFFR             LENGTH OF BUFFER
         SH    R4,CMMDBFFR+2           MINUS OFFSET FIRST OPERAND
         CH    R4,=H'4'                NO OPERANDS ?
         BNE   TSO065                  NO: DO NOT SET BIT IN ECT
         OI    ECTSWS,ECTNOPD          SET SWITCH: NO OPERANDS
         B     TSO065                  BRANCH OVER EXEC INSTRUCTION
MVECECT  MVC   ECTPCMD(0),0(R4)        MOVE COMMAND NAME INTO ECT
MVECMDA  MVC   CMDAREA(0),0(R4)        MOVE COMMAND NAME INTO CMDAREA
         DROP  R2
TSO065   MVC   CPPLCBUF,CSPLCBUF       MOVE IN ADDRESS COMMAND BUFFER.
         XC    PROCECB,PROCECB         CLEAR PROCESSOR'S ECB.
         CLC   COMMSIZE,=H'27'         27x132 screen               @rp3
         BNE   TSO065A                 No: branch                  @rp3
         STLINENO LINE=1,MODE=ON       Yes: leave fullscr on       @rp3
         B     TSO065B                 Skip                        @rp3
TSO065A  DS    0H                                                  @rp3
         STLINENO LINE=8,MODE=OFF      FIRST NFS-LINE = 08.
TSO065B  DS    0H                                                  @rp3
         MVC   BLDLCMD,INPCMD          Move command name           @rp1
         BLDL  0,BLDLLIST              Try to find command         @rp1
         LTR   R15,R15                 RC = 0000 ?                 @rp1
         BNZ   TSO066                  No: attach failed           @rp1
         L     R2,CMMDLOC              POINT TO PROCESSOR NAME.
         LR    R1,R10                  STORE CPPL-ADDRESS INTO REG. 1.
         ATTACH EPLOC=(2),ECB=PROCECB,ESTAI=(RPFSTAE)
         LTR   R15,R15                 WAS ATTACH SUCCESFULL?
         BZ    TSO070                  YES.
         CALL  MSGRTN,(4,1)            ATTACH FOR PROCESSOR FAILED.
         B     TSO068                  Issue fullscreen mode again @rp1
TSO066   DS    0H                                                  @rp1
         CALL  MSGRTN,(7,1)            Command not found           @rp1
TSO068   DS    0H                                                  @rp1
         STFSMODE ON,INITIAL=NO        SET FULLSCREEN MODE ON AGAIN.
         B     TSO010
         BOXRPF
*        SAVE TCB-ADDRESS AND WAIT FOR COMPLETION.
         BOXRPF
TSO070   EQU   *
         ST    R1,PROCTCB              SAVE TCB-ADDRESS.
         WAIT  ECB=PROCECB             WAIT FOR COMPLETION.
         BOXRPF
*        PROCESSOR COMPLETED.
         BOXRPF
         LA    R2,PROCTCB              POINT TO TCB-ADDRESS.
         DETACH (R2)                   DETACH SUB-TASK.
         XC    PROCTCB,PROCTCB         CLEAR TCB-ADDRESS.
         CALL  MSGRTN,(5,1)            RPF-TSO: READY.
         STFSMODE ON,INITIAL=NO        RE-ESTABLISH FULLSCREEN MODE.
         B     TSO010
         BOXRPF
*        ROUTINE TERMINATION.
         BOXRPF
         BOXRPF
*        FREE TSO-AREA'S.
         BOXRPF
TSO080   EQU   *
         L     R2,RPFCSPL              HAPPENS TO BE START OF WORKAREA.
         LA    R0,LCSPL+LCSOA+LCSPLFLG TOTAL LENGTH OF WORKAREA.
         FREEMAIN RC,LV=(0),A=(2),SP=1
         BOXRPF
*        CANCEL STAX-ROUTINE AND RESET SCREEN.
         BOXRPF
         STAX  ,                       CANCEL STAX-ROUTINE.
         TCLEARQ INPUT                 CLEAR TERMINAL INPUT QUEUES.
         TCLEARQ OUTPUT                CLEAR TERMINAL OUTPUT QUEUES.
         BOXRPF
*        CANCEL ESTAE-ROUTINE AND EXIT TO MAIN.
         BOXRPF
         ESTAE 0
         MVC   CPPLCBUF,ORGCBUF        RESTORE ORIGIN CMD BUF ADDRESS
         L     R13,4(R13)
         RETURN (14,12),RC=0
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*        ROUTINE NAME - RPFSTAE.                                      *
*                                                                     *
*        FUNCTION -                                                   *
*             THIS ROUTINE TRAPS ABENDS THAT EITHER OCCUR IN A        *
*             SUBTASK AND ARE NOT DEALT WITH BY THE SUBTASK'S         *
*             ESTAE-ROUTINE OR ABENDS OCCURRING IN RPFTSO ITSELF.     *
*             AS PART OF IT'S CLEAN-UP IT WILL ALSO CLEAR ALL THE     *
*             TERMINAL I/O-QUEUES.                                    *
*             IF A SUBTASK HAS BEEN ATTACHED (PROCTCB ›= ZERO),       *
*             THIS ROUTINE WILL DETACH IT AND BECAUSE THE SUBTASKS    *
*             OPERATE IN NON-FULLSCREEN MODE, THIS ROUTINE WILL SET   *
*             THE FULLSCREEN MODE ON.                                 *
*             A RETRY WILL BE ATTEMPTED IN THE RPFTSO-MAINLINE        *
*             AT THE BEGINNING OF COMMAND-PROCESSING IF A SDWA IS     *
*             AVAILABLE. IF NOT, THIS ROUTINE WILL PERCOLATE TO THE   *
*             NEXT HIGHER ESTAE.                                      *
*                                                                     *
*        INPUT -                                                      *
*             1. SDWA (IF AVAILABLE).                                 *
*             2. REGISTER UPDATE BLOCK (EXTERNAL).                    *
*             3. RETRY-ADDRESS (EXTERNAL).                            *
*             4. SUBTASK-ECB (EXTERNAL).                              *
*                                                                     *
*        OUTPUT -                                                     *
*             1. IF NO SDWA IS AVAILABLE, A RETURN CODE OF 0 (SETRP)  *
*                MEANING: PERCOLATE.                                  *
*             2. A RETURN CODE OF 4 (SETRP) MEANING RETRY.            *
*             3. A RETURN-ADDRESS IN THE RPFTSO-MAINLINE.             *
*             4. THE REGISTER-CONTENTS AT THE RETRY-ADDRESS.          *
*                                                                     *
*        REGISTER USAGE -                                             *
*             R0        - INDICATES PRESENCE OF SDWA (R0 ›= 12).      *
*             R1        - POINTER TO SDWA.                            *
*             R2        - PTR TO PROCTCB/RETRY-ADDRESS.               *
*             R3        - PTR TO REGISTER UPDATE BLOCK.               *
*             R4 - R5   - WORK REGISTERS.                             *
*             R6 - R12  - NOT USED.                                   *
*             R13       - SAVE AREA POINTER/ROUTINE BASE REGISTER.    *
*             R14       - LINKAGE REGISTER.                           *
*             R15       - ENTRY POINT REGISTER.                       *
*                                                                     *
*        AUTHOR -                                                     *
*             A.W. VERMEER - SYSTEMS PROGRAMMING DEPT. EXT. 3444      *
*                                                                     *
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
RPFSTAE  DS    0H
         DROP  ,
         USING *,15
         STM   14,12,12(13)            SAVE REGISTERS
         L     8,SAVESTAE+20           RESTORE OLD BASE REGISTER
         DROP  15
         USING RPFTSO+28,8             ESTAE ADDRESSABILITY
         BOXRPF
*        CLEAR I/O-QUEUES AND SAVE POINTERS.
         BOXRPF
         LR    R4,R0                   SAVE SDWA INDICATOR.
         LR    R5,R1                   SAVE SDWA-POINTER.
         TCLEARQ INPUT                 CLEAR TERMINAL INPUT QUEUES.
         TCLEARQ OUTPUT                CLEAR TERMINAL OUTPUT QUEUES.
         BOXRPF
*        IF A SDWA WAS PROVIDED, RETURN TO RTM WITH RETRY OPTION.
         BOXRPF
STAE010  EQU   *
         C     R4,=F'12'               IS THERE A SDWA?
         BE    STAE020                 NO, PERCOLATE.
         USING SDWA,R5
         XR    R2,R2               CLEAR REGISTER 2
         ICM   R2,7,SDWACMPC       INSERT COMPLETION CODE
         SRL   R2,12               SHIFT OUT USER COMPLETION CODE
         STH   R2,SYSTEMCC         SAVE SYSTEM COMPLETION CODE
         DROP  R5
         BOXRPF
*        DETERMINE IF THIS ROUTINE IS SCHEDULED FOR ESTAE OR ESTAI
         BOXRPF
         LA    R2,PROCTCB
         L     R3,0(R2)                GET ADDRESS OF SUBTASK'S TCB.
         LTR   R3,R3                   IS THERE A SUBTASK?
         BZ    STAE015                 NO.
         SYCONVHX IN=SYSTEMCC,OUT=SYSEBC,L=2 CONVERT TO ZONED
         MVC   MSG09+29(3),SYSEBC+1
         TPUT  MSG09,40                ISSUE MESSAGE
         STFSMODE ON,INITIAL=NO        SET FULLSCREEN MODE ON AGAIN.
         LR    R1,R5                   RESTORE SDWA-POINTER.
         SETRP RC=16,DUMP=NO,          INDICATE FLUSH SUBTASK          *
               REGS=(14,12),           RESTORE REGISTERS               *
               COMPCOD=(0,USER)        COMPLETION CODE.
STAE015  LR    R1,R5                   RESTORE SDWA-POINTER.
         L     R2,=V(ESTAERUB)         LOAD REGISTER UPDATE BLOCK
         SETRP RC=4,                   INDICATE RETRY.                 *
               RETADDR=STAERET,        RETRY-ADDRESS.                  *
               RETREGS=YES,RUB=(2),    RESTORE REGS FOR RETRY...       *
               REGS=(14,12),           RESTORE REGISTERS               *
               FRESDWA=YES,            AND FREE THE SDWA               *
               COMPCOD=(0,USER)        COMPLETION CODE.
         BOXRPF
*        THERE IS NO SDWA; PERCOLATE TO NEXT HIGHER ESTAE.
         BOXRPF
STAE020  EQU   *
         SETRP RC=0,                   INDICATE PERCOLATION            *
               COMPCOD=(998,USER)      WITH USER COMPLETION CODE.
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGISTERS.
         BR    R14                     RETURN TO RTM.
         DROP  8
         EJECT
         BOXRPF
*        ESTAE RETRY ROUTINE                                          *
         BOXRPF
STAERET  DS    0H
         USING RPFTSO+28,13            GET ADDRESSABILITY
         LM    8,13,SAVESTAE           PICKUP OLD ADDRESSES
         SYCONVHX IN=SYSTEMCC,OUT=SYSEBC,L=2 CONVERT TO ZONED
         MVC   MSG08+33(3),SYSEBC+1
         TPUT  MSG08,40                ISSUE MESSAGE
RET010   B     TSO010                  GO TO REAL RETRY ADDRES
MSG08    DC    CL40'RPF-TSO: abended completion code=XXX'
MSG09    DC    CL40'RPF-TSO: command abended CC= XXX'
*
         LTORG
         BOXRPF
*        DATA AREA'S.
         BOXRPF
         BOXRPF
*        SCREEN AND COMMAND INPUT-AREA.
         BOXRPF
SCR1TXT  DC    X'27F5C21140401DF8' WRITE ERASE / R01,C00
SCR1LNE  DC    CL79'RPF TSO Command Processor--------------------------C
               ---------------------------'
         DC    X'11C2601DF0',AL3(YELLOW)  R03,C01                  @rp2
         DC    C'Enter command or '
         DC    AL3(WHITE),C'END',AL3(YELLOW),C' below or hit '     @rp2
         DC    AL3(WHITE)                                          @rp2
         DC    C'PF03/15',AL3(YELLOW),C' to terminate TSO'         @rp2
         DC    X'11C3F01DC113',AL3(GREEN) R04,C01                  @rp2
TSOCMD   DC    255X'00',X'1DF0'        COMMAND AREA
SCR1LEN  EQU   *-SCR1TXT
*
BUFFER   DS    CL256                   COMMAND INPUT BUFFER.
SAVESTAE DS    6F
SYSEBC   DS    F                       COMPLETION CODE (EBCDIC)
SAVE14S  DS    F                       SAVE R14 IN ESTAE ROUTINE
SYSTEMCC DS    H                       COMPLETION CODE
TRTEX    TRT   BUFFER(0),SKIPSPCS      SEARCH FOR FIRST NON-BLANK.
MVCEX    MVC   TSOCMD(0),BUFFER        MOVE COMMAND INTO PANEL
MVCINBUF MVC   CMMDBFFR+4(0),BUFFER    FOR EXECUTE ONLY.
INPCMD   DC    CL8' '
CMMDBFFR DS    0CL260
BFFRLGTH DS    H                       LENGTH OF BUFFER + HEADER.
BFFROFFS DS    H                       OFFSET TO COMMANDNAME/OPERAND.
         DC    CL256' '                MAXIMUM LENGTH OF TSO-COMMAND.
*
BLDLLIST DS    0F                      ALIGN ON FULLWORD
         DC    AL2(1)                  ONE ENTRY
         DC    AL2(14)                 LENGTH OF DATA
BLDLCMD  DC    CL8'RPFTSOEX'           NAME OF EXIT or TSO cmd     @rp1
         DC    XL6'00'                 TTRKZC
*
RPFCPPL  DC    F'0'                    ADDRESS OF CPPL.
ORGCBUF  DC    F'0'                    ORIGIN COMMAND BUFFER
RPFCSPL  DC    F'0'                    ADDRESS OF CSPL.
RPFCSOA  DC    F'0'                    ADDRESS OF CSOA.
RPFCSFLG DC    F'0'                    ADDRESS OF CSPL-FLAGS.
CMMDLOC  DC    F'0'                    ADDRESS OF PROCESSORNAME.
         CNOP  2,4
ESTAERUB DS    0CL66                   REGISTER UPDATE BLOCK.
         DC    2X'FF'                  RESTORE REGS. 0 TM 15.
         DC    16F'0'                  REGS. 0 TM 15.
         ENTRY ESTAERUB                PROVIDE EXTERNAL ADDRESSABILITY.
         BOXRPF
*        EQUATES FOR TEST OF COMMAND SCAN.
         BOXRPF
VALDCMD1 EQU   B'10000000'             COMMAND VALID.
VALDCMD2 EQU   B'01000000'             COMMAND VALID.
CMDQMARK EQU   B'00100000'             COMMAND NAME IS QUESTION MARK.
EMPTSEPR EQU   B'00010000'             BFFR EMPTY/ONLY SEPARATORS.
SYNTXERR EQU   B'00001000'             SYNTAX ERROR IN COMMAND NAME.
IMPLEXEC EQU   B'00000100'             IMPLICIT EXECUTE.
         BOXRPF
*        FLAGS FOR ATTENTION EXIT ROUTINE.
         BOXRPF
         BOXRPF
*        SUBTASK RELATED VARIABLES.
         BOXRPF
PROCTCB  DC    A(0)                    TCB-ADDRESS OF SUBTASK.
         ENTRY PROCTCB                 PROVIDE EXTERNAL ADDRESSABILITY.
PROCECB  DC    F'0'                    SUBTASK ECB.
         BOXRPF
*        TRT-TABLE USED TO DETERMINE OFFSET OF COMMANDNAME.
         BOXRPF
SKIPSPCS DC    256X'FF'
         ORG   SKIPSPCS+X'40'
         DC    X'00'                   SKIP ALL SPACES.
         ORG   ,                       RESET LOCATION COUNTER.
         LTORG
         DROP
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*        ROUTINE NAME - MSGRTN.                                       *
*                                                                     *
*        FUNCTION -                                                   *
*             THIS ROUTINE TAKES CARE OF ALL THE (NON)-FULLSCREEN     *
*             OUTPUT. ALL THE MESSAGES AND SCREENS NEEDED ARE DEFI-   *
*             NED IN THIS ROUTINE AND ARE ACCESSED THRU AN ADDRESS-   *
*             VECTOR.                                                 *
*                                                                     *
*        INPUT -                                                      *
*             1. A MESSAGE CODE.                                      *
*             2. A SCREEN INDICATOR:                                  *
*                  - 0 = FULLSCREEN MESSAGE.                          *
*                  › 0 = NON FULLSCREEN MESSAGE.                      *
*                                                                     *
*        OUTPUT -                                                     *
*             1. SCREENS AND (NON-)FULLSCREEN MESSAGES.               *
*                                                                     *
*        REGISTER USAGE -                                             *
*             R0        - NOT USED.                                   *
*             R1        - PARAMETER LIST POINTER.                     *
*             R2        - CONTAINS MESSAGE CODE (PARAMETER 1).        *
*             R3        - CONTAINS SCREEN INDICATOR (PARAMETER 2).    *
*             R4 - R12  - NOT USED.                                   *
*             R13       - ROUTINE BASE REGISTER.                      *
*             R14       - LINKAGE REGISTER.                           *
*             R15       - ENTRY POINT REGISTER.                       *
*                                                                     *
*        AUTHOR -                                                     *
*             A.W. VERMEER - SYSTEMS PROGRAMMING DEPT.                *
*             Rob Prins    - SYSTEMS PROGRAMMING DEPT.                *
*                                                                     *
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
MSGRTN   RPFBEGIN
         BOXRPF
*        USE MESSAGE CODE TO ACCESSS MESSAGE.
         BOXRPF
         L     R2,0(R1)                GET MESSAGE CODE.
         L     R3,4(R1)                GET SCREEN INDICATOR.
         BCTR  R2,R0                   COMPUTE AN OFFSET.
         SLL   R2,2
         L     R2,MSGTABLE(R2)         PICK UP ADDRESS OF MESSAGE.
         LTR   R3,R3                   TEST SCREEN-INDICATOR.
         BZ    MSG010                  FULLSCREEN MESSAGE.
         B     MSG020                  NON FULLSCREEN MESSAGE.
         BOXRPF
*        A FULLSCREEN TPUT IS ISSUED.
         BOXRPF
MSG010   EQU   *
         MVC   MSGTXT,0(R2)            MOVE MESSAGETEXT INTO SCREEN.
         TPUT  SCR2TXT,SCR2LEN,FULLSCR WRITE MESSAGE TO SCREEN.
         B     MSG030
         BOXRPF
*        A NON FULLSCREEN TPUT IS ISSUED.
         BOXRPF
MSG020   EQU   *
         TPUT  (R2),40
         BOXRPF
*        EXIT TO CALLER.
         BOXRPF
MSG030   EQU   *
         L     R13,4(R13)              RESTORE OLD SAVE AREA POINTER.
         RETURN (14,12),RC=0           RETURN TO CALLER.
         EJECT
         BOXRPF
*        THIS VECTOR CONTAINS THE ADDRESSES OF ALL MESSAGES.
         BOXRPF
         CNOP  0,4
MSGTABLE DC    A(MSG01)
         DC    A(MSG02)
         DC    A(MSG03)
         DC    A(MSG04)
         DC    A(MSG05)
         DC    A(MSG06)
         DC    A(MSG07)                                            @rp1
         BOXRPF
*        MESSAGE SCREEN.
         BOXRPF
SCR2TXT  DC    X'27F1C21140401DF8'
         DC    X'11407A'       R01,C59
MSGTXT   DC    CL22' ',X'1DF0'
         DC    X'11C3F01DC113'         R04,C01
SCR2LEN  EQU   *-SCR2TXT
         BOXRPF
*        MESSAGE ENTRIES .
         BOXRPF
MSG01    DC    CL40'  Invalid command name'
MSG02    DC    CL40'Command name syntx err'
MSG03    DC    CL40' Command not supported'
MSG04    DC    CL40'RPF-TSO: ATTACH for processor failed'
MSG05    DC    CL40'RPF-TSO: ready'
MSG06    DC    CL40'RPF-TSO: interrupt received'
MSG07    DC    CL40'RPF-TSO: Command not found'                    @rp1
         LTORG
         DROP
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*        ROUTINE NAME - RPFSTAX.                                      *
*                                                                     *
*        FUNCTION -                                                   *
*             THIS ROUTINE TRAPS ATTENTION INTERRUPTS THAT ARE        *
*             GENERATED EITHER IN RPFTSO ITSELF OR IN A SUBTASK       *
*             WHOSE ATTENTION EXITS COULD NOT COPE WITH THE           *
*             INTERRUPT'S ATTENTION LEVEL.                            *
*                                                                     *
*        INPUT -                                                      *
*             1. A POINTER TO THE ATTENTION EXIT PARAMETER LIST.      *
*                                                                     *
*        OUTPUT -                                                     *
*             N.A.                                                    *
*                                                                     *
*        REGISTER USAGE -                                             *
*             R0 - R1   - NOT USED.                                   *
*             R2        - USED TO ACCESS EXTERNAL TCB-ADDRESS.        *
*             R3 - R12  - NOT USED.                                   *
*             R13       - ROUTINE BASE REGISTER.                      *
*             R14       - LINKAGE REGISTER.                           *
*             R15       - ENTRY POINT REGISTER.                       *
*                                                                     *
*        AUTHOR -                                                     *
*             A.W. VERMEER - SYSTEMS PROGRAMMING DEPT. EXT. 3444      *
*                                                                     *
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
RPFSTAX  RPFBEGIN
         STFSMODE ON,INITIAL=NO        REISSUE FULL SCREEN MODE
         BOXRPF
*        ISSUE NON-FULLSCREEN MESSAGE.
         BOXRPF
         CALL  MSGRTN,(6,1)            INTERRUPT RECEIVED.
         BOXRPF
         BOXRPF
*        CLEAR TERMINAL I/O-QUEUES.
         BOXRPF
         TCLEARQ INPUT                 CLEAR TERMINAL INPUT QUEUES.
         TCLEARQ OUTPUT                CLEAR TERMINAL OUTPUT QUEUES.
         BOXRPF
*        RETURN TO CALLER.
         BOXRPF
         L     R13,4(R13)
         RETURN (14,12),RC=0
         LTORG
         DROP
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        TIME - GET TIME OF DAY, CONVERT TO DISPLAY FORMAT            *
*                                                                     *
*               INPUT:  NONE                                          *
*               OUTPUT: TIMEDISP - TIME OF DAY, HH:MM:SS              *
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
RPFTIME  RPFBEGIN                  NEW CSECT
         TIME  DEC                 GET TIME OF DAY
         SRL   0,4                 SHIFT OUT THOUSANDS OF SECONDS
         ST    0,TIMEWORK          SAVE TIME
         OI    TIMEWORK+3,X'0F'    INSERT SIGN
         MVC   TIMEDISP,PATT1      PATTERN INTO OUTPUT FILED
         ED    TIMEDISP(10),TIMEWORK EDIT TIME
TIME99   L     13,4(13)            LOAD CALLERS SAVEAREA
         RETURN (14,12),RC=0       RETURN
TIMEWORK DC    F'0'                WORK AREA TIME
TIMEDISP DC    CL10' '             OUTPUT AREA TIME
         ENTRY TIMEDISP
PATT1    DC    X'402021207A20207A2020' EDIT PATTERN
         END
