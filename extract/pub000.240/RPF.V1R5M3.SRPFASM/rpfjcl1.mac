         TITLE '                   J C L   S Y N T A X - C H E C K E R'
         SPACE 5
*///////////////////////////////////////////////////////VERSIE 1.0////
*/                                                                   /
*/       FUNCTIE : DIT PROGRAMMA CONTROLEERT EEN JCL-STREAM OP       /
*/                 SYNTAX-FOUTEN. EVENTUELE FOUTEN WORDEN OP         /
*/                 SCHERM VAN DE USER ZICHTBAAR GEMAAKT.             /
*/                                                                   /
*/       INPUT   : POINTER NAAR DE COMMON-AREA.                      /
*/                                                                   /
*/       OUTPUT  : ERROR MESSAGES ON THE SCREEN.                     /
*/                                                                   /
*/                                                                   /
*/       DOOR    : A.J.A.LEEMREIZE                                   /
*/       DATUM   : 18-05-1983                                        /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       OS-LINKAGE                                                  /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
RPFJCL   CSECT
         PRINT NOGEN
         USING RPFJCL,R15              SET BASE ADRESSING
         STM   R14,R12,12(R13)         SAVE REGISTERS
         B     SAVE
         SPACE 2
SAVEREG  DC    18F'0'                  AREA OM REGISTERS TE SAVEN
BASE2    DC    A(RPFJCL+4096)          BASE-ADRES 2
BASE3    DC    A(RPFJCL+8192)          BASE-ADRES 3
BASE4    DC    A(RPFJCL+12288)         BASE-ADRES 4
         SPACE 2
SAVE     EQU   *
         LA    R14,SAVEREG             POINTER NAAR NIEUWE SAVE AREA
         ST    R13,4(R14)              SAVE POINTER NAAR OUDE SAVE AREA
         ST    R14,8(R13)              SAVE POINTER NAAR NIEUWE AREA
         LR    R13,R14                 POINTER NAAR HUIDIGE SAVE AREA
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       VERZORG DE BASE-ADRESSING                                   /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 2
         LR    R2,R15                  VUL BASE REGISTER 1
         L     R3,BASE2                VUL BASE REGISTER 2
         L     R4,BASE3                VUL BASE REGISTER 3
         L     R5,BASE4                VUL BASE REGISTER 4
         DROP  R15
         USING RPFJCL,R2,R3,R4,R5
         L     R9,VCOMMON
         USING COMMON,R9
         XC    0(COMLEN,R9),0(R9)      CLEAR COMMON AREA
         ZAP   NUL,=P'0'               INITIALISEER AREA
         ZAP   EEN,=P'1'
         ZAP   BRFLAG,=P'0'
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       REGISTER GEBRUIK                                            /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 3
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       REGISTER  0    OS                                           /
*/       REGISTER  1    OS                                           /
*/       REGISTER  2    BASE-REGISTER 1                              /
*/       REGISTER  3    BASE-REGISTER 2                              /
*/       REGISTER  4    BASE-REGISTER 3                              /
*/       REGISTER  5    BASE-REGISTER 4                              /
*/       REGISTER  6    WERKREGISTER                                 /
*/       REGISTER  7    WERKREGISTER                                 /
*/       REGISTER  8    WERKREGISTER                                 /
*/       REGISTER  9    BASE-REGISTER COMMON AREA                    /
*/       REGISTER 10    POINTER NAAR CURRENT LINE                    /
*/       REGISTER 11    POINTER NAAR CURRENT CHARACTER               /
*/       REGISTER 12    POINTER NAAR RPF DATA                        /
*/       REGISTER 13    OS                                           /
*/       REGISTER 14    OS                                           /
*/       REGISTER 15    OS                                           /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       VERWERK PARAMETER                                           /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
         L     R12,0(R1)
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       RESERVEER GEHEUGEN VOOR MESSAGE AREA                        /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
         GETMAIN R,LV=AREALEN          RESERVEER GEHEUGEN
         ST    R1,STARTERR             POINTER NAAR BUFFER-AREA
         ST    R1,CURRERR              CURRENT POINTER
         LTR   R15,15
         BZ    STCHECK                 START CHECK
         LA    R6,ERR999               ERROR TIJDENS RPFJCL EXEC
         SUBR  ERRMESS                 GENEREER MESSAGE
         B     CHECKEND                EINDE EXECUTIE
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       START JCL-CHECK                                             /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
STCHECK  EQU   *
         USING RPFDATA,R12             BASE ADRESSING
         L     R10,FIRSTREC            POINTER NAAR EERSTE REGEL
NEXTLINE EQU   *
         C     R10,LASTREC             ALLE RECORDS GEDAAN ?
         DROP  R12
         BH    CHECKEND                JA.
         MVC   LINENBR,0(R10)          SAVE LINENUMBER
         LR    R11,R10                 INCR. POINTER
         LA    R11,8(R10)              POINT TO DATA PORTION
         ST    R11,STARTLNE            BEWAAR BEGIN REGEL
         LA    R6,71                   MAXIMALE REGELLENGTE
         AR    R6,R11                  POINTER EINDE REGEL
         ST    R6,ENDLINE              BEWAAR IN COMMON AREA
         SPACE 2
         CLC   0(3,R11),=C'//*'        COMMENTAAR ?
         BE    INCRLINE                JA.
         CLI   DATAFLAG,X'FF'          DD DATA ?
         BE    INSTREAM                JA.
         CLC   0(2,R11),=C'//'         RECORD MOET MET // BEGINNEN
         BE    SLASHES                 OK.
         CLI   FIRSTFLG,X'00'          EERSTE RECORD ?
         BE    NOJOBC                  GEEN JOBKAART
         CLI   DATAFLAG,X'F0'          IN-STREAM DATA ?
         BE    INSTREAM                JA.
         CLC   0(2,R11),=C'/*'         SKIP /* RECORDS
         BE    INCRLINE                OK.
         LA    R6,ERRSLASH             GEEN SLASHES.
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         B     INCRLIN1                VOLGENDE REGEL
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       CHECK COMMENT,    NAAMVELD EN SCHEIDINGS SPATIE             /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
SLASHES  EQU   *
         XC    DATAFLAG,DATAFLAG       END OF INSTREAM DATA
         LA    R11,2(R11)              INCR.   POINTER
         CLI   0(R11),C' '             NAAMVELD GEVULD ?
         BNE   SLASH00A                NEE.
         CLI   CONTFLAG,X'FF'          CONTINUE COMMENT ?
         BNE   SLASH010                NEE.
         L     R11,ENDLINE             POINTER TO COLUMN 72
         CLI   0(R11),C' '             CONTINUATION ?
         BNE   INCRLIN1                YES.
         XC    CONTFLAG,CONTFLAG       NO. CLEAR FLAG
         B     INCRLIN1                VOLGENDE REGEL
SLASH00A EQU   *
         SUBR  NAMES                   CHECK OP EEN NAAM
         B     *+4(R15)                CHECK RETURNCODES
         B     SLASH000                OK
         B     NAMEERR                 GEEN SPACE, ERROR IN NAAM
SLASH000 EQU   *
         CLI   CONTFLAG,X'FF'          CONTINUE COMMENT ?
         BNE   SLASH005                NEE.
         LA    R6,ERRNOCON             CONTINUATION NOT RECEIVED
         SUBR  ERRMESS                 MESSAGE
         XC    CONTFLAG,CONTFLAG       CLEAR FLAG
SLASH005 EQU   *
         CLI   0(R11),C'.'             OVERSCHRIJVING ?
         BNE   SLASH010                NEE.
         CLI   EPROCFLG,X'00'          EXEC PROC ?
         BE    NAMEERR                 NEE. ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         SUBR  NAMES                   NAAM ?
         B     *+4(R15)                RETURNCODES
         B     SLASH010                OK.
         B     NAMEERR                 ERROR
SLASH010 EQU   *
         SUBR  SKIPSPC                 SKIP SPACES
         B     *+4(R15)                CHECK RETURNCODES
         B     TESTPROC                OK
         B     NAMEERR                 GEEN SPACE, ERROR IN NAAM
         B     INCRLINE                EINDE RECORD, NULL RECORD
         SPACE 2
NAMEERR  EQU   *
         LA    R6,ERRNAME              ERROR IN NAME
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         B     INCRLINE                VOLGENDE REGEL
OPERERR  EQU   *
         LA    R6,ERROPER              FOUT IN OPERATIO
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         B     INCRLINE                VOLGENDE REGEL
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       CHECK OP PROC EN PEND                                       /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
TESTPROC EQU   *
         CLI   PROCFLAG,X'FF'          IN PROCEDURE JCL ?
         BNE   TESTJOB                 NEE.
         CLC   0(4,R11),=C'PEND'       EINDE PROCEDURE ?
         BNE   INCRLIN1                NEE.
         XC    PROCFLAG,PROCFLAG       RESET FLAG
         B     INCRLIN1                VOLGENDE REGEL
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       CHECK OP JOBKAART, EXECKAART OF DDKAART                     /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
TESTJOB  EQU   *
         CLC   0(3,R11),=C'JOB'        JOB-KAART ?
         BE    JOBKAART                JA.
TESTEXEC EQU   *
         CLI   FIRSTFLG,X'00'          FIRST RECORD ?
         BE    NOJOBC                  NO JOBCARD
         CLC   0(4,R11),=C'EXEC'       EXEC-KAART ?
         BE    EXEKAART                JA.
TESTDD   EQU   *
         CLC   0(2,R11),=C'DD'         DD-KAART ?
         BE    DDKAART                 JA.
         CLC   0(4,R11),=C'PROC'       START VAN PROCEDURE ?
         BNE   TESTDATA                NEE.
         MVI   PROCFLAG,X'FF'          INSTREAM PROCEDURE
         B     INCRLIN1                VOLGENDE REGEL
TESTDATA EQU   *
         CLI   DATAFLAG,X'FF'          'DD DATA'
         BE    INSTREAM                INSTREAM DATA
         B     OPERERR                 FOUT IN OPERATIE
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       VOLGENDE REGEL                                              /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
INCRLINE EQU   *
         L     R6,STATNBR              AANTAL STATEMENTS + 1
         LA    R6,1(R6)
         ST    R6,STATNBR
INCRLIN1 EQU   *
         LA    R10,NEXTENT(R10)        VERHOOG LINE POINTER
         B     NEXTLINE
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       EINDE JCL-CHECK; DISPLAY OP SCHERM                          /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
         ENTRY CHECKEND
CHECKEND EQU   *
         GTSIZE ,                      HAAL DE SCHERMGROOTTE
         C     R0,=F'24'               24 REGELS ?
         BNE   DISPL010                NEE.
         MVC   SCRWCC,=X'27F5'         WRITE STANDARD
         LA    R0,SCR24LEN             LENGTE MESSAGE NAAR 3270
         ST    R0,SCREENL              BEWAAR LENGTE
         B     DISPL040                VERWERK
DISPL010 EQU   *
         C     R0,=F'32'               32 REGELS ?
         BNE   DISPL020                NEE.
         LA    R0,SCR32LEN             LENGTE MESSAGE NAAR 3270
         MVC   SCRWCC,=X'277E'         WRITE ALTERNATE
         ST    R0,SCREENL              BEWAAR LENGTE
         B     DISPL040                VERWERK
DISPL020 EQU   *
         C     R0,=F'43'               43 REGELS ?
         BNE   DISPL030                NEE.
         MVC   SCRWCC,=X'277E'         WRITE ALTERNATE
         LA    R0,SCR43LEN             LENGTE MESSAGE NAAR 3270
         ST    R0,SCREENL              BEWAAR LENGTE
         B     DISPL040                VERWERK
DISPL030 EQU   *
         LA    R0,SCRMIN               MINIMUM LENGTE
         ST    R0,SCREENL              BEWAAR LENGTE
DISPL040 EQU   *
         LA    R15,SCRBEGIN            ADRES REGEL 1
         L     R6,STATNBR              AANTAL STATEMENTS
         CVD   R6,WORKDOUB             CONVERTEER
         UNPK  HEADING(6),WORKDOUB     UNPACK AANTAL RECORDS
         OI    HEADING+5,X'F0'         TEKENBIT
         L     R6,ERRNBR               AANTAL FOUTEN
         CVD   R6,WORKDOUB             CONVERTEER
         UNPK  HEADING+23(6),WORKDOUB  UNPACK AANTAL FOUTEN
         OI    HEADING+28,X'F0'        TEKENBIT
         MVC   SMESSAGE(MESLEN,R15),HEADING
         LA    R15,SLINELEN(R15)
DISPL050 EQU   *
         L     R8,STARTERR             BEGIN AREA
         C     R8,CURRERR              GEEN ERRORS ?
         BE    DISPLEND                JA. EINDE
         B     DISPL070
DISPL060 EQU   *
         LA    R15,SCRBEGIN            REGEL 1
DISPL070 EQU   *
         MVC   SNBR(6,R15),0(R8)       REGEL NUMMER NAAR SCHERM
         XR    R6,R6                   CLEAR R6
         LA    R7,MESLEN               LENGTE MESSAGE
         LA    R14,ERR999              MAXIMAAL FOUT NUMMER
         CH    R14,6(R8)               GROTER ?
         BL    DISPL075                JA. FOUT IN PROGRAMMA
         LH    R14,6(R8)               NEE
DISPL075 EQU   *
         MR    R6,R14                  BEREKEN OFFSET
         LA    R6,MESSTXT              BEGIN ADRES TEXT
         AR    R6,R7                   ADRES MESSAGE
         MVC   SMESSAGE(MESLEN,R15),0(R6)   MOVE TEXT
         LA    R8,8(R8)                VOLGENDE ERROR MESSAGE
         C     R8,CURRERR              ALLEMAAL GEDAAN ?
         BE    DISPLEND                JA. EINDE
         LA    R15,SLINELEN(R15)       POINTER NAAR VOLGENDE REGEL
         LA    R14,SCREEN              BEREKEN EINDE
         A     R14,SCREENL             + LENGTE
         CR    R14,R15                 EINDE SCHERM ?
         BNE   DISPL070                VOLGENDE REGEL
         L     R6,SCREENL              LENGTE SCHERM
         TPUT  SCREEN,(R6),FULLSCR     ZEND SCHERM
         XC    BUFFER,BUFFER           CLEAR INPUT BUFFER
         TGET  BUFFER,L'BUFFER         HAAL INPUT
         CLC   BUFFER(3),=X'859584'    EINDE ?
         BE    DISPLEN1                JA
         CLC   BUFFER(3),=C'END'       EINDE ?
         BE    DISPLEN1                JA
         LA    R15,SCRBEGIN            BEGIN SCHERM
         LA    R14,SCREEN              BEREKEN EINDE
         A     R14,SCREENL             + LENGTE
DISPL080 EQU   *
         MVC   SNBR(6,R15),=C'      '  CLEAR LINENUMBER
         MVI   SMESSAGE(R15),C' '      CLEAR TEXT
         MVC   SMESSAGE+1(MESLEN-1,R15),SMESSAGE(R15)
         LA    R15,SLINELEN(R15)       VOLGENDE REGEL
         CR    R14,R15                 EINDE SCHERM ?
         BE    DISPL060                JA.
         B     DISPL080
DISPLEND EQU   *
         L     R6,SCREENL              LENGTE SCHERM
         TPUT  SCREEN,(R6),FULLSCR     ZEND SCHERM
         TGET  BUFFER,L'BUFFER         HAAL INPUT
DISPLEN1 EQU   *
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       KEER TERUG NAAR OS                                          /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
         L     R13,4(R13)              BACK TO CALLER
         LM    R14,R12,12(R13)
         LA    R15,0
         BR    R14
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       INSTREAM DATA                                               /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
INSTREAM EQU   *
         L     R11,STARTLNE            BEGIN VAN DE REGEL
         CLC   DLM,0(R11)              DELIMITER ?
         BNE   INCRLIN1                NEE.
         MVI   DATAFLAG,X'00'          CLEAR DATA-FLAG
         CLC   DLM,=C'/*'              JCL STATEMENT ?
         BE    INCRLINE                JA.
         B     INCRLIN1                VOLGENDE REGEL
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       GEEN JOBKAART                                               /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
NOJOBC   EQU   *
         LA    R6,ERRNOJOB             GEEN JOBKAART
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         B     CHECKEND                EINDE CHECK
         TITLE '                   J C L   S Y N T A X - C H E C K E R C
                   J O B K A A R T'
         SPACE 15
**********************************************************************
**********************************************************************
**                                                                  **
**                                                                  **
**              JJJJJJJJJJ   OOOOOOOOOO   BBBBBBBBBBB               **
**              JJJJJJJJJJ  OOOOOOOOOOOO  BBBBBBBBBBBB              **
**                  JJ      OO        OO  BB        BB              **
**                  JJ      OO        OO  BB        BB              **
**                  JJ      OO        OO  BB       BB               **
**                  JJ      OO        OO  BBBBBBBBBB                **
**                  JJ      OO        OO  BBBBBBBBBB                **
**                  JJ      OO        OO  BB       BB               **
**            JJ    JJ      OO        OO  BB        BB              **
**            JJ    JJ      OO        OO  BB        BB              **
**            JJJJJJJJ      OOOOOOOOOOOO  BBBBBBBBBBBB              **
**             JJJJJJ        OOOOOOOOOO   BBBBBBBBBBB               **
**                                                                  **
**                                                                  **
**********************************************************************
**********************************************************************
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       CONTROLEER SYNTAX JOBKAART                                  /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JOBKAART EQU   *
         MVI   FIRSTFLG,X'FF'          GEEN EERSTE REGEL MEER
         MVI   CARDFLAG,X'01'          DIT IS EEN JOBKAART
         XC    JFLAGS(FJLEN),JFLAGS    CLEAR PARAMETER FLAGS
         L     R11,STARTLNE            BEGIN VAN DE REGEL
         LA    R11,2(R11)              SKIP //
         SUBR  NAMES                   CONTROLEER OP NAAM
         B     *+4(R15)                RETURNCODES
         B     JJOBNAME                JOBNAME AANWEZIG
         LA    R6,ERRNJOBN             GEEN JOBNAAM
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SPACE 2
JJOBNAME EQU   *
         SUBR  SKIPSPC                 SKIP SPATIE(S)
         LA    R11,3(R11)              SKIP 'JOB'
         SUBR  SKIPSPC                 SKIP SPATIE(S)
         B     *+4(R15)                RETURNCODES
         B     JCHKACC                 CHECK ACCOUNTING
         B     JACCTERR                GEEN SPATIE, ERROR IN ACCOUNTING
         B     JACCTERR                EINDE REC, ERROR IN ACCOUNTING
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       CONTROLEER ACCOUNTING IN JOBKAART                           /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JCHKACC  EQU   *                       CHECK POSITIONAL PARM 1
         CALL  BALANCE                 () EN '' IN BALANS ?
         B     *+4(R15)                RETURNCODES
         B     JACCT00A                JA.
         B     INCRLINE                NEE.
JACCT00A EQU   *
         LA    R6,ERRJACC              ERROR IN ACCOUNTING
         CLI   0(R11),C'('             BEGINT MET (
         BNE   JACCT000                NEE.
         LA    R11,1(R11)              VERHOOG POINTER
         AP    BRFLAG,EEN              SET FLAG
JACCT000 EQU   *
         LA    R7,=C'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789@#$- '
         CALL  PARMTEST                TEST PARAMETER
         B     *+4(R15)                RETURNCODES
         B     JACCT010                OK
         B     JACCTERR                ERROR.
JACCT010 EQU   *
         CP    BRFLAG,NUL              ( AANWEZIG ?
         BE    JACCTOK                 NEE.
         CLI   0(R11),C')'             EINDE ACCOUNTING ?
         BE    JACCT020
         CALL  DELIMIT                 DELIMITER ?
         B     *+4(R15)                RETURNCODES
         B     JACCT000                JA.
         B     JACCTERR                ERROR.
JACCT020 EQU   *
         LA    R11,1(R11)              VERHOOG POINTER
         SP    BRFLAG,EEN              RESET FLAG
JACCTOK  EQU   *
         MVI   FJPOS1,X'FF'            SET FLAG
         CLI   0(R11),C','             KOMMA
         BNE   JACCTERR                ERROR
         SUBR  DELIMIT                 VOLGENDE PARAMETER
         B     *+4(R15)                RETURNCODES
         B     JCHKPRN                 CHECK POS PARM 2
         B     INCRLINE                NO CONTINUATION
JACCTERR EQU   *
         SUBR  ERRMESS                 MESSAGE
         B     INCRLINE                NEXT LINE
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       CONTROLEER PROGRAMMEURSNAAM IN JOBKAART                     /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JCHKPRN  EQU   *                       CHECK POSITIONAL PARM 2
         LA    R6,ERRJPRN              ERROR IN PROGRAMMEURSNAAM
         LA    R7,=C'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789@#$-. '
         CALL  PARMTEST                TEST PARAMETER
         B     *+4(R15)                RETURNCODES
         B     JPRNOK                  OK
         B     JPRNERR                 ERROR
JPRNOK   EQU   *
         MVI   FJPOS2,X'FF'            SET FLAG
         CLI   0(R11),C' '             EINDE KAART ?
         BE    JNEXTKEY                COMMENTAAR ?
         CLI   0(R11),C','             KOMMA ?
         BNE   JPRNERR                 ERROR
         SUBR  DELIMIT                 VOLGENDE PARAMETER
         B     *+4(R15)                RETURNCODES
         B     JOBKEY                  CHECK KEYWORD PARAMETERS
         B     INCRLINE                NO CONTINUATION
JPRNERR  EQU   *
         SUBR  ERRMESS                 MESSAGE
         B     INCRLINE                NEXT LINE
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       CONTROLEER KEYWORD PARAMETERS JOBKAART                      /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JOBKEY   EQU   *
         SPACE 3
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       ADDRSPC PARM IN JOBKAART ?                                  /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JADDRSPC EQU   *
         CLC   0(8,R11),=C'ADDRSPC='   ADDRSPC= ?
         BNE   JCLASS                  NEE.
         CLI   FJADDRSP,X'FF'          REEDS GESPECIFICEERD ?
         BE    JDOUBLE                 JA.
         MVI   FJADDRSP,X'FF'          VUL FLAG
         LA    R6,ERRJADDR             FOUT IN KEYWORD PARM
         LA    R11,8(R11)              VERHOOG POINTER
         CLC   0(4,R11),=C'VIRT'       VIRT ?
         BNE   JREAL                   NEE.
JADDROK  EQU   *
         LA    R11,4(R11)              VERHOOG POINTER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
JREAL    EQU   *
         CLC   0(4,R11),=C'REAL'       REAL ?
         BE    JADDROK                 JA.
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       CLASS PARM IN JOBKAART ?                                    /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JCLASS   EQU   *
         CLC   0(6,R11),=C'CLASS='     CLASS= ?
         BNE   JCOND                   NEE.
         CLI   FJCLASS,X'FF'           REEDS GESPECIFICEERD ?
         BE    JDOUBLE                 JA.
         MVI   FJCLASS,X'FF'           VUL FLAG
         LA    R6,ERRJCLAS             FOUT IN KEYWORD PARM
         LA    R11,6(R11)              VERHOOG POINTER
         LA    R7,=C'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789 '
         SUBR  TESTCHAR                A ===> Z OF 0 ===> 9 ?
         B     *+4(R15)                RETURNCODES
         B     JCLASSOK                OK.
         B     JCLASSER                ERROR IN CLASS
JCLASSOK EQU   *
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
JCLASSER EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       COND PARM IN JOBKAART ?                                     /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JCOND    EQU   *
         CLC   0(5,R11),=C'COND='      COND= ?
         BNE   JGROUP                  NEE.
         CLI   FJCOND,X'FF'            REEDS GESPECIFICEERD ?
         BE    JDOUBLE                 JA.
         MVI   FJCOND,X'FF'            VUL FLAG
         LA    R6,ERRJCOND             FOUT IN KEYWORD PARM
         LA    R11,5(R11)              VERHOOG POINTER
         SUBR  JCONDCDE                TEST OP JOB COND-CODE
         B     *+4(R15)                RETURNCODES
         B     JCONDOK                 OK.
         B     JCONDERR                ERROR IN COND
JCONDOK  EQU   *
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
JCONDERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       GROUP PARM IN JOBKAART ?                                    /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JGROUP   EQU   *
         CLC   0(6,R11),=C'GROUP='     GROUP= ?
         BNE   JMSGCLAS                NEE.
         CLI   FJGROUP,X'FF'           REEDS GESPECIFICEERD ?
         BE    JDOUBLE                 JA.
         MVI   FJGROUP,X'FF'           VUL FLAG
         LA    R6,ERRJGROU             KEYWORD NIET TOEGESTAAN
         LA    R11,6(R11)              VERHOOG POINTER
         SUBR  ERRMESS                 GEEF MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       MSGCLASS PARM IN JOBKAART ?                                 /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JMSGCLAS EQU   *
         CLC   0(9,R11),=C'MSGCLASS='  MSGCLASS ?
         BNE   JMSGLEVL                NEE.
         CLI   FJMSGCLS,X'FF'          REEDS GESPECIFICEERD ?
         BE    JDOUBLE                 JA.
         MVI   FJMSGCLS,X'FF'          VUL FLAG
         LA    R6,ERRJMSGC             FOUT IN KEYWORD PARM
         LA    R11,9(R11)              VERHOOG POINTER
         LA    R7,=C'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789 '
         SUBR  TESTCHAR                A ===> Z OF 0 ===> 9 ?
         B     *+4(R15)                RETURNCODES
         B     JMSGCLOK                OK.
         B     JMSGCLER                ERROR IN MSGCLASS
JMSGCLOK EQU   *
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
JMSGCLER EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       MSGLEVEL PARM IN JOBKAART ?                                 /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JMSGLEVL EQU   *
         CLC   0(9,R11),=C'MSGLEVEL='  MSGLEVEL ?
         BNE   JNOTIFY                 NEE.
         CLI   FJMSGLVL,X'FF'          REEDS GESPECIFICEERD ?
         BE    JDOUBLE                 JA.
         MVI   FJMSGLVL,X'FF'           VUL FLAG
         LA    R6,ERRJMSGL             FOUT IN KEYWORD PARM
         LA    R11,9(R11)              VERHOOG POINTER
         CLI   0(R11),C'('             (
         BNE   JMSGL010                GEEN HAAKJE
         LA    R11,1(R11)              VERHOOG POINTER
         AP    BRFLAG,EEN              HAAKJE AANWEZIG
JMSGL010 EQU   *
         LA    R7,=C'012 '             0 1 OF 2 ?
         CALL  TESTCHAR                TEST CHARACTER
         B     *+4(R15)                RETURNCODES
         B     JMSGL020                OK.
         CP    BRFLAG,NUL              ( AANWEZIG ?
         BE    JMSGLERR                NEE.
JMSGL020 EQU   *
         CP    BRFLAG,NUL              ( AANWEZIG ?
         BE    JMSGLOK                 NEE.
         SUBR  DELIMIT                 VOLGENDE SUBPARAMETER ?
         B     *+4(R15)                RETURNCODES
         B     JMSGL040                TEST REST
         B     INCRLINE                GEEN CONTINUATION
JMSGL040 EQU   *
         CLI   0(R11),C'0'             MESSAGES = 0 ?
         BE    JMSGL050                TEST )
         CLI   0(R11),C'1'             MESSAGES = 1 ?
         BNE   JMSGLERR                ERROR
JMSGL050 EQU   *
         LA    R11,1(R11)              VERHOOG POINTER
         CLI   0(R11),C')'             )
         BNE   JMSGLERR                OK.
         LA    R11,1(R11)              VERHOOG POINTER
         SP    BRFLAG,EEN              RESET FLAG
JMSGLOK  EQU   *
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
JMSGLERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       NOTIFY PARM IN JOBKAART ?                                   /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JNOTIFY  EQU   *
         CLC   0(7,R11),=C'NOTIFY='    NOTIFY ?
         BNE   JPASWORD                NEE.
         CLI   FJNOTIFY,X'FF'          REEDS GESPECIFICEERD ?
         BE    JDOUBLE                 JA.
         MVI   FJNOTIFY,X'FF'          VUL FLAG
         LA    R6,ERRJNOTI             FOUT IN KEYWORD PARM
         LA    R11,7(R11)              VERHOOG POINTER
         SUBR  NAMES                   NAAM ?
         B     *+4(R15)                RETURNCODES
         B     JNOTIFOK                OK.
         B     JNOTIFER                ERROR IN NOTIFY
JNOTIFOK EQU   *
         CLI   7(R1),C' '              MAXIMAAL ZEVEN LANG ?
         BNE   JNOTIFER                NEE LANGER.
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
JNOTIFER EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       PASSWORD PARM IN JOBKAART ?                                 /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JPASWORD EQU   *
         CLC   0(9,R11),=C'PASSWORD='  PASSWORD= ?
         BNE   JPERFORM                NEE.
         CLI   FJPASSW,X'FF'           REEDS GESPECIFICEERD ?
         BE    JDOUBLE                 JA.
         MVI   FJPASSW,X'FF'           VUL FLAG
         LA    R6,ERRJPASS             KEYWORD NIET TOEGESTAAN
         LA    R11,9(R11)              VERHOOG POINTER
         SUBR  ERRMESS                 GEEF MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       PERFORM PARM IN JOBKAART ?                                  /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JPERFORM EQU   *
         CLC   0(8,R11),=C'PERFORM='   PERFORM ?
         BNE   JPRTY                   NEE.
         CLI   FJPERFRM,X'FF'          REEDS GESPECIFICEERD ?
         BE    JDOUBLE                 JA.
         MVI   FJPERFRM,X'FF'          VUL FLAG
         LA    R6,ERRJPERF             FOUT IN KEYWORD PARM
         LA    R11,8(R11)              VERHOOG POINTER
         SUBR  NUMBER                  GETAL ?
         B     *+4(R15)                RETURNCODES
         B     JPERFMOK                OK.
         B     JPERFMER                ERROR IN PERFORM
JPERFMOK EQU   *
         C     R1,=F'1'                TEST OP 1
         BL    JPERFMER                ERROR
         C     R1,=F'255'              TEST OP 255
         BH    JPERFMER                ERROR
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
JPERFMER EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       PRTY PARM IN JOBKAART ?                                     /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JPRTY    EQU   *
         CLC   0(4,R11),=C'PRTY='      PRTY ?
         BNE   JRD                     NEE.
         LA    R6,ERRPRTY              PRTY IS NIET TOEGESTAAN
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       RD PARM IN JOBKAART ?                                       /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JRD      EQU   *
         CLC   0(3,R11),=C'RD='        RD= ?
         BNE   JREGION                 NEE.
         CLI   FJRD,X'FF'              REEDS GESPECIFICEERD ?
         BE    JDOUBLE                 JA.
         MVI   FJRD,X'FF'              VUL FLAG
         LA    R6,ERRJRD               KEYWORD NIET TOEGESTAAN
         LA    R11,3(R11)              VERHOOG POINTER
         SUBR  ERRMESS                 GEEF MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       REGION PARM IN JOBKAART ?                                   /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JREGION  EQU   *
         CLC   0(7,R11),=C'REGION='    REGION ?
         BNE   JRESTART                NEE.
         CLI   FJREGION,X'FF'          REEDS GESPECIFICEERD ?
         BE    JDOUBLE                 JA.
         MVI   FJREGION,X'FF'          VUL FLAG
         LA    R6,ERRJREGN             FOUT IN KEYWORD PARM
         LA    R11,7(R11)              VERHOOG POINTER
         SUBR  NUMBER                  GETAL ?
         B     *+4(R15)                RETURNCODES
         B     JREGNUM                 OK.
         B     JREGNERR                ERROR IN REGION
JREGNUM  EQU   *
         CLI   0(R11),C'K'             K ?
         BNE   JREGNERR                ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
JREGNERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       RESTART PARM IN JOBKAART ?                                  /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JRESTART EQU   *
         CLC   0(8,R11),=C'RESTART='   RESTART ?
         BNE   JTIME                   NEE.
         CLI   FJRESTRT,X'FF'          REEDS GESPECIFICEERD ?
         BE    JDOUBLE                 JA.
         MVI   FJRESTRT,X'FF'          VUL FLAG
         LA    R6,ERRJREST             FOUT IN KEYWORD PARM
         LA    R11,8(R11)              VERHOOG POINTER
         CLI   0(R11),C'*'             *
         BNE   JREST010                NEE
         LA    R11,1(R11)              VERHOOG POINTER
         B     JRESTROK                OK
JREST010 EQU   *
         SUBR  NAMES                   TEST OP NAAM
         B     *+4(R15)                RETURNCODES
         B     JREST020                NAAM OK
         B     JRESTERR                ERROR
JREST020 EQU   *
         CLI   0(R11),C'.'             .
         BNE   JRESTROK                OK
         LA    R11,1(R11)              VERHOOG POINTER
         SUBR  NAMES                   TEST NAAM
         B     *+4(R15)                RETURNCODES
         B     JRESTROK                OK
         B     JRESTERR                ERROR
JRESTROK EQU   *
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
JRESTERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       TIME PARM IN JOBKAART ?                                     /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JTIME    EQU   *
         CLC   0(5,R11),=C'TIME='      TIME ?
         BNE   JTYPRUN                 NEE.
         CLI   FJTIME,X'FF'            REEDS GESPECIFICEERD ?
         BE    JDOUBLE                 JA.
         MVI   FJTIME,X'FF'            VUL FLAG
         LA    R6,ERRJTIME             FOUT IN KEYWORD PARM
         LA    R11,5(R11)              VERHOOG POINTER
         CLI   0(R11),C'('             (
         BNE   JTIME010                NEE
         LA    R11,1(R11)              VERHOOG POINTER
         AP    BRFLAG,EEN              HAAKJE AANWEZIG
JTIME010 EQU   *
         SUBR  NUMBER                  TEST GETAL
         B     *+4(R15)                RETURNCODES
         B     JTIME020                MINUTEN
         B     JTIME040                GEEN MINUTEN
JTIME020 EQU   *
         C     R1,=F'1441'             MOET KLEINER ZIJN DAN 1440
         BL    JTIME030                OK
         B     JTIMEERR                ERROR
JTIME030 EQU   *
         CP    BRFLAG,NUL              HAAKJE AANWEZIG ?
         BE    JTIMEOK
JTIME040 EQU   *
         CLI   0(R11),C','             KOMMA
         BNE   JTIME070                TEST )
         SUBR  DELIMIT                 VOLGENDE SUBPARAMETER
         B     *+4(R15)                RETURNCODES
         B     JTIME050                AANWEZIG
         B     INCRLINE
JTIME050 EQU   *
         SUBR  NUMBER                  TEST OP GETAL
         B     *+4(R15)                RETURNCODES
         B     JTIME060                OK
         B     JTIMEERR                ERR
JTIME060 EQU   *
         C     R1,=F'60'               MOET KLEINER ZIJN DAN 60
         BL    JTIME070                TEST )
         B     JTIMEERR                ERROR
JTIME070 EQU   *
         CLI   0(R11),C')'             HAAKJE AANWEZIG ?
         BNE   JTIMEERR                ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         SP    BRFLAG,EEN              RESET FLAG
JTIMEOK  EQU   *
         B     JNEXTKEY                VOLGENDE KEYWORD
JTIMEERR EQU   *
         SUBR  ERRMESS                 ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     JNEXTKEY
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       TYPRUN PARM IN JOBKAART ?                                   /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JTYPRUN  EQU   *
         CLC   0(7,R11),=C'TYPRUN='    TYPRUN ?
         BNE   JUSER                   ONBEKEND KEYWORD
         CLI   FJTYPRUN,X'FF'          REEDS GESPECIFICEERD ?
         BE    JDOUBLE                 JA.
         MVI   FJTYPRUN,X'FF'          VUL FLAG
         LA    R6,ERRJTYPR             FOUT IN KEYWORD PARM
         LA    R11,7(R11)              VERHOOG POINTER
         CLC   0(4,R11),=C'HOLD'       HOLD ?
         BE    JTYPROK2                OK
         CLC   0(7,R11),=C'JCLHOLD'    JCLHOLD ?
         BE    JTYPROK1                OK
         CLC   0(4,R11),=C'SCAN'       SCAN ?
         BE    JTYPROK2                OK
         CLC   0(4,R11),=C'COPY'       COPY ?
         BE    JTYPROK2                OK
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
JTYPROK1 EQU   *
         LA    R11,3(R11)              VERHOOG POINTER
JTYPROK2 EQU   *
         LA    R11,4(R11)              VERHOOG POINTER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       USER PARM IN JOBKAART ?                                     /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JUSER    EQU   *
         CLC   0(5,R11),=C'USER='      USER= ?
         BNE   JKEYERR                 NEE.
         CLI   FJUSER,X'FF'            REEDS GESPECIFICEERD ?
         BE    JDOUBLE                 JA.
         MVI   FJUSER,X'FF'            VUL FLAG
         LA    R6,ERRJUSER             KEYWORD NIET TOEGESTAAN
         LA    R11,5(R11)              VERHOOG POINTER
         SUBR  ERRMESS                 GEEF MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       ONBEKENDE KEYWORD PARAMETER                                 /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JKEYERR  EQU   *
         LA    R6,ERRJKEY              ONBEKEND KEYWORD
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       DUBBELE KEYWORD PARAMETER                                   /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JDOUBLE  EQU   *
         LA    R6,ERRJDOUB             FOUT IN KEYWORD PARM
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     JNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       VOLGENDE KEYWORD PARAMETER ?                                /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
JNEXTKEY EQU   *
         CLI   0(R11),C','             KOMMA ?
         BNE   JNEXTK1                 NEE.
         SUBR  DELIMIT                 VOLGENDE PARAMETER
         B     *+4(R15)                RETURNCODES
         B     JOBKEY                  AANWEZIG
         B     INCRLINE                NO CONTINUATION
JNEXTK1  EQU   *
         CLI   0(R11),C' '             EINDE REGEL
         BNE   JNEXTK2                 NEE.
         L     R11,ENDLINE             EINDE REGEL
         CLI   0(R11),C' '             CONTINUE COMMENT ?
         BE    INCRLINE                NEE.
         MVI   CONTFLAG,X'FF'          SET FLAG
         B     INCRLINE
JNEXTK2  EQU   *
         SUBR  ERRMESS                 ERROR MESSAGE
         B     INCRLINE                VOLGENDE REGEL
         TITLE '                   J C L   S Y N T A X - C H E C K E R C
                   E X E C K A A R T'
         SPACE 15
**********************************************************************
**********************************************************************
**                                                                  **
**                                                                  **
**       EEEEEEEEEEEE  XX        XX  EEEEEEEEEEEE   CCCCCCCCCC      **
**       EEEEEEEEEEEE  XX        XX  EEEEEEEEEEEE  CCCCCCCCCCCC     **
**       EE             XX      XX   EE            CC        CC     **
**       EE              XX    XX    EE            CC               **
**       EE               XX  XX     EE            CC               **
**       EEEEEEEE          XXXX      EEEEEEEE      CC               **
**       EEEEEEEE          XXXX      EEEEEEEE      CC               **
**       EE               XX  XX     EE            CC               **
**       EE              XX    XX    EE            CC               **
**       EE             XX      XX   EE            CC        CC     **
**       EEEEEEEEEEEE  XX        XX  EEEEEEEEEEEE  CCCCCCCCCCCC     **
**       EEEEEEEEEEEE  XX        XX  EEEEEEEEEEEE   CCCCCCCCCC      **
**                                                                  **
**                                                                  **
**********************************************************************
**********************************************************************
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       CONTROLEER SYNTAX EXECKAART                                 /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
EXEKAART EQU   *
         MVI   CARDFLAG,X'02'          DIT IS EEN EXECKAART
         XC    EFLAGS(FELEN),EFLAGS    CLEAR PARAMETER FLAGS
         L     R11,STARTLNE            BEGIN VAN DE REGEL
         LA    R11,2(R11)              SKIP //
         SUBR  NAMES                   CONTROLEER OP NAAM
         SUBR  SKIPSPC                 SKIP SPATIE(S)
         LA    R11,4(R11)              SKIP 'EXEC'
         SUBR  SKIPSPC                 SKIP SPATIES
         B     *+4(R15)                RETURNCODES
         B     ECHKPOS1                OK.
         B     EPOS1ERR                ERROR; GEEN SPATIE
         B     EPOS1ERR                ERROR; GEEN POSITIONAL PARM
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       CONTROLEER POSITIONAL PARAMETERS EXECKAART                  /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
ECHKPOS1 EQU   *
         CALL  BALANCE                 () EN '' IN BALANS ?
         B     *+4(R15)                RETURNCODES
         B     ECHKPOS2                JA.
         B     INCRLINE                NEE.
ECHKPOS2 EQU   *
         XC    EPROCFLG,EPROCFLG       CLEAR PROCEDURE FLAG
         CLC   0(4,R11),=C'PGM='       PGM= ?
         BNE   ECHKPS11                NEE.
         LA    R11,4(R11)              VERHOOG POINTER
         SUBR  NAMES                   PROGRAMMANAAM ?
         B     *+4(R15)                RETURNCODES
         B     EXECKEY1                KEYWORD PARM ?
         CLC   0(2,R11),=C'*.'         REFERENCE ?
         BNE   EPOS1ERR                NEE. ERROR.
         LA    R11,2(R11)              VERHOOG POINTER
         SUBR  NAMES                   NAAM ?
         B     *+4(R15)                RETURNCODES
         B     EPOSDDN                 OK
         B     EPOS1ERR                ERROR
EPOSDDN  EQU   *
         CLI   0(R11),C'.'             . AANWEZIG ?
         BNE   EPOS1ERR                NEE
         LA    R11,1(R11)              VERHOOG POINTER
         SUBR  NAMES                   NAAM ?
         B     *+4(R15)                RETURNCODES
         B     EPOSSTEP                OK
         B     EPOS1ERR                ERROR
EPOSSTEP EQU   *
         CLI   0(R11),C'.'             . AANWEZIG ?
         BNE   EXECKEY1                NEE.
         LA    R11,1(R11)              VERHOOG POINTER
         SUBR  NAMES                   NAAM ?
         B     *+4(R15)                RETURNCODES
         B     EXECKEY1                OK
         B     EPOS1ERR                ERROR
ECHKPS11 EQU   *
         MVI   EPROCFLG,X'FF'          SET FLAG
         CLC   0(5,R11),=C'PROC='      PROC= ?
         BNE   ECHKPS12                NEE
         LA    R11,5(R11)              VERHOOG POINTER
         SUBR  NAMES                   NAAM ?
         B     *+4(R15)                RETURNCODES
         B     EXECKEY1                KEYWORD PARM ?
         B     EPOS1ERR                GEEN NAAM
ECHKPS12 EQU   *
         SUBR  NAMES                   PROCEDURENAAM ?
         B     *+4(R15)                RETURNCODES
         B     EXECKEY1                KEYWORD PARMS ?
         B     EPOS1ERR                GEEN POS PARM
EPOS1ERR EQU   *
         LA    R6,ERREPOS1             GEEN POS PARM BIJ EXEC
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         B     INCRLINE
         SPACE 3
EXECKEY1 EQU   *
         CLI   0(R11),C','             KOMMA
         BE    EPARMS                  KEYWORDS
         CLI   0(R11),C' '             EINDE KAART
         BNE   EPOS1ERR                NEE
         B     ENEXTKEY                COMMENTAAR ?
EPARMS   EQU   *
         SUBR  DELIMIT                 KEYWORD PARAMETERS ?
         B     *+4(R15)                RETURNCODES
         B     EXECKEY                 AANWEZIG
         B     INCRLINE                GEEN CONTINUATION
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       CONTROLEER KEYWORD PARAMETERS EXECKAART                     /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
EXECKEY  EQU   *
         SPACE 3
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       ACCT PARM IN EXECKAART ?                                    /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
EACCT    EQU   *
         CLC   0(5,R11),=C'ACCT='      ACCT= ?
         BE    EACCT020                JA
         CLC   0(5,R11),=C'ACCT.'      ACCT. ?
         BNE   EADDRSPC                NEE
         LA    R11,5(R11)              VERHOOG POINTER
         CLI   EPROCFLG,X'FF'          EXEC PROC ?
         BNE   EACCTERR                ERROR
         SUBR  NAMES                   STEPNAAM ?
         B     *+4(R15)                RETURNCODES
         B     EACCT010                OK
         B     EACCTERR                ERROR
EACCT010 EQU   *
         CLI   0(R11),C'='             = ?
         BNE   EACCTERR                ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    EACCTOK                 JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    EACCTOK                 JA.
         B     EACCT030
EACCT020 EQU   *
         LA    R11,5(R11)              VERHOOG POINTER
EACCT030 EQU   *
         CLI   FEACCT,X'FF'            REEDS GESPECIFICEERD ?
         BE    EDOUBLE                 JA.
         MVI   FEACCT,X'FF'            VUL FLAG
         LA    R6,ERREACCT             FOUT IN KEYWORD PARM
         CLI   0(R11),C'('             BEGINT MET (
         BNE   EACCT040                NEE.
         LA    R11,1(R11)              VERHOOG POINTER
         AP    BRFLAG,EEN              SET FLAG
EACCT040 EQU   *
         LA    R7,=C'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789@#$- '
         CALL  PARMTEST                TEST PARAMETER
         B     *+4(R15)                RETURNCODES
         B     EACCT050                OK
         B     EACCTERR                ERROR.
EACCT050 EQU   *
         CP    BRFLAG,NUL              ( AANWEZIG ?
         BE    EACCTOK                 NEE.
         CLI   0(R11),C')'             EINDE ACCOUNTING ?
         BE    EACCT060
         CALL  DELIMIT                 DELIMITER ?
         B     *+4(R15)                RETURNCODES
         B     EACCT040                JA.
         B     EACCTERR                ERROR.
EACCT060 EQU   *
         LA    R11,1(R11)              VERHOOG POINTER
         SP    BRFLAG,EEN              RESET FLAG
EACCTOK  EQU   *
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
EACCTERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       ADDRSPC PARM IN EXECKAART ?                                 /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
EADDRSPC EQU   *
         CLC   0(8,R11),=C'ADDRSPC='   ADDRSPC= ?
         BE    EADDR020                JA.
         CLC   0(8,R11),=C'ADDRSPC.'   ADDRSPC. ?
         BNE   ECOND                   NEE.
         LA    R6,ERREADDR             FOUT IN KEYWORD PARM
         LA    R11,8(R11)              VERHOOG POINTER
         CLI   EPROCFLG,X'FF'          EXEC PROC ?
         BNE   EADDRERR                ERROR
         SUBR  NAMES                   STEPNAAM ?
         B     *+4(R15)                RETURNCODES
         B     EADDR010                OK
         B     EADDRERR                ERROR
EADDR010 EQU   *
         CLI   0(R11),C'='             = ?
         BNE   EADDRERR                ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    EADDROK                 JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    EADDROK                 JA.
         B     EADDR030
EADDR020 EQU   *
         LA    R6,ERREADDR             FOUT IN KEYWORD PARM
         LA    R11,8(R11)              VERHOOG POINTER
EADDR030 EQU   *
         CLI   FEADDRSP,X'FF'          REEDS GESPECIFICEERD ?
         BE    EDOUBLE                 JA.
         MVI   FEADDRSP,X'FF'          VUL FLAG
         CLC   0(4,R11),=C'VIRT'       VIRT ?
         BNE   EADDR050                NEE.
EADDR040 EQU   *
         LA    R11,4(R11)              VERHOOG POINTER
EADDROK  EQU   *
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
EADDR050 EQU   *
         CLC   0(4,R11),=C'REAL'       REAL ?
         BE    EADDR040                JA.
EADDRERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       COND PARM IN EXECKAART ?                                    /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
ECOND    EQU   *
         CLC   0(5,R11),=C'COND='      COND= ?
         BE    ECOND020                JA
         CLC   0(5,R11),=C'COND.'      COND. ?
         BNE   EDPRTY                  NEE.
         LA    R6,ERRECOND             FOUT IN KEYWORD PARM
         LA    R11,5(R11)              VERHOOG POINTER
         CLI   EPROCFLG,X'FF'          EXEC PROC ?
         BNE   ECONDERR                ERROR
         SUBR  NAMES                   STEPNAAM ?
         B     *+4(R15)                RETURNCODES
         B     ECOND010                OK
         B     ECONDERR                ERROR
ECOND010 EQU   *
         CLI   0(R11),C'='             = ?
         BNE   ECONDERR                ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    ECONDOK                 JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    ECONDOK                 JA.
         B     ECOND030
ECOND020 EQU   *
         LA    R6,ERRECOND             FOUT IN KEYWORD PARM
         LA    R11,5(R11)              VERHOOG POINTER
ECOND030 EQU   *
         CLI   FECOND,X'FF'            REEDS GESPECIFICEERD ?
         BE    EDOUBLE                 JA.
         MVI   FECOND,X'FF'            VUL FLAG
         SUBR  ECONDCDE                TEST OP JOB COND-CODE
         B     *+4(R15)                RETURNCODES
         B     ECONDOK                 OK.
         B     ECONDERR                ERROR IN COND
ECONDOK  EQU   *
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
ECONDERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       DPRTY PARM IN EXECKAART ?                                   /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
EDPRTY   EQU   *
         CLC   0(6,R11),=C'DPRTY='     DPRTY= ?
         BE    EDPRT020                JA
         CLC   0(6,R11),=C'DPRTY.'     DPRTY. ?
         BNE   EDYNAM                  NEE.
         LA    R6,ERREDPRT             FOUT IN KEYWORD PARM
         LA    R11,6(R11)              VERHOOG POINTER
         SUBR  NAMES                   STEPNAAM ?
         B     *+4(R15)                RETURNCODES
         B     EDPRT010                OK
         B     EDPRT040                ERROR
EDPRT010 EQU   *
         CLI   0(R11),C'='             = ?
         BNE   EDPRT040                ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         B     EDPRT030
EDPRT020 EQU   *
         LA    R6,ERREDPRT             FOUT IN KEYWORD PARM
         LA    R11,6(R11)              VERHOOG POINTER
EDPRT030 EQU   *
         CLI   FEDPRTY,X'FF'           REEDS GESPECIFICEERD ?
         BE    EDOUBLE                 JA.
         MVI   FEDPRTY,X'FF'           VUL FLAG
         CLI   0(R11),C'('             ( AANWEZIG ?
         BNE   EDPRT040                NEE.
         LA    R11,1(R11)              VERHOOG POINTER
         AP    BRFLAG,EEN              SET FLAG
EDPRT040 EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       DYNAMNBR PARM IN EXECKAART ?                                /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
EDYNAM   EQU   *
         CLC   0(9,R11),=C'DYNAMNBR='  DYNAMNBR= ?
         BE    EDYNM020                JA.
         CLC   0(9,R11),=C'DYNAMNBR.'  DYNAMNBR. ?
         BNE   EPARM                   NEE.
         LA    R6,ERREDYNM             FOUT IN KEYWORD PARM
         LA    R11,9(R11)              VERHOOG POINTER
         CLI   EPROCFLG,X'FF'          EXEC PROC ?
         BNE   EDYNMERR                ERROR
         SUBR  NAMES                   STEPNAAM ?
         B     *+4(R15)                RETURNCODES
         B     EDYNM010                OK
         B     EDYNMERR                ERROR
EDYNM010 EQU   *
         CLI   0(R11),C'='             = ?
         BNE   EDYNMERR                ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    EDYNMOK                 JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    EDYNMOK                 JA.
         B     EDYNM030
EDYNM020 EQU   *
         LA    R6,ERREDYNM             FOUT IN KEYWORD PARM
         LA    R11,9(R11)              VERHOOG POINTER
EDYNM030 EQU   *
         CLI   FEDYNAM,X'FF'           REEDS GESPECIFICEERD ?
         BE    EDOUBLE                 JA.
         MVI   FEDYNAM,X'FF'           VUL FLAG
         SUBR  NUMBER                  GETAL ?
         B     *+4(R15)                RETURNCODES
         B     EDYNM040                OK.
         B     EDYNMERR                ERROR IN DYNAMNBR
EDYNM040 EQU   *
         C     R1,=F'1'                MOET GROTER DAN 0 ZIJN.
         BL    EDYNMERR                TE KEIN
         C     R1,=F'1635'             MAG NIET GROTER DAN 1635 ZIJN
         BH    EDYNMERR                TE GROOT
EDYNMOK  EQU   *
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
EDYNMERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       PARM IN EXECKAART ?                                         /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
EPARM    EQU   *
         CLC   0(5,R11),=C'PARM='      PARM= ?
         BE    EPARM020                JA.
         CLC   0(5,R11),=C'PARM.'      PARM. ?
         BNE   EPERFORM                NEE.
         LA    R6,ERREPARM             FOUT IN KEYWORD PARM
         LA    R11,5(R11)              VERHOOG POINTER
         CLI   EPROCFLG,X'FF'          EXEC PROC ?
         BNE   EPARMERR                ERROR
         SUBR  NAMES                   STEPNAAM ?
         B     *+4(R15)                RETURNCODES
         B     EPARM010                OK
         B     EPARMERR                ERROR
EPARM010 EQU   *
         CLI   0(R11),C'='             = ?
         BNE   EPARMERR                ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    EPARMOK                 JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    EPARMOK                 JA.
         B     EPARM030
EPARM020 EQU   *
         LA    R6,ERREPARM             FOUT IN KEYWORD PARM
         LA    R11,5(R11)              VERHOOG POINTER
EPARM030 EQU   *
         CLI   FEPARM,X'FF'            REEDS GESPECIFICEERD ?
         BE    EDOUBLE                 JA.
         MVI   FEPARM,X'FF'            SET FLAG
         CLI   0(R11),C'('             BEGINT MET (
         BNE   EPARM040                NEE.
         LA    R11,1(R11)              VERHOOG POINTER
         AP    BRFLAG,EEN              SET FLAG
EPARM040 EQU   *
         LA    R7,=C'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789@#$ '
         CALL  PARMTEST                TEST PARAMETER
         B     *+4(R15)                RETURNCODES
         B     EPARM050                OK
         B     EPARMERR                ERROR.
EPARM050 EQU   *
         CP    BRFLAG,NUL              ( AANWEZIG ?
         BE    EPARMOK                 NEE.
         CLI   0(R11),C')'             EINDE PARAMETER ?
         BE    EPARM060
         CALL  DELIMIT                 DELIMITER ?
         B     *+4(R15)                RETURNCODES
         B     EPARM040                JA.
         B     EPARMERR                ERROR.
EPARM060 EQU   *
         LA    R11,1(R11)              VERHOOG POINTER
         SP    BRFLAG,EEN              RESET FLAG
EPARMOK  EQU   *
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
EPARMERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       PERFORM PARM IN EXECKAART ?                                 /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
EPERFORM EQU   *
         CLC   0(8,R11),=C'PERFORM='   PERFORM= ?
         BE    EPERF020                JA.
         CLC   0(8,R11),=C'PERFORM.'   PERFORM. ?
         BNE   ERD                     NEE.
         LA    R6,ERREPERF             FOUT IN KEYWORD PARM
         LA    R11,8(R11)              VERHOOG POINTER
         CLI   EPROCFLG,X'FF'          EXEC PROC ?
         BNE   EPERFERR                ERROR
         SUBR  NAMES                   STEPNAAM ?
         B     *+4(R15)                RETURNCODES
         B     EPERF010                OK
         B     EPERFERR                ERROR
EPERF010 EQU   *
         CLI   0(R11),C'='             = ?
         BNE   EPERFERR                ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    EPERFOK                 JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    EPERFOK                 JA.
         B     EPERF030
EPERF020 EQU   *
         LA    R6,ERREPERF             FOUT IN KEYWORD PARM
         LA    R11,8(R11)              VERHOOG POINTER
EPERF030 EQU   *
         CLI   FEPERFRM,X'FF'          REEDS GESPECIFICEERD ?
         BE    EDOUBLE                 JA.
         MVI   FEPERFRM,X'FF'          VUL FLAG
         SUBR  NUMBER                  GETAL ?
         B     *+4(R15)                RETURNCODES
         B     EPERFOK                 OK.
         B     EPERFERR                ERROR IN PERFORM
EPERFOK  EQU   *
         C     R1,=F'1'                TEST OP 1
         BL    EPERFERR                ERROR
         C     R1,=F'255'              TEST OP 255
         BH    EPERFERR                ERROR
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
EPERFERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       RD PARM IN EXECKAART ?                                      /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
ERD      EQU   *
         CLC   0(3,R11),=C'RD='        RD= ?
         BE    ERD020                  JA.
         CLC   0(3,R11),=C'RD.'        RD. ?
         BNE   EREGION                 NEE.
         LA    R6,ERRERD               FOUT IN KEYWORD PARM
         LA    R11,3(R11)              VERHOOG POINTER
         SUBR  NAMES                   STEPNAAM ?
         B     *+4(R15)                RETURNCODES
         B     ERD010                  OK
         B     ERD040                  ERROR
ERD010   EQU   *
         CLI   0(R11),C'='             = ?
         BNE   ERD040                  ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         B     ERD030
ERD020   EQU   *
         LA    R6,ERRERD               FOUT IN KEYWORD PARM
         LA    R11,3(R11)              VERHOOG POINTER
ERD030   EQU   *
         CLI   FERD,X'FF'              REEDS GESPECIFICEERD ?
         BE    EDOUBLE                 JA.
         MVI   FERD,X'FF'              VUL FLAG
ERD040   EQU   *
         SUBR  ERRMESS                 GEEF MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       REGION PARM IN EXECKAART ?                                  /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
EREGION  EQU   *
         CLC   0(7,R11),=C'REGION='    REGION= ?
         BE    EREGN020                JA.
         CLC   0(7,R11),=C'REGION.'    REGION. ?
         BNE   ETIME                   NEE.
         LA    R6,ERREREGN             FOUT IN KEYWORD PARM
         LA    R11,7(R11)              VERHOOG POINTER
         CLI   EPROCFLG,X'FF'          EXEC PROC ?
         BNE   EREGNERR                ERROR
         SUBR  NAMES                   STEPNAAM ?
         B     *+4(R15)                RETURNCODES
         B     EREGN010                OK
         B     EREGNERR                ERROR
EREGN010 EQU   *
         CLI   0(R11),C'='             = ?
         BNE   EREGNERR                ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    EREGNOK                 JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    EREGNOK                 JA.
         B     EREGN030
EREGN020 EQU   *
         LA    R6,ERREREGN             FOUT IN KEYWORD PARM
         LA    R11,7(R11)              VERHOOG POINTER
EREGN030 EQU   *
         CLI   FEREGION,X'FF'          REEDS GESPECIFICEERD ?
         BE    EDOUBLE                 JA.
         MVI   FEREGION,X'FF'          VUL FLAG
         SUBR  NUMBER                  GETAL ?
         B     *+4(R15)                RETURNCODES
         B     EREGNUM                 OK.
         B     EREGNERR                ERROR IN REGION
EREGNUM  EQU   *
         CLI   0(R11),C'K'             K ?
         BNE   EREGNERR                ERROR
         LA    R11,1(R11)              VERHOOG POINTER
EREGNOK  EQU   *
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
EREGNERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       TIME PARM IN EXECKAART ?                                    /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
ETIME    EQU   *
         CLC   0(5,R11),=C'TIME='      TIME= ?
         BE    ETIME020                JA.
         CLC   0(5,R11),=C'TIME.'      TIME. ?
         BNE   EKEYERR                 NEE.
         LA    R6,ERRETIME             FOUT IN KEYWORD PARM
         LA    R11,5(R11)              VERHOOG POINTER
         CLI   EPROCFLG,X'FF'          EXEC PROC ?
         BNE   ETIMEERR                ERROR
         SUBR  NAMES                   STEPNAAM ?
         B     *+4(R15)                RETURNCODES
         B     ETIME010                OK
         B     ETIMEERR                ERROR
ETIME010 EQU   *
         CLI   0(R11),C'='             = ?
         BNE   ETIMEERR                ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    ETIMEOK                 JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    ETIMEOK                 JA.
         B     ETIME030
ETIME020 EQU   *
         LA    R6,ERRETIME             FOUT IN KEYWORD PARM
         LA    R11,5(R11)              VERHOOG POINTER
ETIME030 EQU   *
         CLI   FETIME,X'FF'            REEDS GESPECIFICEERD ?
         BE    JDOUBLE                 JA.
         MVI   FETIME,X'FF'            VUL FLAG
         CLI   0(R11),C'('             (
         BNE   ETIME040                NEE
         LA    R11,1(R11)              VERHOOG POINTER
         AP    BRFLAG,EEN              SET FLAG
ETIME040 EQU   *
         SUBR  NUMBER                  TEST GETAL
         B     *+4(R15)                RETURNCODES
         B     ETIME050                MINUTEN
         B     ETIME070                GEEN MINUTEN
ETIME050 EQU   *
         C     R1,=F'1441'             MOET KLEINER ZIJN DAN 1440
         BL    ETIME060                OK
         B     ETIMEERR                ERROR
ETIME060 EQU   *
         CP    BRFLAG,NUL              HAAKJE AANWEZIG ?
         BE    ETIMEOK                 NEE
ETIME070 EQU   *
         CLI   0(R11),C','             KOMMA
         BNE   ETIME100                TEST )
         SUBR  DELIMIT                 VOLGENDE SUBPARAMETER ?
         B     *+4(R15)                RETURNCODES
         B     ETIME080                AANWEZIG
         B     INCRLINE                GEEN CONTINIATION
ETIME080 EQU   *
         SUBR  NUMBER                  TEST OP GETAL
         B     *+4(R15)                RETURNCODES
         B     ETIME090                OK
         B     ETIMEERR                ERROR
ETIME090 EQU   *
         C     R1,=F'60'               MOET KLEINER ZIJN DAN 60
         BL    ETIME100                TEST )
         B     ETIMEERR                ERROR
ETIME100 EQU   *
         CLI   0(R11),C')'             )
         BNE   ETIMEERR                ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         SP    BRFLAG,EEN              RESET FLAG
ETIMEOK  EQU   *
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
ETIMEERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       ONBEKENDE KEYWORD PARAMETER                                 /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
EKEYERR  EQU   *
         CLI   EPROCFLG,X'00'          PROCEDURE CALL
         LA    R6,ERREKEY              ONBEKEND KEYWORD
         BNE   EKEYERR1                JA.
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
EKEYERR1 EQU   *
         CLI   0(R11),C'='             = ?
         BE    EKEYERR2                JA.
         LA    R11,1(R11)              VERHOOG POINTER
         C     R11,ENDLINE             EINDE REGEL ?
         BNE   EKEYERR1                NEE
         SUBR  ERRMESS                 ONBEKEND KEYWORD
         B     INCRLINE                VOLGENDE REGEL
EKEYERR2 EQU   *
         LA    R11,1(R11)              VERHOOG POINTER
         C     R11,ENDLINE             EINDE REGEL
         BE    INCRLINE                JA.
         CLI   0(R11),C' '             EINDE REGEL
         BE    INCRLINE                JA
         CLI   0(R11),C','             DELIMITER
         BE    ENEXTKEY                JA
         CLI   0(R11),C''''            TUSSEN QUOTES ?
         BNE   EKEYERR2                NEE.
EKEYERR3 EQU   *
         LA    R11,1(R11)              VERHOOG POINTER
         C     R11,ENDLINE             EINDE REGEL
         BNE   EKEYERR4                NEE.
         SUBR  ERRMESS                 ERROR
         B     INCRLINE                VOLGENDE REGEL
EKEYERR4 EQU   *
         CLI   0(R11),C''''            TWEEDE QUOTE ?
         BNE   EKEYERR3                NEE.
         B     EKEYERR2                JA.
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       DUBBELE KEYWORD PARAMETER                                   /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
EDOUBLE  EQU   *
         LA    R6,ERREDOUB             FOUT IN KEYWORD PARM
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     ENEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       VOLGENDE KEYWORD PARAMETER ?                                /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
ENEXTKEY EQU   *
         CLI   0(R11),C','             KOMMA ?
         BNE   ENEXTK1                 NEE.
         SUBR  DELIMIT                 VOLGENDE PARAMETER
         B     *+4(R15)                RETURNCODES
         B     EXECKEY                 AANWEZIG
         B     INCRLINE                NO CONTINUATION
ENEXTK1  EQU   *
         CLI   0(R11),C' '             EINDE REGEL
         BNE   ENEXTK2                 NEE.
         L     R11,ENDLINE             EINDE REGEL
         CLI   0(R11),C' '             CONTINUE COMMENT ?
         BE    INCRLINE                NEE.
         MVI   CONTFLAG,X'FF'          SET FLAG
         B     INCRLINE
ENEXTK2  EQU   *
         SUBR  ERRMESS                 ERROR MESSAGE
         B     INCRLINE                VOLGENDE REGEL
         TITLE '                   J C L   S Y N T A X - C H E C K E R C
                   D D K A A R T'
         SPACE 15
**********************************************************************
**********************************************************************
**                                                                  **
**                                                                  **
**                    DDDDDDDDDDD   DDDDDDDDDDD                     **
**                    DDDDDDDDDDDD  DDDDDDDDDDDD                    **
**                    DD        DD  DD        DD                    **
**                    DD        DD  DD        DD                    **
**                    DD        DD  DD        DD                    **
**                    DD        DD  DD        DD                    **
**                    DD        DD  DD        DD                    **
**                    DD        DD  DD        DD                    **
**                    DD        DD  DD        DD                    **
**                    DD        DD  DD        DD                    **
**                    DDDDDDDDDDDD  DDDDDDDDDDDD                    **
**                    DDDDDDDDDDD   DDDDDDDDDDD                     **
**                                                                  **
**                                                                  **
**********************************************************************
**********************************************************************
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       CONTROLEER SYNTAX DDKAART                                   /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DDKAART  EQU   *
         MVI   CARDFLAG,X'03'          DIT IS EEN DDKAART
         XC    DFLAGS(FDLEN),DFLAGS    CLEAR PARAMETER FLAGS
         LA    R11,2(R11)              SKIP 'DD'
         SUBR  SKIPSPC                 SKIP SPATIES
         B     *+4(R15)                RETURNCODES
         B     DCHKPOS                 OK.
         B     DDPOSERR                ERROR; GEEN SPATIE
         B     INCRLINE                ERROR; GEEN POSITIONAL PARM
         SPACE 2
DDPOSERR EQU   *
         LA    R6,ERRDPOS              ERROR CODE
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         B     INCRLINE                VOLGENDE REGEL
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       CONTROLEER POSITIONAL PARAMETERS DDKAART                    /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DCHKPOS  EQU   *
         CALL  BALANCE                 () EN '' IN BALANS ?
         B     *+4(R15)                RETURNCODES
         B     DCHKPOS1                JA.
         B     INCRLINE                NEE.
DCHKPOS1 EQU   *
         CLI   0(R11),C'*'             INSTREAM DATA ?
         BE    DDSTAR                  JA.
         CLC   0(4,R11),=C'DATA'       INSTREAM DATA ?
         BE    DDDATA                  JA.
         CLC   0(5,R11),=C'DUMMY'      DUMMY DATASET ?
         BE    DDDUMMY                 JA.
         CLC   0(5,R11),=C'DYNAM'      DYNAMIC ALLOCATION ?
         BE    DDDYNAM                 JA.
         B     DDKEY                   CHECK KEYWORD PARAMETERS
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       POSITIONAL PARAMETER * IN DDKAART                           /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DDSTAR   EQU   *
         LA    R6,ERRSTAR              FOUT IN DD *
         MVI   DATAFLAG,X'F0'          DD * IN DDKAART, VUL FLAG
         MVC   DLM,=C'/*'              DEFAULT DELIMITER
         MVI   DFLAGS,X'FF'            VUL DFLAGS
         MVC   DFLAGS+1(FDLEN-1),DFLAGS
         XC    FDDLM,FDDLM             DLM TOEGESTAAN
         XC    DCBBLKSZ,DCBBLKSZ       DCB=BLKSIZE TOEGESTAAN
         XC    DCBBUFNO,DCBBUFNO       DCB=BUFNO TOEGESTAAN
         XC    DCBLRECL,DCBLRECL       DCB=LRECL TOEGESTAAN
         LA    R11,1(R11)              VERHOOG POINTER
         CLI   0(R11),C' '             EINDE KAART ?
         BE    INCRLINE                JA
         CLI   0(R11),C','             CONTINUE MET KEYWORD PARAMETERS
         BNE   DDSTARER                NEE
         SUBR  DELIMIT                 VERVOLG MET KEYWORDS ?
         B     *+4(R15)                RETURNCODES
         B     DDKEY                   KEYWORDS AANWEZIG
         B     INCRLINE                GEEN CONTINUATION
DDSTARER EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         B     INCRLINE                VOLGENDE REGEL
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       POSITIONAL PARAMETER DATA IN DDKAART                        /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DDDATA   EQU   *
         LA    R6,ERRDDATA             FOUT IN DD DATA
         MVI   DATAFLAG,X'FF'          DD DATA IN DDKAART, VUL FLAG
         MVC   DLM,=C'/*'              DEFAULT DELIMITER
         MVI   DFLAGS,X'FF'            VUL DFLAGS
         MVC   DFLAGS+1(FDLEN-1),DFLAGS
         XC    FDDLM,FDDLM             DLM TOEGESTAAN
         XC    DCBBLKSZ,DCBBLKSZ       DCB=BLKSIZE TOEGESTAAN
         XC    DCBBUFNO,DCBBUFNO       DCB=BUFNO TOEGESTAAN
         XC    DCBLRECL,DCBLRECL       DCB=LRECL TOEGESTAAN
         LA    R11,4(R11)              VERHOOG POINTER
         CLI   0(R11),C' '             EINDE KAART ?
         BE    INCRLINE                JA
         CLI   0(R11),C','             CONTINUE MET KEYWORD PARAMETERS
         BNE   DDDATAER                NEE
         SUBR  DELIMIT                 VERVOLG MET KEYWORDS ?
         B     *+4(R15)                RETURNCODES
         B     DDKEY                   KEYWORDS AANWEZIG
         B     INCRLINE                GEEN CONTINUATION
DDDATAER EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         B     INCRLINE                VOLGENDE REGEL
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       POSITIONAL PARAMETER DUMMY IN DDKAART                       /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DDDUMMY  EQU   *
         LA    R6,ERRDUMMY             FOUT IN DD DUMMY
         MVI   FDDNAME,X'FF'           DDNAME NIET TOEGESTAAN
         LA    R11,5(R11)              VERHOOG POINTER
         CLI   0(R11),C' '             EINDE KAART ?
         BE    INCRLINE                JA
         CLI   0(R11),C','             CONTINUE MET KEYWORD PARAMETERS
         BNE   DDDUMMER                NEE
         SUBR  DELIMIT                 VERVOLG MET KEYWORDS ?
         B     *+4(R15)                RETURNCODES
         B     DDKEY                   KEYWORDS AANWEZIG
         B     INCRLINE                GEEN CONTINUATION
DDDUMMER EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         B     INCRLINE                VOLGENDE REGEL
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       POSITIONAL PARAMETER DYNAM IN DDKAART                       /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DDDYNAM  EQU   *
         LA    R6,ERRDYNAM             FOUT IN DD DYNAM
         LA    R11,5(R11)              VERHOOG POINTER
         CLI   0(R11),C' '             EINDE KAART ?
         BE    INCRLINE                JA
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         B     INCRLINE                VOLGENDE REGEL
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       CONTROLEER KEYWORD PARAMETERS DDKAART                       /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DDKEY    EQU   *
         SPACE 3
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       AMP PARM IN DDKAART ?                                       /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DAMP     EQU   *
         CLC   0(4,R11),=C'AMP='       AMP= ?
         BNE   DBURST                  NEE.
         LA    R11,4(R11)              VERHOOG POINTER
         CLI   FDAMP,X'FF'             REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDAMP,X'FF'             VUL FLAG
         LA    R6,ERRDAMP              FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DAMP000                 NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DAMPOK                  JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DAMPOK                  JA.
DAMP000  EQU   *
         SUBR  SKIPPARM                SKIP PARAMETER
         B     *+4(R15)                RETURNCODES
DAMPOK   EQU   *
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
DAMPERR  EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       BURST PARM IN DDKAART ?                                     /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DBURST   EQU   *
         CLC   0(6,R11),=C'BURST='     BURST= ?
         BNE   DCHARS                  NEE.
         CLI   FDBURST,X'FF'           REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDBURST,X'FF'           VUL FLAG
         LA    R6,ERRDBURS             KEYWORD NIET TOEGESTAAN
         LA    R11,6(R11)              VERHOOG POINTER
         SUBR  ERRMESS                 GEEF MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       CHARS PARM IN DDKAART ?                                     /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DCHARS   EQU   *
         CLC   0(6,R11),=C'CHARS='     CHARS= ?
         BNE   DCHKPT                  NEE.
         CLI   FDCHARS,X'FF'           REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDCHARS,X'FF'           VUL FLAG
         LA    R6,ERRDCHAR             KEYWORD NIET TOEGESTAAN
         LA    R11,6(R11)              VERHOOG POINTER
         SUBR  ERRMESS                 GEEF MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       CHKPT PARM IN DDKAART ?                                     /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DCHKPT   EQU   *
         CLC   0(6,R11),=C'CHKPT='     CHKPT= ?
         BNE   DCOPIES                 NEE.
         CLI   FDCHKPT,X'FF'           REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDCHKPT,X'FF'           VUL FLAG
         LA    R6,ERRDCHKP             KEYWORD NIET TOEGESTAAN
         LA    R11,6(R11)              VERHOOG POINTER
         SUBR  ERRMESS                 GEEF MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       COPIES PARM IN DDKAART ?                                    /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DCOPIES  EQU   *
         CLC   0(7,R11),=C'COPIES='    COPIES= ?
         BNE   DDCB                    NEE.
         LA    R11,7(R11)              VERHOOG POINTER
         CLI   FDCOPIES,X'FF'          REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDCOPIES,X'FF'          VUL FLAG
         LA    R6,ERRDCOP              FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DCOP000                 NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DCOPOK                  JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DCOPOK                  JA.
DCOP000  EQU   *
         SUBR  NUMBER                  GETAL ?
         B     *+4(R15)                RETURNCODE
         B     DCOPOK                  OK
         B     DCOPERR                 ERROR
DCOPOK   EQU   *
         C     R1,=F'1'                MAG NIET KLEINER DAN 1 ZIJN
         BL    DCOPERR                 ERROR
         C     R1,=F'255'              MAG NIET GROTER DAN 255 ZIJN
         BH    DCOPERR                 ERROR
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
DCOPERR  EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       DCB PARM IN DDKAART ?                                       /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DDCB     EQU   *
         CLC   0(4,R11),=C'DCB='       DCB= ?
         BNE   DDNAME                  NEE.
         LA    R11,4(R11)              VERHOOG POINTER
         CLI   FDDCB,X'FF'             REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDDCB,X'FF'             VUL FLAG
         LA    R6,ERRDDCB              FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DDCB000                 NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DDCBOK                  JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DDCBOK                  JA.
DDCB000  EQU   *
         CLI   0(R11),C'('             ( ?
         BNE   DDCB005                 TEST ATTRIBUTES
         AP    BRFLAG,EEN              ( AANWEZIG
         LA    R11,1(R11)              VERHOOG POINTER
DDCB005  EQU   *
         SUBR  DCBATTR                 TEST OP ATTRIBUTES
         B     *+4(R15)                RETURNCODES
         B     DDCB010                 OK
         B     DDCB020                 GEEN ATTRIBUTE LIST
DDCB010  EQU   *
         CP    BRFLAG,NUL              ( AANWEZIG ?
         BE    DDCBOK                  NEE.
         B     DDCB070                 JA.
DDCB020  EQU   *
         SUBR  DSNTEST                 TEST OP EEN DSNAME
         B     *+4(R15)                RETURNCODES
         B     DDCB050                 JA. TEST ATTIBUTES
         B     DDCBERR                 GEEN DSNAME
DDCB050  EQU   *
         CP    BRFLAG,NUL              HAAKJE AANWEZIG ?
         BE    DDCBOK                  NEE
         CLI   0(R11),C','             SCHEIDINGSTEKEN
         BNE   DDCB070                 TEST )
         SUBR  DELIMIT                 VERVOLG SUBPARAMETERS
         B     *+4(R15)                RETURNCODES
         B     DDCB060                 OK.
         B     INCRLINE                NO CONTINUATION
DDCB060  EQU   *
         SUBR  DCBATTR                 TEST ATTRIBUTES
         B     *+4(R15)                RETURNCODES
         B     DDCB070                 OK
         B     DDCBERR                 ERROR IN ATTRIBUTES
DDCB070  EQU   *
         CLI   0(R11),C')'             ) AANWEIZG ?
         BNE   DDCBERR                 ERROR IN ATTRIBUTES
         LA    R11,1(R11)              VERHOOG POINTER
         SP    BRFLAG,EEN              RESET FLAG
DDCBOK   EQU   *
         B     DNEXTKEY                VOLGENDE KEYWORD
DDCBERR  EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       DDNAME PARM IN DDKAART ?                                    /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DDNAME   EQU   *
         CLC   0(7,R11),=C'DDNAME='    DDNAME= ?
         BNE   DDEST                   NEE.
         LA    R11,7(R11)              VERHOOG POINTER
         CLI   FDDNAME,X'FF'           REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDDNAME,X'FF'           VUL FLAG
         LA    R6,ERRDDNM              FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DDNAM000                NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DDNAMOK                 JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DDNAMOK                 JA.
DDNAM000 EQU   *
         MVI   DFLAGS,X'FF'            VUL DFLAGS
         MVC   DFLAGS+1(FDLEN-1),DFLAGS
         XC    DCBDIAGN,DCBDIAGN       DCB=DIAGNS TOEGESTAAN
         XC    DCBBLKSZ,DCBBLKSZ       DCB=BLKSIZE TOEGESTAAN
         XC    DCBBUFNO,DCBBUFNO       DCB=BUFNO TOEGESTAAN
         SUBR  NAMES                   TEST OP EEN NAAM
         B     *+4(R15)                RETURNCODES
         B     DDNAMOK                 OK.
         B     DDNAMERR                ERROR.
DDNAMOK  EQU   *
         B     DNEXTKEY                OK.
DDNAMERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         B     INCRLINE                VOLGENDE REGEL
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       DEST PARM IN DDKAART ?                                      /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DDEST    EQU   *
         CLC   0(5,R11),=C'DEST='      DEST= ?
         BNE   DDISP                   NEE.
         LA    R11,5(R11)              VERHOOG POINTER
         CLI   FDDEST,X'FF'            REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDDEST,X'FF'            VUL FLAG
         LA    R6,ERRDDEST             FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DDEST000                NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DDESTOK                 JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DDESTOK                 JA.
DDEST000 EQU   *
         SUBR  NAMES                   TEST OP EEN NAAM
         B     *+4(R15)                RETURNCODES
         B     DDESTOK                 OK.
         B     DDESTERR                ERROR IN DEST
DDESTOK  EQU   *
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
DDESTERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       DISP PARM IN DDKAART ?                                      /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DDISP    EQU   *
         CLC   0(5,R11),=C'DISP='      DISP= ?
         BNE   DDLM                    NEE.
         LA    R11,5(R11)              VERHOOG POINTER
         CLI   FDDISP,X'FF'            REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDDISP,X'FF'            SET FLAG
         LA    R6,ERRDDISP             FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DDISP000                NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DDISPOK                 JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DDISPOK                 JA.
DDISP000 EQU   *
         CLI   0(R11),C'('             ( ?
         BNE   DDISP010                NEE.
         LA    R11,1(R11)              VERHOOG POINTER
         AP    BRFLAG,EEN              SET FLAG
DDISP010 EQU   *
         CLC   0(3,R11),=C'NEW'        NEW ?
         BNE   DDISP020                NEE
         LA    R11,3(R11)              VERHOOG POINTER
         B     DDISP050
DDISP020 EQU   *
         CLC   0(3,R11),=C'OLD'        OLD ?
         BNE   DDISP030                NEE
         LA    R11,3(R11)              VERHOOG POINTER
         B     DDISP050
DDISP030 EQU   *
         CLC   0(3,R11),=C'SHR'        SHR ?
         BNE   DDISP035                NEE
         LA    R11,3(R11)              VERHOOG POINTER
         B     DDISP050
DDISP035 EQU   *
         CLC   0(5,R11),=C'SHARE'      SHARE ?
         BNE   DDISP040                NEE
         LA    R11,5(R11)              VERHOOG POINTER
         B     DDISP050
DDISP040 EQU   *
         CLC   0(3,R11),=C'MOD'        MOD ?
         BNE   DDISP045                NEE
         LA    R11,3(R11)              VERHOOG POINTER
         B     DDISP050
DDISP045 EQU   *
         CLI   0(R11),C','             DEFAULT ?
         BNE   DDISPERR                ERROR
         CP    BRFLAG,NUL              ( AANWEZIG ?
         BE    DDISPERR                NEE. ERROR.
DDISP050 EQU   *
         CP    BRFLAG,NUL              ( AANWEZIG ?
         BE    DDISPOK
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DDISP140
         SUBR  DELIMIT                 VOLGENDE SUBPARAMETER ?
         B     *+4(R15)                RETURNCODES
         B     DDISP055                OK
         B     INCRLINE                NEE.
DDISP055 EQU   *
         CLC   0(6,R11),=C'DELETE'     DELETE ?
         BNE   DDISP060                NEE
         LA    R11,6(R11)              VERHOOG POINTER
         B     DDISP100
DDISP060 EQU   *
         CLC   0(4,R11),=C'KEEP'       KEEP ?
         BNE   DDISP070                NEE
         LA    R11,4(R11)              VERHOOG POINTER
         B     DDISP100
DDISP070 EQU   *
         CLC   0(4,R11),=C'PASS'       PASS ?
         BNE   DDISP080                NEE
         LA    R11,4(R11)              VERHOOG POINTER
         B     DDISP100
DDISP080 EQU   *
         CLC   0(5,R11),=C'CATLG'      CATLG ?
         BNE   DDISP090                NEE
         LA    R11,5(R11)              VERHOOG POINTER
         B     DDISP100
DDISP090 EQU   *
         CLC   0(7,R11),=C'UNCATLG'    UNCATLG ?
         BNE   DDISP095                NEE
         LA    R11,7(R11)              VERHOOG POINTER
         B     DDISP100
DDISP095 EQU   *
         CLI   0(R11),C','             DEFAULT ?
         BNE   DDISPERR                NEE
DDISP100 EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DDISP140
         SUBR  DELIMIT                 VOLGENDE SUBPARAMETER ?
         B     *+4(R15)                RETURNCODES
         B     DDISP105                OK
         B     INCRLINE                NEE.
DDISP105 EQU   *
         CLC   0(6,R11),=C'DELETE'     DELETE ?
         BNE   DDISP110                NEE
         LA    R11,6(R11)              VERHOOG POINTER
         B     DDISP140
DDISP110 EQU   *
         CLC   0(4,R11),=C'KEEP'       KEEP ?
         BNE   DDISP120                NEE
         LA    R11,4(R11)              VERHOOG POINTER
         B     DDISP140
DDISP120 EQU   *
         CLC   0(5,R11),=C'CATLG'      CATLG ?
         BNE   DDISP130                NEE
         LA    R11,4(R11)              VERHOOG POINTER
         B     DDISP140
DDISP130 EQU   *
         CLC   0(7,R11),=C'UNCATLG'    UNCATLG ?
         BNE   DDISPERR                NEE
         LA    R11,7(R11)              VERHOOG POINTER
DDISP140 EQU   *
         CLI   0(R11),C')'             ) AANWEZIG ?
         BNE   DDISPERR
         LA    R11,1(R11)              VERHOOG POINTER
         SP    BRFLAG,EEN              RESET FLAG
DDISPOK  EQU   *
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
DDISPERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       DLM PARM IN DDKAART ?                                       /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DDLM     EQU   *
         CLC   0(4,R11),=C'DLM='       DLM= ?
         BNE   DDSID                   NEE.
         LA    R11,4(R11)              VERHOOG POINTER
         CLI   FDDLM,X'FF'             REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDDLM,X'FF'             VUL FLAG
         LA    R6,ERRDDLM              FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DDLM000                 NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DDLMOK                  JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DDLMOK                  JA.
DDLM000  EQU   *
         CLI   0(R11),C''''            ' AANWEZIG ?
         BE    DDLM030                 JA.
         LA    R7,=C'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789@#$ '
         SUBR  TESTCHAR                TEST CHARACTER
         B     *+4(R15)                RETURNCODES
         B     DDLM010                 OK
         B     DDLMERR                 ERROR.
DDLM010  EQU   *
         MVC   DLM(1),0(R1)            EERSTE CHARACTER
         SUBR  TESTCHAR                TEST CHARACTER
         B     *+4(R15)                RETURNCODES
         B     DDLM020                 OK
         B     DDLMERR                 ERROR.
DDLM020  EQU   *
         MVC   DLM+1(1),0(R1)          EERSTE CHARACTER
         B     DDLMOK                  OK
DDLM030  EQU   *
         LA    R11,1(R11)              VERHOOG POINTER
         CLC   0(2,R11),=C''''''       '' AANWEZIG ?
         BNE   DDLM040                 NEE.
         MVI   DLM,C''''               EERSTE CHARACTER
         LA    R11,2(R11)              VERHOOG POINTER
         B     DDLM060
DDLM040  EQU   *
         CLC   0(2,R11),=C'&&&&'       && AANWEZIG ?
         BNE   DDLM050                 NEE.
         MVI   DLM,C'&&'               EERSTE CHARACTER
         LA    R11,2(R11)              VERHOOG POINTER
         B     DDLM060
DDLM050  EQU   *
         CLI   0(R11),C''''            EINDE STRING ?
         BE    DDLMERR                 ERROR
         MVC   DLM(1),0(R11)           EERSTE CHARACTER
         LA    R11,1(R11)              VERHOOG POINTER
DDLM060  EQU   *
         CLC   0(2,R11),=C''''''       '' AANWEZIG ?
         BNE   DDLM070                 NEE.
         MVI   DLM+1,C''''             TWEEDE CHARACTER
         LA    R11,2(R11)              VERHOOG POINTER
         B     DDLM090
DDLM070  EQU   *
         CLC   0(2,R11),=C'&&&&'       && AANWEZIG ?
         BNE   DDLM080                 NEE.
         MVI   DLM+1,C'&&'             TWEEDE CHARACTER
         LA    R11,2(R11)              VERHOOG POINTER
         B     DDLM090
DDLM080  EQU   *
         CLI   0(R11),C''''            EINDE STRING ?
         BE    DDLMERR                 ERROR
         MVC   DLM+1(1),0(R11)         TWEEDE CHARACTER
         LA    R11,1(R11)              VERHOOG POINTER
DDLM090  EQU   *
         CLI   0(R11),C''''            EINDE STRING ?
         BNE   DDLMERR                 NEE.
         LA    R11,1(R11)              VERHOOG POINTER
DDLMOK   EQU   *
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
DDLMERR  EQU   *
         MVC   DLM,=C'/*'              DEFAULT DELIMITER
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       DSID PARM IN DDKAART ?                                      /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DDSID    EQU   *
         CLC   0(5,R11),=C'DSID='      DSID= ?
         BNE   DDSNAME                 NEE.
         CLI   FDDSID,X'FF'            REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDDSID,X'FF'            VUL FLAG
         LA    R6,ERRDDSID             KEYWORD NIET TOEGESTAAN
         LA    R11,5(R11)              VERHOOG POINTER
         SUBR  ERRMESS                 GEEF MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       DSNAME PARM IN DDKAART ?                                    /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DDSNAME  EQU   *
         CLC   0(4,R11),=C'DSN='       DSN= ?
         BE    DDSN                    JA.
         CLC   0(7,R11),=C'DSNAME='    DSNAME= ?
         BNE   DFCB                    NEE.
         LA    R11,3(R11)              VERHOOG POINTER
DDSN     EQU   *
         LA    R11,4(R11)              VERHOOG POINTER
         CLI   FDDSN,X'FF'             REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDDSN,X'FF'             VUL FLAG
         LA    R6,ERRDDSN              FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DDSN000                 NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DDSNOK                  JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DDSNOK                  JA.
DDSN000  EQU   *
         SUBR  DSNTEST                 TEST OP DSNAME
         B     *+4(R15)                RETURNCODES
         B     DDSNOK                  OK
         B     DDSNERR                 ERROR
DDSNOK   EQU   *
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
DDSNERR  EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       FCB PARM IN DDKAART ?                                       /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DFCB     EQU   *
         CLC   0(4,R11),=C'FCB='       FCB= ?
         BNE   DFLASH                  NEE.
         LA    R11,4(R11)              VERHOOG POINTER
         CLI   FDFCB,X'FF'             REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDFCB,X'FF'             VUL FLAG
         LA    R6,ERRDFCB              FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DFCB000                 NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DFCBOK                  JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DFCBOK                  JA.
DFCB000  EQU   *
         CLI   0(R11),C'('             (
         BNE   DFCB010                 NEE
         AP    BRFLAG,EEN              SET FLAG
         LA    R11,1(R11)              VERHOOG POINTER
DFCB010  EQU   *
         SUBR  NAMES                   TEST OP FCB-NAAM
         B     *+4(R15)                RETURNCODES
         B     DFCB020                 OK
         B     DFCBERR                 ERROR
DFCB020  EQU   *
         CP    BRFLAG,NUL              ( AANWEZIG ?
         BE    DFCBOK                  NEE
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DFCB040
         SUBR  DELIMIT                 VERVOLG MET SUBPARAMETERS
         B     *+4(R15)                RETURNCODES
         B     DFCB025                 OK
         B     INCRLINE                NO CONTINUATION
DFCB025  EQU   *
         CLC   0(5,R11),=C'ALIGN'      ALIGN ?
         BNE   DFCB030                 NEE
         LA    R11,5(R11)              VERHOOG POINTER
         B     DFCB040                 ) AANWEZIG ?
DFCB030  EQU   *
         CLC   0(6,R11),=C'VERIFY'     VERIFY ?
         BNE   DFCBERR                 NEE
         LA    R11,5(R11)              VERHOOG POINTER
DFCB040  EQU   *
         CLI   0(R11),C')'             ) AANWEZIG ?
         BNE   DFCBERR                 ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         SP    BRFLAG,EEN              RESET FLAG
DFCBOK   EQU   *
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
DFCBERR  EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       FLASH PARM IN DDKAART ?                                     /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DFLASH   EQU   *
         CLC   0(6,R11),=C'FLASH='     FLASH= ?
         BNE   DFREE                   NEE.
         CLI   FDFLASH,X'FF'           REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDFLASH,X'FF'           VUL FLAG
         LA    R6,ERRDFLAS             KEYWORD NIET TOEGESTAAN
         LA    R11,6(R11)              VERHOOG POINTER
         SUBR  ERRMESS                 GEEF MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       FREE PARM IN DDKAART ?                                      /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DFREE    EQU   *
         CLC   0(5,R11),=C'FREE='      FREE= ?
         BNE   DHOLD                   NEE.
         LA    R11,5(R11)              VERHOOG POINTER
         CLI   FDFREE,X'FF'            REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDFREE,X'FF'            VUL FLAG
         LA    R6,ERRDFREE             FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DFREE000                NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DFREEOK                 JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DFREEOK                 JA.
DFREE000 EQU   *
         CLC   0(3,R11),=C'END'        END ?
         BNE   DFREE010                NEE
         LA    R11,3(R11)              VERHOOG POINTER
         B     DFREEOK                 OK
DFREE010 EQU   *
         CLC   0(5,R11),=C'CLOSE'      CLOSE ?
         BNE   DFREEERR                NEE
         LA    R11,5(R11)              VERHOOG POINTER
DFREEOK  EQU   *
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
DFREEERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       HOLD PARM IN DDKAART ?                                      /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DHOLD    EQU   *
         CLC   0(5,R11),=C'HOLD='      HOLD= ?
         BNE   DLABEL                  NEE.
         LA    R11,5(R11)              VERHOOG POINTER
         CLI   FDHOLD,X'FF'            REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDHOLD,X'FF'            VUL FLAG
         LA    R6,ERRDHOLD             FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DHOLD000                NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DHOLDOK                 JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DHOLDOK                 JA.
DHOLD000 EQU   *
         CLC   0(3,R11),=C'YES'        END ?
         BNE   DHOLD010                NEE
         LA    R11,3(R11)              VERHOOG POINTER
         B     DHOLDOK                 OK
DHOLD010 EQU   *
         CLC   0(2,R11),=C'NO'         NO ?
         BNE   DHOLDERR                NEE
         LA    R11,2(R11)              VERHOOG POINTER
DHOLDOK  EQU   *
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
DHOLDERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       LABEL PARM IN DDKAART ?                                     /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DLABEL   EQU   *
         CLC   0(6,R11),=C'LABEL='     LABEL= ?
         BNE   DMODIFY                 NEE.
         LA    R11,6(R11)              VERHOOG POINTER
         CLI   FDLABEL,X'FF'           REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDLABEL,X'FF'           VUL FLAG
         LA    R6,ERRDLABL             FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DLAB000                 NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DLABOK                  JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DLABOK                  JA.
DLAB000  EQU   *
         CLI   0(R11),C'('             (
         BNE   DLAB010                 NEE
         AP    BRFLAG,EEN              SET FLAG
         LA    R11,1(R11)              VERHOOG POINTER
         CLI   0(R11),C','             NO SEQ. NUMBER
         BE    DLAB030                 JA.
DLAB010  EQU   *
         SUBR  NUMBER                  SEQUENCE NUMBER
         B     *+4(R15)                RETURNCODES
         B     DLAB020                 OK
         B     DLAB160                 TEST EXPDT EN RETPD
DLAB020  EQU   *
         C     R1,=F'1'                NIET KLEINER DAN 1
         BL    DLABERR                 ERROR
         C     R1,=F'9999'             NIET GROTER DAN 9999
         BH    DLABERR
         CP    BRFLAG,NUL              ( AANWEZIG ?
         BE    DLABOK                  NEE
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DLAB200                 TEST )
DLAB030  EQU   *
         SUBR  DELIMIT                 VERVOLG SUBPARAMETERS ?
         B     *+4(R15)                RETURNCODES
         B     DLAB035                 JA.
         B     INCRLINE                NEE.
DLAB035  EQU   *
         CLI   0(R11),C','             , AANWEZIG
         BE    DLAB110                 JA
         CLC   0(2,R11),=C'SL'         SL ?
         BNE   DLAB040                 NEE.
         LA    R11,2(R11)              VERHOOG POINTER
         B     DLAB110                 TEST ,
DLAB040  EQU   *
         CLC   0(3,R11),=C'SUL'        SUL ?
         BNE   DLAB050                 NEE.
         LA    R11,3(R11)              VERHOOG POINTER
         B     DLAB110                 TEST ,
DLAB050  EQU   *
         CLC   0(2,R11),=C'AL'         AL ?
         BNE   DLAB060                 NEE.
         LA    R11,2(R11)              VERHOOG POINTER
         B     DLAB110                 TEST ,
DLAB060  EQU   *
         CLC   0(3,R11),=C'AUL'        AUL ?
         BNE   DLAB070                 NEE.
         LA    R11,3(R11)              VERHOOG POINTER
         B     DLAB110                 TEST ,
DLAB070  EQU   *
         CLC   0(3,R11),=C'NSL'        NSL ?
         BNE   DLAB080                 NEE.
         LA    R11,3(R11)              VERHOOG POINTER
         B     DLAB110                 TEST ,
DLAB080  EQU   *
         CLC   0(2,R11),=C'NL'         NL ?
         BNE   DLAB090                 NEE.
         LA    R11,2(R11)              VERHOOG POINTER
         B     DLAB110                 TEST ,
DLAB090  EQU   *
         CLC   0(3,R11),=C'BLP'        BLP ?
         BNE   DLAB100                 NEE.
         LA    R11,3(R11)              VERHOOG POINTER
         B     DLAB110                 TEST ,
DLAB100  EQU   *
         CLC   0(2,R11),=C'LTM'        LTM ?
         BNE   DLAB160                 RETPD OD EXPDT ?
         LA    R11,3(R11)              VERHOOG POINTER
DLAB110  EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DLAB200                 TEST )
         SUBR  DELIMIT                 VERVOLG SUBPARAMETERS ?
         B     *+4(R15)                RETURNCODES
         B     DLAB115                 JA.
         B     INCRLINE                NEE.
DLAB115  EQU   *
         CLI   0(R11),C','             , AANWEZIG
         BE    DLAB130                 JA
         CLC   0(8,R11),=C'PASSWORD'   PASSWORD ?
         BNE   DLAB120                 NEE.
         LA    R11,8(R11)              VERHOOG POINTER
         B     DLAB130                 TEST ,
DLAB120  EQU   *
         CLC   0(8,R11),=C'NOPWREAD'   NOPWREAD ?
         BNE   DLAB160                 TEST RETPD EN EXPDT
         LA    R11,8(R11)              VERHOOG POINTER
DLAB130  EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DLAB200                 TEST )
         SUBR  DELIMIT                 VERVOLG SUBPARAMETERS ?
         B     *+4(R15)                RETURNCODES
         B     DLAB135                 JA.
         B     INCRLINE                NEE.
DLAB135  EQU   *
         CLC   0(2,R11),=C'IN'         IN ?
         BNE   DLAB140                 NEE.
         LA    R11,2(R11)              VERHOOG POINTER
         B     DLAB150                 TEST ,
DLAB140  EQU   *
         CLC   0(3,R11),=C'OUT'        OUT ?
         BNE   DLAB160                 TEST RETPD EN EXPDT
         LA    R11,3(R11)              VERHOOG POINTER
DLAB150  EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DLAB200                 TEST )
         SUBR  DELIMIT                 VERVOLG SUBPARAMETERS ?
         B     *+4(R15)                RETURNCODES
         B     DLAB160                 JA.
         B     INCRLINE                NEE.
DLAB160  EQU   *
         CLC   0(6,R11),=C'RETPD='     RETPD= ?
         BNE   DLAB170                 NEE.
         LA    R11,6(R11)              VERHOOG POINTER
         B     DLAB180                 TEST GETAL
DLAB170  EQU   *
         CLC   0(6,R11),=C'EXPDT='     EXPDT= ?
         BNE   DLABERR                 ERROR
         LA    R11,6(R11)              VERHOOG POINTER
DLAB180  EQU   *
         SUBR  NUMBER                  TEST GETAL
         B     *+4(R15)                RETURNCODES
         B     DLAB190                 TEST )
         B     DLABERR                 ERROR
DLAB190  EQU   *
         CP    BRFLAG,NUL              ) AANWEZIG
         BE    DLABOK                  NEE
DLAB200  EQU   *
         CLI   0(R11),C')'             ) AANWEZIG ?
         BNE   DLABERR                 ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         SP    BRFLAG,EEN              RESET FLAG
DLABOK   EQU   *
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
DLABERR  EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       MODIFY PARM IN DDKAART ?                                    /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DMODIFY  EQU   *
         CLC   0(7,R11),=C'MODIFY='    MODIFY= ?
         BNE   DMSVGP                  NEE.
         CLI   FDMODIFY,X'FF'          REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDMODIFY,X'FF'          VUL FLAG
         LA    R6,ERRDMODI             KEYWORD NIET TOEGESTAAN
         LA    R11,7(R11)              VERHOOG POINTER
         SUBR  ERRMESS                 GEEF MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       MSVGP PARM IN DDKAART ?                                     /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DMSVGP   EQU   *
         CLC   0(6,R11),=C'MSVGP='     MSVGP= ?
         BNE   DOUTLIM                 NEE.
         CLI   FDMSVGP,X'FF'           REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDMSVGP,X'FF'           VUL FLAG
         LA    R6,ERRDMSVG             KEYWORD NIET TOEGESTAAN
         LA    R11,6(R11)              VERHOOG POINTER
         SUBR  ERRMESS                 GEEF MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       OUTLIM PARM IN DDKAART ?                                    /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DOUTLIM  EQU   *
         CLC   0(7,R11),=C'OUTLIM='    OUTLIM= ?
         BNE   DPROTECT                NEE.
         LA    R11,7(R11)              VERHOOG POINTER
         CLI   FDOUTLIM,X'FF'          REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDOUTLIM,X'FF'          VUL FLAG
         LA    R6,ERRDOUTL             FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DOUTL000                NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DOUTLOK                 JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DOUTLOK                 JA.
DOUTL000 EQU   *
         SUBR  NUMBER                  AANTAL REGELS
         B     *+4(R15)                RETURNCODES
         B     DOUTLOK                 OK
         B     DOUTLERR                ERROR
DOUTLOK  EQU   *
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
DOUTLERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       PROTECT PARM IN DDKAART ?                                   /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DPROTECT EQU   *
         CLC   0(8,R11),=C'PROTECT='   PROTECT= ?
         BNE   DQNAME                  NEE.
         CLI   FDPROTEC,X'FF'          REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDPROTEC,X'FF'          VUL FLAG
         LA    R6,ERRDPROT             KEYWORD NIET TOEGESTAAN
         LA    R11,8(R11)              VERHOOG POINTER
         SUBR  ERRMESS                 GEEF MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       QNAME PARM IN DDKAART ?                                     /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DQNAME   EQU   *
         CLC   0(6,R11),=C'QNAME='     QNAME= ?
         BNE   DSPACE                  NEE.
         LA    R11,6(R11)              VERHOOG POINTER
         CLI   FDQNAME,X'FF'           REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDQNAME,X'FF'           VUL FLAG
         LA    R6,ERRDQNAM             FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DQNAM000                NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DQNAMOK                 JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DQNAMOK                 JA.
DQNAM000 EQU   *
         SUBR  NAMES                   QNAME
         B     *+4(R15)                RETURNCODES
         B     DQNAMOK                 OK
         B     DQNAMERR                ERROR
DQNAMOK  EQU   *
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
DQNAMERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       SPACE PARM IN DDKAART ?                                     /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DSPACE   EQU   *
         CLC   0(6,R11),=C'SPACE='     SPACE= ?
         BNE   DSUBSYS                 NEE.
         LA    R11,6(R11)              VERHOOG POINTER
         CLI   FDSPACE,X'FF'           REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDSPACE,X'FF'           VUL FLAG
         LA    R6,ERRDSPAC             FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DSPAC000                NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DSPACOK                 JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DSPACOK                 JA.
DSPAC000 EQU   *
         CLI   0(R11),C'('             ( AANWEZIG ?
         BNE   DSPACERR                ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         AP    BRFLAG,EEN              HAAKJE
         CLC   0(3,R11),=C'TRK'        TRK ?
         BNE   DSPAC010                NEE
         LA    R11,3(R11)              VERHOOG POINTER
         B     DSPAC035                TEST QUANTITY
DSPAC010 EQU   *
         CLC   0(3,R11),=C'CYL'        CYL ?
         BNE   DSPAC020                NEE
         LA    R11,3(R11)              VERHOOG POINTER
         B     DSPAC035                TEST QUANTITY
DSPAC020 EQU   *
         CLC   0(5,R11),=C'ABSTR'      ABSTR ?
         BNE   DSPAC030                NEE
         LA    R11,5(R11)              VERHOOG POINTER
         B     DSPAC160                TEST REST
DSPAC030 EQU   *
         SUBR  NUMBER                  TEST BLOCKSIZE
         B     *+4(R15)                RETURNCODES
         B     DSPAC035                OK
         B     DSPACERR                ERROR
DSPAC035 EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DSPACERR                ERROR
         SUBR  DELIMIT                 VERVOLG SUBPARAMETERS ?
         B     *+4(R15)                RETURNCODES
         B     DSPAC040                JA.
         B     INCRLINE                NEE
DSPAC040 EQU   *
         CLI   0(R11),C'('             ( AANWEZIG ?
         BNE   DSPAC050                NEE
         LA    R11,1(R11)              VERHOOG POINTER
         AP    BRFLAG,EEN              SET BRFLAG
DSPAC050 EQU   *
         SUBR  NUMBER                  PRIMARY QUANTITY ?
         B     *+4(R15)                RETURNCODES
         B     DSPAC060                OK
         B     DSPACERR                ERROR
DSPAC060 EQU   *
         CP    BRFLAG,EEN              ( AANWEZIG ?
         BE    DSPAC090                NEE. TEST ,
         CLI   0(R11),C','             , AANWEZIG
         BNE   DSPAC080                TEST )
         SUBR  DELIMIT                 VERVOLG SUBPARAMETERS ?
         B     *+4(R15)                RETURNCODES
         B     DSPAC065                JA.
         B     INCRLINE                NEE
DSPAC065 EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BE    DSPAC070                JA
         SUBR  NUMBER                  SECONDARY QUANTITY ?
         B     *+4(R15)                RETURNCODES
         B     DSPAC070                JA
         B     DSPACERR                ERROR
DSPAC070 EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DSPAC080                TEST )
         SUBR  DELIMIT                 VERVOLG SUBPARAMETERS ?
         B     *+4(R15)                RETURNCODES
         B     DSPAC075                JA.
         B     INCRLINE                NEE
DSPAC075 EQU   *
         SUBR  NUMBER                  INDEX OF DIRECTORY
         B     *+4(R15)                RETURNCODES
         B     DSPAC080                OK. TEST )
         B     DSPACERR                ERROR
DSPAC080 EQU   *
         CLI   0(R11),C')'             ) AANWEZIG ?
         BNE   DSPACERR                ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         SP    BRFLAG,EEN              SET FLAG
DSPAC090 EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DSPAC150                TEST )
         SUBR  DELIMIT                 VERVOLG SUBPARAMETERS ?
         B     *+4(R15)                RETURNCODES
         B     DSPAC095                JA.
         B     INCRLINE                NEE
DSPAC095 EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BE    DSPAC100                JA.
         CLC   0(4,R11),=C'RLSE'       RLSE ?
         BNE   DSPAC110                NEE. CONTIG/MXIG/ALX/
         LA    R11,4(R11)              VERHOOG POINTER
DSPAC100 EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DSPAC150                TEST )
         SUBR  DELIMIT                 VERVOLG SUBPARAMETERS ?
         B     *+4(R15)                RETURNCODES
         B     DSPAC110                JA.
         B     INCRLINE                NEE
DSPAC110 EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BE    DSPAC140                JA.
         CLC   0(6,R11),=C'CONTIG'     CONTIG ?
         BNE   DSPAC120                NEE.
         LA    R11,6(R11)              VERHOOG POINTER
         B     DSPAC140                TEST ,
DSPAC120 EQU   *
         CLC   0(4,R11),=C'MXIG'       MXIG ?
         BNE   DSPAC130                NEE.
         LA    R11,4(R11)              VERHOOG POINTER
         B     DSPAC140                TEST ,
DSPAC130 EQU   *
         CLC   0(3,R11),=C'ALX'        ALX ?
         BNE   DSPACERR                ERROR
         LA    R11,3(R11)              VERHOOG POINTER
         B     DSPAC140                TEST ,
DSPAC140 EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DSPAC150                TEST )
         SUBR  DELIMIT                 VERVOLG SUBPARAMETERS ?
         B     *+4(R15)                RETURNCODES
         B     DSPAC145                JA.
         B     INCRLINE                NEE
DSPAC145 EQU   *
         CLC   0(5,R11),=C'ROUND'      ROUND ?
         BNE   DSPACERR                ERROR
         LA    R11,5(R11)              VERHOOG POINTER
DSPAC150 EQU   *
         CLI   0(R11),C')'             TEST )
         BNE   DSPACERR                ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         B     DSPACOK                 OK
DSPAC160 EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DSPACERR                ERROR
         SUBR  DELIMIT                 VERVOLG SUBPARAMETERS ?
         B     *+4(R15)                RETURNCODES
         B     DSPAC165                JA.
         B     INCRLINE                NEE
DSPAC165 EQU   *
         CLI   0(R11),C'('             ( AANWEZIG ?
         BNE   DSPACERR                NEE
         LA    R11,1(R11)              VERHOOG POINTER
         AP    BRFLAG,EEN              SET FLAG
         SUBR  NUMBER                  PRIMARY QUANTITY ?
         B     *+4(R15)                RETURNCODES
         B     DSPAC170                OK
         B     DSPACERR                ERROR
DSPAC170 EQU   *
         CLI   0(R11),C','             , AANWEZIG
         BNE   DSPACERR                ERROR
         SUBR  DELIMIT                 VERVOLG SUBPARAMETERS ?
         B     *+4(R15)                RETURNCODES
         B     DSPAC175                JA.
         B     INCRLINE                NEE
DSPAC175 EQU   *
         SUBR  NUMBER                  ADDRESS ?
         B     *+4(R15)                RETURNCODES
         B     DSPAC180                JA
         B     DSPACERR                ERROR
DSPAC180 EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DSPAC190                TEST )
         SUBR  DELIMIT                 VERVOLG SUBPARAMETERS ?
         B     *+4(R15)                RETURNCODES
         B     DSPAC185                JA.
         B     INCRLINE                NEE
DSPAC185 EQU   *
         SUBR  NUMBER                  INDEX OF DIRECTORY
         B     *+4(R15)                RETURNCODES
         B     DSPAC190                OK. TEST )
         B     DSPACERR                ERROR
DSPAC190 EQU   *
         CLI   0(R11),C')'             ) AANWEZIG ?
         BNE   DSPACERR                ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         CP    BRFLAG,EEN              NOG MEER HAAKJES ?
         BE    DSPACOK                 NEE.
         SP    BRFLAG,EEN              SET FLAG.
         CLI   0(R11),C')'             ) AANWEZIG ?
         BNE   DSPACERR                ERROR
         LA    R11,1(R11)              VERHOOG POINTER
DSPACOK  EQU   *
         SP    BRFLAG,EEN              RESET FLAG
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
DSPACERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       SUBSYS PARM IN DDKAART ?                                    /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DSUBSYS  EQU   *
         CLC   0(7,R11),=C'SUBSYS='    SUBSYS= ?
         BNE   DSYSOUT                 NEE.
         CLI   FDSUBSYS,X'FF'          REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDSUBSYS,X'FF'          VUL FLAG
         LA    R6,ERRDSUBS             KEYWORD NIET TOEGESTAAN
         LA    R11,7(R11)              VERHOOG POINTER
         SUBR  ERRMESS                 GEEF MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       SYSOUT PARM IN DDKAART ?                                    /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DSYSOUT  EQU   *
         CLC   0(7,R11),=C'SYSOUT='    SYSOUT= ?
         BNE   DTERM                   NEE.
         LA    R11,7(R11)              VERHOOG POINTER
         CLI   FDSYSUT,X'FF'           REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDSYSUT,X'FF'           VUL FLAG
         LA    R6,ERRDSYST             FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DSYST000                NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DSYSTOK                 JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DSYSTOK                 JA.
DSYST000 EQU   *
         CLI   0(R11),C'('             ( AANWEZIG ?
         BNE   DSYST010                NEE.
         LA    R11,1(R11)              VERHOOG POINTER
         AP    BRFLAG,EEN              SET FLAG
DSYST010 EQU   *
         LA    R7,=C'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789* '
         SUBR  TESTCHAR                A ===> Z OF 0 ===> 9 OF * ?
         B     *+4(R15)                RETURNCODES
         B     DSYST020                JA.
         B     DSYSTERR                ERROR.
DSYST020 EQU   *
         CP    BRFLAG,NUL              ( AANWEZIG ?
         BE    DSYSTOK                 NEE
         CLI   0(R11),C','             , AANWEZIG
         BNE   DSYST040                TEST )
         SUBR  DELIMIT                 VERVOLG MET SUBPARAMETERS ?
         B     *+4(R15)                RETURNCODES
         B     DSYST025                JA.
         B     INCRLINE                NO CONTINUTION
DSYST025 EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BE    DSYST030                JA
         SUBR  NAMES                   PROGRAMMA NAAM
         B     *+4(R15)                RETURNCODES
         B     DSYST030                TEST ,
         B     DSYSTERR                ERROR
DSYST030 EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DSYST040                NEE
         SUBR  DELIMIT                 VERVOLG MET SUBPARAMETERS ?
         B     *+4(R15)                RETURNCODES
         B     DSYST040                JA.
         B     INCRLINE                NO CONTINUTION
DSYST040 EQU   *
         CLI   0(R11),C')'             ) AANWEZIG ?
         BNE   DSYST050                NEE.
         SP    BRFLAG,EEN              RESET FLAG
         LA    R11,1(R11)              VERHOOG POINTER
         B     DSYSTOK
DSYST050 EQU   *
         LA    R11,1(R11)              VERHOOG POINTER
         C     R11,ENDLINE             EINDE REGEL
         BE    DSYSTERR                JA. ERROR
         B     DSYST040                NEE.
DSYSTOK  EQU   *
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
DSYSTERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       TERM PARM IN DDKAART ?                                      /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DTERM    EQU   *
         CLC   0(5,R11),=C'TERM='      TERM= ?
         BNE   DUCS                    NEE.
         LA    R11,5(R11)              VERHOOG POINTER
         CLI   FDTERM,X'FF'            REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDTERM,X'FF'            VUL FLAG
         LA    R6,ERRDTERM             FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DTERM000                NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DTERMOK                 JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DTERMOK                 JA.
DTERM000 EQU   *
         CLC   0(2,R11),=C'TS'         TS ?
         BNE   DTERMERR                ERROR
         LA    R11,2(R11)              VERHOOG POINTER
DTERMOK  EQU   *
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
DTERMERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       UCS PARM IN DDKAART ?                                       /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DUCS     EQU   *
         CLC   0(4,R11),=C'UCS='       UCS= ?
         BNE   DUNIT                   NEE.
         LA    R11,4(R11)              VERHOOG POINTER
         CLI   FDUCS,X'FF'             REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDUCS,X'FF'             VUL FLAG
         LA    R6,ERRDUCS              FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DUCS000                 NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DUCSOK                  JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DUCSOK                  JA.
DUCS000  EQU   *
         CLI   0(R11),C'('             ( AANWEZIG ?
         BNE   DUCS010                 NEE
         LA    R11,1(R11)              VERHOOG POINTER
         AP    BRFLAG,EEN              SET FLAG
DUCS010  EQU   *
         SUBR  NAMES                   CHARACTER SET CODE
         B     *+4(R15)                RETURNCODES
         B     DUCS020                 OK
         B     DUCSERR                 ERROR
DUCS020  EQU   *
         CP    BRFLAG,NUL              ( AANWEZIG ?
         BE    DUCSOK                  NEE.
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DUCS050                 TEST )
         SUBR  DELIMIT                 VERVOLG MET SUBPARAMETERS
         B     *+4(R15)                RETURNCODES
         B     DUCS025                 JA
         B     INCRLINE                NO CONTINUATION
DUCS025  EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BE    DUCS030                 JA
         CLC   0(4,R11),=C'FOLD'       FOLD ?
         BNE   DUCS040                 NEE
         LA    R11,4(R11)              VERHOOG POINTER
DUCS030  EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DUCS050                 TEST )
         SUBR  DELIMIT                 VERVOLG MET SUBPARAMETERS
         B     *+4(R15)                RETURNCODES
         B     DUCS040                 JA
         B     INCRLINE                NO CONTINUATION
DUCS040  EQU   *
         CLC   0(6,R11),=C'VERIFY'     VERIFY ?
         BNE   DUCSERR                 ERROR
         LA    R11,6(R11)              VERHOOG POINTER
DUCS050  EQU   *
         CLI   0(R11),C')'             ) AANWEZIG ?
         BNE   DUCSERR                 ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         SP    BRFLAG,EEN              RESET FLAG
DUCSOK   EQU   *
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
DUCSERR  EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       UNIT PARM IN DDKAART ?                                      /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DUNIT    EQU   *
         CLC   0(5,R11),=C'UNIT='      UNIT= ?
         BNE   DVOLUME                 NEE.
         LA    R11,5(R11)              VERHOOG POINTER
         CLI   FDUNIT,X'FF'            REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDUNIT,X'FF'            VUL FLAG
         LA    R6,ERRDUNIT             FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DUNIT000                NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DUNITOK                 JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DUNITOK                 JA.
DUNIT000 EQU   *
         CLC   0(4,R11),=C'AFF='       AFF= ?
         BE    DUNIT060                JA.
         CLI   0(R11),C'('             ( AANWEZIG ?
         BNE   DUNIT010                NEE
         LA    R11,1(R11)              VERHOOG POINTER
         AP    BRFLAG,EEN              SET FLAG
DUNIT010 EQU   *
         SUBR  SKIPPARM                SKIP PARAMETER
         CP    BRFLAG,NUL              ( AANWEZIG ?
         BE    DUNITOK                 NEE
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DUNIT050                NEE. TEST )
         SUBR  DELIMIT                 VERVOLG MET SUBPARAMETERS
         B     *+4(R15)                RETURNCODES
         B     DUNIT015                JA
         B     INCRLINE                NO CONTINUATION
DUNIT015 EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BE    DUNIT030                JA.
         CLI   0(R11),C'P'             P ?
         BNE   DUNIT020                NEE.
         LA    R11,1(R11)              VERHOOG POINTER
         B     DUNIT030                TEST ,
DUNIT020 EQU   *
         SUBR  NUMBER                  UNIT COUNT ?
         B     *+4(R15)                RETURNCODES
         B     DUNIT030                TEST ,
         B     DUNIT040                TEST DEFER
DUNIT030 EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DUNIT050                TEST )
         SUBR  DELIMIT                 VERVOLG MET SUBPARAMETERS
         B     *+4(R15)                RETURNCODES
         B     DUNIT040                JA
         B     INCRLINE                NO CONTINUATION
DUNIT040 EQU   *
         CLC   0(5,R11),=C'DEFER'      DEFER ?
         BNE   DUNITERR                ERROR
         LA    R11,5(R11)              VERHOOG POINTER
DUNIT050 EQU   *
         CLI   0(R11),C')'             ) AANWEZIG ?
         BNE   DUNITERR                ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         SP    BRFLAG,EEN              RESET FLAG
         B     DUNITOK
DUNIT060 EQU   *
         LA    R11,4(R11)              VERHOOG POINTER
         SUBR  NAMES                   TEST OP DDNAME
         B     *+4(R15)                RETURNCODES
         B     DUNITOK                 OK.
         B     DUNITERR
DUNITOK  EQU   *
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
DUNITERR EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       VOLUME PARM IN DDKAART ?                                    /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DVOLUME  EQU   *
         CLC   0(4,R11),=C'VOL='       VOL= ?
         BE    DVOL000                 JA.
         CLC   0(7,R11),=C'VOLUME='    VOLUME= ?
         BNE   DKEYERR                 NEE
         LA    R11,3(R11)              VERHOOG POINTER
DVOL000  EQU   *
         LA    R11,4(R11)              VERHOOG POINTER
         CLI   FDVOLUME,X'FF'          REEDS GESPECIFICEERD ?
         BE    DDOUBLE                 JA.
         MVI   FDVOLUME,X'FF'          VUL FLAG
         LA    R6,ERRDVOL              FOUT IN KEYWORD PARM
         CLI   EPROCFLG,X'FF'          OVERSCHRIJVING ?
         BNE   DVOL010                 NEE
         CLI   0(R11),C','             NULLIFY PARAMETER ?
         BE    DVOLOK                  JA.
         CLI   0(R11),C' '             NULLIFY PARAMETER ?
         BE    DVOLOK                  JA.
DVOL010  EQU   *
         CLI   0(R11),C'('             ( AANWEZIG ?
         BNE   DVOL060                 TEST REF EN SER
         LA    R11,1(R11)              VERHOOG POINTER
         AP    BRFLAG,EEN              SET FLAG
         CLC   0(7,R11),=C'PRIVATE'    PRIVATE ?
         BNE   DVOL020                 NEE. TEST ,
         LA    R11,7(R11)              VERHOOG POINTER
DVOL020  EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DVOL110                 ERROR
         SUBR  DELIMIT                 VERVOLG MET SUBPARAMETERS
         B     *+4(R15)                RETURNCODES
         B     DVOL025                 JA
         B     INCRLINE                NO CONTINUATION
DVOL025  EQU   *
         CLC   0(6,R11),=C'RETAIN'     RETAIN ?
         BNE   DVOL030                 NEE. TEST ,
         LA    R11,6(R11)              VERHOOG POINTER
DVOL030  EQU   *
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DVOL110                 ERROR
         SUBR  DELIMIT                 VERVOLG MET SUBPARAMETERS
         B     *+4(R15)                RETURNCODES
         B     DVOL035                 JA
         B     INCRLINE                NO CONTINUATION
DVOL035  EQU   *
         SUBR  NUMBER                  VOL SEQ NUMBER
         CLI   0(R11),C','             , AANWEZIG
         BNE   DVOL060                 ERROR
         SUBR  DELIMIT                 VERVOLG MET SUBPARAMETERS
         B     *+4(R15)                RETURNCODES
         B     DVOL045                 JA
         B     INCRLINE                NO CONTINUATION
DVOL045  EQU   *
         SUBR  NUMBER                  VOLUME COUNT ?
         B     *+4(R15)                RETURNCODES
         B     DVOL050                 JA.
         B     DVOL060                 NEE.
DVOL050  EQU   *
         CLI   0(R11),C','             , AANWEZIG
         BNE   DVOL110                 NEE.
         SUBR  DELIMIT                 VERVOLG MET SUBPARAMETERS
         B     *+4(R15)                RETURNCODES
         B     DVOL060                 JA
         B     INCRLINE                NO CONTINUATION
DVOL060  EQU   *
         CLC   0(4,R11),=C'SER='       SER= ?
         BE    DVOL070                 JA
         CLC   0(4,R11),=C'REF='       REF= ?
         BNE   DVOLERR                 ERROR
         LA    R11,4(R11)              VERHOOG POINTER
         SUBR  DSNTEST                 TEST OP DSNAME
         B     *+4(R15)                RETURNCODES
         B     DVOL110                 OK. TEST )
         B     DVOLERR                 ERROR
DVOL070  EQU   *
         LA    R11,4(R11)              VERHOOG POINTER
         CLI   0(R11),C'('             ( AANWEZIG ?
         BNE   DVOL080                 NEE.
         LA    R11,1(R11)              VERHOOG POINTER
         AP    BRFLAG,EEN              BRFLAG +
DVOL080  EQU   *
         SUBR  VOLSER                  VOLSER ?
         B     *+4(R15)                RETURNCODES
         B     DVOL090                 OK
         B     DVOLERR                 ERROR
DVOL090  EQU   *
         CP    BRFLAG,NUL              ( AANWEZIG ?
         BE    DVOL110                 NEE.
         CLI   0(R11),C','             , AANWEZIG ?
         BNE   DVOL100                 NEE. TEST )
         SUBR  DELIMIT                 VERVOLG MET SUBPARAMETERS
         B     *+4(R15)                RETURNCODES
         B     DVOL080                 JA
         B     INCRLINE                NO CONTINUATION
DVOL100  EQU   *
         CLI   0(R11),C')'             ) AANWEZIG ?
         BNE   DVOLERR                 ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         SP    BRFLAG,EEN              FLAG - 1
DVOL110  EQU   *
         CP    BRFLAG,NUL              ( AANWEZIG ?
         BE    DVOLOK                  NEE
         CLI   0(R11),C')'             ) AANWEZIG ?
         BNE   DVOLERR                 ERROR
         LA    R11,1(R11)              VERHOOG POINTER
         SP    BRFLAG,EEN              RESET FLAG
DVOLOK   EQU   *
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
DVOLERR  EQU   *
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       ONBEKENDE KEYWORD PARAMETER                                 /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DKEYERR  EQU   *
         LA    R6,ERRDKEY              ONBEKEND KEYWORD
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       DUBBELE KEYWORD PARAMETER                                   /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DDOUBLE  EQU   *
         LA    R6,ERRDDOUB             FOUT IN KEYWORD PARM
         SUBR  ERRMESS                 GENEREER ERROR MESSAGE
         SUBR  SKIPPARM                SKIP PARAMETER
         B     DNEXTKEY                VOLGENDE KEYWORD PARAMETER
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       VOLGENDE KEYWORD PARAMETER ?                                /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
DNEXTKEY EQU   *
         C     R11,ENDLINE             EINDE REGEL ?
         BNE   DNEXTK00                NEE
         B     INCRLINE                VOLGENDE REGEL
DNEXTK00 EQU   *
         BL    DNEXTK01                VERDER DAN EINDE ?
         SUBR  ERRMESS                 JA.
         B     INCRLINE                VOLGENDE REGEL
DNEXTK01 EQU   *
         CLI   0(R11),C','             KOMMA ?
         BNE   DNEXTK1                 NEE.
         SUBR  DELIMIT                 VOLGENDE PARAMETER
         B     *+4(R15)                RETURNCODES
         B     DDKEY                   AANWEZIG
         B     INCRLINE                NO CONTINUATION
DNEXTK1  EQU   *
         CLI   0(R11),C' '             EINDE REGEL
         BNE   DNEXTK2                 NEE.
         L     R11,ENDLINE             EINDE REGEL
         CLI   0(R11),C' '             CONTINUE COMMENT ?
         BE    INCRLINE                NEE.
         MVI   CONTFLAG,X'FF'          SET FLAG
         B     INCRLINE
DNEXTK2  EQU   *
         SUBR  ERRMESS                 ERROR MESSAGE
         B     INCRLINE                VOLGENDE REGEL
         TITLE '                   J C L   S Y N T A X - C H E C K E R C
                   D A T A - A R E A'
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       LITERAL POOL                                                /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
         LTORG
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       ALGEMENE DATA                                               /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
VCOMMON  DC    V(COMMON)
BUFFER   DS    CL10
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       LAY-OUT SCHERM                                              /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
SNBR     EQU   5                       OFFSET LINENUMBER
SMESSAGE EQU   17                      OFFSET MESSAGE
SLINELEN EQU   57                      REGEL LENGTE
SCREENL  DS    A                       SCHERM LENGTE
         SPACE 1
SCREEN   DS    0F
SCRWCC   DC    X'27F5',X'C2'           WRITE ERASE
         DC    X'1140401DF8'           R01 C01
         DC    C'- RPF JCL CHECK -----------------------'
         DC    C'----------------------------------------',X'1DF0'
         DC    X'11C150',X'1DF8'       R02 C01
         DC    C'=====>',X'1DC113'
REPLY    DC    CL3'   ',X'1DF0'
SCRMIN   EQU   *
SCRBEGIN EQU   *
         DC    X'11C3F0',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11C540',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11C650',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11C760',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11C8F0',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'114A40',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'114B50',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'114C60',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'114DF0',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'114F40',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'115050',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11D160',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11D2F0',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11D440',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11D550',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11D660',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11D7F0',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11D940',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'115A50',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'115B60',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'115CF0',X'1DF8',CL10' ',X'1DF0',CL40' '
SCR24LEN EQU   *-SCREEN
         DC    X'115E40',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'115F50',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'116060',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'1161F0',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11E340',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11E450',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11E560',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11E6F0',X'1DF8',CL10' ',X'1DF0',CL40' '
SCR32LEN EQU   *-SCREEN
         DC    X'11E840',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11E950',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'116A60',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'116BF0',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'116D40',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'116E50',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'116F60',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11F0F0',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11F240',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11F350',X'1DF8',CL10' ',X'1DF0',CL40' '
         DC    X'11F460',X'1DF8',CL10' ',X'1DF0',CL40' '
SCR43LEN EQU   *-SCREEN
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       ERROR MESSAGES                                              /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
HEADING  DC    CL40'XXXXXX JCL STATEMENTS; XXXXXX MESSAGES'
         SPACE 1
MESLEN   EQU   40
MESSTXT  EQU   *
         DC    CL40'// MISSING, INSTREAM DATA ?'                00
         DC    CL40'ERROR IN NAME-FIELD'                        01
         DC    CL40'UNKNOWN OPERATION'                          02
         SPACE 2
         DC    CL40'JOBCARD MISSING'                            03
         DC    CL40'NO JOBNAME SPECIFIED'                       04
         DC    CL40'ERROR IN ACCOUNTING'                        05
         DC    CL40'ERROR IN PROGRAMMERNAME'                    06
         DC    CL40'ERROR IN ADDRSPC-PARM IN JOBCARD'           07
         DC    CL40'ERROR IN CLASS-PARM IN JOBCARD'             08
         DC    CL40'ERROR IN COND-PARM IN JOBCARD'              09
         DC    CL40'GROUP SPECIFIED; RACF NOT INSTALLED'        10
         DC    CL40'ERROR IN MSGCLASS-PARM IN JOBCARD'          11
         DC    CL40'ERROR IN MSGLEVEL-PARM IN JOBCARD'          12
         DC    CL40'ERROR IN NOTIFY-PARM IN JOBCARD'            13
         DC    CL40'PASSWORD SPECIFIED; RACF NOT INSTALLED'     14
         DC    CL40'ERROR IN PERFORM-PARM IN JOBCARD'           15
         DC    CL40'USE OF PRTY IS RESTRICTED'                  16
         DC    CL40'DO NOT SPECIFY RD IN JOBCARD'               17
         DC    CL40'ERROR IN REGION-PARM IN JOBCARD'            18
         DC    CL40'ERROR IN RESTART-PARM IN JOBCARD'           19
         DC    CL40'ERROR IN TIME-PARM IN JOBCARD'              20
         DC    CL40'ERROR IN TYPRUN-PARM IN JOBCARD'            21
         DC    CL40'USER SPECIFIED; RACF NOT INSTALLED'         22
         SPACE 2
         DC    CL40'EXPECTED CONTINUATION NOT RECIEVED'         23
         DC    CL40'DOUBLE KEYWORD-PARM IN JOBCARD'             24
         DC    CL40'UNKNOWN PARAMETER IN JOBCARD'               25
         SPACE 2
         DC    CL40'ERROR IN POS-PARM IN EXECCARD'              26
         DC    CL40'ERROR IN ACCT-PARM IN EXECCARD'             27
         DC    CL40'ERROR IN ADDRSPC-PARM IN EXECCARD'          28
         DC    CL40'ERROR IN COND-PARM IN EXECCARD'             29
         DC    CL40'USE OF DPRTY IS RESTRICTED'                 30
         DC    CL40'ERROR IN DYNAMNBR-PARM IN EXECCARD'         31
         DC    CL40'ERROR IN PARM= IN EXECCARD'                 32
         DC    CL40'ERROR IN PERFORM-PARM IN EXECCARD'          33
         DC    CL40'DO NOT SPECIFY RD-PARM IN EXECCARD'         34
         DC    CL40'ERROR IN REGION-PARM IN EXECCARD'           35
         DC    CL40'ERROR IN TIME-PARM IN EXECCARD'             36
         DC    CL40'UNKNOWN PARAMETER IN EXECCARD'              37
         DC    CL40'DOUBLE KEYWORD-PARM IN EXECCARD'            38
         SPACE 2
         DC    CL40'ERROR IN DD *'                              39
         DC    CL40'ERROR IN POS. PARAMETER'                    40
         DC    CL40'ERROR IN DD DATA'                           41
         DC    CL40'ERROR IN DD DUMMY'                          42
         DC    CL40'ERROR IN DD DYNAM'                          43
         DC    CL40'ERROR IN AMP-PARM IN DDCARD'                44
         DC    CL40'DO NOT SPECIFY BURST'                       45
         DC    CL40'DO NOT SPECIFY CHARS'                       46
         DC    CL40'DO NOT SPECIFY CHKPT'                       47
         DC    CL40'ERROR IN COPIES-PARM IN DDCARD'             48
         DC    CL40'ERROR IN DCB-PARM IN DDCARD'                49
         DC    CL40'ERROR IN DDNAME-PARM IN DDCARD'             50
         DC    CL40'ERROR IN DEST-PARM IN DDCARD'               51
         DC    CL40'ERROR IN DISP-PARM IN DDCARD'               52
         DC    CL40'ERROR IN DLM-PARM IN DDCARD'                53
         DC    CL40'DO NOT SPECIFY DSID'                        54
         DC    CL40'ERROR IN DSNAME-PARM IN DDCARD'             55
         DC    CL40'ERROR IN FCB PARM IN DDCARD'                56
         DC    CL40'DO NOT SPECIFY FLASH'                       57
         DC    CL40'ERROR IN FREE-PARM IN DDCARD'               58
         DC    CL40'ERROR IN HOLD-PARM IN DDCARD'               59
         DC    CL40'ERROR IN LABEL-PARM IN DDCARD'              60
         DC    CL40'DO NOT SPECIFY MODIFY'                      61
         DC    CL40'DO NOT SPECIFY MSVGP'                       62
         DC    CL40'ERROR IN OUTLIM-PARM IN DDCARD'             63
         DC    CL40'PROTECT SPECIFIED; RACF NOT INSTALLED'      64
         DC    CL40'ERROR IN QNAME-PARM IN DDCARD'              65
         DC    CL40'ERROR IN SPACE-PARM IN DDCARD'              66
         DC    CL40'DO NOT SPECIFY SUBSYS'                      67
         DC    CL40'ERROR IN SYSOUT-PARM IN DDCARD'             68
         DC    CL40'ERROR IN TERM-PARM IN DDCARD'               69
         DC    CL40'ERROR IN UCS-PARM IN DDCARD'                70
         DC    CL40'ERROR IN UNIT-PARM IN DDCARD'               71
         DC    CL40'ERROR IN VOLUME-PARM IN DDCARD'             72
         DC    CL40'UNKNOWN PARAMETER IN DDCARD'                73
         DC    CL40'MUTUALLY EXCLUSIVE KEYWORDS SPECIFIED'      74
         DC    CL40'UNBALANCED PARENTHESIS'                     75
         DC    CL40'UNBALANCED APOSTROPHES'                     76
         DC    CL40'ERROR IN PROGRAM; CONTACT ALPHA'            77
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       COMMON AREA                                                 /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
         PRINT GEN
COMMON   JCLCOMM
         EJECT
*/////////////////////////////////////////////////////////////////////
*/                                                                   /
*/       RPF DATA AREA                                               /
*/                                                                   /
*/////////////////////////////////////////////////////////////////////
         SPACE 3
RPFDATA  RPFCOMM
         EJECT
         END
