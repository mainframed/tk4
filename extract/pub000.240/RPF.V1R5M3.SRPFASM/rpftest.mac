RPFTEST  TITLE 'TEST MONITOR FOR RPF INTERNAL USE ONLY'
RPFTEST  CSECT
         SAVE  (14,12),,*
         USING RPFTEST,R15
         LA    R11,SAVE
         ST    R13,SAVE+4
         ST    R11,8(0,R13)
         LR    R13,R11
         B     START
         DS    0F
SAVE     DC    18F'-1'
         DC    CL8'&SYSDATE',C' ',CL8'&SYSTIME'
START    DS    0H
         DROP  R15
         LA    R12,2048(R13)
         LA    R12,2048(R12)
         LA    R10,2048(R12)
         LA    R10,2048(R10)
         USING SAVE,R13,R12,R10
         L     R11,0(R1)
         USING COMMAREA,R11
         MVC   SCR1UID,USERID          USERID INTO SCREEN1
AUTH     CLC   PASSTXT,BLANKS
         BE    PUT1
         CLC   PASSTXT,NULL
         BE    PUT1
         CLC   USERID,=C'UPON002 '
         BE    PUT1
         CLC   USERID,=C'Z531415 '
         BE    PUT1
         LA    R4,3
AUTH01   TPUT  SCR3TXT,SCR3LEN,FULLSCR
         L     R3,COMMSCR
         TGET  (3),80,ASIS
         OC    0(80,R3),BLANKS
         CLC   6(L'PASSTXT,R3),PASSTXT
         BE    PUT1
         MVC   MSG3(L'ERR4TXT),ERR4TXT
         BCT   R4,AUTH01
         B     RETURN
PUT1     TPUT  SCR1TXT,SCR1LEN,FULLSCR MAIN MENU ON SCREEN
         MVC   BUFFER,BLANKS           BLANK OUTPUT BUFFER
GET1     BAL   R14,CONV                READ SCREEN
         B     GET1A(R15)              INITIATE BRANCHLIST
GET1A    B     GET1B                   CONTINUE PROCESSING
         B     RETURN                  PF03/PF15 PRESSED
         B     PUT1                    OTHER KEY PRESSED
GET1B    LTR   R1,R1                   NOTHING RECEIVED ?
         BZ    PUT1                    YES: BRANCH
         OC    BUFFER,BLANKS
         B     FUNC0                   SELECT FUNCTION
FUNC0    CLI   BUFFER,C'0'
         BNE   FUNC1
         LINK  EP=RPFSTAT,PARAM=((11)),VL=1
         B     PUT1
FUNC1    CLI   BUFFER,C'1'
         BE    OPER
FUNC2    CLI   BUFFER,C'2'
         BE    ALTER
FUNC3    CLI   BUFFER,C'3'
         BNE   FUNC4
         XC    ECBTEMP,ECBTEMP     CLEAR ECB
         LOAD  EP=RPFTEMP,ERRET=ERR2
         ATTACH EP=RPFTEMP,PARAM=((11)),VL=1,ECB=ECBTEMP
         ST    1,SAVETCB           SAVE TCB ADDRESS
         WAIT  ECB=ECBTEMP         WAIT FOR COMPLETION
         DETACH SAVETCB            DETACH SUBTASK
         CVD   15,DWB              CONVERT RETURN CODE
         UNPK  MSG1TXT+18(4),DWB   PLACE INTO MESSAGE
         OI    MSG1TXT+21,X'F0'    CLEAR SIGN
         MVC   MSG1,MSG1TXT        MOVE MESSAGE
         DELETE EP=RPFTEMP         delete xpftemp
         B     PUT1
FUNC4    CLI   BUFFER,C'4'         RESERVED
         BE    ERR3
FUNC5    CLI   BUFFER,C'5'         RESERVED
         BE    ERR3
FUNC6    CLI   BUFFER,C'6'         RESERVED
         BE    ERR3
FUNC7    CLI   BUFFER,C'7'         RETURN CODES
         BNE   FUNC8
         BAL   R14,RCODES
         B     PUT1
FUNC8    CLI   BUFFER,C'8'         RESERVED
         BE    ERR3
FUNC9    CLI   BUFFER,C'9'         RESERVED
         BNE   FUNCX
FUNC9T   TPUT  SCR9TXT,SCR9LEN,FULLSCR
         L     R3,COMMSCR
         TGET  (3),80,ASIS
         OC    0(80,3),BLANKS
         CLC   6(4,R3),=C'EXIT'
         BNE   FUNC9T
         B     PUT1
FUNCX    CLI   BUFFER,C'X'         EXIT
         BE    RETURN
ERR1     MVC   MSG2(L'ERR1TXT),ERR1TXT
         TPUT  SCR2TXT,SCR2LEN,FULLSCR
         B     GET1
ERR3     MVC   MSG2(L'ERR3TXT),ERR3TXT
         TPUT  SCR2TXT,SCR2LEN,FULLSCR
         B     GET1
ERR2     MVC   MSG2(L'ERR2TXT),ERR2TXT
         TPUT  SCR2TXT,SCR2LEN,FULLSCR
         B     GET1
ALTER    MVC   MSG4,NULL
         TPUT  SCR4TXT,SCR4LEN,FULLSCR
ALTER@@  MVC   HEXBIN,GETMSTRT
         BAL   R14,HEXDIS
         MVC   PTR51(8),DISFLD
         MVC   HEXBIN,GETMLEN
         BAL   R14,HEXDIS
         MVC   PTR52(8),DISFLD
         MVC   HEXBIN,GETMEND
         BAL   R14,HEXDIS
         MVC   PTR53(8),DISFLD
         MVC   HEXBIN,FIRSTREC
         BAL   R14,HEXDIS
         MVC   PTR54(8),DISFLD
         MVC   HEXBIN,LASTREC
         BAL   R14,HEXDIS
         MVC   PTR55(8),DISFLD
         MVC   HEXBIN,FIRSTSCR
         BAL   R14,HEXDIS
         MVC   PTR56(8),DISFLD
         MVC   HEXBIN,LASTSCR
         BAL   R14,HEXDIS
         MVC   PTR57(8),DISFLD
         ST    11,HEXBIN
         BAL   R14,HEXDIS
         MVC   PTR58(8),DISFLD
         MVC   HEXBIN,BUFFADDR     BUFFER ADDRESS INTO WORK FIELD
         BAL   R14,HEXDIS          CONVERT TO DISPLAY
         MVC   PTR59(8),DISFLD     WORK FIELD INTO SCREEN
         MVC   HEXBIN,EPDAIR
         BAL   R14,HEXDIS
         MVC   EPA51(8),DISFLD
         MVC   HEXBIN,EPDATAL
         BAL   R14,HEXDIS
         MVC   EPA52(8),DISFLD
         MVC   HEXBIN,EPALLOC
         BAL   R14,HEXDIS
         MVC   EPA53(8),DISFLD
         MVC   HEXBIN,EPEDITL
         BAL   R14,HEXDIS
         MVC   EPA54(8),DISFLD
         MVC   HEXBIN,EPEDIT
         BAL   R14,HEXDIS
         MVC   EPA55(8),DISFLD
         MVC   HEXBIN,EPFIND
         BAL   R14,HEXDIS
         MVC   EPA57(8),DISFLD
         MVC   HEXBIN,EPUTIL
         BAL   R14,HEXDIS
         MVC   EPA58(8),DISFLD
         MVC   HEXBIN,EPSAVE
         BAL   R14,HEXDIS
         MVC   EPA59(8),DISFLD
         MVC   HEXBIN,EPCHNG       ENTRY POINT OF XPFCHNG
         BAL   R14,HEXDIS          CONVERT TO EBCDIC
         MVC   EPA60(8),DISFLD
         TPUT  SCR5TXT,SCR5LEN,FULLSCR
ALTER00  EQU   *
         BAL   R14,CONV
         B     ALTER00A(R15)           INITIATE BRANCHLIST
ALTER00A B     ALTER00B                CONTINUE PROCESSING
         B     PUT1                    PF03/PF15 PRESSED
         B     PUT1                    OTHER KEY PRESSED
ALTER00B EQU   *
         OC    BUFFER,BLANKS
         LTR   1,1
         BZ    PUT1
         CLC   BUFFER(3),=C'END'
         BE    PUT1
         CLC   BUFFER(7),=C'RESTORE'
         BE    RESTORE
         CLI   BUFFER,C'P'
         BNE   ALTER57
         CLI   BUFFER+2,C'='
         BNE   ALTER55
         CLI   BUFFER+1,C'8'
         BNL   ALTER53
         CLI   BUFFER+1,C'2'
         BNH   ALTER53
         LA    3,8
         LA    4,BUFFER+3
ALTER01  LA    5,DISTAB1
         LA    6,DISTAB2
         LA    7,16
ALTER03  CLC   0(1,4),0(5)
         BE    ALTER05
         LA    5,1(5)
         LA    6,1(6)
         BCT   7,ALTER03
         B     ALTER51
ALTER05  MVC   0(1,4),0(6)
         LA    4,1(4)
         BCT   3,ALTER01
         PACK  DISBIN(5),BUFFER+3(9)
         CLC   DISBIN,GETMSTRT
         BL    ALTER51
         CLC   DISBIN,SAVEP3
         BH    ALTER51
         CLC   BUFFER(3),=C'P3='
         BNE   ALTER07
         MVC   GETMEND,DISBIN
         B     ALTER@@
ALTER07  CLC   BUFFER(3),=C'P4='
         BNE   ALTER09
         MVC   FIRSTREC,DISBIN
         B     ALTER@@
ALTER09  CLC   BUFFER(3),=C'P5='
         BNE   ALTER11
         MVC   LASTREC,DISBIN
         B     ALTER@@
ALTER11  CLC   BUFFER(3),=C'P6='
         BNE   ALTER13
         MVC   FIRSTSCR,DISBIN
         B     ALTER@@
ALTER13  CLC   BUFFER(3),=C'P7='
         BNE   ALTER57
         MVC   LASTSCR,DISBIN
         B     ALTER@@
ALTER51  MVC   MSG6(L'ERR5TXT),ERR5TXT
         TPUT  SCR6TXT,SCR6LEN,FULLSCR
         B     ALTER00
ALTER53  MVC   MSG6(L'ERR6TXT),ERR6TXT
         TPUT  SCR6TXT,SCR6LEN,FULLSCR
         B     ALTER00
ALTER55  MVC   MSG6(L'ERR7TXT),ERR7TXT
         TPUT  SCR6TXT,SCR6LEN,FULLSCR
         B     ALTER00
ALTER57  MVC   MSG6(L'ERR8TXT),ERR8TXT
         TPUT  SCR6TXT,SCR6LEN,FULLSCR
         B     ALTER00
HEXDIS   ST    14,SAVE14
         UNPK  DISFLD(9),HEXBIN(5)
         MVZ   DISFLD(9),HEXNULL
         TR    DISFLD(8),HEXTAB
         MVI   DISFLD+8,C' '
         L     14,SAVE14
         BR    14
*---------------------------------------------------------------------*
*                                                                     *
*        C O N V.   CONVERT THE SESSION DEFAULT SCREEN AND            *
*                   ALLOCATION SCREEN                                 *
*                   DO AN TGET ASIS TO OBTAIN THE AID BYTE TO         *
*                   TEST IF 'ENTER' IS USED OR PF03 OR PF15.          *
*                   AFTER THIS TEST REMOVE ALL DE UNNECESARY SBA'S.   *
*                                                                     *
*        RETURNCODES: 00 - SCREEN CONVERTED. ENTER KEY IS USED        *
*                          REG 1 CONTAINS THE LENGTH OF THE SCREEN    *
*                     04 - SCREEN NOT CONVERTED. PF03/PF15 KEY USED   *
*                     08 - SCREEN NOT CONVERTED. OTHER KEY IS USED    *
*                                                                     *
*-------------------------------------------(C)-2001 SKYBIRD SYSTEMS--*
         SPACE 2
CONV     DS    0H
         ST    R14,SAVE14C             SAVE REG 14
         LA    R5,BUFFER               OUTPUT FORMATTED BUFFER
         L     R3,COMMSCR              LOAD SCREEN ADDRESS
         TGET  (R3),802,ASIS           READ THE SCREEN
         LR    R4,R1                   SAVE LENGTH
         CLI   0(R3),X'F3'             PFK3 (END) PRESSED
         BE    CONV051                 YES: EXIT
         CLI   0(R3),X'C3'             PFK15 (END) PRESSED
         BE    CONV051                 YES: EXIT
         CLI   0(R3),X'7D'             'ENTER' PRESSED
         BNE   CONV053                 NO: END WITH RC=0008
         XR    R2,R2                   INIT OUTPUT LENGTH
         SH    R4,=H'3'                SKIP AID AND CURSOR ADDRESS
         C     R4,F0000                REGISTER ZERO
         BNH   CONV053                 NOT HIGH: END OF ROUTINE
         LA    R3,3(R3)
         LA    R5,BUFFER               OUTPUT ADDRESS
*        INPUT SCREEN IS POINTED TO BY R3
*        OUTPUT SCREEN IS POINTED TO BY R5
*        THE NEXT SECTION WILL REMOVE ALL THE SBA'S
CONV001  CLI   0(R3),X'11'             SET BUFFER ADDRESS ?
         BE    CONV003                 YES: SKIP NEXT 3 BYTES INPUT
CONV002  MVC   0(1,R5),0(R3)           MOVE INPUT BYTE
         LA    R3,1(R3)                NEXT BYTE INPUT
         LA    R2,1(R2)                COUNT OUTPUT BYTES
         LA    R5,1(R5)                NEXT BYTE OUTPUT
         BCT   R4,CONV001              TEST NEXT BYTE
         B     CONV055                 END OF ROUTINE
CONV003  LA    R3,3(R3)                SKIP 3 BYTES INPUT
         BCTR  R4,0                    MINUS 1 BCT
         BCTR  R4,0                    MINUS 1 BCT
         C     R4,F0000                REGISTER ZERO ?
         BNH   CONV055                 YES: TERMINATE CONVERSION
         BCT   R4,CONV001              TEST NEXT BYTE
         B     CONV055
CONV051  DS    0H                      PFK03/15 PRESSED
         LA    R15,4                   RC=0004
         B     CONV099
CONV053  DS    0H                      UNKNOWN KEY PRESSED
         LA    R15,8                   RC=0008
         B     CONV099
CONV055  DS    0H                      SUCCESSFUL END
         LA    R15,0                   RC=0000
CONV099  LR    R1,R2                   OUTPUT LENGTH IN REGISTER 1
         L     R14,SAVE14C             RELOAD REGISTER
         BR    R14                     RETURN
RCODES   ST    14,SAVE14           SAVE REG.14
         TPUT  SCR4TXT,SCR4LEN,FULLSCR
RCODES01 TPUT  SCR7TXT,SCR7LEN,FULLSCR
         BAL   R14,CONV
         B     RCODES0A(R15)           INITIATE BRANCHLIST
RCODES0A B     RCODES0B                CONTINUE PROCESSING
         B     RCODES99                PF03/PF15 PRESSED
         B     RCODES0B                OTHER KEY PRESSED
RCODES0B EQU   *
         OC    BUFFER,BLANKS
         CLC   BUFFER(3),=C'END'
         BE    RCODES99
         TPUT  SCR8TXT,SCR8LEN,FULLSCR
         BAL   R14,CONV
         B     RCODES0C(R15)           INITIATE BRANCHLIST
RCODES0C B     RCODES0D                CONTINUE PROCESSING
         B     RCODES99                PF03/PF15 PRESSED
         B     PUT1                    OTHER KEY PRESSED
RCODES0D EQU   *
         OC    BUFFER,BLANKS
         CLC   BUFFER(4),=C'BACK'
         BE    RCODES01
RCODES99 L     R14,SAVE14              RESTORE REG.14
         BR    R14                     RETURN
RESTORE  MVC   GETMEND,SAVEP3
         MVC   FIRSTREC,SAVEP4
         MVC   LASTREC,SAVEP5
         MVC   FIRSTSCR,F0000
         MVC   LASTSCR,F0000
         B     ALTER@@
PASSWORD TPUT  SCR3TXT,SCR3LEN,FULLSCR
         TGET  BUFFER,L'BUFFER
         OC    BUFFER,BLANKS
         MVC   PASSTXT,BUFFER
         B     PUT1
OPER     TPUT  SCR9999,L'SCR9999,FULLSCR
         LINK  EP=RPFOPER,PARAM=((11))
         B     PUT1
RETURN   L     R13,SAVE+4
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
SCR3TXT  DC    X'27F5C21140401DF8' WRITE ERASE / R01,C00
         DC    CL79'RPF TEST SIGNON: ----------------------------------C
               ----------------------------'
         DC    X'11407A'           R01,C59
MSG3     DC    CL22'----------------------'
         DC    X'11C150',X'1DF8',C'PASSWORD =>',X'1D4C13'
         DC    CL23' ',X'1DF0'
SCR3LEN  EQU   *-SCR3TXT
ERR5TXT  DC    CL22'-------Invalid address'
ERR6TXT  DC    CL22'---Address not altered'
ERR7TXT  DC    CL22'-Pointer not specified'
ERR8TXT  DC    CL22'-------Invalid command'
         TITLE 'SCREEN FORMATS'
SCR1TXT  DC    X'27F5C21140401DF8' WRITE ERASE / R01,C00
SCR1LNE  DC    CL79'------------------------------ RPF Test menu ------C
               ----------------------------'
         ORG   SCR1LNE+57
MSG1     DS    CL22
         ORG   ,
         DC    X'11C150',X'1DF8',C'OPTION  ===>',X'1DC113'
         DC    X'404040401DF0'
         DC    X'11C35D',C'USERID  - '    R03,C62
SCR1UID  DS    CL7
         DC    X'11C3F3',X'1DF8',C'0 ',X'1DF0'
         DC    C'STATUS      - Display current RPF status'
         DC    X'11C5C3',X'1DF8',C'1 ',X'1DF0'
         DC    C'OPERATOR    - Enter operator mode'
         DC    X'11C6D3',X'1DF8',C'2 ',X'1DF0'
         DC    C'ALT / DISP  - Enter alter / display mode'
         DC    X'11C7E3',X'1DF8',C'3 ',X'1DF0'
         DC    C'ATTACH      - ATTACH XPFTEMP module to be tested'
         DC    X'11C8F3',X'1DF8',C'4 ',X'1DF0'
         DC    C'RESERVED    - For future use'
         DC    X'114AC3',X'1DF8',C'5 ',X'1DF0'
         DC    C'RESERVED    - For future use'
         DC    X'114BD3',X'1DF8',C'6 ',X'1DF0'
         DC    C'RESERVED    - For future use'
         DC    X'114CE3',X'1DF8',C'7 ',X'1DF0'
         DC    C'RC CODES    - Display RPF internal return codes'
         DC    X'114DF3',X'1DF8',C'8 ',X'1DF0'
         DC    C'RESERVED    - For future use'
         DC    X'114FC3',X'1DF8',C'9 ',X'1DF0'
         DC    C'WAIT        - Set RPF in WAIT status'
         DC    X'1150D3',X'1DF8',C'X ',X'1DF0'
         DC    C'EXIT        - Return.'
         DC    X'1DF0'
         DC    X'11D2F01DF0'       R16,C01
         DC    C'Hit',X'1DF8',C'PF03/15'
         DC    X'1DF0',C'to return.'
SCR1LEN  EQU   *-SCR1TXT
SCR2TXT  DC    X'27F1C21140401DF8'
         DC    X'11407A'           R01,C59
MSG2     DS    CL22' '
         DC    X'11C150',X'1DF8',C'OPTION  ===>',X'1DC113'
         DC    X'404040401DF0'
SCR2LEN  EQU   *-SCR2TXT
SCR9999  DC    X'27F5C21140401D4013'
SCR4TXT  DC    X'27F5C21140401DF8' WRITE ERASE / R01,C00
         DC    CL79'RPF ALTER/DISPLAY AND RETURNCODES: ----------------C
               ----------------------------'
         DC    X'11407A'           R01,C59
MSG4     DC    CL22'----------------------'
         DC    X'11C150',X'1DF8',C'CMD =>',X'1D4013'
         DC    CL24' ',X'1DF0'
SCR4LEN  EQU   *-SCR4TXT
SCR5TXT  DC    X'27F1C2'
         DC    X'11407A'           R01,C59
MSG5     DC    CL22'----------------------'
         DC    X'11C150',X'1DF8',C'CMD =>',X'1D4013'
REP5     DC    CL24' ',X'1DF0'
         DC    X'11C540',X'1DF8'   R05,C01
         DC    CL78'                   R P F - ADDRESSES, ALTER / DISPLC
               AY MODE                    '
         DC    X'1DF0'
         DC    CL80' '
         DC    CL40'ADDRESS: REF: POINTER:                  '
         DC    CL40'ADDRESS: REF: LOAD MODULE:              '
         DC    CL40'-------- ---- ------------------------- '
         DC    CL40'-------- ---- --------------------------'
         DC    CL80' '
PTR51    DC    CL40'00000000 (P1) GETMSTRT                  '
EPA51    DC    CL40'00000000 (E1) RPFDAIR                   '
PTR52    DC    CL40'00000000 (P2) GETMLEN                   '
EPA52    DC    CL40'00000000 (E2) RPFDATAL                  '
PTR53    DC    CL40'00000000 (P3) GETMEND                   '
EPA53    DC    CL40'00000000 (E3) RPFALLOC                  '
PTR54    DC    CL40'00000000 (P4) FIRSTREC                  '
EPA54    DC    CL40'00000000 (E4) RPFEDITL                  '
PTR55    DC    CL40'00000000 (P5) LASTREC                   '
EPA55    DC    CL40'00000000 (E5) RPFEDIT                   '
PTR56    DC    CL40'00000000 (P6) FIRSTSCR                  '
EPA56    DC    CL40'00000000 (E6) RESERVED                  '
PTR57    DC    CL40'00000000 (P7) LASTSCR                   '
EPA57    DC    CL40'00000000 (E7) RPFFIND                   '
PTR58    DC    CL40'00000000 (P8) COMMAREA                  '
EPA58    DC    CL40'00000000 (E8) RPFUTIL                   '
PTR59    DC    CL40'00000000 (P9) BUFFER                    '
EPA59    DC    CL40'00000000 (E9) RPFSAVE                   '
PTR60    DC    CL40' '
EPA60    DC    CL40'00000000 (EA) RPFCHNG                   '
         DC    X'115CF0',X'1DF8'   R24,C01
         DC    CL78'                                                   C
                                   ...LAST'
         DC    X'1DF0'
SCR5LEN  EQU   *-SCR5TXT
SCR6TXT  DC    X'27F1C21140401DF8' WRITE ERASE / R01,C00
         DC    CL57'RPF ALTER/DISPLAY AND RETURNCODES: ----------------C
               ------'
MSG6     DC    CL22'----------------------'
         DC    X'11C150',X'1DF8',C'CMD =>',X'1D4013'
         DC    CL24' ',X'1DF0'
SCR6LEN  EQU   *-SCR6TXT
SCR7TXT  DC    X'27F1C2'
         DC    X'11407A'           R01,C59
MSG7     DC    CL22'----------------------'
         DC    X'11C150',X'1DF8',C'CMD =>',X'1D4013'
REP7     DC    CL24' ',X'1DF0'
         DC    X'11C540',X'1DF8'   R05,C01
         DC    CL78'                    R P F - RETURN CODES, INTERNAL C
               USE ONLY                   ',X'1DF0'
         DC    CL80'                                                   C
                                            '
         DC    CL80' MODULE:  RC:  DESCRIPTION:                        C
                                            '
         DC    CL80' -------- ---- ------------------------------------C
               -----------------------------'
         DC    CL80' RPFEDITL 0000 LOAD SUCCESSFUL                     C
                                            '
         DC    CL80'          0004 LOAD TERMINATED AT END OF WORKSPACE C
                                            '
         DC    CL80'          0008 NOTHING LOADED, WORKSPACE FULL      C
                                            '
         DC    CL80'          0012 NOTHING LOADED, FILE EMPTY          C
                                            '
         DC    CL80'          0016 - NOT USED -                        C
                                            '
         DC    CL80'          0020 DATASET/MEMBER IN USE               C
                                            '
         DC    CL80' RPFEDIT  0000 EDIT SUCCESSFUL                     C
                                            '
         DC    CL80'          0004 LOAD TERMINATED                     C
                                            '
         DC    CL80'          0008 NOTHING LOADED, WORKSPACE FULL      C
                                            '
         DC    CL80'          0012 NOTHING LOADED, FILE EMPTY          C
                                            '
         DC    CL80'          0016 EDIT CANCELED, WORKSPACE EMPTY      C
                                            '
         DC    CL80'          0020 EDIT CANCELED, DATASET/MEMBER IN USEC
                                            '
         DC    CL80'                                                   C
                                            '
         DC    CL80'                                                   C
                                            '
         DC    CL80'                                                   C
                                            '
         DC    X'1DF8'
         DC    CL78'                                                   C
                                 1 MORE...'
         DC    X'1DF0'
SCR7LEN  EQU   *-SCR7TXT
SCR8TXT  DC    X'27F1C2'
         DC    X'11407A'           R01,C59
MSG8     DC    CL22'----------------------'
         DC    X'11C150',X'1DF8',C'CMD =>',X'1D4013'
REP8     DC    CL24' ',X'1DF0'
         DC    X'11C540',X'1DF8'   R05,C01
         DC    CL78'                    R P F - RETURN CODES, INTERNAL C
               USE ONLY                   ',X'1DF0'
         DC    CL80'                                                   C
                                            '
         DC    CL80' MODULE:  RC:  DESCRIPTION:                        C
                                            '
         DC    CL80' -------- ---- ------------------------------------C
               -----------------------------'
         DC    CL80' RPFDATAL 0000 ALLOCATION SUCCESSFUL               C
                                            '
         DC    CL80'          0004 ALLOCATION FAILED, IGNORE ENTERED   C
                                            '
         DC    CL80'          0008 ALLOCATION FAILED, EXIT ENTERED     C
                                            '
         DC    CL80'          0012 - NOT USED -                        C
                                            '
         DC    CL80' RPFSAVE  0000 WORKSPACE SAVED                     C
                                            '
         DC    CL80'          0004 - NOT USED -                        C
                                            '
         DC    CL80'          0008 NOTHING SAVED                       C
                                            '
         DC    CL80'          0012 - NOT USED -                        C
                                            '
         DC    CL80'                                                   C
                                            '
         DC    CL80'                                                   C
                                            '
         DC    CL80'                                                   C
                                            '
         DC    CL80'                                                   C
                                            '
         DC    CL80'                                                   C
                                            '
         DC    CL80'                                                   C
                                            '
         DC    CL80'                                                   C
                                            '
         DC    X'1DF8'
         DC    CL78'                                                   C
                                   ...LAST'
SCR8LEN  EQU   *-SCR8TXT
SCR9TXT  DC    X'27F5C21140401DF8' WRITE ERASE / R01,C00
         DC    CL79'RPF TEST MODE: ------------------------------------C
               ----------------------------'
         DC    X'11407A'           R01,C59
MSG9     DC    CL22'----------------------'
         DC    X'11C150',X'1DF8',C'CMD =>',X'1D4013'
REP9     DC    CL24' ',X'1DF0'
         DC    X'11D26F1DF8'       R15,C80
         DC    CL80'--------==> PLEASE DO NOT LOGOFF THIS TERMINAL, I AC
               M STILL USING IT. <==--------'
         DC    CL80'                                                   C
                                            '
         DC    CL80'                                                   C
                                            '
         DC    CL80'                                            (IN CASC
               E OF EMERGENCIE ENTER ''EXIT'')',X'1DF0'
SCR9LEN  EQU   *-SCR9TXT
ERR1TXT  DC    CL22'--------Invalid option'
ERR2TXT  DC    CL22'---RPFTEMP not present'
ERR3TXT  DC    CL22'--------For future use'
ERR4TXT  DC    CL22'--Password not matched'
MSG1TXT  DC    CL22'RPFTEMP ended rc= XXXX'
SAVE14   DS    F
F0000    DC    F'0'
SAVE14C  DS    F
ECBTEMP  DC    F'0'                ECB FOR SUBTASK
SAVETCB  DC    F'0'                SAVE AREA TCB SUBTASK
NULL     DC    80X'00'
BUFFER   DC    CL80' '
HEXNULL  DS    0CL9
         DC    9X'00'
HEXBIN   DS    F'0'
         DC    X'00'
HEXTAB   DC    CL16'0123456789ABCDEF'
DISTAB1  DC    X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
DISTAB2  DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
DISBIN   DC    F'0'
DISFLD   DC    CL8' '              WORK FIELD HEXDIS
         DC    CL1' '              PADD CHAR DISFLD
         DC    X'00'
DWB      DS    D                   FOR CVD/CVB INSTRUCTIONS
         LTORG
COMMAREA RPFCOMM
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
