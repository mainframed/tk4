RPFHELP  TITLE 'THE RPF HELP PROCESSOR'
*---------------------------------------------------------------------*
*                                                                     *
*        MODULE:    RPFHELP                                           *
*                                                                     *
*        FUNCTION:  DISPLAY 5 CATAGORIES HELP INFORMATION:            *
*                   1. RPF INTRODUCTION             (MEMBER RPFHELP1) *
*                   2. EXPLANATION OF ALL COMMANDS  (MEMBER RPFHELP2) *
*                   3. RPF RETURN CODES             (MEMBER RPFHELP3) *
*                   4. RPF MESSAGES                 (MEMBER RPFHELP4) *
*                   5. RPF LATEST IMPROVEMENTS      (MEMBER RPFHELP5) *
*                                                                     *
*                   ALL THESE MEMBERS ARE ON A PARTITIONED DATASET    *
*                   THAT CAN BE SPECIFIED IN 2 MANNERS.               *
*                   1. VIA HELP=DSNAME STATEMENT IN MEMBER RPFKEY00   *
*                      OF THE SYS1.PARMLIB (2ND RECORD)               *
*                      THE PARMLIB WILL BE READ IN RPFINIT AND THE    *
*                      NAME OF THE DATASET WILL BE SET IN FIELD       *
*                      COMMHELP AND THE BIT $NOHELP WILL BE SET OFF.  *
*                      IN THIS CASE THE DATASET WILL BE OBTAINED VIA  *
*                      A DYNAMIC ALLOCATION.                          *
*                   2. VIA A DD-STATEMENT CALLED 'RPFHELP'            *
*                      IN THIS CASE THE BIT $HELPDD WILL BE SET.      *
*                      BIT $NOHELP WILL BE SET OFF                    *
*                                                                     *
*                   IF BOTH ARE SPECIFIED, MANNER 2 WILL BE PREFERRED.*
*                   IF NONE IS SPECIFIED, THE HELP FACILITY WILL BE   *
*                   DISABLED.                                         *
*                                                                     *
*        Modifications:                                               *
*        Dec, 08,2002 Add 7 color support in screen                   *
*       Feb 15,2003 RPr  Extend command area and =n/=n.n cmd added @rp1
*                                                                     *
*-------------------------------------------(C)-2003 SKYBIRD SYSTEMS--*
         SPACE 2
RPFHELP  CSECT
         SAVE  (14,12),,*
         USING RPFHELP,R15
         LA    R11,SAVE
         ST    R13,SAVE+4
         ST    R11,8(0,R13)
         LR    R13,R11
         B     START
         DS    0F
SAVE     DC    18F'-1'
         DC    CL8'&SYSDATE',C' ',CL8'&SYSTIME'
START    DS    0H
         DROP  R15
         USING SAVE,R13
         L     R11,0(R1)
         USING COMMAREA,R11
         MVC   SCR1UID(7),USERID       USERID INTO SCREEN
         MVC   SCR1TXT(2),=X'27F5'     WRITE
         CLC   COMMSIZE,=H'43'         Model 4 screen ?
         BE    WRALT                   YES: MOVE WRITE ALTERNATE 277E
         CLC   COMMSIZE,=H'32'         Model 3 screen ?
         BE    WRALT                   YES: MOVE WRITE ALTERNATE 277E
         B     PUT1                    BRANCH
WRALT    DS    0H
         MVC   SCR1TXT(2),=X'277E'     WRITE ALTERNATE
PUT1     EQU   *
         TM    INFCODE2,$RETURN        IS RETURN INDICATOR ON ?
         BO    END                     YES: RETURN TO MAIN
         TM    INFCODE2,$OPTION        OPTION ALREADY SEL. IN MAIN ?
         BNO   PUT1O                   NO BRANCH
         MVC   BUFFER(1),HELPOPT       MOVE OPTION
         OI    INFCODE2,$RETURN        SET RETURN INDICATOR ON
         B     FUNC0                   SCAN THE OPTION DIRECTLY
PUT1O    DS    0H
         TPUT  SCR1TXT,SCR1LEN,FULLSCR UTIL MENU ON SCREEN
GET1     LA    R5,BUFFER               OUTPUT FORMATTED BUFFER
         L     R3,COMMSCR              LOAD SCREEN ADDRESS
         TGET  (R3),40,ASIS            READ THE SCREEN
         LR    R4,R1                   SAVE LENGTH
*        DO AN TGET ASIS TO OBTAIN THE AID BYTE
*        AFTER THE TGET ASIS REMOVE THE SBA'S FROM THE OUTPUT
         CLI   0(R3),X'F3'             PFK3 (END) PRESSED
         BE    RETURN                  YES: TERMINATE RPF
         CLI   0(R3),X'C3'             PFK15 (END) PRESSED
         BE    RETURN                  YES: TERMINATE RPF
         XR    R2,R2                    INIT OUTPUT LENGTH
         LR    R4,R1                   SAVE INPUT LENGTH
         SH    R4,=H'3'                SKIP AID AND CURSOR ADDRESS
         C     R4,F0000                REGISTER ZERO
         BNH   CONV099                 NOT HIGH: END OF ROUTINE
         LA    R3,3(R3)
         LA    R5,BUFFER               OUTPUT ADDRESS
*        INPUT SCREEN IS POINTED TO BY R3
*        OUTPUT SCREEN IS POINTED TO BY R5
*        THE NEXT SECTION WILL REMOVE ALL THE SBA'S
CONV001  CLI   0(R3),X'11'             SET BUFFER ADDRESS ?
         BE    CONV003                 YES: SKIP NEXT 3 BYTES INPUT
CONV002  MVC   0(1,R5),0(R3)           MOVE INPUT BYTE
         LA    R3,1(R3)                NEXT BYTE INPUT
         LA    R2,1(R2)                COUNT OUTPUT BYTES
         LA    R5,1(R5)                NEXT BYTE OUTPUT
         BCT   R4,CONV001              TEST NEXT BYTE
         B     CONV099                 END OF ROUTINE
CONV003  LA    R3,3(R3)                SKIP 3 BYTES INPUT
         BCTR  R4,0                    MINUS 1 BCT
         BCTR  R4,0                    MINUS 1 BCT
         C     R4,F0000                REGISTER ZERO ?
         BNH   CONV099                 YES: TERMINATE CONVERSION
         BCT   R4,CONV001              TEST NEXT BYTE
CONV099  LR    R1,R2                   OUTPUT LENGTH IN REGISTER 1
         LTR   R1,R1                   NOTHING RECEIVED
         BZ    PUT1
         CLI   BUFFER,X'6E'            RESHOW CODE ?
         BE    PUT1                    YES: REVIEW SCREEN
         MVI   MSG1,C'-'
         MVC   MSG1+1(21),MSG1
         OC    BUFFER,BLANKS
         CLI   BUFFER,C'?'
         BE    PUT1
FUNC0    CLI   BUFFER,C'0'
         BE    ERR2
FUNC1    CLI   BUFFER,C'1'             RPF INTRODUCTION ?
         BNE   FUNC2
         MVI   HELPMEMB+7,C'1'         MEMBER=RPFHELP1
         B     BROWSE
FUNC2    CLI   BUFFER,C'2'             RPF COMMANDS ?
         BNE   FUNC3
         MVI   HELPMEMB+7,C'2'         MEMBER=RPFHELP2
         B     BROWSE
FUNC3    CLI   BUFFER,C'3'             RPF CODES ?
         BNE   FUNC4
         MVI   HELPMEMB+7,C'3'         MEMBER=RPFHELP3
         B     BROWSE
FUNC4    CLI   BUFFER,C'4'             RPF MESSAGES ?
         BNE   FUNC5
         MVI   HELPMEMB+7,C'4'         MEMBER=RPFHELP4
         B     BROWSE
FUNC5    CLI   BUFFER,C'5'             RPF HISTORY  ?
         BNE   FUNC6
         MVI   HELPMEMB+7,C'5'         MEMBER=RPFHELP4
         B     BROWSE
FUNC6    CLI   BUFFER,C'6'             RESERVED
         BE    ERR2
FUNC7    CLI   BUFFER,C'7'             RESERVED
         BE    ERR2
FUNC8    CLI   BUFFER,C'8'             RESERVED
         BE    ERR2
FUNC9    CLI   BUFFER,C'9'             RESERVED
         BE    ERR2
FUNCX    CLI   BUFFER,C'X'             EXIT ?
         BE    RETURN
FUNC#    CLI   BUFFER,C'='             '=' selected ?              @rp1
         BNE   ERR1                    No: invalid option          @rp1
         MVC   CMAINOPT,BUFFER+1       Move option after '='       @rp1
         OI    INFCODE2,$RETURN        Set return indicator        @rp1
         B     RETURN                  Exit                        @rp1
ERR1     MVC   MSG2(L'ERR1TXT),ERR1TXT
         TM    INFCODE2,$OPTION        OPTION WAS SELECTED IN MAIN
         BO    ERR1A                   NO READ THE SCREEN
         TPUT  SCR2TXT,SCR2LEN,FULLSCR
         B     GET1                    BRANCH
ERR1A    DS    0H
         MVC   MSG1(L'ERR1TXT),ERR1TXT MOVE ERROR MESSAGE
         NI    INFCODE2,255-$OPTION-$RETURN
         B     PUT1
ERR2     MVC   MSG2(L'ERR2TXT),ERR2TXT
         TM    INFCODE2,$OPTION        OPTION WAS SELECTED IN MAIN
         BO    ERR2A                   NO READ THE SCREEN
         TPUT  SCR2TXT,SCR2LEN,FULLSCR
         B     GET1                    BRANCH
ERR2A    DS    0H
         MVC   MSG1(L'ERR2TXT),ERR2TXT MOVE ERROR MESSAGE
         NI    INFCODE2,255-$OPTION-$RETURN
         B     PUT1
ERR3     MVC   MSG2(L'ERRSTXT),ERRSTXT
         TM    INFCODE2,$OPTION        OPTION WAS SELECTED IN MAIN
         BO    ERR3A                   NO READ THE SCREEN
         TPUT  SCR2TXT,SCR2LEN,FULLSCR
         B     GET1                    BRANCH
ERR3A    DS    0H
         MVC   MSG1(L'ERRSTXT),ERRSTXT MOVE ERROR MESSAGE
         NI    INFCODE2,255-$OPTION-$RETURN
         B     PUT1
BROWSE   DS    0H
         TM    INFCODE3,$HELPDD        RPFHELP DD-STATEMENT ?
         BO    BROWSE03                YES: BRANCH
         SYALLOC DDN=RPFHELP,DSN=COMMHELP,DISP1=SHR,MF=E
         LTR   R15,R15                 OK ?
         BNZ   BROWSER1                NO: ERROR
BROWSE03 OPEN  HELPDCB1                OPEN THE DCB
         BLDL  HELPDCB1,BLDLLIST       TRY TO FIND MEMBER
         LTR   R15,R15                 BLDL OK ?
         BNZ   BROWSER2                NO: ERROR
         CLOSE HELPDCB1                CLOSE DCB
         RDJFCB HELPDCB2               OBTAIN JFCB INFO
         MVC   JFCB+44(8),HELPMEMB     MOVE NAME OF HELP MEMBER
         OI    JFCB+86,X'01'           MARK MEMBER OF PDS
         OPEN  HELPDCB2,TYPE=J         WRITE JCFB BACK
         CLOSE HELPDCB2                CLOSE THE DATASET
         FREEPOOL HELPDCB2             AND FREE THE BUFFERS
         MVC   CMDAREA,BLANKS          BLANK CMDAREA
         MVC   CMDAREA(8),=CL8'RPFHELP' MOVE DDNAME
         LINK  EP=RPFBRO,PARAM=((11)),VL=1 LINK TO BROWSE
         MVC   CMDAREA,BLANKS          BLANK CMDAREA
         TM    INFCODE3,$HELPDD        HELP PROVIDED VIA DD ?
         BO    BROWSE05                RESTORE JCFB INFO IF YES
         SYALLOC FREE=RPFHELP,MF=E     FREE THE DATASET
         B     PUT1
BROWSE05 DS    0H
         RDJFCB HELPDCB2               OBTAIN JFCB INFO
         MVC   JFCB+44(8),BLANKS       BLANK NAME OF HELP MEMBER
         NI    JFCB+86,X'FF'-X'01'     RESTORE VALUE
         OPEN  HELPDCB2,TYPE=J         WRITE JCFB BACK
         CLOSE HELPDCB2                CLOSE THE DATASET
         B     PUT1
BROWSER1 DS    0H
         SYCONVHX IN=DAERROR,OUT=ERR3TXT+13,L=2
         MVC   ERRSTXT,ERR3TXT         MOVE ERROR MESSAGE
         B     ERR3                    GIVE ERROR MESSAGE
BROWSER2 DS    0H
         CLOSE HELPDCB1                CLOSE DATASET
         MVC   ERR4TXT+14(8),HELPMEMB  MOVE MEMBERNAME
         MVC   ERRSTXT,ERR4TXT         MOVE ERROR MESSAGE
         TM    INFCODE3,$HELPDD        HELP VIA DD-STATEMENT ?
         BO    ERR3                    YES: DO NOT FREE
         SYALLOC FREE=RPFHELP,MF=E     FREE THE DATASET
         B     ERR3
END      DS    0H
RETURN   L     R13,SAVE+4
         LM    R14,R12,12(R13)
         XR    R15,R15
         BR    R14
         TITLE 'DATA AREAS AND CONSTANTS'
SCR1TXT  DC    X'27F5C21140401DF8' WRITE ERASE / R01,C00
SCR1LNE  DC    CL79'------------------------------ RPF HELP MENU ------C
               ----------------------------'
         ORG   SCR1LNE+57
MSG1     DS    CL22
         ORG   ,
         DC    X'11C150',X'1DF8',C'Option  ===>',X'1DC113'         @rp1
         DC    C'       ',X'1DF0'                                  @rp1
         DC    X'11C35D',AL3(TURQ),C'Userid  - '    R03,C62
SCR1UID  DS    CL7
         DC    X'11C3F3',AL3(WHITE),C'0  '
         DC    AL3(TURQ)
         DC    C'Reserved    - '
         DC    AL3(GREEN)
         DC    C'For future use'
         DC    X'11C5C3',AL3(WHITE),C'1  '
         DC    AL3(TURQ)
         DC    C'Intro       - '
         DC    AL3(GREEN)
         DC    C'Introduction to RPF'
         DC    X'11C6D3',AL3(WHITE),C'2  '
         DC    AL3(TURQ)
         DC    C'Commands    - '
         DC    AL3(GREEN)
         DC    C'Explanation of all RPF commands'
         DC    X'11C7E3',AL3(WHITE),C'3  '
         DC    AL3(TURQ)
         DC    C'Codes       - '
         DC    AL3(GREEN)
         DC    C'Explanation of all RPF return codes'
         DC    X'11C8F3',AL3(WHITE),C'4  '
         DC    AL3(TURQ)
         DC    C'Messages    - '
         DC    AL3(GREEN)
         DC    C'Display all RPF messages'
         DC    X'114AC3',AL3(WHITE),C'5  '
         DC    AL3(TURQ)
         DC    C'History     - '
         DC    AL3(GREEN)
         DC    C'Display RPF latest improvements'
         DC    X'114BD3',AL3(WHITE),C'6  '
         DC    AL3(TURQ)
         DC    C'Reserved    - '
         DC    AL3(GREEN)
         DC    C'For future use'
         DC    X'114CE3',AL3(WHITE),C'7  '
         DC    AL3(TURQ)
         DC    C'Reserved    - '
         DC    AL3(GREEN)
         DC    C'For future use'
         DC    X'114DF3',AL3(WHITE),C'8  '
         DC    AL3(TURQ)
         DC    C'Reserved    - '
         DC    AL3(GREEN)
         DC    C'For future use'
         DC    X'114FC3',AL3(WHITE),C'9  '
         DC    AL3(TURQ)
         DC    C'Reserved    - '
         DC    AL3(GREEN)
         DC    C'For future use'
         DC    X'1150D3',AL3(WHITE),C'X  '
         DC    AL3(TURQ)
         DC    C'Exit        - '
         DC    AL3(GREEN)
         DC    C'Return'
         DC    X'11D2F1',AL3(TURQ)      R16,C02
         DC    C'Hit ',AL3(WHITE),C'PF03/15 ',AL3(TURQ)
         DC    C'to return'
SCR1LEN  EQU   *-SCR1TXT
SCR2TXT  DC    X'27F1C21140401DF8'
         DC    X'11407A'           R01,C59
MSG2     DS    CL22' '
         DC    X'11C150',X'1DF8',C'Option  ===>',X'1DC113'         @rp1
         DC    C'       ',X'1DF0'                                  @rp1
SCR2LEN  EQU   *-SCR2TXT
ERR1TXT  DC    CL22'--------Invalid option'
ERR2TXT  DC    CL22'--------For future use'
ERR3TXT  DC    CL22'-Alloc error XXXX HELP'     13
ERR4TXT  DC    CL22'----No member XXXXXXXX'     14
ERRSTXT  DC    CL22' '
F0000    DC    F'0'
BUFFER   DC    CL80' '
*
BLDLLIST DS    0F
         DC    AL2(1)                  NUMBER OF ENTRIES
         DC    AL2(14)                 LENGTH OF ENTRY
HELPMEMB DC    CL8'RPFHELPX'           MEMBER NAME
         DS    CL6                     REST OF ENTRY
*
         PRINT NOGEN
HELPDCB1 DCB   DSORG=PO,DDNAME=RPFHELP,MACRF=R
HELPDCB2 DCB   DSORG=PS,DDNAME=RPFHELP,MACRF=GM,EXLST=EXLST
EXLST    DS    0F
         DC    X'87',AL3(JFCB)
JFCB     DS    CL176
         SYALLOC MF=L
         PRINT GEN
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         LTORG
         TITLE 'RPFCOMM DSECT'
COMMAREA RPFCOMM
         END
