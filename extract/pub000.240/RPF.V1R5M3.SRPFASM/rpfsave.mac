RPFSAVE  TITLE 'SAVE WORKSPACE INTO DATASET'
*---------------------------------------------------------------------*
*                                                                     *
*        MODULE:        RPFSAVE.                                      *
*                                                                     *
*        FUNCTION:      PRESENT A DATASET MENU IN WHICH THE WORKSPACE *
*                       IS BEIING SAVED.                              *
*                       YOU CAN TYPE IN THE NAME,VOLUME AND MEMBER    *
*                       OF THE REQUESTED DATASET (NOTE: IF THE DATA-  *
*                       SET IS A SEQUENTIAL DATASET THE MEMBERNAME    *
*                       MUST BE BLANKS).                              *
*                       IF OPT '3' IS SELECTED IN THE MENU THE        *
*                       WORKSPACE WILL BE SAVED IN LIBRARIAN VIA      *
*                       MODULE RPFLIB01.                              *
*                       IF OPT '4' IS SELECTED IN THE MENU THE        *
*                       WORKSPACE WILL BE SAVED IN PANVALET  VIA      *
*                       MODULE RPFPAN01.                              *
*                       AFTER TYPING IN CONTROL WILL BE PASSED TO     *
*                       RPFDAIR TO ALLOCATE THE REQUESTED DATASET     *
*                       IF THE RETURNCODE OF RPFDAIR IS 00 OR 32      *
*                       THE WORKSPACE WILL BE SAVED, OTHERWISE A      *
*                       RETRY MENU WILL BE DISPLAYED TO RETRY OR EXIT.*
*                       IF THE MEMBERNAME ALLREADY EXISTS AND THE     *
*                       EDITED MEMBERNAME IS NOT THE SAME A RETRY     *
*                       MENU WILL BE DISPLAYED TOO.                   *
*         NOTE1:        IF THE DATASET IS A PDS THE WORKSPACE WILL BE *
*                       SAVED ALLWAYS IN MEMBER 'TEMPRPF'             *
*                       AFTER SUCCESSFUL SAVE THE ORIGINAL MEMBER     *
*                       WILL BE DELETE IF PRESENT AND 'TEMPRPF'       *
*                       WILL BE RENAMED INTO THE ORIGINAL MEMBERNAME. *
*                       THIS MANNER OF SAVE DO NOT DESTROY THE        *
*                       ORIGINAL MEMBER IF AN ABEND OCCURS.           *
*         NOTE2:        UPON ENTRY OF RPFSAVE IN FIELD 'CMDAREA'      *
*                       IS PLACED THE TRANSLATED MEMBERNAME (UNPRINTA-*
*                       BLES ARE REMOVED) THIS FIELD IS THE XLATED    *
*                       NAME OF FIELD 'MEMBER' IN COMMAREA.           *
*                       THE XLATED MEMBER WILL BE SET ON THE SAVE     *
*                       SCREEN TO PREVENT DAMAGING THE SCREEN.        *
*         NOTE3:        IF THE SUBCOMMAND 'END' IS GIVEN IN RPFEDIT   *
*                       FLAG $SAMEDS WILL BE SET IN RPFEDIT AND       *
*                       RPFSAVE WILL SAVE THE DATA IN THE SAME        *
*                       DATASET AS THE TO BE EDITED DS/MEMBER         *
*         NOTE4:        IF both flags $EDTCOPY and $SAMEDS are on     *
*                       RPFSAVE will re-allocate the dataset/member   *
*                       and reuse the member if present               *
*                       The reason of re-allocation of the dataset    *
*                       is, that the COPY command of RPFEDIT had      *
*                       allocated another dataset (RPFDATAL will be   *
*                       invoked in COPY).                             *
*                       $EDTCOPY will be set by the EDIT COPY cmd.    *
*                                                                     *
*        RETURNCODES (SET IN RTNCODE FIELD OF COMMAREA):              *
*                       00: SAVE SUCCESSFUL.                          *
*                           POINTER TO RPF USERDATA SET IN 'CMDAREA'  *
*                           (RPF UDATA IS THE SAME AS SPF UDATA)      *
*                       08: EXIT ENTERED BY USER ON RETRY MENU        *
*                           FIELD 'CMDAREA' SET TO BLANK.             *
*                                                                     *
*        Nov 27,2002 RPr: add 7 color support in screen            @rp1
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
         SPACE 2
RPFSAVE  CSECT
         SAVE  (14,12),,*
         USING RPFSAVE,R15
         LA    R11,SAVEAREA            PICKUP SAVE AREA
         ST    R13,SAVEAREA+4          BACKWARD POINTER
         ST    R11,8(0,R13)            FORWARD POINTER
         LR    R13,R11
         B     START
         DS    0F
SAVEAREA DC    18F'-1'
         DC    CL8'&SYSDATE',C' ',CL8'&SYSTIME'
START    DS    0H
         LA    R12,2048(R13)           LOAD
         LA    R12,2048(R12)              SECOND BASE
         DROP  R15
         USING SAVEAREA,R13,R12
         L     R11,0(R1)
         USING COMMAREA,R11
         MVI   SW,0                    CLEAR FLAGS
         STM   R11,R13,HEREADDR        SAVE R11,12,13 FOR ESTAE ROUTINE
         MVC   SCR1TXT(2),=X'27F5'     MOVE DEFAULT WRITE CC
         MVC   SCR3LAST+1(2),=X'5CF1'  DEFAULT R24,C02 LAST LINE   @rp1
         CLC   COMMSIZE,=H'43'         43 LINES RECEIVED ?         @rp1
         BE    START1                  YES: BRANCH
         CLC   COMMSIZE,=H'32'         32 LINES RECEIVED ?         @rp1
         BE    START2                  YES: BRANCH
         B     START3                  ELSE DEFAULT
START1   MVC   SCR1TXT(2),=X'277E'     WRITE CC ALTERNATE
         MVC   SCR3LAST+1(2),=X'F461'  R43,C02 FOR LAST LINE       @rp1
         B     START3                  AND BRANCH
START2   MVC   SCR1TXT(2),=X'277E'     WRITE CC ALTERNATE
         MVC   SCR3LAST+1(2),=X'E6F1'  R32,C02 FOR LAST LINE       @rp1
         B     START3                  AND BRANCH
START3   MVI   SCR3CMD,C'1'            DEFAULT OPTION 1
         MVC   SAVEMBR,CMDAREA         MOVE XLATED 'MEMBER'
         MVC   SCR1UID+23(7),USERID    UID INTO SCREEN
         LA    R3,SCR1UID+29           REPLACE TRAILING BLANKS
         LA    R4,7                    MAX 7 TIMES
UID1     CLI   0(R3),C' '              BLANK FOUND ?
         BNE   UID99                   NO: EXIT
         MVI   0(R3),C'-'              REPLACE BY HYPHEN
         BCTR  R3,0                    PREVIOUS POSITION
         BCT   R4,UID1                 LOOP UNTIL ALL DONE
UID99    EQU   *
         XC    EPNEXT,EPNEXT           CLEAR EPNEXT
         XC    RTNCODE,RTNCODE         0 INTO RETURN CODE
         MVI   MSG3,C'-'               CLEAR MESSAGE
         MVC   MSG3+1(21),MSG3
         TM    INFCODE4,$SAMEDS        SAVE IN SAME DATASET/MEMBER?
         BNO   RESTART                 NO: PERFORM NORMAL SAVE
         TM    INFCODE3,$EDTCOPY       Copy command of edit ?
         BO    DYNALCPY                Yes: allocate without panel
         B     REUSE                   No: save immediately
RESTART  DS    0H
         NI    INFCODE3,255-$EDTCOPY   Reset flag, normal save
         TPUT  SCR1TXT,SCR1LEN,FULLSCR DISPLAY SCREEN
DYNALC   MVC   SCR3VOL(6),VOLUME       VOLUME INTO SCREEN
         MVC   SCR3DSN(44),DSNAME      DSNAME INTO SCREEN
         MVC   SCR3MEM(8),SAVEMBR      MEMBER INTO SCREEN
         TPUT  SCR3TXT,SCR3LEN,FULLSCR WRITE SCREEN3
GET1     DS    0H
         LA    R5,BUFFER               OUTPUT FORMATTED BUFFER
         L     R3,COMMSCR              LOAD SCREEN ADDRESS
         TGET  (R3),300,ASIS           READ THE SCREEN
         LR    R4,R1                   SAVE LENGTH
*        DO AN TGET ASIS TO OBTAIN THE AID BYTE
*        AFTER THE TGET ASIS REMOVE THE SBA'S FROM THE OUTPUT
         CLI   0(R3),X'F3'             PFK3 (END) PRESSED
         BE    DYNALCR5                YES: EXIT
         CLI   0(R3),X'C3'             PFK15 (END) PRESSED
         BE    DYNALCR5                YES: EXIT
         XR    R2,R2                    INIT OUTPUT LENGTH
         LR    R4,R1                   SAVE INPUT LENGTH
         SH    R4,=H'3'                SKIP AID AND CURSOR ADDRESS
         C     R4,F0000                REGISTER ZERO
         BNH   CONV099                 NOT HIGH: END OF ROUTINE
         LA    R3,3(R3)
         LA    R5,BUFFER               OUTPUT ADDRESS
*        INPUT SCREEN IS POINTED TO BY R3
*        OUTPUT SCREEN IS POINTED TO BY R5
*        THE NEXT SECTION WILL REMOVE ALL THE SBA'S
CONV001  CLI   0(R3),X'11'             SET BUFFER ADDRESS ?
         BE    CONV003                 YES: SKIP NEXT 3 BYTES INPUT
CONV002  MVC   0(1,R5),0(R3)           MOVE INPUT BYTE
         LA    R3,1(R3)                NEXT BYTE INPUT
         LA    R2,1(R2)                COUNT OUTPUT BYTES
         LA    R5,1(R5)                NEXT BYTE OUTPUT
         BCT   R4,CONV001              TEST NEXT BYTE
         B     CONV099                 END OF ROUTINE
CONV003  LA    R3,3(R3)                SKIP 3 BYTES INPUT
         BCTR  R4,0                    MINUS 1 BCT
         BCTR  R4,0                    MINUS 1 BCT
         C     R4,F0000                REGISTER ZERO ?
         BNH   CONV099                 YES: TERMINATE CONVERSION
         BCT   R4,CONV001              TEST NEXT BYTE
CONV099  LR    R1,R2                   OUTPUT LENGTH IN REGISTER 1
         OC    BUFFER,BLANKS           XLATE TO UPPERCASE
DYNALCA0 MVC   BUFVOL(6),BLANKS        CLEAR VOLUME
         MVC   BUFMEM(8),BLANKS        CLEAR MEMBER
         MVC   BUFDSN(44),BLANKS       CLEAR DSNAME
         LTR   R1,R1                   NO INPUT RECEIVED ?
         BZ    RESTART                 BRANCH IF YES
         LA    R5,BUFFER               POINT TO BEGINNING OF BUFFER
         LR    R6,R1                   ACTUAL LENGTH OF BUFFER
         LA    R4,BUFCMD               BUFFER ADDRESS
DYNALC02 CLC   0(7,R5),=C'MEMBER='
         BE    DYNALC03
         MVC   0(1,R4),0(R5)           MOVE BYTE COMMAND
         LA    R5,1(R5)                NEXT BYTE
         LA    R4,1(R4)                NEXT BYTE
         BCT   R6,DYNALC02             TRY NEXT BYTE
         B     RESTART                 CLEAR KEY PRESSED
DYNALC03 LA    R5,7(R5)                POINT AFTER 'MEMBER='
         LA    R4,BUFMEM
DYNALC05 CLC   0(8,R5),=C',DSNAME='
         BE    DYNALC07
         MVC   0(1,R4),0(R5)
         LA    R5,1(R5)
         LA    R4,1(R4)
         BCT   R6,DYNALC05             TRY NEXT BYTE
         B     RESTART                 CLEAR KEY PRESSED
DYNALC07 LA    R5,8(R5)                POINT AFTER 'DSNAME='
         LA    R4,BUFDSN
DYNALC09 CLC   0(7,R5),=C'VOLUME='
         BE    DYNALC11
         MVC   0(1,R4),0(R5)
         LA    R5,1(R5)
         LA    R4,1(R4)
         BCT   R6,DYNALC09             TRY NEXT BYTE
         B     RESTART                 CLEAR KEY PRESSED
DYNALC11 LA    R5,7(R5)                POINT AFTER 'VOLUME='
         LA    R4,BUFVOL
DYNALC13 CLC   0(8,R5),=C',DDNAME='
         BE    DYNALC15
         MVC   0(1,R4),0(R5)
         LA    R5,1(R5)
         LA    R4,1(R4)
         BCT   R6,DYNALC13             TRY NEXT BYTE
         B     RESTART                 CLEAR KEY PRESSED
DYNALC15 DS    0H
         MVI   MSG3,C'-'               CLEAR MESSAGE
         MVC   MSG3+1(21),MSG3
         CLI   BUFCMD,C'1'             CONTINUE SAVE ?
         BE    DYNAL$15                YES: GO ON
         CLI   BUFCMD,C'2'             REUSE MEMBER ?
         BE    DYNAL$15                YES: BRANCH
         CLI   BUFCMD,C'3'             LIBRARIAN SAVE ?
         BE    DYNALCT5                YES: BRANCH
         CLI   BUFCMD,C'4'             PANVALET  SAVE ?
         BE    DYNALCW5                YES: BRANCH
         CLI   BUFCMD,C'X'             EXIT ?
         BE    DYNALCR5                YES: BRANCH
         MVC   MSG2,ERR1TXT            INVALID OPTION
         B     ERROR                   BRANCH
DYNALCR5 DS    0H
         LA    R0,8                    RC=0008
         ST    R0,RTNCODE              IN RTNCODE
         MVC   CMDAREA,BLANKS          BLANK CMDAREA
         B     RETURN                  EXIT
DYNALCT5 DS    0H                      LIBRARIAN SAVE
         MVC   COMMMOD,BUFMEM          MOVE MEMBERNAME
         LINK  EP=RPFLIB01,PARAM=((11)),VL=1 LINK TO LIBWRITE
         B     RETURN
DYNALCW5 DS    0H                      PANVALET  SAVE
         MVC   PANMOD,BLANKS           BLANK PANVALET MEMBERNAME
         MVC   PANMOD(8),BUFMEM        MOVE MEMBERNAME
         LINK  EP=RPFPAN01,PARAM=((11)),VL=1 LINK TO LIBWRITE
         B     RETURN
DYNAL$15 CLC   BUFMEM,BLANKS           NO MEMBERNAME PROVIDED ?
         BE    DYNALC16                POSSIBLE NOT A PDS.
         CLC   BUFMEM,=CL8'TEMPRPF'    SAVE UNDER TEMPRPF
         BNE   DYNAL#15                NO: BRANCH
         MVC   MSG2,ERR7TXT        MOVE ERROR MSG (TEMPRPF RESTRICTED)
         B     ERROR                   GO TO ERROR AND RETRY ROUTINE
DYNAL#15 DS    0H
         CLC   BUFMEM,SAVEMBR          MEMBERNAME NOT CHANGED ?
         BE    DYNALC16                YES: DO NOT CHECK NAME
         TRT   BUFMEM(1),TRTAB         TEST IF 1ST CHAR VALID
         BZ    DYNALC16                YES: LINK TO EPDAIR
         MVC   MSG2,ERR6TXT            MEMBER NAME INVALID INTO SCREEN
         B     ERROR                   GIVE ERROR MESSAGE
DYNALC16 DS    0H
         CLC   BUFMEM,SAVEMBR          BUFMEM THE SAME AS XLATED MBR
         BE    DYNAL#16                YES: DO NOT REPL. FIELD 'MEMBER'
         MVC   MEMBER,BUFMEM           MOVE MEMBER INTO COMMAREA
DYNAL#16 DS    0H
         MVC   VOLUME,BUFVOL           MOVE VOLUME INTO COMMAREA
         MVC   DSNAME,BUFDSN           MOVE DSNAME INTO COMMAREA
DYNALCPY DS    0H
         L     R15,EPDAIR              ENTRY POINT RPFDAIR
         ST    R11,DAIRPARM            ADDRESS COMMAREA
         LA    R1,DAIRPARM             ADDRESS PARMLIST
         BALR  R14,R15                 BRANCH TO RPFDAIR
DYNALC17 CLC   RTNCODE,F0000           RC= 0000
         BNE   DYNALC19                NO: TEST OTHER VALUES
         TM    INFCODE3,$EDTCOPY       Copy command of edit???
         BO    REUSE                   Reuse member anyway
         TM    INFCODE,SEQALC          SEQUENTIAL DATASET ?
         BO    REUSE                   REUSE ALWAYS
         CLC   LASTEDIT,MEMBER     SAVE MEMBER = LAST MEMBER EDITED ?
         BE    REUSE                   YES: REUSE MEMBER
         CLI   BUFCMD,C'2'             REUSE SELECTED ?
         BE    REUSE                   YES: REUSE ALWAYS IF SELECTED
         MVC   ERR4TXT+7(8),MEMBER     NO: MEMBER INTO MESSAGE
         MVC   MSG2,ERR4TXT            WARNING INTO SCREEN
         B     ERROR                   GIVE WARNING MESSAGE
DYNALC19 CLC   RTNCODE,F0032           MEMBER NOT FOUND ?
         BNE   DYNALC23                NO: ERROR
         NI    INFCODE3,255-$EDTCOPY   RESET COPY FLAG
         NI    INFCODE4,255-$SAMEDS    RESET same dataset flag
         RDJFCB RPFWORK                RE READ JFCB BECAUSE THE JFCB
*                                      IS CHANGED BY RPFDAIR
         MVC   JFCB+44(8),=CL8'TEMPRPF' MEMBER INTO JFCB
         MVI   JFCB+86,X'01'           TURN ON MODIFY BIT IN JFCB
         TM    INFCODE2,ENQUED         IS THERE AN OUTSTANDING ENQUEUE
         BNO   ENQ1                    NO: ENQ IMMED.
         CLC   DSNAME,RNAME            RNAME IS DSNAME
         BNE   RENQ1                   NO: DEQ AND ENQ
         CLC   MEMBER,RNAME+44         RNAME+44 = MEMBER ?
         BNE   RENQ1                   NO: DEQ AND ENQ
         B     OPEN1                   OPEN THE FILE ALREADY ENQ'D
RENQ1    DS    0H                      DEQ
         LA    R3,QNAME                QNAME
         LA    R4,RNAME                RNAME
         DEQ   ((3),(4),52,SYSTEMS),RET=HAVE
         NI    INFCODE2,255-ENQUED     TURN OFF FLAG
ENQ1     DS    0H                      ENQ
         MVC   QNAME(8),=CL8'SPFEDIT'
         MVC   RNAME(44),DSNAME
         MVC   RNAME+44(8),MEMBER      ONLY IF PDS. ELSE BLANKS
         LA    R3,QNAME                QNAME
         LA    R4,RNAME                RNAME
         ENQ   ((3),(4),E,52,SYSTEMS),RET=USE
         LTR   15,15
         BZ    OPEN1                ** SEE PAGE 244 245 MACRO MANUAL
         MVC   MSG2,ERR9TXT            MOVE ERROR MESSAGE
         B     ERROR                   ERROR PROCESSING
* NOW WE HAVE THE RESOURCE EXCLUSIVELY, SO WE CAN OPEN FOR OUTPUT.
OPEN1    DS    0H
         OI    INFCODE2,ENQUED         SET FLAG
         LA    R1,RPFWORK              DCB ADDRESS
         ST    R1,RPFDCB               SAVE THAT ADDRESS FOR ESTAE
         ESTAE RECOVER,PARAM=((11))    ESTABLISH RECOVERY ROUTINE
         OPEN  (RPFWORK,(OUTPUT)),TYPE=J MODIFY JFCB, OPEN FILE
         B     SAVE                    GOTO SAVE
DYNALC23 L     R1,RTNCODE              POINT TO RETURN CODE
         CVD   R1,DOUBLEWD             CONVERT TO DECIMAL
         UNPK  ERR5TXT+18(4),DOUBLEWD  UNPACK INTO MESSAGE
         OI    ERR5TXT+21,X'F0'        CLEAR SIGN
         MVC   MSG2,ERR5TXT            MESSAGE INTO SCREEN
         B     ERROR                   DISPLAY SCREEN
REUSE    DS    0H
         NI    INFCODE3,255-$EDTCOPY   RESET COPY FLAG
         NI    INFCODE4,255-$SAMEDS    RESET FLAG
         TM    INFCODE,SEQALC          SEQUENTIAL DATASET ?
         BO    REUSE1                  DO NOT MODIFY JFCB
         RDJFCB RPFWORK                READ THE JFCB
         MVC   JFCB+44(8),=CL8'TEMPRPF' MOVE MEMBERNAME
         OI    JFCB+86,X'01'           FLAG MEMBER BIT
         TM    INFCODE2,ENQUED         IS THERE AN OUTSTANDING ENQUEUE
         BNO   ENQ2                    NO: ENQ IMMED.
         CLC   DSNAME,RNAME            RNAME IS DSNAME
         BNE   RENQ2                   NO: DEQ AND ENQ
         CLC   MEMBER,RNAME+44         RNAME+44 = MEMBER ?
         BNE   RENQ2                   NO: DEQ AND ENQ
         B     OPEN2                   OPEN THE FILE ALREADY ENQ'D
RENQ2    DS    0H                      DEQ
         LA    R3,QNAME                QNAME
         LA    R4,RNAME                RNAME
         DEQ   ((3),(4),52,SYSTEMS),RET=HAVE
         NI    INFCODE2,255-ENQUED     TURN OFF FLAG
ENQ2     DS    0H                      ENQ
         MVC   QNAME(8),=CL8'SPFEDIT'
         MVC   RNAME(44),DSNAME
         MVC   RNAME+44(8),MEMBER      ONLY IF PDS. ELSE BLANKS
         LA    R3,QNAME                QNAME
         LA    R4,RNAME                RNAME
         ENQ   ((3),(4),E,52,SYSTEMS),RET=USE
         LTR   R15,R15
         BZ    OPEN2                ** SEE PAGE 244 245 MACRO MANUAL
         MVC   MSG2,ERR9TXT            MOVE ERROR MESSAGE
         B     ERROR                   ERROR PROCESSING
OPEN2    DS    0H
         OI    INFCODE2,ENQUED          SET FLAG
         LA    R1,RPFWORK              DCB ADDRESS
         ST    R1,RPFDCB               SAVE THAT ADDRESS FOR ESTAE
         ESTAE RECOVER,PARAM=((11))    ESTABLISH RECOVERY ROUTINE
         OPEN  (RPFWORK,(OUTPUT)),TYPE=J REWRITE JFCB AND OPEN
         B     REUSE2
REUSE1   LA    R1,RPFWORK              DCB ADDRESS
         ST    R1,RPFDCB               SAVE THAT ADDRESS FOR ESTAE
         ESTAE RECOVER,PARAM=((11))    ESTABLISH RECOVERY ROUTINE
         OPEN  (RPFWORK,(OUTPUT))      OPEN FILE
REUSE2   TM    RPFWORK+DCBOFLGS-IHADCB,DCBOFOPN  DCB OPENED ?
         BO    REUSE3                  YES: OK DS IS OPENED
         CLOSE RPFWORK                 NO: CLOSE DCB IN ERROR
         ESTAE 0                       UNSTACK RECOVERY ROUTINE
         TM    INFCODE2,ENQUED         ENQUEED ?
         BNO   REUSE2A                 NO: BRANCH
         LA    R3,QNAME                QNAME
         LA    R4,RNAME                RNAME
         DEQ   ((3),(4),52,SYSTEMS),RET=HAVE
         NI    INFCODE2,255-ENQUED     TURN OFF FLAG
REUSE2A  DS    0H
         MVC   MSG2,ERR8TXT            MOVE ERROR MSG IN SCREEN
         B     ERROR                   PRODUCE ERROR MESSAGE
REUSE3   LA    R1,RPFWORK              LOAD DCBADDRESS
         ST    R1,RPFDCB               INTO COMMAREA FOR ESTAE
         TITLE 'SAVE THE WORKSPACE'
SAVE     ZAP   PUTCNT,=P'0'            ZERO COUNT
         L     R3,FIRSTREC             FIRST RECORD IN WORKSPACE
         L     R5,LASTREC              LAST RECORD IN WORKSPACE
         LA    R5,1(R5)                +1 FOR BXLE
         LA    R4,NEXTENT              LENGTH OF RECORD
         USING WORKSPCE,R3             ADDRESS WORKSPACE RECORD
SAVE03   DS    0H
         TM    WORKFLGS,$INSERTD       Inserted line without data?
         BO    SAVE04                  Yes: do not save that line
         MVC   RECORD,WORKDATA         MOVE RECORD BELOW 16 MB
         LA    R15,PUTIT               LOAD ROUTINE ADDRESS
*        BASSM R14,R15                 EXEC THIS ROUTINE IN 24 BIT MODE
         BALR  R14,R15                 EXEC THIS ROUTINE
         AP    PUTCNT,=P'1'            INCREMENT COUNT
SAVE04   DS    0H
         DROP  R3
         BXLE  R3,R4,SAVE03            LOOP UNTIL ALL DONE
         CLOSE RPFWORK                 CLOSE FILE
         FREEPOOL RPFWORK              FREE BUFFERS
         ESTAE 0                       UNSTACK THE RECOVERY ROUTINE
         TM    INFCODE,SEQALC      SEQUENTIAL DATASET ALLOCATED ?
         BO    SAVEEND             YES: PROCEED
         OPEN  (RPFWORK2,(UPDAT))  OPEN PDSDCB
         MVC   BLDLMEM,MEMBER      MOVE MEMBER INTO LIST
         BLDL  RPFWORK2,BLDLAREA   RETRIEVE DIR. INFORMATIE
         LTR   R15,R15             BLDL OK ?
         BNZ   NEWSTATS            NO: MEMBER DOES NOT EXIST
         NI    BLDLC,B'00011111'   CLEAR FIRST THREE BITS
         MVC   SAVEC,BLDLC             SAVE BLDLC BYTE
         MVC   SAVEREST,INPREST        SAVE "other" user data
         CLI   BLDLC,X'0F'         DOES IT CONTAIN 15 HALFWORDS UDATA?
         BL    NEWSTATS                Low: add ISPF like statistics
         TM    INPDATEI+3,X'0C'    DOES DATE CONTAIN A SIGN ?     @2000
         BNO   NEWSTATS            NO: ADD STATISTICS IN RPF NEW FMT
         TM    INPDATE+3,X'0C'     DOES DATE CONTAIN A SIGN ?
         BNO   NEWSTATS            NO: ADD STATISTICS IN RPF NEW FMT
         CLI   INPTIME,X'23'       TEST TIME FIELD (PACKED UNSIGNED HH)
         BH    NEWSTATS            NO: ADD STATISTICS IN RPF NW FMT
         CLI   INPTIME+1,X'59'     TEST TIME FIELD (PACKED UNSIGNED MM)
         BH    NEWSTATS            NO: ADD STATISTICS IN RPF NW FMT
         B     UPDSTATS                UPDATE THE STATISTICS
NEWSTATS L     R2,X'4C'                GET CVTADDRESS
         OI    SW,$NEWSTAT             SET NEW STATS FLAG
         ZAP   REPDATEI,56(4,R2)       MOVE CVTDATE               @2000
         OI    REPDATEI+3,X'0F'        MAKE SIGN ABSOLUTE         @2000
         MVI   REPVERS,X'50'           MOVE VERSION NUMBER (80 IS RPF)
         XC    REPMOD,REPMOD           MOVE MOD. LEVEL
         XC    REPCARDM,REPCARDM       CLEAR FIELD
         ZAP   DWB,PUTCNT              # CARDS
         CVB   R2,DWB                  CONVERT TO DECIMAL
         STCM  R2,R3,REPCARDI          STORE THE RESULT INIT
         B     CONSTATS                CONTINUE
UPDSTATS MVC   REPDATEI,INPDATEI       COPY
         MVC   REPCARDI,INPCARDI         THE
         MVC   REPCARDM,INPCARDM           FIELDS
         MVC   REPVERS,INPVERS         MOVE VERSION NUMBER
         MVC   REPMOD,INPMOD           MOVE MODIFICATION NUMBER
         CLI   REPMOD,X'63'            IS MOD. LEVEL ALREADY 99
         BNL   TESTVERS                YES: MAKE 00, INCREASE VERSION
         XR    R2,R2                   CLEAR REG 2
         IC    R2,REPMOD               INSERT REP MOD
         LA    R2,1(R2)                INCREASE BY 1
         STC   R2,REPMOD               AND STORE
         B     CONSTATS                BRANCH
TESTVERS DS    0H                      INCREASE VERSION
         CLI   REPVERS,X'63'           99 OR HIGHER ?
         BNL   CONSTATS                YES: LEAVE VERSION:MOD UNCHANGED
         XR    R2,R2                   CLEAR REG 2
         IC    R2,REPVERS              VERSION NUMBER
         LA    R2,1(R2)                INCREASE BY ONE
         STC   R2,REPVERS              AND SAVE IT
         XC    REPMOD,REPMOD           CLEAR MOD. LEVEL
CONSTATS MVC   SCR2MEM,MEMBER          MOVE MEMBERNAME
         MVC   SCR1MEM,=CL8'TEMPRPF'   MOVE MEMBERNAME
         STOW  RPFWORK2,SCR2MEM,D      DELETE OLD MEMBER
         STOW  RPFWORK2,SCR1MEM,C      RENAME 'TEMPRPF' INTO 'MEMBER'
         MVC   BLDLAREA+4(8),SCR2MEM   MOVE MEMBER TO BUILD LIST
         BLDL  RPFWORK2,BLDLAREA       RETRIEVE DIR INFORMATION
         L     R2,X'4C'                GET CVTADDRESS
         ZAP   REPDATE,56(4,R2)        MOVE CVTDATE               @2000
         OI    REPDATE+3,X'0F'         MAKE SIGN ABSOLUTE
         MVC   REPUID(10),BLANKS       BLANK FIELD FIRST
         MVC   REPUID(7),USERID        MOVE USERID FROM COMMAREA
         MVC   REPMEM,SCR2MEM          MOVE MEMBERNAME
         XC    REPNULL,REPNULL         CLEAR THE NULLS FIELD
         MVC   REPTTR,BLDLTTR          DUPLICATE TTR
         ZAP   DWB,PUTCNT              # CARDS
         CVB   R2,DWB                  CONVERT TO DECIMAL
         STCM  R2,R3,REPCARDS          STORE THE RESULT
         TIME  DEC                     GET TIME
         SRL   R0,16                   SHIFT OUT FRACTIONS OF SECONDS
         STCM  R0,3,REPTIME        MOVE TIME INTO REPAREA WITHOUT SIGN
         TM    SW,$NEWSTAT             NEW STATS REQUESTED ?
         BNZ   CONST01                 BRANCH
         MVC   REPREST,SAVEREST        COPY DATA FROM OTHER USER
         MVC   REPC,SAVEC              COPY 'C' BYTE
         B     CONST02                 AND BRANCH
CONST01  DS    0H
         NI    BLDLC,X'FF'-X'1F'       SET COUNT BITS OFF
         XR    R2,R2                   CLEAR REG 2
         IC    R2,BLDLC                INSERT 'C' BYTE
         O     R2,=F'15'               SET COUNT
         STC   R2,REPC                 AND STORE THE RESULT
CONST02  DS    0H
         STOW  RPFWORK2,REPAREA,R  REPLACE DIR INFO
         MVC   CMDAREA,=A(REPAREA) SET A(MEMBER+TTRC+USERDATA) IN CMDA.
         CLOSE RPFWORK2
SAVEEND  XC    RTNCODE,RTNCODE         0 INTO RET. CODE - MEMBER SAVED
         TM    INFCODE2,ENQUED         ENQUEED ?
         BNO   SAVEEND2                NO: BRANCH
         LA    R3,QNAME                QNAME
         LA    R4,RNAME                RNAME
         DEQ   ((3),(4),52,SYSTEMS),RET=HAVE
         NI    INFCODE2,255-ENQUED TURN OFF FLAG
SAVEEND2 DS    0H
         B     RETURN                  RETURN
PUTIT    DS    0H                      ENTERED IN 24 BIT MODE
         ST    R14,SAVE14PU            SAVE REGISTER 14
         PUT   RPFWORK,RECORD          WRITE RECORD
         L     R14,SAVE14PU            RESTORE R14
*        BSM   R0,R14                  RETURN TO 31 BIT CALLER
         BR    R14                     RETURN
         TITLE 'RETURN TO CALLER'
RETURN   DS    0H
         TM    INFCODE2,ENQUED         OUTSTANDING ENQ ?
         BNO   RETURN1                 NO: EXIT
         LA    R3,QNAME
         LA    R4,RNAME
         DEQ   ((3),(4),52,SYSTEMS),RET=HAVE FREE THE RESOURCE
         NI    INFCODE2,255-ENQUED TURN OFF FLAG
RETURN1  DS    0H
         L     R13,4(R13)              PICKUP CALLERS SAVEAREA
         LM    R14,R12,12(R13)         RESTORE REGISTERS
         SR    R15,R15                 RETURN CODE = 0000
         BR    R14                     RETURN TO MAIN MENU
         TITLE 'PROCESS ERROR AND RETRY MENU'
ERROR    MVC   SCR3VOL(6),BUFVOL       VOLUME INTO SCREEN
         MVC   SCR3DSN(44),BUFDSN      DSNAME INTO SCREEN
         MVC   SCR3MEM(8),BUFMEM       MEMBER INTO SCREEN
         TPUT  SCR2TXT,SCR2LEN,FULLSCR DISPLAY SCREEN
         MVI   BUFCMD,C' '         INIT COMMAND
         B     GET1
         TITLE 'ESTAE RECOVERY ROUTINE, RETRY WITH ABEND913'
RECOVER  EQU   *
         DROP  ,
         USING *,R15                   GET TEMPORARY ADDRESSABILITY
         LM    R11,R13,HEREADDR        RESTORE REGISTERS
         DROP  R15                     KILL LOCAL ADDRESSABILITY
         USING SAVEAREA,R13,R12        RESTORE ADDRESSING
         USING SDWA,R1                 GET ADDRESSABILITY OVER SDWA
         ESTAE 0                       DELETE THIS RECOVERY ROUTINE
         XR    R2,R2                   CLEAR REGISTER 2
         ICM   R2,7,SDWACMPC           INSERT COMPLETION CODE
         SRL   R2,12                   SHIFT OUT USER COMPLETION CODE
         CLM   R2,3,=X'0913'           AUTHORIZATION ABEND ?
         BE    SETRP04                 YES: TRY TO RETRY
         SETRP RC=0                    ELSE PERCOLATE
         BR    R14                     BRANCH TO RTM
         DROP  R1                      KILL ADDRESSABILITY
SETRP04  DS    0H
         SETRP RC=4,RETADDR=RET,RETREGS=NO,FRESDWA=YES
         BR    R14                     BRANCH TO RTM
         TITLE 'ESTAE RETRY ROUTINE'
RET      EQU   *
         DROP  ,
         USING *,R15                   LOCAL ADDR
         LM    R11,R13,HEREADDR        RESTORE REGISTERS
         DROP  R15                     KILL LOCAL ADDRESSABILITY
         USING SAVEAREA,R13,R12        RESTORE ADDRESSABILITY
         USING COMMAREA,R11
         TM    RPFWORK+DCBOFLGS-IHADCB,DCBOFOPN DS OPENED ?
         BZ    RET1                    NO: BRANCH
         CLOSE RPFWORK                 CLOSE THAT POSSIBLE DCB
         FREEPOOL RPFWORK              AND FREE THE BUFFERS
RET1     TM    INFCODE2,ENQUED         ANY ENQ'S OUTSTANDING ?
         BNO   RET2                    NO, ALLRIGHT
         LA    R3,QNAME
         LA    R4,RNAME
         DEQ   ((3),(4),52,SYSTEMS),RET=HAVE
         NI    INFCODE2,255-ENQUED     TURN OFF FLAG
RET2     DS    0H
         CLC   BUFMEM,SAVEMBR          BUFMEM STILL THE SAME ?
         BE    *+10                    YES: BRANCH
         MVC   SAVEMBR,BUFMEM          ELSE RESET SAVEMBR
*+10
         MVC   MSG2,ERRATXT            MOVE ERROR MESSAGE
         MVC   MSG3,ERRATXT            MOVE ERROR MESSAGE
         B     ERROR                   RETRY SUCCESSFUL
         TITLE 'SHIFT ROUTINE'
*---------------------------------------------------------------------*
*                                                                     *
*        S H I F T - SUBROUTINE, REPLACE TRAILING SPACES WITH LEADING *
*                    ZEROES.                                          *
*                                                                     *
*                    INPUT:  NUMFLD, FIELD TO BE SHIFTED, MAX 8 CHAR, *
*                            FILLCHAR, FILL CHARACTER.                *
*                    OUTPUT: NUMFLD, TRAILING SPACES REPLACED WITH    *
*                                    FILL CHARACTER.                  *
*                                                                     *
*------------------------------------------ (C)-2001 SKYBIRD SYSTEMS -*
SHIFT    ST    14,SAVE14               SAVE REG.14
         LA    R3,8                    MAX 8 CHARS
SHIFT01  CLI   NUMFLD+7,X'40'          LAST CHAR SPACE ?
         BNE   SHIFT99                 NO: GOTO EXIT
         MVC   WRKFLD+1(7),NUMFLD      MOVE FIRST 5 CHARS INTO WRKFLD+1
         MVC   WRKFLD(1),FILLCHAR      INSERT FILLCHAR
         MVC   NUMFLD,WRKFLD           RESTORE NUMFLD
         BCT   R3,SHIFT01              SHIFT MAX 8 TIMES
SHIFT99  L     R14,SAVE14              RESTORE REG.14
         BR    R14                     RETURN
         TITLE 'SCREEN FORMATS'
SCR1TXT  DC    X'27F5C21140401DF8' WRITE ERASE / R01,C00
SCR1UID  DC    CL79'RPF Save menu: userid= XXXXXXX---------------------C
               ----------------------------'
         DC    X'1DF0'
SCR1LEN  EQU   *-SCR1TXT
SCR2TXT  DC    X'27F1C21140401DF8' WRITE NOERAS/ R01,C00
         DC    X'11407A'           R01,C59
MSG2     DS    CL22
         DC    X'11C150',X'1DF8',C'Option ===>',X'1DC113',X'001DF0'
SCR2LEN  EQU   *-SCR2TXT
SCR3TXT  DC    X'27F1C21140401DF8' WRITE NOERAS/ R01,C00
         DC    X'11407A'           R01,C59
MSG3     DC    CL22'----------------------'
         DC    X'11C150',X'1DF8',C'Option ===>',X'1DC113'
SCR3CMD  DC    X'001DF0'
         DC    X'11C3F3',AL3(WHITE),C'1  ',AL3(TURQ)         R04,C04
         DC    C'SAVE     OS - ',AL3(GREEN)
         DC    C'Change allocations and save'
         DC    X'11C5C3',AL3(WHITE),C'2  ',AL3(TURQ)         R05,C04
         DC    C'RE-USE   OS - ',AL3(GREEN)
         DC    C'Re-use member or dataset'
         DC    X'11C6D3',AL3(WHITE),C'3  ',AL3(TURQ)         R06,C04
         DC    C'SAVE  LIBRA - ',AL3(GREEN)
         DC    C'Save into LIBRARIAN master'
         DC    X'11C7E3',AL3(WHITE),C'4  ',AL3(TURQ)         R07,C04
         DC    C'SAVE    PAN - ',AL3(GREEN)
         DC    C'Save into PANVALET library'
         DC    X'11C8F3',AL3(WHITE),C'X  ',AL3(TURQ)         R08,C04
         DC    C'EXIT        - ',AL3(GREEN)
         DC    C'Exit (do not save)'
         DC    X'11D161',AL3(YELLOW)   R15,C02
         DC    C'Data set information (only if opt 1 or 2 selected)'
         DC    X'11D440'           R17,C01
         DC    X'1DF5',AL3(TURQ),C'Member=',X'1DC913',AL3(RED)
SCR3MEM  DC    CL8' ',X'1DF5',AL3(TURQ),C',dsname=',X'1DC9',AL3(RED)
SCR3DSN  DC    CL44' ',X'1DF0'
         DC    X'11D550'           R18,C01
         DC    X'1DF5',AL3(TURQ),C'Volume=',X'1DC9',AL3(RED)
SCR3VOL  DC    CL6' ',X'1DF5',X'11D5E2',AL3(TURQ) R18,C19
         DC    C',ddname=',X'1DF0',AL3(WHITE)
SCR3DDN  DC    C'RPFWORK '
SCR3LAST DC    X'115CF1',AL3(TURQ)     R24,C02
         DC    C'Hit ',AL3(WHITE),C'PF03/15 ',AL3(TURQ)
         DC    C'to return (no save will be done)'
SCR3LEN  EQU   *-SCR3TXT
ERR1TXT  DC    CL22'--------Invalid option'
ERR4TXT  DC    CL22'Member XXXXXXXX exists'
ERR5TXT  DC    CL22'Alloc failed, rc= XXXX'
ERR6TXT  DC    CL22'---Member name invalid'
ERR7TXT  DC    CL22'--''TEMPRPF'' restricted'
ERR8TXT  DC    CL22'----Dataset not opened'
ERR9TXT  DC    CL22'-Dataset/member in use'
ERRATXT  DC    CL22'--Authorization failed'
         TITLE 'VARIABLES, CONSTANTS AND CONTROL BLOCKS'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
DAIRPARM DC    F'0'
BUFCMD   DS    C
NUMFLD   DC    CL8' '              FOR SHIFT ROUTINE
WRKFLD   DC    CL8' '              FOR SHIFT ROUTINE
FILLCHAR DC    C' '                FOR SHIFT ROUTINE
TRTAB    DC    256X'FF'
         ORG   TRTAB+C'A'
         DC    9X'00'
         ORG   TRTAB+C'J'
         DC    9X'00'
         ORG   TRTAB+C'S'
         DC    8X'00'
         ORG   TRTAB+C'@'
         DC    X'00'
         ORG   TRTAB+C'#'
         DC    X'00'
         ORG   TRTAB+C'$'
         DC    X'00'
         ORG
RECORD   DS    CL80                FOR PUTIT ROUTINE
DWB      DS    D                   FOR CVD/CVB INSTRUCTIONS
HEREADDR DS    3F                  SAVE AREA R11,12,13 FOR ESTAE
SAVE14   DS    F
SAVE14PU DS    F
F0000    DC    F'0'
F0008    DC    F'8'
F0032    DC    F'32'
*
SW       DC    X'00'
$NEWSTAT EQU   128                 New ISPF/RPF stats requested
SAVEC    DS    X                   Copy of "C" byte
SAVEREST DS    CL32                Copy of User data not from ISPF/RPF
*
ZERO     DC    PL3'0'
SAVEMBR  DS    CL8
BUFFER   DC    CL120' '
BUFMEM   DS    CL8
BUFVOL   DS    CL6
BUFDSN   DS    CL44
PUTCNT   DC    PL3'0'
SCR1MEM  DS    CL8
SCR2MEM  DS    CL8
DOUBLEWD DC    D'0'
         DS    0F
EXLST    DC    X'87',AL3(JFCB)
JFCB     DS    CL176
BLDLAREA DS    0H
         DC    AL2(1)              # OF MEMBERS TO BE RETRIEVED
         DC    AL2(76)               LENGTH OF ENTRY
BLDLMEM  DC    CL8' '
BLDLTTR  DS    XL3
BLDLKZ   DS    CL2
BLDLC    DS    X
BLDLUSER DS    0CL62                OBTAINED USERDATA IF ANY
INPVERS  DS    C
INPMOD   DS    C
INPNULL  DS    XL2                                                @2000
INPDATEI DS    PL4                                                @2000
INPDATE  DS    PL4
INPTIME  DS    XL2
INPCARDS DS    XL2
INPCARDI DS    XL2
INPCARDM DS    XL2
INPUID   DS    CL8
         DS    CL2
INPREST  DS    CL32                    Data not from ISPF/RPF
*
REPAREA  DS    0H
REPMEM   DS    CL8
REPTTR   DS    XL3
REPC     DS    X
REPUSER  DS    0CL62
REPVERS  DS    X
REPMOD   DS    X
REPNULL  DS    XL2                                                @2000
REPDATEI DS    PL4                                                @2000
REPDATE  DS    PL4
REPTIME  DS    XL2
REPCARDS DS    XL2
REPCARDI DS    XL2
REPCARDM DS    XL2
REPUID   DS    CL8
         DS    CL2
REPREST  DS    CL32                    Data not from ISPF/RPF
*
         EJECT
RPFWORK  DCB   DDNAME=RPFWORK,DSORG=PS,MACRF=PM,LRECL=80,              X
               EXLST=EXLST
RPFWORK2 DCB   DSORG=PO,DDNAME=RPFWORK2,MACRF=(R,W)
         EJECT
         LTORG
         TITLE 'DSECTS USED BY RPFSAVE'
COMMAREA RPFCOMM
         PRINT NOGEN
         IHASDWA
         DCBD  DSORG=PS,DEVD=DA
         END
