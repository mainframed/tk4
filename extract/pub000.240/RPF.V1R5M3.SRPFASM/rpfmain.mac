         PUNCH ' ALIAS RPF'
         PUNCH ' SETCODE AC(1)'
RPFMAIN  TITLE 'R P F  FULLSCREEN TSO COMMAND PROCESSOR'
*---------------------------------------------------------------------*
*                                                                     *
*       PROGRAM:        RPFMAIN.                                      *
*       ALIAS:          RPF.                                          *
*                                                                     *
*       FUNCTION:       RPF IS A TSO COMMAND PROCESSOR, WHICH WILL    *
*                       GIVE YOU FULL SCREEN SUPPORT ON YOUR TSO      *
*                       TERMINAL.                                     *
*                       THE FUNCTIONS OF RPF ARE:                     *
*                       01. EDIT AND CHANGE DATA ON YOUR SCREEN       *
*                           (NONUM OR NUM).                           *
*                       02. PERFORM PDS MAINTENANCE. (LIKE DELETE AND *
*                           RENAME OF PDS MEMBERS)                    *
*                       03. DISPLAY HELP INFORMATION ABOUT RPF        *
*                       04. FOREGROUND ASSEMBLY AND LINK              *
*                       05. CREATION OF A NEW DATASET                 *
*                       07. OPER MODE (DISPLAY THE RUNNING TASKS IN   *
*                           THE SYSTEM)                               *
*                       08. TEST FUNCTION (AUTHORIZED) TEST THE LOGIC *
*                           OF RPF (RPF TEST IS MADE ONLY FOR THE     *
*                           BUILDERS OF RPF).                         *
*                       09. DELETE DATASETS.                          *
*                       10. PERFORM VTOC FUNCTIONS                    *
*                       11. ALTER/DISPLAY SESSION DEFAULTS            *
*                       12. EDIT A LIBRARIAN MODULE OR INDEX.         *
*                       13. ADD/REP/DLM LIBRARIAN MEMBERS             *
*                       14. EDIT A PANVALET MODULE OR INDEX.          *
*                       15. ADD/REP PANVALET MEMBERS                  *
*                       16. EXECUTE TSO COMMANDS                      *
*                       17. View   A DATASET OR OUTPUT LISTING.       *
*                       18. INVOKE RPFUSER1 EXIT.                     *
*                                                                     *
*        PROCESSOR:     OS/VS ASSEMBLER.                              *
*        ENVIRONMENT:   OS/VS2 TSO/VTAM                               *
*        CONTROL BLOCK: RPFCOMM WHICH SHOULD BE PASSED VIA REG 1 TO   *
*                       THE RPF MODULES                               *
*        USER EXIT:     RPFUSER1: FUNCTION -2- IN MAIN MENU           *
*                                                                     *
*        NOTE:          EDIT THIS MEMBER IN 'ASIS' MODE,              *
*                       BECAUSE THERE IS LOWER CASE TEXT IN THIS      *
*                       MODULE.                                       *
*                                                                     *
*        RPF V1R5M0     VERSION JULY 2002.                            *
*        DOCUMENTATION HANDBOOK RPFUGUID.PDF                          *
*                                                                     *
*        AUTHOR:        ROB PRINS.                                    *
*                                                                     *
*        Modifications:                                               *
*        2001/06/02 RPr: Goto allocation menu (RPFDATAL) if returned  *
*                        from EDIT or BROWSE instead of return to MAIN*
*        2002/07/17 Rpr: Process the selection immediately if CMAINOPT*
*                        is not blank. These options are entered      *
*                        with the '=n'/'=n.n' cmd from BROWSE/EDIT/PDS*
*        2002/07/31 Rpr: Add name of TSO Proc in main menu         @rp2
*        2002/08/24 Rpr: Block editing RECFM=V(B) data             @rp3
*        2002/08/31 Rpr: Load RPFVTOC1 module                      @rp4
*        2002/10/11 Rpr: Add Calendar in MAIN menu                 @rp5
*        2002/10/16 Rpr: Add Logo in menu                          @rp6
*        2002/11/01 Rpr: Display logo only initially               @rp7
*        2002/11/07 Rpr: Menu in extended colors                   @rp8
*        2003/02/14 Rpr: Ignore the '=' before the option          @rp9
*        2004/01/29 Rpr: Calender re-calculated after refresh      @rpa
*        2006/03/20 Rpr: Build date/time in splash screen          @rpb
*                                                                     *
*------------------------------------------ (C)-2006 SKYBIRD SYSTEMS -*
         SPACE 2
RPFMAIN  CSECT
         SAVE  (14,12),,*
         USING RPFMAIN,R15
         LA    R11,SAVE
         ST    R13,SAVE+4
         ST    R11,8(0,R13)
         LR    R13,R11
         B     START
         DS    0F
SAVE     DC    18F'-1'
         DC    CL8'&SYSDATE',C' ',CL8'&SYSTIME'
START    DS    0H
         DROP  R15
         LA    R12,2048(R13)
         LA    R12,2048(R12)
         USING SAVE,R13,R12
         L     R11,=V(COMMAREA)
         STM   R11,R13,HEREADR         SAVE BASE REGISTERS+PARM
         USING COMMAREA,R11            ADDRESS COMMAREA
         XC    INFCODE,INFCODE         CLEAR BITSTRING OF RPF
         ST    R1,COMMCPPL             SAVE CPPL ADDDRESS
         L     R2,0(R1)                ADDRESS OF TMP COMMAND BUFFER
         XR    R3,R3                   CLEAR REGISTER 3
         IC    R3,3(R2)                OFFSET OF FIRST OP. IN CMD BUF
         AR    R2,R3                   ADD OFFSET
         CLC   4(4,R2),=C'FAST'        BYPASS DATABASE ?
         BE    FAST                    YES: SET UP BIT
         CLC   4(4,R2),=X'8681A2A3'    FAST ENTERED IN LOWER-CASE ?
         BNE   NOFAST                  NO: USE RPF DATABASE
FAST     OI    INFCODE,NODB            MARK BYPASS DATABASE
NOFAST   DS    0H
         MVC   SCR1TXT(2),=X'27F5'     NORMAL WRITE
         MVC   SCR1SBA,=X'115DE3'      R24,C52 FOR LAST LINE       @rp8
         GTSIZE ,                      OBTAIN SCREEN SIZE
         STH   R0,COMMSIZE             Save screen size
         CH    R0,=H'24'               AT LEAST 24 LINES ?
         BL    NOFULLSC                NO: ISSUE MESSAGE AND TERMINATE
         CLC   COMMSIZE,=H'43'         Model 4 screen ?
         BE    WRALT43                 YES: MOVE WRITE ALTERNATE 277E
         CLC   COMMSIZE,=H'32'         Model 3 screen ?
         BE    WRALT32                 YES: MOVE WRITE ALTERNATE 277E
         B     ENDWR                   BRANCH
WRALT32  DS    0H
         MVC   SCR1TXT(2),=X'277E'     WRITE ALTERNATE
         MVC   SCR1SBA,=X'11E7E3'      R32,C52 FOR LAST LINE       @rp8
         B     ENDWR                   BRANCH
WRALT43  DS    0H
         MVC   SCR1TXT(2),=X'277E'     WRITE ALTERNATE
         MVC   SCR1SBA,=X'11F5D3'      R43,C52 FOR LAST LINE       @rp8
ENDWR    DS    0H
         TIME  DEC
         SRL   R0,4
         ST    R0,STIME
         XR    R0,0
         ST    R0,EPNEXT
         ST    R0,RTNCODE
         LOAD  EP=RPFCHNG
         ST    R0,EPCHNG              'C' SUBCOMMAND OF EDIT
         LOAD  EP=RPFDAIR
         ST    R0,EPDAIR               DYNAMIC ALLOCATION INTERFACE
         LOAD  EP=RPFDATAL
         ST    R0,EPDATAL              ALLOCATION MENU EDIT
         LOAD  EP=RPFALLOC
         ST    R0,EPALLOC              allocation dataset menu
         LOAD  EP=RPFEDIT
         ST    R0,EPEDIT
         LOAD  EP=RPFEDITL
         ST    R0,EPEDITL              WORKSPACE LOADER
         LOAD  EP=RPFUTIL
         ST    R0,EPUTIL               UTILITY MENU
         LOAD  EP=RPFFIND
         ST    R0,EPFIND               'F' SUBCOMMAND OF EDIT
         LOAD  EP=RPFSAVE
         ST    R0,EPSAVE               'SAVE' SUBCOMMAND OF EDIT
         LOAD  EP=RPFVTOC1                                         @rp4
         ST    R0,EPVTOC1              RPFVTOC selection list      @rp4
         LOAD  EP=RPFCNVDT                                         @rp5
         ST    R0,EPCNVDT              Convert date routine        @rp5
         LA    R0,COMMAREA
         ST    R0,PARMLIST
         LINK  EP=RPFINIT,PARAM=COMMAREA,VL=1
         MVC   SCR1UID(7),USERID
         MVC   CMAINOPT,BLANKS         Blank opts from edit/browse/pds
         MVC   MSG1(L'COP1TXT),COP1TXT COPYRIGHT INTO MESSAGE
         MVC   SPLASH3+32(17),SAVE+72  Move Build number           @rpb
         CLC   RTNCODE,F0000           INITIALIZATION ALLRIGHT ?
         BE    ESTAE1                  YES: PROCEED.
         L     R2,RTNCODE              RC RPFINIT
         CVD   R2,DBW                  CONVERT TO DECIMAL
         UNPK  ERRCTXT+18(4),DBW       UNPK RTNCODE
         OI    ERRCTXT+21,X'F0'        CLEAR SIGN
         XC    RTNCODE,RTNCODE         RESET RTNCODE
         MVC   MSG1,ERRCTXT            MOVE ERROR MESSAGE
ESTAE1   DS    0H
         ESTAE STAEEXIT,PARAM=COMMAREA  PROTECT AGAINST ABENDS
PUT1     BAL   R14,TIME
         CALL  CALENDAR,((11))         Call calendar routine       @rpa
         LM    R4,R5,0(R1)             Pickup output               @rpa
         MVC   SCR1CAL1,0(R4)          Month                       @rpa
         MVC   SCR1CAL3,0(R5)          Days                        @rpa
         MVC   SCR1CAL4,42(R5)         Days                        @rpa
         MVC   SCR1CAL5,84(R5)         Days                        @rpa
         MVC   SCR1CAL6,126(R5)        Days                        @rpa
         MVC   SCR1CAL7,168(R5)        Days                        @rpa
         MVC   SCR1CAL8,210(R5)        Days                        @rpa
         MVC   CMDAREA,BLANKS          Blank cmdarea field in commarea
         NI    INFCODE,X'FF'-NOTLOAD MARK NOTLOAD OFF
         NI    INFCODE2,255-$RETURN-$OPTION SET INDICATORS OFF
         NI    INFCODE3,255-$PDSBR-$PDSEDIT mark browse/edit off
         XC    RPFDCB,RPFDCB           CLEAR RPFDCB FIELD
         CLC   CMAINOPT,BLANKS         Options from edit/brw/pds?
         BE    PUT1#                   No: branch
         MVC   BUFFER,CMAINOPT         Move options to buffer
         MVC   CMAINOPT,BLANKS         Clear the options
         B     FUNC0                   And process
PUT1#    DS    0H
         MVC   SCR1TME(8),TIMEDISP+2
         L     R2,CVTPTR               Pointer cvt
         USING CVT,R2                  Address it
         L     R3,CVTSMCA              Point to smca
         USING SMCABASE,R3             Address smca
         MVC   SCR1SID,SMCASID         Move system id
         DROP  R2,R3                   Kill addressability
         EXTRACT ADDRTIOT,FIELDS=TIOT  Obtain task io table        @rp2
         L     R2,ADDRTIOT             Obtain address of TIOT      @rp2
         MVC   SCR1PRC,16(R2)          Move stepname of TSO-user   @rp2
         TM    INFCODE2,$NOGO
         BNO   PUT1A
         MVC   MSG1(L'ERR3TXT),ERR3TXT
PUT1A    TPUT  SCR1TXT,SCR1LEN,FULLSCR MAIN MENU ON SCREEN
         MVC   BUFFER,BLANKS           BLANK OUTPUT BUFFER
         MVC   SPLASH1,BLANKS          Blank                       @rp7
         MVC   SPLASH2,BLANKS               logo                   @rp7
         MVC   SPLASH3,BLANKS                                      @rp7
         MVC   SPLASH4,BLANKS                                      @rp7
         MVC   SPLASH5,BLANKS                                      @rp7
         MVC   SPLASH6,BLANKS                                      @rp7
         MVC   SPLASH7,BLANKS                                      @rp7
         MVC   USPL1,=AL3(HDEFAULT)    Remove USCORE               @rp8
         MVC   USPL2,=AL3(HDEFAULT)    Remove USCORE               @rp8
GET1     LA    R5,BUFFER               OUTPUT FORMATTED BUFFER
         L     R3,COMMSCR              LOAD SCREEN ADDRESS
         TGET  (R3),40,ASIS            READ THE SCREEN
         LR    R4,R1                   SAVE LENGTH
*        DO AN TGET ASIS TO OBTAIN THE AID BYTE
*        AFTER THE TGET ASIS REMOVE THE SBA'S FROM THE OUTPUT
         CLI   0(R3),X'F3'             PFK3 (END) PRESSED
         BE    END                     YES: TERMINATE RPF
         CLI   0(R3),X'C3'             PFK15 (END) PRESSED
         BE    END                     YES: TERMINATE RPF
         XR    R2,R2                    INIT OUTPUT LENGTH
         LR    R4,R1                   SAVE INPUT LENGTH
         SH    R4,=H'3'                SKIP AID AND CURSOR ADDRESS
         C     R4,F0000                REGISTER ZERO
         BNH   CONV099                 NOT HIGH: END OF ROUTINE
         LA    R3,3(R3)
         LA    R5,BUFFER               OUTPUT ADDRESS
*        INPUT SCREEN IS POINTED TO BY R3
*        OUTPUT SCREEN IS POINTED TO BY R5
*        THE NEXT SECTION WILL REMOVE ALL THE SBA'S
CONV001  CLI   0(R3),X'11'             SET BUFFER ADDRESS ?
         BE    CONV003                 YES: SKIP NEXT 3 BYTES INPUT
CONV002  MVC   0(1,R5),0(R3)           MOVE INPUT BYTE
         LA    R3,1(R3)                NEXT BYTE INPUT
         LA    R2,1(R2)                COUNT OUTPUT BYTES
         LA    R5,1(R5)                NEXT BYTE OUTPUT
         BCT   R4,CONV001              TEST NEXT BYTE
         B     CONV099                 END OF ROUTINE
CONV003  LA    R3,3(R3)                SKIP 3 BYTES INPUT
         BCTR  R4,0                    MINUS 1 BCT
         BCTR  R4,0                    MINUS 1 BCT
         C     R4,F0000                REGISTER ZERO ?
         BNH   CONV099                 YES: TERMINATE CONVERSION
         BCT   R4,CONV001              TEST NEXT BYTE
CONV099  LR    R1,R2                   OUTPUT LENGTH IN REGISTER 1
         LTR   R1,R1                   NO DATA RECEIVED ?  (MVS SP 1.3)
*        THE RESHOW CHARACTER HAS BEEN DELETED SINCE MVS SP 1.3
         BZ    PUT1A                   RESHOW SCREEN
         CLI   BUFFER,X'6E'            RESHOW CODE ? (PRE MVS SP 1.3)
         BE    PUT1A                   YES: RESHOW SCREEN
         MVI   MSG1,C'-'               CLEAR MESSAGE
         MVC   MSG1+1(21),MSG1         CLEAR MESSAGE
         MVC   CMDAREA,BLANKS          BLANK 'CMDAREA' FIELD
         OC    BUFFER,BLANKS           XLATE TO UPPER CASE
         CLI   BUFFER,C'='             An '=' before the option?   @rp9
         BNE   FUNC0                   No: Branch                  @rp9
         MVC   DBW(7),BUFFER+1         Remove the '='              @rp9
         MVC   BUFFER(7),DBW                                       @rp9
         MVI   BUFFER+7,C' '           Blank last character        @rp9
         B     FUNC0                   SELECT FUNCTION
ERR1     MVC   MSG1(L'ERR1TXT),ERR1TXT INVALID option
         B     PUT1                                                @rp1
ERR5     MVC   MSG1(L'MSG2TXT),MSG2TXT SELECT -0- TO INCREASE WORKSPACE
         B     PUT1
ERR6     MVC   MSG1(L'ERR6TXT),ERR6TXT UNACCEPTABLE FORMAT RECEIVED
         B     PUT1
ERR7     MVC   MSG2(L'ERRETXT),ERRETXT RPFUSER1 DISABLED (NOT FOUND)
         TPUT  SCR2TXT,SCR2LEN,FULLSCR
         B     GET1
ERR8     MVC   MSG2(L'ERRFTXT),ERRFTXT NO HELP DATASET
         TPUT  SCR2TXT,SCR2LEN,FULLSCR
         B     GET1
NOFULLSC DS    0H
         TPUT  NFULLMSG,L'NFULLMSG     ISSUE MESSAGE
         L     R13,SAVE+4              OBTAIN CALLERS SAVEAREA
         RETURN (14,12),RC=12          TERMINATE WITH CC=0012
FUNC0    CLI   BUFFER,C'0'             RESERVED
         BNE   FUNC1                   NO: TEST NEXT FUNCTION
         CLI   BUFFER+1,C'.'           2ND OPTION SELECTED ?
         BNE   FUNC0D                  NO: GO TO DEFAULTS
         MVC   DEFOPT(1),BUFFER+2      MOVE OPTION FOR DEFAULTS
         OI    INFCODE2,$OPTION        MARK IT
FUNC0D   DS    0H
         LINK  EP=RPFDEF,PARAM=((11)),VL=1 CHANGE DEFAULTS
         CLC   RTNCODE,F0000           NO RETURNCODE ?
         BE    PUT1                    YES: PROCEED
         L     R2,RTNCODE              LOAD INTO REG 2
         CVD   R2,DBW                  CONVERT TO PACKED
         UNPK  ERRDTXT+18(4),DBW       CONVERT TO ZONED
         OI    ERRDTXT+21,X'F0'        AND CLEAR SIGN
         XC    RTNCODE,RTNCODE         RESET RTNCODE
         MVC   MSG1,ERRDTXT            MOVE ERROR MESSAGE
         B     PUT1                    BACK TO MAIN MENU
FUNC1    CLI   BUFFER,C'1'             BROWSE SELECTED ?
         BNE   FUNC2                   NO: TEST NEXT FUNCTION
         OI    INFCODE,$DAIR00         BIT ON: ALLOW ONLY DAIRCODE 0
         ST    R11,PARMLIST            PARAMETER
         OI    INFCODE4,$DATAU         Let rpfdatal act as rpfdatau
         L     R15,EPDATAL             RPFDATAL MODULE
         LA    R1,PARMLIST             PARAMETER IN REG 1
         BALR  R14,R15                 BRANCH TO RPFDATAU
         NI    INFCODE,X'FF'-$DAIR00   BIT OFF
         NI    INFCODE4,255-$DATAU     Reset flag
         CLC   RTNCODE,F0012           member name=TEMPNAME ?
         BE    FUNC1P                  Member no suppl, link to RPFPDS
         CLC   RTNCODE,F0000           RETURN CODE 0 ?
         BNE   PUT1                    DO NOT GO TO RPFBRO
         MVC   CMDAREA,=CL24'RPFWORK'  DDNAME INTO CMDAREA
         LINK  EP=RPFBRO,PARAM=((11)),VL=1 LINK TO BROWSE MODULE
         TM    INFCODE2,$RETURN        Return pressed ?
         BO    FUNC1A                  Yes: go to main menu
         CLC   RTNCODE,F0000           RC browse = 0000 ?
         BNE   FUNC1                   NO: message in cmdarea
         MVC   CMDAREA,BLANKS          YES: Blank cmdarea
         B     FUNC1
FUNC1A   DS    0H                      Invoke RPFPDS
         CLC   RTNCODE,F0000           RC=0000?
         BE    PUT1                    Yes: branch
         MVC   MSG1,CMDAREA            Else move message
         B     PUT1                    No: stay in allocation menu
FUNC1P   DS    0H                      Invoke RPFPDS
         OI    INFCODE3,$PDSBR         IF ON THE B line-cmd is valid
         LINK  EP=RPFPDS,PARAM=((11)),VL=1 LINK TO ROUTINE
         MVC   CMDAREA,BLANKS          Blank cmdarea field in commarea
         TM    INFCODE2,$RETURN        Return pressed ?
         BO    PUT1                    Yes: go to main menu
         B     FUNC1                   No: stay in allocation menu
FUNC2    CLI   BUFFER,C'2'             EDIT SELECTED ?
         BNE   FUNC3                   NO: TEST NEXT FUNCTION
         CLI   BUFFER+1,C'.'           2ND OPTION SELECTED ?
         BNE   FUNC2A                  NO: GO TO UTILITY
         MVC   EDITOPT(1),BUFFER+2     MOVE OPTION FOR RPFDATAL
         OI    INFCODE2,$OPTION        MARK IT
FUNC2A   DS    0H
         L     R15,EPDATAL             LOAD ADDRESS RPFDATAL
         ST    R11,PARMLIST            ADDRES COMMAREA INTO PARMLIST
         LA    R1,PARMLIST             ADDRESS PARMLIST
         BALR  R14,R15                 BRANCH TO RPFDATAL
         CLC   RTNCODE,F0000           ALLOC SUCCESSFUL ?
         BE    FUNC2E                  YES: EDIT
         CLC   RTNCODE,F0004           LOAD TERM. AT END WORKSPC (LIBR)
         BE    FUNC2E                  YES: BACK TO MAIN MENU
         CLC   RTNCODE,F0008           BACK TO MAIN MENU ?
         BE    PUT1                    YES: BACK TO MAIN MENU
         CLC   RTNCODE,F0012           Invoke RPFPDS instead of Edit ?
         BE    FUNC2P                  YES: DO THAT
         MVC   MSG1,ERR7TXT            NO: UNEXPECTED RETCDE RECEIVED
         B     PUT1                    NO: BACK TO MAIN MENU
FUNC2E   CLC   NUMBERED,=C'YES'        NUMBERED ?
         BNE   FUNC2E2                 NO: TEST IF NOT NUMBERED
         L     R15,EPEDIT              YES: EDIT NUMBERED
         B     FUNC2E4                 LOAD EDIT ROUTINE
FUNC2E2  CLC   NUMBERED(2),=C'NO'      NOT NUMBERED ?
         BNE   ERR6                    NO: ERROR
         L     R15,EPEDIT              YES: EDIT UNNUMBERED
FUNC2E4  ST    R11,PARMLIST            ADDRESS COMMAREA INTO PARMLIST
         LA    R1,PARMLIST             ADDRESS PARMLIST
         BALR  R14,R15                 BRANCH TO RPFEDIT
         CLC   RTNCODE,F0000           EDIT SUCCESSFUL ?
         BE    FUNCNEXT
         CLC   RTNCODE,F0004           LOAD TERMINATED ?
         BE    ERR5                    YES: GIVE MESSAGE
         CLC   RTNCODE,F0008           WORKSPACE FULL ?
         BNE   FUNC2E6
         MVC   MSG1,ERR8TXT            YES: MESSAGE INTO SCREEN
         B     PUT1
FUNC2E6  CLC   RTNCODE,F0012           FILE EMPTY ?
         BNE   FUNC2E7
         MVC   CMDAREA(22),ERR9TXT     YES: MESSAGE for rpfdatal
         TM    INFCODE2,$RETURN        Return pressed ?
         BNO   FUNC2                   NO: redisplay alloc. menu
         NI    INFCODE2,255-$RETURN    Set return indicator off
         MVC   MSG1,ERR9TXT            msg in main menu
         B     PUT1
FUNC2E7  CLC   RTNCODE,F0016           WORKSPACE EMPTY ?
         BNE   FUNC2E8
         MVC   MSG1,ERR4TXT            YES: MESSAGE INTO SCREEN
         B     PUT1
FUNC2E8  CLC   RTNCODE,F0020           DATASET/MEMBER IN USE ?
         BNE   FUNC2E9
         MVC   CMDAREA,ERR2TXT         YES: MESSAGE for rpfdatal
         B     FUNC2
FUNC2E9  CLC   RTNCODE,F0024           RECFM=V(B) ?                @rp3
         BNE   FUNC2EA                                             @rp3
         MVC   MSG1,ERRGTXT            YES: MESSAGE INTO SCREEN    @rp3
         B     PUT1                                                @rp3
FUNC2EA  MVC   MSG1,ERR7TXT            UNEXPECTED RETURN CODE RECEIVED
         B     PUT1
FUNC2P   DS    0H                      Invoke RPFPDS
         OI    INFCODE3,$PDSEDIT       IF ON THE E line-cmd is valid
         LINK  EP=RPFPDS,PARAM=((11)),VL=1 LINK TO ROUTINE
         MVC   CMDAREA,BLANKS          No message in rpfdatal
         TM    INFCODE2,$RETURN        Return pressed ?
         BO    PUT1                    Yes: go to main menu
         NI    INFCODE2,255-$RETURN    Set return indicator off
         B     FUNC2
FUNC3    CLI   BUFFER,C'3'             UTILITY SELECTED ?
         BNE   FUNC4                   NO: TEST NEXT FUNCTION
         CLI   BUFFER+1,C'.'           2ND OPTION SELECTED ?
         BNE   FUNC3U                  NO: GO TO UTILITY
         MVC   UTILOPT(1),BUFFER+2     MOVE OPTION FOR UTILITY
         OI    INFCODE2,$OPTION        MARK IT
FUNC3U   DS    0H
         L     R15,EPUTIL              ENTRY POINT UTIL MENU
         ST    R11,PARMLIST            COMMAREA
         LA    R1,PARMLIST             PICKUP PARAMETER
         BALR  R14,R15                 BRANCH TO UTILITY
         B     PUT1                    GOTO MAIN MENU
FUNC4    CLI   BUFFER,C'4'             FOREGROUND ASSEMBLY ?
         BNE   FUNC5                   NO: TEST NEXT FUNCTION
         XC    ASMECB,ASMECB           CLEAR ECB
         ATTACH EP=RPFASM,ETXR=ATCHEXIT,ECB=ASMECB,PARAM=COMMAREA
*                                      GO TO SUBTASK RPFASM
         ST    R1,TCBADDR              SAVE ADDRESS CREATED TCB
         WAIT  ECB=ASMECB              WAIT FOR COMPLETION
         B     PUT1                    WRITE MAIN MENU
FUNC5    CLI   BUFFER,C'5'             RPFUSER1 SELECTED ?
         BNE   FUNC6                   NO: TEST NEXT FUNCTION
         BLDL  0,RPFUSER1              TRY TO FIND USER ROUTINE
         LTR   R15,R15                 FOUND ?
         BNZ   ERR7                    NO: GIVE MESSAGE
         MVC   CMDAREA(25),BLANKS      BLANK MSG FIELD FOR THE USER
         LINK  EP=RPFUSER1,PARAM=((11)),VL=1 LINK TO ROUTINE
         CVD   R15,DBW                 CONVERT RETURNCODE
         UNPK  ERRATXT+18(4),DBW       UNPACK
         OI    ERRATXT+21,X'F0'        CLEAR SIGN
         MVI   MSG1,C'-'               CLEAR MESSAGE
         MVC   MSG1+1(21),MSG1         CLEAR MESSAGE
         CLC   CMDAREA,BLANKS          NO MSG FROM RPFUSER1 ?
         BNE   FUNC5B                  NO: DISPLAY MESSAGE FROM USER
         MVC   MSG1(L'ERRATXT),ERRATXT DISPLAY ONLY THE RETURN CODE
         B     PUT1                    DISPLAY MAIN MENU
FUNC5B   DS    0H
         MVC   MSG1,CMDAREA            MOVE MESSAGE FROM USER
         B     PUT1                    DISPLAY MENU AND USERMSG
FUNC6    CLI   BUFFER,C'6'             TSO IN THE FOREGROUND ?
         BNE   FUNC7                   NO: TEST NEXT FUNCTION
         XC    ASMECB,ASMECB           CLEAR ECB ADDRESS
         ATTACH EP=RPFTSO,ETXR=ATCHEXIT,ECB=ASMECB,PARAM=COMMAREA
*                                      GO TO SUBTASK RPFTSO
         ST    R1,TCBADDR              SAVE ADDRESS CREATED TCB
         WAIT  ECB=ASMECB              WAIT FOR COMPLETION
         B     PUT1                    WRITE MAIN MENU
FUNC7    CLI   BUFFER,C'7'             HELP SELECTED ?
         BNE   FUNC8                   NO: TEST NEXT FUNCTION
         TM    INFCODE2,$NOHELP        NO HELP DATASET ?
         BNZ   ERR8                    YES: GIVE MESSAGE
         CLI   BUFFER+1,C'.'           2ND OPTION SELECTED ?
         BNE   FUNC7H                  NO: GO TO HELP
         MVC   HELPOPT(1),BUFFER+2     MOVE OPTION FOR HELP
         OI    INFCODE2,$OPTION        MARK IT
FUNC7H   DS    0H
         LINK  EP=RPFHELP,PARAM=COMMAREA,VL=1 LINK TO HELP MODULE
         B     PUT1                    MAIN MENU ON SCREEN
FUNC8    CLI   BUFFER,C'8'             TEST SELECTED ?
         BNE   FUNC9                   NO: TEST NEXT FUNCTION
         LINK  EP=RPFTEST,PARAM=COMMAREA,VL=1 LINK TO TEST MODULE
         B     PUT1                    MAIN MENU ON SCREEN
FUNC9    CLI   BUFFER,C'9'             OPER SELECTED ?
         BNE   FUNCX                   NO: TEST NEXT FUNCTION
         TPUT  SCR9999,L'SCR9999,FULLSCR CLEAR SCREEN
         LINK  EP=RPFOPER,PARAM=COMMAREA,VL=1 LINK TO OPER MODULE
         B     PUT1                    GET REPLY
FUNCX    CLI   BUFFER,C'X'             END SELECTED ?
         BE    END                     GOTO END
         CLI   BUFFER,C'?'             RESET SCREEN ?
         BE    PUT1                    MAIN MENU ON SCREEN
         B     ERR1                    GIVE ERROR MESSAGE
FUNCNEXT DS    0H
         CLC   EPNEXT,F0000            NEXT MODULE ?
         BE    FUNCNXT2
         L     15,EPNEXT
         XC    EPNEXT,EPNEXT           Clear next module field
         LA    R1,PARMLIST
         BALR  R14,R15
         L     R0,RTNCODE              RTNCODE INTO REG.0
         CVD   R0,DBW                  CONVERT TO PACKED
         UNPK  ERRATXT+18(4),DBW       UNPACK
         OI    ERRATXT+21,X'F0'        CLEAR SIGN
         MVC   CMDAREA(22),ERRATXT     YES: MESSAGE for rpfdatal
         TM    INFCODE2,$RETURN        Return pressed ?
         BNO   FUNC2                   No: redisplay allocation menu
         NI    INFCODE2,255-$RETURN    Set return indicator off
         MVC   MSG1,ERRATXT            Msg in main menu
         B     PUT1                    Redisplay main menu
FUNCNXT2 DS    0H
         TM    INFCODE2,$RETURN        Return pressed ?
         BNO   FUNC2                   No: redisplay allocation menu
         NI    INFCODE2,255-$RETURN    Set return indicator off
         B     PUT1                    Redisplay main menu
END      TIME  DEC
         SRL   R0,4
         ST    R0,TTIME
         ESTAE 0                       Kill ESTAE environment      @rp5
         LINK  EP=RPFEND,PARAM=COMMAREA,VL=1 LINK TO END MODULE
         TM    INFCODE2,ENQUED         ANY ENQ'S OUTSTANDING ?
         BNO   RETURN                  NO, ALLRIGHT
         LA    R3,QNAME
         LA    R4,RNAME
         DEQ   ((3),(4),52,SYSTEMS),RET=HAVE
         NI    INFCODE2,255-ENQUED     TURN OFF FLAG
RETURN   L     R13,SAVE+4              RESTORE ADDRESS SAVE AREA
         LM    R14,R12,12(R13)         RESTORE REGISTERS
         SR    R15,R15                 RETURN CODE = 0000
         BR    R14                     BRANCH BACK TO TSO
         TITLE 'TIME OF DAY ROUTINE'
*---------------------------------------------------------------------*
*                                                                     *
*        TIME - GET TIME OF DAY, CONVERT TO DISPLAY FORMAT            *
*                                                                     *
*               INPUT:  NONE                                          *
*               OUTPUT: TIMEDISP - TIME OF DAY, HH:MM:SS              *
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
TIME     ST    R14,SAVE14T             SAVE REG.14
         TIME  DEC                     GET TIME OF DAY
         SRL   R0,4                    SHIFT OUT HUNDREDTHS OF SECONDS
         ST    R0,TIMEWORK
         OI    TIMEWORK+3,X'0F'        INSERT SIGN
         MVC   TIMEDISP,PATT1          PATTERN INTO OUTPUT FILED
         ED    TIMEDISP(10),TIMEWORK
TIME99   L     R14,SAVE14T             RESTORE REG.14
         BR    R14                     RETURN
         TITLE 'ASYNCHRONOUS END OF TASK EXIT'
ATCHEXIT DS    0H                      ASYNCHRONOUS EXIT RPFASM
         DROP  ,
         USING *,R15
         LA    R2,TCBADDR              TCBADDRESS IN REGISTER 1
         DETACH (2)                    KILL SUBTASK
         BR    R14                     BRANCH
         DROP  R15
         TITLE 'ESTAE EXIT ROUTINE'
STAEEXIT DS    0H
         DROP  ,
         USING *,R15                   GET TEMPORARY ADDRESSABILITY
         LM    R11,R13,HEREADR         RESTORE REGISTERS
         DROP  R15                     KILL LOCAL ADDRESSABILITY
         USING SAVE,R13,R12            RESTORE ADDRESSING
         USING SDWA,R1                 GET ADDRESSABILITY OVER SDWA
         XR    R2,R2                   CLEAR REGISTER 2
         ICM   R2,7,SDWACMPC           INSERT COMPLETION CODE
         SRL   R2,12                   SHIFT OUT USER COMPLETION CODE
         STH   R2,SYSTEMCC             SAVE SYSTEM COMPLETION CODE
         DROP  R1                      KILL ADDRESSABILITY
         SETRP RC=4,RETADDR=RET,RETREGS=NO
         BR    R14
         TITLE 'ESTAE RETRY ROUTINE'
RET      DS    0H
         DROP  ,
         USING *,R15                   LOCAL ADDR
         LM    R11,R13,HEREADR         RESTORE REGISTERS
         DROP  R15                     KILL LOCAL ADDRESSABILITY
         USING SAVE,R13,R12            RESTORE ADDRESSABILITY
         USING COMMAREA,R11
         L     R2,RPFDCB               LOAD DCB ADDRESS
         USING IHADCB,R2               GET ADDRESSABILITY OVE DCB
         LTR   R1,R2                   DCB ADDRESS SPECIFIED ?
         BZ    RET1                    NO: PROCEED
         TM    DCBOFLGS,DCBOFOPN       IS THAT DCB STILL OPEN ?
         BNO   RET1                    NO: NO CLEANUP
         CLOSE ((2))                   CLOSE THAT POSSIBLE DCB
         FREEPOOL ((2))                AND FREE THE BUFFERS
RET1     TM    INFCODE2,ENQUED         ANY ENQ'S OUTSTANDING ?
         BNO   RET2                    NO, ALLRIGHT
         LA    R3,QNAME
         LA    R4,RNAME
         DEQ   ((3),(4),52,SYSTEMS),RET=HAVE
         NI    INFCODE2,255-ENQUED     TURN OFF FLAG
RET2     SYCONVHX IN=SYSTEMCC,OUT=EBCCOMP,L=2
         DELETE EP=RPFSAVE             REFRESH RPFSAVE
         LOAD  EP=RPFSAVE              GET FRESH COPY OF RPFSAVE
         ST    R0,EPSAVE               STORE NEW EP
         NI    INFCODE3,255-$EDTCOPY   SET COPY FLAG OFF
         DROP  R2                      KILL ADDRESSABILITY
         MVC   ERRBTXT+10(3),EBCCOMP+1 MOVE INTO MESSAGE
         MVC   MSG1(L'ERRBTXT),ERRBTXT MOVE ERROR MESSAGE
         B     PUT1                    RETRY SUCCESSFUL
         TITLE 'SCREEN FORMATS'
SCR1TXT  DC    X'27F5C21140401DF8' WRITE ERASE / R01,C00
SCR1LNE  DC    CL78'------------------------------ RPF MAIN MENU ------C
               ---------------------------'
         ORG   SCR1LNE+57
MSG1     DS    CL22
         ORG   ,
         DC    X'11C150',X'1DF8',C'Option  ===>',X'1DC113'
         DC    X'404040401DF0'
         DC    X'11C35D',AL3(TURQ),C'USERID  - '    R03,C62
SCR1UID  DS    CL7
         DC    X'11C3F3',AL3(WHITE),C'0  ',AL3(TURQ)
         DC    C'Defaults    - '
         DC    AL3(GREEN),C'Alter / Display session defaults'
         DC    X'11C46D',AL3(TURQ),C'Time    - '    R04,C62
SCR1TME  DS    CL8
         DC    X'11C5C3',AL3(WHITE),C'1  ',AL3(TURQ)
         DC    C'View        - '
         DC    AL3(GREEN),C'Display source data or output listings'
         DC    X'11C57D',AL3(TURQ),C'System  - '    R05,C62
SCR1SID  DS    CL4
         DC    X'11C6D3',AL3(WHITE),C'2  ',AL3(TURQ)
         DC    C'Edit        - '
         DC    AL3(GREEN),C'Update / Create a member or dataset'
         DC    X'11C74D',AL3(TURQ),C'TSO-proc- '    R06,C62        @rp2
SCR1PRC  DS    CL8                                                 @rp2
         DC    X'11C7E3',AL3(WHITE),C'3  ',AL3(TURQ)
         DC    C'Utility     - '
         DC    AL3(GREEN),C'Enter UTILITY'
         DC    X'11C85D',AL3(TURQ),C'Release - '    R07,C62        @rp7
         DC    CL8'V1R5M3'                                         @rp7
         DC    X'11C8F3',AL3(WHITE),C'4  ',AL3(TURQ)
         DC    C'Assembler   - '
         DC    AL3(GREEN),C'Foreground ASSEMBLER and LINK'         @rp5
         DC    X'114AC3',AL3(WHITE),C'5  ',AL3(TURQ)
         DC    C'User        - '
         DC    AL3(GREEN),C'Execute RPF user routine'
         DC    X'114BD3',AL3(WHITE),C'6  ',AL3(TURQ)
         DC    C'TSO         - '
         DC    AL3(GREEN),C'Execute TSO commands'
         DC    X'114C4A',AL3(RED)         R10,C59                  @rp5
SCR1CAL1 DC    CL21' '                                             @rp5
         DC    X'114CE3',AL3(WHITE),C'7  ',AL3(TURQ)
         DC    C'Tutorial    - '
         DC    AL3(GREEN),C'Display HELP information'              @rp5
         DC    X'114D5A',AL3(RED)         R11,C59                  @rp5
SCR1CAL2 DC    CL21' Su Mo Tu We Th Fr Sa'                         @rp5
         DC    X'114DF3',AL3(WHITE),C'8  ',AL3(TURQ)
         DC    C'Test        - '
         DC    AL3(GREEN),C'Enter TEST mode (Authorized)'
         DC    X'114E6A',AL3(TURQ)        R12,C59                  @rp5
SCR1CAL3 DC    CL42' '                                             @rp5
         DC    AL3(HDEFAULT),X'114FC3',AL3(WHITE),C'9  ',AL3(TURQ) @rp8
         DC    C'Operator    - '
         DC    AL3(GREEN),C'Enter OPERATOR mode'
         DC    X'114F7A',AL3(TURQ)        R13,C59                  @rp5
SCR1CAL4 DC    CL42' '                                             @rp5
         DC    AL3(HDEFAULT),X'1150D3',AL3(WHITE),C'X  ',AL3(TURQ) @rp8
         DC    C'Exit        - '
         DC    AL3(GREEN),C'Terminate RPF'
         DC    X'11D14A',AL3(TURQ)        R14,C59                  @rp5
SCR1CAL5 DC    CL42' '                                             @rp5
         DC    X'1DF0',AL3(HDEFAULT)                               @rp8
         DC    X'11D25A',AL3(TURQ)        R15,C59                  @rp5
SCR1CAL6 DC    CL42' ',AL3(HDEFAULT)                               @rp8
         DC    X'11D2F01DF0'       R16,C01
         DC    C'Hit ',AL3(WHITE),C'PF03/15'
         DC    AL3(TURQ),C' to terminate RPF'
         DC    X'11D36A'                  R16,C59                  @rp5
SCR1CAL7 DC    CL42' ',AL3(HDEFAULT)                               @rp8
         DC    X'11D4C1',AL3(WHITE)       R17,C02                  @rp8
USPL1    DC    AL3(USCORE)                                         @rp8
SPLASH1  DC    C'                                                    '
         DC    AL3(HDEFAULT)                                       @rp8
         DC    X'11D47A',AL3(TURQ)        R17,C59                  @rp5
SCR1CAL8 DC    CL42' ',AL3(HDEFAULT)                               @rp8
         DC    X'11D5D1',AL3(WHITE)       R18,C02                  @rp6
SPLASH2  DC    C'×                                                  ×'
         DC    X'11D661',AL3(WHITE)       R19,C02                  @rp6
SPLASH3  DC    C'× RPF Version 1 Rel. 5.3 Build: xxxxxxxxxxxxxxxxx  ×'
         DC    X'11D7F1',AL3(WHITE)       R20,C02                  @rp6
SPLASH4  DC    C'×                                                  ×'
         DC    X'11D9C1',AL3(WHITE)       R21,C02                  @rp6
SPLASH5  DC    C'× Use of RPF is free, modifications are not allowed×'
         DC    X'115AD1',AL3(WHITE)       R22,C02                  @rp6
SPLASH6  DC    C'× Information: email rpf@quicknet.nl               ×'
         DC    X'115B61',AL3(WHITE)       R23,C02                  @rp6
USPL2    DC    AL3(USCORE)                                         @rp8
SPLASH7  DC    C'×                                                  ×'
         DC    AL3(HDEFAULT)
SCR1SBA  DC    X'115DE2',AL3(WHITE)    R24/32/43,C52               @rp8
         DC    CL29'(C)-1979-2006 Skybird Systems'
SCR1LEN  EQU   *-SCR1TXT
SCR2TXT  DC    X'27F1C21140401DF8'
         DC    X'11407A'           R01,C59
MSG2     DS    CL22' '
         DC    X'11C150',X'1DF8',C'Option  ===>',X'1DC113'
         DC    X'404040401DF0'
SCR2LEN  EQU   *-SCR2TXT
SCR9999  DC    X'27F5C21140401D4013'
ERR1TXT  DC    CL22'--------Invalid option'
ERR2TXT  DC    CL22'-Dataset/Member in use'
ERR3TXT  DC    CL22'No workspace available'
ERR4TXT  DC    CL22'-------Workspace empty'
ERR6TXT  DC    CL22'---Unacceptable format'
ERR7TXT  DC    CL22'Unexpected return code'
ERR8TXT  DC    CL22'--------Workspace full'
ERR9TXT  DC    CL22'------Dataset is empty'
ERRATXT  DC    CL22'--------------Rc= XXXX' 18
ERRBTXT  DC    CL22'----Abend XXX detected'
ERRCTXT  DC    CL22'-Init. error: Rc= XXXX' 18
ERRDTXT  DC    CL22'---Not saved: Rc= XXXX' 18
ERRETXT  DC    CL22'----RPFUSER1 not found'
ERRFTXT  DC    CL22'-------No HELP dataset'
ERRGTXT  DC    CL22'-RECFM=V not supported'
COP1TXT  DC    CL22'-(C)-1979-2006 Skybird'
MSG2TXT  DC    CL22'SELECT ''0'' TO INCR. WS'
NFULLMSG DC    C'RPF: No 3270 terminal, RPF inoperative'
         TITLE 'VARIABLES, CONSTANTS AND CONTROL BLOCKS'
PARMLIST DC    F'0'
ADDRTIOT DS    F                       To contain addres of TIOT   @rp2
TIMEDISP DC    CL10' '                 OUTPUT TIME ROUTINE
TIMEWORK DC    F'0'                    WORK FIELD TIME ROUTINE
PATT1    DC    X'F02021207A20207A2020' PATTERN ED STMT TIME ROUTINE
SAVE14T  DC    F'0'                    SAVE REG.14 TIME ROUTINE
BUFFER   DC    CL80' '
DBW      DC    D'0'                    DOUBLE WORD USED BY UNPACK
F0000    DC    F'0'
F0004    DC    F'4'
F0008    DC    F'8'
F0012    DC    F'12'
F0016    DC    F'16'
F0020    DC    F'20'
F0024    DC    F'24'
F0032    DC    F'32'
SYSTEMCC DS    H
EBCCOMP  DS    CL4
HEREADR  DS    3F                      SAVE AREA OF ESTAE ROUTINE
TCBADDR  DC    F'0'                    SUBTASK TCB
ASMECB   DC    F'0'
*
RPFUSER1 DS    0F                      ALIGN ON FULLWORD
         DC    AL2(1)                  ONE ENTRY
         DC    AL2(14)                 LENGTH OF DATA
         DC    CL8'RPFUSER1'           NAME OF EXIT
         DC    XL6'00'                 TTRKZC
         DS    0D
         LTORG
         DS    0H
         DROP
         TITLE 'Calendar Routine'
*---------------------------------------------------------------------*
*                                                                     *
*        Routine:   Calendar                                          *
*        Function:  Create the calendar, which should appear on the   *
*                   MAIN menu                                         *
*        Input:     R1 pointer to COMMAREA                            *
*        Output     2 fullwords. The first fullword contains the      *
*                   address of a 21 byte monthname. The second        *
*                   word contains the day table (6 rows of 28 bytes)  *
*                   The two words are pointed to by R1                *
*                                                                     *
*------------------------------------- (c)-2002 Skybird Systems ------*
         SPACE 2
CALENDAR CSECT
         SAVE  (14,12),,*
         USING CALENDAR,R15
         LA    R11,SAVECAL
         ST    R13,SAVECAL+4
         ST    R11,8(0,R13)
         LR    R13,R11
         B     STARTCAL
         DS    0F
SAVECAL  DC    18F'-1'
         DC    CL8'&SYSDATE',C' ',CL8'&SYSTIME'
STARTCAL DS    0H
         DROP  R15
         USING SAVECAL,R13
         L     R11,0(R1)               Point to common area
         USING COMMAREA,R11            Adress common area
         MVC   SUN,BLANKS              Blank calendar
         L     R6,X'4C'                Pointer CVT
         MVC   YYDDD,57(R6)            Julian date
         L     R15,EPCNVDT             Load convdate routine
         MVI   DDMMYY,X'00'            Parm 1 is 3 bytes long
         CALL  (15),(YYDDD,DDMMYY)     Execute this routine
         MVC   TODAY,DD                Move day# in month of today
         LA    R7,YEAR                 Table with daynumbers
         PACK  DWB,YY                  Year
         CVB   R4,DWB                  Make binary
         XR    R5,R5                   Clear Odd reg.
         SRDL  R4,2                    Divide by 4
         LTR   R5,R5                   Still zero
         BNZ   NOLEAP                  No: not a leap year
         LA    R7,LEAP                 Use LEAP table instead of YEAR
NOLEAP   DS    0H
         PACK  DWB,MONNBR              Number of the month
         CVB   R4,DWB                  Make it binary
         BCTR  R4,0                    Minus 1
         SLL   R4,1                    x2
         LA    R7,0(R4,R7)             Pount to right offset in table
         LH    R4,0(R7)                Pick up daynumber
         CVD   R4,DWB                  Make 1st day on month packed
         MVC   YYDDD+1(2),DWB+6        And move in Julian date
         L     R15,EPCNVDT             Load convdate routine
         CALL  (15),(YYDDD,DDMMYY)     Execute this routine
         PACK  DWB,DAYNBR              Year
         CVB   R4,DWB                  Make binary
         BCTR  R4,0                    Minus 1
         MH    R4,=H'6'                x6                          @rp8
         LA    R3,42                   42 entries
         LA    R8,SUN                  Find first day in month
         MVC   0(3,R8),=AL3(HDEFAULT)  Move default attribute      @rp8
         MVI   3(R8),C' '                                          @rp8
         LA    R8,6(R8)                Next address                @rp8
         BCT   R3,*-14                 Loop
         LA    R8,SUN                  Find first day in month
         LA    R8,0(R4,R8)             This day is the 1st of the month
         LH    R6,2(R7)                Next month in table
         LH    R5,0(R7)                This month
         SR    R6,R5                   # days in the month
         LA    R4,1                    First day
LOOP     DS    0H
         CVD   R4,DWB                  make zoned
         UNPK  4(2,R8),DWB             Unpack day#
         OI    5(R8),X'F0'             Clear sign                  @rp8
         CLC   4(2,R8),TODAY           Day of today                @rp8
         BNE   *+10                    No: branch                  @rp8
         MVC   0(3,R8),=AL3(REVERSE)   Move attr reversed video    @rp8
         CLI   4(R8),C'0'              not significant zero?       @rp8
         BNE   *+8                     No: branch
         MVI   4(R8),C' '              Yes: replace by blank       @rp8
         LA    R8,6(R8)                Next day in table           @rp8
         LA    R4,1(R4)                Count day
         BCT   R6,LOOP                 Loop until table filled
*
         LA    R8,SUN                  Point to day table
         ST    R8,ADAYTAB              Save it's address
         MVC   MONTH#1+4(9),MNAME#E    Move monthname
         MVC   MONTH#1+14(4),CC        Move year including century
         LA    R8,MONTH#1              Monthname
         ST    R8,AMONTH               Save it's address
         LA    R1,AMONTH               Set in reg.1
         L     R13,4(R13)              Restore address savearea
         L     R14,12(R13)             Restore reg.14
         LM    R2,R12,28(R13)          Restore reg.2-12
         SR    R15,R15                 Return code = 0000
         BR    R14                     Return to RPFMAIN
*
DWB      DS    D
AMONTH   DS    F
ADAYTAB  DS    F
*
YEAR     DC    H'1'                    1 january
         DC    H'32'                   1 february
         DC    H'60'                   1 march
         DC    H'91'                   1 april
         DC    H'121'                  1 may
         DC    H'152'                  1 june
         DC    H'182'                  1 july
         DC    H'213'                  1 august
         DC    H'244'                  1 september
         DC    H'274'                  1 october
         DC    H'305'                  1 november
         DC    H'335'                  1 december
         DC    H'366'                  1 jan next year
*
LEAP     DC    H'1'                    1 january
         DC    H'32'                   1 february
         DC    H'61'                   1 march
         DC    H'92'                   1 april
         DC    H'122'                  1 may
         DC    H'153'                  1 june
         DC    H'183'                  1 july
         DC    H'214'                  1 august
         DC    H'245'                  1 september
         DC    H'275'                  1 october
         DC    H'306'                  1 november
         DC    H'336'                  1 december
         DC    H'367'                  1 jan next year
*
SUN      DS    CL252                   Each day max 5 in a month   @rp8
*
DDMMYY   DS    0CL49                   Output field from CONVDATE
DAYNBR   DS    CL2                     Daynumber in the week - 01=SUN
DAYNM#E  DS    CL9                     Dayname in English
DAYNM#D  DS    CL9                     Dayname in Dutch
         DS    CL1
DD       DS    CL2
         DS    CL1
MONNBR   DS    CL2                     Monthnumber in year - 01=Jan
MNAME#E  DS    CL9                     Monthname in English
MNAME#D  DS    CL9                     Monthname in Dutch
         DS    C
CC       DS    CL2                     Century
YY       DS    CL2                     Year
*
YYDDD    DS    PL3                     Julian date (packed)
TODAY    DS    CL2
*
MONTH#1  DC    CL21' '
         LTORG ,
         TITLE 'COMMON AREA OVER ALL RPF MODULES'
COMMAREA RPFCOMM DSECT=NO
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE 'DSECTS USED BY RPFMAIN'
         PRINT NOGEN
         CVT   DSECT=YES,LIST=NO
         IEESMCA
         IHASDWA
         DCBD  DSORG=PS,DEVD=DA
         END
