RPFCHNG  TITLE 'CHANGE CHAR STRINGS IN THE EDIT WORKSPACE'
*---------------------------------------------------------------------*
*                                                                     *
*        MODULE:        RPFCHNG                                       *
*                                                                     *
*        FUNCTION:      CHANGE CHARACTER STRING IN THE EDIT WORKSPACE *
*                       INPUT IS A CHANGE COMMAND ISSUED BY RPFEDIT,  *
*                       WHICH ADDRESS IS SPECIFIED IN                 *
*                       "CMDAREA" OF COMMAREA.                        *
*                       THE FORMAT OF THE CHANGE COMMAND =            *
*                       -1- C /STRING1/STRING2/ CHANGE ONLY CHARACTER *
*                       STRINGS ON THE CURRENT LINE (FIRSTSRC).       *
*                       -2- C /STRING1/STRING2/A CHANGE CHARACTER     *
*                       STRINGS FROM THE CURRENT LINE TILL THE END    *
*                       OF THE WORKSPACE.                             *
*                       -3- C /STRING1/STRING2/NNN,MMM                *
*                       CHANGE STRINGS IN LINERANGE NNN,MMM           *
*                       THE DELIMITER (IN THIS EXAMPLE "/") CAN BE    *
*                       ANY CHARACTER INCLUDING THE BLANK.            *
*                       IF THE CHANGE IS SUCCESSFUL IN THE FIRST 3    *
*                       BYTES OF "CMDAREA" WILL SET A PACKED          *
*                       CONTAINING THE NUMBER OF CHANGES.             *
*        NOTE:          IF RPFCHNG IS INVOKED BY RPFEDIT IN 'NUM '    *
*                       MODE MAX 72 BYTES WILL BE SEARCHED IN 'NONUM' *
*                       MODE MAX 80 BYTES WILL BE SEARCHED.           *
*                       THE LENGTH OF SEARCH IS DEPENDED OF THE EDIT  *
*                       SCOPE (SCOPE CMD IN RPFEDIT)                  *
*                                                                     *
*        RETURNCODES (SET IN RTNCODE FIELD OF CMDAREA):               *
*                                                                     *
*                       00 EXECUTION ALLRIGTH                         *
*                       08 ERRORS DETECTED: ERRORMESSAGE IN THE       *
*                          'CMDAREA' FIELD.                           *
*                                                                     *
*        08FEB2005: RPR: CHECK END OF CMD TO PREVENT LINE-1 OR        *
*                   LINE-2 INVALID MSG                             @RP1
*                                                                     *
*------------------------------------------ (C)-2005 SKYBIRD SYSTEMS -*
         SPACE 2
RPFCHNG  CSECT                         CHANGE CHAR. STRINGS IN WRKSPC
         SAVE (14,12),,*               SAVE REGISTERS
         USING RPFCHNG,R15             LOCAL ADDRESSABILITY
         LA    R14,SAVE                SAVE AREA
         ST    R14,8(0,R13)            FORWARD POINTER
         ST    R13,SAVE+4              BACKWARD POINTER
         LR    R13,R14
         B     START                   BRANCH OVER CONSTANTS
SAVE     DC    18F'-1'
         DC    CL8'&SYSDATE',C' ',CL8'&SYSTIME'
START    DS    0H
         DROP  R15                     KILL ADDRESSABILITY
         USING SAVE,R13                GET ADDRESSABILITY
         L     R11,0(R1)               PICKUP PARM
         USING COMMAREA,R11            GET ADDRESSABILITY FOR COMMAREA
         MVC   UPD001+8(2),=S(UPD003)  RESTORE BRANCH ADDRESS
         MVI   SW01,X'00'              CLEAR SWITCH
         ZAP   CHCNTR,=P'0'            INIT COUNTER
         MVC   FIRSTLNE,FIRSTSCR       DEFAULT FROM CURRENT LNE
         MVC   LASTLNE,LASTREC         DEFAULT ENTIRE WORKSPACE
         ICM   R6,15,CMDAREA           LOAD ADDRESS CHANGE COMMAND
         MVC   DLMCHAR,2(R6)           MOVE DELIMETER
         CLI   DLMCHAR,X'40'           DELIMITER SPACE ?
         BE    TXT055                  YES: INVALID
         CLI   DLMCHAR,X'00'           DELIMETER NULL ?
         BE    TXT055                  YES: INVALID
         LA    R4,45                   LENGTH OF REPLY AREA (CMD-3)
         XR    R5,R5                   CLEAR R5
         LA    R6,3(R6)                POINT AFTER DELIMITER
         ST    R6,OLDSTRNG             SAVE ADDRESS
UPD001   EQU   *
         CLC   0(1,R6),DLMCHAR         END OF CHAR STRING ?
         BE    UPD003                  BRANCH IF YES
         LA    R5,1(R5)                # OF BYTES LENGTH
         LA    R6,1(R6)                TEST NEXT POSITION
         BCT   R4,UPD001               TRY AGAIN
         B     TXT055                  INVALID CHARACTER STRING
UPD003   EQU   *
         MVC   UPD001+8(2),=S(UPD005)  MOVE DIFFERENT BRANCH ADR
         LR    R7,R5                   SAVE LENGTH OLD CHAR. STRING
         LA    R4,45                   SEARCH MAX 45 TIMES.
         SR    R4,R7                   SUBTRACT LENGTH OF 1ST STRING
         BCTR  R4,0                    MINUS LENGTH OF DLM
         LA    R6,1(R6)                NEW CHAR. STRING.
         ST    R6,SAVER6
         XR    R5,R5                   CLEAR GR. 5
         B     UPD001                  TEST LENGTH NEW STRING
UPD005   EQU   *
         LA    R6,1(R6)                POINT AFTER LAST DLM
         CLI   0(R6),C'A'              CHANGE ALL COMMAND ?
         BE    UPDALL                  YES: MARK CHANGE ALL
         CLI   0(R6),X'81'             CHANGE ALL COMMAND ? (ASIS)
         BE    UPDALL                  YES: MARK ALL
         CLI   0(R6),X'40'             BLANK ?
         BE    UPDCURR                 UPDATE CURRENT LINE
         CLI   0(R6),X'00'             NULL ?
         BE    UPDCURR                 UPDATE CURRENT LINE
TXT006   STM   R5,R7,SAVE57            SAVE REGISTERS
         LR    R3,R6                   START ADDRES FOR SCAN
         BAL   R14,LINE                FIND LINE NUMBER(S)
         LTR   R15,R15                 RC= 0000 ?
         BZ    TXT012                  YES: PROCEED
         C     R15,F0004               RC= 0004 - INVALID LINE(S) ?
         BE    TXT073
         C     R15,F0008               RC= 0008 - NOT NUMERIC ?
         BE    TXT073
         B     TXT077                  UNEXPECTED RETURN CODE
TXT012   CLC   LN2FLD,C0000            LN2 ENTERED ?
         BNE   TXT013                  YES: SKIP
         MVC   LN2FLD,LN1FLD           NO: LN2 = LN1
TXT013   CLI   LN1FLD,C'*'             * - CURRENT LINE ?
         BE    TXT014                  YES: CURRENT LINE
         CLC   LN1FLD,C0000            LN1 ENTERED ?
         BNE   TXT015                  YES: SKIP
TXT014   L     R3,FIRSTSCR             DEFAULT CURRENT LINE
         MVC   LN1FLD+2(6),OFFSETN2(R3) NUMBER INTO LN1FLD
         MVC   LN1FLD(2),=C'00'        LEADING ZEROES
         CLC   LN2FLD,C0000            LN2 ENTERED ?
         BNE   TXT015                  YES: SKIP
         L     R3,FIRSTSCR             DEFAULT CURRENT LINE
         MVC   LN2FLD+2(6),OFFSETN2(R3) NUMBER INTO LN2FLD
         MVC   LN2FLD(2),=C'00'        LEADING ZEROES
TXT015   EQU   *
         MVI   ERRPTXT+11,C'1'         MOVE ID IN MSG
         LA    R1,LN1FLD+2             PARM FOR SRCHLINE ROUTINE
         L     R15,EPSRCH              EP OF ROUTINE
         BALR  R14,R15                 TRY TO FIND LINE
         LTR   R15,R15                 LINE FOUND ?
         BNZ   TXT085                  NO: ISSUE ERRMSG
         LR    R7,R1                   A(FOUND LINE)
         MVI   ERRPTXT+11,C'2'         MOVE ID IN MSG
         LA    R1,LN2FLD+2             PARM FOR SRCHLINE ROUTINE
         L     R15,EPSRCH              EP OF ROUTINE
         BALR  R14,R15                 TRY TO FIND LINE
         LTR   R15,R15                 LINE FOUND ?
         BNZ   TXT085                  NO: ISSUE ERRMSG
         LR    R8,R1                   A(FOUND LINE)
         ST    R7,FIRSTLNE             FIRST LINE TO SCAN
         ST    R8,LASTLNE              LAST LINE TO SCAN
         LM    R5,R7,SAVE57            RESTORE REGISTERS
UPDALL   OI    SW01,CHALL              MARK CHANGE ALL
UPDCURR  ST    R7,LENGTH1              SAVE LENGTH OLD CHAR STRING
         ST    R5,LENGTH2              SAVE LENGTH NEW CHAR STRING
         CLC   LENGTH1,SCOPEL          CHAR STRING .GT. SCOPE ?
         BH    TXT079                  YES: INVALID
         CLC   LENGTH2,SCOPEL          CHAR STRING .GT. SCOPE ?
         BH    TXT079                  YES: INVALID
         L     R2,FIRSTLNE             FIRST LINE TO CHANGE
         ST    R2,SAVELST              SAVE THAT ADDRESS
UPD006   EQU   *                       TRY TO FIND CHAR STRING
         LA    R2,8(R2)                POINT TO DATA PORTION
         AL    R2,SCOPES               ADD SCOPE OFFSET
         L     R7,LENGTH1              LENGTH OLD STRING
         L     R5,LENGTH2              LENGTH NEW STRING
         LTR   R7,R7                   LENGTH OLD CHAR STRING ZERO
         BZ    UPD009                  BRANCH IF YES.
         L     R4,SCOPEL               LENGTH OF SCOPE
         SL    R4,LENGTH1              MINUS LENGTH OLD STRING
         LA    R4,1(R4)                INCLUDING LAST POSITION
         BCTR  R7,0                    DECREASE WITH 1 FOR EX
UPD007   EQU   *                       FIND OLD CHAR STRING AND REPLACE
         L     R8,OLDSTRNG             ADDRESS OF OLD STRING
         EX    R7,COMPARE              TEST IF STRING PRESENT
         BE    UPD009                  REPLACE INTO NEW STRING
         LA    R2,1(R2)                NEXT CHARACTER
         BCT   R4,UPD007               TRY AGAIN
         B     UPDNEXT                 TRY NEXT LINE IF AVAILABLE
COMPARE  CLC   0(0,R2),0(R8)           **** EXECUTE ONLY ****
UPD009   EQU   *
         ST    R2,SAVER2               SAVE POSITION IN LINE
         L     R7,LENGTH1
         L     R5,LENGTH2
         SR    R7,R5                   DIFFERENT LENGTH.
         C     R7,=F'0'                LENGTHS EQUAL
         BE    UPD015                  NO PROBLEMS REPLACE.
         BH    UPD011                  OLD STRING LONGER.
*---------------------------------------------------------------------*
*        NEW STRING LONGER   ---
*---------------------------------------------------------------------*
         L     R3,SAVER2               RESTORE POSITION.
         AL    R3,LENGTH1              ADD LENGTH OF OLD STRING
         ST    R3,SAVER3               SAVE END OF OLD STRING IN LINE.
         LPR   R5,R7                   MAKE RESULT POSITIVE
         L     R4,SAVELST              LOAD FIRST POS CHANG. LINE
         MVC   CARD(NEXTENT-8),8(R4)    SAVE LINE.
         LA    R4,8(R4)                POINT TO DATA PORTION IN LINE
         AL    R4,SCOPES               ADD SCOPE OFFSET
         AL    R4,SCOPEL               LAST POSITION IN LINE.
         SR    R4,R3                   LENGTH OF REMAINING DATA.
         L     R8,LENGTH1              LOAD LENGTH OLD STRING
         AR    R8,R4                   LEN REM DATA INCLUDING OLD STR.
         C     R8,LENGTH2              FITS THE NEW STRG IN THE LINE ?
         BL    UPDNEXT                 NO: DO NOT REPLACE
         SR    R4,R5
         AR    R3,R5
         L     R5,SAVELST              RESTORE FIRST POS OF LINE
         LA    R5,8(R5)                POINT TO DATA PORTION
         AL    R5,SCOPES               ADD SCOPE OFFSET
         SR    R3,R5                   OFFSET.
         LR    R5,R3                   SAVE OFFSET.
         LA    R3,CARD
         AL    R3,SCOPES               OFFSET SCOPE
         AR    R3,R5
         L     R5,SAVER3
         BCTR  R4,0
         CH    R4,=H'0'                NEGATIVE ?
         BL    UPD015                  IF SO NO SHIFT REM DATA
         EX    R4,MOVE2                SHIFT REMAINING DATA
         L     R4,SAVELST              RESTORE CHANGED LINE
         MVC   8(NEXTENT-8,R4),CARD    RESTORE CHANGED LINE
         B     UPD015                  REPLACE.
*---------------------------------------------------------------------*
*        OLD STRING LONGER   ---
*---------------------------------------------------------------------*
UPD011   EQU   *
         L     R3,SAVER2               RESTORE POSITION.
         AL    R3,LENGTH1              ADD L OF OLD STRING.
         L     R4,SAVELST              LOAD FIRST POS CH. LINE
         LA    R4,8(R4)                POINT TO DATA PORTION
         AL    R4,SCOPES               ADD SCOPE OFFSET
         AL    R4,SCOPEL               LAST POSTITION IN LINE.
         ST    R3,SAVER3               1ST CHAR AFTER 'OLD STRING'
         SR    R4,R3                   LENGTH OF REMAINING DATA
         BCTR  R4,0                    FOR EX. INSTRUCTION.
         XC    CARD,CARD               CLEAR WORK-AREA
         C     R4,=F'0'                LAST CHAR OF 1ST STRING IN C72
         BL    UPD013                  YES: DO NOT MOVE REMAINING DATA
         EX    R4,MOVE3
UPD013   SR    R3,R7
         AR    R4,R7                   SUBST. BLANKS IN REM. DATA
         EX    R4,MOVE4                SHIFT REMAINING DATA TO LEFT
UPD015   EQU   *                       REPLACE OLD STR WITH NEW STRING
         L     R6,SAVER6               RESTORE R6.
         L     R5,LENGTH2
         LTR   R5,R5
         BZ    UPD017                  NEW STRING HAS LENGTH=0.
         BCTR  R5,0                    FOR EX.
         EX    R5,MOVE1                MOVE NEW CHAR STRING
UPD017   AP    CHCNTR,=P'1'            ADD CHANGE
         AL    R2,LENGTH2              ADD LENGTH OF STRING TO ADDRESS
         L     R5,LENGTH2
         L     R7,LENGTH1
         LTR   R7,R7                   L OLD STRING ZERO ?
         BZ    UPDNEXT                 GO CHANGE NEXT LINE
         BCTR  R7,0                    MINUS 1 FOR EX
         LA    R2,0(R2)                CLEAR HIGH ORDER BIT
*/*   REGISTER 2 MUST POINT AFTER THE CHANGED STRING  ****/
*/*   THE OLD CHARACTER STRING CAN APEAR MORE THAN 1 TIME IN
*/*   A LINE, THEY MUST BE CHANGED ALL                ****/
         L     R4,SAVELST              LOAD START ADDRESS OF LINE
         LA    R4,8(R4)                POINT TO DATA PORTION
         AL    R4,SCOPES               ADD SCOPE OFFSET
         AL    R4,SCOPEL               ADD LENGTH OF LINE
         SR    R4,R2                   CALC REST OF LENGTH OF LINE
         SL    R4,LENGTH1              MINUS LENGTH OLD STRING
         A     R4,=F'1'                INCLUDING LAST POS
         C     R4,F0000                ZERO ?
         BNH   UPDNEXT                 TAKE NEXT LINE
         B     UPD007                  FIND CHAR STR IN THE SAME LINE
UPDNEXT  TM    SW01,CHALL              CHANGE ALL COMMAND ?
         BNO   UPDEND                  NO: CHANGE ONLY CURRENT LINE
         L     R2,SAVELST              LOAD START LINE
         TR    8(NEXTENT-8,R2),TRTAB   REMOVE X'00' CHARS
         LA    R2,NEXTENT(R2)          NEXT LINE IN WORKSPACE
         C     R2,LASTLNE              LAST RECORD REACHED ?
         BH    UPDEND                  END OF UPDATE IF YES
         ST    R2,SAVELST              SAVE NEW CURRENT LINE
         B     UPD006                  TAKE NEXT LINE
MOVE1    MVC   0(0,R2),0(R6)           **** EXECUTE ONLY ****
MOVE2    MVC   0(0,R3),0(R5)           **** EXECUTE ONLY ****
MOVE3    MVC   CARD(0),0(R3)           **** EXECUTE ONLY ****
MOVE4    MVC   0(0,R3),CARD            **** EXECUTE ONLY ****
TXT053   EQU   *
         MVC   CMDAREA,ERRVTXT         TEXT NOT FOUND
         B     RETURN8
TXT055   EQU   *
         MVC   CMDAREA,ERRWTXT         INVALID CHARACTER STRING
         B     RETURN8
TXT073   EQU   *
         MVC   CMDAREA,ERRJTXT         LINE INVALID
         B     RETURN8
TXT077   EQU   *
         MVC   CMDAREA,ERR2TXT         UNEXPECTED RETURN CODE
         B     RETURN8
TXT079   EQU   *
         MVC   CMDAREA,ERRETXT         OUT OF SCOPE
         B     RETURN8
TXT085   EQU   *
         MVC   CMDAREA,ERRPTXT         LINE-X NOT FOUND
RETURN8  EQU   *
         MVC   RTNCODE,F0008           RC = 0008
         B     RETURN
UPDEND   EQU   *                       END OF CHANGE
         CP    CHCNTR,=P'0'            COUNTER ZERO ?
         BE    TXT053                  YES: TEXT NOT FOUND
         UNPK  MSG4TXT+2(5),CHCNTR     COUNT INTO MESSAGE
         OI    MSG4TXT+6,X'F0'         CLEAR SIGN
         MVC   CMDAREA,MSG4TXT         MESSAGE INTO CMDAREA
RETURN   EQU   *
         L     R13,SAVE+4              LOAD CALLERS SAVE AREA
         RETURN (14,12),RC=0           RETURN TO CALLER
*---------------------------------------------------------------------*
*                                                                     *
*        N U M T S T - SUBROUTINE, NUMERIC TEST OF A 8 CHAR FIELD.    *
*                                                                     *
*                    INPUT:  NUMFLD, FIELD CONTAINS CHARS TO TEST     *
*                    OUTPUT: NUMFLD, REMAINS UNCHAINGED               *
*                            REG.15, RETURN CODE 0000 - NUMERIC       *
*                                                0008 - NOT NUMERIC   *
*                                                                     *
*------------------------------------------ (C)-1985 SKYBIRD SYSTEMS -*
NUMTST   ST    R14,SAVE14              SAVE REG.14
         LA    R3,8                    LENGTH
         LA    R4,NUMFLD               FIELD TO BE TESTED
NUMTST01 CLI   0(R4),X'F0'             LOW ?
         BL    NUMTST03                YES: NOT NUMERIC
         CLI   0(R4),X'F9'             HIGH ?
         BH    NUMTST03                YES: NOT NUMERIC
         LA    R4,1(R4)                NEXT CHAR.
         BCT   3,NUMTST01              TEST MAX 8 CHARS.
         XR    R15,R15                 RC= 0000, NUMERIC
         B     NUMTST99                GOTO EXIT
NUMTST03 LA    R15,8                   RC= 0008, NOT NUMERIC
NUMTST99 L     R14,SAVE14              RESTORE REG.14
         BR    R14                     RETURN
*---------------------------------------------------------------------*
*                                                                     *
*        S H I F T - SUBROUTINE, REPLACE TRAILING SPACES WITH         *
*                    FILLCHAR.                                        *
*                                                                     *
*                    INPUT:  NUMFLD, FIELD TO BE SHIFTED, MAX 8 CHAR, *
*                            FILLCHAR, FILL CHARACTER.                *
*                    OUTPUT: NUMFLD, TRAILING SPACES REPLACED WITH    *
*                                    LEADING FILLCHAR                 *
*                            REG.15, RC= 0000 - NORMAL END            *
*                                    RC= 0004 - ALL SPACES            *
*                                                                     *
*------------------------------------------ (C)-1985 SKYBIRD SYSTEMS -*
SHIFT    ST    R14,SAVE14              SAVE REG.14
         LA    R3,8                    MAX 8 CHARS
         XR    R15,R15                 RC= 0000
SHIFT01  CLI   NUMFLD+7,X'40'          LAST CHAR SPACE ?
         BNE   SHIFT99                 NO: GOTO EXIT
         MVC   WRKFLD+1(7),NUMFLD      MOVE FIRST 5 CHARS INTO WRKFLD+1
         MVC   WRKFLD(1),FILLCHAR      INSERT LEADING ZERO
         MVC   NUMFLD,WRKFLD           RESTORE NUMFLD
         BCT   R3,SHIFT01              SHIFT MAX 8 TIMES
         LA    R15,4                   RC= 0004 - ALL SPACES
SHIFT99  L     R14,SAVE14              RESTORE REG.14
         BR    R14                     RETURN
*---------------------------------------------------------------------*
*                                                                     *
*        S C A N - SUBROUTINE, SCANS AREA FOR ' ' OR ',' AND PUTS     *
*                              PRECEEDING CHARS IN NUMFLD.            *
*                                                                     *
*                  INPUT:  REG.3,  POINTS TO FIRST CHAR TO SCAN       *
*                  OUTPUT: NUMFLD, CHARS PRECEEDING ' ' OR ','        *
*                          REG.3,  POINTS TO LATS CHAR SCANNED        *
*                          REG.15, RETURN CODE 0000 - CHARS FOUND     *
*                                              0008 - NO CHARS FOUND  *
*                                                                     *
*------------------------------------------ (C)-2005 SKYBIRD SYSTEMS -*
SCAN     ST    R14,SAVE14              SAVE REG.14
         LA    R15,8                   RC= 08,INITIAL: NO CHARS FOUND
         LA    R4,9                    MAX 9 CHARS
         LA    R5,NUMFLD               POINT TO NUMFLD
         MVC   NUMFLD,BLANKS           CLEAR NUMFLD
         L     R1,CMDAREA              POINT TO ORIGINAL COMMAND   @RP1
         LA    R1,48(R1)               ADD LENGTH OF COMMAND FIELD @RP1
SCAN01   DS    0H
         CR    R3,R1                   END OF COMMAND FIELD?       @RP1
         BNL   SCAN03                  YES: COMMAND COMPLETE       @RP1
         CLI   0(R3),X'40'             SPACE ?
         BE    SCAN03                  YES: END OF SCAN
         CLI   0(R3),X'6B'             COMMA ?
         BE    SCAN03                  YES: END OF SCAN
         CLI   0(R3),X'00'             NULL ?
         BE    SCAN03                  YES: END OF SCAN
         MVC   0(1,R5),0(R3)           MOVE 1 CHAR INTO NUMFLD
         LA    R3,1(R3)                NEXT CHAR TO SCAN
         LA    R5,1(R5)                NEXT CHAR NUMFLD
         BCT   R4,SCAN01               SCAN MAX 9 TIMES
         B     SCAN99                  GOTO EXIT, NO CHARS FOUND
SCAN03   XR    R15,R15                 RC= 0000, SCAN SUCCESSFUL
SCAN99   L     R14,SAVE14              RESTORE REG.14
         BR    R14                     RETURN
*---------------------------------------------------------------------*
*                                                                     *
*        L I N E - SCAN INPUT BUFFER FOR LINE NUMBERS                 *
*                                                                     *
*                  INPUT:  REG.3, START ADDRESS SCAN,                 *
*                  OUTPUT: LN1FLD, LINE-1                             *
*                          LN2FLD, LINE-2                             *
*                          LN3FLD, LINE-3                             *
*                          REG.15, RC= 0000 - ALL LINES VALID,        *
*                                  RC= 0004 - INVALID LINE FOUND,     *
*                                  RC= 0008 - LINES NOT NUMERIC.      *
*                          ERRJTXT, ERROR INDICATING LINE NUMBER      *
*                                                                     *
*------------------------------------------ (C)-1985 SKYBIRD SYSTEMS -*
LINE     ST    R14,SAVE14LN            SAVE REG.14
         MVI   ERRJTXT+13,C'1'         MOVE LINE ID IN ERROR MSG
         BAL   R14,SCAN                SCAN FOR LN1FLD
         LTR   R15,R15                 RC= 0000 ?
         BNZ   LINE73                  NO: LN1 INVALID
         ST    R3,SAVE3                SAVE REG.3
         CLI   NUMFLD,C'*'             * - CURRENT LINE ?
         BE    LINE04                  YES: SKIP NUMTST
         MVI   FILLCHAR,X'F0'          '0' INTO FILLCHAR
         BAL   R14,SHIFT               REP TRAIL.SPC WITH LEADING ZERO
         C     R15,F0004               RC= 0004 - ALL SPACES
         BE    LINE03
         CLC   NUMFLD,C0000            LINE-1 ZERO ?
         BE    LINE73
LINE03   BAL   R14,NUMTST              NUMERIC TEST
         LTR   R15,R15                 RC= 0000 ?
         BNZ   LINE75                  NO: LN1 NOT NUMERIC
LINE04   MVC   LN1FLD,NUMFLD           SAVE LN1 INTO LN1FLD
         L     R3,SAVE3                RESTORE REG.3
         LA    R3,1(R3)                POINT TO LN2
         MVI   ERRJTXT+13,C'2'         MOVE LINE ID IN ERROR MSG
         BAL   R14,SCAN                SCAN FOR LN2FLD
         LTR   R15,R15                 RC= 0000 ?
         BNZ   LINE73                  NO: LN2 INVALID
         ST    R3,SAVE3                SAVE REG.3
         MVI   FILLCHAR,X'F0'          '0' INTO FILLCHAR
         BAL   R14,SHIFT               REP TRAIL.SPC WITH LEADING ZERO
         C     R15,F0004               RC= 0004 - ALL SPACES ?
         BE    LINE05
         CLC   NUMFLD,C0000            LINE-2 ZERO ?
         BE    LINE73
LINE05   BAL   R14,NUMTST              NUMERIC TEST
         LTR   R15,R15                 RC= 0000 ?
         BNZ   LINE75                  NO: LN2 NOT NUMERIC
         MVC   LN2FLD,NUMFLD           SAVE LN2 INTO LN1FLD
         LA    R15,0                   RC= 0000 - SCAN SUCCESSFUL
         B     LINE99                  EXIT
LINE73   XR    R15,R15                 RC= 0004 - INVALID LINE(S) FOUND
         B     LINE99                  EXIT
LINE75   LA    R15,8                   RC= 0008 - LINE(S) NOT NUMERIC
         B     LINE99                  EXIT
LINE99   L     R14,SAVE14LN            RESTORE REG.14
         BR    R14                     RETURN
         TITLE 'WORKAREAS AND CONSTANTS'
F0000    DC    F'0'
F0004    DC    F'4'
F0008    DC    F'8'
F0086    DC    F'86'               LENGTH 1 LINE IN WS
C0000    DC    C'00000000'
FIRSTLNE DS    F
LASTLNE  DS    F
ERR2TXT  DC    CL22'UNEXPECTED RETURN CODE'
ERRETXT  DC    CL22'       SCOPE TOO SMALL'
ERRJTXT  DC    CL22'        LINE-X INVALID'
ERRPTXT  DC    CL22'      LINE-X NOT FOUND'
ERRVTXT  DC    CL22'        TEXT NOT FOUND'
ERRWTXT  DC    CL22'  INVALID CHAR. STRING'
MSG4TXT  DC    CL22'  XXXXX CHANGE(S) MADE'
SAVE14LN DS    F                   SAVE AREA REG.14 LINE
SAVE14   DS    F                   SAVE AREA REG.14 ALL
SAVE14T  DC    F'0'
SAVE3    DS    F                   SAVE AREA REG.3 SCAN
SAVE3TXT DS    F                   SAVE AREA REG.3
SAVE57   DS    3F                  SAVE AREA REG.5,6,7
MAXLINES DS    F
FILLCHAR DC    CL1' '              FILL CHARACTER SHIFT ROUTINE.
NUMFLD   DS    CL8                 WORK FIELD NUMTST/SHIFT
WRKFLD   DS    CL8                 WORK FIELD NUMTST/SHIFT
LN1FLD   DS    CL8                 LINE-1 COPY/LINE
LN2FLD   DS    CL8                 LINE-2 COPY/LINE
LN3FLD   DS    CL8                 LINE-3 COPY/LINE
CARD     DS    CL86                 WORKFIELD
OLDSTRNG DS    F
SAVELST  DS    F                    SAVE START OF CURRENT LINE
LENGTH1  DS    F                    LENGTH OF OLD CHAR STRING
LENGTH2  DS    F                    LENGTH OF NEW CHAR STRING
SAVER2   DS    F                    SAVE REG 2.
SAVER3   DS    F                    SAVE REG 3.
SAVER6   DS    F                    SAVE REG 6.
DLMCHAR  DS    C                    DELIMETER
CHCNTR   DS    PL3
SW01     DC    X'00'
CHALL    EQU   128
TRTAB    DC    256AL1(*-TRTAB)
         ORG   TRTAB
         DC    X'40'               X'00' BECOMES SPACE
         ORG
         LTORG
         TITLE 'RPFCOMM DSECT'
COMMAREA RPFCOMM
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
OFFSETN2 EQU   0
         END
