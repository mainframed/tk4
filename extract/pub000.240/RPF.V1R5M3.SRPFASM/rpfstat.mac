RPFSTAT  TITLE 'DISPLAY RPF STATUS PANEL'
*---------------------------------------------------------------------*
*                                                                     *
*        MODULE:        RPFSTAT                                       *
*        FUNCTION:      DISPLAY THE RPF STATUS LIKE WORKPACE SIZE,    *
*                       WORKSPACE LINES IN USE, SESSION TIME, CURRENT *
*                       ALLOCATIONS, DEFAULT OPTIONS, CURRENT OPTIONS *
*                       AND USERID                                    *
*                                                                     *
*------------------------------------------ (C)-2001 SKYBIRD SYSTEMS -*
RPFSTAT  CSECT  ,                      RPF STATUS PANEL
         SAVE  (14,12),,*
         USING RPFSTAT,R15
         LA    R11,SAVE
         ST    R13,SAVE+4
         ST    R11,8(0,R13)
         LR    R13,R11
         B     START
         DS    0F
SAVE     DC    18F'-1'                 SAVE AREA
         DC    CL8'&SYSDATE',C' ',CL8'&SYSTIME'
START    DS    0H
         DROP  R15
         LA    R12,2048(R13)
         LA    R12,2048(R12)           SECOND BASE REGISTER
         USING SAVE,R13,R12
         L     R11,0(R1)
         USING COMMAREA,R11
         MVC   SCR1UID+26(7),USERID    USERID INTO SCREEN
         LA    R3,SCR1UID+32           REMOVE TRAILING BLANKS
         LA    R4,7                    MAX 7 TIMES
UID1     CLI   0(R3),C' '              TRAILING BLANK FOUND ?
         BNE   UID99                   NO: EXIT
         MVI   0(R3),C'-'              REPLACE BY HYPHEN
         BCTR  R3,0                    PREVIOUS POS.
         BCT   R4,UID1                 LOOP UNTIL ALL DONE
UID99    EQU   *
         TPUT  SCR1TXT,SCR1LEN,FULLSCR
         MVC   FLD51A,USERID
         MVC   TIMEWORK,STIME          GET LOGON TIME
         BAL   R14,TIME                CONVERT TO DISPLAY
         MVC   FLD52A,TIMEDISP+2       MOVE INTO SCREEN
         TIME  DEC                     GET TIME OF DAY
         SRL   R0,4                    SHIFT OUT THOUSANDS OF SECONDS
         ST    R0,TIMEWORK             STORE INTO WORKFIELD
         BAL   R14,TIME                CONVERT TO DISPLAY
         MVC   FLD52B,TIMEDISP+2       MOVE INTO SCREEN
         MVC   ADDR1,GETMSTRT          START WORKSPACE
         MVC   ADDR2,GETMEND           END WORKSPACE
         BAL   R14,LINE                CALCULATE # OF LINES
         MVC   FLD53A,LINES            MOVE # LINES INTO SCREEN
         MVC   ADDR1,FIRSTREC          FIRST RECORD IN WORKSPACE
         MVC   ADDR2,LASTREC           LAST RECORD IN WORKSPACE
         BAL   R14,LINE                CALCULATE # OF LINES IN WS
         MVC   FLD53B,LINES            MOVE # LINES INTO SCREEN
         MVC   FLD54A,DEFOUT
         MVC   FLD54B,DEFLOG
         MVC   FLD55A,DEFOLD
         MVC   FLD55B,FOLD
         MVC   FLD56A,DEFNUM
         MVC   FLD56B,NUMBERED
         MVC   FLD57A,DEFAPP
         MVC   FLD57B,APPEND
         MVC   FLD58A,MEMBER
         MVC   FLD58B,DSNAME
         MVC   FLD59A,VOLUME
         BAL   R14,DATE                CONVERT THE DATE
         MVC   FLD51B,DATEDISP         MOVE CONVERTED DATE
PUT5     TPUT  SCR5TXT,SCR5LEN,FULLSCR
         TGET  BUFFER,L'BUFFER,ASIS
RETURN   L     R13,SAVE+4
         LM    R14,R12,12(R13)
         XR    R15,R15
         BR    R14
*---------------------------------------------------------------------*
*                                                                     *
*        LINE - CALCULATE NUMBER OF LINES BETWEEN TWO ADDRESSES.      *
*                                                                     *
*               INPUT:  ADDR1 - START ADDRESS,                        *
*                       ADDR2 - END ADDRESS.                          *
*                                                                     *
*               OUTPUT: LINES - NUMBER OF LINES.                      *
*                                                                     *
*------------------------------------------ (C)-2001 SKYBIRD SYSTEMS -*
LINE     ST    R14,SAVE14L         SAVE REG.14
         MVC   LINES,=C'FFFFFFF'   DEFAULT FFFF INTO LINES
         CLC   ADDR1,F0000         WORKSPACE EMPTY ?
         BNE   LINE03              NO: PROCEED
         MVC   LINES,=C'0000000'   0 LINES IN WORKSPACE
         B     LINE99
LINE03   L     R4,ADDR1            START ADDRES INTO REG.4
         L     R5,ADDR2            END ADDRESS INTO REG.5
         LA    R5,NEXTENT(R5)      REG.5 = REG.5 + 86
         SR    R5,R4               # OF BYTES INTO REG.5
         XR    R4,R4               CLEAR REG.4
         LA    R6,NEXTENT          LENGTH 1 LINE INTO REG.6
         DR    R4,R6               DIVIDE # BYTES BY NEXTENT
         CVD   R5,DBW              CONVERT INTO PACKED
         UNPK  LINES(7),DBW        CONVERT INTO DISPLAY
         OI    LINES+6,X'F0'       CLEAR SIGN
LINE99   L     R14,SAVE14L         RESTORE REG.14
         BR    R14                 RETURN
*---------------------------------------------------------------------*
*                                                                     *
*        TIME - CONVERT TIME OF DAY TO DISPLAY FORMAT                 *
*                                                                     *
*               INPUT:  TIMEWORK - TIME OF DAY                        *
*               OUTPUT: TIMEDISP - TIME OF DAY, HH:MM:SS              *
*                                                                     *
*------------------------------------------ (C)-2001 SKYBIRD SYSTEMS -*
TIME     ST    R14,SAVE14T         SAVE REG.14
         OI    TIMEWORK+3,X'0F'    INSERT SIGN
         MVC   TIMEDISP,PATT1      PATTERN INTO OUTPUT FILED
         ED    TIMEDISP(10),TIMEWORK
TIME99   L     R14,SAVE14T         RESTORE REG.14
         BR    R14                 RETURN
*---------------------------------------------------------------------*
*                                                                     *
*        DATE - CONVERT A THE JULIAN DATE IN THE CVT (YYDDD) TO       *
*               DD/MM/JJ                                              *
*               INPUT  - JULIAN DATE IN CVT                           *
*               OUTPUT - DD/MM/YYYY RETURNED IN FIELD "DATEDISP"      *
*                                                                     *
*------------------------------------------ (C)-2001 SKYBIRD SYSTEMS -*
DATE     ST    R14,SAVE14D             SAVE REG 14
         L     R6,X'4C'                POINTER CVT
         UNPK  DAYNBR,56(4,R6)         JULIAN DATE
         MVC   DATEDISP+6(2),=C'20'    21TH CENTURY
         MVC   DATEDISP+8(2),DAYNBR+2  MOVE FRAGMENTS OF YEAR
         CLC   DAYNBR(2),=C'00'        00 IN JULIAN DATE ? (00YYDDD)
         BNE   *+10                    NO BRANCH
         MVC   DATEDISP+6(2),=C'19'    20TH CENTURY
         PACK  YEAR,DATEDISP+6(4)
         DP    YEAR(4),=PL1'4'         DIVIDE BY 4
         CP    YEAR+3(1),=PL1'0'       LEAP YEAR
         BNE   DATE2                   BRANCH IF NOT
         ZAP   FEB,=PL2'29'            29 DAYS IN FEBRUARI
*
DATE2    EQU   *
         LA    R7,JAN                  MONTHTABLE
         ZAP   DAY,58(2,R6)
*
DATE3    EQU   *
         CP    DAY(3),0(2,R7)
         BNH   DATE4
         SP    DAY,0(2,R7)             SUBTRACT ULTIMO DATE
         LA    7,2(R7)                 NEXT MONTH
         AP    MNDNR,=PL1'1'
         B     DATE3                   RETURN
*
DATE4    EQU   *
         UNPK  UNPKMND,MNDNR
         OI    UNPKMND+2,X'F0'
         MVC   DATEDISP+3(2),UNPKMND+1
         UNPK  UNPKMND,DAY
         OI    UNPKMND+2,X'F0'
         MVC   DATEDISP(2),UNPKMND+1
         MVI   DATEDISP+2,X'61'
         MVI   DATEDISP+5,X'61'        SLASHES BETWEEN NUMBERS
         L     R14,SAVE14D             RESTORE R14
         BR    R14                     BACK TO CALLER
         TITLE 'SCREEN FORMATS'
SCR1TXT  DC    X'27F5C21140401DF8' WRITE ERASE / R01,C00
SCR1UID  DC    CL78'RPF STATUS PANEL: USERID= XXXXXXX------------------C
               ---------------------------'
         DC    X'11C150',X'1DF8',C'=====>',X'1D4013'
REP1     DC    CL24' ',X'1DF0'
SCR1LEN  EQU   *-SCR1TXT
SCR5TXT  DC    X'27F1C2'
         DC    X'11C150',X'1DF8',C'=====>',X'1D4013'
REP5     DC    CL24' ',X'1DF0'
         DC    X'11C540',X'1DF0'   R05,C01
         DC    C'USER-ID     =',X'1DF8'
FLD51A   DC    CL8' '
         DC    X'11C5E6',X'1DF0'   R05,C39
         DC    C'DATE        =',X'1DF8'
FLD51B   DC    CL10' '
         DC    X'11C650',X'1DF0'   R06,C01
         DC    C'LOGON TIME  =',X'1DF8'
FLD52A   DC    CL8' '
         DC    X'11C6F6',X'1DF0'   R06,C39
         DC    C'TIME OF DAY =',X'1DF8'
FLD52B   DC    CL8' '
         DC    X'11C760',X'1DF0'   R07,C01
         DC    C'WS-SIZE     =',X'1DF8'
FLD53A   DC    CL7' '
         DC    X'11C8C6',X'1DF0'   R07,C39
         DC    C'IN USE      =',X'1DF8'
FLD53B   DC    CL7' '
         DC    X'11C8F0',X'1DF0'   R08,C01
         DC    C'OUT CLASS   =',X'1DF8'
FLD54A   DC    C' '
         DC    X'11C9D6',X'1DF0'   R08,C39
         DC    C'LOGGING     =',X'1DF8'
FLD54B   DC    CL3' '
         DC    X'114A40',X'1DF0'   R09,C01
         DC    C'DEF. FOLD   =',X'1DF8'
FLD55A   DC    CL3' '
         DC    X'114AE6',X'1DF0'   R09,C39
         DC    C'CUR. FOLD   =',X'1DF8'
FLD55B   DC    CL3' '
         DC    X'114B50',X'1DF0'   R10,C01
         DC    C'DEF. NUMBERS=',X'1DF8'
FLD56A   DC    CL3' '
         DC    X'114BF6',X'1DF0'   R10,C39
         DC    C'CUR. NUMBERS=',X'1DF8'
FLD56B   DC    CL3' '
         DC    X'114C60',X'1DF0'   R11,C01
         DC    C'DEF. APPEND =',X'1DF8'
FLD57A   DC    CL3' '
         DC    X'114DC6',X'1DF0'   R11,C39
         DC    C'CUR. APPEND =',X'1DF8'
FLD57B   DC    CL3' '
         DC    X'115050'           R14,C01
         DC    X'1DF8',C'CURRENT ALLOCATIONS:'
         DC    X'11D2F0'           R16,C01
         DC    X'1DF0',C'MEMBER=',X'1DF8'
FLD58A   DC    CL8' ',X'1DF0',C',DSNAME=',X'1DF8' MEMBER
FLD58B   DC    CL44' ',X'1DF0'     DSNAME
         DC    X'11D440'           R17,C01
         DC    X'1DF0',C'VOLUME=',X'1DF8'
FLD59A   DC    CL6' ',X'1DF0',X'11D4D2',C',DDNAME=',X'1DF8' VOLUME
FLD59B   DC    C'RPFWORK ',X'1DF0' DDNAME
SCR5LEN  EQU   *-SCR5TXT
         TITLE 'WORKAREAS AND CONSTANTS'
BUFFER   DC    CL80' '
SAVE14D  DC    F'0'                SAVE AREA DATE SUBROUTINE
SAVE14T  DC    F'0'                SAVE AREA TIME SUBROUTINE
SAVE14L  DC    F'0'                SAVE AREA LINE SUBROUTINE
TIMEWORK DC    F'0'
TIMEDISP DC    CL10' '
PATT1    DC    X'F02021207A20207A2020'
ADDR1    DC    F'0'
ADDR2    DC    F'0'
F0000    DC    F'0'
LINES    DC    CL7'FFFFFFF'
DBW      DC    D'0'
         DS    CL2                     DO NOT DELETE THIS STMNT   @2000
DATEDISP DS    CL10
JAN      DC    PL2'31'
FEB      DC    PL2'28'
MRT      DC    PL2'31'
APR      DC    PL2'30'
MAY      DC    PL2'31'
JUN      DC    PL2'30'
JUL      DC    PL2'31'
AUG      DC    PL2'31'
SEP      DC    PL2'30'
OKT      DC    PL2'31'
NOV      DC    PL2'30'
DEC      DC    PL2'31'
MNDNR    DC    PL2'1'
UNPKMND  DS    CL3
DAYNBR   DS    CL7
YEAR     DC    PL4'0'
DAY      DC    PL3'0'
         LTORG
         TITLE 'RPFCOMM DSECT'
COMMAREA RPFCOMM
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
