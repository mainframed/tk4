RPFDOC   TITLE 'FIND //* SYPRID CARD AND ADD DATE/TIME STAMP'
***********************************************************************
*                                                                     *
*        MODULE:        RPFDOC.                                       *
*                                                                     *
*        FUNCTION:      FIND //* SYPRID CARD IN THE EDIT WORKSPACE    *
*                       THE SEARCH STARTS WITH THE FIRST LINE IN      *
*                       THE WORKSPACE AND ENDED AT THE LAST LINE IN   *
*                       THE WORKSPACE ("LASTREC" IN RPFCOMM)          *
*                       WHEN A MATCH IS FOUND IN A LINE, DATE AND TIME*
*                       ARE ADDED AND THIS LINE                       *
*                       WILL BE THE FIRST LINE ON THE SCREEN.         *
*                       THIS MODULE IS CALLED BY RPFEDITN.            *
*                                                                     *
*        RETURNCODES:   00 MATCH FOUND OF STRING.                     *
*                       04 NO MATCH FOUND OF STRING.                  *
*                          MESSAGE WILL BE SET IN 'CMDAREA'           *
*                                                                     *
********************************************(C)-1982 SKYBIRD SYSTEMS***
         SPACE 2
RPFDOC   CSECT                         FIND ROUTINE
         SAVE  (14,12),,*              SAVE REGISTERS
         USING RPFDOC,15               GET LOCAL ADDRESSABILITY
         LA    14,SAVE                 SAVE AREA
         ST    14,8(0,13)              FORWARD POINTER
         ST    13,SAVE+4               BACKWARD POINTER
         LR    13,14                   LOAD THIS SAVEAREA
         B     START                   BRANCH AROUND SAVEAREA
SAVE     DC    18F'-1'                 SAVE AREA
         DC    CL8'&SYSDATE',C' ',CL8'&SYSTIME'
START    DS    0H
         DROP  15                      KILL LOCAL ADDRESSABLILITY
         USING SAVE,13                 GET ADDR OVER SAVE
         L     11,0(1)                 RPFCOMM AREA
         USING COMMAREA,11             DSECT OVER PARM
         LA    2,72                    DEFAULT 72 POSITIONS
         CLC   NUMBERED(3),=C'YES'     INVOKED BY NUMBERED EDITOR ?
         BE    START1                  YES: DO NOTHING
         LA    2,80                    INVOKED BY 'NONUM' MODE
START1   ST    2,LLINE                 STORE LENGTH
RPF021   EQU   *                       SEARCH STRING IN WORKSPACE
         L     2,FIRSTREC              R2= FIRSTREC
         L     5,LASTREC               R4= LASTREC
         LA    5,79(5)                 R4= ADDR AFTER LASTREC
         L     4,RLENGTH               INCREMENT IS 80 (REC LENGTH)
         ST    2,SAVE02                SAVE R2
         LH    6,LENSTR                LENGTH OF STRING
         BCTR  6,0                     MINUS 1 FOR COMPARE
RPF023   L     7,LLINE                 LENGTH OF RECORD
         SH    7,LENSTR                MINUS LENGTH OF STRING
         LA    7,1(7)                  PLUS 1 IS BCT VALUE
         L     2,SAVE02                FIRST CHAR OF RECORD
RPF025   EQU   *                       LOOK IF STRING IS IN RECORD
         EX    6,RPFCOMP1              COMPARE
         BE    RPF051                  YES: MATCH FOUND
         LA    2,1(2)                  NEXT POSITION
         BCT   7,RPF025
         L     2,SAVE02                STARTADDRESS RECORD
         BXLE  2,4,RPF027              NEXT RECORD
         B     RPF053                  RC=04 NO MATCH FOUND
RPF027   EQU   *
         ST    2,SAVE02                SAVE REG 2
         B     RPF023                  SEARCH AGAIN
RPFCOMP1 CLC   0(0,2),STRING           *** EXECUTE ONLY ***
RPF051   EQU   *                       MATCH FOUND IN LINE
         MVC   FIRSTSCR,SAVE02         ADDRESS MATCHED LINE
*                                      BECOME FIRST LINE ON SCREEN
         BAL   14,TIME                 GET TIME OF DAY
         BAL   14,DATE                 GET DATE
         MVC   11(6,2),DATEDISP        INSERT DATE
         MVC   18(4,2),TIMEDISP+2      INSERT TIME
         LA    15,0                    RC=0
         MVC   CMDAREA,BLANKS          BLANK 'CMDAREA'
         B     RPF099                  BACK TO CALLER
RPF053   EQU   *                       NO MATCH FOUND
         MVC   CMDAREA(22),ERR1TXT     MOVE ERROR MESSAGE
         LA    15,4                    RC=4
         B     RPF099                  BACK TO CALLER
RPF099   EQU   *                       END OF ROUTINE
         L     13,SAVE+4               LOAD CALLERS SAVEAREA
         RETURN (14,12),RC=(15)        BACK TO CALLER
***********************************************************************
*                                                                     *
*        TIME - CONVERT TIME OF DAY TO DISPLAY FORMAT                 *
*                                                                     *
*               OUTPUT: TIMEDISP - TIME OF DAY, HHMMSS                *
*                                                                     *
********************************************(C)-1982 SKYBIRD SYSTEMS***
TIME     ST    14,SAVE14T          SAVE REG.14
         TIME  DEC                 GET TIME OF DAY
         SRL   0,4                 SHIFT OUT THOUSANDS OF SECONDS
         ST    0,TIMEWORK          STORE INTO WORKFIELD
         OI    TIMEWORK+3,X'0F'    INSERT SIGN
         MVC   TIMEDISP,PATT1      PATTERN INTO OUTPUT FILED
         ED    TIMEDISP(8),TIMEWORK
TIME99   L     14,SAVE14T          RESTORE REG.14
         BR    14                  RETURN
***********************************************************************
*                                                                     *
*        DATE - CONVERT A THE JULIAN DATE IN THE CVT (YYDDD) TO       *
*               YYMMDD                                                *
*               OUTPUT - YYMMDD RETURNED IN FIELD "DATEDISP"          *
*                                                                     *
********************************************(C)-1982 SKYBIRD SYSTEMS***
DATE     ST    14,SAVE14D          SAVE REG 14
         L     6,X'4C'             POINTER CVT
         UNPK  DAYNBR,57(3,6)      JULIAN DATE
         PACK  YEAR,DAYNBR(2)
         MVC   DATEDISP(2),DAYNBR YEAR IN DATE
         DP    YEAR(4),=PL1'4'    DIVIDE BY 4
         CP    YEAR+3(1),=PL1'0'  LEAP YEAR
         BNE   DATE2              BRANCH IF NOT
         ZAP   FEB,=PL2'29'       29 DAYS IN FEBRUARI
*
DATE2    EQU   *
         LA    7,JAN             MONTHTABLE
         ZAP   DAY,58(2,6)
*
DATE3    EQU   *
         CP    DAY(3),0(2,7)
         BNH   DATE4
         SP    DAY,0(2,7)        SUBTRACT ULTIMO DATE
         LA    7,2(7)              NEXT MONTH
         AP    MNDNR,=PL1'1'
         B     DATE3               RETURN
*
DATE4    EQU   *
         UNPK  UNPKMND,MNDNR
         OI    UNPKMND+2,X'F0'
         MVC   DATEDISP+2(2),UNPKMND+1
         UNPK  UNPKMND,DAY
         OI    UNPKMND+2,X'F0'
         MVC   DATEDISP+4(2),UNPKMND+1
         L     14,SAVE14D          RESTORE R14
         BR    14                  BACK TO CALLER
         TITLE  'VARIABLES AND CONSTANTS'
SAVE14D  DC    F'0'                SAVE AREA DATE SUBROUTINE
SAVE14T  DC    F'0'                SAVE AREA TIME SUBROUTINE
TIMEWORK DC    F'0'
TIMEDISP DC    CL8' '
PATT1    DC    X'F020212020202020'
ADDR1    DC    F'0'
ADDR2    DC    F'0'
LINES    DC    CL4'FFFF'
DBW      DC    D'0'
DAYNBR   DS    CL5
YEAR     DC    PL4'0'
JAN      DC    PL2'31'
FEB      DC    PL2'28'
MRT      DC    PL2'31'
APR      DC    PL2'30'
MAY      DC    PL2'31'
JUN      DC    PL2'30'
JUL      DC    PL2'31'
AUG      DC    PL2'31'
SEP      DC    PL2'30'
OKT      DC    PL2'31'
NOV      DC    PL2'30'
DEC      DC    PL2'31'
MNDNR    DC    PL2'1'
UNPKMND  DS    CL3
DATEDISP DS    CL6
DAY      DC    PL3'0'
SAVE02   DS    F
F0080    DC    F'80'
ERR1TXT  DC    CL22'  //* SYPRID NOT FOUND'
RLENGTH  DC    A(NEXTENT)              LENGTH OF WORKSPACE ENTRY
LLINE    DS    F
*                0..........11.....18...22..26......
*              C'//* SYPRID JJMMDD HHMM HST COMPNAME'
STRING   DC    C'//* SYPRID '
LENSTR   DC    H'11'                   LENGTH STRING
         LTORG
         TITLE 'RPFCOMM DSECT'
COMMAREA RPFCOMM
         END
