RPFTSOEX TITLE 'RPF USER EXIT TO CHECK IF USER IS ALLOWED TO ISSUE THIS/
                TSO COMMAND'
***********************************************************************
* THIS ROUTINE IS A USER-EXIT IN RPFTSO TO VALIDATE THE COMMAND       *
* THAT THE USER WANTS TO EXECUTE.                                     *
*                                                                     *
* WHEN ACF2 IS ACTIVE ON THE SYSTEM, THE TSO USER MIGHT HAVE A        *
* TSO-COMMAND-LIMITING LIST. IF HE HAS, WE HAVE TO CHECK THE          *
* COMMAND HE WANTS TO EXECUTE. THIS WAY WE ARE SURE THAT HE CANNOT    *
* EXECUTE ANY COMMANDS UNDER RPF THAT HE IS NOT ALLOWED TO EXECUTE.   *
*                                                                     *
* PLEASE NOTE THAT THIS SOURCE NEEDS THE ACF2 MACRO LIBRARY           *
* IN THE SYSLIB CONCATENATION OF THE ASSEMBLER. THIS IS NEEDED FOR    *
* THE MACRO'S "ACFSVC", "ACFREGS" AND "ACCVT".                        *
*                                                                     *
* IN THE FINAL LINKAGE EDITOR RUN FOR THIS ROUTINE AN INCLUDE SHOULD  *
* BE INSERTED FOR A MODULE IN THE "SYS2.ACFMOD".                      *
* THE NAME OF THE MODULE IS "$ACFGCVT".                               *
*                                                                     *
* FOR FURTHER INFORMATION ON ACF2, PLEASE REFER TO THE                *
*        "ACF2 SYSTEM PROGRAMMERS GUIDE"                              *
********************************************(C)-1981 SKYBIRD SYSTEMS***
RPFTSOEX CSECT                         RPF FOREGROUND ASSEMBLER
         SAVE  (14,12),,*              SAVE REGISTERS
         USING RPFTSOEX,15             GET TEMP ADDRESSABILITY
         LA    14,SAVE                 SAVE AREA
         ST    13,SAVE+4               BACKWARD POINTER
         ST    14,8(0,13)              FORWARD POINTER
         LR    13,14                   OVERIDE CALLERS SAVEAREA
         B     START                   BRANCH AROUND SAVEAREA
SAVE     DC    18F'-1'                 SAVEAREA
         DC    CL8'&SYSDATE',C' ',CL8'&SYSTIME'
START    DS    0H
         DROP  15                      KILL TEMP ADDRESSABILITY
         USING SAVE,13
         L     11,0(1)                 PICKUP PARAMETER
         USING COMMAREA,11             GET ADDRESSABILITY
         MVC   ACFNAME(8),CMDAREA      MOVE TSO COMMAND
         XR    1,1                     CLEAR R1.
         LA    0,ACFLIST               LOAD PARAMETER LIST FOR ACF2.
         ACFSVC ACFLIST,TYPE=C,NONE=NOACF,CVT=FIND
         ST    15,RTNCODE              STORE RETURNCODE
         B     GOBACK
NOACF    MVC   RTNCODE(4),=F'12'       ACF2 NOT ACTIVE.
         B     GOBACK                  GO TO FINAL PROCESSING
GOBACK   L     13,SAVE+4               RESTORE CALLERS SAVEAREA.
         RETURN (14,12),RC=0           RETURN TO CALLER.
         EJECT
ACFLIST  DS    0F
         DC    XL2'0001'               ENTRY COUNT. MUST BE X'0001'
         DC    XL2'0008'               LENGTH OF DATA. MUST BE 8.
ACFNAME  DC    CL8' '                 NAME OF COMMAND TO VALIDATE.
         ACFREGS
         LTORG
         EJECT
         ACCVT
         EJECT
COMMAREA RPFCOMM
         END
