RPFLISTC TITLE 'Create a dataset selection list from IDCAMS LISTCAT'
*---------------------------------------------------------------------*
*                                                                     *
*        MODULE:        RPFLISTC                                      *
*                                                                     *
*        Called by:     RPFVTOC                                       *
*                                                                     *
*        FUNCTION:      Call IDCAMS to process a                      *
*                       LISTCAT LEVEL('hlq') NONVSAM VOLUMES          *
*                       Interpret the output and place the            *
*                       found datasets with DCB and SPACE information *
*                       in the RPF workspace.                         *
*                       For the SPACE and DCB information the OBTAIN  *
*                       macro will be used.                           *
*                       If the dataset is on tape, no SPACE and DCB   *
*                       info will set in the workspace record         *
*                       For this info an OBTAIN will be used.         *
*                       See DSECT VTOCAREA for the layout.            *
*                       Each dataset uses two lines in the RPF        *
*                       workspace.                                    *
*                       Upon completion the number of found datasets  *
*                       is placed into byte 0-2 of CMDAREA            *
*                       After completion of RPFLISTC control will     *
*                       be passed to RPFVTOC1 (via RPFVTOC) to        *
*                       process the data set list.                    *
*                                                                     *
*        Input parm     A('hlq') address of 'hlq' 8 positions         *
*                       A(COMMAREA) address of the RPFCOMM area       *
*                                                                     *
*        Returncodes:   00     - alright                              *
*                       04     - Workspace truncated                  *
*                       08     - No datasets matched with 'hlq'       *
*                       12     - allocation error SYSIN/SYSPRINT      *
*                                                                     *
*        Modifications:                                               *
*        Nov 04,2002 Rpr: Obtain real volume if OS/390 variable or    *
*                         cataloged on volume '******'             @rp1
*        Nov 13,2002 RPr: Dataset level increased to 17 bytes      @rp2
*                                                                     *
*-------------------------------------- (C)-2002 Skybird Systems -----*
         SPACE 2
RPFLISTC CSECT                         *
         SAVE  (14,12),,*              Save registers
         USING RPFLISTC,R15            Get local addressability
         LA    R14,SAVE                New save area
         ST    R13,SAVE+4              Backward pointer
         ST    R14,8(R13)              Foreward pointer
         LR    R13,R14                 Exchange savearea
         DROP  R15                     Kill local addressability
         LA    R12,2048(R13)           2nd base register
         LA    R12,2048(R12)
         USING SAVE,R13,R12            Get address via R13,R12
         B     RPFINITT                Branch around savearea
*
SAVE     DC    18F'-1'                 SAVE AREA
         DC    CL8'&SYSDATE',C' ',CL8'&SYSTIME'
RPFINITT DS    0H
         L     R8,0(R1)                Point to 'hlq'
         L     R11,4(R1)               Point to commarea
         USING COMMAREA,R11            Adress commarea
         ZAP   DSNCNTR,=P'0'           Clear record counter
         SYALLOC FREE=RPFIN,MF=E       Free previous RPFIN
         SYALLOC DDN=RPFIN,UNITA=COMMSTOR,PRI=1,SEC=1,                 X
               SPTYP=TRK,DISP1=NEW,                                    X
               MF=E
         LTR   R15,R15                 Alloc ok ?
         BNZ   IDCERR1                 Error..
         SYALLOC FREE=RPFPRINT,MF=E    Free previous RPFPRINT
         SYALLOC DDN=RPFPRINT,DISP1=NEW,PRI=7,SEC=3,SPTYP=CYL,         X
               UNITA=COMMSTOR,MF=E
         LTR   R15,R15                 Alloc ok ?
         BNZ   IDCERR1                 Error
         OPEN  (SYSIN,(OUTPUT))        Control statement
         MVC   RECORD,BLANKS           Blank
         MVC   RECORD(47),=C' LISTC LEVEL(SYS1             ) NONVSAM VOx
               LUMES'                                              @rp2
         MVC   RECORD+13(17),0(R8)     Move 'hlq' from caller      @rp2
         PUT   SYSIN,RECORD            And Write
         CLOSE SYSIN                   Close the file
         FREEPOOL SYSIN                Free the buffers
         LINK  EP=IDCAMS,PARAM=(OPTLIST,IDCDDNS),VL=1
         B     IDC999                  Branch
IDCERR1  DS    0H
         SYALLOC FREE=RPFIN,MF=E       Free the
         LA    R15,12                  DS could not be allocated
         B     RETURN                  Exit program
IDC999   DS    0H
         LR    R2,R15                  Save RC of IDCAMS
         SYALLOC FREE=RPFIN,MF=E       Free SYSIN
         LTR   R2,R2                   RC IDCAMS ok?
         BNZ   DSL099                  No: terminate
         OPEN  RPFPRINT                Open catlg listing from IDCAMS
         USING VTOCAREA,R10
         L     R10,GETMSTRT            Start of RPF workspace
         ST    R10,FIRSTREC            Is also first record
         SL    R10,=A(2*NEXTENT)       Minus 1 entry
DSL002   DS    0H
         LA    R15,READAMS             IO routine
         BALR  R14,R15                 Branch to routine
*        BASSM R14,R15                 Branch to routine
         LTR   R15,R15                 EOF ?
         BNZ   DSL099                  Yes: branch
         CLC   TYPE1,NONVS             Do we have a NONVSAM?
         BE    DSL040                  NO.
         CLC   VOLIND,=C'VOLSER'       Is this a VOLSER-line?
         BE    DSL052                  Yes: set in output field
         CLC   VOLIND-2(7),=C'SMSDATA' Is dataset SMS managed
         BE    DSL060                  Yes: set in output field
         B     DSL002                  Read next record
DSL040   DS    0H
         LA    R10,2*NEXTENT(R10)      Next entry in workspace (2 recs)
         C     R10,GETMEND             Compare with GETMEND
         BL    DSL042                  Low: branch
         CLOSE RPFPRINT                Close
         FREEPOOL RPFPRINT                 and free the buffers
         LA    R5,4                    Workspace truncated
         B     RETURN                  Return to caller
DSL042   DS    0H
         LA    R3,NEXTENT(R10)         Next record
         ST    R3,LASTREC              Point to last record
         AP    DSNCNTR,=P'1'           Add found dsname
         MVI   PREFIX,C' '             Blank
         MVC   PREFIX+1(175),PREFIX         output area
         XC    PREFIX+6(2),PREFIX+6    Clear flags
         XC    PREFIX2+6(2),PREFIX2+6        of both workspace lines
         MVC   DSN,NAME                Move dataset name to output
         MVC   SMS,=C'NO '             Mark default not SMS managed
         ZAP   VOLCNTR,=P'0'           Clear record counter
         B     DSL002                  And branch
DSL052   DS    0H
         AP    VOLCNTR,=P'1'           Count volume
         CP    VOLCNTR,=P'1'           Multivolume dataset?
         BNH   DSL054                  Branch
         MVI   MULT,C'+'               Mark multivolume dataset
         B     DSL002                  And read next record
DSL054   DS    0H
         MVC   VOL,VOLSER              Move volume from IDCAMS line
         CLI   VOLSER,C'&&'            An OS/390 variable (&SYSR1) @rp1
         BE    *+12                    Yes: do a locate for the vol@rp1
         CLI   VOLSER,C'*'             Cataloged on sysres ?       @rp1
         BNE   DSL054A                 No: branch                  @rp1
         MVC   LOCDSN,DSN              Move dataset name           @rp1
         LOCATE LOC                    Locate to get real volume   @rp1
         LTR   R15,R15                 Ok?                         @rp1
         BNZ   DSL054A                 Ignore if not               @rp1
         MVC   VOL,LOCVOL              Move real volume            @rp1
         MVC   VOLSER,LOCVOL           Overwrite original volser   @rp1
         SYCONVHX IN=LOCTYPE,OUT=DEVTYP1,L=4 Move real devtype     @rp1
DSL054A  DS    0H
         MVC   TRKCYL,=H'15'           Default 15 trks/cyl
         LA    R3,TYPETAB              Table with devtype+Trk/Cyl
         LA    R4,#TYPES               # entries
DSL054B  DS    0H
         CLC   0(2,R3),DEVTYP4         Match found?
         BE    DSL054C                 Yes: end
         LA    R3,4(R3)                Next entry in TYPETAB
         BCT   R4,DSL054B              Branch until found or done
         B     DSL054D
DSL054C  DS    0H
         MVC   TRKCYL,2(R3)            Trks/cyl
DSL054D  DS    0H
         TR    DEVTYP1(8),TRTAB        Xlate EBCDIC DEVTYPE to Hex
         PACK  DWB(5),DEVTYP1(9)       Remove zones
         MVC   TYPE,DWB                Move Hex device type
         CLI   TYPE+2,X'20'            Dasd dataset?
         BNE   DSL002                  No: read next record
         MVC   INPDSN,DSN              Move dsname to obtain
         OBTAIN OBTCMLST               Try to obtain dataset info
         LTR   R15,R15                 Obtain OK ?
         BNZ   DSL002                  No: possible not online
         XC    FORMAT2,FORMAT2         Clear next DSCB
         CLC   DS1PTRDS,=XL5'0'        No DSCB 2 or 3?
         BE    DSL056                  Branch
*        MVC   VOL,DS1DSSN             Save VOLUME
         MVC   CCHHR,DS1PTRDS          Save CCHHR of DSCB 2 or 3
         MVC   FORMAT2,=A(AREA2)       Repair FORMAT2
         MVC   SEEKLST+12(4),FORMAT2
         OBTAIN SEEKLST                   OBTAIN for DSCB2 or 3
         XC    FORMAT3,FORMAT3         Clear FORMAT3 ind
         CLC   AREA2+135(5),=XL5'0'    Another DSCB for the dataset?
         BE    DSL056                  No: branch
         MVC   CCHHR,AREA2+135         Sace CCHHR 2nd DSCB
         MVC   FORMAT3,=A(AREA3)       Repair format3
         MVC   SEEKLST+12(4),FORMAT3
         OBTAIN SEEKLST                OBTAIN for 2nd DSCB
DSL056   DS    0H
         IC    R15,DS1NOEPV            # extents on volume
         CVD   R15,DWB                 To packed
         MVC   DEXT,=X'40202120'       Move edit mask
         ED    DEXT,DWB+6              Edit the result
         XR    R8,R8                   Clear register
         IC    R8,DS1CREDT             Year of creation date
         CVD   R8,DWB                  Convert to packed
         UNPK  CREAT(2),DWB            Unpack year
         OI    CREAT+1,X'F0'           Clear sign
         ICM   R8,3,DS1CREDT+1         Days of creation date
         CVD   R8,DWB                  Convert to packed
         UNPK  CREAT+3(3),DWB          Convert to zoned
         OI    CREAT+5,X'F0'           Clear sign
         MVI   CREAT+2,C'.'            Move period
         XR    R8,R8                   Clear register
         IC    R8,DS1REFD              Year of reference date
         CVD   R8,DWB                  Convert to packed
         UNPK  REFDT(2),DWB            Unpack year
         OI    REFDT+1,X'F0'           Clear sign
         ICM   R8,3,DS1REFD+1          Days of reference date
         CVD   R8,DWB                  Convert to packed
         UNPK  REFDT+3(3),DWB          Convert to zoned
         OI    REFDT+5,X'F0'           Clear sign
         MVI   REFDT+2,C'.'            Move period
         TM    DS1DSORG+1,DS1ACBM      VSAM dataset ?           @bandke
*        TM    DS1DSORG+1,DS1ORGAM     VSAM dataset ?
         BNO   *+14                    If not
         MVC   DSORG,=C'VSAM'          Mark vsam dataset
         B     DSORGEND                Bypass test DSORG
         TM    DS1DSORG,DS1DSGIS       Dsorg=IS ?
         BNO   *+14                    Test next function
         MVC   DSORG,=C'IS  '          Dsorg=IS
         B     DSORGEND                End of test
         TM    DS1DSORG,DS1DSGPS       Dsorg=PS ?
         BNO   *+14                    Next DSORG if not
         MVC   DSORG,=C'PS  '          Physical sequential
         B     DSORGEND                End of test
         TM    DS1DSORG,DS1DSGDA       Dsorg=DA ?
         BNO   *+14                    Next DSORG test
         MVC   DSORG,=C'DA  '          Direct access
         B     DSORGEND                End of DSORG test
         TM    DS1DSORG,DS1DSGPO       Dsorg=PO ?
         BNO   *+14                    DSORG undefined
         MVC   DSORG,=C'PO  '          Partitioned
         B     DSORGEND                End of dsorg test
         MVC   DSORG,=C'UND '          No valid DSORG
DSORGEND TM    DS1RECFM,X'C0'             undefined ?
         BNO   RECF                    Test next RECFM
         MVC   RECFM(4),=C'U   '       Recfm=u
         B     RECEND
RECF     TM    DS1RECFM,X'80'          Fixed ?
         BNO   RECV                    Test next RECFM
         MVI   RECFM,C'F'              Fixed
         TM    DS1RECFM,X'10'          Blocked ?
         BNO   RECFS                   No: proceed
         MVI   RECFM+1,C'B'            Mark blocked
RECFS    TM    DS1RECFM,X'08'          Standard blocks ?
         BNO   RECFA                   No: proceed
         MVI   RECFM+2,C'S'            Standard
         B     RECEND
RECFA    TM    DS1RECFM,X'04'          ASA control chars ?
         BNO   RECFBM                  No: proceed
         MVI   RECFM+2,C'A'            Asa
         B     RECEND
RECFBM   TM    DS1RECFM,X'02'          Mach control char ?
         BNO   RECEND                  No: proceed
         MVI   RECFM+2,C'M'            Mach cc
         B     RECEND
RECV     TM    DS1RECFM,X'40'          Variable
         BNO   RECUND                  No valid RECFM
         MVI   RECFM,C'V'              Fixed
         TM    DS1RECFM,X'10'          Blocked ?
         BNO   RECVS                   No: proceed
         MVI   RECFM+1,C'B'            Mark blocked
RECVS    TM    DS1RECFM,X'08'          Spanned blocks ?
         BNO   RECVA                   No: proceed
         MVI   RECFM+2,C'S'            Standard
         B     RECEND
RECVA    TM    DS1RECFM,X'04'          Asa control chars ?
         BNO   RECVBM                  No: proceed
         MVI   RECFM+2,C'A'            Asa
         B     RECEND
RECVBM   TM    DS1RECFM,X'02'          Mach control char ?
         BNO   RECEND                  No: proceed
         MVI   RECFM+2,C'M'            Mach cc
         B     RECEND
RECUND   MVC   RECFM(4),=C'UND '
RECEND   EQU   *
         XR    R9,R9                                            @BANDKE
         ICM   R9,3,DS1LRECL           Lrecl                    @bandke
         MVC   LRECL,=X'402020202120'  Move edit pattern
         CVD   R9,DWB                  Convert to decimal
         ED    LRECL(6),DWB+5          Edit into LRECL
         XR    R9,R9                   Clear register           @bandke
         ICM   R9,3,DS1BLKL            Blksize                  @bandke
         MVC   BLKSIZE,=X'402020202120' Move edit pattern
         CVD   R9,DWB                  Convert to decimal
         ED    BLKSIZE(6),DWB+5        Edit into record
*
*        CALCULATE SPACE PER EXTENT
*
         LA    R2,DS1EXT1              Point to first extent
         SR    R5,R5
         LA    R9,3                    3 extents from FORMAT1 DSCB
         BAL   R14,EXTENT
         BCT   R9,*-4
         LA    R1,LIST                 List address
         L     R3,0(R1)                1st entry in list
         LTR   R3,R3                   Another dscb?
         BZ    FORM111                 No: no DSCBs anymore
         CLI   44(R3),C'3'             Format 3
         BNE   FORM107                 No: skip
         LR    R2,R3                   Yes: format3 to reg2
         BAL   R14,FORM109             Compute extents
FORM107  LA    R1,LIST                 List address
         L     R4,4(R1)                2nd entry in list
         LTR   R4,R4                   Another DSCB
         BZ    FORM111                 No,total
         CLI   44(R4),C'3'             Format 3
         BNE   FORM111                 No,total
         LR    R2,R4                   Yes: format3 to reg 2
         BAL   R14,FORM109             Compute extents
         CLC   DS3PTRDS-IECSDSL3(5,R4),=XL5'0' More F3DSCBs chained ?
         BE    FORM111                 No: branch
         MVC   CCHHR,DS3PTRDS-IECSDSL3(R4) move cchhr next DSCB
         MVC   FORMAT3,=A(AREA3)       Repair format3
         MVC   SEEKLST+12(4),FORMAT3
         OBTAIN SEEKLST                Obtain for next DSCB
         B     FORM107                 Branch
         EJECT
*
*        EXTENTS FROM FORMAT3 DSCB  R2 ---> A(F3DSCB)
*
FORM109  DS    0H
         ST    R14,SAVE14F             Save register
         LA    R9,4
         LA    R2,4(R2)
         BAL   R14,EXTENT
         BCT   R9,*-4                  First four extents
         LA    R2,1(R2)                Skip f3 format id
         LA    R9,R9                   Remaining nine extents
         BAL   R14,EXTENT
         BCT   R9,*-4
         L     R14,SAVE14F             Restore register
         BR    R14                     Return
         SPACE 5
*
*        COMPUTE UNUSED SPACE
*
FORM111  DS    0H
         CVD   R5,DWB                  Cum tracks
         MVC   TRK(6),=X'402020202120' Pattern over field
         ED    TRK(6),DWB+5            Edit the result
         MVC   UNUSED+3(3),=C'N/A'
         TM    DS1DSORG+1,DS1ACBM      Skip vsam                @bandke
*        TM    DS1DSORG+1,DS1ORGAM     Skip vsam
         BO    DSL002                  Read next IDCAMS record
         LH    R3,DS1LSTAR             Relative adress unused
         BCTR  R5,0
         SR    R5,R3
         C     R5,=F'0'                Negative ?
         BL    DSL002                  Yes: read next IDCAMS record
         CVD   R5,DWB                  Convert to decimal
         MVC   UNUSED,=X'402020202120' Move edit pattrn
         ED    UNUSED(6),DWB+5         Edit the result
         B     DSL002                  Read next record
DSL060   DS    0H
         MVC   SMS,=C'YES'             Mark sms managed
         B     DSL002                  Read next record
DSL099   DS    0H
         XR    R15,R15                 RC = 0000
         CP    DSNCNTR,=P'0'           No datasets ?
         BNE   RETURN                  No: alright
         LA    R15,8                   No datasets matched with 'hlq'
RETURN   DS    0H
         LR    R2,R15                  Save reg.15
         SYALLOC FREE=RPFPRINT,MF=E    Free RPFPRINT
         ZAP   CMDAREA(3),DSNCNTR      # DS in CMDAREA
         LR    R15,R2                  Restore reg.15
         L     R13,4(R13)              Pickup callers savearea
         RETURN (14,12),RC=(15)        Return to caller
         TITLE 'Accumulate the extents of the dataset'
EXTENT   EQU   *
*
*        2     EXTENT ADRESS
*        5     CUMULATIVE TRACKS
*        14    RETURN ADRESS
*
         ST    R14,SAVE14E             SAVE REG 14
         CLI   0(R2),0                 No extent desc in the field  ?
         BE    EXTEND                  Yes: EXIT
         MVC   DWB(8),2(R2)            Move extent boundaries
         LH    R8,DWB+4
         SH    R8,DWB
         MH    R8,TRKCYL
         AH    R8,DWB+6
         SH    R8,DWB+2
         LA    R8,1(R8)
         AR    R5,R8
         LA    R2,10(R2)
EXTEND   L     R14,SAVE14E             Restore R14
         BR    R14
         TITLE 'Retrieve a record from LISTCAT output'
*---------------------------------------------------------------------*
*                                                                     *
*        Routine:      READAMS                                        *
*        Function:     Get a record from the IDCAMS LISTCAT output    *
*        Return codes: 00 - record read                               *
*                      04 - End of File detected                      *
*                                                                     *
*-------------------------------------- (C)-2002 Skybird Systems -----*
READAMS  DS    0H
         ST    R14,SAVE14R             Save reg.14
         MVC   LISTING(125),BLANKS     Blank line first
         GET   RPFPRINT,LISTING        Read IDCAMS LISTCAT output
         XR    R15,R15                 RC = 0000
         B     READ999
READ001  DS    0H
         CLOSE RPFPRINT
         FREEPOOL RPFPRINT
         LA    R15,4                   RC = 0004
READ999  DS    0H
         L     R14,SAVE14R             Load reg.14
         BR    R14                     Return
*        BSM   0,R14                   Return
         TITLE 'Data areas and constants'
DWB      DS    D                       Work field
SAVE14R  DS    F                       Reg.14 READAMS routine
SAVE14E  DS    F                       Reg.14 EXTENT  routine
SAVE14F  DS    F                       Reg.14 FORM109 routine
TRKCYL   DS    H                       # tracks/cylinder
DSNCNTR  DS    PL5                     # of found datasets with 'hlq'
VOLCNTR  DC    PL3'0'                  # volumes per dataset
NONVS    DC    C'NONVS'
GDGB     DC    C'GDG B'                GDG BASE ENTRY
OPTLIST  DC    H'0'                    NO OPTIONS FOR IDCAMS.
IDCDDNS  DS    0F
         DC    AL2(STOPC-STARTC)
STARTC   EQU   *
         DC    XL8'00'                 Empty entry (SYSLIN)
         DC    XL8'00'                 EMPTY ENTRY (SYSLMOD MBR)
         DC    XL8'00'                 EMPTY ENTRY (SYSLMOD)
         DC    XL8'00'                 Empty entry (SYSLIB)
         DC    CL8'RPFIN'              SYSIN
         DC    CL8'RPFPRINT'           SYSPRINT
         DC    XL8'00'                 Empty entry (SYSPUNCH)
         DC    XL8'00'                 Empty entry (SYSUT1)
         DC    XL8'00'                 Empty entry (SYSUT2)
         DC    XL8'00'                 Empty entry (SYSUT3)
         DC    XL8'00'                 Empty entry (SYSGO)
         DC    XL8'00'                 Empty entry (SYSTERM)
STOPC    EQU   *
*
TYPETAB  DS    0H                      devtyp4 + trks/cyl
         DC    C'08',H'20'             2314
         DC    C'09',H'19'             3330
         DC    C'0B',H'30'             3350
         DC    C'0C',H'12'             3375
         DC    C'0D',H'19'             3330-1
         DC    C'0E',H'15'             3380
         DC    C'0F',H'15'             3390
TYPEEND  EQU   *
#TYPES   EQU   (TYPEEND-TYPETAB)/4
OBTCMLST CAMLST SEARCH,INPDSN,VOLSER,DS1FMTID
SEEKLST  CAMLST SEEK,CCHHR,VOLSER,FORMAT3
         DS    0D
         IECSDSL1 (1)
INPDSN   DS    CL44                    Data set name to obtain
CCHHR    DS    CL5                     For CAMLST SEEK
LIST     DS    0F
FORMAT2  DC    A(AREA2)
FORMAT3  DC    A(AREA3)
*
AREA2    DS    CL144
AREA3    DS    CL144
RECORD   DS    CL80                    IDCAMS SYSIN record
*
         DS    0F                                                  @rp1
LOC      CAMLST NAME,LOCDSN,,LOCAREA                               @rp1
         DS    0D                                                  @rp1
LOCAREA  DS    CL2                                                 @rp1
LOCTYPE  DS    CL4                     Device type                 @rp1
LOCVOL   DS    CL6                     Volume                      @rp1
         DS    CL253                   Rest of LOCAREA             @rp1
LOCDSN   DS    CL44                                                @rp1
*                                                                  @rp1
TRTAB    DC    193XL1'00'              Translation table to xlate
         DC    X'0A0B0C0D0E0F'         EBCDIC to HEX
         DC    41XL1'00'
         DC    X'00010203040506070809'
         DC    6XL1'00'
         TITLE 'INPUT LINES FROM IDCAMS OUTPUT'
         DS    0H                      ALIGN ON HW-BOUNDARY.
LISTING  DS    0CL125                  CONTAINS IDCAMS-OUTPUTLINE.
RECLENG  DS    H                       CONTAINS RECORD-LENGTH.
         DS    CL123
         ORG   *-125
IDENT    DS    0CL125                  CONTAINS ENTRY-TYPE AND NAME.
         DS    CL5
TYPE1    DS    CL5
         DS    CL11
NAME     DS    0CL44
HLQ      DS    CL9                     High level qualifier
         DS    CL35
         DS    CL60
         ORG   *-125
IDCVOL   DS    0CL125                  CONTAINS VOLUME-INFORMATION.
         DS    CL12
VOLIND   DS    CL6                     IDENTIFIES A VOLSER-LINE.
         DS    CL12
VOLSER   DS    CL6
         DS    CL20
DEVTYP1  DS    CL2
DEVTYP2  DS    CL2
DEVTYP3  DS    CL2
DEVTYP4  DS    CL2
         DS    CL27
IMPFILE  DS    CL3
         DS    CL31
*
         PRINT NOGEN
SYSIN    DCB   DSORG=PS,DDNAME=RPFIN,MACRF=PM,                         X
               RECFM=F,LRECL=80,BLKSIZE=80
RPFPRINT DCB   DDNAME=RPFPRINT,DSORG=PS,MACRF=(GM),LRECL=125,          *
               RECFM=VBA,EODAD=READ001
         SYALLOC MF=L
         PRINT GEN
         LTORG  ,
         TITLE 'DSECTS'
VTOCAREA DSECT                         Entry IN workspace
PREFIX   DS    CL8                     contains linenumber and flags
DSN      DS    CL44                    Dataset name
MULT     DS    C                       Contains a '+' if multivol. ds
STARTADR DS    CL16                    Start address
         DS    C
VOL      DS    CL6                     Volume
         DS    C
TYPE     DS    CL4                     UCB type
         DS    C
SMS      DS    CL3                     SMS  (YES or NO)
OPER     DS    C                       OPER COULD BE
         DS    CL2
PREFIX2  DS    CL8                     Characteristics line
NODSN    DS    CL27                    Filler
CREAT    DS    CL6                     data set creation date
         DS    C
REFDT    DS    CL6                     last reference date
         DS    C
DSORG    DS    CL4
RECFM    DS    CL4
LRECL    DS    CL6
         DS    C
BLKSIZE  DS    CL6
DEXT     DS    CL4                     Number of extents
TRK      DS    CL6                     Data set size in tracks
UNUSED   DS    CL6                     Unused space in data set
         DS    CL2
*
COMMAREA RPFCOMM
F3DSCB   DSECT
         IECSDSL1 (3)
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
