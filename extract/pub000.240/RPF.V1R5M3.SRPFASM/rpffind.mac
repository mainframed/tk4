RPFFIND  TITLE 'FIND A CHARACTER STING IN THE EDIT WORKSPACE'
*---------------------------------------------------------------------*
*                                                                     *
*        MODULE:        RPFFIND.                                      *
*                                                                     *
*        FUNCTION:      FIND A CHARACTER STRING IN THE EDIT WORKSPACE *
*                       THE SEARCH IS STARTED WITH THE SECOND LINE    *
*                       ON THE SCREEN AND ENDED AT THE LAST LINE IN   *
*                       THE WORKSPACE ("LASTREC" IN RPFCOMM)          *
*                       WHEN A MATCH IS FOUND IN A LINE, THIS LINE    *
*                       WILL BE THE FIRST LINE ON THE SCREEN.         *
*                       THIS MODULE IS CALLED BY RPFEDIT.             *
*                       A POINTER TO THE COMMAND IS IN 'CMDAREA'      *
*                       RPFFIND WILL ONLY SCAN THE COLUMS, WHICH ARE  *
*                       SPECIFIED IN THE 'SCOPE' COMMAND IN RPFEDIT.  *
*                       THIS MODULE WILL BE USED FOR THE 'O' (OMIT)   *
*                       COMMAND TOO. OMIT IS THE OPPOSITE OF THE FIND *
*                       IF THE CHAR STRING NOT MATCHES THIS LINE WILL *
*                       BE THE CURRENT LINE                           *
*                                                                     *
*        Formats of the command:                                      *
*        F 'string' or F 'string',line1,line2                         *
*        F "string" or F "string",line1,line2                         *
*        F string                                                     *
*        If the string contains blanks put the string between quotes  *
*        or double quotes. Double quotes can be used if the string    *
*        contains single quotes.                                      *
*        If line1,line2 are used, quotes are always needed            *
*        If the Omit command is used replace the "F" into "O"         *
*                                                                     *
*        RETURNCODES:   00 MATCH FOUND OF STRING.                     *
*                       08 ERRORS FOUND (MESSAGE IN 'CMDAREA' OF      *
*                          RPFCOMM).                                  *
*                                                                     *
*        Modifications:                                               *
*        May 24,2004: Change format of the command                    *
*                                                                     *
*------------------------------------------ (C)-2004 SKYBIRD SYSTEMS -*
         SPACE 2
RPFFIND  CSECT
         SAVE  (14,12),,*              Save registers
         USING RPFFIND,R15             Get local addressability
         LA    R14,SAVE                Our save area
         ST    R14,8(0,R13)            Foreward pointer
         ST    R13,SAVE+4              Backward pointer
         LR    R13,R14                 Load this savearea
         B     START                   Branch around savearea
SAVE     DC    18F'-1'                 Save area
         DC    CL8'&SYSDATE',C' ',CL8'&SYSTIME'
START    DS    0H
         DROP  R15                     KILL LOCAL ADDRESSABLILITY
         USING SAVE,R13                GET ADDR OVER SAVE
         L     R11,0(R1)               RPFCOMM AREA
         USING COMMAREA,R11            DSECT OVER PARM
         MVC   DLMCHAR,=C''''          Default delimiter
         ICM   R9,15,CMDAREA           LOAD POINTER TO FIND COMMAND
         MVC   FINDBR1(4),FIBR1        MODIFY INSTRUCTIONS
         MVC   FINDBR2(4),FIBR2        ) FOR THE 'FIND' COMMAND
         CLI   0(R9),C'O'              'OMIT' COMMAND ?
         BNE   START01                 NO: ISSUE 'FIND' COMMAND
         MVC   FINDBR1(4),OMBR1        MODIFY INSTRUCTIONS
         MVC   FINDBR2(4),OMBR2        ) FOR THE 'OMIT' COMMAND
START01  L     R2,FIRSTSCR             Default from current lne
         LA    R2,NEXTENT(R2)          OFFSET AFTER 1ST LINE
         ST    R2,FIRSTLNE
         MVC   LASTLNE,LASTREC         DEFAULT ENTIRE WORKSPACE
         LA    R10,FINDAREA            LOAD ADDRESS AREA
         CLI   2(R9),X'40'             ONLY 'F' OR 'O' ENTERED ?
         BE    TXT000                  YES: USE OLD STRING
         CLI   2(R9),X'00'             ONLY 'F' OR 'O' ENTERED ?
         BE    TXT000                  YES: USE OLD STRING
         MVC   FINDAREA,0(R9)          No: move new command
TXT000   EQU   *
         MVC   1(47,R9),FINDAREA+1 MOVE OLD  COMMAND (L=48 POS)
TXT001   DS    0H
         LA    R2,2(R10)               A(string) in F or O command
         CLI   0(R2),C''''             Delimiter found (quote)?
         BE    TXT002                  Yes: string with delimiter
         CLI   0(R2),C'"'              Delimiter found (dbl quote)?
         BE    TXT002                  Yes: string with delimiter
         B     TXT004                  No: string without delimiter
TXT002   DS    0H
         MVC   DLMCHAR,0(R2)           Move possible delimiter
         LA    R2,1(R2)                POINT AFTER DELIMITER
         XR    R3,R3                   R3= 0 used to count length
         LA    R4,45                   MAX L= 45 WITHOUT START DLM
TXT003   DS    0H                      *** START LOOP ***
         CLC   0(1,R2),DLMCHAR         Ending quote found?
         BE    TXT010                  Yes: end of loop
         LA    R2,1(R2)                NEXT POSITION IN STRING
         LA    R3,1(R3)                Count length of string
         BCT   R4,TXT003               Loop until ending quote found
         B     TXT055                  Invalid character string
*
TXT004   DS    0H                      F string cmd (without quotes)
         LA    R2,45(R2)               Point to last position
         LA    R3,46                   R3= 46 max. length string
TXT006   DS    0H
         CLI   0(R2),X'40'             End of string ?
         BE    TXT007                  YES: TEST PREVIOUS CHAR
         CLI   0(R2),X'00'             TRAILING NULL ?
         BNE   TXT031                  No: string complete
TXT007   DS    0H
         BCTR  R2,0                    Previous position
         BCT   R3,TXT006               Test again on end of string
         B     TXT055                  No string detected, invalid ...
TXT010   DS    0H
         ST    R3,SAVE3TXT             Save length string
         LA    R3,2(R2)                Start address for scan
         BAL   R14,LINE                Try to find line number
         LTR   R15,R15                 RC= 0000 ?
         BZ    TXT012                  Yes: proceed
         C     R15,F0004               Rc= 0004 - invalid line(s)?
         BE    TXT073
         C     R15,F0008               Rc= 0008 - not numeric ?
         BE    TXT073
         B     TXT077                  Unexpected return code
TXT012   CLC   LN2FLD,C0000            LN2 ENTERED ?
         BNE   TXT013                  YES: SKIP
         MVC   LN2FLD,LN1FLD           NO: LN2 = LN1
TXT013   CLI   LN1FLD,C'*'             * - CURRENT LINE ?
         BE    TXT014                  YES: CURRENT LINE
         CLC   LN1FLD,C0000            LN1 ENTERED ?
         BNE   TXT015                  YES: SKIP
TXT014   L     R3,FIRSTSCR             DEFAULT CURRENT LINE
         USING WORKSPCE,R3             ADDRESS WORKSPACE RECORD
         LA    R3,NEXTENT(R3)          AFTER CURRENT LINE
         MVC   LN1FLD+2(6),WORKSLIN    NUMBER INTO LN1FLD
         MVC   LN1FLD(2),=C'00'        LEADING ZEROES
         CLC   LN2FLD,C0000            LN2 ENTERED ?
         BNE   TXT015                  YES: SKIP
         L     R3,LASTREC              DEFAULT END OF WORKSPACE
         MVC   LN2FLD+2(6),WORKSLIN    NUMBER INTO LN2FLD
         MVC   LN2FLD(2),=C'00'        LEADING ZEROES
         DROP  R3
TXT015   EQU   *
         MVI   ERRPTXT+11,C'1'         MOVE ID IN MSG
         LA    R1,LN1FLD+2             PARM FOR SRCHLINE ROUTINE
         L     R15,EPSRCH              EP OF ROUTINE
         BALR  R14,R15                 TRY TO FIND LINE
         LTR   R15,R15                 LINE FOUND ?
         BNZ   TXT085                  NO: ISSUE ERRMSG
         LR    R7,R1                   A(FOUND LINE)
         MVI   ERRPTXT+11,C'2'         MOVE ID IN MSG
         LA    R1,LN2FLD+2             PARM FOR SRCHLINE ROUTINE
         L     R15,EPSRCH              EP OF ROUTINE
         BALR  R14,R15                 TRY TO FIND LINE
         LTR   R15,R15                 Line found ?
         BNZ   TXT085                  No: issue errmsg
         LR    R8,R1                   A(found line)
         ST    R7,FIRSTLNE             First line to scan
         ST    R8,LASTLNE              Last line to scan
         L     R3,SAVE3TXT             Restore length string
TXT031   EQU   *
         LTR   R3,R3                   NO STRING FOUND ?
         BZ    TXT055                  SET RC=08 AND GO TO CALLER
         C     R3,SCOPEL               STRING .LE. SCOPE ?
         BH    TXT079                  NO: ERROR
         STCM  R3,3,LENSTR             SAVE LENGTH OF STRING
         BCTR  R3,0                    LENGTH - 1 FOR EX
         LA    R2,2(R10)               String after F or O command
         CLC   DLMCHAR,0(R2)           Quoted string ?
         BNE   *+8                     No: branch
         LA    R2,1(R2)                Otherwise string starts next.pos
         MVC   STRING,BLANKS           CLEAR STRING
         EX    R3,TXTMOV1              MOVE STRING INTO FIELD
         OC    STRING,BLANKS           XLATE TO UPPERCASE
         CLC   FIRSTLNE,LASTLNE        FIRSTLNE AFTER LASTLNE
         BH    TXT053                  Yes: text not found
         B     TXT041                  SEARCH STRING IN WORKSPACE
TXTMOV1  MVC   STRING(0),0(R2)         MOVE CHAR STRING INTO FIELD
TXT041   EQU   *                       SEARCH STRING IN WORKSPACE
         L     R2,FIRSTLNE             R2= FIRSTSCR
         L     R5,LASTLNE              R5= LASTREC
         LA    R5,79(R5)               R5= ADDR AFTER LASTREC
         LA    R4,NEXTENT              INCREMENT IS 86 (LRECL)
         USING WORKSPCE,R2
         MVC   UCREC,WORKDATA          MOVE TO UPPERCASE RECORD
         OC    UCREC,BLANKS            XLATE TO UPPERCASE
         XR    R6,R6                   CLEAR REG.6
         LH    R6,LENSTR               LENGTH OF STRING
         BCTR  R6,0                    MINUS 1 FOR COMPARE
TXT043   L     R7,SCOPEL               LENGTH OF SCOPE
         SH    R7,LENSTR               MINUS LENGTH OF STRING
         LA    R7,1(R7)                INCLUDE LAST CHAR
         LA    R8,UCREC                FIRST CHAR OF RECORD
         A     R8,SCOPES               ADJUST WITH SCOPE
TXT045   EQU   *                       LOOK IF STRING IS IN RECORD
         EX    R6,TXTCOMP1             COMPARE
FINDBR1  BE    TXT046     (OMIT)       YES: MATCH FOUND
         LA    R8,1(R8)                NEXT POSITION
         BCT   R7,TXT045
FINDBR2  B     TXT051     (OMIT)
FIBR1    BE    TXT051    WILL BE SET IN FINDBR1 IF USED AS FIND
FIBR2    B     TXT046    WILL BE SET IN FINDBR2 IF USED AS FIND
OMBR1    BE    TXT046    WILL BE SET IN FINDBR1 IF USED AS OMIT
OMBR2    B     TXT051    WILL BE SET IN FINDBR2 IF USED AS OMIT
TXT046   DS    0H
         BXLE  R2,R4,TXT047            NEXT RECORD
         B     TXT053                  RC=04 NO MATCH FOUND
TXT047   DS    0H
         MVC   UCREC,WORKDATA          MOVE TO RECORD FIELD
         OC    UCREC,BLANKS            XLATE TO UPPER CASE
         B     TXT043                  SEARCH AGAIN
TXTCOMP1 CLC   0(0,R8),STRING          *** EXECUTE ONLY ***
TXT051   DS    0H                      MATCH FOUND IN LINE
         ST    R2,FIRSTSCR             ADDRESS MATCHED LINE
*                                      BECOME FIRST LINE ON SCREEN
         XR    R15,R15                 RC=0
         MVC   CMDAREA,BLANKS
         B     TXT099                  BACK TO CALLER
TXT053   EQU   *                       Text not found
         MVC   CMDAREA,ERRVTXT
         B     TXT098
TXT055   EQU   *                       Invalid character string
         MVC   CMDAREA,ERRWTXT
         B     TXT098
TXT073   EQU   *                       Line Invalid
         MVC   CMDAREA,ERRJTXT
         B     TXT098
TXT077   EQU   *                       Unexpected return code
         MVC   CMDAREA,ERR2TXT
         B     TXT098
TXT079   EQU   *                       SCOPE TOO SMALL
         MVC   CMDAREA,ERRETXT
         B     TXT098
TXT085   EQU   *                       Line X not found
         MVC   CMDAREA,ERRPTXT
TXT098   EQU   *                   ERROR ROUTINE
         LA    R15,8               RC= 0008
TXT099   EQU   *                   END OF ROUTINE
         L     R13,SAVE+4          LOAD CALLERS SAVEAREA
         RETURN (14,12),RC=(15)    BACK TO CALLER
         DROP   R2
*---------------------------------------------------------------------*
*                                                                     *
*        N U M T S T - SUBROUTINE, NUMERIC TEST OF A 8 CHAR FIELD.    *
*                                                                     *
*                    INPUT:  NUMFLD, FIELD CONTAINS CHARS TO TEST     *
*                    OUTPUT: NUMFLD, REMAINS UNCHAINGED               *
*                            REG.15, RETURN CODE 0000 - NUMERIC       *
*                                                0008 - NOT NUMERIC   *
*                                                                     *
*------------------------------------------ (C)-1985 SKYBIRD SYSTEMS -*
NUMTST   ST    R14,SAVE14          SAVE REG.14
         LA    R3,8                LENGTH
         LA    R4,NUMFLD           FIELD TO BE TESTED
NUMTST01 CLI   0(R4),X'F0'         LOW ?
         BL    NUMTST03            YES: NOT NUMERIC
         CLI   0(R4),X'F9'         HIGH ?
         BH    NUMTST03            YES: NOT NUMERIC
         LA    R4,1(R4)            NEXT CHAR.
         BCT   R3,NUMTST01         TEST MAX 8 CHARS.
         XR    R15,R15             RC= 0000, NUMERIC
         B     NUMTST99            GOTO EXIT
NUMTST03 LA    R15,8               RC= 0008, NOT NUMERIC
NUMTST99 L     R14,SAVE14          RESTORE REG.14
         BR    R14                 RETURN
*---------------------------------------------------------------------*
*                                                                     *
*        S H I F T - SUBROUTINE, REPLACE TRAILING SPACES WITH         *
*                    FILLCHAR.                                        *
*                                                                     *
*                    INPUT:  NUMFLD, FIELD TO BE SHIFTED, MAX 8 CHAR, *
*                            FILLCHAR, FILL CHARACTER.                *
*                    OUTPUT: NUMFLD, TRAILING SPACES REPLACED WITH    *
*                                    LEADING FILLCHAR                 *
*                            REG.15, RC= 0000 - NORMAL END            *
*                                    RC= 0004 - ALL SPACES            *
*                                                                     *
*------------------------------------------ (C)-1985 SKYBIRD SYSTEMS -*
SHIFT    ST    R14,SAVE14          SAVE REG.14
         LA    R3,8                MAX 8 CHARS
         XR    R15,R15             RC= 0000
SHIFT01  CLI   NUMFLD+7,X'40'      LAST CHAR SPACE ?
         BNE   SHIFT99             NO: GOTO EXIT
         MVC   WRKFLD+1(7),NUMFLD  MOVE FIRST 5 CHARS INTO WRKFLD+1
         MVC   WRKFLD(1),FILLCHAR  INSERT LEADING ZERO
         MVC   NUMFLD,WRKFLD       RESTORE NUMFLD
         BCT   R3,SHIFT01          SHIFT MAX 8 TIMES
         LA    R15,4               RC= 0004 - ALL SPACES
SHIFT99  L     R14,SAVE14          RESTORE REG.14
         BR    R14                 RETURN
*---------------------------------------------------------------------*
*                                                                     *
*        S C A N - SUBROUTINE, SCANS AREA FOR ' ' OR ',' AND PUTS     *
*                              PRECEEDING CHARS IN NUMFLD.            *
*                                                                     *
*                  INPUT:  REG.3,  POINTS TO FIRST CHAR TO SCAN       *
*                  OUTPUT: NUMFLD, CHARS PRECEEDING ' ' OR ','        *
*                          REG.3,  POINTS TO LATS CHAR SCANNED        *
*                          REG.15, RETURN CODE 0000 - CHARS FOUND     *
*                                              0008 - NO CHARS FOUND  *
*                                                                     *
*------------------------------------------ (C)-2004 SKYBIRD SYSTEMS -*
SCAN     ST    R14,SAVE14          SAVE REG.14
         LA    R15,8               RC= 0008, INITIAL: NO CHARS FOUND
         LA    R4,9                MAX 9 CHARS
         LA    R5,NUMFLD           POINT TO NUMFLD
         MVC   NUMFLD,BLANKS       CLEAR NUMFLD
SCAN01   CLI   0(R3),X'40'         SPACE ?
         BE    SCAN03              YES: END OF SCAN
         CLI   0(R3),X'6B'         COMMA ?
         BE    SCAN03              YES: END OF SCAN
         CLI   0(R3),X'00'         NULL ?
         BE    SCAN03              YES: END OF SCAN
         MVC   0(1,R5),0(R3)       MOVE 1 CHAR INTO NUMFLD
         LA    R3,1(R3)            NEXT CHAR TO SCAN
         LA    R5,1(R5)            NEXT CHAR NUMFLD
         BCT   R4,SCAN01           SCAN MAX 9 TIMES
         B     SCAN99              GOTO EXIT, NO CHARS FOUND
SCAN03   LA    R15,0               RC= 0000, SCAN SUCCESSFUL
SCAN99   L     R14,SAVE14          RESTORE REG.14
         BR    R14                 RETURN
*---------------------------------------------------------------------*
*                                                                     *
*        L I N E - SCAN INPUT BUFFER FOR LINE NUMBERS                 *
*                                                                     *
*                  INPUT:  REG.3, START ADDRESS SCAN,                 *
*                  OUTPUT: LN1FLD, LINE-1                             *
*                          LN2FLD, LINE-2                             *
*                          LN3FLD, LINE-3                             *
*                          REG.15, RC= 0000 - ALL LINES VALID,        *
*                                  RC= 0004 - INVALID LINE FOUND,     *
*                                  RC= 0008 - LINES NOT NUMERIC.      *
*                          ERRJTXT, ERROR INDICATING LINE NUMBER      *
*                                                                     *
*------------------------------------------ (C)-2004 SKYBIRD SYSTEMS -*
LINE     ST    R14,SAVE14LN        SAVE REG.14
         MVI   ERRJTXT+13,C'1'     MOVE LINE ID IN ERROR MSG
         BAL   R14,SCAN            SCAN FOR LN1FLD
         LTR   R15,R15             RC= 0000 ?
         BNZ   LINE73              NO: LN1 INVALID
         ST    R3,SAVE3            SAVE REG.3
         CLI   NUMFLD,C'*'         * - CURRENT LINE ?
         BE    LINE04              YES: SKIP NUMTST
         MVI   FILLCHAR,X'F0'      '0' INTO FILLCHAR
         BAL   R14,SHIFT           REP TRAIL. SPACE WITH LEADING ZERO
         C     R15,F0004           RC= 0004 - ALL SPACES
         BE    LINE03
         CLC   NUMFLD,C0000        LINE-1 ZERO ?
         BE    LINE73
LINE03   BAL   R14,NUMTST          NUMERIC TEST
         LTR   R15,R15             RC= 0000 ?
         BNZ   LINE75              NO: LN1 NOT NUMERIC
LINE04   MVC   LN1FLD,NUMFLD       SAVE LN1 INTO LN1FLD
         L     R3,SAVE3            RESTORE REG.3
         LA    R3,1(R3)            POINT TO LN2
         MVI   ERRJTXT+13,C'2'     MOVE LINE ID IN ERROR MSG
         BAL   R14,SCAN            SCAN FOR LN2FLD
         LTR   15,15               RC= 0000 ?
         BNZ   LINE73              NO: LN2 INVALID
         ST    R3,SAVE3            SAVE REG.3
         MVI   FILLCHAR,X'F0'      '0' INTO FILLCHAR
         BAL   R14,SHIFT           REP TRAIL. SPACE WITH LEADING ZERO
         C     R15,F0004               Rc= 0004 - all spaces ?
         BE    LINE05
         CLC   NUMFLD,C0000            LINE-2 ZERO ?
         BE    LINE73
LINE05   BAL   R14,NUMTST              NUMERIC TEST
         LTR   R15,R15                 RC= 0000 ?
         BNZ   LINE75                  NO: LN2 NOT NUMERIC
         MVC   LN2FLD,NUMFLD       SAVE LN2 INTO LN1FLD
         XR    R15,R15             RC= 0000 - SCAN SUCCESSFUL
         B     LINE99              EXIT
LINE73   LA    R15,4               RC= 0004 - INVALID LINE(S) FOUND
         B     LINE99              EXIT
LINE75   LA    R15,8               RC= 0008 - LINE(S) NOT NUMERIC
         B     LINE99              EXIT
LINE99   L     R14,SAVE14LN        RESTORE REG.14
         BR    R14                 RETURN
         TITLE 'WORKAREAS AND CONSTANTS'
DWB      DS    D
F0004    DC    F'4'
F0008    DC    F'8'
C0000    DC    C'00000000'
FIRSTLNE DS    F
LASTLNE  DS    F
LENSTR   DC    H'0'
STRING   DC    CL48' '
DLMCHAR  DC    C' '
ERR2TXT  DC    CL22'Unexpected return code'
ERRETXT  DC    CL22'-------Scope too small'
ERRJTXT  DC    CL22'--------Line-X invalid'
ERRPTXT  DC    CL22'------Line-X not found'  13
ERRVTXT  DC    CL22'--------Text not found'  11
ERRWTXT  DC    CL22'--Invalid char. string'
FINDAREA DC    CL48' '
UCREC    DS    CL100                   RECORD OF WORKSPACE UPPER CASE
SAVE14LN DS    F                   SAVE AREA REG.14 LINE
SAVE14   DS    F                   SAVE AREA REG.14 ALL
SAVE14T  DC    F'0'
SAVE3    DS    F                   SAVE AREA REG.3 SCAN
SAVE3TXT DS    F                   SAVE AREA REG.3
MAXLINES DS    F
FILLCHAR DC    CL1' '              FILL CHARACTER SHIFT ROUTINE.
NUMFLD   DS    CL8                 WORK FIELD NUMTST/SHIFT
WRKFLD   DS    CL8                 WORK FIELD NUMTST/SHIFT
LN1FLD   DS    CL8                 LINE-1 COPY/LINE
LN2FLD   DS    CL8                 LINE-2 COPY/LINE
LN3FLD   DS    CL8                 LINE-3 COPY/LINE
         LTORG
         TITLE 'RPFCOMM DSECT'
COMMAREA RPFCOMM
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
