RPFASM00 TITLE 'STAND ALONE RPF FOREGROUND ASSEMBLER MONITOR'
**********************************************************************
*                                                                    *
*  THIS ROUTINE IS A ROUTINE TO EXEC RPF FOREGROUND ASSEMBLER STAND- *
*  ALONE, OR LIKE ROB AND ROB IT DOES UNDER 'SPF'                    *
*  WHAT WE DO IS THE FOLLOWING:                                      *
*  - INITIALIZE THE RPF COMMAREA:                                    *
*  - DEFINE RPF DEFAULT PFKS,                                        *
*  - LOAD RPFDAIR AND STORE ADDRESS IN EPDAIR                        *
*  - TURN ON ASMBIT AND LINK TO RPFDATAU                             *
*  - IF RPFDATAU OK, ATTACH RPFASM                                   *
*  - DETACH RPFASM                                                   *
*  - FREE RPFWORK AND RPFWORK2                                       *
*  - TPUT NON-FULLSCREEN MSG TO GET THE RESHOW CODE.                 *
* SUBROUTINES: RPFDAIR                                               *
*              RPFASM                                                *
*              RPFASM1                                               *
*              RPFASM2                                               *
*              RPFLKD1                                               *
*              RPFPFK                                                *
*  NOTE: COMMAREA OF RPF IS USED WITH DSECT=NO!!!!                   *
*        THE SYS1.AMODGEN LIBRARY SHOULD BE INCLUDED AS MACLIB       *
*                                                                    *
*                                                                    *
********************************************(C)-2006 SKYBIRD SYSTEMS**
RPFASM00 CSECT
         SAVE  (14,12),,*              SAVE REGISTERS
         USING RPFASM00,R15            GET TEMP ADDRESSABILITY
         LA    R14,SAVE                SAVE AREA
         ST    R13,SAVE+4              BACKWARD POINTER
         ST    R14,8(0,R13)            FORWARD POINTER
         LR    R13,R14                 OVERIDE CALLERS SAVEAREA
         B     START                   BRANCH AROUND SAVEAREA
SAVE     DC    18F'-1'                 SAVEAREA
         DC    CL8'&SYSDATE',C' ',CL8'&SYSTIME'
START    DS    0H
         DROP  R15                     KILL TEMP ADDRESSABILITY
         LA    R12,2048(R13)           2ND BASE
         LA    R12,2048(R12)
         USING SAVE,R13,R12            ADDRESSABILITY VIA R13
         L     R10,0(R1)               ADDRESS OF PARAMETER
         L     R11,=V(COMMAREA)        FOR SUBROUTINES
         USING COMMAREA,R11
         XC    GETMSTRT,GETMSTRT       CLEAR FIELDS IN COMMAREA
         XC    GETMLEN,GETMLEN         ,,     ,,    ,,    ,,
         XC    FIRSTREC,FIRSTREC       ,,     ,,    ,,    ,,
         XC    LASTREC,LASTREC         ,,     ,,    ,,   ,,
         XC    INFCODE,INFCODE         ,,     ,,    ,,   ,,
         GETMAIN RU,LV=4096,BNDRY=PAGE  OBTAIN SCREEN AREA
         ST    R1,COMMSCR
         MVC   DEFPFK01(156),PFK0106   MOVE DEFAULT RPF PFKEY'S
         MVC   DEFPFK07(156),PFK0712   ,,
         MVC   DEFPFK13(156),PFK1318   ,,
         MVC   DEFPFK19(156),PFK1924   ,,
         MVC   DSNAME,BLANKS
         MVC   MEMBER,BLANKS
         MVC   VOLUME,BLANKS
         MVC   COMMASM,=CL8'IFOX00'
         LH    R5,0(R10)               LOAD LENGTH OF PARM
         LTR   R5,R5                   IS LENGTH ZERO ?
         BZ    NODSNDEF                YES: NO DEFAULT DSNAME
         CH    R5,=H'44'               PARM LONGER THAN 44 ?
         BH    NODSNDEF                NO DEFAULT DSNAME IF YES
         BCTR  R5,0                    DECREASE WITH 1 FOR EX.
         EX    5,DSNMOVE               MOVE DEFAULT DSN
NODSNDEF L     R3,X'4C'                A(CVT)
         L     R3,0(R3)                A(MY ASCB)
         L     R3,12(R3)               A(TCT)
         L     R3,176(R3)              A(USERID)
         MVC   USERID,0(R3)            MOVE USERID INTO COMMAREA
         LOAD  EP=RPFDAIR              LOAD ENTRY POINT
         ST    R0,EPDAIR               STORE IN COMMAREA
         MVI   DEFOUT,C'C'             DEFAULT SYSOUT CLASS
ASM      XC    ASMECB,ASMECB           CLEAR THE WAIT ECB
         ATTACH EP=RPFASM,ECB=ASMECB,PARAM=((11))  ASSEMBLE
         ST    1,TCBADDR               SAVE TCB ADDRESS FOR DETACH
         WAIT  ECB=ASMECB              WAIT FOR COMPLETION
         L     R2,TCBADDR              LOAD TCB ADDRESS
         USING TCB,R2                  PROVIDE ADDRESSABILITY
         L     R6,TCBCMP               INSERT COMPLETION CODE
         CLM   R6,8,=X'00'             RPFASM ABENDED ?
         BE    TESTRC                  NO: PROCESS RETURNCODE
         LA    R6,0(R6)                CLEAR HI-ORDER 8 BITS
         SRDL  R6,12                   SYSTEMCC IN R6
         SRL   R7,20                   USERCC IN LO-ORDER 16 BITS
         STCM  R6,3,SYSTEMCC           SYSTEM  COMPLETION CODE
         SYCONVHX IN=SYSTEMCC,OUT=SYSEBC,L=2
         MVI   SYSEBC,C' '             BLANK FIRST POS (ZERO)
         CVD   R7,DWB                  TO DECIMAL
         UNPK  USEREBC,DWB             TO EBCDIC
         OI    USEREBC+3,X'F0'         CLEAR SIGN
         TPUT  ABEND,ABENDLEN          ISSUE NONFULLSCREEN MSG
         B     DETACH                  KILL SUBTASK
TESTRC   DS    0H                      RPFASM TERMINATED SUCCESSFULLY
         L     R5,TCBCMP               INSERT RETURNCODE OF RPFASM
         CVD   R5,DWB                  CONVERT RETURNCODE
         UNPK  MSGRC(4),DWB            UNPACK RETURNCODE
         OI    MSGRC+3,X'F0'           AND CLEAR SIGN
         TPUT  MSG,MSGLEN              ISSUE NONFULLSCREEN MSG
         DROP  R2                      KILL ADDRESSABILITY OF TCB
DETACH   DS    0H
         LA    R2,TCBADDR              GET TCBADDRESS FOR DETACH
         DETACH (2)                    DETACH RPFASM
THEEND   DS    0H
         SYALLOC FREE=RPFWORK,MF=E     FREE RPFWORK
         SYALLOC FREE=RPFWORK2,MF=E    FREE RPFWORK2
         L     R2,COMMSCR              LOAD SCREEN ADDRESS
         FREEMAIN R,LV=4096,A=(2)      FREEMAIN AREA
         L     R13,4(R13)              GET SAVE AREA POINTER
         RETURN (14,12),RC=0           AND RETURN
DSNMOVE  MVC   DSNAME(0),2(R10)        MOVE DSNAME FROM EXEC PARM
*
         TITLE 'SYALLOC CONTROL BLOCKS'
         SYALLOC MF=L
         TITLE 'WORKAREAS AND CONSTANTS'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
DWB      DC    D'0'
ASMECB   DC    F'0'
TCBADDR  DC    F'0'
MSG      DC    C'(C)-1984 SKYBIRD SYSTEMS: RPFASM ENDED, RC= '
MSGRC    DS    CL4
MSGLEN   EQU   *-MSG
ABEND    DC    C'RPFASM: ABEND SYSTEM'
SYSEBC   DS    CL4
         DC    C', USER '
USEREBC  DS    CL4
ABENDLEN EQU   *-ABEND
SYSTEMCC DS    H
PFK0106  DC    X'F1',C'N',C'HELP                    '
         DC    X'F2',C'X',C'                        '
         DC    X'F3',C'N',C'END                     '
         DC    X'F4',C'N',C'RETURN                  '
         DC    X'F5',C'N',C'FIND                    '
         DC    X'F6',C'X',C'                        '
PFK0712  DC    X'F7',C'N',C'SCB                     '
         DC    X'F8',C'N',C'SCF                     '
         DC    X'F9',C'X',C'                        '
         DC    X'7A',C'N',C'SCL                     '
         DC    X'7B',C'N',C'SCR                     '
         DC    X'7C',C'N',C'                        '
PFK1318  DC    X'C1',C'N',C'HELP                    '
         DC    X'C2',C'X',C'                        '
         DC    X'C3',C'N',C'END                     '
         DC    X'C4',C'N',C'RETURN                  '
         DC    X'C5',C'N',C'FIND                    '
         DC    X'C6',C'X',C'                        '
PFK1924  DC    X'C7',C'N',C'SCB                     '
         DC    X'C8',C'N',C'SCF                     '
         DC    X'C9',C'X',C'                        '
         DC    X'4A',C'N',C'SCL                     '
         DC    X'4B',C'N',C'SCR                     '
         DC    X'4C',C'N',C'                        '
         LTORG
COMMAREA RPFCOMM DSECT=NO
COMMLEN  EQU   *-COMMAREA
         PRINT NOGEN
         IKJTCB
         END
