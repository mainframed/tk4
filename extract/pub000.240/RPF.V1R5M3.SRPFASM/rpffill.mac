RPFFILL  TITLE 'MOVE FILL CHARACTERS IN EDIT WORKSPACE'
*---------------------------------------------------------------------*
*                                                                     *
*        MODULE:        RPFFILL.                                      *
*                                                                     *
*        FUNCTION:      SET FILL CHARACTERS IN THE EDIT WORKSPACE.    *
*                       THIS ROUTINE IS INVOKED BY RPFEDIT, WITH THE  *
*                       COMMAND 'FILL /STRING/N,M                     *
*                       RPFFILL WILL SET EDIT FILL CHARACTERS IN      *
*                       THIS LINE RANGE STARTING WITH THE COLUMN      *
*                       WHICH IS SET BY THE RPFEDIT 'SCOPE' COMMAND.  *
*                       IF FOR EXAMPLE 5 FILL CHARACTERS ARE SPECIFIED*
*                       5 CHARACTERS WILL BE FILLED. OF COURSE THE    *
*                       LENGTH OF THE FILL STRING CANNOT EXCEED THE   *
*                       LENGTH OF THE EDIT SCOPE (LAST COL-FIRST COL  *
*                       IN EDIT SCOPE.)                               *
*                       A POINTER TO THE FILL COMMAND (48 BYTES)      *
*                       IS SPECIFIED IN 'CMDAREA' OF RPFCOMM.         *
*                       IF FLAG $LC OR $UC IS ON A XLATE TO LOWER OR  *
*                       UPPERCASE IS EXECUTED IN THE LINES SPECIFIED  *
*                       WITHIN THE EDIT SCOPE.                        *
*                                                                     *
*        RETURNCODES IN R15.                                          *
*                        00 FILL SUCCESSFUL (MSG WITH NUMBER OF LINES *
*                           FILLED IN 'CMDAREA' OF RPFCOMM (25 BYTES))*
*                        08 ERROR DETECTED (ERRMSG IN 'CMDAREA')      *
*                                                                     *
*------------------------------------------ (C)-1985 SKYBIRD SYSTEMS -*
         SPACE 2
RPFFILL  CSECT
         SAVE  (14,12),,*          SAVE REGISTERS
         USING RPFFILL,R15         GET LOCAL ADDRESSABILITY
         LA    R14,SAVE            SAVE AREA
         ST    R14,8(0,13)         FORWARD POINTER
         ST    R13,SAVE+4          BACKWARD POINTER
         LR    R13,R14             LOAD THIS SAVEAREA
         B     START               BRANCH AROUND SAVEAREA
SAVE     DC    18F'-1'             SAVE AREA
         DC    CL8'&SYSDATE',C' ',CL8'&SYSTIME'
START    DS    0H
         DROP  R15                 KILL LOCAL ADDRESSABLILITY
         USING SAVE,R13            GET ADDR OVER SAVE
         L     R11,0(R1)           TXTCOMM AREA
         USING COMMAREA,R11        DSECT OVER PARM
         MVC   FIRSTLNE,FIRSTSCR   DEFAULT FROM CURRENT LNE
         MVC   LASTLNE,LASTREC     DEFAULT ENTIRE WORKSPACE
         ICM   R10,15,CMDAREA      ADDRESS FILL COMMAND
         TM    INFCODE2,$UC+$LC    'UC' OR 'LC' COMMAND ?
         BZ    TXT001              YES SCAN FILL STRING
         LA    R3,1                LENGTH OF STRING IS 1
         ST    R3,SAVE3TXT         SAVE LENGTH
         LA    R2,3(R10)           POINT AFTER 'LC' OF 'UC' SUBCOMMAND
         LR    R3,R2               IN REG3 FOR 'LINE' ROUTINE
         B     TXT008
TXT001   EQU   *
         LA    R2,5(R10)           LOAD ADDRESS CHAR STRING
         MVC   DLMCHAR,0(R2)       MOVE DELIMITER
         LA    R2,1(R2)            POINT AFTER DELIMITER
         XR    R3,R3               R3= 0 USED FOR COUNT POS
         LA    R4,42               MAX L= 42 WITHOUT START DLM
TXT003   EQU   *                   *** START LOOP ***
         CLC   0(1,R2),DLMCHAR     END DLM FOUND ?
         BE    TXT006              YES: END OF LOOP
         LA    R2,1(R2)            NEXT POSITION IN STRING
         LA    R3,1(R3)            COUNT POSITION
         BCT   R4,TXT003           BRANCH BACK
         BCTR  R2,0                R2 ON LAST POS STRING
TXT004   EQU   *                   NO END DLM FOUND
         CLI   0(R2),X'40'         TRAILING BLANK ?
         BE    TXT005              YES: TEST PREVIOUS CHAR
         CLI   0(R2),X'00'         TRAILING NULL ?
         BNE   TXT031              NO: EXIT
TXT005   BCTR  R2,0                1 POSITION BACK IN STRING
         BCT   R3,TXT004           TEST NEXT BLANK
         B     TXT055              SET RC=08 AND GO TO CALLER
TXT006   ST    R3,SAVE3TXT         SAVE LENGHT STRING
         LA    R3,1(R2)            START ADDRES FOR SCAN
TXT008   EQU   *
         BAL   R14,LINE            FIND LINE NUMBER(S)
         LTR   R15,R15             RC= 0000 ?
         BZ    TXT012              YES: PROCEED
         C     R15,F0004           RC= 0004 - INVALID LINE(S) ?
         BE    TXT073
         C     R15,F0008           RC= 0008 - NOT NUMERIC ?
         BE    TXT073
         B     TXT077              UNEXPECTED RETURN CODE
TXT012   CLC   LN2FLD,C0000        LN2 ENTERED ?
         BNE   TXT013              YES: SKIP
         MVC   LN2FLD,LN1FLD       NO: LN2 = LN1
TXT013   CLI   LN1FLD,C'*'         * - CURRENT LINE ?
         BE    TXT014              YES: CURRENT LINE
         CLC   LN1FLD,C0000        LN1 ENTERED ?
         BNE   TXT015              YES: SKIP
TXT014   L     R3,FIRSTSCR         DEFAULT CURRENT LINE
         USING WORKSPCE,R3             ADDRESS WORKSPACE RECORD
         MVC   LN1FLD+2(6),WORKSLIN     NUMBER INTO LN1FLD
         MVC   LN1FLD(2),=C'00'    LEADING ZEROES
         CLC   LN2FLD,C0000        LN2 ENTERED ?
         BNE   TXT015              YES: SKIP
         L     R3,LASTREC          DEFAULT END OF WORKSPACE
         MVC   LN2FLD+2(6),WORKSLIN     NUMBER INTO LN2FLD
         MVC   LN2FLD(2),=C'00'    LEADING ZEROES
         DROP  R3
TXT015   EQU   *
         MVI   ERRPTXT+11,C'1'         MOVE ID IN MSG
         LA    R1,LN1FLD+2             PARM FOR SRCHLINE ROUTINE
         L     R15,EPSRCH              EP OF ROUTINE
         BALR  R14,R15                 TRY TO FIND LINE
         LTR   R15,R15                 LINE FOUND ?
         BNZ   TXT085                  NO: ISSUE ERRMSG
         LR    R7,R1                   A(FOUND LINE)
         MVI   ERRPTXT+11,C'2'         MOVE ID IN MSG
         LA    R1,LN2FLD+2             PARM FOR SRCHLINE ROUTINE
         L     R15,EPSRCH              EP OF ROUTINE
         BALR  R14,R15                 TRY TO FIND LINE
         LTR   R15,R15                 LINE FOUND ?
         BNZ   TXT085                  NO: ISSUE ERRMSG
         LR    R8,R1                   A(FOUND LINE)
         ST    R7,FIRSTLNE         FIRST LINE TO SCAN
         ST    R8,LASTLNE          LAST LINE TO SCAN
         L     R3,SAVE3TXT         RESTORE LENGTH STRING
TXT031   EQU   *                       *** END   LOOP ***
         LTR   R3,R3                   NO STRING FOUND ?
         BZ    TXT055              SET RC=08 AND GO TO CALLER
         C     R3,SCOPEL           STRING .LE. SCOPE ?
         BH    TXT079              NO: ERROR
         STCM  R3,3,LENSTR         SAVE LENGTH OF STRING
         BCTR  R3,0                LENGTH - 1 FOR EX
         LA    R2,6(R10)           ADDRESS STRING
         MVC   STRING,BLANKS       CLEAR STRING
         EX    R3,TXTMOV1          MOVE STRING INTO FIELD
         TM    INFCODE2,$UC+$LC    'LC' OR 'UC' COMMAND ?
         BZ    TXT041              NO: PROCESS 'FILL' COMMAND
         L     R3,SCOPEL           LENGTH OF SCOPE
         STCM  R3,3,LENSTR         SAVE THIS LENGTH
         B     TXT041              SEARCH STRING IN WORKSPACE
TXTMOV1  MVC   STRING(0),0(R2)     MOVE CHAR STRING INTO FIELD
TXT041   EQU   *                   SEARCH STRING IN WORKSPACE
         L     R2,FIRSTLNE         R2= FIRSTSCR
         LA    R2,8(R2)            POINT TO DATA PORTION OF WORKSPACE
         L     R5,LASTLNE          R5= LASTREC
         LA    R5,79(R5)           R5= ADDR AFTER LASTREC
         LA    R4,NEXTENT          INCREMENT IS 86 (LRECL)
         ST    R2,SAVE02           SAVE R2
         LH    R6,LENSTR           LENGTH OF STRING
         BCTR  R6,0                MINUS 1 FOR COMPARE
TXT043   L     R7,SCOPEL           LENGTH OF SCOPE
         L     R2,SAVE02           FIRST CHAR OF RECORD
         A     R2,SCOPES           AJUST WITH SCOPE
TXT045   EQU   *                   LOOK IF STRING IS IN RECORD
         TM    INFCODE2,$LC+$UC    'UC' OR 'LC' COMMAND ?
         BZ    TXT047              NO: BRANCH
         TM    INFCODE2,$LC        'LC' COMMAND ?
         BNO   TXT046              NO: PROCESSS 'UC' COMMAND
         EX    R6,LCTRAN           XLATE UPPER TO LOWER CASE
         B     TXT048
TXT046   EQU   *                   LOOK IF STRING IS IN RECORD
         EX    R6,UCTRAN           XLATE LOWER TO UPPER CASE
         B     TXT048
TXT047   EQU   *
         EX    R6,TXTMOV2          MOVE FILL STRING
TXT048   EQU   *
         AP    FILLCNT,=P'1'       INCREMENT COUNT
         L     R2,SAVE02           STARTADDRESS RECORD
         BXLE  R2,R4,TXT049        NEXT RECORD
         B     TXT051              END OF WORKSPACE
TXT049   EQU   *
         ST    R2,SAVE02           SAVE REG 2
         B     TXT043              SEARCH AGAIN
TXTMOV2  MVC   0(0,R2),STRING      *** EXECUTE ONLY ***
LCTRAN   TR    0(0,R2),LCXLATE     *** EXECUTE ONLY ***
UCTRAN   TR    0(0,R2),UCXLATE     *** EXECUTE ONLY ***
TXT051   EQU   *                   MATCH FOUND IN LINE
         UNPK  ERRXTXT+04(5),FILLCNT UNPACK COUNT INTO MSG
         OI    ERRXTXT+08,X'F0'    CLEAR SIGN
         MVC   ERRYTXT+04(5),ERRXTXT+4
         MVC   CMDAREA(22),ERRXTXT MESSAGE INTO CMDAREA
         TM    INFCODE2,$LC+$UC    'LC' OR 'UC' COMMAND GIVEN ?
         BZ    TXT052              YES BRANCH
         MVC   CMDAREA(22),ERRYTXT MESSAGE INTO CMDAREA
TXT052   EQU   *                   MATCH FOUND IN LINE
         XR    R15,R15             RC= 0000
         B     TXT099              BACK TO CALLER
TXT055   LA    R15,8               INVALID CHARACTER STRING
         MVC   CMDAREA(22),ERRWTXT
         B     TXT099
TXT073   LA    R15,8               LINE INVALID
         MVC   CMDAREA(22),ERRJTXT
         B     TXT099
TXT077   LA    R15,8               UNEXPEXTED RETURN CODE
         MVC   CMDAREA(22),ERR2TXT
         B     TXT099
TXT079   LA    R15,8               SCOPE TOO SMALL
         MVC   CMDAREA(22),ERRETXT
         B     TXT099
TXT085   LA    15,8                LINE NOT FOUND
         MVC   CMDAREA(22),ERRPTXT
         B     TXT099
TXT099   EQU   *                       END OF ROUTINE
         L     R13,SAVE+4              LOAD CALLERS SAVEAREA
         RETURN (14,12),RC=(15)        BACK TO CALLER
*---------------------------------------------------------------------*
*                                                                     *
*        N U M T S T - SUBROUTINE, NUMERIC TEST OF A 8 CHAR FIELD.    *
*                                                                     *
*                    INPUT:  NUMFLD, FIELD CONTAINS CHARS TO TEST     *
*                    OUTPUT: NUMFLD, REMAINS UNCHAINGED               *
*                            REG.15, RETURN CODE 0000 - NUMERIC       *
*                                                0008 - NOT NUMERIC   *
*                                                                     *
*------------------------------------------ (C)-1985 SKYBIRD SYSTEMS -*
NUMTST   ST    R14,SAVE14          SAVE REG.14
         LA    R3,8                LENGTH
         LA    R4,NUMFLD           FIELD TO BE TESTED
NUMTST01 CLI   0(R4),X'F0'         LOW ?
         BL    NUMTST03            YES: NOT NUMERIC
         CLI   0(R4),X'F9'         HIGH ?
         BH    NUMTST03            YES: NOT NUMERIC
         LA    R4,1(R4)            NEXT CHAR.
         BCT   R3,NUMTST01         TEST MAX 8 CHARS.
         XR    R15,R15             RC= 0000, NUMERIC
         B     NUMTST99            GOTO EXIT
NUMTST03 LA    R15,8               RC= 0008, NOT NUMERIC
NUMTST99 L     R14,SAVE14          RESTORE REG.14
         BR    R14                 RETURN
*---------------------------------------------------------------------*
*                                                                     *
*        S H I F T - SUBROUTINE, REPLACE TRAILING SPACES WITH         *
*                    FILLCHAR.                                        *
*                                                                     *
*                    INPUT:  NUMFLD, FIELD TO BE SHIFTED, MAX 8 CHAR, *
*                            FILLCHAR, FILL CHARACTER.                *
*                    OUTPUT: NUMFLD, TRAILING SPACES REPLACED WITH    *
*                                    LEADING FILLCHAR                 *
*                            REG.15, RC= 0000 - NORMAL END            *
*                                    RC= 0004 - ALL SPACES            *
*                                                                     *
*------------------------------------------ (C)-1985 SKYBIRD SYSTEMS -*
SHIFT    ST    R14,SAVE14          SAVE REG.14
         LA    R3,8                MAX 8 CHARS
         XR    R15,R15             RC= 0000
SHIFT01  CLI   NUMFLD+7,X'40'      LAST CHAR SPACE ?
         BNE   SHIFT99             NO: GOTO EXIT
         MVC   WRKFLD+1(7),NUMFLD  MOVE FIRST 5 CHARS INTO WRKFLD+1
         MVC   WRKFLD(1),FILLCHAR  INSERT LEADING ZERO
         MVC   NUMFLD,WRKFLD       RESTORE NUMFLD
         BCT   R3,SHIFT01          SHIFT MAX 8 TIMES
         LA    R15,4               RC= 0004 - ALL SPACES
SHIFT99  L     R14,SAVE14          RESTORE REG.14
         BR    R14                 RETURN
*---------------------------------------------------------------------*
*                                                                     *
*        S C A N - SUBROUTINE, SCANS AREA FOR ' ' OR ',' AND PUTS     *
*                              PRECEEDING CHARS IN NUMFLD.            *
*                                                                     *
*                  INPUT:  REG.3,  POINTS TO FIRST CHAR TO SCAN       *
*                  OUTPUT: NUMFLD, CHARS PRECEEDING ' ' OR ','        *
*                          REG.3,  POINTS TO LATS CHAR SCANNED        *
*                          REG.15, RETURN CODE 0000 - CHARS FOUND     *
*                                              0008 - NO CHARS FOUND  *
*                                                                     *
*------------------------------------------ (C)-1985 SKYBIRD SYSTEMS -*
SCAN     ST    R14,SAVE14          SAVE REG.14
         LA    R15,8               RC= 0008, INITIAL: NO CHARS FOUND
         LA    R4,9                MAX 9 CHARS
         LA    R5,NUMFLD           POINT TO NUMFLD
         MVC   NUMFLD,BLANKS       CLEAR NUMFLD
SCAN01   CLI   0(R3),X'40'         SPACE ?
         BE    SCAN03              YES: END OF SCAN
         CLI   0(R3),X'6B'         COMMA ?
         BE    SCAN03              YES: END OF SCAN
         MVC   0(1,R5),0(R3)       MOVE 1 CHAR INTO NUMFLD
         LA    R3,1(R3)            NEXT CHAR TO SCAN
         LA    R5,1(R5)            NEXT CHAR NUMFLD
         BCT   R4,SCAN01           SCAN MAX 9 TIMES
         B     SCAN99              GOTO EXIT, NO CHARS FOUND
SCAN03   XR    R15,R15             RC= 0000, SCAN SUCCESSFUL
SCAN99   L     R14,SAVE14          RESTORE REG.14
         BR    R14                 RETURN
*---------------------------------------------------------------------*
*                                                                     *
*        L I N E - SCAN INPUT BUFFER FOR LINE NUMBERS                 *
*                                                                     *
*                  INPUT:  REG.3, START ADDRESS SCAN,                 *
*                  OUTPUT: LN1FLD, LINE-1                             *
*                          LN2FLD, LINE-2                             *
*                          LN3FLD, LINE-3                             *
*                          REG.15, RC= 0000 - ALL LINES VALID,        *
*                                  RC= 0004 - INVALID LINE FOUND,     *
*                                  RC= 0008 - LINES NOT NUMERIC.      *
*                          ERRJTXT, ERROR INDICATING LINE NUMBER      *
*                                                                     *
*------------------------------------------ (C)-1985 SKYBIRD SYSTEMS -*
LINE     ST    R14,SAVE14LN        SAVE REG.14
         MVI   ERRJTXT+13,C'1'     MOVE LINE ID IN ERROR MSG
         BAL   R14,SCAN            SCAN FOR LN1FLD
         LTR   R15,R15             RC= 0000 ?
         BNZ   LINE73              NO: LN1 INVALID
         ST    R3,SAVE3            SAVE REG.3
         CLI   NUMFLD,C'*'         * - CURRENT LINE ?
         BE    LINE04              YES: SKIP NUMTST
         MVI   FILLCHAR,X'F0'      '0' INTO FILLCHAR
         BAL   R14,SHIFT           REP TRAIL SPC WITH LEADING ZERO
         C     R15,F0004           RC= 0004 - ALL SPACES
         BE    LINE03
         CLC   NUMFLD,C0000        LINE-1 ZERO ?
         BE    LINE73
LINE03   BAL   R14,NUMTST          NUMERIC TEST
         LTR   R15,R15             RC= 0000 ?
         BNZ   LINE75              NO: LN1 NOT NUMERIC
LINE04   MVC   LN1FLD,NUMFLD       SAVE LN1 INTO LN1FLD
         L     R3,SAVE3            RESTORE REG.3
         LA    R3,1(R3)            POINT TO LN2
         MVI   ERRJTXT+13,C'2'     MOVE LINE ID IN ERROR MSG
         BAL   R14,SCAN            SCAN FOR LN2FLD
         LTR   R15,R15             RC= 0000 ?
         BNZ   LINE73              NO: LN2 INVALID
         ST    R3,SAVE3            SAVE REG.3
         MVI   FILLCHAR,X'F0'      '0' INTO FILLCHAR
         BAL   R14,SHIFT           REP TRAIL SPC WITH LEADING ZERO
         C     R15,F0004           RC= 0004 - ALL SPACES ?
         BE    LINE05
         CLC   NUMFLD,C0000        LINE-2 ZERO ?
         BE    LINE73
LINE05   BAL   R14,NUMTST          NUMERIC TEST
         LTR   R15,R15             RC= 0000 ?
         BNZ   LINE75              NO: LN2 NOT NUMERIC
         MVC   LN2FLD,NUMFLD       SAVE LN2 INTO LN1FLD
         XR    R15,R15             RC= 0000 - SCAN SUCCESSFUL
         B     LINE99              EXIT
LINE73   LA    R15,4               RC= 0004 - INVALID LINE(S) FOUND
         B     LINE99              EXIT
LINE75   LA    R15,8               RC= 0008 - LINE(S) NOT NUMERIC
         B     LINE99              EXIT
LINE99   L     R14,SAVE14LN        RESTORE REG.14
         BR    R14                 RETURN
         TITLE 'WORKAREAS AND CONSTANTS'
SAVE02   DS    F
F0004    DC    F'4'
F0008    DC    F'8'
C0000    DC    C'00000000'
FIRSTLNE DS    F
LASTLNE  DS    F
LENSTR   DC    H'0'
STRING   DC    CL48' '
DLMCHAR  DC    C' '
ERR2TXT  DC    CL22'UNEXPECTED RETURN CODE'
ERRETXT  DC    CL22'-------SCOPE TOO SMALL'
ERRJTXT  DC    CL22'--------LINE-X INVALID'        OFFSET 13
ERRPTXT  DC    CL22'------LINE-X NOT FOUND'        OFFSET 11
ERRWTXT  DC    CL22'--INVALID CHAR. STRING'
ERRXTXT  DC    CL22'----XXXXX LINES FILLED'        OFFSET 04
ERRYTXT  DC    CL22'----XXXXX LINES XLATED'        OFFSET 04
SAVE14LN DS    F                   SAVE AREA REG.14 LINE
SAVE14   DS    F                   SAVE AREA REG.14 ALL
SAVE14T  DC    F'0'
SAVE3    DS    F                   SAVE AREA REG.3 SCAN
SAVE3TXT DS    F                   SAVE AREA REG.3
MAXLINES DS    F
FILLCNT  DC    PL3'0'              # OF LINES CHANGED
FILLCHAR DC    CL1' '              FILL CHARACTER SHIFT ROUTINE.
NUMFLD   DS    CL8                 WORK FIELD NUMTST/SHIFT
WRKFLD   DS    CL8                 WORK FIELD NUMTST/SHIFT
LN1FLD   DS    CL8                 LINE-1 COPY/LINE
LN2FLD   DS    CL8                 LINE-2 COPY/LINE
LN3FLD   DS    CL8                 LINE-3 COPY/LINE
         LTORG
         TITLE 'TRANSLATE TABLES FOR ''LC'' AND ''UC'' COMMAND'
LCXLATE  DC    256AL1(*-LCXLATE)
         ORG   LCXLATE+X'C1'
         DC    X'818283848586878889'
         ORG   LCXLATE+X'D1'
         DC    X'919293949596979899'
         ORG   LCXLATE+X'E2'
         DC    X'A2A3A4A5A6A7A8A9'
         ORG   ,
UCXLATE  DC    256AL1(*-UCXLATE)
         ORG   UCXLATE+X'81'
         DC    C'ABCDEFGHI'
         ORG   UCXLATE+X'91'
         DC    C'JKLMNOPQR'
         ORG   UCXLATE+X'A2'
         DC    C'STUVWXYZ'
         ORG   ,
         TITLE 'RPFCOMM DSECT'
COMMAREA RPFCOMM
OFFSETN2 EQU   (NEXTENT-6)
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
