RPFPFK   TITLE 'ALTER/DISPLAY RPF PFK SETTINGS'
RPFPFK   CSECT
         SAVE  (14,12),,*
         USING RPFPFK,R15
         LA    R11,SAVE
         ST    R13,SAVE+4
         ST    R11,8(0,R13)
         LR    R13,R11
         B     START
         DS    0F
SAVE     DC    18F'-1'
         DC    CL8'&SYSDATE',CL8'&SYSTIME'
START    DS    0H
         DROP  R15
         USING SAVE,R13
         L     R11,0(R1)
         USING COMMAREA,R11
         MVC   SCR5UID+27(7),USERID    USERID INTO SCREEN
         LA    R3,SCR5UID+33            REPLACE TRAILING BLANKS
         LA    R4,7                    MAX 7 TIMES
UID1     CLI   0(R3),C' '              TRAILING BLANK FOUND ?
         BNE   UID99                   NO: EXIT
         MVI   0(R3),C'-'              REPLACE BY HYPHEN
         BCTR  R3,R0                   PREVIOUS POS.
         BCT   R4,UID1                 LOOP UNTIL ALL DONE
UID99    EQU   *
PUT1     TPUT  ERASE,L'ERASE,FULLSCR
PUT5     MVC   PFK01(24),DEFPFK01+2
         MVC   PFK02(24),DEFPFK02+2
         MVC   PFK03(24),DEFPFK03+2
         MVC   PFK04(24),DEFPFK04+2
         MVC   PFK05(24),DEFPFK05+2
         MVC   PFK06(24),DEFPFK06+2
         MVC   PFK07(24),DEFPFK07+2
         MVC   PFK08(24),DEFPFK08+2
         MVC   PFK09(24),DEFPFK09+2
         MVC   PFK10(24),DEFPFK10+2
         MVC   PFK11(24),DEFPFK11+2
         MVC   PFK12(24),DEFPFK12+2
         MVC   PFK13(24),DEFPFK13+2
         MVC   PFK14(24),DEFPFK14+2
         MVC   PFK15(24),DEFPFK15+2
         MVC   PFK16(24),DEFPFK16+2
         MVC   PFK17(24),DEFPFK17+2
         MVC   PFK18(24),DEFPFK18+2
         MVC   PFK19(24),DEFPFK19+2
         MVC   PFK20(24),DEFPFK20+2
         MVC   PFK21(24),DEFPFK21+2
         MVC   PFK22(24),DEFPFK22+2
         MVC   PFK23(24),DEFPFK23+2
         MVC   PFK24(24),DEFPFK24+2
         MVC   CON01(1),DEFPFK01+1
         MVC   CON02(1),DEFPFK02+1
         MVC   CON03(1),DEFPFK03+1
         MVC   CON04(1),DEFPFK04+1
         MVC   CON05(1),DEFPFK05+1
         MVC   CON06(1),DEFPFK06+1
         MVC   CON07(1),DEFPFK07+1
         MVC   CON08(1),DEFPFK08+1
         MVC   CON09(1),DEFPFK09+1
         MVC   CON10(1),DEFPFK10+1
         MVC   CON11(1),DEFPFK11+1
         MVC   CON12(1),DEFPFK12+1
         MVC   CON13(1),DEFPFK13+1
         MVC   CON14(1),DEFPFK14+1
         MVC   CON15(1),DEFPFK15+1
         MVC   CON16(1),DEFPFK16+1
         MVC   CON17(1),DEFPFK17+1
         MVC   CON18(1),DEFPFK18+1
         MVC   CON19(1),DEFPFK19+1
         MVC   CON20(1),DEFPFK20+1
         MVC   CON21(1),DEFPFK21+1
         MVC   CON22(1),DEFPFK22+1
         MVC   CON23(1),DEFPFK23+1
         MVC   CON24(1),DEFPFK24+1
         TPUT  SCR5TXT,SCR5LEN,FULLSCR
         MVI   MSG5,C'-'               CLEAR
         MVC   MSG5+1(21),MSG5              MESSAGE
         L     R3,COMMSCR              LOAD SCREEN ADDRESS
         TGET  (R3),850,ASIS           READ THE SCREEN
         LR    R4,R1                   SAVE LENGTH
*        DO AN TGET ASIS TO OBTAIN THE AID BYTE
*        AFTER THE TGET ASIS REMOVE THE SBA'S FROM THE OUTPUT
         CLI   0(R3),X'F3'             PFK3 (END) PRESSED
         BE    RETURN                  YES: RETURN
         CLI   0(R3),X'C3'             PFK15 (END) PRESSED
         BE    RETURN                  YES: RETURN
         XR    R2,R2                    INIT OUTPUT LENGTH
         SH    R4,=H'3'                SKIP AID AND CURSOR ADDRESS
         C     R4,F0000                REGISTER ZERO
         BNH   CONV099                 NOT HIGH: END OF ROUTINE
         LA    R3,3(R3)
         LA    R5,BUFFER               OUTPUT ADDRESS
*        INPUT SCREEN IS POINTED TO BY R3
*        OUTPUT SCREEN IS POINTED TO BY R5
*        THE NEXT SECTION WILL REMOVE ALL THE SBA'S
CONV001  CLI   0(R3),X'11'             SET BUFFER ADDRESS ?
         BE    CONV003                 YES: SKIP NEXT 3 BYTES INPUT
CONV002  MVC   0(1,R5),0(R3)           MOVE INPUT BYTE
         LA    R3,1(R3)                NEXT BYTE INPUT
         LA    R2,1(R2)                COUNT OUTPUT BYTES
         LA    R5,1(R5)                NEXT BYTE OUTPUT
         BCT   R4,CONV001              TEST NEXT BYTE
         B     CONV099                 END OF ROUTINE
CONV003  LA    R3,3(R3)                SKIP 3 BYTES INPUT
         BCTR  R4,0                    MINUS 1 BCT
         BCTR  R4,0                    MINUS 1 BCT
         C     R4,F0000                REGISTER ZERO ?
         BNH   CONV099                 YES: TERMINATE CONVERSION
         BCT   R4,CONV001              TEST NEXT BYTE
CONV099  LR    R1,R2                   OUTPUT LENGTH IN REGISTER 1
         OC    BUFFER(24),BLANKS       XLATE BUFFER TO UPPER-CASE
         CLI   BUFFER,C'?'             RESET ?
         BE    PUT1                    YES: RESET SCREEN
         C     R1,F0624                EOF / DEL KEY USED ?
         BNE   ERR1                    YES: GIVE ERROR MESSAGE
         CLC   BUFFER(3),=C'END'
         BE    RETURN
         LA    R3,12
         LA    R4,DEFPFK01
         LA    R5,BUFFER+24
LOOP1    OC    0(50,R5),BLANKS
         MVC   2(24,R4),0(R5)
         MVC   1(1,R4),24(R5)
         MVC   314(24,R4),25(R5)
         MVC   313(1,R4),49(R5)
         LA    R4,26(R4)
         LA    R5,50(R5)
         BCT   R3,LOOP1
         MVC   MSG5,MSG1TXT
         B     PUT5
ERR1     MVC   MSG5,ERR1TXT
         B     PUT1
RETURN   L     R13,SAVE+4
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         TITLE 'WORKAREA''S AND CONSTANTS'
ERASE    DC    X'27F5C01140401DF8' WRITE ERASE / R01,C00
SCR5TXT  DC    X'27F1C21140401DF8' WRITE NOERASE / R01,C00
SCR5UID  DC    CL78'RPF PFK DEFINITIONS USERID=       -----------------C
               ---------------------------'
         DC    X'11407A'           R01,C59
MSG5     DC    CL22'----------------------'
         DC    X'11C150',X'1DF8',C'=====>',X'1DC113'
REP5     DC    CL24' ',X'1DF0'
         DC    X'11C3F01DF0',C'PFK01=',X'1DC9'
PFK01    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON01    DC    C' ',X'1DF011C4D81DF0',C'PFK13=',X'1DC9'
PFK13    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON13    DC    C' ',X'1DF011C5401DF0',C'PFK02=',X'1DC9'
PFK02    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON02    DC    C' ',X'1DF011C5E81DF0',C'PFK14=',X'1DC9'
PFK14    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON14    DC    C' ',X'1DF011C6501DF0',C'PFK03=',X'1DC9'
PFK03    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON03    DC    C' ',X'1DF011C6F81DF0',C'PFK15=',X'1DC9'
PFK15    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON15    DC    C' ',X'1DF011C7601DF0',C'PFK04=',X'1DC9'
PFK04    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON04    DC    C' ',X'1DF011C8C81DF0',C'PFK16=',X'1DC9'
PFK16    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON16    DC    C' ',X'1DF011C8F01DF0',C'PFK05=',X'1DC9'
PFK05    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON05    DC    C' ',X'1DF011C9D81DF0',C'PFK17=',X'1DC9'
PFK17    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON17    DC    C' ',X'1DF0114A401DF0',C'PFK06=',X'1DC9'
PFK06    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON06    DC    C' ',X'1DF0114AE81DF0',C'PFK18=',X'1DC9'
PFK18    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON18    DC    C' ',X'1DF0114B501DF0',C'PFK07=',X'1DC9'
PFK07    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON07    DC    C' ',X'1DF0114BF81DF0',C'PFK19=',X'1DC9'
PFK19    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON19    DC    C' ',X'1DF0114C601DF0',C'PFK08=',X'1DC9'
PFK08    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON08    DC    C' ',X'1DF0114DC81DF0',C'PFK20=',X'1DC9'
PFK20    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON20    DC    C' ',X'1DF0114DF01DF0',C'PFK09=',X'1DC9'
PFK09    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON09    DC    C' ',X'1DF0114ED81DF0',C'PFK21=',X'1DC9'
PFK21    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON21    DC    C' ',X'1DF0114F401DF0',C'PFK10=',X'1DC9'
PFK10    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON10    DC    C' ',X'1DF0114FE81DF0',C'PFK22=',X'1DC9'
PFK22    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON22    DC    C' ',X'1DF01150501DF0',C'PFK11=',X'1DC9'
PFK11    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON11    DC    C' ',X'1DF01150F81DF0',C'PFK23=',X'1DC9'
PFK23    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON23    DC    C' ',X'1DF011D1601DF0',C'PFK12=',X'1DC9'
PFK12    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON12    DC    C' ',X'1DF011D2C81DF0',C'PFK24=',X'1DC9'
PFK24    DC    C'                        ',X'1DF0',C'CON=',X'1DC9'
CON24    DC    C' ',X'1DF0'
         DC    X'11D4401DF0',C'HIT',X'1DF8',C'  ENTER',X'1DF0'
         DC    C'    TO MAKE THE CHANGES EFFECTIVE'
         DC    X'11D5501DF0',C'PRESS',X'1DF8',C'PF03/PF15',X'1DF0'
         DC    C'TO CANCEL THE CURRENT CHANGES'
SCR5LEN  EQU   *-SCR5TXT
ERR1TXT  DC    CL22'---EOF OR DEL KEY USED'
MSG1TXT  DC    CL22'-----------PFK''S RESET'
F0000    DC    F'0'
F0624    DC    F'624'
BUFFER   DS    CL648
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         LTORG
COMMAREA RPFCOMM
         END
