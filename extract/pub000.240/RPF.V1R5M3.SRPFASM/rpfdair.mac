RPFDAIR  TITLE 'DYNAMIC ALLOCATION ROUTINE FOR RPF'
*---------------------------------------------------------------------*
*                                                                     *
*        MODULE:        RPFDAIR.                                      *
*                                                                     *
*        FUNCTION:      ALLOCATE DATASETS FOR THE ROB PROGRAM         *
*                       FACILITY (RPF) THE FULL SCREEN EDITOR         *
*                       THE FOLLOWING PARM IS GIVEN:                  *
*                       THE RPFCOMM AREA WHICH IS DESCIBED            *
*                       IN THE RPFCOMM MACRO.                         *
*                                                                     *
*        RETURNCODES:   SET IN RTNCODE FIELD OF RPFCOMM AREA          *
*                       00 - ALLOCATION SUCCESSFUL.                   *
*                       04 - DATASET NOT IN CATALOG.                  *
*                       08 - DATASET NOT ON VOLUME.                   *
*                       12 - DYNALLOC ERROR (REFER ERRCODE)           *
*                       20 - RECFM NOT F(B).                          *
*                       24 - PS DATASET WITH MEMBER                   *
*                       28 - DSORG NOT PS OR PO                       *
*                       32 - MEMBER NOT FOUND (NOT SEVERE)            *
*                       36 - AUTHORIZATION FAILED                  @RP1
*        NOTE: THE SYS1.AMODGEN SHOULD BE INCLUDED IN THE ASSEMBLY    *
*                                                                     *
*        MODIFICATIONS:                                               *
*        AUG 21,2002 RPR: SUPPORT RECFM=VB AND ADD ESTAE ROUTINE      *
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
         SPACE 2
RPFDAIR  CSECT
         SAVE  (14,12),,*              SAVE REGISTERS.
         USING RPFDAIR,R15             GET ADDRESSABILITY
         LA    R11,SAVE                LOAD SAVEAREA
         ST    R13,SAVE+4              BACKWARD POINTER
         ST    R11,8(0,R13)            FORWARD POINTER
         LR    R13,R11                 EXCHANGE SA
         B     START                   BRANCH AROUND SAVEAREA
SAVE     DC    18F'-1'                 SAVE AREA
         DC    CL8'&SYSDATE',C' ',CL8'&SYSTIME'
START    DS    0H
         DROP  R15                     KILL ADDRESSABILITY
         USING SAVE,R13                GET ADDRESSABILITY
         L     R11,0(R1)               OBTAIN PARM FROM MAIN ROUT
         USING COMMAREA,R11            GET ADDRESSABILITY OF THE PARM
         STM   R11,R13,HEREADDR        SAVE REGS FOR ESTAE         @RP1
         XC    ERRCODE,ERRCODE         CLEAR THE FIELDS
         XC    RTNCODE,RTNCODE
         NI    INFCODE,255-SEQALC      TURN OFF FLAG
         NI    INFCODE4,255-$RECFB-$RECVB TURN OF RECFM FLAGS      @RP1
         NI    ALLOCSW,255-CATLGD      TURN OFF FLAG
         MVC   LOCDSN,DSNAME           MOVE DSNAME TO ALLOCATE
*
         CLI   VOLUME,C' '             NO VOLUME GIVEN ?
         BNE   RPF003                  BRANCH IF NOT
         LOCATE LOC                    TRY TO FIND DATASET IN THE CAT.
         LTR   R15,R15                 CHECK COMPLETION
         BZ    RPF001                  ALLRIGHT
         LA    R15,4                   RETURN CODE 4 *DS NOT IN CATLG*
         ST    R15,RTNCODE             STORE INTO RPFCOMM
         B     RPFRTN                  RETURN TO CALLER
*
RPF001   EQU   *
         OI    ALLOCSW,CATLGD          MARK CATALOGED DATASET
         MVC   DSNAME,LOCDSN           MOVE ORIGINAL NAME
* IF LOCDSN WAS AN ALIAS BEFORE LOCATE, AFTER THE LOCATE LOCDSN
*    WILL BE THE TRUE NAME
         MVC   VOLUME,LOCVOL           MOVE VOLUME SERIAL
RPF003   EQU   *
         TM    INFCODE3,$DAIRALC       IS DATASET ALLOCATED ?
         BNO   RPF011                  NO: NO COMPARE, ALLOCATE
         CLC   DSNAME,DALDSN           COMPARE WITH ALLOC CONTROL BLK
         BNE   RPF009                  BRANCH IF NOT
         CLC   VOLUME,DAVOLSER         COMPARE VOLUME NUMBER
         BNE   RPF009                  BRANCH IF NOT
         B     RPF012                  MODIFY JFCB, BLDL AND OPENJ
RPF009   EQU   *                       ALLOCATE THE SPECIFIED DATASET
         BAL   R14,UNALLOC             UNALLOCATE
RPF011   EQU   *
         BAL   R14,RPFALLOC            ALLOCATE THE DATASET
         LTR   R15,R15                 ALLOC OK ?
         BZ    RPF012                  INFORM CALLER AND RETURN
         BAL   R14,UNALLOC             UNALLOCATE OLD STUFF
         B     RPFRTN                  RETURN
RPF012   OBTAIN OBTCMLST               CHECK IF DATASET IS IN VTOC
         LTR   R15,R15                 CHECK COMPLETION
         BZ    RPF013                  OK
         BAL   R14,UNALLOC             UNALLOC *DS NOT IN VTOC*
         LA    R15,8                   RETURNCODE *08 = DS ^ IN VTOC*
         ST    R15,RTNCODE             MOVE IN RPFCOMM
         B     RPFRTN                  RETURN TO CALLER
RPF013   DS    0H                      TEST RECFM IN DSCB
         TM    DSCB1+40,X'C0'          RECFM=U ?                   @RP1
         BO    RPF017                  RECFM=U IS NOT ALLOWED.     @RP1
         TM    DSCB1+40,X'40'          RECFM=V(B)
         BNO   RPF014                  NO TEST IF FIXED
         OI    INFCODE4,$RECVB         MARK VARIABLE RECORDS       @RP1
         B     RPF018                  AND BRANCH
RPF014   DS    0H
         TM    DSCB1+40,X'80'          RECFM=F(B)
         BNO   RPF017                  NO RECFM DEFINED            @RP1
         OI    INFCODE4,$RECFB         MARK FIXED RECORDS          @RP1
         B     RPF018                  AND BRANCH
RPF017   DS    0H                      ERROR ROUTINE
         LA    R15,20                  RC 20 *RECFM NOT FIXED*
         ST    R15,RTNCODE             STORE INTO RPFCOMM
         B     RPF018                  TEST NEXT ATTRIBUTES
RPF018   TM    DSCB1+38,X'40'          TEST IF DSORG DATASET IS PS
         BNO   RPF019                  BRANCH IF NOT
         OI    INFCODE,SEQALC          MASK SEQ DS ALLOCATED
         CLI   MEMBER,X'40'            LOOK IF MEMBERNAME SPECIFIED
         BE    RPFRTN                  OK IF NOT SPECIFIED
         LA    R15,24                  RC 24 *PS DATASET WITH MEMBER*
         ST    R15,RTNCODE             MOVE IN RPFCOMM AREA
         BAL   R14,UNALLOC             UNALLOC DATASET
         B     RPFRTN                  RETURN TO CALLER
RPF019   EQU   *                       CHECK IF PDS AND MEMBER SPEC.
         TM    DSCB1+38,X'02'          DSORG DATASET PO ?
         BO    RPF021                  BRANCH IF YES
         BAL   R14,UNALLOC             ELSE ERROR (^PS OR ^PO)
         LA    R15,28                  RC 28 *DS NOT PS OR NOT PO*
         ST    R15,RTNCODE             STORE INTO RPFCOMM
         B     RPFRTN                  RETURN TO CALLER
RPF021   EQU   *                       BLDL MEMBERNAME AND OPENJ
         CLI   MEMBER,X'40'            MEMBERNAME PRESENT FOR PDS ?
         BNE   RPF023                  TAKE MEMBER 'TEMPNAME'
         MVC   MEMBER,=C'TEMPNAME'     IF NOT PRESENT IN RPFCOMM
RPF023   EQU   *                       CHECK MEMBERNAME
         ESTAE RECOVER,PARAM=((11))    ESTABLISH RECOVERY ROUTINE  @RP1
         OPEN  PDSDCB                  OPEN THE PDS (DDN=RPFWORK2)
         ESTAE 0                       KILL ESTAE ENVIRONMENT      @RP1
         MVC   BLDLNAME,MEMBER         STORE MEMBER INTO BLDLLIST
         BLDL  PDSDCB,BLDLLIST         BLDL MEMBER ENTRY IN DIR
         LTR   R15,R15                 CHECK COMPLETION
         BZ    RPF025                  GO TO RDJFCB AND OPENJ
         CLC   RTNCODE,F0000           ALREADY A RETURN CODE
         BNE   RPF024                  DONT FILL IN IF YES
         LA    R15,32                  RC 32 *MEMBER NOT FOUND*
         ST    R15,RTNCODE             STORE INT RPFCOMM
RPF024   CLOSE PDSDCB                  CLOSE THE DATASET
         B     RPFRTN                  AND RETURN TO CALLER
RPF025   EQU   *                       NOT A SEVERE ERROR
         CLOSE PDSDCB                  CLOSE THE PDS
         RDJFCB SEQDCB                 JFCB OF RPFWORK
         MVC   JFCB+44(8),MEMBER       MEMBERNAME IN JFCB
         OI    JFCB+86,X'01'           MARK PDS SEQ OPEN
         ESTAE RECOVER,PARAM=((11))    ESTABLISH RECOVERY ROUTINE  @RP1
         OPEN  SEQDCB,TYPE=J           OPEN THE DCB AND WRITE JFCB
         CLOSE SEQDCB                  AND CLOSE THE DCB
         FREEPOOL SEQDCB               AND RELEASE THE BUFFERS
         ESTAE 0                       KILL ESTAE ENVIRONMENT      @RP1
RPFRTN   EQU   *                       END OF MODULE
         L     R13,4(R13)              SAVE AREA OF CALLER
         RETURN (14,12),RC=0           AND RETURN TO THE RPF CALLER
         TITLE 'ESTAE RECOVERY ROUTINE, RETRY WITH ABEND913'
RECOVER  EQU   *                                                   @RP1
         DROP  ,                                                   @RP1
         USING *,R15                   GET TEMPORARY ADDRESSABILITY@RP1
         LM    R11,R13,HEREADDR        RESTORE REGISTERS           @RP1
         DROP  R15                     KILL LOCAL ADDRESSABILITY   @RP1
         USING SAVE,R13                RESTORE ADDRESSING          @RP1
         USING SDWA,R1                 GET ADDRESSABILITY OVER SDWA@RP1
         XR    R2,R2                   CLEAR REGISTER 2            @RP1
         ICM   R2,7,SDWACMPC           INSERT COMPLETION CODE      @RP1
         SRL   R2,12                   SHIFT OUT USER COMPLETION CODEP1
         CLM   R2,3,=X'0913'           AUTHORIZATION ABEND ?       @RP1
         BE    SETRP04                 YES: TRY TO RETRY           @RP1
         SETRP RC=0                    ELSE PERCOLATE              @RP1
         BR    R14                     BRANCH TO RTM               @RP1
         DROP  R1                      KILL ADDRESSABILITY         @RP1
SETRP04  DS    0H                                                  @RP1
         SETRP RC=4,RETADDR=RET,RETREGS=NO,FRESDWA=YES             @RP1
         BR    R14                     BRANCH TO RTM               @RP1
         TITLE 'ESTAE RETRY ROUTINE'                               @RP1
RET      EQU   *                                                   @RP1
         DROP  ,                                                   @RP1
         USING *,R15                   LOCAL ADDR                  @RP1
         LM    R11,R13,HEREADDR        RESTORE REGISTERS           @RP1
         DROP  R15                     KILL LOCAL ADDRESSABILITY   @RP1
         USING SAVE,R13                RESTORE ADDRESSABILITY      @RP1
         USING COMMAREA,R11                                        @RP1
         ESTAE 0                       DELETE THIS RECOVERY ROUTINE@RP1
         TM    SEQDCB+DCBOFLGS-IHADCB,DCBOFOPN DS OPENED ?         @RP1
         BZ    RET1                    NO: BRANCH                  @RP1
         CLOSE SEQDCB                  CLOSE THAT POSSIBLE DCB     @RP1
         FREEPOOL SEQDCB               AND FREE THE BUFFERS        @RP1
RET1     DS    0H                                                  @RP1
         TM    PDSDCB+DCBOFLGS-IHADCB,DCBOFOPN DS OPENED ?         @RP1
         BZ    RET2                    NO: BRANCH                  @RP1
         CLOSE PDSDCB                  CLOSE THAT POSSIBLE DCB     @RP1
RET2     DS    0H                                                  @RP1
         LA    R15,36                  RC=0036                     @RP1
         ST    R15,RTNCODE                                         @RP1
         B     RPFRTN                  RETRY SUCCESSFUL            @RP1
         EJECT
*        GENERAL EQUATES.
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3                       ADDRESS OF RPFCOMM PARM
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10                      BASE REGISTER
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         SPACE 3
LOC      CAMLST NAME,LOCDSN,,LOCAREA   FOR LOCATE DATASET
LOCAREA  DS    0D                      DOUBLE WORD BOUNDARY
         DS    CL6                     NOT USED BY RPFDAIR
LOCVOL   DS    CL6                     OBTAINED VOLUME
         DS    CL253                   REST OF LOCAREA
LOCDSN   DS    CL44
*
OBTCMLST CAMLST SEARCH,DALDSN,DAVOLSER,DSCB1
         DS    0D
DSCB1    DS    CL140
*
BLDLLIST DS    0F                      FOR BLDL MACRO
N1       DC    AL2(1)                  NUMBER OF ENTRIES
L1       DC    AL2(14)                 LENGTH OF ENTRY
BLDLNAME DS    CL8                     CONTAINS MEMBERNAME
         DS    CL6                     REST OF BLDLLIST
*
         PRINT NOGEN                   SUPPRESS PRINT OF DCBS
PDSDCB   DCB   DSORG=PO,BLKSIZE=256,MACRF=R,DDNAME=RPFWORK2
*
SEQDCB   DCB   DSORG=PS,MACRF=GM,DDNAME=RPFWORK,EXLST=EXLST
         PRINT GEN                     PRINT EXPANSIONS
*
F0000    DC    F'0'
HEREADDR DS    4F                      FOR ESTAE RECOVER ROUTINE   @RP1
         DS    0F
EXLST    DC    X'87',AL3(JFCB)         JFCB EXIT OF SEQDCB
JFCB     DS    CL176                   CONTAINS DATASET INFORMATION
         TITLE 'DYNAMIC ALLOCATION ROUTINE'
*---------------------------------------------------------------------*
*                                                                     *
*        THIS ROUTINE DYNAMICALLY ALLOCATES DATASETS THROUGH          *
*        THE SYALLOC-MACRO <SVC 99>. TO DO THIS IT NEEDS THE DSNAME   *
*        THE DATASET WILL BE ALLOCATED WITH THE                       *
*        DDNAME RPFWORK AND RPFWORK2.     A RETURNCODE WILL BE        *
*        AVAILABLE IN REG. 15 AND AN ERRORCODE IN 'DAERROR'.          *
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
         SPACE 2
RPFALLOC EQU   *
         ST    R14,ALLOCR14            SAVE RETURNREGISTER
         LA    R2,0
ALLOC001 EQU   *
         TM    ALLOCSW,CATLGD          IS IT A CATALOGED DATASET ?
         BNO   ALLOC050                NO: ALLOC WITH VOL=SER
         SYALLOC DDN=RPFWORK,                                          X
               DSN=DSNAME,                                             X
               DISP1=SHR,                                              X
               MF=E                    ALLOCATE
         LTR   R15,R15                 TEST RETURNCODE
         BNZ   ALLOCERR                GO TO ERROR ROUTINE
         SYALLOC DDN=RPFWORK2,                                         X
               DSN=DSNAME,                                             X
               DISP1=SHR,                                              X
               MF=E                    ALLOCATE
         LTR   R15,R15                 TEST RETURNCODE
         BNZ   ALLOCERR                GO TO ERROR ROUTINE
         RDJFCB SEQDCB                 OBTAIN DATASET INFORMATION
         LA    R4,JFCB                 GET ADDRES
         USING JFCBSECT,R4             GET ADDRESSABILITY
         MVC   VOLUME,JFCBVOLS         GET VOLUME ADDRESS
         MVC   DAVOLSER,JFCBVOLS       MOVE VOLUME NUMBER
         DROP  R4                      KILL ADDRESSABILITY
         XR    R2,R2                   CLEAR RETURN CODE
         OI    INFCODE3,$DAIRALC       MARK ALLOCATED
         B     ALLOC999                RETURN
ALLOC050 EQU   *
         SYALLOC DDN=RPFWORK,                                          X
               DSN=DSNAME,                                             X
               UNITA=COMMUNIT,                                         X
               VOL=VOLUME,                                             X
               DISP1=SHR,                                              X
               MF=E                    ALLOCATE
         LTR   R15,R15                 TEST RETURNCODE
         BNZ   ALLOCERR                GO TO ERROR ROUTINE
         SYALLOC DDN=RPFWORK2,                                         X
               DSN=DSNAME,                                             X
               UNITA=COMMUNIT,                                         X
               VOL=VOLUME,                                             X
               DISP1=SHR,                                              X
               MF=E                    ALLOCATE
         LTR   R15,R15                 TEST RETURNCODE
         BNZ   ALLOCERR                GO TO ERROR ROUTINE
         XR    R2,R2                   CLEAR RETURN CODE
         OI    INFCODE3,$DAIRALC       MARK ALLOCATED
         B     ALLOC999                RETURN
ALLOCERR DS    0H
         MVC   ERRCODE+2(2),DAERROR    DYN ALLOC ERRCODE IN RPFCOMM
         LA    R15,12                  *RC 12 IS DYNALLOC ERROR*
         ST    R15,RTNCODE             MOVE IN RPFCOMM
         LA    R2,12                   RETURNCODE
ALLOC999 DS    0H
         LR    R15,R2                  SET IN RETURN CODE
         L     R14,ALLOCR14            RESTORE R14
         BR    R14                     RETURN
         TITLE 'DYNAMIC UNALLOCATION ROUTINE'
UNALLOC  DS    0H
         ST    R14,UNALLR14            SAVE R14
         SYALLOC FREE=RPFWORK,MF=E     ELSE UNALLOCATE FIRST
         SYALLOC FREE=RPFWORK2,MF=E    UNALLOC RPFWORK2
         NI    INFCODE3,255-$DAIRALC   TURN OFF FLAG
UNALL999 DS    0H                      RETURN
         L     R14,UNALLR14            RESTORE RETURN REGISTER
         BR    R14                     RETURN
         TITLE 'ALLOCATION CONTROL-BOCKS'
*---------------------------------------------------------------------*
*                                                                     *
*        THESE ARE THE CONTROL-BLOCKS USED BY THE DYNAMIC ALLOCATION  *
*        ROUTINE.                                                     *
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
         SPACE 2
         SYALLOC MF=L
ALLOCR14 DS    F                       SAVE R14
*
WORK     DS    D                       FOR CVD INSTRUCTIONS
UNALLR14 DS    F
ALLOCSW  DC    X'00'                   ALLOCSW
CATLGD   EQU   128
         TITLE 'RPFCOMM AREA'
COMMAREA RPFCOMM
JFCBSECT DSECT
         IEFJFCBN
         PRINT NOGEN
         IHASDWA
         DCBD  DSORG=PS,DEVD=DA
         END
