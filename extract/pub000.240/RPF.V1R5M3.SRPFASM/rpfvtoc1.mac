RPFVTOC1 TITLE 'Process VTOC selection list'
*---------------------------------------------------------------------*
*                                                                     *
*        MODULE         RPFVTOC1                                      *
*                                                                     *
*        FUNCTION:      Process the selection list created with       *
*                       RPFVTOC or RPFLISTC.                          *
*                       RPFVTOC and RPFLISTC had created a dataset    *
*                       list With PARM=LONG in the workspace.         *
*                       See DSECT VTOCAREA for the layout.            *
*                       If the dataset list has been created by       *
*                       RPFVTOC, the datasets are on a certain volume *
*                       If created with RPFLISTC, the dataset does    *
*                       have a certain high level qualifier (hlq)     *
*                       and are gotten from the catalog.              *
*                                                                     *
*                       You can select the following line cmds        *
*                       before the dataset name:                      *
*                                                                     *
*                       V or B: Browse/View the dataset               *
*                       C     : Catalog a dataset                  @rp2
*                       U     : Uncatalog a dataset                @rp2
*                       E     : Edit the dataset                      *
*                       I     : Dataset Information                @rp5
*                       M     : Member selection list                 *
*                       R     : Rename dataset                     @rp7
*                       D     : Delete the dataset                    *
*                       Z     : Compress the dataset               @rp3
*                                                                     *
*                       RPFVTOC2 Primary commands:                    *
*                       -  BOTTOM:   Scroll to last page (alias 'B')  *
*                       -  END:      Terminate RPFVTOC1               *
*                       -  L dsn     Locate at dataset 'dsn'          *
*                       -  PFK:      Alter program function keys      *
*                       -  RETURN:   terminate VTOC1 and return to main
*                       -  SCB:      Scroll 1 page backward           *
*                       -  SCF:      Scroll 1 page foreward.          *
*                       -  TOP:      SCROLL TO FIRST PAGE (ALIAS 'T') *
*                       -  =n or =n.n Return to RPFMAIN and exec.     *
*                          option 'n' or option 'n.n'                 *
*                                                                     *
*                       If the primary commands are preceded by an    *
*                       Amphersand ('&&') the command will be repeated*
*                       in the command line after completion.         *
*                                                                     *
*        SUBROUTINES:   RPFBRO    The browse and print function       *
*                       RPFEDIT   The editor                          *
*                       RPFDAIR   Dynamic allocation interface        *
*                       RPFPDS    Member selection list if DSORG=PO   *
*                       RPFPFK    Process PFK command                 *
*                                                                     *
*        28aug2002 RPr: First version of RPFVTOC1.                    *
*        21sep2002 RPr: Add 'C' (Catalog) and 'U' (Uncatalog) cmd  @rp2
*        23sep2002 RPr: Add 'Z' (Compress)                         @rp3
*        02oct2002 RPr: Free RPFWORK/2 if allocated ds deleted     @rp4
*        09oct2002 RPr: Add 'I' line command (INFO)                @rp5
*        12oct2002 RPr: Convert dates in Info screen to gregorian  @rp6
*        25oct2002 RPr: Add 'R' line command (RENAME)              @rp7
*        31oct2002 RPr: Check multivolume and tape datasets        @rp8
*        01nov2002 Rpr: different header if dataset level          @rp9
*        13nov2002 RPr: HLQ inceased to 17 bytes                   @rpa
*        20jun2003      Protect unused input fields                @pw*
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
         SPACE 2
RPFVTOC1 CSECT                         *
         SAVE  (14,12),,*              Save registers
         USING RPFVTOC1,R15            Get local addressability
         LA    R14,SAVE                New save area
         ST    R13,SAVE+4              Backward pointer
         ST    R14,8(R13)              Foreward pointer
         LR    R13,R14                 Exchange savearea
         DROP  R15                     Kill local addressability
         LA    R12,2048(R13)           2nd base register
         LA    R12,2048(R12)
         LA    R9,2048(R12)            3rd base register
         LA    R9,2048(R9)
         LA    R8,2048(R9)             4th base
         LA    R8,2048(R8)
         USING SAVE,R13,R12,R9,R8      Get address via R13,R12,R9,R8
         B     RPFINITT                Branch around savearea
*
SAVE     DC    18F'-1'                 SAVE AREA
         DC    CL8'&SYSDATE',C' ',CL8'&SYSTIME'
RPFINITT DS    0H
         L     R11,0(R1)               Obtain RPFCOMM area
         USING COMMAREA,R11            Get addressability
         STM   R8,R13,HEREADDR         Save for ESTAE
         NI    INFCODE,255-NOTLOAD     Set notload indicator off
*                                      ) is set in rpfutil
         MVI   MSG5,C'-'               Clear
         MVC   MSG5+1(L'MSG5-1),MSG5        message area
         GETMAIN RU,LV=4096            Obtain a screen area
         ST    R1,SCRAREA              Save it's address
         MVI   SW,X'00'                Clear switch
         CLC   COMMSIZE,=H'24'         24 lines screen?
         BE    START19                 Yes: normal 24X80 display
         CLC   COMMSIZE,=H'32'         32 lines screen ?
         BE    START17                 YES: 3278/79 MODEL 3
         CLC   COMMSIZE,=H'43'         43 LINES screen ?
         BNE   START19                 Not one of aboveL model2 display
START15  MVC   ERASE(2),=X'277E'       Generate write alternate
         MVC   SCR2TXT(2),=X'277E'                                 @rp5
         MVC   SCR3TXT(2),=X'277E'                                 @rp5
         MVC   SCRLEN,F1104            MOVE LENTGTH OF OUTPUT SCR.
         MVC   #LINES,=F'40'
         LA    R4,SCR543               Lentgh of screen to write
         ST    R4,TPUTLEN              Store the result
         L     R4,=A(LINES40)          40 * NEXTENT (88) * 2
         ST    R4,NEXTSCR              Store the result
         B     START21
START17  MVC   ERASE(2),=X'277E'       Write alternate
         MVC   SCR2TXT(2),=X'277E'                                 @rp5
         MVC   SCR3TXT(2),=X'277E'                                 @rp5
         MVC   SCRLEN,F807             Move length of output screen
         MVC   #LINES,=F'29'
         LA    R4,SCR532               Length of screen to write
         ST    R4,TPUTLEN              Store the result
         L     R4,=A(LINES29)          29 * NEXTENT (88) * 2
         ST    R4,NEXTSCR              Store the result
         B     START21                 Ok branch
START19  MVC   ERASE(2),=X'27F5'       Write control character
         MVC   SCR2TXT(2),=X'27F5'                                 @rp5
         MVC   SCR3TXT(2),=X'27F5'                                 @rp5
         MVC   SCRLEN,F591             Move length of output screen
         MVC   #LINES,=F'21'
         LA    R4,SCR524               Length of screen to write
         ST    R4,TPUTLEN              Store the result
         L     R4,=A(LINES21)          21 * NEXTENT (88) * 2
         ST    R4,NEXTSCR              Store the result
START21  DS    0H
         MVC   SCR5VAR,=CL23'Datasets on volume '                  @rp8
         MVC   SCR5VOL,BLANKS                                      @rp8
         MVC   SCR5VOL(6),CMDAREA      Move Volume name into screen
         MVC   SCR5HDR(6),=C'Cre.dt'   If vtoc then cre.dt         @rp9
         ZAP   #DS,CMDAREA+21(3)       # datasets                  @rpa
         MVC   SCR5#,=X'402020202120'  Move edit pattrn            @rp9
         ED    SCR5#,#DS               Edit the result             @rp9
         CLC   =C'LVL=',CMDAREA        Dataset name list from catlg@rpa
         BNE   START23                 No branch                   @rp8
         MVC   SCR5VAR,=CL23'Datasets starting with '              @rp8
         MVC   SCR5VOL,CMDAREA+4       Move dataset level          @rpa
         MVC   SCR5HDR(6),=C'Volume'   If catlg then volume        @rp9
         OI    SW,$LEVEL               Mark ds list from catalog   @rp9
START23  DS    0H
         MVC   CMDAREA,BLANKS          Clear CMDAREA
         MVC   FIRSTSCR,GETMSTRT       Save startadr in firstscr
         USING VTOCAREA,R10            Get addressablility
         TPUT  ERASE,L'ERASE,FULLSCR
         BAL   R14,RPFTPUT             Write the screen
         TITLE 'PROCESS PRIMARY COMMANDS'
VTOC01   EQU   *                       Process the output
         LA    R2,AIDBYTE              Address AIDbyte
         L     R5,SCRAREA              Point to obtained  screen area
         BAL   R14,CONVIN              Get the screen A(SCRAREA)
         MVC   REP5,BLANKS             Clear command area
         LTR   R1,R1                   Clear key pressed ?
         BZ    VTOC09                  New screen if yes
         CLI   0(R5),X'6E'             Reshow code ?
         BE    VTOC09                  Yes: review screen
         OC    0(24,R5),BLANKS         Xlate to uppercase
         LR    R3,R1                   SAVE REG 1
         CLI   AIDBYTE,X'7D'           Enter key ?
         BE    VTOC07                  Yes: Branch
         LR    R2,R5                   Address of screen for PFK rout
         LA    R1,AIDBYTE              AIDbyte address
         BAL   R14,PFK                 Get PFK definitions
         LTR   R15,R15                 Alright ?
         BZ    VTOC07                  Yes: exec command
         C     R15,F0004               Confirmation ?
         BE    VTOC05                  No: do not exec command
         C     R15,F0008               PFK not defined ?
         BE    VTOC03                  ERROR
         MVC   MSG5,ERRATXT            PFK definition invalid
         MVC   REP5,BLANKS             Blank command area
         BAL   R14,RPFTPUT             Write screen
         MVI   MSG5,C'-'               Blank message area
         MVC   MSG5+1(L'MSG5-1),MSG5   Blank message area
         B     VTOC01                  Review screen
VTOC03   DS    0H
         MVC   MSG5,ERR9TXT            PFK not defined
         MVC   REP5,BLANKS             Blank command area
         BAL   R14,RPFTPUT             Write screen
         MVI   MSG5,C'-'               Clear
         MVC   MSG5+1(L'MSG5-1),MSG5         message
         B     VTOC01                  Review screen
VTOC05   DS    0H
         MVI   MSG5,C'-'               Clear
         MVC   MSG5+1(L'MSG5-1),MSG5         message
         MVC   REP5,0(R5)              Repeat command
         B     VTOC45                  Write the screen
VTOC07   DS    0H
         CLI   0(R5),C'?'              Clear pressed ?
         BNE   VTOC11                  Continue if not
VTOC09   DS    0H
         OI    SW,$ERASE+$RESHOW       Re-display only + erase
         BAL   R14,RPFTPUT             Write screen unmodified
         B     VTOC01                  Process the screen
VTOC11   DS    0H
         MVI   MSG5,C'-'               Clear
         MVC   MSG5+1(L'MSG5-1),MSG5        message area
         C     R3,SCRLEN               Length not changed ?
         BE    VTOC13                  Allright
         MVC   MSG5,ERR4TXT            Logic error code 0601
         BAL   R14,RPFTPUT             Write screen
         MVI   MSG5,C'-'               Clear
         MVC   MSG5+1(L'MSG5-1),MSG5        message area
         B     VTOC01                  and read the screen
VTOC13   DS    0H                      Process the primary commands
         MVC   BUFFER(24),0(R5)        Move command to buffer
         CLI   BUFFER,C'&&'            &&  entered ?
         BNE   VTOC15                  No: no repeat
         MVC   0(24,R5),BUFFER+1       Move command back without '&&'
         MVI   23(R5),C' '             Reg.5 still points to scr.area
         MVC   REP5,BUFFER             Repeat command
VTOC15   DS    0H
         BAL   R14,RPFSCRN             Process 'B', 'V', 'E', 'I',
*                                      'M', 'C', 'U' or 'Z' linecmd
         LTR   R15,R15                 RC <> 0000 ?
         BNZ   RETURN                  EXIT (return or =n.n cmd)
         TM    SW,$CHANGE              Something changed ?
         BNO   VTOC19                  Process cmds if not changed
         CLC   0(24,R5),BLANKS         No command entered ?
         BE    VTOC17                  Do not write pause msg
         MVC   MSG5,INF1TXT            Move pause message
         CLI   REP5,C' '               No command in command area
         BNE   VTOC17                  No: continue
         MVC   REP5(24),0(R5)          Else repeat command
VTOC17   DS    0H                      Process the screen
         BAL   R14,RPFTPUT             Write the screen
         MVI   MSG5,C'-'               Clear
         MVC   MSG5+1(L'MSG5-1),MSG5        message area
         NI    SW,X'FF'-$CHANGE        Set modifications off
         B     VTOC01                  and process the screen
VTOC19   DS    0H                      PROCESS THE SCREEN
         CLC   0(3,R5),=C'SCF'         'SCF' (scroll foreward) command
         BNE   VTOC23                  No: other commands
         L     R10,FIRSTSCR            First line on screen
         AL    R10,NEXTSCR             Next screen
         C     R10,LASTREC             Compare with endaddress
         BH    VTOC21                  Error if high
         CLI   DSN,C' '                Blank entry ?
         BE    VTOC21                  Error blanks at the end
         ST    R10,FIRSTSCR            New first line
         BAL   R14,RPFTPUT             Write the screen
         B     VTOC01                  Process the screen
VTOC21   DS    0H                      Error in 'SCF' command
         MVC   MSG5,ERR1TXT            Page forward no effect
         B     VTOC45                  Write the screen
VTOC23   DS    0H                      Error in 'SCF' command
         CLC   0(3,R5),=C'PFK'         PFK entered ?
         BNE   VTOC25                  No: test next command
         LINK  EP=RPFPFK,PARAM=((11)),VL=1 Invoke RPFPFK
         B     VTOC09                  Write new screen
VTOC25   DS    0H                      ----------> SCB command
         CLC   0(3,R5),=C'SCB'         'SCB' command (scroll backward)?
         BNE   VTOC29                  No: other commands
         L     R10,FIRSTSCR            1st line on screen
         C     R10,GETMSTRT            Already first screen ?
         BNH   VTOC27                  Yes: scroll had no effect
         SL    R10,NEXTSCR             1 screen back
         C     R10,GETMSTRT            And compare with begin
         BNL   *+8                     If low: reset to first screen
         L     R10,FIRSTREC            Reset to first screen
         ST    R10,FIRSTSCR            New 1st line
         BAL   R14,RPFTPUT             Write the screen
         B     VTOC01                  And process the screen
VTOC27   DS    0H                      Error routine 'SCB'
         MVC   MSG5,ERR1TXT            Scroll had no effect
         B     VTOC45                  Write the screen
VTOC29   DS    0H                      Process 'SCB' command
         CLI   0(R5),C'B'              Bottom command ?
         BNE   VTOC31                  No: process other commands
         L     R3,LASTREC              Load begin address
         SL    R3,NEXTSCR              Subtract screen length
         LA    R3,NEXTENT(R3)                minus one
         C     R3,FIRSTREC             Before firstrec ?
         BNL   *+8                     No: proceed
         L     R3,FIRSTREC             Divide by length screen
         ST    R3,FIRSTSCR             Save address
         BAL   R14,RPFTPUT             Write the screen
         B     VTOC01                  Process the screen
VTOC31   DS    0H
         CLC   0(2,R5),=C'L '         'LOCATE ON dataset command'
         BNE   VTOC35                  No: process other commands
         LR    R1,R5                   A(screen area) into Reg.1
         BAL   R14,LOCATE              Try to locate string
         LTR   R15,R15                 Found ?
         BZ    VTOC45                  Yes: branch
         MVC   MSG5,ERRCTXT            Text not found
         BAL   R14,RPFTPUT             Write the screen
         B     VTOC01                  and process the screen
VTOC35   DS    0H
         CLC   0(3,R5),=C'TOP'         TOP entered ?
         BE    VTOC37                  Yes: process command
         CLC   0(2,R5),=C'T '          TOP command (alias 'T') ?
         BNE   VTOC39                  No: process other commands
VTOC37   DS    0H
         MVC   FIRSTSCR,GETMSTRT       First line is 1st member
         BAL   R14,RPFTPUT             Write the screen
         B     VTOC01                  and process the screen
VTOC39   DS    0H
         CLI   0(R5),C'='              =n or =n.n command?
         BNE   VTOC41                  No: branch
         MVC   CMAINOPT,1(R5)          Move options
         OI    INFCODE2,$RETURN        Set return indicator
         B     RETURN                  And terminate (to main)
VTOC41   DS    0H
         CLC   0(6,R5),=C'RETURN'      RETURN command ?
         BNE   VTOC43                  No: branch
         OI    INFCODE2,$RETURN        Set RETURN indicator
         B     RETURN                  And terminate
VTOC43   DS    0H
         CLC   0(3,R5),=C'END'         END command ?
         BE    RETURN                  Back to caller
         CLI   0(R5),C' '              Nothing entered ?
         BE    VTOC45                  Go on
         MVC   MSG5,ERR3TXT            Invalid command
         MVC   REP5,0(R5)              Repeat command in error
VTOC45   DS    0H
         BAL   R14,RPFTPUT
         B     VTOC01
         TITLE 'TPUT MODIFIED SCREEN'
RPFTPUT  DS    0H                      Routine to write screen
*---------------------------------------------------------------------*
*                                                                     *
*        REGISTER USAGE IN THIS ROUTINE:                              *
*                                                                     *
*        R3    NUMBER OF LINES ON THE SCREEN (21/29/40)               *
*        R4    ADDRESS OF SCREEN TO BE TPUTTED.                       *
*        R5    ADDRESS OF MESSAGE TABLE                               *
*        R6    TOTAL NUMBER OF MESSAGES, WHICH CAN BE GIVEN           *
*        R10   CURRENT ADDRESS OF MEMBERLIST IN WORKSPACE.            *
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
         ST    R14,PUTR14              Save return reg
         TM    SW,$RESHOW              Only reshow of screen?
         BNO   TPUT000                 No: rebuild screen area
         NI    SW,255-$RESHOW          Reset flag
         B     TPUT014                 And write screen
TPUT000  DS    0H                      *START LOOP*
         LA    R4,LNE5                 First line on screen (outp)
         L     R10,FIRSTSCR            First line on screen (input)
         L     R3,#LINES               Number of lines on screen
TPUT001  DS    0H                      *START LOOP*
         MVI   1(R4),X'C9'             Set Input field as default  @pw
         MVI   5(R4),C' '              Blank
         MVC   8(26,R4),BLANKS              the fields
         MVC   39(50,R4),BLANKS                       in the screen@rp9
         MVC   36(3,R4),=AL3(TURQ)     Default TURQ
         C     R10,LASTREC             Past last record pointer ?
         BH    TPUT008                 Yes branch                  @pw
         MVC   8(26,R4),DSN            Data set name
         MVC   39(6,R4),CREAT          Creation date
         TM    SW,$LEVEL               Ds-list from catalog?       @rp9
         BNO   *+10                    No: branch                  @rp9
         MVC   39(6,R4),VOL            Move volume instead         @rp9
         MVC   46(6,R4),REFDT          Last reference date
         MVC   53(4,R4),DSORG          Data set organisation
         MVC   58(4,R4),RECFM          Record format
         MVC   62(6,R4),LRECL          Record length
         MVC   68(6,R4),BLKSIZE        Block length
         MVC   74(4,R4),DEXT           # extents
         MVC   78(6,R4),TRK            Data set size
         MVC   84(5,R4),UNUSED+1       Empty space in dataset
         LA    R6,MSG#                 Number of possible msgs
         LA    R5,MSGTBLE              Load table address
TPUT007  DS    0H
         CLC   0(1,R5),OPER            Match found of OPER ?
         BE    TPUT009                 Branch if yes
         LA    R5,32(R5)               NEXT ENTRY IN MSGTBLE
         BCT   R6,TPUT007              LOOP UNTIL ALL DONE
         MVI   OPER,C' '                      ,,
         B     TPUT011                 NEXT ENTRY
TPUT008  DS    0H                      End of list                @pw
         MVI   1(R4),X'F9'               so protect input field   @pw
         B     TPUT013                                            @pw
TPUT008A DS    0H
         MVC   8(1,R4),1(R4)
         B     TPUT013
TPUT009  DS    0H
         MVC   36(3,R4),=AL3(WHITE)    Make message high light
         MVC   39(30,R4),1(R5)         Move message after DSN
         MVC   69(20,R4),BLANKS        And blank the rest
         MVC   OPER,31(R5)             MOVE REPLACEMENT OPER.
TPUT011  DS    0H                      Set pointers on next line
         LA    R10,2*NEXTENT(R10)      Next dataset in workspace
TPUT013  DS    0H
         LA    R4,#SCRENT(R4)          Next line on screen
         BCT   R3,TPUT001              Loop until all lines done
TPUT014  DS    0H
         TM    SW,$ERASE               Should the screen eraser first ?
         BNO   TPUT015                 NO: TPUT IMMED.
         NI    SW,255-$ERASE           Flag off
         TPUT  ERASE,L'ERASE,FULLSCR   Erase the screen
TPUT015  DS    0H
         L     R2,TPUTLEN              Length to be written
         TPUT  SCR5TXT,(R2),FULLSCR    Write screen
         MVC   SCR5TXT(2),NOERASE      Repair WCC
         L     R14,PUTR14              Restore reg.14
         BR    R14
         TITLE 'PROCESS THE LINE-COMMANDS'
*---------------------------------------------------------------------*
*                                                                     *
*        Process the line-commands entered before the dataset-name    *
*        The line-cmds  are: B/V (Browse), E (EDIT), M (Member list)  *
*        U (Uncatalog), C (Catalog), D (Delete), Z (Compress)         *
*        I (Information), R (Rename)                               @rp7
*                                                                     *
*        Returncodes                                                  *
*                    00. EVERYTHING ALLRIGHT                          *
*                    04. Return or '=n.n' command entered             *
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
         SPACE 2
RPFSCRN  DS    0H                      Process line commands
         ST    R14,GETR14              Save register 14
         L     R3,SCRAREA              Point to obtained screen area
         MVC   0(24,R3),BUFFER         Move command
         LA    R3,24(R3)               Skip command area
         L     R10,FIRSTSCR            Corresponding line in workspace
         L     R4,#LINES               Number of line on screen
SCRN001  DS    0H
         OC    0(27,R3),BLANKS         Xlate to uppercase
         CLI   0(R3),C' '              No operation ?
         BE    SCRN098                 Take next entry
         CLI   1(R3),C' '              No DSN    in entry ?
         BE    SCRN098                 Possible last screen.
         CLI   DSN,C'-'                Already deleted ?           @rp2
         BE    SCRN098                 Yes: next entry             @rp2
*---------------------------------------------------------------------*
*        Uncatalog data set
*---------------------------------------------------------------------*
         CLI   0(R3),C'U'              Uncatalog the dataset       @rp2
         BNE   SCRN009                 No: Test next line cmd      @rp3
         MVC   LOCDSN,DSN              Dsname to locate            @rp2
         OI    SW,$CHANGE              Mark changes on             @rp2
         CLC   DSORG(4),=C'VSAM'       Vsam dataset?               @rp2
         BE    SCRN007                 Yes: cannot uncatalogued    @rp2
         LOCATE LOC                    Locate the dataset          @rp2
         LTR   R15,R15                 Data set catalogued?        @rp2
         BNZ   SCRN005                 No: nothing to uncatalog    @rp2
         CLC   LOCVOL,VOL              Catalogued on this volume?  @rp2
         BNE   SCRN005                 Dataset not catalogued      @rp2
         CATALOG UNCAT                 Uncatalog the dataset       @rp2
         LTR   R15,R15                 Alright?                    @rp2
         BNZ   SCRN003                 No: error in uncatalog      @rp2
         MVI   OPER,C'U'               Successful uncatalogue      @rp2
         B     SCRN098                 Next entry                  @rp2
SCRN003  DS    0H                                                  @rp2
         MVI   OPER,X'ED'              Uncatalogue error           @rp2
         B     SCRN098                 Next entry                  @rp2
SCRN005  DS    0H                                                  @rp2
         MVI   OPER,X'EF'              Dataset not catalogued      @rp2
         B     SCRN098                 Next entry                  @rp2
SCRN007  DS    0H                                                  @rp2
         MVI   OPER,X'EE'              VSAM dataset                @rp2
         B     SCRN098                 Next entry                  @rp2
*---------------------------------------------------------------------*
*        Information about a data set
*---------------------------------------------------------------------*
SCRN009  DS    0H                                                  @rp3
         CLI   0(R3),C'I'              Information about dataset   @rp5
         BNE   SCRN011                 No: Test next line cmd      @rp5
         BAL   R14,INFO                Process information         @rp5
         MVI   OPER,C'I'               Information                 @rp5
         B     SCRN098                 Next entry                  @rp5
*---------------------------------------------------------------------*
*        Edit data set
*---------------------------------------------------------------------*
SCRN011  DS    0H                                                  @rp2
         CLI   TYPE+2,X'20'            DEVTYPE = Dasd?
         BNE   SCRN083                 No: error msg ds not on dasd
         CLI   MULT,C'+'               Multivolume dataset ?
         BE    SCRN085                 Yes: error ms, multivolume
         CLI   0(R3),C'E'              Edit selected ?
         BNE   SCRN023                 No test others
         OI    SW,$CHANGE              Mark modifications on
         TM    INFCODE,$NOTMP          No temporary dataset ?
         BNO   SCRN013                 No: proceed
         MVI   OPER,X'F7'              Move error code in OPER
         B     SCRN098                 Next entry
SCRN013  DS    0H
         CLC   DSORG(2),=C'PS'         Sequential?
         BE    SCRN015                 Yes: proceed
         CLC   DSORG(2),=C'PO'         Partitioned?
         BNE   SCRN021                 No: error
SCRN015  DS    0H
         CLI   RECFM,C'F'              Fixed records?
         BE    SCRN017                 Yes: proceed
         CLI   RECFM,C'V'              Variable records ?
         BNE   SCRN021                 No: EDIT N/A
         CLC   LRECL,=C'   255'        Record length is 255 or lower?
         BH    SCRN021                 No: edit not available
         B     SCRN019                 Yes: perform edit
SCRN017  DS    0H
         CLC   LRECL,=C'    80'        Record length is 80 ?
         BNE   SCRN021                 No: edit not available
SCRN019  DS    0H
         MVC   DSNAME,DSN              Move dsname to edit
         MVC   VOLUME,VOL              Move volume to edit
         MVC   MEMBER,BLANKS           Blank Member in COMMAREA
         BAL   R14,EDIT                Edit dataset
         OI    SW,$ERASE               Mark erase on.
         LTR   R15,R15                 Rc = 0000 ?
         BNZ   SCRN999                 No: branch (=n.n cmd given)
         B     SCRN098                 And branch
SCRN021  DS    0H
         MVI   OPER,X'F8'              Edit not available
         B     SCRN098                 Next entry
*---------------------------------------------------------------------*
*        View/Browse the data set
*---------------------------------------------------------------------*
SCRN023  DS    0H                      ----------> Browse
         CLI   0(R3),C'B'              Browse selected ?
         BE    SCRN024                 Yes: process browse/view func
         CLI   0(R3),C'V'              Browse selected (alias view)?
         BNE   SCRN033                 No: test other line commands
SCRN024  DS    0H
         TM    INFCODE,$NOTMP          No temporary dataset ?
         BNO   SCRN025                 NO: proceed
         MVI   OPER,X'F7'              MSG No temporary dataset
         B     SCRN098                 tell user what is wrong
SCRN025  DS    0H
         OI    SW,$CHANGE              Mark modifications on
         CLC   DSORG(2),=C'PS'         Sequential?
         BE    SCRN026                 Yes: proceed
         CLC   DSORG(2),=C'PO'         Partitioned?
         BNE   SCRN029                 No: error
SCRN026  DS    0H
         CLI   RECFM,C'F'              Fixed records?
         BE    SCRN027                 Yes: proceed
         CLI   RECFM,C'V'              Variable records ?
         BNE   SCRN029                 No: Browse N/A
SCRN027  DS    0H
         CLC   LRECL,=C'   255'        Record length is 255 or lower?
         BNH   SCRN031                 Yes: Browse
SCRN029  DS    0H                      Browse not available
         MVI   OPER,X'F4'              Error code
         B     SCRN098                 Next entry
SCRN031  DS    0H
         MVC   DSNAME,DSN              Move dsname to browse
         MVC   VOLUME,VOL              Move volume to browse
         MVC   MEMBER,BLANKS           Blank Member in COMMAREA
         BAL   R14,BROWSE              Browse dataset
         OI    SW,$ERASE               Mark erase on.
         LTR   R15,R15                 Rc = 0000 ?
         BNZ   SCRN999                 No: branch (=n.n cmd given)
         B     SCRN098                 And branch
*---------------------------------------------------------------------*
*        Member list
*---------------------------------------------------------------------*
SCRN033  DS    0H                      ----------> Member list
         CLI   0(R3),C'M'              Member selection list?
         BNE   SCRN039                 No: Try next line command
         OI    SW,$CHANGE              Mark modifications on
         CLC   DSORG(2),=C'PO'         Partitioned data?
         BNE   SCRN037                 No: tell user
         TM    INFCODE,$NOTMP          No temporary dataset ?
         BNO   SCRN035                 NO: proceed
         MVI   OPER,X'F7'              Move error code and ....
         B     SCRN098                 tell user what is wrong
SCRN035  DS    0H
         MVC   DSNAME,DSN              Move dsname to browse
         MVC   VOLUME,VOL              Move volume to browse
         MVC   MEMBER,BLANKS           Blank Member in COMMAREA
         BAL   R14,MEMLIST             Process Member selection list
         OI    SW,$ERASE               Mark erase on.
         LTR   R15,R15                 Rc = 0000 ?
         BNZ   SCRN999                 No: branch (=n.n cmd given)
         B     SCRN098                 And branch
SCRN037  DS    0H
         MVI   OPER,X'F2'              Member list N/A
         B     SCRN098                 Next entry
*---------------------------------------------------------------------*
*        Delete data set
*---------------------------------------------------------------------*
SCRN039  DS    0H                      ----------> Delete data set
         CLI   0(R3),C'D'              Delete the dataset
         BNE   SCRN053                 No: Try other commands      @rp2
         OI    SW,$CHANGE              Mark modifications on
         MVC   LOCDSN,DSN              Move dataset name for locate
         MVC   VOLTYP,TYPE             Move UCB type
         MVC   CATVOL1,VOL             Move volume name            @rp2
         CLC   DSORG(4),=C'VSAM'       VSAM dataset ?
         BE    SCRN047                 Yes: could not be deleted
SCRN041  DS    0H
         MVC   SCR2DSN,LOCDSN          Move dataset to be deleted
         TPUT  SCR2TXT,SCR2LEN,FULLSCR
         OI    SW,$ERASE               Mark erase on
         TGET  TEMP,10,ASIS            Get aidbyte
         LTR   R1,R1                   Input length is zero ?
         BZ    SCRN041                 Yes: error
         CLI   TEMP,X'F3'              PF03 pressed ?
         BE    SCRN048                 Yes cancel deletions
         CLI   TEMP,X'C3'              PF15 pressed ?
         BE    SCRN048                 Yes cancel deletions
         CLI   TEMP,X'7C'              PF12 pressed ?
         BE    SCRN043                 Yes: continue
         CLI   TEMP,X'4C'              PF24 pressed ?
         BNE   SCRN041                 NO: Redisplay confirmation
SCRN043  DS    0H
         LOCATE LOC                    Locate the dataset
         LTR   R15,R15                 Data set catalogued?
         BNZ   SCRN045                 No: scratch only
         CLC   LOCVOL,CATVOL1          Does volume match ?
         BNE   SCRN045                 No: scratch only
         CATALOG UNCAT                 Uncatalog the dataset
         LTR   R15,R15                 Uncatalog OK?
         BZ    SCRN045                 Yes: branch and scratch
         MVI   OPER,X'ED'              Uncatalog error dataset     @rp2
         B     SCRN098                 And branch
SCRN045  DS    0H
         XR    R0,R0                   Needed for SCRATCH macro    @rp7
         SCRATCH SCRATCH               Scratch dataset             @rp2
         LTR   R15,R15                 Alright ?                   @rp2
         BNZ   SCRN051                 No: error in delete         @rp2
         CLC   DSN,DSNAME              Deleted ds allocated to RPF @rp4
         BNE   SCRN046                 Branch if not               @rp4
         SYALLOC FREE=RPFWORK,MF=E     Free the                    @rp4
         SYALLOC FREE=RPFWORK2,MF=E         allocated datasets     @rp4
         NI    INFCODE3,255-$DAIRALC   Tell RPFDAIR to alloc next t@rp4
SCRN046  DS    0H
         MVC   DSN,=CL44'------- Deleted -------'
         MVI   OPER,C'D'               Mark dataset deleted
         B     SCRN098                 and branch
SCRN047  DS    0H
         MVI   OPER,X'F0'              MSG: Use IDCAMS delete
         B     SCRN098                 And branch
SCRN048  DS    0H
         MVI   OPER,X'FE'              Delete rejected
         B     SCRN098                 Next entry
SCRN051  DS    0H
         MVI   OPER,X'FF'              Error in delete
         B     SCRN098                 Next entry
*---------------------------------------------------------------------*
*        Catalog data set
*---------------------------------------------------------------------*
SCRN053  DS    0H                      ----------> Catalog data set
         CLI   0(R3),C'C'              Catalog the dataset         @rp2
         BNE   SCRN059                 No: try other linecmds      @rp2
         OI    SW,$CHANGE              Mark changes on             @rp2
         MVC   LOCDSN,DSN              Dsname to locate            @rp2
         MVC   VOLTYP,TYPE             Move UCB type
         MVC   CATVOL1,VOL             Move volume name            @rp2
         LOCATE LOC                    Locate the dataset          @rp2
         LTR   R15,R15                 Data set catalogued?        @rp2
         BZ    SCRN057                 Yes: already catalogued     @rp2
         CLC   DSORG(4),=C'VSAM'       Vsam dataset ?              @rp2
         BE    SCRN056                 Yes: cannot be catalogued   @rp2
         CLC   LOCDSN(3),=C'SYS'       Start with SYS?             @rp2
         BNE   SCRN055                 No: catalog                 @rp2
         CLI   LOCDSN+4,C'.'           Does start with 'SYSn'      @rp2
         BE    SCRN055                 Yes: ok                     @rp2
         MVI   OPER,X'EC'              Temporary dataset           @rp2
         B     SCRN098                 Branch                      @rp2
SCRN055  DS    0H                                                  @rp2
         CATALOG NEWCAT                Catalog the dataset         @rp2
         LTR   R15,R15                 Catalogue ok?               @rp2
         BNE   SCRN058                 No: error                   @rp2
         MVI   OPER,C'C'               Dataset catalogued          @rp2
         B     SCRN098                 Branch                      @rp2
SCRN056  DS    0H                                                  @rp2
         MVI   OPER,X'EE'              VSAM dataset                @rp2
         B     SCRN098                 Next entry                  @rp2
SCRN057  DS    0H                                                  @rp2
         MVI   OPER,X'FB'              Dataset already catalogued  @rp2
         B     SCRN098                 Next entry                  @rp2
SCRN058  DS    0H                                                  @rp2
         MVI   OPER,X'FA'              Error catalog processing    @rp2
         B     SCRN098                 Next entry                  @rp2
*---------------------------------------------------------------------*
*        Compress dataset with IEBCOPY
*---------------------------------------------------------------------*
SCRN059  DS    0H                      ----------> Compress        @rp3
         CLI   0(R3),C'Z'              Compress the dataset?       @rp3
         BNE   SCRN079                 No: Test next line cmd      @rp3
         CLC   =C'SYS1.LINKLIB',DSN    Try to compress linklib?    @rp3
         BE    SCRN067                 Not a good idea             @rp3
         CLC   =C'PO ',DSORG           PDS? (not a PDSE)           @rp3
         BNE   SCRN067                 Compress N/A                @rp3
         BAL   R14,COMPRESS            Compress the dataset        @rp3
         B     SCRN098                 Next entry                  @rp3
SCRN067  DS    0H                                                  @rp3
         MVI   OPER,X'EB'              Compress N/A                @rp3
         B     SCRN098                 Next entry                  @rp3
*---------------------------------------------------------------------*
*        Rename the data set
*---------------------------------------------------------------------*
SCRN079  DS    0H                                                  @rp5
         CLI   0(R3),C'R'              Rename a dataset?           @rp7
         BNE   SCRN081                 No: Invalid line command    @rp7
         BAL   R14,RENAME              Rename the data set         @rp7
         B     SCRN098                 Next entry                  @rp7
SCRN081  DS    0H                                                  @rp7
         MVI   OPER,X'AA'              Invalid line command        @rp5
         B     SCRN098                 Next entry
SCRN083  DS    0H                                                  @rp7
         MVI   OPER,X'DD'              Dataset not on dasd
         B     SCRN098                 Next entry
SCRN085  DS    0H                                                  @rp7
         MVI   OPER,X'DC'              Multivolume dataset
         B     SCRN098                 Next entry
SCRN098  DS    0H
         LA    R3,27(R3)               Next entry input
         LA    R10,2*NEXTENT(R10)      Next entry output
         BCT   R4,SCRN001              Process next line command
         XR    R15,R15                 Clear reg.15
SCRN999  EQU   *
         L     R14,GETR14              RESTORE R14
         BR    R14
         TITLE 'Rename the dataset'
*---------------------------------------------------------------------*
*                                                                     *
*        RENAME      - Rename the dataset, prompt for a new dataset   *
*                      name. If the dataset is catalogued, recatalog  *
*                      the dataset with the newname.                  *
*                      Reg.10 points to the first dataset line        *
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
         SPACE 2
RENAME   DS    0H
         STM   R0,R15,SAVEROUT         Save registers
         NI    SW,255-$CATLGD          Default ds not catalogued
         MVC   SCR4DSN,DSN             Move data set
         MVC   SCR4NEW,DSN                  names into screen
         MVC   SCR4VOL,VOL             and volume
         MVC   SCR4MSG,BLANKS          Blank message
REN005   DS    0H
         MVC   NEWNAME,BLANKS
         TPUT  SCR4TXT,SCR4LEN,FULLSCR Write screen
         TGET  SCR4BFR,50,ASIS         Read the screen
         CLI   BUF4AID,X'F3'           PFK3?
         BE    REN099                  Yes: End
         CLI   BUF4AID,X'C3'           PFK15?
         BE    REN099                  Yes: End
         CLI   BUF4AID,X'7D'           Enter Key
         BE    REN007                  No: invalid key, redisplay
         MVC   SCR4MSG,=CL22'Invalid key pressed'
         B     REN005                  Branch
REN007   DS    0H
         CLC   DSORG(4),=C'VSAM'       VSAM dataset
         BNE   REN008                  No: ok
         MVC   SCR4MSG,=CL22'VSAM dataset'
         B     REN005                  Branch
REN008   DS    0H
         OC    NEWNAME,BLANKS          Xlate to uppercase
         CLC   DSN,NEWNAME             The same?
         BE    REN009                  Yes: error
         MVC   LOCDSN,NEWNAME          Look if newname cataloged
         LOCATE LOC
         LTR   R15,R15                 Catalogued?
         BNZ   REN011                  No: alright
         CLC   LOCVOL,VOL              Catalogued on this volume?
         BNE   REN011                  No: rename dataset
REN009   DS    0H
         MVC   SCR4MSG,=CL22'Newname already exists'
         MVC   SCR4NEW,NEWNAME
         B     REN005                  Branch
REN011   DS    0H
         MVC   LOCDSN,DSN              Look if catalogued
         LOCATE LOC
         LTR   R15,R15
         BNZ   REN015                  Rename if not catalogued
         CLC   LOCVOL,VOL              Catalogued on same volume
         BNE   REN015                  No: other dataset, rename
         CLC   LOCAREA,=H'1'           More than 1 volume?
         BNH   REN013                  no: ok branch
         MVC   SCR4MSG,=CL22'MULTIVOLUME dataset'
         B     REN005
REN013   DS    0H
         CLC   SMS,=C'YES'             SMS managed? (N/A for MVS38J)
         BE    REN015                  Yes: (UN)catalog not needed
*                                      SMS managed datasets will be
*                                      recataloged during RENAME
         OI    SW,$CATLGD              Else: let RPF recatalog
REN015   DS    0H
         MVC   CATVOL,=H'1'            # volumes
         MVC   VOLTYP,TYPE             Device type
         MVC   CATVOL1,VOL             Move volume
         XR    R0,R0                   Needed for RENAME macro
         RENAME RENAMELS               Rename dataset
         MVI   OPER,C'R'               Mark renamed?
         LTR   R15,R15                 Ok?
         BZ    REN017                  Yes: continue
         MVI   OPER,X'DE'              Error in rename
         B     REN099                  Exit
REN017   DS    0H
         MVC   DSN,NEWNAME             Newname in dataset list
         TM    SW,$CATLGD              Dataset catalogued?
         BNO   REN099                  No: ready
         CATALOG UNCAT                 Uncatalog old dsn
         MVC   LOCDSN,NEWNAME          Move new dataset name
         CATALOG NEWCAT                Catalog new dsn
REN099   DS    0H
         OI    SW,$ERASE+$CHANGE       Erase screen with next TPUT
         LM    R0,R14,SAVEROUT         Restore the registers
         XR    R15,R15                 RC = 0000
         BR    R14
         TITLE 'Display dataset information'
*---------------------------------------------------------------------*
*                                                                     *
*        INFO        - Display information about the dataset          *
*                      This routine will be invoked if line-command   *
*                      'I' has been given                             *
*                      Reg.10 points to the first dataset line        *
*                                                                     *
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
         SPACE 2
INFO     DS    0H
         STM   R0,R15,SAVEROUT         Save registers
         XR    R6,R6                   Clear # dirblks
         XR    R7,R7                   Clear # members
         XR    R2,R2                   Clear # free dirblks
         MVC   SCR3DSN,DSN             Move dataset name
         MVC   SCR3VOL,VOL             Move volume
         MVC   SCR3DEVT,=C'??????'     Unknown device type
         TM    TYPE+2,X'20'            Direct Access ?
         BNO   DEVT999                 No: branch
         CLI   TYPE+3,D3330            3330 model 1?
         BNE   DEVT002
         MVC   SCR3DEVT,=C'3330  '     3330 model 1
         B     DEVT999
DEVT002  DS    0H
         CLI   TYPE+3,D3350            3350 ?
         BNE   DEVT003                 No
         MVC   SCR3DEVT,=C'3350  '     3350
         B     DEVT999
DEVT003  DS    0H
         CLI   TYPE+3,D3331            3330 model 11?
         BNE   DEVT004
         MVC   SCR3DEVT,=C'3330-1'     3330 model 11
         B     DEVT999
DEVT004  DS    0H
         CLI   TYPE+3,D3380            3380 ?
         BNE   DEVT005
         MVC   SCR3DEVT,=C'3380  '     3380
         B     DEVT999
DEVT005  DS    0H
         CLI   TYPE+3,D3375            3375 ?
         BNE   DEVT007
         MVC   SCR3DEVT,=C'3375  '     3375
         B     DEVT999
DEVT007  DS    0H
         CLI   TYPE+3,D2314            2314 ?                      @rp9
         BNE   DEVT009                                             @rp9
         MVC   SCR3DEVT,=C'2314  '     2314                        @rp9
         B     DEVT999                                             @rp9
DEVT009  DS    0H                                                  @rp9
         CLI   TYPE+3,D3390            3390 ?
         BNE   DEVT999
         MVC   SCR3DEVT,=C'3390  '     3390
DEVT999  DS    0H
         MVC   SCR3REF,BLANKS                                      @rp9
         MVC   SCR3CRE,BLANKS                                      @rp9
         CLC   CREAT,BLANKS            No creation date filled in  @rp9
         BE    INF000                  Nothing filled in           @rp9
*                                 because obtain failed or on tape @rp9
         MVC   DWB(2),CREAT            First part of creation date @rp6
         MVC   DWB+2(3),CREAT+3        Skip the period in CREAT    @rp6
         PACK  YYDDD,DWB(5)            Pack the field              @rp6
         L     R15,EPCNVDT             Module RPFCNVDT             @rp6
         CALL  (15),(YYDDD,DDMMYY)     Call conversion routine     @rp6
         MVC   SCR3CRE(3),MNAME#E      Monthname                   @rp6
         MVC   SCR3CRE+3(2),=C', '                                 @rp6
         MVC   SCR3CRE+5(2),DD         Daynumber in the month      @rp6
         MVI   SCR3CRE+7,C' '                                      @rp6
         MVC   SCR3CRE+8(4),CC         Move year including cent.   @rp6
         MVC   SCR3REF,=CL12'Not refer.'                           @rp9
         CLC   REFDT,=C'00.000'        Not referenced ?            @rp9
         BE    INF000                  Yes: skip date conversion   @rp9
         MVC   DWB(2),REFDT            First part of ref date      @rp6
         MVC   DWB+2(3),REFDT+3        Skip the period in REFDT    @rp6
         PACK  YYDDD,DWB(5)            Pack the field              @rp6
         L     R15,EPCNVDT             Module RPFCNVDT             @rp6
         CALL  (15),(YYDDD,DDMMYY)     Call conversion routine     @rp6
         MVC   SCR3REF(3),MNAME#E      Monthname                   @rp6
         MVC   SCR3REF+3(2),=C', '                                 @rp6
         MVC   SCR3REF+5(2),DD         Daynumber in the month      @rp6
         MVI   SCR3REF+7,C' '                                      @rp6
         MVC   SCR3REF+8(4),CC         Move year including cent.   @rp6
INF000   DS    0H                                                  @rp9
         MVC   SCR3REC,RECFM           Move Record format
         MVC   SCR3RECL,LRECL          Move Record length
         MVC   SCR3BLK,BLKSIZE         Move blocksize
         MVC   SCR3ORG,DSORG           Move data set organisation
         MVC   SCR3TRK,TRK             Move dataset size in tracks
         MVC   SCR3FREE,UNUSED         Move unused space in dataset
         MVC   SCR3EXT,DEXT            Move number of extents
         MVC   SCR3#DIR,=C'   N/A'     Init # dirblks
         MVC   SCR3#FBL,=C'   N/A'     Init # free dirblks
         MVC   SCR3#MBR,=C'   N/A'     Init # members
         CLC   DSORG(2),=C'PO'         Partitioned?
         BNE   INF017                  No: No number of DIR's/Members
         MVC   ESTAERET,=A(INF015)     Move return pointer for ESTAE
         ST    R10,HEREADDR+8          Save reg.10
         SYALLOC FREE=RPFUT2,MF=E      Free previous RPFUT1
         SYALLOC DDN=RPFUT2,VOL=SCR3VOL,DSN=SCR3DSN,DISP1=SHR,         X
               UNITA=COMMSTOR,MF=E
         LTR   R15,R15                 Alloc ok ?
         BNZ   INF015                  Error..
         ESTAE RECOVER,PARAM=(11)      Establish ESTAE routine
         OPEN  RPFUT2                  Open the PDS(E)
         ESTAE 0                       Kill ESTAE environment
*        LA    R1,INF001               Routine address
*        BSM   0,R1                    Switch to AMODE24
INF001   DS    0H
         READ  DECB,SF,RPFUT2,GETAREA,'S'
         CHECK DECB                    Wait for completion
         LA    R6,1(R6)                Count directory blk
         LA    R3,GETAREA              Directory block
         LR    R5,R3                   Same address into R5
         AH    R5,0(R3)                Add # of usable bytes
         BCTR  R5,0                    Minus 1 for BXLE
         LA    R3,2(R3)                Point to first member in blk
INF003   DS    0H
         CLI   0(R3),X'FF'             Last member?
         BE    INF007                  Yes: read empty dirblks to EOF
INF005   DS    0H
         LA    R7,1(R7)                Count member
         NI    11(R3),B'00011111'      Number of additional halfwords
         XR    R4,R4                   in 'TTRC' in directory
         IC    R4,11(R3)               and insert it into R3
         AR    R4,R4                   Double because halfwords
         AH    R4,=H'12'               + length (member+TTRC)
         BXLE  R3,R4,INF003            Next member if present
         B     INF001                  Read next directory block
INF007   DS    0H
         READ  DECB2,SF,RPFUT2,GETAREA,'S' Read empty directory blk
         CHECK DECB2                   Wait for completion
         LA    R2,1(R2)                Count free dirblk
         LA    R6,1(R6)                Count directory blk
         B     INF007                  Read until EOF
INF011   DS    0H
*        L     R1,=A(INF013+X'80000000') Pickup routine address
*        BSM   R0,R1                   Return to AMODE31
INF013   DS    0H
         CVD   R6,DWB                  # dirblks
         MVC   SCR3#DIR,=X'402020202120' Edit pattrn
         ED    SCR3#DIR,DWB+5
         CVD   R7,DWB                  # members
         MVC   SCR3#MBR,=X'402020202120' Edit pattrn
         ED    SCR3#MBR,DWB+5
         CVD   R2,DWB                  # free dirblks
         MVC   SCR3#FBL,=X'402020202120' Edit pattrn
         ED    SCR3#FBL,DWB+5
         CLOSE RPFUT2                  Close the file
         SYALLOC FREE=RPFUT2,MF=E      Free previous RPFUT2
         B     INF017
INF015   DS    0H
         CLOSE RPFUT2                  Close the file
         SYALLOC FREE=RPFUT2,MF=E      Free previous RPFUT2
         MVC   SCR3#DIR,=C'   ERR'     Error # dirblks
         MVC   SCR3#FBL,=C'   ERR'     Error # free dirblks
         MVC   SCR3#MBR,=C'   ERR'     Error # members
INF017   DS    0H
         TPUT  SCR3TXT,SCR3LEN,FULLSCR Write info screen
         TGET  TEMP,10,ASIS            Dummy tget
         LTR   R1,R1                   Input length is zero ?
         BZ    INF017                  Yes: redisplay
         CLI   TEMP,X'7D'              Enter key ?
         BE    INF019                  Yes: continue
         CLI   TEMP,X'F3'              PFK03     ?
         BE    INF019                  Yes: continue
         CLI   TEMP,X'C3'              PFK15     ?
         BNE   INF017                  NO: redisplay
INF019   DS    0H
         OI    SW,$ERASE+$CHANGE       Erase screen with next TPUT
         LM    R0,R14,SAVEROUT         Restore the registers
         XR    R15,R15                 RC = 0000
         BR    R14
         TITLE 'Compress dataset with IEBCOPY'
*---------------------------------------------------------------------*
*                                                                     *
*        COMPRESS    - Compress dataset with IEBCOPY                  *
*                      Allocate RPFIN, RPFUT1 and RPFPRINT,           *
*                      Invoke IEBCOPY and gives Return code           *
*                                                                     *
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
         SPACE 2
COMPRESS DS    0H
         STM   R0,R15,SAVEROUT         Save registers
         SYALLOC FREE=RPFPRINT,MF=E    Free previous RPFPRINT
         SYALLOC DDN=RPFPRINT,PRI=3,SEC=2,SPTYP=CYL,DSN=PRTDSN,        X
               UNITA=COMMSTOR,DISP1=MOD,MF=E,DISP2=DELETE
         LTR   R15,R15                 Alloc ok ?
         BNZ   COMPER1                 Error if not
         SYALLOC FREE=RPFUT1,MF=E      Free previous RPFUT1
         SYALLOC DDN=RPFUT1,VOL=VOL,DSN=DSN,DISP1=SHR,                 X
               UNITA=COMMSTOR,MF=E
         LTR   R15,R15                 Alloc ok ?
         BNZ   COMPER1                 Error
         SYALLOC FREE=RPFIN,MF=E       Free previous RPFIN
         SYALLOC DDN=RPFIN,UNITA=COMMSTOR,PRI=1,SEC=1,DSN=INDSN,       X
               SPTYP=TRK,DISP1=NEW,                                    X
               MF=E
         LTR   R15,R15                 Alloc ok ?
         BNZ   COMPER1                 Error
         OPEN  (SYSIN,(OUTPUT))        Control statement
         MVI   GETAREA,C' '            Blank
         MVC   GETAREA+1(L'GETAREA-1),GETAREA   area
         MVC   GETAREA(20),=C' C I=RPFUT1,O=RPFUT1' Compress statement
*        LA    R1,COMP001              Routine address
*        BSM   0,R1                    Exec this routine in amode24
COMP001  DS    0H
         PUT   SYSIN,GETAREA           Write statement
*        L     R1,=A(COMP003+X'80000000') routine address
*        BSM   0,R1                    Exec this routine in amode31
COMP003  DS    0H
         CLOSE SYSIN                   Close and
         FREEPOOL SYSIN                      free buffers
         OI    SW,$ABDALL              Intercept all abends
         MVC   ESTAERET,=A(COMPER2)    Move return pointer for ESTAE
         ST    R10,HEREADDR+8          Save reg.10
         ESTAE RECOVER,PARAM=(11)      Establish ESTAE routine
         LINK  EP=IEBCOPY,PARAM=(COPYPARM,COPYDDNS),VL=1
         CVD   R15,DWB                 RC IEBCOPY
         UNPK  COMPRC(4),DWB           Unpack
         OI    COMPRC+3,X'F0'          Remove sign
         MVI   OPER,C'Z'               Compressed
         ESTAE 0                       Kill ESTAE routine
         NI    SW,255-$ABDALL          Reset all abends flag
         B     COMP999
COMPER1  DS    0H                      Allocation error(s)
         MVI   OPER,X'EA'              Allocation error
         B     COMP999
COMPER2  DS    0H
         MVI   OPER,X'DF'              IEBCOPY abended
COMP999  DS    0H
         SYALLOC FREE=RPFIN,MF=E       Free
         SYALLOC FREE=RPFUT1,MF=E          the
         SYALLOC FREE=RPFPRINT,MF=E           datasets
         LM    R0,R14,SAVEROUT         Restore the registers
         BR    R14
         TITLE 'Process member selection list'
*---------------------------------------------------------------------*
*                                                                     *
*        MEMLIST     - Invoke the RPF PDS processor                   *
*                      Module RPFPDS will be linked                   *
*                                                                     *
*        INPUT:        Workspace entry containing the dataset to      *
*                      be browsed (in reg.10)                         *
*                      Reg.10 points to the second dataset line       *
*        OUTPUT:       None, Reg. 0-14 remains unchanged.             *
*                                                                     *
*        Returncodes:    0000 - Normal                                *
*                        0004 - Return or '=n.n' cmd entered in PDS   *
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
         SPACE 2
MEMLIST  DS    0H
         STM   R0,R15,SAVEROUT         Save registers
         MVC   SFIRSTSC,FIRSTSCR       Save FIRSTSCR
         MVC   ESTAERET,=A(LIST011)    Move return pointer for ESTAE
         OPEN  (VTOCDCB,(OUTPUT))      Open RPFVTC1 dataset
         L     R3,FIRSTREC             Firstrec pointer
         LA    R4,NEXTENT*2            Length of entry (2 workspc lnes)
         L     R5,LASTREC              LASTREC pointer
         LA    R5,1(R5)                Plus 1 for BXLE
LIST001  EQU   *
         MVC   GETAREA,0(R3)           Move record below 16 MB line
         LA    R15,WRITEIT             Write routine in 24 bit mode
*        BASSM R14,R15                 Write workspace record
         BALR  R14,R15                 Write workspace record
         BXLE  R3,R4,LIST001           Loop until all done
         CLOSE VTOCDCB                 Close temp dataset
         L     R15,EPDAIR              Addr of RPFDAIR routine
         CALL  (15),((11))             Branch to routine
         CLC   RTNCODE,=F'0'           RC=0000?
         BE    LIST003                 Yes: ok branch
         CLC   RTNCODE,=F'20'          RC=0020 (RECFM not F or V)
         BE    LIST003                 Yes: ok branch
         CLC   RTNCODE,=F'32'          RC=0032 (member not found)
         BNE   LIST011                 No: Error in Browse
LIST003  DS    0H
         ESTAE RECOVER,PARAM=(11)      Establish estae (for abend913)
         LINK  EP=RPFPDS,PARAM=((11))  Invoke RPFPDS
         ESTAE 0                       Delete ESTAE routine
         TM    INFCODE2,$RETURN        Return indicator set by PDS
         BO    LIST083                 End with RC=0004
         MVI   OPCDE,C'M'              Member selection list processed
         B     LIST085                 And branch
LIST011  DS    0H
         MVI   OPCDE,X'F1'             Error in member list
         B     LIST085                 And branch
LIST083  DS    0H
         LA    R15,4                   RC=0004
         B     BRO099                  Exit
LIST085  DS    0H
         MVC   VTOCDCB+DCBEODA-IHADCB(3),=AL3(LIST095)
         OPEN  VTOCDCB                 Open saved workspace (RPFVTC1)
         L     R10,FIRSTREC            Reload reg 10
LIST087  DS    0H
*        LA    R1,LIST089              Routine to run in 24 bit (XA)
*        BSM   R0,R1                   Exec this routine
LIST089  DS    0H
         GET   VTOCDCB,GETAREA         Read the file
*        L     R1,=A(LIST091+X'80000000') Routine address
*        BSM   R0,R1                   Goto 31 bit mode (MVS/XA)
LIST091  DS    0H
         C     R10,GETMEND             End of workspace ?
         BH    LIST095                 Terminate, but accept truncation
         MVC   0(2*NEXTENT,R10),GETAREA Move back to workspace
         CLC   DSN,DSNAME              Dataset edited reached ?
         BNE   LIST093                 No: branch
         MVC   OPER,OPCDE              Member selection list processed
LIST093  DS    0H
         LA    R10,2*NEXTENT(R10)      Next entry in workspace
         B     LIST087                 Read next record
LIST095  DS    0H
*        L     R1,=A(LIST096+X'80000000') routine address
*        BSM   R0,R1                   Goto 31 bit mode
LIST096  DS    0H
         SL    R10,RLENGTH             Minus 1 record in workspace
         ST    R10,LASTREC             And save in Last record
         CLOSE VTOCDCB                 Close the DCB
         FREEPOOL VTOCDCB              and free the buffers
         MVC   FIRSTSCR,SFIRSTSC       Restore FIRSTSCR
         XR    R15,R15                 RC=0000
LIST099  DS    0H
         LM    R0,R14,SAVEROUT         Restore the registers
         BR    R14
         TITLE 'BROWSE DATASETS'
*---------------------------------------------------------------------*
*                                                                     *
*        B R O W S E - Invoke the RPF browse processor                *
*                      Module RPFPDS will be linked if DSORG=PO or    *
*                      RPFBRO will be linked if DSORG=PS              *
*                                                                     *
*        INPUT:        Workspace entry containing the dataset to      *
*                      be browsed (in reg.10)                         *
*                      Reg.10 points to the second dataset line       *
*        OUTPUT:       NONE, ALL REGISTERS ARE UNCHANGED.             *
*                                                                     *
*        Returncodes:    0000 - Normal                                *
*                        0004 - Return or '=n.n' cmd entered in Browse*
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
         SPACE 2
BROWSE   DS    0H
         STM   R0,R15,SAVEROUT         Save registers
         MVC   SFIRSTSC,FIRSTSCR       Save FIRSTSCR
         MVC   ESTAERET,=A(BRO011)     Error routine if abend913
         OPEN  (VTOCDCB,(OUTPUT))      Open RPFVTC1 dataset
         L     R3,FIRSTREC             Firstrec pointer
         LA    R4,NEXTENT*2            Length of entry
         L     R5,LASTREC              LASTREC pointer
         LA    R5,1(R5)                Plus 1 for BXLE
BRO001   EQU   *
         MVC   GETAREA,0(R3)           Move record below 16 MB line
         LA    R15,WRITEIT             Write routine in 24 bit mode
*        BASSM R14,R15                 Write workspace record
         BALR  R14,R15                 Write workspace record
         BXLE  R3,R4,BRO001            Loop until all done
         CLOSE VTOCDCB                 Close temp dataset
         L     R15,EPDAIR              Addr of RPFDAIR routine
         CALL  (15),((11))             Branch to routine
         CLC   RTNCODE,=F'0'           RC=0000?
         BE    BRO003                  Yes: ok branch
         CLC   RTNCODE,=F'32'          RC=0032 (member not found)
         BNE   BRO011                  No: Error in Browse
BRO003   DS    0H
         CLC   DSORG(2),=C'PO'         Partitioned dataset ?
         BNE   BRO005                  No: branch
         OI    INFCODE3,$PDSBR         Perform browse only in RPFPDS
         LINK  EP=RPFPDS,PARAM=((11))  Invoke RPFPDS
         NI    INFCODE3,255-$PDSBR     Reset flag
         B     BRO007                  Test return indicator
BRO005   DS    0H
         MVC   CMDAREA(8),=CL8'RPFWORK' DDNAME to browse
         LINK  EP=RPFBRO,PARAM=((11))  Invoke browse processor
         CLC   RTNCODE,F0000           RC = 0000?                  @rp8
         BNE   BRO011                  No: inform user             @rp8
         MVC   CMDAREA,BLANKS          Blank area
BRO007   DS    0H
         TM    INFCODE2,$RETURN        Return indicator set by PDS
         BO    BRO083                  Yes: RC=0004, exit
         MVI   OPCDE,C'V'              Dataset Viewed
         B     BRO085                  And branch
BRO011   DS    0H
         MVI   OPCDE,X'F3'             Error in View
         B     BRO085                  And branch
BRO083   DS    0H
         LA    R15,4                   RC=0004
         B     BRO099                  Exit
BRO085   DS    0H
         MVC   VTOCDCB+DCBEODA-IHADCB(3),=AL3(BRO095)
         OPEN  VTOCDCB                 Open saved workspace
         L     R10,FIRSTREC            Relead reg.10
BRO087   EQU   *
*        LA    R1,BRO089               Routine to be run in 24 bit
*        BSM   R0,R1                   Exec this routine
BRO089   DS    0H
         GET   VTOCDCB,GETAREA         Read the file
*        L     R1,=A(BRO091+X'80000000') Routine address
*        BSM   R0,R1                   Goto 31 bit mode (MVS/XA)
BRO091   DS    0H
         C     R10,GETMEND             End of workspace ?
         BH    BRO095                  Terminate, but accept truncation
         MVC   0(2*NEXTENT,R10),GETAREA Move back to workspace
         CLC   DSN,DSNAME              Dataset edited reached ?
         BNE   BRO093                  No: branch
         MVC   OPER,OPCDE              Mark dataset Browsed
BRO093   DS    0H
         LA    R10,2*NEXTENT(R10)      Next entry in workspace
         B     BRO087
BRO095   DS    0H
*        L     R1,=A(BRO096+X'80000000') routine address
*        BSM   R0,R1                   Goto 31 bit mode
BRO096   DS    0H
         SL    R10,RLENGTH             Minus 1 record in workspace
         ST    R10,LASTREC             And save in Last record
         CLOSE VTOCDCB                 Close the DCB
         FREEPOOL VTOCDCB              and free the buffers
         MVC   FIRSTSCR,SFIRSTSC       Restore FIRSTSCR
         XR    R15,R15                 RC=0000
BRO099   DS    0H
         LM    R0,R14,SAVEROUT         Restore the registers
         BR    R14
         TITLE 'EDIT A DATA SET'
*---------------------------------------------------------------------*
*                                                                     *
*        E D I T   Edit the selected dataset                          *
*                  One dataset will be processed                      *
*                  IF DSORG=PO, RPFPDS will be invoked. If the        *
*                  DSORG=PS, RPFEDIT will be called.                  *
*                                                                     *
*                  The workspace will be first saved in dataset       *
*                  RPFVTC1.                                           *
*        NOTES     REG 10 points to the current dataset entry in      *
*                  the workspace                                      *
*                                                                     *
*        After completion of this routine, the operation codes will   *
*        be set in the dataset entry in the workspace (Field OPER)    *
*                                                                     *
*                                                                     *
*        Returncodes:    0000 - Normal                                *
*                        0004 - Return or '=n.n' cmd entered in EDIT  *
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
EDIT     DS    0H
         STM   R0,R15,SAVEROUT         Save all the registers
         MVC   SFIRSTSC,FIRSTSCR       Save FIRSTSCR
         MVC   ESTAERET,=A(EDIT79)     Error routine if abend913
         MVC   VTOCDCB+DCBEODA-IHADCB(3),=AL3(EDIT91)
         OPEN  (VTOCDCB,(OUTPUT))      Open RPFVTC1
         L     R3,FIRSTREC             Firstrec pointer
         LA    R4,NEXTENT*2            Length of entry
         L     R5,LASTREC              LASTREC pointer
         LA    R5,1(R5)                Plus 1 for BXLE
EDIT01   EQU   *
         MVC   GETAREA,0(R3)           Move record below 16 MB line
         LA    R15,WRITEIT             Write routine in 24 bit mode
*        BASSM R14,R15                 Write workspace record
         BALR  R14,R15                 Write workspace record
         BXLE  R3,R4,EDIT01            Loop until all done
         CLOSE VTOCDCB                 Close RPFVTC1
EDIT03   EQU   *
         XC    EPNEXT,EPNEXT           Init EPNEXT field
         MVC   APPEND(3),DEFAPP        Default append
         MVC   NUMBERED(3),DEFNUM      Default numbered
         MVC   FOLD(3),DEFOLD          Default Caps
         L     R15,EPDAIR              Addr of RPFDAIR routine
         CALL  (15),((11))             Branch to routine
         CLC   RTNCODE,=F'0'           RC=0000?
         BE    EDIT04                  Yes: ok branch
         CLC   RTNCODE,=F'32'          RC=0032 (member not found)
         BNE   EDIT79                  No: Error in edit
EDIT04   DS    0H
         XC    RTNCODE,RTNCODE         Clear Return code
         CLC   DSORG(2),=C'PO'         Partitioned dataset ?
         BNE   EDIT05                  No: branch
         OI    INFCODE3,$PDSEDIT       Edit only in RPFPDS
         LINK  EP=RPFPDS,PARAM=((11))  Invoke RPFPDS
         NI    INFCODE3,255-$PDSEDIT   Reset flag
         TM    INFCODE2,$RETURN        Return indicator set by PDS
         BO    EDIT99B                 End with RC=0004
         MVI   OPCDE,C'E'              Dataset edited
         B     EDIT81                  And branch
EDIT05   DS    0H
         ESTAE RECOVER,PARAM=(11)      Protect against abend913
         L     R15,EPEDIT              Entry point RPFEDIT
         CALL  (15),((11))             Branch to editor
         ESTAE 0                       Remove protection
         CLC   RTNCODE,F0020           Dataset/member in use?
         BNE   EDIT07
         MVI   OPCDE,X'F6'             Dataset/Member in use
         B     EDIT81                  Branch
EDIT07   DS    0H
         XC    RTNCODE,RTNCODE         Clear return code
         MVI   OPCDE,C'E'              Edited
         CLC   EPNEXT,F0000            EPNEXT fld ? (RPFSAVE)
         BNE   EDIT09                  Yes: Save data with RPFSAVE
         MVC   CMDAREA,BLANKS          Blank 'CMDAREA'
         B     EDIT11                  Skip save
EDIT09   EQU   *
         L     R15,EPNEXT              PICKUP EPNEXT FIELD
         XC    EPNEXT,EPNEXT           Clear EPNEXT field
         CALL  (15),((11))             Branch to routine
*                            RPFSAVE completed
EDIT11   DS    0H
         TM    INFCODE2,$RETURN       Return indicator set by edit
         BO    EDIT99B                 End with RC=0004
         B     EDIT81                  Restore workspace
EDIT79   DS    0H                      Error in edit
         MVI   OPCDE,X'F5'             Move error code
         XC    RTNCODE,RTNCODE         Clear returncode of RPFDAIR
EDIT81   DS    0H
         L     R3,GETMSTRT             Pickup workspace begin
         ST    R3,FIRSTREC             Save in 'FIRSTREC'
         S     R3,RLENGTH              Minus 88
         ST    R3,LASTREC              Save in 'LASTREC'
         L     R10,FIRSTREC            Reload reg 10
         OPEN  VTOCDCB                 Open RPFVTC1
EDIT83   EQU   *
*        LA    R1,EDIT85               Routine to be run in 24 bit
*        BSM   R0,R1                   Exec this routine
EDIT85   DS    0H
         GET   VTOCDCB,GETAREA         Read the file
*        L     R1,=A(EDIT87+X'80000000') routine address
*        BSM   R0,R1                   Goto 31 bit mode (MVS/XA)
EDIT87   DS    0H
         C     R10,GETMEND             End of workspace ?
         BH    EDIT91                  Terminate, but accept truncation
         MVC   0(2*NEXTENT,R10),GETAREA Move back to workspace
         CLC   DSN,DSNAME              Dataset edited reached ?
         BNE   EDIT89                  NO: BRANCH
         MVC   OPER,OPCDE              Mark dataset edited
EDIT89   DS    0H
         LA    R10,2*NEXTENT(R10)      Next entry in workspace
         B     EDIT83
EDIT91   DS    0H
*        L     R1,=A(EDIT92+X'80000000') routine address
*        BSM   R0,R1                   Goto 31 bit mode
EDIT92   DS    0H
         SL    R10,RLENGTH             Minus 1 record in workspace
         ST    R10,LASTREC             And save in Last record
         CLOSE VTOCDCB                 Close the DCB
         FREEPOOL VTOCDCB               And free the buffers
EDIT99   EQU   *
         MVC   FIRSTSCR,SFIRSTSC       Restore FIRSTSCR
         B     EDIT99C
EDIT99B  DS    0H
         LA    R15,4                   RC=0004
         B     EDIT99D                 Return
EDIT99C  DS    0H
         XR    R15,R15                 RC=0000
EDIT99D  DS    0H
         LM    R0,R14,SAVEROUT         Restore the registers
         BR    R14                     Return to caller
OPCDE    DS    C                       To contain error code
         TITLE 'WRITE MEMBER LIST ON TEMPORARY FILE'
*---------------------------------------------------------------------*
*                                                                     *
*        WRITEIT: ROUTINE TO SAVE THE WORKSPACE RECORDS               *
*                 THIS ROUTINE WILL BE ENTERED IN 24 BIT MODE         *
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
WRITEIT  DS    0H
         ST    R14,SAVE14WR            Save register 14
         PUT   VTOCDCB,GETAREA         Save workspace record
         L     R14,SAVE14WR            Restore reg 14
         BR    R14                     Return
*        BSM   0,R14                   Return to 31 bit caller
         TITLE 'CONVERT SCREEN ROUTINE'
*---------------------------------------------------------------------*
*                                                                     *
*        C O N V I N:        Convert a TGET ASIS screen to normal     *
*        The SBA's and attribute bytes will be removed and the        *
*        rest will be placed in the area point to by                  *
*        'SCRAREA'. The aidbyte will be placed in AIDBYTE             *
*        The length of the output will be placed in register 1        *
*        Register 2 points to the AIDbyte.                            *
*        Register 5 points to the output screen area                  *
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
         SPACE 2
CONVIN   EQU   *                       CONVERT SCREEN ROUTINE
         ST    R14,CONVR14             SAVE R14
         ST    R5,SAVER5               Save Reg.5
         L     R3,COMMSCR              Screen address from COMMAREA
         CNOP  0,4                     Align on fullword
         TGET  (R3),2800,ASIS          Read the screen
         MVC   0(1,R2),0(R3)           SAVE AIDBYTE
         XR    R2,R2                   Init output length
         LR    R4,R1                   SAVE INPUT LENGTH
         SH    R4,=H'3'                Skip AID and cursor address
         C     R4,F0000                REGISTER ZERO
         BNH   CONV099                 NOT HIGH: END OF ROUTINE
         LA    R3,3(R3)
*        OUTPUT SCREEN IS POINTED BY R5
CONV001  CLI   0(R3),X'11'             Set buffer address (SBA)?
         BE    CONV003                 Yes: skip next 3 bytes input
         MVC   0(1,R5),0(R3)           Move input byte
         LA    R3,1(R3)                Next byte input
         LA    R2,1(R2)                Count output bytes
         LA    R5,1(R5)                Next byte output
         BCT   R4,CONV001              Test next byte
         B     CONV099                 End of routine
CONV003  LA    3,3(3)                  Skip 3 bytes input
         BCTR  4,0                     Minus 1 bct
         BCTR  4,0                     Minus 1 bct
         BCT   4,CONV001               Test next byte
CONV099  LR    R1,R2                   Output length in register 1
         L     R14,CONVR14             Restore r14
         L     R5,SAVER5               Restore reg.5
         BR    R14                     Return to caller
         TITLE 'P F K  SEARCH ROUTINE'
*---------------------------------------------------------------------*
*                                                                     *
*        P F K    ROUTINE TO CHECK THE AIDBYTE WITH THE PFK DEFINITION*
*                 IN COMMAREA IF THE PFK IS FOUND THE COMMAND         *
*                 IN THE PFK WILL BE PLACED INTO THE FIELD POINTED    *
*                 BY REGISTER 2                                       *
*        REGISTER 1 POINTS TO THE AIDBYTE.                            *
*        REGISTER 2 POINTS TO THE COMMAND AREA OF THE SCREEN          *
*                                                                     *
*        RETURNCODES: 00 PFK FOUND CONFIRMATION IS NO                 *
*                     04 PFK FOUND CONFIRMATION IS YES                *
*                     08 PFK NOT DEFINED OR NOT FOUND                 *
*                     12 Invalid PFK definition                       *
*                                                                     *
*------------------------------------------ (C)-2002 SKYBIRD SYSTEMS -*
         SPACE 2
PFK      ST    R14,PFKR14
         ST    R5,SAVER5               Save A(screen area)
         LA    R4,DEFPFK01
         LA    R5,24
PFK01    CLC   0(1,R4),0(R1)           PFK found ?
         BE    PFK05                   Yes: process
         LA    R4,26(R4)               Next PFK
         BCT   R5,PFK01                Loop until found
         LA    R15,8
         B     PFK99
PFK05    CLI   1(R4),C'X'              Undefined?
         BNE   PFK06
         LA    R15,8
         B     PFK99
PFK06    DS    0H                      Clear input command field
         MVC   0(24,R2),BLANKS         Clear output command field
         L     R1,COMMSCR              Pickup unformatted screen
         CLI   6(R1),X'94'             Max operand (scroll) ?
         BE    PFK06A                  Yes: branch
         CLI   6(R1),C'M'              Max operand (scroll) ?
         BNE   PFK08                   No: ignore
PFK06A   EQU   *
         CLC   2(3,R4),=C'SCF'         Scroll foreward ?
         BNE   PFK07                   No: try SCB
         MVC   0(6,R2),=C'BOTTOM'      Yes: BOTTOM command
         B     PFK99
PFK07    CLC   2(3,R4),=C'SCB'         Scroll backward ?
         BNE   PFK08                   No: ignore
         MVC   0(3,R2),=C'TOP'         Yes: TOP command
         B     PFK99
PFK08    DS    0H                      Move PFK command into REP AREA
         MVC   0(24,R2),2(R4)          Move PFK command into BUFFER
         CLI   1(R4),C'N'
         BNE   PFK09
         LA    R15,0
         B     PFK99
PFK09    CLI   1(R4),C'Y'
         BNE   PFK11
         LA    R15,4
         B     PFK99
PFK11    LA    R15,12
         B     PFK99
PFK99    L     R14,PFKR14
         L     R5,SAVER5               Restore A(screen area)
         BR    R14
         TITLE 'RETURN TO CALLER'
RETURN   DS    0H
         L     R2,SCRAREA              Address of 4096 byte screenarea
         FREEMAIN RU,LV=4096,A=(2)     Get rid of this area
         L     R13,SAVE+4              Previous save area
         RETURN (14,12),RC=0           Back to caller
         TITLE 'Locate on Dataset name'
*---------------------------------------------------------------------*
*                                                                     *
*        Routine:   LOCATE                                            *
*        Function:  Locate on dataset name                            *
*                   The to be located string is in 'SCREEN'+2         *
*                   After successful locate, R10 will be loaded with  *
*                   the workspace entry that matches the string       *
*                   The screen area is pointed to by R1               *
*        Returncodes:                                                 *
*                   0000: located string found, R10 loaded            *
*                         R1=0000 = exact match on dataset name       *
*                         R1=0004 = fuzzy match                       *
*                   0004: located string not found                    *
*                                                                     *
*------------------------------------------ (C)-2001 SKYBIRD SYSTEMS -*
         SPACE
LOCATE   DS    0H
         ST    R14,SAVE14L             SAVE RETURN REG
         ST    R5,SAVER5               Save Reg.5
         LA    R3,2(R1)                LOAD LOCATE CHAR STRING
         LA    R4,16                   MAX 16 CHARS
         XR    R5,R5                   COUNT LENGTH IN GR 5
LOC001   DS    0H
         CLI   0(R3),C' '              END OF STRING ?
         BE    LOC003                  YES: STOP SEARCH
         LA    R5,1(R5)                COUNT NON BLANK BYTE
         LA    R3,1(R3)                NEXT BYTE IN 'SCREEN'
         BCT   R4,LOC001               LOOP.
LOC003   DS    0H
         LTR   R5,R5                   NO BYTES FOUND ?
         BE    LOCERR1                 YES: RC=0004
         BCTR  R5,0                    SUBTRACT 1 FOR EX INSTRUCTION
         L     R10,GETMSTRT            Load start address    (BXLE)
         L     R7,LASTREC              LAST RECORD POINTER   (BXLE)
         LA    R7,160(R7)              ADJUST OFFSET         (BXLE)
         LA    R6,NEXTENT*2            LENGTH OF ENTRY       (BXLE)
         LA    R3,2(R1)                ADDRESS CHAR STRING
LOC005   DS    0H
*
COMPM    DS    0H                      Compare data set name
         CH    R5,=H'25'               Not longer than 26
         BNH   *+8                     No: branch
         LA    R5,26                   Load max value (8-1 for ex)
         EX    R5,LCOMP1               Compare if match found
         BL    COMPBX                  LOW: LOOP
         BE    LOC097                  Exact match
         B     LOC098                  High or equal: Found
*
COMPBX   DS    0H                      COMPARE dataset name
         BXLE  R10,R6,LOC005           LOOP                  (BXLE)
LOCERR1  DS    0H                      COMPARE dataset name
         LA    R15,4                   RC=0004
         B     LOC999                  Branch
LOC097   DS    0H                      LOCATE THE FOUND dataset
         XR    R1,R1                   Exact match
         B     LOC099
LOC098   DS    0H                      LOCATE THE FOUND dataset
         LA    R1,4                    Fuzzy match
LOC099   DS    0H
         XR    R15,R15                 RC=0000
         ST    R10,FIRSTSCR            Adress found dataset
         B     LOC999
LCOMP1   CLC   DSN(0),0(R3)            **** EX ONLY ****
LOC999   DS    0H
         L     R5,SAVER5               Restore Reg.5 (screen area)
         L     R14,SAVE14L             Restore return reg
         BR    R14                     Return
         TITLE 'ESTAE recovery routine, Retry From abends'
RECOVER  EQU   *
         DROP  ,
         USING *,R15                   Get temporary addressability
         LM    R8,R13,HEREADDR         Restore registers
         DROP  R15                     Kill local addressability
         USING SAVE,R13,R12,R9,R8      Restore addressing
         USING SDWA,R1                 Get addressability over sdwa
         TM    SW,$ABDALL              Recover from all IEBCOPY abends?
         BO    SETRP04                 Try to retry
         XR    R2,R2                   Clear register 2
         ICM   R2,7,SDWACMPC           Insert completion code
         SRL   R2,12                   Shift out user completion codep1
         CLM   R2,3,=X'0913'           Authorization abend ?
         BE    SETRP04                 Yes: try to retry
         SETRP RC=0                    Else percolate
         BR    R14                     Branch to rtm
         DROP  R1                      Kill addressability
SETRP04  DS    0H
         SETRP RC=4,RETADDR=RET,RETREGS=NO,FRESDWA=YES
         BR    R14                     Branch to rtm
         TITLE 'ESTAE RETRY ROUTINE'
RET      EQU   *
         DROP  ,
         USING *,R15                   Local addr
         LM    R8,R13,HEREADDR         Restore registers
         DROP  R15                     Kill local addressability
         USING SAVE,R13,R12,R9,R8      Restore addressing
         USING COMMAREA,R11
         ESTAE 0                       Delete this recovery routine
         NI    SW,255-$ABDALL          Reset all abends flag
         L     R14,ESTAERET            Return address
         BR    R14                     Retry successful
         TITLE 'VARIABLES AND CONTROL BLOCKS'
         DS    0F
LOC      CAMLST NAME,LOCDSN,,LOCAREA
         DS    0D
LOCAREA  DS    CL2
LOCTYPE  DS    CL4                     Device type
LOCVOL   DS    CL6                     Volume
         DS    CL253                   Rest of LOCAREA
LOCDSN   DS    CL44
*                                                                  @rp7
SCR4BFR  DS    0CL50                   Output area RENAME screen   @rp7
BUF4AID  DS    C                                                   @rp7
BUF4CSR  DS    CL2                                                 @rp7
BUF4SBA  DS    CL3                                                 @rp7
NEWNAME  DS    CL44                                                @rp7
*                                                                  @rp7
NEWCAT   CAMLST CAT,LOCDSN,,CATVOL                                 @rp2
         DS    0D                                                  @rp2
CATVOL   DC    H'1'                                                @rp2
VOLTYP   DS    CL4                     UCBTYPE field               @rp2
CATVOL1  DS    CL6                                                 @rp2
UNCAT    CAMLST UCATDX,LOCDSN          UNCATALOG ENTRY             @rp2
SCRATCH  CAMLST SCRATCH,LOCDSN,,CATVOL,,OVRD                       @rp2
RENAMELS CAMLST RENAME,LOCDSN,NEWNAME,CATVOL                       @rp7
*
TEMP     DS    CL16
DWB      DS    D                       FOR CVD/CVB INSTRUCTIONS
SAVEROUT DS    16F
HEREADDR DS    6F
ESTAERET DS    F
SFIRSTSC DS    F
PUTR14   DS    F
SAVE14L  DS    F
GETR14   DS    F
SCRLEN   DS    F
CONVR14  DS    F
SAVE14WR DS    F
PFKR14   DS    F
SAVER5   DS    F
F0000    DC    F'0'
F0004    DC    F'4'
F0008    DC    F'8'
F0020    DC    F'20'
F591     DC    F'591'                  LENGTH OF 'SCREEN' 3278 MOD 2
F807     DC    F'807'                  LENGTH OF 'SCREEN' 3278 MOD 3
F1104    DC    F'1104'                 LENGTH OF 'SCREEN' 3278 MOD 4
NEXTSCR  DS    F                       21/29/40 * NEXTENT
#LINES   DS    F                       CONTAINS 21/29/40
TPUTLEN  DS    F                       Length of screen to be written
SCRAREA  DS    F                       Adress of 4096 byte screenarea
*
RLENGTH  DC    A(NEXTENT)
*
GETAREA  DS    CL256                                               @rp5
BUFFER   DS    CL24
AIDBYTE  DS    C
#DS      DS    PL3                     # of datasets               @rp9
*
SW       DC    X'00'
$CHANGE  EQU   128                     Changes in screen
$ERASE   EQU   64                      Screen should be erased
$RESHOW  EQU   32                      Rewrite screen if on
$ABDALL  EQU   16                      Suppress all abends if on   @rp3
$CATLGD  EQU   8                       Dataset not catlg in rename @rp7
$LEVEL   EQU   4                       CMDAREA contains LEVEL=     @rp9
         LTORG
         PRINT NOGEN                   SUPPRESS PRINT FOR DCB
VTOCDCB  DCB   DSORG=PS,DDNAME=RPFVTC1,MACRF=(GM,PM),EODAD=EDIT91,     X
               RECFM=FB,LRECL=176,BLKSIZE=8800
SYSIN    DCB   DSORG=PS,DDNAME=RPFIN,MACRF=PM,                         X
               RECFM=F,LRECL=80,BLKSIZE=80
RPFUT2   DCB   DSORG=PS,DDNAME=RPFUT2,MACRF=R,RECFM=F,EODAD=INF011,    X
               BLKSIZE=256
         PRINT GEN
INDSN    DC    CL44'&&RPFIN'                                       @rp3
PRTDSN   DC    CL44'&&RPFPRINT'                                    @rp3
         TITLE 'SCREEN FORMATS'
NOERASE  DC    X'27F1'
ERASE    DC    X'27F5C01140401D40'
SCR5TXT  DC    X'27F1C21140401DF8'
SCR5VAR  DC    CL23'Datasets starting with '                       @rp8
SCR5VOL  DS    CL17                                                @rpa
SCR5#    DS    CL6                                                 @rp9
         DC    CL11'-----------'                                   @rpa
         DC    X'11407A'           R01,C59
MSG5     DC    CL22' '
         DC    X'11C150',X'1DF8',C'Cmd =>',X'1DC113'
REP5     DC    CL24' ',X'1DF0'
         DC    X'11C25F',X'1DF0'   R02,C80
         DC    AL3(YELLOW)
         DC    C' C Dataset name               '
SCR5HDR  DC    C'Cre.dt Ref.dt Org  RECFM RECL   BLK Ext  Size Free' p9
         DC    X'11C3F0'
         DC    AL3(GREEN)
LNE5     DC    X'1DC9',X'11C3F1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
#SCRENT  EQU   *-LNE5                  Length of entry
         DC    X'1DC9',X'11C5C1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'11C6D1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'11C761',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'11C8F1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'114AC1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'114BD1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'114C61',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'114DF1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'114FC1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'1150D1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'11D161',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'11D2F1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'11D4C1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'11D5D1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'11D661',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'11D7F1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'11D9C1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'115AD1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'115B61',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'115CF1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
SCR524   EQU   *-SCR5TXT               3278/3279 MODEL 3 SECTION
         DC    X'1DC9'
         DC    X'115EC1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'115FD1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'116061',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'1161F1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'11E3C1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'11E4D1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'11E561',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'11E6F1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(GREEN),CL50' ',AL3(GREEN)
SCR532   EQU   *-SCR5TXT          3278/3279 MODEL 4 SECTION
         DC    X'1DC9'
         DC    X'11E8C1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(TURQ),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'11E9D1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(TURQ),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'116A61',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(TURQ),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'116BF1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(TURQ),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'116DC1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(TURQ),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'116ED1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(TURQ),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'116F61',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(TURQ),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'11F0F1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(TURQ),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'11F2C1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(TURQ),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'11F3D1',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(TURQ),CL50' ',AL3(GREEN)
         DC    X'1DC9',X'11F461',CL1' ',X'1DF1',CL26' '
         DC    X'1DF0',AL3(TURQ),CL50' ',AL3(GREEN)
SCR543   EQU   *-SCR5TXT
*
SCR2TXT  DC    X'27F5C01140401DF8'
         DC    C'Confirm DELETE of data set '
SCR2DSN  DS    CL44
         DC    X'11C1D113'             R02,C02
         DC    X'11C3F0',X'1DF0'       R04,C01
         DC    C'Hit  ',X'1DF8',C'PF12/PF24',X'1DF0',C'To delete'
         DC    X'11C540',X'1DF0'       R05,C01
         DC    C'Press',X'1DF8',C'PF03/PF15',X'1DF0',C'To cancel'
SCR2LEN  EQU   *-SCR2TXT
*
SCR3TXT  DC    X'27F5C01140401DF0',AL3(YELLOW)
         DC    C'                          Data set information'
         DC    X'11C150',AL3(WHITE),C' Null ==>',X'1DC113'
REP3     DC    C' ',X'1DF0'
         DC    X'11C3F3',AL3(TURQ),C'Data set name'  R04,C04
         DC    X'11C4D2',AL3(GREEN)    R04,C35
SCR3DSN  DS    CL44
         DC    X'11C5C3',AL3(TURQ),C'Creation date'  R05,C04
         DC    X'11C5E2',AL3(GREEN)    R05,C35
SCR3CRE  DS    CL12                    Form: Oct, 12 2002          @rp6
         DC    X'11C6D3',AL3(TURQ),C'Last reference date'  R06,C04
         DC    X'11C6F2',AL3(GREEN)    R06,C35
SCR3REF  DS    CL12                                                @rp6
         DC    X'11C7E3',AL3(TURQ),C'Dataset organisation'  R07,C04
         DC    X'11C8C2',AL3(GREEN)    R07,C35
SCR3ORG  DS    CL4
         DC    X'11C8F3',AL3(TURQ),C'Record format      '   R08,C04
         DC    X'11C9D2',AL3(GREEN)    R08,C35
SCR3REC  DS    CL4
         DC    X'114AC3',AL3(TURQ),C'Logical record length' R09,C04
         DC    X'114AE2',AL3(GREEN)    R09,C35
SCR3RECL DS    CL6
         DC    X'114BD3',AL3(TURQ),C'Block size'            R10,C04
         DC    X'114BF2',AL3(GREEN)    R10,C35
SCR3BLK  DS    CL6
         DC    X'114DF3',AL3(TURQ),C'Allocated tracks'      R12,C04
         DC    X'114ED2',AL3(GREEN)    R12,C35
SCR3TRK  DS    CL6
         DC    X'114E61',AL3(TURQ),C'Volume         '       R12,C50
         DC    AL3(GREEN)
SCR3VOL  DS    CL6
         DC    X'114FC3',AL3(TURQ),C'Free tracks'           R13,C04
         DC    X'114FE2',AL3(GREEN)    R13,C35
SCR3FREE DS    CL6
         DC    X'114FF1',AL3(TURQ),C'Device type    ' R13,C50
         DC    AL3(GREEN)
SCR3DEVT DS    CL6
         DC    X'1150D3',AL3(TURQ),C'Number of extents'     R14,C04
         DC    X'1150F4',AL3(GREEN)    R14,C37
SCR3EXT  DS    CL4
         DC    X'11D1E3',AL3(TURQ),C'Number of directory blocks' 15,4
         DC    X'11D2C2',AL3(GREEN)    R15,C35
SCR3#DIR DS    CL6
         DC    X'11D2F3',AL3(TURQ),C'Number of free dir. blocks' 16,4
         DC    X'11D3D2',AL3(GREEN)    R16,C35
SCR3#FBL DS    CL6
         DC    X'11D4C3',AL3(TURQ),C'Number of members'     R17,C04
         DC    X'11D4E2',AL3(GREEN)    R17,C35
SCR3#MBR DS    CL6
         DC    X'115CF3',AL3(TURQ)     R24,C04
         DC    C'Hit '
         DC    AL3(WHITE),C'ENTER',AL3(TURQ),C' to return'
SCR3LEN  EQU   *-SCR3TXT
SCR4TXT  DC    X'27F5C01140401DF0'                                 @rp7
         DC    C'                          Rename data set'        @rp7
         DC    X'11C3F3',C'Old data set name'  R04,C04             @rp7
         DC    X'11C4D2'               R04,C35                     @rp7
SCR4DSN  DS    CL44                                                @rp7
         DC    X'11C5C3',C'        on volume'  R05,C04             @rp7
         DC    X'11C5E2'               R05,C35                     @rp7
SCR4VOL  DS    CL6                                                 @rp7
         DC    X'11C7E3',C'New data set name'  R07,C04             @rp7
         DC    X'11C8C11DC913'         R07,C34                     @rp7
SCR4NEW  DC    CL44' ',X'1DF0'                                     @rp7
         DC    X'114BD21DF8'           R10,C03                     @rp7
SCR4MSG  DC    CL22' ',X'1DF0'                                     @rp7
         DC    X'115CF3'               R24,C04                     @rp7
         DC    C'Hit'                                              @rp7
         DC    X'1DF8',C'ENTER',X'1DF0',C'to rename or'            @rp7
         DC    X'1DF8',C'PFK03/PFK15',X'1DF0',C'to return'         @rp7
SCR4LEN  EQU   *-SCR4TXT                                           @rp7
INF1TXT  DC    CL22' Hit Enter to exec cmd'
ERR1TXT  DC    CL22'- Scroll had no effect'
ERR3TXT  DC    CL22'------ Invalid command'
ERR4TXT  DC    CL22' Logic error, Rc= 0601'
ERR9TXT  DC    CL22'------ PFK not defined'
ERRATXT  DC    CL22'PFK definition invalid'
ERRCTXT  DC    CL22'------- Text not found'
*        THE NEXT TABLE IS BUILT AS FOLLOWS
*        1. THE OPERATION CODE TO BE TESTED
*        2. THE ASSOCIATE MESSAGE
*        3. THE REPLACEMENT OPERATION CODE
MSGTBLE  DC    C'E',CL030'Edited                        ',C' '
         DC    C'V',CL030'Viewed                        ',C' '
         DC    C'M',CL030'Member list                   ',C' '
         DC    C'D',CL030'Deleted                       ',C'D'
         DC    C'C',CL030'Catalogued                    ',C' '     @rp2
         DC    C'U',CL030'Uncatalogued                  ',C' '     @rp2
COMPMSG  DC    C'Z',CL030'Compress RC=xxxx              ',C' '     @rp3
COMPRC   EQU   COMPMSG+13
         DC    C'I',CL030'Info - I                      ',C' '     @rp5
         DC    C'R',CL030'Renamed                       ',C' '     @rp7
         DC    X'FF',CL30'Scratch error                 ',C' '     @rp2
         DC    X'FE',CL30'Delete rejected by user       ',C' '
         DC    X'FC',CL30'View not available            ',C' '
         DC    X'FB',CL30'Dataset already catalogued    ',C' '     @rp2
         DC    X'FA',CL30'Error in catalog processing   ',C' '     @rp2
         DC    X'F9',CL30'Delete rejected by user       ',C' '
         DC    X'F8',CL30'Edit not available            ',C' '
         DC    X'F7',CL30'No temporary dataset          ',C' '
         DC    X'F6',CL30'Dataset/Member in use         ',C' '
         DC    X'F5',CL30'Error in EDIT                 ',C' '
         DC    X'F4',CL30'View not available            ',C' '
         DC    X'F3',CL30'Error in view                 ',C' '
         DC    X'F2',CL30'Member list not available     ',C' '
         DC    X'F1',CL30'Error processing member list  ',C' '
         DC    X'F0',CL30'Use IDCAMS delete for VSAM    ',C' '
         DC    X'EF',CL30'Dataset not catalogued        ',C' '     @rp2
         DC    X'EE',CL30'VSAM dataset                  ',C' '     @rp2
         DC    X'ED',CL30'Error uncatalog processing    ',C' '     @rp2
         DC    X'EC',CL30'Temporary dataset             ',C' '     @rp2
         DC    X'EB',CL30'Compress not available        ',C' '     @rp3
         DC    X'EA',CL30'DYNALLOC error                ',C' '     @rp3
         DC    X'DF',CL30'IEBCOPY abended               ',C' '     @rp3
         DC    X'DE',CL30'Error in RENAME               ',C' '     @rp7
         DC    X'DD',CL30'Tape data set                 ',C' '     @rp8
         DC    X'DC',CL30'Multivolume data set          ',C' '     @rp8
         DC    X'AA',CL30'Select B,C,D,E,I,M,R,U,V or Z ',C' '     @rp7
MSG#     EQU   (*-MSGTBLE)/32
NONHL    EQU   X'F0'                   NON HIGHLIGHT NO MDT ON
HL       EQU   X'F8'                   HIGHLIGHT NO MDT ON
NONHLM   EQU   X'F1'                   NON HIGHLIGHT MDT ON
HLM      EQU   X'F9'                   HIGHLIGHT MDT ON
*
D3350    EQU   X'0B'                   Indicates type 3350.
D2314    EQU   X'08'                   Indicates type 2314.
D3330    EQU   X'09'                   Indicates type 3330.
D3331    EQU   X'0D'                   Indicates type 3330-1.
D3380    EQU   X'0E'                   Indicates type 3380.
D3375    EQU   X'0C'                   Indicates type 3375.
D3390    EQU   X'0F'                   Indicates type 3390.
*
DDMMYY   DS    0CL49                   Output field from CONVDATE  @rp6
DAYNBR   DC    CL2' '                  Daynbr in the week - 01=SUN @rp6
DAYNM#E  DS    CL9                     Dayname in English          @rp6
DAYNM#D  DS    CL9                     Dayname in Dutch            @rp6
         DS    CL1                                                 @rp6
DD       DS    CL2                                                 @rp6
         DS    CL1                                                 @rp6
MONNBR   DS    CL2                     Monthnbr in year - 01=Jan   @rp6
MNAME#E  DS    CL9                     Monthname in English        @rp6
MNAME#D  DS    CL9                     Monthname in Dutch          @rp6
         DS    C                                                   @rp6
CC       DS    CL2                     Century                     @rp6
YY       DS    CL2                     Year                        @rp6
*                                                                  @rp6
YYDDD    DS    PL3                     Julian date (packed)        @rp6
*                                                                  @rp6
         DS    0F                                                  @rp3
COPYPARM DC    AL2(0)                   First parm of IEBCOPY      @rp3
COPYDDNS DS    0F
         DC    AL2(STOPC-STARTC)
STARTC   EQU   *
         DC    XL8'00'                  Empty entry (SYSLIN)
         DC    XL8'00'                  EMPTY ENTRY (SYSLMOD MBR)
         DC    XL8'00'                  EMPTY ENTRY (SYSLMOD)
         DC    XL8'00'                  Empty entry (SYSLIB)
         DC    CL8'RPFIN'               SYSIN
         DC    CL8'RPFPRINT'            SYSPRINT
         DC    XL8'00'                  Empty entry (SYSPUNCH)
         DC    CL8'RPFUT1'              SYSUT1
         DC    XL8'00'                  Empty entry (SYSUT2)
         DC    XL8'00'                  Empty entry (SYSUT3)
         DC    XL8'00'                  Empty entry (SYSGO)
         DC    XL8'00'                  Empty entry (SYSTERM)
STOPC    EQU   *
         SYALLOC MF=L                                              @rp3
         TITLE 'DSECTS USED BY RPFVTOC1'
*
*        This dsect contains two lines in the workspace
*        The first line contains the dataset name, start address
*        and operation code
*        The second line contains the dataset characteristics
*        This is the same layout as VTOC with PARM=LONG.
*
*
VTOCAREA DSECT                         Entry IN workspace
PREFIX   DS    CL8                     contains linenumber and flags
DSN      DS    CL44                    Dataset name
         DS    C
STARTADR DS    CL16                    Start address
MULT     DS    C                       Contains a '+' if multivolume
VOL      DS    CL6                     Volume
         DS    C
TYPE     DS    CL4                     UCB type
         DS    C
SMS      DS    CL3                     SMS  (YES or NO)            @rp7
OPER     DS    C                       OPER COULD BE
*                                      C'D' = SUCCESSFUL DELETE
*                                      C'E' = SUCCESSFUL Edit
*                                      C'V' = Successful Browse
*                                      C'C' = Successful Catalog   @rp2
*                                      C'U' = Successful Uncatalog @rp2
*                                      C'Z' = Successful Compress  @rp3
*                                      C'I' = Dataset information  @rp5
*                                      C'R' = Successful rename    @rp7
*                                      X'FF'= Scratch error        @rp2
*                                      X'FE'= Delete rejected
*                                      X'FC'= BROWSE NOT AVAILABLE
*                                      X'FB'= Error in catalog     @rp2
*                                      X'FA'= Already catalogued   @rp2
*                                      X'F9'= DELETE not confirmed
*                                      X'F8'= EDIT not available
*                                      X'F7'= NO TEMP. SAVE DS
*                                      X'F6'= Dataset/member in use
*                                      X'F5'= Error in Edit
*                                      X'F4'= View not available
*                                      X'F3'= Error in view
*                                      X'F2'= Member list not avail.
*                                      X'F1'= Error processing memlist.
*                                      X'F0'= Use IDCAMS delete
*                                      X'EF'= Not catalogued
*                                      X'EE'= VSAM dataset         @rp2
*                                      X'ED'= Uncatalog error      @rp2
*                                      X'EC'= Temporary dataset    @rp2
*                                      X'EB'= Compress N/A         @rp3
*                                      X'EA'= Allocation error     @rp3
*                                      X'DF'= IEBCOPY abended      @rp3
*                                      X'DE'= Error in RENAME      @rp7
*                                      X'DD'= Tape data set        @rp8
*                                      X'DC'= Multivolume data set @rp8
*                                      X'AA'= INVALID OPERATION
*
         DS    CL2
PREFIX2  DS    CL8                     Characteristics line
NODSN    DS    CL27                    Filler
CREAT    DS    CL6                     data set creation date
         DS    C
REFDT    DS    CL6                     last reference date
         DS    C
DSORG    DS    CL4
RECFM    DS    CL4
LRECL    DS    CL6
         DS    C
BLKSIZE  DS    CL6
DEXT     DS    CL4                     Number of extents
TRK      DS    CL6                     Data set size in tracks
UNUSED   DS    CL6                     Unused space in data set
         DS    CL2
         SPACE 3
COMMAREA RPFCOMM
         PRINT NOGEN
         IHASDWA
         DCBD  DSORG=PO,DEVD=DA
         IEZDEB
         IKJCPPL
         IKJECT
LINES21  EQU   (21*NEXTENT*2)          NEXT SCREEN IN MEMBER AREA
LINES29  EQU   (29*NEXTENT*2)          NEXT SCREEN IN MEMBER AREA
LINES40  EQU   (40*NEXTENT*2)          NEXT SCREEN IN MEMBER AREA
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
