RPFEDITL TITLE 'LOAD DATASET INTO WORKSPACE'
*
*       RETURN CODES (DEC): 0000 - ALLRIGHT
*                           0004 - WORKSPACE TOO SMALL
*                           0008 - WORKSPACE FULL
*                           0012 - FILE EMPTY
*                           0016 - RESERVED
*                           0020 - ENQUEUE FAILED (RESOURCE IN USE)
*                           0024 - RECFM=V NOT SUPPORTED
*       ALL RETURN CODES ARE SET IN 'RTNCODE' OF COMMAREA.
*
RPFEDITL CSECT
         SAVE  (14,12),,*
         USING RPFEDITL,15
         LA    11,SAVEAREA
         ST    13,SAVEAREA+4
         ST    11,8(0,13)
         LR    13,11
         B     START
         DS    0F
SAVEAREA DC    18F'-1'
         DC    CL8'&SYSDATE',C' ',CL8'&SYSTIME'
START    DS    0H
         DROP  15
         USING SAVEAREA,13
         L     11,0(1)
         USING COMMAREA,11
         XC    EPNEXT,EPNEXT       ZERO EPNEXT
         XC    RTNCODE,RTNCODE     ZERO RTNCODE
         TM    INFCODE4,$RECVB     VARIABLE RECORDS ?
         BNO   START1              NO: BRANCH
         LA    0,24                24 INTO R0
         ST    0,RTNCODE           24 INTO RETURN CODE
         B     RETURN
START1   DS    0H
         CLC   LASTREC,F0000       WORKSPACE EMPTY ?
         BE    APPEND06            YES: SKIP APPEND
         CLC   APPEND,=C'YES'      LOAD APPEND ?
         BNE   APPEND02            NO: TEST FOR NO APPEND
         L     3,LASTREC           POINT TO LAST RECORD IN WS
         LA    3,NEXTENT(3)        POINT AFTER LAST RECORD IN WS
         B     LOAD                START LOAD
APPEND02 CLC   APPEND(2),=C'NO'    LOAD NO APPEND ?
         BE    APPEND06            YES: LOAD NO APPEND
         LA    0,12                12 INTO R0
         ST    0,RTNCODE           12 INTO RETURN CODE
         B     RETURN
APPEND06 L     3,GETMSTRT
         ST    3,FIRSTREC
LOAD     ZAP   GETCNT,=P'0'        ZERO RECORD COUNT
         C     3,GETMEND           END OF WORKSPACE ?
         BNH   LOAD01              NO: GOTO LOAD
         LA    0,8                 YES: 8 INTO R0 - WORKSPACE FULL
         ST    0,RTNCODE                8 INTO RETURN CODE
         B     RETURN              RETURN TO CALLER
LOAD01   DS    0H                  ENQ AND OPEN THE FILE
* BEFORE WE OPEN THE DATASET, WE ENQ ON THE SPF Q- AND R-NAME
         TM    INFCODE2,ENQUED     ANY ENQ OUTSTANDING ?
         BNO   LOAD01A             NO: DO ONLY AN ENQ
         TM    INFCODE3,$EDTCOPY   COPY COMMAND OF EDIT ?
         BO    LOAD01C             YES: DO NOT RE-ENQUEUE
         LA    7,QNAME             LOAD QNAME ADDRESS
         LA    8,RNAME                  RNAME ADDRESS
         DEQ   ((7),(8),52,SYSTEMS),RET=HAVE
         NI    INFCODE2,255-ENQUED FLAG OFF
LOAD01A  EQU   *
         MVC   QNAME(8),=CL8'SPFEDIT'
         MVC   RNAME(44),DSNAME
         MVC   RNAME+44(8),MEMBER      ONLY IF PDS. ELSE BLANKS
         LA    7,QNAME             QNAME ADDRESS OF COMMAREA
         LA    8,RNAME             RNAME ADDRESS OF COMMAREA
         ENQ   ((7),(8),E,52,SYSTEMS),RET=USE
         LTR   15,15
         BZ    LOAD01C            SEE PAGE 244 245 MACRO MANUAL
         LA    1,20                RETURN CODE
         ST    1,RTNCODE           20- ENQ FAILED
         B     RETURN              EXIT.
* NOW WE HAVE THE RESOURCE EXCLUSIVELY, SO WE CAN OPEN FOR INPUT.
LOAD01C  DS    0H                  ENQUEUE DONE, OPEN
         OI    INFCODE2,ENQUED     SET FLAG
         OPEN  (RPFWORK)           OPEN FILE
LOAD02   GET   RPFWORK             GET NEXT RECORD
         AP    GETCNT,=P'1'        INCREMENT RECORD COUNT
         C     3,GETMEND           END OF WORKSPACE ?
         BNH   LOAD03              IF NOT, SET RECORD IN WORKSPACE
         LA    0,4                 4 INTO R0 - END OF WORKSPACE
         ST    0,RTNCODE           4 INTO RETURN CODE
         B     LOAD05              SKIP READ OF RECORDS (WORKSPC TRUNC)
LOAD03   MVC   8(80,3),0(1)        RECORD INTO WORKSPACE
         XC    0(8,3),0(3)         CLEAR FIRST 8 BYTES
         LA    3,NEXTENT(3)        POINT TO NEXT RECORD
         B     LOAD02              READ A NEW RECORD
LOAD05   LA    2,NEXTENT           86 INTO R2
         CLC   GETCNT,ZERO         FILE EMPTY ?
         BNE   LOAD07              IF NOT, SKIP MESSAGE
         LA    0,12                12 INTO R0 - FILE EMPTY
         ST    0,RTNCODE           12 INTO RETURN CODE
         B     LOAD09              SAVE POINTERS
LOAD07   SR    3,2                 RESET ADDRESS
         ST    3,LASTREC           SAVE ADDRESS
LOAD09   MVC   SAVEP4,FIRSTREC     SAVE FIRST RECORD IN WORKSPACE
         MVC   SAVEP5,LASTREC      SAVE LAST RECORD IN WORKSPACE
         MVC   LASTEDIT,MEMBER     SAVE LAST MEMBER EDITED
         CLOSE RPFWORK             CLOSE FILE
         FREEPOOL RPFWORK          FREE THE BUFFERPOOL
RETURN   L     13,SAVEAREA+4       RESTORE POINTER TO SAVE AREA
         LM    14,12,12(13)        RESTORE REGISTERS
         SR    15,15               RETURN CODE = 0000
         BR    14                  RETURN TO MAIN MENU
         TITLE 'WORKAREAS AND CONSTANTS'
F0000    DC    F'0'                0000
ZERO     DC    PL3'0'              000
GETCNT   DC    PL3'0'              COUNT
DOUBLEWD DC    D'0'                DOUBLE WORD
         DS    0F                  BOUNDARY
EXLST    DC    X'87',AL3(JFCB)     DCB EXIT LIST
JFCB     DS    CL176               JOB FILE CONTROL BLOCK
RPFWORK  DCB   DDNAME=RPFWORK,DSORG=PS,MACRF=GL,                       C
               EODAD=LOAD05,EXLST=EXLST
         LTORG                     LITERAL POOL
         TITLE 'RPFCOMM DSECT'
COMMAREA RPFCOMM ,                 COMMON AREA
         END   ,                   END
