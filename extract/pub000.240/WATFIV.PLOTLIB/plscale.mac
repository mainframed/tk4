      SUBROUTINE  SCALE   (ARRAY,AXLEN,NPTS,INC)
C  SCALE    COPYRIGHT 1967 CALIFORNIA COMPUTER PRODUCTS         V097
C.....     ARRAY   NAME OF ARRAY CONTAINING VALUES TO BE SCALED.
C.....     AXLEN   LENGTH IN INCHES OVER WHICH ARRAY IS TO BE SCALED.
C.....     NPTS    NUMBER OF POINTS TO BE SCALED.
C.....     INC     INCREMENT OF LOCATION OF SUCCESSIVE POINTS.
      DIMENSION  ARRAY(1),SAVE(7)
      SAVE(1)=1.0
      SAVE(2)=2.0
      SAVE(3)=4.0
      SAVE(4)=5.0
      SAVE(5)=8.0
      SAVE(6)=10.0
      SAVE(7)=20.
      FAD=0.01
      K=IABS(INC)
      N=NPTS*K
      Y0=ARRAY(1)
      YN=Y0
      DO  25  I=1,N,K
      YS=ARRAY(I)
      IF  (Y0-YS)  22,22,21
21    Y0=YS
      GO  TO  25
22    IF  (YS-YN)  25,25,24
24    YN=YS
25    CONTINUE
      FIRSTV=Y0
      IF  (Y0)  34,35,35
34    FAD=FAD-1.0
35    DELTAV=(YN-FIRSTV)/AXLEN
      IF (DELTAV) 70,70,40
40    I=ALOG10(DELTAV)+1000.0
      P=10.0**(I-1000)
      DELTAV=DELTAV/P-0.01
      DO  45  I=1,6
      IS=I
      IF  (SAVE(I)-DELTAV)  45,50,50
45    CONTINUE
50    DELTAV=SAVE(IS)*P
      FIRSTV=DELTAV*AINT(Y0/DELTAV+FAD)
      T=FIRSTV+(AXLEN+0.01)*DELTAV
      IF (T-YN)  55,57,57
55    IS=IS+1
      GO  TO  50
57    FIRSTV=FIRSTV-AINT((AXLEN+(FIRSTV-YN)/DELTAV)/2.0)*DELTAV
      IF (Y0*FIRSTV) 58,58,59
58    FIRSTV=0.0
59    IF  (INC) 61,61,65
61    FIRSTV=FIRSTV+AXLEN*DELTAV
      DELTAV=-DELTAV
65    N=N+1
      ARRAY(N)=FIRSTV
      N=N+K
      ARRAY(N)=DELTAV
67    RETURN
70    DELTAV=1.0
      FIRSTV=FIRSTV-0.5
      GO TO 65
      END
