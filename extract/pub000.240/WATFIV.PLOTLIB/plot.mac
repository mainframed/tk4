      SUBROUTINE PLPLOT(X,Y,IPEN)
      INTEGER*2 CHAR(3)/' ','.','*'/
      INTEGER*2 GRID(350,132)
      COMMON/PLOTB/GRID,XVIRT,YVIRT,XPOS,YPOS,XSCALE,YSCALE,XMAX,YMAX,
     1ITAG,IERRO,LINES,MLINES
      IF(ITAG.EQ.0) GO TO 10
   13 XNEW=X+XVIRT
      YNEW=Y+YVIRT
      IF(XNEW.GT.XMAX.OR.XNEW.LT.0.) GO TO 70
      IF(YNEW.GT.YMAX.OR.YNEW.LT.0.) GO TO 70
      LTEST=XNEW/XSCALE+1
      IF(LTEST.GE.LINES) LINES=LTEST
C
C     PROCESS THE ALTERNATIVES
      IF(IPEN.EQ.-4) GO TO 20
      IF(IPEN.EQ.-3) GO TO 30
      IF(IPEN.EQ. 3) GO TO 40
      IF(IPEN.EQ.-2) GO TO 50
      IF(IPEN.EQ. 2) GO TO 60
      WRITE(6,300)
      IERRO=IERRO+1
      IF(IERRO.GT.20) GO TO 80
      RETURN
C
C     ITAG =0 WILL INITIALIZE ALL VALUES, EXECUTE ONLY ON 1ST CALL
   10 XVIRT=0.
      YVIRT=0.
      ITAG=1
      LINES=1
      XPOS=0.
      YPOS=0.
      DO 11 I=1,MLINES
      DO 11 J=1,132
   11 GRID(I,J)=CHAR(1)
      XSCALE=8.68E0/5.2E1
      YSCALE=1E-1
      XMAX=(MLINES-1)*XSCALE
      YMAX=131E0*YSCALE
      WRITE(6,500) XMAX,YMAX
      GO TO 13
C
C     CALL OF -4, TO DRAW THE PLOT AND REINITIALIZE
   20 LINES=LINES+1
      XANEW=FLOAT(LINES-2)*XSCALE
      WRITE(6,700) XANEW,LINES
      WRITE(6,100)
      DO 12 I=1,LINES
   12 WRITE(6,200)(GRID(I,J),J=1,132)
      WRITE(6,100)
      ITAG=0
      RETURN
C
C     CALL OF -3 TO SET A NEW PLOT VIRTUAL ORIGIN
   30 XVIRT=XNEW
      YVIRT=YNEW
      XPOS=XNEW
      YPOS=YNEW
      RETURN
C
C     CALL OF 3 TO GO TO THE NEW POINT, WITHOUT DRAWING LINE
   40 XPOS=XNEW
      YPOS=YNEW
      RETURN
C
C     CALL OF -2, DARK VECTOR TO A NEW ORIGIN
   50 XVIRT=XNEW
      YVIRT=YNEW
C
C     CALL OF 2, DRAW THE DARK VECTOR
   60 XANEW=XNEW/XSCALE
      YANEW=YNEW/YSCALE
      XAPOS=XPOS/XSCALE
      YAPOS=YPOS/YSCALE
      XAINCR=XANEW-XAPOS
      YAINCR=YANEW-YAPOS
      DIVID=AMAX1(ABS(XAINCR),ABS(YAINCR))
      IF(DIVID.LT.0.3) RETURN
      XAINCR=XAINCR/DIVID
      YAINCR=YAINCR/DIVID
      IXMEM=XAPOS
      IYMEM=YAPOS
      DO 61 I=1,3000
          IXAPOS=XAPOS+1
          IYAPOS=YAPOS+1
          IF(GRID(IXAPOS,IYAPOS).NE.CHAR(1)) GOTO 63
   64 GRID(IXAPOS,IYAPOS)=CHAR(2)
          IXMEM=IXAPOS
          IYMEM=IYAPOS
          GO TO 65
   63     IF(IXMEM.EQ.IXAPOS.AND.IYMEM.EQ.IYAPOS) GO TO 64
          GRID(IXAPOS,IYAPOS)=CHAR(3)
   65     IF(ABS(XANEW-XAPOS).LE.1E0.AND.ABS(YANEW-YAPOS).LE.1E0)GOTO62
          XAPOS=XAPOS+XAINCR
   61     YAPOS=YAPOS+YAINCR
   62 XPOS=XNEW
      YPOS=YNEW
      RETURN
C
C     DONE. PRINT ERRORS AND FORMATS
   70 WRITE(6,400)X,Y,XNEW,YNEW,IPEN
      IERRO=IERRO+1
      IF(IERRO.GT.20) GO TO 80
      RETURN
   80 WRITE(6,600)
      STOP
C
  100 FORMAT('1')
  200 FORMAT(1X,132A1)
  300 FORMAT(' ***ERROR*** IMPROPER IPEN OPTION')
  400 FORMAT(' ***ERROR*** PLOT VALUE OUT OF RANGE. X = ',F7.3,' Y = ',
     1F7.3,' ACTUAL X = ',F7.3,' ACTUAL Y = ',F7.3,' PEN POSITION =',I3)
  500 FORMAT(1X,'MAXIMUM X = ',F7.3,' INCHES. MAXIMUM Y = ',F7.3,
     1' INCHES.')
  600 FORMAT(' ***ERROR*** EXECUTION TERMINATING. TOO MANY ERRORS.')
  700 FORMAT(' LARGEST X CALLED =',F8.3,' USING',I6,' LINES OF OUTPUT')
      END
