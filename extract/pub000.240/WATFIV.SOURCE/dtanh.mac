LTNH   TITLE   'HYPERBOLIC TANGENT FUNCTION (LONG)'
         PRINT     NOGEN
         DISK
         PRINT     GEN
         SPACE 2
DTANH#   CSECT
*
*      HYPERBOLIC TANGENT FUNCTION (LONG)
*              1. IF /X/ LESS THAN 0.54931, USE A FRACTION APPROX.
*              2. IF /X/ GREATER THAN 20.101, ANSWER IS +1 OR -1.
*              3. FOR OTHER VALUE OF X, USE EXTERNAL DEXP FUNCTION.
       SPACE
         ENTRY     TANH
       EXTRN   DEXP
       ENTRY   DTANH
       SPACE
GRA    EQU     1               ARGUMENT POINTER
GRS    EQU     13              SAVE AREA REGISTER
GRR    EQU     14              RETURN REGISTER
GRL    EQU     15              LINK REGISTER
GR0    EQU     0               SCRATCH REGISTERS
GR1    EQU     1
FR0    EQU     0               ANSWER REGISTER
FR2    EQU     2               SCRATCH REGISTERS
FR4    EQU     4
FR6    EQU     6
ISN    EQU     X'102'          IDENTIFIER NUMBER FOR DEXP CALL
       SPACE
TANH     CALLDUBL  DTANH,2
         SPACE     1
DTANH    FENT      (XDTH,REAL8),1
       SPACE
         LD        FR6,XDTH        OBTAIN ARGUMENT X
       LD      FR4,ONE         FR4 AND FR6 TRANSPARENT TO DEXP
       LPDR    FR0,FR6         /X/ TO FR2
       CE      FR0,MLIM
       BC      12,SMALL        IF /X/ LESS THAN 0.54931, JUMP
       CE      FR0,HLIM        IF /X/ GREATER THAN 20.101
       BC      10,BIG            ANS = + OR -1, JUMP
       SPACE
       ADR     FR0,FR0         FOR /X/ BETWEEN 0.54931 AND 20.101,
       STD     FR0,BUFF          /ANS/ = 1-2/(1+E**/2X/)
         FCALL     (DEXP,REAL8),(BUFF,REAL8)
       ADR     FR0,FR4
       LDR     FR2,FR4
       ADR     FR2,FR2
       DDR     FR2,FR0
       LDR     FR0,FR4
       SDR     FR0,FR2
       SPACE
SIGN   LTER    FR6,FR6         TANH(/X/) READY, ADJUST SIGN
       BC      10,*+6
       LNER    FR0,FR0
       SPACE
EXIT     SDR   FR6,FR6             ***WATFIV CONVENTION***
         B         XFRET
       SPACE
BIG    LDR     FR0,FR4         CASE OF BIG ARGUMENT
       BC      15,SIGN
       SPACE
SMALL  CE      FR0,LLIM        IF /X/ LESS THAN 2**-28, ANS=ARG.
       BC      12,SIGN           THIS AVOIDS UNDERFLOW
       MDR     FR0,FR0         /X/ SMALLER THAN 0.54931
       LD      FR4,C5            TANH(X) = X+X*F, WHERE
       ADR     FR4,FR0             F = C0*X**2/(X**2+C1+C2/
       LD      FR2,C4                (X**2+C3+C4/(X**2+C5)))
       DDR     FR2,FR4
       AD      FR2,C3
       ADR     FR2,FR0
       LD      FR4,C2
       DDR     FR4,FR2
       AD      FR4,C1
       ADR     FR4,FR0
       MD      FR0,C0
       DDR     FR0,FR4
       MDR     FR0,FR6
       ADR     FR0,FR6
       BC      15,EXIT         TANH(X) READY, SET TO RETURN
       SPACE
XDTH     DS        D
       DS      0F
HLIM   DC      X'421419DB'
LLIM   DC      X'3A100000'
MLIM   DC      X'408C9F95'
BUFF   DS      D
C0     DC      X'C0F6E12F40F5590A'    -0.9643735440816707
C1     DC      X'419DA5D6FD3DBC84'     0.9852988232825539E+1
C2     DC      X'C31C504FEF537AF6'    -0.4530195153485250E+3
C3     DC      X'424D2FA31CAD8D00'     0.7718608264195518E+2
C4     DC      X'C3136E2A5891D8E9'    -0.3108853383729134E+3
C5     DC      X'4219B3ACA4C6E790'     0.2570185308319156E+2
ONE    DC      X'4110000000000000'
       END
