CLEX   TITLE   'COMPLEX EXPONENTIAL FUNCTION (LONG)'
         PRINT     NOGEN
         DISK
         PRINT     GEN
         SPACE     2
CDEXP#   CSECT
*
*C108000,013900-014000                                           A43096
*A012200,012400                                                  A43096
*D012300                                                         A43096
*
*        COMPLEX EXPONENTIAL FUNCTION (LONG)
*              EXP(X+IY) = E**X * COS(Y) + I*E**X * SIN(Y)
* STATUS  -  CHANGE LEVEL 01  15JULY71  RELEASE 21
       SPACE
         ENTRY     CEXP
       EXTRN   DEXP
       EXTRN   DSIN
       EXTRN   DCOS
       ENTRY   CDEXP           ENTRY POINT NAME
       SPACE
GR2    EQU     2
RG3    EQU     3
GR1    EQU     1               SCRATCH REGISTERS
GR3    EQU     14
GRA    EQU     1               ARGUMENT POINTER
GRB    EQU     2               BASE REGISTER (TEMPORARY)
GRS    EQU     13              SAVE AREA POINTER
GRR    EQU     14              RETURN REGISTER
GRL    EQU     15              LINK REGISTER
       SPACE
FR0    EQU     0               ANSWER REGISTER (REAL PART)
FR2    EQU     2               ANSWER REGISTER (IMAG PART)
FR6    EQU     6               SCRATCH REGISTER
       SPACE
ISNEX  EQU     X'308'          IDENTIFIER NO. FOR DEXP CALL
ISNSN  EQU     X'30A'          IDENTIFIER NO. FOR DSIN CALL
ISNCN  EQU     X'30C'          IDENTIFIER NO. FOR DCOS CALL
       SPACE
CEXP     CALLDCL   CDEXP,2
         SPACE     1
CDEXP    FENT      (XCDE,COMPL16),1
         SPACE     1
         LD        FR0,XCDE        PICKUP REAL(X) IN FR0
         LD        FR2,XCDE+8      AND IMAG(X) IN FR2
       LPER    FR2,FR2         /Y/ IN FR2 TO BE SCREENED
       CE      FR2,YMAX
       BC      10,ERROR2         IF /Y/ GE PI*2**50, ERROR2
       CE      FR0,XMAX        X TO BE SCREENED
       BC      2,ERROR1          IF X GRT THAN 174.673, ERROR1
       SPACE
         FCALL     (DEXP,REAL8),(XCDE,REAL8)
         STD    FR0,BUFF2    STORE E**X IN BUFF2 FOR LATER MULT  A43096
       SPACE
         FCALL     (DSIN,REAL8),(XCDE+8,REAL8)
         MD     FR0,BUFF2    E**X*SIN(Y),  SAVE                  A43096
         STD    FR0,BUFF3    IN BUFF3                            A43096
       SPACE
         FCALL     (DCOS,REAL8),(XCDE+8,REAL8)
         SPACE
         MD     FR0,BUFF2    E**X*COS(Y),REAL PART, READY IN FR0 A43096
         LD     FR2,BUFF3    E**X*SIN(Y),IMAG PART, READY IN FR2 A43096
       SPACE
EXIT   EQU     *
         SDR       FR6,FR6         ***WATFIV CONVENTION***
         B         XFRET
         SPACE     1
*  LI-3   REAL PORTION OF ARGUMENT > 174.673
ERROR1   $ERROR    (NOAC,LI,3),XRETRACE                            V1L5
         SPACE     1
*  LI-6  ABS(IMAG PORTION OF ARG) > 174.673
ERROR2   $ERROR    (NOAC,LI,6),XRETRACE                            V1L5
         SPACE     1
XCDE     DS        2D
       SPACE
BUFF2  DS      D                  FOR CALC AND REAL PART IF ERROR
BUFF3    DS     D                                                A43096
XMAX   DC      X'42AEAC4F'     174.673
YMAX   DC      X'4DC90FDA'     PI*2**50
       END
