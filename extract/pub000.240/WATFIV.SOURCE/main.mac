MAIN     TITLE     'MAIN     MAIN PROGRAM'
         GBLC      &STATUSP                                        V1L5
         COPY      OPTIONS
         $PUNCH    MAIN                                            DVS2
         ENTRY     WATFOR,WATFIV
         AIF       ('&MEMTYPE' EQ 'MAX').MAIN77
         ENTRY     OBJECT
.MAIN77  ANOP
         AIF       (&NDVSD).NOXS@VE                                DOS3
         ENTRY     XS@VE3                                          DOS3
.NOXS@VE ANOP                                                      DOS3
         ENTRY     XTRUE,XFALSE,XTART,XSTART10,MAINREST
         ENTRY     XBUFFER,XOBJECT,ERROR#,XEXECSW,XISNTRAC
         AIF       ('&DIRACC' EQ 'OMIT').XNODA0
         ENTRY     PIOINTDA,PDEFILE
.XNODA0  ANOP
         ENTRY     PCHARSIM,PCHARSUB,PCHARARR,PIOINTCC,PIOCCFRM,XRADDR
         ENTRY     XIOBASE,XIOEND,XIOERR,XERRDA2,XTRACEB1,XDSRN
         AIF       ('&PROFILE' EQ 'OMIT').PROF70                   V1L5
         ENTRY     PCHKCNT,PIFOBJ,XAPROF                           V1L5
.PROF70  ANOP                                                      V1L5
         ENTRY     PC16EXPI,PDATBASE,PIOINIT,XIOINIT,XDATA
         ENTRY     PCMULT8,PCMULT16,PCDIV18,PCDIV28,PCDIV116,PCDIV216
         ENTRY     STARTAE,STARTBE,XLINES,YENTA                    V1L4
         AIF       ('&ISNTRAC' EQ 'OMIT').XNOISNT                  V1L4
         ENTRY     XISNOUT                                         V1L4
.XNOISNT ANOP                                                      V1L4
         AIF       ('&VM' NE 'USE').XVMNO                          VM1
         ENTRY     XVMDEBUG                                        VM1
         ENTRY     XVMDSWT                                         VM1
.XVMNO   ANOP                                                      VM1
         AIF       ('&TRACE' EQ 'OMIT').XNOTRAC
         ENTRY     TRACEPTR
.XNOTRAC ANOP
         AIF   ('&NAMLIST' EQ 'OMIT' AND '&DUMPLST' EQ 'OMIT').XNONL1
         EXTRN     NAMELRD,NAMELWR
.XNONL1  ANOP
         EXTRN     LENDS,LENDPROG,RPH3,SCAN
         EXTRN     INBCDI,OUTBCDI,INFREEI,OUTFREEI,INBCD,OUTBCD
         EXTRN     INBIN,OUTBIN,INFREE,OUTFREE,INBINI,OUTBINI
         EXTRN     FORMATEX,FMCNBASE,FRIOSCAN
         EXTRN     FDUMMY,DLOG,DEXP,ERROR
         EXTRN     ERROPEN,FORMCONE,FORMCONV                       V1L5
         AIF       ('&ERROPT' NE 'READ').NOCLOS1                   V1L4
         EXTRN     ERRCLOSE                                        V1L4
.NOCLOS1 ANOP                                                      V1L4
         EJECT
&DECK    SETC      'MAIN'                                          V1L3
         PRINT     &ON(1),&LIST                                    V1L4
         COPY      CDEFN
STARTA   CSECT
         PRINT     &ON(2),&LIST                                    V1L4
         COPY      STARTA
XTART    EQU       START
STARTB   CSECT
         PRINT     &ON(3),&LIST                                    V1L4
         COPY      STARTB
COMMR    CSECT
         PRINT     &ON(4),&LIST                                    V1L4
         COPY      COMMR
KOMMR    EQU       COMMR
         ENTRY     KOMMR
         TITLE     'MAIN     MAIN PROGRAM'
         PRINT     OFF
         ERRTABLE
         PRINT     ON
         PRINT     ON,NOGEN
         PRINT     &ON(5),&LIST                                    V1L4
MAINP    CSECT
         AIF       ('&PROFILE' EQ 'OMIT').NOSTATS                  V1L5
         SPACE     1                                               V1L5
         STATUS    PROF@                                           V1L5
&STATUSP SETC      '&STATUS'                                       V1L5
.NOSTATS ANOP                                                      V1L5
         SPACE
***********************************************************************
***********************************************************************
***********************************************************************
***********************************************************************
***********************************************************************
***********************************************************************
*****                                                             *****
*****    THIS THE MAIN ENTRY POINT FOR VERSION 1 OF WATFOR        *****
*****                                                             *****
***********************************************************************
***********************************************************************
***********************************************************************
***********************************************************************
***********************************************************************
***********************************************************************
         SPACE
***********************************************************************
*****    THE NEXT SECTION OF CODE IS THE BATCH INITIALIZATION     *****
***********************************************************************
WATFOR   STM       14,12,12(13)
WATFIV   EQU       WATFOR
         LR        RP,15                  GET A BASE REGISTER
         USING     WATFOR,RP
         L         RX,=A(XTART)           BASE FOR STARTA
         L         RC,MCSAVER             BASE FOR COMMR
         LR        R15,13                 SAVE OLD SAVE AREA ADDR
         LA        R13,XSAVER             NEW SAVE AREA ADDR
         ST        R13,ZR15+8             FORWARD LINK
         ST        R15,XSAVER+4           BACKWARD LINK
         L         R1,XAFIOCS          CALL  WATIO   TO OPEN  THE
         BALR      R0,R1               READER AND PRINTER.
         DC        X'06'               CODE FOR OPEN
         DC        X'00'
         CLINK1    ERROPEN                                         V1L3
         LTR       R15,R15                                         V1L3
         BZ        OPENOK                                          V1L3
         $ERROR    (NOAC,KO,9),MCLOSE  NO ERROR FILE -> CLOSE RDR  V1L5
OPENOK   EQU       *                                               V1L3
         SPACE
         CLINK1    BSTART              START-OF-BATCH ACCOUNTING
         SPACE
         AIF                  (&NDVSD).MSEQQ1                      DVS1
         AIF       ('&TIMER' NE 'TASK').TSEQQ1                     DVS2
         CLINK1    JJTIME                                          DVS2
.TSEQQ1  AIF       ('&MEMTYPE' NE 'MAX').MSEQQ1                    DVS2
         CLINK1    JGETFREE                                        DVS2
.MSEQQ1  AIF       ('&LIBRARY' EQ 'NO').MLIB1                      DVS2
         AIF  (&NDVSD).DLIB1                                       DVS1
         CLINK1 JOPENLIB          OPEN THE SOURCE LIBRARY          DOS2
         B     *+4(R15)                                            DOS2
         B     *+8                BR IF LIBRARY OPEN               DOS2
         B     *+8                BR IF LIBRARY NOT OPEN           DOS2
         MVI   MLIBR11+1,NOP      FOR MLIBR                        DOS2
*        THIS IS THE END OF THE LIBRARY CODING                     DOS2
         AGO   .MLIB1                                              DOS2
.DLIB1   ANOP                                                      DOS2
         AIF       ('&VM' EQ 'USE').VMMAIN1                        VM1
         CLINK1    JTIOT               OBTAIN ADDR OF TIOT IN R1
         LA        R14,24(,R1)         OBTAIN ADDR OF TIOT IN R14
CPLOOP   CLC       ZR14(4),KF0         END OF TIOT?
         BNE       MNOLIB1             NO SO CONTINUE SEARCH
         $ERROR    (WARN,UN,J),MNOLIB  YES SO ISSUE ERROR          V1L5
MNOLIB1  CLC       ZR14+4(8),MWATLIB   IS WATLIB IN TIOT?
         BE        CPGOTFT             WATLIB COULD BE USED
CPLOOP1  SR        R0,R0
         IC        R0,ZR14             ENTRY LENGTH
         AR        R14,R0              BUMP TO NEXT ENTRY
         B         CPLOOP              AND BACK TO TRY AGAIN
CPGOTFT  CLC       ZR14+16(4),KF0      END OF TIOT?
         BE        MNOLIB              YUP SO NO WATLIB
.VMMAIN1 ANOP                                                      VM1
         CLINK1    JOPENLIB            OPEN WATLIB DATA SET        V1L5
         L         R1,=A(WATLIB)       R1 -> WATLIB DCB            V1L5
         USING     IHADCB,R1           INFORM ASSEMBLER            V1L5
         TM        DCBOFLGS,X'10'      DID WATLIB OPEN?            V1L5
         BO        MLIBOK              BRANCH IF OPEN OK           V1L5
         $ERROR    (WARN,UN,N),MNOLIB  I/O ERROR                   V1L5
         $FREE     R1                                              V1L5
MLIBOK   DS        0H                                              V1L5
         MVI       MAIN62+1,NOP        FOR CLOSE
         MVI       MLIBR11+1,NOP       FOR MLIBR
         ST        R2,MRANGEL          BUFFER LENGTH FROM OPEN
         ST        R2,MRANGEL+4        FOR GETMAIN OF THE BUFFER
         LA        R1,MSCBL            ADDRES OF INFO BLOCK
         CLINK1    JGET                GET A BUFFER
         B         *+4(R15)            NOT ENOUGH SPACE FOR BUFFER
         B         MWAT2               ENOUGH  ROOM SO CONTINUE
         B         MAINMO4             NOT ENOUGH ROOM FOR BUFFER
MWAT2    MVC       CBUFF,MVALUESL      ADDR OF BUFFER
         CSNAP     MRANGEL,MRANGEL,8
         CSNAP     MSCBL,MSCBL,10
         CSNAP     MVALUESL,MVALUESL,8
MNOLIB   DC        0H'0'
* THIS IS THE END OF THE LIBRARY CODING
.MLIB1   ANOP
         AIF       ('&MEMTYPE' NE 'MAX' OR '&TRACE' EQ 'OMIT').MTYPE1
         BAL       R14,XTRACON    **** TRACEON *************
         BAL       R14,XTRACOFF      **** TRACEOFF *******************
.MTYPE1  ANOP
         CLINK1    JMSPIE         OFF TO SET COMPILER INTERRUPT EXIT
         AIF       (&DVSD).XMSTAE                                  DVS2
         CLINK1    JMSTAE             ISSUE STAE FOR EOV ABENDS     UOW
.XMSTAE  ANOP                                                      V1L4
         B         MAIN           AND NOW OFF TO ''COMPILE''
         $FREE     RP                                              V1L5
***********************************************************************
*        END OF BATCH INITIALIZATION. SKIP AROUND END-OF-JOB CODING.  *
***********************************************************************
         EJECT
***********************************************************************
*       WE COME HERE AT THE END OF EACH PROGRAM IN THE BATCH TO DO    *
*       JOB ACCOUNTING. WE GET HERE FROM XSTOP WHICH IS THE END-OF-   *
*       JOB EXIT IN STARTA.                                           *
***********************************************************************
MAINREST DC        0H'0'
         USING     *,RP
         L         RC,MCSAVER     RESET COMMR BASE REGISTER
         LA        R13,XSAVER     AND ALSO SAVE AREA PTR
         MVC       MTIMER3(4),XTIME     GET MAX TIME FOR JOB
         AIF       ('&TRACE' EQ 'OMIT').MAINR1
***PC    BAL       R14,XTRACOFF
.MAINR1  ANOP
         CLINK1    JTCANCEL            END OF EXECUTION OR COMPILE
         AIF       (&NDVSD).MDCNVT                                 DVS2
         SRDL      R0,32               SHIFT TIME REMAINING TO R1  DVS1
         D         R0,=F'100'          CONVERT TO SECONDS          DVS1
.MDCNVT  ANOP                                                      DVS2
         IF        (XEXECSW,ON),MCMPL0 DID PROGRAM EXECUTE?        V1L5
         ST        R0,MTIMER2          NO -> SAVE INFO HERE        V1L5
         STATUS    FIOCS#                                          V1L5
         AIF       ('&STATUS' NE 'DISK').MAINR2                    V1L5
         L         R1,AFIOCS           ZERO ADDR OF FIOCS          V1L5
         XC        0(4,R1),0(R1)       IN WATIO                    V1L5
.MAINR2  ANOP                                                      V1L5
         STATUS    DIOCS#                                          V1L5
         AIF       ('&STATUS' NE 'DISK').MAINR3                    V1L5
         L         R1,ADIOCS@          ZERO ADDR OF DIOCS          V1L5
         XC        0(4,R1),0(R1)       IN WATIO                    V1L5
.MAINR3  ANOP                                                      V1L5
         B         MPPDONE             ALL DONE PROCESSING         V1L5
MCMPL0   DS        0H                                              V1L5
         ST        R0,MTIMER3        YES SO SAVE TIME HERE
         L         R1,XAFIOCS          CLOSE SEQUENTIAL FILES      V1L4
         BALR      R0,R1               OFF TO FIOCS VIA WATIO
         DC        X'04'               'CLOSE' CODE
         DC        X'00'
         L         R5,=A(XAIBCNT1)     CLOSE DIRECT ACCESS FILES   V1L5
         L         R1,ZR5
         BALR      R0,R1               OFF TO DIOCS VIA WATIO
         DC        X'04'               'CLOSE' CODE
         DC        X'00'
MPPDONE  DS        0H                                              V1L5
         L         RW,=A(JMSTAE)       SET UP RW FOR ADDRESSABILITY UOW
         USING     JMSTAE,RW           INFORM ASSEMBLER             UOW
         MVI       STAESW,X'00'        INIT STAE EXIT SW            UOW
         DROP      RW                  NO LONGER NEEDED             UOW
         EJECT
         AIF       ('&PROFILE' EQ 'OMIT').PROFAA                   V1L5
         IF        (XEXECSW,OFF),PROFA     WAS THERE EXECUTION ?   V1L5
         CLC       XPRSTART(4),KF0     WERE STATEMENTS COUNTED ?   V1L5
         BE        PROFA               NO                          V1L5
         L         R15,XAPROF          GET THE ADDR OF THE PROFILERV1L5
         BALR      R14,R15             GO DO A PROFILE OF THE PROG V1L5
PROFA    DS        0H                                              V1L5
.PROFAA  ANOP                                                      V1L5
         CLINK1    BENDJOB             CALL END OF JOB ACCOUNTING  V1L5
         SPACE
         AIF       ('&MEMTYPE' NE 'MAX').MTYPE22
***********************************************************************
*        THIS CODING FREES ALL THE CORE USED BY THE LAST JOB          *
***********************************************************************
         L         R1,MVALUESG         LOW ADDR OF WORK AREA
         L         R2,XENDOBJ          ADDR OF END OF USER AREA
         SR        R2,R1               LENGHT OF AREA
         ST        R2,MVALUESG+4       SAVE IN INFOR BLOCK
         LA        R1,MSCBG            GIVE ADDR TO FREEMAIN
         CLINK1    JFREE               AND AWAY TO FREE THE CORE
         CSNAP     FREEM1,XOBJECT,20
**********************************************************************
.MTYPE22 ANOP
         EJECT
***********************************************************************
*         BEGINNING-OF-JOB INITIALIZATION
*             COME HERE AT THE START OF EACH JOB IN THE BATCH
***********************************************************************
MAIN     BALR      R11,0            SET UP BASE FOR MAIN
         USING     *,R11
         LA        R1,MTIMER1       MAX TIME FOR COMPILE TIME
         L         R0,MAXTIMNT      INT ROUT TO HANDLE TOO MUCH TIME
         CLINK1    JSTIMER          AND AWAY TO SET UP TIME
         AIF       ('&TRACE' EQ 'OMIT').MNT3
         BAL       R14,XTRACOFF        TRACEOFF
.MNT3    ANOP
         MVI       XERRSWT,X'00'       TURN ERROR SWITCH OFF
         THEN      (XEXECSW,OFF)       EXECUTION/COMPILE SWT
         AIF       ('&VM' NE 'USE').XVM111                         VM1
         MVI       XVMDSWT,X'F0'       TURN OFF MONITORING         VM1
         MVI       XVM1STSW,X'FF'      TURN ON 1ST TIME SWITCH     VM1
.XVM111  ANOP                                                      VM1
         AIF       ('&MEMTYPE' NE 'MAX').MTYPE2
***********************************************************************
*        GET AS MUCH SPACE AS POSSIBLE IN THE PARTITION.  FREE A
*        LITTLE FOR THE SYSTEM. SET UP APPROPRIATE POINTERS.
**********************************************************************
         LA        R1,MSCBG          ADDRESS OF INFO BLOCK
         CLINK1    JGET              AWAY TO GETMAIN ROUTINE
         B         *+4(R15)          WELL THERE WASN'T ENOUGH
         B         MGOTAREA            WELL WE GOT A WORK AREA
         B         MAINMO4
MGOTAREA L         R1,MVALUESG         LOW ADDR OF GETMAIN
         ST        R1,XOBJECT          START OF WORK AREA
         ST        R1,XBEGDATA         ZERO OUT ARRAY AREA         V1L5
         ST        R1,CENDARAY         AND OBJECT CODE STATISTICS  V1L5
         A         R1,MVALUESG+4       ADD LENGTH OBTAINED
         S         R1,MVALUESS+4       AND LEAVE A LITTLE FOR O.S.
         ST        R1,XENDOBJ          END OF WORK AREA
         ST        R1,MVALUESS         AND IN INFO BLOCK
         LA        R1,MSCBS            ADDR OF INFO  BLOCK
         CLINK1    JFREE               FREE CORE FOR O.S.
         L         R1,XENDOBJ          END OF WORK AREA ADDR
         AIF       (&NOCCRDS GE 5).MSTACK1
         S         R1,=F'280*4'        LENGTH OF MINIMUM STACK     V1L4
         AGO       .MSTACK2
.MSTACK1 ANOP                                                      V1L5
         S         R1,MSTKLEN          LENGTH OF STACK             V1L5
.MSTACK2 ANOP
         ST        R1,XSTACK           ADDRESS OF STACK
         S         R1,KF24             INIT START OF STACK
         ST        R1,XSYMBOL          START OF SYMTAB
         C         R1,XOBJECT          STILL WITHIN WORKAREA ?     V1L4
         BNH       MAINMO4             BIF NOT                     V1L4
         L         R1,XENDOBJ          MAKE AN                     V1L4
         S         R1,MSTKEND            ADJUSTMENT                V1L4
         ST        R1,STACKEND             FOR STACK               V1L4
         THEN      (CVRFMTSW,OFF)
         CSNAP     GETMAIN,MRANGE,24
         CSNAP     POINTERS,XOBJECT,20
MNOGET   DC        0H'0'
.MTYPE2  ANOP
         L         R1,XSYMBOL          SYMBOL TABLE ADDR
         MVC       ZR1(8),MSTCKCON     INIT PART OF STACK
         EJECT
**********************************************************************
*        FORTRAN JOB INITIALIZATION                                   *
*        ALSO INCLUDED IS A CALL TO LENDPROG FOR PROGRAM-SEGMENT      *
*        INITIALIZATION. THIS IS MAINLY TO SET UP SEGMENT SYMTABS.    *
***********************************************************************
         L         R5,XOBJECT          START OF WORK AREA
         L         R6,XSYMBOL          START OF SYMTAB
         THEN      (CMOSWTCH,OFF)      WORK AREA OVERFLOW SWT
         GETSYM    16,NOCHECK          16 BYTES FOR LLIST          V1L4
         LA        R0,52(,R5)          LENDPROG WANTS 52 BYTES     V1L4
         CR        R6,R0               CAN HE GET IT ?             V1L4
         BNH       MAINMO4             BIF NOT TO SHUTDOWN         V1L4
         ST        R6,CLBEG            LLIST START PTR
         ST        R6,CLEND            LLIST END PTR
         CLINK1    LENDPROG            OFF TO RELOCATOR INITIALIZING
         THEN      (XISNSW+1,EQU,BRA)  RESET TIMER OVERFLOW SWITCH
         THEN      (CSRSWTCH,EQU,NORTN)  SET TO NO SOURCE ENCOUNTERED
         SR        R0,R0
         STH       R0,XISN             ZERO ISN NUMBER
         ST        R0,XNOERRS          SET DIAGNOSTICS TO 0        V1L3
         STH       R0,XNOWARNS                                     V1L3
         ST        R0,CGVBEG           PTR TO START OF GLIST
         ST        R0,CGVEND           PTR TO END OF GVLIST
         ST        R0,CXD              INITIALIZE CXD FOR LOADER
         ST        R0,CDMPLIST         ZERO DUMPLIST POINTER       V1L2
         ST        R0,CERRLIST         ZERO END OF ONERROR CHAIN   V1L2
         ST        R0,XIORECUR         ZERO STARTA SWS (4 OF THEM) V1L2
         ST        R0,XFALSE           XFALSE=0;
         AIF       ('&PROFILE' EQ 'OMIT').PROFAB                   V1L5
         ST        R0,XPRSTART         STMT COUNT POINTER          V1L5
.PROFAB  ANOP                                                      V1L5
         BCTR      R0,0                R0=-1;
         ST        R0,XTRUE            XTRUE=-1;
         LA        R0,XSTART10         INITIALIZE DATA ENTRY POINT
         ST        R0,XENTRYPD
         LA        R0,XENTRYPD-4       INITIALIZE FOR DATA COMPILER
         ST        R0,CENTRYPD
         LA        R0,XERRSR9          INITIALIZE M/PROG ENTRY POINT
         ST        R0,XENTRYP
         LA        R0,CERRLIST         GET START OF ONERROR CHAIN  V1L2
         ST        R0,XERRLIST         POINT AT IT                 V1L2
         LA        R0,CDMPLIST         GET ADDR OF END OF CHAIN    V1L2
         ST        R0,XDMPLIST         STORE INTO FIRST POINTER    V1L2
         AIF       ('&ISNTRAC' EQ 'OMIT').XISNGO6  ISN TRACE?      V1L2
         MVI       XISNPATC+1,X'F0'    TURN ISN TRACE OFF          V1L2
.XISNGO6 ANOP                                                      V1L2
         THEN      (CSUBRDS,OFF)       SET TO READ FROM'&READ'
         EJECT
***********************************************************************
*       NOW READY TO COMPILE A JOB                                    *
*       READ CARDS UNTIL A WATFOR CONTROL CARD IS ENCOUNTERED         *
*       IF $STOP THEN GO TO END-OF-BATCH PROCESSOR                    *
*       IF $JOB START JOB CARD ANALYSIS  OF PARAMETERS                *
*       IF ANYTHING ELSE IGNORE IT                                    *
***********************************************************************
MAIN10   BAL       R14,CREAD           READ ANOTHER CARD
         B         MCONTROL            HAH A CONTROL CARD SO OK
         B         MAIN10              GO READ ANOTHER
         B         MAIN10
         B         MAIN10
         B         MAIN10
MCONTROL EX        R0,COPSTOP          IS IT $STOP?
         BE        MSTOP               YUP SO TERMINATE BATCH
         AIF       ('&CONTROL'(1,1) EQ '&CONTROL').MAIN11          V1L4
         EX        R0,COPCSTOP                                     V1L4
         BE        MSTOP                                           V1L4
.MAIN11  ANOP                                                      V1L4
         EX        R0,COPJOB           IS IT $JOB?
         AIF       ('&CONTROL'(1,1) EQ '&CONTROL').MAIN12          V1L4
         BE        MAIN15                                          V1L4
         EX        R0,COPCJOB                                      V1L4
.MAIN12  ANOP                                                      V1L4
         BNE       MAIN10              NOPE SO READ ANOTHER
* SET DEFAULT VALUES FOR ACCOUNTING SWITCHES
MAIN15   MVI       XMODEKP,&VCHECK3    SET DEFAULT KP.             V1L5
         AIF       ('&CHRMODE' EQ 'EBCDIC').MAIN15
         MVI       XQUOTCON,C'@'       DEFAULT QUOTE SET
         AGO        .MAIN16
.MAIN15  MVI       XQUOTCON,C''''      DEFAULT QUOTE SET
.MAIN16  ANOP
         MVI       CUNDEFSW,&VCHECK1   UNDEFINED VARIABLE CODE SWT
         MVI       XNOPDEFZ+1,&VCHECK2
         MVI       XNOPDEFN+1,&VCHECK2
         THEN      (CWARNSW,ON)        PRINT WARN & EXT'S MSG'S
         THEN      (CEXTNSW,&EXTEN)                                V1L3
         THEN      (CLISTSW,&LISTSW)   SET TO LIST  SOURCE
         THEN      (CLIBLSW,&LIBLSW)   SET TO (NO)LIST OF LIB SRS
         THEN      (CMNPRGSW,OFF)
         THEN      (KOMPSW,OFF)                                    V1L5
         THEN      (CWHPERF,OFF)                                   V1L5
         THEN      (C$OPTSW,OFF)                                   V1L5
         MVI       XPROFILE+1,BRA      SET BRANCH TO OBJECT CODE   V1L5
         LA        R0,&PROFSWA         GET THE DEFAULT             V1L5
         STC       R0,CCNTSW           SET CCNTSW                  V1L5
         LA        R0,1
         ST        R0,XEXUFLOW         UNDERFLOW COUNTER
         ST        R0,XEXOFLOW         OVERFLOW COUNTER
         ST        R0,XFXOFLOW         OVERFLOW COUNTER(FIXED)
         ST        R0,XFXDVCNT         DIVIDE CHECK COUNTER(FIXED)
         ST        R0,XFLDVCNT         DIVIDE CHECK COUNTER(FLOATING)
         MVC       XLINES(20),MSTANOPT  DEFAULT LINES PER PAGE
         IF        (DEBUGSW,OFF),MNODEB WAS DEBUG PARM SPECIFIED?  V1L5
         L         R1,=A(MONSTOPT)     GET ADDR OF DEBUG OPTIONS   V1L5
         MVC       XLINES(20),0(R1)    SET DEFAULTS                V1L5
         OI        KOMPSW,LINES0                                   V1L5
MNODEB   DS        0H                                              V1L5
         SPACE
         CLINK1    BBEGJOB             BEGINNING OF JOB ACCOUNTING
         LM        R5,R6,XSAVER+ZSV5-ZSAVER   RESTORE 5 AND 6
         AIF       ('&OMR' EQ 'OMIT').MNOOMR1                      V1L2
         OI        KOMPSW,XOMRSW                                   V1L5
.MNOOMR1 ANOP                                                      V1L2
         EJECT
***********************************************************************
**       HERE'S WHERE WE GO TO SCAN UNTIL A $ENTRY CARD IS ENCOUNTERED
**       THEN OFF TO LANDR AND MLIBR
***********************************************************************
MAIN18   CLINK1    SCAN
MAIN30   IFNOT     (CSRSWTCH,RTN),MAIN35   DID WE FIND A'END'CARD?
         CLINK1    LENDS               NOPE SO GENERATE ONE
MAIN35   EX        R0,COPENTRY         WAS LAST CARD READ $ENTRY?
         AIF       ('&CONTROL'(1,1) EQ '&CONTROL').MAIN37          V1L4
         BE        MAIN40                                          V1L4
         EX        R0,COPCENTR                                     V1L4
.MAIN37  ANOP                                                      V1L4
         BNE       MAIN41              NO SO DON'T PRINT IT
MAIN40   MVC       XCARD1+2(5),KBL5    BLANK OUT ISN FIELD
         AIF       ('&PAGSKIP' EQ '1').MPAG
         MVI       XCARD1,C'&PAGSKIP'  SET CARRIAGE CONTROL CHAR
.MPAG    ANOP
         SPRIN     XCARD1,90           PRINT THE CARD
         MVI       XCARD1,C' '         AND RESET CARRIAGE CONTROL CHAR
         SPACE
MAIN41   CLINK1    BENDSRS             END OF SOURCE PROGRAM INPUT
         SPACE
         L         R3,CGVBEG       PTR TO START OF GVLIST
         LTR       R3,R3           DID WE COMPILE A PROGRAM?
         BZ        XSTOP           NO SO LET'S FIND THE NEXT ONE
         AGO       .NOLOOK                                         V1L5
         AIF       ('&STATUSP' EQ 'CORE').NOLOOK                   V1L5
         TM        KOMPSW,SPROF1SW        WAS üPROFON CARD FOUND?  V1L5
         BNO       MNOPROF             BR IF NO                    V1L5
         LR        R3,R8               SAVE R8                     V1L5
         LA        R8,PROF@-4                                      V1L5
         LOOKUP    GLBLVAR                                         V1L5
         MVI       2(R15),FUNC+REAL4   TYPE AS FUNCTION            V1L5
         LR        R8,R3               RESTORE R8                  V1L5
MNOPROF  DS        0H                                              V1L5
.NOLOOK  ANOP                                                      V1L5
         CLINK1    MLIBR           OFF TO FIND UNRESOLVED NAMES
         CLINK1    RPH3            OFF TO RELOC PHASE THREE
         AIF       ('&SNAPS' EQ 'OMIT').MSN1
***********************************************************************
* DEBUG VERSION ONLY - DUMPS COMPLETE OBJECT CODE                     *
***********************************************************************
         IF        (CMOSWTCH,ON),MAIN42     WAS THERE ENOUGH ROOM?
         L         R1,XOBJECT          NO SO GET START OF WORKAREA
         L         R2,XBEGDATA         START OF DATA AREA
         S         R2,XOBJECT
         BCTR      R2,0
         CSNAP     OBJECT
MAIN42   DC        0H'0'
***********************************************************************
.MSN1    ANOP
         LA        R5,ZR5+31       WE COULD OVER-WRITE  THIS MUCH
         C         R5,XENDOBJ      IS IT PAST END OF WORK AREA
         BH        MO1ERR              YES - SO ERROR              V1L3
         C         R5,XBEGDATA         IS IT LESS THAN XBEGDAT     V1L3
         BH        MAIN45              NO SO OK TO PROCEED         V1L3
MO1ERR   EQU       *                                               V1L3
         $ERROR    (NOEX,MO,1)     WELL WELL WORK AREA IS FULL     V1L5
MAIN45   DS        0H                                              V1L5
         CLC       XTIME(4),KF0        WAS TIME=0 SPECIFIED?       V1L5
         BE        XSTOP               YES -> DO NOT EXECUTE       V1L5
         CLI       XERRSWT,XFATAL      WERE THERE ANY FATAL ERRORS V1L5
         BH        XSTOP           SUPERFATAL INHIBIT EXECUTION
         BL        MAIN51          NO FATAL ERRORS
         IF        (CUNDEFSW,NE,X'FF'),XSTOP  CONTINUE ONLY IF RUN=FREE
MAIN51   DC        0H'0'
         AIF       ('&MEMTYPE' NE 'MAX').MTYPE3
**********************************************************************
*        FREE ALL UNUSED CORE                                         *
**********************************************************************
         L         R1,XENDDATA   PTR OF END DATA AREA
         A         R1,KF32   SAVE SOME CORE FOR ZAPPING ARRAYS     V1L4
         IF        (CVRFMTSW,OFF),MAIN52   WERE THER VAR FMT'S
         A         R1,MVARFORM         SAVE SOME CORE FOR OBJ FORMAT
         C         R1,XENDOBJ          ARE WE PAST ENDOOF WORKAREA V1L4
         BH        MO1ERR              YES -> INSUFFICIENT MEM MSG V1L4
MAIN52   ST        R1,MVALUESF          PUT IN INFO AREA
         L         R2,XENDOBJ           AND NOW LET'S FIND THE
         SR        R2,R1                LENGTH OF THE AREA TO BE
         ST        R1,XENDOBJ           FREED.
         ST        R2,MVALUESF+4        SAVE IT IN INFO AREA
         LA        R1,MSCBF             ADDR OF INFO AREA
         CLINK1    JFREE                AND AWAY TO FREE.CORE NOT USED
         CSNAP     FREEM2,XOBJECT,20
***********************************************************************
.MTYPE3  ANOP
         SPACE
         CLINK1    JTCANCEL            END OF COMPILATION
         AIF       (&NDVSD).D6                                     DVS2
         SRDL      R0,32               SHIFT TIME REMAINING TO R1  DVS1
         D         R0,=F'100'          CONVERT TO SECONDS          DVS1
         ST        R1,MTIMER2          SAVE TIME LEFT FOR ACCT'ING DVS1
         AGO       .D7                                             DVS2
.D6      ANOP                                                      DVS2
         ST        R0,MTIMER2          SAVE TIME LEFT FOR ACCT'ING
.D7      ANOP                                                      DVS2
         CLINK1    BENDCOMP            END OF COMPILATION TIMING
         SPACE
         LA        R1,XTIME            SET UP MAX RUN TIME
         L         R0,MAXTIMNT         TIMER INTERRUPT ROUTINE ADDR
         CLINK1    JSTIMER             OFF TO SET TIMER
         EJECT
         $FREE     RC                                              V1L5
         SPACE
*******************************************************************
*********          UNDEFINE THE ARRAY AREA               **********
*******************************************************************
XSTART   LM        R9,R10,XBEGDATA   BEGINNING AND END OF ARRAY
         SR        R10,R9            AREA.IF ZERO NO ZAP REQ'D
         BZ        XZAPD99
         LR        R1,R9             SAVE STARTING POINT
         LA        R0,32             LENGTH OF ZAAAAP
         LA        R10,ZR10+31       ADD ON FUDGE FACTER
         SRL       R10,5             DIVIDE BY 32
         LM        R2,R5,XUNDEF      GET SPECIAL CODE
         LM        R6,R9,XUNDEF      HERE TOO
         BALR      R15,0             AND SET UP ZAP LOOP
         STM       R2,R9,ZR1         START STORING X'80808080'S
         AR        R1,R0             ALL THRU THE ARRAY AREA
         BCTR      R10,R15           IF NOT ENOUGH BACK FOR MORE
XZAPD99  DC        0H'0'             FINALLY WE'RE DONE
***********************************************************************
*****    ZERO FR6 FOR WATFOR ARITHMETIC                           *****
***********************************************************************
         SDR       F6,F6                                           V1L4
***********************************************************************
*        GO DO THE DATA STATEMENTS VIA ADDR IN XENTRYPD               *
*        DATA STATEMENTS RETURN TO XSTART10                           *
***********************************************************************
         THEN      (XEXECSW,ON)       SET ON RUN-TIME SWT
         THEN      (XDNTRZSW,ON)      DONT-TRACE-BACK SWITCH
         L         R11,XENTRYPD        GET A(1ST DATA STATEMENT)
         BR        R11                 GO THERE AND RETURN TO XSTART10
         $FREE     R11                                             V1L5
****     DATA STATEMENTS ARE CHAINED TO XSTART10
         USING     XSTART10,R11
XSTART10 LA        R13,XSAVER          XDATA HAS USED R13.RESTORE IT.
         THEN      (XDNTRZSW,OFF)            GIVE TRCBCK IF ERROR
         CLI       XERRSWT,XFATAL      ANY FATAL ERRORS IN DATA STMTS
         BL        XSTART11            NO
         CLI       CUNDEFSW,X'FF'      CONTINUE ONLY IF RUN=FREE
         BNE       XSTOP
XSTART11 EQU       *
         SPACE
         AIF       ('&TRACE' EQ 'OMIT').MTR1
         IF        (COPT,EQ,C'S'),THEN,(DO,XTRACON)
.MTR1    ANOP
***********************************************************************
*        ' CALL ' THE MAIN PROGRAMME OR XERRSR9
***********************************************************************
         LA        R14,XSTOP           RETURN ADDRESS FRO MAIN PROGRAM
         L         R3,XENTRYP          INITIALIZED TO A(XERRSR9)
         AIF       ('&VM' NE 'USE').XVM999                         VM1
         IF        (XEXEC1SW,OFF,XVMASK),XVM999   XDEBUG MODE?     VM1
         MVI       XVMDSWT,X'00'       TURN ON MONITORING          VM1
XVM999   DS        0H                                              VM1
.XVM999  ANOP                                                      VM1
         CNOP      2,4
         BALR      R1,R3
         DC        B'00010001',AL3(0)
         SPACE
XERRSR9  $ERROR    (NOAC,SR,7),XSTOP   RETURN TO XSTOP             V1L5
         SPACE
         $FREE     R11                                             V1L5
         EJECT
         USING     CSAVER,RC
         SPACE
MAINMO4  $ERROR    (NOAC,MO,4)         NO ROOM TO DO ANYTHING      V1L5
         SPACE     1                                               V1L4
***********************************************************************
* CLOSE UP SHOP AND GO HOME
***********************************************************************
MSTOP    BALR      RP,0              SET UP BASE FOR THIS PART
         USING     *,RP
         L         RC,MCSAVER          RESET R10 FOR CLINK1 IF BOMBED
***********************************************************************
         AIF       ('&MEMTYPE' NE 'MAX').MTYPE5
         L         R1,XOBJECT         START OF WORK AREA
         L         R2,XENDOBJ         END OF WORKAREA
         SR        R2,R1              LENGHT OF WORKAREA
         BNP       MNOCORE2            BIF NOTHING TO FREE         V1L4
         ST        R2,MVALUESG+4      SAVE IN INFO BLOCK
         LA        R1,MSCBG           ADDR OF INFO BLOCK
         CLINK1    JFREE              FREE ALL CORE USED(WORKAREA)
***********************************************************************
.MTYPE5  ANOP
MNOCORE2 EQU       *
         AIF   (      &DVSD      ).MLN2                            DVS1
         AIF       ('&LIBRARY' EQ 'NO').MLN2
MAIN62   B         MAIN61              B/NOP
         LA        R1,MSCBL            ADDR OF INFO BLOCK FOR WATLIB
         CLINK1    JFREE               FREE WATLIB BUFFER
         CLINK1    JCLOSLIB            CLOSE WATLIB
MAIN61   DC        0H'0'
.MLN2    ANOP
         IF        (XEXECSW,OFF),MNOCORE1  DID PROGRAM EXECUTE?    V1L5
*        IF WE BOMB WE MIGHT HAVE TO CLOSE FIOCS AND DIOCS
         L         R1,XAFIOCS          CLOSE SEQUENTIAL FILES
         BALR      R0,R1              OFF TO FIOCS VIA WATIO
         DC        X'0400'            'CLOSE' CODE
MNOCORE1 DS        0H                                              V1L5
         L         R1,=A(XAIBCNT1)     OFF TO CLOSE DIR ACC FILES  V1L5
         L         R1,ZR1
         BALR      R0,R1               OFF TO DIOCS VIA WATIO
         DC        X'0400'             'CLOSE' CODE
         AIF       ('&WHOSIT' EQ 'WATFIV').MLN3                    V1L5
         L         R1,=A(DEBUGSW)      DEBUG -> LOCAL VERSION ONLY UOW
         CLI       0(R1),ZSWON         OMIT PRINTING OF $STOP CARD UOW
         BE        MBSTOP              UNDER DEBUG MODE            UOW
.MLN3    ANOP                                                      V1L5
         MVI       XCARD1,C'-'         SET CARRIAGE CONTROL CHAR   V1L5
         LA        R2,XOUTDSRN        SET UP FOR PRINT
         L         R1,XAFIOCS
         BALR      R0,R1               OFF TO OUTPUT ROUTINE
         DC        X'00FF'            'BCD OUTPUT' CODE
         SPRIN     XCARD1,90           AND PRINT THE $STOP CARD
MBSTOP   DS        0H                                              V1L5
         AIF       ('&ERROPT' NE 'READ').NOCLOSE                   V1L4
         CLINK1    ERRCLOSE            CLOSE ERROR TEXT FILE       V1L4
.NOCLOSE ANOP                                                      V1L4
MCLOSE   EQU       *                   CLOSE I/O UNITS AND RETURN  V1L4
         CLINK1    BSTOP               END OF BATCH ACCOUNTING     V1L5
         L         R1,XAFIOCS         NOW WE CAN CLOSE THE READER
         BALR      R0,R1              AND PRINTER(&READ AND &PRINT)
         DC        X'05'               CLOSE CODE FOR WATIO
         DC        X'00'
***********************************************************************
*        FINALLY READY TO RETURN TO THE CALLER                        *
*        REG 15 SET TO:   ZERO FOR A NORMAL RETURN(NO COMPILER ERROR) *
*                         FOUR --COMPILER ERROR(ABLE TO RECOVER AND   *
*                                 EXIT AFTER CLOSING ALL FILES)       *
*                    EIGHT  COMPILER ERROR(IN THIS CASE WE EXIT FROM  *
*                                THE INTERUPT ROUTINE)                *
***********************************************************************
         L         13,XSAVER+4
         SR        R15,R15
         IC        R15,JMRUPTSW        GET RETURN CODE
         ST        R15,16(R13)         PUT IN SAVE AREA
         LM        14,12,12(13) RELOAD REGISTERS, 15 MAY BE RETURN CODE
         BR        14                  RETURN TO SYSTEM
         SPACE     1                                               V1L4
         $FREE     RP                                              V1L5
         EJECT
***********************************************************************
*             LIBRARY SEARCH ROUTINE
***********************************************************************
*        USING THE LLIST IN SYMTAB  AS INPUT THIS ROUTINE DETERMINES
*        IF A NAME(IN LLIST) IS 1. A FORTRAN LIBRARY FUNCTION IN CORE
*                               2. IN WATLIB
***********************************************************************
MLIBR    CENT      MSAVER              LIBRARY LOOKUP ROUTINE
         AIF       ('&LIBRARY' EQ 'NO').MLIBR1
         MVC       CLISTSW,CLIBLSW     GET LIST SWT FOR LIB
         THEN      (CSUBRDS,ON)        SET UP TO READ FROM WATLIB
         THEN      (CIHGACRD,OFF)      OFF I HAVE GOT A CARD SWT
         MVC       MSCARD,XCARD        AND SAVE CURRENT CARD IMAGE
.MLIBR1  ANOP
         L         R4,CLBEG            START OF LIBRARY LIST
         USING     LLIST,R4
         BAL       RLOOP,MLIBR10       SET UP LOOKUP LOOP
         USING     *,RLOOP
         SH        R4,LLINK            LENGHT OF EACH ENTRY
MLIBR10  C         R4,CLEND            ARE WE AT END OF LIST?
         BE        MLIBR99             YES SO AWAY
         IF        (LB2,ON,B'00000010'),LOOP
         IF        (LB1,EQ,X'07'),LOOP     IF A PSEUDO-REGISTER, OK
         IF        (LB2,ON,EXTRN),MLIBR11  YES -> ISSUE FIND       V1L5
         L         R8,MAFLBASE         R8->F-LIST IN DECK FUNCTION
         USING     FLIST,R8
         CSNAP     MLIBLOOK,LLIST,16
         L         R0,LNAME            MOVE NAME
         LH        R1,LNAME+4            TO DUMMY
         ST        R0,FNAME                ENTRY AT
         STH       R1,FNAME+4                START OF F-LIST
         SR        R7,R7               USED AS INDEX
         LR        R2,R7
MLHIGH   IC        R2,FHIPTR(R7)       YOU TAKE THE HIGH ROAD
         LR        R7,R2
         MH        R7,KH12
         CL        R0,FNAME(R7)        CHECK 1ST 4 CHARACTERS
         BH        MLHIGH
         BE        MLEQUAL
MLLOW    IC        R2,FLOWPTR(R7)      AND I'LL TAKE THE LOW ROAD
         BL        MLHIGH+4
MLEQUAL  LA        R1,FLIST(R7)
         CR        R1,R8               CHECK IF IT WAS FOUND IN DUMMY
         BE        MLIBR11             YES => NOT IN CORE
         CLC       LNAME+4(2),FNAME+4-FLIST+ZR1
         BL        MLLOW                GIVE LOW THE BIAS THIS TIME
         BH        MLHIGH
         CSNAP     MLIBFIND,LLIST,16
         CLC       LB1(1),FTYPE-FLIST+ZR1  TEST MODE AGREEMENT
         BE        MLSETADR            OK
         IF        (LB2,ON,USAGE2),MLSETADR  MAYBE WE'RE NOT SURE
         $ERROR    (NOAC,SR,9,NALR4)                               V1L5
MLSETADR MVC       LADDR+1(3),FADDR-FLIST+ZR1  SET THE RTN ADDRESS
         BR        RLOOP
         $FREE     R8                                              V1L5
MLIBR11  DC        0H'0'
         AIF       ('&LIBRARY' EQ 'NO').MLIBR2
MLIBR12  B         MLNOTIN             BRANCH / NOP
         MVC       XCARD(6),LNAME      GET NAME OF SUBPROGRAMME
         MVC       XCARD+6(6),KBL6     PAD IT WITH BLANKS
         LA        R0,XCARD            ADDR OF NAME FOR FIND
         CLINK1    JFIND               ISSUE O.S. FIND
         N         R15,KM2431          SET UP CODE RETURNED
         B         MLJUMP(R15)         AND AWAY
MLJUMP   B         MLOK                WE FOUND IT
         B         MLNOTIN             WE DIDN'T FIND IT
         $ERROR    (NOEX,UN,F),XSTOP   I/O ERROR WOOPS             V1L5
MLOK     EQU       *           NOTE THAT FIND SETS UP WATLIB DCB
         AIF       ('&LIBMODE' EQ 'SAME').MLOK1
         AIF       ('&LIBMODE' EQ 'EBCDIC').MLOK2
         MVI       XMODEKP,X'00'     SET KP MODE(BCDIC)            V1L5
         MVI       XQUOTCON,C'@'     SET UP QUOTE
         AGO       .MLOK3
.MLOK2   MVI       XMODEKP,X'06'     SET KP MODE(EBCDIC)           V1L5
         MVI       XQUOTCON,C''''    SET UP QUOTE
         AGO       .MLOK3
.MLOK1   MVI       XMODEKP,X'0C'     SET KP MODE(SAME AS SOURCE)   V1L5
.MLOK3   ANOP
         MVC       CPOINT(4),CBUFFE    SET SO WE READ FIRST TIME
         CLINK1    SCAN                OFF TO SCAN TO READ
         L         R4,MSAVER+36        RESTORE REG 4
         BR        RLOOP               AND BACK FOR NEXT ENTRY
MLNOTIN  DC        0H'0'
.MLIBR2  ANOP
         MVC       LADDR(4),XAXBOOT    SET ERROR RETURN            V1L2
         CSNAP     SR-0,LLIST,16
         $ERROR    (NOAC,SR,0,NALR4),LOOP                          V1L5
         $FREE     R4                                              V1L5
MLIBR99  DC        0H'0'
         AIF       ('&LIBRARY' EQ 'NO').MLIBR3
         THEN      (CIHGACRD,ON)      RESET I HAVE GOTA CARD SWT
         MVC       XCARD,MSCARD       RESTORE SAVED CARD IMAGE
         THEN      (CSUBRDS,OFF)      TURN LIBRARY READ SWT
         AIF       ('&PAGSKIP' NE '1').MPAGI
         MVI       XCARD1,C'1'        SET CARRIAGE CONTROL CHAR
         SPRIN     XCARD1,4           PRINT THE CARD
.MPAGI   ANOP
.MLIBR3  ANOP
         B         CRET              RETURN TO MAIN
         SPACE
         $FREE     R11,R13                                         V1L5
         SPACE
MSAVER   DS        18F
         AIF       ('&LIBRARY' EQ 'NO').MLIBR4
MSCARD   DS        CL80
.MLIBR4  ANOP
         EJECT
         EJECT
* CONSTANTS
         SPACE
         AIF       ('&MEMTYPE' NE 'MAX').MMEM1
MRANGE   DC        A(&MEMFREE),X'00800000'
MSCBG    DC        A(MRANGE,MVALUESG),AL1(192,0)
MSCBF    DC        A(0,MVALUESF),AL1(192,0)
MSCBS    DC        A(0,MVALUESS),AL1(192,0)
.MMEM1   ANOP
         SPACE
         AIF       ('&LIBRARY' EQ 'NO').MLIB10
MRANGEL  DS        2F
MVALUESL DS        2F
MSCBL    DC        A(MRANGEL,MVALUESL),AL1(192,0)
.MLIB10  ANOP
         SPACE
MAFLBASE DC        A(FDUMMY)           ADDRESS OF F-LIST IN FUNCTION
MAXTIMNT DC        A(XTIMINT)
         SPACE
MSTANOPT DC        F'&LINES'
         DC        F'0'
         DC        F'&PAGES'
         DC        F'0'
         AIF       ('&OPSYS' EQ 'DOS').B1                          DVS1
         AIF       ('&OPSYS' EQ 'DOSVS').B2                        DVS1
         DC        A(&TIME*38400)                                  V1L4
         AGO       .ALLTIM                                         DVS1
.B1      ANOP                                                      DVS1
         DC        A(&TIME*300)                                    DVS1
         AGO       .ALLTIM                                         DVS1
.B2      DC        A(&TIME)                                        DVS1
.ALLTIM  ANOP                                                      DOS2
         SPACE
MSTCKCON DC        0H'0',X'0000',AL1(TERM),X'A8',AL1(SIMV+INT4),3X'00'
MSTKLEN  DC        A(&STCKLEN)
MSTKEND  DC        A(&STCKEND)
MVARFORM DC        A((&VARFORM+7)/8*8) SPACE FOR EXEC TIME FMT STMTV1L5
MCSAVER  DC        A(CSAVER)
MWATLIB  DC        CL8'WATLIB  '
AFIOCS   DC        V(X@FIOC)           A(FIOCS) IN WATIO           V1L4
ADIOCS@  DC        V(XAIBCENT)         A(DIOCS@) IN WATIO          V1L5
         AIF       ('&PROFILE' EQ 'OMIT').PROFX                    V1L5
PROF@    DC        0F'0',CL6'PROF@ '                               V1L5
         AIF       ('&STATUSP' NE 'DISK').PCORE                    V1L5
XAPROF   DC        V(PROF@)            A(PROF@) FILLED IN BY WATL  V1L5
         AGO       .PROFX                                          V1L5
.PCORE   ANOP                                                      V1L5
XAPROF   DC        V(PROF@)            ADDR OF PROF@               V1L5
.PROFX   ANOP                                                      V1L5
         EJECT
* LITERALS
         SPACE
KM2431   DC        X'000000FF'
KF24     DC        F'24'
KF32     DC        F'32'                                           V1L4
KF1000   DC        F'1000'
         SPACE
         LTORG
         TITLE     ' '
         PRINT     &ON(6),&LIST                                    V1L4
         COPY      SYS
         PRINT     &ON(7),&LIST                                    V1L4
         COPY      ACCT
         END
