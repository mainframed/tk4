EROR     TITLE     'ERROR     ERROR MESSAGE EDITOR'
         COPY      OPTIONS
         PRINT     OFF
         $PUNCH    ERROR                                           DVS2
&DECK    SETC      'ERROR'
         COPY      EXTRN
         COPY      CDEFN
STARTA   DSECT
         COPY      STARTA
COMMR    DSECT
         COPY      COMMR
         PRINT     ON
         PRINT     ON,&LIST
         SPACE     2                                               V1L4
*./ DELETE  SEQ1=00120030,SEQ2=00120050                            V1L5
         TITLE     'ERROR     ERROR MESSAGE PROCESSOR'
*******************************************************************
*                                                                 *
*        THIS DECK CONTAINS TWO VERSIONS OF THE ERROR MESSAGE     *
*        PROCESSOR. ONLY ONE VERSION CAN BE ASSEMBLED.            *
*        THE CHOICE OF WHICH VERSION IS ASSEMBLED IS UNDER THE    *
*        CONTROL OF THE GBLC SYMBOL &ERROPT. IT CAN HAVE THREE    *V1L3
*        VALUES. THEY ARE AS FOLLOWS:                             *
*                                                                 *
*          1. A VALUE OF 'FULL' INDICATES THAT THE FIRST VERSION  *
*             IS TO BE ASSEMBLED.THIS ERROR MESSAGE OPTION CAUSES *
*             THE ACTUAL MESSAGE ASSOCIATED WITH THE ERROR TO BE  *
*             PRINTED IN THE FORTRAN SOURCE LISTING.STATEMENT     *
*             RELATED INFORMATION IS INSERTED INTO THE TEXT OF    *
*             THE MESSAGE BY THE TEXT EDITOR.                     *
*                                                                 *
*          2. A VALUE OF 'CODE' INDICATES THAT THE SECOND ERROR   *
*             MESSAGE PROCESSOR IS TO BE ASSEMBLED. THIS OPTION   *
*             ONLY PRINTS A THREE CHARACTER CODE IN THE SOURCE    *
*             LISTING ALONG WITH STATEMENT RELATED INFORMATION.   *
*                                                                 *
*          3. A VALUE OF 'READ' INDICATES FULL TEXTS, ONLY SOME   *V1L3
*             OF WHICH ARE IN CORE, ARE TO BE USED. THIS CAUSES   *V1L3
*             ASSEMBLY OF A VARIATION OF THE 'FULL' VERSION. THE  *V1L3
*             ERRLOCN MACRO IN OPTIONS DEFINES THE TEXTS TO BE    *V1L3
*             GENNED AS CORE-RESIDENT; ALL OTHERS ARE READ AS     *V1L3
*             REQUIRED FROM A DIRECT-ACCESS DATA SET. TWO SUPPORT *V1L3
*             TEXTS (KO9,KOA) ARE AUTOMATICALLY GENNED TO BE      *V1L3
*             CORE-RESIDENT BY ERRLOCN.                           *V1L3
*                                                                 *V1L3
*        THE ADVANTAGE OF THE FIRST SCHEME IS THAT THE USER IS    *
*        GIVEN EXPLICIT INFORMATION IN HIS LISTING AS TO WHERE HE *
*        ERRED. HE NEED CONSULT NO OTHER MATERIAL. ON THE OTHER   *
*        HAND THIS OPTION REQUIRES APPROXIMATELY 14K FOR THE      *
*        MESSAGES TO BE KEPT IN CORE. THE SECOND SCHEME DOES      *
*        NOT HAVE THE  MESSAGES IN-CORE. INSTEAD THE CODE THAT IS *
*        PRINTED IS USED TO REFERENCE A LIST OF THE ERROR         *
*        MESSAGES. THIS PROCEDURE COULD BE TIRESOME,BUT IS        *
*        ESSENTIALLY THE METHOD USED IN WATFOR.                   *
*        THE THIRD SCHEME ALLOWS FULL TEXTS, WITH ONLY FREQUENTLY *V1L3
*        USED TEXTS BEING KEPT IN CORE.                           *V1L3
*                                                                 *
*******************************************************************
         EJECT
SEVERITY DSECT
         SPACE     2
ERCODE   DSECT
         DS        0H,3AL.1,AL.4,AL.9
         DS        AL.1,AL.6,AL.5,AL.4
EREXTRA  EQU       ERCODE
ERBUF    EQU       ERCODE
ERACTION EQU       ERCODE
ERMESS   EQU       ERCODE+2
ERINFO   EQU       ERCODE+2
ERREG    EQU       ERCODE+3
         SPACE     2
***********************************************************************
*   THE ERROR EDIT CONTROL BYTES HAVE THE FOLLOWING FORM:             *
*        DC 0H'0'                                                     *
*        DC AL.1(FLAG),AL.1(BUF),AL.1(0),AL.4(ACTION),AL.9(CODE)      *
*        DC AL.1(0),AL.6(MESSAGE),AL.5(INFO),AL.4(REG)                *
*    WHERE : FLAG=1 IF BYTES 3 AND 4 ARE PRESENT; 0 OTHERWISE         *
*            BUF=1  IF BUFFER CONTENTS TO BE DISPLAYED; 0 OTHERWISE   *
*            ACTION IS A CODE FOR WARN, NOEX, TRAZ, ETC.              *
*            CODE=0-15 IF REAL ERROR CODE IS IN REGISTER CODE         *
*                =16-31 IF REAL ERROR CODE (IN AL2 FORM) IS POINTED   *
*                              TO BY REGISTER CODE-16                 *
*                =REAL ERROR CODE OTHERWISE                           *
*            MESSAGE=0 IF NO MESSAGE IS TO BE SUPPLIED                *
**                  =1-48 IF SPECIAL HOLLERITH-TYPE MESSAGE TO BE USED
*                   =48+N (N=1,2,3,...) WHERE N IS AN INDEX INTO A    *
*                                 EXTENDABLE TABLE OF MESSAGE WORDS   *
*            INFO=0 IF NO ADDITIONAL INFORMATION IS TO BE DISPLAYED   *
*                 OTHERWISE,INFO IS A CODE FOR LOCATION OF ADDITIONAL *
*                           INFORMATION TO BE DISPLAYED,E.G., STACK   *
*            REG IS THE REGISTER THAT POINTS TO THE INFORMATION       *
***********************************************************************
         SPACE     5
         ENTRY     ERROR
         SPACE
ERROR    CSECT
         AIF       ('&ERROPT' EQ 'CODE').CODE                      V1L3
*******************************************************************
*****************        ERROR MESSAGE PROCESSOR     **************
*******************************************************************
         USING     ERROR,R11
         SPACE     5
*-----------------------------------------------------------------*
*        INITIALIZE TEXT EDITOR                                   *
*-----------------------------------------------------------------*
*        LOAD UP R2 FROM ERROR EDIT CONTROL BYTES. EXTRACT THE    *
*        ERROR CODE TO INDEX INTO A TABLE OF POINTERS TO TEXTS.   *
*        USE R2 AS A POINTER TO THE MESSAGE TEXT. AT THE FRONT IS *
*        A ONE BYTE COUNT FIELD.THIS IS RETRIEVED AND SAVED.      *
*-----------------------------------------------------------------*
         USING     ERCODE,R1
         IC        R2,ERACTION         GET                         V1L3
         SRL       R2,1                ACTION/SEVERITY             V1L3
         N         R2,KM2831           CODE                        V1L3
         STH       R2,ESAVACTN         SAVE IT                     V1L3
         IFALL     (ESAVACTN+1,EQ,0),(CEXTNSW,OFF),EINCR           V1L3
         IFALL     (ESAVACTN+1,EQ,1),(CWARNSW,OFF),EINCR           V1L3
         ST        R14,ER14RET                                     V1L3
         ST        R1,EPARMADD                                     V1L3
         LH        R2,ERCODE           GET EDIT CONTROL BYTES
         N         R2,KM2331           GET THE ERROR CODE
         C         R2,KF31             IS IT REAL CODE?
         BH        EREGULAR
         C         R2,KF15             IS IT A POINTER?
         SLL       R2,2
         BNH       EPOINTER
EINREG   L         R2,XHELPS-16*4(R2)  LOAD REAL CODE
         B         EREGULAR
EPOINTER L         R2,XHELPS(R2)       LOAD POINTER REGISTER
         LH        R2,ZR2              LOAD REAL CODE
EREGULAR STH       R2,ESAVCODE         SAVE THE CODE
         AIF       ('&ERROPT' NE 'READ').FULL1                     V1L3
         MVI       EXDAPSW+1,BRA    ASSUME DON'T HAVE TO READ TEXT V1L3
         CH        R2,ELASTRED    IS TEXT LAST ONE READ IN?        V1L3
         BNE       ETRYCORE            BRANCH IF NOT               V1L3
         LA        R2,ETEXTBUF         IF SO, USE AGAIN            V1L3
         B         EGOTTEXT                                        V1L3
ETRYCORE CH        R2,EMINERR          LESS THAN LEAST IN CORE?    V1L3
         BL        EREADTXT       B IF YES TO READ                 V1L3
         CH        R2,EMAXERR       GREATER THAN GREATEST IN CORE? V1L3
         BH        EREADTXT       B IF YES TO READ                 V1L3
         LR        R3,R2          COMPUTE OFFSET                   V1L3
         SH        R3,EMINERR       INTO FIRST LEVEL TABLE         V1L3
         LA        R3,ERRTAB(R3)     POINT R3 TO FIRST LEVEL TABLE V1L3
         IF        (0(R3),NE,0),ECORETXT    IS TEXT IN CORE?       V1L3
EREADTXT STH       R2,ELASTRED    IF NOT, WE MUST READ IT          V1L3
         S         R2,KF32          REDUCE ERROR NUMBER TO 0,1,2...V1L3
         AIF       ('&STATS' EQ 'NO').ERR00                        UOW
         LR        R3,R2               (R3) = RELATIVE ERROR #     UOW
         SLA       R3,1                TIMES 2                     UOW
         LH        R1,ERTABLE(R3)      INDEX INTO ERROR TABLE      UOW
         AH        R1,KH1              BUMP COUNT BY 1             UOW
         STH       R1,ERTABLE(R3)      UPDATE ERROR STATS          UOW
.ERR00   ANOP                                                      UOW
         LR        R1,R2               GET SET                     V1L3
         SR        R0,R0               TO DIVIDE                   V1L3
         D         R0,BLKPRTRK      COMPUTE RELATIVE TRACK & REC   V1L3
         AIF                  (&NDVSD).ERRD1                       DVS1
         A         R0,KF1              BUMP BY 1                   DOS3
         STC       R0,ERRSEK+6         STASH REC. NO. IN SEEK FIELDDOS3
         LH        R2,TRKPRCYL         GET NO. TRK/CYL             DOS3
         SR        R0,R0               CLEAR FOR DIVIDE            DOS3
         DR        R0,R2               GET REL CYL IN R1,TRK IN R0 DOS3
         AH        R1,LOEXTENT         ADD BEG CYL TO REL CYL      DOS3
         AH        R0,LOEXTENT+2       ADD BEG TRK TO REL TRK      DOS3
         CR        R0,R2               IS TRK GT MAX TRK/CYL       DOS3
         BL        TRKOK               BR IF NO                    DOS3
         LA        R1,1(R1)            BUMP CYL                    DOS3
         SR        R0,R2               DUMP TRK                    DOS3
TRKOK    CH        R1,HIEXTENT         CYL GE HI EXTENT LIMIT      DOS3
         BL        EXTOK               BR IF NO                    DOS3
         BH        EXTKOA              ERROR IF HIGH               DOS3
         CH        R0,HIEXTENT+2       TRK GT HI EXTENT LIMIT      DOS3
         BH        EXTKOA              ERROR IF YES                DOS3
EXTOK    STH       R1,ERRSEK+2         STASH CYL IN SEEK FIELD     DOS3
         STH       R0,ERRSEK+4         STASH TRK IN SEEK FIELD     DOS3
         MVI       EXDAPSW+1,NOP       SET SW FOR WAIT ON DISK     DOS3
         EXCP      ERRCCB              DO DISK I/O                 DOS3
         AGO       .ERRO1                                          DOS3
.ERRD1   ANOP                                                      DOS3
         SLL       R1,8                QUOTIENT IS TT              V1L4
         A         R0,KF1              BUMP BY 1                   V1L3
         OR        R0,R1               FORM 0TTR IN R0             V1L4
         MVI       EXDAPSW+1,NOP       SET SWITCH FOR ECB TEST     V1L3
         BAL       R3,EDOXDAP          GO READ ERROR TEXT BLOCK    V1L4
.ERRO1   ANOP                                                      DOS3
         L         R1,EPARMADD         RESTORE R1                  V1L3
         B         EINIT               PROCEED                     V1L3
ECORETXT SR        R2,R2               IF IN CORE                  V1L3
         IC        R2,0(R3)       GET PNTR TO SECOND LEVEL TABLE   V1L3
         AR        R2,R2               DOUBLE IT                   V1L3
         LA        R2,ERR2TAB-2(R2)   POINT R2 INTO ERRTAB         V1L3
         AGO       .FULL2                                          V1L3
.FULL1   ANOP                                                      V1L3
         AR        R2,R2               DOUBLE IT FOR INDEXING
         LA        R2,ERRTAB-32*2(R2)  POINT R2 INTO ERRTAB
.FULL2   ANOP                                                      V1L3
         LH        R2,ZR2              LOAD AL2(TEXT-ERRTEXT)
         LA        R2,ERRTEXT(R2)      POINT R2 AT TEXT ENTRY
EGOTTEXT SR        R0,R0                                           V1L3
         IC        R0,ZR2              LENGTH-1 OF TEXT ENTRY      V1L3
         STH       R0,ETXTLEN          SAVE IT
         LA        R2,ZR2+1            SKIP OVER LENGTH BYTE
         ST        R2,EATEXT           SAVE IT
         SPACE
*-----------------------------------------------------------------*
*        DO OTHER INITIALIZATION                                  *
*-----------------------------------------------------------------*
         SPACE
EINIT    MVC       XPAD1+1(XBUFLN-1),XPAD1  BLANK WORK AREA        V1L3
         L         R0,XBUFFER               SAVE INPUT BUFFER ADDRESS
         ST        R0,EABUFF2
         LA        R3,XERBUF           R3 IS WORK AREA POINTER
         SPACE
**  CHECK FOR PRESENCE OF MESSAGE OR INFO CODES
         IF        (EREXTRA,OFF,X'80'),XERDONE
**  CHECK FOR MESSAGE OF HOL TYPE
         IC        R7,ERMESS
         SRL       R7,1
         N         R7,KM2631
         BZ        XNOMES              0 IF NO MESSAGE
         C         R7,KF48
**  ONLY MESSAGES OF HOL TYPE ARE GIVEN SINCE TEXT EDITOR INSERTS
**  STANDARD MESSAGES WORDS
         BH        XNOMES              HIGH IF NOT HOL TYPE
         LR        R4,R7               R7 CONTAINS 16*REG+LENGTH
         N         R4,KM2427
         SRL       R4,2
         L         R4,XHELPS(R4)
         BCTR      R4,0                ADDRESS OF MESSAGE-1 IN R4
         N         R7,KM2831           NUMBER OF BYTES IN R7
         BCTR      R7,0                -1
         EX        R7,XMOVMES          MOVE MESSAGE TO WORK AREA
         LA        R3,ZR3+2(R7)        SPACE R3 OVER MESSAGE & 1 BLNK
**  CHECK IF WE SUPPLY ANY EXTRA INFO FROM STACK OR SYMBOL TABLE
XNOMES   IC        R7,ERREG            PICKUP INFO REGISTER
         N         R7,KM2831           GET THE REGISTER & ZERO R7
         SLL       R7,2                REGISTER*4
         L         R2,XHELPS(R7)       SETUP R2 TO POINT TO ANY INFO
         MVI       XSWERMES+1,NOP
**       DETERMINE WHICH ERROR PROCESSOR
         LH        R7,ERINFO
         SRL       R7,3
         N         R7,KM2630
         LH        R7,XERTAB2(R7)      ROUTINE ADDRESS
         B         XINFO(R7)           GO TO PROCESSOR
         $FREE     R1                                              V1L5
EINCR    AR        R2,R2                                           V1L3
         LH        R3,XNOEXTNS(R2)                                 V1L3
         AH        R3,KH1                                          V1L3
         STH       R3,XNOEXTNS(R2)                                 V1L3
         BR        R14                                             V1L3
         AIF       ('&STATS' EQ 'NO').ERR01                        UOW
         ENTRY     ERTABLE                                         UOW
ERTABLE  DC        320H'0'                                         UOW
.ERR01   ANOP                                                      UOW
         SPACE     1
XERTAB2  DC        AL2(XNOINFO-XINFO)  0
         DC        AL2(XSYM-XINFO)     1
         DC        AL2(XDEL-XINFO)     2
         DC        AL2(XSTN-XINFO)     3
         DC        AL2(XJSN-XINFO)     4
         DC        AL2(XREL-XINFO)     5
         DC        AL2(XNAM-XINFO)     6
         DC        AL2(XCSN-XINFO)     7
         DC        AL2(XUSN-XINFO)     8
         DC        AL2(XCHR-XINFO)     9
         DC        AL2(XVM8-XINFO)     10
         DC        AL2(XVM6-XINFO)     11
         DC        AL2(XVAR-XINFO)     12
         DC        AL2(XNAV-XINFO)     13
         DC        AL2(XNSS-XINFO)     14
         DC        AL2(XINT-XINFO)     15
         DC        AL2(XARR-XINFO)     16
         DC        AL2(XNAL-XINFO)     17
         DC        AL2(XVLS-XINFO)     18
         DC        AL2(XLIN-XINFO)     19
         DC        AL2(EENT-XINFO)     20
         SPACE     5
*-----------------------------------------------------------------*
*        INDIVIDUAL MESSAGE PROCESSORS                            *
*-----------------------------------------------------------------*
XINFO    DC        0H'0'
**  A CHARACTER TO BE PRINTED
XCHR     MVC       ZR3(1),ZR2
         LA        R3,ZR3+1
         B         XERDONE
         SPACE     2
*   RECREATE SYMTAB ADDRESS USING 2-BYTE POINTER AT 6(R2)
XREL     LH        R2,ZR2+6
         N         R2,KM1631
         A         R2,CSYMBASE
         B         XNAM
         SPACE     2
**  NAME 8 TO LEFT OF PRESENT ADDRESS
XVM8     S         R2,KF2
         SPACE     2
**  NAME 6 TO LEFT OF PRESENT ADDRESS
XVM6     S         R2,KF6
         SPACE     2
**  NAME DIRECTLY AT PRESENT ADDRESS
XVAR     S         R2,KF4
         SPACE     2
*  NAME IN SYMTAB
XNAM     LA        R2,ZR2+2
         SPACE     2
**  NAME IN ENTRY SEQUENCE
EENT     MVC       ZR3(6),ZR2+2
         LA        R3,ZR3+6
         B         XERDONE
         SPACE     2
*  NAME AND ISN
XNAL     MVC       ZR3(6),ZR2+4        SYMBOL NAME
         MVC       ZR3+7(12),=C'USED IN LINE'
         LA        R3,ZR3+20           SPACE OVER IT ALL
         LH        R4,ZR2+10           GET ISN
         B         XSTN2
         SPACE     2
*   VARIABLE LENGTH STRING
XVLS     L         R4,XHELPS+8         GET LENGTH
         BCTR      R4,0                LENGTH - 1
         EX        R4,XVLSMOVE         MOVE TO BUFFER
         LA        R3,ZR3+1(R4)        SKIP OVER STUFF
         B         XERDONE
XVLSMOVE MVC       ZR3(*-*),ZR2
         SPACE     2
*  DELIMITER IN STACK
XDEL2    AH        R2,ZR2
XDEL     CLI       ZR2+2,TERM
         BL        XSYM                IT'S NULL OPERATER,GO FOR SYM
         BH        XDEL1
         MVC       ZR3(16),=C'END-OF-STATEMENT'
         LA        R3,ZR3+16
         B         XERDONE
XDEL1    MVC       ZR3(1),ZR2+2        OPERATER OUT OF STACK
         TR        ZR3(1),XOPTAB
         LA        R3,ZR3+1
XSWERMES NOP       XERDONE             BRA/NOP SWITCH
         MVI       XSWERMES+1,BRA
         MVC       ZR3+1(6),=C'BEFORE'
         LA        R3,ZR3+8
         B         XSYM
         SPACE     2
***      STATEMENT NUMBERS AND ISN'S
*  CURRENT ISN  -  COMPILE OR EXECUTION
XJSN     L         R4,XHELPS+44
         LH        R4,ZR4
         MVC       ZR3(7),=C'IN LINE'
         LA        R3,ZR3+9
         IF        (XEXECSW,ON),XSTN2                              V1L2
         LH        R4,XISN             NO - GET COMPILE TIM ISN
         B         XSTN2                                           V1L2
         SPACE     2
**  ISN FROM SYMTAB AND STATEMENT NUMBER
XUSN     MVC       CSN(2),ZR2+10
         SPACE     2
**  STATEMENTS UNDEFINED AT RELOCATER PHASE 1
XCSN     L         R4,ZR2+4
         SPACE     2
**  ISN SAVED FROM SYMTAB
         CVD       R4,XDFIELD
         MVC       ZR3+1(5),XEDMSK+3
         ED        ZR3(6),XDFIELD+5
         MVC       ZR3+7(12),=C'USED IN LINE'
         LA        R3,ZR3+20           SPACE OVER THIS STUFF
         LH        R4,CSN
         B         XSTN2                                           V1L2
         SPACE     2
**   LINE NUMBER FROM SYMTAB
XLIN     LH        R4,ZR2+10
         B         XSTN2
         SPACE     2
**  STATEMENT NUMBER FROM SYMTAB
XSTN     L         R4,ZR2+4
XSTN2    CVD       R4,XDFIELD
         MVC       ZR3+1(5),XEDMSK+3
         ED        ZR3(6),XDFIELD+5
         LA        R3,ZR3+6
         B         XERDONE
         SPACE     2
**  QUANTITY IN STACK  ( SYMBOL,HOLCON,LITCON,NUMCON)
XSYM     IF        (ZR2+3,EQ,PHI),XDEL2
XTSTSYM  IF        (ZR2+3,NZ,MNAME),XTSTHOL
         SR        R7,R7
         IC        R7,ZR2+3            PICKUP LENGTH CODE
         SLL       R7,2
         BCTR      R7,0
         EX        R7,XMOVNAM
         LA        R3,ZR3+1(R7)
         B         XERDONE
XTSTHOL  IF        (ZR2+3,Z,CHOLL),XTSTCN
         L         R2,ZR2+4            LOAD POINTER TO H-LIST
         L         R2,ZR2+4            LOAD POINTER TO HOLCONST
         MVI       ZR3,C''''
         MVC       ZR3+1(4),ZR2
         LA        R3,ZR3+5
         B         XERDONE
XTSTCN   IF        (ZR2+3,Z,CCONS),XLGCNST
         L         R4,ZR2+4
         N         R4,KM0531           GET RID OF DIGIT COUNT
         CVD       R4,XDFIELD
         MVC       ZR3+1(9),XEDMSK1
         ED        ZR3(10),XDFIELD+3
         LA        R3,ZR3+10
         B         XERDONE
XLGCNST  MVI       ZR3,C'.'
         LA        R3,ZR3+1
         B         XERDONE
         SPACE     2
**  NUMBER OF SUBSCRIPT AND VARIABLE
*   R2 HAS VALUE - 1
*   R4 HAS # OF SS * 4 - 4
*   R4 HAS ADDR OF NAME + 10
XNSS     L         R4,XHELPS+16        OLD R4 = 4*(#SS) -4
         S         R2,KF10             BACK UP TO NAME             V1L4
         IF        (ZR2+7,EQ,X'FC'),XNSS1   IS IT A CHAR*N (N>1)   V1L4
         LA        R4,ZR4+4            NO    4*(# SS)              V1L4
XNSS1    DS        0H                  IGNORED 1ST SS FOR CHAR*N   V1L4
         SRA       R4,2                # OF SS
         STC       R4,ZR3              PLACE IN WORK AREA
         OI        ZR3,X'F0'           MAKE IT EBCDIC
         MVC       ZR3+2(2),=C'OF'
         MVC       ZR3+5(6),ZR2        VARIABLE NAME
         LA        R3,ZR3+11           SKIP OVER THIS STUFF
         CLC       ESAVCODE(2),=AL2(UV3)  THIS UV-3 ?
         BE        XERDONE             THAT'S ALL
         L         R2,XHELPS+8         OLD R2 = VALUE -1
         A         R2,KF1              VALUE
         MVC       ZR3+1(13),=C'HAS THE VALUE'
         LA        R3,ZR3+14           SKIP OVER THIS STUFF
         B         XINT                CONVERT VALUE
         SPACE     2
*   TO PRINT NAME AND VALUE OF VARIABLE
*        THIS ROUTINE PRINTS NAME AND VALUE OF VARIABLE
*        R2 IS  ASSUMED TO CONTAIN THE VALUE OF THE VARIABLE
*        I. E.  XHELPS+8 HAS THIS VALUE
*NAV     IF        (CUNDEFSW,OFF,X'FF'),XERDONE
***  NAMES ARE AROUND EVEN FOR NOCHECK. WHAT A WASTE.
XNAV     MVC       ZR3(6),ZR2+4        MOVE IN NAME
         MVC       ZR3+7(13),=C'HAS THE VALUE'
         LA        R3,ZR3+20
         L         R2,XHELPS+8
         SPACE     2
*        THIS ENTRY PRINTS THE VALUE IN USERS REGISTER 2
XINT     CVD       R2,XDOUBLE
         MVC       ZR3+1(11),XSSMASK
         LA        R1,ZR3+11           FOR EDMK INSTRUCTION
         EDMK      ZR3(12),XDOUBLE+2
         IF        (XDOUBLE+7,Z,X'01'),XINT1 IS IT NEGATIVE?
         BCTR      R1,0
         MVI       0(R1),C'-'        GIVE IT A MINUS SIGN
XINT1    LA        R3,ZR3+12                                       V1L3
         B         XERDONE                                         V1L1
         AIF       ('&DIRACC' NE 'USE').ERDA1
         ORG       *-4                 ORG OVER 'B XERDONE'        V1L1
         CLC       ESAVCODE(2),=AL2(UNC)    IS THIS UNC?
         BNE       XERDONE             IF SO EXTRA GOODIES
         XC        ESAVCODE(2),ESAVCODE     ZAP ESAVCODE
         MVC       ZR3(14),=C',RECORD NUMBER'    FOR UN-C MSG ONLY V1L5
         LA        R3,ZR3+14           BUMP R3 FOR LITERAL         V1L5
         L         R2,XDARECNO         GET THE RECORD NUMBER
         B         XINT                GO AROUND AGAIN
.ERDA1   ANOP
         SPACE     2
* THIS ROUTINE PRINTS THE SUBSCRIPTS FOR AN UNDEFINED ARRAY MEMBER
XARR     S         R2,KF6
XVSS     MVC       ZR3(6),ZR2          MOVE IN NAME
         LR        R5,R3                 FIND FIRST BLANK
XVSS3    LA        R5,ZR5+1
         IF        (ZR5,NE,C' '),XVSS3
         MVI       ZR5,C'('
         LA        R3,ZR5+1            ADVANCE POINTER
         L         R7,XHELPS+12        GET USER'S R3
         S         R7,*-*              SUBTRACT OFF ARRAY BASE
         ORG       *-2                 GIMMICK TO AVOID ALIGNMENT ERROR
         DC        S(ZR2+10)
         SR        R4,R4
         IC        R4,ZR2+14
         SRL       R7,ZR4              DIVIDE BY ELEMENT WIDTH
         SR        R0,R0
         IC        R0,ZR2+10           4*NO.SS.-4 IN R0
         SR        R4,R4
****  THE NEXT LITTLE BIT TAKES CARE OF THE FACT THAT CHARACTER*N
****      ARRAYS ARE DOPED TO HAVE ONE EXTRA DIMENSION.
****      THE TEST FOR X'FC' IS BECAUSE DOT ROUTINES FOR CHARACTER
****      ARRAYS HAVE 'BAL R15,XAN' WITH R12 AS INDEX ,I.E., XAN=D(R12)
****      ORDINARY ARRAYS HAVE R12 AS BASE ,I.E., XAN=D(,R12)
         IF        (ZR2+7,NE,X'FC'),XVSS2  IS IT A CHARACTER*N ARRAY
         LA        R4,4                YES - IGNORE FIRST SUBSCRIPT
         SR        R6,R6
         D         R6,*-*    DIVIDE BY REAL ELEMENT WIDTH
         ORG       *-2                 GIMMICK TO AVOID ALIGNMENT ERROR
         DC        S(ZR2+18)
XVSS2    SR        R6,R6
         D         R6,*-*(R4)          DIVIDE BY DIMENSION
         ORG       *-2                 GIMMICK TO AVOID ALIGNMENT ERROR
         DC        S(ZR2+18)
         LA        R6,ZR6+1            ADD ONE TO GET TRUE VALUE
         CVD       R6,XDOUBLE          R6 IS THEN SUBSCRIPT
         MVC       ZR3+1(11),XSSMASK
         LA        R1,ZR3+10
         EDMK      ZR3(12),XDOUBLE+2
         LA        R5,ZR3+11
         SR        R5,R1
         EX        R5,XVSSMVC
         AR        R5,R3
         MVI       ZR5+1,C','
         LA        R3,ZR5+2
         LA        R4,ZR4+4
         CR        R4,R0
         BNH       XVSS2               NOT DONE
         MVI       ZR5+1,C')'
         MVI       ZR5+2,C' '
         MVC       ZR5+3(9),ZR5+2      CLEAN UP PRINT LINE
         SPACE     5
XNOINFO  EQU       *
*-----------------------------------------------------------------*
*                  DETERMINE MESSAGE TYPE                         *
*        INITIALIZE BUFFER                                        *
*        DETERMINE WHETHER THIS IS A WARNING,EXTENSION OR ERROR.  *
*        PLACE THAT INFORMATION INTO BUFFER.                      *
*-----------------------------------------------------------------*
XERDONE  EQU       *                                               V1L3
***   GO PRIME A BUFFER
         LR        R7,R3                                           V1L3
         LA        R2,XOUTDSRN
         L         R1,XAFIOCS
         BALR      0,1
         DC        X'00'
         DC        X'FF'
         ST        R2,XBUFFER
         LR        R4,R2             SAVE BUFFER PTR IN R4
         LR        R3,R7                                           V1L3
         MVI       ZR4,C' '             BLANK THE BUFFER IN CASE
         MVC       ZR4+1(132),ZR4       FORMCONV PARTLY FILLED IT
** GET THE ACTION/SEVERITY CODE BITS SAVED ABOVE                   V1L3
         LH        R7,ESAVACTN                                     V1L3
         IC        R7,XERTAB1(R7)
         B         XERRTN(R7)          GOTO PARTICULAR RTN
         SPACE     1
XERTAB1  DC        AL1(XLANG-XERRTN)   0
         DC        AL1(XWARN-XERRTN)   1
         DC        AL1(XBUTE-XERRTN)   2
         DC        AL1(XZRRO-XERRTN)   3
         DC        AL1(XNOEX-XERRTN)   4
         DC        AL1(XNOAC-XERRTN)   5
         DC        AL1(XTRAZ-XERRTN)   6
         SPACE     3
*-----------------------------------------------------------------*
*        VARIOUS ROUTINES FOR ACTION/SEVERITY                     *
*-----------------------------------------------------------------*
         USING     SEVERITY-1,R4
XERRTN   DS        0H
XTRAZ    LA        R8,XTRAZ1
         ST        R8,ER14RET
         B         XNOACP4
XTRAZ1   L         R11,XHELPS+44
         B         XTRACEBK
XLANG    LH        R7,XNOEXTNS         NO. OF EXTNS                V1L3
         AH        R7,KH1              ADD 1                       V1L3
         STH       R7,XNOEXTNS         SAVE AGAIN                  V1L3
         MVC       SEVERITY(11),=C'*EXTENSION*'
         CLI       JMRUPTSW,X'00'     CHECK IF ANY MSG ISSUED      V1L4
         BNE       XMES               YES->RETURN CODE ALREADY SET V1L4
         MVI       JMRUPTSW,X'01'     NO->RC=1->EXTENSION          V1L4
         B         XMES
XWARN    LH        R7,XNOWARNS                                     V1L3
         AH        R7,KH1                                          V1L3
         STH       R7,XNOWARNS                                     V1L3
         MVC       SEVERITY(11),=C'**WARNING**'
         CLI       JMRUPTSW,X'02'     CHECK HIGHEST RC SO FAR      V1L4
         BNL       XMES               UPDATE IF NECESSARY          V1L4
         MVI       JMRUPTSW,X'02'     RC=2 -> WARNING ISSUED       V1L4
         B         XMES
XZRRO    LA        R1,EBADARG          =X'FF------'                V1L4
EBADARG  EQU       KFM1                                            V1L4
         LR        R7,R3               SAVE R3 ACROSS CISN,OUTPUT
         B         XOUTBORF
XNOEX    OI        XERRSWT,XINHIBX
         B         XNOACP4
XBUTE    LR        R7,R3               SAVE R3 ACROSS CISN,OUTPUT
         IF        (CISNCDSW,OFF),THEN,(DO,CISN) ENSURE AN ISN
         LA        R1,XBXBOOT
XOUTBORF OUTPUT    4
         ST        R5,XHELPS+20
         LR        R3,R7
XNOAC    OI        XERRSWT,XFATAL
XNOACP4  MVC       SEVERITY(11),=C'***ERROR***'
         LH        R7,XNOERRS                                      V1L3
         AH        R7,KH1                                          V1L3
         STH       R7,XNOERRS                                      V1L3
         IF        (XEXECSW,ON),XRC4  CHECK IF IN EXEC MODE        V1L4
         CLI       JMRUPTSW,X'03'     CHECK HIGHEST RC SO FAR      V1L4
         BNL       XMES               UPDATE IF NECESSARY          V1L4
         MVI       JMRUPTSW,X'03'     RC=3 -> ERROR AT COMPILATION V1L4
         B         XMES               CONTINUE ...                 V1L4
XRC4     CLI       JMRUPTSW,X'04'     IF RC >= 4 THEN              V1L4
         BNL       XMES               DON'T UPDATE RC              V1L4
         MVI       JMRUPTSW,X'04'     RC=4 -> ERROR AT EXEC TIME   V1L4
XMES     EQU       *
         $FREE     R4,R2                                           V1L5
         EJECT
*-----------------------------------------------------------------*
*        ERROR TEXT EDITOR                                        *
*-----------------------------------------------------------------*
*        MERGE MESSAGE TEXT AND STATEMENT RELATED INFORMATION FROM*
*        THE WORK AREA INTO THE BUFFER. MERGING IS UNDER THE      *
*        CONTROL OF THE FOLLOWING SPECIAL CHARACTERS IN THE TEXT: *
*        _,×,% AND " .                                            *
*-----------------------------------------------------------------*
EDIT     LA        R2,ZR4+132          A(END OF BUFFER)
         LA        R4,ZR4+14           THIS IS WHERE TEXT WILL GO  V1L1
         ST        R2,ENDBUFF          STASH IT
         AIF       ('&ERROPT' NE 'READ').FULL3                     V1L3
EXDAPSW  B         EDITIT              B/NOP SWITCH                V1L3
         AIF                  (&NDVSD).ERRD2                       DVS1
         WAIT      ERRCCB                                          DOS3
         AGO       .ERRO2                                          DOS3
.ERRD2   ANOP                                                      DOS3
         WAIT      ECB=EXDAPBLK                                    V1L3
.ERRO2   ANOP                                                      DOS3
         SR        R5,R5               ASSUME TEXT READ IN OK      V1L3
         LA        R7,ETEXTBUF+1                                   V1L3
         IC        R5,ETEXTBUF                                     V1L3
         AIF                  (&NDVSD).ERRD3                       DVS1
         TM        ERRCOM,X'20'        DID WE GET PERMANENT ERROR  DOS3
         BO        EXTKOA              BOOT IF YES                 DOS3
         TM        ERRCOM+1,X'08'      NO RECORD FOUND CONDITION?  DOS3
         BZ        EDITIX              BR IF NO                    DOS3
EXTKOA   EQU       *                                               DOS3
         AGO       .ERRO3                                          DOS3
.ERRD3   ANOP                                                      DOS3
         IF        (EXDAPBLK,EQ,X'7F'),EDITIX   VERIFY THAT        V1L3
.ERRO3   ANOP                                                      DOS3
         L         R7,=A(KOATEXT-1)    FAKE MESSAGE FOR            V1L3
         IC        R5,0(,R7)           POOR BASTARD                V1L3
         LA        R7,1(,R7)                                       V1L3
         XC        ELASTRED(2),ELASTRED   FORCE NO TEXT IN CORE    V1L3
*        CLEAR DCB PERMANENT ERROR FLAGS                           V1L3
         AIF       (      &DVSD      ).ERRD4                       DVS1
         NI        DCBIFLGS+ERRDCB-IHADCB,X'3F'                    V1L3
.ERRD4   ANOP                                                      DOS3
         B         EDITIX                                          V1L3
.FULL3   ANOP                                                      V1L3
EDITIT   L         R7,EATEXT           ADDRESS OF TEXT
         LH        R5,ETXTLEN          LENGTH-1 OF TEXT
         LTR       R5,R5               IS IT NEGATIVE ? IS WE DONE?
         BM        EPRINT              YES,GO PRINT LINE
EDITIX   SR        R2,R2               FOR TRT                     V1L3
         EX        R5,ETRT             DO TRT
         STH       R2,ESTUFTYP         SAVE CODE
         LR        R2,R7
         BZ        ENDTEXT             NO INSERTIONS
ENOTLAST LA        R1,ZR1+1            A(NEXT TEXT BYTE)
         ST        R1,EATEXT           SAVE IT
         SR        R1,R2               LENGTH OF TXT TO PUT INTO BUFFER
         SR        R5,R1               LENGTH-1 OF REST OF TEXT
         STH       R5,ETXTLEN          SAVE LENGTH-1
         S         R1,KF2              LENGTH-1 TO MOVE INTO BUFFER
         BM        EXTRASTF            SPECIAL SYMBOL IS FIRST THING
         THEN      (DO,EBUFFEND)       CHECK FOR END OF BUFFER
         EX        R1,EMOVTEXT         MOVE TEXT INTO BUFFER
         LA        R4,ZR4+1(R1)        POINTER TO NEXT BUFFER LOC'N
EXTRASTF LA        R2,XERBUF           A(EXTRA STUFF)
         LH        R5,ESTUFTYP         RETRIEVE CODE
         B         *(R5)               JUMP ON IT
         B         ESTUFF2
         B         ESTUFF3
         B         ESTUFF4
ESTUFF1  SH        R2,KF8              PUSH POINTER BACK
         MVC       ZR2(7),=C'ON UNIT'
         B         ESTUFF4
ESTUFF2  SH        R2,=H'11'           PUSH POINTER BACK
         MVC       ZR2(10),=C'UNEXPECTED'
         B         ESTUFF4
ESTUFF3  MVC       ZR3+1(10),=C'IS INVALID'
         LA        R3,ZR3+11           ADVANCE POINTER
ESTUFF4  SR        R3,R2               LENGTH OF STUFF
         BZ        EDITIT              IF THERE'S NO STUFF TO INSERT
         LR        R1,R3               GET ANOTHER REGISTER
         BCTR      R1,0                LENGTH-1 TO MOVE
         THEN      (DO,EBUFFEND)       CHECK FOR END OF BUFFER
         EX        R1,EMOVTEXT         MOVE EXTRA STUFF TO BUFFER
         LA        R4,ZR4+1(R1)        POINTER TO NEXT BUFFER LOC'N
         B         EDITIT              SEE IF THERE'S MORE
         SPACE     1                                               V1L3
ETRT     TRT       ZR7(*-*),ETRTABL                                V1L3
         SPACE     3                                               V1L3
EBUFFEND L         R0,ENDBUFF          A(END OF BUFFER)
         LR        R5,R1
         AR        R5,R4     THIS IS WHERE END OF TEXT WOULD BE IN BUFF
         CR        R5,R0               OVERFLOW ?
         BLR       R14                 NO
         LR        R6,R1               SAVE
         SR        R5,R0               # OF BYTES OF OVERHANG
         SR        R6,R5               # OF BYTES - 1 THAT WILL FIT
         LA        R5,ZR2+1(R6)        ADDR OF FIRST OVERHANG BYTE
EBLANK   IF        (ZR5,EQ,C' '),ENDWORD    END OF A WORD ?
         BCT       R5,EBLANK           NOPE,SO LOOP
ENDWORD  LA        R6,ZR5+1            YES,NEXT TEXT BYTE ADDRESS
         SR        R5,R2               LENGTH TO MOVE TO BUFFER
         BCTR      R5,0                LENGTH-1
         EX        R5,EMOVTEXT         MOVE 1ST PART TO BUFFER
         SR        R1,R5               # OF OVERHANG BYTES
         BCTR      R1,0                # OF BYTES -1 LEFT TO MOVE
         BCTR      R1,0                DECREMENT ONCE MORE         V1L5
         LR        R2,R6               ADDRESS OF REMAINDER
*        PRINT OUT CURRENT BUFFER AND GET ANOTHER ONE
         STM       R14,R2,XPRINTS
         L         R3,XBUFFER
         LA        R2,133
         THEN      (DO,XPRNTERR)
         LR        R4,R2               SAVE BUFFER POINTER
         ST        R2,XBUFFER          SAVE BUFFER POINTER
         LM        R14,R2,XPRINTS
         LA        R4,ZR4+16           CONTINUATION COLUMN
         LA        R3,ZR4+132          END OF BUFFER
         ST        R3,ENDBUFF          END OF BUFFER
         BR        R14
         SPACE     2
ENDTEXT  LR        R1,R5               LENGTH-1
         THEN      (DO,EBUFFEND)       CHECK FOR BUFFER OVERFLOW
         EX        R1,EMOVTEXT         MOVE TEXT TO BUFFER
EPRINT   L         R3,XBUFFER
         CLC       ESAVCODE(2),=AL2(SR1)   SR-1 ?                  V1L5
         BNE       XTRYSR4                 NO                      V1L5
         L         R2,XARGCNT          YES, GET THE ARGUMENT COUNT V1L5
         CVD       R2,XDFIELD          CONVERT IT                  V1L5
         MVC       ZR3+119(3),XEDMSK+5       MOVE IN THE MASK      V1L5
         ED        ZR3+118(4),XDFIELD+6      PUT IN THE NUMBER     V1L5
         B         XERR                                            V1L5
XTRYSR4  CLC       ESAVCODE(2),=AL2(SR4)   SR-4 ?                  V1L5
         BNE       XERR                    NO                      V1L5
         L         R2,XARGCNT          YES, GET THE ARGUMENT COUNT V1L5
         CVD       R2,XDFIELD          CONVERT IT                  V1L5
         MVC       ZR3+47(3),XEDMSK+5        MOVE IN THE MASK      V1L5
         ED        ZR3+46(4),XDFIELD+6       PUT IN THE NUMBER     V1L5
XERR     DS        0H                                              V1L5
         LA        R2,133
         THEN      (DO,XPRNTERR)
         L         R4,EPARMADD         PARM ADDRESS
         USING     ERCODE,R4
         IF        (ERBUF,OFF,X'40'),ERREXIT
         $FREE     R4                                              V1L5
         L         R3,XBUFFER          YES,GET OUTPUT BUFFER ADDRESS
         L         R4,EABUFF2          INPUT BUFFER TO BE DISPLAYED
         MVC       ZR3(ELEN),ECHAR80   MOVE MESSAGE AND
         MVC       ZR3+ELEN(80),ZR4    FIRST 80 CHARS OF INPUT
         MVI       ZR3+ELEN+80,C''''   PUT A QUOTE BEHIND
         LA        R2,133              BUFFER LENGTH
         THEN      (DO,XPRNTERR)       PRINT LINE
ERREXIT  L         R14,ER14RET
         BR        R14
         SPACE     1                                               V1L3
         $FREE     RP                                              V1L5
         SPACE     5                                               V1L3
         AIF                  (&NDVSD).OSCMT                       DVS1
*******************************************************************DOS3
*                                                                 *DOS3
*  ERROPEN - ENTRY TO OPEN ERROR TEXT DATA SET AND DO SOME        *DOS3
*            INITIALIZATION FOR THE 'READ' VERSION. IF '&ERROPT'  *DOS3
*            IS NOT 'READ', THIS IS A DO-NOTHING ENTRY WHICH IS   *DOS3
*            HERE TO SATISFY EXTRN IN MAIN.                       *DOS3
*                                                                 *DOS3
*            TO SAVE SPACE, THE ERROR TEXTS ARE READ IN OVER THIS *DOS3
*            INITIALIZATION CODE.                                 *DOS3
*                                                                 *DOS3
*******************************************************************DOS3
         AGO       .CONCMT                                         DOS3
.OSCMT   ANOP                                                      DOS3
*******************************************************************DVS2
*                                                                 *V1L3
*  ERROPEN - ENTRY TO OPEN ERROR TEXT DATA SET AND DO SOME        *V1L3
*            INITIALIZATION FOR THE 'READ' VERSION. IF '&ERROPT'  *V1L3
*            IS NOT 'READ', THIS IS A DO-NOTHING ENTRY WHICH IS   *V1L3
*            HERE TO SATISFY EXTRN IN MAIN.                       *V1L3
*                                                                 *V1L3
*            INITIALIZATION DONE IS THE CALCULATION OF THE        *V1L3
*            NUMBER OF ERROR TEXT BLOCKS ON A TRACK OF THE DIRECT *V1L3
*            ACCESS DEVICE USED TO HOLD THEM. THE ALGORITHM USED  *V1L3
*            IS BASED ON INFO ABOUT DEVTYPE MACRO IN OS SYSTEM    *V1L3
*            PROGRAMMER'S GUIDE, C28-6550-8, PAGES 223-226.       *V1L3
*                                                                 *V1L3
*            A TEST IS MADE FOR AN RPS DEVICE AND APPROPRIATE     *V1L4
*            INITIALIZATION IS DONE. SEE SECTION ON XDAP IN       *V1L4
*            SYSTEM PROGRAMMER'S GUIDE.                           *V1L4
*                                                                 *V1L4
*            TO SAVE SPACE, THE ERROR TEXTS ARE READ IN OVER THIS *V1L3
*            INITIALIZATION CODE.                                 *V1L3
*                                                                 *V1L3
*******************************************************************V1L3
.CONCMT  ANOP                                                      DOS3
         SPACE     2                                               V1L3
         ENTRY     ERROPEN                                         V1L3
         SPACE     1                                               V1L3
ETEXTBUF DS        0F,CL257            SERVES AS ERROR TEXT BUFFER V1L4
         ORG       ETEXTBUF            BACKUP                      V1L4
ERROPEN  CENT      ERROPSAV                                        V1L3
         AIF       ('&ERROPT' NE 'READ').FULL4                     V1L4
         AIF                  (&NDVSD).ERRD5                       DVS1
         OPEN      ERRDTF              OPEN ERROR FILE             DOS3
         LA        R15,4               ASSUME WE HAVE TO ABORT     DOS3
         CLI       ERROPNSW,X'20'      DID IT OPEN                 DOS3
         BNE       CRET                RETURN IF NO                DOS3
         CLI       ERRDEV,X'20'        IS DEVICE A DISK            DOS3
         BNE       CRET                RETURN IF NO                DOS3
         LH        R1,ENTPC            GET HI TRK ADDR THIS DEV    DOS3
         LA        R1,1(R1)            MAKE MAX TRK/CYL            DOS3
         STH       R1,TRKPRCYL         STASH IT                    DOS2
         SR        R1,R1               CLEAR REG                   DOS3
         IC        R1,ENRPT            GET NO. BLK/TRK+1           DOS3
         BCTR      R1,0                MINUS 1                     DOS3
         ST        R1,BLKPRTRK         STASH NO. BLK/TRK           DOS3
         MVC       ELUNIT(2),ERRDTF+6  MOVE LOGICAL UNIT NO TO CCB DOS3
         MVC       HIEXTENT(4),EXTNTHI GET HIGH EXTENT LIMIT       DOS3
         MVC       LOEXTENT(4),EXTNTLO GET LOWER EXTENT LIMIT      DOS3
         AGO       .FULL4                                          DOS3
.ERRD5   ANOP                                                      DOS3
         L         R11,EAERROR         ADJUST TO COMMON BASE       V1L4
         USING     ERROR,R11                                       V1L4
         DEVTYPE   DCBDDNAM+ERRDCB-IHADCB,ERDVSTUF,DEVTAB          V1L3
         LTR       R15,R15             BAD NEWS FROM DEVTYPE ?     V1L4
         BNZ       ERBADOPN            BIF TROUBLE IN RIVER CITY   V1L4
         CLI       ERRUCBWD+2,DASD     TRYING TO SLIP US A BUMMER ?V1L4
         BNE       ERBADOPN            BIF SO                      V1L4
         OPEN      (ERRDCB,INPUT)                                  V1L3
         IF        (DCBOFLGS+ERRDCB-IHADCB,OFF,X'10'),ERBADOPN     V1L4
         CLI       ESSCCW,SETSECTR     IN CASE RE-ASSEMBLED WITH   V1L4
         BNE       EOLDXDAP              PRE-RPS FORM OF XDAP MACROV1L4
         TM        ERRUCBWD+1,RPSDASD  RPS DEVICE ?                V1L4
         BO        ERITSRPS            BIF SO                      V1L4
         L         R1,EIOBCCWP         ELSE RESET                  V1L4
         LA        R1,8(,R1)             CHANNEL PROGRAM           V1L4
         ST        R1,EIOBCCWP             POINTER IN IOB          V1L4
EOLDXDAP NI        EDOSECTR+1,X'F0'    DISABLE SECTOR CALCULATIONS V1L4
ERITSRPS MVI       ERDCCW,RDCOUNT      SET CCW TO READ COUNT       V1L4
         MVC       ESECTRPT(1),ERRUCBWD+3  SAVE UNIT TYPE FOR LATERV1L4
         L         R1,CVTPTR           THE CVT KNOWS WHERE         V1L4
         MVC       ERRPCNVT,CVTPCNVT(R1)   TO CONVERT TTR'S        V1L4
         MVC       ERROSCR1,CVTOSCR1(R1)   AND SECTORS             V1L4
         SR        R0,R0               SET TO SEARCH RECORD ZERO   V1L4
         BAL       R3,EDOXDAP          GO READ COUNT OF RECORD 1   V1L4
         WAIT      ECB=EXDAPBLK        WAIT FOR IT TO HAPPEN       V1L4
         IF        (EXDAPBLK,NE,X'7F'),ERBADOPN                    V1L4
         MVI       ERDCCW,RDKEYDTA     RESET CCW TO READ DATA      V1L4
         LH        R2,ETEXTBUF+6       GET BLOCK LENGTH            V1L4
         LTR       R2,R2               SHOULD BE POSITIVE          V1L4
         BNP       ERBADOPN            BIF LOOKS BAD               V1L4
         CH        R2,ERCON257         CANT BE .GT. 257            V1L4
         BH        ERBADOPN            BIF TROUBLE                 V1L4
         CLI       ETEXTBUF+5,0        SHOULD BE UNKEYED           V1L4
         BNE       ERBADOPN            BIF NOT                     V1L4
         STH       R2,ERDCCWLN         STASH LENGTH IN CCW         V1L4
         STH       R2,ERPSWORD         AND FOR SECTOR CALCS        V1L4
         LH        R1,ERRMXTRK         GET DEVICE TRACK SIZE       V1L3
         SR        R3,R3               ZAP FOR IC'S                V1L4
         LH        R0,ERRBLOV1         ASSUME UNDOCUMENTED CALC    V1L4
         TM        ERRTOLSW,X'08'      VERIFY FROM MYSTERY BIT     V1L4
         BO        EMYSTERY            BIF ON                      V1L4
         SR        R0,R0                                           V1L4
         IC        R0,ERRBLOV1         GET 1ST BLOCK OVERFLOW      V1L4
         IC        R3,ERRBLOV2         GET LAST BLOCK OVERFLOW     V1L4
         AR        R1,R0               MAXTRK + B1                 V1L4
         SR        R1,R3               - BL                        V1L4
EMYSTERY TM        ERRTOLSW,X'01'      TOLERANCE FACTOR ON BLOCKS ?V1L4
         BNO       *+12                BIF NOT                     V1L4
         MH        R2,ERRTOLFR         TOLERATE                    V1L4
         SRL       R2,9                                            V1L4
         AR        R2,R0               TOLERATED BLOCK + B1        V1L4
         IC        R3,ERRBLOV3                                     V1L4
         SR        R2,R3               - BK    (NO-KEY ADJUSTMENT) V1L4
         SR        R0,R0                                           V1L4
         DR        R0,R2               (MAXTRK+B1-BL)/(TOLBLK+B1-BKV1L4
         ST        R1,BLKPRTRK         STASH BLOCKS PER TRACK      V1L4
.FULL4   SR        R15,R15             GOOD RETURN AHEAD           V1L3
         B         CRET                                            V1L3
         SPACE     1                                               V1L4
         AIF       ('&ERROPT' NE 'READ').FULL41                    V1L4
ERBADOPN LA        R15,4               SIGNAL ERROR                V1L4
         B         CRET                RETURN TO MAIN              V1L4
.FULL41  ANOP                                                      V1L4
         SPACE     1                                               V1L3
         $FREE     R11,R13                                         V1L5
         SPACE     1                                               V1L3
         AIF       ('&ERROPT' NE 'READ').FULL5                     V1L3
         AIF                  (&NDVSD).ERRD6                       DVS1
         CNOP      0,8                                             DOS3
ERRDTF   DC        H'0'                RESIDUAL COUNT              DOS3
         DC        X'8400'             COMMUNICATION BYTES         DOS3
         DC        X'0800'             CSW STATUS BYTES            DOS3
         DC        AL1(1)              LOGICAL UNIT CLASS          DOS3
         DC        AL1(0)              LOGICAL UNIT NUMBER         DOS3
         DC        X'00'                                           DOS3
         DC        AL3(ERRCCW)         CCW ADDRESS                 DOS3
         DC        XL1'0'              STATUS BYTE                 DOS3
         DC        XL3'0'              CSW CCW ADDRESS             DOS3
         DC        XL1'0'                                          DOS3
         DC        XL3'0'              LOGIC MODULE ADDRESS        DOS3
ERROPNSW DC        X'32'               DTF TYPE                    DOS3
         DC        X'02'               OPEN INDICATORS             DOS3
         DC        CL8'WATERRS'        FILE NAME                   DOS3
ERRDEVT  EQU       *-1                                             DOS3
         DC        XL2'00'                                         DOS3
         DC        XL4'00'                                         DOS3
         DC        2X'00'                                          DOS3
         DC        X'08'               OPEN SW                     DOS3
         DC        2X'00'                                          DOS3
         DC        X'20'                                           DOS3
ERRDEV   DC        X'20'               INDICATE DEVICE TO LOGIC    DOS3
         DC        X'F3'               INDICATOR FOR LOGIC         DOS3
         DC        AL1(128)            LOGIC INDICATORS            DOS3
         DC        XL3'0'              IOAREA2 ADDRESS             DOS3
         DC        X'80000000'                                     DOS3
         DC        XL2'0'              LOWER HEAD LIMIT            DOS3
EXTNTHI  DC        4X'00'              EXTENT UPPER LIMIT          DOS3
         DC        H'0'                SEEK BB                     DOS3
         DC        X'0000FF00'         SEEK CCHH                   DOS3
         DC        X'00'               SEEK R                      DOS3
         DC        XL3'00'             EOF ADDRESS                 DOS3
         DC        2X'00'              CC                          DOS3
ENTPC    DC        2X'00'              HH UPPER LIMIT              DOS3
ENRPT    DC        AL1(18+1)           NO. OF REC/TRK+1            DOS3
         DC        X'01'               FRRST RECORD                DOS3
         DC        X'0018'             CONSTANT                    DOS3
EXTNTLO  DC        4X'00'              CCHH COUNT ID               DOS3
         DC        X'01'               COUNT FILED RECORD          DOS3
         DC        X'00'               KEY LENGTH                  DOS3
         DC        H'132'              DATA LENGTH                 DOS3
         DC        XL4'00'             LOAD USER I/O RECORD INS    DOS3
ERRCCB   CCB       SYS000,ERRCCW       ERR FI E CCB                DOS3
ERRCOM   EQU       ERRCCB+2            COMMUNICATIONS BYTES        DOS3
ERRSTAT  EQU       ERRCCB+4            STATUS BYTES                DOS3
ELUNIT   EQU       ERRCCB+6            LOGICAL UNIT CLASS & NUMBER DOS3
ERRCCW   CCW       X'07',ERRSEK,X'40',6     SEEK TRACK             DOS3
         CCW       X'31',ERRSEK+2,X'40',5   SEARCH ID              DOS3
         CCW       X'08',ERRCCW+8,X'20',1   TRANSFER               DOS3
         CCW       X'06',ETEXTBUF,X'20',132 READ                   DOS3
BLKPRTRK DC        F'0'                NO. BLKS PER TRACK          DOS3
ERRSEK   DC        XL6'00'             SEEK ADDRESS FIELD          DOS3
ELASTRED DC        H'0'                LAST ERR CODE READ          DOS3
TRKPRCYL DC        H'0'                NO. TRACKS PER CYL          DOS3
LOEXTENT DS        XL4                 LOW EXTENT LIMIT            DOS3
HIEXTENT DS        XL4                 HIGH EXTENT LIMIT           DOS3
         AGO       .FULL5                                          DOS3
.ERRD6   ANOP                                                      DOS3
         SPACE     1                                               V1L3
EAERROR  DC        A(ERROR)                                        V1L4
ERCON257 DC        H'257'                                          V1L4
         SPACE     1                                               V1L4
         ORG       ,                   TO END OF BUFFER OR WORSE   V1L4
         SPACE     3                                               V1L4
         USING     ERROR,R11           COMMON BASE                 V1L4
EDOXDAP  STC       R0,ERPSWORD+3       STASH RECORD NO. FOR SECTOR V1L4
         SLL       R0,8                FORM TTR0 FROM 0TTR         V1L4
         STM       R8,R15,ETEXTBUF     SAVE SOME REGS              V1L4
         L         R1,DCBDEBAD+ERRDCB-IHADCB  DEB ADDRESS FOR CNVT RTN
         LA        R2,MBBCCHHR         WHERE TO PUT FULL DASD ADDR V1L4
         L         R15,ERRPCNVT        GET TTR CONVERT ADDRESS     V1L4
         LR        R8,R11              RE-BASE ACROSS CNVT RTNS    V1L4
         $FREE     R11                                             V1L5
         USING     ERROR,R8                                        V1L4
         BALR      R14,R15             CONVERT TTR TO FULL DASD ADDR L4
         LM        R15,R2,ERROSCR1     LOAD REGS FOR SECTOR CALC   V1L4
EDOSECTR BALR      R14,R15             BALR 14,15/BALR 14,0 SWITCH V1L4
         LM        R8,R15,ETEXTBUF     RESTORE OUR REGS            V1L4
         $FREE     R8                                              V1L5
         USING     ERROR,R11                                       V1L4
         MVI       EXDAPBLK,0          CLEAR ECB                   V1L4
         XDAP      EXDAPBLK,MF=E       INITIATE READ               V1L4
         BR        R3                  RETURN TO CALLER            V1L4
         $FREE     R11                                             V1L5
         SPACE     1                                               V1L4
ERRPCNVT DS        A                   TTR TO ABS CONVERT RTN      V1L4
*** DON'T MOVE NEXT 4 FIELDS. USED WITH LM R15,R2,...              V1L4
ERROSCR1 DS        A                   RPS SECTOR CONVERT RTN      V1L4
ERPSWORD DC        F'0'                INPUT FOR SECTOR CNVT RTN   V1L4
BLKPRTRK DS        F                   ERROR TEXT BLOCKS PER TRACK V1L4
ESECTRPT DC        AL1(*-*),AL3(SECTOR)    DITTO                   V1L4
ELASTRED DC        H'0'                ERROR NO. OF TEXT LAST READ V1L4
SECTOR   DS        X                   SECTOR VALUE FOR RPS DEVICE V1L4
         SPACE     1                                               V1L4
CVTPTR   EQU       16                  FOUNTAIN OF ALL KNOWLEDGE   V1L4
CVTPCNVT EQU       28                  OFFSET TO SYSTEM TTR CNVT RTV1L4
CVTOSCR1 EQU       232                 OFFSET TO SYSTEM SECTOR RTN V1L4
DASD     EQU       X'20'               DASD DEVICE IN UCBTYP+2     V1L4
RPSDASD  EQU       X'10'               RPS BIT IN UCBTYP+1         V1L4
         SPACE     2                                               V1L4
         XDAP      EXDAPBLK,RI,ERRDCB,ETEXTBUF,8,,MBBCCHHR,        V1L4X
               SECTOR,MF=L             LENGTH SET FOR READ COUNT CCW L4
EXDAPND  EQU       *                   END OF XDAP +1              V1L4
         SPACE     1                                               V1L4
EIOBCCWP EQU       EXDAPBLK+20         OFFSET TO IOB CCW POINTER   V1L4
MBBCCHHR EQU       EXDAPBLK+36         OFFSET TO FULL DASD ADDR    V1L4
E1STCCW  EQU       EXDAPBLK+44         1ST CCW IN XDAP             V1L4
ENUMCCWS EQU       (EXDAPND-E1STCCW)/8 NO. OF CCWS IN XDAP         V1L4
ESSCCW   EQU       E1STCCW             SET SECTOR WILL BE 1ST CCW  V1L4
***  OFFSET TO READ CCW DEPENDS ON PRESENCE OF SET SECTOR CCW IN XDAP
ERDCCW   EQU       E1STCCW+(ENUMCCWS-1)*8  OFFSET TO READ CCW      V1L4
ERDCCWLN EQU       ERDCCW+6            LENGTH FIELD IN READ CCW    V1L4
         SPACE     1                                               V1L4
RDCOUNT  EQU       X'12'               READ-COUNT CCW OPCODE       V1L4
RDKEYDTA EQU       X'0E'               READ-KEY-AND-DATA CCW OPCODEV1L4
SETSECTR EQU       X'23'               SET-SECTOR OPCODE IN CCW    V1L4
         SPACE     2                                               V1L4
ERRDCB   DCB       MACRF=E,DDNAME=WATERRS    SHORT DCB             V1L4
         SPACE     2                                               V1L4
.FULL5   ANOP                                                      V1L4
         SPACE     5                                               V1L4
XMOVMES  MVC       ZR3(*-*),ZR4+1
XMOVNAM  MVC       ZR3(*-*),ZR2+4
EMOVTEXT MVC       ZR4(*-*),ZR2        MOVE STUFF INTO BUFFER
XVSSMVC  MVC       ZR3(*-*),ZR1
XTFIELD  DS        F                   TEMP FIELD FOR MSG LITERAL  V1L5
XDFIELD  DS        D
XEDMSK1  EQU       XEDMASK
XEDMSK   EQU       XEDMASK+1
XBUFLN   EQU       50
XPRINTLN DC        0CL90' '
XPAD1    DC        C' '
XERBUF   EQU       *
ERROPSAV DS        18F                 PUT SAVEAREA IN WORK SPACE  V1L3
         ORG       XPRINTLN+XBUFLN
XPRNTND  EQU       *-1
         ORG       ,                   ORG  TO HIGHEST             V1L3
XOPTAB   DC        C'$@((,='
         DC        3X'00'
         DC        C''''
         DC        6X'00'
         DC        C'...'              OR,AND,NOT
         DC        15X'00'
         DC        C'.'                .GT.
         DC        X'00'
         DC        C'.'                .LT.
         DC        2X'00'
         DC        C'..'               .NE.  .EQ.
         DC        X'00'
         DC        C'.'                .GE.
         DC        X'00'
         DC        C'.'                .LE.
         DC        29X'00'
         DC        C'+-*/'
         DC        C'*'                EXPONENTIATION
         DC        55X'00'
         DC        C').&&'
         SPACE     1                                               V1L4
ERDVSTUF DS        0F                  DEVTYPE INFO GOES HERE      V1L4
ERRUCBWD DS        F                   UCB DEVICE TYPE             V1L4
ERRMXBLK DS        F                   MAXIMUM BLOCK SIZE          V1L4
ERRNOCYL DS        H                   NUMBER OF CYLINDERS         V1L4
ERRNOTRK DS        H                   TRACKS PER CYLINDER         V1L4
ERRMXTRK DS        H                   MAXIMUM TRACK LENGTH        V1L4
ERRBLOV1 DS        X                   OVERHEAD FOR MOST BLOCKS    V1L4
ERRBLOV2 DS        X                   OVERHEAD FOR LAST BLOCK     V1L4
ERRBLOV3 DS        X                   FUDGE FACTOR FOR NO KEYS    V1L4
ERRTOLSW DS        X                   TOLERANCE FACTOR SWITCH     V1L4
ERRTOLFR DS        H                   TOLERANCE ADJUSTMENT        V1L4
         ORG       ERDVSTUF            RECOVER SPACE FOR NEXT 5 DS'S
         SPACE     1                                               V1L4
ENDBUFF  DC        A(*-*)
EATEXT   DC        A(*-*)
ER14RET  DC        A(*-*)
EPARMADD DC        A(*-*)
EABUFF2  DC        A(*-*)
KF32     DC        F'32'                                           V1L3
KF48     DC        F'48'
KF6      DC        F'6'
KM2427   DC        X'000000F0'
KM2331   DC        X'000001FF'
KM2630   DC        X'0000003E'
ETXTLEN  DC        H'0'
ESTUFTYP DC        H'0'
ESAVACTN DS        H                                               V1L3
ESAVCODE DC        H'0'
ECHAR80  DC        C'0FIRST 80 CHARACTERS OF INPUT RECORD ARE->'''
ELEN     EQU       *-ECHAR80
*        USE UNDERSCORE  '_' AS SPECIAL CHARACTER IN TEXT
ETRTABL  EQU       *-C' '
         DC        XL186'00'           FROM ' ' TO '9' INCLUSIVE
ETRTEND  EQU       *
         ORG       ETRTABL+C'"'
         DC        X'10'               'ON UNIT'
         ORG       ETRTABL+C'%'
         DC        X'04'               'UNEXPECTED'
         ORG       ETRTABL+C'×'
         DC        X'08'               'IS INVALID'
         ORG       ETRTABL+C'_'
         DC        X'0C'
         ORG       ETRTEND
         SPACE     2
         AIF       (&DVSD).FULL6                                   DVS2
         AIF       ('&ERROPT' NE 'READ').FULL6                     V1L4
         ENTRY     ERRCLOSE                                        V1L4
ERRCLOSE CENT      ERROPSAV            SAVE REGISTERS              V1L4
         CLOSE     (ERRDCB)            CLOSE ERROR TEXT FILE       V1L4
         B         CRET                AND RETURN                  V1L4
.FULL6   ANOP                                                      V1L4
         LTORG
         EJECT
***********************************************************************
*****
*****    MACRO ERRTABLE DEFINES THE ERROR CODES IN USE BY THE COMPILER
*****    AND SETS UP A TABLE OF POINTERS TO THE TEXTS FOR EACH MESSAGE.
*****    EACH ERROR CODE IS ASSIGNED A UNIQUE NUMBER FROM 32 ON.
*****                                                              V1L3
*****    CASE 1. - '&ERROPT' EQUALS 'FULL'                         V1L3
*****    E.G., FOR CODE SX-3, THE FOLLOWING IS GENERATED
*****            SX3    EQU SOME INTEGER
*****            SX3TAB DC AL2(SX3TEXT-ERRTEXT)
*****    THE AL2 IS GENERATED ONLY IN THIS DECK.
*****                                                              V1L3
*****    CASE 2. - '&ERROPT' EQUALS 'READ'                         V1L3
*****    FOR CODE SX-3, FOR EXAMPLE, THE FOLLOWING IS GENERATED    V1L3
*****            SX3    EQU SOME INTEGER                           V1L3
*****                   DC AL1((SX3TAB2-ERR2TAB)/2+1)              V1L3
*****    IF TEXT FOR SX-3 IS NOT IN CORE, DC AL1(0) IS GENERATED   V1L3
*****    NO AL1'S ARE GENERATED UNTIL THE 1ST ERROR CODE IS        V1L3
*****    ENCOUNTERED FOR WHICH ERRLOCN DEFINES THE TEXT TO BE IN   V1L3
*****    CORE.                                                     V1L3
*****                                                              V1L3
*****    MACRO GENERR2 IS NULL UNLESS '&ERROPT' EQUALS 'READ'      V1L3
*****    THEN IT GENERATES 3 DC'S AS FOLLOWS                       V1L3
*****         EMINERR DC AL2(LEAST IN-CORE ERROR NUMBER)           V1L3
*****         EMAXERR DC AL2(GREATEST IN-CORE ERROR NUMBER)        V1L3
*****         ERR2TAB DC 0H'0'                                     V1L3
*****   FOLLOWED BY A TABLE OF POINTERS TO IN-CORE TEXTS. EACH     V1L3
*****   EACH ENTRY IS OF FORM, E.G.,                               V1L3
*****        SX3TAB2 DC AL2(SX3TEXT-1-ERRTEXT)                     V1L3
*****   EMINERR IS ORG'D BACK OVER ANY DC'S GENERATED BY ERRTABLE  V1L3
*****   FOR CODES GREATER THAN GREATEST IN-CORE CODE.              V1L3
*****                                                              V1L3
*****    THE MESSAGE TEXTS ARE COPIED FROM SOURCE MODULE ERRTEXTS.
*****    EACH TEXT IS DEFINED BY AN EMESSAGE MACRO WHICH EXPANDS
*****    AS FOLLOWS:
*****             EMESSAGE   EG,1,'TEXT'
*****    EG1TEXT  DC         AL1(L'TEXT),C'TEXT'
*****
***********************************************************************
         PRINT     ON,GEN
ERRTAB   DS        0H
         ERRTABLE
ERR2TAB  GENERR2   ORGLABL=ERRTAB                                  V1L3
ERRTEXT  EQU       *
         COPY      ERRTEXTS            ERROR TEXTS GO HERE
         SPACE     5
.CODE    AIF       ('&ERROPT' NE 'CODE').NONE
*******************************************************************
*                                                                 *
*                  ERROR MESSAGE PROCESSOR                        *
*                                                                 *
*******************************************************************
**
         SPACE     2
         USING     ERROR,R11
         SPACE     2
*------------------------------------------------------------------*
*                  INITIALIZATION                                  *
         USING     ERCODE,R1                                       V1L3
*------------------------------------------------------------------*
         IC        R2,ERACTION                                     V1L3
         SRL       R2,1                                            V1L3
         N         R2,KM2831                                       V1L3
         STH       R2,ESAVACTN                                     V1L3
         IFALL     (ESAVACTN+1,EQ,0),(CEXTNSW,OFF),EINCR           V1L3
         IFALL     (ESAVACTN+1,EQ,1),(CWARNSW,OFF),EINCR           V1L3
         ST        R1,EPARMADD         SAVE PARM LIST ADDR
         L         R0,XBUFFER          SAVE INPUT BUFFER ADDRESS
         ST        R0,EABUFF2
         MVC       XPAD3+1(XPRNTND-XPAD3),XPAD3  BLANK PRINT LINE
         LH        R2,ERCODE           GET EDIT CONTROL BYTES
         N         R2,KM2331           GET ERROR CODE BITS
         C         R2,KF31             TEST FOR REAL ERROR CODE
         BH        EREGULAR
         C         R2,KF15             TEST FOR POINTER
         SLL       R2,2
         BNH       EPOINTER
EINREG   L         R2,XHELPS-16*4(R2)  LOAD REGISTER CONTAINING CODE
         B         EREGULAR
EPOINTER L         R2,XHELPS(R2)       LOAD POINTER REGISTER
         LH        R2,ZR2              LOAD REAL ERROR CODE
EREGULAR STH       R2,ESAVCODE         SAVE THE CODE
         LA        R3,0(R2,R2)         TRIPLE THE CODE FOR INDEXING
         AR        R3,R2
         LA        R3,ERRTAB-32*3(R3)  POINT R3 AT CL3'ERRORCODE'
         MVC       XBUFACD(2),ZR3      SET UP ERROR CODE
         MVC       XBUFNCD(1),ZR3+2
         LA        R3,XPAD3+2          R3 IS PRINT LINE POINTER
         SPACE
***  CHECK FOR MESSAGE AND INFO CODES
         IF        (EREXTRA,OFF,X'80'),XERDONE
         IC        R7,ERMESS           GET MESSAGE CONTROL BITS
         SRL       R7,1
         N         R7,KM2631
         BZ        XNOMES
         C         R7,KF48             TEST FOR HOL TYPE CODE
         BH        EMESGE              HIGH IF REGULAR MESSAGE
         LR        R4,R7               R7 CONTAINS 16*REG+LENGTH
         N         R4,KM2427
         SRL       R4,2
         L         R4,XHELPS(R4)
         BCTR      R4,0                ADDRESS OF MESSAGE-1 IN R4
         N         R7,KM2831           NUMBER OF BYTES     IN R7
         BCTR      R7,0                NUMBER OF BYTES - 1 IN R7
         B         EMOVMES
EMESGE   IC        R7,EMESNDX-49(R7)   GET POINTER TO MESSAGE WORD-1
         LA        R4,EMESTAB(R7)
         IC        R7,ZR4              GET LENGTH-1 OF WORD
EMOVMES  EX        R7,XMOVMES
         LA        R3,ZR3+2(R7)        SPACE R3 OVER MESSAGE & 1 BLNK
**  CHECK IF WE SUPPLY ANY EXTRA INFO FROM STACK OR SYMBOL TABLE
XNOMES   IC        R7,ERREG
         N         R7,KM2831           GET THE REGISTER & ZERO R7
         SLL       R7,2                REGISTER*4
         L         R2,XHELPS(R7)       SETUP R2 TO POINT TO ANY INFO
         MVI       XSWERMES+1,NOP
         LH        R7,ERINFO           GET INFO CODE
         SRL       R7,3
         N         R7,KM2630
         LH        R7,XERTAB2(R7)
         B         XINFO(R7)
         $FREE     R1                                              V1L5
EINCR    AR        R2,R2                                           V1L3
         LH        R3,XNOEXTNS(R2)                                 V1L3
         AH        R3,KH1                                          V1L3
         STH       R3,XNOEXTNS(R2)                                 V1L3
         BR        R14                                             V1L3
         SPACE     1
XERTAB2  DC        AL2(XNOINFO-XINFO)  0
         DC        AL2(XSYM-XINFO)     1
         DC        AL2(XDEL-XINFO)     2
         DC        AL2(XSTN-XINFO)     3
         DC        AL2(XJSN-XINFO)     4
         DC        AL2(XREL-XINFO)     5
         DC        AL2(XNAM-XINFO)     6
         DC        AL2(XCSN-XINFO)     7
         DC        AL2(XUSN-XINFO)     8
         DC        AL2(XCHR-XINFO)     9
         DC        AL2(XVM8-XINFO)     10
         DC        AL2(XVM6-XINFO)     11
         DC        AL2(XVAR-XINFO)     12
         DC        AL2(XNAV-XINFO)     13
         DC        AL2(XNSS-XINFO)     14
         DC        AL2(XINT-XINFO)     15
         DC        AL2(XARR-XINFO)     16
         DC        AL2(XNAL-XINFO)     17
         DC        AL2(XVLS-XINFO)     18
         DC        AL2(XLIN-XINFO)     19
         DC        AL2(EENT-XINFO)     20
         SPACE     5
*-----------------------------------------------------------------*
*                  INDIVIDUAL ERROR ROUTINES                      *
*-----------------------------------------------------------------*
XINFO    DC        0H'0'
**  A CHARACTER TO BE PRINTED
XCHR     MVC       ZR3(1),ZR2
         B         XERDONE
         SPACE     2
**  RECREATE SYMTAB ADDRESS USING 2-BYTE POINTER AT 6(R2)
XREL     LH        R2,ZR2+6
         N         R2,KM1631
         A         R2,CSYMBASE
         B         XNAM
         SPACE     2
**  NAME 8 TO LEFT OF PRESENT ADDRESS
XVM8     S         R2,KF2
         SPACE     2
**  NAME 6 TO LEFT OF PRESENT ADDRESS
XVM6     S         R2,KF6
         SPACE     2
**  NAME DIRECTLY AT PRESENT ADDRESS
XVAR     S         R2,KF4
         SPACE     2
** NAME IN SYMTAB
XNAM     LA        R2,ZR2+2
         SPACE     2
**  NAME IN ENTRY SEQUENCE
EENT     MVC       ZR3(6),ZR2+2
         B         XERDONE
         SPACE     2
*  SYMBOL AND ISN
XNAL     MVC       ZR3(6),ZR2+4        MOVE SYMBOL NAME
         MVC       ZR3+7(12),=C'USED IN LINE'
         LA        R3,ZR3+20           SKIP OVER ALL THIS STUFF
         LH        R4,ZR2+10           LOAD ISN
         B         XSTN2
*   VARIABLE LENTH STRING
XVLS     L         R4,XHELPS+8         GET STRING LENGTH
         BCTR      R4,0                LENGTH - 1
         EX        R4,XVLSMOVE         MOVE STRING TO BUFFER
         B         XERDONE
XVLSMOVE MVC       ZR3(*-*),ZR2
         SPACE     2
*  DELIMITER IN STACK
XDEL2    AH        R2,ZR2
XDEL     CLI       ZR2+2,TERM
         BL        XSYM                IT'S NULL OPERATER,GO FOR SYM
         BH        XDEL1
         MVC       ZR3(16),=C'END-OF-STATEMENT'
         B         XERDONE
XDEL1    MVC       ZR3(1),ZR2+2        OPERATER OUT OF STACK
         TR        ZR3(1),XOPTAB
XSWERMES NOP       XERDONE             BRA/NOP SWITCH
         MVI       XSWERMES+1,BRA
         MVC       ZR3+2(6),=C'BEFORE'
         LA        R3,ZR3+9
         B         XSYM
         SPACE     2
***      STATEMENT NUMBERS AND ISN'S
*  CURRENT ISN  -  COMPILE OR EXECUTION
XJSN     L         R4,XHELPS+44
         LH        R4,ZR4
         MVC       ZR3(7),=C'IN LINE'
         LA        R3,ZR3+9
         IF        (XEXECSW,ON),XSTN2                              V1L2
         LH        R4,XISN             NO - GET COMPILE TIME ISN
         B         XSTN2                                           V1L2
         SPACE     2
**  ISN FROM SYMTAB AND STAT NO.
XUSN     MVC       CSN(2),ZR2+10
         SPACE     2
**  STATEMENTS UNDEFINED AT RELOCATER PHASE 1
XCSN     L         R4,ZR2+4
         SPACE     2
**  ISN SAVED FROM SYMTAB
         CVD       R4,XDFIELD
         MVC       ZR3+1(5),XEDMSK+3
         ED        ZR3(6),XDFIELD+5
         MVC       ZR3+7(12),=C'USED IN LINE'
         LA        R3,ZR3+20           SPACE OVER THIS STUFF
         LH        R4,CSN
         B         XSTN2                                           V1L2
         SPACE     2
**   LINE NUMBER FROM SYMTAB
XLIN     LH        R4,ZR2+10
         B         XSTN2
         SPACE     2
**  STATEMENT NUMBER FROM SYMTAB
XSTN     L         R4,ZR2+4
XSTN2    CVD       R4,XDFIELD
         MVC       ZR3+1(5),XEDMSK+3
         ED        ZR3(6),XDFIELD+5
         B         XERDONE
         SPACE     2
**  QUANTITY IN STACK  ( SYMBOL,HOLCON,LITCON,NUMCON)
XSYM     IF        (ZR2+3,EQ,PHI),XDEL2   TRY NEXT DELIMITER
XTSTSYM  IF        (ZR2+3,NZ,MNAME),XTSTHOL
         SR        R7,R7
         IC        R7,ZR2+3            PICKUP LENGTH CODE
         SLL       R7,2
         BCTR      R7,0
         EX        R7,XMOVNAM
         B         XERDONE
XTSTHOL  IF        (ZR2+3,Z,CHOLL),XTSTCN
         L         R2,ZR2+4           LOAD PTR TO H-LIST
         L         R2,ZR2+4           LOAD PTR TO HOLCONST
         MVI       ZR3,C''''
         MVC       ZR3+1(4),ZR2
         B         XERDONE
XTSTCN   MVI       ZR3+1,C'.'          ASSUME LOGICAL CONST IN STACK
         IF        (ZR2+3,Z,CCONS),XERDONE    MAYBE ITS A CONSTANT?
         L         R4,ZR2+4
         N         R4,KM0531           GET RID OF DIGIT COUNT
         CVD       R4,XDFIELD
         MVC       ZR3+1(9),XEDMSK1
         ED        ZR3(10),XDFIELD+3
         B         XERDONE
         SPACE     2
**   NUMBER OF SUBSCRIPT ,VARIABLE NAME AND VALUE (SS-1)
*    R2  VALUE -1
*    R4  4*(# SS)-4
*    R15 ADDR OF NAME+10
XNSS     MVC       ZR3(9),=C'SUBSCRIPT'
         L         R4,XHELPS+16        4*(# SS)-4
         LA        R4,ZR4+4            # OF SS * 4
         SRA       R4,2                # OF SS
         STC       R4,ZR3+10           MOVE TO WORK AREA
         OI        ZR3+10,X'F0'        MAKE IT EBCDIC
         MVC       ZR3+12(2),=C'OF'
         S         R2,KF10             BACKUP TO NAME
         MVC       ZR3+15(6),ZR2       VARIABLE NAME
         CLC       ESAVCODE(2),=AL2(UV3)  THIS UV-3 ?
         BE        XERDONE             THAT'S ALL
         L         R2,XHELPS+8         VALUE-1
         LA        R2,ZR2+1            VALUE
         MVC       ZR3+22(13),=C'HAS THE VALUE'
         LA        R3,ZR3+35           BUMP R3 FOR NEXT PART
         B         XINT
         SPACE     2
*   TO PRINT NAME AND VALUE OF VARIABLE
*        THIS ROUTINE PRINTS NAME AND VALUE OF VARIABLE
*        R2 IS  ASSUMED TO CONTAIN THE VALUE OF THE VARIABLE
*        I. E.  XHELPS+8 HAS THIS VALUE
*NAV     IF        (CUNDEFSW,OFF,X'FF'),XERDONE
***  NAMES ARE AROUND EVEN WITH NOCHECK. WHAT A WASTE.
XNAV     MVC       ZR3(6),ZR2+4        MOVE IN NAME
         MVC       ZR3+7(13),=C'HAS THE VALUE'
         LA        R3,ZR3+20
         L         R2,XHELPS+8
         SPACE     2
*        THIS ENTRY PRINTS THE VALUE IN USERS REGISTER 2
XINT     CVD       R2,XDOUBLE
         MVC       ZR3+1(11),XSSMASK
         LA        R1,ZR3+11           FOR EDMK INSTRUCTION
         EDMK      ZR3(12),XDOUBLE+2
         IF        (XDOUBLE+7,Z,X'01'),XINT1  IS IT NEGATIVE?
         BCTR      R1,0
         MVI       0(R1),C'-'        GIVE IT A MINUS SIGN
XINT1    LA        R3,ZR3+12                                       V1L3
        B          XERDONE                                         V1L3
         AIF       ('&DIRACC' NE 'USE').ERDA2
         ORG       *-4                 ORG OUT 'B XERDONE'         V1L3
         CLC       ESAVCODE(2),=AL2(UNC)        IS THIS UN-C?
         BNE       XERDONE                    IF SO THEN EXTRA GOODIES
         XC        ESAVCODE(2),ESAVCODE       ZAP ESAVCODE
         MVC       ZR3(17),=C'RECORD NUMBER IS'                    V1L3
         LA        R3,ZR3+12           BUMP R3 FOR THIS            V1L3
         L         R2,XDARECNO                 GET THE RECORD NO.
         B         XINT                        GO AROUND AGAIN
.ERDA2   ANOP
         SPACE     2
* THIS ROUTINE PRINTS THE SUBSCRIPTS FOR AN UNDEFINED ARRAY MEMBER
XARR     S         R2,KF6
XVSS     MVC       ZR3(6),ZR2          MOVE IN NAME
         LR        R5,R3                 FIND FIRST BLANK
XVSS3    LA        R5,ZR5+1
         IF        (ZR5,NE,C' '),XVSS3
         MVI       ZR5,C'('
         LA        R3,ZR5+1            ADVANCE POINTER
         L         R7,XHELPS+12        GET USER'S R3
         S         R7,*-*              SUBTRACT OFF ARRAY BASE
         ORG       *-2                 GIMMICK TO AVOID ALIGNMENT ERROR
         DC        S(ZR2+10)
         SR        R4,R4
         IC        R4,ZR2+14
         SRL       R7,ZR4              DIVIDE BY ELEMENT WIDTH
         SR        R0,R0
         IC        R0,ZR2+10           4*NO.SS.-4 IN R0
         SR        R4,R4
****  THE NEXT LITTLE BIT TAKES CARE OF THE FACT THAT CHARACTER*N
****      ARRAYS ARE DOPED TO HAVE ONE EXTRA DIMENSION.
****      THE TEST FOR X'FC' IS BECAUSE DOT ROUTINES FOR CHARACTER
****      ARRAYS HAVE 'BAL R15,XAN' WITH R12 AS INDEX.
****      ORDINARY ARRAYS HAVE R12 AS BASE.
         IF        (ZR2+7,NE,X'FC'),XVSS2  IS IT A CHARACTER*N ARRAY?
         LA        R4,4                YES - IGNORE FIRST SUBSCRIPT
         SR        R6,R6
         D         R6,*-*              DIVIDE BY REAL ELEMENT WIDTH
         ORG       *-2                 GIMMICK TO AVOID ALIGNMEMT ERROR
         DC        S(ZR2+18)
XVSS2    SR        R6,R6
         D         R6,*-*(R4)          DIVIDE BY DIMENSION
         ORG       *-2                 GIMMICK TO AVOID ALIGNMENT ERROR
         DC        S(ZR2+18)
         LA        R6,ZR6+1  ADD ONE TO GET TRUE VALUE
         CVD       R6,XDOUBLE          R6 IS THEN SUBSCRIPT
         MVC       ZR3+1(11),XSSMASK
         LA        R1,ZR3+10
         EDMK      ZR3(12),XDOUBLE+2
         LA        R5,ZR3+11
         SR        R5,R1
         EX        R5,XVSSMVC
         AR        R5,R3
         MVI       ZR5+1,C','
         LA        R3,ZR5+2
         LA        R4,ZR4+4
         CR        R4,R0
         BNH       XVSS2               NOT DONE
         BCTR      R3,0                DONE
         MVI       ZR5+1,C')'
         MVI       ZR5+2,C' '
         MVC       ZR5+3(9),ZR5+2      CLEAN UP PRINT LINE
         SPACE     5
XNOINFO  EQU       *
*-----------------------------------------------------------------*
*                  FINISH UP                                      *
*-----------------------------------------------------------------*
XERDONE  LH        R7,ESAVACTN         GET ACTION/SEVERITY BITS    V1L3
         IC        R7,XERTAB1(R7)
         LR        R8,R14              SAVE R14 ACROSS OUTPUT
         B         XERRTN(R7)          GOTO PARTICULAR RTN
         SPACE     1
XERTAB1  DC        AL1(XLANG-XERRTN)   0
         DC        AL1(XWARN-XERRTN)   1
         DC        AL1(XBUTE-XERRTN)   2
         DC        AL1(XZRRO-XERRTN)   3
         DC        AL1(XNOEX-XERRTN)   4
         DC        AL1(XNOAC-XERRTN)   5
         DC        AL1(XTRAZ-XERRTN)   6
         SPACE     2
*-----------------------------------------------------------------*
*        ACTION/SEVERITY ROUTINES                                 *
*-----------------------------------------------------------------*
XERRTN   DS        0H
XTRAZ    LA        R8,XTRAZ1
         B         XNOACP4
XTRAZ1   L         R11,XHELPS+44
         B         XTRACEBK
XLANG    LH        R7,XNOEXTNS         NO. OF EXTNS                V1L3
         AH        R7,KH1              ADD 1                       V1L3
         STH       R7,XNOEXTNS         SAVE AGAIN                  V1L3
         MVC       XERBUF+1(9),=C'EXTENSION'
         B         XMES
XWARN    LH        R7,XNOWARNS                                     V1L3
         AH        R7,KH1                                          V1L3
         STH       R7,XNOWARNS                                     V1L3
         MVC       XERBUF+1(9),=C'*WARNING*'
         B         XMES
XZRRO    LA        R1,EBADARG          =X'FF------'
EBADARG  EQU       KFM1
         B         XOUTBORF
XNOEX    OI        XERRSWT,XINHIBX
         B         XNOACP4
XBUTE    IF        (CISNCDSW,OFF),THEN,(DO,CISN) ENSURE AN ISN
         LA        R1,XBXBOOT
XOUTBORF THEN      (OUTPUT,4)
         ST        R5,XHELPS+20
XNOAC    OI        XERRSWT,XFATAL
XNOACP4  MVC       XERBUF+1(9),=C'**ERROR**'
         LH        R7,XNOERRS                                      V1L3
         AH        R7,KH1                                          V1L3
         STH       R7,XNOERRS                                      V1L3
***  GO PRIME A BUFFER
XMES     LA        R2,XOUTDSRN
         L         R1,XAFIOCS
         BALR      R0,R1
         DC        X'00'
         DC        X'FF'
         ST        R2,XBUFFER
XMES1    SPRIN     XPRINTLN,XBUFLN     PRINT THE ERROR MESSAGE
         L         R4,EPARMADD         PARM LIST ADDR
         USING     ERCODE,R4
         TM        ERBUF,X'40'         TEST FOR BUF CODE
         BZR       R8
         $FREE     R4                                              V1L5
         L         R4,EABUFF2          INPUT BUFFER ADDRESS
         L         R3,XBUFFER          OUTPUT BUFFER ADDRESS
         MVC       ZR3(ELEN),ECHAR80   MOVE MESSAGE AND
         MVC       ZR3+ELEN(80),ZR4    FIRST 80 CHARS OF INPUT
         MVI       ZR3+ELEN+80,C''''   PUT A QUOTE BEHIND IT
         LA        R3,133              BUFFER LENGTH
         THEN      (DO,XPRNTERR)       PRINT MESSAGE
ERREXIT  BR        R8                  RETURN VIA R8               V1L3
         SPACE     5
*******************************************************************V1L3
*                                                                 *V1L3
*  ERROPEN - DUMMY ENTRY TO SATISFY EXTRN IN MAIN. IF '&ERROPT'   *V1L3
*            EQUALS 'READ', THIS ENTRY INCLUDES CODE TO OPEN THE  *V1L3
*            ERROR TEXT DATA SET.                                 *V1L3
*                                                                 *V1L3
*******************************************************************V1L3
         SPACE     2
         ENTRY     ERROPEN                                         V1L3
         SPACE     1                                               V1L3
XDORG    DS        0D                                              V1L3
ERROPEN  CENT      ERROPSAV                                        V1L3
         SR        R15,R15               SET RETURN CODE           V1L3
         B         CRET                                            V1L3
         SPACE     3                                               V1L3
         ORG       XDORG                 ORG BACK OVER CODE        V1L3
XDFIELD  DS        D                                               V1L3
EPARMADD DS        F                                               V1L3
EABUFF2  DS        F                                               V1L3
ESAVACTN DS        H                                               V1L3
ESAVCODE DS        H                                               V1L3
         ORG       ,                                               V1L3
XMOVMES  MVC       ZR3(*-*),ZR4+1
XMOVNAM  MVC       ZR3(*-*),ZR2+4
XVSSMVC  MVC       ZR3(*-*),ZR1
KM2427   DC        0F'0',X'000000F0'                               V1L3
KM2526   DC        X'00000060'
KF48     DC        F'48'
KF6      DC        F'6'
KM2331   DC        X'000001FF'
KM2630   DC        X'0000003E'
XEDMSK1  EQU       XEDMASK
XEDMSK   EQU       XEDMASK+1
XBUFLN   EQU       133
XPRINTLN DC        CL133' '
         ORG       XPRINTLN
XPAD1    DC        C' '
XERBUF   DC        11C'*'              EXTENSION,WARNING OR ERROR
XPAD2    DC        3C' '                                           V1L1
XBUFACD  DC        CL3'AC-'   ALPHA CODE
XBUFNCD  DC        CL1'N'              NUMERIC CODE
XPAD3    DC        C' '
ERROPSAV DS        18F                 PUT SAVE AREA IN WORK SPACE V1L3
         ORG       ,                   ORG TO HIGHEST              V1L3
         ORG       XPRINTLN+XBUFLN
XPRNTND  EQU       *-1
XOPTAB   DC        C'$@((,='
         DC        3X'00'
         DC        C''''
         DC        6X'00'
         DC        C'...'              OR,AND,NOT
         DC        15X'00'
         DC        C'.'                .GT.
         DC        X'00'
         DC        C'.'                .LT.
         DC        2X'00'
         DC        C'..'               .NE.  .EQ.
         DC        X'00'
         DC        C'.'                .GE.
         DC        X'00'
         DC        C'.'                .LE.
         DC        29X'00'
         DC        C'+-*/'
         DC        C'*'                EXPONENTIATION
         DC        55X'00'
         DC        C').&&'
         SPACE     2
ECHAR80  DC        C'0FIRST 80 CHARACTERS OF INPUT RECORD ARE->'''
ELEN     EQU       *-ECHAR80
         SPACE
EMESNDX  DC        AL1(EUNEX-EMESTAB)  49
         DC        AL1(EINVAL-EMESTAB) 50
         DC        AL1(ENEAR-EMESTAB)  51
         DC        AL1(EUNIT-EMESTAB)  52
EMESTAB  EQU       *
EUNEX    DC        AL1(9),C'UNEXPECTED'
EINVAL   DC        AL1(6),C'INVALID'
ENEAR    DC        AL1(3),C'NEAR'
EUNIT    DC        AL1(3),C'UNIT'
         SPACE
         LTORG
         EJECT
***********************************************************************
*****    THE NEXT MACRO ASSIGNS A UNIQUE VALUE TO EACH ERROR CODE USED
*****    BY THE COMPILER AND BUILDS A TABLE OF ALPHAMERIC CODES TO BE
*****    INSERTED INTO THE ERROR MESSAGE TO BE PRINTED.
*****    E.G., FOR THE CODE SX-3, THE FOLLOWING IS GENERATED:
*****              SX3     EQU  SOME INTEGER
*****              SX3TAB  DC   CL3'SX3'
*****    THE CL3 IS GENERATED ONLY IN THIS DECK SINCE ALL OTHER DECKS
*****    CALL ERRTABLE.
***********************************************************************
         PRINT     ON,NOGEN
ERRTAB   EQU       *                   ERROR CODE TABLE GOES HERE
         ERRTABLE
         SPACE     5
.NONE    ANOP
         SPACE     2                                               V1L3
         END
