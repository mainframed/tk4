         TITLE     'BACCT     ACCOUNTING ROUTINE'
BACCT    CSECT
*******************************************************************
*********          ACCOUNTING ROUTINES                    *********
*******************************************************************
*        REGISTER CONTENTS UPON ENTRY                             *
*                  R10                 COMMR POINTER              *
*                  R12                 STARTA POINTER             *
*                  R13                 SAVE AREA POINTER          *
*                  R14                 RETURN ADDRESS             *
*                  R5 & R6             USED BY WATFIV             *
*******************************************************************
         SPACE     5
***********************************************************************
*        FOLLOWING IS A DESCRIPTION OF THE ENTRY AND EXIT ROUTINES
*        CENT AND CRET USED IN ACCT.  ALSO A DESCRIPTION OF THE
*        METHOD USED TO PRINT A LINE IS GIVEN.
*******************************************************************
         SPACE     5
***********************************************************************
*        THE CENT ROUTINE IS USED AT EACH ENTRY POINT TO SAVE         *
*        REGISTERS, TO SET UP A NEW SAVE AREA AND TO SET UP A BASE    *
*        REGISTER FOR THE ROUTINE.   THE CALLING SEQUENCE TO CENT IS  *
*                  CENT      BSAVE                                    *
*            WHERE BSAVE IS AN 18 WORD SAVE AREA IN ACCT.             *
*        WHEN CENT RETURNS                                            *
*          REGISTER 11 IS SET UP AS A BASE REGISTER                   *
*          REGISTER 13 POINTS TO ACCT'S SAVE AREA                     *
*            (IT ALSO IS USED TO ADDRESS ANY ROUTINE OR LOCATIONS     *
*            (FOLLOWING THE DEFINITION OF BSAVE)                      *
***********************************************************************
         SPACE     2
***********************************************************************
*        THE CRET ROUTINE IS USED TO RETURN TO THE CALLER.            *
*        IT RESTORES ONLY REGISTERS 11,13,AND 14                      *
***********************************************************************
         SPACE     2
***********************************************************************
*        THE FOLLOWING CALLING SEQUENCE CAN BE USED TO PRINT A LINE   *
*                                                                     *
*        LA        R2,XOUTDSRN         ADDR OF DSRN(&PRINT)           *
*        L         R1,XAFIOCS          ADDR OF I/O ROUTINE            *
*        BALR      R0,R1               OFF TO PRIME OUTPUT            *
*        DC        X'00'               INIT CODE                      *
*        DC        X'FF'               FORMATTED OUTPUT               *
*   NOTE THE ABOVE CODING NEED ONLY BE DONE IF THE PREVIOUS I/O       *
*        OPERATION WAS NOT AN OUTPUT OPERATION ON THE &PRINT UNIT     *
*                                                                     *
*        NOW TO PRINT THE LINE THE FOLLOWING MACRO CALL CAN BE USED   *
*                                                                     *
*        SPRIN     LINE,NUMBER                                        *
*    WHERE                                                            *
*        LINE IS THE ADDRESS OF THE BUFFER                            *
*        NUMBER IS THE NUMBER OF BYTES IN THE LINE                    *
*        NOTE THAT &PRINT UNIT USES RECFM=FA                          *
***********************************************************************
***********************************************************************
         SPACE
*******************************************************************
*********          INITIAL LOAD                           *********
*******************************************************************
         USING     CSAVER,R10
BSTART   CENT      BSAVE
         SPACE
         MVI       BOPT,C' '
         AIF       (&NDVSD).ACCTOS                                 DVS1
         COMRG                         GET COM REG ADDR IN REG 1   DOS2
         MVC       BXTIME+1(8),0(1)    MOVE DATE TO OUTPUT AREA    DOS2
         TM        23(R1),X'80'        UPSI 1 SPECIFIED ?          DVS2
         BNO       INVPARM             NO                          DVS2
         AGO       .DEBUG                                          DVS2
.ACCTOS  ANOP                                                      DOS2
         TIME
         SR        R0,R0
         ST        R0,BTEMP1
         SPACE
* EXAMINE PARAMETER LIST FROM EXECUTE STEP
         L         R1,XSAVER+4         POINTER TO SYSTEM SAVE AREA
         L         R1,ZR1+24           ARGUMENT LIST FROM SYSTEM
         L         R1,ZR1              ADDRESS OF PARAMETER LIST
         CLC       0(2,R1),KF0         IF PARM LENGTH IS ZERO      V1L5
         BE        CRET                THEN RETURN                 V1L4
         LA        R1,ZR1+2            OVER COUNTER FIELD
         AIF       ('&VM' NE 'USE').BVM1                           VM1
         NI        XEXEC1SW,X'FF'-XVMASK   INITIALIZE XDEBUG SW    VM1
         CLC       ZR1(6),=C'XDEBUG'                               VM1
         BNE       BNOTXDBG            NOT XDEBUG MODE - SKIP      VM1
         OI        XEXEC1SW,XVMASK     SET XDEBUG MODE SWITCH      VM1
BNOTXDBG DS        0H                                              VM1
.BVM1    ANOP                                                      VM1
         SPACE
         AIF       ('&WATTYPE' EQ 'DISTR').BTYPE2
         CLC       ZR1(4),=C'TEST'
         BNE       BST10
         LA        R1,ZR1+4            R1=R1+4;
         MVI       BOPT,C'S'
         MVI       COPT,C'S'           ALLOW DEBUG PRINTING
BST10    DC        0H'0'
.BTYPE2  ANOP
         SPACE
         CLC       0(5,R1),KDEBUG      IS THIS A DEBUG RUN?        V1L5
         BNE       INVPARM             NO - BRANCH                 V1L5
.DEBUG   ANOP                                                      V1L5
         MVI       COBJLET,NO          NO OBJECT DECKS UNDER DEBUG V1L5
         IF        (5(R1),NE,C'S'),NOSTATS                         V1L5
         MVI       STATSW,ON           TURN ON STATS SW            V1L5
NOSTATS  MVI       DEBUGSW,ZSWON       YES - SET SWITCH            V1L5
         MVI       XDEBUGSW,X'FF'      SET FOR NO DIRECT ACCESS    V1L5
INVPARM  DS        0H                                              V1L5
         B         CRET                PARM PROCESSING FINISHED    V1L5
         SPACE
         $FREE     RP,R13                                          V1L5
         EJECT
*******************************************************************
*********          BEGINNING OF JOB                       *********
*******************************************************************
         ENTRY     BBEGJOB                                         V1L5
BBEGJOB  CENT      BSAVE
         IF        (C$OPTSW,OFF),OPTSWOFF $OPTIONS CARD ?          V1L5
         LA        R1,XCARD+7          LOOK FOR A BLANK            V1L5
FINDBLNK CLI       0(R1),C' '          IS IT A BLANK ?             V1L5
         BE        SCANBLNK            YES                         V1L5
         LA        R1,1(,R1)           NO, TRY THE NEXT CHARACTER  V1L5
         B         FINDBLNK                                        V1L5
CHKBLANK CLI       0(R1),C' '          NON BLANK ?                 V1L5
         BNE       OPTSTART            YES                         V1L5
SCANBLNK LA        R1,1(,R1)           SKIP THE BLANK              V1L5
         C         R1,=A(XCARD+80)     END OF THE LINE ?           V1L5
         BNL       CRET                YES                         V1L5
         B         CHKBLANK            IS THE NEXT ONE A BLANK ?   V1L5
OPTSWOFF DS        0H                                              V1L5
* THIS ROUTINE ANALYSES THE JOB CARD
         SPACE
         AIF       ('&WATTYPE' EQ 'DISTR').BTYPE4
         MVC       COPT,BOPT
         SPACE
         XC        BDEBUG(6),BDEBUG
         IF        (XCARD+7-1,EQ,C'S'),THEN,(COPT,EQU,C'S')
.BTYPE4  ANOP
         SPACE
         AIF       ('&WHOSIT' NE 'BILL').BSTAR                     V1L5
         LA        R6,13               MAX OF 14 CHARS BLANKED OUT V1L5
         LA        R1,XCARD+&PARMCOL-1 ACCT INFO AT CC &PARMCOL    V1L5
COMMACHK DS        0H                                              V1L5
         CLI       0(R1),C','          CHECK FOR COMMA             V1L5
         BE        BSTAR1              BRANCH IF SUCCESSFUL        V1L5
         LA        R1,1(,R1)           BUMP PTR TO NEXT COLUMN     V1L5
         BCT       R6,COMMACHK         KEEP ON SEARCHING           V1L5
BSTAR1   DS        0H                                              V1L5
         LA        R6,XCARD+&PARMCOL   R6 -> CARD COL &PARMCOL     V1L5
         SR        R1,R6               FIND # OF CHARS BEFORE COMMAV1L5
         LTR       R1,R1               CHECK IF ACCT# SPECIFIED    V1L5
         BM        BSTAR4              BRANCH IF NEGATIVE COUNT    V1L5
         LA        R6,XCARD+&PARMCOL-1 RESET R6 TO CC &PARMCOL     V1L5
         EX        R1,BSTAR2           MOVE IN STARS               V1L5
         L         R15,=V(RESB99)  RESET TAPE 99                   V1L5
         BALR      R14,R15                                         V1L5
         B         BSTAR4              PRINT $JOB CARD             V1L5
BSTAR2   MVC       0(*-*,R6),BSTAR3    MAKE ACCT# SECURE           V1L5
BSTAR3   DC        14C'*'                                          V1L5
BSTAR4   DS        0H                                              V1L5
.BSTAR   ANOP                                                      V1L5
         MVI       XCARD1,C'1'         SET SKIP TO PRINT JOB CARD
         SPRIN     XCARD1,90           PRINT THE JOB CARD
         MVI       XCARD1,C' '         RESET CONTROL CHAR
         THEN      (CIHGACRD,OFF)      DON'T SAVE IT ANY LONGER
***********************************************************************
* START LOOKING FOR THE PARAMETERS
***********************************************************************
         LA        R1,XCARD+&PARMCOL-1 ADDRESS OF PARAMETER FIELD
         SPACE
OPTSTART MVI       XCARD+80-1,C' '     END OF PARAMETER FIELD      V1L5
         LA        R14,BIDNO           SAVE THE ADDRESS OF BIDNO   V1L5
         IF        (C$OPTSW,ON),$OPTIONS       $OPTIONS ?          V1L5
         BAL       RLOOP,BID&IDENT
BIDNO    EQU       *                                               V1L3
         USING     *,RLOOP
         IF        (ZR1,EQ,C' '),BANALDON        NO PARAMETERS
         IF        (ZR1,NE,C','),BANALERR        1ST CHAR IS COMMA?
BLOOP3   LA        R1,ZR1+1            R1=R1+1;
         SPACE
*/       DELETE    SEQ1=01290000,SEQ2=01290000                     V1L3
         SPACE
         B         BJOB                SKIP SETTING UP A BASE      V1L5
$OPTIONS LR        R11,R14             SET UP A BASE               V1L5
BJOB     DS        0H                                              V1L5
         AIF       ('&TRACE' EQ 'OMIT').BOMIT3
* THE FOLLOWING CAUSES TRACE TO BE TURNED ON IF DEBUG=0001
         TM        BDEBUG+3,X'01'
         BZ        BDEBUGA1
         BAL       R14,XTRACON
BDEBUGA1 TM        BDEBUG+3,X'02'
         BZ        BDEBUGA2
         TM        205,X'10'
BDEBUGA2 TM        BDEBUG+3,X'04'
         BZ        BDEBUGA3
         TM        205,X'11'
BDEBUGA3 TM        BDEBUG+3,X'08'
         BZ        BDEBUGA4
         TM        205,X'11'
BDEBUGA4 DC        0H'0'
.BOMIT3  ANOP
         SPACE
***********************************************************************
*****    TRY FOR RUN= OPTION
***********************************************************************
         SPACE
         CLC       ZR1(3),KRUN                                     V1L5
         BNE       BANAL01             CHECK NEXT OPTION           V1L5
         LA        R1,ZR1+3            R1=R1+3;
         THEN      (DO,BEQCHECK)       DOES '=' FOLLOW?
         CLC       KNOCHECK,ZR1
         BNE       BANAL11
         SPACE     1                                               V1L5
***********************************************************************
*    RUN = NOCHECK
***********************************************************************
BNOCHECK LA        R1,ZR1+L'KNOCHECK   POINT TO NEXT PARM          V1L2
*        SET SWITCHES TO GENERATE APPROPRIATE CODE FOR RUN=NOCHECK
BANAL100 THEN      (CUNDEFSW,EQU,X'00')                            V1L5
         MVI       XNOPDEFZ+1,NOP
         MVI       XNOPDEFN+1,NOP
         BR        RLOOP
BANAL11  CLC       ZR1(4),KFREE        RUN=FREE?                   V1L2
         BNE       BANAL12             NOPE
BFREE    LA        R1,ZR1+L'KFREE      POINT TO NEXT PARM          V1L2
BANAL101 THEN      (CUNDEFSW,EQU,X'FF')                            V1L5
         B         BANAL121       OTHER SETTINGS SAME AS RUN=CHECK
BANAL12  CLC       KCHECK,ZR1          RUN = CHECK ?
         BNE       BANAL13             NO - NEXT TEST              V1L5
BCHECK   LA        R1,ZR1+L'KCHECK     POINT TO NEXT PARM          V1L2
BANAL102 THEN      (CUNDEFSW,EQU,X'0F')                            V1L5
BANAL121 MVI       XNOPDEFZ+1,BE       BRANCH EQUAL
         MVI       XNOPDEFN+1,BE       BRANCH EQUAL
         BR        RLOOP               RETURN TO SCAN LOOP
*           SHORT FORM FOR NOCHECK                                 V1L5
BANAL13  CLI       ZR1,C'N'            IS IT NOCHECK?              V1L5
         BNE       BANAL14             NO - NEXT TEST              V1L5
         LA        R1,ZR1+1            BUMP POINTER                V1L5
         B         BANAL100            GO TO NOCHECK ROUTINE       V1L5
*           SHORT FORM FOR FREE                                    V1L5
BANAL14  CLI       ZR1,C'F'            IS IT FREE?                 V1L5
         BNE       BANAL15             NO - NEXT TEST              V1L5
         LA        R1,ZR1+1            BUMP POINTER                V1L5
         B         BANAL101            GO TO FREE ROUTINE          V1L5
*           SHORT FORM FOR CHECK                                   V1L5
BANAL15  CLI       ZR1,C'C'            IS IT CHECK?                V1L5
         BNE       BANALERR            NO ERROR                    V1L5
         LA        R1,ZR1+1            BUMP POINTER                V1L5
         B         BANAL102            GO TO CHECK ROUTINE         V1L5
         SPACE     3
***********************************************************************
*****    SET UP JOB TIME
***********************************************************************
BANAL01  DS        0H                                              V1L5
         CLC       ZR1(4),KTIME        DOES USER HAVE THE TIME?    V1L5
         BNE       BPGE00              FALL THRU IF YES            V1L5
         LA        R1,ZR1+4            MARCH OVER TIME             V1L5
BANAL111 DS        0H                                              V1L5
         THEN      (DO,BEQCHECK),(DO,BTMESCAN) VERIFY OPERANDS     V1L5
         CLI       DEBUGSW,ZSWON       ARE WE IN DEBUG MODE?       V1L5
         BNE       BANAL110            YES->IMPOSE RESTRICTIONS    V1L5
         LTR       R3,R3               WAS +VE VALUE SPECIFIED?    V1L5
         BP        BANAL1              YES -> LET USER PROCEED     V1L5
         BZ        BSTXTME             STORE IF ZERO               V1L5
         L         R3,BDEFTME          ELSE TAKE DEFAULT VALUE     V1L5
         B         BSTXTME             SAVE FOR EXECUTION TIME     V1L5
BANAL1   DS        0H                  VERIFY TIME RESTRICTIONS    V1L5
         CL        R3,BMAXTME          TIME SPECIFIED WITHIN LIMITSV1L5
         BNH       BANAL110            PROCEED IF YES              V1L5
         L         R3,BMAXTME          ELSE RESET TO MAXIMUM       V1L5
BANAL110 DS        0H                                              V1L5
         AIF       ('&OPSYS' NE 'DOS').NDOSM                       V1L5
         M         R2,KF300            CONVERT TO DOS TIME UNITS   V1L5
         AGO       .MDONE                                          V1L5
.NDOSM   AIF       ('&OPSYS' EQ 'DOSVS').MDONE                     V1L5
         M         R2,KF38400          CONVERT INTO TIMER UNITS    V1L5
.MDONE   ANOP                                                      V1L5
BSTXTME  DS        0H                                              V1L5
         ST        R3,XTIME            SAVE FOR PROGRAM EXECUTION  V1L5
         BR        RLOOP               PRESS ON FOR NEXT OPTION    V1L5
*******************************************************************V1L5
******  THIS ROUTINE SCANS OFF THE TIME OF THE FORM 'TIME=(M,S)'   V1L5
*******************************************************************V1L5
BTMESCAN DS        0H                                              V1L5
         ST        RDONE,TSAVE         SAVE LINKREG                V1L5
         CLI       0(R1),C'('          IS IT EBCDIC L-BRACKET?     V1L5
         BE        LBJUMP              YES - CONTINUE              V1L5
         CLI       0(R1),C'%'          IS IT BCD L-BRACKET?        V1L5
         BNE       BTNOBRK             NO - GO TO NO-BRACKET SCAN  V1L5
LBJUMP   DS        0H                  SCAN RTN WITH BRACKETS      V1L5
         LA        R1,1(R1)            BUMP POINTER PAST BRACKET   V1L5
         THEN      (DO,BANALCVB)       GET MINUTES ENTRY           V1L5
         M         R2,KF60             CONVERT TO SECONDS          V1L5
         ST        R3,BTEMP1           SAVE # OF SECONDS           V1L5
         CLI       0(R1),C','          ARE WE FINISHED?            V1L5
         LA        R1,1(R1)            ADVANCE ONE COLUMN          V1L5
         BNE       BTEND               RETURN IF YES               V1L5
         THEN      (DO,BANALCVB)       GET SECONDS ENTRY           V1L5
         A         R3,BTEMP1           KEEP RUNNING TOTAL          V1L5
         LA        R1,1(R1)            BUMP POINTER PAST DELIMITER V1L5
         B         BTEND               STORE AND TIDY UP           V1L5
BTNOBRK  DS        0H                                              V1L5
         THEN      (DO,BANALCVB)       SCAN OFF ENTRY              V1L5
         CLI       DEBUGSW,ZSWON       ARE WE IN DEBUG MODE?       V1L5
         BNE       BTNODB              FALL THRU IF YES            V1L5
         M         R2,=A(B$&MONTIME)   CONVERT TO UNITS FOR MONITORV1L5
         B         BTEND               AND CONTINUE                V1L5
BTNODB   DS        0H                                              V1L5
         M         R2,=A(B$&TIMEIN)    CONVERT TO PROPER UNITS     V1L5
BTEND    DS        0H                                              V1L5
         L         RDONE,TSAVE         FETCH RETURN REGISTER       V1L5
         BR        RDONE               RETURN                      V1L5
         SPACE     1                                               V1L5
BANALCVB SR        R3,R3                                           V1L5
BCVBLOOP IFANY     (ZR1,LT,C'0'),(ZR1,GT,C'9'),DONE                V1L5
         M         R2,KF10             R3=R3*10                    V1L5
         IC        R2,ZR1                                          V1L5
         N         R2,KM2831                                       V1L5
         AR        R3,R2                                           V1L5
         LA        R1,ZR1+1                                        V1L5
         B         BCVBLOOP                                        V1L5
         SPACE     1                                               V1L5
         LTR       R3,R3               DID WE GET A TIME?
***********************************************************************
******    MAX NO. OF PAGES ALLOWED FOR THE JOB
***********************************************************************
BPGE00   DS        0H                                              V1L5
         CLC       ZR1(5),KPAGES       IS IT PAGES?                V1L5
         BNE       BANAL3              NOPE
         LA        R1,ZR1+5            POINT TO'=' AND CHECK IF,THEN
BPGE10   DS        0H                                              V1L5
         THEN      (DO,BEQCHECK),(DO,BANALCVB)   CONVERT NUMBER
         LTR       R3,R3               IS THE NUMBER > 0 ?         V1L5
         BZ        BPGE20              NO                          V1L5
         CLI       DEBUGSW,ZSWON       DEBUG MODE ?                V1L5
         BNE       BPGE90              NO                          V1L5
         CL        R3,BMAXPGE          IS # > MAXIMUM ALLOWED ?    V1L5
         BNH       BPGE90              NO                          V1L5
         L         R3,BMAXPGE          YES, GET THE MAXIMUM        V1L5
         B         BPGE90              GO STORE IT                 V1L5
BPGE20   DS        0H                                              V1L5
         L         R3,BDEFPGE          GET THE DEFAULT FOR DEBUG   V1L5
         CLI       DEBUGSW,ZSWON       DEBUG MODE ?                V1L5
         BE        BPGE90              YES                         V1L5
         L         R3,DEFPGE           NON-MONITOR DEF. # OF PAGES V1L5
BPGE90   DS        0H                                              V1L5
         ST        R3,XPAGES           SAVE IT FOR RUN TIME
         BR        RLOOP               RETURN TO SCAN LOOP
         SPACE     3
***********************************************************************
*****     TYPE OF KEY PUNCH SPECIFIED
***********************************************************************
BANAL3   DS        0H                                              V1L5
         CLC       ZR1(2),KKP          CHECK FOR KP PARM           V1L5
         BNE       BLNE00              NOPE                        V1L5
         LA        R1,ZR1+2            POINT TO'='
         THEN      (DO,BEQCHECK)       CHECK IT
*****    SET    'XMODEKP'    X'00'  FOR BCDIC                      V1L5
****                         X'06'    FOR EBCDIC
****     SET  XQUOTCON  TO CONTAIN PROPER TYPE OF QUOTE
         CLC       ZR1(2),K26          BCD REQ'D?                  V1L5
         BNE       BANAL41             NOPE
         LA        R1,ZR1+2            POINT TO NEXT PARM
         MVI       XMODEKP,X'00'       SET FOR SCAN ENTRY          V1L5
         MVI       XQUOTCON,C'@'       WE'LL USE THIS FOR HOLLERITHS
         BR        RLOOP               AND RETURN
BANAL41  DS        0H                                              V1L5
         CLC       ZR1(2),K29          EBCDIC REQ'D?               V1L5
         BNE       BANALERR            NO SO AN ERROR
         LA        R1,ZR1+2            POINT TO NEXT PARM
         MVI       XMODEKP,X'06'       SET FOR SCAN                V1L5
         MVI       XQUOTCON,C''''      USE THIS ONE FOR HOLLERITHS
         BR        RLOOP               RETURN TO SCAN LOOP
         SPACE     3
***********************************************************************
******     SET NUMBER OF LINES PER PAGE
***********************************************************************
BLNE00   DS        0H                                              V1L5
         CLC       ZR1(5),KLINES       LINES SPECIFIED?            V1L5
         BNE       BANAL5              NO SO CONTINUE
         LA        R1,ZR1+5            POINT TO EQUAL AND CHECK
BLNE10   DS        0H                                              V1L5
         THEN      (DO,BEQCHECK),(DO,BANALCVB)   CONVERT VALUE
         NI        KOMPSW,255-LINES0   TURN OFF LINES0 SW          V1L5
         LTR       R3,R3               IS NUMBER > 0 ?             V1L5
         BP        BLNE20              YES                         V1L5
         OI        KOMPSW,LINES0       LINES = 0 DEFAULT           V1L5
         B         BLNE30              GO GET THE DEFAULT          V1L5
BLNE20   DS        0H                                              V1L5
         CLI       DEBUGSW,ZSWON       DEBUG MODE ?                V1L5
         BNE       BLNE90              NO                          V1L5
         CL        R3,BMAXLNE          IS # > MAXIMUM ALLOWED ?    V1L5
         BNH       BLNE90              NO                          V1L5
         L         R3,BMAXLNE          YES, GET THE MAXIMUM        V1L5
         B         BLNE90              GO STORE IT                 V1L5
BLNE30   DS        0H                                              V1L5
         L         R3,BDEFLNE          GET THE DEFAULT FOR DEBUG   V1L5
         CLI       DEBUGSW,ZSWON       DEBUG MODE ?                V1L5
         BE        BLNE90              YES                         V1L5
         L         R3,DEFLNE           NON-MONITOR DEF. # OF LINES V1L5
BLNE90   DS        0H                                              V1L5
         ST        R3,XLINES           SAVE IT FOR RUN TIME
         BR        RLOOP
BANAL5   DC        0H'0'     BANAL5:
         SPACE
         AIF       ('&WATTYPE' EQ 'DISTR').BNW1
         CLC       ZR1(5),=C'DEBUG'
         BNE       BANAL6
         LA        R1,ZR1+5
         THEN      (DO,BEQCHECK)       CALL BEQCHECK;
         PACK      BDEBUG+2(3),ZR1(5)
         LA        R1,ZR1+4            R1=R1+4;
         BR        RLOOP
         SPACE
         ENTRY     BDEBUG
BDEBUG   DS        6C
BANAL6   DC        0H'0'     BANAL6:
         SPACE
         CLC       ZR1(4),=C'TEST'
         BNE       BANAL7
         LA        R1,ZR1+4            R1=R1+4;
         MVI       COPT,C'S'           TURN ON SNAP SWITCH
         BR        RLOOP
BANAL7   DC        0H'0'     BANAL7:
.BNW1    ANOP
         SPACE     1                                               V1L5
*          MAINLINES CALLED FROM WATLIB                            V1L5
BANALW5  CLC       ZR1(3),KPGM         CHECK FOR 'PGM' KEYWORD     V1L5
         BNE       BANALW6             NO - NEXT TEST              V1L5
         LA        R1,ZR1+3            BUMP POINTER                V1L5
BANALW52 THEN      (DO,BEQCHECK)       CHECK EQUALS SIGN           V1L5
         LR        R2,R1               SET SCAN POINTER            V1L5
         LA        R3,8                SET COUNTER                 V1L5
BANALW53 IF        (ZR2,EQ,C','),BANALW51                          V1L5
         IF        (ZR2,EQ,C' '),BANALW51                          V1L5
         LA        R2,ZR2+1            BUMP POINTER                V1L5
         BCT       R3,BANALW53         AROUND AGAIN                V1L5
         B         BANALERR            NAME TOO LONG               V1L5
BANALW51 SR        R2,R1               CALCULATE FIELD LENGTH      V1L5
         LTR       R2,R2               CHECK FOR ZERO LENGTH       V1L5
         BZ        BSRZ                ERROR IF YES                V1L5
         BCTR      R2,0                DECREMENT BY ONE            V1L5
         MVI       RTNADDR,C' '        BLANK OUT                   V1L5
         MVC       RTNADDR+1(7),RTNADDR    RTNADDR FIELD           V1L5
         EX        R2,BMVC1            SAVE S/R NAME IN RTNADDR    V1L5
         LA        R4,1(R1,R2)         POSITION R1 AND SAVE IN R4  V1L5
         LA        R0,RTNADDR          POINT TO THE ROUTINE NAME   V1L5
         CLINK1    JFIND               FIND IT                     V1L5
         LTR       R15,R15             WAS IT THERE?               V1L5
         BZ        RTNFOUND            YES - BRANCH                V1L5
BSRZ     DS        0H                                              V1L5
         LA        R7,RTNADDR          NO - POINT AT INVALID NAME  V1L5
         $ERROR    (NOAC,SR,B,NAMR7),XSTOP                         V1L5
         DS        0F                                              V1L5
RTNADDR  DC        8C' '               NAME SPECIFIED BY PGM=      V1L5
RTNFOUND THEN      (CSUBRDS,ON)                                    V1L5
         MVC       CPOINT(4),CBUFFE    FOR BLOCKED WATLIB?         V1L5
         AIF       ('&LIBMODE' EQ 'SAME').BLIB1                    V1L5
         AIF       ('&LIBMODE' EQ 'EBCDIC').BLIB2                  V1L5
         MVI       XMODEKP,X'00'       INDICATE LIBRARY IN 26 MODE V1L5
         MVI       XQUOTCON,C'@'                                   V1L5
         AGO       .BLIB1                                          V1L5
.BLIB2   MVI       XMODEKP,X'06'       INDICATE LIBRARY IN 29 MODE V1L5
         MVI       XQUOTCON,C''''                                  V1L5
.BLIB1   ANOP                                                      V1L5
         LR        R1,R4               RESTORE SCAN POINTER        V1L5
         BR        RLOOP               NEXT                        V1L5
         SPACE     1                                               V1L5
BMVC1    MVC       RTNADDR(*-*),0(R1)                              V1L5
         SPACE     1                                               V1L5
*          SHORT FORM FOR 'TIME'                                   V1L5
BANALW6  CLI       ZR1,C'T'            TIME?                       V1L5
         BNE       BANALWA             NO - NEXT TEST              V1L5
         LA        R1,ZR1+1            BUMP POINTER                V1L5
         B         BANAL111            GO TO TIME ROUTINE          V1L5
BANALWA  DC        0H'0'               BANALWA;                    V1L5
         SPACE     1                                               V1L5
         SPACE
*******************************************************************
********* THE FOLLOWING SECTION HANDLES OPTIONS OF THE    *********
********* FORM 'XXXX' OR 'NOXXXX'                         *********
********* IT MUST BE THE LAST SECTION OF THE JOB CARD     *********
********* OPTION ROUTINE BECAUSE IT PICKS OFF THE 'NO'    *********
********* SEPARATELY AND SAVES IT IN THE SWITCH 'BNOSW'   *********
********* IF THE KEYWORD HAS A 'NO' THE SWITCH IS OFF     *********
********* IF THE KEYWORD DOES NOT HAVE A 'NO' THE SWITCH  *********
********* IS ON                                           *********
*******************************************************************
         CLC       KNOCHECK,ZR1        RUN=NOCHECK?                V1L2
         BE        BNOCHECK            BRANCH IF YES               V1L2
         THEN      (BNOSW,ON)          BNOSW=1;
         CLC       KNO,ZR1             CHECK FOR 'NO'
         BNE       BANAL91
         THEN      (BNOSW,OFF)         BNOSW=0;
         LA        R1,ZR1+L'KNO        BUMP UP COUNTER
BANAL91  CLC       KLIST,ZR1           CHECK FOR 'LIST'
         BNE       BANAL92
         LA        R1,ZR1+L'KLIST      BUMP UP COUNTER
         MVC       CLISTSW,BNOSW       CLISTSW=BNOSW;
         BR        RLOOP
BANAL92  CLC       KLIBLIST,ZR1        CHECK FOR 'LIBLIST'
         BNE       BANAL93
         LA        R1,ZR1+L'KLIBLIST   BUMP UP COUNTER
         MVC       CLIBLSW,BNOSW       CLIBLSW=BNOSW;
         BR        RLOOP
BANAL93  DS        0H                                              V1L5
         CLC       KSUB,ZR1            WAS SUBCHK SPECIFIED?       V1L5
         BNE       BANAL934            BRANCH IF NO                V1L5
         LA        R1,ZR1+L'KSUB       BUMP CARD PTR  ü            V1L5
         IF        (BNOSW,OFF),BANAL932 BRANCH FOR NOSUBCHK        V1L5
         MVI       XSRBR,X'F0'         MOVE BRANCH MASK FOR SUBCHK V1L5
         B         BANAL933            CONTINUE SCANNING           V1L5
BANAL932 DS        0H                                              V1L5
         MVI       XSRBR,X'00'         NOP BR FOR NOSUBCHK         V1L5
BANAL933 DS        0H                                              V1L5
         MVC       XAN1+1(1),XSRBR     ZAP XATEST ROUTINE          V1L5
         MVC       XAN2+1(1),XSRBR                                 V1L5
         BR        RLOOP               CONTINUE SCANNING           V1L5
BANAL934 DS        0H                                              V1L5
         CLC       KWARN,ZR1           CHECK FOR WARN              V1L5
         BNE       BANAL935                                        V1L3
         LA        R1,ZR1+L'KWARN      BUMP UP COUNTER
         MVC       CWARNSW,BNOSW       CWARNSW=BNOSW;
         BR        RLOOP
BANAL935 CLC       KEXT,ZR1            CHECK FOR 'EXT'             V1L3
         AIF       ('&PROFILE' EQ 'OMIT').PROF2                    V1L5
         BNE       BANAL936            NO                          V1L5
         AGO       .PROF3                                          V1L5
.PROF2   ANOP                                                      V1L5
         BNE       BANALW7             NO                          V1L5
.PROF3   ANOP                                                      V1L5
         LA        R1,ZR1+L'KEXT       SWALLOW EXT                 V1L3
         MVC       CEXTNSW,BNOSW       CEXTNSW = BNOSW             V1L3
         BR        RLOOP                                           V1L3
         AIF       ('&PROFILE' EQ 'OMIT').PROF4                    V1L5
BANAL936 CLC       KPROF,ZR1           PROFILE OPTION ?            V1L5
         BNE       BANALW7             NO                          V1L5
         LA        R1,ZR1+L'KPROF      SKIP TO NEXT                V1L5
         CLI       0(R1),C'C'          COUNTS ?                    V1L5
         BNE       BPERCENT            NO                          V1L5
         IF        (BNOSW,ON),BPROFC   NOPROFC OR PROFC ?          V1L5
         NI        CCNTSW,X'FF'-XPROFC     NOPROFC                 V1L5
         B         BLOOP5                                          V1L5
BPROFC   OI        CCNTSW,XPROFC       PROFC                       V1L5
         B         BLOOP5                                          V1L5
BPERCENT CLI       0(R1),C'P'          PER CENT ?                  V1L5
         BNE       BRELATIV            NO                          V1L5
         IF        (BNOSW,ON),BPROFP   NOPROFP OR PROFP ?          V1L5
         NI        CCNTSW,X'FF'-XPROFP     NOPROFP                 V1L5
         B         BLOOP5                                          V1L5
BPROFP   OI        CCNTSW,XPROFP       PROFP                       V1L5
         B         BLOOP5                                          V1L5
BRELATIV CLI       0(R1),C'A'          ABSOLUTE HISTOGRAM ?        V1L5
         BNE       BTOUT               NO                          V1L5
         IF        (BNOSW,ON),BPROFA   NOPROFA OR PROFA ?          V1L5
         NI        CCNTSW,X'FF'-XPROFR     NOPROFA                 V1L5
         B         BLOOP5                                          V1L5
BPROFA   OI        CCNTSW,XPROFR       PROFA                       V1L5
         B         BLOOP5                                          V1L5
BTOUT    IF        (BNOSW,ON),BPROFALL NOPROF OR PROF ?            V1L5
         NI        CCNTSW,X'FF'-XPROF      NOPROF                  V1L5
         B         BLOOP5                                          V1L5
BPROFALL OI        CCNTSW,XPROF        PROF                        V1L5
         B         BLOOP5                                          V1L5
.PROF4   ANOP                                                      V1L5
         SPACE     3                                               V1L5
*******************************************************************V1L5
******   SET NUMBER OF EXECUTION-TIME PAGES ALLOWED FOR THE JOB    V1L5
*******************************************************************V1L5
         SPACE     1                                               V1L5
*          SHORT FORM FOR 'PAGES'                                  V1L5
BANALW7  CLI       ZR1,C'P'            PAGES?                      V1L5
         BNE       BANALW8             NO - NEXT TEST              V1L5
         IF        (BNOSW,OFF),BANALER2                            V1L5
         LA        R1,1(,R1)           BUMP POINTER                V1L5
         B         BPGE10              GO SET THE # OF PAGES       V1L5
         SPACE     3                                               V1L5
*******************************************************************V1L5
******     SET NUMBER OF LINES PER PAGE                            V1L5
*******************************************************************V1L5
*          SHORT FORM FOR 'LINES'                                  V1L5
BANALW8  CLI       ZR1,C'L'            LINES?                      V1L5
         BNE       BANAL94             NO - NEXT TEST              V1L5
         IF        (BNOSW,OFF),BANALER2                            V1L5
         LA        R1,1(,R1)           BUMP THE POINTER            V1L5
         B         BLNE10              GO SET # OF LINES           V1L5
         SPACE     3                                               V1L5
BANAL94  CLC       KFREE,ZR1           RUN=FREE?                   V1L2
         BE        BFREE               BRANCH IF YES               V1L2
         CLC       KCHECK,ZR1          RUN=CHECK?                  V1L2
         BE        BCHECK              BRANCH IF YES               V1L2
         SPACE
*****    IF YOU CAN'T FIND A COMPARE ASSUME DEFAULT AND PRINT AND ERROR
BANALERR $ERROR    (&JB1,JB,1,,HOL5R1),BLOOP5                      V1L5
         SPACE
BANALER2 S         R1,KF2              POINT TO "NO....            V1L5
         B         BANALERR            GO PRINT THE ERROR          V1L5
         SPACE     1                                               V1L5
*****    COME HERE TO SCAN OVER ACCOUNT NUMBER IF &IDENT='YES'
BIDYES   DC        0H'0'     BLOOP0:
         SPACE
* SEARCH FOR THE NEXT KEYWORD
BLOOP5   IF        (ZR1,EQ,C' '),BANALDON
         IF        (ZR1,EQ,C','),BLOOP3
         LA        R1,ZR1+1            R1=R1+1;
         B         BLOOP5              GO TO BLOOP5;
         SPACE
BEQCHECK CLI       0(R1),C'='                                      V1L5
         BE        $1323                                           V1L5
         CLI       0(R1),C'#'                                      V1L5
         BE        $1323                                           V1L5
         CLI       0(R1),C' '                                      V1L5
         BE        BANALERR                                        V1L5
         CLI       0(R1),C','                                      V1L5
         BE        BANALERR                                        V1L5
         LA        R1,1(,R1)                                       V1L5
         B         BEQCHECK                                        V1L5
$1323    DS        0H                                              V1L5
         LA        R1,ZR1+1            R1=R1+1;
         BR        RDONE               RETURN;
         SPACE
BANALDON DC        0H'0'     BANALDON:
         SPACE
         B         CRET                RETURN;
         SPACE
         $FREE     RP,R13                                          V1L5
         EJECT
*******************************************************************
*********          END OF SOURCE                          *********
*******************************************************************
BENDSRS  BR        RDONE     BENDSRS:  RETURN;
         SPACE
*******************************************************************
*********          END OF COMPILE                         *********
*******************************************************************
BENDCOMP BR        RDONE     BENDCOMP: RETURN;
         EJECT
*******************************************************************
********          END OF JOB                              *********
*******************************************************************
BENDJOB  CENT      BSAVE
* THIS ROUTINE PRINTS THE TIME
BPPDONE  L         R1,MTIMER1
         S         R1,MTIMER2
         MVI       XEXECSW,OFF                                     V1L4
         LA        R2,BCTIME
         THEN      (DO,BCONVERT)       CALL BCONVERT;
         LA        R2,BXTIME
         L         R1,XTIME
         S         R1,MTIMER3
BOFF     THEN      (DO,BCONVERT)       CALL BCONVERT;
         LA        R5,BXDATE           ADDR OF DATE MESSAGE        V1L4
         L         R15,=V(ZMONTH)      CALL ZMONTH                 V1L4
         BALR      R14,R15             INSERT TIME, DAY AND TIME   V1L5
         L         R1,XBEGDATA
         S         R1,XOBJECT          OBJECT CODE
         LA        R2,BOBSP
         THEN      (DO,BSPCVRT)        CALL BSPCVRT;
         L         R1,CENDARAY
         S         R1,XBEGDATA         ARRAY AREA
         LA        R2,BDATSP
         THEN      (DO,BSPCVRT)        CALL BSPCVRT;
         AIF       ('&MEMTYPE' EQ 'MAX').BACCT24
         L         R1,=A(&MEMSIZE)
         AGO       .BACCT25
.BACCT24 L         R1,MVALUESG         AM'T OF CORE FORM GETMAIN
         S         R1,MVALUESS         AM'T OF CORE FOR O.S.
.BACCT25 ANOP
         LA        R2,BMEMSP
         THEN      (DO,BSPCVRT)
         L         R14,XPAGES          ALLOW FOR EXCEEDING PAGE    V1L4
         LA        R14,5(,R14)         LIMIT WHILE PRINTING CORE   V1L5
         ST        R14,XPAGES          AND MESSAGE STATISTICS      V1L4
         BAL       R9,CPRINT
         SPRIN     BBUFFER1,BBUFFE-BBUFFER1
         L         R1,XOBJECT          RESTORE PTR TO DATA AND     V1L4
         ST        R1,XBEGDATA         END OF ARRAY AREA SO        V1L4
         ST        R1,CENDARAY         OBJ CODE & ARRAY ZEROED OUT V1L4
         LH        R1,XNOERRS          GRT NO. OF ERRORS           V1L3
         LA        R2,BNOERRS          AND CONVERT                 V1L3
         THEN      (DO,BSPCVRT)                                    V1L3
         LH        R1,XNOWARNS         GET NO. OF WARNINGS         V1L3
         LA        R2,BNOWARNS         AND CONVERT                 V1L3
         THEN      (DO,BSPCVRT)                                    V1L3
         LH        R1,XNOEXTNS         GET NO. OF EXTENS           V1L3
         LA        R2,BNOEXTNS         AND CONVERT                 V1L3
         THEN      (DO,BSPCVRT)                                    V1L3
         SPRIN     BBUFFER2,BBUFF2-BBUFFER2  PRINT STATS           V1L3
         SPRIN     BBUFFER,BBUFFEND-BBUFFER
         B         CRET                RETURN;
         SPACE
BCONVERT DS        0H                                              V1L5
         SR        R0,R0                                           V1L5
         AIF       ('&OPSYS' NE 'DOS').NDOSD                       V1L5
         D         R0,KF3              R1=R1/3                     V1L5
         AGO       .DDONE                                          V1L5
.NDOSD   AIF       ('&OPSYS' EQ 'DOSVS').DDONE                     V1L5
         D         R0,KF384            R1=R1/384                   V1L5
.DDONE   ANOP                                                      V1L5
         CVD       R1,BTEMP1
         MVC       ZR2+1(8),BEDMASK
         ED        ZR2(9),BTEMP1+4
         BR        RDONE               RETURN;
         SPACE
BSPCVRT  CVD       R1,BTEMP1
         MVC       ZR2+1(7),BEDMASK2
         ED        ZR2(8),BTEMP1+4
         BR        RDONE               RETURN;
         SPACE
         $FREE     RP,R13                                          V1L5
         EJECT
*******************************************************************
********           RETURN TO SYSTEM                       *********
*******************************************************************
BSTOP    CENT      BSAVE               ESTABLISH LINKAGE           V1L5
THATSIT  IF        (STATSW,EQ,OFF),CRET STATS RQRD ?               V1L5
         AIF       ('&STATS' EQ 'NO').BACCT3                       V1L5
         OPEN  (COUNTDCB,(UPDAT))      OPEN STATS FILE             V1L5
         TM    COUNTDCB+48,X'10'       OPEN OK ?                   V1L5
         BZ    ERRDCB                  GIVE ERROR IF NOT           V1L5
BYOPEN   GET   COUNTDCB                GET FILE                    V1L5
         L     R15,=V(ERTABLE)         ADDR OF TABLE               V1L5
         LR    R8,R1                   LOAD R8                     V1L5
         LA    R1,0                    ZAP IN ZERO                 V1L5
         LA    R2,2                    FOR BXLE                    V1L5
         LA    R3,638                  TO STOP BXLE                V1L5
LOOP     LH    R4,0(R1,R8)             INDEX                       V1L5
         AH    R4,0(R1,R15)            ADD COUNT                   V1L5
         STH   R4,0(R1,R8)             STORE IT                    V1L5
         BXLE  R1,R2,LOOP              AND AWAY WE GO              V1L5
         PUTX  COUNTDCB,(R8)           AND SAVE RESULTS            V1L5
         GET   COUNTDCB                FORCE OUTPUT                V1L5
BYCLOS   CLOSE (COUNTDCB)              CLOSE UP                    V1L5
COUNTDCB DCB   DDNAME=ERRCOUNT,BFALN=F,                            V1L5X
               MACRF=(GL,PM),LRECL=640,BLKSIZE=640,RECFM=F,        V1L5X
               DSORG=PS,EODAD=BYCLOS                               V1L5
ERRDCB   WTO   'UNABLE TO OPEN WATFIV STATISTICS FILE'             V1L5
.BACCT3  ANOP                                                      V1L5
         B     CRET                                                V1L5
         SPACE     1                                               V1L5
         EJECT
* STORAGE
         SPACE
BSAVE    DS        18F
*   THESE BLOCKS OF CODE MUST FOLLOW 'BSAVE' TO BE ADDRESSABLE     V1L5
         SPACE     1                                               V1L5
*******************************************************************V1L5
******  THIS ROUTINE CALLS THE DECK 'ACCOUNT' WHICH IS  ***********V1L5
******  THE LINK WITH THE SYSTEM ACCOUNTING ROUTINES.   ***********V1L5
*******************************************************************V1L5
         AIF       ('&STATS' EQ 'NO').BACCT1                       V1L5
WATACT   LA        R3,WCTACCTN         SET POINTER TO WCT          V1L5
         L         R15,=V(ACCOUNT)     GET ADDR OF ACCTNG ROUTINE  V1L5
         BALR      R14,R15             GO TO ACCOUNTING            V1L5
         B         *+4(R15)            PREFORM JUMP ON JUMP-TABLE  V1L5
         B         4(R8)               EVERYTHING IS OKAY          V1L5
         B         ERRAC0              ERROR 0                     V1L5
         B         ERRAC1              ERROR 1                     V1L5
         B         ERRAC2              ERROR 2                     V1L5
         B         ERRAC3              ERROR 3                     V1L5
         B         ERRAC4              ERROR 4                     V1L5
ERRAC0   $ERROR    (NOAC,AC,0),R8      ISSUE ERROR AND RETURN      V1L5
ERRAC1   $ERROR    (NOAC,AC,1),R8      ISSUE ERROR AND RETURN      V1L5
ERRAC2   $ERROR    (NOAC,AC,2),R8      ISSUE ERROR AND RETURN      V1L5
ERRAC3   $ERROR    (NOAC,AC,3),R8      ISSUE ERROR AND RETURN      V1L5
ERRAC4   $ERROR    (NOAC,AC,4),R8      ISSUE ERROR AND RETURN      V1L5
.BACCT1  ANOP                                                      V1L5
BTEMP1   DS        D
TSAVE    DS        F                                               V1L5
BOPT     DS        X
BNOSW    DS        X
         SPACE
BBUFFER  DC        C'0'
         DC        C'COMPILE TIME='
BCTIME   DC        CL10' '
         DC        C'SEC,'
         DC        C'EXECUTION TIME='
BXTIME   DC        CL10' '
         DC        C'SEC,'
BXDATE   DC        39C' '                                          V1L4
         DC        C'    WATFIV - JAN 1976 V1L5 '                  V1L5
BBUFFEND EQU       *
BBUFFER1 DC        C'0CORE USAGE'
         DC        C'       '
         DC        C'OBJECT CODE='
BOBSP    DC        CL9' '
         DC        C'BYTES,'
         DC        C'ARRAY AREA='
BDATSP   DC        CL9' '
         DC        C'BYTES,'
         DC        C'TOTAL AREA AVAILABLE='
BMEMSP   DC        CL10' '
         DC        C'BYTES'
BBUFFE   EQU       *
BBUFFER2 DC        C'0'                                            V1L3
         DC        C'DIAGNOSTICS'                                  V1L3
         DC        CL8' '                                          V1L3
         DC        C'NUMBER OF ERRORS='                            V1L3
BNOERRS  DC        CL8' '                                          V1L3
         DC        CL2', '                                         V1L3
         DC        C'NUMBER OF WARNINGS='                          V1L3
BNOWARNS DC        CL8' '                                          V1L3
         DC        CL2', '                                         V1L3
         DC        C'NUMBER OF EXTENSIONS='                        V1L3
BNOEXTNS DC        CL8' '                                          V1L3
BBUFF2   EQU       *                                               V1L3
         EJECT
* CONSTANTS
         SPACE
KNOCHECK DC        0CL7' '
KNO      DC        C'NO'
KCHECK   DC        C'CHECK'
KFREE    DC        C'FREE'                                         V1L2
         AIF       ('&PROFILE' EQ 'OMIT').PROF1                    V1L5
KPROF    DC        C'PROF'                                         V1L5
.PROF1   ANOP                                                      V1L5
KLIBLIST DC        0CL7' ',C'LIB'
KLIST    DC        C'LIST'
KWARN    DC        C'WARN'
KEXT     DC        C'EXT'                                          V1L3
KSUB     DC        C'SUB'              SUBCHK OPTION               V1L5
KTIME    DC        CL4'TIME'           TIME KEYWORD                V1L5
KPAGES   DC        CL5'PAGES'          PAGES KEYWORD               V1L5
KDEBUG   DC        C'DEBUG'                                        V1L5
KRUN     DC        C'RUN'                                          V1L5
KLINES   DC        C'LINES'                                        V1L5
KKP      DC        C'KP'                                           V1L5
K26      DC        C'26'                                           V1L5
K29      DC        C'29'                                           V1L5
KPGM     DC        C'PGM'                                          V1L5
BEDMASK  EQU       XEDMASK+4
BEDMASK2 EQU       XEDMASK+2
         EJECT
* LITERALS
         SPACE
KF384    DC        F'384'                                          V1L5
         AIF       ('&OPSYS' NE 'DOS').NOKF300                     V1L5
KF300    DC        F'300'                                          V1L5
.NOKF300 ANOP                                                      V1L5
KF60     DC        F'60'                                           V1L5
         SPACE
         TITLE     ' '
********ACCOUNTING TABLE AND SWITCHES*********                     V1L5
         AIF       ('&STATS' EQ 'NO').BACCT2                       V1L5
         ENTRY     WCTACCTN                                        V1L5
WCTACCTN DS        F,CL9          ACCOUNT NUMBER XXXXTPPPPPPPP     V1L5
WCTPRIOR DS        C              PRIORITY CHARACTER               V1L5
         DS        H              RESERVED                         V1L5
WCTCOUNT DS        F              JOB COUNTER                      V1L5
WCTTIME  DS        F              MAXIMUM TIME/JOB (SEC)           V1L5
WCTPAGES DS        F              MAXIMUM PAGES/JOB                V1L5
WCTLINES DS        F              MAXIMUM LINES/PAGE               V1L5
WCTRTIME DS        F              REAL TIME FOR JOB (SEC)          V1L5
WCTREAD  DS        F              ACTUAL READER TIME  (SEC)        V1L5
WCTPRINT DS        F              ACTUAL PRINTER TIME (SEC)        V1L5
WCTPUNCH DS        F              ACTUAL PUNCH TIME  (SEC)         V1L5
WCTTIMEN DS        F                    TASK  TIME    (SEC)        V1L5
WCTCOST  DS        F                    COST(ZERO FOR WATFIV)      V1L5
WCTSPAR1 DS        F                    SPARE #1                   V1L5
WCTSPAR2 DS        F                    SPARE #2                   V1L5
.BACCT2  ANOP                                                      V1L5
STATSW   DC        X'00'               STATISTICS SW               V1L5
SAVER2   DC        3F'00'              SAVE AREA                   V1L5
MONSTOPT DS        0F                                              V1L5
BDEFLNE  DC        F'&MNDFLNE'         DEFAULT # OF LINES          V1L5
         DC        F'0'                                            V1L5
BDEFPGE  DC        F'&MNDFPGE'         DEFAULT # OF PAGES          V1L5
         DC        F'0'                                            V1L5
         AIF       ('OPSYS' NE 'DOS').NDOST                        V1L5
BDEFTME  DC        A(&MNDFTME*300)                                 V1L5
         AGO       .TDONE                                          V1L5
.NDOST   AIF       ('&OPSYS' NE 'DOSVS').NDOSVST                   V1L5
BDEFTME  DC        A(&MNDFTME)                                     V1L5
         AGO       .TDONE                                          V1L5
.NDOSVST ANOP                                                      V1L5
BDEFTME  DC        A(&MNDFTME*38400)   DEFAULT TIME                V1L5
.TDONE   ANOP                                                      V1L5
BMAXTME  DC        F'&MNMXTME'         MAX AMOUNT OF TIME          V1L5
BMAXPGE  DC        F'&MNMXPGE'         MAX AMOUNT OF PAGES         V1L5
BMAXLNE  DC        F'&MNMXLNE'         MAX # OF LINES/PAGE         V1L5
DEFPGE   DC        F'&PAGES'           NON-MONITOR DEFAULT PAGES   V1L5
DEFLNE   DC        F'&LINES'           NON-MONITOR DEFAULT LINES   V1L5
KF38400  DC        F'38400'            CONVERSION FACTOR FOR OS TU V1L5
B$MIN    EQU       60                                              V1L5
B$SEC    EQU       1                                               V1L5
         LTORG                                                     V1L5
