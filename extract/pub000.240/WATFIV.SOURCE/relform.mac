RELF     TITLE     'RELFORM     TRACE SUPPORT PROGRAM'
         MACRO
         DATE      &DATE
         LCLC      &TEMP
&TEMP    SETC      'RF#'.'&DATE'
&TEMP    EQU       *
         ENTRY     &TEMP
         MEND
RELFORM  CSECT
         EXTRN     STARTA,STARTAE
         EXTRN     STARTB,STARTBE
         EXTRN     BACCT
         EXTRN     JSYS
         EXTRN     ERROR
         EXTRN     SCANA
         EXTRN     STRPROG                                         V1L5
         EXTRN     FORMAT
         EXTRN     ARITHCS
         EXTRN     FRIOSECT,FRIOEND
         EXTRN     FUNCTION,FDUMMY
         EXTRN     FORMCONV
         EXTRN     FORMCEND
         EXTRN     INOUT
         EXTRN     SPEX
         EXTRN     WATIO
         EXTRN     XOBJECT
         EXTRN     XIOBASE,PDATBASE,ERROR#
         EXTRN     LRCS
         EXTRN     GODO
         EXTRN     NAMELIST
         EXTRN     NAMELEND
         EXTRN     XTART
         EXTRN     COMMR
         EXTRN     MAINP
         EXTRN     TRGPRST
         EXTRN     FORMSNAP
         EXTRN     XTRACEB1
         EXTRN     XIOINIT
         EXTRN     XDATA
         DATE      SEP20
         EQUREGS
         EJECT
BEGIN    STM       R0,R14,12(R13)
         USING     RELFORM,R15
         BAL       R14,TRACERLD
         LINKTCP   USERTCP
         L         R15,=V(WATFOR)
         LM        R0,R14,12(R13)
         BR        R15
         LTORG
         DROP      R15
USERTCP  EQU       *
         LR        R11,R15
         USING     USERTCP,R11
         L         R2,=A(XOBJECT)
         L         R3,16(0,R2)         XENDOBJ
         L         R2,0(0,R2)          XOBJECT
         STM       R2,R3,OBJPTR+4
         STM       R2,R3,OBJECT
         L         R2,=V(TRINSTAD)
         L         R2,0(0,R2)
         CLC       0(2,R2),=X'0A00'    IS THIS AN EXCP SVC
         BNE       NOTEXCP             SKIP NEXT CODING IF NOT
         LA        R15,11
         BAL       R14,4(0,R14)        OUTPUT BLANK RECORD
NOTEXCP  EQU       *
         LA        R3,REGSTK-12
LOOP     LA        R3,12(0,R3)
         CL        R2,0(0,R3)
         BL        LOOP
         CL        R2,4(0,R3)
         BH        LOOP
         STM       R2,R3,INSTADDR
         L         R3,8(0,R3)
         BR        R3
CONTINUE LM        R2,R3,INSTADDR
         B         LOOP
PRINTON  LA        R15,1
         STH       R15,PRINSW
         L         R1,=V(XISNTRAC)     ISN TRACE SWITCH
         TM        0(R1),X'80'
         BNO       0(R14)
         LA        R15,0
         STH       R15,PRINSW
         BR        R14
PRINTOFF LA        R15,0
         CH        R15,PRINSW
         BNE       INDICATE
         BR        R14
INDICATE EQU       *
         STH       R15,PRINSW
         L         R2,=V(TRBUFFER)
         MVC       0(39,R2),MESSAGE1
         LA        R15,11
         BAL       R14,4(0,R14)
         LA        R15,11
         BAL       R14,4(R14)
         LH        R15,PRINSW
         BR        R14
PRINEXEC L         R4,=V(XEXECSW)
         TM        0(R4),X'80'
         BO        PRINTON
         B         CONTINUE
PRINCOMP L         R4,=V(XEXECSW)
         TM        0(R4),X'80'
         BZ        PRINTON
         B         CONTINUE
SNAP     L         R8,=V(TRGPRST)
         L         R7,=V(TRBUFFER)
         LM        R2,R3,4*2(R8)
         SH        R3,=H'1'
         CL        R3,=F'142'
         BNH       *+8
         LA        R3,142
         EX        R3,SNAPMVC
         LA        R15,11
         BAL       R14,4(0,R14)
         SR        R5,R5
         EX        R3,SNAPMVC
         LA        R4,1(0,R3)
SNAPL1   IC        R5,0(R7,R4)
         SRL       R5,4
         IC        R5,TRTABLE(R5)
         STC       R5,0(R7,R4)
         BCT       R4,SNAPL1
         LA        R15,11
         BAL       R14,4(0,R14)
         EX        R3,SNAPMVC
         LA        R4,1(0,R3)
SNAPL2   IC        R5,0(R7,R4)
         N         R5,=X'0000000F'
         IC        R5,TRTABLE(R5)
         STC       R5,0(R7,R4)
         BCT       R4,SNAPL2
         LA        R15,11
         BAL       R14,4(0,R14)
         B         CONTINUE
SNAPMVC  MVC       1(1,R7),0(R2)
TRTABLE  DC        C'0123456789ABCDEF'
PRINTISN L         R8,=A(TRGPRST+11*4)
         L         R8,0(0,R8)
         LH        R8,0(0,R8)
         CVD       R8,TEMP
         L         R8,=V(TRBUFFER)
         MVC       1(8,R8),=C'*** ISN='
         UNPK      9(7,R8),TEMP+5(4)
         MVC       14(2,R8),=C'  '
         MVC       15(112,R8),1(R8)
         LA        R15,11
         BAL       R14,4(0,R14)
         B         PRINTON
TEMP     DS        1D
REGSTK   DS        0F
         DC        A(FORMSNAP,FORMSNAP,SNAP)
         DC        A(XTART,XTART,PRINTISN)       ISN ROUTINE
         DC        A(ERROR,ERROR+X'600',PRINEXEC)     ERROR PROCESSOR
OBJECT   DC        A(*-*,*-*,PRINEXEC)                OBJECT CODE
         DC        A(FORMAT,FORMAT+X'710',PRINEXEC)   FORMAT
         DC        A(BACCT,BACCT+X'430',PRINEXEC)     ACCOUNTING
         DC        A(FORMCONV,FORMCEND,PRINEXEC)      FORMCONV
         DC        A(FRIOSECT,FRIOEND,PRINEXEC)            FRIOSCAN
         DC        A(MAINP,MAINP+X'C60',PRINEXEC)    MAIN
         DC        A(FUNCTION,FDUMMY,PRINEXEC)
         DC        A(STARTA,STARTAE,PRINEXEC)    ALL OF STARTA
         DC        A(WATIO,WATIO+X'0590',PRINEXEC)
*        DC        A(XDATA,XDATA+X'46',PRINEXEC)
         DC        A(XIOBASE,XIOBASE+X'420',PRINEXEC)      MAIN I/O
         DC        A(PDATBASE,PDATBASE+X'1AA',PRINEXEC)    MAIN DATA
         DC        A(XTRACEB1,XTRACEB1+X'C0',PRINEXEC) TRACE BACK RTN
         DC        A(ERROR#,ERROR#+X'1890'-X'17B0',PRINCOMP) I/O ERROR
         DC        A(STARTB+X'93C',STARTB+X'0100',PRINEXEC)
         DC        A(STARTB+X'B82',STARTBE,PRINEXEC)  YENTA&COMPLARITH
         DC        A(MAINP,RELFORM,PRINCOMP)          MOST DECKS
         DC        A(SCANA,SCANA+X'1400',PRINCOMP)                 V1L5
         DC        A(STRPROG,STRPROG+X'6B0',PRINCOMP)              V1L5
*        DC        A(JSYS,JSYS+X'6C0',PRINCOMP)
         DC        F'0',F'-1',A(PRINTOFF)        END OF STACK INDICATOR
PRINSW   DC        H'1'
MESSAGE1 DC        CL39' ////// TRACE OUTPUT TURNED OFF ///////'
STR14    DS        1F
INSTADDR DS        2F
TRACERLD BALR      R12,0
         USING     *,R12
         TRACERLD  STARTA,                                             X
               (FORMCONV,FORMCEND),                                    X
               SCANA,                                                  X
               FORMAT,                                                 X
               ARITHCS,                                                X
               (FRIOSECT,FRIOEND),                                     X
               FUNCTION,                                               X
               INOUT,                                                  X
               SPEX,                                                   X
               LRCS,                                                   X
               GODO,                                                   X
               WATIO,                                                  X
               (NAMELIST,NAMELEND),                                    X
               ERROR,                                                  X
               (,,'OBJECT',OBJPTR)
         BR        R14
         DROP      R12
         LTORG
         END
