PLOT     TITLE 'PLOT  ***  CALCOMP 770 PLOT PACKAGE'
PLOTS    START 0    PRODUCT NO. 26020   VERSION  V097  DATE  SEPT 1967
          PRINT NOGEN
*
***********************************************************************
*                                                                     *
*        FORTRAN LINKAGE FOR PLOT ROUTINE                             *
*                                                                     *
*        ENTRY PLOTS
*     CALL PLOTS(IBUF,NLOC,LDEV)                                      *
*              IBUF IS THE FIRST LOCATION IN A WORK AREA              *
*              NLOC IS THE NUMBER OF WORDS IN THE WORK AREA           *
*              LDEV IS NOT USED IN THIS SYSTEM                        *
*                                                                     *
         ENTRY PLOT
*     CALL PLOT(XPAGE,YPAGE,IPEN)                                     *
*              (XPAGE,YPAGE) IS THE PLOTTER PAGE COORDINATES IN INCHES*
*              IPEN IS THE PEN INDICATOR (LEGAL VALUES ARE            *
*                    2,3,12,13,22,23,999,-2,-3,-12,-13)               *
*                                                                     *
         ENTRY FACTOR
*     CALL FACTOR(FACT)                                               *
*              FACT IS A MULTIPLICATIVE FACTOR FOR ENTIRE PLOT        *
*                                                                     *
         ENTRY WHERE
*     CALL WHERE(RXPAGE,RYPAGE,RFACT)                                 *
*              (RXPAGE,RYPAGE) IS THE CURRENT PEN LOCATION            *
*              RFACT IS THE CURRENT FACTOR                            *
*                                                                     *
         ENTRY OFFSET
*     CALL OFFSET(XOFF,YFCT,YOFF,YFCT)                                *
*              XOFF IS THE XPAGE OFFSET                               *
*              XFCT IS THE XPAGE FACTOR                               *
*              YOFF IS THE YPAGE OFFSET                               *
*              YFCT IS THE YPAGE FACTOR                               *
*                                                                     *
***********************************************************************
         USING *,15
         B     #ID           SKIP AROUND ID
         DC    AL1(5),CL5'PLOTS'   DECK I.D.
#ID      DS    0H            BRANCH POINT
         DROP  15
         USING PLOTS,11
#SWCH    BCR   0,0                 SWITCH FOR RENTRY
         STM   14,12,12(13)        SAVE REGISTERS
         LR    11,15                AND SET UP
         LA    12,SAVE              BASE AND SAVE
         ST    12,8(0,13)           REGISTERS
         ST    13,SAVE+4        R11 IS BASE
         LR    13,12            R13 IS SAVE
         LE    0,STPSZ
         ME    0,FACT
         STE   0,FACSZ             FACSZ=FACT*STPSZ
         MVI   #SWCH+1,X'FE'       SET SWITCH TO PREVENT REENTRY
         MVC   PLOT(4),#SWCH+2    MAKESTM INSTEAD OF RETURN
         SR    0,0
         ST    0,NPX               NPX=0
         ST    0,NPY               NPY=0
         ST    0,ICOLD             ICOLD=0
         ST    0,XOLD              XOLD=0
         ST    0,YOLD              YOLD=0
         LM    2,4,0(1)            LOAD LINKAGE
         N     2,ADDMSK      REMOVE ANY HIGH ORDER BITS
NODEV    LA    11,PLOT             SET UP NEW BASE REGISTER
         USING PLOT,11
         L     3,0(0,3)            PICK UP COUNT
         BC    15,OPTAP            BRANCH TO OPEN TAPE
         DROP  11
**
         USING *,15
FACTOR   ST    2,SAVE         SAVE REGISTEP
         L     2,0(1)         LOAD LINKAGE
         LE    0,0(0,2)       PICK UP FACTOR
         LE    2,FACT
         DER   2,0
         LER   4,2
         ME    2,XOLD
         STE   2,XOLD    XOLD=XOLD*(OLD FACTOR/NEW FACTOR)
         ME    4,YOLD
         STE   4,YOLD    YOLD=YOLD*(OLD FACTOR/NEW FACTOR)
         STE   0,FACT         STORE FACT
         ME    0,STPSZ
         STE   0,FACSZ        FACSZ=FACT*STPSZ
         L     2,SAVE         RESTORE REGISTER
         BCR   15,14          EXIT
         DROP  15
**
         USING *,15
WHERE    STM   2,5,SAVE       SAVE REGISTERS
         LM    2,4,0(1)       LOAD LINKAGE
         LTR   3,3            TEST FOR THREE ARGUMENTS
         BC    4,NOFACT       IF THREE,
         L     5,FACT          PICK UP FACT AND
         ST    5,0(0,4)        STORE AS THIRD ARGUMENT
NOFACT   LM    4,5,XOLD       PICK UP XOLD AND YOLD
         ST    4,0(0,2)       STORE XOLD AS FIRST ARGUMENT
         ST    5,0(0,3)       STORE YOLD AS SECOND ARGUMENT
         LM    2,5,SAVE       RESTORE REGISTERS
         BCR   15,14          EXIT
         DROP  15
*   CODE  FOR  OFFSET ENTRY
         USING *,15         * OPTIONAL CODE FOR OFFSET ENTRY
OFFSET   STM   2,5,SAVE     *  SAVE REGISTERS
         LM    2,5,0(1)     *  LOAD LINKAGE
         L     2,0(0,2)     *  PICK UP XOFF
         L     3,0(0,3)     *          XFCT
         L     4,0(0,4)     *          YOFF
         L     5,0(0,5)     *          YFCT
         STM   2,5,XOFF     *  STORE XOFF,XFCT,YOFF,YFCT
         LM    2,5,SAVE     *  RESTORE REGISTERS
         BCR   15,14        *  EXIT
         DROP  15           *
**
         USING *,11
PLOT     BCR   15,14          TO PREVENT ENTRY BEFORE PLOTS IS CALLED
         BCR   15,14          REPLACED WITH  STM  14,12,12(13) TO
         LR    11,15          SAVE REGISTERS AND SET UP
         LA    12,SAVE        BASE REGISTER AND SAVE REGISTER
         ST    12,8(0,13)
         ST    13,SAVE+4
         LR    13,12
         LM    2,4,0(1)       LOAD LINKAGE
         LE    0,0(0,2)       PICK UP X
         LE    2,0(0,3)       PICK UP Y
         L     4,0(0,4)       PICK UP IC IN R4
         LM    1,3,BUFID      LOAD BUF INFORMATION
         ST    4,NEWIC        NEWIC=IC
         LPR   4,4            IC=ABS(IC)
         CH    4,TEN          IS IC LSS 10
         BC    4,TESTIC         YES,BRANCH TO TEST IC
*   CODE  FOR  OFFSET ENTRY
         CH    4,TWTY    * IS IC LSS 20
         BC    10,ICGT20 *    NO,BRANCH TO IC GT 20
         SH    4,TEN     *    YES,IC=IC-10
         SE    0,XOFF    *     X=(X-XOFF)/XFCT
         DE    0,XFCT    *
         SE    2,YOFF    *     Y=(Y-YOFF)/YFCT
         DE    2,YFCT    *
         BC    15,TESTIC *     GO TO TEST IC
XOFF     DC    E'0.0'    * OPTIONAL FOR OFFSET ENTRY
XFCT     DC    E'1.0'    *
YOFF     DC    E'0.0'    *
YFCT     DC    E'1.0'    *
**
ICGT20   CH    4,THTY         IS IC LSS 30
         BC    10,ICGT30        NO,BRANCH TO IC GT 30
         OI    NEWIC,X'80'      YES,SET SIGN BIT ON,BUT LEAVE NEWIC
         SH    4,TWTY             IC=IC-20
         BC    15,TESTIC          GO TO TEST IC
ICGT30   STH   4,BLKN         BLKN=IC
         LNR   4,4
         ST    4,NEWIC        NEWIC=-IC
         LA    4,3            IC=3
         MVI   CLSWT+1,X'F0'  SET CLOSE SWITCH ON
TESTIC   C     4,ICOLD             TEST IC
         BC    8,TESTX        IF IC=ICOLD,BRANCH TO TEST X
         ST    4,ICOLD        ICOLD=IC
         CH    4,TWO
         BC    4,TESTX        IF IC LSS 2,BRANCH TO TEST X
         BC    2,MVPNUP       IF IC GTR 2,BRANCH TO MOVE PEN UP
         IC    9,PNDNCD       IF IC = 2,LOAD CD=PEN DOWN CODE
         LH    8,PNDNCT                 LOAD CNT=PEN DOWN COUNT
         BC    15,STPEN                 GO TO STORE PEN
MVPNUP   IC    9,PNUPCD            LOAD CD=PEN UP CODE
         LH    8,PNUPCT            LOAD CNT=PEN UP COUNT
STPEN    STC   9,0(0,1)       STORE CD IN BUF
         BXLE  1,2,TSTPCT     INCREASE BUF AND BRANCH IF NOT FULL
         BAL   10,WBUF         WRITE BUF IF FULL
         BC    15,SVPME        BRANCH TO SAVE PEN CODE
TSTPCT   BCT   8,STPEN        DECREASE COUNT AND BRANCH IF NOT 0
SVPME    STC   9,PMOVE        SAVE CURRENT PEN CODE
TESTX    SR    4,4            DX=0                               R4-DX
         SR    6,6            K=0                                R6-K
         CE    0,XOLD         IS X=XOLD
         BC    8,TESTY          YES,BRANCH TO TEST Y
         STE   0,XOLD           NO,XOLD=X
         LPER  4,0
         ME    4,FACSZ        X=X*FACSZ
         AE    4,RNDCON       ROUND AND FIX X
         AW    4,FXCON
         STD   4,TEMP
         L     7,TEMP+4
         LTER  0,0
         BC    11,CALCDX
         LNR   7,7
CALCDX   LR    4,7
         S     4,NPX          DX=X-NPX
         ST    7,NPX          NPX=X
         BC    10,TESTY       IF DX IS POSITIVE,BRANCH TO TEST Y
         LA    6,1(0,6)       K=K+1
         LPR   4,4            DX=-DX
TESTY    SR    5,5            DY=0                               R5-DY
         CE    2,YOLD         IS Y=YOLD
         BC    8,TESTDX         YES,BRANCH TO TEST DXDY
         STE   2,YOLD           NO,YOLD=Y
         LPER  4,2
         ME    4,FACSZ        Y=Y*FACSZ
         AE    4,RNDCON       ROUND AND FIX Y
         AW    4,FXCON
         STD   4,TEMP
         L     7,TEMP+4
         LTER  2,2
         BC    11,CALCDY
         LNR   7,7
CALCDY   LR    5,7
         S     5,NPY          DY=Y-NPY
         ST    7,NPY          NPY=Y
         BC    10,TESTDX      IF DY IS POSITIVE,BRANCH TO TEST DXDY
         LA    6,2(0,6)       K=K+2
         LPR   5,5            DY=-DY
TESTDX   LR    7,4
         OR    7,5            IF DX AND DY ARE BOTH ZERO
         BC    8,ENDIC             BRANCH TO ENDIC
         CR    4,5
         BC    11,PLTALG      IF DX GEQ DY,BRANCH TO PLOT ALG
         LR    7,4
         LR    4,5            EXCHANGE DX AND DY
         LR    5,7
         LA    6,4(0,6)       K=K+4
         BC    15,PLTALG      BRANCH TO PLOT ALG
ENDIC    SR    0,0            RETURN FROM PLOT ALG
         TM    NEWIC,X'FF'    IF SIGN BIT OF NEWIC IS OFF
         BC    8,EXIT           BRANCH TO EXIT
         ST    0,NPX          NPX=0
         ST    0,NPY          NPY=0
         ST    0,ICOLD        ICOLD=0
         ST    0,XOLD         XOLD=0
         ST    0,YOLD         YOLD=0
         BC    4,EXIT         IF NEWIC IS POSITIVE BRANCH TO EXIT
         BAL   10,WBUF        WRITE PARTIAL BUFFER
         BAL   10,WBLK        WRITE BLOCK ADDRESS
CLSWT    BC    0,CLTAP        IF CLOSE SWITCH IS ON,BRANCH TO CLOSE
EXIT     STM   1,3,BUFID      SAVE BUF INFORMATION
         L     13,SAVE+4      RESTORE REGISTERS
         LM    14,12,12(13)
         MVI   12(13),X'FF'   SET COMPLETION FLAG
         BCR   15,14          EXIT
SAVE     DS    9D
SVR3     DS   3F
NEWIC    DC    F'0'
NPX      DC    F'0'
NPY      DC    F'0'
ICOLD    DC    F'0'
XOLD     DC    F'0'
YOLD     DC    F'0'
TEMP     DC    D'0'
FXCON    DC    X'4E00000000000000'
RNDCON   DC    E'0.5'
FACSZ    DC    E'0.0'
FACT     DC    E'1.0'
BUFID    DC   3F'1'     1-NEXT LOC IN BUFFER,1-ONE,3-LAST LOC IN BUFFER
BUFA     DC   4F'1'      ININTIAL BUFID AND BUFLK VALVES FOR 1ST BUFFER
BUFLK    DC    F'1'      STARTING LOCATION OF NEXT RECORD
BUFFL    DC    F'1'      FIRST DATA LOCATION IN NEXT RECORD
BUFCNT   DC    F'1'      COUNT FOR LAST RECORD WRITTEN
ADDMSK   DC    X'00FFFFFF'
TWO      DC    H'2'
TEN      DC    H'10'
TWTY     DC    H'20'
THTY     DC    H'30'
BLKN     DC    H'1'
PMOVE    DC    H'0'
**
*   CODE  FOR  24 VECTOR ROUTINES
PLTALG   LR    8,5
         SLA   8,1            NR=2*DY                       R4=NC
         IC    10,HAFCD(6)    AFCD=HAFCD(J)                 R5=MJCD
         CR    4,8            IS DX LSS 2*DY                R6=HFCD
         BC    4,DXLSS         YES,BRANCH TO PICK UP CODES  R7=NA
         IC    5,MAJCD(6)     MJCD=MAJCD(J)                 R8=NR
         IC    6,CMBCD(6)     CBCD=CMBCD(J)                 R9=NT
         BC    15,CLCNT
DXLSS    SR    5,4            DY=DX-DY
         LPR   8,5
         SLA   8,1            NR=2*DY
         IC    5,CMBCD(6)     MJCD=CMBCD(J)
         IC    6,MAJCD(6)     CBCD=MAJCD(J)
CLCNT    STC   6,CBCD         SAVE CBCD
         SLA   8,1            NR=2*NR
         LR    6,10           RESTORE HFCD
         LR    7,4            NA=DX
         LR    9,4            NC=DX
         SLA   9,1            NT=2*DX
         CH    4,TWO         IS DX LEQ 2
         BC    12,CHKNC         YES, DO NOT PLACE NOP IN BUFFER
SRTLP    CH    4,TWO          IS NC GTR 2
         BC    2,MNLOP          YES,GO TO MAIN LOOP
         MVC   0(1,1),INCD      NO,MOVE INCRE CODE INTO BUFFER
         BXLE  1,2,CHKNC        TEST FOR FULL BUFFER
         BAL   10,WBUF           WRITE
CHKNC    BCT   4,MNLOP          NC=NC-1,BRANCH TO MAINLOOP IF NOT ZERO
         SRA   8,1               NR=NR/2
         IC    6,CBCD
         O     5,HALVE           HFCD=(HALF)CBCD
         O     6,HALVE           MJCD=(HALF)MJCD
MNLOP    STC   5,0(0,1)
         BXLE  7,8,TSTBUF     NA=NA+NR,IF NA LSS NT,STORE HFCD IN BUF
         SR    7,9             IF NOT,NA=NA-NT
         STC   6,0(0,1)               STORE MJCD IN BUF
TSTBUF   BXLE  1,2,TSTNC      TEST FOR FULL BUFFER
         BAL   10,WBUF         WRITE
TSTNC    SH    4,TWO          NC=NC-2
         BC    2,SRTLP        IF NC GTR 0, REPEAT
         BC    15,ENDIC       EXIT
CBCD     DC    H'0'
**
WBLK     STM   1,3,BUFID      SAVE BUF INFORMATION
         LH    3,BLKN
         CVD   3,TEMP         CONVERT BLKN TO DECIMAL
         LA    3,1(0,3)
         STH   3,BLKN         INCREASE BLKN BY ONE
         L     2,TEMP+4       PICK UP DECIMAL BLKN
         SRL   2,4            SHIFT OFF SIGN
         SRDL  2,4            SHIFT UNITS DIGIT AND
         SRL   3,27           DOUBLE
         LH    5,BKTAB(3)     PICK UP CODES FOR UNITS DIGIT
         STC   5,BKNO+15
         STC   5,BKNO+18
         SRL   5,8            AND STORE CODES IN BLOCK RECORD
         STC   5,BKNO+12
         STC   5,BKNO+21
         SRDL  2,4            SHIFT TENS DIGIT AND
         SRL   3,27           DOUBLE
         LH    5,BKTAB(3)     PICK UP CODES FOR TENS DIGIT
         STC   5,BKNO+9
         STC   5,BKNO+24
         SRL   5,8            AND STORE CODES IN BLOCK RECORD
         STC   5,BKNO+6
         STC   5,BKNO+27
         SRDL  2,4            SHIFT HUNDREDS DIGIT AND
         SRL   3,27           DOUBLE
         LH    5,BKTAB(3)     PICK UP CODES FOR HUNDREDS DIGIT
         STC   5,BKNO+3
         STC   5,BKNO+30
         SRL   5,8            AND STORE CODES IN BLOCK RECORD
         STC   5,BKNO
         STC   5,BKNO+33
         LA    4,BKCD
         LH    5,BKCNT
         WRITE BLKCB,SF,PTDCB,(4),(5)
         CHECK BLKCB
         LM    1,3,BUFID      RESTORE BUF INFORMATION
         BCR   15,10          EXIT
**
WBUF     C     1,BUFA         IF POINTER IS AT FIRST LOC IN BUFFER
         BCR   8,10             THEN EXIT
         STM   4,6,SVR3       SAVE R4-6
         LR    3,1            SET POINTER TO
         BCTR  3,0              LAST LOC FILLED IN BUFFER
BUFCK    BC    15,BUFNC       BRANCH IF CHECK FLAG OFF
         CHECK BUFCB
BUFNC    MVI   BUFCK+1,0      SET CHECK FLAG ON
         L     1,BUFA         SET TO FIRST LOCATION IN BUFFER
         L     4,BUFFL        SET TO FIRST DATA LOC IN OUTPUT
WSTR     IC    6,0(0,1)       LOAD CODE FROM BUFFER
         STC   6,0(0,4)       STORE CODE IN OUTPUT
         LA    4,NOB(0,4)     INCREASE POINTER TO NEXT LOC IN OUTPUT
         BXLE  1,2,WSTR       LOOP BACK TO PICK UP ENTIRE BUFFER
         MVC   0(NOE,4),ENDPL   MOVE END PLOT CODES INTO BUFFER
         LA    4,NOE(0,4)
         S     4,BUFLK        CALCULATE LENGTH OF RECORD
         ST    4,BUFCNT        AND STORE
         L     4,BUFLK        LOAD ADDRESS AND COUNT FOR WRITE
         L     5,BUFCNT
         WRITE BUFCB,SF,PTDCB,(4),(5)
         LM    1,3,BUFA       LOAD BUFFER CONSTANTS
         LM    4,6,SVR3       RESTORE R4-6
         BCR   15,10           EXIT
**
CLTAP    CLOSE (PTDCB,DISP)
         BC    15,EXIT
**
OPTAP    LPR   3,3
         CH    3,BLKMAX       COMPARE AGAINST MAXIMUM
         BC    12,*+8
         LH    3,BLKMAX       IF TOO LARGE REPLACE WITH MAXIMUM
         SLA   3,2            CHANGE COUNT TO BYTES
         LA    7,0(3,2)       SET TO LAST LOC IN BUFFER
         LA    4,NOS+NOE     CALCULATE THE NUMBER
         SR    3,4              OF BYTES
         LR    5,3               IN THE
         SR    4,4                FIRST BUFFER
         LA    3,NOB+1(0,0)        AS FOLLOWS
         DR    4,3            K=(COUNT-NOE-NOS)/(NOB+1)
         ST    2,BUFA         BUFA(1)=BUF
         AR    2,5
         ST    2,BUFLK        BUFLK=BUF+K
         LA    15,0(5,5)     TWICE DATA SIZE
         LA    15,NOS+NOE(5,15)  MAX. BUFFER SIZE ON WRITE
         STH   15,PTDCB+62   STASH IN DCB BLKSIZE
         STH   15,PTDCB+82   AND LRECL
         MVC   0(NOS,2),SYCD  MOVE SYNC CODES INTO BUF+K
         BCTR  2,0
         ST    2,BUFA+8       BUFA(3)=BUF+K-1
         LA    2,NOS+1(0,2)
         ST    2,BUFFL        BUFFL=BUF+K+NOS
         LA    6,1            SET CONSTANT TO 1
         BCTR  7,0
         IC    4,FCDE         LOAD FILLER CODE
         STC   4,0(0,2)       STORE FILLER CODES INTO BUFFER
         BXLE  2,6,*-4        LOOP TO FILLER BUFFER
**
         OPEN  (PTDCB,(OUTPUT,DISP))
         BAL   10,WBLK        WRITE BLOCK ADDRESS 1
         LM    1,3,BUFA       LOAD BUF INFORMATION
**
         BC    15,EXIT        EXIT
**
**
PNDNCT   DC    H'27'          NO. OF PEN DOWN CODES
PNUPCT   DC    H'05'          NO. OF PEN UP CODES
BKCNT    DC    H'132'         LENGTH OF BLOCK ADDRESS RECORD
BLKMAX   DC    0H'0',AL2((((32760-NOS-NOE)*(NOB+1)/NOB)+NOS+NOE)/4)
NOS      EQU   255            NO. OF SYNC. CODES
NOE      EQU   4              NO. OF END PLOT CODES
NOB      EQU   3              BUFFER SPLIT FACTOR
BKTAB    DC    X'20202021'    BLOCK ADDRESS CODES- 0  1
         DC    X'20222023'                         2  3
         DC    X'21202121'                         4  5
         DC    X'21222123'                         6  7
         DC    X'22202221'                         8  9
BKCD     DC   8X'610404'      BLOCK ADDRESS RECORD
         DC   7X'730404'
         DC    X'700404'
BKNO     DC  12X'200404'
         DC    X'700404'
         DC   7X'730404'
SYCD     DC  77X'610404'      SYNC. CODES FOR DATA RECORD
         DC   7X'730404'
         DC    X'710404'
ENDPL    DC    X'73040461'    END PLOT CODES
PNUPCD   DC    X'61'          PEN UP CODE
PNDNCD   DC    X'62'          PEN DOWN CODE
INCD     DC    X'72'          ENTER INCREMENTAL CODE
FCDE     DC    X'04'          FILLER CODE
HALVE    DC    F'128'         CODE TO CHANGE FULL TO HALF
* CODE TABLES FOR VALUES OF J=    *FOR J=0 THE
*                0 1 2 3 4 5 6 7  *CODES ARE
HAFCD    DC    X'E0E1F0F1E2E3F2F3'   +X+Y/2
MAJCD    DC    X'2232223220203030'   +X
CMBCD    DC    X'2133233121332331'   +X+Y
STPSZ    DC    E'400.0'  PLOTTER STEP SIZE
PTDCB    DCB   DDNAME=PLOTTAPE,DSORG=PS,DEVD=TA,MACRF=W,RECFM=U,       *
               DEN=2,NCP=3,BUFNO=1
         END
