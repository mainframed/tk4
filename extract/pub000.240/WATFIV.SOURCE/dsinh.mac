 TITLE ' SCNH HYPERBOLIC SIN-COS (LONG) '
         PRINT     NOGEN
         DISK
         PRINT     GEN
         SPACE 2
DSINH#   CSECT
*        HYPERBOLIC SINE-COSINE FUNCTION (LONG)
*              SINH(X) = (E**X-E**(-1))/2
*              COSH(X) = (E**X+E**(-1))/2
*              SINH(X) FOR SMALL X IS COMPUTED DIRECTLY BY POLYNOMIAL
*              FOR OTHER CASES, ELABORATE USE OF $EXP IS MADE
       SPACE
       EXTRN   DEXP
       ENTRY   DSINH
       ENTRY   DCOSH
         ENTRY     SINH
         ENTRY     COSH
       SPACE
GR2    EQU     2               SCRATCH
GRA    EQU     1               ARGUMENT POINTER
GR1    EQU     1
GRB    EQU     2               MAIN BASE REGISTER
GRS    EQU     13              SAVE AREA POINTER
GRR    EQU     14              RETURN REGISTER
GRL    EQU     15              LINK REGISTER
FR0    EQU     0               ANSWER REGISTER
FR2    EQU     2               SCRATCH REGISTERS
FR4    EQU     4
ISN    EQU     X'104'          IDENTIFIER NUMBER FOR DEXP CALL
       SPACE
SINH     CALLDUBL  DSINH,2
         SPACE     1
COSH     CALLDUBL  DCOSH,2
         SPACE     1
DCOSH    FENT      (XDSH,REAL8),1  COSH ENTRY
         SPACE     1
       MVI     FLAG,X'30'        SET INSTRUCTION 'FLAG' TO 'LPER'
         LA        R11,DSINH-DCOSH(R11)
         USING     DSINH+8,R11
         B         JOIN            JOIN WITH COMMON CIRCUIT
       SPACE
DSINH    FENT      (XDSH,REAL8),1      SINH ENTRY
       MVI     FLAG,X'31'        SET INSTRUCTION 'FLAG' TO 'LNER'
       SPACE
JOIN   EQU     *
         LD        FR4,XDSH         LOAD X
       LPDR    FR0,FR4           /X/ TO FR0
       TM      FLAG,X'01'
       BC      8,EXPN1         IF COSH ENTRY, SKIP
       CE      FR0,LIMIT
       BC      10,EXPN2        IF SINH, AND /X/ GE 0.88137, SKIP
       SPACE
       CE      FR0,UNFLO       IF SINH, AND /X/ LE 0.1626E-9, AVOID
       BC      4,SIGN            INTERMEDIATE UNDERFLOW, ANS = X
       MDR     FR0,FR0
       LDR     FR2,FR0
       MD      FR0,C6          FOR SINH OF MODEST ARGUMENT, USE
       AD      FR0,C5
       MDR     FR0,FR2           SINH(X) = X+X*XSQ*F(XSQ)
       AD      FR0,C4
       MDR     FR0,FR2             WHERE F(XSQ) IS A POLYNOM&AL
       AD      FR0,C3
       MDR     FR0,FR2               OF DEGREE 5 IN XSQ
       AD      FR0,C2
       MDR     FR0,FR2           USE OF EXPONENTIAL FOR THESE
       AD      FR0,C1              ARGUMENTS WOULD RESULT IN A
       MDR     FR0,FR2               LOSS OF ACCURACY
       MDR     FR0,FR4
       ADR     FR0,FR4
         B         XFRET
       SPACE
EXPN1  LPER    FR4,FR4         COSH(X) IS ALWAYS POSITIVE
EXPN2    CE        FR0,MAX
       BC      2,ERROR         IF /X/ TOO LARGE, GIVE ERROR
       AD      FR0,LNV
       STD     FR0,XABS        COMPUTATION BY EXPONENTIAL
         FCALL     (DEXP,REAL8),(XABS,REAL8)
       LD      FR2,VSQ         COMPUTE V**2/EXP(/X/+LOG(V))
       DDR     FR2,FR0
FLAG   LNER    FR2,FR2         'LPER' FOR COSH, 'LNER' FOR SINH
       STD     FR0,XABS        SPECIAL MANEUVER TO MINIMIZE ROUNDING
       ADR     FR0,FR2           ERROR IN EFFECTIVELY EVALUATING
       MD      FR0,DELTA           (E**X + OR - E**-X)/2
       ADR     FR0,FR2         HERE DELTA IS SUCH THAT 1+DELTA=1/2V,
       AD      FR0,XABS          V IS CHOSEN SLIGHTLY LESS THAN 0.5.
       SPACE
SIGN   LTER    FR4,FR4
       BC      10,*+6          IF X IS NEGATIVE, SINH(X) = -SINH(/X/)
       LNER    FR0,FR0
FIN      B         XFRET
         SPACE     1
       SPACE
*  LI-1   ×ARGUMENT×>175.366
ERROR    $ERROR    (TRAZ,LI,1)                                     V1L5
       SPACE
         DS    0D
XABS   DS      D
C6     DC      X'38B2D4C184418A97'       0.1626459177981471(-9)
C5     DC      X'3A6B96B8975A1636'       0.2504995887597646(-7)
C4     DC      X'3C2E3BC881345D91'       0.2755733025610683(-5)
C3     DC      X'3DD00D00CB06A6F5'       0.1984126981270711(-3)
C2     DC      X'3F2222222222BACE'       0.8333333333367232(-2)
C1     DC      X'402AAAAAAAAAAA4D'       0.1666666666666653 +2F
VSQ    DC      X'403FDF9434F03D26'       0.2495052937740537 = V**2
LNV    DC      X'C0B1B30000000000'      -0.6941375732421875 = LOG(V)
DELTA  DC      X'3E40F0434B741C6D'       0.0009908832830238=1/2V-1 +F
MAX    DC      X'42AF5DC0'               175.366
LIMIT  DC      X'40E1A1B8'               0.881374
UNFLO  EQU     C6
XDSH     DS        D
DATA     EQU   XABS
       END
