SYMB     TITLE 'SYMBOL  ***  CALCOMP SYMBOLIC ANNOTATION ROUTINE'
SYMBOL   START 0         CALL SYMBOL(X,Y,HGT,BCD,TH,N)       V018
         EXTRN SIN       COPYRIGHT 1967 CALIFORNIA COMPUTER PRODUCTS
         EXTRN COS
         EXTRN PLOT                                  * REGISTER USAGE
         USING *,15
         B     #ID           SKIP AROUND ID
         DC    AL1(6),CL7'SYMBOL'    DECK I.D.
#ID      DS    0H            BRANCH POINT
         DROP  15
         USING SYMBOL,8
         STM   14,12,12(13)   SAVE REGISTERS AND     * R1 - SCRATCH
         LR    8,15           SET UP BASE REGISTER   * R2 - SCRATCH
         ST    13,SAVE+4      IN REGISTER 8          * R3 - SCRATCH
         LA    7,SAVE                                * R4 - LOC OF OFF
         ST    7,8(0,13)                             * R5 - LOC OF BCD
         LR    13,7                                  * R6 - NO OF OFF
         LA    9,LINK         LOAD LINK FOR PLOT     * R7 - NO OF CHAR
         L     10,ADPLOT      LOAD ADDRESS OF PLOT   * R8 BASE
         LA    0,1            SET CONSTANT 1         * F0 - XC
         LM    2,7,0(1)       LOAD LINKAGE           * F2 - YC
*        LTR   7,7            TEST FOR CORRECT       * F4 - SCRATCH
*        BC    11,EREXIT       LINKAGE               * F6 - SCRATCH
         MVI   IC+3,3         IC=3
         LE    6,SEVEN        DIV=7
         L     7,0(0,7)       PICK UP N
         LTR   7,7            IS N GREATER 0
         BC    13,NNTPOS       YES                        NO
         TM    0(5),X'FF'     TEST FOR DOPE VECTOR        .
         BC    7,NPOS          YES                   NO   .
         L     5,0(0,5)       LOAD ADD FROM VECTOR   .    .
         BC    15,NPOS         BRANCH  TO NPOS  .  . .    .
NNTPOS   LA    5,3(0,5)       BCD=BCD+3           . ... . .
         AR    7,0             IS N LESS -1          .
         BC    11,NMTWO         YES      NO          .
         MVI   IC+3,2           IC=2     .           .
NMTWO    LR    7,0             N=1   . . .           .
         SR    1,1                                   .
         IC    1,0(0,5)        K=CONTENTS OF BCD     .
         CH    1,THRTN         IS K GREATER 13       .
         BC    2,NPOS           NO          YES      .
         LE    6,FOUR           DIV=4        .       .
NPOS     LE    2,0(0,4)       PICK UP HGT  . . . . . .
         LTER  2,2            IS HGT ZERO OR LESS
         BC    13,HNEG         NO                         YES
         MVI   STATE,1         STATE=1                     .
         DER   2,6             FCT=HGT/DIV                 .
         LE    0,0(0,6)        PICK UP ANGLE               .
         CE    0,THETA         IS ANGLE=THETA              .
         BC    7,STRTH          YES                  NO    .
         CE    2,FACT           IS FCT=FACT           .    .
         BC    8,HNEG            NO                   .   YES
         STE   2,FACT            FACT=FCT             .    .
         BC    15,GTCALC      GO TO CALCULATE OFFSETS .    .
STRTH    STE   2,FACT         FACT=FCT            . . .    .
         STE   0,THETA        THETA=ANGLE                  .
         ME    0,RADCO        CHANGE TO RADIANS            .
         STE   0,INCC                                      .
         LA    1,ADANG                                     .
         L     15,ADSIN                                    .
         BALR  14,15          CALL SIN                     .
         STE   0,INCS         INCS=SIN(THETA)              .
         LA    1,ADANG                                     .
         L     15,ADCOS                                    .
         BALR  14,15          CALL COS                     .
         STE   0,INCC         INCC=COS(THETA)              .
GTCALC   STM   2,3,SVXYL                                   .
         BAL   4,CALC         CALCULATE OFFSETS            .
         LM    2,3,SVXYL                                   .
HNEG     LE    0,0(0,2)       PICK UP X                . . .
         CE    0,NNN          IS X EQUAL TO 999.0
         BC    08,XNN          NO               YES
*        CE    0,MZRO         IS X=-0.0          .
*        BC    8,XNN           NO               YES
         SE    0,XA+8          X=X-XA(2)+YA(2)   .
         AE    0,YA+8                            .
         STE   0,XO            XO=X              .
         STE   0,XC            XC=X              .
XNN      LE    2,0(0,3)       PICK UP Y      . . .
         CE    2,NNN          IS Y EQUAL TO 999.0
         BC    08,YNN          NO                   YES
*        CE    2,MZRO         IS Y=-0.0              .
*        BC    8,YNN           NO                   YES
         SE    2,XA+8          Y=Y-XA(2)-YA(2)       .
         SE    2,YA+8                                .
         STE   2,YO            YO=Y                  .
         STE   2,YC            YC=Y                  .
YNN      LE    0,XC           X=XC               . . .
         LE    2,YC           Y=YC
NXTCH    IC    3,0(0,5)     * K=CONTENTS OF BCD
         N     3,CHMSK        K=K MODULO 128
         SLL   3,2            SET UP K FOR INDEX
         L     4,TABLE(3)     SET LOC TO ADDRESS OF OFFSETS
         N     4,THBYT          FOR CHARACTER K
         SR    6,6            SET J  TO NUMBER OF OFFSETS
         IC    6,TABLE+1(3)     FOR CHARACTER K
NXTOF    SR    2,2          *
         IC    2,0(4,8)       PICK UP OFFSETS FROM LOC
         SRDL  2,4            SET I TO X-OFFSET
         CH    2,THRTN        IS I GREATER 13
         BC    10,SPCDE        NO
         SLA   2,2            SET UP I FOR INDEX
         AE    0,XA(2)        X=X+XA(I)
         AE    2,YA(2)        Y=Y+YA(I)
         SR    2,2
         SLDL  2,4            SET I TO Y-OFFSET
         SLA   2,2            SET UP I FOR INDEX
         SE    0,YA(2)        X=X-YA(I)
         AE    2,XA(2)        Y=Y-XA(I)
         STE   0,XT           XT=X
         STE   2,YT           YT=Y
         LR    1,9
         LR    15,10
         BALR  14,15          CALL PLOT(XT,YT,IC)
         MVI   IC+3,2         IC=2
         LE    0,XC           X=XC
         LE    2,YC           Y=YC
GTNOF    AR    4,0          * INCREASE LOC TO NEXT OFFSET PAIR
         BCT   6,NXTOF        J=J-1, IF J IS NOT ZERO REPEAT NXTOF
         AE    0,XA+28        X=X+XA(7)
         AE    2,YA+28        Y=Y+YA(7)
GTNCH    MVI   IC+3,3       * IC=3
         STE   0,XC           XC=X
         STE   2,YC           YC=Y
         AR    5,0            INCREASE BCD TO NEXT CHARACTER
         BCT   7,NXTCH        N=N-1,IF N IS NOT ZERO REPEAT NXTCH
EREXIT   L     13,SAVE+4
         LM    14,10,12(13)   RESTORE REGISTERS
         MVI   12(13),X'FF'   SET COMPLETION CODE
         BCR   15,14          EXIT
SPCDE    SR    2,2         ** DECODE Y-OFFSET
         SLDA  2,4            SET I TO Y OFFSET
         SR    2,0
         BC    4,PNUP
         BC    8,SUPSC        IF I=1, GO TO SUPERSCRIPT CODE
         SR    2,0
         BC    8,SUBSC        IF I=2, GO TO SUBSCRIPT CODE
         SR    2,0
         BC    8,CRET         IF I=3, GO TO CARRIAGE RETURN CODE
         SR    2,0
         BC    7,GTNCH        IF I=5, SKIP TO NEXT CHARACTER
BCKSP    SE    0,XA+28     ** X=X-XA(7)
         SE    2,YA+28        Y=Y-YA(7)
         BC    15,GTNCH       BRANCH TO GET NEXT CHARACTER
CRET     LE    0,XO        **
         LE    2,YO
         AE    0,YA+48        X
         SE    2,XA+48
         STE   0,XO           XO=X
         STE   2,YO           YO=Y
         BC    15,GTNCH       BRANCH TO GET NEXT CHARACTER
SUBSC    CLI   STATE,1     ** TEST STATE
         BC    4,GTNCH         IF 0, BRANCH TO GET NEXT CHARATER
         BC    8,SUBER         IF 1, BRANCH TO SUBER
         MVI   STATE,1         IF 2, RETURN TO NORMAL MODE,STATE=1
         LE    0,FACT
         DE    0,FCTR          FACT=FACT/FCTR
         STE   0,FACT
         BAL   4,CALCA        CALCULATE OFFSETS
         AE    0,YA+16        X=X+YA(4)
         SE    2,XA+16        Y=Y-XA(4)
         BC    15,GTNCH       BRANCH TO GET NEXT CHARACTER
SUBER    MVI   STATE,0      * CHANGE TO SUBSCRIPT MODE,STATE=0
         AE    0,YA+8         X=X+YA(2)
         SE    2,XA+8         Y=Y-XA(2)
         BC    15,SUA         BRANCH                   . . . . . . .
SUPSC    CLI   STATE,1     ** TEST STATE                           .
         BC    2,GTNCH         IF 2, BRANCH TO GET NEXT CHARACTER  .
         BC    8,SUPER         IF 1, BRANCH TO SUPER               .
         MVI   STATE,1         IF 0, RETURN TO NORMAL MODE,STATE=1 .
         LE    0,FACT                                              .
         DE    0,FCTR         FACT=FACT/FCTR                       .
         STE   0,FACT                                              .
         BAL   4,CALCA        CALCULATE OFFSETS                    .
         SE    0,YA+8         X=X-YA(2)                            .
         AE    2,XA+8         Y=Y+XA(2)                            .
         BC    15,GTNCH       BRANCH TO GET NEXT CHARACTER         .
SUPER    MVI   STATE,2      * CHANGE TO SUPERSCRIPT MODE,STATE=2   .
         SE    0,YA+16        X=X-YA(4)                            .
         AE    2,XA+16        Y=Y+XA(4)                            .
SUA      STE   0,XC           XC=X                     . . . . . . .
         STE   2,YC           YC=Y
         LE    0,FACT
         ME    0,FCTR         FACT=FACT*FCTR
         STE   0,FACT
         BAL   4,CALCA        CALCULATE OFFSETS
         BC    15,GTNCH       BRANCH TO GET NEXT CHARACTER
PNUP     MVI   IC+3,3      ** RAISE PEN, IC=3
         BC    15,GTNOF       BRANCH TO GET NEXT OFFSET
CALC     LE    0,FACT      ** CALCULATE OFFSETS
CALCA    LER   2,0
         ME    0,INCC         X=FACT*INCC
         ME    2,INCS         Y=FACT*INCS
         LER   4,0            XI=X
         LER   6,2            YI=Y
         LM    1,3,MFOR       I=1
CALCB    STE   0,XA(1)        XA(I)=X                    . . . .
         STE   2,YA(1)        YA(I)=Y                          .
         AER   0,4            X=X+XI                           .
         AER   2,6            Y=Y+YI                           .
         BXLE  1,2,CALCB      I=I+1 AND REPEAT UNTIL I IS 12 . .
         LE    0,XC           X=XC
         LE    2,YC           Y=YC
         BCR   15,4           RETURN
THRTN    DC    H'13'       ** CONSTANTS AND VARIABLES
SAVE     DS    9D
SVXYL    DS    2F
ADPLOT   DC    A(PLOT)
ADSIN    DC    A(SIN)
ADCOS    DC    A(COS)
ADANG    DC    A(INCC)
LINK     DC    A(XT)
         DC    A(YT)
         DC    X'80'
         DC    AL3(IC)
CHMSK    DC    F'127'
THBYT    DC    X'00000FFF'
MFOR     DC    F'4'
         DC    F'4'
         DC    F'48'
SEVEN    DC    E'7.0'
FOUR     DC    E'4.0'
RADCO    DC    E'0.0174533'
NNN      DC    E'999.0'
MZRO     DC    X'80000000'
FCTR     DC    E'0.7'
FACT     DC    E'0.0'
THETA    DC    E'0.0'
INCC     DC    E'1.0'
INCS     DC    E'0.0'
XA       DC    13F'0'
YA       DC    13F'0'
XO       DC    F'0'
YO       DC    F'0'
XC       DC    F'0'
YC       DC    F'0'
XT       DC    F'0'
YT       DC    F'0'
IC       DC    F'0'
TABLE    MVI   S0,08       N=  0 HEX=00          CENTER SQUARE
         MVI   S1,12       N=  1 HEX=01          CENTER OCTAGON
         MVI   S2,06       N=  2 HEX=02          CENTER TRIANGLE
         MVI   S3,07       N=  3 HEX=03          CENTER PLUS
         MVI   S4,07       N=  4 HEX=04          CENTER X
         MVI   S5,07       N=  5 HEX=05          CENTER DIAMOND
         MVI   S6,07       N=  6 HEX=06          CENTER UP ARROW
         MVI   S7,06       N=  7 HEX=07          CENTER BAR X
         MVI   S8,08       N=  8 HEX=08          CENTER Z
         MVI   S9,07       N=  9 HEX=09          CENTER Y
         MVI   SA,14       N= 10 HEX=0A          CENTER SQUARE X
         MVI   S3,13       N= 11 HEX=0B          CENTER ASTERISK
         MVI   SC,06       N= 12 HEX=0C          CENTER DOUBLE BAR X
         MVI   SD,04       N= 13 HEX=0D          CENTER VERTICAL
         MVI   SE,06       N= 14 HEX=0E          STAR
         MVI   SF,02       N= 15 HEX=0F          HORIZ VECTOR
         MVI   SG,02       N= 16 HEX=10          VERTICAL VECTOR
         MVI   U4,01       N= 17 HEX=11          ** BACKSPACE
         MVI   SL,03       N= 18 HEX=12          CARAT
         MVI   SJ,08       N= 19 HEX=13          EQUIVELENCE
         MVI   SK,05       N= 20 HEX=14          RIGHT ARROW
         MVI   U3,01       N= 21 HEX=15          ** CARRIAGE RETURN
         MVI   SM,08       N= 22 HEX=16          NOT EQUAL
         MVI   SN,08       N= 23 HEX=17          PLUS MINUS
         MVI   SO,02       N= 24 HEX=18          UNDERSCORE
         MVI   U5,01       N= 25 HEX=19          ** NULL CHARACTER
         MVI   TL,02       N= 26 HEX=1A          OVERSCORE
         MVI   NH,08       N= 27 HEX=1B          INTEGRAL
         MVI   NJ,06       N= 28 HEX=1C          IMPLIES
         MVI   NK,03       N= 29 HEX=1D          OR
         MVI   NL,06       N= 30 HEX=1E
         MVI   NL,13       N= 31 HEX=1F
         MVI   NM,09       N= 32 HEX=20          RIGHT BRACKET
         MVI   NN,09       N= 33 HEX=21          LEFT  BRACKET
         MVI   NP,09       N= 34 HEX=22          MU
         MVI   NQ,06       N= 35 HEX=23          PI
         MVI   NR,12       N= 36 HEX=24          PHI
         MVI   NS,12       N= 37 HEX=25          THETA
         MVI   NT,10       N= 38 HEX=26          PSI
         MVI   NU,07       N= 39 HEX=27          CHI
         MVI   NV,09       N= 40 HEX=28          OMEGA
         MVI   NW,07       N= 41 HEX=29          LAMBDA
         MVI   NO,14       N= 42 HEX=2A          ALPHA
         MVI   NX,12       N= 43 HEX=2B          DELTA
         MVI   NY,09       N= 44 HEX=2C          EPSILON
         MVI   NZ,08       N= 45 HEX=2D          ETA
         MVI   U1,01       N= 46 HEX=2E          ** SUPERSCRIPT
         MVI   U2,01       N= 47 HEX=2F          ** SUBSCRIPT
         MVI   SV,05       N= 48 HEX=30          SUMATION
         MVI   SW,14       N= 49 HEX=31          DIVIDE
         MVI   SB,06       N= 50 HEX=32          LESS THAN OR EQUAL
         MVI   SX,06       N= 51 HEX=33          GREATER  OR EQUAL
         MVI   SZ,04       N= 52 HEX=34          DELTA
         MVI   T0,04       N= 53 HEX=35          LEFT  BRACE
         MVI   SF,04       N= 54 HEX=36          RIGHT BRACE
         MVI   T3,02       N= 55 HEX=37          REVERSE SLASH
         MVI   T5,07       N= 56 HEX=38          GAMMA
         MVI   T6,05       N= 57 HEX=39          SQUARE ROOT
         MVI   T7,10       N= 58 HEX=3A
         MVI   T7,13       N= 59 HEX=3B
         MVI   T8,05       N= 60 HEX=3C          LEFT ARROW
         MVI   NI,05       N= 61 HEX=3D          TIMES
         MVI   SI,05       N= 62 HEX=3E          UP ARROW
         MVI   SH,05       N= 63 HEX=3F          DN ARROW
         MVI   U0,01       N= 64 HEX=40          BLANK
         MVI   TA,09       N= 65 HEX=41          A
         MVI   TB,12       N= 66 HEX=42          B
         MVI   TC,08       N= 67 HEX=43          C
         MVI   TD,07       N= 68 HEX=44          D
         MVI   T2,07       N= 69 HEX=45          E
         MVI   T2,06       N= 70 HEX=46          F
         MVI   TC,10       N= 71 HEX=47          G
         MVI   TH,06       N= 72 HEX=48          H
         MVI   TI,06       N= 73 HEX=49          I
         MVI   TE,11       N= 74 HEX=4A          CENT
         MVI   TG,05       N= 75 HEX=4B          PERIOD
         MVI   SB,03       N= 76 HEX=4C          LESS
         MVI   T9,04       N= 77 HEX=4D          LEFT PAREN
         MVI   SS,05       N= 78 HEX=4E          PLUS
         MVI   SQ,02       N= 79 HEX=4F          VERTICAL
         MVI   SR,10       N= 80 HEX=50          AMBERSEND
         MVI   TJ,05       N= 81 HEX=51          J
         MVI   TK,07       N= 82 HEX=52          K
         MVI   T1,03       N= 83 HEX=53          L
         MVI   TM,05       N= 84 HEX=54          M
         MVI   TN,04       N= 85 HEX=55          N
         MVI   TO,12       N= 86 HEX=56          O
         MVI   TP,07       N= 87 HEX=57          P
         MVI   TQ,12       N= 88 HEX=58          Q
         MVI   TP,10       N= 89 HEX=59          R
         MVI   ST,07       N= 90 HEX=5A          EXCLAMATION
         MVI   TR,12       N= 91 HEX=5B          DOLLAR SIGN
         MVI   TX,08       N= 92 HEX=5C          ASTERISK
         MVI   NA,04       N= 93 HEX=5D          RIGHT PARENTHESIS
         MVI   TF,12       N= 94 HEX=5E          SEMI COLON
         MVI   NB,03       N= 95 HEX=5F          NOT
         MVI   TX,02       N= 96 HEX=60          MINUS
         MVI   T4,02       N= 97 HEX=61          SLASH
         MVI   TS,12       N= 98 HEX=62          S
         MVI   TT,04       N= 99 HEX=63          T
         MVI   TU,06       N=100 HEX=64          U
         MVI   TV,03       N=101 HEX=65          V
         MVI   TW,05       N=102 HEX=66          W
         MVI   T3,05       N=103 HEX=67          X
         MVI   TY,05       N=104 HEX=68          Y
         MVI   TZ,07       N=105 HEX=69          Z
         MVI   NO,17       N=106 HEX=6A          INFINITY
         MVI   TG,06       N=107 HEX=6B          COMMA
         MVI   NC,14       N=108 HEX=6C          PER CENT
         MVI   ND,02       N=109 HEX=6D          DASH
         MVI   SY,03       N=110 HEX=6E          GREATER THAN
         MVI   SU,13       N=111 HEX=6F          QUESTION MARK
         MVI   TQ,09       N=112 HEX=70          0
         MVI   N1,05       N=113 HEX=71          1
         MVI   N2,08       N=114 HEX=72          2
         MVI   N3,13       N=115 HEX=73          3
         MVI   N4,08       N=116 HEX=74          4
         MVI   N5,09       N=117 HEX=75          5
         MVI   N6,11       N=118 HEX=76          6
         MVI   N7,05       N=119 HEX=77          7
         MVI   N8,16       N=120 HEX=78          8
         MVI   N9,12       N=121 HEX=79          9
         MVI   TF,11       N=122 HEX=7A          COLON
         MVI   NE,11       N=123 HEX=7B          POUND
         MVI   NF,16       N=124 HEX=7C          AT
         MVI   NG,04       N=125 HEX=7D          APOSTROPE
         MVI   SM,05       N=126 HEX=7E          EQUAL
         MVI   NG,09       N=127 HEX=7F          QUOTATIONS
S0       DC    X'22240400404424'
S1       DC    X'2224140301103041433424'
S2       DC    X'2224014124'
S3       DC    X'222420220242'
S4       DC    X'220440220044'
S5       DC    X'222402204224'
S6       DC    X'222024024224'
S7       DC    X'2200440440'
S8       DC    X'22440444004000'
S9       DC    X'220422442220'
SA       DC    X'22443313041311001131403133'
SC       DC    X'2244044000'
SD       DC    X'222420'
SG       DC    X'2226'
SJ       DC    X'2767F06525F0'
SN       DC    X'2363F0'
SS       DC    X'4448462666'
SM       DC    X'2464F06626F03357'
SB       DC    X'682664F0'
SX       DC    X'6323F0'
SY       DC    X'246628'
T0       DC    X'69'
T1       DC    X'29'
SF       DC    X'2262'
T2       DC    X'69292656262262'
T3       DC    X'2962F0'
T4       DC    X'2269'
TA       DC    X'222565252839596862'
TB       DC    X'636556265667'
TD       DC    X'68592922526368'
TC       DC    X'68593928233252636555'
TH       DC    X'222926666962'
TI       DC    X'325242493959'
TE       DC    X'6657372624335364F04248'
TF       DC    X'3536464535F0'
TG       DC    X'423233434231'
T9       DC    X'69585362'
SQ       DC    X'212A'
SR       DC    X'62373849582524334364'
TU       DC    X'29'
TJ       DC    X'2332526369'
TK       DC    X'29222569F04762'
TM       DC    X'222945'
TN       DC    X'6962'
TW       DC    X'2922466269'
TO       DC    X'4769F0'
TQ       DC    X'685939282332526368F04462'
TP       DC    X'22295968675626566562'
ST       DC    X'4944F0'
SU       DC    X'32435232F04446566768593928'
TT       DC    X'2969'
TR       DC    X'4942F0235364655636273868'
TX       DC    X'2565456327452367'
NA       DC    X'29383322'
NB       DC    X'257574'
N8       DC    X'5667'
TS       DC    X'6859392827365665635232232536'
TV       DC    X'294269'
TY       DC    X'2947424769'
TZ       DC    X'29692262F03656'
NC       DC    X'3828293938F06922F05363625253'
ND       DC    X'1575'
N1       DC    X'3849423252'
N2       DC    X'2839596866242262'
N3       DC    X'283959686756'
N6       DC    X'3656656352322328395968'
N4       DC    X'2924645459524262'
N5       DC    X'23325263655626'
N7       DC    X'2969684342'
N9       DC    X'233252636859392826355566'
NE       DC    X'2464545357566626363733'
NF       DC    X'66574736354454656758382724335364'
NG       DC    X'57495957F037293937'
NO       DC    X'6757463727161524'
         DC    X'344546455464757667'
SE       DC    X'325872268632'
T6       DC    X'2334424A7A'
SH       DC    X'423454'
SI       DC    X'4249375749'
SK       DC    X'1575565475'
SL       DC    X'224662'
SO       DC    X'1181'
TL       DC    X'1A8A'
SW       DC    X'1565F03334444333F03637474636'
SV       DC    X'7121562A7A'
SZ       DC    X'12477212'
T5       DC    X'28394842485968'
T7       DC    X'42443454444838584849463656'
T8       DC    X'1536341575'
NH       DC    X'12213142495A6A79'
NI       DC    X'2367F02763'
NJ       DC    X'245465665727'
NK       DC    X'264266'
NL       DC    X'142535536374F0766555372716'
NM       DC    X'2131424556474A3B2B'
NN       DC    X'6151424536474A5B6B'
NP       DC    X'223734435364676473'
NQ       DC    X'333727675753'
NT       DC    X'26363443545666F0'
NR       DC    X'4248F0'
NS       DC    X'372624335364665737F01575'
NU       DC    X'2366F026365363'
U0       DC    X'F0'
U1       DC    X'F1'       ENTER SUPERSCRIPT MODE OR LEAVE SUBSCRIPT.
U2       DC    X'F2'       ENTER SUBSCRIPT MODE OR LEAVE SUPERSCRIPT.
U3       DC    X'F3'       CARRIAGE RETURN.
U4       DC    X'F4'       BACKSPACE CHARACTER.
U5       DC    X'F5'       NULL CHARACTER
NV       DC    X'272433444744536467'
NW       DC    X'28374563F02245'
NX       DC    X'584837465665645343343546'
NY       DC    X'674736344363F06525'
NZ       DC    X'2637463446576652'
STATE    DS    C
         END
