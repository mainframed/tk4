*        MACRO                                                     V1L2
*        IHCFIOSM                                                  V1L2
         GBLA  &ERR
&ERR     SETA      0                                               V1L2
         AIF   (&ERR EQ 0).FINO
IOCS     TITLE 'IHCEFIOS - OPERATING SYSTEM/360 - FORTRAN INPUT/OUTPUT'
IHCEFIOS CSECT
         AGO   .FI001
.FINO    ANOP
IOCS     TITLE 'IHCFIOSH - OPERATING SYSTEM/360 - FORTRAN INPUT/OUTPUT'
IHCFI#SH CSECT                                                     V1L2
.FI001   ANOP
*
*
*
*A328030,328060,331400,331600,332000,640300                      MAINT
*D331800,639600                                                  MAINT
*C327000,639300                                                  MAINT
*D403400                                                         A37386
*D698400                                                         A37386
*A697900-698400                                                  A37386
*A113100,403200,404000,405300                                    A38926
*D403400                                                         A38926
*A860558-860566,860692-860700                                    A42666
*C734500                                                         A42696
*C685710                                                         A43159
*A697810-697860                                                  A47947
*
*
* STATUS - CHANGE LEVEL 05   21MAY72   RELEASE 21.6
*
*
*FUNCTION/OPERATION--ALL I/O READ AND WRITE AND MANIPULATIVE COMMANDS
*   OF A FORTRAN SOURCE PROGRAM RESULT IN ONE OR MORE ENTRIES INTO THIS
*   ROUTINE, AND IN TURN, FIOCS MAKES AN APPROPRIATE REQUEST OF THE
*   CONTROL PROGRAM.
*ENTRY POINTS--ONE ENTRY POINT WITH PARAMETERS AS FOLLOWS-
*
*    INITIALIZATION
*        LA    GRX,DSRN                POINTER TO DATA SET REF NO
*        L     L,=V(FIOCS#)
*        BALR  R,L
*        DC    AL1(0)
*        DC    AL1(FQUALS)
*              WHERE DSRN IS OF THE FORM XL1'FLAG',AL3(UNIT)
*              AND FQUALS = X'F0' FOR FORMATTED INPUT
*                           X'FF' FOR FORMATTED OUTPUT
*                           X'00' FOR NON-FORMATTED INPUT
*                           X'0F' FOR NON-FORMATTED OUTPUT
*
*    READ OPERATION
*        L     L,=V(FIOCS#)
*        BALR  R,L
*        DC    AL1(1)
*        DC    AL1(0)
*
*    WRITE OPERATION
*        L     GRX,RECLEN
*        L     L,=V(FIOCS#)
*        BALR  R,L
*        DC    AL1(2)
*        DC    AL1(0)
*              WHERE RECLEN = NUMBER OF BYTES OF OUTPUT
*
*
*    CONTROL OPERATION
*        LA    GRX,DSRN
*        L     L,=V(FIOCS#)
*        BALR  R,L
*        DC    AL1(3)
*        DC    AL1(CQUALS)
*              WHERE DSRN IS OF THE FORM XL1'FLAG',AL3(UNIT)
*              AND CQUALS = X'00' FOR BACKSPACE BLOCK
*                           X'01' FOR REWIND
*                           X'02' FOR WRITE END OF FILE MARK
*
*    CLOSE DATA SETS
*        L     L,=V(FIOCS#)
*        BALR  R,L
*        DC    AL1(4)
*        DC    AL1(0)
*
*INPUT--ALL INPUT PARAMETERS ARE FROM THE MODULE IHCFCOMH,
*   AND ARE CONTAINED IN THE REGISTERS 0, 2, OR 3.
*
*OUTPUT--POINTERS TO BUFFERS, AND BUFFER LENGTHS, ARE GIVEN TO EITHER
*   FCOMH, THE CALLING ROUTINE, OR TO THE CONTROL PROGRAM.
*
*EXTERNAL ROUTINES--
*        IHCFCOMH--ERROR MESSAGES, FROM ERRORS DETERMINED BY FIOCS,
*   ARE PASSED TO IHCFCOMH.
*
*EXITS--
*   NORMAL--TO IHCFCOMH VIA REGISTER 1
*   ERROR--(VIA REGISTER 15 WITH OFFSET)-
*   1.* IF DUE TO SYNAD, TO IHCFCOMH'S SYNAD ENTRY POINT.
*   2.* IF AT END OF DATA SET, TO IHCFCOMH'S EODAD ENTRY POINT.
*   3.* IF DUE TO AN ERROR,TO IHCFCOMH'S IBFERR ENTRY.
*   4.* IF FOR LACK OF A DD CARD TO PRINT ERROR MESSAGE, TO
*   IHCFCOMH'S IBEXIT ENTRY.
*
*TABLES/WORK AREAS--
*        IHCUATBL--(REFERENCED EXTERNALLY)-
*   UNIT ASSIGNMENT TABLE, CONSISTING OF -
*   WORD ONE- ABSOLUTE VALUE = (NO. OF DSRN'S X 4) + 4
*   WORD TWO- FOUR 1 BYTE INDEXES TO ASSIGNED DSRN'S
*   WORDS THREE AND FOLLOWING- 1 WORD PER DSRN, TO CONTAIN A POINTER TO
*   THE UNIT TABLE FOR THAT DSRN. AFTER THE LAST SUCH POINTER, 2 WORDS
*   PER DSRN CONTAINING DEFAULT VALUES.
*        UNIT TABLE--
*   ONE UNIT TABLE IS CONSTRUCTED OR IS IN CORE PER DSRN REFERENCED.
*   EACH SUCH TABLE CONSISTS OF THREE SECTIONS, USED AS FOLLOWS-
*   1.* HOUSEKEEPING SECTION - 5 WORDS
*   2.* DECB AREA - 5 WORDS
*   3.* DCB AREA - 88 BYTES
*
*ATTRIBUTES--THIS MODULE IS NOT REENTRANT AND ALSO NOT SERIALLY
*   REUSEABLE.
*
*NOTES--DEPENDENCIES
*   THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS
*   EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME. THE CODING HAS
*   BEEN ARRANGED SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS,
*   BY REASSEMBLY, WILL RESULT IN A CORRECT MODULE FOR THE NEW
*   DEFINITIONS.
*
*
         EJECT
*****
* REGISTER DEFINITIONS
R        EQU   0                       RETURN REG. USED IN CUR. IBCOM
L        EQU   1                       LINKAGE REG. USED IN CUR. IBCOM
GRX      EQU   2                       FIRST ARGUMENT
GRY      EQU   3                       SECOND ARGUMENT
RX       EQU   4                       UTILITY AND BRANCH
RD       EQU   5                       UTILITY
CSECT2R  EQU   5                       BASE REG FOR IHCFIOS2
UTR      EQU   6                       UNIT TABLE REGISTER
DCBR     EQU   UTR+1
DECBR    EQU   UTR+2
RI       EQU   9                       UTILITY REGISTER
RJ       EQU   10                      UTILITY AND BRANCH REGISTER
*
BASE2    EQU   11                      UAT ADDRESSABILITY REGISTER
BASE     EQU   12                      FIOCS ADDRESSABILITY REGISTER
SAVER    EQU   13
*
*                                      FOR CONCATENATION
CLSOPN   EQU   X'08'                   FORCES UNLIKE ATTRIBUTES
* IBCOM OFFSETS
IBFEROFS EQU   60
DSRNOF   EQU   60                      OFFSET IN IBCOM TO DSRN FIELD
THRTNUSR EQU   184                     OFFSET IN IBCOM(SAVE) TO REG 13
FRTNUSR  EQU   124                     OFFSET IN IBCOM(SAVE) TO REG 14
IBCSV    EQU   X'C4'                   OFFSET IN IBCOM TO ERR. SAVEAREA
BUFPTRS  EQU   X'114'                  ADDRESS IN IBCOM OF WHERE TO
*                                      STORE CURRENT BUFFER AND ITS LNG
ENDFILE  EQU   X'10C'              OFFSET IN IBCOM FOR EMD= PARAM
DISASTER EQU   16
SETIBXIT EQU   68
NUMEROF  EQU   2                       OFFSET IN TABLE ENTRY TO ERR CNT
         SPACE 3
*****
RDECB    EQU   X'80'
WDECB    EQU   X'20'
MINLEN   EQU   18                      MINIMUM RECORD LENGTH
VARUNIT  EQU   X'01'                   VARIABLE UNIT FLAG
XIOCLASS EQU   X'04'                   ERRMSG,READ,PRINT,PUNCH
VFORM    EQU   X'40'                   V FORMAT RECORDS
VSFORM   EQU   X'48'               VARIABLE SPANNED FORMAT        II232
SOFF     EQU   X'F7'               TURN SPANNING BIT OFF          II232
SFORM    EQU   X'08'                                              II232
VBSFORM  EQU   X'54'               VARIABLE BLOCKED SPANNED       II232
BLOCKG   EQU   X'10'                   BLOCKED RECORDS
RECMSK   EQU   X'C0'                   U FORMAT RECORDS
PRNTRCTL EQU   X'06'                   RCDS WITH PRINTER CTL CHARS 4993
END      EQU   X'10'
OPENBIT  EQU   X'10'                   OPEN BIT IN DCBOFLGS        4595
FCNTL    EQU   X'03'                   RWD,BSP,EOF
PRIMOP   EQU   0
SECOP    EQU   1
*
ONES     EQU   1
ON       EQU   1                                                  II232
EQUAL    EQU   8
ZERO     EQU   8
NOTPLUS  EQU   12                      MINUS OR ZERO
         EJECT
*****
*              ABYTE BITS
FORMAT   EQU   X'F0'                   FORMAT QUALIFIER
OUTPUT   EQU   X'0F'                   OUTPUT QUALIFIER
         SPACE 3
*****
*              BBYTE BITS
ETEST    EQU   X'80'                  'ERR=' PARAM ON READ
EYES     EQU   X'40'                  SYNAD TAKEN ON CHK
BSW      EQU   X'20'                   BUF SW, IBCOM BUF IS 2
ERR      EQU   X'10'                   ERROR OCCURRED.BIT NOT     10595
*                                        TURNED OFF TILL NXT CHECK10595
EOBSW    EQU   X'08'                  IBCOM BUF EXHAUSTED
BLSW     EQU   X'04'                  BLOCKED RECORDS
VSWITCH  EQU   X'02'                  VAR. RECS. USE BLK'G ROUTINES
*                                      MASKS TO SET OR TEST ABOVE
VEOBSW   EQU   X'0A'
BLEOBSET EQU   X'0C'
VBLSW    EQU   X'06'
ETYES    EQU   X'C0'
EOFF     EQU   X'3F'
BINIT    EQU   X'08'
DUMMY    EQU   X'01'              BIT IN CBYTE INDICATING DUMMY DS 7434
EOBSWOFF EQU X'F7'                     SET END OF BUFFER SWITCH  A37386
         SPACE 3
*****
*              CBYTE BITS
OPENSW   EQU   X'80'                  DCB IS OPEN,CLOSED IF PREV RWD
NTCLOSE  EQU   X'40'                  EOF DOES TCLOASE,CAN BSP OVER
PROPEN   EQU   X'20'                  PREV. OPEN
POOLSW   EQU   X'10'                  BUFFER POOL ASSIGNED
RWNDFLAG EQU   X'08'                  NOT RWD PREV.
CONCAT   EQU   X'02'
*                                     TEST OR SET ABOVE SWS.
NORMFLAG EQU   X'C4'                  OPEN,NOT TCLOSE,NOT BSP
CLOSRWND EQU   X'37'
CLSET    EQU   X'BF'
INITSET  EQU   X'FC'
POOLOFF  EQU   X'EF'
DASD     EQU   X'01'                    TEST FOR DASD I/O            **
FF       EQU   X'FF'                                              10595
         EJECT
*****
         ENTRY FIOCS#
         AIF   (&ERR EQ 0).FI201
         ENTRY FIOCSBEP
.FI201   ANOP
FIOCS#   EQU   *
FIOCSA   BCR   1,0                 'PIPELINE DRAIN' FOR MOD 91     ****
         USING FIOCSA,L
         AIF   (&ERR EQ 0).FI202
         B     SAVRGSS                 GO TO BEGINNING
         USING FIOCSB,L
FIOCSB   EQU   *
FIOCSBEP EQU   FIOCSB
         BCR   1,0                     DRAIN THE PIPE LINE FOR MODEL 91
         L     1,FIOSHCON              SET UP ADDRESSABILITY AS FOR
         USING FIOCSA,L                NORMAL ENTRY TO FIOCS
         STM   14,12,12(13)            SAVE REGS IN USER'S AREA
         LA    BASE2,LSAVEA            SER 'LSAVEA' AS NEXT SAVE AREA
         ST    BASE2,8(0,13)
         LR    BASE2,13                SET UP FOR USER'S 13 TO GO INTO
*                                      'LSAVEA' +4.
         MVI   SW1,1                   SET SPECIAL ENTRY SWITCH
         B     GETRGS
SAVRGSS  EQU   *
.FI202   ANOP
         ST    13,SAVE+4
         STM   14,12,SAVE+12
         AIF   (&ERR EQ 0).FI210
         LA    BASE2,SAVE              SET SAVE AS PREVIOUS SAVE AREA
GETRGS   EQU   *
         ST    BASE2,LSAVEA+4          PUT PREVIOUS SAVE AREA IN LSAVEA
.FI210   ANOP
         LM    BASE2,SAVER,UATCON
         DROP  L
         USING FIOCSA,BASE
         USING IHCUAT,BASE2
         USING UB,UTR
         USING IHADCB,DCBR                                         4595
         USING DECB,DECBR
         LR    RX,R
         MVC   OPTION1(1),PRIMOP(RX)
         SR    RJ,RJ
         IC    RJ,PRIMOP(RX)       PICKUP PRIMARY OPERATION
         SLA   RJ,2
TRTN     B     ENTRY(RJ)
ENTRY    B     FINIT
         B     FREAD                   SECONDARY ENTRY FOR READ
         B     FRITE
         B     FCTRL
         B     FCLOS
         EJECT
*****
         DS    0D
PKDD     DC    2F'0'
WORK2    DC    2F'0'                  INIT SET IN CVTB
TEMP     DS    F             TEMPORARY STORAGE AREA               10842
LENGTH   DS    H                      CHARS. WRITTEN BY IBCOM
TOOSMALL DC    AL2(17)
CON4     DC    H'4'
CON16    DC    H'16'                    FOR BKSP ERROR CHECKING
CON256   DC    F'256'                  CONSTANT FOR DIVIDE         4595
*                                      SWITCHES
OPTION1  DC    X'00'
FLAG     DC    X'00'
ECHECK   DC    X'00'
PRNTCD   DC    X'0808'                 UCB CODE FOR 1403 OR 1404 PRNTR
PARAMS   DC    XL1'00'
         AIF   (&ERR EQ 0).FI211
SW1      DC    XL1'00'                 SWITCH USED TO INDICATE ENTRY
*                                      FOR WRITING ON OBJ. ERROR UNIT
.FI211   ANOP
SAVEOPT  DC    XL1'00'
ZEROS    DC    F'0'
MIL      DC    H'16'         MAXIMUM ITEM LENGTH                  II232
XLRECL   DC    X'8000'             LRECL=X.                       II232
XSW      DC    X'00'                                              II232
XSWON    EQU   X'F0'                                              II232
*                                      SAVE AREAS
         DS    0F
UTREGSV  EQU   *                      PTRS SAVECD BY UTINIT
UTPTR    DC    F'0'
DCBPTR   DC    F'0'
DECBPT   DC    F'0'          POINTER TO LAST USED DECB            II210
ERRUB    DC    F'1'
         DS    2F                      AREA FOR USE BY OBJ. ERROR UNIT
*
SAVE     DC    2F'0'                  USED UPON ENTRY FROM IBCOM
         DC    A(LSAVEA)
         DC    15F'0'
*
MSAVEA   DC    9F'0'
*
LSAVEA   DC    1F'0'
         DC    A(SAVE)
         DC    16F'0'
VIHCERRM DC        V(IHCER@)                                       V1L2
VFIOCS2  DC    V(IHCFIOS2)             ADDRES OF SECOND CSECT
         AIF   (&ERR EQ 0).FI1
VRETEMP  DC    F'0'                    HOLDS OPEN EXIT RTNE       32775
*                                       ADDRESSABILITY DURING 214 32775
*
*
PRMS214  DC    A(MSG214)               ADDRESS OF THE MESSAGE
         DC    A(RETCD)                ADDRESS OF THE RETURN CODE FIELD
         DC    A(E214)                 ADDRESS OF THE ERROR NUMBER
         DC    XL1'80'                 LAST PARAMETER INDICATOR
         DC    AL3(DSRNPTR)            ADDRESS OF THE DATA SET
*                                      REFERENCE NUMBER
PRMS217  DC    A(MSG217)
         DC    A(RETCD)
         DC    A(E217)
         DC    XL1'80'
         DC    AL3(DSRNPTR)
PRMS218  DC    A(0)
         DC    A(RETCD)
         DC    A(E218)
         DC    A(DSRNPTR)
         DC    XL1'80'
         DC    AL3(0)
PRMS219  DC    A(MSG219)
         DC    A(RETCD)
         DC    A(E219)
         DC    XL1'80'
         DC    AL3(DSRNPTR)
PRMS220  DC    A(MSG220)
         DC    A(RETCD)
         DC    A(E220)
         DC    XL1'80'
         DC    AL3(DSRNPTR)
PRMS231  DC    A(MSG231)
         DC    A(RETCD)
         DC    A(E231)
         DC    XL1'80'
         DC    AL3(DSRNPTR)
E214     DC    F'214'
E217     DC    F'217'
E218     DC    F'218'
E219     DC    F'219'
E220     DC    F'220'
E231     DC    F'231'
RETCD    DC    F'0'
DSRNPTR  DC    F'0'
OBJPTR   DC    F'0'
WTP218   DC    AL2(108)            MSG LENGTH                     25557
         DC    X'8000'             MCSFLAGS FIELD                 25557
WTPSW    DC    X'00'               SW TO INDICATE ERR 218 OR 219  25557
         DS    0H
*
*
         AGO   .FI7
.FI1     ANOP
ADERRNO  DC    A(0)
         DC    F'0'
         DC    XL1'80'
         DC    AL3(ERRORNO)
ERRORNO  DC    F'0'
MSG219   WTO   'IHC219I FIOCS - MISSING DD CARD FOR         ',MF=L,    X
               ROUTCDE=(11),DESC=(7)                              25557
MCSCODE1 DC    H'108'             LENGTH OF MSG 218               25557
         DC    X'8000'             MCSFLAGS FIELD                 25557
MCSCODE2 DC    X'0200'             DESCRIPTOR CODE                25557
         DC    X'0020'                                            25557
.FI7     ANOP
         EJECT
*****
*****
FINIT    EQU   *
         BAL   RJ,UTINIT
         BC    15,DAFINIT               SEQUENTIAL RETURN FROM UTINIT**
         AIF   (&ERR EQ 0).FI2
         LA    1,PRMS231               GET PARAMETER LIST ADDRESS
         L     CSECT2R,VFIOCS2         GET ADDR OF 2ND. CSECT
         BAL   RJ,OFFCMINT(0,CSECT2R)  GO PUT OUT ERROR
ERRRET   L     13,4(0,13)              RESTORE ADDRESS OF SAVE AREA FRO
         LM    14,12,12(13)            WHICH TO RESTORE REGS. AND
         DROP  BASE                    RESTORE REGISTERS
         USING FIOCSA,L
         CLI   SW1,1                   IS THIS A SPECIAL ENTRY
         BE    *+8                     YES, BRANCH
         L     13,4(0,13)              NO, RESTORE REG. 13 FOR USER
         MVI   SW1,0                   RESET ENTRY SWITCH
         SR    GRY,GRY                 INDICATE ZERO LENGTH RECORD
         DROP  L
         USING FIOCSA,BASE
         LR    1,0
         B     2(0,1)                  RETURN AT ERROR OFFSET
         AGO   .FI6
.FI2     ANOP
         LA    1,231                   INDICATE ERROR 231
COMERRHN ST    1,ERRORNO               SAVE ERROR NUMBER IN PARAM. LIST
         LA    1,ADERRNO               GET ADDRESS OF PARAMETERS
         L     15,IBCOMCON             GET IBCOM ADDRESS
         L     13,THRTNUSR(0,15)       GET USER'S REG 13
         MVC   12(16,13),FRTNUSR(15)   MOVE HIS REGS 14-1 TO HIS AREA
         ST    13,IBCSV+4(0,15)        LINK SAVE AREAS
         LA    13,IBCSV(0,15)          GET IBCOMS SAVE AREA
         L     15,VIHCERRM             GET ADDDRESS OF ERROR MONITOR
         BR    15                      GO TO GIVE MESSAGE
.FI6     ANOP
DAFINIT  TM    CBYTE,POOLSW             RTN HERE IF NORMFLAG ON
         BO    SETOP
         L     CSECT2R,VFIOCS2         GET ADDR OF 2ND. CSECT
         BAL   RJ,OFFGTPL(0,CSECT2R)   GO GET BUFFER POOL
SETOP    TM    PARAMS,OUTPUT
         BO    TESTOUT
         EJECT
*****
FREAD    LM    UTR,DECBR,UTREGSV
         TM    ABYTE,OUTPUT *    WAS PREVIOUS OPERATION A WRITE?  MAINT
         BNO   TESTEOB *           NO, BRANCH                     MAINT
         BAL   RJ,INVERT                                          MAINT
         BAL   RJ,DOCHECK                                         MAINT
         NOP   0                                                  MAINT
         B     SETABYTE                                           MAINT
         SPACE 3
*****
TESTEOB  TM    BBYTE,EOBSW            AT END OF BUFFER ?
         BO    CR1                      YES. CHECK NEXT
*
*                                     IF VAR/BLK'D, GETLEN COMES HERE
*
DEBLOCK  BAL   RI,ENDREC              GET NEXT RECORD
*
         LA    GRX,0(RX,RD)
         TM    DCBRECFM,VFORM
         BO    DBVAR
*
         LH    GRY,DCBLRECL
        AR    RD,GRY
DBCOMMON ST    RD,RECPTR               RECPTR FOR NEXT READ
         CH    RD,LN                    IS NEXT RECPTR WITHIN BLK
         BL    DBRETURN
         OI    BBYTE,EOBSW            SET END OF BUFFER
DBRETURN B     FIORET
*
DBVAR    MVC   TEMP(2),0(GRX)          MOVE   AND
         LH    GRY,TEMP                    LOAD LITTLE LL
         AR    RD,GRY                  BUMP TO NEXT RECORD
         SH    GRY,CON4               SUBTR.J OFF CTL WD
         AH    GRX,CON4                BUMP PAST CTL WD
         B     DBCOMMON
*
*
*
         EJECT
CR1      EQU   *                                                  31899
         TM    BBYTE,ERR               IF ERROR OCCURRED BRANCH TO10595
         BO    CR                       ALLOW USER TO GET RECORD  10595
CR2      BAL   RJ,DOREAD               GO READ CURRENT RECORD      4595
         BAL   RJ,INVERT     FLIP BUFFERS AND DECBS               II210
CR       BAL   RJ,DOCHECK               CHECK FOR ANY I/O ERROR
         B     SETEOD                  EOD RETURN
         L     GRX,AREA                ADDR OF BUFFER JUST CHECKED 4595
*
* FOLLOWING LENGTH DETERMINATION IS EFFECTIVE FOR ALL V, U, AND TRUE
* TRUNCATED OR NON-TRUNCATED FIXED LENGTH RECORDS.  -  IF NOT TRUE
* TRUNCATED FOR FIXED, THEN SYNAD IS TAKEN.
*
GETLEN   TM    BBYTE,VSWITCH           TEST FOR VARIABLE FORMAT   10842
         BO    GETVLN                                             10842
         LH    GRY,DCBBLKSI                                       10842
         L     RD,STATLOC             STATUS IN DECB
         SH    GRY,14(RD)             SUBTR BYTES NOT READ
*                                     NOW HAVE LGTH OF BLOCK
TESTRVBL TM    BBYTE,VBLSW             VAR OR BLK'D
         BZ    FIORET                 NO
         STH   GRY,LN                   SAVE LGTH OF BLOCK READ
         B     DEBLOCK                YES
GETVLN   LH    GRY,0(0,GRX)            GET LENGTH OF VAR BLOCK    10842
         B     TESTRVBL            BRANCH TO RECORD TEST          II210
         EJECT
*
DOCHECK  OI    BBYTE,ETEST              ENABLE I/O ERROR CHECKING
CKCOM    NI    BBYTE,FF-ERR            TURN OFF ERROR LAST TIME BT10595
         CLI   LIVECT,0                ANY I/O PENDING            10595
         BE    4(0,RJ)                 NO                          4595
         MVI   LIVECT,0                                            4595
*         CHECK (DECBR)            CHECK LAST I/O OPERATION
         CHECK (DECBR)                 CHECK LAST I/O OPERATION
         TM    CBYTE,CONCAT            WAS RTN FROM CONCATENATION
         BO    SETUBYTE                YES, RE-INITIALIZE AND RE-READ
         MVC   ECHECK(1),BBYTE         MOVE OUT BBYTE FOR ERR CHECK
         NI    BBYTE,EOFF              TURN OFF ERROR TESTING BITS
         TM    ECHECK,ETYES            ERR TEST AND ERROR OCCURRED?
         BO    SETSYN                  YES, TAKE ERROR EXIT
CKOUT    B     4(RJ)                   NORMAL RETURN
*
INVERT   CLI   DCBBUFNO,1    IS DATA SET SINGLE BUFFERED          II210
         BCR   EQUAL,RJ      DO NOT INVERT BUFFERS IF YES         II210
         BAL   RX,GETABUF    GET CURRENT BUFFER AND DECB PTRS     II210
         ST    DECBR,DECBPTR SAVE DECB POINTER IN UNIT BLOCK      II210
         AIF   (&ERR EQ 1).FI657
         ST    DECBR,DECBPT
         AGO   .FI647
.FI657   ANOP
         CLI   SW1,1                                       23848
         BNE   PRESTR                                             MAINT
         CLC   ERRUB(4),UTREGSV   ERROR ON SAME UNIT AS OBJ ERR   MAINT
         BNE   STR                                                MAINT
PRESTR   ST    DECBR,DECBPT      YES,CHANGE DECB SAVED            MAINT
*                                      BY REGULAR CALL            MAINT
STR      EQU   *                                           23848
         ST    DECBR,ERRUB+8                                      II210
.FI647   ANOP
        XI    BBYTE,BSW               INVERT BUF BIT
         BR    RJ
*
GETABUF  L     RD,BUF2                                             4595
         LA    DECBR,DECB2                                        II210
         TM    BBYTE,BSW                                           4595
         BCR   1,RX                    BUF2,SWITCH SET             4595
         L     RD,BUF1                 BUF1,SWITCH NOT SET         4595
         LA    DECBR,DECB1                                        II210
         BR    RX                                                  4595
         EJECT                                                     4595
*                                                                  4595
DOREAD   MVI   LIVECT,1                                            4595
         MVI   DECBIO,X'80'            INPUT, READ 'BLKSIZE'       4595
*         READ  (DECBR),SF,(DCBR),MF=E
         READ  (DECBR),SF,(DCBR),MF=E                              4595
         BR    RJ                                                  4595
DOWRITE  MVI   LIVECT,1                                            4595
         MVI   DECBIO,X'00'            OUTPUT, WRITE LENGTH        4595
*         WRITE (DECBR),SF,(DCBR),MF=E
         WRITE (DECBR),SF,(DCBR),MF=E                              4595
         BR    RJ                                                  4595
*
*
*
ENDREC   TM    BBYTE,EOBSW            AT END OF BUFFER ?
         BZ    EOBOFF                 NO
         L     GRX,BUF1                 FOR SINGLE BUFFERING
         CLI   DCBBUFNO,1              SINGLE BUFFERED?
         BE    GOTBUFPT                 YES, BRANCH
         BAL   RX,GETABUF             YES, GET PTR TO BUFFER
         X     RD,BUFMASK         FLIP BUFFERS                    II210
         L     DECBR,DECBPTR                                      II210
         LR    GRX,RD
GOTBUFPT SR    GRY,GRY                  CLEAR REG
         TM    DCBRECFM,VFORM         VAR RECORD ?
         BZ    *+8                    NO
         LA    GRY,4                  YES, SKIP BLK LGTH
         STM   GRX,GRY,BLKPTR         STORE BLKPTR & RECPTR
         XI    BBYTE,EOBSW            FLIP EOBSW
*
EOBOFF   LM    RX,RD,BLKPTR           LOAD BLKPTR & RECPTR
         BR    RI
*
*
*
         EJECT
*****
TESTOUT  TM    ABYTE,OUTPUT             WAS PREV. OP READ
         BO    CHECKBL                  NO, BRANCH
         MVC   ABYTE(1),PARAMS *   INITIALIZE ABYTE               27374
         OI    DBYTE,X'10' *       INDICATE NOT TO FREE BUFFERS   27374
         TM    BBYTE,EOBSW *       AT END OF BUFFER           27374
         BZ    BUFTST *            NO - BRANCH                27374
         TM    BBYTE,BLSW *        ARE RECORDS BLOCKED            27374
         BZ    BUFTST              NO - PROCESS READ AHEAD RECORD MAINT
         TM    BBYTE,VSWITCH *     ARE RECORDS VARIABLE           27374
         BO    INVT *              YES - PROCESS READ AHEAD RECORD27343
         CLC   RECPTR+2(2),DCBBLKSI WILL ANOTHER FIXED BLK FIT    27343
         BNL   INVT *              NO - PROCESS READ AHEAD RECORD 27374
         NI    BBYTE,X'F7' *       SET SW OFF                     27374
         B     BUFTST *            FILL REST OF BUFFER            27374
INVT     BAL   RJ,INVERT *         FLIP BUFFERS                   27374
         BAL   RI,ENDREC *         GET PTRS                       27374
         BAL   RJ,DOCHECK  *                                      27374
         B     BLNK  *             EOD RETURN                     27374
           CLI     DCBBUFNO,1          SINGLE BUFFERED?          MAINT
           BE      BLNK                YES - DO NOT BKSP         MAINT
         B     ONEBK *              BACKSPACE ONE RECORD          27374
BUFTST   CLI   DCBBUFNO,1 *        SINGLE BUFFERED                27374
         BE    BACK1  *                                           27374
*                                      2 BUFS READS AHEAD
         BAL   RJ,INVERT     FLIP BUFFERS AND DECBS               II210
         BAL   RJ,CKCOM                YES, CHECK LAST READ
         B     BACK1          EOD RETURN                          27374
         BAL   RJ,BKSPONE
BACK1      TM     BBYTE,BLSW           ARE RECS BLKD?             MAINT
           BZ     SETDBYTE             NO - BRANCH                MAINT
           BAL     RJ,INVERT                                      MAINT
ONEBK    BAL   RJ,BKSPONE *                                       27374
BLNK     EQU   *                                                  27374
         L     CSECT2R,VFIOCS2         GET ADDR OF 2ND. CSECT     27374
         BAL   RJ,OFFBLANK(0,CSECT2R)  BLANK REST OF BUFFER       27374
         NI    DBYTE,X'EF' *       SET OFF SWITCH                 27374
CHECKBL  TM    BBYTE,EOBSW             ONLY COME HERE IF HAVE BEEN
         BO    FINDBUF                 WRITING.
*                                      FRITE WILL HAVE SET UP PTRS
*                                      FOR RECORD AND PASSED TO
*                                      CALLER BUT CALLER DID NOT USE.
*
*
         EJECT
*                                     FINIT EXITS TO HERE ON V/B
BLOCINIT BAL   RI,ENDREC              OBTAIN NEXT RECORD
         LH    GRY,DCBLRECL           LGTH
         LA    GRX,0(RX,RD)            PTR TO RECORD              10842
ZSCW     TM    DCBRECFM,RECMSK     RECFM=U?                       II232
         BO    FIORET              YES,BRANCH.                    II232
         TM    DCBRECFM,VFORM      IS RECFM VARIABLE             10842
         BZ    FIORET                                             10842
*
         TM    PARAMS,FORMAT       FORMATTED WRITE?               II232
         BO    CKMIXED             YES, BRANCH.                   II232
         LH    GRY,DCBBLKSI                                       II232
         SR    GRY,RD                                             II232
VOUT     EQU   *                                                  II232
         SH    GRY,CON4                                           II232
VBFMAT02 EQU   *                                                  II232
         MVC   2(2,GRX),ZEROS      ZERO LAST 2 BYTES OF SCW      10842
         LA    GRX,4(0,GRX)            PTR TO RECORD              10842
         B     FIORET
         EJECT
*
CKMIXED  EQU   *                                                  II232
         LH    0,DCBBLKSI                                         II232
         SR    0,RD                                               II232
         TM    XSW,XSWON           LRECL>BLKSIZE-4?               II232
         BO    BIGLRECL            YES, BRANCH.                   II232
         CH    GRY,XLRECL          LRECL=X?                       II232
         BE    FORCE               YES, BRANCH.                   II232
         CR    0,GRY          REMAINING BYTES > OR = LRECL?       II232
         BNL   VOUT                YES,BRANCH                     II232
         CH    RD,CON4             IS THIS A NEW BLOCK            II232
         BE    SUBLRECL            YES, BRANCH.                   II232
         AR    0,RD          (0)=BLKSIZE                          II232
         SH    0,CON4                                             II232
         CR    0,GRY               IS LRECL>BLKSIZE-4?            II232
         BNL   BEND2               NO, BRANCH.                    II232
         OI    XSW,XSWON           INDICATE LRECL>BLKSIZE-4       II232
         B     BEND2               WRITE CURRENT BUFFER           II232
FORCE    EQU   *                                                  II232
         CH    RD,CON4             IS THIS A NEW BLOCK?           II232
         BE    SUBLRECL            YES, BRANCH.                   II232
         OI    XSW,XSWON                                          II232
         B     BEND2                                              II232
BIGLRECL EQU   *                                                  II232
         XI    XSW,XSWON           TURN XSW OFF                   II232
SUBLRECL EQU   *                                                  II232
         LR    GRY,0         SUBSTITUTE BLKSIZE-4 FOR LRECL       II232
         B     VOUT                                               II232
*
*
         SPACE 3
FINDBUF  BAL   RX,GETABUF               GET NEXT BUFFER
         X     RD,BUFMASK         FLIP BUFFERS                    II210
         LR    GRX,RD                  POINTER TO BUFFER
         LH    GRY,DCBBLKSI           SIZE
         B     FIORET
         EJECT
*****
FRITE    STH   GRX,LENGTH             IBCOM RTNS # BYTES WRITTEN
         AIF   (&ERR EQ 0).FI203
         CLI   SW1,1                   IS THIS ENTRY FOR OBJ. ERR. UNIT
         BE    LOADOBJ                 YES¨ BRANCH
.FI203   ANOP
         LM    UTR,DECBR,UTREGSV      PTRS TO UB,DCB,DECB
TEST1    EQU   *
         TM    BBYTE,VBLSW            VAR OR BLK4'D ?
         BZ    COMOUT                 NO
*
*
*
BLOCK    LM    RX,RD,BLKPTR           LOAD BLKPTR & RECPTR
         LH    0,DCBBLKSI                                         II232
         TM    DCBRECFM,VFORM         VAR ?
         BO    BVAR                   YES
*
         LH    GRY,DCBLRECL                                       II232
         AR    RD,GRY                 NO. ADD LRECL TO CUR REC OFFSET
         SR    0,RD                                               II232
         CR    0,GRY                                              II232
         BL    BEOBSET                                            II232
         LA    GRX,0(RX,RD)           PTR TO NEXT RECORD(FIXED)
         ST    RD,RECPTR                                          II232
         B     FIORET                                             II232
         EJECT
         AIF   (&ERR EQ 0).FI204
*
LOADOBJ  LM    UTR,DECBR,ERRUB         LOAD REGS FROM SECONDARY SAVE
         B     TEST1                    AREA THEN BRANCH BACK
.FI204   ANOP
*
BVAR     LTR   GRX,GRX                CHECK LENGTH OF DATA PORTION.4993
         LA    GRX,4(GRX)             ADD LENGTH OF SEGMENT CW.    4993
         BNZ   BVAR1                  IS SEGMENT LENGTH GT 4?      4993
         TM    DCBRECFM,PRNTRCTL      NO.  IS A PRINTER CONTROL    4993
         BZ    BVAR1                  CHARACTER NEEDED?            4993
         LA    GRX,2(GRX)             YES.  MAKE SEGMENT LENGTH 6. 4993
BVAR1    LA    RJ,0(RX,RD)            POINT TO SEGMENT CTL WORD.   4993
*              OF SEG JUST FILLED BY IBCOM.                       II232
         STH   GRX,TEMP
         MVC   0(2,RJ),TEMP            MOVE LITTLE LL TO CTL WD
         AR    RD,GRX                 NEXT RECORD
         LA    GRX,0(RX,RD)            PTR TO SCW OF NEXT RECORD  10842
*
BCOMMON  ST    RD,RECPTR              RECORD OFFSET
         TM    BBYTE,BLSW              IS THIS VARIABLE UNBLOCKED
         BZ    BEND                      YES, TERMINATE BLOCK
         TM    PARAMS,FORMAT       IS IT FORMATTED OUTPUT         II232
         BO    VBFMAT01      YES, BRANCH.                         II232
         SR    0,RD      BLKSIZE-RD=# OF BYTES REMAINING IN THE   II232
*                        BLOCK                                    II232
         SH    0,CON4                                             II232
*              (0)=# OF ACTUAL REMAINING BYTES-LADW=# USEABLE     II232
*              REMAINING BYTES.                                   II232
         CH    0,MIL     IS REMAINING USEABLE BYTES>OR=MIL?       II232
         BL    BEND      NO, BRANCH                               II232
         LR    GRY,0     (GRY)=# OF USEABLE BYTES IN BUFFER       II232
         B     VBFMAT02                                           II232
VBFMAT01 EQU   *                                                  II232
         SR    0,RD      (0)=# OF BYETS REMAINING IN BLOCK        II232
         CH    0,DCBLRECL     REMAINING BYTES>OR=DCBLRECL?        II232
         BL    BEND      NO, BRANCH                               II232
         LH    GRY,DCBLRECL                                       II232
         B     VOUT                                               II232
*
BEND     TM    DCBRECFM,VFORM         TERMINATE BLOCK
         BZ    BEOBSET
BEND2    EQU   *                                                  II232
         L     RD,RECPTR              SET BLOCK LGTH IN BIG LL
         SLL   RD,16         INSURE LAST 2 BYTES OF BDW WILL BE 0 II210
         ST    RD,0(RX)      STORE BIG LL                         II210
*
BEOBSET  OI    BBYTE,EOBSW            CURR BUF DONE IN
         EJECT
*
COMOUT   CLC   LENGTH(2),TOOSMALL                                 II210
         BC    2,MOVLEN                OK
         MVI   LENGTH+1,MINLEN         SET MINIMUM LENGTH
MOVLEN   MVC   6(2,DECBR),LENGTH      FIELD USED IF U RECS
         BAL   RJ,DOWRITE                                          4595
         BAL   RJ,INVERT     FLIP BUFFERS AND DECBS               II210
         NI     BBYTE,EOBSWOFF                                   A38926
         BAL    RJ,DOCHECK             CHECK PREVIOUS WRITE      A38926
         NOP   0                        DUMMY END EXIT
         OI     BBYTE,EOBSW                                      A38926
TESTWVBL BAL   RX,SETBUF     GO TO CLEAR BUFFER                   10842
         TM    BBYTE,VBLSW                                        10842
         BZ    FIORET                 NO
        B     BLOCINIT                INIT BLOCKING
*
SETBUF   TM    PARAMS,FORMAT                                      10842
         BCR   8,RX         DO NOT CLEAR BUF FOR UNFORMATTED WRITE10842
         L     RI,AREA       CURRENT BUFFER POINTER               II210
         MVI   0(RI),C' '              FMT TO BLANKS               4595
COMBUF   LH    GRY,DCBBLKSI            LOAD BUFFER LGTH            4595
SUBT     BCTR  GRY,0               LESS ONE FOR EXECUTE           24797
         BCTR  GRY,0                   LESS ONE FOR DOMINOE EFFECT 4595
         EX    GRY,ZEROPART            INIT FIRST PART             4595
         SR    GRX,GRX                                             4595
         D     GRX,CON256              DIVIDE FOR NO. OF MVC'S     4595
         LTR   GRY,GRY                 ANY OVER 256                4595
         BZ    RITOUT                  NO                          4595
         LA    RI,1(GRX,RI)            BUMP PTR TO NEXT LGTH       4595
ZEROBUF  MVC   1(256,RI),0(RI)                                     4595
         LA    RI,256(RI)              NEXT LGTH                   4595
         BCT   GRY,ZEROBUF             ANOTHER LGTH                4595
RITOUT   L     GRX,AREA      CURRENT BUFFER POINTER               II210
         LH    GRY,DCBBLKSI                                        4595
         BR    RX                                                  4595
ZEROPART MVC   1(1,RI),0(RI)                                       4595
         EJECT
*****
UTINIT   MVC   PARAMS(1),SECOP(RX)
         MVC   FLAG(1),0(GRX)
         TM    FLAG,VARUNIT
         BZ    NOTVAR
         L     GRX,0(GRX)
NOTVAR   L     GRX,0(GRX)
         TM    FLAG,XIOCLASS
         BZ    LOGUTCL
         IC    GRX,ERRMSG(GRX)
LOGUTCL  LA    RD,0(GRX)
         STH   GRX,BYTES               STORE DSRN IN UAT
         AIF   (&ERR EQ 0).FI5
         CLI   SW1,1                   IS ENTRY FOR OBJ. ERR. UNIT
         BE    SAVEOBJ                 YES, BRANCH
         ST    RD,DSRNPTR              NO, SAVE DSRN
         L     15,IBCOMCON             GET ADDRESS OF IBCOM
         ST    RD,DSRNOF(0,15)         SAVE DSRN FOR IBCOM'S POSSIBLE
         B     CHKRNG                  USE(IF AN ERROR OCCURS)
SAVEOBJ  ST    RD,OBJPTR               SAVE DSRN FOR LATER USE
CHKRNG   EQU   *
.FI5     ANOP
         LTR   GRX,RD                   IS DSRN ZERO
         BC    13,DSRNERR               BR IF DSRN ZERO OR NEGATIVE
         BCTR  GRX,0                    SET UP FOR 0-ORIGIN INDEXING **
         SLA   GRX,4                    MULT. BY 16 FOR INDEX VALUE  **
         CH    GRX,ENDT
         BL    GOODUT
         AIF   (&ERR EQ 0).FI3
DSRNERR  LA    1,PRMS220               GIVE MESSAGE 220
         LA    RJ,ERRRET               SET RETURN POINT
         L     CSECT2R,VFIOCS2         GET ADDR OF 2ND. CSECT
         B     OFFCMINT(0,CSECT2R)     GO PUT OUT ERROR
         AGO   .FI8
.FI3     ANOP
DSRNERR  LA    1,220                   GIVE MESSAGE 220
         B     COMERRHN
.FI8     ANOP
GOODUT   LA    GRX,UTENTRY(GRX)         COMPUTE UAT POINTER ADDRESS  **
         TM    15(GRX),DASD             IS THIS A DASD DSRN          **
         BC    1,DACHK             BR IF A SEQUENTIAL DSRN           **
         BC    15,4(RJ)            MAKE DASD RETURN                  **
DACHK    TM    3(GRX),X'01'             IS UNIT BLOCK ALREADY LOADED **
         BC    8,LOADED            BR IF ALREADY IN STORAGE          **
         EJECT
LOADUT   EQU   *
*        GETMAIN R,LV=164          GETMAIN FOR UB,DCB AND 2 DECBS II210
         GETMAIN R,LV=164          GETMAIN FOR UB,DCB AND 2 DECBS II210
         ST    1,0(GRX)                ADDR OF UB
         LR    UTR,1
         MVI   UB,0                                                4595
         MVC   UB+1(99),UB   ZERO OUT UNIT BLOCK                  II210
         MVC   DOFFSET+16(72),DCBBL+16
         LA    DCBR,DOFFSET
         ST    DCBR,DCBOFFS
         LA    DECBR,RWOFS   ADDRESS OF DECB1                     II210
         ST    DECBR,DECBPTR                                      II210
*****
LOADED   L     UTR,0(GRX)              UTR PTS TO UB
         L     DCBR,DCBOFFS           PTS TO DCB
         L     DECBR,DECBPTR                                      II210
         AIF   (&ERR EQ 0).FI205
         CLI   SW1,1                   IS THIS OBJ. ERR. UNIT
         BNE   SAVRGS                  NO- BRANCH
         STM   UTR,DECBR,ERRUB         SAVE FOR NEXT ENTRY IN SECONDARY
         B     *+8                     AREA AND BRANCH.
SAVRGS   EQU   *
.FI205   ANOP
         STM   UTR,DECBR,UTREGSV      SAVE FOR NEXT ENTRY
         CLI   OPTION1,X'00'                                      24797
         BNE   CTLTST                                             24797
         TM    PARAMS,OUTPUT       IF OUTPUT                      24797
         BO    NFLG                LEAVE SWITCH ON                24797
         B     SETOFF                                             24797
         EJECT
CTLTST   EQU   *                                                  24797
         CLI   OPTION1,X'03'       CTRL OPERATION?                II210
         BNE   SETOFF              NO                             II210
         CLI   PARAMS,X'00'        2ND BKSP AFTER EOF?            II210
         BE    NFLG                YES                            II210
SETOFF   NI    DBYTE,X'FD'         SET SWITCH OFF                 II210
NFLG     TM    CBYTE,X'C0'         OPEN, NOT T CLOSE?             II210
         BC    12,OP               NO                             II210
         TM    DBYTE,X'40'         PREV OP BKSP?                  II210
         L     CSECT2R,VFIOCS2         GET ADDR OF 2ND. CSECT     II210
         BZ    OFFOPNED(0,CSECT2R)     NO - BRANCH TO 214 TEST    II210
OP       EQU   *                                                  II210
         TM    CBYTE,RWNDFLAG          TEST IF OPENED ( WILL NOT
*                                      BE OPENED IF PREVIOUSLY REWOUND
*                                      OR IF NEVER OPENED BEFORE)
         BO    NOTRWD                  CLOSED IF PREV RWD
         TM    OPTION1,FCNTL          DCB CLOSED
         BZ    NOTRWD                 BRANCH IF NOT CNTL
         TM    PARAMS,X'02'           EOF ? (CQUALS)
         BZ    NEOFOPEN            BR IF NOT EOF AND OPENED
NOTRWD   TM    CBYTE,PROPEN           PREV OPEN ?
         BO    PRIOROP
         SPACE 3
*****
FIXUT    BAL   RI,CVTB                SET UNIT # IN DCB
         MVC   DCBDDNAM+2(2),WORK2+6  STORE IN FTXXF001 SKEL.
         L     CSECT2R,VFIOCS2         GET ADDR OF 2ND. CSECT
         B     OFFOPEN(0,CSECT2R)      OPENING FOR FIRST TIME
         EJECT
*****
PRIOROP  TM    CBYTE,RWNDFLAG           WAS THERE A PREVIOUS REWIND
         BZ    SETDDTO1            YES, SET DDNAME TO SEQ 1
         TM    DBYTE,X'40'         PREVIOUS OP BKSP?              II210
         L     CSECT2R,VFIOCS2         GET ADDR OF 2ND. CSECT     II210
         BO    OFFPRBKS(0,CSECT2R)     BRANCH IF YES              II210
*                                       PREVIOUS EOF PROCEDURE
PREVEOF  OI    CBYTE,NTCLOSE           TURN OFF TCLOSE SWITCH
         CLI   OPTION1,FCNTL                                       4595
         BNE   EOFSOP                                              4595
         CLI   PARAMS,X'01'                                        4595
         BL    GOBSP2                  BACKSPACE=X'00'             4595
         BCR   8,RJ                                               14552
*                                      ENDFILE = X'02'             4595
*                                                                  4595
EOFSOP   EQU   *
         CLOSE ((DCBR),LEAVE)
         NI    CBYTE,CLOSRWND+RWNDFLAG INDICATE NOT OPENED
*
INCRDD   PACK  PKDD+6(2),DCBDDNAM+5(3)
         CVB   RD,PKDD
         LA    RD,1(RD)
         BAL   RI,CVTB
         MVC   DCBDDNAM+5(3),WORK2+5
         L     CSECT2R,VFIOCS2         GET ADDR OF 2ND. CSECT
         B     OFFOPEN(0,CSECT2R)      OPEN
*
         SPACE 3
*****
*                                      JUST RWD, REINIT TO FTXXF001
SETDDTO1 MVC   DCBDDNAM+5(3),DDMSK+5
         L     CSECT2R,VFIOCS2         GET ADDR OF 2ND. CSECT
         B     OFFOPEN(0,CSECT2R)      OPEN
         EJECT
*****
CVTB     CVD   RD,PKDD                 RD CONTAINS UNIT NO.
         UNPK  WORK2(8),PKDD+4(4)      LAST HALF-WD CONTAINS NO.
         OI    WORK2+7,X'F0'           GET RID OF SIGN             4595
         BR    RI
         SPACE 3
*
SETUBYTE MVI   CBYTE,INITSET           INITIALIZE CBYTE
           BAL     RX,GETBUFFS    SET ADDR OF BUF1&BUF2 INTO UB   MAINT
SETDBYTE   MVI     DBYTE,X'00'         INITIALIZE DBTYE           MAINT
SETABYTE MVI   BBYTE,BINIT                                        II210
SETA     MVC   ABYTE(1),PARAMS                                    II210
         L     CSECT2R,VFIOCS2         SECTION MOVED BECAUSE OF
         B     OFFSTBYT(0,CSECT2R)     ADDRESSABILITY PROBLEM
*
GETBUFFS L     RD,DCBBUFCB            STORE ADDR OF BUF1 & BUF2
         L     RI,0(RD)                 IN UNIT BLOCK
         ST    RI,BUF1
         ST    RI,AREA1      SET ADDRESS OF BUFF1 IN AREA1        II210
         CLI   DCBBUFNO,1               SINGLE BUFFERED
         BCR   EQUAL,RX            YES, RETURN
         AH    RI,6(RD)            ADD BLKSIZE TO BEGINNING
         ST    RI,BUF2                  PUT 2ND BUF. ADDR. IN UB
         ST    RI,AREA2      SET ADDRESS OF BUF2 IN DECB2         II210
         MVC   BUFMASK,BUF1                                       II210
         XC    BUFMASK,BUF2  CREATE MASK FOR FLIPPING BUFFERS     II210
         BR    RX
         EJECT
*****
FCTRL    BAL   RJ,UTINIT
         BC    15,CTLRTN                SEQUENTIAL RETURN
NEOFOPEN LA    GRX,FORMAT(0)            TELL IHCFCOME NOT TO READ    **
*                                       DASD CTRL RETURNS HERE       **
*                                       COMES HERE ALSO FROM LOADED
*                                       RTNE IF NOT EOF AND OPENED
         BC    15,FIORET                RETURN TO CALLER IF DASD     **
CTLRTN   CLI   PARAMS,X'01'                                        4595
         BH    EOFM                    'ENDFILE' = X'02'           4595
         BE    RWND                    'REWIND' = X'01'            4595
*                                      'BACKSPACE' = X'00'         4595
         SPACE 3
*****
BKSP     EQU   *                                                  II210
BLTEST   TM    BBYTE,BLSW          ARE RECORDS BLOCKED ?          II210
         BO    EOFBB               YES - BRANCH                   II210
         TM    DBYTE,X'80'         PREVIOUS OPERATION A BKSP?     II210
         BO    BKSPBLK             YES - BRANCH                   II210
         OI    DBYTE,X'10'         INDICATE NOT TO FREE BUFS      II210
CK1      EQU   *                                                  II210
         BAL   RJ,INVERT           INVERT BUFS                    II210
         BAL   RJ,CKCOM            CHECK PENDING I/O              II210
         B     BKSPBLK             EOD RETURN                     II210
         NI    DBYTE,X'EF'         SET SW OFF                     II210
TESTDS   EQU   *                                                  II210
         TM    ABYTE,OUTPUT        OUTPUT DATA SET?               II210
         BZ    BTEST               BRANCH ON INPUT                II210
         BAL   RI,TCLOSE           WRITE LAST RECORD AND EOF      II210
         B     BKSPBLK             GO BACKSPACE                   II210
BTEST    EQU   *                                                  II210
         CLI   DCBBUFNO,1          CHECK BUFFERING                II210
         BE    BKSPBLK             SINGLE BUFFERED                II210
NBLOOP   BAL   RJ,BKSPONE          BKSP TWICE IF BUFNO = 2        II210
BKSPBLK  BAL   RJ,BKSPONE                                         II210
         TM    ABYTE,FORMAT        FORMATTED I/O?                 II210
         BO    NBOUT               YES - RETURN                   II210
         BAL   RI,BLOOP3           READ - CHECK                   II210
         L     GRX,AREA            OBTAIN CURRENT BLOCK PTR       II210
         TM    6(GRX),X'02'        ANY PRECEEDING SEGMENTS?       II210
         BO    NBLOOP              YES- BKSP AGAIN                II210
         BAL   RJ,BKSPONE          NO - DO FINAL BKSP             II210
NBOUT    OI    DBYTE,X'C0'         SET LOG AND PHY BKSP SWITCHES  II210
         B     FIORET              RETURN TO IBCOM                II210
         EJECT
EOFBB    EQU   *                                                  II210
         TM    DBYTE,X'02'         SECOND BKSP AFTER EOF?         II210
         BZ    VORF                NO - BRANCH TO TEST REC TYPE   II210
         NI    DBYTE,X'FD'         SET SWITCHES OFF               II210
         TM    CBYTE,POOLSW        ARE BUFFS ATTACHED?            II210
         BO    SPEC1               YES - BRANCH                   II210
         L     CSECT2R,VFIOCS2         GET ADDR OF 2ND. CSECT     II210
         BAL   RJ,OFFGTPL(0,CSECT2R)   GO TO GET BUFFER POOL      II210
SETBLK   MVC   BLKPTR(4),BUF1      SET CURRENT BUFF PTR           II210
         L     CSECT2R,VFIOCS2         GET ADDR OF 2ND. CSECT     II210
         B     OFFOPBK(0,CSECT2R)      GET ADDRESS OF DECB1       II210
         SPACE 3
SPEC1    BAL   RI,BLOOP2           BKSP - READ - CHECK            II210
SETSW    OI    DBYTE,X'C0'         SET DBYTE SWITCHES             II210
         LH    GRX,DCBBLKSI        CALC LEN OF BLOCK              II210
         L     RJ,STATLOC                                         II210
         SH    GRX,14(RJ)                                         II210
         TM    BBYTE,VSWITCH       VARIABLE RECORDS?              II210
         BO    VAR                 YES - BRANCH                   II210
         B     SUB1                GO SET RECPTR TO PRECEEDING RECII210
         EJECT
VORF     EQU   *                                                  II210
         TM    BBYTE,VSWITCH       VARIABLE RECORDS?              II210
         BO    VAR1                YES - BRANCH                   II210
PRBK     EQU   *                                                  II210
         SR    GRX,GRX                                            II210
         C     GRX,RECPTR          RECPTR=0?                      II210
         BNE   DSTEST              BRANCH IF NOT 0                II210
         TM    ABYTE,OUTPUT        OUTPUT DS?                     II210
         BZ    IN1                 BRANCH IF INPUT                II210
         TM    DBYTE,X'40'         PREVIOUS OP A BKSP?            II210
         BO    BACKSP              YES - BRANCH                   II210
TCL      BAL   RI,TCLOSE           WRITE LAST RECORD AND EOF      II210
         B     SETBLK              BRANCH TO SET BLKPTR           II210
INVRT    BAL   RJ,INVERT           INVERT BUFFS                   II210
BACKSP   EQU   *                                                  II210
         BAL   RI,BLOOP1           BKSP - BKSP - READ - CHECK     II210
         B     SETSW               BRANCH TO SET DBYTE            II210
IN1      EQU   *                                                  II210
         TM    DBYTE,X'80'         HAS A PHYSICAL BKSP OCCURRED?  II210
         BO    BACKSP              BRANCH IF YES                  II210
BUFTEST  CLI   DCBBUFNO,1          SINGLE BUFFERED?               II210
         BE    BACKSP              BACKSPACE TWICE                II210
CKC      EQU   *                                                  II210
         OI    DBYTE,X'10'         INDICATE NOT TO FREE BUFFS     II210
         BAL   RJ,INVERT           DOUB BUF - INVERT AREA         II210
         BAL   RJ,DOCHECK          CHECK PENDING I/O              II210
         B     INVRT               EODAD RETURN                   II210
         NI    DBYTE,X'EF'         SET SW OFF                     II210
         BAL   RJ,INVERT           INVERT BUFFERS                 II210
         LA    RI,SETSW                                           II210
         B     BLOOP               BACKSPACE THREE TIMES          II210
DSTEST   EQU   *                                                  II210
         TM    DBYTE,X'40'         PREVIOUS OP A BKSP?            II210
         BO    SUB                YES - BRANCH                    II210
         TM    ABYTE,OUTPUT        OUTPUT DS?                     II210
         BO    TCL                 YES - WRITE LAST REC AND EOF   II210
IN       OI    DBYTE,X'40'         SET DBYTE                      II210
         SPACE
SUB      EQU   *                                                  II210
         L     GRX,RECPTR                                         II210
SUB1     SH    GRX,DCBLRECL        OBTAIN PTR TO PRECEEDING RECORDII210
         ST    GRX,RECPTR                                         II210
         B     FIORET              RETURN TO IBCOM                II210
         EJECT
VAR1     LA    GRX,4                                              II210
         C     GRX,RECPTR          RECPTR AT FIRST RECORD IN BLK? II210
         BNE   VAR3                NO - BRANCH                    II210
         TM    ABYTE,OUTPUT        OUTPUT DATA SET?               II210
         BZ    PTR4                BRANCH ON INPUT                II210
         TM    DBYTE,X'40'         PREV OP A BKSP?                II210
         BO    PTR4                YES - BKSP TWICE               II210
         B     TCL                 TCLOSE IF FIRST BACKSPACE      II210
VAR3     MVC   RECSV(2),RECPTR+2   SAVE RECPTR                    II210
         TM    DBYTE,X'40'         PREVIOUS OP A BKSP?            II210
         BO    SEARCH              NO - BRANCH                    II210
         TM    ABYTE,OUTPUT        OUTPUT DS?                     II210
         BO    TCL                 TCLOSE IF FIRST BACKSPACE      II210
         OI    DBYTE,X'40'         SET DBYTE                      II210
SEARCH   BAL   RJ,LOOP1            BR TO LOCATE PRECEEDING REC    II210
PTR4     EQU   *                                                  II210
         TM    DBYTE,X'80'      PHYSICAL BKSP OCCURRED PREVIOUSLY?II210
         BO    BACKSP              YES - BRANCH                   II210
         TM    ABYTE,OUTPUT        OUTPUT DS?                     II210
         BZ    BUFTEST             BRANCH IF INPUT                II210
         B     BACKSP              BR TO BKSP TWICE               II210
VAR      L     RX,BLKPTR           OBTAIN PTR TO BUFF             II210
         MVC   RECSV(2),0(RX)      OBTAIN BIG LL                  II210
         BAL   RJ,LOOP1            LOCATE PRECEEDING RECORD       II210
         B     BACKSP              BR TO BKSP TWICE               II210
         SPACE 3
LOOP1    EQU   *                                                  II210
         SR    GRX,GRX                                            II210
         STH   GRX,PTSV                                           II210
         LA    GRX,4               SET RECPTR TO FIRST REC IN BLK II210
         ST    GRX,RECPTR                                         II210
LOOP     EQU   *                                                  II210
         A     GRX,BLKPTR          OBTAIN PTR TO REC              II210
         TM    2(GRX),X'02'        ANY PRECEEDING SEGMENTS?      II210
         BO    CONT                YES - BRANCH                  II210
         MVC   PTSV(2),RECPTR+2    NO - SAVE RECPTR               II210
CONT     EQU   *                                                  II210
         MVC   WKBK2(2),0(GRX)     OBTAIN LITTLE LL               II210
         L     GRX,WKBK2                                          II210
         SLL   GRX,1              ZERO OUT HIGH ORDER BIT         II210
         SRL   GRX,17                                             II210
         A     GRX,RECPTR          INCREASE RECPTR BY LITTLI LL   II210
         ST    GRX,RECPTR          SET RECPTR                     II210
         CH    GRX,RECSV           END OF SEARCH?                 II210
         BNE   LOOP                NO - GO THRU LOOP AGAIN        II210
         SR    GRX,GRX                                            II210
         CH    GRX,PTSV            FIRST SEGMENT FOUND?           II210
         BCR   8,RJ                NO - RETURN TO BKSP AGAIN      II210
         MVC   RECPTR+2(2),PTSV    YES - SET RECPTR               II210
         B     FIORET              RETURN TO IBCOM                II210
         EJECT
BLOOP    BAL   RJ,BKSPONE                                         II210
BLOOP1   BAL   RJ,BKSPONE                                         II210
BLOOP2   BAL   RJ,BKSPONE                                         II210
BLOOP3   BAL   RJ,DOREAD           READ CURRENT BLOCK             II210
         BAL   RJ,DOCHECK          CHECK PREVIOUS READ            II210
         B     NBOUT                   BRANCH IF DUMMY DATA SET  A43159
         BR    RI                  RETURN                         II210
         SPACE 3
GOBSP2   EQU   *                                                  II210
         OI    DBYTE,X'C2'         SET SWITCHES                   II210
         B     FIORET              RETURN TO IBCOM                II210
         SPACE 3
RECSV    DS    H                                                  II210
PTSV     DS    H                                                  II210
*
BKSPONE  EQU   *
*        BSP   (DCBR)
         BSP   (DCBR)
         BR    RJ
         EJECT
*****
RWND     DS    0H                                                 II210
         BAL   RJ,FINBLOCK                                         9351
         BAL   RJ,INVERT     FLIP BUFFERS AND DECBS               II210
         TM    ABYTE,OUTPUT                                       20213
         BZ    NOERRCK             FOR INPUT DATA SET DO NOT      20213
         OI    BBYTE,ETEST         ENABLE ERROR CHECKING          20213
NOERRCK BAL   RJ,CKCOM             CHECK PREVIOUS I/O             20213
         NOP   0
         CLOSE ((DCBR),REREAD)
         NI    CBYTE,CLOSRWND
         L     CSECT2R,VFIOCS2         GET ADDR OF 2ND. CSECT
         BAL   RI,OFFFRPL(0,CSECT2R)   GET RID OF YOUR BUFFERS
         B     FIORET                   RETURN TO IBCOM
         SPACE 3
*****
EOFM     BAL   RJ,FINBLOCK
         BAL   RJ,INVERT     FLIP BUFFERS AND DECBS               II210
         BAL   RJ,DOCHECK               CHECK PREVIOUS I/O OPERATION
         B     SETCL
         TM        ABYTE,OUTPUT        WAS LAST OPERATION OUTPUT? 47947
         BC        5,TSTDA             YES...GO TEST FOR D.A.DEV. 47947
         CLI       DCBBUFNO,1          DATA SET SINGLE BUFFERED?  47947
         BE        TSTDA               YES...GO TEST FOR D.A.DEV. 47947
         BAL       RJ,BKSPONE          DO ONE PHYSICAL BACKSPACE  47947
TSTDA    EQU       *                                              47947
         TM    DCBDEVT,X'20'    IS THIS A DIRECT ACCESS DATA SET A37386
         BNO   NOTDA                   NO,BRANCH                 A37386
         LA    1,0(DCBR)               PUT DCB ADDRS INTO REG 1  A37386
*        SVC   25                      TRACK BALANCE SVC         A37386
         SVC   25                                                A37386
NOTDA    OI    DCBOFLGS,X'80'          FORCE BSAM TO WRITE EOF   A37386
         BAL   RI,TCLOSE
SETCL    NI    CBYTE,CLSET
         L     CSECT2R,VFIOCS2         GET ADDR OF 2ND. CSECT
         BAL   RI,OFFFRPL(0,CSECT2R)   GET RID OF YOUR BUFFERS
         B     FIORET
         EJECT
*****
TCLOSE   EQU   *
         BAL   RJ,FINBLOCK              PUT OUT LAST BLOCK           **
         BAL   RJ,INVERT     FLIP BUFFERS AND DECBS               II210
         BAL   RJ,DOCHECK              CHECK PREV I/O              4595
         NOP   0                                                   4595
TCLS1    EQU   *
*        CLOSE ((DCBR),LEAVE),TYPE=T  CLOSE DATA SET
         CLOSE ((DCBR),LEAVE),TYPE=T        CLOSE DATA SET           **
         BR    RI
         EJECT
*****
FCLOS    LA    GRX,UTENTRY
         LA    RX,16                    SET INCREMENT FOR BXLE
         LH    RD,ENDT
         LA    RD,UTENTRY-8(RD)
TESTOPEN TM    3(GRX),X'01'             WAS DSRN EVER USED           **
         BO    NEXTCL              BR IF NEVER USED                  **
         OC        0(4,GRX),0(GRX)                                 V1L2
         BZ        NEXTCL                                          V1L2
         TM    15(GRX),X'01'            IS THIS A DASD DSRN          **
         BC    8,NEXTCL            BR IF IT IS A DASD DSRN           **
         L     UTR,0(GRX)
         ST    RD,TEMP       SAVE CONTENTS OF REG                 II210
         L     DECBR,DECBPTR                                      II210
         L     DCBR,DCBOFFS
         TM    DCBOFLGS,OPENBIT        IS DATA SET OPEN            4595
         BZ    FREEUBLK                BR IF CLOSED                4595
CKFINBLK BAL   RJ,FINBLOCK
         BAL   RJ,INVERT     FLIP BUFFERS AND DECBS               II210
CONTCLOS BAL   RJ,CKCOM                CHECK PREVIOUS I/O        A42696
         NOP   0
CNTCLOSE EQU   *
*        CLOSE ((DCBR))            CLOSE DATA SET
         CLOSE ((DCBR))      CLOSE DATA SET WITH DISP OPTION      II210
         L     CSECT2R,VFIOCS2         GET ADDR OF 2ND. CSECT      4595
         BAL   RI,OFFFRPL(0,CSECT2R)   GET RID OF YOUR BUFFERS     4595
FREEUBLK LR    1,UTR                   LOAD UNIT BLOCK ADDRESS FOR 4595
*                                       FREEING STORAGE              **
*        FREEMAIN R,LV=164,A=(1)   FREE UB STORAGE
         FREEMAIN R,LV=164,A=(1)  FREE UNIT BLOCK STORAGE         II210
INVDTUB  OI    3(GRX),1                RESET UNIT BLOCK PTR        4595
         L     RD,TEMP       RELOAD REG MODIFIED IN GETABUF       II210
         LA    RX,16    RESET INCR REG MODIFIED IN INVERT         II210
NEXTCL   BXLE  GRX,RX,TESTOPEN
         B     NORMAL
         EJECT
*****
FINBLOCK STM   GRX,RJ,MSAVEA           ONLY OUTPUT
         TM    ABYTE,OUTPUT           OUTPUT?
         BCR   ZERO,RJ                NO
         TM    BBYTE,BLSW             BLK'G ?
         BCR   ZERO,RJ                NO
         TM    BBYTE,EOBSW            END OF BUF ?
         BCR   ONES,RJ                YES
         OI    BBYTE,EOBSW             SET BUF FINISHED
         SR    GRY,GRY
         TM    DCBRECFM,VFORM         VAR ?
         BZ    FBFIX                  NO
         SH    GRY,CON4                YES
FBFIX    A     GRY,RECPTR
         BC    NOTPLUS,FBRETURN       ANY REC USED? BR IF NO
*
         LH    RI,DCBBLKSI   SAVE DCB INFORMATION                 II210
         TM    DCBRECFM,VFORM         VAR ?
         BO    FBENDVAR               YES
*                                     NO
         STH   GRY,DCBBLKSI
         B     FBIO                BRANCH TO WRITE LAST RECORD    II210
FBENDVAR LM    RX,RD,BLKPTR
         SLL   RD,16         INSURE LAST 2 BYTES OF BDW WILL BE 0 II210
         ST    RD,0(RX)      STORE BIG LL                         II210
FBIO     BAL   RJ,DOWRITE    WRITE LAST RECORD                    II210
         STH   RI,DCBBLKSI   RESTORE DCB INFORMATION              II210
         BAL   RJ,INVERT     FLIP BUFFERS AND DECBS               II210
         BAL   RJ,DOCHECK                                         II210
         NOP   0                                                  II210
*
FBRETURN LM    GRX,RJ,MSAVEA
         BR    RJ
         EJECT
*****
SETSYN   EQU   *
         AIF   (&ERR EQ 1).FI18
         LA    1,218                                              25557
         CLC   BYTES(2),ERRMSG                                    25557
* IS CURRENT UNIT THE OBJECT ERROR UNIT?                          25557
         BE    WTP                 YES, ISSUE WTP                 25557
         B     COMERRHN                                           25557
WTP      L     1,ADERRNO           GET ADDR OF MESSAGE            25557
         MVC   0(4,1),MCSCODE1     MOVE LENGTH,MCSFLAGS           25557
         MVC   108(4,1),MCSCODE2   MOVE DESCRIPTOR,ROUTING CODES  25557
         WTO   MF=(E,(1))          ISSUE WTP                      25557
         B     COMERRHN                                           25557
         AGO   .FI19
.FI18    ANOP
         CLC   BYTES+1(1),ERRMSG  IS CURRENT DSRN THE OBJERR UNIT
         L     CSECT2R,VFIOCS2         GET ADDR OF 2ND. CSECT     25557
         BE    OFFNOBJ(0,CSECT2R)      YES,SET NO SUMMARY SWITCH  25557
*                                  AND TERMINATE                  25557
         LA    1,PRMS218           GET ADDRESS OF PARAMETER LIST  25557
         L     CSECT2R,VFIOCS2         GET ADDR OF 2ND. CSECT     25557
         BAL   RJ,OFFPSCMT(0,CSECT2R)  GIVE MESSAGE 218           25557
         B     ERRRET              ON RETURN IGNORE I/O REQUEST   25557
.FI19    ANOP
SETEOD   EQU   *
         AGO       .WATFIV1                                        V1L2
         L     GRY,IBCOMCON                                       19727
         L     GRX,ENDFILE(0,GRY)                                 19727
         LA    GRX,0(0,GRX)        IS THERE AN                    19727
         LTR   GRX,GRX             END= PARAMETER ?               19727
         BZ    NOEND               NO - WRITE MSG AND TRACEBACK   19727
         MVI   X'7C'(GRY),X'FF'                                   19727
         LR    14,GRX              YES - DON'T WRITE MSG OR       19727
         LM    0,13,132(GRY)       TRACEBACK                      19727
         L     14,0(0,14)          BRANCH TO                      19727
         BR    14                  ADDRESS SPECIFIED              19727
NOEND    EQU   *                                                  19727
.WATFIV1 ANOP                                                      V1L2
         AIF   (&ERR EQ 1).FI20
         LA    1,217                   GIVE MESSAGE 217
         B     COMERRHN
         AGO   .FI21
.FI20    ANOP
         MVC   PARAMS(1),ABYTE         SAVE ABYTE
         LA    1,PRMS217               GIVE MESSAGE 217
         L     CSECT2R,VFIOCS2         GET ADDR OF 2ND. CSECT
         BAL   RJ,OFFCMINT(0,CSECT2R)  GIVE MESSAGE 217
         L     14,RETCD                TEST RETURN CODE FIELD
         LTR   14,14                   IF =0 UPDATE FORTRAN SEQUENCE
         BNZ   ERRRET                  NUMBER ON DD CARD
         L     CSECT2R,VFIOCS2         GET ADDR OF 2ND. CSECT
         BAL   RI,OFFFRPL(0,CSECT2R)   IF =1 IGNORE REST OF I/O REQUEST
         B     EOFSOP
.FI21    ANOP
         EJECT
FIORET   EQU   *
         AIF   (&ERR EQ 0).FI200
         L     15,IBCOMCON             SAVE BUFFER ADDRESS AND
         LA    GRX,0(0,GRX)            ITS LENGTH
         CLI   SW1,1                   IF FOR OBJ. ERR. UNIT BRANCH
         BE    SPEC
         STM   GRX,GRY,BUFPTRS(15)     ELSE SAVE THEM
.FI200   ANOP
NORMAL   EQU   *
         L     13,4(0,13)              RESTORE SAVE ADDRESS WHERE REGS
         LM    14,1,12(13)             WERE SAVED AND RESTORE REGS
         LM    4,12,36(13)
         DROP  BASE
         USING FIOCSA,L
         AIF   (&ERR EQ 0).FI212
         CLI   SW1,1                   IF OBJ. ERR. UNIT BRANCH
         BE    *+8
.FI212   ANOP
         L     13,4(0,13)              ELSE RESTORE REG. 13 ALSO
         AIF   (&ERR EQ 0).FI213
         MVI   SW1,0                   RESET SPECIAL ENTRY SWITCH
.FI213   ANOP
         DROP  L
         USING FIOCSA,BASE
         LR    1,0
         B     6(0,L)                  NORMAL RETURN
         AIF   (&ERR EQ 0).FI300
SPEC     C     GRX,BUFPTRS(0,15)       IF BUFFER ADDRESS IS SAME AS CUR
         BNE   NORMAL                  RENT ONE THEN ZERO OUT BUFFER
         MVI   BUFPTRS(15),X'00'       ADDRESS, SINCE BUFFER CONTENTS
         MVC   BUFPTRS+1(7,15),BUFPTRS(15) ARE DESTROYED. ELSE RETURN
         B     NORMAL                  WITHOUT CHANGING ADDRESS
.FI300   ANOP
         EJECT
DCBBL    DCB   DSORG=PS,MACRF=(R,W),DDNAME=FTXXF001,                   X
               SYNAD=SYNAD,EODAD=EODAD,EXLST=EXLIST
         EJECT
*****
*
HERE     EQU   *
         ORG   DCBBL
UATCON   DC        V(UNIT@)                                        V1L2
*                                      ADDRESS CONSTANTS
*                                     USING FIRST PART OF DCB(ABOVE)
FIOSHCON DC    A(FIOCSA)
SAVECON  DC    A(LSAVEA)
IBCOMCON DC    V(IBCOM#)
         ORG   DCBBL+40
DDMSK    EQU   *
         ORG   HERE
         EJECT                                                    25557
*        MESSAGES                                                 25557
         AIF   (&ERR EQ 0).FI13                                   25557
MSG214   DC    F'83'                                              25557
         DC    C'IHC214I FIOCS - UNFORMATTED I/O, RECORD FORMAT SPECIFIX
               ED AS F, U OR V ON UNIT  '                         25557
         DC    XL4'40404040'                                      25557
MSG217   DC    F'44'                                              25557
         DC    C'IHC217I FIOCS - END OF DATA SET ON UNIT '        25557
         DC    XL4'40404040'                                      25557
MSG219   DC    F'44'                                              25557
         DC    C'IHC219I FIOCS - MISSING DD CARD FOR '            25557
DSNUM    DC    XL8'4040404040404040'                              25557
CODES    DC    X'0200'             DESCRIPTOR CODE                25557
         DC    X'0020'             ROUTING CODE                   25557
WTPCODE  DC    AL2(48)             MESSAGE LENGTH                 25557
         DC    X'8000'             MCSFLAGS FIELD                 25557
MSG220   DC    F'53'                                              25557
         DC    C'IHC220I FIOCS - UNIT NUMBER OUT OF RANGE. UNIT = '
         DC    XL4'40404040'                                      25557
MSG231   DC    F'80'                                              25557
         DC    C'IHC231I FIOCS - SEQUENTIAL I/O STATEMENTS USED FOR DIRX
               ECT ACCESS DATA SET   '                            25557
         DC    XL4'40404040'                                      25557
.FI13    ANOP                                                     25557
*****
         DS    0F
EXLIST   DC    X'85'                   '05' IS DCB EXIT INDICATION
         DC    AL3(IHCDCBXE)
         DC    X'87'                   '07' IS JFCB ADDR INDICATION
         DC    AL3(0)
*****
         TITLE 'IHCFIOS2 - O/S 360 - FORTRAN INPUT/OUTPUT'
IHCFIOS2 CSECT
FIOCS2   EQU   *
         USING *,CSECT2R
         USING FIOCSA,BASE
         USING IHCUAT,BASE2
         USING UB,UTR
         USING IHADCB,DCBR
         USING DECB,DECBR
         SPACE 3
*****
OPENED   EQU   *
         TM    BBYTE,VSWITCH      VARIABLE RECS?                  14452
         BCR   1,RJ               IF YES RETURN                   14552
         TM    PARAMS,X'F0'       IS REQUEST FORMATTED            14552
         BCR   1,RJ               IF YES RETURN                   14552
         TM    OPTION1,FCNTL      IS CURRENT OPERATION CONTROL    14552
         BCR   1,RJ               IF YES RETURN                   14552
         AIF   (&ERR EQ 1).FI372
         LA    1,214              PICK UP ERROR NUMBER            14552
         B     COMERRHN           ISSUE ERROR MESSAGE             14552
         AGO   .FI375
.FI372   ANOP
         LA    1,PRMS214          ERROR MESSAGE INDICATOR         14552
         LA    RJ,ERRRET                                          14552
         B     COMINTFC           GO TO PUT OUT THE ERROR         14552
.FI375   ANOP
         EJECT
*****
OPEN     EQU   *                                                   7897
         MVI   SAVEOPT,X'00'       ZERO OUT FIELD
         TM    OPTION1,FCNTL       IS CURRENT OPERATION CONTROL?
         BO    STOUTIN             YES, BR TO OPEN FOR OUTIN
         TM    PARAMS,OUTPUT       IS THIS AN OUTPUT REQUEST
         BO    STOUTIN             BR IF OUTPUT
*        OPEN  ((DCBR),INOUT)      OPEN FOR INOUT
         OPEN  ((DCBR),INOUT)      OPEN FOR INOUT
         B     CHKOPEN             CHECK THE OPEN
STOUTIN  EQU   *
*        OPEN ((DCBR),OUTIN)       OPEN FOR OUTIN
         OPEN ((DCBR),OUTIN)       OPEN FOR OUTIN
CHKOPEN  EQU   *
         AIF   (&ERR EQ 0).FI9325
         LA    RI,PRMS214+12       GET END OF LIST IND           A42666
         TM    0(RI),X'40'         IS THIS SPECIAL 214 CASE      A42666
         BO    SPEC214             YES, 214 IS TERMINAL ERROR    A42666
.FI9325  CLI   SAVEOPT,X'FF'       DID OPEN EXIT FIND ERROR      A42666
         AIF   (&ERR EQ 0).FI9
         BE    ERRRET              YES, BRANCH
         AGO   .FI10
.FI9     BNE   TSTOPEN             NO, BRANCH
         LA    1,214               YES, GIVE MESSAGE 214
         B     COMERRHN            ISSUE ERROR MESSAGE
.FI10    ANOP
TSTOPEN  EQU   *
         TM    DCBOFLGS,OPENBIT    IS DATA SET OPEN                4595
         BO    SETUBYTE            BR IF OPEN                      4595
         EJECT
         AIF   (&ERR EQ 1).FI206                                  25557
CHKNODD  EQU   *                                                  25557
*                                                                 25557
*                       IF NO DD CARD FOR ERROR MSG UNIT          25557
*                       THEN GO TO JOB TERMINATION                25557
*                                                                 25557
         CLC   BYTES+1(1),ERRMSG    IS THIS OBJ ERR UNIT?         25557
         BNE   NODD                 NO...                         25557
         MVC   MSG219+40(8),DCBDDNAM    MOVE DDNAME TO MSG        25557
*        WTO   MF=(E,MSG219)                                      25557
         WTO   MF=(E,MSG219)                                      25557
         L     15,IBCOMCON              GET ADDR OF IBCOM         25557
         BAL   14,68(0,15)              GO TERMINATE JOB          25557
         DC    AL2(16)                                            25557
         AGO   .FI4                                               25557
.FI206   ANOP                                                     25557
CHKNODD  EQU   *                                                  25557
*                                                                 25557
*                       IF NO DD CARD FOR ERROR MSG UNIT          25557
*                       THEN GO TO JOB TERMINATION                25557
*                                                                 25557
         CLC   BYTES+1(1),ERRMSG   IS THIS THE OBJ ERROR UNIT?    25556
         BNE   NODD                NO, BRANCH                     25557
         MVI   WTPSW,X'FF'         INDICATE 219 ERROR             25557
NOOBJERR MVI   SW1,0               TURN OFF OBJERR ENTRY SWITCH   25557
         L     1,VIHCERRM          GET ADDR IF ERROR MONITOR      25557
         MVI   19(1),X'FF'         SET NOP AT IHCERRM ENTRY+16 TO 25557
*                        INDICATE NO ERROR SUMMARY. THIS IS       25557
*                        DONE WHEN I/O ERROR OR NO DD ERROR       25557
*                        OCCURRED ON THE OBJECT ERROR UNIT        25557
*                        TERMINATE JOB                            25557
         CLI   WTPSW,X'00'         DID ERROR 218 OCCUR?           25557
         BE    ERR218              BRANCH IF YES                  25557
         MVC   DSNUM(8),DCBDDNAM                                  25557
         MVC   MSG219(4),WTPCODE                                  25557
*        WTO   MF=(E,MSG219)                                      25557
         WTO   MF=(E,MSG219)                                      25557
         B     TERM                                               25557
ERR218   L     1,PRMS218           GET ADDR OF GETMAINED AREA     25557
         MVC   0(4,1),WTP218       MOVE LENGTH,MCSFLAGS           25557
         MVC   108(4,1),CODES      MOVE DESC, ROUTE CODES         25557
*        WTO   MF=(E,(1))          ISSUE WTP                      25557
         WTO   MF=(E,(1))          ISSUE WTP                      25557
TERM     EQU   *                                                  25557
         L     15,IBCOMCON         GET ADDR OF IBCOM              25557
         BAL   14,68(0,15)         GO TERMINATE JOB               25557
         DC    AL2(16)                                            25557
NODD     LA    1,PRMS219           GIVE ERROR MESSAGE 219
         L     RJ,0(0,1)
         MVC   40(8,RJ),DCBDDNAM   MOVE DD NAME TO MESSAGE
         LA    RJ,ERRRET           SET RETURN POINT
         B     PSCMNTFC            PUT OUT MESSAGE
SPEC214  EQU   *                                                 A42666
         NI    12(RI),X'80'        RESET END OF LIST IND         A42666
         L     15,IBCOMCON         GET ADDR OF IBCOM             A42666
         BAL   14,68(0,15)         GO TO TERMINATE JOB           A42666
         DC    AL2(16)             RETCODE                       A42666
         AGO   .FI12
.FI4     ANOP
NODD     LA    1,219               GIVE MESSAGE 219
         B     COMERRHN            PUT OUT MESSAGE
.FI12    ANOP
         DS    0F
         EJECT
         AIF   (&ERR EQ 0).FI14
COMINTFC EQU   *
         LR    14,1                    GET PARAM. LIST ADDRESS
         L     3,0(0,1)                GET ADDRESS OF MESSAGE
         L     15,0(0,3)               GET LENGTH OF MESSAGE
         LA    3,0(15,3)               SET TO CONVERT DSRN INTO
         L     15,IBCOMCON             MESSAGE VIA THE CONVERSION RT
         LA    2,DSRNPTR
         CLI   SW1,1                   IF ENTRY FOR OBJ. ERR.UNIT GET
         BNE   *+8                     DSRN FROM DIFFERENT AREA
         LA    2,OBJPTR
         EX    0,82(0,15)
         BALR  0,1
         DC    XL2'0404'
         LR    1,14                    RESET PARAMETER LIST ADDRESS
PSCMNTFC EQU   *
         L     15,IBCOMCON             GET ADDRESS OF IBCOM
*
*        THIS CODE DOES NOT SET UP THE HIGH (I.E. NEXT) SAVE AREA
*        POINTER IN THE USER'S SAVE AREA BECAUSE THIS IS NOT NECESSARY
*        CURRENTLY IN TRACEBACK
*
         L     13,THRTNUSR(0,15)       GET USER'S REG 13
         MVC   12(16,13),FRTNUSR(15)   MOVE REGS 14-1 TO HIS AREA
         ST    13,IBCSV+4(0,15)        LINK SAVE AREAS
         LA    13,IBCSV(0,15)          GET IBCOMS SAVE AREA
         L     3,LSAVEA+4              SAVE CONTENTS OF LSAVEA+4 AND OF
         IC    0,PARAMS                PARAMS SINCE ERROR MONITOR WILL
*                                      USE FIOCS DURING ITS OPERATION
*
*   THIS ROUTINE DOES NOT ZERO THE RETURN CODE BECAUSE THE ERROR
*   WILL DO SO BEFORE GOING TO THE USER'S EXIT
*
         L     15,VIHCERRM             GET ADDR OF ERROR MONITOR
         BALR  14,15                   GO TO ERROR MONITOR
         STC   0,PARAMS                RESTORE PARAMS
         LA    13,LSAVEA               RESTORE REG. 13
         ST    3,LSAVEA+4              RESTORE LSAVEA+4
         BR    RJ                      RETURN
         EJECT
.FI14    ANOP
*
FREEPOOL TM    CBYTE,POOLSW
         BCR   ZERO,RI                 RETURN TO CALLER
*        FREEPOOL  (DCBR)
         FREEPOOL  (DCBR)
         NI    CBYTE,POOLOFF
         BR    RI                      RETURN TO CALLER
*
*                                      COME HERE FROM *FINIT*
*                                          OR *PRBKSP*
GETAPOOL EQU   *
         SR    0,0
         IC    0,DCBBUFNO
         SLL   0,16
         AH    0,DCBBLKSI             REG0.. BUFNO,LGTH (2 BYTES EACH)
*        GETPOOL  (DCBR),(0)
         GETPOOL  (DCBR),(0)
         BAL   RX,GETBUFFS            SET ADDR OF BUFS IN UB
         OI    CBYTE,POOLSW           INDICATE POOL ATTACHED
         BR    RJ                     RETURN TO CALLER
         EJECT
*****
PRBKSP   EQU   *                                                  II210
         CLI   OPTION1,X'03'       CURRENT OP CTRL?               II210
         BNE   NOBK                NO - BRANCH                    II210
         CLI   PARAMS,X'00'        CURRENT OP BKSP?               II210
         BNE   NOBK                NO - BRACNH                    II210
         TM    BBYTE,BLSW          BLOCKED RECORDS?               II210
         BCR   1,RJ                YES - RETURN                   II210
         B     POOLTST             BR TO TEST FOR BUFFERS         II210
NOBK     NI    DBYTE,X'BF'         SET OFF LOG BKSP SW            II210
         TM    BBYTE,BLSW          ARE RECORDS BLOCKED?           II210
         BZ    CTLTEST             NO - BRANCH                    II210
         CLI   OPTION1,X'00'       CURRENT OPERATION OUTPUT?      II210
         BNE   CTLTEST             NO - BR TO SET PHY BKSP SW OFF II210
         TM    PARAMS,X'0F'        IF OUTPUT DATA SET, BRANCH     II210
         BO    RBW2                TO TEST FOR NO. OF BKSP'S      II210
         TM    DBYTE,X'80'         HAS A PHYSICAL BKSP OCCURRED ? II210
         BO    SETBBYTE            YES - BRANCH                   II210
         NI    BBYTE,X'F7'         SET OFF EOB SW                 II210
         B     DEBLOCK             AND GO GET NEXT RECORD         II210
RBW2     TM    DBYTE,X'02' *       ENDFILE - BKSP - WRITE         27374
         BZ    RBW3                NO - BR TO CLEAR REST OF BUFF  24797
         NI    DBYTE,X'7D'         SET SWS OFF                    24797
         B     POOLTST             GO TEST FOR BUFFS              24797
RBW3     BAL   RJ,BLANK *          BLANK REST OF BUFFER           27374
RBW4     TM    DBYTE,X'80'         HAS PHY BKSP OCCURRED          24797
         BO    BK1                 YES - BRANCH                   II210
         CLI   DCBBUFNO,1          SINGLE BUFFERED ?              II210
         BE    BK1                 YES - BRANCH                   II210
         OI    DBYTE,X'10'         INDICATE NOT TO FREE BUFFS     II210
         BAL   RJ,INVERT           INVERT BUFFS                   II210
         BAL   RJ,DOCHECK          CHECK PREVIOUS READ            II210
         B     INV2                EODAD RETURN                   II210
         NI    DBYTE,X'EF'         SET SW OFF                     II210
         BAL   RJ,BKSPONE          GO BACKSPACE                   II210
INV2     BAL   RJ,INVERT           INVERT BUFFERS                 II210
BK1      BAL   RJ,BKSPONE          BACKSPACE ONE BLOCK            II210
         EJECT                                                    II210
SETBBYTE EQU   *                                                  II210
         NI    DBYTE,X'7F'         SET PHYS BKSP SW OFF           II210
         NI    BBYTE,X'20'         INITIALIZE BBYTE               II210
         TM    OPTION1,FCNTL       IS CURRENT OP CNTL?            26130
         BCR   1,RJ                RETURN IF YES                  26130
         B     SETA                BR TO INITIALIZE UB            II210
CTLTEST  NI    DBYTE,X'7F'         SET OFF PHY BK SW              II210
CKABYTE  EQU   *                                                  II210
         TM    BBYTE,BLSW          ARE RECORDS BLOCKED?           II210
         BO    SETBBYTE            YES - BRANCH                   II210
POOLTST  TM    CBYTE,POOLSW        ARE BUFFS ATTACHED?            II210
         BZ    STOR                NO - BRANCH                    II210
         TM    OPTION1,FCNTL       CONTROL OP?                    II210
         BCR   1,RJ                YES - RETURN                   II210
         B     SETABYTE            BRANCH TO INITIALIZE UB        II210
STOR     EQU   *                                                  II210
         ST    RJ,TEMP             SAVE REG 10                    II210
         L     CSECT2R,VFIOCS2     GET ADDR OF 2ND. CSECT         II210
         BAL   RJ,OFFGTPL(0,CSECT2R)   GO GET BUFFER POOL         II210
         L     RJ,TEMP             RESTORE REG 10                 II210
         TM    OPTION1,FCNTL       CURRENT OP CONTROL?            II210
         BCR   1,RJ                YES - RETURN                   II210
         B     SETUBYTE            BRANCH TO INITIALIZE UB        II210
         SPACE 3                                                  II210
BLANK    TM    PARAMS,FORMAT *     IS IT FORMATTED?               27374
         BCR   8,RJ *              DO NOT CLEAR FOR UNFORMATTED   27374
         L     RI,AREA *           GET THE AREA                   27374
         A     RI,RECPTR *         WHERE TO START BLANKS          27374
         MVI   0(RI),C' ' *        SET BLANK CHAR                 27374
         LH    GRY,DCBBLKSI *      GET BLKSIZE TO COMPUTE         27374
         S     GRY,RECPTR *        LENGTH OF BLANKS               27374
         BAL   RX,SUBT *           GO AND CLEAR                   27374
         BR    RJ *                RETURN TO CALLER               27374
         EJECT
SETBYTE  EQU   *
         TM    DCBRECFM,RECMSK     U FORMAT?                       4595
         BO    OPENRW              YES                             4595
         TM    DCBRECFM,VFORM      V FORMAT?
         BZ    *+8                 NO
         OI    BBYTE,VSWITCH       YES                            II210
         TM    DCBRECFM,BLOCKG     IS DS BLK'D?
         BZ    OPENRW              NO
         OI    BBYTE,BLSW          YES                            II210
         TM    BBYTE,EOBSW         AT END OF BUFFER?              II210
         BO    OPENRW              YES - BRANCH                   II210
         TM    ABYTE,OUTPUT        OUTPUT DATA SET?               II210
         BO    BLOCINIT            YES - INITIALIZE BLOCKING      II210
         B     INV1                BRANCH TO CHECK BUFNO          II210
*
OPENRW   TM    OPTION1,FCNTL          CONTROL OPERATION?.
         BO    CTLRTN                 IF YES, DO NOTHING
OPBK     EQU   *                      ENTRY FOR BACKSPACE         II210
         LA    DECBR,RWOFS            ADDRESS OF DECB1            II210
         ST    DECBR,DECBPTR                                      II210
         AIF   (&ERR EQ 1).FI757
         ST    DECBR,DECBPT
         AGO   .FI747
.FI757   ANOP
         CLI   SW1,1                                              23848
         BE    STR1                                               23848
         ST    DECBR,DECBPT
STR1     EQU   *                                                  23848
.FI747   ANOP
         MVI   LIVECT1,X'00'                                      26620
         MVI   LIVECT2,X'00'                                      26620
         CLI   DCBBUFNO,1              SINGLE BUFFERED?
         BE    *+8                     YES, BRANCH
         OI    BBYTE,BSW
         TM    OPTION1,FCNTL           CONTROL OPERATION?         II210
         BO    SPEC1                   YES - RETURN               II210
         TM    PARAMS,OUTPUT IS IT OUTPUT?
         BO    BUFSET        IF YES BR TO CLEAR BUFFERS           II210
         BAL   RJ,DOREAD     READ RECORD                          II210
INV1     CLI   DCBBUFNO,1    IS DATA SET SINGLE BUFFERED          II210
         BE    CR            IF YES DO NOT INVERT AND READ AHEAD  II210
         BAL   RJ,INVERT     FLIP BUFFERS AND DECBS               II210
         B     CR2           BRANCH TO READ AHEAD                 II210
BUFSET   DS    0H                                                 II210
         BAL   RX,SETBUF              ZERO/BLANK BUF,GRX& GRY SET
         TM    BBYTE,VBLSW            VAR OR BLK'D ?
         BZ    FIORET                 NEITHER, EXIT
         B     BLOCINIT               YES, RESET GRX & GRY
         EJECT
         DS    0H                                                 II232
SYNAD    EQU   *                                                  II232
         BALR  RD,0                    FREE SYNAD FROM FIOCS BASE II232
         USING *,RD                                               II232
         OI    BBYTE,EYES+ERR          INDICATE ERROR OCCURRED    II232
         TM    BBYTE,ETEST             IF ERROR TEST BIT IS OFF,   4659
         BCR   14,14                   THEN RETURN
         LR    RX,14                   SAVE RETURN REG. SINCE SVC TO BE
         AIF   (&ERR EQ 0).FI15        ISSUED
         ST    0,PRMS218+16            PUT DECB ADDRESS IN PARAMETER
         MVI   PRMS218+16,X'80'        LIST AND RESET LAST PARM. IND.
.FI15    ANOP
*        SYNADAF ACSMETH=BSAM
         SYNADAF    ACSMETH=BSAM                                   4659
         LR    RJ,1                    SAVE ADDR OF SYNADAF MSAGE  4659
*        GETMAIN R,LV=112
         GETMAIN R,LV=112
         AIF   (&ERR EQ 0).FI31
         ST    1,PRMS218               SAVE ADDRESS OF MESSAGE IN PRM.
         AGO   .FI30                   LIST.
.FI31    ST    1,ADERRNO               SAVE MESSAGE ADDRESS
.FI30    ANOP
         MVC   4(27,1),MSG218          MOVE HEADING PART OF MESSAGE
         MVC   31(77,1),50(RJ)         MOVE SYNADAF TEXT
         LA    RJ,104                  SET LENGTH OF MESSAGE
         ST    RJ,0(0,1)
*        SYNADRLS
         SYNADRLS                                                  4659
         BR    RX                      RETURN
MSG218   DC    C'IHC218I FIOCS - I/O ERROR  '                     II232
         DROP  RD                                                 II232
         EJECT
         DS    0H
EODAD    EQU   *
         L     5,VFIOCS2           SET UP REG PROPERLY
         USING FIOCS2,CSECT2R
         ST    RJ,SAVE             SAVE PRIMARY LINK REGISTER   **
         BAL   RI,TCLS1            BRANCH TO TCLOSE DATA SET      II210
         MVI   LIVECT1,X'00'                                      26620
         MVI   LIVECT2,X'00'                                      26620
         TM    DBYTE,X'10'         WANT TO FREE BUFS?             II210
         BZ    SETC                YES - BRANCH                   II210
         NI    DBYTE,X'EF'         SET SW OFF                     II210
         B     BKRET               RETURN                         II210
         EJECT
SETC     EQU   *                                                  II210
         NI    CBYTE,CLSET         RECORD THIS IN THOSE BITS
         BAL   RI,FREEPOOL         FREE THE BUFFERS
BKRET    L     RJ,SAVE             RESTORE RETURN REGISTER        II210
         BR    RJ                  AND RETURN
         DROP  5
         EJECT
*****
IHCDCBXE DS    0H                                                 II210
         BALR  3,0
         USING *,3
         USING DEFAULTS,GRX                                        4595
         LA    RD,FMTST4                                          II232
FMTST2   TM    PARAMS,X'F0'        IS THIS A FORMATTED REQUEST     SIR1
         BCR   ON,RD               YES, BRANCH.                   II232
         TM    OPTION1,FCNTL                                      11798
         BCR   ON,RD               YES,BRANCH                     II232
         CLI   DCBRECFM,0          IS FIELD=0                     II232
         BNE   TUMMY               NO, BRANCH                     II232
         OI    DCBRECFM,VSFORM     RECFM=VS BY DEFAULT            II232
         BR    RD                  BRANCH                         II232
TUMMY    EQU   *                                                  II232
         TM    DCBRECFM,RECMSK     RECFM=U?                       II232
         AIF   (&ERR EQ 0).FI23205                                II232
         BO    BADRFM1             YES,BRANCH.                    II232
         AGO   .FI23206                                           II232
.FI23205 ANOP                                                     II232
         BO    ERRSET              YES,BRANCH                     II232
.FI23206 ANOP                                                     II232
         TM    DCBRECFM,VSFORM     RECFM=VS(B)?                   II232
         BCR   ON,RD               YES,BRANCH                     II232
         TM    PARAMS,OUTPUT       OPENED OUTIN                   II232
         AIF   (&ERR EQ 0).FI23201                                II232
         BO    BADRFM1             YES, BRANCH                    II232
         AGO   .FI23204                                           II232
.FI23201 ANOP                                                     II232
         BO    ERRSET              YES,BRANCH                     II232
.FI23204       ANOP                                               II232
         TM    DCBRECFM,VFORM      RECFM=V?                       II232
         AIF   (&ERR EQ 0).FI23202                                II232
         BZ    BADRFM1             NO,BRANCH                      II232
         AGO   .FI23203                                           II232
.FI23202 ANOP                                                     II232
         BZ    ERRSET              NO,BRANCH                      II232
.FI23203 ANOP                                                     II232
         B     FMTST5              BRANCH                         II232
         AIF   (&ERR EQ 0).FI11                                   II232
BADRFM1  EQU   *                                                  II232
         BAL   RI,CALLM214         GIVE ERR MSG 214               II232
         LTR   1,1                 STANDARD RECFM FIXUP DESIRED?  II232
         BNZ   ERRSET              NO,BRANCH                      II232
         TM    DCBRECFM,RECMSK     RECFM=U?                       II232
         BO    MAKEVS              YES,BRANCH.                    II232
         TM    DCBRECFM,VFORM      RECFM=V?                       II232
         BO    FMTST5              YES, BRANCH                    II232
MAKEVS   EQU   *                                                  II232
         MVI   DCBRECFM,VSFORM     DEFAULT VALUE FOR UNFORMATTED  II232
         BR    RD                                                 II232
         SPACE 3                                                  II232
CALLM214 EQU   *                                                  II232
         LA    1,PRMS214               GIVE ERROR MESSAGE 214
         LR    RX,GRX                  SAVE REG.
         ST    14,SAVE                 SAVE REG.
         ST    3,VRETEMP               SAVE REG 3                 32775
         L     CSECT2R,VFIOCS2         GET ADDR OF 2ND. CSECT
         BAL   RJ,OFFCMINT(0,CSECT2R)  GO PUT OUT ERROR
         L     3,VRETEMP               RESTORE REG 3              32775
         L     14,SAVE                 RESTORE REG.
         LR    GRX,RX                  RESTORE REG.
         L     1,RETCD                 YES TEST RETURN CODE FIELD
         BR    RI                                                 II232
         SPACE 3                                                  II232
.FI11    ANOP
ERRSET   EQU   *
         MVI   SAVEOPT,X'FF'           INDICATE ERROR FOUND, SKIP I/O
         BR    RD                                                 II232
FMTST5   OI    DCBRECFM,SFORM          INDICATE SPANNED           II232
FMTST4   LA    GRX,4(0,GRX)                                        4595
         SR    RI,RI                                               4595
TBLKSI   CH    RI,DCBBLKSI             IS FIELD = 0                4595
         BNE   TRECFM                  NO                          4595
         MVC   DCBBLKSI,BLKSIZE        YES,MOVE IN DEFAULT         4595
TRECFM   CLI   DCBRECFM,0              IS FIELD = 0                4595
         BNE   TBUFNO                  NO                          4595
         MVC   DCBRECFM,RECFM          YES,MOVE IN DEFAULT         4595
TBUFNO   CLI   DCBBUFNO,1              SINGLE BUFFERED?           II210
         BE    TLRECL                  YES - BRANCH TO NEXT TEST  II210
         MVI   DCBBUFNO,2              SET DEFAULT BUFNO          II210
         MVI   DCBNCP,2            SET DEFAULT N0. OF CHAN PROGS  II210
TLRECL   CH    RI,DCBLRECL             IS FIELD = 0                4595
         BNE   DEFOUT                  NO                          4595
         MVC   DCBLRECL,LRECL          YES,MOVE IN DEFAULT         4595
DEFOUT   LH    RJ,DCBBLKSI             LOAD BLOCK SIZE             9922
         TM    DCBRECFM,X'80'          IS RECORD FORMAT F OR U ?   9922
         BO    DEFOUT1A                YES                         9922
         SH    RJ,CON4                 REDUCE MAXIMUM LRECL VALUE  9922
DEFOUT1A CH    RI,DCBLRECL             IS DCB LRECL ZERO?          9922
         BE    DEFOUT1B                YES                         9922
         TM    PARAMS,FORMAT       FORMATTED READ/WRITE?          II232
         BO    DEEF                YES, BRANCH.                   II232
         TM    DCBRECFM,VSFORM     VARIBLE-LENGTH-SPANNED?        II232
         BO    DEFOUT2             YES, BRANCH                    II232
DEEF     EQU   *                                                  II232
         CLC   DCBLRECL,XLRECL     IS DEFAULT LRECL=X?            II232
         BE    DEFOUT1B            YES, BRANCH.                   II232
         TM    DCBRECFM,RECMSK     U TYPE RECORDS                 23855
         BO    DEFOUT1B            YES - BRANCH                   23855
         CH    RJ,DCBLRECL             IS DCB LRECL GT MAXIMUM?    9922
         BNL   DEFOUT2                 NO                          9922
DEFOUT1B STH   RJ,DCBLRECL             ADJUST LRECL SIZE           9922
DEFOUT2  TM    CBYTE,OPENSW            IS DATA SET OPENED          4595
         BCR   8,14                    NO,BIT OFF                  4595
         OI    CBYTE,CONCAT            YES,MUST BE CONCATENATION   4595
         BR    14                                                  4595
         DROP  GRX                                                 4595
         DROP  3
         EJECT
****
****
*        EQUATES FOR BRANCHES INTO THE 2ND. CSECT
OFFGTPL  EQU   GETAPOOL-FIOCS2
OFFFRPL  EQU   FREEPOOL-FIOCS2
OFFOPNED EQU   OPENED-FIOCS2
OFFOPEN  EQU   OPEN-FIOCS2
OFFSTBYT EQU   SETBYTE-FIOCS2
OFFOPBK  EQU   OPBK-FIOCS2
OFFPRBKS EQU   PRBKSP-FIOCS2
OFFBLANK EQU   BLANK-FIOCS2
         AIF   (&ERR EQ 0).FIKKK
OFFNOBJ  EQU   NOOBJERR-FIOCS2
OFFCMINT EQU   COMINTFC-FIOCS2
OFFPSCMT EQU   PSCMNTFC-FIOCS2
.FIKKK   ANOP
*****
IHCUAT   DSECT
BYTES    DS    H'0'
ENDT     DS    AL2(0)
ERRMSG   DS    X'00'
READ     DS    X'00'
PRINT    DS    X'00'
PUNCH    DS    X'00'
*
UTENTRY  DS    A                                                   4595
DEFAULTS DSECT                                                     4595
DEFBITS  DS    X'00'                                               4595
         DS    X'00'                                               4595
BLKSIZE  DS    XL2'00'                                             4595
RECFM    DS    X'00'                                               4595
BUFNO    DS    X'00'                                               4595
LRECL    DS    XL2'00'                                             4595
         SPACE 3
******
         SPACE 3
*****
UB       DSECT
ABYTE    DS    X'00'
BBYTE    DS    X'00'
CBYTE    DS    X'00'
DBYTE    DS    X'00'                                              II210
BUF1     DS    F
BUF2     DS    F
BLKPTR   DS    F
RECPTR   DS    F
*
DECBPTR  DS    F             POINTER TO LAST USED DECB            II210
BUFMASK  DS    F             MASK FOR FLIPPING BUFS VIA EXC OR    II210
RWOFS    DS    0F            BEGINNING OF DECB AREA               II210
DECB1    DS    0F                                                 II210
         DS    2F                                                 II210
DCBOFFS  DS    F             DCB ADDRESS                          II210
AREA1    DS    F             ADDRESS OF BUFFER1                   II210
         DS    F                                                  II210
WKBK1    DS    XL3'00'       WORK AREA FOR BACKSPACE ROUTINE      II210
LIVECT1  DS    X'00'         SWITCH INDICATING IF DECB IS ACTIVE  II210
DECB2    DS    0F                                                 II210
         DS    3F                                                 II210
AREA2    DS    F             ADDRESS OF BUFFER2                   II210
         DS    F                                                  II210
WKBK2    DS    XL3'00'       WORK AREA FOR BACKSPACE ROUTINE      II210
LIVECT2  DS    X'00'         SWITCH INDICATING IF DECB IS ACTIVE  II210
DOFFSET  DS    0F
*
         DS    22F           DCB AREA                             II210
         EJECT
*****
         DCBD  DSORG=PS                                            4595
         SPACE 3
*****
DECB     DSECT
ECB      DS    F
DECBIO   DS    X'00'
IOTYPE   DS    X'00'
LN       DS    H
DCBAD    DS    F
AREA     DS    F
STATLOC  DS    F
         DS    XL3'00'                                            II210
LIVECT   DS    X'00'                                              II210
         END
