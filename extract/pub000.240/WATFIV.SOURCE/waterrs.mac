         PRINT  ON,NOGEN
         COPY      OPTIONS
&ERROPT  SETC      'FULL'
         EQUREGS
         DCBD      DSORG=PS,DEVD=DA
*
ERROR    CSECT
*        INITIALIZATION CODING
GENTEXTS STM       R14,R12,12(R13)
         BALR      R11,0
         USING     *,R11
         LA        R15,SAVE
         ST        R15,8(,R13)
         ST        R13,4(,R15)
         LR        R13,R15
*        DETERMINE THE MAX ERRTEXT LENGTH
         SR        R1,R1
         SR        R3,R3
         SR        R5,R5
         LA        R4,1
SCANLOOP LA        R2,ERRTEXT(R5)      ADDRESS OF L'TEXT
         C         R2,=A(ENDTEXTS)     ARE WE AT END
         BE        SETBLKSI
         IC        R1,0(,R2)           PUT LENGTH IN R1
         AR        R5,R4
         AR        R5,R4
         AR        R5,R1
         CR        R1,R3
         BNH       SCANLOOP
         LR        R3,R1               LOAD MAX SO FAR
         B         SCANLOOP
*
*        SET THE LRECL AND BLKSI
SETBLKSI LA        R3,2(R3)
         LA        R3,132              ASSUME BLKSIZE=132
         STH       R3,DCBLRECL+OUT-IHADCB
         STH       R3,DCBBLKSI+OUT-IHADCB
*
*        OUTPUT THE ERRTEXT
         OPEN      (OUT,(OUTPUT),PRINT,(OUTPUT))
         LA        R2,ERRTAB
         SR        R3,R3
         SR        R5,R5
         LA        R6,1
         LH        R7,ERRCOUNT
         LA        R8,2
OUTLOOP  BXH       R5,R6,ALLDONE
         MVI       BUFF+1,C' '
         MVC       BUFF+2(255),BUFF+1
         LH        R4,0(,R2)
         LA        R4,ERRTEXT(R4)
         IC        R3,0(,R4)
         EX        R3,MOVETEXT         LENGTH OF TEXT IS IN R3
         STC       R3,BUFF             LENGTH AT BEGINNING OF EACH TEXT
         PUT       OUT,BUFF
         AR        R2,R8
         B         OUTLOOP
ALLDONE  LH        R1,DCBBLKSI+OUT-IHADCB
         CVD       R1,DBLE
         MVC       PRBLKSI(4),MASK
         ED        PRBLKSI(4),DBLE+6
         LH        R1,ERRCOUNT
         CVD       R1,DBLE
         MVC       PRCOUNT(4),MASK
         ED        PRCOUNT(4),DBLE+6
         PUT       PRINT,PRLINE1
         PUT       PRINT,PRLINE2
         CLOSE     (OUT,,PRINT)
*
         L         R13,4(R13)
         LM        R14,R12,12(R13)
         SR        R15,R15
         BR        R14
*
MOVETEXT MVC       BUFF+1(*-*),1(R4)
DBLE     DS        D
*AXSOFAR DC        F'0'
ERRCOUNT DC        AL2((TABEND-ERRTAB)/2)
SAVE     DS        18F
BUFF     DS        CL257
OUT      DCB       DSORG=PS,MACRF=PM,RECFM=FS,OPTCD=C,DDNAME=WATERRS,  X
               BUFNO=3
PRINT    DCB       DSORG=PS,DDNAME=SYSPRINT,RECFM=FA,BLKSIZE=100,      C
               LRECL=100,MACRF=PM
PRLINE1  DC        C'0 BLOCK SIZE IS '
PRBLKSI  DC        CL84' '
PRLINE2  DC        C'0 NUMBER OF ERROR TEXTS IS '
PRCOUNT  DC        CL75' '
MASK     DC        X'40202021'
         LTORG
ERRTAB   DS        0H
         ERRTABLE
TABEND   EQU       *
ERRTEXT  EQU       *
         DC        AL1(39)
         DC        CL40'  RESERVED FOR FUTURE USE '
         COPY      ERRTEXTS
         END
