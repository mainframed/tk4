LAT2   TITLE   'ARCTANGENT FUNCTION (LONG, 2 ENTRY POINTS)'
         PRINT     NOGEN
         DISK
         PRINT     GEN
         SPACE 2
DATAN#   CSECT
*HJNX 013600,0126-0130,0210-0234                                  18303
*
*        ARCTANGENT FUNCTION   (LONG, 2 ENTRY POINTS)
*              1. REDUCE THE CASE TO THE 1ST OCTANT BY USING
*                   ATAN(-X)=-ATAN(X), ATAN(1/X)=PI/2-ATAN(X)
*              2. REDUCE FURTHER TO THE CASE /X/ LESS THAN TAN(PI/12)
*                   BY ATAN(X)=PI/6+ATAN((X*SQRT3-1)/(X+SQRT3)).
*              3. FOR THE BASIC RANGE (X LESS THAN TAN(PI/12)), USE
*                   A CONTINUED FRACTION APPROXIMATION
*              4. DATAN2 ENTRY REQUIRES A PAIR OF ARGUMENTS Y AND X,
*                   AND COMPUTES ATAN(Y/X).   ANSWER IS IN (-PI,PI)
       SPACE
         ENTRY     ATAN
         ENTRY     ATAN2
       ENTRY   DATAN
       ENTRY   DATAN2
       SPACE
GR0    EQU     0               SCRATCH REGISTERS
GR1    EQU     1
GR2    EQU     14
GRA    EQU     1               ARGUMENT POINTER
GRS    EQU     13              SAVE AREA POINTER
GRR    EQU     14              RETURN REGISTER
GRL    EQU     15              LINK REGISTER
FR0    EQU     0               ANSWER REGISTER
FR2    EQU     2               SCRATCH REGISTERS
FR4    EQU     4
FR6    EQU     6
       SPACE
ATAN     CALLDUBL  DATAN,1
         SPACE     1
ATAN2    CALLDUBL  DATAN2,2,TWO ARGUMENTS
         SPACE     1
DATAN2   FENTZ
         SPACE     1
         STM       GRR,GR0,12(GRS)     SAVE REGISTERS
         SPACE     1
         CHCKA     0(GRA),REAL8
         SPACE     1
         L         GR2,0(,GRA)
         CHCKDEF   REAL8,0(GR2)
         SPACE     1
         CHCKA2    4(GRA),REAL8
         SPACE     1
         L         R3,4(GRA)
         CHCKL     8(GRA),REAL8
         SPACE     1
         CHCKDEF   REAL8,0(R3)
         SPACE     1
       MVI     XFLAG,X'80'       PRESET XFLAG TO MINUS
         LA        GRL,DATAN-DATAN2(GRL)
         USING     DATAN+4,GRL
         B         MERGE
         SPACE     1
DATAN    FENTZ
         SPACE     1
         STM       GRR,GRL,12(GRS)     SAVE REGISTERS
         CHCKA     0(GRA),REAL8
         L         GR2,0(,GRA)
         CHCKDEF   REAL8,0(GR2)
         SPACE     1
         CHCKL     4(GRA),REAL8
         SPACE     1
       MVI     XFLAG,X'00'       PRESET XFLAG TO PLUS
       SPACE
MERGE    EQU       *
       LD      FR0,0(GR2)      OBTAIN 1ST (OR ONLY) ARGUMENT X1
       L       GR0,0(GR2)        SAVE ITS SIGN
       LPER    FR0,FR0         FORCE SIGN POSITIVE
       TM      XFLAG,X'80'
       BC      8,ATAN1         IF ATAN ENTRY, SKIP TO MAIN CIRCUIT
       SPACE
         LD        FR6,0(R3)       R3 SET ABOVE
       LPDR    FR2,FR6         FORCE SIGN POSITIVE
       BC      8,VERTL         IF X2=0, OR IF /X1/X2/ IS GREATER
       STE     FR0,XFLAG         THAN 16**14, GIVE + OR -PI/2 AS
       L       GR2,XFLAG           ANSWER TO AVOID IBFINT TROUBLE
       STE     FR2,XFLAG
       S       GR2,XFLAG
       STE     FR6,XFLAG       SAVE SIGN OF X2
       C       GR2,LIM1
       BC      2,VERTL1
       LTER    FR6,FR6         IF X2 GRT 0, GIVE NOISY UNDERFLOW
       BC      10,NORML
       C       GR2,LIM2
       BC      10,NORML
       SDR     FR0,FR0         IF X2 LST 0 AND /X1/X2/ LST 16**-14,
       BC      15,XNEG           GIVE + OR - PI AND AVOID UNDERFLOW
       SPACE
VERTL  LTER    FR0,FR0
       BC      8,ERROR           IF X1=X2=0, ERROR
VERTL1 LD      FR0,ONE         IF X2=0 AND X1 NOT =0, OR /X1/X2/
       SD      FR0,MPO2P1        GRT 16**6, GIVE + OR - PI/2
       BC      15,STEST
       SPACE
NORML  DDR     FR0,FR2         JOIN MAIN CIRCUIT WITH X=X1/X2
       SPACE
ATAN1  LD      FR4,ONE         MAIN CIRCUIT
       SR      GR1,GR1         GR1, GR2 FOR DISTINGUISHING CASES
       LA      GR2,ZERO
       CER     FR0,FR4
       BC      12,SKIP1
       LDR     FR2,FR4         IF X GREATER THAN 1, TAKE INVERSE
       DDR     FR2,FR0           AND INCREMEMENT GR1 BY 16
       LDR     FR0,FR2
       LA      GR1,16
       SPACE
SKIP1  CE      FR0,SMALL       IF ARG LESS THAN 16**-7, ANS=ARG.
       BC      12,READY          THIS AVOIDS UNDERFLOW EXCEPTION
       CE      FR0,TAN15
       BC      12,SKIP2
       LDR     FR2,FR0         IF X GREATER THAN TAN(PI/12),
       MD      FR0,RT3M1         REDUCE X TO (X*SQRT3-1)/(X+SQRT3)
       SDR     FR0,FR4         COMPUTE X*SQRT3-1 AS
       ADR     FR0,FR2           X*(SQRT3-1)-1+X
       AD      FR2,RT3             TO GAIN ACCURACY
       DDR     FR0,FR2
       LA      GR2,8(GR2)      INCREMENT GR2 BY 8
       SPACE
SKIP2  LDR     FR6,FR0         COMPUTE ATAN OF REDUCED ARGUMENT BY
       MDR     FR0,FR0           ATAN(X) = X+X*X**2*F, WHERE
       LD      FR4,C7              F = C1+C2/(X**2+C3+C4/
       ADR     FR4,FR0               (X**2+C5+C6/(X**2+C7)))
       LD      FR2,C6
       DDR     FR2,FR4
       AD      FR2,C5
       ADR     FR2,FR0
       LD      FR4,C4
       DDR     FR4,FR2
       AD      FR4,C3
       ADR     FR4,FR0
       LD      FR2,C2
       DDR     FR2,FR4
       AD      FR2,C1
       MDR     FR0,FR2
       MDR     FR0,FR6
       ADR     FR0,FR6
       SPACE
READY  AD      FR0,0(GR1,GR2)  DEPENDING ON THE CASE,
       LNR     GR1,GR1           EITHER ADD 0 OR PI/6, OR
       SD      FR0,ZERO(GR1)       SUBTRACT FROM PI/3 OR PI/2
       LPER    FR0,FR0         DO THE LATTER IN TWO STEPS
       TM      XFLAG,X'80'
       BC      8,STEST         IF DATAN2 ENTRY AND X2 IS NEGATIVE
XNEG   SD      FR0,PI            SUBTRACT FR0 FROM PI
       LPER    FR0,FR0
       SPACE
STEST  LTR     GR0,GR0         SIGN OF ANS SHOULD AGREE
       BC      10,*+6            WITH SIGN OF ARG
       LCER    FR0,FR0
       SPACE
EXIT   L       GRR,12(GRS)
         SDR       FR6,FR6         ***WATFIV CONVENTION***
       BCR     15,GRR
       SPACE
       SPACE
*   LI-D   BOTH ARGUMENTS OF DATAN2 ARE ZERO
ERROR    $ERROR    (TRAZ,LI,D)                                     V1L5
         EJECT
       DS      0D
C1     DC      X'BF1E31FF1784B965'    -0.7371899082768562E-2
C2     DC      X'C0ACDB34C0D1B35D'    -0.6752198191404210
C3     DC      X'412B7CE45AF5C165'     0.2717991214096480E+1
C4     DC      X'C11A8F923B178C78'    -0.1660051565960002E+1
C5     DC      X'412AB4FD5D433FF6'     0.2669186939532663E+1
C6     DC      X'C02298BB68CFD869'    -0.1351430064094942
C7     DC      X'41154CEE8B70CA99'     0.1331282181443987E+1
PI     DC      X'413243F6A8885A31'
RT3M1  DC      X'40BB67AE8584CAA8'     SQRT(3)-1
ONE    DC      X'4110000000000000'             THESE
RT3    DC      X'411BB67AE8584CAB'     SQRT(3)   SIX
ZERO   DC      D'0'                    0           CONSTANTS
       DC      X'40860A91C16B9B2C'     PI/6          MUST
MPO2P1 DC      X'C0921FB54442D184'     -PI/2+1         BE
       DC      X'BFC152382D736574'     -(PI/3-F)+1       CONSECUTIVE
LIM1   DC      X'0E000000'
LIM2   DC      X'F2000000'
SMALL  DC      X'3A100000'
XFLAG  DS      F
TAN15  DC      X'40449851'
       END
