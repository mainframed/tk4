CLSR   TITLE   'COMPLEX SQUARE ROOT FUNCTION (LONG)'
         PRINT     NOGEN
         DISK
         PRINT     GEN
         SPACE 2
CDSQT#   CSECT
*
*      COMPLEX SQUARE ROOT FUNCTION (LONG)
*              1. THE PRINCIPLE BRANCH OF THE SQUARE ROOT IS TAKEN,
*                   I.E., THE REAL PART OF THE ANSWER IS POSITIVE.
*              2. WRITE SQRT(X+IY) = U+IV, WHERE U IS REAL, AND V IS
*                   IMAGINARY. IF X=Y=0, U=V=0.
*              3. IF X IS NON-NEGATIVE, U = SQRT((/X/+/X+IY/)/2) AND
*                   V = Y/(2*U).
*              4. IF X IS NEGATIVE, U = Y/(2*V) AND
*                   V = SIGN(Y)*SQRT((/X/+/X+IY/)/2).
       SPACE
       EXTRN   DSQRT
       ENTRY   CDSQRT
         ENTRY     CSQRT
       SPACE
GRA    EQU     1               ARGUMENT POINTER
GRB    EQU     3               TEMPORARY BASE REGISTER
GRS    EQU     13              SAVE AREA POINTER
GRR    EQU     14              RETURN REGISTER
GRL    EQU     15              LINK REGISTER
GR0    EQU     0               SCRATCH REGISTERS
GR1    EQU     1
GR2    EQU     2
FR0    EQU     0               ANSWER REGISTERS
FR2    EQU     2
FR4    EQU     4               SCRATCH REGISTERS
FR6    EQU     6
ISNSQ1 EQU     X'304'          IDENTIFIER NUMBER FOR 1ST DSQRT CALL
ISNSQ2 EQU     X'306'          IDENTIFIER NUMBER FOR 2ND DSQRT CALL
       SPACE
CSQRT    CALLDCL   CDSQRT,3
         SPACE     1
CDSQRT   FENT      (XCDSQ,COMPL16),2
         SPACE     1
         LD        FR6,XCDSQ
         LD        FR2,XCDSQ+8
       LPER    FR6,FR6         FORCE SIGNS POSITIVE
       LPER    FR2,FR2
       LDR     FR4,FR6         SAVE /X/ IN FR4
       CDR     FR6,FR2         A = MAX(/X/,/Y/), B = MIN(/X/,/Y/)
       BC      10,READY        IF NECESSARY SWITCH REGISTERS SO THAT
       LDR     FR6,FR2           IN ALL CASES FR6 CONTAINS A AND FR2
       LDR     FR2,FR4             CONTAINS B
READY  LDR     FR0,FR6         IF CHARACTERISTICS ARE DIFFERENT BY
       SWR     FR0,FR2           14 OR MORE, OR B=0, JUMP.
       CDR     FR0,FR6             THIS IS DONE TO  AVOID ANY
       BC      8,PURE                INTERMEDIATE UNDERFLOW
       DDR     FR2,FR6         D = B/A
       HDR     FR2,FR2         TAKE SQRT OF 0.25+D*D/4
       MDR     FR2,FR2           THIS IS A MORE ACCURATE PROCEDURE
       AW      FR2,QUART           THAN SQRT OF 1+D*D
       STD     FR2,BUFF
         FCALL     (DSQRT,REAL8),(BUFF,REAL8)
         LD        FR2,XCDSQ
       LPER    FR2,FR2         /X/
       CE      FR2,LLIM
       BC      4,XSMALL        IF A IS VERY SMALL, AVOID PREMATURE
       HDR     FR2,FR2           UNDERFLOW BY SPECIAL HANDLING
       MDR     FR0,FR6         (/X/)/2 IN FR2, A*F IN FR0
       CE      FR0,HLIM        IF A IS NEAR OVERFLOW THRESHOLD,
       BC      10,ABIG           ALSO GIVE SPECIAL TREATMENT
JOIN1  LD      FR6,ONE
JOIN2  ADR     FR0,FR2         (/X/)/2+A*F, (/X/)+2*A*F,
       STD     FR0,BUFF          OR (/X/)/4+A*F/2 NOW READY
         FCALL     (DSQRT,REAL8),(BUFF,REAL8)
         SPACE     1
         LD        FR2,XCDSQ+8
         LE        FR4,XCDSQ
       MDR     FR0,FR6         SQRT((/X/+/X+IY/)/2) READY IN FR0
       BC      8,EXIT          IF FR0 IS 0, THEN X=Y=0
       DDR     FR2,FR0
       HDR     FR2,FR2
       LTER    FR4,FR4
       BC      10,EXIT         IF X GRT OR = 0, ANSWER IS READY
       LDR     FR4,FR0         IF X LST 0, GIVE V SAME SIGN AS Y
       LPDR    FR0,FR2
       LTER    FR2,FR2
       BC      10,*+6
       LNER    FR4,FR4
       LDR     FR2,FR4
       SPACE
EXIT     SDR       FR6,FR6         ***WATFIV CONVENTION***
         B         XFRET
         SPACE     1
XSMALL ADR     FR6,FR6         CASE WHEN X IS VERY SMALL, OR SMALL
       MDR     FR0,FR6           RELATIVE TO Y. /X/ IN FR2, 2*A*F IN
JOIN3  LD      FR6,R2OV2           FR0, 1/SQRT2 IN FR6
       BC      15,JOIN2
       SPACE
ABIG   HDR     FR0,FR0         CASE WHEN A IS VERY NEAR OVERFLOW
       HDR     FR2,FR2           THRESHHOLD. (/X/)/4 IN FR2, A*F/2
       LD      FR6,R2OV2           IN FR0
       ADR     FR6,FR6         SQRT2 IN FR6
       BC      15,JOIN2
       SPACE
PURE   CDR     FR0,FR4         CASE WHEN B IS NEGLIGIBLE RELATIVE TO A
       BC      8,JOIN1           A=X, PURE REAL, A=(/X/+/X+IY/)/2
       BC      15,JOIN3          A=Y, PURE IMAG, A=/X/+/X+IY/
       SPACE
       DS      0F
XCDSQ    DS        2D
HLIM   DC      X'7F800000'
BUFF   DS      D
QUART  DC      X'4040000000000001'     0.25 WITH ROUNDING FUDGE
ONE    DC      X'4110000000000000'
R2OV2  DC      X'40B504F333F9DE65'     1/SQRT2
LLIM   DC      X'00200000'
       END
