 TITLE ' GAMA GAMMA-LOGGAMMA (LONG) '
         PRINT     NOGEN
         DISK
         PRINT     GEN
         SPACE 2
DGAMA#   CSECT
*      LONG FORM GAMMA-LOGGAMMA FUNCTIONS
*        1. GAMMA(X) = INTEGRAL FROM 0 TO +NF OF T**(Z-1)*E**(-T)*DT
*             X MUST LIE BETWEEN 2**(-252) AND 57.5744
*        2. LOGGAMMA(X) = LOG(GAMMA(X))
*             X MUST BE POSITIVE AND BE LESS THAN 4.2937*10**73
       SPACE
       ENTRY   DGAMMA
       ENTRY   DLGAMA
       EXTRN   DEXP
       EXTRN   DLOG
         ENTRY     ALGAMA
         ENTRY     GAMMA
       SPACE
GR0    EQU     0             DUMMY REGISTER, MAY BE CLOBBERED
GRA    EQU     1             ARGUMENT LIST POINTER
GR2    EQU     2             MAIN BASE REGISTER
GR3    EQU     3             INDEX REGISTERS
GR4    EQU     4
GR5    EQU     5
GRS    EQU     13            SAVE AREA POINTER
GRR    EQU     14            RETURN REGISTER
GRL    EQU     15            LINK REGISTER
FR0    EQU     0             ANSWER REGISTER
FR2    EQU     2             SCRATCH REGISTERS
FR4    EQU     4
FR6    EQU     6
ISN1   EQU     X'10A'        IDENTIFIER CODE FOR 1ST DLOG CALL
ISN2   EQU     X'10C'        IDENTIFIER CODE FOR 2ND DLOG CALL
ISN3   EQU     X'10E'        IDENTIFIER CODE FOR 3RD DLOG CALL
ISN4   EQU     X'110'        IDENTIFIER CODE FOR DEXP CALL
       SPACE
GAMMA    CALLDUBL  DGAMMA,2
         SPACE     1
ALGAMA   CALLDUBL  DLGAMA,2
         SPACE     1
DLGAMA   FENT      (XGAM,REAL8),1
         SPACE     1
       MVI     TEST+1,X'00'          SET SWITCH TO NO BRANCH
         LA        R11,DGAMMA-DLGAMA(R11)
         USING     DGAMMA+8,R11
         B         MERGE
DGAMMA   FENT      (XGAM,REAL8),1
         SPACE     1
       MVI     TEST+1,X'F0'          SET SWITCH TO UNCOND. BRANCH
       SPACE
MERGE    LD        FR2,XGAM        ARG TO FR2
       CE      FR2,MIN       TEST THE SIZE OF ARGUMENT
       BC      2,OK1
       TM      TEST+1,X'80'  CASE WHEN ARG IS LESS OR EQ 2**(-252)
       BC      1,ERROR1        IF GAMMA ENTRY, GIVE ERROR
       LTER    FR2,FR2
       BC      12,ERROR2       IF ARG 0 OR NEGATIVE, GIVE ERROR
         FCALL     (DLOG,REAL8),(XGAM,REAL8)  LOGGAMMA(X)=-LOG(X)
         SPACE     1
       LCER    FR0,FR0
       BC      15,FIN          SKIP TO EXIT
       SPACE
OK1    CE      FR2,MAX1      ARG IS GREATER THAN 2**(-252)
       BC      12,OK2
       TM      TEST+1,X'80'    IF GAMMA ENTRY AND IF ARG IS
       BC      1,ERROR1          GREATER THAN 57.5744, GIVE ERROR
       CE      FR2,MAX2        IF LOGGAMMA WANTED, AND IF ARG IS
       BC      2,ERROR2          GREATER THAN 4.2937*10**73, GIVE ERROR
       SPACE
OK2    EQU     *                  ARGUMENT IN RANGE, JUMP TO
       CE      FR2,EIGHT       SECTION 1 IF ARG IS IN (2**-252,1)
       BC      10,SECT4        SECTION 2 IF ARG IS IN (1,2)
       CE      FR2,ONE         SECTION 3 IF ARG IS IN (2,8)
       BC      4,SECT1       SECTION 4 IF ARG IS GREATER THAN 8
       MVI     MRDDR,X'2C'   FOR SECTION 2 AND 3, SET MRDDR TO
       LD      FR4,ONE         'MDR FR0,FR4'   FR4=1.0 FOR SECTION 2
       SPACE
SECT3  CE      FR2,TWO       IF ARG IS IN (2,8), REDUCE ARG BY 1.0
       BC      12,SECT2        AT A TIME, BUILD FACTOR (X-1)*(X-2)*
       SD      FR2,ONE          ...*(X-N) TILL ARG IS REDUCED TO
       MDR     FR4,FR2             (1,2)
       BC      15,SECT3
       SPACE
SECT1  MVI     MRDDR,X'2D'   IF ARG IS IN (2**-252,1)
       LDR     FR4,FR2         EFFECTIVELY RAISE ARG BY 1, SUBTRACTING
       SD      FR2,HALF          0.5 INSTEAD OF 1.5.   SET MRDDR TO
       BC      15,MINMAX           'DDR FR0,FR4
       SPACE
SECT2  SD      FR2,ONHLF     NOW ARG IS IN (1,2).   SUBTRACT 1.5
MINMAX LD      FR0,A6        COMPUTE GAMMA(1.5+Z) WHERE Z=X-1.5
       MDR     FR0,FR2         BY MEANS OF MINIMAX FRACTION OF DEGREE
       LD      FR6,B6            (7,7) FOR Z IN (-0.5,0.5)
       ADR     FR6,FR2
       LM      GR3,GR5,INDEX          Z(A0+A1*Z+A2*Z**2+...+A6*Z**6)
LOOP   AD      FR0,A5(GR3)       C0 + -------------------------------
       MDR     FR0,FR2                B0+B1*Z+B2*Z**2+...+B6*Z**6+Z**7
       MDR     FR6,FR2
       AD      FR6,B5(GR3)
       BXLE    GR3,GR4,LOOP
       DDR     FR0,FR6
       AD      FR0,C0
       SPACE
MRDDR  MDR     FR0,FR4       'MDR FR0,FR4' OR 'DDR FR0,FR4'
       SPACE
TEST   BC      15,FIN        'BC 15' IF GAMMA, 'BC 0' IF LOGGAMMA
       STD     FR0,BUFF        IF LOGGAMMA WANTED, GO TO DLOG
         FCALL     (DLOG,REAL8),(BUFF,REAL8)
         SPACE     1
FIN      SDR       FR6,FR6         ***WATFIV CONVENTION***
         B         XFRET
         SPACE     1
       SPACE
SECT4  SDR     FR6,FR6       CASE WHEN ARG IS GREATER THAN 8
       CE      FR2,LMBIG
       BC      2,BIG           LOGGAMMA(X) = X*(LOG(X)-1)-(LOG(X))/2
       LD      FR0,ONE                         +LOG(2PI)/2+G(1/X)
       DDR     FR0,FR2
       LDR     FR4,FR0           HERE G(1/X) = DO/X+D1/X**3+D2/X**5
       MDR     FR4,FR4                             +D3/X**7+D4/X**9
       LE      FR6,D4
       MER     FR6,FR4           IF X GREATER THAN 2**25, SET G(1/X)
       AE      FR6,D3              TO 0 TO AVOID INTERMEDIATE
       MER     FR6,FR4               UNDERFLOW
       AD      FR6,D2
       MDR     FR6,FR4
       AD      FR6,D1
       MDR     FR6,FR4
       AD      FR6,D0
       MDR     FR6,FR0
BIG    STD     FR6,BUFF        G(1/X) IS READY.   SAVE IT
         FCALL     (DLOG,REAL8),(XGAM,REAL8)
       HDR     FR2,FR0         LOG(X)/2 IN FR2
       SD      FR0,ONE
         MD        FR0,XGAM        X*(LOG(X)-1) IN FR0
       SD      FR2,BUFF        ASSEMBLE LOGGAMMA(X)
       SD      FR2,LG2PI
       STE     FR0,BUFF          GIVE BIAS TO X*(LOG(X)-1) TO
       MVC     BIAS(1),BUFF        COMPENSATE FOR ROUND OFF
       AW      FR0,BIAS
       SDR     FR0,FR2
       SPACE
       TM      TEST+1,X'80'
       BC      8,FIN         IF LOGGAMMA WANTED, SKIP TO EXIT
       STD     FR0,BUFF      IF GAMMA WANTED, RAISE TO E'S POWER
         FCALL     (DEXP,REAL8),(BUFF,REAL8)
       BC      15,FIN          SKIP TO EXIT
       SPACE
       SPACE   2
*  LI-0   ARGUMENT OUT OF RANGE FOR DGAMMA
ERROR1   $ERROR    (NOAC,LI,0),XRETRACE                            V1L5
         SPACE     1
*   LI-F   ARGUMENT OUT OF RANGE FOR DLGAMMA
ERROR2   $ERROR    (NOAC,LI,F),XRETRACE                            V1L5
         SPACE     1
XGAM     DS        D
BUFF   DS      D
INFINY   DC    X'7FFFFFFFFFFFFFFF'
A6     DC      X'C0C1B71B59A1A1F6'   A6 = -   0.7567002385928
A5     DC      X'41B33F20CFA73CB3'   A5 =    11.2029121505218
       DC      X'4153CF867C239860'   A4 =     5.2381653641874
       DC      X'C23EBA40FFB0397B'   A3 = -  62.7275543027149
       DC      X'43441182D7048BE6'   A2 =  1089.0944433381650
       DC      X'43C3CDE7AC8F2232'   A1 =  3132.8690610495717   -3F
       DC      X'42E8A532ACC72020'   A0 =   232.6453044878145
B6     DC      X'C1A5004D879829C5'   B6 = -  10.3125739380508
B5     DC      X'41E62A3573ECF95D'   B5 =    14.3853048828456
       DC      X'42C97F1D84DC37A0'   B4 =   201.4965441739693
       DC      X'C327558408F56C71'   B3 = - 629.3447351061687
       DC      X'C358DA535E278586'   B2 = -1421.6453534644901
       DC      X'4411F52476FDA8AB'   B1 =  4597.1424406563556
       DC      X'441C1A16BED21CC5'   B0 =  7194.0888491935961
C0     DC      X'40E2DFC48DA77B56'   GAMMA(1.5) = 0.8862269254527580'+F
D4     DC      X'3E31E4F7'           D4 =  0.00076132793
D3     DC      X'BE26EC11'           D3 = -0.00059390466
D2     DC      X'3E340314721524AF'   D2 =  0.00079364060555471
D1     DC      X'BEB60B60907F58F2'   D1 = -0.00277777774362899
D0     DC      X'4015555555554ADC'   D0 =  0.08333333333329612
LG2PI  DC      X'40EB3F8E4325F5C5'   0.5*LOG(2PI)   +32F
HALF   DC      X'4080000000000000'
ONE    DC      X'4110000000000000'
BIAS   DC      X'0000000000000001'
ONHLF  DC      X'41180000'           4 WORDS FROM ONHLF MUST
INDEX  DC      X'00000000'             BE LOCATED TOGETHER STARTING
       DC      F'8'                      AT DOUBLE WORD BOUNDARY
       DC      F'40'
TWO    DC      X'41200000'
EIGHT  DC      X'41800000'
MIN    DC      X'02100000'           2**(-252)
MAX1   DC      X'4239930C'           57.5744
MAX2   DC      X'7E184D2F'           4.2937*10**73
LMBIG  DC      X'47200000'           2**25
       END
