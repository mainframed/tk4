CLSC   TITLE   'COMPLEX SINE-COSINE FUNCTION (LONG)'
         PRINT     NOGEN
         DISK
         PRINT     GEN
         SPACE 2
CDSIN#   CSECT
*
*        COMPLEX SINE-COSINE ROUTINE  (LONG)
*              SIN(X+IY) = SIN(X)*COSH(Y) + I*COS(X)*SINH(Y)
*              COS(X+IY) = COS(X)*COSH(Y) - I*SIN(X)*SINH(Y)
       SPACE
       EXTRN   DSIN
       EXTRN   DCOS
       EXTRN   DEXP
       ENTRY   CDSIN
       ENTRY   CDCOS
         ENTRY     CSIN
         ENTRY     CCOS
       SPACE
GR0    EQU     0               SCRATCH REGISTERS
GR1    EQU     1
GR2    EQU     2
GR3    EQU     3
GRA    EQU     1               ARGUMENT POINTER
GRS    EQU     13              SAVE AREA POINTER
GRT    EQU     3               PREVIOUS SAVE AREA POINTER
GRR    EQU     14              RETURN REGISTER
GRL    EQU     15              LINK REGISTER
GRB    EQU     2               BASE REGISTER (FOR THE MOST PART)
FR0    EQU     0               ANSWER REGISTERS
FR2    EQU     2
FR4    EQU     4               SCRATCH REGISTER
ISN1   EQU     X'314'
ISN2   EQU     X'316'
ISN3   EQU     X'318'
       SPACE
CSIN     CALLDCL   CDSIN,2
         SPACE     1
CCOS     CALLDCL   CDCOS,2
         SPACE     1
CDCOS    FENT      (XCDSIN,COMPL16),1
         SPACE     1
         MVI       SWITCH+1,X'00'
         LA        R11,CDSIN-CDCOS(R11)
         USING     CDSIN+8,R11
         B         JOIN
         SPACE     1
CDSIN    FENT      (XCDSIN,COMPL16),1
       MVI     SWITCH+1,X'F0'  MAKE SWITCH UNCONDITIONAL
       SPACE
JOIN     LD        FR0,XCDSIN          REAL PART
         LD        FR2,XCDSIN+8        IMAG PART
       STD     FR2,Y              SAVE Y
       SPACE
       LPER    FR0,FR0         IF /X/ GREATER THAN PI*2**50, ERROR
       CE      FR0,XMAX
       BC      10,ERROR1
         FCALL     (DSIN,REAL8),(XCDSIN,REAL8)
         STD       FR0,SINX
         SPACE     1
         FCALL     (DCOS,REAL8),(XCDSIN,REAL8)
         STD       FR0,COSX
         SPACE
       LD      FR2,Y              OBTAIN REAL
       LPER    FR2,FR2            FORCE Y POSITIVE
       CE      FR2,YMAX
       BC      2,ERROR2           IF /Y/ GREATER THAN 174.673, ERROR
       STD     FR2,YABS           ABS VALUE OF Y
       SPACE
         FCALL     (DEXP,REAL8),(YABS,REAL8)
         SPACE     1
       SPACE
       SPACE
       LD      FR4,ONE         OBTAIN EXP(-/Y/) IN FR4
       DDR     FR4,FR0
       LD      FR2,YABS
       CE      FR2,LIM1
       BC      4,SMALL         IF /Y/ LESS THAN 0.48121183, JUMP
       SDR     FR0,FR4         OTHERWISE COMPUTE SINH(/X/) AS
       HDR     FR2,FR0           (EXP(/Y/)-EXP(-/Y/))/2
       SPACE
MERGE  LDR     FR0,FR2         SINH(/Y/) IN FR2
       ADR     FR0,FR4         COSH(Y) = EXP(-/Y/)+SINH(/Y/) IN FR0
COMPUT EQU     *
       TM      Y,X'80'
       BC      8,*+6
       LNER    FR2,FR2         SINH(Y) IN FR2
SWITCH BC      0,CSINE         IF CDSIN ENTRY, JUMP
       SPACE
       MD      FR0,COSX        COMBINE 4 QUANTITIES TO
       MD      FR2,SINX          OBTAIN COS(X+IY) IN FR0, FR2
       LCER    FR2,FR2
       BC      15,EXIT
       SPACE
CSINE  MD      FR0,SINX        COMBINE 4 QUANTITIES TO
       MD      FR2,COSX          OBTAIN SIN(X+IY) IN FR0, FR2
       SPACE
EXIT   EQU     *
         B         XFRET
       SPACE
SMALL  CE      FR2,LIM2        IF /Y/ SMALLER THAN 16*-7, SINH(/Y/)=/Y/
       BC      4,MERGE
       MDR     FR2,FR2         COMPUTE SINH(/Y/) FOR SMALL /Y/
       LDR     FR0,FR2           BY USE OF MINIMAX POLYNOMIAL
       MD      FR0,C5              OF DEGREE 5
       AD      FR0,C4
       MDR     FR0,FR2
       AD      FR0,C3
       MDR     FR0,FR2
       AD      FR0,C2
       MDR     FR0,FR2
       AD      FR0,C1
       MDR     FR2,FR0
       MD      FR2,YABS
       AD      FR2,YABS
       BC      15,MERGE
       SPACE
*  LI-5  ×REAL PORTION OF ARG1× > PI*2**50
ERROR1   $ERROR    (NOAC,LI,5),XRETRACE                            V1L5
         SPACE     1
*  LI-4  × IMAGINARY PORTION OF ARG× > 174.673
ERROR2   $ERROR    (NOAC,LI,4),XRETRACE                            V1L5
         SPACE     1
XCDSIN   DS        2D
       SPACE
SINX   DS      D
COSX   DS      D
YABS   DS      D
Y      DS      D
ONE    DC      X'4110000000000000'
C5     DC      X'3A6C11A069B2EA82'     0.2516174082560345E-7
C4     DC      X'3C2E3BA8BC90245F'     0.2755704132663989E-5
C3     DC      X'3DD00D01083D72C5'     0.1984127016066868E-3
C2     DC      X'3F222222221F51A6'     0.8333333333173353E-2
C1     DC      X'402AAAAAAAAAAB66'     0.1666666666666693
XMAX   DC      X'4DC90FDA'
YMAX   DC      X'42AEAC4F'
LIM1   DC      X'407B30B3'
LIM2   DC      X'3A100000'
       END
