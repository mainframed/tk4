CLLG   TITLE   'COMPLEX LOGARITHMIC FUNCTION (LONG)'
         PRINT     NOGEN
         DISK
         PRINT     GEN
         SPACE 2
CDLOG#   CSECT
*
*      COMPLEX LOGARITHMIC FUNCTION (LONG)
*              1. WRITE LOG(X+IY) = U+IV, WHERE U IS THE REAL PART
*                   OF THE ANSWER, AND V IS THE IMAGINARY PART.
*              2. THEN U = LOG(/X+IY), AND V = ATAN2(Y/X).
*              3. WRITE LOG(/X+IY/) = LOG(X**2+Y**2)/2 =
*                   LOG(S**2((X/S)**2+(Y/S)**2))/2 =
*                     LOG((X/S)**2+(Y/S)**2)/2+LOG(S), WHERE
*                       S IS A POWER OF 16, SUCH THAT, MAX(X,Y)/S
*                         IS BETWEEN 1 AND 1/16 OR BETWEEN 1 AND 16.
       SPACE
       EXTRN   DLOG
       EXTRN   DATAN2
       ENTRY   CDLOG
         ENTRY     CLOG
       SPACE
GRA    EQU     1               ARGUMENT POINTER
GRB    EQU     2               BASE REGISTER
GRS    EQU     13              SAVE AREA POINTER
GRR    EQU     14              RETURN REGISTER
GRL    EQU     15              LINK REGISTER
GR0    EQU     0               SCRATCH REGISTERS
GR1    EQU     1
GR2    EQU     14
FR0    EQU     0               ANSWER REGISTERS
FR2    EQU     2
FR4    EQU     4               SCRATCH REGISTER
ISNAT  EQU     X'310'          IDENTIFIER NUMBER FOR DATAN2 CALL
ISNLG  EQU     X'312'          IDENTIFIER NUMBER FOR DLOG CALL
         SPACE     1
CLOG     CALLDCL   CDLOG,3
         SPACE     1
       SPACE
CDLOG    FENT      (XCDLG,COMPL16),2
         SPACE     1
MERGE    LD        FR0,XCDLG
         LD        FR2,XCDLG+8
       SPACE
       LPER    FR0,FR0         FORCE X POSITIVE
       LPER    FR2,FR2         FORCE Y POSITIVE
       CDR     FR0,FR2         A = MAX(/X/,/Y/), B = MIN(/X/,/Y/)
       BC      10,SKIP1        IF NECESSARY SWITCH REGISTERS SO THAT
       LDR     FR4,FR0           IN ALL CASES FR0 CONTAINS A AND FR2
       LDR     FR0,FR2             CONTAINS B
       LDR     FR2,FR4
       SPACE
SKIP1  SWR     FR2,FR0         GIVE B SAME CHARACTERISTIC AS A
       AWR     FR2,FR0
       STE     FR0,BUFF1
       STE     FR2,BUFF1+4
       SR      GR0,GR0
       LA      GR2,65
       IC      GR0,BUFF1
       SR      GR0,GR2         S = 16**T
       SLA     GR0,2           4*T IN GR0
       BC      10,SKIP2        IF A LESS THAN 1, T = CHAR(A)-64
       LPR     GR0,GR0           GIVE /X/ AND /Y/ CHARACTERISTIC OF 64
       AL      GR0,MFLOAT      OTHERWISE, T = CHAR(A)-65 AND
       LA      GR2,64            GIVE /X/ AND /Y/ CHARACTERISTIC OF 65
       SPACE
SKIP2  STC     GR2,BUFF1
       STC     GR2,BUFF1+4
       O       GR0,CH46        FLOAT T
       ST      GR0,INTEG
       LE      FR0,BUFF1
       LE      FR2,BUFF1+4
       MDR     FR0,FR0
       MDR     FR2,FR2
       ADR     FR0,FR2
       BC      8,ERROR         ERROR IF /X/=/Y/=0
       STD     FR0,BUFF1
         SPACE     1
         FCALL     (DATAN2,REAL8),(XCDLG+8,REAL8),(XCDLG,REAL8)
       STD     FR0,BUFF2
       SPACE
         SPACE     1
         FCALL     (DLOG,REAL8),(BUFF1,REAL8)
       LD      FR4,INTEG
       MD      FR4,LOG2        LOG(S) = 4*T*LOG(2)
       HDR     FR0,FR0
       ADR     FR0,FR4         FR0 HAS LOG(/X+IY/), REAL ANS PART
       LD      FR2,BUFF2       FR2 HAS ATAN2(Y/X)
       SPACE
EXIT     B         XFRET
       SPACE
*  LI-8  ARGUMENT IS ZERO
ERROR    $ERROR    (NOAC,LI,8),XRETRACE                            V1L5
         SPACE     1
       SPACE   3
BUFF1    DS    D
BUFF2    DS    D
INTEG  DC      X'0000000000000000'
LOG2   DC      X'40B17217F7D1CF7B'     LOG(2) BASE E +FUDGE 1
MFLOAT DC      X'7FFFFFFC'
CH46   DC      X'46000000'
XCDLG    DS        2D
       END
