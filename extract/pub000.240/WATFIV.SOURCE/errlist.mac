         TITLE     '*** ERROR STATISTICS ROUTINE ***'
RC       EQU       10
         PRINT     OFF
         COPY      CDEFN
         PRINT     ON,NOGEN
CRD      EQU       R11
BL       EQU       R10
CNT      EQU       R9
CARD     DSECT
REC      DSECT
ZR1      DSECT
ZR3      DSECT
ERROR    CSECT
         $FREE     R1,R3                                           V1L5
         SAVE      (14,12)
         LR        15,13
         BALR      12,0
         USING     *,12
         LA        13,SAVE
         ST        15,SAVE+4
         ST        13,8(15)
         EJECT
         OPEN      (BPAM,(INPUT),PRINT,(OUTPUT))
         SR        R6,R6
         LH        R7,BPAM+62          BLKSIZE
         D         R6,=F'80'
         ST        R7,#RECBLK          # OF RECORDS PER BLOCK
         CNTRL     PRINT,SK,1          SKIP TO NEW PAGE
         PUT       PRINT               GET AN ADDRESS
         ST        R1,PRINTADD
         BLDL      BPAM,BLDLIST
         GETMAIN   EC,LV=16000,A=RECORD
         MVC       RECORD2(4),RECORD   2 COPIES
         EJECT
***********************************************************************
**********         MEMBER FINDING ROUTINE                   ***********
***********************************************************************
START    L         BL,=F'-14'
         B         FIND
NEXTMEMB MVI       IHAB,C'N'
         L         BL,BLSAVE
FIND     LA        BL,14(BL)           POINT TO NEXT LIST ENTRY
         ST        BL,BLSAVE
         LA        R3,LIST(BL)
         CLC       ZR3+8(4),FFFF            ARE WE AT THE END ?
         BE        SORTHEM
         CLC       ZR3+8(4),ZERO       IS MEMBER THERE
         BE        FIND                NO
         FIND      BPAM,ZR3+8,C        FIND THE MEMBER
         B         PROCESS
         EJECT
***********************************************************************
**********         READ ROUTINE                             ***********
***********************************************************************
READ     STM       R2,R14,SAVE2        FOR GOOD MEASURE
         IF        (IHAB,EQ,C'Y'),GOTBLOK
         READ      DECB1,SF,BPAM,BUFFER
         LA        R3,BUFFER
         ST        R3,BUFFAD
         CHECK     DECB1
         MVI       IHAB,C'Y'
         L         CNT,#RECBLK
         ST        CNT,CNTSAVE
GOTBLOK  EQU       *
*        TRACEON                       ********************************
         L         CNT,CNTSAVE
         L         R1,BUFFAD           A(CARD WE WANT)
         LA        R3,ZR1+80
         ST        R3,BUFFAD           A(NEXT CARD TO BE GOTTEN)
         BCT       CNT,COUNTBCT        ARE WE AT BUFFER END ?
         MVI       IHAB,C'N'           YES
COUNTBCT ST        CNT,CNTSAVE
*        TRACEOFF                      ********************************
         LM        R2,R14,SAVE2
         LR        CRD,R1
         BR        R14
         EJECT
***********************************************************************
**********         MAIN PROCESSOR                           ***********
***********************************************************************
RECSIZE  EQU       32
PROCESS  EQU       *
NEXT     EQU       *
*        TRACEOFF                      ********************************
         USING     CARD,CRD
         THEN      (DO,READ)
         LR        R4,CRD              SAVE UNTIL LATER
         LA        R2,71               SO WE DON'T RUN OFF END OF CARD
         IF        (CARD,EQ,C'*'),NEXT
         IF        (CARD,EQ,C'.'),NEXT
         IF        (CARD,EQ,C' '),BL1
LABEL    LA        CRD,CARD+1
         IF        (CARD,EQ,C' '),BL1
         BCT       R2,LABEL
         B         NEXT
BL1      LA        CRD,CARD+1
         IF        (CARD,NE,C' '),OPCODE
         BCT       R2,BL1
         B         NEXT
***      WE HAVE NOW ARRIVED AT THE OPCODE FIELD
OPCODE   CLC       CARD(6),=C'ERROR '
         BE        THATSIT2
         CLC       CARD(8),=C'ERRCODE '
         BE        THATSIT
         CLC       CARD(8),=C'SETTERR '
         BE        THATSIT
         CLC       CARD(4),=C'END '
         BE        NEXTMEMB            GET NEXT MEMBER
         B         NEXT
THATSIT  LA        CRD,CARD+2
THATSIT2 LA        CRD,CARD+5
         L         R3,COUNT
         LA        R3,ZR3+1            BUMP FIELD COUNTER
         ST        R3,COUNT            AND STASH IT
*        TRACEON                       ********************************
BL3      LA        CRD,CARD+1
         IF        (CARD,NE,C' '),OPERAND
         BCT       R2,BL3
         B         END
         USING     REC,RC
OPERAND  L         RC,RECORD
         MVI       REC,C' '            BLANK OUT
         MVC       REC+1(31),REC       THE FIELD
         MVC       REC(2),CARD+6       ALPHA CODE
         MVC       REC+2(1),CARD+9     NUMERIC CODE
         LA        CRD,CARD+10
         IF        (CARD,EQ,C')'),END
         IF        (CARD+1,EQ,C','),BL4
         MVC       REC+4(3),CARD+1     PARM 4
         LA        CRD,CARD+4
BL4      LA        CRD,CARD+1
         IF        (CARD,EQ,C')'),END
         IF        (CARD,EQ,C','),OP5
         BCT       R2,BL4
         B         END
OP5      MVC       REC+8(5),CARD+1
         IF        (CARD+5,NE,C')'),END
         MVI       REC+12,C' '         MAKE IT LOOK LIKE IT SHOULD
END      L         R3,BLSAVE
         LA        R3,LIST(R3)
         MVC       REC+23(8),ZR3       MEMBER NAME
         LR        CRD,R4              POINTER TO START OF CARD
         MVC       REC+14(8),CARD+72   SEQUENCE NUMBER
         L         R3,PRINTADD         ADDRESS OF PRINT BUFFER
         MVC       ZR3(80),CARD        MOVE CARD TO BUFFER
         MVI       ZR3+80,C' '         BLANK REST
         MVC       ZR3+81(33),ZR3+80   OF BUFFER
         MVC       ZR3+82(8),REC+23    MEMBER NAME
         PUT       PRINT
         ST        R1,PRINTADD
         LA        RC,RECSIZE(RC)
         ST        RC,RECORD
         B         NEXT                GET NEXT CARD
         EJECT
***********************************************************************
**********         SORT ROUTINE                             ***********
***********************************************************************
SORTHEM  EQU       *
         $FREE     R9                                              V1L5
         CNTRL     PRINT,SK,1
*        TRACEON                       ********************************
         L         R8,COUNT            # OF FIELDS    (N)
         BCTR      R8,0                N-1 TIMES
LOOP1    L         R9,RECORD2          A(START OF N FIELDS)
         LR        R7,R8               # OF TIMES IN THIS LOOP
         MVI       SWAPSW,C'N'         INITIALIZE SWITCH
LOOP2    CLC       ZR9(3),ZR9+32       COMPARE
         BNH       EL2
         MVC       TEMP(32),ZR9+32     **
         MVC       ZR9+32(32),ZR9      **  SWAP THE FIELDS
         MVC       ZR9(32),TEMP        **
         MVI       SWAPSW,C'Y'         WE HAVE HAD A SWAP
EL2      LA        R9,ZR9+32           POINT TO NEXT FIELD
         BCT       R7,LOOP2            LOOP IN INNER ON
         IF        (SWAPSW,EQ,C'N'),SORTEND
         BCT       R8,LOOP1
         SPACE     2
***      NOW PRINT OUT THE SORTED FIELDS
SORTEND  L         R8,COUNT
         L         R9,RECORD2
         L         R1,PRINTADD
LOOP3    MVC       ZR1(32),ZR9
         MVI       ZR1+32,C' '         BLANK
         MVC       ZR1+33(81),ZR1+32
         PUT       PRINT
         LA        R9,ZR9+32
         BCT       R8,LOOP3
         MVI       ZR1,C' '
         MVC       ZR1+1(31),ZR1       BLANK BUFFER WRITTEN BY CLOSE
         EJECT
***********************************************************************
**********         FINISH UP                                ***********
***********************************************************************
RETURN   CLOSE     (BPAM,,PRINT,)
         STOP
EODAD    B         NEXTMEMB
SYNAD    WTO       'SYNAD'
         B         RETURN
         EJECT
***********************************************************************
**********         DATA AREA                                ***********
***********************************************************************
BLDLIST  DC        H'21',H'14'
LIST     DC        CL8'ACCT    ',XL6'0'          1
         DC        CL8'ARITH   ',XL6'0'          2
         DC        CL8'COMMR   ',XL6'0'          3
         DC        CL8'DIOCS   ',XL6'0'          4
         DC        CL8'DODO    ',XL6'0'          5
         DC        CL8'FIOCS   ',XL6'0'          6
         DC        CL8'FORMAT  ',XL6'0'          7
         DC        CL8'FORMCONV',XL6'0'          8
         DC        CL8'FRIOSCAN',XL6'0'          9
         DC        CL8'FUNCTION',XL6'0'         10
         DC        CL8'INOUT   ',XL6'0'         11
         DC        CL8'LANDR   ',XL6'0'         12
         DC        CL8'MAIN    ',XL6'0'         13
         DC        CL8'NAMELIST',XL6'0'         14
         DC        CL8'NEWFUNC ',XL6'0'         TEMPORARY
         DC        CL8'SCAN    ',XL6'0'         15
         DC        CL8'SPEX    ',XL6'0'         16
         DC        CL8'STARTA  ',XL6'0'         17
         DC        CL8'STARTB  ',XL6'0'         18
         DC        CL8'SYS     ',XL6'0'         19
         DC        CL8'WATLOAD ',XL6'0'         20
         DC        CL8'XXXXXXXX',XL6'FFFFFFFFFFFF'
         SPACE     2
SAVE     DC        18F'0'
SAVE2    DC        18F'0'
PRINTADD DC        A(*-*)
RECORD   DC        A(*-*)
RECORD2  DC        A(*-*)
BUFFAD   DC        A(*-*)
BLSAVE   DC        F'0'
CNTSAVE  DC        F'0'
COUNT    DC        F'0'
#RECBLK  DC        F'0'
ZERO     DC        F'0'
FFFF     DC        X'FFFFFFFF'
IHAB     DC        C'N'
SWAPSW   DC        C'N'
TEMP     DC        CL32' '
         SPACE     2
         LTORG
         SPACE     2
         PRINT     NOGEN
BPAM     DCB       DSORG=PO,MACRF=R,DDNAME=SYSIN,RECFM=FB,             *
               EODAD=EODAD,SYNAD=SYNAD
PRINT    DCB       DSORG=PS,MACRF=PLC,DDNAME=SYSPRINT,                 *
               RECFM=F,LRECL=114,BLKSIZE=114
         SPACE     2
BUFFER   DS        5000C
         END
