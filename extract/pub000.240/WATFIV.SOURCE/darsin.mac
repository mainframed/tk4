 TITLE ' ASCN ARC SIN-COS (LONG) '
         PRINT     NOGEN
         DISK
         PRINT     GEN
         SPACE     2
DARSN#   CSECT
*C020920                                                          MAINT
*        ARCSIN-ARCCOSINE FUNCTION (LONG)
*              1. IF X BETWEEN 0 AND 1/2, COMPUTE ARCSIN BY POLYNOMIAL
*              2. IF X BETWEEN 1/2 AND 1,
*                      ARSIN(X) = PI/2-2*ARSIN(SQRT((1-X)/2))
*              3. IF X NEGATIVE, ARSIN(X) = -ARSIN(/X/)
*              4. ARCOS(X) = PI/2-ARSIN(X)
* STATUS -- CHANGE LEVEL 02,   21OCT70     RELEASE 20                 *
       EXTRN   DSQRT
       ENTRY   DARSIN
       ENTRY   DARCOS
       SPACE
         ENTRY     ARSIN
         ENTRY     ARCOS
GR2    EQU     2               ARGUMENT ADDRESS
GRA    EQU     1               ARGUMENT POINTER
GRT    EQU     3               PREVIOUS SAVE AREA POINTER
GRB    EQU     4               MAIN BASE REGISTER
GRS    EQU     13              SAVE AREA POINTER
GRR    EQU     14              RETURN REGISTER
GRL    EQU     15              LINK REGISTER
FR0    EQU     0               ANSWER REGISTER
FR2    EQU     2               SCRATCH REGISTERS
FR4    EQU     4
FR6    EQU     6
ISN    EQU     X'106'          INDENTIFIER NUMBER FOR DSQRT CALL
       SPACE
ARSIN    CALLDUBL  DARSIN,2
         SPACE     1
ARCOS    CALLDUBL  DARCOS,2
         SPACE     1
DARCOS   FENT      (XDAR,REAL8),1
         SPACE     1
       MVI     SWICH1+1,X'00'   SET SWICH1 TO 'BC 0'
         LA        R11,DARSIN-DARCOS(R11)
         USING     DARSIN+8,R11
         BC        15,JOIN
       SPACE
DARSIN   FENT      (XDAR,REAL8),1
         SPACE     1
       MVI     SWICH1+1,X'F0'    SET SWICH1 TO 'BC 15'
       SPACE
JOIN     LD        FR6,XDAR
       LPDR    FR0,FR6         /X/ TO FR0
       MVI     SWICH2+1,X'10'
       CE      FR0,HALF        IF/X/ SMALLER THAN 1/2, SET SWICH2
       BC      12,MINMAX         TO 'BC 1' AND SKIP TO MINMAX SECTION
       SPACE
       MVI     SWICH2+1,X'80'  IF /X/ GREATER THAN 1/2, SET SWICH2
       LNER    FR0,FR0           TO 'BC 8' AND COMPUTE 1-/X/
       AD      FR0,ONE
       BC      4,ERROR         IF /X/ GREATER THAN 1, ERROR
       HDR     FR6,FR0         LET Z = SQRT((1-/X/)/2),
       ADR     FR0,FR0           KEEP Z**2 IN FR6 AND COMPUTE
       STD     FR0,BUFF            2*Z WHICH IS SQRT(2*(1-/X/)).
         FCALL     (DSQRT,REAL8),(BUFF,REAL8)
       BC      15,MERGE        MERGE WITH MINIMAX EVALUATION SECTION
       SPACE
MINMAX CE      FR0,LOLIM       IF /X/ IS SMALLER THAN 16**-7,
       BC      2,*+6             SUBSTITUTE 0 FOR X**2 TO AVOID
       SDR     FR6,FR6             UNDERFLOW MESSAGE
       MDR     FR6,FR6         FOR /X/ LE 1/2, GET X**2 IN FR6
MERGE  LD      FR4,C5          COMMON CIRCIUT
       ADR     FR4,FR6           COMPUTE ARCSIN(/X/)
       LD      FR2,D4              OR 2*ARCSIN(Z) AS THE CASE MAY BE
       DDR     FR2,FR4
       AD      FR2,C4            FR0 CONTAINS /X/ OR 2*Z
       ADR     FR2,FR6           FR6 CONTAINS X**2 OR Z**2
       LD      FR4,D3
       DDR     FR4,FR2           USE MINIMAX APPROXIMATION OF FORM,
       AD      FR4,C3
       ADR     FR4,FR6
       LD      FR2,D2            ARCSIN(W) = W+F*W**3   WHERE
       DDR     FR2,FR4
       AD      FR2,C2              F = C1+D1/(WSQ+C2+D2/(WSQ+C3+D3
       ADR     FR2,FR6
       LD      FR4,D1                       /(WSQ+C4+D4/(WSQ+C5))))
       DDR     FR4,FR2
       AD      FR4,C1
       MDR     FR4,FR6
       MDR     FR4,FR0           POSTPONE COMBINING FR4 AND FR0
       SPACE
       TM      SWICH1+1,X'80'  IF ARSIN FOR BIG /X/ OR
SWICH2 BC      1,SIGN            ARCOS FOR SMALL /X/, SUBTRACT
       SD      FR4,ONE             THE WORK FROM PI/2.  DO THIS CARE-
       SD      FR0,PO2M1             FULLY TO REDUCE ROUND-OFF ERROR
SIGN   ADR     FR0,FR4         AT THIS POINT COMBINE FR6 AND FR0 TO
       LPDR    FR0,FR0           COMPLETE COMPUTATION OF ANS FOR /X/
         TM        XDAR,X'80'
       BC      8,FIN           IF ARG IS POSITIVE, DONE
       LNER    FR0,FR0         IF ARG NEGATIVE AND ARSIN, SWITCH SIGN
SWICH1 BC      15,FIN          IF ARG NEGATIVE AND ARCOS,
       AD      FR0,PI            SUBTRACT FR0 FROM PI
       SPACE
FIN      SDR       FR6,FR6         ***WATFIV CONVENTION***
         B         XFRET
         SPACE
*   LI-B   ×ARGUMENT×>1
ERROR    $ERROR    (NOAC,LI,B),XRETRACE                            V1L5
XDAR     DS        D
       SPACE
BUFF   DS      D
C1     DC      X'3F180CD96B42A610'        0.00587162904063511
D1     DC      X'C07FE6DD798CBF27'       -0.49961647241138661
C2     DC      X'C1470EC5E7C7075C'       -4.44110670602864049
D2     DC      X'C1489A752C6A6B54'       -4.53770940160639666
C3     DC      X'C13A5496A02A788D'       -3.64565146031194167
D3     DC      X'C06B411D9ED01722'       -0.41896233680025977
C4     DC      X'C11BFB2E6EB617AA'       -1.74882357832528117
D4     DC      X'BF99119272C87E78'       -0.03737027365107758
C5     DC      X'C11323D9C96F1661'       -1.19625261960154476
ONE    DC      X'4110000000000000'        1.0
PO2M1  DC      X'40921FB54442D184'        PI/2 - 1.0
PI     DC      X'413243F6A8885A30'        PI   -F
HALF   DC      X'40800000'                0.5
LOLIM  DC      X'3A100000'      16**-7
       END
