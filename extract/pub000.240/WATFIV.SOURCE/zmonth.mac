         TITLE     'ZMONTH-  DATE AND TIME ROUTINE'
         PRINT     ON,GEN
         EQUREGS
***********************************************************************
*                                                                     *
*                                                                     *
*            DATE AND TIME ROUTINE                                    *
*                                                                     *
*         THIS ROUTINE PLACES THE TIME IN CHARACTER IN LOCATION 'ZTIME'
*         IT IS IN FORMAT 'HH:MM:SS' WITH LEADING 0 SUPPRESSED
*         THE DATE IN CHARACTER IS PLACED IN LOCATION 'ZDATE'
*         IT IS IN FORMAT 'DD MON YY' WITH LEADING 0 SUPPRESSED
*
*        THE ROUTINE ALSO DETERMINES THE DAY OF THE WEEK AND PLACES IT
*        AT ZDAY.  IT ONLY WORKS FOR 1901 TO 1999.
*        THE DAY OF THE WEEK SECTION OF THIS ROUTINE WAS INSPIRED BY,
*        AND IS SOMEWHAT MODELLED AFTER, THE PROGRAM 'WEEKDAY' (TYPE 3,
*        NUMBER 360D-03.8.003, AUTHOR RICHARD L. CONNER, OCTOBER 15,
*        1966, IBM CONTRIBUTED PROGRAM LIBRARY).
*
*        CALLING SEQUENCE:  $CALL   ZMONTH
*                                                                     *
*                                                                     *
*                                                                     *
*        INPUT/OUTPUT       R5 -> BUFFER LINE WHERE WE ARE STORING    *
*                                                                     *
*        RETURN    =0   ALWAYS                                        *
*                                                                     *
*        ATTRIBUTES:   SERALLY REUSEABLE                              *
*                                                                     *
***********************************************************************
         SPACE     3
ZMONTH   CSECT
         STM       R14,R12,12(R13)
         BALR      R11,0               SET UP A BASE
         USING     *,R11               TELL ASSEMBLER
         LR        R2,R13              SAVE R13
         LA        R13,SAVE            GET OUR SAVE AREA ADDR
         ST        R13,8(,R2)          SAVE IT IN CALLERS SAVE AREA
         ST        R2,SAVE+4           SAVE CALLERS IN OURS AS WELL
         TIME      DEC                 GET TIME AND DATE
         ST        R0,ZWORK            STORE TIME
         MVC       ZTIME-1(9),ZEDIT2   PLACE IN ZTIME
         ED        ZTIME-1(9),ZWORK
         SR        R0,R0               MAKE YR & DAY DBL WORD
         STM       R0,R1,ZWORK
         UNPK      ZDATE+7(3),ZWORK+5(2) PLACE YR IN OUTPUT
         CVB       R1,ZWORK            YR & DAY DBL BINARY WORD
         D         R0,=F'1000'         YR IN R1, DAY IN R0
         BCTR      R1,0                YR-1
         LR        R15,R1              YR-1
         SRL       R15,2               (YR-1)/4
         N         R1,=F'3'            (YR-1) MOD 4
         MH        R15,=H'5'           (YR-1)/4*5
         AR        R15,R1              (YR-1)/4*5 + ((YR-1) MOD 4)
         AR        R15,R0              (YR-1)/4*5 + ((YR-1) MOD 4)+DAYS
         SR        R14,R14             DBL WORD LONG
         D         R14,=F'7'           DAY OF WEEK IN R14
         MH        R14,=H'9'           INDEX DAY NAME TABLE
         LA        R14,ZDAYTAB(R14)    GET THE DAY
         MVC       ZDAY,0(R14)
         IC        R1,ZFEBTAB(R1)      28 OR 29 DAYS IN FEB
         STC       R1,ZFEB+1
         LA        R1,ZTAB-6           POINT AT MONTH TABLE
ZLOOP    LA        R1,6(0,R1)          POINT AT NEXT MONTH
         SH        R0,0(0,R1)          SUBTRACT NO. DAYS IN MONTH
         BH        ZLOOP               DONE IF 0 OR NEGATIVE
         AH        R0,0(0,R1)          ADD BACK LAST MONTH
         CVD       R0,ZWORK            CHANGE DAY IN MONTH TO PACKED
         MVC       ZDATE-2(4),ZEDIT    EDIT DAY INTO OUTPUT
         ED        ZDATE-2(4),ZWORK+6
         MVC       ZDATE+3(3),2(R1)    MOVE NAME OF MONTH TO OUTPUT
         MVC        5(8,R5),ZTIME      MOVE IN TIME
         MVC       17(9,R5),ZDAY       MOVE IN DAY
         MVC       30(9,R5),ZDATE      MOVE IN DATE
         L         R13,4(,R13)         GET SAVE AREA
         RETURN    (14,12)             BACK TO CALLER
ZFEBTAB  DC        AL1(28,28,28,29)
ZDAYTAB  DC        C'MONDAY   '
         DC        C'TUESDAY  '
         DC        C'WEDNESDAY'
         DC        C'THURSDAY '
         DC        C'FRIDAY   '
         DC        C'SATURDAY '
         DC        C'SUNDAY   '
ZWORK    DC        D'0'
         DC        2X'00'
ZDATE    DC        C'XX XXX XX'        DO NOT MOVE THIS CARD
         DC        X'00'
ZTIME    DC        C'XX.XX.XX'
ZDAY     DC        CL9' '
ZTAB     DC          H'31',C'JAN'
ZFEB     DC          H'28',C'FEB'
         DC          H'31',C'MAR'
         DC          H'30',C'APR'
         DC          H'31',C'MAY'
         DC          H'30',C'JUN'
         DC          H'31',C'JUL'
         DC          H'31',C'AUG'
         DC          H'30',C'SEP'
         DC          H'31',C'OCT'
         DC          H'30',C'NOV'
         DC        H'32767',C'DEC'
         SPACE     3
ZEDIT    DC        C' ',X'202120'
ZEDIT2   DC        C' ',X'2120',C'.',X'2020',C'.',X'2020'
SAVE     DC        18F'0'
         LTORG
         END
