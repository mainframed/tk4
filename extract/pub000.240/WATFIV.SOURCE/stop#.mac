         PRINT     NOGEN                                           V1L4
         DISK
         PRINT     GEN                                             V1L4
         SPACE     1                                               V1L4
STOP#    CSECT                         ENTRY FOR STOP AND PAUSE    V1L4
         SPACE     1                                               V1L4
******   IMPLEMENTS RUNTIME STOP,PAUSE WITH OPERATOR MESSAGE
******     ENTRY POINT STOP# IS CALLED FROM OBJECT CODE BY
******          L  R15,=V(STOP#)
******          BALR R14,R15
******         DC  AL1(C),AL3(X)                                   V1L4
******       FOR STOP N, C=0,X=2*N+1
******       FOR PAUSE N, C=0,X=2*N
******       FOR PAUSE 'HOLCON', C=LENGTH OF HOLCON ,X=HOLCON
         SPACE
MESLEN   EQU       72                  TOTAL MESSAGE LENGTH        V1L4
         SPACE     2
         USING     PSTOPN,R15
PSTOPN   IF        (ZR14+3,OFF,X'01'),PAUSEHOL TEST IF STOP OR PAUSE
         MVI       PSTOPSW+1,BRA       SET SWITCH TO BRANCH
         LA        R1,PWTO             SETUP FOR WTO MACRO
         MVC       PWTOMES+5(8),=C'2I STOP '
         BAL       R4,PSETCON          GO EDIT STOP CODE           V1L4
         STH       R0,XSTOP#           STASH FOR WATFIV            V1L4
         LA        R4,STOPCDS          SET POINTER TO MCS CODES    V1L4
         B         POUTMES             JOIN COMMON CODE            V1L4
         SPACE     1                                               V1L4
PAUSEN   LA        R4,PAUSE1           CHANGE BINARY CONST TO HOLCONST
PSETCON  L         R0,ZR14             GET 2*N OR 2*N+1
         SRL       R0,1                THIS GETS N
         CVD       R0,PCVD             GIVE IT THE OLD CVD
         MVC       PWTO1+1(5),=X'2020202120'   EDIT IT INTO THE MESSAGE
         ED        PWTO1(6),PCVD+5
         LA        R3,5-1              MESSAGE IS 5 BYTES
         LA        R2,PWTO1+1          AND ITS HERE
         BR        R4                  ALL SET
         SPACE     1                                               V1L4
PAUSEHOL IF        (ZR14,EQ,X'00'),PAUSEN
         SR        R3,R3
         IC        R3,ZR14             PICKUP LENGTH OF HOLCONST
         BCTR      R3,0                LENGTH-1 FOR EXECUTE
         L         R2,ZR14             ADDRESS OF HOLCONST
PAUSE1   MVI       PSTOPSW+1,NOP       SET SWITCH TO NOP
         LA        R1,PWTOR            SETUP FOR WTOR MACRO
         MVC       PWTOMES+5(8),=C'1A PAUSE'
         LA        R4,PAUSECDS         SET POINTER TO MCS CODES    V1L4
         MVI       PAUSEECB,0          CLEAR PAUSE ECB             V1L4
POUTMES  CH        R3,PMESMAX          MAKE SURE ITS NOT TOO LONG
         BNH       PSHRTNUF
         LH        R3,PMESMAX          CHOP IT OFF, OH GLEE
PSHRTNUF EX        R3,PMOVEO           MOVE THE MESSAGE INTO THE MEDIUM
         LA        R3,13+1+1+4(,R3)    ADJUST LENGTH FOR CLAG      V1L4
         STH       R3,PMESLEN          STASH LENGTH
         LA        R2,ZR14+4           WE'LL RETURN VIA R2
         LA        R3,PWTOMES-4(R3)    WHERE TO PUT MCS CODES      V1L4
         MVC       0(4,R3),0(R4)       ADD MCS CODES TO MESSAGE    V1L4
         LR        R3,R15              GET A NEW BASE REGISTER     V1L4
         B         PWTOSVC
         SPACE     1                                               V1L4
PMOVEO   MVC       PWTO1+1(*-*),ZR2
         SPACE     1                                               V1L4
         $FREE     R15                                             V1L5
         USING     PSTOPN,R3
         SPACE     1                                               V1L4
         CNOP      0,4                 WTO/WTOR EXPANSION          V1L4
PWTOR    DC        AL1(1)              1 BYTE REPLY
         DC        AL3(PWTOMES+5)      PUT IT HERE
         DC        AL4(PAUSEECB)
PWTO     EQU       *
PMESLEN  DC        AL2(*-*),X'8000'    MESSAGE LENGTH, MCS FLAG    V1L4
PWTOMES  DC        C'IHC00',8C' '      GOOD LUCK TO OPERATOR
PWTO1    DC        (MESLEN-13)C' '     USER'S MESSAGE              V1L4
         DC        XL4'0'              EXTRA SPACE FOR MCS CODES   V1L4
         SPACE     1                                               V1L4
PWTOSVC  SVC       35                  WTO/WTOR SVC
****  WTO/WTOR CLOBBERS R0,1,15,SO WHO CARES
PSTOPSW  B         XSTOP               B/NOP SWITCH ,B FOR STOP
         WAIT      1,ECB=PAUSEECB      WAIT FOR REPLY FOR PAUSE
         BR        R2                  BACK TO PROGRAM
         SPACE     1                                               V1L4
PAUSEECB DC        F'0'                PAUSE ECB
STOPCDS  DC        X'02004000'         STOP : DESC=7,ROUTCDE=2     V1L4
PAUSECDS DC        X'40008000'         PAUSE: DESC=2,ROUTCDE=1     V1L4
PMESMAX  DC        AL2(MESLEN-13-2)    MAXMESSAGE-1                V1L4
         SPACE     1                                               V1L4
         $FREE     R3                                              V1L5
PCVD     EQU       XCMT1
         SPACE     1                                               V1L4
         LTORG
         SPACE     1                                               V1L4
         END
