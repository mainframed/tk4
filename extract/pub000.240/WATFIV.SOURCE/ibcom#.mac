ERRORS   DSECT
         CNOP      2,4
ERRN2    DC        H'0'                ADDRESS OF ERROR CODE
PARAMS   DC        F'0'
GERRSW   DC        X'00'
GRETURN  DC        AL3(*-*)
PARMSG   DC        C'0I/O ERROR   IN LINE          ',78C' '
         SPACE     2
GERRLIST DSECT
GERRMSG  DS        F
GERRDSRN DS        F
         DS        X
         DS        AL3
GERRNO   DS        F
R1       EQU       1
R0       EQU       0
R15      EQU       15
R3       EQU       3
R2       EQU       2
R4       EQU       4
XSYNFLAG EQU       X'04'
XENDFLAG EQU       X'08'
XOFFLAG  EQU       X'00'
XERRFLAG EQU       X'0C'
         SPACE     5
IBCOM@   CSECT
         ENTRY     IBCOM#,IHCER@
IBCOM#   EQU       *
IHCER@   EQU       *
*        WELL WE HAVE 60 BYTES TO FOOL WITH UNTIL FIRST ORG
         USING     IBCOM#,R15
         USING     GERRLIST,R1
         USING     ERRN2,R4
         B         GERRPROC
*        WE STILL HAVE ROOM FOR 7 MORE FIOCS 0R DIOCS ERROR CODES
*        THEIR CORRESPONDING WATFOR 'UN' TYPE CODES
GTABST   DC        AL1(212),AL2(UNE)                               V1L1
         DC        AL1(213),AL2(UND)                               V1L1
         DC        AL1(214),AL2(UNI)                               V1L1
         DC        AL1(217),AL2(UN1)
         DC        AL1(218),AL2(UN2)
         DC        AL1(219),AL2(UN3)
         DC        AL1(220),AL2(UN6)
         DC        AL1(231),AL2(UN8)
         DC        AL1(232),AL2(UNC)
         DC        AL1(233),AL2(UNB)
         DC        AL1(234),AL2(UNH)
         DC        AL1(235),AL2(UNA)
         DC        AL1(236),AL2(UNL)                               V1L1
         DC        AL1(237),AL2(UNB)
GTABEND  DC        AL1(*-*),AL2(KO7)
         PRINT     OFF
         ERRTABLE
         PRINT     ON,GEN
*        FIOCS WILL ENTER  HERE IF NO DD CARD CARD HAS
*              HAS BEEN SUPPLIED FOR THE ERROR UNIT
*              WATFOR USES THE PRINTER AND HENCE WILL
*              NOT DO ANY SPECIAL PROCESSING BUT RATHER
*              WILL CONTINUE IN ITS NORMAL FASHION
*    ********************************
         ORG       IBCOM#+X'3C'
*    ********************************
         B         GERRPROC            CONTINUE AS NAORMAL
GSYNAD   L         R3,GERRMSG          ADDR OF SYNAD ERROR
         MVC       PARMSG+29(78),30(3) MOVE IN MESSAGE
         MVI       GERRSW,XSYNFLAG     SET ERROR FLAG 1
         FREEMAIN  R,LV=96,A=(3)       CORE GOTEBY FIOCS
         B         RETURN
GENDFILE MVI       GERRSW,XENDFLAG     EOF FLAG
RETURN   L         15,GRETURN-1
         BR        15
*    ********************************
         ORG       IBCOM#+X'7C'
*    ********************************
SAVE     DC        X'FF'
         DC        AL3(*-*)
PLACE    DC        10F'0'
         DC        4F'0'
         DC        A(PLACE)
         DC        3F'0'
         DC        16F'0'
         USING     ERRN2,4
GERRPROC L         4,ERRLIST
         LA        2,GTABST            ERROR TAB ADDR
         MVC       GTABEND(1),GERRNO+3 SAVE CODE AT END OF TABLE
GLOOP1   CLC       0(1,R2),GERRNO+3
         BE        GOTIT1
         LA        R2,3(,R2)
         B         GLOOP1
GOTIT1   MVC       ERRN2(2),1(R2)
         CLC       GERRNO+2(2),=H'217'
         BE        GENDFILE
         CLC       GERRNO+2(2),=H'218'
         BE        GSYNAD
         MVI       GERRSW,XERRFLAG
         B         RETURN
ERRLIST  DC        V(ERR#2)
         LTORG
         END
