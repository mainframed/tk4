CLAB   TITLE   'COMPLEX ABSOLUTE VALUE FUNCTION (LONG)'
         PRINT     NOGEN
         DISK
         PRINT     GEN
         SPACE 2
CDABS#   CSECT
*
*      COMPLEX ABSOLUTE VALUE FUNCTION (LONG)
*              1. WRITE /X+IY/ = U+IV, WHERE U IS THE REAL PART
*                   OF THE ANSWER, AND V IS THE IMAGINARY PART OF
*                     THE ANSWER.
*              2. LET Z1 = MAX(/X/,/Y/) AND Z2 = MIN(/X/,/Y/).
*              3. THEN U = 2*Z1*SQRT(0.25+(Z2/(2*Z1))**2), AND V = 0.
       SPACE
       EXTRN   DSQRT
       ENTRY   CDABS
         ENTRY     CABS
       SPACE
GRA    EQU     1               ARGUMENT POINTER
GRS    EQU     13              SAVE AREA POINTER
GRR    EQU     14              RETURN REGISTER
GRL    EQU     15              LINK REGISTER
GR0    EQU     0               SCRATCH REGISTERS
GR1    EQU     1
FR0    EQU     0               ANSWER REGISTERS
FR2    EQU     2
FR6    EQU     6               SCRATCH REGISTER
ISNSQ  EQU     X'302'          IDENTIFIER NUMBER FOR DSQRT CALL
       SPACE
CABS     FENTZ
         CHCKA     0(R1),COMPL8
         L         R3,0(,R1)
         CHCKDEF   COMPL8,0(R3)
         LE        F0,ZR3
         STE       F0,CDABSWK
         LE        F0,ZR3+4
         STE       F0,CDABSWK+8
         CHCKL     4(R1),REAL4
         CNOP      0,4
         BAL       R1,CDABS
AXNAME   ARG       CDABSWK,COMPL16
         LARG      CDABSWK,REAL8
         DROP      R15
CDABSWK  DC        2D'0'
         SPACE     1
         CNOP      0,4
         SPACE     1
CDABS    STM       R14,R12,12(R13)
         BAL       R11,XENTSPEC
         USING     *,R11
         DC        X'0000'
         DC        CL6'CDABS'
         DC        V(XS@VE1)
         ARG       XCAB,COMPL16
         LARG      CDABS,REAL8
         CLC       XUNDEF(LCOMPL16),XCAB
         EX        R0,XNOPDEFN
         SPACE     1
         LD        FR6,XCAB         REAL PART
         LD        FR2,XCAB+8       IMAG PART
         SPACE     1
       LPER    FR6,FR6         FORCE SIGNS POSITIVE
       LPER    FR2,FR2
       SPACE
       CDR     FR6,FR2         A = MAX(/X/,/Y/), B = MIN(/X/,/Y/)
       BC      10,READY        IF NECESSARY SWITCH REGISTERS SO THAT
       LDR     FR0,FR2           IN ALL CASES FR6 CONTAINS A AND FR2
       LDR     FR2,FR6             CONTAINS B
       LDR     FR6,FR0
       SPACE
READY  LDR     FR0,FR6         IF CHARACTERISTICS ARE DIFFERENT BY
       SWR     FR0,FR2           15 OR MORE,OR B=0, THE ANSWER IS A.
       CDR     FR0,FR6           THIS IS DONE TO AVOID ANY
       BC      8,EXIT              INTERMEDIATE UNDERFLOW
       DDR     FR2,FR6         D = B/A
       HDR     FR2,FR2         TAKE SQRT OF 0.25+D*D/4
       MDR     FR2,FR2           THIS IS A MORE ACCURATE PROCEDURE
       AW      FR2,QUART           THAN SQRT OF 1+D*D
       SPACE
         STD       FR2,BUFF
         SPACE     1
         FCALL     (DSQRT,REAL8),(BUFF,REAL8)
       SPACE
       CE      FR6,THRESH      IF A IS VERY SMALL, AVOID PREMATURE
       BC      4,SMALL           UNDERFLOW BY CHANGING THE ORDER OF
       MDR     FR0,FR6             MULTIPLICATION
       ADR     FR0,FR0
       SPACE
EXIT   SDR     FR2,FR2
         SDR       FR6,FR6         ***WATFIV CONVENTION***
         B         XFRET
       SPACE
SMALL  ADR     FR6,FR6
       MDR     FR0,FR6
       BC      15,EXIT
       SPACE
       DS      0F
BUFF   DS      D
XCAB     DC        2D'0'
QUART  DC      X'4040000000000001'     0.25 WITH ROUNDING FUDGE
THRESH DC      X'00200000'
       END
