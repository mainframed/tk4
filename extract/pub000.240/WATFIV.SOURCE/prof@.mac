         TITLE     '            WATFIV PROGRAM PROFILER'           V1L5
         COPY      OPTIONS                                         V1L5
         PRINT     OFF                                             V1L5
         COPY      CDEFN                                           V1L5
STARTA   DSECT                                                     V1L5
         COPY      STARTA                                          V1L5
COMMR    DSECT                                                     V1L5
         COPY      COMMR                                           V1L5
         ERRTABLE
         COPY      RESOLVE             RESOLVE EXTERNAL REFERENCES V1L5
         PRINT     ON,GEN                                          V1L5
         TITLE     '            WATFIV PROGRAM PROFILER'           V1L5
PROFL#   CSECT                                                     V1L5
         ENTRY     PROF@
PROF@    DS        0H
MPROFILE STM       R0,R14,XHELPS       SAVE THE REGISTERS          V1L5
         BALR      R11,0                                           V1L5
         USING     *,R11               SET UP A BASE REGISTER      V1L5
         L         R1,XPAGES           GET THE NUMBER OF PAGES     V1L5
         LA        R1,4095(,R1)        BUMP IT UP                  V1L5
         ST        R1,XPAGES           STORE IT                    V1L5
         BAL       R9,CPRINT                                       V1L5
         SPRIN     MPROF1,MPROF1E-MPROF1   PRINT HEADER            V1L5
         L         R4,XTOTCNT1         GET COUNT                   V1L5
         LA        R3,MPROF2+6         CONVERT IT                  V1L5
         BAL       R14,MCVRTFR                                     V1L5
         SPRIN     MPROF2,MPROF2E-MPROF2                           V1L5
         L         R4,XTOTCNT2         GET COUNT                   V1L5
         LA        R3,MPROF3+6         CONVERT IT                  V1L5
         BAL       R14,MCVRTFR                                     V1L5
         SPRIN     MPROF3,MPROF3E-MPROF3                           V1L5
         SR        R0,R0                                           V1L5
         SR        R4,R4                                           V1L5
         MVC       MOUT+1(129),MOUT    BLANK OUT THE OUTPUT LINE   V1L5
         L         R1,XPRSTART                                     V1L5
MDEBUCNT CLI       6(R1),X'C0'         IS IT AN IF ?               V1L5
         BH        MTRYNEXT            HIGHER CODE                 V1L5
         CLC       7(3,R1),KF0         WAS IT EXECUTED ?           V1L5
         BNE       MTRYNEXT            YES                         V1L5
         LA        R4,1(,R4)           NO, BUMP COUNT              V1L5
MTRYNEXT CLC       3(3,R1),KF0         IS THIS THE LAST STATEMENT  V1L5
         BE        MFINICNT            YES                         V1L5
         L         R1,2(,R1)           NO, GET THE NEXT ONE        V1L5
         B         MDEBUCNT                                        V1L5
         SPACE     1                                               V1L5
MFINICNT LA        R3,MPROF4+6         CONVERT                     V1L5
         BAL       R14,MCVRTFR                                     V1L5
         SPRIN     MPROF4,MPROF4E-MPROF4                           V1L5
         TM        CCNTSW,XPROFC       COUNTS WANTED ?             V1L5
         BNO       MDONE1              NO                          V1L5
         MVI       MOUT,C'-'           TRIPLE SPACE                V1L5
         MVC       MOUT+51(24),MHEADC  MOVE IN HEADER              V1L5
         SPRIN     MOUT,75             PRINT HEADER                V1L5
         OI        CCNTSW,MONETYPE     ONE TYPE PRINTED            V1L5
         THEN      (MHEAD1SW,OFF)      HEADER1 NOT PRINTED YET     V1L5
         THEN      (MHEAD2SW,OFF)      HEADER2 NOT PRINTED YET     V1L5
         LA        R3,MOUT+1           SET UP POINTER              V1L5
         THEN      (MBREAKSW,ON)       TURN BREAK SWITCH ON        V1L5
         THEN      (MNONEXSW,OFF)  TURN OFF NON-EXECUTED STMT SW   V1L5
         ST        R0,MDOCOUNT         ZERO THE DOLOOP COUNT       V1L5
         MVI       MOUT,C' '           CONTROL CHARACTER           V1L5
         MVC       MOUT+1(129),MOUT    BLANK OUT THE LINE          V1L5
         L         R1,XPRSTART         GET THE START OF THE LIST   V1L5
MSTPROF  CLC       7(3,R1),KF0         IS IT A ZERO COUNT ?        V1L5
         BNE       MPROCESS            NO                          V1L5
         CLC       3(3,R1),KF0         IS THIS THE LAST STMT ?     V1L5
         BE        MFINI               YES                         V1L5
         L         R1,2(,R1)           YES, TRY THE NEXT ONE       V1L5
         B         MSTPROF                                         V1L5
         SPACE     1                                               V1L5
MPROCESZ SPRIN     MPROF5,MPROF5E-MPROF5                           V1L5
MPROCESS LH        R4,0(,R1)           GET THE ISN                 V1L5
         LR        R5,R4               LET IT BE THE "TO" FOR NOW  V1L5
         CLI       6(R1),X'C0'         IS THIS AN IF STMT ?        V1L5
         BNL       MCHKCODE            C0 OR GREATER               V1L5
         L         R6,6(,R1)           NORMAL STMT, GET COUNT      V1L5
MBRKOFF  THEN      (MBREAKSW,OFF)      TURN OFF THE BREAK SWITCH   V1L5
         B         MNEXTISN            GO GET THE NEXT ISN         V1L5
         SPACE     1                                               V1L5
MCHKCODE L         R6,7(,R1)           GET THE COUNT               V1L5
         SRL       R6,8                                            V1L5
         BNE       MCHKMORE            IS IT C0 ?,   NO            V1L5
         IF        (MNONEXSW,ON),MBRKOFF     YES IT'S C0           V1L5
         C         R6,10(,R1)          ARE THE COUNTS EQUAL ?      V1L5
         BE        MREADAHD            YES, GO READ AHEAD          V1L5
         BAL       R8,MBREAK           NO, OUTPUT THE FIRST PART   V1L5
         L         R6,10(,R1)          GET THE SECOND COUNT        V1L5
         CR        R6,R0               IS IT ZERO ?                V1L5
         BE        MNEXTISN            YES, GO GET THE NEXT ISN    V1L5
         BAL       R14,MCVRTFR         NO, CONVERT "FROM"          V1L5
         MVC       11(6,R3),=C'OBJECT'    INDICATE OBJ OF STMT     V1L5
         BAL       R14,MCVRTCNT        CONVERT THE COUNT           V1L5
         BAL       R8,MBUMPPTR         BUMP THE OUTPUT LINE PTR    V1L5
         B         MNEXTISN            GET THE NEXT ISN            V1L5
         SPACE     1                                               V1L5
MCHKMORE CLI       6(R1),X'C2'         ENDIF ?                     V1L5
         BNL       MGTC1               C2,OR GREATER               V1L5
         BAL       R14,MCVRTFR         C1, CONVERT "FROM"          V1L5
         MVC       12(5,R3),=C'ENDIF'  INDICATE ENDIF              V1L5
         IF        (MNONEXSW,ON),MSKIPZ1                           V1L5
         BAL       R14,MCVRTCNT        CONVERT THE COUNT           V1L5
MSKIPZ1  BAL       R8,MBUMPPTR         BUMP THE OUTPUT LINE PTR    V1L5
         B         MNEXTISN            GO GET THE NEXT ISN         V1L5
         SPACE     1                                               V1L5
MGTC1    BNE       MGTC2               ELSEDO ?                    V1L5
         BAL       R14,MCVRTFR         YES, CONVERT "FROM"         V1L5
         MVC       11(6,R3),=C'ELSEDO'   INDICATE ELSEDO           V1L5
         IF        (MNONEXSW,ON),MSKIPZ2                           V1L5
         BAL       R14,MCVRTCNT        CONVERT THE COUNT           V1L5
MSKIPZ2  BAL       R8,MBUMPPTR         BUMP THE OUTPUT LINE PTR    V1L5
         B         MNEXTISN            GET THE NEXT ISN            V1L5
         SPACE     1                                               V1L5
MGTC2    CLI       6(R1),X'C4'         IS IT DOLOOP ?              V1L5
         BE        MEQC4               YES                         V1L5
         BAL       R14,MCVRTFR         C3, CONVERT "FROM"          V1L5
         MVC       9(8,R3),=C'ENDWHILE'   INDICATE ENDWHILE        V1L5
         IF        (MNONEXSW,ON),MSKIPZ3                           V1L5
         BAL       R14,MCVRTCNT        CONVERT THE COUNT           V1L5
MSKIPZ3  BAL       R8,MBUMPPTR         BUMP THE OUTPUT LINE PTR    V1L5
         B         MNEXTISN            GO GET THE NEXT ISN         V1L5
         SPACE     1                                               V1L5
MEQC4    BAL       R14,MCVRTFR         CONVERT "FROM"              V1L5
         L         R5,MDOCOUNT         GET LEVEL COUNT FOR DOLOOP  V1L5
         CR        R0,R5               IS THE DOLOOP COUNT = 0 ?   V1L5
         BNE       MSKIPDO             NO                          V1L5
         LR        R2,R1               SAVE R1                     V1L5
         LR        R8,R4               SAVE R4                     V1L5
         LA        R5,1                START THE COUNT AT ONE      V1L5
MLOOP    L         R2,2(,R2)           POINT AT THE NEXT ISN       V1L5
         C         R2,=X'FF000000'     DOES IT EXIST ?             V1L5
         BE        MQUITC4             NO                          V1L5
         CLI       6(R2),X'C4'         IS IT A DOLOOP ?            V1L5
         BNE       MQUITC4             NO                          V1L5
         CH        R8,0(,R2)           IS THE ISN THE SAME ?       V1L5
         BNE       MQUITC4             NO                          V1L5
         LA        R5,1(,R5)           BUMP THE COUNT OF DOLOOPS   V1L5
         B         MLOOP               TRY THE NEXT ONE            V1L5
         SPACE     1                                               V1L5
MQUITC4  ST        R5,MDOCOUNT         SAVE THE COUNT              V1L5
MSKIPDO  BAL       R14,MCVRTTO         CONVERT THE LEVEL COUNT     V1L5
         MVC       10(5,R3),=C'LEVEL'  MOVE OUT 'LEVEL'            V1L5
         S         R5,KF1              KNOCK ONE OFF THE COUNT     V1L5
         ST        R5,MDOCOUNT         SAVE LEVEL COUNT            V1L5
         IF        (MNONEXSW,ON),MSKIPZ4                           V1L5
         BAL       R14,MCVRTCNT        CONVERT THE COUNT           V1L5
MSKIPZ4  BAL       R8,MBUMPPTR         BUMP THE OUTPUT LINE PTR    V1L5
         SPACE     1                                               V1L5
MNEXTISN L         R1,2(,R1)           GET A(NEXT ISN)             V1L5
         C         R1,=X'FF000000'     ARE THERE NO MORE ?         V1L5
         BE        MCNTDONE            RIGHT, NO MORE              V1L5
         IF        (MNONEXSW,OFF),MSKIPZ5                          V1L5
         CLC       7(3,R1),KF0         IS COUNT ZERO ?             V1L5
         BE        MSKIPA              YES                         V1L5
         C         R0,10(,R1)          NO, IS 2ND COUNT ZERO ?     V1L5
         BNE       MNEXTISN            NO, TRY THE NEXT ONE        V1L5
M2NDZERO IF        (MBREAKSW,ON),MSKIPOUT YES, TAKE CARE OF IT     V1L5
         BAL       R8,MBREAK           PUT OUT THE LAST ONE        V1L5
MSKIPOUT LH        R4,0(,R1)           GET "FROM"                  V1L5
         BAL       R14,MCVRTFR         CONVERT "FROM"              V1L5
         MVC       11(6,R3),=C'OBJECT' INDICATE OBJECT OF STMT     V1L5
         BAL       R8,MBUMPPTR         BUMP THE OUTPUT LINE PTR    V1L5
         THEN      (MBREAKSW,ON)       TURN ON THE BREAK SWITCH    V1L5
         B         MNEXTISN            GO GET THE NEXT ISN         V1L5
         SPACE     1                                               V1L5
MSKIPZ5  CLC       7(3,R1),KF0         IS COUNT = 0 ?              V1L5
         BE        MNEXTISN            YES                         V1L5
MSKIPA   IF        (MBREAKSW,ON),MPROCESS GO PROCESS THIS ONE      V1L5
         SPACE     1                                               V1L5
MISNDIFF LH        R2,0(,R1)           GET THE ISN                 V1L5
         S         R2,KF1              SUBTRACT ONE FROM IT        V1L5
         CR        R2,R5               IS THE ISN DIFF = 1 ?       V1L5
         BE        MCDGTC0             YES                         V1L5
MSETBRK  BAL       R8,MBREAK           GO OUTPUT THE LAST ONE      V1L5
         B         MPROCESS            GO PROCESS THIS ONE         V1L5
         SPACE     1                                               V1L5
MCDGTC0  CLI       6(R1),X'C0'         IS IT C0 ?                  V1L5
         BH        MSETBRK             NO, IT'S HIGHER             V1L5
         BNE       MCODEQ0             NO                          V1L5
         IF        (MNONEXSW,ON),MGETNEXT                          V1L5
         L         R9,7(,R1)           GET THE COUNT               V1L5
         SRL       R9,8                                            V1L5
         CR        R6,R9               IS THIS COUNT = LAST COUNT  V1L5
         BNE       MSETBRK             NO                          V1L5
         CLC       7(3,R1),11(R1)      YES, IS COUNT=C0 COUNT ?    V1L5
         LH        R5,0(,R1)           GET "TO"                    V1L5
         BE        MSKIPC              YES                         V1L5
         BAL       R8,MBREAK           NO, PRINT THIS ONE          V1L5
         C         R0,10(,R1)          IS THE C0 COUNT = 0 ?       V1L5
         BE        MNEXTISN            YES                         V1L5
         LR        R4,R5               GET "FROM"                  V1L5
         BAL       R14,MCVRTFR         NO, CONVERT "FROM"          V1L5
         MVC       11(6,R3),=C'OBJECT'   INDICATE OBJECT OF STMT   V1L5
         L         R6,10(,R1)          GET THE COUNT               V1L5
         BAL       R14,MCVRTCNT        CONVERT THE COUNT           V1L5
         BAL       R8,MBUMPPTR         BUMP THE OUTPUT LINE PTR    V1L5
         B         MNEXTISN            GO TURN OFF THE IF SWITCH   V1L5
         SPACE     1                                               V1L5
MSKIPC   BAL       R9,MRDAHEAD         GO CHECK THE NEXT ISN       V1L5
MGETNEXT LH        R5,0(,R1)           GET "TO"                    V1L5
         THEN      (MBREAKSW,OFF)      TURN OFF THE BREAK SWITCH   V1L5
         B         MNEXTISN            GO GET THE NEXT ISN         V1L5
         SPACE     1                                               V1L5
MCODEQ0  C         R6,6(,R1)           ARE THE COUNTS EQUAL ?      V1L5
         BE        MGETNEXT            YES, GO GET THE NEXT ISN    V1L5
         BAL       R8,MBREAK           PUT THE LAST ONE OUT        V1L5
         B         MPROCESS            GO PROCESS THIS ONE         V1L5
         SPACE     1                                               V1L5
MREADAHD BAL       R9,MRDAHEAD         CHECK THE NEXT ISN          V1L5
         THEN      (MBREAKSW,OFF)      TURN OFF BREAK SWITCH       V1L5
         B         MNEXTISN            GO GET THE NEXT ISN         V1L5
         SPACE     1                                               V1L5
MRDAHEAD L         R7,2(,R1)           GET A(NEXT ISN)             V1L5
         C         R7,=X'FF000000'     ARE THERE NO MORE ?         V1L5
         BE        MIFOUT              RIGHT, NO MORE              V1L5
         CLI       6(R7),X'C0'         IS THE CODE GREATER THAN C0 V1L5
         BH        MIFOUT              YES IT IS                   V1L5
         LH        R2,0(,R7)           GET THE ISN                 V1L5
         S         R2,KF1              SUBTRACT ONE FROM IT        V1L5
         CR        R2,R5               IS IT ONE MORE ?            V1L5
         BNE       MIFOUT              NO                          V1L5
         L         R2,7(,R7)           GET THE COUNT               V1L5
         SRL       R2,8                                            V1L5
         CR        R6,R2               IS IT THE SAME ?            V1L5
         BNE       MIFOUT              NO                          V1L5
         BR        R9                  IT'S OK, RETURN             V1L5
         SPACE     1                                               V1L5
MIFOUT   LH        R5,0(,R1)           GET "TO"                    V1L5
         BAL       R8,MBREAK           PUT THIS ONE OUT            V1L5
         B         MNEXTISN            GO GET THE NEXT ISN         V1L5
         SPACE     1                                               V1L5
MBREAK   BAL       R14,MCVRTFR         CONVERT "FROM"              V1L5
         BAL       R14,MCVRTTO         CONVERT "TO"                V1L5
         IF        (MNONEXSW,ON),MBUMPPT2                          V1L5
         BAL       R14,MCVRTCNT        CONVERT THE COUNT           V1L5
MBUMPPTR IF        (MNONEXSW,OFF),MSKIPZ7                          V1L5
MBUMPPT2 LA        R3,26(,R3)          BUMP THE POINTER            V1L5
         B         MSKIPZ8                                         V1L5
MSKIPZ7  LA        R3,32(,R3)          BUMP THE POINTER            V1L5
MSKIPZ8  C         R3,=A(MOUT+120)     IS THE LINE FULL ?          V1L5
         THEN      (MBREAKSW,ON)       TURN ON THE BREAK SWITCH    V1L5
         BLR       R8                  NO, RETURN                  V1L5
         IF        (MHEAD1SW,ON),MSKIPHD1                          V1L5
         SPRIN     MHEADER,MHEADERE-MHEADER                        V1L5
         THEN      (MHEAD1SW,ON)       HEADER1 HAS BEEN PRINTED    V1L5
MSKIPHD1 IF        (MNONEXSW,OFF),MSKIPHD2                         V1L5
         IF        (MHEAD2SW,ON),MSKIPHD2                          V1L5
         SPRIN     MHEADER2,MHEAD2E-MHEADER2                       V1L5
         THEN      (MHEAD2SW,ON)       HEADER2 HAS BEEN PRINTED    V1L5
MSKIPHD2 SPRIN     MOUT,MOUTE-MOUT     YES, PRINT IT               V1L5
         LA        R3,MOUT+1           SET UP THE POINTER          V1L5
         MVC       MOUT+1(129),MOUT    BLANK OUT THE OUTPUT LINE   V1L5
         BR        R8                  RETURN                      V1L5
         SPACE     1                                               V1L5
MCVRTFR  CVD       R4,MTEMP1           CONVERT "FROM"              V1L5
         MVC       1(7,R3),XSSMASK+4                               V1L5
         ED        0(8,R3),MTEMP1+4                                V1L5
         BR        R14                                             V1L5
         SPACE     1                                               V1L5
MCVRTTO  CVD       R5,MTEMP1           CONVERT "TO"                V1L5
         MVC       10(7,R3),XSSMASK+4                              V1L5
         ED        9(8,R3),MTEMP1+4                                V1L5
         BR        R14                 RETURN                      V1L5
         SPACE     1                                               V1L5
MCVRTCNT CVD       R6,MTEMP1           CONVERT THE COUNT           V1L5
         MVC       19(7,R3),XSSMASK+4                              V1L5
         ED        18(8,R3),MTEMP1+4                               V1L5
         BR        R14                                             V1L5
         SPACE     1                                               V1L5
MCNTDONE IF        (MBREAKSW,ON),MCHKDONE  ARE WE DONE ?           V1L5
         BAL       R8,MBREAK           NO, PRINT THIS ONE          V1L5
         B         MCHKDONE            GO CHECK THE POINTER        V1L5
MPRTCHK  IF        (MHEAD1SW,ON),MSKIPH1                           V1L5
         STM       R0,R15,XPRINTS                                  V1L5
         LA        R1,MHEADER          A(START OF THE LINE)        V1L5
         LR        R2,R3               A(END OF THE LINE)          V1L5
         S         R2,=A(MOUT+1)       LENGTH = A(END) - A(START)  V1L5
         BAL       R14,XPRINT          GO PRINT IT                 V1L5
         LM        R0,R14,XPRINTS                                  V1L5
         THEN      (MHEAD1SW,ON)       HEADER1 HAS BEEN PRINTED    V1L5
MSKIPH1  IF        (MNONEXSW,OFF),MSKIPH4                          V1L5
         IF        (MHEAD2SW,ON),MSKIPH4                           V1L5
         STM       R0,R15,XPRINTS                                  V1L5
         LA        R1,MHEADER2         A(START OF THE LINE)        V1L5
         LR        R2,R3               A(END OF THE LINE)          V1L5
         S         R2,=A(MOUT+1)       LENGTH = A(END) - A(START)  V1L5
         BAL       R14,XPRINT          PRINT THE LINE              V1L5
         LM        R0,R15,XPRINTS                                  V1L5
MSKIPH4  SPRIN     MOUT,MOUTE-MOUT     PRINT THE LINE              V1L5
MCHKFINI IF        (MNONEXSW,ON),MDONE1                            V1L5
         MVC       MOUT+1(129),MOUT    BLANK OUT THE OUTPUT LINE   V1L5
         B         MFINI               FINISHED THE EXECUTED ONES  V1L5
         SPACE     1                                               V1L5
MCHKDONE C         R3,=A(MOUT+1)       IS THE POINTER AT THE START V1L5
         BE        MCHKFINI            YES                         V1L5
         B         MPRTCHK             NO, GO PRINT IT             V1L5
         SPACE     1                                               V1L5
MFINI    THEN      (MNONEXSW,ON)       NON-EXECUTED STMTS SW ON    V1L5
         LA        R3,MOUT+1           SET UP A POINTER            V1L5
         THEN      (MBREAKSW,ON)       TURN ON THE BREAK SWITCH    V1L5
         ST        R0,MDOCOUNT         ZERO DOLOOP COUNT           V1L5
         L         R1,XPRSTART         GET THE FIRST ISN           V1L5
MSTARTZ  CLC       7(3,R1),KF0         IS THE COUNT = 0 ?          V1L5
         BE        MPROCESZ            YES                         V1L5
         C         R0,10(,R1)          NO, IS THE 2ND COUNT = 0 ?  V1L5
         BNE       MCHKLAST            NO, GO CHECK FOR LAST ONE   V1L5
         SPRIN     MPROF5,MPROF5E-MPROF5                           V1L5
         B         M2NDZERO            GO PROCESS IT               V1L5
         SPACE     1                                               V1L5
MCHKLAST L         R1,2(,R1)           GET THE NEXT ONE            V1L5
         C         R1,=X'FF000000'     ARE THERE ANY MORE ?        V1L5
         BNE       MSTARTZ             YES                         V1L5
         SPACE     1                                               V1L5
MCONVERT EQU       MCVRTFR                                         V1L5
MDONE1   TM        CCNTSW,XPROFP     PERCENTAGE HISTOGRAM WANTED ? V1L5
         BNO       MDONE2              NO                          V1L5
         MVC       MOUT+1(43),MOUT     BLANK OUT THE FIRST PART    V1L5
         TM        CCNTSW,MONETYPE     IS THIS THE FIRST OUTPUT ?  V1L5
         BNO       MPFIRST             YES                         V1L5
         MVI       MOUT,C'1'           PAGE EJECT                  V1L5
         TM        KOMPSW,LINES0       LINES = 0 ?                 V1L5
         BNO       MPRINTP             NO                          V1L5
MPFIRST  MVI       MOUT,C'-'           YES                         V1L5
MPRINTP  MVC       MOUT+44(39),MHEADP  MOVE IN HEADER              V1L5
         SPRIN     MOUT,83             PRINT HEADER                V1L5
         OI        CCNTSW,MONETYPE     ONE TYPE PRINTED            V1L5
         THEN      (CLIFSW,OFF)        TURN OFF THE IF SWITCH      V1L5
         SPRIN     MPROF6,MPROF6E-MPROF6                           V1L5
         L         R2,XTOTCNT1         GET THE STMT COUNT          V1L5
         A         R2,XTOTCNT2         ADD THE OBJ STMT COUNT      V1L5
         L         R1,XPRSTART         POINT TO THE FIRST STMT     V1L5
MHISTO   MVC       MOUT(120),MLINE     GET A LINE FOR THE GRAPH    V1L5
         CLI       6(R1),X'C0'         IS THE CODE GT C0 ?         V1L5
         BH        MNEXT               YES, GO TRY THE NEXT ONE    V1L5
         CLC       7(3,R1),KF0         IS THE COUNT = 0 ?          V1L5
         BE        MNEXT               YES, TRY THE NEXT ONE       V1L5
         LH        R4,0(,R1)           GET THE ISN                 V1L5
         LA        R3,MOUT+1           POINT TO THE ISN FIELD      V1L5
         BAL       R14,MCONVERT        CONVERT THE ISN             V1L5
         L         R6,7(,R1)           GET THE COUNT               V1L5
MCALC    SRDA      R6,40                                           V1L5
         LR        R4,R7               R4 NOW HAS THE COUNT        V1L5
         LA        R3,MCOUNT           POINT TO THE COUNT FIELD    V1L5
         BAL       R14,MCONVERT        CONVERT THE COUNT           V1L5
         MH        R7,=H'100'          COUNT * 100                 V1L5
         DR        R6,R2               / TOTAL COUNT               V1L5
         SLL       R6,1                R6 * 2 (CHECK FOR ROUNDING) V1L5
         CR        R6,R2               IS 2*REM GT THE DIVISOR ?   V1L5
         BL        MSKIPRND            NO, DON'T ROUND IT          V1L5
         LA        R7,1(,R7)           YES, ADD ONE TO THE QUOT    V1L5
MSKIPRND CR        R7,R0               IS THE % = ZERO ?           V1L5
         BNH       MPRTLINE            YES                         V1L5
         S         R7,KF1              NO, SUBTRACT 1 FOR THE MVC  V1L5
         STC       R7,MVCLINE+1        STICK IT IN THE INSTRUCTION V1L5
MVCLINE  MVC       MGRAPH+1(1),MGRAPH  PUT IN THE STARS            V1L5
MPRTLINE SPRIN     MOUT,MLINEE-MLINE   PRINT A LINE OF THE GRAPH   V1L5
         IF        (CLIFSW,ON),MNEXT                               V1L5
         CLI       6(R1),X'C0'         IS THIS A C0 ?              V1L5
         BNE       MNEXT               NO                          V1L5
         C         R0,10(,R1)          YES, IS THE COUNT = 0 ?     V1L5
         BE        MNEXT               YES                         V1L5
         MVC       MOUT(120),MLINE     GET A LINE FOR THE GRAPH    V1L5
         MVC       MOUT+3(6),=C'OBJECT' INDICATE OBJ OF LAST STMT  V1L5
         L         R6,11(,R1)          GET THE COUNT               V1L5
         THEN      (CLIFSW,ON)         TURN ON THE IF SWITCH       V1L5
         B         MCALC               GO CALCULATE THE # OF STARS V1L5
         SPACE     1                                               V1L5
MNEXT    THEN      (CLIFSW,OFF)        TURN OFF THE IF SWITCH      V1L5
         L         R1,2(,R1)           POINT TO THE NEXT ISN       V1L5
         C         R1,=X'FF000000'     ARE THERE ANY MORE ?        V1L5
         BNE       MHISTO              YES                         V1L5
         LA        R3,MTOTCNT          POINT TO THE OUTPUT LINE    V1L5
         LR        R4,R2               GET THE TOTAL COUNT         V1L5
         BAL       R14,MCONVERT        CONVERT IT                  V1L5
         SPRIN     MPROF7,MPROF7E-MPROF7                           V1L5
         SPACE     1                                               V1L5
MDONE2   TM        CCNTSW,XPROFR       RELATIVE HISTOGRAM WANTED ? V1L5
         BNO       MDONEALL            NO                          V1L5
         MVC       MOUT+1(44),MOUT     BLANK OUT THE FIRST PART    V1L5
         TM        CCNTSW,MONETYPE     IS THIS THE FIRST OUTPUT ?  V1L5
         BNO       MRFIRST             YES                         V1L5
         MVI       MOUT,C'1'           PAGE EJECT                  V1L5
         TM        KOMPSW,LINES0       LINES = 0 ?                 V1L5
         BNO       MPRINTR             NO                          V1L5
MRFIRST  MVI       MOUT,C'-'           YES                         V1L5
MPRINTR  MVC       MOUT+45(37),MHEADR  MOVE IN HEADER              V1L5
         SPRIN     MOUT,82             PRINT HEADER                V1L5
         MVI       MOUT,C' '           CONTROL CHARACTER           V1L5
         MVC       MOUT+1(129),MOUT    BLANK OUT THE OUTPUT LINE   V1L5
         L         R1,XPRSTART         POINT TO THE FIRST STMT     V1L5
         MVC       MIN+1(3),7(R1)      MAKE MIN THE FIRST COUNT    V1L5
         MVC       MAX+1(3),7(R1)      MAKE MAX THE FIRST COUNT    V1L5
MFINDM   CLI       6(R1),X'C0'         IS THE CODE GT C0 ?         V1L5
         BH        MNEXTM              YES                         V1L5
         BNE       MNOTIF              IT'S NOT AN IF STMT         V1L5
         CLC       10(4,R1),MIN        IS THE 2ND COUNT LT MIN     V1L5
         BNL       MTRYMAX             NO                          V1L5
         MVC       MIN(4),10(R1)       YES, SAVE IT                V1L5
         B         MNEXTM                                          V1L5
MNOTIF   CLC       7(3,R1),MIN+1       COMPARE THE COUNT TO MIN    V1L5
         BNL       MTRYMAX             NOT LOWER, GO TRY MAX       V1L5
         MVC       MIN+1(3),7(R1)      LOWER, SAVE IT              V1L5
         B         MNEXTM              GET THE NEXT ONE            V1L5
         SPACE     1                                               V1L5
MTRYMAX  CLC       7(3,R1),MAX+1       COMPARE THE COUNT TO MAX    V1L5
         BNH       MNEXTM              NOT HIGHER, GET THE NEXT    V1L5
         MVC       MAX+1(3),7(R1)      HIGHER, SAVE IT             V1L5
MNEXTM   L         R1,2(,R1)           POINT TO THE NEXT STMT      V1L5
         C         R1,=X'FF000000'     ARE THERE ANY MORE ?        V1L5
         BNE       MFINDM              YES                         V1L5
         SPACE     1                                               V1L5
         L         R2,KF10                                         V1L5
         L         R8,MAX                                          V1L5
         S         R8,MIN              MAX - MIN                   V1L5
         C         R8,KF5              IS THE DIFF LE 5 ?          V1L5
         BNH       MNOLT5              YES                         V1L5
         ST        R8,MDIFF            SAVE THE DIFFERENCE         V1L5
MCHKMAX  SRDA      R8,32               NO, SHIFT R8 INTO R9        V1L5
         DR        R8,R2               DIFF / 10                   V1L5
         CR        R8,R0               IS THE REMAINDER = 0 ?      V1L5
         BE        MAKELINE            YES, GO MAKE THE LINE       V1L5
         L         R8,MDIFF            GET THE DIFFERENCE          V1L5
         LA        R8,1(,R8)           ADD ONE TO IT               V1L5
         ST        R8,MDIFF            SAVE IT                     V1L5
         B         MCHKMAX             GO CHECK THIS ONE           V1L5
         SPACE     1                                               V1L5
MNOLT5   LA        R3,MOUT+12          POINT TO THE FIRST COLUMN   V1L5
         MVC       MDIFF(4),KF5        MAKE THE DIFFERENCE = 5     V1L5
         L         R4,MIN              GET THE MIN                 V1L5
MCVRTON  BAL       R14,MCONVERT        CONVERT THE NUMBER          V1L5
         LA        R3,20(,R3)          BUMP THE LINE POINTER       V1L5
         LA        R4,1(,R4)           BUMP THE NUMBER             V1L5
         C         R3,=A(MOUT+117)     IS THE LAST ONE CONVERTED ? V1L5
         BNH       MCVRTON             NO, KEEP ON CONVERTING      V1L5
         B         MPRTOUT             YES, PRINT THE LINE         V1L5
         SPACE     1                                               V1L5
MAKELINE L         R4,MIN              GET THE MIN                 V1L5
         LA        R3,MOUT+12          POINT TO THE FIRST COLUMN   V1L5
MPRINT   BAL       R14,MCONVERT        CONVERT THE NUMBER          V1L5
         AR        R4,R9               ADD THE INCREMENT           V1L5
         LA        R3,10(,R3)          BUMP THE LINE POINTER       V1L5
         C         R3,=A(MOUT+117)     IS THE LAST ONE PRINTED     V1L5
         BNH       MPRINT              NO                          V1L5
MPRTOUT  MVI       MPROF6,C' '         CONTROL CHARACTER           V1L5
         SPRIN     MPROF6,18           PRINT   'STMT    COUNT'     V1L5
         MVI       MPROF6,C'0'         RESTORE THE CONTROL CHAR    V1L5
         SPRIN     MOUT,MOUTE-MOUT                                 V1L5
         MVC       MOUT+1(129),MOUT    RESTORE THE OUTPUT LINE     V1L5
         THEN      (CLIFSW,OFF)        TURN OFF THE IF SWITCH      V1L5
         L         R1,XPRSTART         POINT TO THE FIRST STMT     V1L5
         L         R9,MDIFF                                        V1L5
         L         R8,MIN                                          V1L5
MHISTREL MVC       MOUT(120),MLINE     GET A LINE FOR THE GRAPH    V1L5
         CLI       6(R1),X'C0'         IS THE CODE GT C0 ?         V1L5
         BH        MNEXTREL            YES                         V1L5
         CLC       7(3,R1),KF0         IS THE COUNT = ZERO ?       V1L5
         BE        MNEXTREL            YES                         V1L5
         LH        R4,0(,R1)           NO, GET THE ISN             V1L5
         LA        R3,MOUT+1           POINT TO THE ISN FIELD      V1L5
         BAL       R14,MCONVERT        CONVERT THE ISN             V1L5
         L         R6,7(,R1)           GET THE COUNT               V1L5
MCALCREL SRDA      R6,40                                           V1L5
         LR        R4,R7               GET THE COUNT               V1L5
         LA        R3,MCOUNT           SET UP THE POINTER          V1L5
         BAL       R14,MCONVERT        CONVERT THE COUNT           V1L5
         SR        R7,R8               COUNT - MIN                 V1L5
         MH        R7,=H'100'          * 100                       V1L5
         LR        R2,R9                                           V1L5
         DR        R6,R2               100(COUNT-MIN)/(MAX-MIN)    V1L5
         SLL       R6,1                R6 * 2                      V1L5
         CR        R6,R2               SHOULD IT BE ROUNDED ?      V1L5
         BL        MRELGT0             NO                          V1L5
         LA        R7,1(,R7)           YES, ADD ONE                V1L5
MRELGT0  CR        R7,R0               IS THE # OF STARS = 0 ?     V1L5
         BE        MPRINTRL            YES, GO PRINT IT            V1L5
         S         R7,KF1              NO, CALC # FOR MVC          V1L5
         STC       R7,MVCREL+1         STICK # IN MVC INSTR        V1L5
MVCREL   MVC       MGRAPH+1(1),MGRAPH  PUT IN THE STARS            V1L5
MPRINTRL SPRIN     MOUT,MOUTE-MOUT                                 V1L5
         IF        (CLIFSW,ON),MNEXTREL                            V1L5
         CLI       6(R1),X'C0'         IS IT A C0 ?                V1L5
         BNE       MNEXTREL            NO, GO GET THE NEXT ONE     V1L5
         C         R0,10(,R1)          IS THE 2ND COUNT = ZERO ?   V1L5
         BE        MNEXTREL            YES                         V1L5
         MVC       MOUT(120),MLINE     RESTORE THE GRAPH LINE      V1L5
         MVC       MOUT+3(6),=C'OBJECT' INDICATE OBJ OF LAST STMT  V1L5
         L         R6,11(,R1)          GET THE COUNT               V1L5
         THEN      (CLIFSW,ON)         TURN ON THE IF SWITCH       V1L5
         B         MCALCREL            GO CALC THE # OF STARS      V1L5
         SPACE     1                                               V1L5
MNEXTREL THEN      (CLIFSW,OFF)        TURN OFF THE IF SWITCH      V1L5
         L         R1,2(,R1)           POINT TO THE NEXT ISN       V1L5
         C         R1,=X'FF000000'     ARE THERE ANY MORE ?        V1L5
         BNE       MHISTREL            YES                         V1L5
         L         R4,XTOTCNT1         GET THE PRIMARY COUNT       V1L5
         A         R4,XTOTCNT2         ADD THE SECONDARY COUNT     V1L5
         LA        R3,MTOTCNT          SET UP THE POINTER          V1L5
         BAL       R14,MCONVERT        CONVERT THE TOTAL COUNT     V1L5
         SPRIN     MPROF7,MPROF7E-MPROF7  PRINT THE TOTAL COUNT    V1L5
MDONEALL LM        R0,R14,XHELPS       RESTORE THE REGISTERS       V1L5
         BR        R14                 RETURN                      V1L5
         SPACE     1                                               V1L5
MPROF1   DC        C'1',CL20' ',C'WATFIV PROGRAM PROFILE'          V1L5
MPROF1E  EQU       *                                               V1L5
MPROF2   DC        C'0',CL15' ',C'STATEMENT(S) EXECUTED'           V1L5
MPROF2E  EQU       *                                               V1L5
MPROF3   DC        C' ',CL15' ',C'SECONDARY STATEMENT(S) EXECUTED' V1L5
MPROF3E  EQU       *                                               V1L5
MPROF4   DC        C' ',CL15' ',C'STATEMENT(S) NOT EXECUTED'       V1L5
MPROF4E  EQU       *                                               V1L5
MPROF5   DC        C'0',CL15' ',C'THE FOLLOWING STATEMENTS WERE NOT EXEX
               CUTED'                                              V1L5
MPROF5E  EQU       *                                               V1L5
MPROF6   DC        C'0',CL8'    STMT',CL10'    COUNT '             V1L5
         DC        C'0%',CL7' ',C'10%',CL7' ',C'20%',CL7' '        V1L5
         DC        C'30%',CL7' ',C'40%',CL7' ',C'50%',CL7' '       V1L5
         DC        C'60%',CL7' ',C'70%',CL7' ',C'80%',CL7' '       V1L5
         DC        C'90%',CL7' ',C'100%'                           V1L5
MPROF6E  EQU       *                                               V1L5
MPROF7   DC        CL10' '                                         V1L5
MTOTCNT  DC        CL9' ',C'TOTAL STATEMENT(S) EXECUTED'           V1L5
MPROF7E  EQU       *                                               V1L5
MLINE    DC        CL19' ',C'*',CL9' ',C'.',CL9' ',C'.',CL9' '     V1L5
         DC        C'.',CL9' ',C'.',CL9' ',C'.',CL9' ',C'.',CL9' ' V1L5
         DC        C'.',CL9' ',C'.',CL9' ',C'.',CL9' ',C'.'        V1L5
MLINEE   EQU       *                                               V1L5
MHEADER  DC        C'0'                                            V1L5
         DC        CL32'    FROM       TO    COUNT'                V1L5
         DC        CL32'    FROM       TO    COUNT'                V1L5
         DC        CL32'    FROM       TO    COUNT'                V1L5
         DC        C'    FROM       TO    COUNT'                   V1L5
MHEADERE EQU       *                                               V1L5
MHEADER2 DC        C'0'                                            V1L5
         DC        CL26'    FROM       TO'                         V1L5
         DC        CL26'    FROM       TO'                         V1L5
         DC        CL26'    FROM       TO'                         V1L5
         DC        CL26'    FROM       TO'                         V1L5
         DC        C'    FROM       TO'                            V1L5
MHEAD2E  EQU       *                                               V1L5
MHEADC   DC        C'TABLE OF FREQUENCY COUNT'                     V1L5
MHEADP   DC        C'HISTOGRAM OF PERCENTAGE FREQUENCY COUNT'      V1L5
MHEADR   DC        C'HISTOGRAM OF ABSOLUTE FREQUENCY COUNT'        V1L5
MOUT     DC        C' ',130C' '                                    V1L5
MOUTE    EQU       *                                               V1L5
MCOUNT   EQU       MOUT+10                                         V1L5
MGRAPH   EQU       MOUT+19                                         V1L5
MONETYPE EQU       X'10'               ONE TYPE OF PROFILE OUTPUT  V1L5
MTEMP1   DC        D'0'                                            V1L5
MDOCOUNT DC        F'0'                                            V1L5
MIN      DC        F'0'                                            V1L5
MAX      DC        F'0'                                            V1L5
MDIFF    DC        F'0'                                            V1L5
MBREAKSW DS        X                                               V1L5
MHEAD1SW DS        X                                               V1L5
MHEAD2SW DS        X                                               V1L5
MNONEXSW DS        X                                               V1L5
         LTORG                                                     V1L5
.PROFM   ANOP                                                      V1L5
         END
