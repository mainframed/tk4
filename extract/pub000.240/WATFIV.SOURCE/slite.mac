         PRINT     NOGEN                                           V1L4
         DISK
         PRINT     GEN                                             V1L4
         SPACE     1                                               V1L4
SLITE#   CSECT                                                     V1L4
         SPACE     1                                               V1L4
         ENTRY     SLITE,SLITET                                    V1L4
         SPACE     1                                               V1L4
*                  SENSE LIGHT ROUTINES - SLITE , SLITET
         SPACE
**      MAXIMUM OF 8 LIGHTS BY THIS METHOD
***   PROGRAM CURRENTLY SET FOR 4
**    1-BIT IN POSITION I (1.LE.I.LE.4) OF SLITES MEANS LIGHT I IS ON
         SPACE
***    ERROR CONDITIONS - I .LT.0 .OR. I .GT. 4 FOR SLITE
*                      - I .LE. 0 .OR. I .GT. 4 FOR SLITET
         SPACE
SLITE    FENTZ
         CHCKA     0(R1),INTGER4
         L         R3,0(,R1)           ADDRESS OF I TO R3
         CHCKLSR   4(R1)               CHECK FOR LAST ARG
         CHCKDEF   INTGER4,0(R3)
         L         R2,ZR3              I TO R2                     V1L4
         C         R2,SLIMIT           IS IT GT LIMIT
         BH        ERRSLT
         LTR       R2,R2
         BM        ERRSLT              IS I NEGATIVE
         BZ        ALLOFF              LIGTHS OUT ,I =0
         IC        R2,LITEON-1(R2)
         EX        R2,SETLITE          TURN ON LITE I
         BR        R14                 BACK TO CALLER
ALLOFF   MVI       SLITES,X'00'        TURN OFF ALL LITES
         BR        R14                 BACK TO CALLER
SETLITE  OI        SLITES,0                                        V0L0
         SPACE     3
SLITET   FENTZ
         CHCKA     0(R1),INTGER4
         L         R3,0(,R1)
         CHCKDEF   INTGER4,0(R3)
         L         R2,ZR3              I TO R2                     V1L4
         CHCKA2    4(R1),INTGER4                                   V1L4
         IF        (4(R1),Z,UNCHNGNG),XERRSR41                     V1L4
         L         R3,4(,R1)                                       V1L4
         CHCKLSR   8(R1)               CHECK FOR LAST ARG
         C         R2,SLIMIT           IS I GT 4
         BH        ERRSLT
         LTR       R2,R2
         BNH       ERRSLT              IS I GT 0
         IC        R2,LITEON-1(R2)
         EX        R2,TESTLITE         IS LITE ON
         BZ        ITSOFF
         EX        R2,TURNOFF          IT'S ON,TURN IT OFF
         LA        R2,1
         B         STASHJ
ITSOFF   LA        R2,2                LITES OFF,J=2
STASHJ   ST        R2,ZR3              SET J                       V1L4
         BR        R14                 BACK TO CALLER
         SPACE
LITEON   DC        X'80402010'
TESTLITE TM        SLITES,0
TURNOFF  XI        SLITES,0
SLIMIT   EQU       KF4
SLITES   DC        X'00'               STORAGE FOR SENSE LIGTHS
         SPACE
*  LI-2  ARGUMENT NEGATIVE OR GREATER THAN ALLOWABLE LIMIT
ERRSLT   $ERROR    (TRAZ,LI,2)                                     V1L5
         END
