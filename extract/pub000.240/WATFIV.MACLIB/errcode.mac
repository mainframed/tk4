         MACRO
&NAME    ERRCODE   &LIST
         GBLC      &AC(8),&IN(21)
         GBLA      &EXPAND,&IACMAX,&IINMAX
         LCLA      &IAC,&IIN,&BUFV,&FLAG
         LCLC      &INP,&REG,&MESNO,&ERCD,&ACTN,&ACD,&NCD
&REG     SETC      '0'
&MESNO   SETC      '0'
         AIF       (&EXPAND GT 0).CHKACTN
.*  THE FOLLOWING DEFINITIONS ARE DONE ONCE PER ASSEMBLY
&EXPAND  SETA      1
&AC(1)   SETC      'LANG'              0
&AC(2)   SETC      'WARN'              1
&AC(3)   SETC      'BOOT'              2
&AC(4)   SETC      'ZERO'              3
&AC(5)   SETC      'NOEX'              4
&AC(6)   SETC      'NOAC'              5
&AC(7)   SETC      'TRAZ'              6
&IACMAX  SETA      8
&IN(1)   SETC      'SYM'               1
&IN(2)   SETC      'DEL'               2
&IN(3)   SETC      'STN'               3
&IN(4)   SETC      'ISN'               4
&IN(5)   SETC      'REL'               5
&IN(6)   SETC      'NAM'               6
&IN(7)   SETC      'CSN'               7
&IN(8)   SETC      'USN'               8
&IN(9)   SETC      'CHR'               9
&IN(10)  SETC      'VM8'              10
&IN(11)  SETC      'VM6'              11
&IN(12)  SETC      'VAR'              12
&IN(13)  SETC      'NAV'              13
&IN(14)  SETC      'NSS'              14
&IN(15)  SETC      'INT'              15
&IN(16)  SETC      'ARR'              16
&IN(17)  SETC      'NAL'              17
&IN(18)  SETC      'VLS'              18
&IN(19)  SETC      'LIN'              19
&IN(20)  SETC      'ENT'              20
&IINMAX  SETA      21
.*  CHECK ACTION PARAMETER
.CHKACTN ANOP
&ACTN    SETC      '&LIST(1)'
&AC(&IACMAX) SETC  '&ACTN'
.ACLOOP  ANOP
&IAC     SETA      &IAC+1
         AIF       ('&ACTN' NE '&AC(&IAC)').ACLOOP
         AIF       (&IAC NE &IACMAX).CHKCODE
         MNOTE     '    &ACTN IS INVALID ACTION PARAMETER'
.*    CHECK ERROR CODE PARAMETERS
.CHKCODE ANOP
&ACD     SETC      '&LIST(2)'
&NCD     SETC      '&LIST(3)'
         AIF       ('&ACD' NE 'POINTER').CHK1
&ERCD    SETC      '&NCD'
         AGO       .CHKINFO
.CHK1    AIF       ('&ACD' NE 'INREG').CHK2
&ERCD    SETC      '&NCD'.'+16'
         AGO       .CHKINFO
.CHK2    ANOP
&ERCD    SETC      '&ACD'.'&NCD'
.*     CHECK INFO PARAMETERS
.CHKINFO AIF       (T'&LIST(4) EQ 'O' OR K'&LIST(4) EQ 0).CHKMES
&FLAG    SETA      1
&INP     SETC      '&LIST(4)'(1,3)
&IN(&IINMAX) SETC  '&INP'
.INFLOOP ANOP
&IIN     SETA      &IIN+1
         AIF       ('&INP' NE '&IN(&IIN)').INFLOOP
         AIF       (&IIN NE &IINMAX).OKIN
         MNOTE     '    &INP IS INVALID INFO KEYWORD'
         AGO       .CHKMES
.OKIN    ANOP
&REG     SETC      '&LIST(4)'(4,8)
.*    CHECK MESSAGE PARAMETER
.CHKMES  AIF       (T'&LIST(5) EQ 'O' OR K'&LIST(5) EQ 0).CHKBUF
&FLAG    SETA      1
&MESNO   SETC      '&LIST(5)'
.*    CHECK BUFFER PARAMETER
.CHKBUF  AIF       (T'&LIST(6) EQ 'O' OR K'&LIST(6) EQ 0).OUTPUT
&BUFV    SETA      1
.OUTPUT  ANOP
&NAME    DC  0H'0',AL.1(&FLAG),AL.1(&BUFV),AL.1(0),AL.4(&IAC-1),AL.9(&E*
               RCD)
         AIF   (&FLAG EQ 0).END
         DC        AL.1(0),AL.6(&MESNO),AL.5(&IIN),AL.4(&REG)
.END     MEND
