         MACRO
&A       OFFSET    &B,&C,&D,&E
         GBLC      &OFFSET
         GBLA      &OFFSET#
&OFFSET# SETA      &OFFSET#+1
         AIF       ('&B'EQ'L').L
         AIF       ('&B'EQ'ST').ST
         AIF       ('&B'EQ'LR').LR
         AIF       ('&B'EQ'BR').BR
         AIF       ('&B'EQ'LRAL').LRAL
         AIF       ('&B'EQ'BALR').BALR
         AIF       ('&B'EQ'LLA').LLA
         AIF       ('&B'EQ'USING').USING
         AIF       ('&B'EQ'DROP').DROP
         AIF       ('&B'EQ'CNOP').CNOP
         AIF       ('&B'EQ'DC').DC
         AIF       ('&B'EQ'EQU').EQU
         AIF       ('&B'EQ'BLR').BLR
         AIF       ('&B'EQ'BER').BER
         AIF       ('&B'EQ'BHR').BHR
         MNOTE     '***ERROR*** ILLEGAL ''OFFSET'' OPERATION'
&A       DC        H'0'
         MEXIT
.L       AIF       ('&OFFSET'EQ'YES').LST
&A       L         &C,&D                                           V1L5
         MEXIT
.ST      AIF       ('&OFFSET'EQ'YES').LST
&A       ST        &C,&D
         MEXIT
.LST     ANOP
&A       &B        &C,&D.(RO)
         MEXIT
.LR      AIF       ('&OFFSET'EQ'YES').LR2
         AIF       ('&C'EQ'&D').LR1
         AGO       .COMMON
.LR2     ANOP
&A       LA        &C,Z&D.(RO)
.LR1     MEXIT
.BR      AIF       ('&OFFSET'EQ'YES').BR1
&A       BR        &C
         MEXIT
.BR1     ANOP
&A       B         Z&C.(RO)
         MEXIT
.BLR     AIF       ('&OFFSET'EQ'YES').BLR1
&A       BLR       &C
         MEXIT
.BLR1    ANOP
&A       BL        Z&C.(RO)
         MEXIT
.BER     AIF       ('&OFFSET'EQ'YES').BER1
&A       BER       &C
         MEXIT
.BER1    ANOP
&A       BE        Z&C.(RO)
         MEXIT
.BHR     AIF       ('&OFFSET'EQ'YES').BHR1
&A       BHR       &C
         MEXIT
.BHR1    ANOP
&A       BH        Z&C.(RO)
         MEXIT
.BALR    AIF       ('&OFFSET'EQ'NO').COMMON
&A       BAL       &C,Z&D.(RO)
         MEXIT
.LRAL    AIF       ('&OFFSET'EQ'YES').LRAL1
&A       L         &C,&D                                           V1L5
         MEXIT
.LRAL1   ANOP
&A       LR        &C,RO
         AL        &C,&D
         MEXIT
.LLA     AIF       ('&OFFSET'EQ'YES').LLA1
&A       L         &C,&D                                           V1L5
         MEXIT
.LLA1    ANOP
&A       LR        &C,RO
         AL        &C,&D.(RO)
         MEXIT
.USING   AIF       ('&OFFSET'EQ'YES').USING1
         USING     &C,&D
         MEXIT
.USING1  ANOP
         USING     &C,&E
         LA        &E,Z&D.(RO)
         MEXIT
.DROP    AIF       ('&OFFSET'EQ'YES').DROP1
         USING     Z&C,&C
         MEXIT
.DROP1   ANOP
         USING     Z&D,&D
         MEXIT
.COMMON  ANOP
&A       &B        &C,&D
         MEXIT
.CNOP    AIF       ('&C'EQ'BALR').CNOP2
         MNOTE     '***ERROR*** ILLEGAL ''OFFSET'' OPERATION'
&A       DC        H'0'
         MEXIT
.CNOP2   AIF       ('&OFFSET'EQ'YES').CNOP1
         CNOP      2,4
         MEXIT
.CNOP1   ANOP
         CNOP      0,4
         MEXIT
.DC      ANOP
&A       DC        &C.(&D)
         MEXIT
.EQU     AIF       ('&OFFSET'EQ'YES').EQU1
&A       EQU       &C
         MEXIT
.EQU1    ANOP
&A       EQU       &D
         MEND
