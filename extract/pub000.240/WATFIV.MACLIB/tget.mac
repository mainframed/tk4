         MACRO
&NAME    TGET  &BFF,&SIZE,&EDIT,&WAIT
.*********************************************************************.
.*                                                                    *
.*RELEASE 23 DELETIONS                                               *.
.*                                                                   *.
.*RELEASE 22 DELETIONS                                               *.
.*                                                                   *.
.*RELEASE 21 DELETIONS                                               *.
.*                                                                   *.
.*STATUS - CHANGE LEVEL 000                                          *.
.*                                                                   *.
.*NAME - TGET                                                        *.
.*                                                                   *.
.*FUNCTION - THIS MACRO IS USED TO MOVE A LINE OF INPUT FROM THE TIOC*.
.*   INPUT BUFFER TO THE USER'S BUFFER AREA.                         *.
.*                                                                   *.
.*********************************************************************.
.*
.*   TGET BUFFER AREA,BUFFER SIZE,EDIT,WAIT  (EDIT/WAIT ARE DEFAULT)
.*          (2-12)      (2-12)    ASIS NOWAIT
.*
         LCLA  &OPT
         LCLB  &SW1,&SW2
         LCLB  &E,&W,&RET
         LCLC  &ERROPT
&OPT     SETA  X'80'
         AIF   ('&BFF' EQ '' OR '&SIZE' EQ '').ERROR1  ERROR CHECK
         AIF   ('&EDIT' EQ 'R').RF      CHECK FOR R FORM
         AIF   ('&EDIT' EQ '').WAIT       SKIP IF NULL
         AIF   ('&EDIT' EQ 'EDIT').EDI    SET EDIT
         AIF   ('&EDIT' EQ 'ASIS').ASI    SET ASIS
         AIF   ('&EDIT' EQ 'WAIT').WAI    SET WAIT
         AIF   ('&EDIT' EQ 'NOWAIT').NOW  SET NOWAIT
&ERROPT  SETC  '&EDIT'                  SET FOR ERROR MSG
         AGO   .ERROR3
.WAIT    ANOP
&RET     SETB  1
         AIF   ('&WAIT' EQ '').CHKREG     SKIP IF NULL
         AIF   ('&WAIT' EQ 'EDIT').EDI    SET EDIT
         AIF   ('&WAIT' EQ 'ASIS').ASI    SET ASIS
         AIF   ('&WAIT' EQ 'WAIT').WAI    SET WAIT
         AIF   ('&WAIT' EQ 'NOWAIT').NOW  SET NOWAIT
&ERROPT  SETC  '&WAIT'                  SET FOR ERROR MSG
         AGO   .ERROR3
.EDI     ANOP
         AIF   (&E).ERROR2              DUP OPTION
&E       SETB  1                        EDIT OPTION SPECFIED
         AGO   .RET
.ASI     ANOP
         AIF   (&E).ERROR2              DUP OPTION
&E       SETB  1                        EDIT OPTION SPECFIED
&SW1     SETB  1                        ASIS OPTION
         AGO   .RET
.WAI     ANOP
         AIF   (&W).ERROR2              DUP OPTION
&W       SETB  1                        WAIT OPTION SPECIFIED
         AGO   .RET
.NOW     ANOP
         AIF   (&W).ERROR2              DUP OPTION
&W       SETB  1                        WAIT OPTION SPECIFIED
&SW2     SETB  1                        NOWAIT OPTION
         AGO   .RET
.RET     ANOP
         AIF   (&RET).CHKREG
         AGO   .WAIT                    NEXT POSITION
.CHKREG  SPACE 1
         CNOP  0,4
&OPT     SETA  &OPT+&SW2*16+&SW1        SET OPTIONS
         AIF   ('&BFF'(1,1) EQ '(' OR '&SIZE'(1,1) EQ '(').REGST REG.
.*                                      NOTATION
.*
.*   REGULAR EXPANSION
.*
&NAME    B     *+12                     BRANCH AROUND CONSTANTS
         DC    AL2(0)                   FILLER
         DC    AL2(&SIZE)               BUFFER SIZE
         DC    AL1(&OPT)                OPTION BITS
         DC    AL3(&BFF)                ADDR OF BUFFER
         LM    0,1,*-8                  LOAD PARAMETER REGISTERS
.SVC     SVC   93                       ISSUE SVC
         SPACE
         MEXIT
.*
.*   REGISTER NOTATION
.*
.REGST   ANOP
&NAME    B     *+8                      BRANCH AROUND CONSTANT
         AIF   ('&BFF'(1,1) EQ '(' AND '&SIZE'(1,1) EQ '(').BOTH BOTH
.*                                      PARAMETERS ARE IN REG. FORM
         AIF   ('&SIZE'(1,1) EQ '(').SZREG   SIZE IN REG
.*
.*   BUFFER ADDR IN REGISTER FORM
.*
         DC    AL1(&OPT)                OPTION BITS
         DC    AL3(0)                   BUFFER ADDR
         LA    1,0(,&BFF(1))            CLR HIGH ORDR BYTE + LD BFR ADR
         O     1,*-8                    SET OPTIONS
         LA    0,&SIZE                  LOAD SIZE OF BUFFER
         AGO   .SVC                     GOTO ISSUE SVC
.*
.*   BUFFER SIZE IN REGISTER FORM
.*
.SZREG   DC    AL1(&OPT)                OPTION BITS
         DC    AL3(&BFF)                BUFFER ADDR
         L     1,*-4                    LOAD PARAMETER REGISTER
         AIF   ('&SIZE' EQ '(0)').SVC   SIZE IN REG. 0
         LR    0,&SIZE(1)               LOAD BUFFER SIZE IN REG 0
         AGO   .SVC                     GOTO ISSUE SVC
.*
.*   WHEN BOTH ARE IN REGISTER NOTATION
.*
.BOTH    DC    AL1(&OPT)                OPTION BITS
         DC    AL3(0)                   BUFFER ADDR
         LA    1,0(,&BFF(1))            CLR HIGH ORDR BYTE + LD BFR ADR
         O     1,*-8                    SET OPTIONS
         AIF   ('&SIZE' EQ '(0)').SVC   GOTO SVC IF (0) SPECIFIED
         LR    0,&SIZE(1)               LOAD BUFFER SIZE IN REG 0
         AGO   .SVC                     GOTO ISSUE SVC
.RF      ANOP
         AIF   ('&WAIT' NE '').RERR
         AIF   ('&BFF'(1,1) NE '(' OR '&SIZE'(1,1) NE '(').RERR
&NAME    DS    0H
         AIF   ('&BFF' EQ '(1)').SIZE
         LR    1,&BFF(1)                LOAD BUFFER ADDRESS IN REG 1
.SIZE    AIF   ('&SIZE' EQ '(0)').SVC
         LR    0,&SIZE(1)               LOAD BUFFER SIZE IN REG 0
         AGO   .SVC
.RERR    IHBERMAC 192
         MEXIT
.ERROR1  IHBERMAC 24
         MEXIT
.ERROR2  IHBERMAC 54,,,
         MEXIT
.ERROR3  IHBERMAC 49,,&ERROPT
         MEND
