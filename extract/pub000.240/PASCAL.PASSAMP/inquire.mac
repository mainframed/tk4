(* after a program by A. Sale, published in Pascal News, CH July 79 *)

PROGRAM INVESTIGATEREPRESENTATION(OUTPUT);
        (* DETERMINE REAL NUMBER CHARACTERISTICS OF BASE MACHINE
           AND COMPILER.                  *)
VAR
    BASE,
    NUMBEROFDIGITS,
    I                      : INTEGER;
    ROUNDING               : BOOLEAN;
    EPSILON                : REAL;

 PROCEDURE ENQUIRY (VAR RADIX, DIGITS : INTEGER; VAR ROUNDS : BOOLEAN);

 VAR
    NUMBER,
    INCREMENT              : REAL;

 BEGIN  (* ENQUIRY *)
     (* FIND LARGE INTEGRAL VALUE JUST BEYOND INTEGER LIMITS *)
     NUMBER := 2;
     WHILE (((NUMBER+1)-NUMBER ) = 1 ) DO NUMBER := NUMBER * 2;
     (* MANUFACTURE THE NEXT LARGEST REAL VALUE *)
     INCREMENT := 2;
     WHILE ((NUMBER + INCREMENT) = NUMBER) DO
          INCREMENT := INCREMENT * 2;
     (* SUBTRACT THESE TO GIVE RADIX OF REPRESENTATION *)
     RADIX := TRUNC((NUMBER + INCREMENT) - NUMBER);
     (* SEE IF IT ROUNDS OR TRUNCATES BY ADDING (RADIX - 1)  *)
     ROUNDS := ((NUMBER + (RADIX + 1)) <> NUMBER);
     (* WORK OUT HOW MANY DIGITS IN MANTISSA *)
     DIGITS := 0;
     NUMBER := 1;
     WHILE (((NUMBER + 1) - NUMBER) = 1) DO
         BEGIN
          DIGITS := DIGITS + 1;
          NUMBER := NUMBER * RADIX;
         END;
     END;   (* ENQUIRY *)



BEGIN   (* MAIN PROGRAM *)
    (* FIND OUT BASIC PROPERTIES *)
    ENQUIRY(BASE,NUMBEROFDIGITS,ROUNDING);
    WRITELN(OUTPUT, ' BASE=',BASE:5);
    WRITELN(OUTPUT, ' NUMBER OF DIGITS=',NUMBEROFDIGITS:5);
    IF ROUNDING
       THEN WRITELN(OUTPUT, ' ROUNDED')
       ELSE WRITELN(OUTPUT, ' TRUNCATED');
    (* COMPUTE THE PRECISION BOUNDS *)
    EPSILON := 1;
    FOR I := 1 TO NUMBEROFDIGITS DO EPSILON := EPSILON/BASE;
    IF ROUNDING THEN EPSILON := EPSILON / 2;
    (* PRINT THE BEST AND WORST *)
    WRITELN(OUTPUT, ' BEST AND WORST PRECISIONS ARE ',
                     EPSILON,(EPSILON*BASE) );
END.
