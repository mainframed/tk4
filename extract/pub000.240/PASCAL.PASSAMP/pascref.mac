PROGRAM PASCREF(INPUT,OUTPUT);      (*$D-,L+,M+   N.WIRTH  2.7.75  *)
(*CROSS REFERENCE GENERATOR FOR PASCAL PROGRAMS*)
(*QUADRATIC QUOTIENT HASH METHOD*)
(*MODIFIED SLIGHTLY BY A. MICKEL 75/12/08 AND D. LALIBERTE
  78/03/15 TO PRODUCE PROCEDURE LIST AND SKIP COMPILER TITLE*)
(* PASCAL-6000 DEPENDENT CONSTRUCTS CHANGED. S.HAZEGHI,  02-15-79 *)
(* 6/86 - CORRECTIONS FOR COMPILER OUTPUT PROCESSING (LPP, LINE#)   *)
(*             INCREASED HASH TABLE SIZE; FIXED BRACED COMMENTS     *)
(*          G. POSTPISCHIL, EXPERT SYSTEM PROGRAMMING, INC. / 86180 *)
(*$D-  TESTS OFF*)
LABEL 99;
CONST P = 8999;     (*SIZE OF HASH TABLE*)                 (* 86180 *)
  NK = 33;          (*NO. OF KEYWORDS*)
  KLN = 10;         (*KEYLENGTH*)
  LPPG = 55;        (*NO. OF LINES PER PAGE*)
  LLMAX = 132;      (*LINE LENGTH DEFAULT MAX*)
  LLMIN = 72;       (*LINE LENGTH MINIMUM*)
  MAXN = 10000;     (*MAX NO. OF LINES*)
  DGPN =  6;        (*NO. OF DIGITS PER NUMBER*)
  LITL = 3;         (*NUMBER OF LINES IN COMPILER TITLE*)
  ADDRWIDTH = 11;        (*NUMBER OF DIGITS IN CODE ADDRESS / 86180 *)
  EMPTY = '          ';
  STARS = ' *****';
TYPE INDEX = 0..P;
  ALFA = PACKED ARRAY [1..KLN] OF CHAR;
  REF = @ITEM;
  WORD = RECORD KEY: ALFA;
           FIRST: REF;
         END ;
  ITEM = PACKED RECORD
           LNO: 0..MAXN;
           NEXT: REF
         END ;
  PROCREF = @PROC;  (*PROCEDURE OR FUNCTION REFERENCE*)
  PROC = PACKED RECORD
           NAME: ALFA;
           LNO: 0..MAXN;
           NEXT: PROCREF
         END ;
VAR I: INDEX;
  K: INTEGER;
  M: INTEGER;       (*NO. OF LINES ON PAGE*)
  N: INTEGER;       (*NO. OF LINES INPUT*)
  LN: INTEGER;      (*CURRENT LINE NUMBER*)
  OLDLN: INTEGER;               (* LINE NUMBER OF LAST LINE / 86180 *)
  LLNGOUT: INTEGER; (*LINE LENGTH FOR OUTPUT*)
  LLNGIN: INTEGER;  (*LINE LENGTH FOR INPUT*)
  CCOUNT: INTEGER;  (*CHARACTER COUNT IN LINE*)
  NOPL: INTEGER;    (*NO. OF LINE-NUMBERS PER LINE*)
  ID: RECORD CASE BOOLEAN OF
             FALSE: (A: ALFA);
             TRUE:  (ORD: INTEGER)
      END ;
  KEY: ARRAY [1..NK] OF ALFA;
  PROCORFUNC,
  COMPILERLISTING,
  LINENUMBERS: BOOLEAN;
  C1, C2, C3 : INTEGER;    (* ID, OCCUR, PROC. COUNTERS *)
  FIRSTPROC,
  PROCPTR: PROCREF; (*POINTERS TO CHAIN OF PROCEDURES*)
  T: ARRAY [INDEX] OF WORD;    (*HASH TABLE*)


   FUNCTION LETTER(C: CHAR): BOOLEAN;

     BEGIN
     LETTER := (('A' <= C) AND (C <= 'Z')) OR
               (('a' <= C) AND (C <= 'i')) OR
               (('j' <= C) AND (C <= 'r')) OR
               (('s' <= C) AND (C <= 'z')) ;
     END ;

   FUNCTION DIGIT(C: CHAR): BOOLEAN ;

     BEGIN
     DIGIT := ('0' <= C) AND (C <= '9') ;
     END ;

   FUNCTION SPECIAL(C: CHAR): BOOLEAN;
     BEGIN  SPECIAL := (C = '$') OR (C = '_')  END ;

FUNCTION NOKEY: BOOLEAN;
   VAR I,J,K: INTEGER;
BEGIN I := 1; J := NK;   (*BINARY SEARCH*)
  REPEAT K := (I+J) DIV 2;
    IF KEY[K] <= ID.A THEN I := K+1 ELSE J := K-1
  UNTIL I > J;
  IF J = 0 THEN NOKEY := TRUE ELSE
    NOKEY := KEY[J] <> ID.A
END (*NOKEY*) ;

PROCEDURE COUNTLINE;
BEGIN
  IF M >= LPPG THEN
    BEGIN PAGE(OUTPUT); WRITELN(OUTPUT); """WRITELN(OUTPUT); """
      M := 0
    END;
  M := M + 1
END (*COUNTLINE*) ;

PROCEDURE ADVANCE;
BEGIN
  WRITE(OUTPUT,INPUT@); GET(INPUT);
  CCOUNT := CCOUNT + 1;
  IF CCOUNT = LLNGIN THEN
    WHILE NOT EOLN(INPUT) DO
      BEGIN WRITE(OUTPUT,INPUT@); GET(INPUT);
      END
END (*ADVANCE*);

PROCEDURE SPACE(J: INTEGER);
BEGIN
  REPEAT J := J-1; WRITELN(OUTPUT); COUNTLINE
  UNTIL J = 0
END (*SPACE*) ;

PROCEDURE SKIPCOMPILERTITLE;                               (* 86180 *)
                                                           (* 86180 *)
  VAR I: INTEGER;                                          (* 86180 *)
BEGIN                                                      (* 86180 *)
  COMPILERLISTING := INPUT@ = '1';                         (* 86180 *)
  PAGE(OUTPUT);                                            (* 86180 *)
  IF COMPILERLISTING THEN                                  (* 86180 *)
  BEGIN I := 0; GET(INPUT);                                (* 86180 *)
    WHILE I < LITL DO                                      (* 86180 *)
      BEGIN I := I + 1;                                    (* 86180 *)
        WHILE NOT EOLN(INPUT) DO                           (* 86180 *)
          ADVANCE;                                         (* 86180 *)
        READLN; WRITELN(OUTPUT);                           (* 86180 *)
      END;                                                 (* 86180 *)
    """COUNTLINE;"""      (* GENERATES INCORRECT LINES/PAGE / 86180 *)
         M := 0;(* SET LINE COUNT CORRECTLY - WE JUST EJECTED/86180 *)
    LINENUMBERS := TRUE                                    (* 86180 *)
  END ELSE                                                 (* 86180 *)
  BEGIN WRITELN(OUTPUT); WRITELN(OUTPUT);                  (* 86180 *)
    LINENUMBERS := """INPUT@ IN ['0'..'9']""" DIGIT(INPUT@)(* 86180 *)
  END;                                                     (* 86180 *)
END (*SKIPCOMPILERTITLE*) ;                                (* 86180 *)

PROCEDURE HEADSHOP;                                        (* 86180 *)
BEGIN                                                      (* 86180 *)
  IF COMPILERLISTING AND NOT EOLN AND (INPUT@ = '1')       (* 86180 *)
    THEN SKIPCOMPILERTITLE                                 (* 86180 *)
END; (* HEADSHOP *)                                        (* 86180 *)
                                                           (* 86180 *)
PROCEDURE FLUSHLINE;                                       (* 86180 *)
BEGIN                                                      (* 86180 *)
  WHILE NOT EOLN DO                                        (* 86180 *)
    ADVANCE;                                               (* 86180 *)
END; (* FLUSHLINE *)                                       (* 86180 *)

PROCEDURE BLANKSKIP;                                       (* 86180 *)
BEGIN                                                      (* 86180 *)
  IF NOT EOLN AND (INPUT@ = ' ') THEN                      (* 86180 *)
    ADVANCE;                                               (* 86180 *)
END; (* BLANKSKIP *)                                       (* 86180 *)

PROCEDURE NEWLINE;
BEGIN CCOUNT := 0;
  HEADSHOP;      (* CHECK FOR COMPILER TITLE *)            (* 86180 *)
  OLDLN := LN;                         (* SAVE PRIOR LINE # / 86180 *)
  LN := 0;
  IF N < MAXN THEN
  BEGIN COUNTLINE;  N := N + 1;
    IF COMPILERLISTING THEN
       BEGIN
       WHILE NOT EOLN AND (INPUT@ <>  ')') DO              (* 86180 *)
         BEGIN                                             (* 86180 *)
         FOR I := 1 TO 9 DO                                (* 86180 *)
           BLANKSKIP;                                      (* 86180 *)
         IF NOT DIGIT(INPUT@) THEN FLUSHLINE;              (* 86180 *)
         WHILE DIGIT(INPUT@) DO                            (* 86180 *)
             BEGIN LN := 10*LN + ORD(INPUT@) - ORD('0');   (* 86180 *)
             ADVANCE;                                      (* 86180 *)
             END;                                          (* 86180 *)
         FOR I := 1 TO ADDRWIDTH DO                        (* 86180 *)
           IF NOT EOLN THEN ADVANCE;                       (* 86180 *)
       END;                                                (* 86180 *)
         IF NOT EOLN AND (INPUT@ <> ')') THEN FLUSHLINE    (* 86180 *)
                            ELSE ADVANCE;                  (* 86180 *)
       END                                                 (* 86180 *)
    ELSE WRITE(OUTPUT,' ');
    IF LINENUMBERS THEN
      BEGIN
      WHILE DIGIT(INPUT@) DO
"""   WHILE INPUT@ IN ['0'..'9'] DO   """
        BEGIN LN := 10*LN + ORD(INPUT@) - ORD('0');
          ADVANCE;
        END;
      END
    ELSE BEGIN
      LN := N;  WRITE(OUTPUT,LN:6, ') ')
      END
    END
  ELSE BEGIN
    WRITELN(STARS, ' TEXT TOO LONG', STARS);
    "GOTO 99"  EXIT(99);
    END;
    IF LN = 0 THEN LN := OLDLN         (* REL. TO PRIOR LINE /86180 *)
END (*NEWLINE*) ;

PROCEDURE SEARCH;   (*MODULO P HASH SEARCH*)
  VAR H,D: INDEX;
      X: REF; F: BOOLEAN;
      K: INTEGER;
BEGIN  I := ABS(ID.ORD);  H := I MOD P;
  F := FALSE; D := 1;
  NEW(X); X@.LNO := LN;
  REPEAT
    IF T[H].KEY = ID.A THEN
    BEGIN (*FOUND*) F := TRUE;
      X@.NEXT := T[H].FIRST; T[H].FIRST := X;
    END ELSE
    IF T[H].KEY = EMPTY THEN
    BEGIN (*NEW ENTRY*) F := TRUE;
      T[H].KEY := ID.A;
      T[H].FIRST := X; X@.NEXT := NIL
    END ELSE
    BEGIN (*COLLISION*) H := H+D; D := D+2;
      IF H >= P THEN H := H-P;
      IF D = P THEN
        BEGIN WRITELN(OUTPUT); WRITELN(STARS,' TABLE FULL',STARS);
        """GOTO 99"""   EXIT(99);
        END
    END
  UNTIL F
END (*SEARCH*) ;

PROCEDURE SORT(MIN, MAX: INTEGER);

(* QUICKSORT WITH BOUNDED RECURSION DEPTH *)
(* REQUIRES MIN <= MAX *)

   VAR
         LOW,
        HIGH: INDEX;
      MIDKEY: ALFA;
        TEMP: WORD;

   BEGIN
      REPEAT (*PICK SPLIT POINT*)
         MIDKEY := T[(MIN + MAX) DIV 2].KEY;
         LOW := MIN;
         HIGH := MAX;
         REPEAT (*PARTITION*)
            WHILE T[LOW].KEY < MIDKEY DO
               LOW := LOW + 1;
            WHILE T[HIGH].KEY > MIDKEY DO
               HIGH := HIGH - 1;
            IF LOW <= HIGH THEN
               BEGIN
                  TEMP := T[LOW];
                  T[LOW] := T[HIGH];
                  T[HIGH] := TEMP;
                  LOW := LOW + 1;
                  HIGH := HIGH - 1
               END;
         UNTIL LOW > HIGH;

         (*RECURSIVELY SORT SHORTER SUB-SEGMENT*)
         IF HIGH - MIN < MAX - LOW
         THEN
            BEGIN
               IF MIN < HIGH THEN
                  SORT(MIN, HIGH);
               MIN := LOW
            END
         ELSE
            BEGIN
               IF LOW < MAX THEN
                  SORT(LOW, MAX);
               MAX := HIGH
            END
      UNTIL MAX <= MIN
   END (*SORT*);


PROCEDURE NOTEPROC;   (*NOTE INSTANCE OF PROCEDURE OR FUNCTION*)
  VAR P: PROCREF;
BEGIN PROCORFUNC := FALSE;
  NEW(P); PROCPTR@.NEXT := P;
  P@.NAME := ID.A; P@.LNO := LN; P@.NEXT := NIL;
  PROCPTR := P
END (*NOTEPROC*) ;

PROCEDURE PRINTWORD(W: WORD);
  VAR L: INTEGER; X,Y,Z: REF;
BEGIN COUNTLINE; WRITE(OUTPUT,' ', W.KEY);
  X := W.FIRST; Y := X@.NEXT; X@.NEXT := NIL;
  WHILE Y <> NIL DO
    BEGIN Z := Y@.NEXT; Y@.NEXT := X; X := Y; Y := Z
    END ;
  L := 0;
  REPEAT
    IF L = NOPL THEN
      BEGIN L := 0; WRITELN(OUTPUT); COUNTLINE; WRITE(OUTPUT,' ', EMPTY)
      END;
    L := L+1; WRITE(OUTPUT,X@.LNO: DGPN); X := X@.NEXT; C2 := C2+1;
  UNTIL X = NIL;
  WRITELN(OUTPUT);
END (*PRINTWORD*) ;

PROCEDURE PRINTTABLE;
  VAR I,N: INDEX;
BEGIN N := 0;    (*COMPRESS TABLE*)
  FOR I := 0 TO P-1 DO
    IF T[I].KEY <> EMPTY THEN
      BEGIN T[N] := T[I]; N := N+1
      END ;
  IF N > 0 THEN SORT(0,N-1);
  NOPL := (LLNGOUT-KLN-1) DIV DGPN;
# M := LPPG; SPACE(1); WRITELN(' CROSS REFERENCE OF IDENTIFIERS,',
            ' LABEL DECLARATIONS AND GOTO STATEMENTS:');
# COUNTLINE; SPACE(1);  C1 := N;  C2 := 0;
  FOR I := 0 TO N-1 DO PRINTWORD(T[I])
END (*PRINTTABLE*) ;

PROCEDURE PRINTPROCS;
BEGIN SPACE(2); COUNTLINE;
  WRITELN(' LIST OF PROCEDURES AND FUNCTIONS:');
# C3 := 0;  COUNTLINE;  SPACE(1);
  PROCPTR := FIRSTPROC@.NEXT;
  WHILE PROCPTR <> NIL DO
    BEGIN WITH PROCPTR@ DO WRITELN(NAME:24,LNO:10);
#     C3 := C3+1;  COUNTLINE; PROCPTR := PROCPTR@.NEXT
    END;
# SPACE(2);
# WRITELN(OUTPUT, ' # OF IDENTIFIERS: ', C1:1, ',  # OF OCCURENCES: ',
#                 C2:1, ',  # OF PROCEDURES: ', C3:1, '.');
END (*PRINTPROCS*) ;

PROCEDURE INITIALIZE;
  TYPE SETTING = PACKED RECORD
                   CASE SWITCH: BOOLEAN OF
                     TRUE: (ONOFF: CHAR);
                     FALSE: (SIZE: 0..999999)
                   END;
  VAR S: SETTING;
  FUNCTION OPTION(NAME: CHAR; VAR S: SETTING): BOOLEAN;
    EXTERNAL;
BEGIN N := 0; M := 0;
  LLNGIN := LLMAX; LLNGOUT := LLMAX;
"""IF OPTION('U',S) THEN
    IF S.SWITCH AND (S.ONOFF = '+')
      THEN LLNGIN := LLMIN;
  IF OPTION('W',S) THEN
    IF S.SWITCH AND (S.ONOFF = '+')
      THEN LLNGOUT := LLMIN;    """
  FOR I := 0 TO P-1 DO T[I].KEY := EMPTY;
  NEW(PROCPTR); FIRSTPROC := PROCPTR; PROCPTR@.NEXT := NIL;
  PROCORFUNC := TRUE;   (*TO GET P R O G R A M NAME IN PROCEDURE INDEX*)
  KEY[ 1] := 'AND       '; KEY[ 2] := 'ARRAY     ';
  KEY[ 3] := 'BEGIN     '; KEY[ 4] := 'CASE      ';
  KEY[ 5] := 'CONST     '; KEY[ 6] := 'DIV       ';
  KEY[ 7] := 'DOWNTO    '; KEY[ 8] := 'DO        ';
  KEY[ 9] := 'ELSE      '; KEY[10] := 'END       ';
  KEY[11] := 'FILE      '; KEY[12] := 'FOR       ';
  KEY[13] := 'FUNCTION  '; KEY[14] := 'IF        ';
  KEY[15] := 'IN        '; KEY[16] := 'MOD       ';
  KEY[17] := 'NIL       '; KEY[18] := 'NOT       ';
  KEY[19] := 'OF        '; KEY[20] := 'OR        ';
  KEY[21] := 'PACKED    '; KEY[22] := 'PROCEDURE ';
  KEY[23] := 'PROGRAM   '; KEY[24] := 'RECORD    ';
  KEY[25] := 'REPEAT    '; KEY[26] := 'SET       ';
  KEY[27] := 'THEN      '; KEY[28] := 'TO        ';
  KEY[29] := 'TYPE      '; KEY[30] := 'UNTIL     ';
  KEY[31] := 'VAR       '; KEY[32] := 'WHILE     ';
  KEY[33] := 'WITH      '
END (*INITIALIZE*) ;

PROCEDURE SCANANDLISTINPUT;
BEGIN
  WHILE NOT EOF(INPUT) DO
  BEGIN NEWLINE;
    WHILE NOT EOLN(INPUT) DO
    CASE INPUT@ OF
     'a','b','c','d','e','f','g','h','i','j','k','l','m',
     'n','o','p','q','r','s','t','u','v','w','x','y','z',
     'A','B','C','D','E','F','G','H','I','J','K','L','M',
     'N','O','P','Q','R','S','T','U','V','W','X','Y','Z':
      BEGIN K := 0; ID.A := EMPTY;
        REPEAT
          IF K < KLN THEN
            BEGIN K := K+1; ID.A[K] := INPUT@
            END;
          ADVANCE
"""     UNTIL NOT(INPUT@ IN ['A'..'Z', '0'..'9']);   """
        UNTIL NOT(LETTER(INPUT@) OR DIGIT(INPUT@) OR SPECIAL(INPUT@));
        IF NOKEY THEN
        BEGIN SEARCH;
          IF PROCORFUNC THEN NOTEPROC
        END ELSE
        IF (ID.A = 'PROCEDURE ') OR (ID.A = 'FUNCTION  ') THEN
          PROCORFUNC := TRUE
      END;
     '0','1','2','3','4','5','6','7','8','9':
        REPEAT ADVANCE;
"""     UNTIL NOT (INPUT@ IN ['B','E','0'..'9']);  """
        UNTIL NOT DIGIT(INPUT@) ;
     '''':
      BEGIN (*STRING*)
        REPEAT ADVANCE;
        UNTIL (INPUT@ = '''') OR EOLN(INPUT);
        IF NOT EOLN(INPUT) THEN
          ADVANCE
      END;
#    '"':
      BEGIN (*COMMENT*)
        REPEAT ADVANCE;
          WHILE EOLN(INPUT) DO
            BEGIN WRITELN(OUTPUT); GET(INPUT); NEWLINE
            END
        UNTIL INPUT@ = '"';
        ADVANCE
      END;
     '¯':                                                   (* 86180 *)
      BEGIN (*COMMENT*)                                     (* 86180 *)
        REPEAT ADVANCE;                                     (* 86180 *)
          WHILE EOLN(INPUT) DO                              (* 86180 *)
            BEGIN WRITELN(OUTPUT); GET(INPUT); NEWLINE      (* 86180 *)
            END                                             (* 86180 *)
        UNTIL INPUT@ = 'ò';                                 (* 86180 *)
        ADVANCE                                             (* 86180 *)
      END;                                                  (* 86180 *)
     '(':
      BEGIN ADVANCE;
        IF INPUT@ = '*' THEN
        BEGIN (*COMMENT*) ADVANCE;
          REPEAT
            WHILE INPUT@ <> '*' DO
            BEGIN
              IF EOLN(INPUT) THEN
                BEGIN GET(INPUT); WRITELN(OUTPUT); NEWLINE
                END ELSE
                ADVANCE
            END ;
            ADVANCE
          UNTIL INPUT@ = ')';
          ADVANCE
        END
      END;
     '+','-','*','/',')','$','=',' ',',','.','[',']',
     ':','!','×','&','@','?','<','>','Ö','\','^',';','#','_','%','ò':
      ADVANCE
    END (*CASE*) ;
    WRITELN(OUTPUT); GET(INPUT)
  END ;
END (*SCANANDLISTINPUT*) ;

BEGIN (*CROSSREF*)
"""LINELIMIT(OUTPUT, MAXN);PAGE(OUTPUT)"""  ; INITIALIZE;
  IF NOT EOF(INPUT) THEN
  BEGIN SKIPCOMPILERTITLE;
    SCANANDLISTINPUT; """LINELIMIT(OUTPUT, MAXN); """
    PRINTTABLE; PRINTPROCS
  END ELSE WRITELN(STARS,' NO PROGRAM FOUND TO CROSS REFERENCE',STARS);
99:END .
