         TITLE ' REMOTE -- REMOTE CONTROL PROGRAM FOR DUCHESS'
REMOTE   CSECT
         STM   R14,R12,12(R13)    SAVE REGISTERS
         BALR  R12,0
         USING *,R12
         LA    R15,SAVEAREA
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         OPEN  (TSOOUT,(OUTPUT),REMOTDCB,(UPDAT))
FOREVER  ZERO  R1
         ST    R1,WAITTIME
         LOOP
         MVC   TBUF(DATALEN),BLANKS
         TGET  TBUF,TLEN,EDIT,NOWAIT
         TST   R15
         IF    Z
         LOOP
         ZERO  R1
         BAL   R10,REMOURS
         TST   R15
         BREAK Z
         BAL   R10,PAUSE
         ENDLOOP
         MVC   REMDATA(DATALEN),TBUF
         BAL   R10,REMWRT
         B     FOREVER
         ENDIF
         LA    R1,1
         BAL   R10,REMOURS
         TST   R15
         IF    Z
         PUT   TSOOUT,REMDATA
         BAL   R10,REMWRT
         B     FOREVER
         ENDIF
         BAL   R10,PAUSE
         ENDLOOP
         EJECT
PAUSE    L     R1,WAITTIME
         LA    R1,15(R1)
         C     R1,=F'100'
         IF    NH
         ST    R1,WAITTIME
         ENDIF
         STIMER WAIT,BINTVL=WAITTIME
         BR    R10
         EJECT
REMOURS  LA    R1,X'100'(R1)
         ST    R1,REMOTTRZ
         POINT REMOTDCB,REMOTTRZ
         READ  REMDECB1,SF,REMOTDCB,REMBUFF,'S',MF=E
         CHECK REMDECB1
         ZERO  R15
         XI    REMOWNER,X'01'
         BNER  R10
         LA    R15,4
         BR    R10
         EJECT
REMWRT   WRITE REMDECB1,SF,REMOTDCB,REMBUFF,'S',MF=E
         CHECK REMDECB1
         BR    R10
         EJECT
         EJECT
SAVEAREA DS    9D
REMOTTRZ DS    F
WAITTIME DS    F
         READ  REMDECB1,SF,REMOTDCB,REMBUFF,'S',MF=L
TSOOUT   DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),BLKSIZE=DATALEN,    *
               RECFM=F
REMOTDCB DCB   DDNAME=REMOTE,DSORG=PS,MACRF=(RP,W),BLKSIZE=REMOTLEN,   *
               RECFM=F
TBUF     DS    CL133
TLEN     EQU   133
BLANKS   DC    CL133' '
REMBUFF  DS    0CL135
REMOWNER DS    X
UNUSED   DS    X
REMDATA  DS    CL133
DATALEN  EQU   80
REMOTLEN EQU   DATALEN+2
         REGS
         END
