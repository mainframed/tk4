         TITLE 'ECHOUTIL -- ROUTINES TO PROCESS ECHO COMMAND'
ECHOUTIL CSECT
         ENTRY ECHO
         ENTRY FILMPTR
         ENTRY FILNAMP
         EXTRN SWITCH
ECHO     ENTER DSA=Y
         SCALL SWITCH             GET ON OR OFF SWITCH
         TST   R15                GOT ONE ?
         IF    NZ
         PRNTF 'Invalid or missing echo switch operand%L'
         ELSE
         ST    R1,ECHOSW          SAVE 1 OR 0 FOR ON OR OFF
         LA    R1,1               STANDARD INPUT
         ST    R1,SRCFM           SOURCE FILE MAP
         AR    R1,R1              STANDARD OUTPUT
         ST    R1,DSTFM           DESTINATION FILE MAP
         GTKN                     A SOURCE FILE NAME ?
         TST   R15
         IF    Z                  IF SOURCE NAME, ...
         LR    R2,R1              SAVE ADDRESS OF FILE NAME
         SCALL FILNAMP            GET BIT MAP FOR THIS FILE
         TST   R15                DOES THAT FILE EXIST ?
         IF    NZ                 NO, ERROR
         PRNTF 'Invalid source file ''%S''%L',((R2))
         B     ENDECHO
         ENDIF
         ST    R1,SRCFM           SAVE OVER DEFAULT FILE MAP
         GTKN                     A DESTINATION FILE ?
         TST   R15
         IF    Z
         LR    R2,R1              SAVE ADDRESS OF DESTINATIO FILE NAME
         SCALL FILNAMP            GET FILE BIT MAP FOR DEST FILE
         TST   R15                DOES DEST FILE EXIST ?
         IF    NZ                 NO,  ERROR
         PRNTF 'Invalid destination file ''%S''%L',((R2))
         B     ENDECHO
         ENDIF
         ST    R1,DSTFM           SAVE MAP FOR DESTINATION FILE
         ENDIF
         ENDIF
*
*        NOTE -- AT THIS POINT ALL INPUT HAS BEEN PARSED
*        NOW WE CHECK FOR VALID FILES AND NO LOOPS
*
         L     R1,DSTFM           DESTINATION FILE MAP
         SCALL FILMPTR            GET POINTER TO THAT FILE
         LR    R2,R1              SAVE POINTER TO FILE DESC TBL FOR FIL
         USING FILEDSCT,R2
         TM    FILEFLAG,FILEOPT   DOES THIS FILE ACCEPT OUTPUT ?
         IF    Z                  NO, ERROR
         PRNTF 'Destination file is not an output file%L'
         B     ENDECHO
         ENDIF
         TM    FILEFLAG,FILEALOC  IS FILE ALLOCATED ?
         IF    Z                  NO, ERROR
         PRNTF 'Destination file is not allocated%L'
         B     ENDECHO
         ENDIF
         DROP  R2
         L     R0,SRCFM           SOURCE MAP
         L     R1,DSTFM           DESTINATION MAP
         SCALL CHKELOOP
         TST   R15                ANY LOOPS ?
         IF    NZ
         PRNTF 'Loop in echo request%L'
         ELSE
         L     R1,SRCFM           GET SOURCE FILE MAP
         SCALL FILMPTR            GET POINTER TO FILE DESC TBL
         LR    R2,R1              SAVE POINTER
         USING FILEDSCT,R2
         L     R3,FILECHOD        GET MAP OF FILES TO ECHO TO
         L     R4,ECHOSW          GET BIT INDICATING OFF OR ON
         LNR   R4,R4              ZERO OR ONES
         L     R5,DSTFM           DESTINATION MAP
         NR    R4,R5              ALLOW ONLY DESTINATION BIT ON (OR ZER
         X     R5,=F'-1'          SET TO ALL BITS BESIDES DESTINATION
         NR    R3,R5              CLEAR DESTINATION BIT (IF ON)
         OR    R3,R4              SET DESTINATION BIT (IF OK)
         ST    R3,FILECHOD        SAVE NEW ECHO WORD
         DROP  R2
         ENDIF
         ENDIF
ENDECHO  EXIT
         EJECT
CHKELOOP ENTER
         LR    R2,R0
         LR    R3,R1
         NR    R0,R1              A LOOP ?
         IF    NZ                 YES, RETURN WITH RC=4
         LA    R10,4
         ELSE
         SR    R10,R10            ASSUME NO LOOP
NXTEBIT  TST   R3                 ANY MORE TO CHECK ?
         IF    NZ                 YES, ...
         LCR   R1,R3              STRIP OFF LOW BIT NEXT
         NR    R1,R3
         XR    R3,R1              CLEAR IN R3
         SCALL FILMPTR            GET POINTER TO FILE
         TST   R15                CHECK IF FILE EXISTS
         BNZ   BOMB               IF NOT, SERIOUS ERROR
         LR    R4,R1              R4 -> FILE DESC TABLE
         USING FILEDSCT,R4
         L     R1,FILECHOD        GET ALL POSIBILITIES
         LR    R0,R2
         SCALL CHKELOOP           RECURSIVE !!!
         TST   R15                FAIL ?
         BZ    NXTEBIT            NO,  CONTINUE
         LR    R10,R15            SAVE RETURN CODE
         ENDIF
         ENDIF
         EXIT  RC=(R10)
         DROP  R4
         EJECT
*
*        FILMPTR -- ROUTINE TO GO FROM FILE BIT MAP TO POINTER TO FDT
*
*        R1 HAS BIT MAP,  RETURNS POINTER
*
FILMPTR  ENTER
         LA    R10,4              ASSUME FAILURE
         LCR   R2,R1              STRIP OFF LOW BIT
         NR    R2,R1
         IF    NZ                 IF WE WERE PASSED ANYTHING ...
         L     R3,=V(FILETBL)     ADDRESS OF START OF FDT
NXTFILE  SR    R4,R4              ZERO R4
         C     R4,0(,R3)          AT END OF FDT
         BE    ENDFILMP           YES, RETURN (WITH FAILURE)
         LA    R4,1               A 1
         NR    R4,R2              AT CORRECT FILE ?
         IF    Z                  NO, ...
         LA    R3,FILETLEN(,R3)   ADVANCE TO NEXT FDT
         SRL   R2,1               SHIFT OVER 1
         B     NXTFILE            AND LOOP
         ENDIF
         SR    R10,R10            INDICATE SUCCESS
         ENDIF
ENDFILMP EXIT  RTN=(R3),RC=(R10)
         EJECT
*
*        FILNAMP -- ROUTINE TO GO FROM FILE NAME TO BIT MAP FOR FDT
*
*        R1 POINTS TO STRING (NAME), RETURNS BIT MAP
*
FILNAMP  ENTER
         SR    R2,R2              FOR END OF FDT CHECKS
         LA    R6,1               EVENTUAL BIT MAP TO RETURN
         SR    R10,R10            ASSUME SUCCESS
         L     R3,=V(FILETBL)     ADDRESS OF FDT
         USING FILEDSCT,R3
         LH    R4,0(,R1)          LENGTH OF FILE NAME
         CH    R4,=H'8'           IS IT TOO LONG ?
         BH    NOFILE             YES, CAN'T BE A FILE
         LA    R5,7
         SR    R5,R4              LENGTH - 1 OF SPACES TRAILING IN FDT
         S     R4,=F'1'           LENGTH - 1 OF FILE NAME
NXTFILE1 C     R2,0(,R3)          END ?
         BE    NOFILE             YES,  ERROR
         EX    R4,CHKNAME         MATCH ?
         IF    E                  YES, ...
         TST   R5                 ANY TRAILING SPACES ?
         BM    RETURN
         LA    R7,1(R4,R3)        POINT TO NEXT CHAR IN FDT ENTRY
         EX    R5,CHKSPACE
         BE    RETURN             EVERYTHING MATCHES, LEAVE
         ENDIF
         LA    R3,FILETLEN(,R3)
         SLL   R6,1               UPDATE MAP
         B     NXTFILE1           AND LOOP
NOFILE   LA    R10,4
RETURN   EXIT  RC=(R10),RTN=(R6)
CHKNAME  CLC   2(1,R1),FILEDDNM
CHKSPACE CLC   0(1,R7),=CL7' '
         DROP  R3
         EJECT
BOMB     ABEND 1003,DUMP
         SPACE 5
         LTORG
         DSA
ECHOSW   DS    F
SRCFM    DS    F
DSTFM    DS    F
         ENDDSA
         FILEDSCT
         REGS
         END
