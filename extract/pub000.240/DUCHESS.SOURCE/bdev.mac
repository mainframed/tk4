         TITLE 'BDEV -- STATIC EVALUATOR'
*
*        MACRO TO ADD THE INTERSECTION OF TWO REGISTERS TO A REGISTER
*
         MACRO
&LABEL   ADINT &R1,&R2,&R3,&T1
&LABEL   LR    &T1,&R2            GET THE FIRST SET INTO TEMP.
         NR    &T1,&R3            AND WITH THE OTHER SET.
         COBY  &T1
         AR    &R1,&T1            ADD THE NUMBER INTO THE COUNT.
         MEND
*
*        MACRO TO COMPUTE A POSITIONAL TERM.
*
         MACRO
&LABEL   COMPUT &CVALUE,&VAL
&LABEL   LR    R15,&VAL.VAL       GET VALUE INTO TEMP. REG.
         ST    R15,&VAL.STR       SAVE IN COMMUNICATIONS REGION
         M     R14,&VAL.WGT
         AR    &CVALUE,R15
         MEND
*
*        MACRO TO INCLUDE A MATERIAL TYPE INTO THE CALCULATION.
*
         MACRO
&LABEL   MATRL &DST,&PTR1,&PTR2,&BPTR,&WEIGHT
&LABEL   LH    &PTR2,&BPTR
         LR    R5,&PTR1
         SR    R5,&PTR2
         M     R4,&WEIGHT
         AR    &DST,R5
         MEND
*
*        MACRO TO DEFINE A TERM IN THE EVALUATOR.
*
         MACRO
&LABEL   TERM  &CHNAME,&INTNAME,&WGT
&LABEL   DC    A(S&SYSNDX)
&INTNAME.STR DC F'0'
&INTNAME.WGT DC A(&WGT)
BDEVSTRS CSECT
S&SYSNDX STRNG &CHNAME.
&SYSECT  CSECT
         MEND
*
*        MACRO TO SMEAR A BIT PATTERN
*
         MACRO
&LABEL   SMEAR &DST,&TMP,&MSK,&EDIR
&LABEL   LBR   &TMP,&DST
         NBR   &TMP,&MSK
         SHFTB &TMP,&EDIR
         NBR   &TMP,&MSK
         OBR   &DST,&TMP
         SHFTB &TMP,&EDIR
         NBR   &TMP,&MSK
         OBR   &DST,&TMP
         MEND
*
*        MACRO TO COMPUTE THE BACKWARD PAWN TERMS FOR EACH SIDE.
*
         MACRO
&LABEL   BCKPN &US,&THEM
         LCLC  &EDIR
         LCLC  &DIR
&EDIR    SETC  'D'
&DIR     SETC  'U'
         AIF   ('&US' EQ 'W').GENPLUS
&EDIR    SETC  'U'
&DIR     SETC  'D'
.GENPLUS ANOP
&LABEL   LB    HOLEB,&US.HOLES
         LB    R2,&US.P           GET OUR PAWNS.
         NBR   HOLEB,R2           HOLEB HAS ALL OF OUR WEAK PAWNS.
         LBR   R4,R2
         SHFTB R4,&DIR.R          SHIFT ...
         SHFTB R2,&DIR.L          ... IN BOTH DIRECTIONS.
         XBR   R2,R4              R2 HAS ALL OF OUR SINGLE THREATS.
         LB    R14,&THEM.P        GET HIS PAWNS.
         LBR   R4,R14
         SHFTB R4,&EDIR.R
         SHFTB R14,&EDIR.L
         NBR   R4,R14             R4 HAS ALL HIS DOUBLE THREATS.
         NBR   R2,R4              R2 HAS ALL OUR SINGLE + HIS DOUBLE.
         LB    R14,&US.PT         GET OUR PAWN THREATS.
         COMB  R14
         STB   HOLEB,TMPSTR
         LBR   R6,R14
         NBR   R14,R4             R14 HAS ALL HIS DOUBLE + OUR NONE.
         SHFTB R14,&EDIR          SHIFT OVER OUR PAWNS.
         LR    R1,R11
         SMEAR R14,R10,R6,&EDIR
         LR    R11,R1
         NB    R14,TMPSTR         KEEP ONLY WEAK PAWNS.
         COBF  VBKPNVAL,R1,R10,R14
         XB    R4,&THEM.PT        GET ONLY SINGLE THREATS.
         LB    R14,&US.PT         GET OUR THREATS.
         COMB  R14                GET SQRS WHERE WE HAVE NONE.
         NBR   R4,R14             R4 NOW HAS ALL HIS SINGLE VERSUS OUR
.*                                NONE.
         OBR   R4,R2              OR THAT IN TOO
         SHFTB R4,&EDIR           SHIFT OVER OUR PAWNS.
         SMEAR R4,R14,R6,&EDIR
         LB    HOLEB,TMPSTR
         NBR   R4,HOLEB           R4 HAS WEAK PAWNS ON HIS SINGLE
.*                                THREAT VERSUS OUR NO THREATS.
         COBF  BCKPNVAL,R1,R10,R4 CUMULATE VALUE.
         GETFL R15,R14,HOLEB      GET FILES WHERE WE HAVE A WEAK PAWN.
         LH    R14,&THEM.PAWNMSK  GET HIS PAWN FILE MASK.
         X     R14,=X'000000FF'   COMPLEMENT IT.
         NR    R14,R15            KEEP ONLY BACKWARD PAWNS ON OPEN FILE
         STH   R14,&US.WKPNFIL    SAVE IT AWAY.
         MEND
         EJECT
BDEV     CSECT
         ENTRY BDEVTRMS
         ENTER DSA=YES
         LR    R11,R1
         USING POSDB,R11
         EJECT
*
*        COMPUTE  MATERIAL BALANCE TERM
*
         ZERO  R8
         ZERO  R9
         LH    R2,WEO             GET WHITE END OFFSET.
         MATRL R8,R2,R3,WPO,PAWNWGT
         ST    R5,WPMATRL
         LR    R10,R5
         MATRL R8,R3,R2,WNO,KNITWGT
         MATRL R8,R2,R3,WBO,BISHWGT
         MATRL R8,R3,R2,WRO,ROOKWGT
         MATRL R8,R2,R3,WQO,QUENWGT
         ST    R8,WMATRL
         LH    R2,BEO             GET BLACK END OFFSET.
         MATRL R9,R2,R3,BPO,PAWNWGT
         ST    R5,BPMATRL
         AR    R10,R5             TOTAL PAWN MATERIAL
         MATRL R9,R3,R2,BNO,KNITWGT
         MATRL R9,R2,R3,BBO,BISHWGT
         MATRL R9,R3,R2,BRO,ROOKWGT
         MATRL R9,R2,R3,BQO,QUENWGT
         ST    R9,BMATRL
         LR    VALUE,R8           GET WHITE MATERIAL
         SR    VALUE,R9           SUBTRACT BLACK MATERIAL
         ST    VALUE,TOTMATRL
*
*  TRADE DOWN BONUS = 2048 * PAWN FACTOR * MATERIAL FACTOR
*  PAWN FACTOR = #UP / (#UP + 1)
*  MATERIAL FACTOR = (UM - DM) / (TOTAL NON-PAWN MATERIAL + 1024)
*
         LH    R2,WEO             GET WHITE END OFFSET
         SH    R2,WPO             COMPUTE NUMBER OF WHITE PAWNS
         LTR   R5,VALUE           IS WHITE UP?
         IF    L                  NO -- INVERT THE TERMS
         LH    R2,BEO             COMPUTE UP SIDE'S PAWNS
         SH    R2,BPO
         LR    R7,R8              SWAP VALUE REGISTERS
         LR    R8,R9              ...
         LR    R9,R7              COMPLETE SWAP
         ENDIF
         ST    R9,DM              SAVE DOWN MATERIAL FOR MOPUP
         MR    R4,R2              COMPUTE NUMERATOR
         LA    R7,1024(R8,R9)
         SR    R7,R10             TOTAL NON-PAWN MATERIAL + 1
         LA    R3,1(R2)           COMPUTE # OF UP + 1
         MR    R2,R7              COMPUTE DENOMINATOR
         SLDA  R4,11
         DR    R4,R3              COMPLETE RATIO COMPUTATION
         AR    VALUE,R5           AND ADD THE TRADE TERM IN
         ST    VALUE,MATRLSTR     SAVE MATERIAL
         EJECT
*
*  HUNG PIECE TERM
*  COMPUTES PIECES WHICH ARE EITHER ATTACKED AND UNDEFENDED,
*  OR ARE ATTACKED BY LOWER VALUED MEN.
*
         ZERO  HUNGVAL
         LC    R2,COLOR
         SLA   R2,2
         LR    R14,R11
         LR    R15,R11
         A     R14,DBOF(R2)
         A     R15,DBOF+4(R2)
         XBR   R6,R6
         LB    R2,WN-WK(R14)
         OB    R2,WB-WK(R14)
         LB    R4,BPT-BK(R15)
         NBR   R2,R4
         OBR   R6,R2
         LB    R2,WR-WK(R14)
         OB    R4,BNT-BK(R15)
         OB    R4,BBT-BK(R15)
         NBR   R2,R4
         OBR   R6,R2
         LB    R2,WQ-WK(R14)
         OB    R4,BRT-BK(R15)
         NBR   R2,R4
         OBR   R6,R2
         LB    R2,WK-WK(R14)
         NB    R2,BT-BK(R15)
         OBR   R6,R2
*
*  COMPUTE ATTACKED AND UNDEFENDED
*
         LB    R8,W-WK(R14)
         NB    R8,BT-BK(R15)
         LB    R2,WT-WK(R14)
         NBR   R2,R8
         XBR   R8,R2
         OBR   R8,R6
*
*  R6 = PIECES ATTACKED BY LESSER VALUED MAN
*  R8 = R6 + ATTACKED AND UNDEFENDED MEN
*
         TSTB  R8
         IF    NZ
         COBB  R1,R2,R3,R8        NUMBER OF HUNG PIECES
         AR    R1,R1
         LH    HUNGVAL,HUNGPIEC(R1)
         CH    R1,=H'2'
         IF    H
         COBB  R1,R2,R3,R6
         AR    R1,R1
         AH    HUNGVAL,HUNGPIEC(R1)
         ENDIF
         CLI   COLOR,X'00'
         IF    E
         NEG   HUNGVAL
         ENDIF
         ENDIF
         COMPUT VALUE,HUNG
         EJECT
         SR    R4,R4              CLEAR CASTLE PRV WORD
         L     R2,WMATRL
         L     R3,WPMATRL
         SRA   R3,1
         SR    R2,R3
         C     R2,=F'2000'
         IF    L
         LA    R4,1
         C     R2,=F'1350'
         IF    L
         LA    R4,2
         ENDIF
         ENDIF
         ST    R4,BLCSFLG
         SR    R4,R4
         L     R2,BMATRL
         L     R3,BPMATRL
         SRA   R3,1
         SR    R2,R3
         C     R2,=F'2000'
         IF    L
         LA    R4,1
         C     R2,=F'1350'
         IF    L
         LA    R4,2
         ENDIF
         ENDIF
         ST    R4,WHCSFLG
         ZERO  ISLPNVAL           CLEAR ISOLATED PAWN VALUE.
         ZERO  DBLPNVAL           CLEAR DOUBLED PAWN TERM
*
*        BLACK PAWNS -- STRUCTURE
*
         ZERO  PFILES             CLEAR FILES WITH PAWNS.
         LB    PAWNB,BP           GET BLACK PAWNS
         SHFTB PAWNB,U            UP ONE RANK
         REPEAT                   LOOP OVER PAWN BOARD
         GETH  TEMP,PAWNB         GET LOW RANK OF PAWN BOARD
         ADINT DBLPNVAL,PFILES,TEMP,TEMP2
         OR    PFILES,TEMP        ADD IN THE NEW PAWNS
         SHFTB PAWNB,U            UP TO NEXT RANK
         TSTB  PAWNB              TEST PAWN BOARD
         UNTIL Z                  LOOP UNTIL NO MORE
         LR    R14,PFILES
         STH   PFILES,BPAWNMSK    SAVE PAWN MASK FOR FUTURE REFERENCE.
         LR    OFILES,PFILES      COMPUTE OPEN FILES ...
         AR    OFILES,OFILES      SHIFT ONE BIT
         X     OFILES,=X'000003FF'
         NR    PFILES,OFILES      AND OFF ALL PAWNS WITH AN OPEN FILE
         SRL   OFILES,2           ON ONE SIDE, THEN ALL WITH AN OPEN
         NR    PFILES,OFILES      FILE ON BOTH SIDES.
         COBY  PFILES             COUNT THE BITS
         AR    ISLPNVAL,PFILES    GET ISOLATED PAWN VALUE
*
*        WHITE PAWNS -- STRUCTURE
*
         NEG   ISLPNVAL           NEGATE ISOLATED PAWNS
         NEG   DBLPNVAL           NEGATE DOUBLED PAWN VALUE
         ZERO  PFILES             CLEAR PAWN-OCCUPIED FILES.
         LB    PAWNB,WP           GET WHITE PAWNS.
         SHFTB PAWNB,D            GET NEXT RANK.
         REPEAT
         GETL  TEMP,PAWNB         GET THE NEXT RANK OF PAWNS.
         ADINT DBLPNVAL,PFILES,TEMP,TEMP2
         OR    PFILES,TEMP        SET MORE FILES AS CLOSED.
         SHFTB PAWNB,D            GET NEXT RANK.
         TSTB  PAWNB              ANY PAWNS LEFT?
         UNTIL Z                  REPEAT UNTIL NONE LEFT.
         STH   PFILES,WPAWNMSK    SAVE PAWN MASK FOR FUTURE REFERENCE.
         LR    OFILES,PFILES      COMPUTE OPEN FILES.
         AR    OFILES,OFILES      SHIFT ONE BIT
         X     OFILES,=X'000003FF'
         NR    PFILES,OFILES      KEEP ONLY ISOLATED PAWNS ...
         SRL   OFILES,2           CHECK OTHER SIDE OF PAWN.
         NR    PFILES,OFILES      AND WITH OPEN AGAIN.
         COBY  PFILES             GET THE NUMBER OF ISOLATED PAWNS.
         AR    ISLPNVAL,PFILES    ADD NUMBER OF ISOLATED PAWNS IN.
         COMPUT VALUE,DBLPN       CUMULATE DOUBLED PAWNS
         COMPUT VALUE,ISLPN       COMPUTE ISOLATED PAWNS.
*
*        PAWN PHALANXES -- BLACK
*
         ZERO  PHALXVAL
         LB    R2,BP              GET OUR PAWNS.
         LBR   R4,R2              INTO TEMP. REG.
         SHFTB R2,L               SHIFT ONE SQE. OVER.
         NBR   R2,R4
         GETFL R1,R4,R2           GET ONLY ONE PHALANX PER FILE.
         COBY  R1
         AR    PHALXVAL,R1
*
*        PAWN PHALANXES -- WHITE
*
         NEG   PHALXVAL
         LB    R2,WP
         LBR   R4,R2
         SHFTB R2,L
         NBR   R2,R4
         GETFL R1,R4,R2
         COBY  R1
         AR    PHALXVAL,R1
         COMPUT VALUE,PHALX       COMPUTE THE VALUE OF PHALANXES.
         LA    R1,COTAB           GET ADDRESS OF COUNT ONES TABLE.
         L     R15,=V(BDEVUTIL)   GET ADDRESS OF SUBROUTINE.
         BALR  R14,R15            CALL WEAK SQS SUBR.
         COMPUT VALUE,HOLES
         COMPUT VALUE,OCCHL
         COMPUT VALUE,ATTHL
         COMPUT VALUE,PSDPN
*
*        BACKWARD PAWN EVALUATION - BLACK.
*
         ZERO  VBKPNVAL
         ZERO  BCKPNVAL
         BCKPN B,W
         NEG   VBKPNVAL
         NEG   BCKPNVAL
*
*        BACKWARD PAWN EVALUATION FOR WHITE.
*
         BCKPN W,B
         COMPUT VALUE,VBKPN
         COMPUT VALUE,BCKPN
*
*        WEAK PAWN ON OPEN FILE - BLACK
*
         ZERO  WPNOFVAL
         LH    R2,BWKPNFIL        GET BLACK WEAK PAWNS.
         LH    R3,WPAWNMSK        GET WHITE PAWN FILES.
         X     R3,=X'000000FF'
         NR    R2,R3              GET INTERSECTION OF WHITE O FILES.
         LB    R4,WR              GET ENEMY ROOKS.
         OB    R4,WQ              AND QUEENS.
         ANYB  R1,R4
         NR    R2,R1              KEEP VALUE ONLY IF ENEMY HAS ROOKS.
         COBY  R2                 COUNT # OF PAWNS.
         AR    WPNOFVAL,R2
         NEG   WPNOFVAL
*
*        WEAK PAWN ON OPEN FILE - WHITE
*
         LH    R2,WWKPNFIL        GET WHITE WEAK PAWNS.
         LH    R3,BPAWNMSK        GET BLACK PAWN MASK.
         X     R3,=X'000000FF'
         NR    R2,R3
         LB    R4,BR
         OB    R4,BQ
         ANYB  R1,R4
         NR    R2,R1
         COBY  R2
         AR    WPNOFVAL,R2
         COMPUT VALUE,WPNOF
         ZERO  OPNFRVAL           CLEAR OPEN FILES FOR ROOKS.
         ZERO  HOPFRVAL           CLEAR HALF OPEN FILE VALUE.
         ZERO  RKWPFVAL           CLEAR ROOK ON FILE OF WEAK PAWN VALUE
*
*        BLACK ROOKS -- OPEN FILES
*
         LH    OFILES,BPAWNMSK    RECOVER BLACK PAWNS.
         X     OFILES,=X'000000FF'
         LH    DOFILES,WPAWNMSK   GET THE MASK OF ENEMY PAWNS.
         X     DOFILES,=X'000000FF' SET TO FILES NOT PAWNS.
         NR    DOFILES,OFILES     OPEN FILES ARE THE INTERSECTION.
         LH    WPFILES,WWKPNFIL   GET THE FILES OF WEAK ENEMY PAWNS.
         LB    ROOKB,BR           GET BLACK ROOKS
         OB    ROOKB,BQ           GET BLACK QUEENS TOO
         REPEAT                   LOOP FOR ALL ROOKS AND QUEENS
         GETH  TEMP,ROOKB         GET NEXT RANK
         ADINT OPNFRVAL,TEMP,DOFILES,TEMP2 GET COMPLETELY OPEN FILES.
         ADINT HOPFRVAL,TEMP,OFILES,TEMP2 GET HALF-OPEN FILES.
         ADINT RKWPFVAL,TEMP,WPFILES,TEMP2 GET FILES OF WEAK PAWNS.
         SHFTB ROOKB,U            GET NEXT RANK.
         TSTB  ROOKB              ANY MORE ROOKS?
         UNTIL Z                  LOOP UNTIL NO MORE
         NEG   OPNFRVAL           NEGATE OPEN FILES FOR ROOKS
         NEG   HOPFRVAL           NEGATE HALF OPEN FILES VALUE.
         NEG   RKWPFVAL           NEGATE ROOK ON WEAK PAWN FILE VALUE.
*
*        WHITE ROOKS -- OPEN FILES.
*
         LH    OFILES,WPAWNMSK
         X     OFILES,=X'000000FF'
         LH    DOFILES,BPAWNMSK   GET ENEMY PAWN FILES.
         X     DOFILES,=X'000000FF' SET TO FILES, NOT PAWNS.
         NR    DOFILES,OFILES     OPEN FILES ARE THE INTERSECTION.
         LH    WPFILES,BWKPNFIL   GET ENEMY WEAK PAWN FILES.
         LB    ROOKB,WR           GET WHITE ROOKS
         OB    ROOKB,WQ           GET WHITE QUEENS TOO
         REPEAT                   LOOP FOR ALL ROOKS.
         GETL  TEMP,ROOKB         GET LOW RANK OF ROOK BOARD.
         ADINT OPNFRVAL,TEMP,DOFILES,TEMP2 GET COMPLETELY OPEN FILES.
         ADINT RKWPFVAL,TEMP,WPFILES,TEMP2 GET FILES OF WEAK PAWNS.
         ADINT HOPFRVAL,TEMP,OFILES,TEMP2 GET HALF-OPEN FILES
         SHFTB ROOKB,D            GET NEXT RANK.
         TSTB  ROOKB              ANYTHING LEFT?
         UNTIL Z                  LOOP UNTIL NOTHING LEFT.
         COMPUT VALUE,OPNFR       CUMULATE VALUE FOR OPEN FILES
         COMPUT VALUE,HOPFR       GET HALF-OPEN FILE VALUE.
         COMPUT VALUE,RKWPF
*
*        COMPUTE THE PIECE POSITION TERMS.
*
         LA    R9,WHCSFLG         SET UP PARAMETERS TO THE POSITION RTN
         LA    R10,COTAB
         L     R15,=V(CMPTPOST)   GET A(POSITION VALUE COMPUTATION)
         BALR  R14,R15
         COMPUT VALUE,POSTN
         LH    R15,BPAWNMSK       GET FILES OF BLACK PAWNS.
         LH    R14,WPAWNMSK       AND WHITE PAWNS.
*
*        CASTLED KING TERMS - BLACK
*
         X     R14,=X'000000FF'
         X     R15,=X'000000FF'
         ZERO  PCASLVAL
         ZERO  KCASLVAL
         ZERO  OOFCPVAL           CLEAR OPEN FILES ON KING.
         ZERO  OMPCPVAL           CLEAR OPPONENT MAJOR PIECE ON KING.
         ZERO  MPPCPVAL           CLEAR MACHINE PAWN POSITION IN CAS. P
         L     R1,BLCSFLG         COUNT BLACK CASTLE TERMS?
         C     R1,=F'2'
         IF    L
         SR    R1,R1              CLR TMP. REG.
         IC    R1,BCASTLE         GET CASTLING PRIV.
         AR    R1,R1              GET TO HALF-WORD.
         SH    PCASLVAL,CASLTB(R1)
         LB    KINGB,BK           GET THE BLACK KING.
         GETFL KFILE,R1,KINGB     COMPUTE FILE OF THE KING.
         LR    R8,KFILE           INTO TMP. REG.
         NR    R8,R15             IS IT ON THE FILE?
         ANY   R8,R8              WE WILL GET POINTS IF IT IS ON THIS
         SR    KCASLVAL,R8        ADD IT IN ...
         SR    KCASLVAL,R8        ... TWICE FOR THE FILE PAWN.
         LR    R8,KFILE           RECOVER KING FILE.
         AR    R8,R8              SHIFT LEFT.
         NR    R8,R15             FRIENDLY PAWNS?
         ANY   R8,R8
         SR    KCASLVAL,R8
         LR    R8,KFILE
         SRL   R8,1               SHIFT RIGHT.
         NR    R8,R15             IS A PAWN THERE?
         ANY   R8,R8
         SR    KCASLVAL,R8        SUBTRACT THAT FROM VALUE.
         LR    R8,KFILE           GET THE KING'S FILE.
         NR    R8,R14             AND WITH OPPONENTS PAWNS.
         ANY   R8,R8              SET BITS IN R8 IF ANY.
         SR    OOFCPVAL,R8
         SR    OOFCPVAL,R8
         LR    R8,KFILE
         AR    R8,R8
         NR    R8,R14
         ANY   R8,R8
         SR    OOFCPVAL,R8
         LR    R8,KFILE
         SRL   R8,1
         NR    R8,R14
         ANY   R8,R8
         SR    OOFCPVAL,R8
         LB    R2,WR
         OB    R2,WQ              GET OPP. MAJOR PIECES.
         GETFL R1,R8,R2           COMPUTE THE FILES.
         LR    R8,KFILE
         AR    R8,R8
         OR    R8,KFILE
         AR    R8,R8
         OR    R8,KFILE
         SRL   R8,1
         NR    R8,R14
         NR    R8,R1              KEEP ONLY MAJOR PS ON OPEN FL.
         COBY  R8                 COUNT THE NUMBER OF ENEMY FILES W MP
         AR    OMPCPVAL,R8
         LB    KINGB,BKT          ALL KING THREATS -- NEIGHBORING SQS.
         NB    KINGB,BP           KEEP ONLY PAWNS ABOUT KING.
         GETFL R1,R8,KINGB        COUNT ONLY ONE PAWN PER FILE.
         COBY  R1
         AR    MPPCPVAL,R1        ADD IT IN.
         ENDIF
*
*        CASTLED KING TERMS - WHITE
*
         NEG   OMPCPVAL
         NEG   OOFCPVAL
         NEG   MPPCPVAL
         L     R1,WHCSFLG
         C     R1,=F'2'
         IF    L
         SR    R1,R1
         IC    R1,WCASTLE
         AR    R1,R1
         AH    PCASLVAL,CASLTB(R1)
         LB    KINGB,WK           GET KING.
         GETFL KFILE,R1,KINGB     GET OUR FILE.
         LR    R8,KFILE
         NR    R8,R14             FRIENDLY PAWNS ON FILE?
         ANY   R8,R8
         AR    KCASLVAL,R8        WE GET POINTS FOR PAWN DIRECTLY ON FL
         AR    KCASLVAL,R8        EQUAL TO TWICE WEIGHT OF PAWNS AROUND
         LR    R8,KFILE
         AR    R8,R8              SHIFT LEFT.
         NR    R8,R14             ANY FRIENDLY PAWNS?
         ANY   R8,R8              ANY PAWNS THERE?
         AR    KCASLVAL,R8        ADD THAT IN TOO
         LR    R8,KFILE
         SRL   R8,1               SHIFT RIGHT.
         NR    R8,R14
         ANY   R8,R8              FRIENDLY PAWNS?
         AR    KCASLVAL,R8        ADD THAT IN TOO.
         LR    R8,KFILE           GET THE KING'S FILE.
         NR    R8,R15             AND WITH OPPONENT'S PAWNS.
         ANY   R8,R8              ANY OPPONENT'S PAWNS MISSING?
         SR    OOFCPVAL,R8
         SR    OOFCPVAL,R8
         LR    R8,KFILE           GET KING FILE BACK.
         AR    R8,R8
         NR    R8,R15             GET OPP. PAWNS.
         ANY   R8,R8
         SR    OOFCPVAL,R8
         LR    R8,KFILE
         SRL   R8,1
         NR    R8,R15
         ANY   R8,R8
         SR    OOFCPVAL,R8
         LB    R2,BR
         OB    R2,BQ
         GETFL R1,R8,R2           COMPUTE THE FILES OF OPP MAJOR PCS
         LR    R8,KFILE
         AR    R8,R8
         OR    R8,KFILE
         AR    R8,R8
         OR    R8,KFILE
         SRL   R8,1
         NR    R8,R15             AND OFF ALL NOT ON OPEN FILES.
         NR    R8,R1              AND WITHOUT OPPONENT MAJOR PIECES.
         COBY  R8
         AR    OMPCPVAL,R8
         LB    KINGB,WKT          GET ALL NEIGHBORING SQS OF KING.
         NB    KINGB,WP
         GETFL R1,R8,KINGB
         COBY  R1
         AR    MPPCPVAL,R1
         ENDIF
         L     R1,WHCSFLG
         A     R1,BLCSFLG
         C     R1,=F'1'
         IF    NH
         AR    PCASLVAL,PCASLVAL
         AR    KCASLVAL,KCASLVAL
         AR    OOFCPVAL,OOFCPVAL
         AR    OMPCPVAL,OMPCPVAL
         AR    MPPCPVAL,MPPCPVAL
         ENDIF
         COMPUT VALUE,PCASL
         COMPUT VALUE,KCASL
         COMPUT VALUE,OOFCP
         COMPUT VALUE,OMPCP
         COMPUT VALUE,MPPCP
*
*        OTHER TERMS -- BLACK
*
         LB    ROOKB,BR           GET BLACK ROOKS
         NB    ROOKB,BRT          AND WITH ROOK THREATS
         ANYB  DBLRKVAL,ROOKB     ANY DOUBLED ROOKS?
*
*        OTHER TERMS -- WHITE
*
         LB    ROOKB,WR           GET WHITE ROOKS
         NB    ROOKB,WRT          AND WITH ROOK THREATS
         ANYB  TEMP,ROOKB         KEEP ONLY DOUBLED ROOKS
         SR    DBLRKVAL,TEMP      ADD 1 IF ANY.
         COMPUT VALUE,DBLRK       CUMULATE DOUBLED ROOK VALUE
         EJECT
*
*  MOP UP TERM
*  IF LOSING SIDE IS DOWN SIGNIFICANT MATERIAL AND
*  HAS LITTLE ABSOLUTE MATERIAL, THEN CREDIT IS GIVEN FOR:
*  DISTANCE OF LOSING KING FROM CENTER OF BOARD
*  DISTANCE OF WINNING KINGS AND KNIGHTS FROM LOSING KING
*  PLACEMENT OF WINNING KING TWO FROM EDGE
*
         ZERO  MOPVAL
         L     R1,TOTMATRL
         LPR   R2,R1
         C     R2,=F'400'
         IF    NL
         LR    R14,R11
         LA    R15,BK
         CR    R1,R2
         IF    NE
         LR    R14,R15
         LR    R15,R11
         ENDIF
*
*  R14 -> WINNING DB, R15 -> LOSING DB
*
         L     R3,DM
         C     R3,=F'700'
         IF    NH
         LC    R2,BSQUARES-BK(R15)
         SRDL  R2,3
         SRL   R3,29              R2,R3 = SQUARE OF LOSING KING
         LA    R4,27              CENTER OF BOARD
         DIST  MOPVAL,R4,R2
         SLA   MOPVAL,3           LOSING KING CENTER TROPISM
*
*  WINNING KING/KNIGHT TO LOSING KING TROPISM
*
         ZERO  R6
         LA    R7,14
         LC    R4,WSQUARES-WK(R14)
         DIST  R6,R4,R2
         LH    R8,WNO-WK(R14)
         CH    R8,WPO-WK(R14)
         WHILE L
         LC    R4,WSQUARES-WK(R14,R8)
         LA    R8,1(R8)
         DIST  R6,R4,R2
         LA    R7,14(R7)
         CH    R8,WPO-WK(R14)
         ENDWHILE
         SR    R7,R6
         AR    MOPVAL,R7
*
*  WINNING KING ON RANK 3,7 OR FILE C,F
*
         LB    R4,WK-WK(R14)
         NB    R4,KPLACE
         ANYB  R6,R4
         SR    MOPVAL,R6          KING POSITION CREDIT
*
*  ADJUST TO WHITE
*
         L     R1,MATRLSTR
         TST   R1
         IF    M
         NEG   MOPVAL
         ENDIF
         ENDIF
         ENDIF
         COMPUT VALUE,MOP
         L     R1,TOTMATRL
         L     R15,=V(EVBOUND)    GET ADDRESS OF EVALUATOR BOUND RTN.
         BR    R15                AND RETURN THRU THERE.
         EJECT
BDEVTRMS TERM  'Material',MATRL,1
         TERM  'Hung pieces',HUNG,1
         TERM  'Open files',OPNFR,15
         TERM  'Rook on weak P',RKWPF,8
         TERM  'Half files',HOPFR,3
         TERM  'Doubled rooks',DBLRK,8
         TERM  'Castling priv.',PCASL,5
         TERM  'King position',KCASL,8
         TERM  'King/Pawn pos',MPPCP,8
         TERM  'King opp file',OOFCP,-5
         TERM  'King opp mp',OMPCP,-8
         TERM  'Position',POSTN,1
         TERM  'Isolated Pawns',ISLPN,-8
         TERM  'Doubled Pawns',DBLPN,-10
         TERM  'Phalanx',PHALX,6
         TERM  'Passed Pawns',PSDPN,3
         TERM  'Holes',HOLES,-1
         TERM  'Backward Pawns',BCKPN,-10
         TERM  'Very backward P',VBKPN,-15
         TERM  'Attacked holes',ATTHL,-1
         TERM  'Occupied holes',OCCHL,-2
         TERM  'Open weak Pawns',WPNOF,-8
         TERM  'Mop up',MOP,4
         DC    F'0,0,0'
KPLACE   DC    X'2424FF2424FF2424'
DBOF     DC    A(WK-WK)
         DC    A(BK-WK)
         DC    A(WK-WK)
HUNGPIEC DC    H'0,16,38,100,200,300,400,500,550'
         DC    H'600,650,700,750,800,850,900,950'
CASLTB   DC    H'-3'              00 K-SIDE AND Q-SIDE CASTLING IMPOSS.
         DC    H'-1'              01 Q-SIDE CASTLING IMPOSSIBLE.
         DC    H'-2'              10 K-SIDE CASTLING IMPOSSIBLE.
         DC    H'0'               11 CASTLING LEGAL BOTH SIDES.
PAWNWGT  DC    F'100'             VALUE OF A PAWN.
KNITWGT  DC    F'310'             VALUE OF A KNIGHT
BISHWGT  DC    F'330'             VALUE OF A BISHOP
ROOKWGT  DC    F'500'             VALUE OF A ROOK.
QUENWGT  DC    F'900'             VALUE OF A QUEEN.
         PRINT NOGEN
         LTORG
COTAB    COTAB                    COUNT ONES TABLE
         PRINT GEN
VALUE    EQU   0                  CUMULATIVE POSITION VALUE
TEMP     EQU   1                  TEMPORARY REGISTER
PAWNB    EQU   2                  REGISTER PAIR FOR PAWN BOARD
ROOKB    EQU   2                  REGISTER PAIR FOR ROOK BOARD
KINGB    EQU   2                  REGISTER PAIR FOR KING BOARD.
KFILE    EQU   4                  THE FILE ON WHICH THE KING STANDS.
TEMP2    EQU   4                  SECONDARY SCRATCH REGISTER
PFILES   EQU   5                  FILES WITH PAWNS ON THEM
DOFILES  EQU   5                  DOUBLE (COMPLETELY) OPEN FILES.
MPPCPVAL EQU   5                  MACHINE PAWN POS. ABOUT CASTLED POS.
OMPCPVAL EQU   6                  OPPONENT'S MAJOR PIECE ON OPEN FILE K
WPFILES  EQU   6                  WEAK PAWN FILES.
HOLEB    EQU   6
PCASLVAL EQU   7                  CASTLING PRIVILEGES TERM.
PSDPNVAL EQU   7                  PASSED PAWN VALUE REGISTER.
DBLRKVAL EQU   8                  DOUBLED ROOKS REGISTER
POSTNVAL EQU   8                  POSITION VALUE REGISTER.
WPNOFVAL EQU   8                  PAWN ON OPEN FILE IS WEAK.
HOPFRVAL EQU   8                  OPEN FILES (HALF) FOR ROOK.
ISLPNVAL EQU   8                  ISOLATED PAWNS REGISTER
VBKPNVAL EQU   8                  VALUE OF VERY BACKWARD PAWNS.
ATTHLVAL EQU   8                  ATTACKED HOLE VALUE.
OOFCPVAL EQU   9                  OPPONENT'S OPEN FILES ON KING.
OPNFRVAL EQU   9                  OPEN FILES FOR ROOKS VALUE
BCKPNVAL EQU   9                  BACKWARD PAWNS REGISTER.
OCCHLVAL EQU   9                  OCCUPIED HOLE VALUE.
HUNGVAL  EQU   10
RKWPFVAL EQU   10                 ROOK ON WEAK PAWN FILES.
PHALXVAL EQU   10
HOLESVAL EQU   10                 HOLES IN GENERAL VALUE.
DBLPNVAL EQU   10                 DOUBLED PAWNS REGISTER
KCASLVAL EQU   10                 KING CASTLED TERM REGISTER.
MOPVAL   EQU   10
OFILES   EQU   15                 OPEN FILES MASK REGISTER.
         DSA
WHOLES   DS    2F                 WHITE HOLES.
BHOLES   DS    2F                 BLACK HOLES.
TMPSTR   DS    2F
TOTMATRL DS    F
WMATRL   DS    F
BMATRL   DS    F
WPMATRL  DS    F                  WHITE PAWN MATERIAL
BPMATRL  DS    F
WHCSFLG  DS    F
BLCSFLG  DS    F
DM       DS    F                  MATERIAL OF THE LOSING SIDE
BWKPNFIL DS    H                  BLACK WEAK PAWN FILES.
WWKPNFIL DS    H                  WHITE WEAK PAWN FILES.
BPAWNMSK DS    H                  FILES WITH BLACK PAWNS.
WPAWNMSK DS    H                  FILES WITH WHITE PAWNS.
         ENDDSA
         REGS
POSDB    DSECT
         POSDB
         END
