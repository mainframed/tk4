         TITLE 'BDKEY -- ENCODE BOARD FOR BOOK SEARCH'
*
*        THIS SUBROUTINE ENCODES THE BOARD INTO A 22-BYTE KEY
*        USING A SPECIAL HUFFMANN ENCODING ALGORITHM.
*
BDKEY    CSECT
         ENTER
         L     R11,0(R1)          PICK UP A(POSITION DATABASE)
         USING POSDB,R11          NOTE ITS USAGE.
         L     R10,4(R1)          PICK UP A(KEY DEST)
         XC    0(24,R10),0(R10)   CLEAR THE KEY.
         SR    R14,R14            CLEAR THE ACCUM. DATA.
         SR    R15,R15
         SR    R9,R9              R9 HAS THE NUMBER OF BITS IN R14-R15.
         BSETB R2,0               SET THE HIGHEST BIT IN.
         EJECT
         REPEAT
         LBR   R4,R2              GET THE BOARD.
         NB    R4,WK              IS IT A WHITE KING?
         TSTB  R4
         IF    NZ
         SLDL  R14,6              KINGS ARE 6 BITS LONG (AS ARE QUEENS)
         LA    R9,6(R9)           ACCUM BIT COUNT.
         A     R15,WKING          ADD IN THE BIT PATTERN FOR KING.
         B     FOUND
         ENDIF
         LBR   R4,R2
         NB    R4,BK              IS IT A BLACK KING?
         TSTB  R4
         IF    NZ
         SLDL  R14,6
         LA    R9,6(R9)           ACCUM BIT COUNT.
         A     R15,BKING          GET THE BLACK KING CODE.
         B     FOUND              AND LEAVE.
         ENDIF
         EJECT
         LBR   R4,R2              RECOVER BOARD.
         NB    R4,WQ              WHITE QUEENS.
         TSTB  R4
         IF    NZ
         SLDL  R14,6              QUEENS ARE ALSO 6 BITS LONG.
         A     R15,WQUEEN         ADD THE CODE IN.
         LA    R9,6(R9)           ACCUM BIT COUNT.
         B     FOUND
         ENDIF
         LBR   R4,R2
         NB    R4,BQ              BLACK QUEENS.
         TSTB  R4
         IF    NZ
         SLDL  R14,6              SHIFT THE COUNT.
         A     R15,BQUEEN         ADD IN THE BLACK QUEEN COUNT.
         LA    R9,6(R9)           ACCUM BIT COUNT.
         B     FOUND
         ENDIF
         EJECT
         LBR   R4,R2              RECOVER BOARD.
         NB    R4,WR              WHITE ROOKS.
         TSTB  R4                 ANYTHING THERE?
         IF    NZ
         SLDL  R14,5              ROOKS, KNIGHTS, AND BISHOPS ARE 5.
         A     R15,WROOK          ADD IN ROOK CODE
         LA    R9,5(R9)           ACCUM BIT COUNT.
         B     FOUND
         ENDIF
         LBR   R4,R2
         NB    R4,BR              BLACK ROOKS
         TSTB  R4
         IF    NZ
         SLDL  R14,5
         A     R15,BROOK          ACCUM BLACK ROOK CODE
         LA    R9,5(R9)           AND THE BLACK ROOK BIT COUNT.
         B     FOUND
         ENDIF
         EJECT
         LBR   R4,R2
         NB    R4,WB              WHITE BISHOPS.
         TSTB  R4
         IF    NZ
         SLDL  R14,5              SHIFT 5
         A     R15,WBISHOP        ADD IN THE WHITE BISHOPS.
         LA    R9,5(R9)           ACCUM BIT COUNT.
         B     FOUND
         ENDIF
         LBR   R4,R2
         NB    R4,BB              BLACK BISHOPS.
         TSTB  R4
         IF    NZ
         SLDL  R14,5              SHIFT BISHOP COUNT.
         A     R15,BBISHOP
         LA    R9,5(R9)           ACCUM BIT COUNT.
         B     FOUND
         ENDIF
         EJECT
         LBR   R4,R2
         NB    R4,WN              WHITE KNIGHTS.
         TSTB  R4
         IF    NZ
         SLDL  R14,5              KNIGHTS ARE ALSO 5.
         A     R15,WKNIGHT        ADD IN THE WHITE KNIGHT CODE
         LA    R9,5(R9)           ACCUM BIT COUNT.
         B     FOUND
         ENDIF
         LBR   R4,R2
         NB    R4,BN              BLACK KNIGHTS.
         TSTB  R4
         IF    NZ
         SLDL  R14,5
         A     R15,BKNIGHT
         LA    R9,5(R9)
         B     FOUND
         ENDIF
         EJECT
         LBR   R4,R2              RECOVER BOARD ONE LAST TIME
         NB    R4,WP              WHITE PAWNS.
         TSTB  R4
         IF    NZ
         SLDL  R14,3              PAWNS ARE 3.
         A     R15,WPAWN
         LA    R9,3(R9)
         B     FOUND
         ENDIF
         LBR   R4,R2
         NB    R4,BP              BLACK PAWNS
         TSTB  R4
         IF    NZ
         SLDL  R14,3
         A     R15,BPAWN          GET A BLACK PAWN IN.
         LA    R9,3(R9)
         B     FOUND
         ENDIF
         EJECT
         SLDL  R14,1              EMPTY SQUARES ARE 1
         LA    R9,1(R9)           ACCUM BIT COUNT.
FOUND    C     R9,=F'50'          OVER TOP OF BITS?
         IF    H
         LA    R8,64              COMPUTE SHIFT COUNT.
         SR    R8,R9              SHIFT COUNT = 64 - BITS IN REG.
         SLDL  R14,0(R8)          SHIFT IT.
         ST    R14,0(R10)         SAVE IT AWAY.
         LA    R10,4(R10)         BUMP POINTER.
         SRDL  R14,0(R8)          REPLACE THE BIT STRING.
         S     R9,=F'32'          WE JUST STORED 32 BITS OF IT.
         ENDIF
         SRDL  R2,1               GET TO NEXT SQUARE.
         TSTB  R2                 STILL THERE?
         UNTIL Z                  LOOP UNTIL NOT.
         EJECT
         SLDL  R14,11             MAKE ROOM FOR THE REST OF THE KEY.
         LA    R9,11(R9)          NOTE THE USAGE OF THE BITS.
         TM    WCASTLE,X'01'      CAN WHITE CASTLE K-SIDE?
         IF    NZ
         O     R15,=X'00000400'
         ENDIF
         TM    BCASTLE,X'01'      CAN BLACK CASTLE K-SIDE?
         IF    NZ
         O     R15,=X'00000200'
         ENDIF
         TM    WCASTLE,X'02'      CAN WHITE CASTLE Q-SIDE?
         IF    NZ
         O     R15,=X'00000100'
         ENDIF
         TM    BCASTLE,X'02'
         IF    NZ
         O     R15,=X'00000080'
         ENDIF
         CLI   COLOR,X'00'        IS IT WHITE TO MOVE?
         IF    Z
         O     R15,=X'00000040'
         ENDIF
         SR    R0,R0              MAKE SPACE FOR ENPASS CODE.
         IC    R0,ENPASS          GET IT.
         OR    R15,R0             AND MOVE IT INTO THE REG.
         LA    R8,64              COMPUTE THE INDEX.
         SR    R8,R9              COMPUTE BIT SHIFT.
         SLDL  R14,0(R8)          SHIFT BIT STRING.
         ST    R14,0(R10)         SAVE FIRST PART.
         ST    R15,4(R10)         SAVE SECOND PART.
         EXIT  RC=0
         EJECT
WKING    DC    X'0000003F'        WHITE KING CODE
BKING    DC    X'0000002F'        BLACK KING CODE
WQUEEN   DC    X'0000003E'        WHITE QUEEN CODE
BQUEEN   DC    X'0000002E'        BLACK QUEEN CODE
WROOK    DC    X'0000001E'        WHITE ROOK CODE
BROOK    DC    X'00000016'        BLACK ROOK CODE.
WBISHOP  DC    X'0000001D'        WHITE BISHOP CODE.
BBISHOP  DC    X'00000015'        BLACK BISHOP CODE.
WKNIGHT  DC    X'0000001C'        WHITE KNIGHT CODE.
BKNIGHT  DC    X'00000014'        BLACK KNIGHT CODE.
WPAWN    DC    X'00000006'        WHITE PAWN CODE.
BPAWN    DC    X'00000004'        BLACK PAWN CODE
POSDB    DSECT
         POSDB
         REGS
         END
