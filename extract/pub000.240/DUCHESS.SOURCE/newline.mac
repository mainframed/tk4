         TITLE 'LINE HANDLING UTILITIES'
*
* GET NEXT LINE INTO BUFFER
*
NEWLINE  CSECT
         ENTRY ENDLINE
         ENTRY PROMPT
         ENTRY SINLEFT
         ENTER
         L     R2,=V(SINENT)
         USING FILEDSCT,R2
         L     R4,FILE@IB
         LH    R5,0(R4)
         TST   R5
         IF    H
         L     R6,=V(DELIMSTR)
         CLC   2(1,R4),2(R6)
         IF    E
         S     R5,=F'1'
         STH   R5,0(R4)
         EX    R5,MOVEBUFF
         ENDIF
         ZERO  R15
         ELSE
         LR    R1,R2
         L     R15,FILE@RD
         BALR  R14,R15
         ENDIF
         EXIT  RC=(R15)
MOVEBUFF MVC   2(1,R4),3(R4)
         DROP  R2
         EJECT
*
* WARNS ABOUT EXTRA INPUT, AND DRAINS LINE
*
ENDLINE  ENTER DSA=YES
         GTKN
         TST   R15
         IF    Z
         LR    R5,R1
         GSTR
         LR    R6,R1
         PRNTF 'Extra input --> %S%S%L',((R5),(R6)),PLIST=PLIST
         ENDIF
         EXIT
         EJECT
*
* PROMPTS IF BUFFER IS EMPTY. R1 -> PROMPT
*
PROMPT   ENTER DSA=YES
         LR    R6,R1              SAVE @PROMPTSTRING
         SCALL SINLEFT
         TST   R15
         IF    NP
         LA    R4,4
         L     R1,=V(STATSLVL)
         L     R1,0(R1)
         C     R1,=F'1'
         IF    NL
         LA    R4,6
         ENDIF
         PRNTF '%S%L',((R6)),PLIST=PLIST,FO=(R4)
         ENDIF
         EXIT
         EJECT
*
* RETURNS NUMBER OF CHARACTERS IN SIN BUFFER
*
SINLEFT  ENTER
         L     R2,=V(SINENT)
         USING FILEDSCT,R2
         L     R4,FILE@IB
         DROP  R2
         LH    R15,0(R4)
         EXIT  RC=(R15)
         EJECT
         DSA
PLIST    DS    8F
         ENDDSA
         FILEDSCT
         REGS
         END
