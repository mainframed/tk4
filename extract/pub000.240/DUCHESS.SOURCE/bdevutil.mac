         TITLE 'BDEVUTIL -- WEAK SQUARES, PASSED PAWNS'
*
*        ALL PARAMETERS ARE PASSED IN
*        REGISTERS AND ALL RESULTS ARE RETURNED IN REGISTERS OR IN
*        PRE-DEFINED OFFSETS IN THE BOARD EVALUATOR'S DATA AREA.
*
*        ON ENTRY:
*              R1 -> COUNT ONE'S TABLE
*              R11 -> POSITION DATABASE
*              R13 -> BOARD EVALUATOR'S DATA AREA.
*
*        ON EXIT:
*              ALL REGISTERS EXCEPT R0, R1, R11,R12,R13,R14 ARE
*              ALTERED. REGISTERS 7, 8, 9, AND 10 HAVE VALUES FOR
*              POSITIONAL TERMS. WHOLES AND BHOLES ON THE DATA AREA
*              HAVE BEEN INITIALISED.
*
         EJECT
*
*        MACRO TO COMPUTE A WEAK SQUARE MAP.
*
         MACRO
&LABEL   GWSQS &US,&THEM
         LCLC  &DIR
&DIR     SETC  'U'
         AIF   ('&US' EQ 'W').GENPLUS
&DIR     SETC  'D'
.GENPLUS ANOP
&LABEL   LB    EPAWNB,&THEM.P
         COMB  EPAWNB
         LB    HOLEB,&US.P
         LBR   TEMPB,HOLEB
         SHFTB TEMPB,&DIR
         NBR   TEMPB,EPAWNB
         OBR   HOLEB,TEMPB
         SHFTB TEMPB,&DIR
         NBR   TEMPB,EPAWNB
         OBR   HOLEB,TEMPB
         LBR   TEMPB,HOLEB
         SHFTB TEMPB,&DIR.L
         SHFTB HOLEB,&DIR.R
         OBR   HOLEB,TEMPB
         COMB  HOLEB
         NBR   HOLEB,EPAWNB
         MEND
*
*        MACRO TO EVALUATE WEAK SQUARES.
*
         MACRO
&LABEL   WKSQS &US,&THEM
&LABEL   LBR   R2,HOLEB           GET THE HOLES FOR THIS SIDE.
         NB    R2,&THEM.PT        FOR NOW, KEEP ONLY PAWN THREATS.
         NB    R2,&US.WSMSK       KEEP ONLY A MASK OF THE SQUARES.
         LB    R4,&US.P           GET OUR PAWNS.
         COMB  R4
         NBR   R2,R4              IGNORE OUR WEAK PAWNS FOR NOW.
         GETFL R14,R15,R2
         COBY  R14
         MH    R14,&THEM.PO       WEIGHT BY ENEMY PIECES
         AR    HOLESVAL,R14
         LB    R4,&THEM.N
         OB    R4,&THEM.B         GET HIS MINOR PIECES.
         NBR   R4,R2              COMPUTE THE WEAK SQUARES WITH HIS PCS
         GETFL R14,R15,R4
         COBY  R14
         MH    R14,&THEM.PO
         AR    OCCHLVAL,R14
         LB    R4,&THEM.NT        GET HIS MINOR PIECE THREATS.
         OB    R4,&THEM.BT
         NBR   R4,R2              KEEP ONLY SQUARES HE ATTACKS.
         GETFL R14,R15,R4
         COBY  R14
         MH    R14,&THEM.PO
         AR    ATTHLVAL,R14
         MEND
*
* PASSED PAWNS MACRO
*
         MACRO
&LABEL   PASSP &US,&USDIR,&THEM,&THEMDR,&EWHITE,&PROMR
&LABEL   LB    R6,MASK&THEM.P
         LB    R4,&THEM.P
         OB    R4,&THEM.PT
         COMB  R4
         LBR   R2,R6
         REPEAT
         NBR   R6,R4
         OBR   R2,R6
         SHFTB R6,&THEMDR
         TSTB  R6                 MORE POTENTIAL PASSED PAWN FILES?
         UNTIL Z
         NB    R2,&US.P
         TSTB  R2
         IF    NZ
*
* PAWN RACE: QUEENABLE TERM
*
         LH    R4,&THEM.PO
         C     R4,=F'1'
         IF    NH                 OPPONENT HAS ONLY KING AND PAWNS
         LB    R14,W
         OB    R14,B
         COMB  R14
         STB   R14,EMPTYB
         LB    R14,&US.KT
         COMB  R14
         STB   R14,SAFSQ
         LA    R10,PQVALS
         LB    R6,&THEM.K
         CLI   COLOR,X'00'
         IF    &EWHITE
         LB    R6,&THEM.KT
         LA    R10,2(10)          1 MOVE DELAY PENALTY
         ENDIF
         LBR   R4,R2
         REPEAT
         SHFTB R4,&USDIR
         NB    R4,EMPTYB
         LBR   R14,R6
         SHFTB R14,L
         OBR   R14,R6
         SHFTB R6,R
         OBR   R6,R14
         LBR   R14,R6
         SHFTB R14,U
         OBR   R14,R6
         SHFTB R6,D
         OBR   R6,R14
         NB    R6,SAFSQ           REMOVE SQUARES OUR PIECES DEFEND
         OBR   R4,R6
         XBR   R4,R6
         AIF   ('&PROMR' EQ 'H').GENH
         GETL  R15,R4
         AGO   .GENM
.GENH    GETH  R15,R4
.GENM    IF    NZ
         AH    R9,0(R10)
         BREAK ,2
         ENDIF
         LA    R10,2(R10)
         TSTB  R4
         UNTIL Z
         ENDIF
*
* PASSED PAWN ADVANCEMENT, BLOCKING, ATTACK, AND DEFENSE
*
         LH    R5,&US.PO
         CH    R5,&US.EO
         WHILE L
         LC    R4,&US.SQUARES(R5)
         LA    R5,1(R5)
         BSETB R6,0(R4)
         NBR   R6,R2
         TSTB  R6
         IF    NZ
         LA    R10,5
         SHFTB R6,&USDIR
         LB    R14,&THEM
         NBR   R14,R6
         ANYB  R14,R14
         AR    R10,R14
         AR    R10,R14
         LB    R14,&THEM.T
         NBR   R14,R6
         ANYB  R14,R14
         AR    R10,R14            LOSE 1 PT FOR ATTACK
         LB    R14,&US.T
         NBR   R14,R6
         ANYB  R14,R14
         SR    R10,R14            GAIN 1 PT FOR DEFENSE
         SRL   R4,3
         AR    R4,R4
         MH    R10,&US.PVALS(R4)
         AR    R9,R10
         ENDIF
         CH    R5,&US.EO
         ENDWHILE
         ENDIF
         MEND
         EJECT
BDEVUTIL CSECT
         STM   R12,R14,12(R13)    SAVE NECESSARY REGISTERS.
         LR    R12,R15            GET A(THIS CSECT)
         USING BDEVUTIL,R12       NOTE ITS USAGE.
         USING POSDB,R11          NOTE POSITION DATABSSE.
         USING COTAB,R1           NOTE COUNT ONE'S TABLE.
         USING SAVEAREA,R13       NOTE R13 USAGE.
         ZERO  HOLESVAL           CLEAR HOLE VALUE.
         ZERO  OCCHLVAL           CLEAR OCCUPIED HOLES VALUE.
         ZERO  ATTHLVAL           CLEAR ATTACKED HOLES VALUE.
*
*        WEAK SQUARE COMPUTATION AND EVALUATION - BLACK.
*
         GWSQS B,W
         WKSQS B,W
         STB   HOLEB,BHOLES
         NEG   HOLESVAL
         NEG   OCCHLVAL
         NEG   ATTHLVAL
*
*        WEAK SQUARE COMPUTATION AND EVALUATION - WHITE
*
         GWSQS W,B
         WKSQS W,B
         STB   HOLEB,WHOLES
         STM   R8,R10,TMPREGS
         EJECT
*
* PASSED PAWNS - BLACK
*
         ZERO  R9
         PASSP B,D,W,U,E,L
*
* PASSED PAWNS - WHITE
*
         NEG   R9
         PASSP W,U,B,D,NE,H
         LR    R7,R9
         LM    R8,R10,TMPREGS
         LM    R12,R14,12(R13)
         BR    R14                AND LEAVE.
         EJECT
         CNOP  0,8
BWSMSK   DC    X'FFFFFFFFFF000000'
WWSMSK   DC    X'000000FFFFFFFFFF'
MASKWP   DC    X'000000000000FF00'
MASKBP   DC    X'00FF000000000000'
EMPTYB   DS    D
SAFSQ    DS    D
TMPREGS  DS    3F
PQVALS   DC    H'140,100,70,50,33,20,15'
WPVALS   DC    H'000,10,7,5,3,2,1,000'
BPVALS   DC    H'000,1,2,3,5,7,10,000'
         DSA
WHOLES   DS    2F                 WHITE HOLES.
BHOLES   DS    2F                 BLACK-HOLES.
COTAB    DSECT
POSDB    DSECT
         POSDB
         REGS
EPAWNB   EQU   2                  ENEMY PAWN BOARD.
TEMPB    EQU   4                  TEMPORARY BOARD.
HOLEB    EQU   6                  HOLE BOARD.
HOLESVAL EQU   10                 VALUE OF THE HOLES.
OCCHLVAL EQU   9                  V&LUE OF OCCUPIED HOLES.
ATTHLVAL EQU   8                  VALUE OF ATTACKED HOLES.
         END
