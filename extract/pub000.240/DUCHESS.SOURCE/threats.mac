         MACRO
&LABEL   EXTND &R1,&R2,&DIR,&EMPTY,&SRC
         LBR   &R2,&SRC           GET SOURCE.
         REPEAT
         SHFTB &R2,&DIR           SHIFT BOARD.
         OBR   &R1,&R2            OR IN NEW THREATS.
         NBR   &R2,&EMPTY         AND OFF ALL BUT EMPTY SQUARES.
         TSTB  &R2                ANYTHING LEFT?
         UNTIL Z                  LOOP UNTIL NOTHING LEFT.
         MEND
         TITLE 'THREATS -- THREAT GENERATOR FOR DUCHESS 6.0'
THREATS  CSECT
         ENTRY THRTGEN
THRTGEN  ENTER
         LR    R11,R1             GET THE POSITION DATABASE.
         USING POSDB,R11          NOTE USAGE
         L     R10,=V(THRTTBLS)   GET THE THREAT TABLES.
         USING THRTSDEF,R10
         XBR   R0,R0              CLEAR TOTAL WHITE THREATS.
         XBR   R14,R14            CLEAR TOTAL BLACK THREATS.
*
*        KING THREATS
*
         SR    R2,R2              CLEAR TEMP REG.
         IC    R2,WSQUARES        GET KING.
         SLL   R2,3               SHIFT TO DOUBLEWORD BOUNDARY.
         LB    R4,KINGTBL(R2)     GET THE APPROPRIATE TABLE.
         STB   R4,WKT             SAVE IN KING THREATS.
         OBR   R0,R4              OR KING THREATS INTO TOTAL
         SR    R2,R2              CLEAR REG. AGAIN.
         IC    R2,BSQUARES        GET BLACK KING.
         SLL   R2,3               GET TO DOUBLEWORD BOUNDARY
         LB    R4,KINGTBL(R2)     GET APPROPRIATE BOARD
         STB   R4,BKT             SAVE IT
         OBR   R14,R4             OR IN KING THREATS INTO TOTAL.
         EJECT
*
*        PAWN THREATS
*
         LB    R2,WP              GET WHITE PAWNS
         LBR   R4,R2
         SHFTB R4,UR              SHIFT TO ...
         SHFTB R2,UL              ... APPROPRIATE OFFSETS
         OBR   R2,R4              OR TOGETHER
         STB   R2,WPT             SAVE THEM
         OBR   R0,R2              OR PAWN THREATS INTO TOTAL.
         LB    R2,BP              GET BLACK PAWNS
         LBR   R4,R2              MOVE INTO OTHER REG.
         SHFTB R4,DR              MOVE DOWN.
         SHFTB R2,DL              SHIFT OTHER WAY
         OBR   R2,R4              OR TOGETHER.
         STB   R2,BPT             SAVE THEM
         OBR   R14,R2             OR PAWN THREATS INTO TOTAL.
         EJECT
*
*        KNIGHT THREATS
*
         XBR   R4,R4              CLEAR A BOARD.
         LH    R2,WNO
         CH    R2,WNO+2           OFF END YET?
         WHILE NZ                 LOOP OVER BOARD
         SR    R3,R3              CLEAR A TEMP. REG.
         IC    R3,WSQUARES(R2)    GET A SQUARE
         SLL   R3,3               SHIFT TO DOUBLEWORDS.
         OB    R4,KNITTBL(R3)     OR IN KNIGHT THREATS.
         LA    R2,1(R2)           INCREMENT POINTER.
         CH    R2,WNO+2           OFF END YET?
         ENDWHILE                 LOOP UNTIL DONE.
         STB   R4,WNT             SAVE
         OBR   R0,R4              OR KNIGHT THREATS INTO TOTAL.
         LH    R2,BNO             GET BLACK KNIGHTS
         XBR   R4,R4              CLEAR A BOARD.
         CH    R2,BNO+2           OFF END YET?
         WHILE NZ                 LOOP OVER KNIGHTS.
         SR    R3,R3              CLEAR A REG.
         IC    R3,BSQUARES(R2)    GET A KNIGHT
         SLL   R3,3               SHIFT IT
         OB    R4,KNITTBL(R3)     OR IN THAT BOARD
         LA    R2,1(R2)           INCREMENT POINTER
         CH    R2,BNO+2           OFF END YET?
         ENDWHILE                 LOOP OVER ENTIRE BOARD.
         STB   R4,BNT             SAVE BLACK KNIGHT THREATS.
         OBR   R14,R4             OR KNIGHT THREATS INTO TOTAL.
         EJECT
*
*        BISHOP THREATS
*
         LB    R8,W               GET WHITE PIECES.
         OB    R8,B               AND BLACK PIECES.
         COMB  R8                 COMPLEMENT BOARD.
         XBR   R2,R2              CLEAR A BOARD.
         LB    R6,WB              GET WHITE BISHOPS.
         EXTND R2,R4,UR,R8,R6     EXTEND BISHOPS.
         EXTND R2,R4,DR,R8,R6     EXTEND DOWN RIGHT
         EXTND R2,R4,DL,R8,R6     EXTEND DOWN LEFT.
         EXTND R2,R4,UL,R8,R6     EXTEND UP LEFT.
         STB   R2,WBT             SAVE IN BISHOP THREATS.
         OBR   R0,R2              OR BISHOP THREATS INTO TOTAL.
         XBR   R2,R2              CLEAR A BOARD.
         LB    R6,BB              GET BLACK BISHOPS.
         EXTND R2,R4,UR,R8,R6     EXTEND UP RIGHT
         EXTND R2,R4,DR,R8,R6     EXTEND DOWN RIGHT
         EXTND R2,R4,DL,R8,R6     EXTEND DOWN LEFT.
         EXTND R2,R4,UL,R8,R6     EXTEND UP LEFT.
         STB   R2,BBT             SAVE IN BISHOP THREATS.
         OBR   R14,R2             OR BISHOP THREATS INTO TOTAL.
         EJECT
*
*        ROOK THREATS
*
         XBR   R2,R2              CLEAR A BOARD.
         LB    R6,WR              GET WHITE ROOKS.
         EXTND R2,R4,U,R8,R6      EXTEND UP
         EXTND R2,R4,L,R8,R6      EXTEND LEFT.
         EXTND R2,R4,D,R8,R6      EXTEND DOWN.
         EXTND R2,R4,R,R8,R6      EXTEND RIGHT
         STB   R2,WRT             SAVE IN ROOK THREATS.
         OBR   R0,R2              OR ROOK THREATS INTO TOTAL.
         LB    R6,BR              GET BLACK ROOKS
         XBR   R2,R2              CLEAR A REGISTER.
         EXTND R2,R4,U,R8,R6      EXTEND UP
         EXTND R2,R4,L,R8,R6      EXTEND LEFT
         EXTND R2,R4,D,R8,R6      EXTEND DOWN
         EXTND R2,R4,R,R8,R6      EXTEND RIGHT
         STB   R2,BRT             SAVE IN BLACK ROOK THREATS.
         OBR   R14,R2             OR ROOK THREATS INTO TOTAL.
         EJECT
*
*        QUEEN THREATS.
*
         LB    R6,WQ              GET WHITE QUEENS.
         XBR   R2,R2              GET AN EMPTY BOARD.
         EXTND R2,R4,U,R8,R6      UP ...
         EXTND R2,R4,UR,R8,R6     UP RIGHT ...
         EXTND R2,R4,R,R8,R6      RIGHT ...
         EXTND R2,R4,DR,R8,R6     DOWN RIGHT ...
         EXTND R2,R4,D,R8,R6      DOWN ...
         EXTND R2,R4,DL,R8,R6     DOWN LEFT ...
         EXTND R2,R4,L,R8,R6      LEFT ...
         EXTND R2,R4,UL,R8,R6     UP LEFT ...
         STB   R2,WQT             SAVE IN QUEEN THREATS.
         OBR   R0,R2              OR QUEEN THREATS INTO TOTAL.
         XBR   R2,R2              GET AN EMPTY BOARD.
         LB    R6,BQ              GET BLACK QUEENS.
         EXTND R2,R4,U,R8,R6      UP ...
         EXTND R2,R4,UR,R8,R6     UP RIGHT ...
         EXTND R2,R4,R,R8,R6      RIGHT ...
         EXTND R2,R4,DR,R8,R6     DOWN RIGHT ...
         EXTND R2,R4,D,R8,R6      DOWN ...
         EXTND R2,R4,DL,R8,R6     DOWN LEFT ...
         EXTND R2,R4,L,R8,R6      LEFT ...
         EXTND R2,R4,UL,R8,R6     UP LEFT ...
         STB   R2,BQT             SAVE IN BLACK QUEEN THREATS.
         OBR   R14,R2             OR QUEEN THREATS INTO TOTAL.
         STB   R0,WT              SAVE WHITE THREATS.
         STB   R14,BT             SAVE BLACK THREATS.
         EXIT                     AND LEAVE.
         EJECT
         REGS
THRTSDEF DSECT
KINGTBL  DS    64D                THREATS FOR THE KING
         DS    64D                ROOK TABLES -- NOT IMP.
         DS    64D                BISHOP TABLES -- NOT IMP.
KNITTBL  DS    64D                THREATS FOR THE KNIGHTS.
POSDB    DSECT
         POSDB
         END
