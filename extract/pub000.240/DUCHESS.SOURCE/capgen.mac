         TITLE 'CAPGEN -- CAPTURES, PROMOTIONS, MATE TRIES, CASTLING'
         MACRO
&LABEL   TRY   &P,&R
&LABEL   LB    R0,W&P.T-WK(R11)
         NBR   R0,R8
         TSTB  R0
         IF    NZ
         L     R15,=V(M&R)
         BALR  R14,R15
         LR    R10,R1
         ENDIF
         MEND
*
*  0(R1) -> POSDB
*  4(R1) -> MLIST
*  RTN -> NEXT AVAILABLE SLOT
*
CAPGEN   CSECT
         EXTRN CASTLGEN
         ENTER
         L     R4,0(R1)
         USING POSDB,R4
         LC    R2,COLOR
         SLA   R2,2
         L     R10,4(R1)
         LR    R11,R4
         LR    R5,R4
         A     R11,DBOF(R2)
         A     R5,DBOF+4(R2)
*
*  R11 -> ACTIVE DB
*  R10 -> MLIST
*  R5 -> PASSIVE DB
*  R4 -> POSDB
*
         LB    R6,W-WK(R11)
         OB    R6,B-BK(R5)
         COMB  R6                 EMPTY SQUARES
         EJECT
*
*  PAWNS
*
         LC    R2,ENPASS
         BSETB R8,0(R2)
         OB    R8,RANKS18         INCLUDE QUEENING SQUARES
         NBR   R8,R6              MAKE SURE SQUARES ARE EMPTY
         OB    R8,B-BK(R5)
         CLI   COLOR,X'00'
         IF    E
         TRY   P,WPAWN
         ELSE
         TRY   P,BPAWN
         ENDIF
         EJECT
*
*  KNIGHT CAPTURES AND FORKS OF KING AND MAJOR PIECE
*
         L     R14,=V(NTTBL)
         LC    R15,BSQUARES-BK(R5)
         SLL   R15,3
         LB    R8,WNT-WK(R11)     SQUARES WHICH KNIGHTS ATTACK
         NBR   R8,R6              EMPTY ATTACKED SQUARES
         NB    R8,0(R14,R15)      KNIGHT KING CHECKS
         TSTB  R8
         IF    NZ
         XBR   R2,R2
         LH    R1,BQO-BK(R5)
         CH    R1,BQO+4-BK(R5)
         WHILE L
         LC    R15,BSQUARES-BK(R5,R1)
         SLL   R15,3
         OB    R2,0(R14,R15)
         LA    R1,1(R1)
         CH    R1,BQO+4-BK(R5)
         ENDWHILE
         NBR   R8,R2              KNIGHT CHECKS W/ FORK
* REMOVE SQUARES DEFENDED BY PAWN OR MINOR PIECE
         LB    R2,BPT-BK(R5)
         OB    R2,BNT-BK(R5)
         OB    R2,BBT-BK(R5)
         OBR   R8,R2
         XBR   R8,R2
         ENDIF
         OB    R8,B-BK(R5)
         TRY   N,KNIGHT
*
* BISHOPS
*
         LB    R8,B-BK(R5)
         TRY   B,BISHOP
         EJECT
*
*  ROOK/QUEEN CAPTURES AND SOME MATE TRIES
*
         LB    R14,BK-BK(R5)      PASSIVE KING
         LBR   R0,R14
         SHFTB R0,L
         LBR   R2,R14
         SHFTB R2,R
         OBR   R0,R2
         OB    R0,WT-WK(R11)
         OB    R0,B-BK(R5)
         LB    R2,BKT-BK(R5)
         NBR   R0,R2
         XBR   R0,R2
         TSTB  R0
         IF    Z
*
*  COMPUTE EMPTY SQUARES ADJACENT TO KING, ON SAME RANK
*
         LBR   R2,R14
         SHFTB R2,L
         NBR   R2,R6
         TSTB  R2
         WHILE NZ
         OBR   R0,R2
         SHFTB R2,L
         NBR   R2,R6
         TSTB  R2
         ENDWHILE
         LBR   R2,R14
         SHFTB R2,R
         NBR   R2,R6
         TSTB  R2
         WHILE NZ
         OBR   R0,R2
         SHFTB R2,R
         NBR   R2,R6
         TSTB  R2
         ENDWHILE
*
*  REMOVE SQUARES WHICH ARE DEFENDED AND NOT MULTIPLY ATTACKED
*
         LB    R2,WR-WK(R11)
         OB    R2,WQ-WK(R11)
         LB    R14,WRT-WK(R11)
         OB    R14,WQT-WK(R11)
         NBR   R2,R14             R,Q BATTERIES
         LB    R14,WBT-WK(R11)    BISHOP ATTACKS
         OB    R14,WNT-WK(R11)    KNIGHT ATTACKS
         NBR   R14,R0
         OBR   R2,R14
         GETFL R15,R14,R2
         MAKFL R2,R15
         LB    R14,BT-BK(R5)
         COMB  R14
         OBR   R2,R14
         NBR   R0,R2              ELIGIBLE "BACK RANK" MATE SQUARES
         OBR   R8,R0
         ENDIF
*
* ROOKS
*
         TRY   R,ROOK
*
* INCLUDE SAFE QUEEN MOVES ADJACENT TO KING
*
         LB    R0,WQT-WK(R11)
         NB    R0,BKT-BK(R5)
         NBR   R0,R6
         TSTB  R0
         IF    NZ
         LB    R2,BPT-BK(R5)
         OB    R2,BNT-BK(R5)
         OB    R2,BBT-BK(R5)
         OB    R2,BRT-BK(R5)
         OB    R2,BQT-BK(R5)
         OBR   R0,R2
         XBR   R0,R2
         TSTB  R0
         IF    NZ
         LB    R2,WQ-WK(R11)
         NB    R2,WRT-WK(R11)
         TSTB  R2
         IF    Z
         LB    R2,WPT-WK(R11)
         OB    R2,WNT-WK(R11)
         OB    R2,WBT-WK(R11)
         OB    R2,WRT-WK(R11)
         NBR   R0,R2
         ENDIF
         OBR   R8,R0
         ENDIF
         ENDIF
         TRY   Q,QUEEN
         EJECT
*
*  KING
*
         CLI   WCASTLE-WK(R11),X'00'
         IF    NZ
         SCALL CASTLGEN
         LR    R10,R1
         ENDIF
         LB    R8,B-BK(R5)
         LB    R0,BT-BK(R5)
         NBR   R0,R8
         XBR   R8,R0
         TRY   K,KING
         EXIT  RTN=(R10)
         EJECT
         CNOP  0,8
RANKS18  DC    X'FF000000000000FF'
DBOF     DC    AL4(WK-WK)
         DC    AL4(BK-WK)
         DC    AL4(WK-WK)
POSDB    DSECT
         POSDB
         REGS
         END
