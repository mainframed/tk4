         TITLE '---- GETOKEN: ROUTINE TO RETURN A TOKEN FROM A BUFFER -*
               --'
***********************************************************************
*                                                                     *
*                                                                     *
*        GETOKEN:  ROUTINE TO RETUTN A TOKEN FROM A BUFFER            *
*                                                                     *
*        AUTHOR:                  E JENSEN                            *
*                                                                     *
*        DATE WRITTEN:            JUNE 10, 1977                       *
*                                                                     *
*                                                                     *
*        CALLING CONVENTIONS:                                         *
*              R0 HAS FILE OPTION WORD                                *
*              R13 -> STACK AREA
*              R14 HAS RETURN ADDRESS                                 *
*                                                                     *
*        RETURN CONVENTIONS:                                          *
*              R1 -> SRTING CONTAINING TOKEN                          *
*              R15 HAS RETURN CODE                                    *
*                                                                     *
*        RETURN CODES:                                                *
*               0 - GETOKEN SUCCEDED,  R1 -> STRING                   *
*               4 - BUFFER IS EMPTY,  R1 -> NOTHING                   *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
GETOKEN  CSECT
         ENTRY CHKDELIM
         EXTRN DELIMSTR
         EXTRN GETCHAR
         EXTRN UNGETCHR
         ENTER
         LR    R2,R0              FILE OPTION WORD
         LA    R3,STARTKN         ADDRESS TO START BUILDING TOKEN
         LR    R4,R3              NEXT SLOT POINTER
NEXTCHAR LR    R0,R2              LOAD FOW
         SCALL GETCHAR
         LTR   R15,R15            ANY THING IN BUFFER ?
         BNZ   ENDTOKEN           NO,  BRANCH
         SCALL CHKDELIM           CHECK IF IT IS A DELIMITER
         LTR   R15,R15            IS IT ?
         BZ    GOTDELIM           YES,  BRANCH
         STC   R1,0(,R4)          SAVE CHAR
         LA    R4,1(,R4)          BUMP POINTER
         B     NEXTCHAR           AND LOOP
UNDELIM  LR    R0,R2              FOW
         SCALL UNGETCHR           PUT DELIMITER BACK IN BUFFER
         B     ENDTOKEN           AND BRANCH
GOTDELIM CR    R3,R4              ANY NON-DELIMITER SEEN SO FAR ?
         BL    UNDELIM            YES, THEN GIVE THIS ONE BACK
         C     R1,=A(X'40')       A SPACE ?
         BE    NEXTCHAR           YES,  NO LEADING SPACES ALLOWED
         STC   R1,0(,R4)          SAVE DELIMITER
         LA    R4,1(,R4)          BUMP POINTER
ENDTOKEN EQU   *
         ZERO  R15
         LA    R1,STARTSTR
         SR    R4,R3
         STH   R4,0(R1)
         IF    NP
         LA    R15,4
         ENDIF
         EXIT  RC=(R15),RTN=(R1)
STARTSTR DC    H'0'
STARTKN  DC    CL80' '
         TITLE '---- CHKDELIM: ROUTINE TO CHECK FOR DELIMITERS ----'
***********************************************************************
*                                                                     *
*                                                                     *
*        CHKDELIM:  ROUTINE TO CHECK IF A CHARACTER IS A DELIMITER    *
*                                                                     *
*        AUTHOR:                  E JENSEN                            *
*                                                                     *
*        DATE WRITTEN:            JUNE 10, 1977                       *
*                                                                     *
*        LAST UPDATE:             JUNE 11, 1977                       *
*                                                                     *
*                                                                     *
*        CALLING CONVENTIONS:                                         *
*              R1 HAS CHARACTER IN QUESTION                           *
*              R13 -> STACK AREA                                      *
*              R14 HAS RETURN ADDRESS                                 *
*                                                                     *
*        RETURN CONVENTIONS:                                          *
*              R15 HAS RETURN CODE                                    *
*                                                                     *
*        RETURN CODES:                                                *
*               0 - CHARACTER IS A DELIMITER                          *
*               4 - CHARACTER IS NOT A DELIMITER                      *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
CHKDELIM ENTER
         LA    R2,X'FF'           LOW ORDER BYTE MASK
         NR    R1,R2              MAKE SURE TOP 3 BYTES ARE ZERO
         L     R2,=A(DELIMSTR)    R2 -> STRING OF DELIMITERS
         LH    R3,0(,R2)          R3 HAS LENGTH OF DELIM STRING
         LA    R2,2(,R2)          R2 -> STRING DATA
         LA    R3,0(R3,R2)        R3 -> PAST END OF STRING
         SR    R4,R4              ZERO FOR IC
NXTDELIM CR    R2,R3              DONE ?
         BNL   NOTDELIM           YES,  MUST NOT BE A DELIMITER
         IC    R4,0(,R2)          GET NEXT DELIM
         CR    R1,R4              MATCH ?
         BE    ADELIM             YES, A DELIMITER !
         LA    R2,1(,R2)          BUMP POINTER
         B     NXTDELIM           AND LOOP
ADELIM   SR    R15,R15
         B     DRETURN
NOTDELIM LA    R15,4
DRETURN  EXIT  RC=(R15)
         EJECT
         LTORG
         REGS
         END
