         TITLE 'GETCHAR -- RETURN THE NEXT CHARACTER IN A BUFFER'
***********************************************************************
*                                                                     *
*                                                                     *
*        GETCHAR:  ROUTINE TO RETURN A CHARACTER FROM A BUFFER        *
*        AUTHOR:                  E JENSEN                            *
*                                                                     *
*        DATE WRITTEN:            JUNE 10, 1977                       *
*                                                                     *
*        LAST UPDATE:             JUNE 11, 1977       ECJ             *
*                                                                     *
*                                                                     *
*        CALLING CONVENTIONS:                                         *
*              R0 HAS FILE OPTION WORD                                *
*              R13 -> STACK AREA                                      *
*              R14 HAS RETURN ADDRESS                                 *
*                                                                     *
*        RETURN CONVENTIONS:                                          *
*              R1 HAS CHARACTER (LOW ORDER BYTE)                      *
*              R15 HAS RETURN CODE                                    *
*                                                                     *
*        RETURN CODES:                                                *
*               0 - GETCHAR SUCCEDED, CHARACTER IS IN REG 1           *
*               4 - BUFFER IS EMPTY,  REG1 HAS NOTHIHG                *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
GETCHAR  CSECT
         ENTRY DELIMSTR
         EXTRN FILETBL
         ENTER
         L     R2,=A(FILETBL)     R2 -> FILE DESCRIPTION TABLE
         USING FILEDSCT,R2
         LA    R3,X'FF'           FILE SELECT BYTE MASK
         NR    R3,R0              LEAVE ONLY FILE SELECT BITS ON
         LCR   R4,R3
         NR    R3,R4              R3 HAS ONLY 1 BIT ON NOW
         SR    R4,R4              COMPARISON REG
NEXTFILE C     R4,0(,R2)          END OF FILES ?
         BE    ABEND              YES, FILE NOT FOUND
         LA    R5,1               A 1
         NR    R5,R3              IS LOW BIT SET ?
         BNZ   ATFILE             YES, THIS IS THE FILE
         LA    R2,FILETLEN(,R2)   POINT TO NEXT FILE
         SRL   R3,1               SHIFT FILE BIT 1
         B     NEXTFILE           AND LOOP
ATFILE   TM    FILEFLAG,FILEALOC+FILEUSE+FILEIPT
         BNO   ABEND              BOMB IF ABOVE CONDITIONS ARE NOT MET
         L     R4,FILE@IB         R4 -> INPUT BUFFER STRING
         LH    R5,0(,R4)          R5 HAS LENGTH OF BUFFER
         LTR   R5,R5              NULL ?
         BNP   RTNEOB             YES, RETURN END-OF-BUFFER MESSAGE
         CLC   2(1,R4),DELIMSTR+2 IS IT A LOGICAL BUFFER MARKER ?
         BE    RTNEOB             YES, RETURN WITH NOTHING
         SR    R1,R1              ZERO FOR IC
         IC    R1,2(,R4)          GET CHAR TO BE RETURNED
         MKU   R1                 MAKE IT UPPER CASE
         S     R5,=F'1'           NEW LENGTH
         STH   R5,0(,R4)          SAVE
         S     R5,=F'1'           LENGTH FOR EXECUTE
         BM    ZERORC             NOTHING LEFT TO SHIFT, LEAVE
         EX    R5,MOVEBUF         MOVE REST OF BUFFER DOWN ONE BYTE
ZERORC   SR    R15,R15            ZERO RETURN CODE
         B     RETURN             AND LEAVE
RTNEOB   SR    R1,R1              RETURN NOTHING
         LA    R15,4
RETURN   EXIT  RC=(R15),RTN=(R1)
ABEND    ABEND 1001,DUMP
MOVEBUF  MVC   2(1,R4),3(R4)
         EJECT
         LTORG
         DS    0H
DELIMSTR DC    AL2(L'DELIMS)
DELIMS   DC    C'; /,.-*%+>?:"=!^''()×Ö_&&<'
         FILEDSCT
         REGS
         END
