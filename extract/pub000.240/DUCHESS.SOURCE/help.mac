         TITLE 'HELP -- PROCESS HELP REQUESTS'
HELP     CSECT
         ENTRY HELPEOF
         EXTRN HELPENT
         EXTRN HELPDCB
         ENTER
         L     R2,=A(HELPENT)
         USING FILEDSCT,R2
         TM    FILEFLAG,FILEALOC+FILEIPT+FILEUSE
         IF    NO
         PRNTF 'HELP PDS not allocated, sorry%L'
         B     RET0
         ENDIF
         TM    FILEFLAG,FILEOPEN
         IF    Z
         L     R3,=A(HELPDCB)
         OPEN  ((R3),(INPUT))
         ENDIF
         GTKN
         TST   R15
         IF    NZ
         LA    R1,DEFSTR
         ENDIF
         REPEAT
         LA    R0,8
         CH    R0,0(R1)
         IF    L
         STH   R0,0(R1)
         ENDIF
         ZERO  R0
         CLC   STARSTR(3),0(R1)
         IF    E
         LA    R0,1
         ENDIF
         SCALL DOHELP
         GTKN
         TST   R15
         UNTIL NZ
         B     RET0
DEFSTR   STRNG 'HELP'
STARSTR  STRNG '*'
         EJECT
*
* PRINT HELP PDS ENTRY
* R0 = 1 FOR LIST OF MEMBERS, 0 FOR NORMAL SEARCH
* R1 -> HELP REQUEST STRING
*
DOHELP   ENTER DSA=YES
         STC   R0,LFLG
         STC   R0,AMBIG
         ZERO  R10                COLUMN COUNTER
         ST    R1,MATCHNM         SAVE NAME TO FIND
         MVI   HEOFLG,X'00'
         XC    FNAME(8),FNAME     CLEAR FULL NAME ALREADY FOUND
         LR    R2,R1
         LA    R1,X'100'
         ST    R1,HELPTTRZ
         L     R2,=A(HELPDCB)
         POINT (R2),HELPTTRZ
*
         LOOP
         L     R2,=A(HELPDCB)
         READ  HELPDECB,SF,(R2),HELPBUFF,'S',MF=E
         CHECK HELPDECB
         LH    R2,HELPBUFF        NUMBER OF BYTES IN CAT BLOCK
         LA    R2,HELPBUFF(R2)    ADDRESS OF FIRST UNUSED BYTE
         ST    R2,ENDCATB         SAVE END ADDRESS
         LA    R2,HELPBUFF+2      NEXT ENTRY IN CATALOG
*
         LOOP
         C     R2,ENDCATB         AT END OF BUFFER ?
         BREAK NL
         CLC   0(8,R2),=8X'FF'
         BE    HFDONE
         CLI   LFLG,X'01'
         IF    NE
         L     R3,MATCHNM         ADDR OF NAME TO FIND
         LH    R4,0(R3)           LENGTH OF NAME
         S     R4,=F'1'           CORRECT FOR EXECUTE
         EX    R4,COMPNM          COMPARE DESIRED:CURRENT
         ENDIF
         BL    HFDONE
         IF    E
         CLI   FNAME,X'00'        FOUND PREVIOUS MATCH
         IF    NE
         CLI   AMBIG,X'00'        FIRST AMBIG ?
         IF    E
         PRNTF '''%S'' is ambiguous:%L',((R3)),PLIST=PLIST
         MVI   AMBIG,X'FF'        SHOW FIRST AMBIG FOUND
         ENDIF
*
         LA    R4,8               LENGTH OF NAME
         STH   R4,FNMLEN          SAVE LENGTH
         LA    R4,FNMLEN          POINT TO STRING
         C     R10,=F'8'
         IF    NL
         ZERO  R10
         PRNTF '%L'
         ENDIF
         TST   R10
         IF    H
         PRNTF ' '
         ENDIF
         LA    R10,1(R10)
         PRNTF '%S',((R4)),PLIST=PLIST
         ENDIF
*
         MVC   FNAME(8),0(R2)     COPY NAME FROM CATALOG
         ENDIF
*
         LC    R3,11(R3)          FLAGS BYTE FROM CAT
         N     R3,=A(X'1F')       LOW 5 BITS
         AR    R3,R3              CONVERT FROM HALF TO BYTES
         LA    R2,12(R3,R2)       BUMP TO NEXT ENT
         ENDLOOP
         ENDLOOP
*
HFDONE   EQU   *
         CLI   FNAME,X'00'
         IF    E
         L     R2,MATCHNM
         PRNTF 'No help for ''%S''%L',((R2)),PLIST=PLIST
         B     RET0
         ENDIF
         CLI   AMBIG,X'00'        ANY AMBIG ?
         IF    E
         L     R2,=A(HELPDCB)
         FIND  (R2),FNAME,D
         MVI   HEOFLG,X'FF'
*
         LOOP
         L     R2,=A(HELPDCB)
         READ  HELPDECB,SF,(R2),HELPBUFF,'S',MF=E
         CHECK HELPDECB
         L     R3,68(R2)          DCBIOBAD
         LH    R3,14+8(R3)        RESIDUAL COUNT
         SH    R3,62(R2)          MINUS MAX COUNT
         NEG   R3                 IS # BYTES READ
         LA    R3,HELPBUFF(R3)
         ST    R3,ENDHELPB        SAVE END POINTER
         LA    R2,HELPBUFF        POINT TO FIRST RECORD
*
         LOOP
         C     R2,ENDHELPB        ANYTHING TO PRINT
         BREAK NL
         LA    R3,80              RECORD LENGTH
         LR    R4,R2              COPY
         S     R4,=F'2'           PREFIX FOR DATA
         LR    R5,R3
         S     R5,=F'8'
         STH   R5,0(R4)           SAVE LENGTH
         PRNTF '%S%L',((R4)),PLIST=PLIST,FO=2
         AR    R2,R3              BUMP POINTER
         ENDLOOP
         ENDLOOP
         ELSE
         LA    R4,8               REPEAT FOR LAST AMBIG
         STH   R4,FNMLEN          SAVE LENGTH
         LA    R4,FNMLEN          POINT TO STRING
         C     R10,=F'8'
         IF    NL
         ZERO  R10
         PRNTF '%L'
         ENDIF
         TST   R10
         IF    H
         PRNTF ' '
         ENDIF
         LA    R10,1(R10)
         PRNTF '%S%L',((R4)),PLIST=PLIST
         ENDIF
HRDONE   EQU   *
RET0     EXIT
COMPNM   CLC   2(1,R3),0(R2)      EXECUTED COMPARE
*
HELPEOF  CLI   HEOFLG,X'00'
         BE    HFDONE             IF ZERO, END OF CATALOG
         B     HRDONE             END OF DATA
*
         READ  HELPDECB,SF,0,0,'S',MF=L
         DSA
LFLG     DS    X
AMBIG    DS    X
HEOFLG   DS    X
FNMLEN   DS    H
FNAME    DS    CL8
MATCHNM  DS    F
HELPTTRZ DS    F
ENDCATB  DS    F
ENDHELPB DS    F
PLIST    DS    8D
HBUFPAD  DS    H
HELPBUFF DS    1600X
         ENDDSA
         FILEDSCT
         REGS
         END
