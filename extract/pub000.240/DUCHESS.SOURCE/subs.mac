         TITLE 'SUBS -- MISCELLANOUS SUBROUTINES'
SUBS     CSECT
         ENTRY INDEX
         ENTRY STRTRIM
         ENTRY STRPAD
         ENTRY STRSTRIP
         EJECT
*
* REMOVE TRAILING BLANKS FROM STRING AT (R1)
*
STRTRIM  ENTER
         LH    R2,0(R1)
         TST   R2
         WHILE H
         LA    R3,1(R2,R1)
         CLI   0(R3),C' '
         BREAK NE
         S     R2,=F'1'
         ENDWHILE
         STH   R2,0(R1)
         B     RET0
         EJECT
*
* PAD STRING AT (R1) WITH BLANKS TO LENGTH R0
*
STRPAD   ENTER
         LH    R2,0(R1)
         CR    R2,R0
         WHILE L
         LA    R3,2(R2,R1)
         MVI   0(R3),C' '
         LA    R2,1(R2)
         CR    R2,R0
         ENDWHILE
         STH   R2,0(R1)
         B     RET0
         EJECT
*
* REMOVE SEQUENCE NUMBER, IF ANY, FROM STRING AT (R1)
* HERE, A SEQUENCE NUMBER IS CHARS 73-80 OF AN 80 CHAR STRING.
*
STRSTRIP ENTER
         LH    R2,0(R1)
         C     R2,=F'80'
         IF    E
         S     R2,=F'8'
         STH   R2,0(R1)
         ENDIF
         B     RET0
         EJECT
*
*  INDEX(X1,X2)
*  0(R1) -> STRING TO BE SEARCHED
*  4(R1) -> STRING TO BE SEARCHED FOR
*  RC=0 IF MATCH, R1=OFFSET IN PATTERN
*
INDEX    ENTER
         L     R2,0(R1)
         L     R4,4(R1)
         LH    R3,0(R2)
         LH    R5,0(R4)
         S     R3,=F'1'
         S     R5,=F'1'
         LA    R2,2(R2)
         LA    R4,2(R4)
         LA    R7,0(R2,R3)
         ZERO  R8
         LOOP
         LA    R6,0(R2,R5)
         CLR   R6,R7
         IF    H
         EXIT  RC=4
         ENDIF
         EX    R5,$$$CLC
         IF    Z
         EXIT  RC=0,RTN=(R8)
         ENDIF
         LA    R8,1(R8)
         LA    R2,1(R2)
         ENDLOOP
$$$CLC   CLC   0(1,R2),0(R4)
         EJECT
RET0     EXIT
         REGS
         END
