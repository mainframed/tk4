         TITLE 'EVBOUND -- BOUND BOARD EVALUATION'
*
*        SUBROUTINE TO PUT BOUNDS ON THE BOARD EVALUATOR'S EVALUATION.
*
*        ALL CALLS TO THE BOARD EVALUATOR RETURN THROUGH THIS
*        PROCEDURE. IF THE POSITION IS A DRAW, OR IF ONE SIDE CANNOT
*        POSSIBLY BE AHEAD, THE VALUE IS ADJUSTED TO REFLECT THIS.
*
EVBOUND  CSECT
         USING *,R15              NOTE BASE REGISTERS.
         USING POSDB,R11
         L     R8,=F'30000'
         LNR   R9,R8
         LPR   R1,R1              GET ABSOLUTE VALUE OF MATERIAL DIFF.
*
* IF PAWNS ON BOARD, INCREASE THRESHOLD BY .5 PAWN
*
         LB    R2,WP
         OB    R2,BP
         TSTB  R2
         IF    NZ
         A     R1,=F'50'
         ENDIF
         C     R1,=F'350'         IS IT LESS THAN 1 MINOR PIECE?
         IF    L
         LB    R2,WP              IF WHITE HAS NO PAWNS
         TSTB  R2
         IF    Z
         LPR   R8,VALUE
         SRA   R8,1               HALVE POTENTIAL VALUE
         ENDIF
         LB    R2,BP              IF BLACK HAS NO PAWNS
         TSTB  R2
         IF    Z
         LNR   R9,VALUE
         SRA   R9,1
         ENDIF
         ENDIF
         LH    R1,WEO             GET NUMBER OF WHITE PIECES.
         C     R1,=F'3'           IS IT 3 OR FEWER?
         IF    NH                 YES -- CANNOT ALL BE KNIGHTS.
         C     R1,=F'1'           IS IT JUST ONE PIECE?
         IF    Z                  YES -- LOOK FOR STALEMATE.
         CLI   COLOR,X'00'        IS WHITE TO MOVE?
         IF    Z                  YES -- THEN HE MIGHT BE STALEMATED.
         LB    R2,BT              GET OPPONENT THREATS.
         COMB  R2
         NB    R2,WKT             GET OUR KING THREATS.
         TSTB  R2                 ANY SAFE SQUARES?
         IF    Z
         ZERO  VALUE
         ENDIF
         ENDIF
         ENDIF
         LB    R2,WP
         OB    R2,WB
         OB    R2,WR
         OB    R2,WQ
         TSTB  R2                 ANY GOOD PIECES?
         IF    Z                  NO -- DRAWN AT BEST IF BLACK HAS 0
         LB    R2,B               HAVE BLACK PIECES ...
         XB    R2,BK              CLEAR ALL BLAKK KINGS.
         TSTB  R2
         IF    Z                  IF ZERO -- CANNOT WIN.
         ZERO  R8
         ENDIF
         ENDIF
         ENDIF
         LH    R1,BEO             GET BLACK END OFFSET.
         C     R1,=F'3'           DOES HE HAVE LESS THAN OR EQUAL TO 3?
         IF    NH                 YES -- CANNOR ALL BE KNIGHTS.
         C     R1,=F'1'           HAS JUST ONE PIECE (KING)?
         IF    Z
         CLI   COLOR,X'00'        YES -- IS BLACK TO MOVE.
         IF    NZ                 YES -- HE MIGHT BE STLLEMATED.
         LB    R2,WT              GET WHITE THREATS
         COMB  R2
         NB    R2,BKT             AND WITH BLACK KING THRESTS.
         TSTB  R2
         IF    Z
         ZERO  VALUE
         ENDIF
         ENDIF
         ENDIF
         LB    R2,BP              GET ALL BLACK NON-KNIGHTS.
         OB    R2,BB
         OB    R2,BR
         OB    R2,BQ
         TSTB  R2                 ANY NON-KNIGHTS?
         IF    Z                  NO
         LB    R2,W               GET ALL WHITE PIECES.
         XB    R2,WK              EXCEPT WHITE KINGS.
         TSTB  R2
         IF    Z                  NO OPPONENT PIECES -- !?
         ZERO  R9                 ZERO OUR MAX VALUE.
         ENDIF
         ENDIF
         ENDIF
         MIN   R6,VALUE,R8
         LR    VALUE,R6
         MAX   R6,VALUE,R9
         LR    VALUE,R6
         CLI   COLOR,X'00'        WAS IT REALLY WHITE TO MOVE?
         IF    NZ
         NEG   VALUE
         ENDIF
         EXIT  RTN=(VALUE)
         EJECT
POSDB    DSECT
         POSDB
VALUE    EQU   0
         REGS
         END
