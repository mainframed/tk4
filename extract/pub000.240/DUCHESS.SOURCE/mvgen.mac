         TITLE 'MVGEN -- GENERATE ALL MOVES'
MVGEN    CSECT
         ENTRY CASTLGEN
*
         EXTRN MWPAWN
         EXTRN MBPAWN
         EXTRN MKNIGHT
         EXTRN MBISHOP
         EXTRN MROOK
         EXTRN MQUEEN
         EXTRN MKING
         ENTER
         L     R4,0(R1)
         USING POSDB,R4
         LC    R2,COLOR
         SLA   R2,2
         L     R10,4(R1)
         LR    R11,R4
         LR    R5,R4
         A     R11,DBOF(R2)
         A     R5,DBOF+4(R2)
*
*        R11 -> ACTIVE DATABASE
*        R10 -> MOVE LIST
*        R5 -> PASSIVE DATABASE
*        R4 -> POSITION DATABASE
*
         LB    R6,W-WK(R11)       GET OUR PIECES
         LBR   R8,R6              THE COMPLEMENT WILL BE ELEGIBLE
         OB    R6,B-BK(R5)        GET ALL PIECES
         COMB  R6                 CONSTRUCT EMPTY
         COMB  R8                 CONSTRUCT ELEGIBLE
         EJECT
*
*  GENERATE MOVES
*
         SCALL MKNIGHT
         LR    R10,R1
         SCALL MBISHOP
         LR    R10,R1
         SCALL MROOK
         LR    R10,R1
         SCALL MQUEEN
         LR    R10,R1
         L     R15,=V(MBPAWN)
         CLI   COLOR,X'00'
         IF    Z
         L     R15,=V(MWPAWN)
         ENDIF
         BALR  R14,R15
         LR    R10,R1
         TM    WCASTLE-WK(R11),KSCA+QSCA
         IF    NZ
         SCALL CASTLGEN
         LR    R10,R1
         ENDIF
         LB    R0,BT-BK(R5)       GET ENEMY THREATS.
         COMB  R0
         NBR   R8,R0              PREVENT MOVING INTO THEM.
         SCALL MKING
         EXIT  RTN=(R1)
         EJECT
*
* GENERATE CASTLING MOVES
*
CASTLGEN ENTER
         LR    R14,R11
         LR    R15,R5
         LR    R11,R4
         USING POSDB,R11
         LA    R9,BLACKSQR
         CLI   COLOR,X'00'        WHITE TO MOVE?
         IF    Z                  YES
         LA    R9,WHITESQR        R9 -> WORKSPACE
         ENDIF
         USING CASTBLS,R9
         LB    R2,BT-BK(R15)      GET OPPONENT THREATS
         LB    R4,WK-WK(R14)      GET OUR KING
         LBR   R6,R4
         NBR   R6,R2              ARE WE IN CHECK?
         TSTB  R6
         IF    Z                  NO -- CONTINUE CHECKS
         TM    WCASTLE-WK(R14),KSCA CAN WE CASTLE K-SIDE?
         IF    NZ                 YES -- CAN WE NOW?
         LBR   R6,R2              GET HIS THREATS
         OB    R6,W-WK(R14)       GET OUR PIECES TOO
         OB    R6,B-BK(R15)       AND HIS MEN
         NB    R6,KSCMASK
         TSTB  R6                 ANYTHING IN THE WAY?
         IF    Z
         LH    R8,KCAS
         STH   R8,0(R10)          SAVE THE MOVE
         LA    R10,2(R10)
         ENDIF
         ENDIF
         TM    WCASTLE-WK(R14),QSCA
         IF    NZ
         LBR   R6,R2              GET HIS THREATS FOR Q-SIDE
         NB    R6,QSCMSK1         TOO MANY THREATS?
         TSTB  R6
         IF    Z
         LB    R6,W-WK(R14)       GET OUR PIECES
         OB    R6,B-BK(R15)       AND HIS MEN
         NB    R6,QSCMSK2         ANY IN THE WAY?
         TSTB  R6
         IF    Z
         LH    R8,QCAS
         STH   R8,0(R10)          SAVE MOVE
         LA    R10,2(R10)
         ENDIF
         ENDIF
         ENDIF
         ENDIF
         EXIT  RTN=(R10)
         EJECT
         CNOP  0,8
BLACKSQR EQU   *
         DC    B'10000100',B'00000110' BLACK KING-SIDE CASTLE
         DC    B'10000100',B'00000010' BLACK QUEEN-SIDE CASTLE
         DC    X'06000000',X'00000000' KING SIDE MASK
         DC    X'30000000',X'00000000' BLACK QUEEN SIDE MASK
         DC    X'70000000',X'00000000' QUEEN SIDE MASK 2
WHITESQR EQU   *
         DC    B'10111100',B'00111110' WHITE KING-SIDE CASTLE
         DC    B'10111100',B'00111010' WHITE QUEEN-SIDE CASTLE
         DC    X'00000000',X'00000006' WHITE KING-SIDE MASK
         DC    X'00000000',X'00000030' WHITE QUEEN SIDE MASK ×
         DC    X'00000000',X'00000070' WHITE QUEEN SIDE MASK 2
QSCA     EQU   X'02'
KSCA     EQU   X'01'
DBOF     DC    AL4(WK-WK)
         DC    AL4(BK-WK)
         DC    AL4(WK-WK)
CASTBLS  DSECT
KCAS     DS    H                  KING SIDE CASTLE MOVE.
QCAS     DS    H                  QUEEN SIDE CASTLE MOVE.
KSCMASK  DS    CL8                KING SIDEE MASK
QSCMSK1  DS    CL8                QUEEN SIDE MASK 1 -- THREATS
QSCMSK2  DS    CL8                QUEEN SIDE MASK 2 -- PIECES
POSDB    DSECT
         POSDB
         REGS
         END
