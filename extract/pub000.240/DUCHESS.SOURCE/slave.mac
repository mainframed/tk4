SLAVE    CSECT
         STM   R14,R12,12(R13)    SAVE CALLING PROG'S REGS
         BALR  R12,0              FIND OURSELVES
         USING *,R12              ESTABLISH ADDRESSIBILITY
         LA    R15,SAVEAREA       POINT TO NEW SAVEAREA
         ST    R13,4(,R15)        SAVE BACK CHAIN TO HSA
         ST    R15,8(,R13)        SAVE FORWARD CHAIN TO LSA
         LR    R13,R15            POINT TO NEW SAVEAREA
         OPEN  (SLAVEDCB,(INPUT),TSOPTDCB,(OUTPUT))
         TIME  BIN
         ST    R1,OLDDATE
         S     R0,TIMELAG
         IF    NM
         ST    R0,OLDDATE+4
         ENDIF
NEXTREAD POINT SLAVEDCB,SLVTTRZ   POINT TO RECORD TO READ
         READ  SLVDECB1,SF,SLAVEDCB,SLVBUFF,'S'
         CHECK SLVDECB1           WAIT FOR I/O COMPLETION
         CLC   OLDDATE(8),SLVBDATE     A NEW RECORD TO PRINT ?
         BNL   WAIT               NOTHING NEW,  WAIT AWHILE
         MVC   OLDDATE(8),SLVBDATE     SAVE LATEST TIME
         PUT   TSOPTDCB,SLVBDATA  PRINT DATA FROM SLAVE FILE
         L     R0,SLVTTRZ         GET CURRENT POINTER
         A     R0,=A(X'100')      POINT TO NEXT RECORD TO READ
         C     R0,=A(SLV#RECS*256)     PAST END OF FILE ?
         IF    H                  YES,  WRAP AROUND
         LA    R0,X'100'          POINT TO BEGINNING
         ENDIF
         ST    R0,SLVTTRZ         SAVE NEW POINTER
         SR    R0,R0              ZERO R0
         ST    R0,#WAITS          INDICATE SUCCESS
         B     NEXTREAD           AND LOOP
         EJECT
WAIT     L     R0,#WAITS          GET OLD COUNT
         A     R0,=F'1'           PLUS THIS ONE
         ST    R0,#WAITS          SAVE IT
         S     R0,=A(MINWAIT#-1)
         IF    NP                 MIN # OF INCREMENTS IS 1
         LA    R0,1
         ENDIF
         C     R0,=A(MAXWAIT#)    CEILING
         IF    H
         L     R0,=A(MAXWAIT#)
         ENDIF
         MH    R0,=AL2(MINWAITM)  TIMES INCREMENT
         ST    R0,SLVWAITM        SAVE AS TIME TO WAIT
         STIMER WAIT,BINTVL=SLVWAITM   SLEEP
         B     NEXTREAD           AND LOOP
BOMB     ABEND 1,DUMP
         EJECT
SAVEAREA DS    9D
         LTORG
SLAVELEN EQU   80
SLV#RECS EQU   50
MINWAIT# EQU   10
MAXWAIT# EQU   10
MINWAITM EQU   50                 50/100 SECONDS
SLVWAITM DC    A(0)
#WAITS   DC    A(0)
SLVTTRZ  DC    A(X'100')
TIMELAG  DC    A(90*60*100)
OLDDATE  DC    XL8'00'
SLVBUFF  DS    0CL(SLAVELEN+12)
SLVBSEQ# DC    A(0)
SLVBDATE DC    A(0)
SLVBTIME DC    A(0)
SLVBDATA DC    CL(SLAVELEN)' '
SLAVEDCB DCB   DDNAME=SLAVE,MACRF=RP,DSORG=PS,RECFM=F,                 *
               BLKSIZE=SLAVELEN+12,EODAD=WAIT
TSOPTDCB DCB   DDNAME=SYSPRINT,MACRF=PM,DSORG=PS,RECFM=F,              *
               BLKSIZE=SLAVELEN
         REGS
         END
