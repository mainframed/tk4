         TITLE '     ---- PRINTF:    FILE OUTPUT FORMATTER ----'
***********************************************************************
*                                                                     *
*                                                                     *
*        PRINTF:   ROUTINE TO FORMAT OUTPUT IN BUFFERS                *
*                                                                     *
*        AUTHOR:                  E JENSEN                            *
*                                                                     *
*        DATE WRITTEN:       MAY 30, 1977                             *
*                                                                     *
*        LAST UPDATE:                                                 *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
*                                                                     *
*        CALLING CONVENTIONS:                                         *
*                                                                     *
*              R0 HAS FILE OPTION WORD                                *
*              R1 -> PARAMETER LIST                                   *
*              R13 -> SAVEAREA STACK                                  *
*              R14 HAS RETURN ADDRESS                                 *
*              R15 HAS ADDRESS OF THIS CSECT                          *
*                                                                     *
*                                                                     *
*        PARAMETER LIST CONVENTIONS:                                  *
*                                                                     *
*              0(R1) -> CONTROL STRING                                *
*              ANY FURTHUR ADDRESSES ARE ARGUMENT ADDRESSES           *
*              PARM LIST IS TERMINATED BY X'80' IN TOP BYTE OF ENTRY  *
*                                                                     *
*                                                                     *
*        CONTROL STRING FORMAT:                                       *
*                                                                     *
*              0(CNTL) IS HALFWORD LENGTH OF FOLLOWING STRING         *
*              2(CNTL) IS BEGINNING OF CONTROL CHARACTERS             *
*                                                                     *
*        CONTROL CHARACTER MEANING:                                   *
*                                                                     *
*              ANY CHARACTER BESIDES DELIM (%) IS PRINTED LITERALLY   *
*              TWO DELIMS IN A ROW PRINT AS ONE                       *
*              %C IMPLIES A SINGLE CHARACTER TO BE PRINTED            *
*              %S IMPLIES A CHAR STRING  TO BE PRINTED LITERALLY
*              %F IMPLIES FULLWORD ARGUMENT TO BE PRINTED AS DECIMAL  *
*              %H IMPLIES HALFWORD ARGUMENT TO BE PRINTED AS DECIMAL  *
*              %X IMPLIES FULLWORD ARGUMENT TO BE PRINTED AS HEX      *
*              %T IMPLIES FULLWORD ARGUMENT TO BE PRINTED AS TIME     *
*              %L CAUSES BUFFER TO BE PRINTED AND SKIP TO NEW LINE    *
*              %P CAUSES BUFFER TO BE PRINTED AND SKIP TO NEW PAGE    *
*                                                                     *
*                                                                     *
*        CHARACTER STRING FORMAT                                      *
*                                                                     *
*              0(CS) IS HALFWORD LENGTH OF FOLLOWING STRING           *
*              2(CS) IS 0 OR MORE CHARACTERS TO BE PRINTED            *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
*                                                                     *
*        RETURN CONVENTIONS:                                          *
*                                                                     *
*              R0 - R14 UNCHANGED                                     *
*              R15 HAS RETURN CODE                                    *
*                                                                     *
*                                                                     *
*        RETURN CODES:                                                *
*                                                                     *
*               0 - NORMAL TERMINATION                                *
*               4 - NORMAL TERMINATION,  EXTRA ARGUMENTS IN PARM LIST *
*               8 - ABNORMAL TERMINATION,  BAD CONTROL STRING         *
*              12 - ABNORMAL TERMINATION,  NOT ENOUGH ARGUMENTS       *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
PRINTF   CSECT
         EXTRN PUTCHAR
         ENTER DSA=Y
         LR    R2,R1              R2 -> -> CONTROL STRING
         ST    R0,FILEOPTS        SAVE FILE OPTION WORD
         L     R3,0(,R2)          R3 -> CONTROL STRING
         LA    R2,4(,R2)          POINT TO NEXT PARAMETER
         LTR   R3,R3              IS THERE ANOTHER PARM ?
         IF    M
         SR    R2,R2              NO,  ZERO POINTER
         ENDIF
         LH    R4,0(,R3)          R4 HAS LENGTH OF CONTROL STRING
         LA    R3,2(,R3)          R3 -> BEGINNING OF CONTROL CHARACTERS
NEXTCHAR LTR   R4,R4              ANYTHING MORE IN CONTROL STRING ?
         BNP   CHKAPARM           NO,  START TO EXIT
         CLI   0(R3),DELIM        A DELIMITER ?
         BE    CONTROL            YES, BRANCH
CPUTCHAR L     R0,FILEOPTS        LOAD FILE OPTION WORD
         SR    R1,R1              ZERO FOR IC
         IC    R1,0(,R3)          GET CHAR TO PRINT
         SCALL PUTCHAR
ADVNCHAR LA    R3,1(,R3)          BUMP TO NEXT CHAR
         S     R4,=F'1'           DECREMENT LENGTH
         B     NEXTCHAR           AND LOOP
CHKAPARM LTR   R2,R2              ANY PARMS NOT USED ?
         BZ    NORMEXIT           NO,  TERMINATE NORMALLY
         EXIT  RC=ERRC1           TOO MANY ARGUMENTS
         EJECT
CONTROL  LA    R3,1(,R3)          BUMP PAST DELIMITER
         S     R4,=F'1'           DECREMENT LENGTH
         BNP   BADF               NO MORE CHARS,  BAD FORMAT
         CLI   0(R3),DELIM        ANOTHER DELIMITER ?
         BE    CPUTCHAR           YES, PRINT THIS ONE
         CLI   0(R3),NEWLF        A NEWLINE CHARACTER ?
         BNE   CHKNEWP            NO, CHECK FOR TOP OF PAGE
         LA    R5,=AL1(NEWLINE)   R5 -> NEWLINE CHARACTER
         LA    R6,1               LENGTH TO PRINT
         B     NEXTCSTR           GO PRINT CHAR
CHKNEWP  CLI   0(R3),NEWPF        A TOP-OF-PAGE CHARACTER ?
         BNE   CHKNPARM           NO, SKIP
         LA    R5,=AL1(NEWPAGE)   R5 -> NEWPAGE CHARACTER
         LA    R6,1               LENGTH TO PRINT
         B     NEXTCSTR           GO PRINT CHAR
CHKNPARM LTR   R2,R2              ANY MORE PARMS ?
         BNZ   GETNPARM           YES, GO GET IT
         EXIT  RC=ERRC3           NOT ENOUGH PARAMETERS
GETNPARM L     R5,0(,R2)          R5 -> PARAMETER
         LA    R2,4(,R2)          POINT TO NEXT PARM
         LTR   R5,R5              ANY MORE PARMS ?
         IF    M
         SR    R2,R2              NO,  ZERO POINTER
         ENDIF
         EJECT
         SR    R7,R7              ASSUME ZERO IS MINIMUM LENGTH
         CLI   0(R3),X'F0'        NUMERIC ?
         BL    CHKCHAR            NO,  NO LENGTH FACTOR GIVEN
         LR    R6,R3              SAVE ADDRESS OF BEGINNING OF NUMBER
NXTLENUM LA    R3,1(,R3)          POINT TO NEXT BYTE OF CONTROL STRING
         S     R4,=F'1'           DECREMENT REMAINING LENGTH
         BNP   BADF               IF NO MORE, INVALID FORMAT OF CONTROL
         CLI   0(R3),X'F0'        STILL A NUMBER ?
         BNL   NXTLENUM           YES, LOOP
         LR    R8,R3              ISOLATE END ADDRESS
         SR    R8,R6              LENGTH OF NUMBER
         C     R8,=F'8'           OVER MAX LENGTH ?
         BH    BADF               YES, ERROR
         S     R8,=F'1'           MINUS ONE FOR EXECUTE INST
         EX    R8,PACKLEN         PACK NUMBER INTO DOUB
         CVB   R7,DOUB            MINIMUM LENGTH OF INPUT TO BUFFER
CHKCHAR  CLI   0(R3),CHARF        A SINGLE CHARACTER ?
         BNE   CHKSTRNG           NO,  TRY STRING
         LA    R6,1               LENGTH OF ARGUEMENT
         B     CHKPAD             GO PRINT IT
CHKSTRNG CLI   0(R3),STRINGF      A STRING ?
         BNE   CHKHALF            NO, TRY HALFWORD DECIMAL
         LH    R6,0(,R5)          LENGTH OF CHAR STRING TO PRINT
         LA    R5,2(,R5)          R5 -> FIRST CHAR TO PRINT
CHKPAD   SR    R7,R6              MINIMUM LENGTH-ACTUAL LENGTH
         BNP   NEXTCSTR           NO PADDING TO BE DONE
NEXTPAD  L     R0,FILEOPTS        GET FILE OPTION WORD
         LA    R1,X'40'           PUT A SPACE IN R1 TO PRINT
         SCALL PUTCHAR            PRINT IT
         S     R7,=F'1'           DECREMENT PAD LENGTH
         BP    NEXTPAD            LOOP IF MORE PADDING TO BE DONE
NEXTCSTR LTR   R6,R6              ANY MORE TO PRINT ?
         BNP   ADVNCHAR           NO, SKIP OUT
         L     R0,FILEOPTS        GET FILE OPTION WORD
         SR    R1,R1
         IC    R1,0(,R5)          GET CHAR TO PRINT
         SCALL PUTCHAR            PRINT IT
         LA    R5,1(,R5)          BUMP POINTER
         S     R6,=F'1'           DECREMENT LENGTH
         B     NEXTCSTR           AND LOOP
         EJECT
CHKHALF  CLI   0(R3),DECHALFF     A HALFWORD FORMAT ?
         BNE   CHKFULL            NO,  TRY FULLWORD DECIMAL
         LH    R5,0(,R5)          GET HALFWORD TO FORMAT
         B     DECCONV
CHKFULL  CLI   0(R3),DECFULLF     A FULLWORD FORMAT ?
         BNE   CHKTIME            NO,  TRY TIME
         L     R5,0(,R5)          NUMBER TO PRINT AS DECIMAL
DECCONV  TODEC R5,RESULT,10
         LA    R6,10              MAX LENGTH OF NUMBER
         B     STRIPBL            GO STRIP BLANKS OFF
CHKTIME  CLI   0(R3),TIMEF        TIME FORMAT ?
         BNE   CHKHEX             NO, TRY HEXADECIMAL
         L     R5,0(,R5)          GET NUMBER TO PRINT AS TIME
         TOTIM R5,RESULT,13
         LA    R6,13              MAX LENGTH IS 13 BYTES
         B     STRIPBL            GO STRIP BLANKS OFF
CHKHEX   CLI   0(R3),HEXF         HEXADECIMAL FORMAT ?
         BNE   BADF               NO, MUST BE A ERROR
         L     R5,0(,R5)          NUMBER TO PRINT IN HEX
         TOHEX R5,RESULT,8
         LA    R6,8               LENGTH IS EXACTLY EIGHT
STRIPBL  LA    R5,RESULT          ADDRESS OF TOKEN TO STRIP
CHKSPACE CLI   0(R5),X'40'        A LEADING SPACE ?
         BNE   CHKPAD             NO, LEAVE WITH ADDRESS AND LENGTH
         LA    R5,1(,R5)          BUMP POINTER
         S     R6,=F'1'           DECREMENT LENGTH
         B     CHKSPACE           LOOP UNTIL NO SPACE
         EJECT
BADF     EXIT  RC=ERRC2           INVALID CONTROL STRING
NORMEXIT EXIT  RC=0
         SPACE 5
PACKLEN  PACK  DOUB,0(1,R6)
         EJECT
HEXTABLE DC    C'0123456789ABCDEF'
         LTORG
         DSA
DOUB     DS    D
FILEOPTS DS    F
HEXWORK  DS    CL9
RESULT   DS    CL13
TIMWORK  DS    CL13
         ENDDSA
DELIM    EQU   C'%'
CHARF    EQU   C'C'
STRINGF  EQU   C'S'
DECHALFF EQU   C'H'
DECFULLF EQU   C'F'
TIMEF    EQU   C'T'
HEXF     EQU   C'X'
NEWLF    EQU   C'L'
NEWPF    EQU   C'P'
NEWLINE  EQU   X'0D'              CR
NEWPAGE  EQU   X'0C'              FF
ERRC1    EQU   4
ERRC2    EQU   8
ERRC3    EQU   12
         REGS
         END
