         TITLE '---- UNGETCHR:  ROUTINE TO PUT A CHARACTER BACK INTO A *
               BUFFER ----'
***********************************************************************
*                                                                     *
*                                                                     *
*        UNGETCHR:  ROUTINE TO PUT A CHARACTER BACK INTO A BUFFER     *
*                                                                     *
*        AUTHOR:                  E JENSEN                            *
*                                                                     *
*        DATE WRITTEN:            JUNE 11, 1977                       *
*                                                                     *
*        LAST UPDATE:             JUNE 12, 1977       ECJ             *
*                                                                     *
*                                                                     *
*        CALLING CONVENTIONS:                                         *
*              R0 HAS FILE OPTION WORD                                *
*              R1 HAS CHARACTER TO UN-GET                             *
*              R13 -> STACK AREA                                      *
*              R14 HAS RETURN ADDRESS                                 *
*                                                                     *
*        RETURN CONVENTIONS:                                          *
*              R15 HAS RETURN CODE   (ALWAYS ZERO)                    *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
UNGETCHR CSECT
         ENTRY UNGETOKN
         ENTRY UNGETSTR
         EXTRN CHKDELIM
         EXTRN FILETBL
         EXTRN GETCHAR
         ENTER
         LR    R9,R1              R9 HAS CHAR TO UN-GET
         LA    R2,X'FF'           LOW ORDER BYTE MASK
         NR    R2,R0              KEEP ONLY FILE SELECT BITS
         L     R3,=A(FILETBL)     R3 -> FILE DESCRIPTION TABLE
         USING FILEDSCT,R3
         SR    R4,R4              COMPARISON REG
NEXTFILE C     R4,0(,R3)          AT END OF FILE TABLE ?
         BE    ENDUGC             YES,  QUIT
         LTR   R2,R2              ANY MORE FILES TO UN-GET TO ?
         BE    ENDUGC             NO,  BRANCH
         LTR   R2,R2              DONE WITH SELECTED FILES ?
         BZ    ENDUGC             YES, BRANCH
         LA    R5,1               A 1
         NR    R5,R2              IS LOW ORDER BIT SET ?
         BZ    ADVNFILE           NO,  NOT THE FILE WE WANT
         TM    FILEFLAG,FILEALOC+FILEUSE+FILEIPT
         BNO   ADVNFILE           SKIP FILE IF ABOVE TESTS FAIL
         L     R5,FILE@IB         R5 -> INPUT STRING
         LH    R6,0(,R5)          R6 HAS CURRENT LENGTH
         CH    R6,FILEIBML        IS BUFFER FULL ?
         BNL   ADVNFILE           YES,  SKIP UN-GET FOR THIS FILE
         LA    R7,7(,R6)          PLUS 7 (FOR ROUNDING UP)
         SRL   R7,3               R7 HAS # OF DOUBLE WORDS NEEDED
         LTR   R7,R7              ANY NEEDED ?
         BNP   SAVECHR            NO,  SKIP MOVING OF BUFFER
         GETC  (R7)               REQUEST STORAGE ON STACK
         LR    R8,R6              CURRENT LENGTH
         S     R8,=F'1'           MINUS 1 FOR EXECUTE INSTRUCTION
         EX    R8,MOVEBOUT        MOVE BUFFER ONTO STACK
         EX    R8,MOVEBIN         MOVE BUFFER BACK IN (ONE BYTE OVER)
         FREEC (R7)               GIVE BACK STORAGE
SAVECHR  STC   R9,2(,R5)          ADD CHAR TO BUFFER
         A     R6,=F'1'           NEW LENGTH OF BUFFER
         STH   R6,0(,R5)          SAVE NEW LENGTH
ADVNFILE LA    R3,FILETLEN(,R3)   POINT TO NEXT FILE TABLE
         SRL   R2,1               SLIDE NEXT BIT OVER
         B     NEXTFILE           AND LOOP
ENDUGC   EXIT  RC=0               LEAVE
MOVEBOUT MVC   0(1,R1),2(R5)      MOVE BUFFER ONTO STACK
MOVEBIN  MVC   3(1,R5),0(R1)      MOVE BUFFER BACK ONE BYTE OVER
         TITLE '---- UNGETSTR:  ROUTINE TO PUT STRING BACK INTO BUFFER *
               ----'
***********************************************************************
*                                                                     *
*                                                                     *
*        UNGETSTR:  ROUTINE TO PUT A STRING BACK INTO A BUFFER        *
*                                                                     *
*        AUTHOR:                  E JENSEN                            *
*                                                                     *
*        DATE WRITTEN:            JUNE 11, 1977                       *
*                                                                     *
*        LAST UPDATE:             JUNE 11, 1977       ECJ             *
*                                                                     *
*                                                                     *
*        CALLING CONVENTIONS:                                         *
*              R0 HAS FILE OPTION WORD                                *
*              R1 -> STRING TO UN-GET                                 *
*              R13 -> STACK AREA                                      *
*              R14 HAS RETURN ADDRESS                                 *
*                                                                     *
*        RETURN CONVENTIONS:                                          *
*              R15 HAS RETURN CODE   (ALWAYS ZERO)                    *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
UNGETSTR ENTER
         LR    R2,R0              FILE OPTION WORD
         LR    R3,R1              R3 -> STRING TO UN-GET
         LH    R4,0(,R3)          R4 HAS LENGTH OF STRING
         SR    R1,R1              CLEAR FOR IC
         LTR   R4,R4              ANY TO DO ?
         BNP   ENDUGS             NO,  LEAVE
NEXTUGS  LR    R0,R2              FOW
         IC    R1,1(R4,R3)        GET CHAR TO UN-GET
         SCALL UNGETCHR
         S     R4,=F'1'           DECREMENT LENGTH
         BP    NEXTUGS            AND LOOP FOR LENGTH OF STRING
ENDUGS   EXIT  RC=0
         TITLE '---- UNGETOKN:  ROUTINE TO PUT A TOKEN BACK INTO A BUFF*
               ER ----'
***********************************************************************
*                                                                     *
*                                                                     *
*        UNGETOKN:  ROUTINE TO PUT A TOKEN BACK INTO A BUFFER         *
*                                                                     *
*        AUTHOR:                  E JENSEN                            *
*                                                                     *
*        DATE WRITTEN:            JUNE 11, 1977                       *
*                                                                     *
*        LAST UPDATE:             JUNE 19, 1977       ECJ             *
*                                                                     *
*                                                                     *
*        CALLING CONVENTIONS:                                         *
*              R0 HAS FILE OPTION WORD                                *
*              R1 -> TOKEN TO UN-GET                                  *
*              R13 -> STACK AREA                                      *
*              R14 HAS RETURN ADDRESS                                 *
*                                                                     *
*        RETURN CONVENTIONS:                                          *
*              R15 HAS RETURN CODE   (ALWAYS ZERO)                    *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
UNGETOKN ENTER
         LA    R2,X'FF'           LOW BYTE MASK
         NR    R2,R0              KEEP FILE SELECT BITS
         LR    R3,R1              R3 -> TOKEN TO UN-GET
         LH    R4,0(,R3)          R4 HAS LENGTH OF TOKEN
         LTR   R4,R4              ANYTHING TO DO ?
         BNP   ENDUGT             NO,  BRANCH
         LR    R5,R2              COPY FILE SELECT BITS
NEXTTFIL LCR   R0,R5
         NR    R0,R5              R0 HAS LOW ORDER BIT SET
         BZ    CALLUGS            IF NO MORE FILES, BRANCH
         XR    R5,R0              ZERO BIT IN R5
         SCALL GETCHAR            GET NEXT CHARACTER FOR THAT FILE
         LTR   R15,R15            END OF BUFFER ?
         BNZ   NEXTTFIL           YES,  THAT DELIMITER IS GOOD ENOUGH
         SCALL UNGETCHR           AND PUT IT BACK
         SCALL CHKDELIM           IS NEXT CHAR A DELIMITER
         LTR   R15,R15
         BZ    NEXTTFIL           YES,  SKIP REST OF LOOP
         IC    R1,1(R4,R3)        GET CHAR ON END OF TOKEN
         SCALL CHKDELIM           IS IT A DELIMITER ?
         LTR   R15,R15
         BZ    NEXTTFIL           YES,  SKIP REST OF LOOP
         LA    R1,X'40'           PUT A SPACE IN R1
         SCALL UNGETCHR           UN-GET A SPACE TO SEPARATE TOKENS
         B     NEXTTFIL           AND LOOP
CALLUGS  LR    R0,R2              COMPLETE FILE SELECT BITS
         LR    R1,R3              R1 -> TOKEN TO UN-GET
         SCALL UNGETSTR           CALL UN-GET STRING ROUTINE
ENDUGT   EXIT  RC=0
         EJECT
         LTORG
         FILEDSCT
         REGS
         END
