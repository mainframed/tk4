         MACRO
&LABEL   MPIEC &P,&M,&D
         LCLA  &I
&LABEL   LH    R5,W&P.O-WK(R11)
         CH    R5,W&P.O+2-WK(R11)
         IF    L
         AIF   ('&M' NE 'MOVE').NOTMOVE
         LBR   R14,R6
         OBR   R14,R8             EMPTY OR ELIGIBLE
         COMB  R8                 INELIGIBLE
.NOTMOVE SR    R4,R4
         REPEAT
         IC    R4,WSQUARES-WK(R5,R11)
         LA    R5,1(R5)
         LR    R3,R4
         SLL   R3,8
&I       SETA  N'&D
.LOOP    AIF   ('&I' LE '0').END
         AIF   ('&M' EQ 'MOVE').MOVE
         AIF   ('&M' EQ 'MOV1').MOV1
         AIF   ('&M' EQ 'MOVP').MOVP
         MNOTE 8,'INVALID MOVE MACRO'
         MEXIT
.MOVE    MOVE  &D(&I-1),&D(&I)
         AGO   .CONT
.MOV1    MOV1  &D(&I-1),&D(&I)
         AGO   .CONT
.MOVP    MOVP  &D(&I-1),&D(&I)
.CONT    ANOP
&I       SETA  &I-2
         AGO   .LOOP
.END     CH    R5,W&P.O+2-WK(R11)
         UNTIL NL
         ENDIF
         MEND
         SPACE 5
         MACRO
&LABEL   MOVE  &DIR,&COUNT
&LABEL   LR    R2,R4
         BSETB R0,0(R2)
         REPEAT
         REPEAT
         SHFTB R0,&DIR
         NBR   R0,R14             EMPTY OR ELIGIBLE
         TSTB  R0
         BREAK Z,2
         A     R2,=F'&COUNT'
         NBR   R0,R8              INELIGIBLE
         TSTB  R0
         UNTIL Z
         LR    R1,R2
         OR    R1,R3
         STH   R1,0(R10)
         LA    R10,2(R10)
         BSETB R0,0(R2)
         NBR   R0,R6              EMPTY
         TSTB  R0
         UNTIL Z
         MEND
         SPACE 5
         MACRO
&LABEL   MOV1  &DIR,&COUNT
&LABEL   BSETB R0,0(R4)
         SHFTB R0,&DIR
         NBR   R0,R8
         TSTB  R0
         IF    NZ
         LR    R2,R4
         A     R2,=F'&COUNT'
         OR    R2,R3
         STH   R2,0(R10)
         LA    R10,2(R10)
         ENDIF
         MEND
         SPACE 5
         MACRO
&LABEL   MOVP  &DIR,&COUNT
&LABEL   LR    R2,R4
         A     R2,=F'&COUNT'
         OR    R2,R3
         BSETB R0,0(R4)
         SHFTB R0,&DIR
         AIF   ('&DIR' NE 'U' AND '&DIR' NE 'D').CAP
         NBR   R0,R6              EMPTY
         TSTB  R0
         IF    NZ
         LBR   R14,R0
         NBR   R14,R8             ELIGIBLE
         TSTB  R14
         IF    NZ
         CQUEEN &COUNT
         ENDIF
         AIF   ('&COUNT'(1,1) NE '-').BLP
         NB    R0,RANK3
         AGO   .CONT2
.BLP     NB    R0,RANK6
.CONT2   SHFTB R0,&DIR
         NBR   R0,R8              ELIGIBLE
         NBR   R0,R6              EMPTY
         TSTB  R0
         IF    NZ
         A     R2,=F'&COUNT'
         O     R2,=X'0000C000'
         STH   R2,0(R10)
         LA    R10,2(R10)
         ENDIF
         ENDIF
         MEXIT
.CAP     NBR   R0,R8              ELIGIBLE
         TSTB  R0
         IF    NZ
         LBR   R14,R0
         NBR   R14,R6
         TSTB  R14
         IF    Z
         CQUEEN &COUNT
         ENDIF
         ENDIF
         MEND
         SPACE 5
         MACRO
&LABEL   CQUEEN &COUNT
&LABEL   LR    R15,R2
         AIF   ('&COUNT'(1,1) NE '-').BLP
         N     R0,=X'FF000000'
         AGO   .CONT
.BLP     N     R1,=X'000000FF'
.CONT    IF    NZ
         O     R15,=X'00004000'
         STH   R15,0(R10)
         LA    R15,64(R15)
         STH   R15,2(R10)
         LA    R15,64(R15)
         STH   R15,4(R10)
         LA    R15,64(R15)
         LA    R10,6(R10)
         ENDIF
         STH   R15,0(R10)
         LA    R10,2(R10)
         MEND
         SPACE 5
         MACRO
&LABEL   ENP   &DIR,&COUNT
&LABEL   LBR   R0,R2
         SHFTB R0,&DIR
         NBR   R0,R14             PAWNS
         TSTB  R0
         IF    NZ
         LA    R1,&COUNT+64(R4)
         SLL   R1,8
         OR    R1,R4
         STH   R1,0(R10)
         LA    R10,2(R10)
         ENDIF
         MEND
         EJECT
PIECGEN  CSECT
         ENTRY MKING
         ENTRY MQUEEN
         ENTRY MROOK
         ENTRY MBISHOP
         ENTRY MKNIGHT
         ENTRY MWPAWN
         ENTRY MBPAWN
*
*  PIECE MOVE GENERATORS
*  R11 -> ACTIVE DB
*  R10 -> MLIST
*  R8,R9 = ELIGIBLE SQUARES
*  R6,R7 = EMPTY SQUARES
*  ELIGIBLE SQUARES MUST NOT INCLUDE ACTIVE PIECES, OR
*  PASSIVE THREATS IF KING MOVES ARE BEING GENERATED
*  RC=0, RTN = NEXT AVAILABLE MOVE SLOT
*
         EJECT
MKING    ENTER
         MPIEC K,MOV1,(U,-8,UR,-7,R,1,DR,9,D,8,DL,7,L,-1,UL,-9)
         EXIT  RTN=(R10)
         EJECT
MQUEEN   ENTER
         MPIEC Q,MOVE,(U,-8,UR,-7,R,1,DR,9,D,8,DL,7,L,-1,UL,-9)
         EXIT  RTN=(R10)
         EJECT
MROOK    ENTER
         MPIEC R,MOVE,(U,-8,R,1,D,8,L,-1)
         EXIT  RTN=(R10)
         EJECT
MBISHOP  ENTER
         MPIEC B,MOVE,(UR,-7,DR,9,DL,7,UL,-9)
         EXIT  RTN=(R10)
         EJECT
MKNIGHT  ENTER
         MPIEC N,MOV1,(UUR,-15,URR,-6,DRR,10,DDR,17,                   *
               DDL,15,DLL,6,ULL,-10,UUL,-17)
         EXIT  RTN=(R10)
         EJECT
MWPAWN   ENTER
         MPIEC P,MOVP,(UL,-9,U,-8,UR,-7)
         CLI   ENPASS-WK(R11),X'00'
         IF    NE
         IC    R4,ENPASS-WK(R11)
         BSETB R2,0(R4)
         NBR   R2,R8              ELIGIBLE SQUARES
         TSTB  R2
         IF    NZ
         LB    R14,WP-WK(R11)
         ENP   DR,9
         ENP   DL,7
         ENDIF
         ENDIF
         EXIT  RTN=(R10)
         EJECT
MBPAWN   ENTER
         MPIEC P,MOVP,(DL,7,D,8,DR,9)
         CLI   ENPASS-BK(R11),X'00'
         IF    NZ
         IC    R4,ENPASS-BK(R11)
         BSETB R2,0(R4)
         NBR   R2,R8              ELIGIBLE SQUARES
         TSTB  R2
         IF    NZ
         LB    R14,WP-WK(R11)
         ENP   UL,-9
         ENP   UR,-7
         ENDIF
         ENDIF
         EXIT  RTN=(R10)
         CNOP  0,8
RANK3    DC    X'0000000000FF0000'
RANK6    DC    X'0000FF0000000000'
POSDB    DSECT
         POSDB
         REGS
         END
