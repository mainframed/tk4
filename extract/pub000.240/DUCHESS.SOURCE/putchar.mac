         TITLE '    ---- PUTCHAR: ROUTINE TO ADD A BYTE TO A BUFFER ---*
               -'
***********************************************************************
*                                                                     *
*                                                                     *
*        PUTCHAR:  ROUTINE TO ADD A CHARACTER TO A BUFFER             *
*                                                                     *
*        AUTHOR:                  E JENSEN                            *
*                                                                     *
*        DATE WRITTEN:            JUNE 10, 1977                       *
*                                                                     *
*        LAST UPDATE:             JUNE 11, 1977                       *
*                                                                     *
*                                                                     *
*        CALLING CONVENTIONS:                                         *
*              R0 HAS FILE OPTION WORD                                *
*              R1 HAS CHARACTER TO PUT INTO BUFFER (LOW ORDER BYTE)   *
*              R13 -> STACK AREA                                      *
*              R14 HAS RETURN ADDRESS                                 *
*                                                                     *
*        RETURN CONVENTIONS:                                          *
*              R15 HAS RETURN CODE  (ALWAYS ZERO)                     *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
PUTCHAR  CSECT
         EXTRN FILETBL
         ENTER
         LA    R4,X'FF'           SET UP FILE SELECT BIT MASK
         NR    R1,R4              ZERO TOP 3 BYTES
         NR    R4,R0              R4 HAS BIT MAP OF FILES TO PRINT TO
         LR    R5,R1              R5 HAS CHARACTER TO PRINT
         L     R2,=A(FILETBL)     R2 -> FILE DESCRIPTION TABLE
         SR    R3,R3              END OF TABLE COMPARISON REG
         USING FILEDSCT,R2
NEXTFILE C     R3,0(,R2)          END OF FILE TABLE ?
         BE    ENDPUTC            YES, DONE
         LTR   R4,R4              ANY MORE FILES TO PRINT TO ?
         BZ    ENDPUTC            NO,  DONE
         LA    R0,1               A 1
         NR    R0,R4              IS THE LOW ORDER BIT SET ?
         BZ    ADVNFILE           NO,  SKIP THIS TABLE ENTRY
         TM    FILEFLAG,FILEALOC+FILEUSE+FILEOPT A DD & OUTPUT & USE ?
         BNO   ADVNFILE           NO,  SKIP THIS FILE
         L     R6,FILE@OB         R6 -> OUTPUT BUFFER STRING
         LH    R7,0(,R6)          CURRENT LENGTH OF BUFFER
         C     R5,=A(NEWLINE)     A NEW LINE CHAR ?
         BE    FLUSHB             YES, FLUSH BUFFER
         C     R5,=A(NEWPAGE)     A NEW PAGE CHAR ?
         BE    FLUSHB             YES, BRANCH
         CH    R7,FILEOBML        IS BUFFER FULL ?
         BNL   ADVNFILE           YES, SKIP THIS FILE
         LA    R8,2(R7,R6)        R8 -> NEXT FREE SPACE IN BUFFER
         STC   R5,0(,R8)          ADD TO BUFFER
         A     R7,=F'1'           INCREMENT LENGTH
         STH   R7,0(,R6)          SAVE NEW LENGTH
         B     ADVNFILE           AND CONTINUE
FLUSHB   LR    R1,R2              R1 -> FILE DESC. TABLE
         L     R15,FILE@WR        ADDRESS OF OUTPUT ROUTINE FOR FILE
         BALR  R14,R15            CALL IT
         MVI   FILECCHR,X'40'     ASSUME SINGLE SPACING
         C     R5,=A(NEWPAGE)     WAS IT A NEW PAGE INDICATOR ?
         BNE   *+8                NO,  SKIP
         MVI   FILECCHR,C'1'      TOP OF FORM INDICATOR
ADVNFILE LA    R2,FILETLEN(,R2)   BUMP FILE POINTER
         SRL   R4,1               SLIDE OVER TO NEXT FILE
         B     NEXTFILE           AND LOOP
ENDPUTC  EXIT  RC=0
         EJECT
         LTORG
NEWLINE  EQU   X'0D'
NEWPAGE  EQU   X'0C'
         FILEDSCT
         REGS
         END
