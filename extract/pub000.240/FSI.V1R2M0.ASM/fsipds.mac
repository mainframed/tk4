FSIPDS   TITLE 'READ A PDS MEMBER AND DEBLOCK'
***********************************************************************
*       THIS JOB WILL READ AND DEBLOCK A PDS MEMBER                   *
*                                                                     *
*       UPON ENTRY REGISTER ONE POINTS TO A PARAMETER LIST            *
*                                                                     *
*        WORD ONE  POINTER TO MEMBER NAME TO BE PROCESSED             *
*        WORD TWO  POINTER TO A FULL WORD IN WHICH THE RECORD ADDRESS *
*                  WILL BE PLACED                                     *
*        WORD THREE POINTER TO A FULL WORD IN WHICH THE LRECL WILL BE *
*                  PLACED                                             *
*        WORD FOUR POINTER TO DD NAME                                 *
*                                                                     *
*        IF THE MEMBER NAME IS CHANGED, ON THE NEXT CALL PROCESSING   *
*        WILL BEGIN FOR A NEW MEMBER                                  *
*                                                                     *
*        IF THE MEMBER NAME IS C'********' THE CURRENT PROCESSING WILL*
*        BE TERMINATED AND THE DCB CLOSED. THIS SHOULD BE DONE AT THE *
*        END OF ALL PROCESSING                                        *
*                                                                     *
*                                                                     *
*             UPON RETURN, REGISTER 15 CONTAINS A RETURN CODE         *
*                 AND REGISTER 0 CONTAINS A REASON CODE.              *
*                                                                     *
*              R15    R0                                              *
*              ---    --                                              *
*               0    N/A     REQUEST COMPLETED SUCESSFULLY            *
*               4    N/A     END OF FILE  CURRENT MEMBER              *
*               8     4      DD STATEMENT NOT FOUND                   *
*               8     8      MEMBER NOT FOUND                         *
*                                                                     *
***********************************************************************
         TITLE '*** P R O G R A M    E N T R Y'
***********************************************************************
*                  P R O G R A M   E N T R Y                          *
***********************************************************************
         SPACE 3
FSIPDS   CSECT
         SAVE  (14,12),,FSIPDS_V2_&SYSDATE_&SYSTIME_TCS3_12/82
         LR    R12,R15            SET BASE
         USING FSIPDS,R12
         LA    R15,SAVEA          POINT TO SAVEA
         ST    R13,4(,R15)        CHAIN
         ST    R15,8(,R13)             SAVE
         LR    R13,R15                     AREAS
         LR    R11,R1             SAVE PARM PTR
         TITLE '*** I N I T I A L   P R O C E S S I N G'
***********************************************************************
*                I N I T I A L   P R O C E S S I N G                  *
***********************************************************************
         SPACE 3
         L     R2,0(,R11)         FIND MEMBER NAME
         CLC   ASTRK,0(R2)        IS IT CLOSE REQUEST
         BE    CLOSE              YES - BRANCH
         L     R2,12(,R11)        FIND DD NAME
         CLC   DDNAME,0(R2)       CHECK FOR NEW DD NAME
         BNE   NEWDD              YES - GO PROCESS
         TM    DCB+48,X'10'       IS DCB OPEN
         BO    INIT010            YES - CONTINUE
         B     NEWDD              ELSE - PROCESS NEW DD
INIT010  DS    0H
         TITLE '*** P R O C E S S   R E A D'
***********************************************************************
*                     P R O C E S S   R E A D                         *
***********************************************************************
         SPACE 3
READ     DS    0H
         L     R2,0(,R11)         POINT TO MEMBER
         CLC   MEMBER,0(R2)       IS IT NEW MEMBER
         BE    READ010            NO - CONTINUE
         MVC   MEMBER,0(R2)       SAVE MEM NAME
         XC    RECPTR,RECPTR      ZERO POINTER
         FIND  DCB,MEMBER,D
         LTR   R15,R15            WAS IT FOUND ?
         BZ    READ010            YES - CONTINUE
         LA    R15,8              RC = 8
         LA    R0,8               CODE = 8
         B     RETURN             AND EXIT
READ010  DS    0H
         L     R2,RECPTR          GET RECORD POINTER
         LTR   R2,R2              DO WE NEED A READ
         BNZ   READ020            NO-CONTINUE
         L     R7,BUF             POINT TO INPUT BUFFER
         READ  READDECB,SF,DCB,(R7)
         CHECK READDECB
         LH    R2,DCB+62          GET BLOCKSIZE
         L     R1,READDECB+16     POINT TO IOB
         SH    R2,14(,R1)         COMPUTE ACTUAL BLOCKSIZE
         ST    R2,BLKLEN          SAVE
         LR    R2,R7              POINT TO INPUT BUFFER
READ020  DS    0H
         LR    R8,R2              CURRENT RECORD ADDRESS IN R8
         LH    R9,DCB+82          GET LRECL
         LA    R2,0(R9,R2)        BUMP TO NEXT RECORD
         ST    R2,RECPTR          SAVE ADDRESS
         L     R2,BLKLEN          GET LEN REMAINING
         SR    R2,R9              ADJUST
         BP    READ030            BRANCH IN NOT END OF BLOCK
         XC    RECPTR,RECPTR      RESET BUFFER POINTER
READ030  DS    0H
         ST    R2,BLKLEN          SAVE BLK LEN
         L     R2,4(,R11)         RECORD RETURN
         ST    R8,0(R2)           RETURN RECORD
         L     R2,8(,R11)         BLOCK LEN
         ST    R9,0(,R2)          RETURN RECORD LEN
         LA    R15,0              SET RC
         LA    R0,0
         B     RETURN
         TITLE '*** P R O C E S S   C L O S E   R E Q U E S T'
***********************************************************************
*             P R O C E S S   C L O S E   R E Q U E S T               *
***********************************************************************
         SPACE 3
CLOSE    DS    0H
         TM    DCB+48,X'10'       IS DCB OPEN
         BNO   CLOSE010           NO - SKIP CLOSE
         LH    R2,DCB+62          POINT TO BLKSIZE
         L     R3,BUF             ADDRESS
         FREEMAIN R,LV=(R2),A=(R3)
         CLOSE (DCB)
CLOSE010 DS    0H
         MVC   DDNAME,=8XL1'FF'   RESET DDNAME
         MVC   MEMBER,=8XL1'FF'   RESET MEMBER NAME
         LA    R15,0              SET RC
         LA    R0,0
         B     RETURN
         TITLE '*** P R O C E S S   E N D   O F   F I L E'
***********************************************************************
*             P R O C E S S   E N D   O F   F I L E                   *
***********************************************************************
         SPACE 3
EOF      DS    0H
         LA    R15,4              SET RC
         LA    R0,0
         MVC   MEMBER,=8XL1'FF'   RESET MEMBER NAME
         B     RETURN
         TITLE '*** P R O C E S S   N E W   D D N A M E'
***********************************************************************
*             P R O C E S S   N E  W   D D N A M E                    *
***********************************************************************
         SPACE 3
NEWDD    DS    0H
         TM    DCB+48,X'10'       IS DCB OPEN
         BNO   NEWDD010           NO - SKIP CLOSE
         LH    R2,DCB+62          GET BLKSIZE
         L     R3,BUF             POINT TO BUFFER
         FREEMAIN R,LV=(R2),A=(R3)
         CLOSE (DCB)
NEWDD010 DS    0H
         L     R2,12(,R11)        POINT TO DDNAME
         EXTRACT FWORD,FIELDS=TIOT
         L     R1,FWORD           POINT TO TIOT
         LA    R1,24(,R1)         POINT TO DD SECTION
         SLR   R0,R0              CLEAR WORK REG
NEWDD020 DS    0H
         IC    R0,0(,R1)          GET ENTRY LENGTH
         LR    R3,R1              SAVE PTR TO ENTRY IN R3
         CLC   0(8,R2),4(R1)      IS THE THE ENTRY
         BXLE  R1,R0,NEWDD030     BRANCH IF NO MORE
         BNE   NEWDD020           NO - TRY NEXT
         B     NEWDD040           CONTINUE ON
NEWDD030 DS    0H
         LA    R15,8              RC
         LA    R0,4               REASON CODE
         B     RETURN             AND EXIT
NEWDD040 DS    0H
         MVC   DCB+40(8),0(R2)    PLUG IN NEW DDNAME
         MVC   DDNAME,0(R2)       SAVE DDNAME
         OPEN  (DCB,INPUT)
         LH    R2,DCB+62          GET BLKSIZE
         GETMAIN R,LV=(R2)        GET A BUFFER
         ST    R1,BUF             SAVE ADDRESS
         MVC   MEMBER,FFS         RESET MEMBER NAME
         B     READ               GO PROCESS IT
         TITLE '*** R E T U R N    T O   C A L L E R'
***********************************************************************
*             R E T U R N   T O    C A L L E R                        *
***********************************************************************
         SPACE 3
RETURN   DS    0H
         L     R13,4(,R13)        UNCHAIN
         L     R14,12(,R13)
         LM    R1,R12,24(R13)
         BR    R14                RETURN
         TITLE '*** D A T A   A R E A S'
***********************************************************************
*                       D A T A    A R E A S                          *
***********************************************************************
         SPACE 3
BUF      DC    F'0'
BLKLEN   DC    F'0'
RECPTR   DC    F'0'
FWORD    DC    F'-1'
SAVEA    DS    18F
ASTRK    DC    C'********'
FFS      DC    X'FFFFFFFFFFFFFFFF'
         PRINT NOGEN
DCB      DCB   DSORG=PO,                                               X
               MACRF=R,                                                X
               DDNAME=XXXXXXXX,                                        X
               EODAD=EOF
         SPACE 2
MEMBER   DC    X'FFFFFFFFFFFFFFFF'
DDNAME   DC    X'FFFFFFFFFFFFFFFF'
         SPACE 5
         TITLE '***  R E G I S T E R   E Q U A T E S ***'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
