FSISCRN   CSECT ,
          SAVE  (14,12),,*
*
          LR    R12,R15
          USING FSISCRN,R12
*
*
         LA    R11,SAVEA
         ST    R13,4(,R11)
         ST    R11,8(,R13)
         LR    R13,R11
*
*
         LR    R11,R1
         USING SCR,R11
*
*
         XC    CSRPOS,CSRPOS
         XC    TRTAB,TRTAB
*
         L     R4,=A(TRTAB)
         L     R2,SCRATTRS        PONIT TO ATTR ARRAY
ATTR010  DS    0H
         CLC   =X'FFFF',0(R2)     END OF LIST
         BE    ATTR020            YES - DONE
*
         SLR   R1,R1              ZERO R1
         IC    R1,0(,R2)          GET ATTR CHAR
         LA    R1,0(R1,R4)        CALC OFFSET INTO TABLE
         MVC   0(1,R1),1(R2)      MOVE IN ENCODED ATTRIBUTES
         LA    R2,2(,R2)          POINT TO NEXT ENTRY
         B     ATTR010            LOOP BACK
*
*
ATTR020  DS    0H
         GETMAIN R,LV=4096
         ST      R1,BUFFER
         ST      R1,BUFFERP
*
         GETMAIN R,LV=4096
         ST      R1,INBUF
*
         L     R3,=A(SVARSIZE*200)
         GETMAIN R,LV=(R3)
         ST    R1,VARTAB
*
         LR    R0,R1            CLEAR THE SCREEN VAR TABLE
         LR    R1,R3
         SLR   R14,R14
         SLR   R15,R15
         MVCL  R0,R14
*
*
         L     R1,SCRPANEL        GET POINTER TO PANEL
         ST    R1,PANELP          SAVE
*
         LA    R1,1               PRIME ROW
         ST    R1,ROW
         LA    R1,0               PRIME COL
         ST    R1,COL
*
         LA    R4,=X'27F5C31140403C404040'
         LA    R5,10
*
PREFIX   DS    0H
         ICM   R1,B'0001',0(R4)
         BAL   R14,PUTCHAR
         LA    R4,1(,R4)
         BCT   R5,PREFIX
*
*
TXTLOOP  DS    0H
         BAL   R14,GETCHAR
         LTR   R15,R15
         BNZ   DONE
*
TXTLOP10 DS    0H
         CLI   CHARTYPE,0       FIELD DEFINITION CHAR?
         BE    TXTLOP20         NO - BRANCH
*
         TM    CHARTYPE,X'03'  OUTPUT FIELD ?
         BO    INPT            PROCESS INPUT/OUTPUT FIELD
         TM    CHARTYPE,X'01'  INPUT FIELD ?
         BO    INPT            PROCESS INPUT/OUTPUT FIELD
         B     TXT             PROCESS TEXT FIELD
*
*
TXTLOP20 DS    0H
         BAL   R14,PUTCHAR
         B     TXTLOOP
***********************************************************************
*
*
***********************************************************************
TXT      DS    0H
         IC    R1,=X'11'   START FIELD
         BAL   R14,PUTCHAR
*
         L     R0,ROW
         L     R1,COL
         BAL   R14,CNVTSBA
*
         LR    R5,R15
         LR    R1,R15
         SRL   R1,8
         BAL   R14,PUTCHAR
*
         LR    R1,R5
         BAL   R14,PUTCHAR
*
         IC    R1,=X'1D'       START FIELD
         BAL   R14,PUTCHAR
         IC    R1,CHARATTR     GET 3270 ATTRIB CHAR
         BAL   R14,PUTCHAR
*
         B     TXTLOOP
***********************************************************************
* INPUT / OUTPUT FIELD
***********************************************************************
INPT     DS    0H
         IC    R1,=X'11'   START FIELD
         BAL   R14,PUTCHAR
*
         L     R0,ROW
         L     R1,COL
         BAL   R14,CNVTSBA
*
         LR    R5,R15
         LR    R1,R15
         SRL   R1,8
         BAL   R14,PUTCHAR
*
         LR    R1,R5
         BAL   R14,PUTCHAR
*
         IC    R1,=X'1D'       START FIELD
         BAL   R14,PUTCHAR
         IC    R1,CHARATTR     GET 3270 ATTRIB CHAR
         BAL   R14,PUTCHAR
*
* START A SVAR TABLE ENTRY
*
         L     R10,VARTAB
         USING SVAR,R10
*
INPT002  DS    0H
         CLC   SVARBUFA,=XL2'0000'
         BE    INPT004              FOUND EMPTY SLOT
*
         LA    R10,SVARSIZE(,R10)      POINT TO NEXT ENTRY
         B     INPT002
*
*
INPT004  DS    0H
         L     R0,ROW
         L     R1,COL
         LA    R1,1(,R1)
         BAL   R14,CNVTSBA
*
         STCM  R15,B'0011',SVARBUFA
*
         TM    CHARTYPE,X'03'      OUTPUT FIELD?
         BO    INPT006             YES - DON'T SET DEFAULT CSR
         CLC   =X'0000',CSRPOS     SAVED CSR POSITION?
         BNE   INPT006             YES - BRANCH
*
         STCM  R15,B'0011',CSRPOS  SAVE
INPT006  DS    0H
*
* PROCESS VAR NAME
*
INPT010  DS    0H
         LA    R4,WORKNAME
         LA    R5,8              MAX VAR NAME LENGTH
         SLR   R3,R3             FIELD LENGTH
         MVC   WORKNAME,=CL8' '
INPT020  DS    0H
         BAL   R14,GETCHAR       GET NEXT CHAR
         LTR   R15,R15           CHECK FOR EOF
         BNZ   INPT040           YES - BRANCH
*
         CLI   CHARTYPE,X'00'
         BNE   INPT050
*
         CLI   CHAR,C' '      END OF NAME?
         BE    INPT030
*
         C     R5,=F'1'       NAME TOO LONG
         BL    INPT030        YES -  BRANCH
*
         MVC   0(1,R4),CHAR   COPY VAR NAME
         LA    R4,1(,R4)
         S     R5,=F'1'
*
         LA    R3,1(,R3)          INCREMENT FIELD LENGTH
         B     INPT020
*
*
INPT030  DS    0H
         LA    R3,1(,R3)       INCREMENT FIELD LENGTH
INPT035  DS    0H
         BAL   R14,GETCHAR
         LTR   R15,R15
         BNZ   INPT040
*
         CLI   CHARTYPE,X'00'
         BNE   INPT050
*
         LA    R3,1(,R3)       INCREMENT FIELD LENGTH
         B     INPT035
*
*
INPT040  DS    0H
         MVI   CHAR,X'FF'
*
INPT050  DS    0H
         MVC   SVARNAME,WORKNAME
         STH   R3,SVARLEN
*
*
         CLI   CHAR,X'FF'
         BE    DONE                BRANCH IF EOF
*
         B     TXTLOP10            GO START NEXT FIELD
*
         DROP  R10
*
*
*--------------------------------------------------------------------
*
*
*
*--------------------------------------------------------------------
*
DONE     DS    0H
         L     R10,VARTAB
         USING SVAR,R10
         L     R9,SCRZVARS
ZVARS010 DS    0H
         CLC   SVARBUFA,=XL2'0000'  END OF VAR TABLE?
         BE    ZVARS040             YES - BRANCH
*
         CLC   =CL8'Z',SVARNAME     IS THIS A ZVAR ?
         BNE   ZVARS020             NO - SKIP IT
*
         CLC   =XL8'FFFFFFFFFFFFFFFF',0(R9)  END OF LIST?
         BE    ZVARS030             YES - TOO BAD...
*
         MVC   SVARNAME,0(R9)       MOVE IN NAME
         LA    R9,8(,R9)            NEXT NAME IN VECTOR
*
ZVARS020 DS    0H
         LA    R10,SVARSIZE(,R10)       POINT TO NEXT ENTRY
         B     ZVARS010
*
*
ZVARS030 DS    0H
         ABEND 111
*
*
         DROP  R10
ZVARS040 DS    0H
*
         L     R10,VARTAB
         USING SVAR,R10
INSV010  DS    0H
         CLC   SVARBUFA,=XL2'0000'  END OF VAR TABLE?
         BE    INSV050              YES - BRANCH
*
         LA    R1,SVARNAME          VAR NAME
         ST    R1,VPARAM
         LH    R1,SVARLEN
         ST    R1,VPLEN
         LA    R1,VPLEN
         ST    R1,VPARAM+4
         LA    R1,VWORK
         ST    R1,VPARAM+8
*
         LA    R1,VPARAM
         L     R15,=V(VLOC)
         BALR  R14,R15
*
         LTR   R15,R15
         BNZ   INSV040            NOT FOUND
*
         IC    R1,=X'11'            SBA
         BAL   R14,PUTCHAR
         IC    R1,SVARBUFA
         BAL   R14,PUTCHAR
         IC    R1,SVARBUFA+1
         BAL   R14,PUTCHAR
*
INSV020  DS    0H
         LH    R3,SVARLEN        VAR AREA LENGTH
         LA    R4,VWORK
INSV030  DS    0H
         IC    R1,0(,R4)
         BAL   R14,PUTCHAR
         LA    R4,1(,R4)
         BCT   R3,INSV030
*
INSV040  DS    0H
         LA    R10,SVARSIZE(,R10)       POINT TO NEXT ENTRY
         B     INSV010
*
*
         DROP  R10
*
*
INSV050  DS    0H
         L     R3,SCRMSG          ANY MESSAGE TO DISPLAY?
         LTR   R3,R3
         BZ    INSV080            NO - BRANCH
*
         LA    R2,2(,R3)          POINT TO DATA
         LH    R3,0(,R3)          GET LENGTH
*
         LTR   R3,R3              ZERO LENGTH?
         BZ    INSV080            YES - NO MESSAGE...
         C     R3,=F'70'          MAX LENGTH
         BNH   INSV060
*
         LA    R3,70
INSV060  DS    0H
         LA    R1,79
         SR    R1,R3              STARTING COL
         BCTR  R1,0               BACK UP 1 MORE
         LA    R0,1
         BAL   R14,CNVTSBA        GO CALC BUFFER POS
         LR    R5,R15             SAVE BUFFER ADDR
         IC    R1,=X'11'
         BAL   R14,PUTCHAR
*
         LR    R1,R5
         SRL   R1,8
         BAL   R14,PUTCHAR
         LR    R1,R5
         BAL   R14,PUTCHAR
*
         IC    R1,=X'1D'       START FIELD
         BAL   R14,PUTCHAR
         IC    R1,=X'F8'
         BAL   R14,PUTCHAR
         IC    R1,=C' '
         BAL   R14,PUTCHAR
*
INSV070  DS    0H
         IC    R1,0(R2)
         BAL   R14,PUTCHAR
         LA    R2,1(,R2)
         BCT   R3,INSV070
*
INSV080  DS    0H
         CLC   SCRCSR,=CL8' '     ANY CURSOR FIELD SPECIFIED?
         BE    CSRP030            NO - USE A DEFAULT
*
         L     R10,VARTAB         POINT TO SCREEN VAR TABLE
         USING SVAR,R10
*
CSRP010  DS    0H
         CLC   SVARBUFA,=XL2'0000'  END OF TABLE?
         BE    CSRP030            YES - USE DEFAULT CSR POS
*
         CLC   SCRCSR,SVARNAME    IS THIS OUR FIELD?
         BE    CSRP020            YES, USE IT
*
         LA    R10,SVARSIZE(,R10) TRY NEXT FIELD
         B     CSRP010
*
*
CSRP020  DS    0H
         MVC   CSRPOS,SVARBUFA    SET CURSOR POSITION
*
CSRP030  DS    0H
         CLC   =X'0000',CSRPOS    ANY CURSOR POSITION?
         BE    DONE010            NO - BRANCH
*
         IC    R1,=X'11'
         BAL   R14,PUTCHAR
         IC    R1,CSRPOS
         BAL   R14,PUTCHAR
         IC    R1,CSRPOS+1
         BAL   R14,PUTCHAR
         IC    R1,=X'13'
         BAL   R14,PUTCHAR
*
         DROP  R10
*
DONE010  DS    0H
         L     R5,BUFFERP
         S     R5,BUFFER
         L     R4,BUFFER
         TPUT  (R4),(R5),FULLSCR
*
*
         LTR   R15,R15
         BZ    TPUTOK
*
         ABEND 101
TPUTOK   DS    0H
*
*
         L     R4,INBUF
         L     R5,=A(4096)
         TGET  (R4),(R5),ASIS
*
*
INVAR    DS    0H
         LR    R5,R1         SAVE INPUT LENGTH
         MVC   AID,0(R4)     SAVE AID VALUE
         LA    R4,3(,R4)     SKIP OVER AID/CSR POS
         S     R5,=F'3'      ADJUST LENGTH
         BNP   INVAR070      BRANCH IF DONE
         CLI   0(R4),X'11'   START FLD?
         BNE   INVAR060      SOME TYPE OF ERROR
*
INVAR010 DS    0H
         LA    R6,1(,R4)     SAVE START OF FIELD
         LA    R4,3(,R4)     POINT TO FLD DATA
         SLR   R3,R3         ZERO FIELD LENGTH
         S     R5,=F'3'      SKIP OVER SF+BUFADR
         BNP   INVAR030      BRANCH IF AT END
*
INVAR020 DS    0H
         CLI   0(R4),X'11'   START OF NEW FIELD ?
         BE    INVAR030      YES - BRANCH
*
         LA    R3,1(,R3)     ADD ONE TO FIELD LENGTH
         LA    R4,1(,R4)
         BCT   R5,INVAR020   LOOP BACK
*
INVAR030 DS    0H
         L     R10,VARTAB     SVAR START
         USING SVAR,R10
*
INVAR040 DS    0H
         CLC   =XL2'0000',SVARBUFA   END OF TABLE ?
         BE    INVAR060              VAR NOT FOUND
*
         CLC   SVARBUFA,0(R6)        IS THIS OUR VAR
         BE    INVAR050              YES - BRANCH
*
         LA    R10,SVARSIZE(,R10)      INCREMENT TO NEXT
         B     INVAR040              LOOP BACK
*
*
INVAR050 DS    0H
         LA    R1,SVARNAME
         ST    R1,VPARAM
         ST    R3,VPLEN
         LA    R1,VPLEN
         ST    R1,VPARAM+4
         LA    R1,2(,R6)
         ST    R1,VPARAM+8
*
         LA    R1,VPARAM
         L     R15,=V(VSTO)
         BALR  R14,R15
*
         LTR   R5,R5              END OF DATA STREAM ?
         BNE   INVAR010           NO - START NEW FIELD
         B     INVAR070
*
*
INVAR060 DS    0H
         ABEND 103
*
*
INVAR070 DS    0H
         MVC   SCRAID,=CL8' '     CLEAR AID VALUE
         LA    R1,AIDTABLE        POINT TO LIST OF AID VALUES
INVAR080 DS    0H
         CLI   0(R1),X'FF'        END OF TABLE?
         BE    INVAR090           YES - BRANCH
*
         CLC   AID,0(R1)          IS THIS OUR ENTRY
         BE    INVAR090
*
         LA    R1,5(,R1)
         B     INVAR080
*
*
INVAR090 DS    0H
         MVC   SCRAID(4),1(R1)
*
*
         DROP  R10
*
*
         L     R2,BUFFER
         FREEMAIN R,LV=4096,A=(R2)
*
         L     R2,INBUF
         FREEMAIN R,LV=4096,A=(R2)
*
         L     R3,=A(SVARSIZE*200)
         L     R2,VARTAB
         FREEMAIN R,LV=(R3),A=(R2)
*
*
          L     R13,4(,R13)
          LM    R14,R12,12(R13)
          SLR   R15,R15
          BR    R14
*
*
*
*
*
***********************************************************************
*
* OUTPUT: R15 = RC (4=EOF)  R1=XXXC
*
***********************************************************************
GETCHAR   DS    0H
          L     R1,COL
          LA    R1,1(,R1)
          ST    R1,COL
*
          C     R1,=F'80'
          BNH   NEXTCH10
*
          LA    R1,1
          ST    R1,COL
          L     R1,ROW
          LA    R1,1(,R1)
          ST    R1,ROW
*
          C     R1,=F'24'
          BH    NEXTCH20
*
*
NEXTCH10  DS    0H
          L     R15,PANELP
          ICM   R1,B'0001',0(R15)
          MVC   CHAR,0(R15)
*
          MVC   CHARTYPE,0(R15)
          TR    CHARTYPE,TRTAB
*
          LA    R15,1(,R15)
          ST    R15,PANELP
*
         SLR   R15,R15
*
         CLI   CHARTYPE,0
         BER   R14
*
          MVC   CHARATTR,CHARTYPE
          NI    CHARATTR,X'0F'
          TR    CHARATTR,ATTRTAB
*
          SLR   R15,R15
          BR    R14
*
*
NEXTCH20  DS    0H
          LA    R15,4
          BR    R14
*
*
***********************************************************************
*
* INPUT: R1 = XXXC
*
***********************************************************************
PUTCHAR  DS    0H
         L     R15,BUFFERP
         STCM  R1,B'0001',0(R15)
         LA    R15,1(,R15)
         ST    R15,BUFFERP
         BR    R14
*
*
***********************************************************************
*
* INPUT: R0=ROW, R1=COL
* OUTPUT: R15 = SBA ADDR
*
***********************************************************************
CNVTSBA   DS   0H
          BCTR R0,0
          BCTR R1,0
          MH   R0,=H'80'
          AR   R1,R0
          SLL  R1,2
          STCM R1,B'0010',SBACMD
          SRL  R1,2
          STCM R1,B'0001',SBACMD+1
          NC   SBACMD(2),=X'3F3F'
          TR   SBACMD(2),TBL3270
          SLR  R15,R15
          ICM  R15,B'0011',SBACMD
          BR   R14
*
SBACMD    DC   XL2'0000'
*
*
AIDTABLE DS    0C
         DC    X'7D',C'    '      ENTER
         DC    X'F1',C'PF01'
         DC    X'F2',C'PF02'
         DC    X'F3',C'PF03'
         DC    X'F4',C'PF04'
         DC    X'F5',C'PF05'
         DC    X'F6',C'PF06'
         DC    X'F7',C'PF07'
         DC    X'F8',C'PF08'
         DC    X'F9',C'PF09'
         DC    X'7A',C'PF10'
         DC    X'7B',C'PF11'
         DC    X'7C',C'PF12'
         DC    X'C1',C'PF13'
         DC    X'C2',C'PF14'
         DC    X'C3',C'PF15'
         DC    X'C4',C'PF16'
         DC    X'C5',C'PF17'
         DC    X'C6',C'PF18'
         DC    X'C7',C'PF19'
         DC    X'C8',C'PF20'
         DC    X'C9',C'PF21'
         DC    X'4A',C'PF22'
         DC    X'4B',C'PF23'
         DC    X'4C',C'PF24'
         DC    X'6C',C'PA1 '
         DC    X'6E',C'PA2 '
         DC    X'6B',C'PA3 '
         DC    X'FF',CL4' '
*
*
ROW       DC    F'1'
COL       DC    F'0'
PANELP    DC    A(0)
BUFFER    DC    A(0)
BUFFERP   DC    A(0)
INBUF     DC    A(0)
CSRPOS    DC    X'0000'
VARTAB    DC    A(0)
VPARAM    DC    A(0,0,0)     VPUT/VGET PARM LIST
VPLEN     DC    F'0'
AID       DC    X'00'
CHAR      DC    C' '
CHARTYPE  DC    C' '
CHARATTR  DC    C' '
WORKNAME  DC    CL8' '
VWORK     DC    CL256' '
*
*
TBL3270   DC   X'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'
          DC   X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F'
          DC   X'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F'
          DC   X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'
*
*
ATTRTAB   DC   X'F040F0F0F8C8F8F87C4C7C7CF040F0F0'
*
*
CLEARSCR  DC   X'27F5C31140403C404040'
*
*
          LTORG ,
*
*
SAVEA     DC    18F'0'
*
*
TRTAB    DC    XL256'00'          ESCAPE CHAR TRANSLATE TABLE
*
*
*
*
R0        EQU   0
R1        EQU   1
R2        EQU   2
R3        EQU   3
R4        EQU   4
R5        EQU   5
R6        EQU   6
R7        EQU   7
R8        EQU   8
R9        EQU   9
R10       EQU   10
R11       EQU   11
R12       EQU   12
R13       EQU   13
R14       EQU   14
R15       EQU   15
*
* ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **
*
         $FSIECT ,
*
* ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** ** **
*
SVAR     DSECT ,
SVARBUFA DS    XL2
SVARLEN  DS    XL2
SVARZNUM DS    XL2
         DS    XL2
SVARNAME DS    CL8
SVARSIZE EQU   *-SVAR
*
*
         $FSISCR ,
*
*
          END   ,
