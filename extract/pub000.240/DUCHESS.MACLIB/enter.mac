         MACRO
&LABEL   ENTER &BASE=R12,&TEMP=R15,&CORE=0,&DSA=N
         LCLC  &ALLOC
&LABEL   STM   R14,R12,12(R13)
         BALR  &BASE.,0
         USING *,&BASE.
         L     &TEMP.,0(,R13)     POINTER TO NEXT FREE AREA
         ST    &TEMP.,8(,R13)     IS FORWARD CHAIN TO OUR SAVEAREA
         ST    R13,4(,&TEMP.)     BACK CHAIN TO LAST SAVEAREA
         LR    R13,&TEMP.         POINT TO NEW SAVEAREA
&ALLOC   SETC  '&CORE*8'
         AIF   ('&CORE' EQ '0' OR '&DSA' EQ 'N').OK
         MNOTE 8,'CORE AND DSA ARE MUTUALLY EXCLUSIVE'
.OK      AIF   ('&DSA' EQ 'N').NODSA
&ALLOC   SETC  '$$DSALEN'
         USING SAVEAREA,R13       ACTIVATE DSA
.NODSA   ANOP
         LA    &TEMP,9*8+(&ALLOC+7)/8*8(R13)    NEW FREE POINTER
         ST    &TEMP.,0(,R13)     SAVE FOR THIS ROUTINE
         MEND
