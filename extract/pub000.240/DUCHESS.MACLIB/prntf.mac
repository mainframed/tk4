         MACRO
&LABEL   PRNTF &CS,&ARGS,&FO=6,&PLIST=
         LCLA  &#A,&I,&LEN
         LCLC  &ARG
&LABEL   CEQU  *
&#A      SETA  N'&ARGS
         AIF   ('&#A' NE '0').FULL
&ARG     SETC  '&FO'
         AIF   ('&ARG'(1,1) EQ '(').FOREG
         LA    R0,&FO
         AGO   .FONOR
.FOREG   LR    R0,&FO
.FONOR   LA    R14,PRR&SYSNDX
         L     R15,=V(PRINTF)
         CNOP  2,4
         BALR  R1,R15
         DC    AL1(X'80'),AL3(*+3)
         DC    AL2(L'PRC&SYSNDX)
PRC&SYSNDX DC    C&CS
PRR&SYSNDX DS    0H
         MEXIT
.FULL    ANOP
&LEN     SETA  ((&#A+1)*4+7)/8
         AIF   ('&PLIST' NE '').NOGETC
         GETC  &LEN
         AGO   .GETCS
.NOGETC  LA    R1,&PLIST
.GETCS   LA    R15,PRS&SYSNDX
         ST    R15,0(R1)
&I       SETA  1
.LOOP    AIF   ('&I' GT '&#A').ENDL
&ARG     SETC  '&ARGS(&I)'
         AIF   ('&ARG'(1,1) NE '(').NOTREG
         ST    &ARGS(&I),&I*4(R1)
         AGO   .NXTARG
.NOTREG  ANOP
         LA    R15,&ARGS(&I)
         ST    R15,&I*4(R1)
.NXTARG  ANOP
&I       SETA  &I+1
         AGO   .LOOP
.ENDL    MVI   &I*4-4(R1),X'80'
&ARG     SETC  '&FO'
         AIF   ('&ARG'(1,1) EQ '(').FULFR
         LA    R0,&FO
         AGO   .FULFNR
.FULFR   LR    R0,&FO
.FULFNR  LA    R14,PRR&SYSNDX
         L     R15,=V(PRINTF)
         BR    R15
PRS&SYSNDX DC    AL2(L'PRC&SYSNDX)
PRC&SYSNDX DC    C&CS
PRR&SYSNDX DS    0H
         AIF   ('&PLIST' NE '').NOFREEC
         FREEC &LEN
.NOFREEC ANOP
         MEND
