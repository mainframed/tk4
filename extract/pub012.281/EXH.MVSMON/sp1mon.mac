*          THE FOLLOWING SOFTWARE PRODUCT IS PROVIDED AS IS/WHERE IS
*          WITH NO WARRANTIES/GUARANTEES SPECIFIED OR IMPLIED
*          BY:       FLORIDA POWER CORPORATION,
*                    3201 34TH STREET SOUTH
*                    ST. PETERSBURG, FLORIDA 33711
*
*
*
*
         TITLE 'GU955010 MVSMON - MVS/SP 1.3 DISPLAY MONITOR'
*              DATE.     06/22/82.  REVISED FROM GU955000 FOR MVS 3.8
*              AUTHOR.   DON HIGGINS, FLORIDA POWER CORPORATION.
*              REMARKS.  MVSMON IS A COMMAND DRIVEN MVS DISPLAY MONITOR
*                        WHICH HAS THE FOLLOWING CHARACTERISTICS.
*
*                          1.  IT IS REENTRANT AND CAN BE PLACED IN
*                              PLPA FOR COMMON USE.
*                          2.  WHEN EXECUTED AS A SYSTEM TASK IT
*                              USES A WTOR/WTO INTERFACE TO THE
*                              MCS MASTER CONSOLE.
*                          3.  WHEN EXECUTED AS A TSO PROGRAM IT
*                              USES A TGET/TPUT INTERFACE TO TERMINAL.
*                          4.  WHEN EXECUTED AS A BATCH PROGRAM IT
*                              USES A GET/PUT INTERFACE TO SYSIN
*                              AND SYSPRINT QSAM FILES.
*                          5.  THE PROGRAM DOES NOT REQUIRE ANY
*                              AUTHORIZATION OR HOOKS.
*                          6.  IF A PARM IS PRESENT, IT IS EXECUTED
*                              AS A SINGLE INPUT COMMAND AND THEN THE
*                              PGM TERMINATES NORMALLY.  THIS IS FOR
*                              USE AS A STARTED TASK TO JUST GET A
*                              SINGLE RESPONSE WITHOUT USING WTOR.
*                              FOR EXAMPLE,  S MVSMON,,,PARM=DE
*                          7.  NOTE PROGRAM CONTAINS NO RLDS TO
*                              REDUCE I/O AND PROCESSING TIME FOR
*                              FETCH WHEN EXECUTING OUT OF PDS
*                              RATHER THAN PLPA.
*                          8.  WHEN EXECUTED AS A CALLED PROGRAM BY
*                              THE MESSAGE PROCESSING SUPERVISOR
*                              (MPS) GU640001 FOR ONLINE SYSTEMS, IT
*                              USES THE FOLLOWING INTERFACES
*
*                              INPUT/OUTPUT CALL ARGUMENTS
*                                1.  TCAM TERMINAL NAME (8 BYTES)
*                                2.  MESSAGE ADDRESS
*                                3.  MESSAGE LENGTH (4 BYTE BINARY)
*
*                              MVSMON IDENTIFIES THIS MODE BY
*                              LOCATING CDE ENTRY POINT NAMED MVSMONVT
*                              WHICH POINTS TO VECTOR TABLE WITH THE
*                              FOLLOWING ENTRIES.
*                                1.  ADDRESS OF MVSMON WORK AREA
*                                    CREATED ON FIRST CALL AND USED
*                                    FOR ALL SUCCEEDING CALLS.
*                                2.  ADDRESS OF OUTPUT MESSAGE
*                                    PROCESSOR CALLED BY MVSMON TO
*                                    OUTPUT MESSAGES.
*
*                          9.  SPIE PROGRAM INTERRUPTION HANDLER
*                              ATTEMPTS TO RECOVER FROM TEMPORARY
*                              INTERRUPTIONS CAUSED BY CONTROL
*                              BLOCK CHANGES DURING ACCESS.
*
*                         10.  STAX INTERRUPTION HANDLER ABORTS
*                              AND/OR SURPRESSES THE REMAINING
*                              OUTPUT FROM THE CURRENT COMMAND
*                              WHEN RUNNING IN TSO MODE.
*
*
*
*
*
*
*              REGISTERS.
*                  R0-R7 WORK REGISTERS
*                  R8-R9 EXTENDED BASES
*                  R10   POINTER TO READ/WRITE WORK AREA
*                  R11   POINTER TO STACK FOR PERFORMS
*                  R12   BASE
*                  R13   SAVE AREA
*                  R14-R15 LINKAGE AND WORK
*              REFERENCES.
*                  GC28-0751-0 OS/VS2 BEBUGGING HANDBOOK VOL. 1
*                  GC28-0709-0 OS/VS2 DEBUGGING HANDBOOK VOL. 2
*                  GC28-0886-0 OS/VS2 MVS PERFORMANCE NOTEBOOK
*                  GC28-0648-2 OS/VS2 TSO GUIDE TO COMMAND PROCESSORS
*                  GC28-0683-1 OS/VS2 SUPR. SERVICES MACRO INSTR.
*                  SY28-0715-1 OS/VS2 SYSTEM LOGIC LIBRARY
*              FILES.
*                  SYSIN - INPUT COMANDS FOR BATCH MODE ONLY
*                  SYSPRINT - OUTPUT FOR BATCH MODE ONLY
*              MAINTENANCE.
*
*  06/12/80 DSH - CHANGE DEFAULT SIOS LIMIT FROM 3 TO 5
*
*  12/26/80 DSH - FIX UCB SCAN LOGIC AS FOLLOWS:
*                   1.  USE ICM TO HANDLE UCB ADDRESSES UP TO 64K
*                   2.  BUILD TABLE OF PRIMARY UCBS AT INIT TIME
*                   3.  SKIP ALT. UCBS IN ALL UCB SCANS.
*
*  03/10/81 DSH - FIX LH WITH LHL FOR 10MK INFO IN DV, R, AND M CMS
*
*  07/18/82 DSH - CONVERT FROM MVS 3.8 TO MVS/SP 1.3
*                   1.  DROP DQ SUPPORT SINCE QCB'S MOVED TO GRS AS
*                   2.  FIX NEW CONTROL BLOCK OFFSETS
*
*  08/04/82 DSH - FIX UCB SCANS TO HANDLE DUAL PATH DISKS
*                   1.  SEE LOCAL MACRO CVTUCB
*
*  07/03/83 DSH - USE CPU 2 INSTEAD OF 1 ON 3081G
*
*
*
********************************************************************
*  STEPS TO FOLLOW DURING MAINTENANCE
*    1.  ENTER COMMENTS ABOVE DESCRIBING CHANGE
*    2.  ADD HELP DOCUMENTATION INCLUDING NEWS COMMAND
*    3.  ADD BATCH TEST TO J.CNTL(GU955TST) FOR ANY COMMANDS
*    4.  COPY MVSMON FROM FPC.TESTLIB TO FPC.CMDLIB AFTER TEST
*    5.  SUBMIT REQUEST TO MOVE GU955000 FROM WORK TO SOURCE
********************************************************************
         TITLE 'LOCAL MACROS'
         MACRO
         CVTUCB &R
.*       GIVEN A(UCB) IN R, RETURN BINARY UCB DEV. ADDR IN R0
         ZR    R15
         IC    R15,UCBNAME(&R)
         IF    (SH,R15,=XL2'00F0',M)
               AH   R15,=AL2(X'F0'-X'C1'+10)
         FI
         LR    R0,R15
         SLL   R0,4
         ZR    R15
         IC    R15,UCBNAME+1(&R)
         IF    (SH,R15,=XL2'00F0',M)
               AH   R15,=AL2(X'F0'-X'C1'+10)
         FI
         AR    R0,R15
         SLL   R0,4
         ZR    R15
         IC    R15,UCBNAME+2(&R)
         IF    (SH,R15,=XL2'00F0',M)
               AH   R15,=AL2(X'F0'-X'C1'+10)
         FI
         AR    R0,R15
         MEND
         MACRO
         WRITE &MSG
.*       WRITE LITERAL MESSAGE
         LA    R0,=CL80&MSG
         PM    PUT
         MEND
         MACRO
         EM    &FIELD
.*       EDIT MEMORY IN BYTES IN R0 TO 5 DIGIT MEMORY IN K
         SRL   R0,10
         CVD   R0,PWORK
         EDIT  TO=&FIELD,FROM=PWORK+5,MASK=' ZZZ99'
         MEND
         MACRO
         EMK   &FIELD
.*       EDIT MEMORY IN K IN R0 TO 5 DIGIT MEMORY IN K
         CVD   R0,PWORK
         EDIT  TO=&FIELD,FROM=PWORK+5,MASK=' ZZZ99'
         MEND
         MACRO
         EMF   &FIELD
.*       EDIT MEMORY IN FRAMES IN R0 TO 5 DIGIT MEMORY IN K
         SLL   R0,2
         CVD   R0,PWORK
         EDIT  TO=&FIELD,FROM=PWORK+5,MASK=' ZZZ99'
         MEND
         MACRO
         IHW   &HW
.*       INCREMENT HALF WORD BINARY COUNTER BY ONE
         XR    R0,R0
         ICM   R0,3,&HW
         AH    R0,=H'1'
         STH   R0,&HW
         MEND
         MACRO
         LHL   &RH,&HW
.*       LOGICAL LOAD HALF WORD
         XR    &RH,&RH
         ICM   &RH,3,&HW
         MEND
         PRINT NOGEN
         TITLE 'MAINLINE'
MVSMON   SUBENTRY BASES=(R12),RENT=YES,STACK=10,                       X
               PSTACK=R11,PSAVE=NO,PCHECK=NO,PBASE=YES
         LR    R4,R1             SAVE PARM FOR GET
         PM    INIT              INITIALIZE WORK AREA
         USING WORK,R10
         USING WORK+4095,R9
         LA    R15,PGMCHK3
         STM   R0,R15,PGMCHKSA   SAVE MAINLINE REGS FOR RESTART
         SPIE  PGMCHK1,MF=(E,SPIELIST)
         ST    R1,SAVEPICA
PGMCHK4  TAG                 RESTART POINT FOLLOWING PGM CHECK
         PM    GET
         DODO  WHILE,(CLI,EOF,TRUE,NE)
               PM   PROCESS
               IF   (CLI,EOF,TRUE,NE)
                    PM   GET
               FI
         OD
         L     R1,SAVEPICA
         SPIE  MF=(E,(1))
         IF    (CLI,MODE,MPSMODE,NE)
               WRITE 'MVSMON ENDED'
         FI
         IF    (CLI,MODE,TSOMODE,EQ)
               STAX
         FI
         IF    (CLI,MODE,IOMODE,EQ)
               CLOSE (SYSIN,,SYSPRINT),MF=(E,DCBLIST)
         FI
         IF    (CLI,MODE,MPSMODE,NE)
               L     R0,=A(LWORK)
               LR    R1,R10
               FREEMAIN R,LV=(0),A=(1)
         FI
         SUBEXIT
PGMCHK3  TAG             THIRD STAGE OF PGMCHK PROCESSING
*
*        NOTE LM IN PGMCHK2 RESETS PSTACK R11 TO MAINLINE LEVEL
*
         ZR    R1
         ICM   R1,7,PGMPSW+5
         LA    R0,0(R12)
         SR    R1,R0
         AAI   R1,8
         ST    R1,PWORK
         UNPK  DWORK,PWORK+1(4)
         TR    DWORK(6),HEX-240
         MVC   DPGMCHK,DWORK
         LA    R0,PGMMSG
         PM    PUT        DISPLAY PGMCHK RELATIVE ADDRESS
         WRITE 'IF PROBLEM PERSISTS CALL DON AT (813) 866-4145'
         B     PGMCHK4
         TITLE 'INIT - INITIALIZE MVSMON'
INIT     PENTRY
         FINDCDE EP=MVSMONVT
         IF    (LTR,R5,R1,NZ)
*
*        R5=A(TPPUT CDE)
*
               L    R5,CDENTPT(R5)
               IF   (ICM,R10,15,0(R5),NZ)
                    LA   R9,4095(R10)
                    MVI  PARM,FIRST
                    MVI  EOF,FALSE
                    PEXIT
               FI
         FI
         L     R0,=A(LWORK)
         GETMAIN R,LV=(0)
         LR    R10,R1           R10=A(USER WORK AREA)
         L     R1,4(R13)
         L     R0,16(R1)        R0=A(MVSMON FROM PREVIOUS SAVE)
         AL    R0,=A(WORK-MVSMON)
         L     R1,=A(LWORK)
         LR    R2,R10
         LR    R3,R1
         MVCL  R2,R0              INITIALIZE WORK AREA
         USING WORK,R10
         LA    R9,4095(R10)
         USING WORK+4095,R9
         ST    R4,APARM
         L     R1,4(R13)
         L     R0,16(R1)
         AL    R0,=A(GETEOF-MVSMON)
         ST    R0,SYSIN+DCBEODAD
         L     R2,PSATOLD
         LA    R1,=A(L'LINE)
         STCM  R1,7,ATPLMSG+1
         IF    (LTR,R5,R5,NZ)
               MVI  MODE,MPSMODE
               L    R0,4(R5)
               ST   R0,ATPPUT          SAVE TPPUT ADDR
               ST   R10,0(R5)          SAVE MVSMON WORK ADDR
         ELSEIF (ICM,R1,15,ASCBTSB(R2),NZ)
               MVI  MODE,TSOMODE
               STAX STAXRTN,OBUF=(STAXMSG,80),MF=(E,STAXLIST)
               LOAD EP=SR309000
               ST   R0,AFSF
               LA   R1,LINE
               ST   R1,FSRLPARM
               LA   R1,ROW
               ST   R1,FSRLPARM+4
               LA   R1,COL
               ST   R1,FSRLPARM+8
               LA   R1,AID
               ST   R1,FSRLPARM+12
               LA   R1,PUTLINE
               ST   R1,FSWLPARM
         ELSEIF (ICM,R1,15,ASCBJBNI(R2),Z)
               MVI  MODE,WTOMODE
         ELSE
               MVI  MODE,IOMODE
               OPEN  (SYSIN,,SYSPRINT,(OUTPUT)),MF=(E,DCBLIST)
         FI
         IF    (CLI,MODE,MPSMODE,NE)
               LA    R0,TITLE1
               PM    PUT
               LA    R0,TITLE2
               PM    PUT
         FI
         L     R2,16
         L     R3,CVTASCVT(R2)
         L     R4,ASVTMAXU(R3)
         MH    R4,=AL2(L'RCT)
         LR    R0,R4
         GETMAIN    R,LV=(0)
         ST    R1,RCTABPTR      SAVE PTR TO CPU RATE TABLE FOR ASCBS
         LR    R0,R1
         LR    R1,R4
         ZR    R14
         ZR    R15
         MVCL  R0,R14
         L     R2,FLCTRACE
         IF    (LTR,R2,R2,NZ)
               LM   R3,R4,4(R2)
               SR   R4,R3
               LR   R0,R4
               GETMAIN R,LV=(0)
               LA   R2,0(R1,R4)
               STM  R1,R2,TTETAB
         FI
         L     R2,16
         L     R2,CVTILK2(R2)
         ZR    R0
         DODO  WHILE,(CLC,0(2,R2),=X'FFFF',NE)
               IF   (CLC,0(2,R2),=X'0000',NE)
                    AH   R0,=H'6'
               FI
               AAI  R2,2
         OD
         GETMAIN R,LV=(0)
         ST    R1,DISIOTAB
         ZR    R4                  LCU
         LR    R5,R1               LCUA
         ZR    R6                  CCU
         L     R2,16
         L     R2,CVTILK2(R2)
         ZR    R3
         ZR    R0
         DODO  WHILE,(CLC,0(2,R2),=X'FFFF',NE)
               IF   (CLC,0(2,R2),=X'0000',NE)
                    ICM  R3,3,0(R2)
                    CVTUCB R3                   R3=A(UCB)
                    LR   R15,R0                 R15=UCB DEV ADDR
                    SRL  R0,4                   R0=UCB CU
                    IF   (CLR,R0,R6,GE)         CU>=CCU
                         IF   (CLR,R0,R6,GT)      CU>CCU
                              LR   R4,R6          LCU=CCU
                              LR   R5,R1          LCUA=CUA
                              LR   R6,R0          CCU=CU
                         FI
                         STH  R15,4(R1)           STORE UCB ADDR
                         AAI  R1,6
                    ELSEIF (CLR,R0,R4,GT)      CU>LCU
                         LR   R1,R5              CUA=LCUA
                         LR   R6,R0              CCU=CU
                         STH  R15,4(R1)           STORE UCBADDR
                         AAI  R1,6
                    FI
               FI
               AAI  R2,2
         OD
         PM    INTERVAL
         IF    (CLI,MODE,TSOMODE,EQ)
               PM   DJ
         FI
         PEXIT
         TITLE 'PROCESS'
PROCESS  PENTRY
         IF    (CLI,PFKMODE,TRUE,EQ)
               WRITE 'COMMAND DEFINED - ENTER NEW PF KEY'
               PEXIT
         FI
         DOCASE LINE
           CASE C'D'
                PM   DISPLAY
           ESAC
           CASE C'E'
                MVI  EOF,TRUE
           ESAC
           CASE C'H' HELP COMMANDS
                DOCASE LINE+1
                  CASE C'C'  COMMANDS BY TYPE
                    PM HC
                  ESAC
                  CASE C'D'  DETAIL ALPHABETICAL LIST
                    PM HD
                  ESAC
                  CASE C'K'  PFK DEFINITION MODE
                    PM HK
                  ESAC
                  CASE C'M'  HELP MODE
                    PM HM
                  ESAC
                  CASE C'S' HELP SUMMARY
                    PM HS
                  ESAC
                  CASE ELSE
                    PM HELP
                  ESAC
                ESACOD
           ESAC
           CASE C'I' HOLD/RELEASE INTERVAL CALCULATIONS
                PM   IHOLD
           ESAC
           CASE C'M'  DISPLAY MEMORY USAGE BY SYSTEM COMPONENT
                PM   MEMORY
           ESAC
           CASE C'P'  DISPLAY PAGE DATA SET INFO.
                PM    PDSNINFO
           ESAC
           CASE C'Q'
                PM    ENQDEQ
           ESAC
           CASE C'R'  DISPLAY CPU RATES BY SYSTEM COMPONENT
                PM    RATES
           ESAC
           CASE C'S' DISPLAY SRM STATUS
                PM   SRM
           ESAC
           CASE C'T'
                PM   TIME
           ESAC
           CASE C'W' WAIT FOR INTERVAL
                PM   WAIT
           ESAC
           CASE C'X'
                PM   XCOMINFO
           ESAC
           CASE ELSE
                PM   UNKNOWN
           ESAC
         ESACOD
         PEXIT
         TITLE 'IHOLD - HOLD/RELEASE INTERVAL UPDATING'
IHOLD    PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'I - HOLD OR RELEASE INTERVAL UPDATING'
               WRITE ' '
               WRITE 'THIS COMMAND ALLOWS EXECUTION OF MULTIPLE'
               WRITE 'INTERVAL BASED COMMANDS USING SAME INTERVAL'
               WRITE 'DATA'
               PEXIT
         FI
         IF  (CLI,INTHOLD,FALSE,EQ)
             MVI  INTHOLD,TRUE
             WRITE 'INTERVAL CALCULATION HELD'
         ELSE
             MVI  INTHOLD,FALSE
             WRITE 'INTERVAL CALCULATION RELEASED'
         FI
         PEXIT
         TITLE 'INTERVAL - PERFORM ALL INTERVAL CALCULATIONS'
INTERVAL PENTRY
         IF    (CLI,INTDONE,TRUE,EQ)
               PEXIT
         FI
         MVI   INTDONE,TRUE
         IF    (CLI,INTHOLD,TRUE,EQ)
               LA   R0,INTMSG
               PM   PUT
               PEXIT
         FI
         TIME  BIN
         LR    R1,R0
         S     R1,INTLAST
         IF    (CL,R1,=F'100',L)
               STIMER WAIT,BINTVL==F'100'
               TIME   BIN
               LR     R1,R0
               S      R1,INTLAST
         FI
         ST    R1,INTBIN
         ST    R0,INTLAST
         ZR    R0
         D     R0,=F'100'
         CVD   R1,PWORK
         EDIT  TO=DINTSEC,FROM=PWORK+5,MASK=' ZZZ99'
         TIME  DEC
         ST    R0,PWORK
         EDIT  TO=DINTEND,FROM=PWORK,MASK=' 99.99'
         PM    DC1
         PM    DP1
         PM    RC1
         PM    RT1
         PM    DI1
         IF    (CLC,INTBIN,INTLAST,NE)
               LA    R0,INTMSG
               PM    PUT
         FI
         PEXIT
         TITLE 'DISPLAY ROUTINE'
DISPLAY  PENTRY
         DOCASE LINE+1
               CASE C'A' DISPLAY ONLY ACTIVE JOBS, STCS, AND USERS
                    PM   DA
               ESAC
               CASE C'B' DISPLAY BASIC SYSTEM INFO.
                    PM   DB
               ESAC
               CASE C'C' DISPLAY CPU AND CHANNEL USAGE
                    PM   DC
               ESAC
               CASE C'D' DISPLAY DISKS
                    PM   DD
               ESAC
               CASE C'E' DISPLAY POSSIBLE ERROR CONDITIONS
                    PM   DE
               ESAC
               CASE C'I' DISPLAY IO COUNT FOR UCBS
                    PM   DI
               ESAC
               CASE C'J' DISPLAY BATCH JOBS
                    PM   DJ
               ESAC
               CASE C'K' DISPLAY ACTIVE LPA MODULES AND USAGE
                    PM   DK
               ESAC
               CASE C'L' DISPLAY COMMUNICATION LINES STATUS
                    PM   DL
               ESAC
               CASE C'M' DISPLAY MOUNTS
                    PM   DM
               ESAC
               CASE C'N' DISPLAY NUMBERS OF THINGS
                    PM   DN
               ESAC
               CASE C'P' DISPLAY PAGING
                    PM   DP
               ESAC
               CASE C'Q' DISPLAY ENQS
                    PM   DQ
               ESAC
               CASE C'R' DISPLAY WTOR REQUESTSAND MOUNTS
                    PM   DR
               ESAC
               CASE C'S' DISPLAY SYSTEM TASKS
                    PM   DS
               ESAC
               CASE C'T' DISPLAY TAPES
                    PM   DT
               ESAC
               CASE C'U' DISPLAY TSO USERS
                    PM   DU
               ESAC
               CASE C'V' DISPLAY VIRTUAL AND REAL STORAGE STATUS
                    PM   DV
               ESAC
               CASE C'W' DISPLAY WAITING JOBS AND TSO USERS
                    PM   DW
               ESAC
               CASE C'Z' DISPLAY TRACE TABLE STATUS
                    PM   DZ
               ESAC
               CASE ELSE
                    PM   UNKNOWN
               ESAC
         ESACOD
         PEXIT
         TITLE 'HELP - DISPLAY HELPFUL INFO'
HELP     PENTRY
         IF   (CLI,HMODE,FALSE,EQ)
              PM  HS
         ELSE
              PM  HC
         FI
         PEXIT
         TITLE 'HS - HELP SUMMARY'
HS       PENTRY
         WRITE 'HS - HELP SUMMARY'
         WRITE ' '
         WRITE 'MVSMON IS A REAL TIME MVS OPERATING SYSTEM MONITOR.  '
         WRITE '                                                     '
         WRITE 'TO SWITCH HELP MODE ON OR OFF, ENTER PFK11 OR HM.    '
         WRITE 'IN HELP MODE, ENTERING ANY COMMAND WILL PROVIDE HELP '
         WRITE 'INFORMATION FOR THAT COMMAND.  IN HELP MODE, THE HELP'
         WRITE 'COMMAND WILL LIST COMMANDS BY TYPE (HC COMMAND).     '
         WRITE 'THE HD COMMAND WILL LIST ALL COMMANDS ALPHABETICALLY.'
         WRITE 'NOTE PF KEYS 1-12 CAN BE REDEFINED BY USING PFK10    '
         WRITE 'OR HK COMMAND TO SWITCH TO PFK DEFINITION MODE. USE  '
         WRITE 'THE HELP MODE TO OBTAIN FURTHER HELP ON HK COMMAND.  '
         WRITE 'FOLLOWING KEYS ARE PERMANENTLY DEFINED AS FOLLOWS:   '
         WRITE '                                                     '
         WRITE ' PA1        STOP CURRENT COMMAND                     '
         WRITE ' PA2   END  STOP MVSMON                              '
         PEXIT
         TITLE 'HD - DETAIL LIST OF COMMANDS ALPHABETICALLY'
HD       PENTRY
         WRITE 'HD - DETAIL LIST OF COMMANDS ALPHABETICALLY'
         WRITE ' '
         WRITE '     DA - DISPLAY ALL ACTIVE TASKS                   '
         WRITE '     DB - DISPLAY BASIC SYSTEM STATUS                '
         WRITE '     DC - DISPLAY CPU AND CHANNEL STATUS             '
         WRITE '     DD - DISPLAY ONLINE DISKS                       '
         WRITE '     DE - DISPLAY POSSIBLE ERROR CONDITIONS          '
         WRITE '     DI - DISPLAY UCB I/O COUNTS                     '
         WRITE '     DJ - DISPLAY BATCH JOBS ONLY                    '
         WRITE '     DL - DISPLAY COMMUNICATION LINES STATUS         '
         WRITE '     DK - DISPLAY ACTIVE LPA MODULES AND USAGE       '
         WRITE '     DM - DISPLAY PENDING MOUNTS                     '
         WRITE '     DN - DISPLAY NUMBERS OF THINGS                  '
         WRITE '     DP - DISPLAY SYSTEM PAGING                      '
****     WRITE '     DQ - DISPLAY ENQUEUED RESOURCES                 '
         WRITE '     DR - DISPLAY PENDING OPERATOR REPLIES           '
         WRITE '     DS - DISPLAY SYSTEM TASKS ONLY                  '
         WRITE '     DT - DISPLAY TAPES                              '
         WRITE '     DV - DISPLAY VIRTUAL AND REAL STORAGE STATUS    '
         WRITE '     DU - DISPLAY TSO USERS ONLY                     '
         WRITE '     DW - DISPLAY WAITING TASKS                      '
         WRITE '     DZ - DISPLAY TRACE TABLE USAGE                  '
         WRITE '     DZS- TRACE TABLE SUMMARY INCLUDING SVC USAGE    '
         WRITE '     E  - END                                        '
         WRITE '     H  - DISPLAY HELP INFORMATION                   '
         WRITE '     HC - LIST COMMANDS BY CONTROL BLOCK TYPE        '
         WRITE '     HD - DETAIL LIST OF ALL COMMANDS ALPHABETICALLY '
         WRITE '     HK - TURN PFK MODE ON/OFF AND LIST PFKS         '
         WRITE '     HM - TURN HELP MODE ON/OFF                      '
         WRITE '     HS - HELP SUMMARY                               '
         WRITE '     I  - HOLD/RELEASE INTERVAL CALCULATIONS         '
         WRITE '     M  - DISPLAY REAL MEMORY BY DOMAIN              '
         WRITE '     P  - DISPLAY PAGE DATA SET INFO.                '
         WRITE '     QD - DEQ NAME                                   '
         WRITE '     QE - ENQ NAME                                   '
         WRITE '     QN - DEFINE ENQ/DEQ RESOURCE                    '
         WRITE '     R  - DISPLAY CPU USAGE BY DOMAIN                '
         WRITE '     S  - DISPLAY SRM UIC, CPU, PAGING, AND ASMQ     '
         WRITE '     T  - TIME AND DATE NOW AND AT LAST IPL          '
         WRITE '     W  - WAIT FOR SPECIFIED NUMBER OF SECONDS       '
         WRITE '     X  - DISPLAY XCOM CROSS MEMORY COMM. INFO.      '
         PEXIT
         TITLE 'HC - LIST COMMANDS BY CONTROL BLOCKS USED'
HC       PENTRY
         WRITE 'HC - LIST COMMANDS BY CONTROL BLOCK USED             '
         WRITE '                                                     '
         WRITE 'CONTROL BLOCKS   RESOURCE TYPE   COMMANDS            '
         WRITE '                                                     '
         WRITE '  UCVT           TIME,XCOM       T,X,DB              '
         WRITE '  ASVT/ASCB      TASKS           DA,DJ,DU,DS,DE,R,   '
         WRITE '                                 M,DN,DB             '
         WRITE '  UCB            DEVICES         DI,DN,DE,DM,DL,DT,  '
         WRITE '                                 DD,DW,DB            '
         WRITE '  PVT            MEMORY          DV,DP,P,DE,DB       '
         WRITE '  UCM            REPLIES         DR,DB               '
         WRITE '  TTE            TRACE TABLE     DZ,DZS,DE           '
         WRITE '  CSD            CPU, CHANNELS   DC,DB               '
         WRITE '  CDE            LINK PACK AREA  DK                  '
         WRITE '  QCB            ENQ/DEQ         QE,QD,QN            '
         WRITE '  RMCT           SRM             S                   '
         WRITE '                 HELP            H,HC,HM,HK          '
         WRITE '                 CONTROL         E,I,W               '
         PEXIT
         TITLE 'HK - TURN PFK DEFINITON MODE ON/OFF AND LIST'
HK       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'HK - TURN PROGRAM FUNCTION KEY (PFK)           '
               WRITE '     DEFINITION MODE ON OR OFF AND LIST        '
               WRITE '     CURRENT PFK DEFINITONS.  WHENEVER THE     '
               WRITE '     PFK MODE IS ON, THE NEXT PFK KEY ENTERED  '
               WRITE '     WILL BE REDEFINED AS THE LAST COMMAND     '
               WRITE '     THAT WAS ENTERED.                         '
               PEXIT
         FI
         IF   (CLI,PFKMODE,FALSE,EQ)
              WRITE 'PFK DEFINITION MODE ON'
              MVI  PFKMODE,TRUE
         ELSEIF (CLI,PFKMODE,PFKDONE,EQ)
              WRITE 'PFK DEFINITION MODE OFF'
              MVI  PFKMODE,FALSE
         FI
         WRITE ' '
         LA    R2,PFKTAB
         LA    R3,PFKTAB+L'PFKTAB
         ZAP   PWORK,=P'0'
         DODO  WHILE,(CLR,R2,R3,L)
               AP   PWORK,=P'1'
               EDIT TO=DPFKNO,FROM=PWORK+6,MASK=' Z99'
               MVC  DPFK,0(R2)
               LA   R0,PFKLINE
               PM   PUT
               LA   R2,L'PFKENT(R2)
         OD
         PEXIT
         TITLE 'HM - TURN HELP MODE ON/OFF'
HM       PENTRY
         IF   (CLI,HMODE,TRUE,EQ)
              WRITE 'HELP MODE OFF'
              MVI  HMODE,FALSE
         ELSE
              WRITE 'HELP MODE ON'
              MVI  HMODE,TRUE
         FI
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE '                                             '
               WRITE 'HM - TURN HELP MODE ON OR OFF                '
               WRITE '     WHEN THE HELP MODE IS ON, THE DETAIL    '
               WRITE '     HELP INFORMATION FOR EACH COMMAND WILL  '
               WRITE '     BE DISPLAYED INSTEAD OF EXECUTING THE   '
               WRITE '     COMMAND. HELP IN HELP MODE WILL LIST THE'
               WRITE '     COMMANDS AVAILABLE.                     '
               PEXIT
         FI
         PEXIT
         TITLE 'ENQDEQ - COMMANDS'
ENQDEQ   PENTRY
         DOCASE     LINE+1
               CASE C'D' DEQ
                    PM   QD
               ESAC
               CASE C'E' ENQ
                    PM   QE
               ESAC
               CASE C'N' DEFINE ENQ/DEQ NAME
                    PM   QN
               ESAC
         ESACOD
         PEXIT
         TITLE 'QD - DEQ NAME'
QD       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'QD - DEQUE RESOURCE DEFINED BY QN'
               PEXIT
         FI
         L     R2,RNAMELNG
         DEQ   (QNAME,RNAME,(R2),SYSTEM),MF=(E,DEQLIST)
         PEXIT
         TITLE 'QE - ENQ NAME'
QE       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'QE - ENQUE RESOURCE DEFINED BY QN'
               PEXIT
         FI
         L     R2,RNAMELNG
         ENQ   (QNAME,RNAME,E,(R2),SYSTEM),MF=(E,ENQLIST)
         PEXIT
         TITLE 'QN - DEFINE NAME'
QN       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'QN,QNAME,RNAME - DEFINE ENQUE/DEQUE RESOURCE'
               WRITE ' '
               WRITE 'QNAME - UP TO 8 CHARACTER MAJOR NAME'
               WRITE 'RNAME - UP TO 44 CHARACTER MINOR NAME'
               WRITE ' '
               WRITE 'THIS COMMAND DEFINES NAMES USED IN QE AND QD'
               PEXIT
         FI
         MVC   QNAME,=CL8' '
         MVC   RNAME,=CL44' '
         LA  R2,QNAME
         LA  R3,LINE+3
         DODO WHILE,(CLI,0(R3),C',',NE),AND,(CLI,0(R3),C' ',NE)
             MVC  0(1,R2),0(R3)
             AAI  R2,1
             AAI  R3,1
         OD
         IF  (CLI,0(R3),C',',NE)
             WRITE 'QN - INVALID SYNTAX QN,MAJ,MIN - RETRY'
             PEXIT
         FI
         AAI   R3,1
         LA    R2,RNAME
         ZR    R4
         DODO  WHILE,(CLI,0(R3),C' ',NE)
               MVC  0(1,R2),0(R3)
               AAI  R2,1
               AAI  R3,1
               AAI  R4,1
         OD
         ST    R4,RNAMELNG
         PEXIT
         TITLE 'SRM - DISPLAY SRM STATUS'
SRM      PENTRY
         WRITE 'SRM - SYSTEM RESOURCE MANAGER STATUS DISPLAY'
         WRITE ' '
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'UIC - UNREFERENCED INTERVAL COUNT FOR PAGES'
               WRITE 'CPU - CENTRAL PROCESSOR PERCENT UTILIZATION'
               WRITE 'PAG - PAGING RATE PER SECOND'
               WRITE 'ASM - AUXILIARY STORAGE MANAGER REQUEST QUEUE'
               WRITE ' '
               WRITE 'WHEN ANY OF THE ABOVE VARIABLES EXCEEDS THE'
               WRITE 'MAXIMUM, THE SRM SELECTS A TASK TO SWAP OUT'
               WRITE 'UNTIL THE ACTIVATING VARIABLE DROPS TO THE'
               WRITE 'LOW LIMIT AGAIN.'
               PEXIT
         FI
         L     R2,16
         L     R2,CVTOPCTP(R2)
         L     R2,RMCTRCT(R2)   NEW WITH MVS/SP3
         MVC   DSTYP,=C'UIC'
         LH    R0,RCCUICTL(R2)
         CVD   R0,PWORK
         EDIT  TO=DSLOW,FROM=PWORK+6,MASK=' Z99'
         LH    R0,RCCUICTH(R2)
         CVD   R0,PWORK
         EDIT  TO=DSHIGH,FROM=PWORK+6,MASK=' Z99'
         LH    R0,RCVUICA(R2)
         CVD   R0,PWORK
         EDIT  TO=DSAVG,FROM=PWORK+6,MASK=' Z99'
         LA    R0,SLINE1
         PM    PUT
         MVC   DSTYP,=C'CPU'
         LH    R0,RCCCPUTL(R2)
         SRL   R0,4
         CVD   R0,PWORK
         EDIT  TO=DSLOW,FROM=PWORK+6,MASK=' Z99'
         LH    R0,RCCCPUTH(R2)
         SRL   R0,4
         CVD   R0,PWORK
         EDIT  TO=DSHIGH,FROM=PWORK+6,MASK=' Z99'
         LH    R0,RCVCPUA(R2)
         SRL   R0,4
         CVD   R0,PWORK
         EDIT  TO=DSAVG,FROM=PWORK+6,MASK=' Z99'
         LA    R0,SLINE1
         PM    PUT
         MVC   DSTYP,=C'PTR'
         LH    R0,RCCPTRTL(R2)
         CVD   R0,PWORK
         EDIT  TO=DSLOW,FROM=PWORK+6,MASK=' Z99'
         LH    R0,RCCPTRTH(R2)
         CVD   R0,PWORK
         EDIT  TO=DSHIGH,FROM=PWORK+6,MASK=' Z99'
         LH    R0,RCVPTR(R2)
         CVD   R0,PWORK
         EDIT  TO=DSAVG,FROM=PWORK+6,MASK=' Z99'
         LA    R0,SLINE1
         PM    PUT
         MVC   DSTYP,=C'ASM'
         LH    R0,RCCASMTL(R2)
         CVD   R0,PWORK
         EDIT  TO=DSLOW,FROM=PWORK+6,MASK=' Z99'
         LH    R0,RCCASMTH(R2)
         CVD   R0,PWORK
         EDIT  TO=DSHIGH,FROM=PWORK+6,MASK=' Z99'
         LH    R0,RCVASMQA(R2)
         CVD   R0,PWORK
         EDIT  TO=DSAVG,FROM=PWORK+6,MASK=' Z99'
         LA    R0,SLINE1
         PM    PUT
         PEXIT
         TITLE 'XCOMINFO - DISPLAY XCOM CROSS MEMORY INFO'
XCOMINFO PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'XCOM - STATUS SUMMARY ON CROSS MEMORY'
               WRITE '       COMMUNICATIONS FACILITY'
               WRITE ' '
               WRITE 'THIS IS A USER INSTALLED FACILITY TO MESSAGE'
               WRITE 'SWITCH BETWEEN ADDRESS SPACES.'
               WRITE ' '
               WRITE 'RCUR  - CURRENT MESSAGES QUEUED IN CSA'
               WRITE 'RMAX  - MAXIMUM ALLOWED IN CSA'
               WRITE 'RTOT  - TOTAL MESSAGES SENT SINCE LAST IPL'
               WRITE ' '
               WRITE 'PID   - DESTINATION PATH NAME'
               WRITE 'TID   - SOURCE PATH NAME'
               WRITE 'ME#   - NUMBER OF MESSAGES FOR THIS PATH'
               PEXIT
         FI
*
*  SEE SR301000 - SR305000, CICS, AND GU640001 FOR MORE
*  INFORMATION ON XCOM.
*
         L     R2,16
         L     R2,CVTUSER(R2)
         IF    (LTR,R2,R2,Z)
               WRITE 'XCOM - NO USER CVT DEFINED'
               PEXIT
         FI
         L     R2,UCVTXCVT(R2)
         IF    (LTR,R2,R2,Z)
               WRITE 'XCOM - NO XCVT DEFINED'
               PEXIT
         FI
         L     R0,VTRCUR(R2)
         CVD   R0,PWORK
         EDIT  TO=DRCUR,FROM=PWORK+5,MASK=' ZZ,Z99'
         L     R0,VTRMAX(R2)
         CVD   R0,PWORK
         EDIT  TO=DRMAX,FROM=PWORK+5,MASK=' ZZ,Z99'
         L     R0,VTRTOT(R2)
         CVD   R0,PWORK
         EDIT  TO=DRTOT,FROM=PWORK+5,MASK=' ZZ,Z99'
         LA    R0,DXCOM1
         PM    PUT
         WRITE ' '
         L     R3,VTPCBH(R2)
         IF    (LTR,R3,R3,Z)
               WRITE 'XCOM - NO PATHS DEFINED AT THIS TIME'
         ELSE
               DODO  WHILE,(LTR,R3,R3,NZ)
                     MVC  DPATH,PCBPID(R3)
                     MVC  DPATHJOB,PCBJOBN(R3)
                     LA   R0,DXCOM2
                     PM   PUT
                     L    R3,PCBNEXT(R3)
               OD
         FI
         WRITE ' '
         L     R3,VTMHBH(R2)
         IF    (LTR,R3,R3,Z)
               WRITE 'XCOM - NO MESSAGES QUEUED AT THIS TIME'
         ELSE
               DODO  WHILE,(LTR,R3,R3,NZ)
                     MVC  DPID,MHBPID(R3)
                     IF   (CLC,MHBTID(4,R3),=4X'00',NE)
                          MVC  DTID,MHBTID(R3)
                     ELSE
                          MVC  DTID,=CL4'NONE'
                     FI
                     LH   R0,MHBME#(R3)
                     CVD  R0,PWORK
                     EDIT TO=DME#,FROM=PWORK+5,MASK=' ZZ,Z99'
                     LA   R0,DXCOM3
                     PM   PUT
                     L    R3,MHBNEXT(R3)
               OD
         FI
         WRITE ' '
         MVC   LINE(20),=CL20'DQ,XCOMPATH'
         PM    DQ
         PEXIT
         TITLE 'UNKNOWN -UNKNOWN REQUEST'
UNKNOWN PENTRY
         WRITE 'FOR HELP ENTER HELP OR PFK12'
         PEXIT
         TITLE 'DA - DISPLAY ACTIVE JOBS, USERS, AND SYSTEM TASKS'
DA       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DA - DISPLAY ALL ACTIVE TASKS, JOBS, AND USERS'
               PM    DAHELP
               PEXIT
         FI
         PM    INTERVAL
         LA    R0,DALINE0
         PM    PUT
         LA    R0,DALINE1
         PM    PUT
         LA    R0,DALINE2
         PM    PUT
         L     R2,16
         L     R2,CVTASCVT(R2)
         LA    R3,ASVTENTY(R2)
         L     R4,ASVTMAXU(R2)
         DODO  WHILE,(SH,R4,=H'1',NM)
               IF   (ICM,R5,15,0(R3),P)
                    IF   (ICM,R1,15,ASCBTSB(R5),NZ)
                         PM   DU1
                    ELSEIF (ICM,R1,15,ASCBJBNI(R5),NZ)
                         PM   DJ1
                    ELSE
                         PM   DS1
                    FI
                    IF   (TM,ASCBRCTF(R5),ASCBOUT,Z)
                         LA   R0,DALINE3
                         PM   PUT
                    FI
               FI
               AAI  R3,4
         OD
         PEXIT
         TITLE 'DAHELP - HELP INFO ON COMMON TASK, JOB, AND USER'
DAHELP   PENTRY
         WRITE ' '
         WRITE 'JOBNAME  - NAME OF JOB, TSO USER, OR STARTED TASK'
         WRITE 'STEPNAME - NAME OF JOB STEP, TSO, OR STC'
         WRITE 'DPG      - DOMAIN AND PERFORMANCE GROUP'
         WRITE 'CPU %    - PERCENT CPU UTILIZATION IN LAST INTERVAL'
         WRITE 'XIO/SEC  - RATE OF EXCP REQUESTS   IN LAST INTERVAL'
         WRITE 'PG/SEC   - RATE OF PAGE FAULTS     IN LAST INTERVAL'
         WRITE 'SVC TOT  - TOTAL MVS CALLS   IN LAST TRACE INTERVAL'
         WRITE 'SIO TOT  - TOTAL START I/O   IN LAST TRACE INTERVAL'
         WRITE 'PGM TOT  - TOTAL PAGE FAULTS IN LAST TRACE INTERVAL'
         WRITE 'INT TOT  - TOTAL INTERRUPTIONS  LAST TRACE INTERVAL'
         WRITE 'T-CPU    - TOTAL TCB AND SRB CPU'
         WRITE 'T-IO     - TOTAL EXCP COUNT FOR JOB OR TSO STEP'
         WRITE 'T-MEM    - TOTAL CURRENT REAL MEMORY IN USE'
         WRITE 'T-XEQ    - TOTAL ELAPSED JOB OR TSO EXECUTION TIME'
         PEXIT
         TITLE 'DB - DISPLAY BASIC SYSTEM STATUS'
DB       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DB - DISPLAY BASIC SYSTEM STATUS'
               WRITE ' '
               WRITE 'CURRENT TIME AND DATE HH.MM.SS.TH  YY.DDD'
               WRITE 'TIME AND DATE OF LAST IPL'
               WRITE ' '
               WRITE 'NUMBER OF ACTIVE TASKS AND DEVICES IN SYSTEM'
               WRITE ' '
               WRITE 'SYSTEM PAGING RATES DURING LAST INTERVAL'
               WRITE ' '
               WRITE 'CPU AND CHANNEL USAGE DURING LAST INTERVAL'
               WRITE ' '
               WRITE 'OUTSTANDING DISK AND TAPE MOUNTS IF ANY'
*****          WRITE 'TASKS WAITING ON ENQUEUED RESOURCES IF ANY'
               WRITE 'NUMBER OF OUTSTANDING OPERATIOR REPLIES'
               WRITE 'SYSTEM ERRORS IF ANY'
               WRITE 'ADDRESS SPACE ERRORS IF ANY'
               WRITE 'DEVICE ERRORS IF ANY'
               PEXIT
         FI
         PM    TIME
         WRITE ' '
         PM    DN
         WRITE ' '
         PM    DP
         WRITE ' '
         PM    DC
         WRITE ' '
         PM    DM
****     PM    DQ
****     PM    DR
         PM    DES
         PM    DEA
         PM    DEU
         PEXIT
         TITLE 'DC - DISPLAY CPU USAGE'
DC       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DC - DISPLAY CPU AND CHANNEL UTILIZATION'
               WRITE ' '
               WRITE 'CPU0 - PERCENT CPU UTILIZATION ON UP OR AP'
               WRITE ' '
               WRITE 'CPU1 - PERCENT CPU UTILIZATION ON MAIN CPU'
               WRITE '       OF AN AP SYSTEM'
               WRITE ' '
               WRITE 'CHANNEL START I/O COUNTS AND PERCENT BUSY'
               WRITE 'ARE OBTAINED FROM RMF CONTROL BLOCKS IF'
               WRITE 'RMF IS ACTIVE IN SYSTEM'
               WRITE ' '
               WRITE 'NOTE PERCENT CHANNEL BUSY IS BASED ON RMF'
               WRITE 'TEST CHANNEL SAMPLES DURING LAST INTERVAL'
               WRITE ' '
               WRITE 'BUSY IS NOT MEANINGFUL FOR MUX CHANNELS'
               PEXIT
         FI
         PM    INTERVAL
         PM    DC2
         PEXIT
DC1      PENTRY
         L     R2,16
         L     R3,CVTCSD(R2)
         L     R4,CVTLCCAT(R2)
         IF    (TM,CSDCPUAL(R3),X'80',O)
               L    R5,LCCAT00P(R4)
               LM   R0,R1,LCCAWTIM(R5)
               SRDL R0,12
               D    R0,=F'10000'
               LR   R6,R1            R6=ENDING WAIT TIME IN 0.01
               S    R6,DCSCPU0W      R6=INTERVAL WAIT TIME
               ST   R1,DCSCPU0W      SAVE ENDING WAIT TIME ACCUM.
               L    R1,INTBIN
               SR   R1,R6
               ST   R1,DCSCPU0I      SAVE INTERVAL CPU TIME
         FI
         IF    (TM,CSDCPUAL(R3),X'40',O)
               L    R5,LCCAT01P(R4)
               LM   R0,R1,LCCAWTIM(R5)
               SRDL R0,12
               D    R0,=F'10000'
               LR   R6,R1            R6=ENDING WAIT TIME IN 0.01
               S    R6,DCSCPU1W      R6=INTERVAL WAIT TIME
               ST   R1,DCSCPU1W      SAVE ENDING WAIT TIME ACCUM.
               L    R1,INTBIN
               SR   R1,R6
               ST   R1,DCSCPU1I      SAVE INTERVAL CPU TIME
         ELSEIF    (TM,CSDCPUAL(R3),X'20',O)
               L    R5,LCCAT02P(R4)
               LM   R0,R1,LCCAWTIM(R5)
               SRDL R0,12
               D    R0,=F'10000'
               LR   R6,R1            R6=ENDING WAIT TIME IN 0.01
               S    R6,DCSCPU1W      R6=INTERVAL WAIT TIME
               ST   R1,DCSCPU1W      SAVE ENDING WAIT TIME ACCUM.
               L    R1,INTBIN
               SR   R1,R6
               ST   R1,DCSCPU1I      SAVE INTERVAL CPU TIME
               MVI  DCPUID1,C'2'      CHANGE CPU ID FROM 1 TO 2
         FI
*
*        CHANNEL DATA
*
         L     R2,16
         IF    (ICM,R2,15,CVTMFCTL(R2),Z) MF OR RMF REQUIRED
               PEXIT
         FI
         L     R2,STGSMMV(R2)  R2= STMMV
*
*        NEXT 2 INSTRUCTIONS LOAD ECCEDT ADDR OUT OF COMMON
*        STMMV MFROUTER TABLE ENTRY FOR MF AND RMF BASED ON
*        MVS PUBLISHED DUMP WITH MF AND ANOTHER DUMP WITH RMF
*
*
         L     R2,8(R2)
         L     R2,4(R2)        R2= ECCEDT
*
*
         L     R3,ECCECPEQ(R2) R3= ECCPE
*
*        NEXT INSTRUCTIONS DETERMINE IF UP OR AP RUNNING
*        IF UP, CHANNELS ARE ON CPU 0
*        IF AP, CHANNELS ARE ON CPU 1
*
         L     R4,ECCPCDBQ(R3) R4=ECCDBT FOR CPU 0
         IF    (ICM,R0,15,ECCDSIOS(R4),Z)   IF AP, SKIP TO CPU 1
               LA   R3,LECCPE(R3)
         FI
*
*
         L     R0,ECCESAMP(R2) R2= TOTAL SAMPLES
         LR    R1,R0
         S     R1,DCNTSAMP
         AAI   R1,1
         STM   R0,R1,DCNTSAMP  SAVE INTERVAL SAMPLE INFO.
         L     R4,ECCPCDBQ(R3) R4= ECCDBT FOR THIS CPU
         LH    R5,ECCPCNUM(R3) R5= NUMBER CHANNELS ON THIS CPU
         IF    (LTR,R5,R5,NP),OR,(CL,R5,=F'16',H)
               WRITE 'MF/RMF CHANNEL DATA BAD - IGNORED'
               PEXIT
         FI
         ZR    R6              R6= CHANNEL NUMBER
         LA    R7,DCNCHANS
         DODO  WHILE,(LTR,R5,R5,NZ)
               IF   (ICM,R0,15,ECCDSIOS(R4),NZ)
                    LR   R1,R0
                    S    R1,0(R7)
                    STM  R0,R1,0(R7)
                    L    R0,ECCDBUSY(R4)
                    LR   R1,R0
                    S    R1,8(R7)
                    STM  R0,R1,8(R7)
               ELSE
                    XC   0(16,R7),0(R7)
               FI
               AAI  R6,1   INCR CHANNEL NUMBER
               AAI  R7,16  INCR SAVE  TABLE
               LA   R4,ECCDBL(R4)  INCR ECCDB TABLE
               SH   R5,=H'1'  DECREMENT TABLE ENTRY COUNT
         OD
         PEXIT
         TITLE 'DC2 - DISPLAY CPU AND CHANNEL INFO FOR INTERVAL'
DC2      PENTRY
         L    R1,DCSCPU0I
         MH   R1,=H'100'
         ZR   R0
         D    R0,INTBIN
         LR   R2,R1
         CVD  R1,PWORK
         EDIT TO=DCDCPU0,FROM=PWORK+6,MASK=' Z99'
         L    R1,DCSCPU1I
         MH   R1,=H'100'
         ZR   R0
         D    R0,INTBIN
         AR   R2,R1
         CVD  R1,PWORK
         EDIT TO=DCDCPU1,FROM=PWORK+6,MASK=' Z99'
         CVD  R2,PWORK
         EDIT TO=DCDCPUT,FROM=PWORK+6,MASK=' Z99'
         LA   R0,DCLINE
         PM   PUT
*
*        CHANNEL DATA
*
         L     R2,16
         IF    (ICM,R2,15,CVTMFCTL(R2),Z) MF OR RMF REQUIRED
               PEXIT
         FI
         ZR    R4              R4= CHANNEL NUMBER
         LA    R2,DCNCHANS
         LA    R3,DCNCHANS+L'DCNCHANS
         DODO  WHILE,(CLR,R2,R3,L)
               IF   (ICM,R1,15,0(R2),NZ)
                    L    R1,4(R2)
                    MH   R1,=H'100'
                    ZR   R0
                    D    R0,INTBIN   R1= SIOS/SEC
                    CVD  R1,PWORK
                    EDIT TO=DCSIOS,FROM=PWORK+6,MASK=' Z99'
                    L    R1,12(R2)
                    MH   R1,=H'100'
                    ZR   R0
                    D    R0,DCNTSAMP+4  PERCENT BUSY FOR INTERVAL
                    CVD  R1,PWORK
                    EDIT TO=DCBUSY,FROM=PWORK+6,MASK=' Z99'
                    CVD  R4,PWORK
                    EDIT TO=DCCHAN,FROM=PWORK+6,MASK=' Z99'
                    LA   R0,DCLINE2
                    PM   PUT
               FI
               AAI  R4,1       INCR CHANNEL NUMBER
               AAI  R2,16      INCR SAVE TABLE
         OD
         PEXIT
         TITLE 'DD - DISPLAY DISKS'
DD       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DD - DISPLAY ONLINE DISK STATUS'
               WRITE ' '
               WRITE 'UNIT ADDRESS'
               WRITE 'VOLUME NAME'
               WRITE ' '
               WRITE 'UNIT STATUS - R=RESV., A=ALLOC., P=PERMANENT'
               WRITE ' '
               WRITE 'VOL. STATUS - PV=PRIVATE'
               WRITE '              PB=PUBLIC'
               WRITE '              ST=STORAGE'
               WRITE ' '
               WRITE 'NUMBER OF OPEN FILES ON VOLUME'
               PEXIT
         FI
         L     R2,16
         L     R2,CVTILK2(R2)
         L     R7,DISIOTAB
         ZR    R3
         DODO  WHILE,(CLC,0(2,R2),=X'FFFF',NE)
               IF   (CLC,0(2,R2),=X'0000',NE)
                  ICM  R3,3,0(R2)
                  CVTUCB R3
                  IF   (CLM,R0,B'0011',4(R7),EQ) SKIP ALT.
                    AAI R7,6
                    IF   (TM,UCBSTAT(R3),X'80',O),                     X
               AND,(TM,UCBTYP+2(R3),X'20',O)
                            PM  DD1
                    FI
                  FI
               FI
               AAI  R2,2
         OD
         PEXIT
         TITLE 'DD1 LIST ONE DISK'
DD1      PENTRY
         MVC   DDUCB,UCBNAME(R3)
         MVC   DDVOL,UCBVOLI(R3)
         MVC   DDMSTAT,=CL3' '
         IF    (TM,UCBSTAT(R3),X'20',O) RESERVED
               MVI  DDMSTAT,C'R'
         FI
         IF    (TM,UCBSTAT(R3),X'08',O) ALLOCATED
               MVI  DDMSTAT+1,C'A'
         FI
         IF    (TM,UCBSTAT(R3),X'04',O) PERMANENTLY MOUNTED
               MVI  DDMSTAT+2,C'P'
         FI
         IF    (TM,UCBSTAB(R3),X'10',O) PRIVATE
               MVC  DDUSTAT,=C'PV'
         ELSE
               IF   (TM,UCBSTAB(R3),X'08',O) PUBLIC
                    MVC  DDUSTAT,=C'PB'
               ELSE
                    MVC  DDUSTAT,=C'ST'
               FI
         FI
         ZR    R0
         IC    R0,UCBDMCT(R3)
         CVD   R0,PWORK
         EDIT  TO=DDDCBS,FROM=PWORK+6,MASK=' Z99'
         LA    R0,DDLINE
         PM    PUT
         PEXIT
         TITLE 'TUCBCALC -CALCULATE TUCBNAME FROM TUCBCHAN'
TUCBCALC PENTRY
         ICM   R1,12,TUCBCHAN
         SLL   R1,4
         ZR    R0
         SLDL  R0,4   0X
         SLL   R0,4   0X0
         SLDL  R0,4   0X0Y
         SLL   R0,4   0X0Y0
         SLDL  R0,4   0X0Y0Z
         STCM  R0,7,TUCBNAME
         TR    TUCBNAME,HEX
         PEXIT
         TITLE 'DE - DISPLAY ERROR CONDITIONS'
DE       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DE - DISPLAY ERROR CONDITIONS'
               WRITE ' '
               WRITE 'ASCB MEMORY FAILURE'
               WRITE 'SQA STORAGE CRITICAL'
               WRITE 'WAITING FOR V=R REGION'
               WRITE 'SMF PRIMARY NOT AVAILABLE'
               WRITE 'SMF SECONDARY NOT AVAILABLE'
               WRITE 'SMF RECORDS LOST'
               WRITE 'MEMORY ERRORS HAVE FLAGGED BAD FRAMES'
               WRITE 'MEMORY FRAMES OFFLINE'
               WRITE 'DEVICE NOT READY'
               WRITE 'UNSOLICITED DEVICE END'
               WRITE 'DDR SWAP PENDING'
               WRITE ' '
               WRITE 'SIO TRACE ENTRIES WITH NON-ZERO CONDITION CODE'
               WRITE 'SVC TRACE ENTRIES FOR ABEND AND IO RETRY'
               WRITE 'PGM TRACE ENTRIES IF NOT PAGE FAULT'
               WRITE 'IO  TRACE ENTRIES FOR CHAN. AND DEVICE ERRORS'
               PEXIT
         FI
         PM    DV1
         PM    DES
         PM    DEA
         PM    DEU
         PM    DET
         PEXIT
         TITLE 'DEA - SCAN ASCBS FOR ERROR CONDITIONS'
DEA      PENTRY
         L     R2,16
         L     R2,CVTASCVT(R2)
         LA    R3,ASVTENTY(R2)
         L     R4,ASVTMAXU(R2)
         DODO  WHILE,(SH,R4,=H'1',NM)
               IF   (ICM,R5,15,0(R3),P)
                    PM   DEA1
               FI
               AAI  R3,4
         OD
         PEXIT
         TITLE 'DEA1 - CHECK ASCB FOR ERRORS'
DEA1     PENTRY
         IF    (TM,ASCBFLG1(R5),X'08',O)
               MVC  DEAMSG,=CL40'MEMORY TERMINATING ABNORMALLY'
               IF   (ICM,R15,15,ASCBJBNI(R5),Z)
                    L    R15,ASCBJBNS(R5)
               FI
               MVC  DEAJOB,0(R15)
               LA   R0,DELINE3
               PM   PUT
         FI
         IF    (TM,ASCBDSP1(R5),X'40',O)
               MVC  DEAMSG,=CL40'ASCB MEMORY FAILURE'
               IF   (ICM,R15,15,ASCBJBNI(R5),Z)
                    L    R15,ASCBJBNS(R5)
               FI
               MVC  DEAJOB,0(R15)
               LA   R0,DELINE3
               PM   PUT
         FI
         PEXIT
         TITLE 'DES - DISPLAY SYSTEM ERROR CONDITIONS'
DES      PENTRY
         L     R2,16
         L     R3,CVTGDA(R2)
         IF    (TM,GDAFLAGS(R3),X'06',NZ)
               WRITE 'SQA STORAGE CRITICAL'
         FI
         IF    (TM,GDAFLAGS(R3),X'02',O)
               WRITE 'WAITING FOR V=R REGION'
         FI
         IF    (ICM,R3,15,CVTSMCA(R2),NZ)
               IF   (ICM,R0,15,SMCADSCT(R3),NZ)
                    WRITE 'SMF RECORDS LOST'
               FI
               IF   (CLC,SMCANMSU(4,R3),=F'100',GT)
                    WRITE 'SMF BUFFER WAITS EXCEED 100'
               FI
         FI
         IF    (CLC,DVBBAD,=H'0',NE)
               WRITE 'MEMORY ERRORS HAVE FLAGGED BAD PAGES'
         FI
         IF    (CLC,DVBBAD,=H'0',NE)
               WRITE 'MEMORY PAGES OFFLINE'
         FI
         PEXIT
         TITLE 'DEU -  SCAN UCBS FOR ERROR CONDITIONS'
DEU      PENTRY
         L     R2,16
         L     R2,CVTILK2(R2)
         L     R7,DISIOTAB
         ZR    R3
         DODO  WHILE,(CLC,0(2,R2),=X'FFFF',NE)
               IF   (CLC,0(2,R2),=X'0000',NE)
                    ICM  R3,3,0(R2)
                    CVTUCB R3
                    IF   (CLM,R0,B'0011',4(R7),EQ) SKIP ALT.
                         AAI R7,6
                         PM   DEU1
                    FI
               FI
               AAI  R2,2
         OD
         PEXIT
         TITLE 'DEU1 CHECK A UCB FOR ERRORS'
DEU1     PENTRY
         IF    (TM,UCBSTAT(R3),X'88',NO) ONLINE AND ALLOCATED
               PEXIT
         FI
         IF    (TM,UCBFLA(R3),X'40',O)
               MVC  DESNS2,=CL40'DEVICE NOT READY'
               PM   DEU2
         FI
         IF    (TM,UCBFLC(R3),X'20',O)
               MVC  DESNS2,=CL40'UNSOLICITED DEVICE END'
               PM   DEU2
         FI
         IF    (TM,UCBFLC(R3),X'01',O)
               MVC  DESNS2,=CL40'DDR SWAP PENDING'
               PM   DEU2
         FI
         PEXIT
DEU2     PENTRY
         MVC   DEUCB2,UCBNAME(R3)
         LA    R0,DELINE2
         PM    PUT
         PEXIT
         TITLE 'DET - SCAN TRACE TABLE FOR ERRORS'
DET      PENTRY
         L     R2,FLCTRACE
         IF    (LTR,R2,R2,Z)
               PEXIT
         FI
         LM    R2,R4,TTETAB
         LR    R5,R4
         DODO  UNTIL,(CLR,R4,R5,EQ)
               LH   R1,TTEASID(R4)
               PM   GETJOB
               MVC  DZJOB,JOBNAME
               ZR   R1
               IC   R1,TTETYP(R4)
               SRL  R1,4
               LR   R15,R1
               MH   R15,=H'3'
               LA   R15,TTENTAB(R15)
               MVC  DZTYP,0(R15)
               UNPK DZPSW(9),TTEPSW(5,R4)
               UNPK DZPSW+8(9),TTEPSW+4(5,R4)
               MVI  DZPSW+16,C' '
               TR   DZPSW,HEX-240
               UNPK DZCSW(9),TTECSW(5,R4)
               UNPK DZCSW+8(9),TTECSW+4(5,R4)
               MVI  DZCSW+16,C' '
               TR   DZCSW,HEX-240
               UNPK DZREGS(9),TTEREGS(5,R4)
               UNPK DZREGS+8(9),TTEREGS+4(5,R4)
               MVI  DZREGS+16,C' '
               TR   DZREGS,HEX-240
               UNPK DZTCBTM(9),TTETCBTM(5,R4)
               MVC  PWORK(4),TTETCBTM+4(R4) MVC TO PREVENT 0C4 ON B5
               UNPK DZTCBTM+8(9),PWORK(5)
               MVI  DZTCBTM+16,C' '
               TR   DZTCBTM,HEX-240
               DOCASE (R1)
                 CASE 0 SIO
                   IF   (CLI,TTESIOCC(R4),0,NE)
                        LA   R0,DZLINE
                        PM   PUT
                   FI
                 ESAC
                 CASE 2 SVC
                   IF   (CLI,TTEPSW+3(R4),13,EQ),                      X
               OR,(CLI,TTEPSW+3(R4),15,EQ)
                        LA   R0,DZLINE
                        PM   PUT
                   FI
                 ESAC
                 CASE 3 PGM
                   IF   (CLI,TTEPSW+3(R4),17,NE)
                        LA   R0,DZLINE
                        PM   PUT
                   FI
                 ESAC
                 CASE 5 I/O
                   IF   (TM,TTEIOUS(R4),X'03',NZ),                     X
               OR,(TM,TTEIOCS(R4),X'3F',NZ)
                        LA   R0,DZLINE
                        PM   PUT
                   FI
                 ESAC
               ESACOD
               LA   R4,LTTE(R4)
               IF   (CLR,R4,R3,NL)
                    LR   R4,R2
               FI
         OD
         PEXIT
         TITLE 'DIXXX - DISPLAY SIO COUNTS FOR 16 UCBS STARTING AT XXX'
DI       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DI - DISPLAY START IO RATES FOR DEVICES'
               WRITE ' '
               WRITE 'DI,S=XXX,E=YYY,M=ZZ'
               WRITE ' '
               WRITE ' XXX - OPTIONAL STARTING ADDR. (DEF.=000)'
               WRITE ' YYY - OPTIONAL ENDING   ADDR. (DEF.=FFF)'
               WRITE ' ZZZ - OPTIONAL MINIMUM  SIOS  (DEF.=10)'
               WRITE ' '
               WRITE 'DEVICE ADDRESS'
               WRITE 'VOLUME IF ANY'
               WRITE 'DEVICE TYPE'
               WRITE 'SIO COUNT DURING LAST INTERVAL'
               WRITE 'JOBNAME OF LAST USER IF ALLOCATED'
               WRITE 'START IO PER SECOND DURING LAST INTERVAL'
               PEXIT
         FI
         PM    INTERVAL
         WRITE ' '
         WRITE ' '
         IF    (CLC,LINE+2(3),=C',S=',EQ)
               MVC  DISUCB,LINE+5
               IF   (CLC,LINE+8(3),=C',E=',EQ)
                    MVC  DIEUCB,LINE+11
                    IF   (CLC,LINE+14(3),=C',M=',EQ)
                         MVC  DIMSIOS,LINE+17
                    FI
               ELSEIF (CLC,LINE+8(3),=C',M=',EQ)
                    MVC  DIMSIOS,LINE+11
               FI
         ELSEIF (CLC,LINE+2(3),=C',E=',EQ)
               MVC  DIEUCB,LINE+5
               IF   (CLC,LINE+8(3),=C',M=',EQ)
                    MVC  DIMSIOS,LINE+11
               FI
         ELSEIF (CLC,LINE+2(3),=C',M=',EQ)
               MVC  DIMSIOS,LINE+5
         FI
         LA    R0,DILINE1
         PM    PUT
         WRITE ' '
         MVC   DISCHN,DISUCB
         MVC   DIECHN,DIEUCB
         TR    DISCHN,EBCBIN
         TR    DIECHN,EBCBIN
         PACK  PWORK,DIMSIOS
         CVB   R4,PWORK       R4= MINIMUM SIOS TO DISPLAY
         L     R5,DISIOTAB    R5= SIO START/END INTERVAL COUNT TABLE
         ZR    R6             R6= TOTAL SIO COUNT FOR RANGE
         L     R2,16
         L     R2,CVTILK2(R2)
         ZR    R3
         DODO  WHILE,(CLC,0(2,R2),=X'FFFF',NE)
               IF   (CLC,0(2,R2),=X'0000',NE)
                    ICM  R3,3,0(R2)
                    CVTUCB R3
                    IF   (CLM,R0,B'0011',4(R5),EQ) SKIP ALT. UCBS
                         PM  DI2
                         AAI R5,6
                    FI
               FI
               AAI  R2,2
         OD
         ZR    R0
         LR    R1,R6
         CVD   R1,PWORK
         EDIT  TO=DIRSIO,FROM=PWORK+4,MASK=' Z,ZZZ,Z99'
         MH    R1,=H'100'
         D     R0,INTBIN
         CVD   R1,PWORK
         EDIT  TO=DIRSIOS,FROM=PWORK+6,MASK=' Z99'
         WRITE ' '
         LA    R0,DILINE3
         PM    PUT
         PEXIT
         TITLE 'DI2 - DISPLAY SIO INFO FOR CURRENT UCB'
DI2      PENTRY
         MVC   DICHN,UCBNAME(R3)
         TR    DICHN,EBCBIN
         IF    (CLC,DISCHN,DICHN,H),OR,(CLC,DIECHN,DICHN,L)
               PEXIT
         FI
         ZR    R0
         ZR    R1
         ICM   R0,3,0(R5)
         ICM   R1,3,2(R5)
         IF    (SR,R1,R0,M)
               A    R1,=F'65536'
         FI
         AR    R6,R1          ACCUM. TOTAL SIO FOR RANGE
         CVD   R1,PWORK
         MH    R1,=H'100'
         ZR    R0
         D     R0,INTBIN
         IF    (CLR,R1,R4,L)
               PEXIT
         FI
         EDIT  TO=DISIO,FROM=PWORK+5,MASK=' ZZ,Z99'
         CVD   R1,PWORK
         EDIT  TO=DISIOS,FROM=PWORK+6,MASK=' Z99'
         MVC   DIUCB,UCBNAME(R3)
         IF    (TM,UCBTYP+2(R3),X'80'+X'20',NZ)
               MVC  DIVOL,UCBVOLI(R3)
               OC   DIVOL,=CL6' '
         ELSEIF (TM,UCBTYP+2(R3),X'40',NZ)
               MVC  DIVOL,=CL6'TP'
         ELSEIF (TM,UCBTYP+2(R3),X'10',NZ)
               MVC  DIVOL,=CL6'DISPLY'
         ELSEIF (TM,UCBTYP+2(R3),X'08',NZ)
               MVC  DIVOL,=CL6'U/R'
         ELSE
               MVC  DIVOL,=CL6'OTHER'
         FI
         IF    (TM,UCBSTAT(R3),X'88',O)   ONLINE AND ALLOCATED
               L     R1,UCBEXTPT(R3)
               LH    R1,UCBASID(R1)
               PM    GETJOB
               MVC   DIJOB,JOBNAME
         ELSE
               MVC   DIJOB,=CL8' '
         FI
         LA    R0,DILINE2
         PM    PUT
         PEXIT
         TITLE 'DJ - DISPLAY JOBS ONLY'
DJ       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DJ - DISPLAY ACTIVE BATCH JOBS'
               PM   DAHELP
               PEXIT
         FI
         PM    INTERVAL
         LA    R0,DALINE0
         PM    PUT
         LA    R0,DALINE1
         PM    PUT
         LA    R0,DALINE2
         PM    PUT
         L     R2,16
         L     R2,CVTASCVT(R2)
         LA    R3,ASVTENTY(R2)
         L     R4,ASVTMAXU(R2)
         DODO  WHILE,(SH,R4,=H'1',NM)
               IF   (ICM,R5,15,0(R3),P)
                    IF   (ICM,R1,15,ASCBJBNI(R5),NZ)
                         PM   DJ1
                         LA   R0,DALINE3
                         PM   PUT
                     FI
               FI
               AAI  R3,4
         OD
         PEXIT
         TITLE 'DJ1 - PROCESS ONE ASCB ENTRY FOR DJ'
DJ1      PENTRY
         MVC   DAJOB,0(R1)
         MVC   DASTEP,=CL8' '
         L     R1,ASCBCSCB(R5)
         DODO  WHILE,(LTR,R1,R1,NZ)
               IF   (CLC,DAJOB,CSCBJOB(R1),EQ)
                    MVC  DASTEP,CSCBSTEP(R1)
               FI
               L    R1,CSCBPTR(R1)
         OD
         PM    DA1
         PEXIT
         TITLE 'DA1 - DISPLAY INFO FROM ASCB FOR ALL DJ, DU, DS'
DA1      PENTRY
         L     R1,ASCBOUCB(R5)
         IF    (TM,ASCBRCTF(R5),ASCBOUT,O)
               MVC  DADPG+1(3),=C'SWP'      SWAPPED OUT
         ELSE
               ZR    R0
               IC    R0,ASCBDP(R5)
               CVD   R0,PWORK
               EDIT  TO=DADPG,FROM=PWORK+6,MASK=' Z99'
         FI
         LH   R0,OUCBSWC(R1)
         IF   (CH,R0,=H'999',GT)
              MVC DASWP+1(3),=C'***'
         ELSE
              CVD R0,PWORK
              EDIT TO=DASWP,FROM=PWORK+6,MASK=' Z99'
         FI
         LH    R1,ASCBASID(R5)
         MH    R1,=AL2(L'RCT)
         SH    R1,=AL2(L'RCT)
         L     R14,RCTABPTR
         LA    R14,0(R1,R14)
         L     R1,RCTICPU-RCT(R14)
         MH    R1,=H'100'
         ZR    R0
         D     R0,INTBIN
         CVD   R1,PWORK
         EDIT  TO=DARCPU,FROM=PWORK+6,MASK=' Z99'
         L     R1,RCTIIO-RCT(R14)
         MH    R1,=H'100'
         ZR    R0
         D     R0,INTBIN
         CVD   R1,PWORK
         EDIT  TO=DARIO,FROM=PWORK+6,MASK=' Z99'
         L     R1,RCTIPG-RCT(R14)
         MH    R1,=H'100'
         ZR    R0
         D     R0,INTBIN
         CVD   R1,PWORK
         EDIT  TO=DARPG,FROM=PWORK+6,MASK=' Z99'
         ZR    R1
         IC    R1,RCTCSVC-RCT(R14)
         CVD   R1,PWORK
         EDIT  TO=DARSVC,FROM=PWORK+6,MASK=' Z99'
         ZR    R1
         IC    R1,RCTCSIO-RCT(R14)
         CVD   R1,PWORK
         EDIT  TO=DARSIO,FROM=PWORK+6,MASK=' Z99'
         ZR    R1
         IC    R1,RCTCPGM-RCT(R14)
         ZR    R0
         IC    R0,RCTCEXT-RCT(R14)
         AR    R1,R0
         ZR    R0
         IC    R0,RCTCIO-RCT(R14)
         AR    R1,R0                R1 = PGM + EXT + IO INT
         CVD   R1,PWORK
         EDIT  TO=DARINT,FROM=PWORK+6,MASK=' Z99'
         L     R1,RCTTCPU-RCT(R14)
         CVD   R1,PWORK
         EDIT  TO=DATCPU,FROM=PWORK+4,MASK=' ZZZZ9.99'
         L     R1,RCTTIO-RCT(R14)
         CVD   R1,PWORK
         EDIT  TO=DATIO,FROM=PWORK+4,MASK=' ZZZZZ99'
         L     R1,ASCBOUCB(R5)
         LH    R0,OUCBWSS(R1)
         IF    (CH,R0,ASCBFMCT(R5),L)
               LH   R0,ASCBFMCT(R5)
         FI
         SLL   R0,2
         CVD   R0,PWORK
         EDIT  TO=DATMEM,FROM=PWORK+5,MASK=' ZZZ99'
         TIME  STCK,PWORK
         LM    R0,R1,PWORK
         SRDL  R0,22               MICRO-SEC X 2*12 TO 1.024 MILLI-SEC
         L     R14,16
         L     R14,CVTOPCTP(R14)
         AL    R1,RMCTTBS(R14)     ADD SRM TOD BASE
         L     R14,ASCBOUCB(R5)
         SL    R1,OUCBTMO(R14)     SUBTRACT SRM TRANS. START TIME
         ZR    R0
         SLDL  R0,10               1.024 MILL-SEC TO MICRO-SECONDS
         D     R0,=F'60000000'     MICRO-SECONDS TO MINUTES
         CVD   R1,PWORK
         EDIT  TO=DATXEQ,FROM=PWORK+5,MASK=' ZZZ99'
         PEXIT
         TITLE 'DL - DISPLAY COMMUNICATION LINES STATUS'
DL       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DL - DISPLAY ALL TP LINE ERRORS'
               WRITE ' '
               WRITE 'LINE ADDRESS'
               WRITE ' '
               WRITE 'NOT READY'
               WRITE 'INTERCEPT'
               WRITE 'NO I/O ACTIVE'
               PEXIT
         FI
         L     R2,16
         L     R2,CVTILK2(R2)
         L     R7,DISIOTAB
         ZR    R3
         DODO  WHILE,(CLC,0(2,R2),=X'FFFF',NE)
               IF   (CLC,0(2,R2),=X'0000',NE)
                  ICM  R3,3,0(R2)
                  CVTUCB R3
                  IF   (CLM,R0,B'0011',4(R7),EQ) SKIP ALT.
                    AAI  R7,6
                    IF   (TM,UCBTYP+2(R3),X'40',O)
                         IF   (TM,UCBSTAT(R3),X'88',O)
                              PM   DL1
                         FI
                    FI
                  FI
               FI
               AAI  R2,2
         OD
         PEXIT
         TITLE 'DL1 - DISPLAY A LINE'
DL1      PENTRY
         IF    (TM,UCBFLA(R3),X'40',O)
               MVC  DLMSG,=CL20'NOT READY'
               PM   DL2
         ELSEIF (TM,UCBFLC(R3),X'10',O)
               MVC  DLMSG,=CL20'INTERCEPT'
               PM   DL2
         ELSEIF (TM,UCBFLA(R3),X'02',Z)
               MVC  DLMSG,=CL20'NO I/O ACTIVE'
               PM   DL2
         FI
         PEXIT
         TITLE 'DL2 - DISPLAY MSG FOR LINE'
DL2      PENTRY
         MVC   DLUCB,UCBNAME(R3)
         LA    R0,DLLINE1
         PM    PUT
         PEXIT
         TITLE 'DK - DISPLAY ACTIVE LPA MODULES AND USAGE'
DK       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DK - DISPLAY ACTIVE LINK PACK AREA PROGRAMS'
               WRITE ' '
               WRITE 'MODULE NAME'
               WRITE ' '
               WRITE 'CURRENT USE COUNT'
               PEXIT
         FI
         L     R2,16
         L     R2,CVTQLPAQ(R2)
         L     R2,CDCHAIN(R2)
         DODO  WHILE,(LTR,R2,R2,NZ)
               MVC   DKNAME,CDNAME(R2)
               LH    R0,CDUSE(R2)
               IF    (CH,R0,=H'0',GT)
                     CVD   R0,PWORK
                     EDIT  TO=DKUSE,FROM=PWORK+6,MASK=' Z99'
                     LA    R0,DKLINE
                     PM    PUT
               FI
               L     R2,CDCHAIN(R2)
         OD
         PEXIT
         TITLE 'DM - DISPLAY MOUNTS'
DM       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DM - DISPLAY CURRENT OUTSTANDING MOUNTS'
               WRITE ' '
               WRITE 'DEVICE ADDRESS'
               WRITE 'VOLUME REQUIRED'
               PEXIT
         FI
         L     R2,16
         L     R2,CVTILK2(R2)
         L     R7,DISIOTAB
         ZR    R3
         DODO  WHILE,(CLC,0(2,R2),=X'FFFF',NE)
               IF   (CLC,0(2,R2),=X'0000',NE)
                    ICM  R3,3,0(R2)
                    CVTUCB R3
                  IF     (CLM,R0,B'0011',4(R7),EQ) SKIP ALT.
                    AAI R7,6
                    IF   (TM,UCBTYP+2(R3),X'FF'-X'80'-X'20',Z)
                         IF   (TM,UCBDMCT(R3),X'80',O)
                              IF   (TM,UCBFLA(R3),X'40',O),            X
               OR,(TM,UCBTYP+2(R3),X'20',O)
                                   PM   DM1
                              FI
                         FI
                    FI
                  FI
               FI
               AAI  R2,2
         OD
         PEXIT
         TITLE 'DM1 DISPLAY MOUNT'
DM1      PENTRY
         MVC   DMUCB,UCBNAME(R3)
         MVC   DMVOL,UCBVOLI(R3)
         LA    R1,DMVOL
         PM    CHKVOL
         LA    R0,DMLINE
         PM    PUT
         PEXIT
         TITLE 'CHKVOL - EDIT UCB VOLUME FOR POSSIBLE SCRTCH'
CHKVOL   PENTRY
         IF    (CLI,0(R1),C'A',LT),OR,(CLI,0(R1),C'9',H)
               MVC  0(6,R1),=CL6'SCRTCH'
         FI
         PEXIT
         TITLE 'DN - DISPLAY NUMBERS OF THINGS'
DN       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DN - DISPLAY NUMBER OF TASKS AND DEVICES'
               WRITE ' '
               WRITE 'NUMBER OF ADDRESS SPACES DEFINED'
               WRITE 'NUMBER OF TASKS READY TO RUN'
               WRITE 'NUMBER OF BATCH JOBS RUNNING'
               WRITE 'NUMBER OF TSO USERS LOGGED ON'
               WRITE 'NUMBER OF SYSTEM TASKS RUNNING'
               WRITE ' '
               WRITE 'NUMBER OF UNIT ADDRESSES DEFINED'
               WRITE 'NUMBER OF ONLINE ALLOCATED DISKS'
               WRITE 'NUMBER OF ONLINE ALLOCATED TAPES'
               WRITE 'NUMBER OF ONLINE ALLOCATED TP PORTS'
               WRITE 'NUMBER OF ONLINE ALLOCATED UNIT RECORD DEVICES'
               WRITE 'NUMBER OF ONLINE ALLOCATED OTHER DEVICES'
         FI
         PM    DNASCB
         PM    DNUCB
         PEXIT
         TITLE 'DNASCB - DISPLAY NUMBER OF ASCBS, READY TCBS'
DNASCB   PENTRY
         MVC   DNFRTCB,=F'0'
         ZAP   DNPASCB,=P'0'
         ZAP   DNPJOB,=P'0'
         ZAP   DNPTSO,=P'0'
         ZAP   DNPSTC,=P'0'
         L     R2,16
         L     R2,CVTASCVT(R2)
         LA    R3,ASVTENTY(R2)
         L     R4,ASVTMAXU(R2)
         DODO  WHILE,(SH,R4,=H'1',NM)
               IF   (ICM,R5,15,0(R3),P)
                    AP   DNPASCB,=P'1'
                    L    R0,DNFRTCB
                    A    R0,ASCBTCBS(R5)
                    ST   R0,DNFRTCB
                    IF   (ICM,R1,15,ASCBJBNI(R5),NZ)
                         AP   DNPJOB,=P'1'
                    ELSEIF (ICM,R1,15,ASCBTSB(R5),NZ)
                         AP   DNPTSO,=P'1'
                    ELSE
                         AP   DNPSTC,=P'1'
                    FI
               FI
               AAI  R3,4
         OD
         EDIT  TO=DNDASCB,FROM=DNPASCB,MASK=' Z99'
         L     R0,DNFRTCB
         CVD   R0,PWORK
         EDIT  TO=DNRTCB,FROM=PWORK+6,MASK=' Z99'
         EDIT  TO=DNJOB,FROM=DNPJOB,MASK=' Z99'
         EDIT  TO=DNTSO,FROM=DNPTSO,MASK=' Z99'
         EDIT  TO=DNSTC,FROM=DNPSTC,MASK=' Z99'
         LA    R0,DNLINE1
         PM    PUT
         PEXIT
         TITLE 'DNUCB  - DISPLAY NUMBER OF UCBS'
DNUCB    PENTRY
         ZAP   DNPUCB,=P'0'
         ZAP   DNPDISK,=P'0'
         ZAP   DNPTAPE,=P'0'
         ZAP   DNPPORT,=P'0'
         ZAP   DNPUR,=P'0'
         ZAP   DNPOTHER,=P'0'
         L     R2,16
         L     R2,CVTILK2(R2)
         L     R7,DISIOTAB
         ZR    R3
         DODO  WHILE,(CLC,0(2,R2),=X'FFFF',NE)
               IF   (CLC,0(2,R2),=X'0000',NE)
                    ICM  R3,3,0(R2)
                    CVTUCB R3
                    IF   (CLM,R0,B'0011',4(R7),EQ) SKIP ALT.
                         AAI R7,6
                         IF   (TM,UCBSTAT(R3),X'88',O)
                              PM   DNUCB1
                         FI
                    FI
               FI
               AAI  R2,2
         OD
         EDIT  TO=DNDUCB,FROM=DNPUCB,MASK=' Z99'
         EDIT  TO=DNDISK,FROM=DNPDISK,MASK=' Z99'
         EDIT  TO=DNTAPE,FROM=DNPTAPE,MASK=' Z99'
         EDIT  TO=DNPORT,FROM=DNPPORT,MASK=' Z99'
         EDIT  TO=DNUR,FROM=DNPUR,MASK=' Z99'
         EDIT  TO=DNOTHER,FROM=DNPOTHER,MASK=' Z99'
         LA    R0,DNLINE2
         PM    PUT
         PEXIT
         TITLE 'DNUCB1 - PROCESS ONLINE UCBS'
DNUCB1   PENTRY
         AP    DNPUCB,=P'1'
         IF    (TM,UCBTYP+2(R3),X'40',O)
               AP   DNPPORT,=P'1'
         ELSEIF (TM,UCBTYP+2(R3),X'20',O)
               AP   DNPDISK,=P'1'
         ELSEIF (TM,UCBTYP+2(R3),X'80',O)
               AP   DNPTAPE,=P'1'
         ELSEIF (TM,UCBTYP+2(R3),X'08',O)
               AP   DNPUR,=P'1'
         ELSE
               AP   DNPOTHER,=P'1'
         FI
         PEXIT
         TITLE 'DP - DISPLAY SYSTEM PAGING'
DP       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DP - DISPLAY SYSTEM PAGING STATUS'
               WRITE ' '
               WRITE 'DEMAND PAGING RATE PER SECOND'
               WRITE ' '
               WRITE 'VIRTUAL IO PAGING RATE PER SECOND'
               WRITE ' '
               WRITE 'SWAP PAGING RATE PER SECOND'
               PEXIT
         FI
         PM    INTERVAL
         PM    DP2
         PEXIT
         TITLE 'DP1 - SAVE INTERVAL PAGING DATA AND XCOM DATA'
DP1      PENTRY
         L     R2,16
         L     R3,CVTPVTP(R2)
         L    R0,PVTNPI(R3)
         A    R0,PVTNPOUT(R3)
         LR   R1,R0
         S    R1,DPSPAGE
         STM  R0,R1,DPSPAGE
         L    R0,PVTVAMI(R3)
         A    R0,PVTVAMO(R3)
         LR   R1,R0
         S    R1,DPSVIO
         STM  R0,R1,DPSVIO
         L    R0,PVTSPIN(R3)
         A    R0,PVTSPOUT(R3)
         LR   R1,R0
         S    R1,DPSSWAP
         STM  R0,R1,DPSSWAP
         L    R2,16
         L    R2,CVTUSER(R2)
         IF   (LTR,R2,R2,Z)
              PEXIT
         FI
         L    R2,UCVTXCVT(R2)
         IF   (LTR,R2,R2,Z)
              PEXIT
         FI
         L    R0,VTRTOT(R2)
         LR   R1,R0
         S    R1,XCOMRATE
         STM  R0,R1,XCOMRATE
         PEXIT
         TITLE 'DP2 - PRINT PAGING INFO FOR INTERVAL'
DP2      PENTRY
         L    R1,DPSPAGE+4
         MH   R1,=H'100'
         ZR   R0
         D    R0,INTBIN
         CVD  R1,PWORK
         EDIT TO=DPDPAGE,FROM=PWORK+6,MASK=' Z99'
         L    R1,DPSVIO+4
         MH   R1,=H'100'
         ZR   R0
         D    R0,INTBIN
         CVD  R1,PWORK
         EDIT TO=DPDVIO,FROM=PWORK+6,MASK=' Z99'
         L    R1,DPSSWAP+4
         MH   R1,=H'100'
         ZR   R0
         D    R0,INTBIN
         CVD  R1,PWORK
         EDIT TO=DPDSWAP,FROM=PWORK+6,MASK=' Z99'
         L    R1,XCOMRATE+4
         MH   R1,=H'100'
         ZR   R0
         D    R0,INTBIN
         CVD  R1,PWORK
         EDIT TO=DPDXCOM,FROM=PWORK+6,MASK=' Z99'
         LA   R0,DPLINE
         PM   PUT
         PEXIT
         TITLE 'DQ - DISPLAY JOBS AND USERS WAITING FOR ENQ'
DQ       PENTRY
         WRITE 'DQ - NOT CURRENTLY AVAILABLE UNDER MVS/SP 1.3'
         PEXIT
*
*        SUPPORT REMOVED SINCE ALL QCBS NOW IN PROTECTED GRS
*        ADDRESS SPACE.  FUNCTION WOULD REQUIRE SYSTEM AUTH.
*
*
*
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DQ - DISPLAY ENQUED RESOURCE AND OWNER/REQUESTOR'
               WRITE ' '
               WRITE 'JOBNAME OF REQUESTOR'
               WRITE 'JOBNAME OF OWNER'
               WRITE 'TYPE OF REQUEST'
               WRITE 'MAJOR RESOURCE NAME'
               WRITE 'MINOR RESOURCE NAME'
               WRITE ' '
               WRITE 'DQ,ALL WILL LIST ALL OWNERS'
               WRITE 'DQ,SYS WILL LIST ALL OWNERS EXCEPT FILES + USERS'
               WRITE 'DQ,X   WILL LIST ALL OWNERS WITH X'
               WRITE 'DQ,X,Y WILL LIST ALL RESOURCES WHICH MATCH X,Y'
               WRITE ' '
               WRITE ' X - UP TO 8 CHARACTER MAJOR RESOURCE NAME'
               WRITE ' Y - UP TO 44 CHARACTER MINOR RESOURCE NAME'
               PEXIT
         FI
         PM    DQPARM
         L     R2,16
         L     R2,CVTFQCB(R2)     FIRST MAJOR QCB
         USING QCBMAJ,R2
         DODO  WHILE,(LTR,R2,R2,NZ)
               L    R3,QMJMINOR
               USING QCBMIN,R3
               DODO WHILE,(LTR,R3,R3,NZ)
                    L    R4,QMNQEL
                    USING QEL,R4
                    MVC  DQ2OWNER,=CL5'OWNER'
                    IF   (CLI,DQMODE,DQWAIT,NE)
                         PM   DQCHK
                         IF   (LTR,R15,R15,Z)
                              PM   DQ2
                         FI
                    FI
                    MVC  DQ2OWNER,=CL5'REQ.'
                    LR   R5,R4     SAVE QEL OF OWNER
                    IF   (TM,QELQFLGS,QELSHAR,O)
                         MVI  TYPE,SHARE
                    ELSE
                         MVI  TYPE,EXCLUDE
                    FI
                    L    R4,QELNQEL
                    ZHB  R4
                    DODO WHILE,(LTR,R4,R4,NZ)
                         IF   (CLI,DQMODE,DQWAIT,NE)
                              PM   DQCHK
                              IF   (LTR,R15,R15,Z)
                                   PM   DQ2
                              FI
                         FI
                         IF   (TM,QELQFLGS,QELSHAR,Z)
                              MVI  TYPE,EXCLUDE
                         FI
                         IF   (CLI,TYPE,EXCLUDE,EQ),                   X
               AND,(CLI,DQMODE,DQWAIT,EQ)
                              PM   DQ1
                         FI
                         L    R4,QELNQEL
                         ZHB  R4
                    OD
                    L    R3,QMNNEXT
               OD
               L    R2,QMJNEXT
         OD
         PEXIT
         TITLE 'DQ1 - DISPLAY INFO FOR WAITING ASID'
DQ1      PENTRY
*              R2   QCBMAJ
*              R3   QCBMIN
*              R4   QEL FOR WAITING TASK
*              R5   QEL FOR OWNER TASK
*
         LH    R1,QELASID-QEL(R4)
         PM    GETJOB
         MVC   DQ1WJOB,JOBNAME
         LH    R1,QELASID-QEL(R5)
         PM    GETJOB
         MVC   DQ1OJOB,JOBNAME
         MVC   DQ1QNAME,QMJNAME
         ZR    R1
         IC    R1,QMNLNM
         LA    R15,L'DQ1RNAME
         LA    R14,DQ1RNAME
         LA    R0,QMNNAME
         ICM   R1,8,=C' '
         MVCL  R14,R0
         LA    R0,DQLINE1
         PM    PUT
         PEXIT
         TITLE 'DQ2 - DISPLAY INFO FOR REQUESTED QCBS'
DQ2      PENTRY
*              R2   QCBMAJ
*              R3   QCBMIN
*              R4   QEL
*
         LH    R1,QELASID-QEL(R4)
         PM    GETJOB
         MVC   DQ2JOB,JOBNAME
         MVC   DQ2QNAME,QMJNAME
         ZR    R1
         IC    R1,QMNLNM
         LA    R15,L'DQ2RNAME
         LA    R14,DQ2RNAME
         LA    R0,QMNNAME
         ICM   R1,8,=C' '
         MVCL  R14,R0
         IF    (TM,QELQFLGS,QELSHAR,O)
               MVC  DQ2TYPE,=CL9'SHARED'
         ELSE
               MVC  DQ2TYPE,=CL9'EXCLUSIVE'
         FI
         LA    R0,DQLINE2
         PM    PUT
         PEXIT
         TITLE 'DQPARM - PROCESS DQ PARMS AND SET MODE'
DQPARM   PENTRY
         IF    (CLI,LINE+2,C' ',EQ)           DQ     - LIST ENQ WAITS
               MVI  DQMODE,DQWAIT
               PEXIT
         ELSEIF (CLC,LINE+2(5),=C',ALL ',EQ)  DQ,SYS - LIST ALL
               MVI  DQMODE,DQALL
               PEXIT
         ELSEIF (CLC,LINE+2(5),=C',SYS ',EQ)  DQ,SYS - LIST NON-DSN
               MVI  DQMODE,DQSYS
               PEXIT
         ELSE
               MVI  DQMODE,DQXY               DQ,X,Y - LIST MATCHING
         FI
         LA    R1,LINE+3
         LA    R0,L'DQMAJ(R1)
         LA    R14,DQMAJ
         ZR    R15
         DODO  WHILE,(CLR,R1,R0,L),                                    X
               AND,(CLI,0(R1),C' ',NE),AND,(CLI,0(R1),C',',NE)
               MVC  0(1,R14),0(R1)
               AAI  R1,1
               AAI  R14,1
               AAI  R15,1
         OD
         LA    R14,DQMAJ
         STM   R14,R15,DQMAJCL
         IF    (CLI,0(R1),C',',NE)
               ZR   R14
               ZR   R15
               STM  R14,R15,DQMINCL
               PEXIT
         FI
         AAI   R1,1
         LA    R0,L'DQMIN(R1)
         LA    R14,DQMIN
         ZR    R15
         DODO  WHILE,(CLR,R1,R0,L),                                    X
               AND,(CLI,0(R1),C' ',NE),AND,(CLI,0(R1),C',',NE)
               MVC  0(1,R14),0(R1)
               AAI  R1,1
               AAI  R14,1
               AAI  R15,1
         OD
         LA    R14,DQMIN
         STM   R14,R15,DQMINCL
         PEXIT
         TITLE 'DQCHK  - CHECK FOR MATCHING QCB'
DQCHK    PENTRY
         IF     (CLI,DQMODE,DQALL,EQ)                 DQ,ALL
                ZR   R15
         ELSEIF (CLI,DQMODE,DQSYS,EQ)                 DQ,SYS
                IF   (CLC,QMJNAME(6),=CL6'SYSDSN',NE),                 X
               AND,(CLC,QMJNAME(7),=CL7'SYSVSAM',NE),                  X
               AND,(CLC,QMJNAME(8),=CL8'SYSIKJUA',NE)
                     ZR   R15
                ELSE
                     LA   R15,1
                FI
         ELSE
               LM   R0,R1,DQMAJCL                     DQ,X,Y
               LA   R14,QMJNAME
               LA   R15,L'QMJNAME
               IF   (CLR,R1,R15,LT)
                    LR   R15,R1
               ELSE
                    LR   R1,R15
               FI
               IF   (CLCL,R0,R14,EQ)     CLCL MAJOR NAME
                    LM   R0,R1,DQMINCL
                    LA   R14,QMNNAME
                    ZR   R15
                    IC   R15,QMNLNM
                    IF   (CLR,R1,R15,LT)
                         LR   R15,R1
                    ELSE
                         LR   R1,R15
                    FI
                    IF   (CLCL,R0,R14,EQ)   CLCL MINOR NAME
                         ZR   R15
                    ELSE
                         LA   R15,1
                    FI
               ELSE
                    LA   R15,1
               FI
         FI
         PEXIT
         TITLE 'GET JOB NAME FROM ASCB'
GETJOB   PENTRY
         SLL   R1,2
         L     R15,16
         L     R15,CVTASCVT(R15)
         L     R1,ASVTENTY-4(R15,R1)
         IF    (LTR,R1,R1,P)
               IF    (ICM,R15,15,ASCBJBNI(R1),Z)
                     L    R15,ASCBJBNS(R1)
               FI
         ELSE
               LA    R15,=CL8' '
         FI
         MVC   JOBNAME,0(R15)
         PEXIT
         TITLE 'DR - DISPLAY OUTSTANDING OPERATOR REPLIES'
DR       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DR - DISPLAY NUMBER OF OUTSTANDING REPLIES'
               PEXIT
         FI
         L     R2,16          CVT
         L     R2,CVTCUCB(R2) UCM
         LH    R0,UCMRQNR(R2) # REPLIES OUTSTANDING
         CVD   R0,PWORK
         EDIT  TO=DRWTORS,FROM=PWORK+6,MASK=' Z99'
         LA    R0,DRLINE
         PM    PUT
         PEXIT
         TITLE 'DS - DISPLAY SYSTEM TASKS ONLY'
DS       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DS - DISPLAY ACTIVE SYSTEM TASKS'
               PM   DAHELP
               PEXIT
         FI
         PM    INTERVAL
         LA    R0,DALINE0
         PM    PUT
         LA    R0,DALINE1
         PM    PUT
         LA    R0,DALINE2
         PM    PUT
         L     R2,16
         L     R2,CVTASCVT(R2)
         LA    R3,ASVTENTY(R2)
         L     R4,ASVTMAXU(R2)
         DODO  WHILE,(SH,R4,=H'1',NM)
               IF   (ICM,R5,15,0(R3),P)
                    IF   (ICM,R1,15,ASCBTSB(R5),Z),                    X
               AND,(ICM,R1,15,ASCBJBNI(R5),Z)
                         PM   DS1
                         LA   R0,DALINE3
                         PM   PUT
                    FI
               FI
               AAI  R3,4
         OD
         PEXIT
         TITLE 'DS1 - PROCESS ONE ASCB ENTRY FOR DS'
DS1      PENTRY
         L     R1,ASCBJBNS(R5)
         MVC   DAJOB,0(R1)
         MVC   DASTEP,=CL8'STC'
         PM    DA1
         MVC   DATXEQ,=CL6' '
         PEXIT
         TITLE 'DT - DISPLAY TAPES'
DT       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DT - DISPLAY ONLINE ALLOCATED TAPE STATUS'
               WRITE ' '
               WRITE 'DEVICE ADDRESS'
               WRITE 'VOLUME NAME'
               WRITE 'JOBNAME'
               WRITE 'CURRENT SIO COUNT'
               WRITE 'TEMPORARY READ, WRITE, NOISE, ERG COUNT'
               WRITE 'PERMANENT READ AND WRITE COUNT'
               PEXIT
         FI
         L     R2,16
         L     R2,CVTILK2(R2)
         L     R7,DISIOTAB
         ZR    R3
         DODO  WHILE,(CLC,0(2,R2),=X'FFFF',NE)
               IF   (CLC,0(2,R2),=X'0000',NE)
                  ICM  R3,3,0(R2)
                  CVTUCB R3
                  IF   (CLM,R0,B'0011',4(R7),EQ) SKIP ALT.
                    AAI R7,6
                    IF   (TM,UCBSTAT(R3),X'88',O),                     X
               AND,(TM,UCBTYP+2(R3),X'80',O)
                                   PM   DT1
                    FI
                  FI
               FI
               AAI  R2,2
         OD
         PEXIT
         TITLE 'DT1 PRINT ONE TAPE'
DT1      PENTRY
         MVC   DTUCB,UCBNAME(R3)
         MVC   DTVOL,UCBVOLI(R3)
         LA    R1,DTVOL
         PM    CHKVOL
         L     R4,UCBEXTPT(R3)
         LH    R1,UCBASID(R4)
         PM    GETJOB
         MVC   DTJOB,JOBNAME
         L     R4,UCBXTN(R3)
         ZR    R0
         ICM   R0,3,UCBSIO(R4)
         CVD   R0,PWORK
         EDIT  TO=DTSIO,FROM=PWORK+5,MASK=' ZZ,Z99'
         ZR    R0
         ZR    R1
         IC    R1,UCBTREAD(R4)
         AR    R0,R1
         IC    R1,UCBTWRIT(R4)
         AR    R0,R1
         IC    R1,UCBNOISE(R4)
         AR    R0,R1
         AH    R0,UCBERG(R4)
         CVD   R0,PWORK
         EDIT  TO=DTTERR,FROM=PWORK+6,MASK=' Z99'
         IC    R1,UCBPREAD(R4)
         LR    R0,R1
         IC    R1,UCBPWRIT(R4)
         AR    R0,R1
         CVD   R0,PWORK
         EDIT  TO=DTPERR,FROM=PWORK+6,MASK=' Z99'
         IF    (TM,UCBFLA(R3),X'40',O)
               MVC  DTMSG,=CL25'INTERVENTION REQUIRED'
         ELSE
               MVC  DTMSG,=CL25' '
         FI
         LA    R0,DTLINE
         PM    PUT
         PEXIT
         TITLE 'DU - DISPLAY TSO USERS ONLY'
DU       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DU - DISPLAY ACTIVE TSO USERS'
               PM   DAHELP
               PEXIT
         FI
         PM    INTERVAL
         LA    R0,DALINE0
         PM    PUT
         LA    R0,DALINE1
         PM    PUT
         LA    R0,DALINE2
         PM    PUT
         L     R2,16
         L     R2,CVTASCVT(R2)
         LA    R3,ASVTENTY(R2)
         L     R4,ASVTMAXU(R2)
         DODO  WHILE,(SH,R4,=H'1',NM)
               IF   (ICM,R5,15,0(R3),P)
                    IF   (ICM,R1,15,ASCBTSB(R5),NZ)
                         PM   DU1
                         LA   R0,DALINE3
                         PM   PUT
                    FI
               FI
               AAI  R3,4
         OD
         PEXIT
         TITLE 'DU1 - PROCESS ONE ASCB ENTRY FOR DU'
DU1      PENTRY
         L     R1,ASCBJBNS(R5)
         IF    (CLI,0(R1),C'A',NL)
               MVC  DAJOB,0(R1)
         ELSE
               MVC  DAJOB,=CL8'*LOGON*'
         FI
         MVC   DASTEP,=CL8'TSO'
         PM    DA1
         PEXIT
         TITLE 'DV - DISPLAY VIRTUAL AND REAL STORAGE STATUS'
DV       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DV - DISPLAY VIRTUAL AND REAL STORAGE ALLOCATION'
               WRITE ' '
               WRITE 'VIRTUAL - 16 MILLION BYTE ADDRESS SPACE ALLOC.'
               WRITE 'REAL    - CURRENT REAL STORAGE ALLOCATION'
               WRITE 'FIXED   - CURRENT FIXED REAL STORAGE ALLOCATION'
               WRITE ' '
               WRITE 'NUC - NUCLEUS (MVS OPERATING SYSTEM)'
               WRITE 'PVT - PRIVATE ADDRESS SPACES'
               WRITE 'CSA - COMMON STORAGE AREA'
               WRITE 'LPA - COMMON LINK PACK AREA FOR PROGRAMS'
               WRITE 'SQA - SYSTEM QUEUE AREA'
               PEXIT
         FI
         PM    DV1
         PM    DV3
         PEXIT
         TITLE 'DV1 - COLLECT DATA FROM PAGE FRAME TABLE'
DV1      PENTRY
         XC    DVDATA(LDVDATA),DVDATA
         L     R2,16                         R2=CVT
         L     R3,CVTPVTP(R2)                R3=PVT
         L     R0,CVTSHRVM(R2)
         SRL   R0,12
         SLL   R0,4
         STH   R0,DVSCSA                     VBN FOR START OF CSA
         L     R0,CVTLPDIA(R2)
         SLL   R0,8
         SRL   R0,20
         SLL   R0,4
         STH   R0,DVSLPA                     VBN FOR START OF LPA
         L     R4,CVTGDA(R2)
         L     R4,GDASQASP(R4)
         L     R4,SPDQEPTR(R4)
         L     R0,DQEBLKAD(R4)
         SRL   R0,12
         SLL   R0,4
         STH   R0,DVSSQA                     VBN FOR START OF SQA
         L     R4,PVTPFTP(R3)
         LR    R5,R4
         LHL   R0,PVTFPFN(R3)                R4=FIRST PFTE
         SLL   R0,4
         AR    R4,R0
         LHL   R0,PVTLPFN(R3)                R5=LAST  PFTE
         SLL   R0,4
         AR    R5,R0
         DODO  WHILE,(CLR,R4,R5,NH)
               PM   DV2
               AAI  R4,16
         OD
         PEXIT
         TITLE 'DV2 - PROCESS ONE PAGE FRAME TABLE ENTRY'
DV2      PENTRY
         IHW  DVBREAL
         IF   (TM,PFTFLAG1(R4),PFTBADPG,O)
              IHW  DVBBAD
              IHW  DVBFIXED
              PEXIT
         FI
         IF   (TM,PFTFLAG2(R4),PFTOFFLN,O)
              IHW  DVBOFF
              IHW  DVBFIXED
              PEXIT
         FI
         IF   (TM,PFTFLAG2(R4),PFTIRRG,O)
              IHW  DVBVIO
              PEXIT
         FI
         IF   (TM,PFTFLAG1(R4),PFTONAVQ,O)
              IHW  DVBFREE
              PEXIT
         FI
         IHW  DVBALLOC
         IF (CLC,PFTFXCT(2,R4),=H'0',NZ)
              IHW  DVBFIXED
         FI
         IF   (TM,PFTFLAG1(R4),PFTVRALC,O)
              IHW  DVBVR
         FI
         IF (CLC,PFTVBN(2,R4),DVSCSA,L)
               IHW  DVHRPVT
               IF (CLC,PFTFXCT(2,R4),=H'0',NZ)
                    IHW  DVHFPVT
               FI
         ELSEIF (CLC,PFTVBN(2,R4),DVSLPA,L)
               IHW  DVHRCSA
               IF   (CLC,PFTFXCT(2,R4),=H'0',NZ)
                    IHW  DVHFCSA
               FI
         ELSEIF (CLC,PFTVBN(2,R4),DVSSQA,L)
               IHW  DVHRLPA
               IF   (CLC,PFTFXCT(2,R4),=H'0',NZ)
                    IHW  DVHFLPA
               FI
         ELSE
               IHW  DVHRSQA
               IHW  DVHFSQA
         FI
         PEXIT
         TITLE 'DV3 - DISPLAY VIRTUAL AND REAL STORAGE INFO'
DV3      PENTRY
*
*        VIRTUAL STORAGE
*
         L     R2,16                    R2=CVT
         L     R3,CVTPVTP(R2)           R3=PVT
         L     R0,CVTNUCB(R2)
         EM    DVVNUC
         L     R0,CVTSHRVM(R2)
         S     R0,CVTNUCB(R2)
         EM    DVVPVT
         L     R1,CVTLPDIA(R2)
         LA    R0,0(R1)
         S     R0,CVTSHRVM(R2)
         EM    DVVCSA
         L     R1,CVTLPDIA(R2)
         ZHB   R1
         L     R4,CVTGDA(R2)
         L     R4,GDASQASP(R4)
         L     R4,SPDQEPTR(R4)
         L     R0,DQEBLKAD(R4)
         SR    R0,R1
         EM    DVVLPA
         L     R0,DQELNTH(R4)
         EM    DVVSQA
         LA    R0,DVLINE1
         PM    PUT
*
*        REAL STORAGE
*
         LHL   R0,PVTFPFN(R3)
         SLL   R0,12
         EM    DVRNUC
         LHL   R0,DVHRPVT
         EMF   DVRPVT
         LHL   R0,DVHRCSA
         EMF   DVRCSA
         LHL   R0,DVHRLPA
         EMF   DVRLPA
         LHL   R0,DVHRSQA
         EMF   DVRSQA
         LA    R0,DVLINE2
         PM    PUT
*
*        FIXED STORAGE
*
         LHL   R0,PVTFPFN(R3)
         SLL   R0,12
         EM    DVFNUC
         LHL   R0,DVHFPVT
         EMF   DVFPVT
         LHL   R0,DVHFCSA
         EMF   DVFCSA
         LHL   R0,DVHFLPA
         EMF   DVFLPA
         LHL   R0,DVHFSQA
         EMF   DVFSQA
         LA    R0,DVLINE3
         PM    PUT
*
*        TOTAL1
*
         LHL   R0,PVTLPFN(R3)
         AH    R0,=H'1'
         SLL   R0,12
         EM    DVTREAL
         LHL   R0,DVBFREE
         EMF   DVTFREE
         LHL   R1,PVTFPFN(R3)
         LHL   R0,DVBALLOC
         AR    R0,R1
         EMF   DVTALLOC
         LHL   R0,DVBFIXED
         AR    R0,R1
         EMF   DVTFIXED
         LHL   R0,DVBVIO
         EMF   DVTVIO
         LHL   R0,DVBBAD
         EMF   DVTBAD
         LA    R0,DVLINE4
         PM    PUT
*
*        TOTAL2
*
         LHL   R0,DVBBAD
         EMF   DVTBAD
         LHL   R0,DVBOFF
         EMF   DVTOFF
         LHL   R0,DVBVIO
         EMF   DVTVIO
         LHL   R0,DVBVR
         EMF   DVTVR
         LA    R0,DVLINE5
         PM    PUT
         PEXIT
         TITLE 'DW - DISPLAY WAITING JOBS AND TSO USERS'
DW       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DW - DISPLAY DEVICES WITH IO WAITING TO END'
               WRITE ' '
               WRITE 'DEVICE ADDRESS'
               WRITE 'VOLUME NAME'
               PEXIT
         FI
         L     R2,16
         L     R2,CVTILK2(R2)
         L     R7,DISIOTAB
         ZR    R3
         DODO  WHILE,(CLC,0(2,R2),=X'FFFF',NE)
               IF   (CLC,0(2,R2),=X'0000',NE)
                  ICM  R3,3,0(R2)
                  CVTUCB R3
                  IF   (CLM,R0,B'0011',4(R7),EQ) SKIP ALT.
                    AAI R7,6
                    IF   (TM,UCBTYP+2(R3),X'FF'-X'80'-X'20',Z)
                         IF   (TM,UCBFLA(R3),X'02',NZ)
                              PM   DW1
                         FI
                    FI
                  FI
               FI
               AAI  R2,2
         OD
         PEXIT
         TITLE 'DW1 - LIST TAPE OR DISK PENDING IO'
DW1      PENTRY
         MVC   DWUCB,UCBNAME(R3)
         MVC   DWVOL,UCBVOLI(R3)
         LA    R0,DWLINE
         PM    PUT
         PEXIT
         TITLE 'DZ - DISPLAY INTERVAL TRACE TABLE'
DZ       PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'DZ - DISPLAY TRACE TABLE'
               WRITE ' '
               WRITE 'SIO - START IO       CC-UCB,CAW,CSW,IOSB'
               WRITE 'EXT - ENTERNAL INT.  PSW,R15,R0,R1'
               WRITE 'SVC - MVS CALL       PSW,R15,R0,R1'
               WRITE 'PGM - PROGRAM INT.   PSW,R15,R0,R1'
               WRITE 'ISD - INIT.SRB DISP. NEWPSW,ASID,R0,R1'
               WRITE 'I/O - IO INTERRUPT   PSW,CSW'
               WRITE 'SSR - SRB REDISP.    NEWPSW,ASID,R0,R1'
               WRITE 'DSP - TASK DISP.     NEWPSW,R15,R0,R1'
               WRITE ' '
               WRITE 'LAST 3 WORDS ARE CPUID-ASID, TCB, AND TIMER'
               WRITE ' '
               WRITE 'DZS - SUMARIZES TRACE TABLE TYPES AND LISTS'
               WRITE '      SVC COUNTS BY TYPE'
               PEXIT
         FI
         L     R2,FLCTRACE
         IF    (LTR,R2,R2,Z)
               WRITE 'NO TRACE TABLE IN SYSTEM'
               PEXIT
         FI
         PM    INTERVAL
         IF    (CLI,LINE+2,C'S',EQ)
               PM   DZS
               PEXIT
         FI
         LM    R2,R4,TTETAB
         LR    R5,R4
         DODO  UNTIL,(CLR,R4,R5,EQ)
               LH   R1,TTEASID(R4)
               PM   GETJOB
               MVC  DZJOB,JOBNAME
               ZR   R1
               IC   R1,TTETYP(R4)
               SRL  R1,4
               MH   R1,=H'3'
               LA   R1,TTENTAB(R1)
               MVC  DZTYP(3),0(R1)
               UNPK DZPSW(9),TTEPSW(5,R4)
               UNPK DZPSW+8(9),TTEPSW+4(5,R4)
               MVI  DZPSW+16,C' '
               TR   DZPSW,HEX-240
               UNPK DZCSW(9),TTECSW(5,R4)
               UNPK DZCSW+8(9),TTECSW+4(5,R4)
               MVI  DZCSW+16,C' '
               TR   DZCSW,HEX-240
               UNPK DZREGS(9),TTEREGS(5,R4)
               UNPK DZREGS+8(9),TTEREGS+4(5,R4)
               MVI  DZREGS+16,C' '
               TR   DZREGS,HEX-240
               UNPK DZTCBTM(9),TTETCBTM(5,R4)
               MVC  PWORK(4),TTETCBTM+4(R4) MVC TO PREVENT 0C4 ON B5
               UNPK DZTCBTM+8(9),PWORK(5)
               MVI  DZTCBTM+16,C' '
               TR   DZTCBTM,HEX-240
               LA   R0,DZLINE
               PM   PUT
               LA   R4,LTTE(R4)
               IF   (CLR,R4,R3,NL)
                    LR   R4,R2
               FI
         OD
         PEXIT
         TITLE 'DZS - SUMMARIZE TRACE TABLE'
DZS      PENTRY
         LA    R0,DZSLINE1
         PM    PUT
         XC    TTEETAB,TTEETAB  CLEAR EXCEPTION TABLE
         XC    TTECTAB,TTECTAB  CLEAR TOTAL     TABLE
         LM    R2,R3,TTETAB
         L     R5,SVCTAB
         IF    (LTR,R5,R5,Z)
               LA   R0,256*4
               GETMAIN R,LV=(0)
               LR   R5,R1
               ST   R1,SVCTAB
         FI
         LR    R0,R5
         LA    R1,256*4
         ZR    R14
         ZR    R15
         MVCL  R0,R14
         DODO  WHILE,(CLR,R2,R3,L)
               ZR   R1
               IC   R1,TTETYP(R2)
               SRL  R1,4
               LR   R4,R1
               SLL  R4,2
               L    R15,TTECTAB(R4)
               AAI  R15,1
               ST   R15,TTECTAB(R4)
               DOCASE (R1)
                 CASE 0 SIO
                   IF   (CLI,TTESIOCC(R2),0,NE)
                        L    R15,TTEETAB(R4)
                        AAI  R15,1
                        ST   R15,TTEETAB(R4)
                   FI
                 ESAC
                 CASE 2 SVC
                   ZR   R15
                   IC   R15,TTEPSW+3(R2)
                   SLL  R15,2
                   LA   R15,0(R15,R5)
                   L    R14,0(R15)
                   AAI  R14,1
                   ST   R14,0(R15)
                   IF   (CLI,TTEPSW+3(R2),13,EQ),                      X
               OR,(CLI,TTEPSW+3(R2),15,EQ)
                        L    R15,TTEETAB(R4)
                        AAI  R15,1
                        ST   R15,TTEETAB(R4)
                   FI
                 ESAC
                 CASE 3 PGM
                   IF   (CLI,TTEPSW+3(R2),17,NE)
                        L    R15,TTEETAB(R4)
                        AAI  R15,1
                        L    R15,TTEETAB(R4)
                   FI
                 ESAC
                 CASE 5 I/O
                   IF   (TM,TTEIOUS(R2),X'03',NZ),                     X
               OR,(TM,TTEIOCS(R2),X'3F',NZ)
                        L    R15,TTEETAB(R4)
                        AAI  R15,1
                        L    R15,TTEETAB(R4)
                   FI
                 ESAC
               ESACOD
               LA   R2,LTTE(R2)
         OD
         LA    R2,TTENTAB
         LA    R3,TTECTAB
         LA    R4,TTEETAB
         LA    R5,TTENTAB+L'TTENTAB
         DODO  WHILE,(CLR,R2,R5,L)
               L    R0,0(R3)
               IF   (LTR,R0,R0,NZ)
                    MVC  DZSTYP,0(R2)
                    CVD  R0,PWORK
                    EDIT TO=DZSTOT,FROM=PWORK+5,MASK=' ZZZ99'
                    L    R0,0(R4)
                    CVD  R0,PWORK
                    EDIT TO=DZSERR,FROM=PWORK+5,MASK=' ZZZ99'
                    LA   R0,DZSLINE2
                    PM   PUT
               FI
               AAI  R2,3
               AAI  R3,4
               AAI  R4,4
         OD
         L     R5,SVCTAB
         LA    R6,256*4(R5)
         ZAP   PSVCNO,=P'0'
         LA    R0,BLANK
         PM    PUT
         LA    R0,DZSLINE3
         PM    PUT
         DODO  WHILE,(CLR,R5,R6,L)
               L    R1,0(R5)
               IF   (LTR,R1,R1,NZ)
                    CVD   R1,PWORK
                    EDIT  TO=DSVCTOT,FROM=PWORK+6,MASK=' Z99'
                    EDIT  TO=DSVCNO,FROM=PSVCNO,MASK=' Z99'
                    LA    R0,DZSLINE4
                    PM    PUT
               FI
               AP   PSVCNO,=P'1'
               AAI  R5,4
         OD
         PEXIT
         TITLE 'MEMORY - DISPLAY MEMORY USAGE BY COMPONENT'
MEMORY   PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'M - SUMMARIZE TOTAL REAL MEMORY USAGE BY DOMAIN'
               WRITE ' '
               WRITE 'FPC DOMAINS ARE AS FOLLOWS'
               WRITE ' '
               WRITE ' 0 - SYSTEM'
               WRITE ' 1 - ONLINE SYSTEMS'
               WRITE ' 2 - TSO'
               WRITE ' 3 - BATCH'
               PEXIT
         FI
         PM    M1
         PM    M3
         PEXIT
         TITLE 'M1 - COLLECT REAL MEMORY BY DOMAIN'
M1       PENTRY
         L     R2,16
         L     R3,CVTPVTP(R2)
         L     R4,PVTPFTP(R3)
         LR    R5,R4
         LHL   R0,PVTFPFN(R3)
         SLL   R0,4
         AR    R4,R0
         LHL   R0,PVTLPFN(R3)
         SLL   R0,4
         AR    R5,R0
         XC    DOMAINK,DOMAINK
         LHL   R7,PVTFPFN(R3)
         STH   R7,DOMAINK         D(0)=SYSTEM
         L     R6,CVTASCVT(R2)
         DODO  WHILE,(CLR,R4,R5,NH)
               PM   M2
               AH   R4,=AL2(LPFTE)
         OD
         PEXIT
         TITLE 'M2 - ASSIGN REAL PAGE FRAME TO DOMAIN'
M2       PENTRY
         LH    R1,PFTASID(R4)
         IF    (LTR,R1,R1,Z)
               LA   R1,DOMAINK
         ELSE
               SLL  R1,2
               L    R1,ASVTENTY-4(R1,R6)
               IF   (LTR,R1,R1,P)
                    L    R1,ASCBOUCB(R1)
                    ZR   R15
                    IC   R15,OUCBDMN(R1)
                    MH   R15,=AL2(LDOMKE)
                    LA   R1,DOMAINK(R15)
               ELSE
                    LA   R1,DOMAINK
               FI
         FI
         LH    R15,0(R1)
         AAI   R15,1      INCR DOMAIN PAGE COUNT
         STH   R15,0(R1)
         AAI   R7,1       INCR TOTAL  PAGE COUNT
         PEXIT
         TITLE 'M3 - PRINT REAL MEMORY BY DOMAIN'
M3       PENTRY
         MVC   DOMDESC,=CL6'DOMAIN'
         SLL   R7,2                  CONVERT TOTAL PAGES TO K
         LA    R2,DOMAINK
         LA    R3,DOMAINK+L'DOMAINK
         ZR    R4
         DODO  WHILE,(CLR,R2,R3,L)
               LH   R1,0(R2)
               IF   (LTR,R1,R1,NZ)
                    SLL  R1,2
                    CVD  R1,PWORK
                    EDIT TO=DOMK,FROM=PWORK+5,MASK=' ZZZ99'
                    MH   R1,=H'100'
                    ZR   R0
                    DR   R0,R7
                    CVD  R1,PWORK
                    EDIT TO=DOMPCT,FROM=PWORK+6,MASK=' Z99'
                    CVD  R4,PWORK
                    EDIT TO=DOMN,FROM=PWORK+6,MASK=' Z99'
                    LA   R0,DOMLINE
                    PM   PUT
               FI
               AH   R2,=AL2(LDOMKE)
               AAI  R4,1
         OD
         MVC   DOMN,=CL4' '
         MVC   DOMDESC,=CL6' TOTAL'
         CVD   R7,PWORK
         EDIT  TO=DOMK,FROM=PWORK+5,MASK=' ZZZ99'
         MVC   DOMPCT,=CL4' 100'
         LA    R0,DOMLINE
         PM    PUT
         PEXIT
         TITLE 'PDSNINFO - DISPLAY PAGE DATA SET INFO.'
PDSNINFO PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'P - SUMMARIZE PAGING DATA SET USAGE'
               WRITE ' '
               WRITE 'SLOT - AREA FOR ONE 4K PAGE ON DISK'
               WRITE ' '
               WRITE 'VIO     - VIRTUAL IO SLOTS'
               WRITE 'NON-VIO - VIRTUAL STORAGE SLOTS'
               WRITE 'UNUSED  - NO REAL PAGE DATA IN SLOT'
               WRITE 'UNRESV  - SLOT NOT RESERVED FOR POSSIBLE USE'
               WRITE 'SWAP-SET- NUMBER OF SLOTS IN ONE SWAP BLOCK'
               WRITE ' '
               WRITE 'TYPE  - TYPE OF PAGES SWAPPED TO THIS DATASET'
               WRITE 'AVAIL - SLOTS AVAILABLE FOR USE'
               WRITE 'WEIGHT- SRM WEIGHT ASSIGNED TO BAL. IO LOAD'
               WRITE 'ERRORS- SLOTS FLAGGED AS BAD DUE TO ERRORS'
               PEXIT
         FI
         LA    R0,PDHEAD1
         PM    PUT
         PM    PDTOTAL
         LA    R0,PDHEAD2
         PM    PUT
         PM    PDPAGE
         PM    PDSWAP
         PEXIT
         TITLE 'PDTOTAL - TOTALS ON ALL PAGE DATA SETS'
PDTOTAL  PENTRY
         L     R2,16
         L     R2,CVTASMVT(R2)
         L     R0,ASMSLOTS(R2)
         LR    R3,R0
         CVD   R0,PWORK
         EDIT  TO=PDSLOTS,FROM=PWORK+4,MASK=' Z,ZZZ,Z99'   SLOTS
         L     R0,ASMVSC(R2)
         SR    R3,R0
         CVD   R0,PWORK
         EDIT  TO=PDVSC,FROM=PWORK+4,MASK=' Z,ZZZ,Z99'  ALLOC VIO
         L     R0,ASMNVSC(R2)
         SR    R3,R0
         CVD   R0,PWORK
         EDIT  TO=PDNVSC,FROM=PWORK+4,MASK=' Z,ZZZ,Z99' ALLOC NON-VIO
         S     R3,ASMBKSLT(R2)
         CVD   R3,PWORK
         EDIT  TO=PDUNUSED,FROM=PWORK+4,MASK=' Z,ZZZ,Z99'  UNUSED
         L     R0,ASMBKSLT(R2)
         CVD   R0,PWORK
         EDIT  TO=PDUNRESV,FROM=PWORK+4,MASK=' Z,ZZZ,Z99'  UNRESERVED
         L     R1,ASMSART(R2)
         L     R0,SARSETSZ(R1)
         CVD   R0,PWORK
         EDIT  TO=PDSETSZ,FROM=PWORK+4,MASK=' Z,ZZZ,Z99' SWAP SET SIZE
         LA    R0,PDTLINE
         PM    PUT
         PEXIT
         TITLE 'PDPAGE- DISPLAY PAGING DATA SET INFO'
PDPAGE   PENTRY
         L     R2,16
         L     R2,CVTASMVT(R2)
         L     R3,ASMPART(R2)
         L     R4,PARTDSNL(R3)
         LA    R5,PARTENTS(R3)
         L     R6,PARTSIZE(R3)
         DODO  WHILE,(LTR,R6,R6,NZ)
               PM   PDPARTE
               LA   R4,LDSN(R4)
               LA   R5,LPARTE(R5)
               SH   R6,=H'1'
         OD
         PEXIT
         TITLE 'PDPARTE- DISPLAY INFO FOR ONE PARTE'
PDPARTE  PENTRY
         IF    (CLI,PARETYPE(R5),X'20',EQ),OR,(CLI,0(R4),C' ',EQ)
               PEXIT
         FI
         MVC   PDDSN,0(R4)
         IF    (CLI,PARETYPE(R5),X'80',EQ)
               MVC  PDTYPE,=CL6'PLPA'
         ELSEIF (CLI,PARETYPE(R5),X'40',EQ)
               MVC  PDTYPE,=CL6'COMMON'
         ELSEIF (CLI,PARETYPE(R5),X'10',EQ)
               MVC  PDTYPE,=CL6'LOCAL'
         ELSE
               PEXIT
         FI
         L     R1,PAREUCBP(R5)
         MVC   PDVOL,UCBVOLI(R1)
         MVC   PDUCB,UCBNAME(R1)
         L     R0,PARESZSL(R5)
         CVD   R0,PWORK
         EDIT  TO=PDSIZE,FROM=PWORK+5,MASK=' ZZ,Z99'
         L     R0,PARESLTA(R5)
         CVD   R0,PWORK
         EDIT  TO=PDAVAIL,FROM=PWORK+5,MASK=' ZZ,Z99'
         L     R0,PARERQTM(R5)
         CVD   R0,PWORK
         EDIT  TO=PDWEIGHT,FROM=PWORK+5,MASK=' ZZ,Z99'
         L     R0,PARERRCT(R5)
         CVD   R0,PWORK
         EDIT  TO=PDERRORS,FROM=PWORK+5,MASK=' ZZ,Z99'
         IF    (TM,PAREFLG1(R5),X'40',O)
               MVI  PDERRORS,C'*'         DSN OUT OF USE
         FI
         LA    R0,PDPSLINE
         PM    PUT
         PEXIT
PDSWAP   PENTRY
         L     R2,16
         L     R2,CVTASMVT(R2)
         L     R3,ASMSART(R2)
         L     R4,SARDSNL(R3)
         LA    R5,SARENTS(R3)
         L     R6,SARSIZE(R3)
         DODO  WHILE,(LTR,R6,R6,NZ)
               PM   PDSARTE
               LA   R4,LDSN(R4)
               LA   R5,LSARTE(R5)
               SH   R6,=H'1'
         OD
         PEXIT
         TITLE 'PDSARTE- DISPLAY INFO FOR ONE SARTE'
PDSARTE  PENTRY
         IF    (CLI,0(R4),C' ',EQ)
               PEXIT
         FI
         MVC   PDDSN,0(R4)
         MVC   PDTYPE,=CL6'SWAP'
         L     R1,SREUCB(R5)
         MVC   PDVOL,UCBVOLI(R1)
         MVC   PDUCB,UCBNAME(R1)
         L     R0,SRETOTSL(R5)
         CVD   R0,PWORK
         EDIT  TO=PDSIZE,FROM=PWORK+5,MASK=' ZZ,Z99'
         L     R0,SREAVLSL(R5)
         CVD   R0,PWORK
         EDIT  TO=PDAVAIL,FROM=PWORK+5,MASK=' ZZ,Z99'
         MVC   PDWEIGHT,=CL7' '
         L     R0,SRERRCT(R5)
         CVD   R0,PWORK
         EDIT  TO=PDERRORS,FROM=PWORK+5,MASK=' ZZ,Z99'
         IF    (TM,SREFLG(R5),X'40',O)
               MVI  PDERRORS,C'*'         DSN OUT OF USE
         FI
         LA    R0,PDPSLINE
         PM    PUT
         PEXIT
         TITLE 'RATES - CPU USAGE RATES BY COMPONENT'
RATES    PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'R - DISPLAY CPU AND REAL MEMORY USAGE BY DOMAIN'
               WRITE ' '
               WRITE 'FPC DOMAINS ARE AS FOLLOWS'
               WRITE ' '
               WRITE '0 - SYSTEM'
               WRITE '1 - ONLINE SYSTEMS'
               WRITE '2 - TSO'
               WRITE '3 - BATCH'
               PEXIT
         FI
         PM    INTERVAL
         PM    RC2
         PM    RC3
         PEXIT
         TITLE 'RC1 - CALCULATE CPU, IO, AND PAGING FOR ASCBS'
RC1      PENTRY
         L     R2,16
         L     R2,CVTASCVT(R2)
         LA    R3,ASVTENTY(R2)
         L     R1,ASVTMAXU(R2)
         SLL   R1,2
         LA    R4,0(R1,R3)
         L     R5,RCTABPTR
         DODO  WHILE,(CLR,R3,R4,L)
               IF   (ICM,R6,15,0(R3),P)
                    LM   R0,R1,ASCBEJST(R6)
                    SRDL R0,12
                    D    R0,=F'10000'
                    LR   R15,R1
                    LM   R0,R1,ASCBSRBT(R6)
                    SRDL R0,12
                    D    R0,=F'10000'
                    AR   R15,R1
                    LR   R14,R15
                    S    R15,RCTTCPU-RCT(R5)
                    IF   (LTR,R15,R15,M),OR,(CLR,R14,R15,EQ)
                         ZR   R15
                    FI
                    STM  R14,R15,RCTTCPU-RCT(R5)
                    L    R1,ASCBOUCB(R6)
                    L    R0,OUCBIOSM(R1)
                    LR   R1,R0
                    S    R1,RCTTIO-RCT(R5)
                    IF   (LTR,R15,R15,M),OR,(CLR,R0,R1,EQ)
                         ZR   R1
                    FI
                    STM  R0,R1,RCTTIO-RCT(R5)
                    L    R1,ASCBOUXB(R6)
                    L    R0,OUXBPIN(R1)
                    A    R0,OUXBPOUT(R1)
                    LR   R1,R0
                    S    R1,RCTTPG-RCT(R5)
                    IF   (LTR,R15,R15,M),OR,(CLR,R0,R1,EQ)
                         ZR   R1
                    FI
                    STM  R0,R1,RCTTPG-RCT(R5)
               ELSE
                    XC   0(L'RCT,R5),0(R5)
               FI
               AAI  R3,4
               AH   R5,=AL2(L'RCT)
         OD
         PEXIT
         TITLE 'RT1 - COLLECT TRACE TABLE RATES FOR ASCBS'
RT1      PENTRY
         L     R2,FLCTRACE
         IF    (LTR,R2,R2,Z)
               PEXIT
         FI
         LM    R3,R4,4(R2)    R3=TTE START, R4=TTE END
         LM    R5,R6,TTETAB   R5=TAB START, R6=TAB END
         LA    R7,10
         DODO  UNTIL,(LTR,R2,R2,Z),OR,(LTR,R7,R7,Z)
               LR   R0,R5
               LR   R1,R6
               SR   R1,R5    R1=LENGTH OF TABLE
               LR   R14,R3
               LR   R15,R1
               MVCL R0,R14   COPY TABLE
               LH   R2,=H'-1' COUNT TIME CHANGES (SHOULD BE 1)
               LR   R1,R6
               SH   R1,=AL2(LTTE)
               L    R0,TTETIME(R1) R0=LAST TTE TIME
               LR   R1,R5
               DODO WHILE,(CLR,R1,R6,L),AND,(LTR,R2,R2,NP)
                    IF   (CL,R0,TTETIME(R1),GT)
                         AH   R2,=H'1'
                         LR   R15,R1   SAVE LOCATION OF TIME CHANGE
                    FI
                    L    R0,TTETIME(R1)
                    LA   R1,LTTE(R1)
               OD
               BCTR R7,0
         OD
         IF   (LTR,R7,R7,NP)
              WRITE 'TRACE TABLE INVALID DUE TO HIGH ACTIVITY'
              MVC  DTRCINT,=C' *****'
              MVC  TRCINT,=F'1'
         ELSE
              ST   R15,TTETAB+8 SAVE FIRST FOR USE IN DISPLAYS
              LR   R14,R15     R14=LAST, R15=FIRST
              SH   R14,=AL2(LTTE)
              IF   (CLR,R14,R5,L)
                   LR   R14,R6
                   SH   R14,=AL2(LTTE)
              FI
              L    R1,TTETIME(R14)
              SL   R1,TTETIME(R15)
              ZR   R0
              D    R0,=F'38'
              CVD  R1,PWORK
              EDIT TO=DTRCINT,FROM=PWORK+5,MASK=' ZZZ99'
              ST   R1,TRCINT
         FI
         L     R7,RCTABPTR
         L     R2,16
         L     R2,CVTASCVT(R2)
         L     R3,ASVTMAXU(R2)
         DODO  WHILE,(SH,R3,=H'1',NM)   CLEAR TRACE COUNTERS
               XC   RCTCTAB-RCT(L'RCTCTAB,R7),RCTCTAB-RCT(R7)
               LA   R7,L'RCT(R7)
         OD
         L     R7,RCTABPTR
         DODO  WHILE,(CLR,R5,R6,L)
               LH   R1,TTEASID(R5)
               IF   (SH,R1,=H'1',M)
                    ZR   R1
               FI
               MH   R1,=AL2(L'RCT)
               LA   R4,0(R1,R7)       R4=A(RCT ENTRY)
               ZR   R1
               IC   R1,TTETYP(R5)
               SRL  R1,4
               DOCASE (R1)
                 CASE 0
                         IC   R15,RCTCSIO-RCT(R4)
                         IF   (CH,R15,=H'255',L)
                              AAI  R15,1
                         FI
                         STC  R15,RCTCSIO-RCT(R4)
                 ESAC
                 CASE 1
                         IC   R15,RCTCEXT-RCT(R4)
                         IF   (CH,R15,=H'255',L)
                              AAI  R15,1
                         FI
                         STC  R15,RCTCEXT-RCT(R4)
                 ESAC
                 CASE 2
                         IC   R15,RCTCSVC-RCT(R4)
                         IF   (CH,R15,=H'255',L)
                              AAI  R15,1
                         FI
                         STC  R15,RCTCSVC-RCT(R4)
                 ESAC
                 CASE 3
                         IC   R15,RCTCPGM-RCT(R4)
                         IF   (CH,R15,=H'255',L)
                              AAI  R15,1
                         FI
                         STC  R15,RCTCPGM-RCT(R4)
                 ESAC
                 CASE 4
                         IC   R15,RCTCISD-RCT(R4)
                         IF   (CH,R15,=H'255',L)
                              AAI  R15,1
                         FI
                         STC  R15,RCTCISD-RCT(R4)
                 ESAC
                 CASE 5
                         IC   R15,RCTCIO-RCT(R4)
                         IF   (CH,R15,=H'255',L)
                              AAI  R15,1
                         FI
                         STC  R15,RCTCIO-RCT(R4)
                 ESAC
                 CASE 6
                         IC   R15,RCTCSRR-RCT(R4)
                         IF   (CH,R15,=H'255',L)
                              AAI  R15,1
                         FI
                         STC  R15,RCTCSRR-RCT(R4)
                 ESAC
                 CASE 7
                         IC   R15,RCTCDSP-RCT(R4)
                         IF   (CH,R15,=H'255',L)
                              AAI  R15,1
                         FI
                         STC  R15,RCTCDSP-RCT(R4)
                 ESAC
               ESACOD
               LA   R5,LTTE(R5)
         OD
         PEXIT
         TITLE 'DI1 - COLLECT SIO COUNTS FOR DI COMMANDS'
DI1      PENTRY
         L     R2,16
         L     R2,CVTILK2(R2)
         L     R3,DISIOTAB
         DODO  WHILE,(CLC,0(2,R2),=X'FFFF',NE),AND,(LTR,R5,R5,NZ)
               IF   (CLC,0(2,R2),=X'0000',NE)
                    ZR   R1
                    ICM  R1,3,0(R2)
                    CVTUCB R1
                    IF   (CLM,R0,B'0011',4(R3),EQ) SKIP ALT.
                         L    R1,UCBEXTPT(R1)
                         ICM  R0,12,2(R3)
                         ICM  R0,03,UCBMFCNT(R1)
                         STCM R0,15,0(R3)
                         AAI  R3,6
                    FI
               FI
               AAI  R2,2
         OD
         PEXIT
         TITLE 'RC2 - CALCULATE CPU USAGE BY DOMAIN'
RC2      PENTRY
         L     R2,16
         L     R2,CVTASCVT(R2)
         LA    R3,ASVTENTY(R2)
         L     R1,ASVTMAXU(R2)
         SLL   R1,2
         LA    R4,0(R1,R3)
         L     R5,RCTABPTR
         XC    DOMAINR,DOMAINR
         DODO  WHILE,(CLR,R3,R4,L)
               IF   (ICM,R6,15,0(R3),P)
                    L    R1,ASCBOUCB(R6)
                    ZR   R15
                    IC   R15,OUCBDMN(R1)
                    MH   R15,=AL2(LDOMRE)
                    LA   R1,DOMAINR(R15)
                    LH   R15,0(R1)
                    AAI  R15,1    COUNT ASCBS IN DOMAIN
                    STH  R15,0(R1)
                    IF   (TM,ASCBRCTF(R6),ASCBOUT,Z)
                         LH   R15,2(R1)
                         AAI  R15,1
                         STH  R15,2(R1)
                    FI
                    L    R0,4(R1)
                    A    R0,4(R5)   ADD ASCB INTERVAL CPU
                    ST   R0,4(R1)
               FI
               AAI  R3,4
               AH   R5,=AL2(L'RCT)
         OD
         PEXIT
         TITLE 'RC3 - PRINT INTERVAL CPU USAGE AND MEMORY BY DOMAIN'
RC3      PENTRY
         PM    M1
         LA    R2,DOMAINR
         LA    R3,DOMAINR+L'DOMAINR
         ZR    R4
         L     R5,INTBIN
         LA    R6,DOMAINK
         ZR    R7
         DODO  WHILE,(CLR,R2,R3,L)
               LH   R0,0(R2)
               IF   (LTR,R0,R0,P)
                    CVD  R0,PWORK
                    EDIT TO=DRCDMNAS,FROM=PWORK+6,MASK=' Z99'
                    LH   R0,2(R2)
                    CVD  R0,PWORK
                    EDIT TO=DRCDMNAR,FROM=PWORK+6,MASK=' Z99'
                    CVD  R4,PWORK
                    EDIT TO=DRCDMN,FROM=PWORK+6,MASK=' Z99'
                    L    R1,4(R2)
                    AR   R7,R1
                    MH   R1,=H'100'
                    ZR   R0
                    DR   R0,R5
                    CVD  R1,PWORK
                    EDIT TO=DRCDMNP,FROM=PWORK+6,MASK=' Z99'
                    LH   R0,0(R6)
                    SLL  R0,2
                    CVD  R0,PWORK
                    EDIT TO=DRCDMNK,FROM=PWORK+5,MASK=' ZZZ99'
                    LA   R0,RCLINE2
                    PM   PUT
               FI
               AH   R2,=AL2(LDOMRE)
               AAI  R4,1
               AH   R6,=AL2(LDOMKE)
         OD
         L     R2,DCSCPU0I
         A     R2,DCSCPU1I
         LR    R1,R2
         IF    (SR,R1,R7,M)
               ZR   R1
         FI
         MH    R1,=H'100'
         ZR    R0
         D     R0,INTBIN
         CVD   R1,PWORK
         EDIT  TO=DRCSYSC,FROM=PWORK+6,MASK=' Z99'
         MVC   DRCSYST,=CL6'SYSTEM'
         LA    R0,RCLINE3
         PM    PUT
         LR    R1,R2
         MH    R1,=H'100'
         ZR    R0
         D     R0,INTBIN
         CVD   R1,PWORK
         EDIT  TO=DRCSYSC,FROM=PWORK+6,MASK=' Z99'
         MVC   DRCSYST,=CL6'TOTAL '
         LA    R0,RCLINE3
         PM    PUT
         PEXIT
         TITLE 'TIME - PRINT CURRENT AND IPL DATE AND TIME'
TIME     PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'T - DISPLAY TIME AND DATE NOW AND AT LAST IPL'
               PEXIT
         FI
                TIME  DEC
                STM   R0,R1,PWORK
                EDIT  TO=TIMEDATE,FROM=PWORK,                          X
               MASK=' 99/99/99.99F ZZ99.999'
                L     R3,16
                L     R3,CVTUSER(R3)
                IF    (LTR,R3,R3,NZ)
                      EDIT TO=IPLTIME,FROM=UCVTIPL(R3),                X
               MASK=' 99/99/99.99F ZZ99.999'
                ELSE
                      MVC  IPLTIME,=CL22'UNKNOWN - NO UCVT'
                FI
                LA    R0,TIMELINE
                PM    PUT
         PEXIT
         TITLE 'WAIT - WAIT INTERVAL '
WAIT     PENTRY
         IF    (CLI,HMODE,TRUE,EQ)
               WRITE 'W - WAIT SPECIFIED NUMBER OF SECONDS'
               WRITE ' '
               WRITE 'WXXX - XXX IS NUMBER OF SECONDS TO WAIT'
               WRITE ' '
               WRITE 'THIS COMMAND IS FOR BATCH EXECUTION TO ALLOW'
               WRITE 'DEFINITION OF INTERVAL LENGTH TO BE FOLLOWED'
               WRITE 'BY ANY OF THE INTERVAL COMMANDS'
               WRITE ' '
               WRITE 'MVSMON CAN BE EXECUTED IN BATCH AS FOLLOWS'
               WRITE ' // EXEC PGM=MVSMON'
               WRITE ' //SYSPRINT DD SYSOUT=A'
               WRITE ' //SYSIN DD *'
               WRITE ' W010 WAIT 10 SECONDS'
               WRITE ' DB   BASIC SYSTEM INFO FOR PREVIOUS INTERVAL'
               WRITE ' I    HOLD LAST INTERVAL'
               WRITE ' DA   ACTIVE TASK ANALYSIS FOR SAME INTERVAL'
               WRITE ' /*'
               PEXIT
         FI
         PM    TIME
         PACK  PWORK,LINE+1(4)
         OI    PWORK+7,X'0F'
         CVB   R0,PWORK
         MH    R0,=H'100'
         ST    R0,WINT
         STIMER WAIT,BINTVL=WINT
         PM    TIME
         PEXIT
         TITLE 'GET - GET NEXT DISPLAY COMMAND AND LIST IT'
GET      PENTRY
         MVI   INTDONE,FALSE
         MVI   ATTN,FALSE
         IF    (CLI,NOMSG,TRUE,EQ)
               WRITE 'NONE'
         FI
         IF    (CLI,PARM,FALSE,NE)
               IF   (CLI,PARM,FIRST,EQ),AND,(CLI,MODE,TSOMODE,NE)
                    IF   (CLI,MODE,MPSMODE,EQ)
                         MVI  PARM,TRUE
                         MVI  NOMSG,TRUE
                         L    R1,APARM
                         L    R14,0(R1)
                         ST   R14,ATPTERM
                         L    R14,4(R1)
                         L    R15,8(R1)
                         L    R15,0(R15)
                         ICM  R15,8,=C' '
                         LA   R0,LINE
                         LA   R1,L'LINE
                         MVCL R0,R14
                         PEXIT
                    FI
                    L    R14,APARM
                    L    R14,0(R14)
                    LH   R15,0(R14)
                    IF   (LTR,R15,R15,NZ)
                         MVI  PARM,TRUE
                         MVI  NOMSG,TRUE
                         AAI  R14,2
                         LA   R0,LINE
                         LA   R1,L'LINE
                         ICM  R15,8,=C' '
                         MVCL R0,R14
                         LA   R0,LINE
                         PM   PUT
                         PEXIT
                    ELSE
                         MVI  PARM,FALSE
                    FI
               ELSEIF (CLI,PARM,TRUE,EQ)
                    MVI  EOF,TRUE
                    PEXIT
               ELSE
                    MVI  PARM,FALSE
               FI
         FI
         MVC   LINE,=CL80' '
         IF    (CLI,MODE,IOMODE,EQ)
               WRITE '?'
               GET   SYSIN,LINE
         ELSEIF (CLI,MODE,WTOMODE,EQ)
               MVI   WTORECB,0
               WTOR  ,LINE,80,WTORECB,MF=(E,WTORLIST)
               WAIT  ECB=WTORECB
         ELSE
*              WRITE '?'
*              LA   R1,LINE
*              ICM  R1,8,=X'80'
*              LA   R0,80
*              TGET  (1),(0),R
*
*  REPLACE TGET WITH FSRL AND SUPPORT PFKS
*
               L    R15,AFSF
               LA   R1,FSRLPARM
               LA   R0,FSRLCODE
               BALR R14,R15
               OC    LINE,=CL80' '
               DOCASE AID+3
                 CASE 1,2,3,4,5,6,7,8,9,10,11,12
                      ZR   R1
                      IC   R1,AID+3
                      MH   R1,=AL2(L'PFKENT)
                      LA   R1,PFKTAB-L'PFKENT(R1)
                      IF   (CLI,PFKMODE,TRUE,EQ)
                           MVI  PFKMODE,PFKDONE
                           MVC  0(L'PFKENT,R1),LASTLINE
                           LA   R1,=CL80'HK'
                      FI
                      MVC  LINE(L'PFKENT),0(R1)
                 ESAC
                 CASE 26
                      MVC LINE(3),=C'END'
                 ESAC
               ESACOD
*              IF    (LTR,R15,R15,NZ)
*                    LA   R0,=CL80'ERROR IN TGET - IGNORED'
*                    PM   PUT
*              FI
         FI
         IF    (CLI,LINE,C' ',EQ)
               MVC  LINE,LASTLINE
         ELSE
               MVC  LASTLINE,LINE
         FI
         IF    (CLI,MODE,IOMODE,EQ)
               LA    R0,LINE
               PM    PUT
         FI
         MVI   NOMSG,TRUE
         PEXIT
GETEOF   MVI   EOF,TRUE
         PEXIT
         TITLE 'PUT'
PUT      PENTRY
         LR    R1,R0
         TR    0(80,R1),PRINTTAB
         IF    (CLI,ATTN,TRUE,EQ)
               IF   (CLI,ABORT,TRUE,EQ)
                    SUBEXIT
               FI
               PEXIT
         FI
         MVI   NOMSG,FALSE
         IF    (CLI,MODE,MPSMODE,EQ)
               ST   R0,ATPMSG
               LA   R1,MPSPARM
               L    R15,ATPPUT
               BALR R14,R15
         ELSEIF (CLI,MODE,IOMODE,EQ)
               PUT  SYSPRINT
         ELSEIF (CLI,MODE,WTOMODE,EQ)
               LR   R1,R0
               MVC  WTODATA,0(R1)
               WTO  MF=(E,WTOLINE)
         ELSE
*              LR   R1,R0
*              ICM  R1,8,=X'00'
*              LA   R0,80
*              TPUT (1),(0),R
*
*  REPLACE TPUT WITH FSWL
*
               LR   R1,R0
               MVC  PUTLINE,0(R1)
               L    R15,AFSF
               LA   R1,FSWLPARM
               LA   R0,FSWLCODE
               BALR R14,R15
         FI
         PEXIT
         TITLE 'LITERALS AND EQUATES'
         EQUAL
         TITLE 'WORK AREA'
WORK     DS    0D
DCBLIST  OPEN  (,,,(OUTPUT)),MF=L
SYSIN    DCB   DSORG=PS,MACRF=GM,RECFM=F,BLKSIZE=80,DDNAME=SYSIN
SYSPRINT DCB   DSORG=PS,MACRF=PM,RECFM=F,BLKSIZE=80,DDNAME=SYSPRINT
*
* DATA FOR FSWR/FSWL
*
PUTLINE  DC    CL80' '
AID      DC    F'0'
ROW      DC    F'0'
COL      DC    F'0'
AFSF     DC    A(0)
FSRLPARM DC    4F'0'
FSWLPARM DC    F'0'
FSRLCODE EQU   7
FSWLCODE EQU   8
*
*  DATA FOR QD, QE, QN
*
QNAME    DC    CL8' '
RNAME    DC    CL44' '
RNAMELNG DC    F'0'
DEQLIST  DEQ   (0,0,0,SYSTEM),MF=L
ENQLIST  ENQ   (0,0,E,0,SYSTEM),MF=L
*
*  MISC. DATA
*
LINE     DC    CL80' '
LASTLINE DC    CL80' '
PFKMODE  LOGICAL FALSE,(TRUE,FALSE,PFKDONE)
HMODE    LOGICAL FALSE,(TRUE,FALSE)
PFKENT   DC    CL20' '
PFKTAB   SDSA
PFK1DEF   DC   CL20'DJ'
PFK2DEF   DC   CL20'DB'
PFK3DEF   DC   CL20'DA'
PFK4DEF   DC   CL20'DE'
PFK5DEF   DC   CL20'DI'
PFK6DEF   DC   CL20'DQ,SYS'
PFK7DEF   DC   CL20'S '
PFK8DEF   DC   CL20'R '
PFK9DEF   DC   CL20'P '
PFK10DEF  DC   CL20'HK'
PFK11DEF  DC   CL20'HM'
PFK12DEF  DC   CL20'HELP'
         EDSA
PFKLINE  DS    0CL80
         DC    C'PFK='
DPFKNO   DC    CL4' ',C'  DEF='
DPFK     DC    CL(L'PFKENT)' '
         FILL  PFKLINE
NOMSG    LOGICAL FALSE,(TRUE,FALSE)
EOF      LOGICAL FALSE,(TRUE,FALSE)
MODE     LOGICAL IOMODE,(IOMODE,WTOMODE,TSOMODE,MPSMODE)
PARM     LOGICAL FIRST,(FIRST,TRUE,FALSE)
VALID    LOGICAL FALSE,(TRUE,FALSE)
ATPPUT   DC   A(0)
MPSPARM  DS   0XL12
ATPTERM  DS   A
ATPMSG   DS   A
ATPLMSG  DS   X'80',AL3(0)
APARM    DC   A(0)
WTORECB  DC   F'0'
PWORK    DC   D'0'
WTORLIST WTOR 'MVSMON',MF=L
WTOLINE  DS   0F
         DC   AL2(WTOEND-*,0)
WTODATA  DC   CL80' '
WTOEND   EQU  *
TITLE1   DS   0CL80
         DC   C'MVSMON GU955000 VERSION='
         DC   C'&SYSDATE',C' '
         DC   C'&SYSTIME'
         FILL TITLE1
TITLE2   DS   0CL80
         DC   C'ENTER COMMAND OR HELP'
         FILL TITLE2
TIMELINE DS   0CL80
         DC   C'NOW='
TIMEDATE DC   CL22' ',C'  LAST IPL='
IPLTIME  DC   CL22' '
         FILL TIMELINE
*
*        INTERVAL PROCESSING DATA
*
WINT     DC    F'0'   INTERVAL FOR WAIT COMMAND
INTLAST  DC    F'0'   END OF LAST INTERVAL IN 0.01 SEC
INTBIN   DC    F'0'   INTERVAL IN 0.01 SEC
INTHOLD  LOGICAL FALSE,(TRUE,FALSE) DO NOT UPDATE INTERVAL INFO.
INTDONE  LOGICAL FALSE,(TRUE,FALSE) ONLY UPDATE ONCE PER INPUT
INTMSG   DS    0CL80
         DC    C'ELAPSED INTERVAL IS '
DINTSEC  DC    CL6' ',C' SECONDS ENDING '
DINTEND  DC    CL6' ',C' - TRACE INTERVAL IS'
DTRCINT  DC    CL6' ',C' MS'
         FILL  INTMSG
*
*        DISPLAY DATA AREAS
*
*
*        DA, DJ, DU, DS DISPLAY DATA
*
DALINE0  DS    0CL80
         DC    C'                          ---RATES--- -TRACE TOT-'
         FILL  DALINE0
DALINE1  DS    0CL80
         DC    C'                  DSP SWP CPU XIO PAG SVC SIO INT'
         DC    C'    T-CPU    T-IO T-MEM T-XEQ'
         FILL  DALINE1
DALINE2  DS    0CL80
         DC    C'JOBNAME  STEPNAME PRI CNT (%) SEC SEC TOT TOT TOT'
         DC    C' SSSSS.TH   COUNT  (K)    MIN'
         FILL  DALINE2
DALINE3  DS    0CL80
DAJOB    DC    CL8' ',C' '
DASTEP   DC    CL8' '
DADPG    DC    CL4' '
DASWP    DC    CL4' '
DARCPU   DC    CL4' '
DARIO    DC    CL4' '
DARPG    DC    CL4' '
DARSVC   DC    CL4' '
DARSIO   DC    CL4' '
DARINT   DC    CL4' '
DATCPU   DC    C' ZZZ99.99'
DATIO    DC    C' ZZZZZ99'
DATMEM   DC    C' ZZZ99'
DATXEQ   DC    C' ZZZ99'
         FILL  DALINE3
*
*        DC DATA
*
DCLINE   DS    0CL80
         DC    C'TOTAL CPU USAGE  CPU0='
DCDCPU0  DC    CL4' ',C'%  CPU'
DCPUID1  DC    C'1='             CHANGED TO 2 FOR 3081G
DCDCPU1  DC    CL4' ',C'%  TOTAL='
DCDCPUT  DC    CL4' ',C'%'
         FILL  DCLINE
DCLINE2  DS    0CL80
         DC    CL12'CHANNEL'
DCCHAN   DC    CL4' ',C' SIOS/S='
DCSIOS   DC    CL4' ',C'  BUSY='
DCBUSY   DC    CL4' ',C'%'
         FILL  DCLINE2
DCSCPU0W DC    F'0'
DCSCPU1W DC    F'0'
DCSCPU0I DC    F'0'
DCSCPU1I DC    F'0'
DCBINT   DC    F'0'
DCNTSAMP DC    XL8'00' LAST TOTAL SAMPLES AND INTERVAL SAMPLES
         DS    0D
DCNCHANS DC    XL(16*16)'00' LSIO, ISIO, LBUSY, IBUSY
*
*        DD DATA
*
DDLINE   DS    0CL80
DDUCB    DC    CL3' ',C' '         UCB
DDVOL    DC    CL6' ',C' MSTAT '   VOLUME
DDMSTAT  DC    CL3' ',C' USTAT '   R-RESERVED P-PERM MOUNT A-ALLOCATED
DDUSTAT  DC    CL2' ',C' DCBS'     PV-PRIVATE PB-PUBLIC ST-STORAGE
DDDCBS   DC    CL4' '              OPEN DCBS
         FILL  DDLINE
*
*        DE DATA
*
DELINE2  DS    0CL80
DEUCB2   DC    CL3' ',C' '
DESNS2   DC    CL40' '
         FILL  DELINE2
DELINE3  DS    0CL80
DEAJOB   DC    CL8' ',C' '
DEAMSG   DC    CL40' '
         FILL  DELINE3
*
*        DI DATA
*
DISIOTAB DC    A(0)
DISCHN   DC    CL3' '
DIECHN   DC    CL3' '
DICHN    DC    CL3' '
DILINE1  DS    0CL80
         DC    C'UCB RANGE S='
DISUCB   DC    C'000',C' E='
DIEUCB   DC    C'FFF',C'  SIOS MINIMUM M='
DIMSIOS  DC    C'05'
         FILL  DILINE1
DILINE2  DS    0CL80
DIUCB    DC    CL3' ',C' '
DIVOL    DC    CL6' ',C' '
DIJOB    DC    CL8' ',C'  SIO='
DISIO    DC    CL7' ',C'  SIO/SEC='
DISIOS   DC    CL4' '
         FILL  DILINE2
DILINE3  DS    0CL80
         DC    C'RANGE TOTAL SIO='
DIRSIO   DC    CL10' ',C'   SIO/SEC='
DIRSIOS  DC    CL4' '
         FILL  DILINE3
EBCBIN   DC    256X'00'
         ORG   EBCBIN+C'A'
         DC    AL1(10,11,12,13,14,15)
         ORG   EBCBIN+C'0'
         DC    AL1(0,1,2,3,4,5,6,7,8,9)
         ORG
PRINTTAB TRTTAB PRINT
*
*        DL DATA
*
DLLINE1  DS    0CL80
DLUCB    DC    CL3' ',C' '
DLMSG    DC    CL20' '
         FILL  DLLINE1
*
*        DK DATA
*
DKLINE   DS    0CL80
DKNAME   DC    CL8' ',C' '
DKUSE    DC    CL4' '
         FILL  DKLINE
*
*        DM DATA
*
DMLINE   DS    0CL80
         DC    C'MOUNT PENDING FOR '
DMUCB    DC    CL3' ',C' '
DMVOL    DC    CL6' '
         FILL  DMLINE
*
*        DN DATA
*
DNFRTCB  DC    F'0'
DNPASCB  DC    PL2'0'
DNPJOB   DC    PL2'0'
DNPTSO   DC    PL2'0'
DNPSTC   DC    PL2'0'
DNPUCB   DC    PL2'0'
DNPDISK  DC    PL2'0'
DNPTAPE  DC    PL2'0'
DNPPORT  DC    PL2'0'
DNPUR    DC    PL2'0'
DNPOTHER DC    PL2'0'
DNLINE1  DS    0CL80
         DC    C'TOTAL ASCBS='
DNDASCB  DC    CL4' ',C'  READY TCBS='
DNRTCB   DC    CL4' ',C'  JOBS='
DNJOB    DC    CL4' ',C'  USERS='
DNTSO    DC    CL4' ',C'  STCS='
DNSTC    DC    CL4' '
         FILL  DNLINE1
DNLINE2  DS    0CL80
         DC    C'ONLINE UCBS='
DNDUCB   DC    CL4' ',C'  DISKS='
DNDISK   DC    CL4' ',C'  TAPES='
DNTAPE   DC    CL4' ',C'  PORTS='
DNPORT   DC    CL4' ',C'  UR='
DNUR     DC    CL4' ',C'  OTHER='
DNOTHER  DC    CL4' '
         FILL  DNLINE2
*
*        DP DATA
*
DPLINE   DS    0CL80
         DC    C'TOTAL PAGING DMD='
DPDPAGE  DC    CL4' ',C' P/S  VIO='
DPDVIO   DC    CL4' ',C' P/S  SWP='
DPDSWAP  DC    CL4' ',C' P/S  XCOM='
DPDXCOM  DC    CL4' ',C' MSG/S'
         FILL  DPLINE
         DS    0F
DPSPAGE  DC    XL8'00'
DPSVIO   DC    XL8'00'
DPSSWAP  DC    XL8'00'
*
*        DQ DISPLAY DATA
*
TYPE     LOGICAL SHARE,(SHARE,EXCLUDE)
DQMODE   LOGICAL DQWAIT,(DQWAIT,DQALL,DQSYS,DQXY)
DQMAJ    DC    CL8' '
DQMIN    DC    CL44' '
DQMAJCL  DC    A(0,0)
DQMINCL  DC    A(0,0)
JOBNAME  DC    CL8' '
DQLINE1  DS    0CL80
DQ1WJOB  DC    CL8' ',C' WAITING ON '
DQ1OJOB  DC    CL8' ',C' FOR '
DQ1QNAME DC    CL8' ',C' '
DQ1RNAME DC    CL30' '
         FILL  DQLINE1
DQLINE2  DS    0CL80
DQ2JOB   DC    CL8' ',C' '
DQ2QNAME DC    CL8' ',C' '
DQ2RNAME DC    CL30' ',C' '
DQ2OWNER DC    CL5' ',C' '
DQ2TYPE  DC    CL9' '
         FILL  DQLINE2
*
*        DR DATA
*
DRLINE   DS    0CL80
         DC    C'WTOR REPLIES OUTSTANDING='
DRWTORS  DC    CL4' '
         FILL  DRLINE
*
*        DT DATA
*
DTLINE   DS    0CL80
DTUCB     DC    CL3' ',C' '
DTVOL     DC    CL6' ',C' '
DTJOB     DC    CL8' ',C' SIO='
DTSIO     DC    CL7' ',C' TERR='
DTTERR    DC    CL4' ',C' PERR='
DTPERR    DC    CL4' ',C' '
DTMSG     DC    CL25' '
         FILL  DTLINE
*
*        DV
*
DVDATA   DS    0H       DATA COLLECTED BY DV1 AND DV2
DVSCSA   DC    H'0'     VBN OF CSA
DVSLPA   DC    H'0'     VBN OF LPA
DVSSQA   DC    H'0'     VBN OF SQA
DVHRPVT  DC    H'0'     REAL PVT
DVHRCSA  DC    H'0'          CSA
DVHRLPA  DC    H'0'          LPA
DVHRSQA  DC    H'0'          SQA
DVHFPVT  DC    H'0'     FIXED PVT
DVHFCSA  DC    H'0'           CSA
DVHFLPA  DC    H'0'           LPA
DVHFSQA  DC    H'0'           SQA
DVBREAL  DC    H'0'     TOTAL REAL
DVBFREE  DC    H'0'           FREE
DVBALLOC DC    H'0'           ALLOC
DVBFIXED DC    H'0'           FIXED
DVBBAD   DC    H'0'           FLAGGED AS BAD AND UNUSABLE
DVBOFF   DC    H'0'           OFFLINE
DVBVIO   DC    H'0'           IN USE FOR VIO
DVBVR    DC    H'0'           ALLOCATED FOR V=R REGION
LDVDATA  EQU   *-DVDATA
DVLINE1  DS    0CL80
         DC    CL7'VIRTUAL',C' NUC='
DVVNUC   DC    CL6' ',C'K PVT='
DVVPVT   DC    CL6' ',C'K CSA='
DVVCSA   DC    CL6' ',C'K LPA='
DVVLPA   DC    CL6' ',C'K SQA='
DVVSQA   DC    CL6' ',C'K'
         FILL  DVLINE1
DVLINE2  DS    0CL80
         DC    CL7'REAL',C' NUC='
DVRNUC   DC    CL6' ',C'K PVT='
DVRPVT   DC    CL6' ',C'K CSA='
DVRCSA   DC    CL6' ',C'K LPA='
DVRLPA   DC    CL6' ',C'K SQA='
DVRSQA   DC    CL6' ',C'K'
         FILL  DVLINE2
DVLINE3  DS    0CL80
         DC    CL7'FIXED',C' NUC='
DVFNUC   DC    CL6' ',C'K PVT='
DVFPVT   DC    CL6' ',C'K CSA='
DVFCSA   DC    CL6' ',C'K LPA='
DVFLPA   DC    CL6' ',C'K SQA='
DVFSQA   DC    CL6' ',C'K'
         FILL  DVLINE3
DVLINE4  DS    0CL80
         DC    CL7'TOTALS',C' REAL='
DVTREAL  DC    CL6' ',C'K  FREE='
DVTFREE  DC    CL6' ',C'K  ALLOC='
DVTALLOC DC    CL6' ',C'K  FIXED='
DVTFIXED DC    CL6' ',C'K'
         FILL  DVLINE4
DVLINE5  DS    0CL80
         DC    CL7'TOTALS',C' BAD='
DVTBAD   DC    CL6' ',C'K  OFFLINE='
DVTOFF   DC    CL6' ',C'K  VIO='
DVTVIO   DC    CL6' ',C'K  V=R='
DVTVR    DC    CL6' ',C'K'
         FILL  DVLINE5
*
*        DW DATA
*
DWLINE   DS    0CL80
DWUCB    DC    CL3' ',C' '
DWVOL    DC    CL6' ',C' I/O PENDING'
         FILL  DWLINE
*
*        DZ DATA
*
DZLINE   DS    0CL80
DZTYP    DC    CL3' ',C' '
DZJOB    DC    CL8' ',C' '
DZPSW    DC    CL16' ',C' '
DZCSW    DC    CL16' ',C' '
DZREGS   DC    CL16' ',C' '
DZTCBTM  DC    CL16' '
         FILL  DZLINE
         DC    C' ' PAD FOR UNPK AND TR CODE
DZSLINE1 DS    0CL80
         DC    C'TYPE TOTAL ERRORS'
         FILL  DZSLINE1
DZSLINE2 DS    0CL80
         DC    C' '
DZSTYP   DC    CL3' '
DZSTOT   DC    CL6' ZZZ99',C' '
DZSERR   DC    CL6' ZZZ99'
         FILL  DZSLINE2
BLANK    DC    CL80' '
DZSLINE3 DS    0CL80
         DC    C'SVC NO.  TOTAL'
         FILL  DZSLINE3
DZSLINE4 DS    0CL80
         DC    CL3' '
DSVCNO   DC    CL4' ',CL3' '
DSVCTOT  DC    CL4' '
         FILL  DZSLINE4
SVCTAB   DC    A(0)
PSVCNO   DC    PL2'0'
*
*        DATA FOR MEMORY USAGE BY DOMAIN
*
         DS    0F
DOMAINK  DC    XL(16*2)'00'  16 DOMAIN PAGE FRAME COUNTERS
LDOMKE   EQU   2
DOMLINE  DS    0CL80
DOMDESC  DC    CL6'DOMAIN',C'='
DOMN     DC    CL4' ',C'  '
DOMK     DC    CL6' ',C'K  '
DOMPCT   DC    CL4' ',C'%'
         FILL  DOMLINE
*
*        CPU USAGE RATE BY ASCB AND DOMAIN DATA
*
RCTABPTR DC    A(0)   POINTER TO ASCB INTERVAL CPU USAGE TABLE
TTETAB   DC    A(0,0,0) TTE TABLE POINTERS (START, END, FIRST)
TTENTAB  DC    CL48'SIOEXTSVCPGMISDI/OSRRDSP' TRACE ENTRY TYPE NAMES
TTECTAB  DS    XL(4*16)  TRACE SUMMARY COUNTS
TTEETAB  DS    XL(4*16)   TRACE ERROR   COUNTS
TRCINT   DC    F'0'   TRACE TABLE INTERVAL IN MS
RCT     SDSA
RCTTCPU  DS    F  TOTAL TCB AND SRB CPU
RCTICPU  DS    F  INTERVAL CPU
RCTTIO   DS    F  TOTAL EXCP IO
RCTIIO   DS    F  INTERVAL EXCP IO
RCTTPG   DS    F  TOTAL PAGING
RCTIPG   DS    F  INTERVAL PAGING
RCTCTAB   SDSA
RCTCSIO    DS  X  SIO COUNTS FROM TRACE TABLE
RCTCEXT    DS  X  EXTERNAL INTERRUPTS
RCTCSVC    DS  X  SVC
RCTCPGM    DS  X  PGM INTERRUPTS
RCTCISD    DS  X  INITIAL SRB DISPATCH
RCTCIO     DS  X  IO INTERRUPTS
RCTCSRR    DS  X  SRB
RCTCDSP    DS  X  TASK DISPATCH
         EDSA
        EDSA
         DS    0F
DOMAINR  DC    XL(16*8)'00' 16 DOMAIN INTERVAL ASCBS AND CPU USAGE
LDOMRE   EQU   8
RCLINE2  DS    0CL80
         DC    C'DOMAIN='
DRCDMN   DC    CL4' ',C'  TOT-ASCBS='
DRCDMNAS DC    CL4' ',C'  RDY-ASCBS='
DRCDMNAR DC    CL4' ',C'  CPU='
DRCDMNP  DC    CL4' ',C'%  '
DRCDMNK  DC    CL6' ',C'K'
         FILL  RCLINE2
RCLINE3  DS    0CL80
DRCSYST  DC    C'SYSTEM',CL39' ',C'CPU='
DRCSYSC  DC    CL4' ',C'% '
         FILL  RCLINE3
*
*        DATA FOR PD - PAGE DSN INFO.
*
PDHEAD1  DS    0CL80
         DC    CL10'     SLOTS'
         DC    CL10'       VIO'
         DC    CL10'   NON-VIO'
         DC    CL10'    UNUSED'
         DC    CL10'    UNRESV'
         DC    CL10'  SWAP-SET'
         FILL  PDHEAD1
PDTLINE  DS    0CL80
PDSLOTS  DC    CL10' '
PDVSC    DC    CL10' '
PDNVSC   DC    CL10' '
PDUNUSED DC    CL10' '
PDUNRESV DC    CL10' '
PDSETSZ  DC    CL10' '
         FILL  PDTLINE
PDHEAD2  DS    0CL80
         DC    CL15'--- DSNAME ----',C' '
         DC    CL6'VOLUME',C' '
         DC    CL3'UCB',C' '
         DC    CL6'TYPE'
         DC    CL7'  SLOTS'
         DC    CL7'  AVAIL'
         DC    CL7' WEIGHT'
         DC    CL7' ERRORS'
         FILL  PDHEAD2
PDPSLINE DS    0CL80
PDDSN    DC    CL15' ',C' '
PDVOL    DC    CL6' ',C' '
PDUCB    DC    CL3' ',C' '
PDTYPE   DC    CL6' '
PDSIZE   DC    CL7' '
PDAVAIL  DC    CL7' '
PDWEIGHT DC    CL7' '
PDERRORS DC    CL7' '
         FILL  PDPSLINE
*
*        DATA FOR  SRM DISPLAY
*
SLINE1   DS    0CL80
         DC    C'SRM '
DSTYP    DC    CL3' ',C' LOW='
DSLOW    DC    CL4' ',C' HIGH='
DSHIGH   DC    CL4' ',C' AVG='
DSAVG    DC    CL4' '
         FILL  SLINE1
*
*        DATA FOR XCOM DISPLAY
*
XCOMRATE DC    A(0,0) TOTAL AND INTERVAL XCOM COUNTS
DXCOM1   DS    0CL80
         DC    C'XCOM TOTALS - RCUR='
DRCUR    DC    CL7' ',C'  RMAX='
DRMAX    DC    CL7' ',C'  RTOT='
DRTOT    DC    CL7' '
         FILL  DXCOM1
DXCOM2   DS    0CL80
         DC    C'XCOM  PATHS - PID='
DPATH    DC    CL4' ',C' '
DPATHJOB DC    CL8' '
         FILL  DXCOM2
DXCOM3   DS    0CL80
         DC    C'XCOM   MSGS - PID='
DPID     DC    CL4' ',C'  TID='
DTID     DC    CL4' ',C'  ME#='
DME#     DC    CL7' '
         FILL  DXCOM3
*
*        DATA AND ROUTINES FOR PROGRAM INTERRUPTION HANDLER
*
         DROP
PGMCHK1  TAG                       STAGE 1 PGMCHK HANDLER
         USING *,R15
         MVC   PGMPSW,4(R1)        SAVE PSW
         LA    R15,PGMCHK2
         STCM  R15,7,9(R1)         CHANGE PSW TO EXIT TO STAGE 2
         BR    R14                 EXIT TO SUPVR FROM STAGE 1
PGMCHK2  BALR  R15,0               STAGE 2 PGMCHK HANDLER
         USING *,R15
         LM    R0,R15,PGMCHKSA
         BR    R15                 EXIT TO STAGE 3 AT END OF MAINLINE
PGMCHKSA DC    16F'0'
SAVEPICA DC    A(0)
SPIELIST SPIE  0,((1,15)),MF=L
         DC    CL8'*PGMPSW*'
PGMPSW   DC    D'0'
DWORK    DC    CL7' '
TUCBCHAN DC    H'0'    BINARY UCB ADDRESS XYZ
TUCBNAME DC    CL3' '  EBCDIC UCB ADDRESS XYZ (SEE TUCBCALC)
HEX      DC    C'0123456789ABCDEF'
PGMMSG   DS    0CL80
         DC    C'PROGRAM CHECK AT '
DPGMCHK  DC    CL6' '
         FILL  PGMMSG
*
*        TSO ATTENTION EXIT AND RELATED DATA AREAS
*
STAXRTN  TAG
         USING *,R15
         IF    (CLI,ATTN,TRUE,NE)
               MVI  ATTN,TRUE        REQ. SURPRESS REST OF COMMAND
         ELSEIF (CLI,ABORT,TRUE,NE)
               MVI  ABORT,TRUE       REQ. IMMEDIATE SUBEXIT
         ELSE
               ABEND 122,DUMP        DUMP IF THE ABOVE DON'T WORK
         FI
         BR    R14
         DROP  R15
ATTN     LOGICAL FALSE,(TRUE,FALSE)
ABORT    LOGICAL FALSE,(TRUE,FALSE)
STAXLIST STAX  MF=L
STAXMSG  DC    CL80'MVSMON COMMAND ABORTED BY ATTN KEY'
*
*        END OF WORK AREA CSECT
*
LWORK    EQU   *-WORK
         TITLE 'DSECTS'
*
*        NOTE FOLLOWING QCB MAJOR AND MINOR HAVE BEEN REPLACED WITH A
*        COMBINED QCB IN GRS ADDR. SPACE. SEE V3 PG 147 IN MVS/SP
*
*              ENQ/DEQ CONTROL BLOCKS  PG 549 OLD MVS 3.8 ONLY
*
*
*              MAJOR QCB
*
QCBMAJ   DSECT
QMJNEXT  DS    A    NEXT MAJOR QCB
QMJPREV  DS    A    PREVIOUS MAJOR QCB
QMJMINOR DS    A    FIRST MINOR QCB
QMJLMIN  DS    A    LAST  MINOR QCB ADDED IN MVS *****************
QMJNAME  DS    CL8  MAJOR QCB NAME
QMJLNE   EQU   *-QCBMAJ
*
*              MINOR QCB              OLD MVS 3.8
*
QCBMIN   DSECT
QMNNEXT  DS    A NEXT MINOR
QMNPREV  DS    A PREVIOUS MINOR
QMNQEL   DS    A FIRST QEL
QMNLQEL  DS    A LAST  QEL
QMNLNM   DS    X LENGTH OF RNAME
QMNFLAGS DS    X
QMNASID  DS    H ASID NUMBER FOR STEP ONLY
QMNNAME  DS    C
*
*              QUEUE ELEMENT        OLD MVS 3.8 (SEE V3 PG 150 FOR SP)
*
QEL      DSECT
QELNQEL  DS    A  NEXT QEL
QELLQEL  DS    A  PREVIOUS QEL
QELTCB   DS    A  TCB
QELQFLGS DS    X  QEL FLAGS
QELSHAR  EQU   X'80'
QELLFLGS DS    X  QEL LIST FLAGS
QELASID  DS    H  ASID OF REQUESTOR
QELSVRB  DS    A  SVRB
QELLCNT  DS    H  # ACTIVE QEL COUNT
QELWCNT  DS    H  # QELS WAITING
         TITLE 'SYSTEM CONTROL BLOCK EQUATES'
*
*        ASVT     V2 PG 67
*
ASVTMAXU EQU  X'204'       NUMBER OF ASCB ENTRIES
ASVTENTY EQU  X'210'       START  OF ASCB ENTRIES
*
*        ASCB     V2 PG 32
*
ASCBASID EQU  X'24'        ASID NUMBER
ASCBDP   EQU  X'2B'        DISPATCHING PRIORITY
ASCBCSCB EQU  X'38'        COMMAND SCHED. CONTROL BLOCK CHAIN
ASCBTSB  EQU  X'3C'        TERMINAL STATUS BLOCK FOR TSO ONLY
ASCBEJST EQU  X'40'        UNSIGNED 64 BIT TCB CPU TIME
ASCBRCTF EQU  X'66'        RCT FLAGS
ASCBOUT  EQU  X'04'        ASCB SWAPPED OUT
ASCBFLG1 EQU  X'67'        FLAG FIELD
ASCBDSP1 EQU  X'72'        DISPATCH FLAG FIELD
ASCBOUCB EQU  X'90'        SRM USER CONTROL BLOCK
ASCBOUXB EQU  X'94'        SRM USER EXTENSION BLOCK
ASCBFMCT EQU  X'98'        PAGE FRAME COUNT - WORKING SET
ASCBJBNI EQU  X'AC'        JOBNAME OF INITS
ASCBJBNS EQU  X'B0'        JOBNAME OF STCS
ASCBSRBT EQU  X'C8'        SRB CPU TIME
ASCBTCBS EQU  X'D8'        COUNT OF READY TCBS
*
*        ASMVT   AUX. STORAGE TABLE  V2 PG 48
*
ASMSART  EQU  X'04'          SWAP ACTIVITY TABLE
ASMPART  EQU  X'08'          PAGE ACTIVITY TABLE
ASMBKSLT EQU  X'6C'          UNRESERVED SLOTS
ASMSLOTS EQU  X'70'          TOTAL SLOTS
ASMVSC   EQU  X'74'          ALLOC VIO SLOTS
ASMNVSC  EQU  X'78'          ALLOC NON-VIO SLOTS
*
*        CDE   V2 PG 121
*
CDCHAIN  EQU  X'00'         NEXT CDE
CDNAME   EQU  X'08'         NAME
CDENTPT  EQU  X'10'         LOAD MODULE ENTRY POINT
CDUSE    EQU  X'18'         USE COUNT
*
*        CSCB  V2 PG 142
*
CSCBPTR  EQU  X'00'         NEXT CSCB
CSCBJOB  EQU  X'08'         JOB NAME
CSCBSTEP EQU  X'40'         STEP NAME
*
*        CSD   V2 PG 154
*
CSDCPUAL EQU  X'08'        MASK OF AVAILABLE CPUS
*
*        CVT    V2 PG 166
*
CVTILK2  EQU  X'028'        UCB HALFWORD ADDR LIST
CVTCUCB  EQU  X'064'        UCM FOR MCS
CVTNUCB  EQU  X'080'        SIZE OF NUC
CVTMZ00  EQU  X'0A4'        SIZE OF VIRTUAL STORAGE
CVTQLPAQ EQU  X'BC'         POINTER TO ACTIVE LPA CDE LIST
CVTSMCA  EQU  X'0C4'        SMF COMMON AREA
CVTUSER  EQU  X'0CC'        POINTER TO FPC USER CVT
CVTPVTP  EQU  X'164'        PAGE VECTOR TABLE
CVTLPDIA EQU  X'168'        START OF LPA (SEE VOL1 5-64)
CVTSHRVM EQU  X'1A0'        START OF CSA
CVTASCVT EQU  X'22C'        ADDRESS SPACE VECTOR TABLE
CVTGDA   EQU  X'230'        GLOBAL DATA AREA
CVTOPCTP EQU  X'25C'        SRM CONTROL TABLE (RMCT)
CVTFQCB  EQU  X'280'   198  FIRST QCB (OLD MVS 3.8 NOT USED IN SP)
CVTCSD   EQU  X'294'        COMMON SYSTEM DATA
CVTASMVT EQU  X'2C0'        AUX. STORAGE TABLE
CVTLCCAT EQU  X'300'        LOGICAL CPU ADDR TABLE
CVTMFCTL EQU  X'31C'        STGST FOR MF/1 OR RMF
CVTRLSTG EQU  X'358'        REAL STORAGE IN K
*
*        DCB  V2 PG 212
*
DCBEODAD EQU  X'20'        END OF FILE ADDRESS
*
*        DQE  V2 PG 329
*
DQEBLKAD EQU  X'08'        START OF BLOCK
DQELNTH  EQU  X'0C'        LENGTH OF BLOCK
*
*        ECCDB  MF/RMF CHANNEL EVENT DATA   (SEE RMF MANUAL)
*
ECCDSIOS EQU  X'08'        TOTAL SIO COUNT
ECCDBUSY EQU  X'0C'        TOTAL CHANNEL BUSY SAMPLES
ECCDBL   EQU  X'30'        LENGHT OF CHANNEL ENTRY (WAS X'14')
*
*        ECCEDT  MF/RMF CHANNEL EVENT DATA TABLE
*
ECCECPEQ EQU  X'00'        POINTER TO ECCPE FOR CPUS
ECCECPUS EQU  X'09'        NUMBER OF CPUS
ECCESAMP EQU  X'0C'        TOTAL CHANNEL SAMPLES
*
*        ECCPE MF/RMF CPU STATUS DATA
*
ECCPCNUM EQU  X'02'        NUMBER OF CHANNEL ECCDB ENTRIES
ECCPCDBQ EQU  X'04'        POINTER TO ECCDB TABLE
LECCPE   EQU  X'14'        LENGTH OF ECCPE ENTRY
*
*        GDA   V2 PG 393
*
GDAFLAGS EQU  X'00'        SYSTEM FLAGS
GDASQASP EQU  X'18'        SQA SPQE
*
*        LCCVT V2 PG 523
*
LCCAT00P EQU  X'00'        CPU 0 LCCA POINTER
LCCAT01P EQU  X'04'        CPU 1 LCCA POINTER
LCCAT02P EQU  X'08'        CPU 2 LCCA POINTER (FOR 3081G)
*
*        LCCA  V2 PG 509
*
LCCAWTIM EQU  X'268'       CPU ACCUMULATED WAIT
*
*        XCOM MESSAGE HEADER CONTROL BLOCKS  (FPC LOCAL MODS)
*
MHBNEXT  EQU  X'00'        NEXT MHB
MHBPID   EQU  X'04'        DESTINATION PATH ID FOR MESSAGE
MHBTID   EQU  X'08'        DESTINATION TERM ID FOR MESSAGE OR ZERO
MHBME#   EQU  X'0E'        CURRENT MESSAGES QUEUED TO THIS MHB (H)
*
*        OUCB V3 PG 17  (NOTE MANY CHANGES FROM MVS 3.8)
*
OUCBNPG  EQU  X'B8'        PERFORMANCE GROUP
OUCBDMN  EQU  X'28'        PERFORMANCE DOMAIN
OUCBTMO  EQU  X'4C'        TRANSACTION START TIME
OUCBWSS  EQU  X'52'        WORKING SET PAGES AT LAST SWAP IN
OUCBIOSM EQU  X'6C'        SMF EXCP COUNT
OUCBPSS  EQU  X'74'        CPU PAGE SECONDS
OUCBSWC  EQU  X'2A'        TRANSACTION SWAP COUNT
*
*        OUXB SRM USER EXTENSION BLOCK  V3 PG 28
*
OUXBPIN  EQU  X'10'        INTERVAL PAGE IN
OUXBPOUT EQU  X'14'        INTERVAL PAGE OUT
*
*        PART/SART PAGE AND SWAP ACTIVITY TABLES  V3 PG 32
*
PARTSIZE EQU  X'04'        NUMBER OF DSNS
PARTDSNL EQU  X'18'        LIST OF DSN NAMES
PARTENTS EQU  X'50'        START OF PARTE/SARTE
LPARTE   EQU  64
LDSN     EQU  44
*
*        PARTE/SARTE DSN ENTRIES V3 PG 32
*
PARETYPE EQU  X'08'        TYPE INDICATORS
PAREFLG1 EQU  X'09'        DAMAGE FLAG
PARESZSL EQU  X'10'        TOTAL SLOTS
PARESLTA EQU  X'14'        ALLOCATED SLOTS
PARERQTM EQU  X'38'        WEIGHT FACTOR BASED ON USAGE
PARERRCT EQU  X'18'        PERM. I/O ERROR COUNT
PAREUCBP EQU  X'2C'        UCB POINTER
*
*        XCOM PATH CONTROL BLOCK (SEE SR301000-SR303000)
*
PCBNEXT  EQU  X'00'        NEXT PCB IN CHAIN
PCBPID   EQU  X'04'        NAME OF PATH
PCBJOBN  EQU  X'14'        NAME OF JOB WHICH CREATED PATH
*
*        PFTE V3 PG 78
*
PFTASID  EQU  X'00'        ASID OF OWNER
PFTVBN   EQU  X'02'        VBN (HIGH ORDER 12 BITS OF ADDR
PFTFXCT  EQU  X'08'        FRAME FIX COUNT
PFTFLAG1 EQU  X'0C'        FIRST FLAG BYTE
PFTONAVQ EQU  X'80'          AVAILABLE
PFTLSQA  EQU  X'20'          LSQA/SQA
PFTBADPG EQU  X'04'          BAD PAGE
PFTVRALC EQU  X'02'          V=R ALLOCATED
PFTFLAG2 EQU  X'0D'        SECOND FLAG BYTE
PFTOFFLN EQU  X'40'          OFFLINE
PFTIRRG  EQU  X'10'          VIO PAGE
LPFTE    EQU  16
*
*        PSA V3 PG 92
*
FLCTRACE EQU  X'54'        TRACE TABLE HEADER POINTER
PSATOLD  EQU  X'224'       CURRENT ASCB
*
*        PVT V3 PG 128
*
PVTPOOL  EQU  X'008'       REAL PAGE FRAMES AVAIL. NOW
PVTPFTP  EQU  X'00C'       APPARENT ORIGIN OF PFTE
PVTFPFN  EQU  X'010'       RBN TO FIRST PFTE
PVTLPFN  EQU  X'012'       RBN TO LAST  PFTE
PVTNPI   EQU  X'0F8'       DEMAND IN
PVTNPOUT EQU  X'0FC'       DEMAND OUT
PVTVAMI  EQU  X'100'       VIO IN
PVTVAMO  EQU  X'104'       VIO OUT
PVTSPIN  EQU  X'10C'       SWAP IN
PVTSPOUT EQU  X'110'       SWAP OUT
PVTCFMCT EQU  X'12C'       CURRENT PAGABLE CSA/LPA REAL PAGES
PVTCNTFX EQU  X'726'       TOTAL FIXED
PVTSQAFX EQU  X'728'       SQA FIXED
PVTCOMFX EQU  X'72A'       CSA FIXED
*
*        RCT RESOURCE CONTROL TABLE V3 PG 183  (SEVERAL CHANGES)
*
RCCUICTL EQU  X'04'        UIC LOW
RCCUICTH EQU  X'06'        UIC HIGH
RCCCPUTL EQU  X'08'        CPU LOW
RCCCPUTH EQU  X'0A'        CPU HIGH
RCCPTRTL EQU  X'0C'        PAG LOW
RCCPTRTH EQU  X'0E'        PAG HIGH
RCCASMTL EQU  X'10'        ASM LOW
RCCASMTH EQU  X'12'        ASM HIGH
RCVUICA  EQU  X'3E'        UIC AVG
RCVCPUA  EQU  X'40'        CPU AVG
RCVASMQA EQU  X'46'        AMS AVG
RCVPTR   EQU  X'48'        PAG AVG
*
*        SRM RESOURCE CONTROL TABLE V3 PG 202
*
RMCTTBS  EQU  X'78'        SRM TOD BASE
RMCTRCT  EQU  X'E4'        ADDRESS OF RCT
*
*        PART/SART PAGE AND SWAP ACTIVITY TABLES  V3 PG 314
*
SARSIZE  EQU  X'04'        NUMBER OF DSNS
SARDSNL  EQU  X'18'        LIST OF DSN NAMES
SARSETSZ EQU  X'38'        SWAP SET SIZE
SARENTS  EQU  X'50'        START OF PARTE/SARTE
LSARTE   EQU  48
*
*        PARTE/SARTE DSN ENTRIES V3 PG 315
*
SREFLG   EQU  X'09'        DAMAGE FLAG
SRETOTSL EQU  X'10'        TOTAL SLOTS
SREAVLSL EQU  X'14'        AVAIL. SLOTS
SRERRCT  EQU  X'18'        PERM. I/O ERROR COUNT
SREUCB   EQU  X'2C'        UCB POINTER
*
*        SMCA V3 PG 373
*
SMCANMSU EQU  X'68'        TIMES TASK WAITED FOR BUFFER
SMCADSCT EQU  X'74'        LOST SMF RECORD COUNT
*
*        SPQE V3 PG 400
*
SPDQEPTR EQU  X'04'        DQE PTR
*
*        STGST  MF/RMF GLOBAL DATA AREA  (SEE RMF)
*
STGSMMV  EQU  X'10'        POINTER TO STMMV MFROUTER CALL INFO TABLE
*
*        TTE - TRACE TABLE ENTRY V3 PG 533
*
TTEPSW   EQU  X'00'            PSW
TTESIOCC EQU  X'00'            SIO CONDITION CODE
TTETYP   EQU  X'02'            FIRST 4 BITS OF THIRD BYTE PSW
TTECSW   EQU  X'08'            CHANNEL STATUS FOR SIO AND I/O
TTEIOUS  EQU  X'0C'            I/O UNIT STATUS (BIT 6-UC,7-UE)
TTEIOCS  EQU  X'0D'            I/O CHANNEL STATUS
TTEREGS  EQU  X'10'            PARMS DEPENDING ON TYPE
TTEASID  EQU  X'16'            ASID OF CURRENT TASK
TTETCBTM EQU  X'18'            TCB ADDRESS AND TIMER VALUE
TTETIME  EQU  X'1C'            TIME
LTTE     EQU  32
*
*        UCM V3 PG 604
*
UCMRQNR  EQU  X'038'           OUTSTANDING REQUESTS
*
*        UCB - UNIT CONTROL BLOCK V3 PG 559
*
UCBSTAT  EQU  X'03'
UCBCHAN  EQU  X'04'
UCBFLA   EQU  X'06'        FLAG BYTE A
UCBNAME  EQU  X'0D'
UCBTYP   EQU  X'10'
UCBFLC   EQU  X'14'        FLAG BYTE C
UCBEXTPT EQU  X'14'        UCB COMMON EXTENSION
UCBVOLI  EQU  X'1C'
UCBSTAB  EQU  X'22'
UCBDMCT  EQU  X'23'
UCBXTN   EQU  X'2C'        UCB MAG. TAPE EXTENSION
*
*        FPC USER CVT FOR LOCAL MODIFICATIONS TO MVS
*
UCVTIPL  EQU  X'08'        TIME AND DATE OF IPL (TIME DEC)
UCVTXCVT EQU  X'84'        POINTER TO XCOM CVT (SEE SR301000)
*
*        UCB COMMON EXTENSION
*
UCBMFCNT EQU  X'0C'        MF TOTAL SIO COUNT
UCBASID  EQU  X'0E'        ASID OF LAST MEMORY ALLOCATED
*
*        UCB MAG. TAPE EXTENSION
*
UCBTREAD EQU  X'04'        TEMPORARY READ ERRORS
UCBTWRIT EQU  X'05'        TEMPORARY WRITE ERRORS
UCBSIO   EQU  X'06'        START I/O COUNT
UCBPREAD EQU  X'08'        PERMANENT READ ERRORS
UCBPWRIT EQU  X'09'        PERMANENT WRITE ERRORS
UCBNOISE EQU  X'0A'        NOISE RECORDS READ
UCBERG   EQU  X'0C'        INTER RECORD GAPS WRITTEN
UCBCLN   EQU  X'0E'        CLEANER ACTIONS
*
*        XCVT - XCOM CVT FOR CROSS MEMORY MESSAGE FACILITY
*
VTPCBH   EQU  X'0C'        POINTER TO PCB CHAIN OR ZERO
VTMHBH   EQU  X'10'        POINTER TO MHB CHAIN OR ZERO
VTRCUR   EQU  X'40'        CURRENT MESSAGES IN CSA QUEUE
VTRMAX   EQU  X'44'        MAXIMUM MESSAGES ALLOWED IN CSA QUEUE
VTRTOT   EQU  X'48'        TOTAL MESSAGES PUT SINCE IPL
         END
