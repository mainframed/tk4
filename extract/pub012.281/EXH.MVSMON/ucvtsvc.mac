          MACRO
&LABEL    UCVTSVC &P1,&P2
.*
.*   MACRO TO COMMUNICATE WITH THE UPDATE UCVT SVC ROUTINE
.*
.*   TWO PARAMETERS ARE REQUIRED
.*       THE FIRST - THE CALLING ID (PASSED TO THE SVC VIA REG ONE)
.*                   MAY BE SPECIFIED AS A DECIMAL VALUE OR A REGISTER
.*       THE SECOND - THE VALUE TO BE PASSED TO THE SVC (VIA REG ZERO)
.*                    IF A REGISTER VALUE IS USED THE VALUE IS
.*                       ASSUMED TO BE IN THE REGISTER
.*                    IF A LABEL IS SPECIFIED THE LABEL MUST BE THAT
.*                       OF THE FIELD CONTAINING THE VALUE
.*
.*   CHANGE ACTIVITY - WRITTEN 06/20/78 PWM
.*
          LCLB  &B
          AIF   ('&P1' EQ '').WRONG
          AIF   ('&P2' EQ '').WRONG
          AIF   ('&P1'(1,1) EQ '(').REGVAL
&LABEL    LA    1,&P1
&B        SETB  1
          AGO   .SECOND
.REGVAL   ANOP
          AIF   ('&P1'(2,1) EQ '1').ONLY
.LOADONE  ANOP
&LABEL    LR    1,&P1
&B        SETB  1
          AGO   .SECOND
.ONLY     ANOP
          AIF   ('&P1'(3,1) EQ ')').SECOND
          AGO   .LOADONE
.SECOND   ANOP
          AIF   ('&P2'(1,1) EQ '(').CHKREG
          AIF   (&B).LOAD0
&LABEL    L     0,&P2
          AGO   .SVC
.LOAD0    ANOP
          L     0,&P2
          AGO   .SVC
.CHKREG   AIF   ('&P2'(2,1) EQ '0').CHKLABL
          AIF   (&B).LOADZER
&LABEL    LR    0,&P2
          AGO   .SVC
.LOADZER  ANOP
          LR    0,&P2
          AGO   .SVC
.CHKLABL  ANOP
          AIF   (&B).SVC
&LABEL    EQU   *
.SVC      ANOP
          SVC   237
          MEXIT
.WRONG    ANOP
          MNOTE '8, ** NO PARM VALUE SPECIFIED ON SVC CALL **'
          MEND
