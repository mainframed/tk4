         MACRO
&N       FINDPGM  &DCB,&LIST,&EP=,&EPLOC=
.*
.*       AUTHOR. DON HIGGINS.
.*       DATE.   01/09/79.
.*       REMARKS. SEARCH FOR PROGRAM IN JOBLIB, STEPLIB,
.*                LINKPACK, OR LINKLIST AND SET CONDITION CODE
.*                AS FOLLOWS:
.*                   RC=0 IF FOUND
.*                   RC=4 IF NOT FOUND
.*       ARGUMENTS.
.*          DCB    - OPEN DCB OF PDS LIBRARY TO SEARCH OR OMIT
.*          LIST   - BLDL LIST (16 BYTE WORK AREA)
.*          EP=    - NAME OF PROGRAM TO SEARCH FOR
.*          EPLOC= - ADDRESS OF NAME OF PROGRAM (8 BYTES)
.CHKL    AIF   (T'&LIST EQ 'O').NOLIST
.LIST    AIF   ('&LIST'(1,1) EQ '(').RLIST
.RXLIST  ANOP
         LA    R0,&LIST
         AGO   .CHKDCB
.RLIST   ANOP
         LR    R0,&LIST
         AGO   .CHKDCB
.NOLIST  ANOP
         LA    R0,20
         GETMAIN RC,LV=(0) GET WORK AREA AND SAVE ADDRESS
         ST    R2,0(R1)
         LR    R2,R1
         LA    R0,4(R1)
.CHKDCB  AIF   (T'&DCB EQ 'O').NODCB
.DCB     AIF   ('&DCB'(1,1) EQ '(').RDCB
.RXDCB   ANOP
         LA    R1,&DCB
         AGO   .MVCPGM
.RDCB    ANOP
         LR    R1,&DCB
         AGO   .MVCPGM
.NODCB   ANOP
         ZR    R1
.MVCPGM  ANOP
         LR    R15,R0
         MVC   0(4,R15),=AL2(1,12)
         AIF   (T'&EP EQ 'O').EPLOC
.EP      ANOP
         MVC   4(8,R15),=CL8'&EP'
         AGO   .BLDL
.EPLOC   ANOP
         MVC   4(8,R15),&EPLOC
.BLDL    ANOP
         BLDL  (1),(0)
         AIF   (T'&LIST NE 'O').EXIT
         LR    R1,R2
         L     R2,0(R1)
         ST    R15,16(R13) SAVE BLDL RC ACROSS FREEMAIN
         LA    R0,20
         FREEMAIN R,LV=(0),A=(1)
         L     R15,16(R13) RESTORE BLBL RC
.EXIT    ANOP
         MEND
