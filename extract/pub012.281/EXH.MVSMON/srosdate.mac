*          DATA SET GU008900   AT LEVEL 001 AS OF 12/30/72
*      IDENTIFICATION DIVISION.
*      PROGRAM-ID.  'DATE CONVERT ROUTINE'
*      AUTHOR.  'HENRY JOHNSON'
*      INSTALLATION.  FLORIDA POWER CORPORATION
*      DATE WRITTEN 02-20-68
*      REMARKS.
*          PURPOSE: THIS ROUTINE CONVERTS THE DATE OF
*                   68.044 TO 02/13/68.
*                   COBOLAID HAS THE NECESSARY LEVELS FOR
*                     THE WORKING STORAGE SECTION. THE
*                     FOLLOWING CARD INCLUDED IN YOUR
*                     WORKING STORAGE SECTION WILL COPY
*                     THE PICTURE.
*                   01  CVDATE COPY 'CVTDATE'
*                 * 01 CVDATE.
*                 *    02 CMONTH                       PICTURE 99.
*                 *    02 FILLER   VALUE '/'           PICTURE X.
*                 *    02 CDAY                         PICTURE 99.
*                 *    02 FILLER   VALUE '/'           PICTURE X.
*                 *    02 CYEAR                        PICTURE 99.
*                 * 01 CONVDATE REDEFINES CVDATE       PICTURE X(8).
*
*                   EXAMPLE OF THE CALL:
*                    ENTER LINKAGE.
*                    CALL 'SROSDATE' USING CVDATE.
*                    ENTER COBOL.
*
*          MODIFIED:     DATE            BY WHOM
* REV1 DON HIGGINS 12/27/72 - SUBENTRY-SUBEXIT TO FIX RETURN CODE
* REV2 DON HIGGINS 12/31/72 - FIX NON STANDARD USE OF R13 FOR WORK
SROSDATE SUBENTRY
         L     3,0(1)                   LOAD CALL PARM ADDR
         TIME  DEC                      GET DATE
         ST    0,TIMESV                 SAVE TIME OF DAY
         CLM   1,8,=X'01'         20NN ?                        GP15138
         BNE   *+8                  NO                          GP15138
         ICM   1,8,=X'20'         CORRECT                       GP15138
         ST    1,WORK                   SAVE DATE
         TM    WORK+1,X'01'             TEST YEAR FOR ODD
         BO    PHSIN2X                  YES, NOT LEAP YEAR
         TM    WORK+1,X'12'             NO,TEST BIT 3 AND 6
         BM    PHSIN2X                  IF MIXED NOT LEAP YEAR
         CLI   WORK+1,0           CENTURY DATE?                 GP15138
         BNE   PHSISLIP             NO; OK AS IS                GP15138
         TM    WORK,X'01'         TEST CENTURY FOR ODD          GP15138
         BO    PHSIN2X              YES, NOT LEAP YEAR          GP15138
         TM    WORK,X'12'         NO,TEST BIT 3 AND 6           GP15138
         BM    PHSIN2X              IF MIXED NOT LEAP YEAR      GP15138
PHSISLIP MVI   FEB,X'1D'                SET NO OF DAYS IN FEB=29
PHSIN2X  XC    DWORD(8),DWORD           CLEAR WORK AREA
         STH   1,DWORD+6                CONVERT DAY OF YEAR
         CVB   0,DWORD                  TO BINARY
         LA    R4,1                     MONTH COUNTER = 1
         LA    12,CALNDR                CALENDAR POINTER TO JANUARY
         SR    14,14                    CLEAR
PHSIN3X  IC    14,0(12)                 SUBT NO OF DAYS IN A MONTH
         SR    0,14                         FROM DAY OF YEAR
         BC    12,PHSIN4X               DONE IF RESULT 0 OR -
         LA    R4,1(R4)                 MONTH COUNTER= MONTH CNTR +1
         LA    12,1(12)                 CALENDAR POINTER = NEXT MONTH
         B     PHSIN3X
PHSIN4X  AR    0,14                     ADD LAST MONTH BACK
         BAL   10,CNVRTX                CONVERT MONTH
         MVC   DATE(2),DWORD+2          AND MOVE IT IN DATE AREA
         LR    R4,0                     CONVERT
         BAL   10,CNVRTX                DAY
         MVC   DATE+3(2),SEQ+2          AND MOVE IT IN DAY AREA
         MVO   WORK+2(2),WORK(2)        PUT SIGN ON YEAR
         EX    0,PHSIN2X                CLEAR WORK AREA
         MVC   DWORD+6(2),WORK+2        MOVE YEAR WITH SIGN TO WORK
         CVB   R4,DWORD                     AREA  AND
         BAL   10,CNVRTX                     CONVERT
         MVC   DATE+6(2),SEQ+2          MOVE YEAR INTO DATE AREA
         CLI   DATE,X'40'
         BNE   NEXT
         MVI   DATE,X'F0'
NEXT     CLI   DATE+3,X'40'
         BNE   RET
         MVI   DATE+3,X'F0'
RET      MVC   0(8,3),DATE
         SUBEXIT
CNVRTX   CVD   R4,DWORD                 CONVERT BINARY WORD
         UNPK  DWORD(4),DWORD+5(3)        IN REG 13
         OI    DWORD+3,X'F0'            UNPACKED FORM IN WORD SEQ
         MVC   SEQ,DWORD
         LA    14,DWORD
SUPPRESS TM    0(14),X'0F'              IS THIS A ZERO
         BCR   4,10                     NO, RETURN
         MVI   0(14),X'40'              YES, REPLACE WITH BLANK
         LA    14,1(14)
         B     SUPPRESS                 LOOP
CALNDR   DC    X'1F'                    NUMBER OF DAYS IN JAN
FEB      DC    X'1C'                    NUMBER OF DAYS IN FEB
         DC    X'1F'                    NUMBER OF DAYS IN MAR
         DC    X'1E'                    NUMBER OF DAYS IN APR
         DC    X'1F'                    NUMBER OF DAYS IN MAY
         DC    X'1E'                    NUMBER OF DAYS IN JUN
         DC    X'1F'                    NUMBER OF DAYS IN JUL
         DC    X'1F'                    NUMBER OF DAYS IN AUG
         DC    X'1E'                    NUMBER OF DAYS IN SEP
         DC    X'1F'                    NUMBER OF DAYS IN OCT
         DC    X'1E'                    NUMBER OF DAYS IN NOV
         DC    X'1F'                    NUMBER OF DAYS IN DEC
WORK     DC    F'0'
TIMESV   DC    F'0'                     TIME OF DAY SAVE
DATE     DC    C'00/00/00'              DATE BASE
TIME     DC    C'00.00 '                TIME OF DAY BASE
SEQ      DC    F'0'                     NUMBER OF LEADING ZEROS
DWORD    DC    D'0'                     NUMBER WITH ZERO SUPPRESSED
         LTORG
         EQUREGS
         END
