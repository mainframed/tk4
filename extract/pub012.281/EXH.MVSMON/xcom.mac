         MACRO
&N       XCOM  &OP,&PID=,&TID=,&MSG=,&RID=,&MF=,&ECB=,&RMAX=
.*             MACRO-ID. AM013700.
.*             AUTHOR.   DON HIGGINS.
.*             DATE.     02/27/78.
.*             REMARKS.  CROSS MEMORY MESSAGE SVC 206 MACRO INTERFACE
.*                       SEE SR301000+SR303000 FOR DOCUMENTATION.
         AIF   ('&MF' EQ 'O').STDFORM
         AIF   ('&MF' EQ 'I').STDFORM
         AIF   ('&MF' EQ 'L').LSTFORM
         AIF   ('&MF(1)' EQ 'E').EXFORM
         MNOTE 8,'XCOM INVALID MF OPERAND'
         MEXIT
.STDFORM ANOP
         CNOP  0,4
&N       BAL   1,*+4+16
         DC    XL16'00'
.PID     AIF   (T'&PID EQ 'O').CHKSET
         AIF   ('&PID'(1,1) EQ '(').RPID
         MVC   0(4,1),&PID
         AGO   .TID
.RPID    ANOP
         MVC   0(4,1),0(&PID(1).)
.TID     AIF   (T'&TID EQ 'O').ZTID
         AIF   ('&TID'(1,1) EQ '(').RTID
         MVC   4(4,1),&TID
         AGO   .MSG
.RTID    ANOP
         MVC   4(4,1),0(&TID(1).)
         AGO   .MSG
.ZTID    ANOP
         XC    4(4,1),4(1)
.MSG     AIF   (T'&MSG EQ 'O').RID
         AIF   ('&MSG'(1,1) EQ '(').RMSG
         LA    0,&MSG
         STCM  0,7,9(1)
         AGO   .RID
.RMSG    ANOP
         STCM  &MSG(1).,7,9(1)
.RID     AIF   (T'&RID EQ 'O').ZRID
         AIF   ('&RID'(1,1) EQ '(').RRID
         MVC   12(4,1),&RID
         AGO   .ECB
.RRID    ANOP
         MVC   12(4,1),0(&RID(1).)
         AGO   .ECB
.ZRID    ANOP
         XC    12(4,1),12(1)
.ECB     AIF   (T'&ECB EQ 'O').OP
         AIF   ('&ECB'(1,1) EQ '(').RECB
         LA    0,&ECB
         STCM  0,7,9(1)
         AGO   .OP
.RECB    ANOP
         STCM  &ECB(1).,7,9(1)
.OP      AIF   ('&OP' EQ 'DEF').DEF
         AIF   ('&OP' EQ 'TRM').TRM
         AIF   (T'&MSG NE 'O').OP2
         MNOTE 8,'XCOM GET/PUT REQUIRES MSG='
         MEXIT
.OP2     AIF   (T'&ECB EQ 'O').OP3
         MNOTE 8,'XCOM GET/PUT PROHIBITS ECB='
         MEXIT
.OP3     AIF   ('&OP' EQ 'GET').GET
         AIF   ('&OP' EQ 'PUT').PUT
.BADOP   ANOP
         MNOTE 8,'XCOM OP INVALID'
         MEXIT
.DEF     AIF   (T'&ECB NE 'O').DEF2
         MNOTE 8.'XCOM DEF REQUIRES ECB='
         MEXIT
.DEF2    MVI   8(1),X'80'
         AGO   .SVC
.TRM     ANOP
         MVI   8(1),X'40'
         AGO   .SVC
.GET     ANOP
         MVI   8(1),X'20'
         AGO   .SVC
.PUT     ANOP
         MVI   8(1),X'11'
         AGO   .SVC
.CHKSET  AIF   (T'&RMAX EQ 'O').NOPATH
         AIF   ('&OP' NE 'SET').BADOP
         AIF   ('&RMAX'(1,1) EQ '(').RRMAX
         LA    0,&RMAX
         ST    0,0(1)
         AGO   .SET1
.RRMAX   ANOP
         MVC   0(4,1),0(&RMAX(1).)
.SET1    ANOP
         MVI   8(1),X'04'
         AGO   .SVC
.NOPATH  ANOP
         MNOTE 8,'XCOM - NO PID DEFINED'
         MEXIT
.SVC     ANOP
         SVC   206
         MEXIT
.LSTFORM ANOP
         CNOP  0,4
&N       DC    XL16'00'
         MEXIT
.EXFORM  AIF   ('&MF(2)'(1,1) EQ '(').RPARM
         LA    1,&MF(2)
         AGO   .PID
.RPARM   ANOP
         LR    1,&MF(2)
         AGO   .PID
         MEND
