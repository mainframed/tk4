*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  THIS ROUTINE IS THE PRELIMINARY MESSAGE PROCESSOR WHICH LOADS      *
*  ADDRESSES FROM THE VECTOR TABLE, EDITS THE INPUT KEY, CALLS THE    *
*  FMP FOR READ, AND BUILDS THE FIRST LINE OF OUTMSG (ACCT NUMBER)    *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
MAINLINE EQU   *
         L     R4,32(R2)                GET INMSG FROM TCVT
         L     R6,36(R2)                GET OUTMSG FROM TCVT
         L     R7,48(R2)                GET FLAG FROM TCVT
         MVI   MTRSET,X'0C'             RESET MTRSET (MRDVELOP) TO ZERO
         MVI   MTRCNT,X'0C'             RESET DESCR PLUG TO ZERO
         CLI   25(R4),C'A'              INQUIRY BY ACCT#?
         BE    TRTACCT                  YES GO EDIT ACCT#
         CLI   25(R4),C'M'              INQUIRY BY MTR#?
         BE    TRTMTR                   YES GO EDIT MTR#
         B     CALLFMP                  BRANCH TO CALL FMP
*                                                                     *
TRTACCT  TRT   27(11,R4),TRTBL          EDIT ACCT# FOR NUMERIC
         BNZ   FLAGF1                   FORMAT ERROR
         B     CALLFMP                  OK CALL FMP
*                                                                     *
TRTMTR   TRT   27(7,R4),TRTBL           EDIT MTR# FOR NUMERIC
         BNZ   FLAGF1                   FORMAT ERROR
CALLFMP  L     R3,40(R2)                GET ACTION FROM TCVT
         MVI   0(R3),C'R'               MOVE 'R' TO ACTION
         L     R15,56(R2)               GET FMP ENTRY POINT FROM FMP
         CALL  (15)                     CALL FMP FOR READ
         L     R5,28(R2)                GET READAREA (MR) FROM TCVT
         CLI   0(R7),X'00'              IS FLAG ZERO?
         BNE   FINI                     NO GET OUT
         CLI   6(R5),C'2'               RETAIL RECORD?
         BE    DVELOP
         CLI   6(R5),C'3'               ITEMIZE RECORD?
         BE    DVELOP
         CLI   6(R5),C'1'               INDICATIVE RECORD?
         BE    INDICD0
         CLI   6(R5),C'4'               MEMO RECORD?
         BE    MEMOD1
         CLI   6(R5),C'7'               STARTER RECORD?
         BE    STARTD2
         CLI   6(R5),C'8'               CONTROL RECORD?
         BE    CNTRLD3
         CLI   6(R5),C'9'               HASH RECORD?
         BE    HASHD4
DVELOP   BAL   R12,MRDVELOP             GO DEVELOP ADDRESSES FOR MASTER
         L     R5,BASIC                 PUT ADDR OF BASIC INTO R5
         UNPK  ACCTWK,0(6,R5)           UNPK ACCT#
         MVC   CYCLE,ACCTWK             REVERSE
         MVC   DIST(2),ACCTWK+2           CYCLE & DIST
         MVC   ROUTE(2),ACCTWK+4        MOVE ROUTE TO WORK
         MVC   FOLIO(4),ACCTWK+6        MOVE FOLIO TO WORK
         MVC   OCC,ACCTWK+10            MOVE OCCUPANCY TO WORK
         MVC   0(5,R6),=X'0000020015'     OUTMSG PREFIX
         L     R8,76(R2)                GET TERM ADDR FROM TCVT
         CLI   3(R8),C'0'               IF LOW TO ZERO THIS IS A VIDEO
         BNL   NOT2260V                 IF NOT LOW TO ZERO IT IS 2740
         MVI   4(R6),X'40'              THIS IS VIDEO, OVLY CARR RET
NOT2260V LA    R6,5(R6)                 INCREMENT R6 PAST PREFIX
         MVC   0(14,R6),UNPKACCT        MOVE ACCT# (WORK) TO OUTMSG
         MVI   14(R6),X'15'             CARR RETURN
         AP    CRETCNT,PONE             ADD 1 TO CARR RETURN COUNT
         LA    R6,15(R6)                INCREMENT R6 PAST ACCT#
         B     LINE2
*                                                                     *
INDICD0  MVI   0(R7),X'D0'
         B     FINI
MEMOD1   MVI   0(R7),X'D1'
         B     FINI
STARTD2  MVI   0(R7),X'D2'
         B     FINI
CNTRLD3  MVI   0(R7),X'D3'
         B     FINI
HASHD4   MVI   0(R7),X'D4'
         B     FINI
