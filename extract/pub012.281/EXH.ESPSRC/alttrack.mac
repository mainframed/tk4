ATRK     TITLE 'A L T T R A C K  ***  ALTERNATE TRACK LISTER'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 1
*         THIS PROGRAM ANALYZES DASD VOLUME SURFACES.
*         A LISTING OF ALL DEFECTIVE AND ALTERNATE
*         TRACKS BY CYLINDER AND HEAD ADDRESS IS PRINTED.
*        BASED ON PROGRAM 'ALTTRK' CIRCULATED BY SHARE
*        ADAPTED BY G. POSTPISCHIL
*                                                                82129
*        EXPERT SYSTEM PROGRAMMING
*        176 OLD STAGE COACH ROAD
*        BRADFORD, VT 05033-8844
         SPACE 1
*        VOLUMES TO BE EXAMINED ARE SPECIFIED EITHER ON A DASD DD
*        CARD, OR ONE OR MORE SYSUTNN CARDS WITH UNIQUE NAMES.
*        DD SHOULD SPECIFY DISP=OLD,UNIT AND VOLSER.
*        BY DEFAULT, THE ENTIRE VOLUME IS READ. FOR A QUICK LISTING,
*        PARM=QUICK WILL READ THE ALTERNATE TRACK AREA ONLY.     82129
*        PARM=SKIP  WILL ALSO REPORT TRACKS WITH SKIP OFFSETS    85064
*                                                                82129
*        ATLAS MAY BE INVOKED TO PERFORM A CONDITIONAL RECOVERY OF A
*        BAD TRACK - SPECIFY // EXEC PGM=FIXTRACK,PARM='CCCCHHHH'
*        WITH A LARGE REGION ( AT LEAST 128K). THE TRACK WILL BE READ,
*        AND AN ALTERNATE ASSIGNMENT ATTEMPTED IF ERRORS ARE FOUND.
*        FOR FIXTRACK, ONLY THE FIRST DD CARD IS USED.           82129
*
*        ATTRIBUTES - AC=1, PAGE; NON-RENT, NON-REFR, NON-REUS   82129
*        REQUIREMENTS - EXTERNAL ROUTINES @SERVICE AND @PRINTER
         EJECT ,
         PRINT &PRTSOR
ALTTRACK PGMHEAD ZERO12,BASE=(R11,R12),PARM=R10,AM=31,RM=24,           *
               ENTRY=FIXTRACK (PRIVILEGED)                       82129
         SPACE 1
MAXSTACK EQU   30            MAXIMUM TRACKS/CYLINDER PER I/O
         SPACE 1
         SERVINIT ,          LOAD @SERVICE ROUTINE
         SERVCALL LPALD,=CL8'@PRINTER'  LOAD THE PRINT ROUTINE
         ST    R0,@PRINTER   SAVE THE ADDRESS
         MVI   PRTDEV,3      OUTPUT TO BOTH FILES                85064
         LA    R15,FIXTRACK  GET SECONDARY ENTRY POINT           85064
         L     R1,SAVE13-SAVE(,R13)  GET CALLER'S SAVE AREA      85064
         CLM   R15,7,SAVE15+1-SAVE(R1)  SECONDARY ENTRY ?        85064
         BNE   OPRTMAIN      NO; MAIN ENTRY                      85064
         OI    FLAGS,FGFIX+FGQUICK  SET FOR QUICK FIX            85064
         PRTOPEN SUMPRINT,OPT=(DUMMY,NOWTO),DEV=2  SUMMARY MESSAGES
         PRTOPEN SYSPRINT,OPT=(ABEND)  MUST BE SUPPLIED
         B     OPRTDONE                                          85064
OPRTMAIN PRTOPEN SYSPRINT,OPT=(DUMMY) NORMAL OUTPUT              85064
         BXH   R15,R15,*+8   AVAILABLE ?                         85064
         MVI   PRTDEV,1      YES; OUTPUT TO SYSPRINT ONLY        85064
         PRTOPEN SUMPRINT,OPT=(DUMMY,NOWTO),DEV=2  SUMMARIES     85064
         PRTLIST TITLESUM,TITLE=3,DEV=2                          85064
OPRTDONE PRTLIST TITSUM,TITLE=1,DEV=2                            85064
         PRTLIST TITPRE,TITLE=1,DEV==PRTDEV  PRELIMINARY ERRORS  85064
         LA    R5,ACCESS
         USING IHADCB,R5
         SPACE 1                                                 82129
         LA    R10,0(,R10)   CLEAN PARM ADDRESS                  82129
         LTR   R10,R10       ANY ?                               82129
         BZ    DFLTPARM      NO                                  82129
         L     R10,0(,R10)   GET ADDRESS OF PARM                GP03025
         LA    R10,0(,R10)   CLEAN ADDRESS                      GP03025
DFLTPARM LA    R15,FIXTRACK  GET SECONDARY ENTRY POINT           82129
         L     R1,SAVE13-SAVE(,R13)  GET CALLER'S SAVE AREA      82129
         CLM   R15,7,SAVE15+1-SAVE(R1)  SECONDARY ENTRY ?        82129
         BNE   INITMAIN      NO; MAIN ENTRY                      82129
         LTR   R10,R10       ANY PARM ?                          82129
         BZ    PARMBAD       NO; ERROR                           82129
         CLI   1(R10),8      PARM LENGTH=8 ?                     82129
         BNE   PARMBAD       NO                                  82129
         OI    FLAGS,FGFIX+FGQUICK  SET FOR QUICK FIX            82129
         LA    R14,2(,R10)   GET FIRST PARM BYTE                 82129
         LA    R15,8         PROCESS 8 BYTES                     82129
PARMHLOP CLI   0(R14),C'A'   VALID HEX ?                         82129
         BL    PARMBAD       NO; TOO BAD                         82129
         CLI   0(R14),C'F'   IN ALPHABETIC RANGE ?               82129
         BNH   PARMHBUM      YES; TRY NEXT                       82129
         CLI   0(R14),C'0'   IN NUMERIC RANGE ?                  82129
         BL    PARMBAD       NO; TOO BAD                         82129
         CLI   0(R14),C'9'   REAL JERK ?                         82129
         BNH   PARMHBUM      NO; USE IT                          82129
PARMBAD  PRTV  PARMBMSG,DEV=(1,2)                                85064
ABEND666 SERVTERM ,          CLOSE AND FREE EVERYTHING           82129
         ABEND 666           BYE-BYE                             82129
PARMBMSG VCON  '-PARM FIELD MISSING OR INVALID'                  82129
         SPACE 1                                                 82129
PARMHBUM LA    R14,1(,R14)                                       82129
         BCT   R15,PARMHLOP  TRY AGAIN                           82129
         MVC   DB,2(R10)     MOVE TO WORK AREA                   82129
         NC    DB,HEXMASK    CLOBBER ZONE BITS                   82129
         TR    DB,HEXUNITS   MAKE HEX UNITS                      82129
         PACK  FIXCCHH(5),DB(9)  PACK + GARBAGE BYTE             82129
         AIF   ('&LOCAL' NE 'CCSI').SKIPAUT                      82129
         SERVCALL ACGET,DB   GET ACCOUNT                         82129
         ST    R0,DB         STASH PRIVILEGES                    82129
         TM    DB+3,VAASUP+VAASYS  PRIVILEGED USER ?             82129
         BNZ   LOOPTIOT      YES; PROCEED                        82130
         ABEND 047           BOMB                                82129
.SKIPAUT B     LOOPTIOT      FALL INTO MAIN CODE                 82129
HEXMASK  DC    8X'1F'        HEX=>BINARY - CLOBBER MOST ZONE BITS
HEXUNITS DC    AL1(0,10,11,12,13,14,15,0,0,0,0,0,0,0,0,0)  C0-CF 82129
         DC    AL1(0,1,2,3,4,5,6,7,8,9)                    F0-F9 82129
         SPACE 1                                                 82129
INITMAIN LTR   R10,R10       ANY PARM ?                          82129
         BZ    LOOPTIOT      NO; NO SPECIAL PROCESSING           82129
         SR    R0,R0                                             82129
         ICM   R0,3,0(R10)   GET PARM LENGTH                     82129
         BZ    LOOPTIOT      NOTHING SPECIAL                     82129
         CLC   =C'SKIP',2(R10)  SKIP MODE ?                      85064
         BNE   *+12          NO                                  85064
         OI    FLAGS,FGSKIPU  SET SKIP READ MODE                 85064
         B     LOOPTIOT      SKIP QUICK TEST                     85064
         CLC   =C'QUICK',2(R10)  QUICK LISTING OF ALTERNATE AREA ?
         BNE   PARMBAD       NO; ERROR                           82129
         OI    FLAGS,FGQUICK SET FOR QUICKY LISTING              82129
         SPACE 1
LOOPTIOT L     R1,TIOTADDR   GET PRIOR TIOT ENTRY ADDRESS
         SERVCALL TIOLK,(R1)  LOOK FOR ANOTHER
         LTR   R2,R0         ANY FOUND ?
         BZ    ENDJOB        NO; QUIT
         ST    R2,TIOTADDR
         USING TIOELNGH,R2   DECLARE IT
         CLC   =C'SYSUT',TIOEDDNM  SYSUTNN NAME ?
         BE    USEDD         YES; USE IT
         CLC   =C'DASD ',TIOEDDNM  OLD NAME ?
         BNE   LOOPTIOT      NO; TRY ANOTHER
USEDD    SERVCALL TIODD,TIOEDDNM  CHECK TIOT ENTRY FOR NAME
         CR    R2,R0         SAME ENTRY ?
         BNE   LOOPTIOT      NO; SKIP DUPLICATE DD
         TM    FLAGS,FGFIX   DOING A FIX ?                       82129
         BZ    *+8           NO                                  82129
         XI    TIOTADDR+1,X'80'  FORCE STOP AFTER FIRST ENTRY    82129
         MVC   DCBDDNAM,TIOEDDNM  SAVE DD NAME
         CLI   TIOELNGH,20   STANDARD LENGTH ?
         BNE   DEVERR        NO
         SR    R10,R10       CLEAN HIGH BYTE                    GP03025
         ICM   R10,7,TIOEFSRT  GET UCB ADDRESS
         BZ    DEVERR        THIS DUMMY DESERVES IT
         USING UCBOB,R10
         CLI   UCBTBYT3,UCB3DACC  DIRECT ACCESS ?
         BNE   DEVERR        NO; TOO BAD
         XC    CURWORK(CURLEN),CURWORK  CLEAR COUNTERS
         MVC   VOLSER,UCBVOLI   SAVE THE VOLUME SERIAL
         MVC   VOLUNIT,UCBCHAN  SAVE ADDRESS                    GP03025
         SERVCALL UCBGN,(R10)  GET GENERIC FROM UCB
         MVC   VOLTYPE,0(R1)  MOVE TYPE OR BLANKS
         TM    UCBTBYT4,255-DVCTYPMK   ONE I KNOW ABOUT ?
         BNZ   DEVERR        NO; TOO BAD
         L     R3,CVTPTR     GET THE CVT
         USING CVTMAP,R3
         IC    R15,UCBTBYT4     GET THE DISK SUB-TYPE
         LA    R14,DVCTYPMK  MASK FOR SUB-TYPE INDEX
         NR    R15,R14       GET INDEX
         BZ    DEVERR        UNSUPPORTED ?
         NI    FLAGS,255-FGSKIPD  RESET DEVICE SKIP              85064
         LA    R2,SKIPOFF(R15)  GET SKIP OFFSET FLAG             85064
         SLR   R1,R1                                             85064
         ICM   R1,1,0(R2)    GET OFFSET                          85064
         BZ    NOTSKIPS      NONE, LEAVE                         85064
         MH    R1,=Y(L'STACKSKP)  MAKE INTO OFFSET               85064
         LA    R1,EXPOFF-L'STACKSKP(R1)  GET EXPECTED SKIP OFFSET
         MVC   WANTSKP,0(R1) SAVE IT                             85064
         OI    FLAGS,FGSKIPD  SHOW SKIP SUPPORT ON DEVICE        85064
NOTSKIPS L     R14,CVTZDTAB  GET CVT DEVICE POINTER              85064
         USING DVCTI,R14     DECLARE INDEX PORTION OF DEVICE TABLE
         IC    R15,DVCTIOFF(R15)   GET INDEX TO DEVICE ENTRY
         AR    R14,R15       GET ENTRY FOR THIS TYPE
         USING DVCT,R14      DECLARE SINGLE ENTRY                82129
         AIF   (NOT &MVSXA).NODCE                                93162
         ICM   R1,15,UCBEXTPT  GET UCB EXTENSION                 93162
         BZ    USEDVCT       HUH ?                               93162
         ICM   R1,15,UCBCLEXT-UCBCMEXT(R1)  GET DCE              93162
         BZ    USEDVCT       HUH ?                               93162
         ICM   R1,3,DCEUDCYL-DCE(R1)  ANY COUNT ?                93162
         BZ    COMDVCT                                           93162
         STCM  R1,3,MAXCYL   SAVE TRUE SIZE                      93162
         B     COMDVCT                                           93162
.NODCE   SPACE 1                                                 93162
USEDVCT  MVC   MAXCYL,DVCCYL  SAVE MAXIMUM NUMBER OF CYLINDERS
COMDVCT  MVC   MAXHEAD,DVCTRK  SAVE MAXIMUM NUMBER OF CYLINDERS
         MVC   MAXALT,DVCALT  SAVE MAXIMUM NUMBER OF ALTERNATE TRACKS
         MVC   FIXLEN+2(2),DVCTRKLN  SAVE TRACK LENGTH           82129
         ST    R10,RELXTENT  SET UCB ADRESS IN NEW EXTENT
         MVI   RELXTENT,FILEMASK  MAKE FULL SEEK MASK
         TM    DVCFLAGS,X'10'  DVCMODU  MODULO DEVICE ?         GP12046
         BZ    *+8           NO                                  86127
         MVI   ACCESS+DCBRECFM-IHADCB,DCBRECU  NO TRK OVERFLOW   86127
         TM    FLAGS,FGSKIP  REQUEST SUPPORTED SKIP MODE ?       85064
         BNO   *+8           NO                                  85064
         OI    RELXTENT,X'04'  SET C.E. FLAG INTO FILE MASK      85064
         DROP  R2,R3,R10,R14
         SPACE 2
         ICM   R8,15,MAXCYL  GET HIGH CYLINDER AND TRACK
         SL    R8,=X'00010001'  MAKE RELATIVE
         STCM  R8,15,RELCYL  SAVE RELATIVE VALUE FOR DEB EXTENT
*        NOTE THAT R8 REMAINS AS LOOP END VALUE UNTIL 'TRACKOK'
         LH    R3,MAXCYL     GET MAX PHYSICAL CYLINDERS
         MH    R3,MAXHEAD    GET TOTAL TRACKS ON VOLUME
         STH   R3,ALLTRKS    SET TOTAL TRACKS ON VOLUME
         SPACE 2
         RDJFCB (ACCESS)     GET VOLSER IF NOT AVR AND UNIT=DEFER
         BXH   R15,R15,DEVERR    ?
         MVC   VOLSER,JFCBVOLS  THAT'S ALL I WANTED
         SPACE 1
         XC    JFCB(JFCBLGTH),JFCB  CLEAR MY JFCB
         MVI   JFCBDSNM,X'04'  MAKE VTOC DSNAME
         MVC   JFCBDSNM+1(L'JFCBDSNM-1),JFCBDSNM
         MVC   JFCBVOLS(6),VOLSER  SET VOLUME SERIAL
         MVI   JFCBNVOL,1    SET 1 VOLUME
         MVI   JFCBTSDM,JFCNWRIT+JFCNDSCB+JFCNDCB  KEEP IT CLEAN
         MVI   JFCBIND2,JFCOLD   DISP=OLD
         OPEN  (ACCESS),TYPE=J   TRY TO OPEN
         TM    DCBOFLGS,DCBOFOPN  OPEN OK ?
         BZ    DEVERRCL      NO; TOO BAD
         SERVCALL DSDJ1,JFCB   GET FORMAT 4 DSCB
         MVC   DS4IDFMT(DS4LNGTH),0(R1)  MOVE DATA
         CLI   DS4IDFMT,C'4'   REALLY A FORMAT 4 ?
         BNE   DEVERR        NO
         PRTLIST TITLE,TITLE=1,DEV==PRTDEV   SET THE TITLE       85064
         MVC   CURCYL(4),DS4DEVSZ  MOVE DEVICE CONSTANTS
         LH    R3,CURCYL     GET MAXIMUM CYLINDERS
         MH    R3,CURHEAD      IN TRACKS
         STH   R3,CURTRACK   SET TRACKS PER PACK
         PRTLIST FORMAT4,DEV==PRTDEV     FORMAT BASIC INFO       85064
         ICM   R8,15,CURCYL  GET SIZES BACK
         SL    R8,=X'00010001'  MAKE RELATIVE
         STCM  R8,15,RELCYL
         MVC   ALLTRKS,CURTRACK  SET TRACKS ON DEVICE
         MVC   MAXCYL(4),CURCYL
         L     R6,DCBDEBAD
         N     R6,=X'00FFFFFF'  JUST IN CASE                    GP03025
         USING DEBBASIC,R6
         SPACE 2
         MODESET KEY=ZERO              ENTER KEY ZERO
         MVC   SAVEDEB(16),DEBBASND+DEBDVMOD-DEBDASD  SAVE OLD EXTENTS
         MVC   DEBBASND+DEBDVMOD-DEBDASD(16),RELXTENT  SET FULL VOLUME
         TM    FLAGS,FGFIX   FIX MODE ?                          82129
         BZ    *+8           NO                                  82129
         OI    DEBBASND+DEBDVMOD-DEBDASD,X'C0'  FOR FORMATTING   85064
         TM    FLAGS,FGSKIP  READ SKIP OFFSETS ?                 91129
         BZ    *+8           NO                                  91129
         OI    DEBBASND+DEBDVMOD-DEBDASD,X'04' ALLOW DIAGNOSTICS 91129
         NI    DEBPROTG,X'0F'    SET PKEY TO ZEROS
         MODESET  KEY=NZERO      / KEY ZERO NOT NEEDED NOW
         TM    FLAGS,FGFIX   IS THIS A QUICK FIX ?               82129
         BZ    NOTFIX        NO                                  82129
         CLC   FIXCCHH(2),MAXCYL  VALID CYLINDER ?               82129
         BNL   BADFIXAD      NO                                  82129
         CLC   FIXCCHH+2(2),MAXHEAD  VALID HEAD ?                82129
         BL    TRYFIX        YES; PROCEED                        82129
BADFIXAD PRTV  BADMSGAD,DEV==PRTDEV      WRITE ERROR             85064
         B     ABEND666      AND QUIT                            82129
BADMSGAD VCON  '- CCCCHHHH ADDRESS IN PARM NOT VALID'            82129
         SPACE 1                                                 82129
BADFIXAL PRTV  BADMSGAL,DEV==PRTDEV   WRITE ERROR - NO ALTERNATES
         B     ABEND666      AND QUIT                            82129
BADMSGAL VCON  '- NO ASSIGNABLE ALTERNATES ON VOLUME'            82129
         SPACE 1                                                 82129
TRYFIX   L     R7,FIXCCHH    LOAD FIX ADDRESS                    82129
         STCM  R7,15,IOBSEEK+3  SET SEEK ADDRESS                 82129
         L     R2,FIXLEN     GET TRACK LENGTH                    82129
         STH   R2,READCCW+6  SET MAXIMUM LENGTH INTO READ CCW    82129
         AH    R2,=H'4095'   TRUNCATE TO FULL PAGE               82129
         N     R2,=X'00FFE000'                                   82129
         STORAGE OBTAIN,LENGTH=(R2),LOC=(BELOW,ANY)             GP03025
         GETMAIN R,LV=(R2)   GET ONE BUFFER                      82129
         STM   R1,R2,FIXADDR   SAVE ADDRESS AND SIZE             82129
         STCM  R1,7,READCCW+1  SET CCW READ ADDRESS              82129
         LA    R1,READR0     SET FOR R0 READ                     82129
         ST    R1,IOBSTART-1 SET IOBSTART ADDRESS                82129
         MVI   IOBFLAG1,X'42'  SET COMMAND CHAIN, UNRELATED      82129
         EXCP  IOB           READ R0                             82129
         WAIT  ECB=ECB       WAIT FOR IT TO FINISH               82129
         CLI   ECB,X'7F'     ANY ERROR ?                         82129
         BNE   TRYATLAS      YES; TRY TO GET AN ALTERNATE        82129
TRYFIXNR LA    R1,READFIX    GET READ CCW CHAIN                  82129
         ST    R1,IOBSTART-1  SET START ADDRESS                  82129
         MVI   IOBFLAG1,X'42'  SET COMMAND CHAIN, UNRELATED      82129
         EXCP  IOB           READ RECORD N+1                     82129
         WAIT  ECB=ECB       WAIT FOR COMPLETION                 82129
         CLI   ECB,X'7F'     ERROR FOUND ?                       82129
         BE    TRYFIXBP      NO; TRY NEXT RECORD                 82129
         CLI   ECB,X'41'     FUNNY ERROR ?                       82129
         BNE   TRYATLAS      YES; ATLAS WON'T FIX ?              82129
         CLC   =X'0C40',IOBSENS0  INCORRECT LENGTH ONLY ?        82129
         BE    TRYFIXBP      YES; TRY NEXT RECORD                82129
         CLC   =X'0E40',IOBSENS0  OVERFLOW RECORD ?              82129
         BE    TRYNOFIX      YES; NO ERROR FOUND                 82129
         B     TRYATLAS      ELSE TRY ATLAS                      82129
TRYFIXBP SR    R1,R1                                             82129
         IC    R1,IOBSEEK+7  GET LAST RECORD NUMBER              82129
         LA    R1,1(,R1)     UP IT BY ONE                        82129
         STC   R1,IOBSEEK+7  STASH IT BACK                       82129
         CLI   IOBSEEK+7,0   >255 ?                              82129
         BH    TRYFIXNR      NO; READ AGAIN                      82129
TRYNOFIX PRTV  FIXNOMSG,DEV==PRTDEV                              85064
         OI    RETCH+1,4     SET MINOR ERROR                     82129
         B     FIXENDED      AND QUIT FIX                        82129
FIXNOMSG VCON  '-***** NO ERROR DURING READ - ATLAS NOT CALLED *****'
         SPACE 1                                                 82129
*        INVOKE THE ATLAS SVC (86) TO COPY THE TRACK AND ASSIGN  82129
*        AN ALTERNATE. NOTE THAT THIS WORKS ONLY IF ATLAS SUCCEEDS
*        IN COPYING THE BAD TRACK; IF NOT, IF ASSIGNS AN ALTERNATE
*        THAT IS NEVER USED.                                     82129
*              TO LIMIT RETRIES IN ATLAS TO ONE, CHANGE          82129
*        ./ C IGG0860C  TO   NOP LOCATE (ONE TRY) 58200020       82129
*        IN SVS, ZAP IGG0860C VER 1C8 4770C1F2,41400010 REP 4700 82129
*        IN MVS (3.8E)  VER 1E0 4770C21E,41400010 REP 4700       84123
*        IN SP 1.3.3    VER 264 4770C2A2,41400010 REP 4700       85064
*                                                                82129
TRYATLAS NI    DCBIFLGS,255-DCBIFPEC  RESET ERROR BITS           82129
         OI    DCBIFLGS,DCBIFNE3  PREVENT SUBSEQUENT DDR         82129
         ATLAS PARMADR=ATLPARM,CHANPRG=NR,CNTPTR=F               82129
         STM   R15,R0,DB                                         82129
         PRTLIST ATLDONE,DEV==PRTDEV   SHOW RESULTS              85064
         ICM   R15,15,DB     GET THE RETURN CODE BACK            84123
         BZ    FIXENDED      EXPECTED CASE 1                     84123
         CH    R15,=H'40'                                        84123
         BE    FIXENDED      EXPECTED CASE 2                     84123
         OI    RETCH+1,8     ELSE UNEXPECTED CONDITION           84123
FIXENDED MVI   IOBSEEK+7,0   RESTORE IOBSEEK ADDRESS             82129
         NI    FLAGS,255-FGFIX   RESET FIX FLAG                  82129
         XC    TIOTADDR,TIOTADDR  RESTART FROM BEGINNING         82129
         LM    R1,R2,FIXADDR   GET GETMAIN ADDRESS AND SIZE      82129
         STORAGE RELEASE,LENGTH=(R2),ADDR=(1)                   GP03025
         CLOSE (ACCESS)      SO SORRY - NEED TO CLOBBER DCBDDNAM 82129
         B     LOOPTIOT      NOW DO QUICK ALTERNATE LIST         82129
         SPACE 1                                                 82129
READR0   CCW   X'07',IOBSEEK+1,X'40',6  SEEK                     82129
         CCW   X'16',RECZERO,X'20',8    READ R0                  82129
         SPACE 1                                                 82129
READFIX  CCW   X'31',IOBSEEK+3,X'40',5  SEARCH ID=               82129
         CCW   X'08',READFIX,0,0        TIC *-8                  82129
READCCW  CCW   X'1E',1,X'20',0          READ CKD                 82129
         SPACE 1                                                 82129
ATLPARM  DC    A(*+4)        ADDRESS OF PARM LIST                82129
         DC    A(IOB,IOBSEEK+3)  IOB/SEEK ADDRESS                82129
ATLDONE  FDOPT NL,CC=C'-'                                        82129
         FDCLC DB,ZEROES,4,BE=ATLDONEZ                           82129
         FDPRT 'ATLAS RETURN VALUES :   R15='                    82129
         FDPRT DB,4,HEX                                          82129
         FDPRT '    R0=',PADL                                    82129
         FDPRT DB+4,4,HEX                                        82129
         FDPRT 'PREVIOUS RECORD WAS',PAD                         82129
         FDGOTO ATLDONEC                                         82129
ATLDONEZ FDPRT 'ALTERNATE ASSIGNED AND COPIED; LAST RECORD='     82129
ATLDONEC FDPRT IOBSEEK+3,2,HEX                                   82129
         FDPRT '.'                                               82129
         FDPRT IOBSEEK+5,2,HEX                                   82129
         FDPRT '.'                                               82129
         FDPRT IOBSEEK+7,1,HEX                                   82129
         FDPRT *END                                              82129
         SPACE 2
NOTFIX   TM    FLAGS,FGQUICK  QUICK SCAN THROUGH ALTERNATES ?    82129
         BZ    NEXTZERO      NO; DO THE WHOLE PACK               82129
         SR    R15,R15                                           82129
         ICM   R15,3,ALLTRKS  GET HIGH TRACK +1                  82129
         SH    R15,MAXALT    SUBTRACT ALTERNATES                 82129
         SR    R14,R14                                           82129
         LH    R0,CURHEAD    GET TRACKS PER CYLINDER             82129
         DR    R14,R0        SEPARATE INTO TRACK/CYLINDER        82129
         SLL   R15,16        SHIFT CYLINDERS LEFT                82129
         OR    R15,R14       OR TRACKS INTO LOW HALF-WORD        82129
         LR    R7,R15        SET STARTING ADDRESS                82129
         B     NEXTRACK      READ ALTERNATES ONLY                82129
         SPACE 1                                                 82129
NEXTZERO SR    R7,R7        SET INITIAL CYLINDER TO ZERO         82129
NEXTRACK STCM  R7,15,IOBSEEK+3  SET TRACK ADDRESS INTO SEEK
         BAS   R9,EXCP      DO I/O                              GP03025
         CLC   HOMECC(4),IOBSEEK+3  CORRECT TRACK READ ?
         BE    TESTR0CN
         MVC   CURCYL(4),IOBSEEK+3
         LH    R3,CURCYL     GET CURRENT CYLINDER
         MH    R3,MAXHEAD      IN TRACKS
         AH    R3,CURHEAD    GET ABSOLUTE TRACK
         STH   R3,CURTRACK   SET CURRENT TRACK ADDRESS
         PRTLIST BADHOME@    SHOW BAD TRACK READ                 85064
TESTR0CN LA    R0,8
         CLM   R0,15,R0COUNT  REC=0, KEYLEN=0, DATALEN=8 ?
         BE    TESTHAFG      YES
         INC   NUMNOSR0      COUNT NON-IBM R0 RECORDS
TESTHAFG TM    HOMEFLAG,X'03'   DEFECTIVE OR ALTERNATE TRACK ?
         BZ    TRACKOK          NO - GO DO NEXT
         TM    HOMEFLAG,X'02'   DEFECTIVE TRACK ?
         BNZ   PRTBADTK      YES; PRINT DETAIL INFORMATION
         CLC   IOBSEEK+3(4),R0CYL  UNUSED ALTERNATE TRACK (2314) ?
         BE    TRACKOK       YES; IGNORE IT
         MVC   CURALT(4),IOBSEEK+3  MOVE ALTERNATE ADDRESS
         MVC   CURCYL(4),R0CYL   ADDRESS OF ORIGINAL DEFECTIVE
         B     PRTBADCM      GO TO COMMON
         SPACE 1
PRTBADTK MVC   CURCYL(4),IOBSEEK+3  SET CURRENT ADDRESS
         MVC   CURALT(4),R0CYL   PROVISIONALLY MOVE ALTERNATE
PRTBADCM LH    R3,CURCYL     GET CURRENT CYLINDER
         MH    R3,MAXHEAD      IN TRACKS
         AH    R3,CURHEAD    GET ABSOLUTE TRACK
         STH   R3,CURTRACK   SET CURRENT TRACK ADDRESS
         TM    HOMEFLAG,X'02'  DEFECTIVE TRACK ?
         BZ    PRTNTDEF      NO; DO NOT COUNT
         INC   NUMDEF        COUNT DEFECTIVE TRACKS
PRTNTDEF LA    R1,BADTRACK   SET MSG FOR PLAIN OLD DEFECTIVE TRACK
         TM    HOMEFLAG,X'01'  ALTERNATE TRACK ?
         BZ    PRTDEF        NO; PRINT
         INC   NUMAALT       COUNT ASSIGNED ALTERNATE
         LA    R1,BADALT     SET FOR DEFECTIVE ALTERNATE (X'03')
         TM    HOMEFLAG,X'02'  DEFECTIVE ?
         BNZ   PRTDEF        YES; PRINT DEFECTIVE ALTERNATE
         LA    R1,BADASSGN   ELSE PRINT ASSIGNED ALTERNATE
PRTDEF   PRTLIST (R1)        PRINT BAD TRACK MESSAGE             85064
         TM    HOMEFLAG,X'02'  DEFECTIVE TRACK ?
         BZ    TRACKOK       NO; SKIP ASSIGNMENT TEST
         CLC   CURALT(4),ZEROES  ANY ALTERNATE ?
         BE    PRTNOALT      NO
         CLC   CURALT(4),CURCYL  OR SAME AS CURRENT ?
         BNE   PRINTALT      NO; PRINT ALTERNATE ASSIGNED
PRTNOALT PRTLIST BADNOALT    SHOW NO ALTERNATE ASSIGNED          85064
         B     TRACKOK        GO DO NEXT TRACK
         SPACE 1
PRINTALT PRTLIST BAD@ALT     PRINT ALTERNATE ADDRESS             85064
*        B     TRACKOK       DO NEXT TRACK
         SPACE 1                                                 85064
TRACKOK  TM    FLAGS,FGSKIP  READING SKIP DATA ?                 85064
         BNO   TRACKDOK      NO                                  85064
         LA    R15,L'STACKSKP/2  NUMBER OF AVAILABLE SKIPS       85064
         LR    R0,R15        COPY FOR BCT                        85064
         SLR   R14,R14       COUNTER OF ERRORS                   85064
         LA    R1,STACKSKP   OFFSET READ                         85064
TRACKOLP CLC   0(2,R1),WANTSKP-STACKSKP(R1)  MATCHING OFFSET ?   85064
         BE    TRACKOBM      YES                                 85064
         LA    R14,1(,R14)   COUNT MISMATCH                      85064
TRACKOBM LA    R1,2(,R1)     INCREASE BY ONE OFFSET              85064
         BCT   R0,TRACKOLP   TRY NEXT ENTRY                      85064
         LTR   R14,R14       ANY UNEXPECTED OFFSETS ?            85064
         BZ    TRACKDOK      NO; DON'T PRINT OR COUNT            85064
         STM   R14,R15,DB    SAVE FOR FORMATTING                 85064
         PRTLIST MSGSKIP     PRINT SKIP OFFSETS                  85064
         INC   NUMWSKIP      COUNT TRACKS WITH SPECIAL SKIP OFFSETS
         SPACE 1
TRACKDOK A     R7,=F'1'      INCREASE SEEK ADDRESS BY ONE TRACK  85064
         CLM   R7,3,RELHEAD    END OF CYLINDER ?
         BNH   NEXTRACK      NO; DO IT
         NI    FLAGERR,255-FOUNDERR  RESET SINGLE I/O FLAG
         A     R7,=X'00010000'  INCREASE CYLINDER
         ICM   R7,3,ZEROES   RESET TRACK NUMBER
         CR    R7,R8         END OF VOLUME?
         BNH   NEXTRACK      NO; READ NEXT ONE
         SPACE 2
*     ALL DONE - PRINT COUNT OF BAD TRACKS
         PRTLIST FINALTOT    DISPLAY FINAL STATISTICS            85064
         PRTLIST FINALSUM,DEV=2  PRINT DEVICE SUMMARY            85064
         SPACE 2
EODAD    MODESET  KEY=ZERO            / KEY ZERO AGAIN
         MVC   DEBBASND+DEBDVMOD-DEBDASD(16),SAVEDEB   RESTORE DEB
         MODESET  KEY=NZERO           / OLD KEY AGAIN
         CLOSE  (ACCESS)
         PRTLIST TITPRE,TITLE=1  PRELIMINARY TITLE FOR ERRORS    85064
         B     LOOPTIOT      LOOK FOR ANOTHER
         SPACE 1
ENDJOB   SERVTERM ,
         LH    R9,RETCH      GET RETURN CODE                     82129
         PGMEXIT RC=(R9)                                         82129
         SPACE 2
EXCP     ICM   R1,15,NUMSTACK  GET NUMBER OF STACKED TRACKS
         BNP   EXCPREAD      NONE; READ A CYLINDER
         BCTR  R1,0
         ST    R1,NUMSTACK   SET NUMBER FOR NEXT ENTRY
         L     R1,IOBSTART-1  GET CURRENT CHAIN
         N     R1,=X'00FFFFFF'  JUST IN CASE                    GP03025
         LA    R2,STACKCLN(R1)  GET NEXT ENTRY
         TM    FLAGS,FGSKIP  READ SKIP OFFSETS ?                 85064
         BNO   *+8           NO                                  85064
         LA    R2,L'SKIPCCW(,R2)  ALLOW FOR READ SKIPS           85064
         L     R14,0(,R1)    POINT TO SEEK ADDRESS
         N     R14,=X'00FFFFFF'  JUST IN CASE                   GP03025
         MVC   IOBSEEK+3(4),2(R14)  MOVE SEEK ADDRESS
         MVC   HOMEFLAG(5+8),6(R14)  MOVE HOME ADDRESS AND R0
         ICM   R0,15,NUMSTACK  HIT LAST ONE ON STACK YET ?
         BP    EXCPBACK      NO; JUST RETURN
         CLI   ECB,X'7F'     GOOD I/O ?
         BNE   BADIO         NO
EXCPBACK ST    R2,IOBSTART-1  STASH IT
         BR    R9            RETURN TO CALLER
         SPACE 1
EXCPREAD STM   R14,R12,SAVE14    SAVE EVERYTHING
         LM    R14,R3,PATCCW  GET CCW PATTERNS
         LA    R8,STACKCCW   GET FIRST STACK CCW
         LA    R9,STACKSEE   GET FIRST STACK DATA ADDRESS
         ALR   R14,R9        RELOCATE SEEK ADDRESS
         ALR   R0,R9         RELOCATE HOME ADDRESS
         ALR   R2,R9         RELOCATE R0 ADDRESS
         STCM  R7,15,IOBSEEK+3  SAVE FIRST SEEK ADDRESS
         LA    R9,STACKCCW+8   FIRST HOME ADDRESS CCW
         ST    R9,IOBSTART-1  SET IOBSTART ADDRESS
         MVI   IOBFLAG1,X'42'  SET CMD CHAIN/UNRELATED
         LA    R9,MAXSTACK   GET MAXIMUM TRACKS TO STACK
         LH    R6,MAXHEAD    GET TRACKS PER CYLINDER
         CR    R6,R9         LARGER ?
         BNH   *+6
         LR    R6,R9         USE SMALLER VALUE
         LA    R9,1          MAKE A CONSTANT
         LA    R4,STACKCLN   SET LENGTH OF CCW CHAIN
         LA    R5,STACKDLN   SET LENGTH OF DATA CHAIN
         TM    FLAGS,FGSKIP  READ SKIP OFFSETS ?                 85064
         BNO   EXCPNSKP      NO                                  85064
         LA    R4,L'SKIPCCW(,R4)  ALLOW FOR READ SKIPS           85064
         B     EXCPYSKP      NO SEEK CCW ALLOWED AFTER X'0A'     85064
EXCPNSKP TM    FLAGERR,FOUNDERR  PRIOR ERROR ?                   85064
         BZ    LOOPCCWS      NO; BUILD CHAIN
EXCPYSKP LR    R6,R9         ELSE USE SINGLE TRACK MODE          85064
LOOPCCWS STM   R14,R3,0(R8)  MAKE THE CCWS
         TM    FLAGS,FGSKIP  DO SKIP READS ?                     85064
         BNO   NOSKIPC                                           85064
         STM   R2,R3,24(R8)  MAKE ROOM FOR READ SKIP CCW         85064
         MVC   16(8,R8),SKIPCCW  ADD SKIP CCW IN                 85064
         ST    R14,DB        SAVE SEEK ADDRESS                   85064
         AL    R14,16(,R8)   RELOCATE SKIP DATA                  85064
         STCM  R14,7,17(R8)  STASH ADDRESS                       85064
         L     R14,DB        RESTORE SEEK                        85064
NOSKIPC  LR    R1,R14        COPY                               GP03025
         N     R1,=X'00FFFFFF'  MAKE TRUE ADDRESS               GP03025
         STCM  R7,15,2(R1)   MAKE THE SEEK ADDRESS              GP03025
         AR    R14,R5        SET TO NEXT SEEK ADDRESS
         AR    R0,R5         SET NEXT HOME ADDRESS
         AR    R2,R5         SET NEXT R0 ADDRESS
         ALR   R8,R4         GET NEXT CCW CHAIN ADDRESS
         ALR   R7,R9         SET NEXT SEEK VALUE
         CLM   R7,3,MAXHEAD  IN NEW CYLINDER ?
         BNL   LOOPCCWX      YES; TERMINATE CHAIN
         BCT   R6,LOOPCCWS   DO MORE
LOOPCCWX SH    R8,=H'8'      BUMP BACK TO LAST CCW
         NI    4(R8),255-X'40'  RESET CHAINING
         LM    R14,R12,SAVE14   RESTORE REGISTERS
         EXCP  IOB
         WAIT  ,ECB=ECB
         SR    R15,R15                                          GP03025
         ICM   R15,7,IOBCSW  GET LAST CCW+8
         LA    R14,STACKCCW  GET FIRST CCW
         SR    R15,R14
         BNP   BADIOTST      NONE EXECUTED ?
         SR    R14,R14                                          GP03025
         LA    R0,STACKCLN   GET LENGTH OF CHAIN
         TM    FLAGS,FGSKIP  READ SKIP OFFSETS ?                 85064
         BNO   *+8           NO                                  85064
         LA    R0,STACKCLN+L'SKIPCCW  ALLOW FOR READ SKIPS       85064
         DR    R14,R0        NUMBER OF TRACKS
         LTR   R15,R15       MAKE NUMBER OF CHAIN ENTRIES READ
         BZ    BADIO         I/O ERROR
         ST    R15,NUMSTACK  SET NUMBER STACKED
         L     R1,IOBSTART-1
         N     R1,=X'00FFFFFF'  JUST IN CASE                    GP03025
         SH    R1,=H'8'
         ST    R1,IOBSTART-1  STASH IT BACK
         B     EXCP          PROCESS; I/O ERRORS DETECTED LATER
BADIOTST TM    FLAGERR,FOUNDERR  PREVIOUS ERROR ?
         BNZ   BADIO         YES; FLAG THIS ONE
         OI    FLAGERR,FOUNDERR  SIGNAL ERROR
         OI    DCBIFLGS,DCBIFNE3  AVOID DDR FOR SUBSEQUENT ERRORS
         B     EXCPREAD      DO IT AGAIN FOR SINGLE TRACK ONLY
         SPACE 1
BADIO    LA    R2,SYNADAT    SET ERROR MESSAGE ADDRESS
         SYNADAF ACSMETH=EXCP,PARM1=IOB
         MVC   0(L'SYNADAT,R2),68(R1)  MOVE FRAGMENT OF MESSAGE
         SYNADRLS ,          FREE WORK AREA
         MVC   CURCYL(4),IOBSEEK+3  MOVE ADDRESS
         LH    R3,CURCYL     GET CURRENT CYLINDER
         MH    R3,MAXHEAD      IN TRACKS
         AH    R3,CURHEAD    GET ABSOLUTE TRACK
         STH   R3,CURTRACK   SET CURRENT TRACK ADDRESS
         PRTLIST BADIOERR    FORMAT MESSGE                       85064
         OI    DCBIFLGS,DCBIFNE3  AVOID DDR FOR SUBSEQUENT ERRORS
         XC    NUMSTACK,NUMSTACK  FORCE I/O NEXT TIME
         OI    FLAGERR,FOUNDERR  FORCE SINGLE I/O PER TRACK
         INC   NUMERR        COUNT ERRORS
         B     TRACKOK       DO NEXT TRACK
         SPACE 2
DEVERRCL CLOSE (ACCESS)      CLOSE THE FILE
DEVERR   PRTLIST BADDEV,DEV=(1,2)    NOTIFY OF BAD DD CARD       85064
         OI    RETCH+1,8     SET DD ERROR                        82129
         B     LOOPTIOT      TRY ANOTHER
         SPACE 1
PATCCW   CCW   X'07',0,X'40',6                  SEEK
         CCW   X'1A',STACKHOM-STACKSEE,X'40',5  READ HOME ADDRESS
         CCW   X'16',STACKR0-STACKSEE,X'60',8   READ RECORD ZERO
SKIPCCW  CCW   X'0A',STACKSKP-STACKSEE,X'60',L'STACKSKP  RD SKIP 85064
         SPACE 2
RELXTENT DC    0A(0),X'40',AL3(0)   FILE MASK / UCB ADDRESS
FILEMASK EQU   X'40'  ALLOW SEEKS, INHIBIT WRITE
         DC    H'0'          WHERE HAVE YOU BIN
         DC    2H'0'         STARTING CYLINDER / TRACK
RELCYL   DC    H'0'          1/2  END CYLINDER
RELHEAD  DC    H'0'          2/2  END TRACK
ALLTRKS  DC    H'0'          TRACKS IN EXTENT
MAXALT   DC    H'0'          MAXIMUM NUMBER OF ALTERNATES
MAXCYL   DC    H'0'          1/2  NUMBER OF CYLINDERS ON PACK
MAXHEAD  DC    H'0'          2/2  NUMBER OF TRACKS PER CYLINDER
CURTRACK DC    H'0'          CURRENT ABSOLUTE TRACK NUMBER
CURCYL   DC    H'0'          1/2  CURRENT CYLINDER NUMBER
CURHEAD  DC    H'0'          2/2  CURRENT TRACK NUMBER
CURALT   DC    H'0'          1/2  CURRENT ALTERNATE CYLINDER
CURALTH  DC    H'0'          2/2  CURRENT ALTERNATE TRACK
ECB      DC    F'0'
SAVEDEB  DC    4F'0'         VTOC DEB EXTENTS
ZEROES   DC    XL8'0'        CONSTANT
         SPACE 1
CURWORK  DS    0F            AREA CLEARED FOR SUBSEQUENT DD
NUMDEF   DC    F'0'          COUNT OF DEFECTIVE TRACKS
NUMAALT  DC    F'0'          NUMBER OF ASSIGNED ALTERNATES
NUMNOSR0 DC    F'0'          NUMBER OF NON-IBM R0 COUNT FIELDS
NUMERR   DC    F'0'          NUMBER OF I/O ERRORS
NUMSTACK DC    F'0'          NUMBER OF TRACKS STACKED IN I/O
NUMWSKIP DC    F'0'          NUMBER OF TRACKS WITH SPECIAL SKIPS 85064
FLAGERR  DC    X'00'         ERROR PROCESSING FLAG
FOUNDERR EQU   X'80'           SET ERROR FOUND
CURLEN   EQU   *-CURWORK     LENGTH CLEARED FOR RECURSION
         SPACE 2
HOMEADDR DS    0CL5       HOME ADDRESS
HOMEFLAG DS    C
HOMECC   DS    CL2
HOMEHH   DS    CL2
         SPACE 2
RECZERO  DS    0CL8       RECORD ZERO
R0CYL    DS    CL2
R0HEAD   DS    CL2
R0COUNT  DS    XL4           RECORD NUMBER/KEYLEN/DATALENGTH
         SPACE 1
VOLSER   DC    CL6' '        CURRENT VOL-SER
VOLTYPE  DC    CL8' '        DEVICE TYPE
VOLUNIT  DC    XL2'0'        DEVICE NUMBER                      GP03025
SYNADAT  DC    CL53' '       SYNADAF MESSAGE
DCALTAT  DC    C'AT ADDRESS'
DCALT    DC    C'ALTERNATE'
         SPACE 1                                                 85064
SKIPOFF  DC    16AL1(0)      SET INDEX TO SUPPORTED SKIP OFFSET  85064
         ORG   SKIPOFF+X'0C' 3375                                85064
         DC    AL1(1)        FIRST SKIP OFFSET ENTRY             85064
         ORG   SKIPOFF+X'0E' 3380                                85064
         DC    AL1(2)        SECOND SKIP OFFSET ENTRY            85064
         ORG   SKIPOFF+X'0F' 3390                                86334
         DC    AL1(3)        POINT TO GUESSTIMATED ENTRY         92185
         ORG   ,                                                 85064
EXPOFF   DC    X'FB85FB81FB7DFB79FB75FB71FB6D'                   85064
*        ABOVE ARE EXPECTED OFFSETS FOR MEMOREX 3695/97/98 SPINDLES
         DC    X'FA03FA00F9FDF9FAF9F7F9F4F9F1'  NAS 3380         86334
         DC    X'F915F912F90FF90CF909F906F903'  IBM 3390 ?       92185
         SPACE 1
FORMAT4  FDPRT 'DEVICE INFORMATION FOR UCB',NL,PADR              82129
         FDPRT VOLUNIT,HEX                                      GP03025
         FDPRT ', TYPE',PADR
         FDPRT VOLTYPE,DEB
         FDPRT ', VOLUME',PADR
         FDPRT VOLSER,DEB
         FDPRT 'IS:',PAD                                         82129
         FDPRT ALLTRKS,I
         FDPRT 'TRACKS,',PAD
         FDPRT MAXCYL,I
         FDPRT 'CYLINDERS,',PAD
         FDPRT MAXHEAD,I
         FDPRT 'TRACKS PER CYLINDER.',PAD
         FDPRT MAXALT,I,NL,RADJ,LEN=10
         FDPRT 'MAXIMUM ALTERNATE TRACKS',PADL
         FDPRT 'THE FORMAT 4 DSCB SPECIFIES:',NL,PADR            82129
         FDPRT CURTRACK,I
         FDPRT 'TRACKS,',PAD
         FDPRT CURCYL,I
         FDPRT 'CYLINDERS,',PAD
         FDPRT CURHEAD,I
         FDPRT 'TRACKS PER CYLINDER.',PAD
         FDPRT DS4NOATK,I,NL,RADJ,LEN=10
         FDPRT 'ALTERNATES ARE AVAILABLE, STARTING',PAD
         FDPRT DCALTAT,PAD
         FDPRT DS4HCCHH,2,HEX
         FDPRT '.'
         FDPRT DS4HCCHH+2,2,HEX
         FDCLC MAXCYL,CURCYL,4,BE=FORMAT4X
         FDPRT '***** FORMAT 4 DOES NOT MATCH DEVICE. FORMAT 4 VALUES W*
               ILL BE USED *****',NL
FORMAT4X FDPRT *END
         SPACE 1
BADASSGN FDOPT NL,CC=C'0'    DOUBLE-SPACE
         FDPRT DCALT,PADR
         FDPRT DCALTAT,PADR
         FDPRT CURALT,HEX
         FDPRT '.'
         FDPRT CURALTH,HEX,PADR
         FDPRT 'ASSIGNED AS BACKUP FOR',PADR
         FDEXEC BADALTCM,BADALTCX  DISPLAY TRACK AND ADDRESS
         FDPRT *END
         SPACE 1
BADALT   FDOPT NL,CC=C'0'    DOUBLE-SPACE
         FDPRT DCALT,PADR    MESSAGE FOR DEFECTIVE ALTERNATE
         FDGOTO BADALTCM     GO TO COMMON
BADTRACK FDOPT NL,CC=C'0'    DOUBLE-SPACE
BADALTCM FDPRT 'TRACK',PADR
         FDPRT CURTRACK,I,RADJ,LEN=6
         FDPRT DCALTAT,PAD
         FDPRT CURCYL,HEX,LEN=4
         FDPRT '.'
BADALTCX FDPRT CURHEAD,HEX,PADR,LEN=4
         FDPRT 'IS DEFECTIVE.'
         FDPRT *END
         SPACE 2
BAD@ALT  FDPRT ' ',LEN=5     INDENT A BIT
         FDPRT 'ITS ALTERNATE IS LOCATED AT ADDRESS'
         FDPRT CURALT,HEX,PADL
         FDPRT '.'
         FDPRT CURALTH,HEX,PADR
         FDPRT *END
         SPACE 2
BADNOALT FDPRT ' ',LEN=5     INDENT
         FDPRT 'THERE IS NO ALTERNATE TRACK ASSIGNED.'
         FDPRT *END
         SPACE 2                                                 85064
MSGSKIP  FDPRT DB,4,I,RADJ,PADR,NL,LEN=8                         85064
         FDPRT 'OF',PAD                                          85069
         FDPRT DB+4,4,I,PAD                                      85064
         FDPRT 'SKIP OFFSETS IN USE ON TRACK',PAD                85069
         FDPRT STACKSEE+2,2,HEX,PADL                             85064
         FDPRT '.'                                               85064
         FDPRT STACKSEE+4,2,HEX,PADR                             85064
         FDPRT ': ',PAD                                          85064
         FDCLC STACKSKP+0,WANTSKP+0,2,BE=MSGSKP2                 85064
         FDPRT STACKSKP+0,2,HEX,PAD                              85064
MSGSKP2  FDCLC STACKSKP+2,WANTSKP+2,2,BE=MSGSKP4                 85064
         FDPRT STACKSKP+2,2,HEX,PAD                              85064
MSGSKP4  FDCLC STACKSKP+4,WANTSKP+4,2,BE=MSGSKP6                 85064
         FDPRT STACKSKP+4,2,HEX,PAD                              85064
MSGSKP6  FDCLC STACKSKP+6,WANTSKP+6,2,BE=MSGSKP8                 85064
         FDPRT STACKSKP+6,2,HEX,PAD                              85064
MSGSKP8  FDCLC STACKSKP+8,WANTSKP+8,2,BE=MSGSKP10                85064
         FDPRT STACKSKP+8,2,HEX,PAD                              85064
MSGSKP10 FDCLC STACKSKP+10,WANTSKP+10,2,BE=MSGSKP12              85064
         FDPRT STACKSKP+10,2,HEX,PAD                             85064
MSGSKP12 FDCLC STACKSKP+12,WANTSKP+12,2,BE=MSGSKP14              85064
         FDPRT STACKSKP+12,2,HEX,PAD                             85064
MSGSKP14 FDPRT *END                                              85064
         SPACE 2
TITPRE   FDOPT NL,CC=C'#'    AUTOMATIC DATE/TIME/PAGE OPTION     81278
         FDPRT 'ERROR LISTING',RADJ,LEN=40                       81278
         FDPRT *END                                              81278
         SPACE 1                                                 85064
TITSUM   FDOPT NL,CC=C'#'    SUMMARY TITLE                       85064
         FDPRT 'SUMMARY',PAD                                     85064
         FDEXEC TITSCOM,TITQUICK                                 85069
         FDPRT *END                                              85069
TITLE    FDOPT NL,CC=C'#'    AUTOMATIC DATE/TIME/PAGE OPTION     81278
         FDTM  FLAGS,FGFIX,BNZ=TITFIX                            82129
TITSCOM  FDPRT 'LISTING OF',PADR                                 85064
         FDTM  FLAGS,FGQUICK,BNZ=TITQUICK                        82129
         FDPRT 'DEFECTIVE AND',PADR                              82129
TITQUICK FDPRT 'ALTERNATE TRACKS'                                85069
         FDPRT 'FOR',PADL                                        85069
         FDGOTO TITCOM                                           82129
TITFIX   FDPRT 'TRACK ANALYSIS AND CORRECTION FOR ADDRESS',PADR  82129
         FDPRT FIXCCHH,2,HEX                                     82129
         FDPRT '.'                                               82129
         FDPRT FIXCCHH+2,2,HEX                                   82129
         FDPRT 'ON',PAD                                          82129
TITCOM   FDPRT VOLSER,DEB,PADL  VOL-SER                          82129
         FDPRT '(',PADL
         FDPRT VOLTYPE,DEB
         FDPRT ') ON',PADR
         FDPRT VOLUNIT,HEX                                      GP03025
         FDPRT *END
         SPACE 2
FINALTOT FDOPT NL,CC=C'0'
         FDPRT '********* NUMBER OF DEFECTIVE TRACKS IS',PADR
         FDTM  FLAGS,FGQUICK,BZ=FINALTNQ                         82129
         FDPRT 'NOT AVAILABLE,',PADR                             82129
         FDGOTO FINALTQK                                         82129
FINALTNQ FDPRT NUMDEF,I,PADR                                     82129
         FDPRT ' ',LEN=10
FINALTQK FDPRT 'NUMBER OF ASSIGNED ALTERNATES IS',PADR           82129
         FDPRT NUMAALT,I
         FDCLC NUMNOSR0,ZEROES,4,BE=FINALTOS                     85064
         FDPRT NUMNOSR0,I,RADJ,PADR,NL,LEN=10
         FDPRT 'RECORDS ARE NOT IN IBM FORMAT'
FINALTOS FDCLC NUMWSKIP,ZEROES,4,BE=FINALTOX                     85064
         FDPRT NUMWSKIP,I,RADJ,PADR,NL,LEN=10                    85064
         FDPRT 'TRACKS WITH SPECIAL SKIP OFFSETS; EXPECTED :',PAD
         FDPRT WANTSKP,2,HEX,PAD                                 85064
         FDPRT WANTSKP+2,2,HEX,PAD                               85064
         FDPRT WANTSKP+4,2,HEX,PAD                               85064
         FDPRT WANTSKP+6,2,HEX,PAD                               85064
         FDPRT WANTSKP+8,2,HEX,PAD                               85064
         FDPRT WANTSKP+10,2,HEX,PAD                              85064
         FDPRT WANTSKP+12,2,HEX,PAD                              85064
FINALTOX FDCLC NUMERR,ZEROES,4,BE=FINALTOY
         FDPRT NUMERR,I,RADJ,PADR,NL,LEN=10
         FDPRT 'I/O ERRORS ENCOUNTERED',PADL
FINALTOY FDPRT *END
         SPACE 1                                                 85064
FINALSUM FDPRT VOLSER,NL,PAD                                     85064
         FDPRT VOLUNIT,PAD,HEX                                   85064
         FDPRT VOLTYPE,PAD                                       85064
         FDPRT ALLTRKS,I,RADJ,PAD,LEN=7                          85069
         FDPRT DS4DEVSZ,2,I,RADJ,PAD,LEN=5                       85069
         FDPRT '('                                               85064
         FDPRT DS4DEVSZ,2,HEX,RADJ,LEN=4                         85069
         FDPRT ')'                                               85064
         FDPRT DS4DEVSZ+2,2,I,RADJ,PAD,LEN=7                     85069
         FDPRT '('                                               85064
         FDPRT DS4DEVSZ+2,2,HEX,RADJ,LEN=4                       85069
         FDPRT ')'                                               85064
         FDPRT DS4NOATK,I,RADJ,PAD,LEN=5                         85064
         FDPRT MAXALT,I,RADJ,PAD,LEN=9                           85069
         FDTM  FLAGS,FGQUICK,BZ=FINSDEF                          85064
         FDPRT 'N/A',RADJ,PAD,LEN=10                             85064
         FDGOTO FINNDEF                                          85064
FINSDEF  FDPRT NUMDEF,I,RADJ,PAD,LEN=10                          85064
FINNDEF  FDPRT NUMAALT,I,RADJ,PAD,LEN=10                         85064
         FDTM  FLAGS,FGSKIP,BZ=FINSSKP,BM=FINSSKP                85064
         FDPRT NUMWSKIP,I,RADJ,PAD,LEN=10                        85064
         FDGOTO FINNSKP                                          85064
FINSSKP  FDPRT 'N/A',RADJ,PAD,LEN=10                             85064
FINNSKP  FDPRT NUMNOSR0,I,RADJ,PAD,LEN=10                        85064
         FDPRT NUMERR,I,RADJ,PAD,LEN=10                          85064
         FDPRT *END                                              85064
         SPACE 1                                                 85064
TITLESUM FDPRT 'VOLUME',NL,PAD,LEN=L'VOLSER                      85064
         FDPRT 'DEV#',PAD,LEN=L'VOLUNIT                         GP03025
         FDPRT 'TYPE',PAD,LEN=L'VOLTYPE                          85064
         FDPRT 'TRACKS',RADJ,PAD,LEN=7                           85064
         FDPRT 'CYLINDERS',RADJ,PAD,LEN=12                       85064
         FDPRT 'TRACKS/CYL',RADJ,PAD,LEN=14                      85069
         FDPRT 'AVAIL',RADJ,PAD,LEN=5                            85064
         FDPRT 'ALTERNATES',PAD                                  85064
         FDPRT 'FLAGGED',RADJ,PAD,LEN=9                          85069
         FDPRT 'ASSIGNED',RADJ,PAD,LEN=10                        85064
         FDPRT 'OFFSET',RADJ,PAD,LEN=10                          85064
         FDPRT 'BAD R0',RADJ,PAD,LEN=10                          85064
         FDPRT 'I/O ERR',RADJ,PAD,LEN=10                         85064
         FDPRT *END                                              85064
         SPACE 2
BADIOERR FDOPT NL,CC=C'0'
         FDPRT 'I/O ERROR ON',PADR
         FDEXEC BADALTCM,BADALTCX
         FDPRT SYNADAT,DEB,PADL
         FDPRT *END
         SPACE 1
BADHOME@ FDOPT NL,CC=C'0'    DOUBLE-SPACE
         FDPRT 'INCORRECT HOME ADDRESS READ FOR',PADR
         FDEXEC BADALTCM,BADALTCX
         FDPRT 'HOME ADDRESS READ IS',RADJ,PADR,LEN=23
         FDPRT HOMECC,HEX
         FDPRT '.'
         FDPRT HOMEHH,HEX
         FDPRT *END
         SPACE 1
BADDEV   FDOPT NL,CC=C'0'
         FDPRT 'ERROR PROCESSING DD',PADR
         FDPRT DCBDDNAM,DEB
         FDPRT '. FILE IS NOT DISK, OR COULD NOT BE OPENED'
         FDPRT *END
         SPACE 2
IOB      DS    0D
IOBFLAG1 DC    B'01000010'  COMMAND-CHAIN AND NON-RELATED BITS ON
IOBFLAG2 DC    X'0'
IOBSENS0 DC    X'0'
IOBSENS1 DC    X'0'
IOBECBCC DC    X'0'
IOBECBPT DC    AL3(ECB)
IOBFLAG3 DC    X'0'
IOBCSW   DC    7X'0'
IOBSIOCC DC    X'0'
IOBSTART DC    AL3(PATCCW)
         DC    X'0'
IOBDCBPT DC    AL3(ACCESS)
IOBRESTR DC    X'0'
         DC    AL3(0)
IOBINCAM DC    2X'0'
IOBERRCT DC    2X'0'
IOBSEEK  DC    X'0',6X'0',X'0'
         SPACE 2
         PRINT &PRTSYS
ACCESS   DCB   DSORG=PS,DDNAME=DASD,MACRF=(E),EODAD=EODAD,IOBAD=IOB,   *
               EXLST=DCBEXIT,RECFM=UT  (RECFM ADDED FOR ATLAS)   82129
         SPACE 1
DCBEXIT  DC    0F'0',X'87',AL3(JFCB)
JFCB     DS    0D
         IEFJFCBN ,          EXPAND JFCB IN-LINE
         SPACE 2
SYSPRINT PRTWORK SYSPRINT,SYSTERM,TITLE=3                        82129
SUMPRINT PRTWORK SUMPRINT,TITLE=5                                85064
         SPACE 2
SAVE     DSECT ,
DB       DS    D             WORK AREA                           82129
@SERVICE DC    A(0)          ADDRESS OF @SERVICE ROUTINE
@PRINTER DC    A(0)          ADDRESS OF PRINT ROUTINE
TIOTADDR DC    A(0)          ADDRESS OF CURRENT TIOT ENTRY
         SPACE 1                                                 82129
FIXADDR  DS    A             STORAGE ADDRESS FOR READ BUFFER     82129
FIXSIZE  DS    A             GETMAINED SIZE OF BUFFER            82129
FIXLEN   DS    A             MAXIMUM BLOCKSIZE ON TRACK (+ IF RPS)
FIXCCHH  DS    XL8,X         CCHH OF TRACK TO BE CHECKED/FIXED   82129
         SPACE 1                                                 82129
FLAGS    DC    X'00'         PROCESSING FLAGS                    82129
FGFIX    EQU   X'80'           FIX KNOWN BAD TRACK               82129
FGQUICK  EQU   X'40'           SCAN TROUGH ALTERNATE AREA ONLY   82129
FGSKIPU  EQU   X'20'           USER REQUESTED SKIP OFFSETS       85064
FGSKIPD  EQU   X'10'           ON FOR SUPPORTED DEVICES          85064
FGSKIP   EQU   FGSKIPD+FGSKIPU  TRIGGER FOR READ SKIP PROCESSING 85064
RETCH    DC    H'0'          RETURN CODE                         82129
         SPACE 1
         IECSDSL1 4          EXPAND FORMAT 4
DS4LNGTH EQU   *-DS4IDFMT
         SPACE 1
STACKCCW CCW   X'07',STACKSEE,X'40',6  SEEK
STACKONE CCW   X'1A',STACKHOM,X'40',5  READ HOME ADDRESS
         CCW   X'16',STACKR0,X'40',8   READ R0
STACKCLN EQU   *-STACKCCW
         DS    (MAXSTACK-1)XL(STACKCLN)  CCWS FOR ONE CYLINDER
         DS    (MAXSTACK)XL(L'SKIPCCW)  ROOM FOR READ SKIP OPT.  85064
STACKSEE DS    XL6           SEEK ADDRESS
STACKHOM DS    XL5           HOME FLAG/ADDRESS
STACKR0  DS    XL8           RECORD ZERO COUNT FIELD
STACKSKP DS    XL14          SKIP OFFSETS                        85064
STACKDLN EQU   *-STACKSEE
         DS    (MAXSTACK-1)XL(STACKDLN)
WANTSKP  DS    XL(L'STACKSKP)  EXPECTED SKIP OFFSET              85064
PRTDEV   DS    X             PRINT DEVICE FLAGS FOR NORMAL MESSAGES
SAVEEND  EQU   *             END OF DYNAMIC WORK AREA
         SPACE 2
         IEZDEB ,
         SPACE 1
CVTDSECT DSECT ,
         CVT   DSECT=YES
         SPACE 1
         IHACDE ,
         SPACE 1
         IEFUCBOB ,
         SPACE 1
         DCBD  DSORG=PS
         SPACE 1
         IHADVCT ,                                               82129
         SPACE 1                                                 82129
         IEFTIOT1 ,
         AIF   (NOT &MVSXA).NODCE99                              93162
         IECDDCE ,                                               93162
.NODCE99 END   ,
