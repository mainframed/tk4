DDAT     TITLE 'U. OF WATERLOO - UNUPDATE'
         COPY  OPTIONGB                                          84164
         SPACE 2                                                 84164
         SYSPARM LIST=YES                                        84164
         SPACE 2                                                 84164
***********************************************************************
*                                                                     *
*        DOWNDATE WAS WRITTEN AND SUPPLIED BY WATERLOO UNIVERSITY.    *
*                                                                     *
*        THIS PROGRAM WAS EXPANDED AND ADAPTED FROM THE ORIGINAL      *
*        VERSION.                                                     *
*           WYLBUR FILE SUPPORT ADDED.                           84164
*                                                                     *
***********************************************************************
         EJECT ,                                                 84164
         PRINT &PRTSOR                                           84164
DOWNDATE PGMHEAD ZERO12,BASE=R12,PARM=R9                        GP03027
         SPACE 1                                                 84164
         SERVINIT ,          PREPARE FOR THE SERVICE ROUTINE     84164
         SERVCALL LPALD,=C'@PRINTER'   LOAD THE PRINT ROUTINE    84164
         ST    R0,@PRINTER                                       84164
         SERVCALL LPALD,=C'@INPREAD'   LOAD THE INPUT ROUTINE    84164
         ST    R0,@INPREAD                                       84164
         PRTOPEN SYSPRINT,OPT=(DUMMY)                            84164
         CH    R15,=H'4'     USABLE ?                            84164
         BNH   OPENPRTS                                          84164
         PRTOPEN CONSOLE,DEV=4  OPEN A CONSOLE FILE INSTEAD      84164
OPENPRTS PRTLIST HEADER1,TITLE=1  PRINT TOP HEADER               84164
         LA    R3,SYSUT1                                         84164
         INPOPEN (R3),DEV=1,OPT=(ABEND)                          84164
         CH    R15,=H'4'     PROBLEM ?                           84164
         BH    OPENFAIL      YES                                 84164
         LA    R3,SYSUT2                                         84164
         INPOPEN (R3),DEV=2,OPT=(ABEND)                          84164
         CH    R15,=H'4'     PROBLEM ?                           84164
         BH    OPENFAIL                                          84164
         SPACE 1                                                 84164
         LA    R3,SYSUT1                                         84164
         SERVCALL RJFCB,(R3) GET THE JFCB FOR 1ST INPUT          84164
         BXH   R15,R15,OPENFAIL   TOO BAD                        84164
         MVC   UT1JFCB(JFCBLGTH),0(R1)  COPY IT                  84164
         LA    R3,SYSUT2                                         84164
         SERVCALL RJFCB,(R3)   GET THE SECOND JFCB               84164
         BXLE  R15,R15,OPENINP  OPEN THE INPUT FILES             84164
OPENFAIL PRTLIST BDOPIN,DEV=(1,3)                                84164
         SERVTERM ,          CLOSE AND FREE                      84164
         ABEND 2540                                              84164
         SPACE 2                                                 84164
OPENINP  MVC   UT2JFCB(JFCBLGTH),0(R1)  COPY PASSED JFCB         84164
         LA    R2,UT1JFCB    POINT TO FIRST INPUT JFCB           84164
         PRTLIST HEADER2,TITLE=3                                 84164
         LA    R2,UT2JFCB    NOW DO OTHER ONE                    84164
         PRTLIST HEADER3,TITLE=4                                 84164
         PRTOPEN SYSPUNCH,DEV=2,OPT=(DUMMY,NOWTO)                84164
         CH    R15,=H'4'     AVAILABLE ?                         84164
         BNH   OPENPUNS                                          84164
         PRTF  'SYSPUNCH FUNCTION DISABLED - NO DD',CC=NO        84164
         PRTSPACE 1          SPACE PRINTER                       84164
OPENPUNS OI    PROFLAGS,FGPUN  SET FOR PUNCHING                  84164
         MVI   BUFFER1,C' '                                      84164
         MVC   BUFFER1+1(L'BUFFER1-1),BUFFER1  BLANK BUFFER      84164
         MVC   BUFFER2,BUFFER1   BLANK SECOND BUFFER ALSO        84164
         B         ADVANCE2
         SPACE 1                                                 84164
COMPARE  CLC   BUF1SEQ,BUF2SEQ  COMPARE SEQUENCE NUMBERS         84164
         BL        DELETE
         BE        ADVANCE
ADD      MVC   BUF2MSG,MESS1                                     84164
         PRTF  BUFFER2,L'BUFFER2                                 84164
         BAL   R8,$PUNCH     PUNCH A CARD, MAYBE                 84164
         INC   NUMADD        COUNT CARDS ADDED                   84164
GETCARD2 BAL   R9,GETUT2     GET A CARD FROM SYSUT2              84164
         B     COMPARE       GO TO COMPARE
DELETE   CLC   BUF1SEQ,=CL8' '                                   84164
         BE        DELETE2
         MVC   BUF1MSG,MESS2                                     84164
         TM    PROFLAGS,FGDEL  DELETION TO BE PUNCHED ?          84164
         BNZ   DELETE0   YES                                     84164
         OI    PROFLAGS,FGDEL  SET FOR DELETION                  84164
         MVC   SEQ1,BUF1SEQ                                      84164
DELETE0  MVC   SEQ2,BUF1SEQ                                      84164
         INC   NUMDEL        COUNT CARDS DELETED                 84164
DELETE1  PRTF  BUFFER1,L'BUFFER1                                 84164
         BAL   R9,GETUT1     GET A CARD FROM SYSUT1              84164
         B     COMPARE       GO TO COMPARE
DELETE2  MVC   BUF1MSG,MESS5                                     84164
         B         DELETE1
ADVANCE  CLC   BUF1CARD,BUF2CARD                                 84164
         BNE   REPLACE       CARD CHANGED                        84164
         INC   NUMEQUAL      COUNT EQUALS                        84164
         B     ADVANCE1      READ TWO MORE                       84164
REPLACE  PRTROOM 2           ASK FOR TWO CONTIGUOUS LINES        84164
         INC   NUMCHG        COUNT CARDS CHANGED                 84164
         MVC   BUF1MSG,MESS3                                     84164
         PRTF  BUFFER1,L'BUFFER1                                 84164
         MVC   BUF2MSG,MESS4                                     84164
         PRTF  BUFFER2,L'BUFFER2                                 84164
         BAL   R8,$PUNCH                                         84164
ADVANCE1 TM    PROFLAGS,FGDEL  DELETION ?                        84164
         BZ    ADVANCE2                                          84164
         NI    PROFLAGS,255-FGPUN  RESET PUNCHING                84164
         BAL   R8,$PUNCH                                         84164
         OI    PROFLAGS,FGPUN  SET PUNCHING                      84164
ADVANCE2 BAL   R9,GETUT1     GET A CARD FROM SYSUT1              84164
         BAL   R9,GETUT2     GET A CARD FROM SYSUT2              84164
         B     COMPARE       TEST SEQUENCES
         SPACE 2                                                 84164
EOF1     LA    R2,BUFFER1                                        84164
         B     EOF                                               84164
EOF2     LA    R2,BUFFER2                                        84164
EOF      TM    PROFLAGS,FGEND  PRIOR EOF ?                       84164
         BNZ   CHKDEL        YES; CHECK FOR PENDING DELETION     84164
         OI    PROFLAGS,FGEND  SET FIRST EOF                     84164
         MVC   BUF1SEQ-BUFFER1(8,R2),=8X'FF'  MAXIMUM SEQUENCE   84164
         BR    R9                                                84164
         SPACE 1                                                 84164
CHKDEL   TM    PROFLAGS,FGDEL  DELETION ?                        84164
         BZ    CHKCHNG                                           84164
         NI    PROFLAGS,255-FGPUN                                84164
         BAL   R8,$PUNCH                                         84164
CHKCHNG  CLI       CHANGESW,0
         BNE       CHECKDA
         PRTF  '0NO CHANGES IN THIS DATASET'                     84164
         B         CLOSE
CHECKDA  DEVTYPE SYSPUNCH,DB  GET DEVICE CHARACTERISTICS         84164
         BXH   R15,R15,CLOSE  SKIP ON FAILURE (?)                84164
         TM    DB+2,UCB3DACC+UCB3TAPE  REREADABLE ?              84164
         BNM   CLOSE         PROBABLY NOT; SKIP                  84164
         PRTCLOSE DEV=2      CLOSE THE PUNCH                     84164
         INPOPEN PUNCHIN,DEV=3,OPT=(NOWTO,DUMMY)  READ THE PUNCH FILE
         CH    R15,=H'4'     GOOD RETURN ?                       84164
         BH    CLOSE         NO; SKIP                            84164
         PRTLIST HEADSUP,TITLE=1  CHANGE THE TITLE               84164
PRTLOOP  INPGET DEV=3        GET A CARD FROM THE PUNCH FILE      84164
         BXH   R15,R15,ERR3  I/O OR PROCESSING ERROR             84164
         PRTF  (R1),80,CC=NO  PRINT IT AS IS                     84164
         B         PRTLOOP
         SPACE 2                                                 84164
CLOSE    LA    R9,20         SET MAXIMUM DESIRED RETURN CODE     84164
         LH    R8,RETCH      GET THE RETURN CODE                 84164
         CR    R8,R9         IS IT VALID CODE ?                  84164
         BNH   *+6           YES; LEAVE IT                       84164
         LR    R8,R9         ELSE CLIP IT                        84164
         LA    R9,4          SET STANDARD CODE                   84164
         CR    R8,R9         NORMAL ?                            84164
         BH    CLOSED        NO; LEAVE IT                        84164
         L     R0,NUMGET1    GET CARDS READ                      84164
         A     R0,NUMGET2      FROM BOTH FILES                   84164
         BNZ   *+12          OK IF SOME                          84164
         LA    R8,16         SET ERROR IF NONE AT ALL            84164
         B     CLOSED                                            84164
         LTR   R8,R8         ANY CHANGES AT ALL                  84164
         BZ    CLOSED        NO; LEAVE ZERO                      84164
         ICM   R0,15,NUMEQUAL  ANY MATCHED ?                     84164
         BNZ   CLOSED        YES; LEAVE CODE 4                   84164
         SLA   R8,1          CHANGE TO CODE 8 - NO MATCH         84164
CLOSED   STH   R8,RETCH      STASH IT BACK                       84164
         PRTLIST MGEND                                           84164
         SERVTERM ,          CLOSE AND FREE EVERYTHING           84164
         PGMEXIT RC=(R8)     RETURN WITH CODE                   GP03027
         SPACE 2                                                 84164
$PUNCH   CLI       CHANGESW,0
         BNE       $PUNCH0
         PRTLIST CHANGCRD,DEV=2                                  84164
         INC   NUMPUN                                            84164
         MVI       CHANGESW,4
$PUNCH0  TM    PROFLAGS,FGDEL  DELETION ?                        84164
         BZ    $PUNCH1                                           84164
         PRTLIST DELETCRD,DEV=2                                  84164
         INC   NUMPUN                                            84164
         NI    PROFLAGS,255-FGDEL  RESET DELETION                84164
$PUNCH1  TM    PROFLAGS,FGPUN  PUNCH CARD ?                      84164
         BZR   R8            NO; RETURN TO CALLER                84164
         PRTF  BUF2CARD,80,CC=NO,DEV=2   PUNCH A CARD            84164
         INC   NUMPUN                                            84164
         BR    R8            RETURN                              84164
         SPACE 2                                                 84164
GETUT1   LA    R2,SYSUT1                                         84164
GETUT1A  INPGET DEV=1        GET A CARD FROM SYSUT1              84164
         BXH   R15,R15,ERR123  I/O OR PROCESSING ERROR           84164
         INC   NUMGET1       COUNT CARDS READ                    84164
         CLC   BUF1SEQ,BUF1SEQ-BUF1CARD(R1)  MONOTONIC INCREASE ?
         BL    GETUT1X       YES                                 84164
         LR    R3,R1         COPY CARD FOR MSG                   84164
         PRTLIST MGSEQERR    PRINT SEQUENCE ERROR                84164
         OI    RETCH,1       SET MAJOR ERROR                     84164
         B     GETUT1A       GET ANOTHER                         84164
GETUT1X  MVC   BUF1CARD,0(R1)  COPY IT                           84164
         BR    R9            RETURN TO CALLER                    84164
         SPACE 2                                                 84164
GETUT2   LA    R2,SYSUT2                                         84164
GETUT2A  INPGET DEV=2        GET A CARD FROM SYSUT2              84164
         BXH   R15,R15,ERR123  I/O OR PROCESSING ERROR           84164
         INC   NUMGET2       COUNT CARDS READ                    84164
         CLC   BUF2SEQ,BUF2SEQ-BUF2CARD(R1)  MONOTONOUS INCREASE ?
         BL    GETUT2X       YES                                 84164
         LR    R3,R1         COPY CARD ADDRESS FOR MESSAGE       84164
         PRTLIST MGSEQERR    PRINT SEQUENCE ERROR                84164
         OI    RETCH,1       SET MAJOR ERROR                     84164
         B     GETUT2A       GET ANOTHER                         84164
GETUT2X  MVC   BUF2CARD,0(R1)  COPY IT                           84164
         BR    R9            RETURN                              84164
         SPACE 1                                                 84164
ERR3     LA    R2,PUNCHIN                                        84164
ERR123   PRTLIST MGIOERR,DEV=(1,3)                               84164
         SERVTERM ,                                              84164
         ABEND 1,DUMP                                            84164
         SPACE 1                                                 84164
HEADER1  FDOPT NL,CC=C'#'    AUTOMATIC PAGE NUMBERING AND DATE   84164
         FDPRT 'DOWNDATE',RADJ,LEN=48                            84164
         FDPRT *END                                              84164
HEADSUP  FDOPT NL,CC=C'#'    AUTOMATIC PAGE NUMBERING AND DATE   84164
         FDPRT 'UPDATE DECK',RADJ,LEN=58                         84164
         FDPRT *END                                              84164
HEADER2  FDPRT 'OLD MASTER    SYSUT1',PAD,NL                     84164
         FDGOTO HEADER2A                                         84164
HEADER3  FDPRT 'NEW MASTER    SYSUT2',PAD,NL                     84164
HEADER2A FDPRT JFCBDSNM-INFMJFCB(R2),L'JFCBDSNM,DEB              84164
         FDCLI JFCBELNM-INFMJFCB(R2),C' ',BL=HEADER2C,BE=HEADER2C
         FDPRT '('                                               84164
         FDPRT JFCBELNM-INFMJFCB(R2),L'JFCBELNM,DEB              84164
         FDPRT ')'                                               84164
HEADER2C FDPRT 'ON',PAD                                          84164
         FDPRT JFCBVOLS-INFMJFCB(R2),6                           84164
         FDPRT *END                                              84164
         SPACE 1                                                 84164
MGEND    FDOPT NL,CC=C'0'                                        84164
         FDPRT 'STATISTICS :',PAD                                84164
         FDPRT NUMGET1,I,NL,RADJ,LEN=10                          84164
         FDPRT 'CARDS READ FROM SYSUT1',PADL                     84164
         FDPRT NUMGET2,I,NL,RADJ,LEN=10                          84164
         FDPRT 'CARDS READ FROM SYSUT2',PADL                     84164
         FDCLC NUMEQUAL,ZEROES,4,BE=MGENDEQU                     84164
         FDPRT NUMEQUAL,I,NL,RADJ,LEN=15                         84164
         FDPRT 'MATCHED',PADL                                    84164
MGENDEQU FDCLC NUMDEL,ZEROES,4,BE=MGENDDEL                       84164
         FDPRT NUMDEL,I,NL,RADJ,LEN=15                           84164
         FDPRT 'DELETED',PADL                                    84164
MGENDDEL FDCLC NUMADD,ZEROES,4,BE=MGENDADD                       84164
         FDPRT NUMADD,I,NL,RADJ,LEN=15                           84164
         FDPRT 'ADDED',PADL                                      84164
MGENDADD FDCLC NUMCHG,ZEROES,4,BE=MGENDCHG                       84164
         FDPRT NUMCHG,I,NL,RADJ,LEN=15                           84164
         FDPRT 'CHANGED',PADL                                    84164
MGENDCHG FDPRT NUMPUN,I,NL,RADJ,LEN=10                           84164
         FDPRT 'CARDS PUNCHED',PADL                              84164
         FDOPT NL,CC=C'0'                                        84164
         FDCLC RETCH,ZEROES,2,BE=MGENDCX                         84164
         FDPRT 'COMPLETION CODE'                                 84164
         FDPRT RETCH,I,PADL                                      84164
MGENDCX  FDPRT *END                                              84164
         SPACE 1                                                 84164
MESS1    DC    CL46'TO BE ADDED            CARD PUNCHED'         84164
MESS2    DC    CL46'TO BE DELETED          DELETE CARD PUNCHED'  84164
MESS3    DC    CL46'TO BE REPLACED'                              84164
MESS4    DC    CL46'TO REPLACE             CARD PUNCHED'         84164
MESS5    DC    CL46'SHOULD BE DELETED      DELETE CARD NOT PUNCHED'
         SPACE 1                                                 84164
DELETCRD FDPRT './',NL,LEN=9                                     84164
         FDPRT 'DELETE SEQ1='                                    84164
         FDPRT SEQ1                                              84164
         FDPRT ',SEQ2='                                          84164
         FDPRT SEQ2                                              84164
         FDOPT SBA=(*,73)                                        84164
         FDPRT SEQ1                                              84164
         FDPRT *END                                              84164
         SPACE 1                                                 84164
CHANGCRD FDPRT './',NL,LEN=9                                     84164
         FDPRT 'CHANGE'                                          84164
         FDCLI JFCBELNM-INFMJFCB+UT2JFCB,C' ',BL=CHANGCRX,BE=CHANGCRX
         FDPRT 'NAME=',PADL                                      84164
         FDPRT JFCBELNM-INFMJFCB+UT2JFCB,L'JFCBELNM,DEBL         84164
CHANGCRX FDPRT *END                                              84164
         SPACE 1                                                 84164
BDOPIN   FDPRT 'OPEN FAILED FOR DD',NL                           84164
         FDPRT (R3),8,DEB,PAD                                    84164
         FDPRT *END                                              84164
MGIOERR  FDOPT NL,CC=C'0'                                        84164
         FDPRT 'UNRECOVERABLE ERROR ON'                          84164
         FDPRT (R2),8,PAD                                        84164
         FDPRT *END                                              84164
         SPACE 1                                                 84164
MGSEQERR FDPRT (R3),L'BUF1CARD,NL   DISPLAY CARD READ            84164
         FDPRT (R2),8,DEB,RADJ,LEN=10   FILE NAME                84164
         FDPRT 'SEQUENCE ERROR - CARD IGNORED'                   84164
         FDPRT *END                                              84164
         SPACE 2                                                 84164
         LTORG
         SPACE 2                                                 84164
SYSPRINT PRTWORK SYSPRINT,SYSOUT,TITLE=5                         84164
SYSPUNCH PUNWORK SYSPUNCH,SYSUPDTE                               84164
PUNCHIN  INPWORK SYSPUNCH,FILL=C' ',EODAD=CLOSE,EDIT=WCMF1INT    84164
CONSOLE  PRTWORK *CONSOLE    OPERATOR/JCL LOG MESSAGES           84164
SYSUT1   INPWORK SYSUT1,FILL=C' ',EODAD=EOF1,EDIT=WCMF1INT       84164
SYSUT2   INPWORK SYSUT2,FILL=C' ',EODAD=EOF2,EDIT=WCMF1INT       84164
         SPACE 2                                                 84164
SAVE     DSECT ,             CONTINUATION OF PGMHEAD AREA        84164
DB       DS    D             WORK AREA                           84164
@SERVICE DS    A             SERVICE ROUTINE                     84164
@PRINTER DS    A             PRINTER ROUTINE                     84164
@INPREAD DS    A             INPUT ROUTINE                       84164
NUMEQUAL DS    A             NUMBER MATCHED                      84164
NUMADD   DS    A             NUMBER ADDED                        84164
NUMDEL   DS    A             NUMBER DELETED                      84164
NUMCHG   DS    A             NUMBER CHANGED                      84164
NUMGET1  DS    A             CARDS READ IN UT1                   84164
NUMGET2  DS    A             CARDS READ IN UT2                   84164
NUMPUN   DS    A             NUMBER OF CARDS PUNCHED             84164
ZEROES   DC    F'0'          CONSTANT                            84164
RETCH    DC    0H'0',X'00'   RETURN CODE                         84164
CHANGESW DC    X'00'                                             84164
PROFLAGS DC    X'00'         PROCESSING FLAGS                    84164
FGEND    EQU   X'80'           END-FILE PROCESSED                84164
FGDEL    EQU   X'40'           DELETE CARD                       84164
FGPUN    EQU   X'20'           PUNCH CARD                        84164
SEQ1     DS    CL8           DELETE: SEQ1                        84164
SEQ2     DS    CL8           DELETE: SEQ2                        84164
         SPACE 1                                                 84164
         PRINT NOGEN                                             84164
UT1JFCB  DS    0F                                                84164
         IEFJFCBN ,          EXPAND THE JFCB                     84164
UT2JFCB  DS    (JFCBLGTH)X   AND ANOTHER                         84164
         SPACE 1                                                 84164
BUFFER1  DS    0CL129' '                                         84164
BUF1CC   DS    C                                                 84164
BUF1CARD DS    0CL80,CL72                                        84164
BUF1SEQ  DS    CL8                                               84164
         DS    CL2           SPACER                              84164
BUF1MSG  DS    CL46                                              84164
BUFFER2  DS    0CL129' '                                         84164
BUF2CC   DS    C                                                 84164
BUF2CARD DS    0CL80,CL72                                        84164
BUF2SEQ  DS    CL8                                               84164
         DS    CL2           SPACER                              84164
BUF2MSG  DS    CL46                                              84164
SAVEEND  EQU   *             END OF BUFFER                      GP03027
         SPACE 1                                                 84164
         PRINT &PRTSYS                                           84164
FLAGPATN SERVCOMP ,          DEFINE EDIT FLAGS FOR @INPREAD      84164
CVTDSECT DSECT ,                                                 84164
         CVT   DSECT=YES                                         84164
         IEFUCBOB ,                                              84164
         IHACDE ,                                                94226
         END   ,
