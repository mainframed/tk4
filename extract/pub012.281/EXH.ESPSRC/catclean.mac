CLYN     TITLE 'C A T C L E A N  ***  UNCATALOG SCRATCHED DATASETS'
         MACRO ,                                                 81117
&NM      CATCVOL &TTR,&ADDR,&ERR=ABENDCAT                        83016
         GBLC  &MACPLAB                                          81137
&MACPLAB SETC  '&NM'                                             81137
         MACPARM (R0),&TTR,OP=L                                  81137
         MACPARM (R1),&ADDR                                      81137
&MACPLAB L     R15,@CVLREAD                                      83016
         BALR  R14,R15                                           81137
         AIF   (T'&ERR EQ 'O' OR '&ERR' EQ '0').MEND            GP12338
         BXH   R15,R15,&ERR  BRANCH ON BAD READ                  83016
.MEND    MEND  ,                                                 83016
         SPACE 1                                                 81137
         MACRO ,                                                 81137
&NM      MODETEST &B,&CC     TEST FOR TEST MODE                  81137
         GBLC  &MACPLAB                                          81137
&MACPLAB SETC  '&NM'                                             81137
         MACPARM (R15),&CC   SET FAKE RETURN CODE                81137
&MACPLAB TM    MODEFG,FGDEBUG                                    81137
         BNZ   &B            SKIP SVC IN TEST MODE               81117
         MEND  ,                                                 81117
         SPACE 1                                                 81117
         COPY  OPTIONGB      DEFINE GLOBAL SYMBOLS               81155
         SPACE 1                                                 81155
         SYSPARM LIST=YES    MERGE AND DISPLAY ASSEMBLY OPTIONS  81155
         EJECT ,                                                 79258
***********************************************************************
*   CATCLEAN      WRITTEN BY UNGER JACOB (TECHNION).
*     HEAVILY MODIFIED BY G. POSTPISCHIL
*     ADDED SORT SUPPORT AND USER RESTRICTIONS ON PARTICULAR
*     ENTRIES DEPENDING ON CVOL.
*     SORT DATA ARE WRITTEN TO EXTERNAL TEMPORARY DATA SETS BECAUSE
*     @OBTAINS NEEDS LOTS OF STORAGE (NOT AVAILABLE IN E35 EXIT).
*
*    CATALOG SPECIFICATION MECHANISM HAS BEEN CHANGED. RATHER THAN
*    LOOPING BY DD NAME (SYSCAT, SYSCAT0-9), THE PROGRAM LOOPS THROUGH
*    THE TIOT, AND USES ANY UNIQUE DD STARTING WITH SYSCAT OR CAT.
*    SYSCAT DD MUST HAVE A VALID DATA SET NAME (EITHER SYSCTLG FOR
*      OS/360 OR SYSCTLG.Vvolser FOR MVS AND LATER SYSTEMS)
* E.G., //SYSCAT88 DD DISP=SHR,DSN=SYSCTLG,UNIT=SYSALLDA,VOL=SER=disk
*    CATLG DD CARDS MUST SPECIFY DISP=SHR AND THE NAME OF A DATA SET
*      CATALOGED IN THE CATALOG TO BE PROCESSED (BUILDS CAX AND PCCB).
* E.G., //CATLGRES DD DISP=SHR,DSN=HERC01.CNTL
***********************************************************************
* THIS PROGRAM UNCATALOGS ALL DATA SETS THAT ARE NOT FOUND ON THE
* VOLUME THAT THEY ARE SUPPOSED TO RESIDE ON ACCORDING TO THE
* INFORMATION FOUND IN THE CATALOG
* ALL CATALOG REFERENCES ARE TO THE CVOL WHICH IS POINTED TO BY THE
*  SYSCTLG DDNAME.
*  THE PROGRAM ALSO UNCATALOGS ALL DATASETS THAT ARE CATALOGED AS
*    RESIDING ON A VOLSER THAT CONTAINS AN INVALID CHARACTER .
* IF A PARAMETER WHICH IS A MULTIPLE OF 6 IS PASSED TO THE PROGRAM
* THEN ANY DSN THAT IS CATALOGED TO RESIDE ON ONE OF THESE VOLUMES
* IS UNCATALOGED WHETHER IT EXISTS ON THE VOLUME OR NOT
* CAUTION:A PARAMETER LENGTH WHICH IS NOT A MULTIPLE OF 6 MAY
* CAUSE UNPREDICTABLE RESULTS
* IF AN OBTAIN ERROR CODE OTHER THAN 8 IS RETURNED,AN ERROR MESSAGE
* IS PRINTED AND UNCATALOGING IS NOT ATTEMPTED
* IF AN CATALOG ERROR CODE IS RETURNED,AN ERROR MESAGE IS PRINTED
* DDNAMES: SYSCTLG , CONNECTED TO THE CORRECTED CVOL
*        OR SYSCTLGN ,  WHERE N MAY BE 0 TO 9 - UP TO 10 UNIQUE DD'S
* SYSPRINT:  THE PRINTING FILE
* SYSUT1: WORK FILE , 200 TRKS SHOULD BE ENOUGH FOR SEVERAL LARGE
*        CATALOGS (6-7 CYLINDERS EACH).
* NOTE THAT THE PROGRAM UNCATALOGS THE DATASETS AFTER IT PASSES
*  THROUGH THE WHOLE CATALOG, SO ALL MESSAGES RELATING TO THE CATALOG
*  ACTIONS APPEAR AFTER THE OBTAIN MESSAGES .
*        SORT ADDED TO MINIMIZE MOUNTING OF VOLUMES.
*        CODE ADDED TO DELETE EMPTY INDEX ENTRIES (IN CVOLS)
*        CODE ADDED TO CHECK EXISTENCE OF INDEX LEVELS SPECIFIED
*        BY CONNECT POINTERS (CVOLS)
*        CODE ADDED TO RECATALOG EXISTING DATASETS SPECIFIED WITH A
*        BAD DEVICE TYPE.
*        LOCAL RESTRICTIONS -
*              WYLBUR CVOL - PERMIT ONLY ENTRIES ADHERING TO NAME
*                  AND ACCOUNT RESTRICTIONS; SINGLE VOLUME DISK
*                  ENTRIES.  EVERYTHING ELSE DELETED.
*              LIBPAK - NAME/ACCOUNT RESTRICTION ENFORCED.  TAPE AND
*                  OTHER FUNNY ENTRIES TEMPORARILY(?) PERMITTED.
*              SYSRES - ONLY CVOL ENTRIES AND SYS1. PERMITTED
*
*        PROGRAM MUST HAVE APF AUTHORIZATION AND RESIDE IN AN    81152
*        APF AUTHORIZED LIBRARY (@OBTAINS, VSAM CATALOG READ).  GP12338
*                                                                83016
*        PRELIMINARY SUPPORT FOR VSAM CATALOGS ADDED             83016
*        CHANGED VOL-LIST FROM TMC TO ACCOUNT TABLE (USERCVT)    89296
***********************************************************************
         EJECT ,
         LCLA  &BLKLEN       LENGTH OF OS CVOL ENTRY            GP12338
&BLKLEN  SETA  256                                              GP12338
         SPACE 1
         PRINT &PRTSOR                                           83016
CATCLEAN PGMHEAD ZERO15,BASE=(R10,R11,R12),BNDRY=PAGE,PARM=R9   GP12338
         LH    R14,=AL2(MISSVOL-SAVE)                           GP12338
         AR    R14,R13       FIRST DELETION VOLSER              GP12338
         LA    R15,MISSVOL2-MISSVOL                             GP12338
         LR    R0,R14                                           GP12338
         SR    R0,R15        BUILD EMPTY BXLE STACK             GP12338
         STM   R14,R0,MISSVOLP                                  GP12338
         SERVINIT ,          LOCATE AND INITIALIZE @SERVICE     GP12338
         SERVLOAD @PARSER,SUBVERB   PARSE ROUTINE +             GP12338
         SERVLOAD @PRINTER                                      GP12338
         SERVLOAD @CVLREAD   CVOL READER                        GP12338
*DEBUG*  MVI   MODEFG,FGDEBUG   MAKE SURE                        81117
         PRTOPEN SYSPRINT,OPT=ABE  ABEND ON DUMMY OR MISSING     81137
         SPACE 1
         PARMLOAD R8,PTR=R9  R8 NOW POINTS TO THE PARAMETER     GP12338
         LTR   R9,R9         VALID LENGTH ?                     GP12338
         BNP   DONEPARM        NO; IGNORE                       GP12338
         OI    PAR$RQFG,PAR$COUP  UPPER-CASE ALL INPUT          GP12338
         OI    PAR$RQFG,PAR$COSP  SEPARATE (,) ITEMS            GP12338
         OI    PAR$RQFG,PAR$PARK  () KEYWORD                    GP12338
         PARSE (R8),(R9),OPT=LIST                               GP12338
         BXLE  R15,R15,PARSEOK                                  GP12338
         PRTDATA 'Parse failed'                                 GP12338
         ABEND 100,DUMP                                         GP12338
         SPACE 1
PARSEOK  LA    R3,PAR@TABL   POINT TO RESULT LIST               GP12338
         USING PRSDSECT,R3   DECLARE ONE RETURN ENTRY           GP12338
LOOPPARM ICM   R3,15,PRSLINK GET NEXT ENTRY                     GP12338
         BZ    DONEPARM             NO; GET OUT                 GP12338
         TM    PRSTYPE,PRSPFKEY   USER SPECIFIED KEYWORD?       GP12338
         BNZ   KEYWPARM             YES; CONTINUE               GP12338
         PARFGSET FLAGSETP,ERR=FLAGBAD,DONE=LOOPPARM            GP12338
         SPACE 1
*---------------------------------------------------------------------*
*   THE CURRENT VERSION OF THE PARSER WILL EITHER TREAT VOL(...) AS   *
*   A KEYWORD WITH ... AS THE DATA, OR (...) AS A PLAIN LIST.         *
*   WHAT I REALLY WANT IS VOL(v1,v2,v3...) WITH THE VOLUMES SPLIT OUT *
*   SINCE I CAN'T GET THAT, ONLY VOL=v1 AND VOL(v1,v2...) FORMS ARE   *
*   ACCEPTED.                                                         *
*---------------------------------------------------------------------*
KEYWPARM LA    R0,PRSKEYWD   POINT TO INPUT                     GP12338
         LA    R1,KEYWSETP   POINT TO TABLE                     GP12338
         L     R2,PRS#TEXT   TEXT LENGTH                        GP12338
         SUBCALL SUBVERB     PERFORM LOOKUP                     GP12338
         BZ    KEYWBAD         NO MATCH; FAIL IT                GP12338
         BR    R1            INVOKE SUBROUTINE                  GP12338
         SPACE 1
*---------------------------------------------------------------------*
*   SAVE SERIALS FROM VOL=id, VOL(id), AND VOL(id,id,id...)           *
*---------------------------------------------------------------------*
SETVOL   ICM   R2,15,PRS#TEXT   LOAD LENGTH                     GP12338
         BNP   SKIPCOMM        IGNORE NULL; MAY HAVE BEEN SUBST GP12338
         CH    R2,=AL2(L'MISSVOL) VALID LENGTH ?                GP12338
         BH    KEYWLEN              NO; FAIL                    GP12338
         SH    R2,=H'1'        ADJUST FOR EX                    GP12338
         BM    SKIPCOMM      ,,                                 GP12338
         LR    R9,R1         SAVE R1                            GP12338
         LM    R15,R1,MISSVOLP    GET THE MISSING VOLUME ARRAY  GP12338
         AR    R1,R0         INCREMENT THE END                  GP12338
         ST    R1,MISSVOLP+8 UPDATE THE END                     GP12338
         MVC   0(6,R1),=CL6' '    CLEAR TRAILING SPACES         GP12338
         EX    R2,EXMVCMIS   MOVE SERIAL                        GP12338
         LR    R1,R9         RESTORE R1                         GP12338
SKIPCOMM CLI   PRSSTOP,PRSSTEQU   SINGLE VOL= ?                 GP12338
         BE    LOOPPARM        YES; LOOK FOR SOMETHING ELSE     GP12338
         TM    PRSSTOP,PRSSTEPR   ENDING PARENTHESIS ?          GP12338
         BNZ   LOOPPARM        YES; LOOK FOR SOMETHING ELSE     GP12338
         ICM   R3,15,PRSLINK   GET NEXT VOLUME IN LIST          GP12338
         BNZ   SETVOL
         PRTDATA 'Volume PARM malformed',(PRS$TEXT,DEB,PADL)    GP12338
         B     EXIT16                                           GP12338
         SPACE 1
EXMVCMIS MVC   0(0,R1),PRS$TEXT  MOVE SERIAL TO BE DELETED      GP12338
         SPACE 1
         PARKEYBX FLAGSET    BXLE                               GP12338
FLAGSET  PARKEYFG 'QUIET',MODEFG,0,FGQUIET       SUPPRESS WTOR  GP12338
FLAGSET2 PARKEYFG 'DELETE',MODEFG,FGDEBUG,0      RUN FOR REAL   GP12338
         PARKEYFG 'ABEND',MODEFG,0,FGDUMP                       GP12338
         PARKEYFG 'DUMP',MODEFG,0,FGDUMP                        GP12338
         PARKEYFG 'TEST',MODEFG,0,FGDUMP                        GP12338
FLAGSETN PARKEYFG 'DEBUG',MODEFG,0,FGDEBUG                      GP12338
         SPACE 1
         PARKEYBX KEYWSET    BXLE
KEYWSET  PARKEYAD 'DELETEV',SETVOL     DELETE VOLUME SERIAL     GP12338
KEYWSET2 PARKEYAD 'VOLSER',SETVOL                               GP12338
KEYWSETN PARKEYAD 'VOLUME',SETVOL                               GP12338
         SPACE 1
FLAGBAD  PRTDATA 'Unknown PARM option',(PRS$TEXT,DEB,PADL)      GP12338
         B     EXIT16                                           GP12338
KEYWBAD  PRTDATA 'Unknown PARM keyword',(PRSKEYWD,DEB,PADL)     GP12338
         B     EXIT16                                           GP12338
KEYWLEN  PRTDATA 'Volume serial has invalid length',(0(R4),8,DEB,PADL)
         B     EXIT16                                           GP12338
         DROP  R3                                               GP12338
DONEPARM PARSE *END          RELEASE PARSE BUFFER               GP12338
         SPACE 1
PARMDONE L     R1,=A(STACK-SAVE)      GET STACK DISPLACEMENT    GP12338
         ALR   R1,R13        GET STACK ADDRESS                  GP12338
         ST    R1,@STACK     SAVE ADDRESS                       GP12338
         AIF   ('&LOCAL' NE 'PID').NOVACT                        93004
         L     R2,CVTPTR                                         89296
         ICM   R2,15,CVTUSER-CVTMAP(R2)                          89296
         BZ    NOVOLIST      SKIP IF NO EXTENSION                89296
         ICM   R2,15,UCLVOLS-USERCVT(R2)  GET VOLUME LIST        89296
         BZ    NOVOLIST      SKIP IF NO EXTENSION                89296
         MVC   TMCBXLE(12),0(R2)  MOVE VOLUME LIST               89296
NOVOLIST SERVCALL TIODD,=CL8'TMC'  IS TMC DD CARD PRESENT ?      82032
         LTR   R1,R0         FOUND ?                             82032
         BZ    *+8           NO                                  82032
         NI    TMCFLAG,255-TMCNODD  RESET TMC DD MISSING         82032
         SERVCALL  ACGET,DB  GET USER'S AUTHORIZATION            81151
         STC   R0,DB                                             81117
         TM    DB,VAASTC+VAASYS+VAASUP  PRIVILEGED ?             81117
         BNZ   NOPARM2       YES                                 81117
         OI    MODEFG,FGDEBUG  ELSE FORCE TEST MODE              81117
NOPARM2  DS    0H                                               GP12338
.NOVACT  SPACE 1                                                 81151
         TM    MODEFG,FGDEBUG  RUNNING IN TEST MODE ?            81117
         BNZ   NOPARM4       YES                                 81117
         MVC   HEADTEST,HEADTEST-1  DELETE 'DEBUG RUN' INFO      81117
         MVC   SUBHTEST,SUBHTEST-1                               81117
NOPARM4  PRTV  HEADER,TITLE=1  PRINT PAGE TITLE                  81137
         PRTV  SUBHEAD,TITLE=3,CC=NO                             81278
         LTCB  R2            GET CURRENT TCB                     89296
         L     R2,12(,R2)    TIOT
         CLC   =C'QUIET',8(R2)  QUIET MODE ?
         BE    SETQUIET                                          81117
         CLC   =C'QUIET',16(R2)  ASMCLG STEP NAME ?              81117
         BNE   *+8           NO
SETQUIET OI    MODEFG,FGQUIET  SUPPRESS MOUNT REQUESTS           81117
         SPACE 1
         SR    R2,R2         FIRST TIME FLAG                    GP12338
         LM    R3,R5,MISSVOLP                                   GP12338
         SR    R3,R4              FINAGLE                       GP12338
MSGMISSL BXH   R3,R4,MSGMISSN     DONE WITH VOLUME LISTING      GP12338
         LTR   R2,R2         FIRST TIME ?                       GP12338
         BNZ   MSGMISSV        NO                               GP12338
         PRTLIST FGMISSID    SHOW HEADER                        GP12338
         LR    R2,R13          SET NON ZERO                     GP12338
MSGMISSV PRTITEM FGMISSER    SHOW SERIAL                        GP12338
         B     MSGMISSL        DO NEXT                          GP12338
         SPACE 1
FGMISSID FD    'The following volumes will be permanently removed:',NL
         FD    *END                                             GP12338
FGMISSER FD    0(R3),6,PAD                                      GP12338
         FD    *END                                             GP12338
         SPACE 1
MSGMISSN OPEN  (TEMPOUT,OUTPUT)  TEMPORARY OUT/IN FILES
         TM    TEMPOUT+DCBOFLGS-IHADCB,DCBOFOPN  OPEN ?          81151
         BZ    ABEND69
         SPACE 1                                                 81151
         AIF   (NOT &MVS).NOVRSCT                                83016
         L     R2,CVTPTR     GET THE CVT                         83016
         USING CVTMAP,R2     DECLARE IT                          83016
         L     R2,CVTCBSP    GET THE AMCBS                       83016
         USING AMCBS,R2      TRY MVS/SP STYLE ENTRY              83016
         LA    R1,CBSCAXCN-(CAXCHN-CAXID)  POINT TO CHAIN HEAD   90302
         USING CAXID,R1      DECLARE CAX ENTRY                   83016
         DROP  R2                                                83016
LOOPCAX  ICM   R1,15,CAXCHN  GET NEXT ENTRY                      90302
         BZ    ABENDCAT      QUIT ON ERROR                       83016
         TM    CAXFLGS,CAXMCT  MASTER CATALOG ENTRY ?            83016
         BZ    LOOPCAX       NO; TRY AGAIN                       83016
         L     R2,CAXUCB     GET MASTER CATALOG UCB              83016
         USING UCBOB,R2      DECLARE IT                          83016
VSCAM    MVC   MASTVOL,UCBVOLI  SAVE THE MASTER CATALOG VOLSER   83016
         DROP  R1,R2                                             83016
.NOVRSCT AIF   ('&LOCAL' NE 'CCSI').NOVINIT                      83016
         SERVCALL INITG      FORCE WYLBUR PROTECT LOAD/GETMAINS  81151
.NOVINIT SPACE 2                                                 81151
*--------------------------------------------------------------------*
*   LOOPT THROUGH TIOT LOOKING FOR SYSCATxx AND CATLGxxx DD CARDS    *
*--------------------------------------------------------------------*
LOOPTIOT CATCVOL =A(0-1),0,ERR=   CLOSE ANY OPEN OS CATALOG      83016
LOOPTENT L     R1,@CURTIOT   GET CURRENT TIOT ENTRY             GP12338
         SERVCALL TIOLK      RETURN NEXT TIOT ENTRY             GP12338
         LTR   R1,R0         ANY ENTRY ?                        GP12338
         BZ    ACTIEND         NO; DONE                         GP12338
         ST    R1,@CURTIOT   SAVE IT                            GP12338
         LR    R4,R1         SAVE                               GP12338
         USING TIOENTRY,R4                                      GP12338
         SERVCALL TIODD,TIOEDDNM                                GP12338
         CR    R0,R4         CHECK FOR DUPLICATE DD NAME        GP12338
         BNE   MSGBDDUP                                         GP12338
         MVC   SYSCTLG+DCBDDNAM-IHADCB(8),TIOEDDNM  MOVE NAME TO DCB
         MVC   SYSCATDD,TIOEDDNM  DITTO FOR CVOL PROCESSING     GP12338
         ICM   R3,7,TIOEFSRT      GET UCB                       GP12338
         CLC   =C'SYSCAT',TIOEDDNM     OS CVOL DD ?             GP12338
         BE    HAVECVOL                  YES; SIMPLER CODE      GP12338
         CLC   =C'SYSCTL',TIOEDDNM     OS CVOL DD ?             GP12338
         BE    HAVECVOL                  YES; SIMPLER CODE      GP12338
         CLC   =C'CAT',TIOEDDNM        OS OR VSAM CATALOG ?     GP12338
         BNE   LOOPTENT                  NO; GET NEXT ENTRY     GP12338
*--------------------------------------------------------------------*
*   VSAM OR OS CATALOG - LOOK UP IN AMCBS TO GET TYPE                *
*--------------------------------------------------------------------*
         LTR   R3,R3         VALID UCB ?                        GP12338
         BZ    MSGBDUCB        NO; IGNORE AFTER MESSAGE         GP12338
         USING UCBOB,R3      DECLARE THE UCB                    GP12338
         CLI   UCBTBYT3,UCB3DACC  DIRECT ACCESS ?               GP12338
         BE    HAVEVSAM             YES; PROCEED                GP12338
         SPACE 1
MSGBDDUP PRTDATA 'DD',(TIOEDDNM,PAD),'already defined - extra ignored'
         OICC  4             SET ERROR CODE                     GP12338
         B     LOOPTENT        AND CONTINUE                     GP12338
         SPACE 1
MSGBDUCB PRTDATA 'DD',(TIOEDDNM,PAD),'is not a DASD unit - ignored'
         OICC  8             SET ERROR CODE                     GP12338
         B     LOOPTENT        AND CONTINUE                     GP12338
MSGBDDSN PRTDATA 'DD',(TIOEDDNM,PAD),'needs a SYSCTLG DSN - ignored'
         OICC  8             SET ERROR CODE                     GP12338
         B     LOOPTENT        AND CONTINUE                     GP12338
         SPACE 1
*--------------------------------------------------------------------*
*   PROCESS VSAM CATALOG:                                            *
*     FOR THE MASTER CATALOG, ALLOCATE WITH CAX DSN AND OPEN         *
*     FOR OTHER CATALOG, NEED CAX, NEED PCCB, FLAG PCCB AS STEPCAT,  *
*     OPEN, AND RESET FLAG (UNLESS SET ALREADY)                      *
*--------------------------------------------------------------------*
HAVEVSAM NI    MODEFG,X'F0'  RESET MASTER, PCCB, ETC. FLAGS     GP12338
         AIF   (NOT &MVS).NOVSCAT                                83016
         MVC   CVOL,UCBVOLI  COPY SERIAL                        GP12338
         L     R4,CVTPTR     GET THE CVT                         90302
         USING CVTMAP,R4     DECLARE IT                          90302
         L     R4,CVTCBSP    GET THE AMCBS                       90302
         USING AMCBS,R4      TRY MVS/SP STYLE ENTRY              90302
         LA    R4,CBSCAXCN-(CAXCHN-CAXID)  POINT TO CHAIN HEAD   90302
         USING CAXID,R4      DECLARE CAX ENTRY                   90302
LOOPCUX  ICM   R4,15,CAXCHN  GET NEXT ENTRY                      90302
         BZ    MSGNOCAX      QUIT ON ERROR                       90302
         C     R3,CAXUCB     MATCHING ENTRY ?                   GP12338
         BNE   LOOPCUX       NO; TRY AGAIN                       90302
         MVC   MCDSN,CAXCNAM MAKE CATALOG NAME                   90302
         MVC   MCVOL,UCBVOLI                                     90302
         MVC   MCUNIT,UCBNAME  UNIT NAME                         83016
         TM    CAXFLGS,CAXMCT  MASTER CATALOG ENTRY ?           GP12338
         BZ    USERVSAM      NO; NEED PCCB                      GP12338
MASTVSAM LA    R1,MCRBP      GET SVC 99 REQUEST BLOCK            83016
         SVC   99            ALLOCATE VSAM CATALOG               83016
         OI    MODEFG,FGMAST      REMEMBER ITS A MASTER CAT     GP12338
         BXLE  R15,R15,VSALOK                                   GP12338
         PRTDATA 'ALLOCATION FAILED FOR VSAM CATALOG',(CAXCNAM,PAD,DEB)
          B     CATABEND                                         \
MSGBDOPN PRTDATA 'OPEN FAILED FOR VSAM CATALOG',(CAXCNAM,PAD,DEB)
         B     CATABEND                                         GP12338
MSGNOCAX PRTDATA 'CATALOG ON',(CVOL,PAD,DEB),'NOT OPEN - SEE DOCUMENTAT*
               ION'                                             GP12338
         B     CATABEND                                         GP12338
MSGNOPCC PRTDATA 'CATALOG ON',(CVOL,PAD,DEB),'HAS NO PCCB - SEE DOCUMEN*
               TATION'                                          GP12338
         B     CATABEND                                         GP12338
         SPACE 1
USERVSAM LTCB  R2,USE=YES    LOAD THE TCB                       GP12338
         ICM   R2,15,TCBJSCB GET THE JSCB                       GP12338
         BZ    MSGNOPCC      UNABLE TO LOCATE PCCB              GP12338
         USING IEZJSCB,R2    DECLARE IT                         GP12338
         ICM   R2,15,JSCBPCC GET PRIVATE CATALOG CHAIN          GP12338
         BZ    MSGNOPCC      NONE                               GP12338
         USING IEFPCCB,R2    DECLARE IT                         GP12338
LOOPPCCB CLC   PCCDSNAM,MCDSN  CORRECT CATALOG ENTRY ?          GP12338
         BE    FUNDPCCB      YES (MAYBE)                        GP12338
         ICM   R2,15,PCCNEXTP  GET NEXT                         GP12338
         BNZ   LOOPPCCB                                         GP12338
         B     MSGNOPCC                                         GP12338
         SPACE 1                                                GP12338
         USING IEFPCCB,R2    DECLARE IT                         GP12338
FUNDPCCB MVC   MCDDN,PCCDDNAM  USE EXISTING DD                  GP12338
         TM    PCCSTAT1,PCCSTEPC  STEP-CAT ?                    GP12338
         BNZ   VSALOK        YES; OPEN                          GP12338
         TESTAUTH FCTN=1                                        GP12338
         BXH   R15,R15,MSGNOPCC  TOO BAD                        GP12338
         MODESET KEY=ZERO                                       GP12338
         OI    PCCSTAT1,PCCSTEPC  FAKE A STEPCAT                GP12338
         MODESET KEY=NZERO                                      GP12338
         OI    MODEFG,FGSTEP SHOW STEPCAT CLEAN-UP NEEDED       GP12338
         ST    R2,MYPCCB     SAVE ADDRESS                       GP12338
         DROP  R2                                               GP12338
         DROP  R3,R4                                            GP12338
VSALOK   MVC   VSAMCAT+DCBDDNAM-IHADCB(8),MCDDN  GET RETURNED DDNAME
*  THIS TRAVESTY AVOIDS A WTOR UNDER TURNKEY MVS TO READ MASTER CATALOG
         MODESET KEY=ZERO                                       GP12338
         L     R2,CVTPTR     GET CVT                            GP12338
         L     R2,CVTTCBP-CVTMAP(,R2)  GET TCB POINTER          GP12338
         L     R2,4(,R2)     GET CURRENT TCB                    GP12338
         L     R2,TCBJSCB-TCB(,R2)  GET JSCB                    GP12338
         OI    JSCBSWT1-IEZJSCB(R2),JSCBPASS  CHEAT             GP12338
         OPEN  (VSAMCAT,INPUT)  OPEN FOR INPUT                   83016
         NI    JSCBSWT1-IEZJSCB(R2),255-JSCBPASS   RESET        GP12338
         TM    MODEFG,FGSTEP STEP FIX-UP ?                      GP12338
         BZ    VSUSEKEY      NO                                 GP12338
         L     R2,MYPCCB     GET PCCB BACK                      GP12338
         NI    PCCSTAT1-IEFPCCB(R2),255-PCCSTEPC  RESET         GP12338
VSUSEKEY MODESET KEY=NZERO                                      GP12338
         TM    VSAMCAT+DCBOFLGS-IHADCB,DCBOFOPN  OPEN ?          83016
         BZ    MSGBDOPN      NO; TOO BAD                         83016
 PRTDATA 'Processing',(CVOL,PAD),'catalog',(MCDSN,PADL,DEB)     GP12338
         SPACE 1                                                 83016
         MVI   PRIVY,VSCAT   SET FOR VSAM CATALOG                83016
         CLC   CVOL,MASTVOL  SAME AS MASTER CATALOG ?            83016
         BNE   *+8           NO                                  83016
         OI    PRIVY,PRISYS  SET SYSTEM FLAG                     83016
         L     R2,@STACK     PREVENT 0C4 IN COMMON CODE          87036
         SPACE 1                                                 83016
READVSAM GET   RPL=VSAMRPL   READ A VSAM BLOCK                   83016
         CHECK RPL=VSAMRPL   CHECK READ                          83016
         BXH   R15,R15,ABENDCAT   TOO BAD                        83016
         CLI   VCTYPE,C'A'   NONVSAM RECORD ?                    83016
         BE    READVSB       YES                                 83016
         B     READVSAM      NO; GET NEXT                        83016
         SPACE 1
VSAMEOF  CLOSE (VSAMCAT)                                         83016
         B     LOOPTIOT      LOOK FOR MORE WORK                 GP12338
         SPACE 1                                                 83016
READVSB  MVI   FLAG,FGTEST   SET FOR DSN OBTAIN TEST             83016
         MVC   DSN,VCDSN     COPY DSN                            83016
         XC    DSCBTTR,DSCBTTR  CLEAR ADDRESS                    83016
         XR    R4,R4                                             83016
         IC    R4,VCNFD      GET NUMBER OF FIELD POINTERS        83016
         LTR   R4,R4         ANY ?                               83016
         BNP   VSAMKILL      NO; KILL THIS RECORD                83016
         LR    R6,R4         COPY                                83016
         MH    R6,=Y(VFLEN)  GET LENGTH OF POINTERS              83016
         LA    R6,VCFLD(R6)  GET START OF FIELD ENTRIES          83016
         LA    R5,VCFLD      POINT TO FIRST POINTER ENTRY        83016
         USING VFSECT,R5                                         83016
LOOPVF   CLI   VFFLG,3       VOLUME FIELD ?                      83016
         BE    HAVEVF        YES; PROCESS                        83016
         LA    R5,VFLEN(,R5)   GET NEXT                          83016
         BCT   R4,LOOPVF                                         83016
         B     READVSAM      SKIP IF NONE                        83016
HAVEVF   MVC   DB(2),VFOFF   ALIGNMENT                           83016
         AH    R6,DB         GET FIELD ADDRESS                   83016
         USING VLSECT,R6     DECLARE IT                          83016
         DROP  R5            DONE WITH POINTERS                  83016
         MVC   VOLSER,VLVOL  COPY VOLUME SERIAL                  83016
         MVC   VOLTYPE,VLTYPE   AND VOLUME TYPE                  83016
         CLI   VLTYPE+2,UCB3TAPE  TAPE ENTRY ?                   83016
         BE    VSAMTAPE      YES                                 83016
         CLI   VLTYPE+2,UCB3DACC  DASD ENTRY ?                   83016
         BNE   VSAMKILL      NO; KILL, KILL                      83016
         MVC   DSCBTTR,VLDSCB   DSCB TTR PRESENT ?               83016
         CLC   =C'SYSCTLG.V',DSN  SPECIAL NAME ?                 83016
         BNE   PUTDSNT       NO; TEST                            83016
         CLI   DSN+15,C' '   NOT TOO LONG ?                      83016
         BE    PUTDSNP       BUT WILL CHECK FOR 'SYSCTLG '       87036
*        B     VSAMKILL      KILL MALFORMED NAME                 83016
VSAMKILL MVI   FLAG,FGUNCT   DON'T KNOW WHAT IT IS - KILL IT     83016
         MVI   MGFLAG,MGTYPE   BAD ENTRY TYPE ?                  83016
         B     PUTDSNT                                           83016
VSAMTAPE MVI   FLAG,FGTMCAT  SET TAPE ENTRY                      83016
         MVI   TAPEFG,FGTMCAT   AND SUB-CODE                     83016
         MVI   MGFLAG,MGNTMC   PROVISIONAL ERROR CODE            83016
         B     PUTDSNTP      PROCESS                             83016
.NOVSCAT SPACE 1                                                 83016
ABENDCAT PRTDATA (CVOL,PADR,DEBR),'catalog open or I/O error'   GP12338
CATABEND PRTCLOSE ,                                             GP12338
         ABEND 666,DUMP      BAD CATALOG                         81117
         SPACE 1
*--------------------------------------------------------------------*
*   OS CVOL CATALOG                                                  *
*--------------------------------------------------------------------*
         USING UCBOB,R3      PASSED                             GP12338
HAVECVOL LTR   R3,R3         VALID UCB ?                        GP12338
         BZ    MSGBDUCB        NO; IGNORE AFTER MESSAGE         GP12338
         CLI   UCBTBYT3,UCB3DACC  DIRECT ACCESS ?               GP12338
         BNE   MSGBDUCB             NO; FAIL                    GP12338
         RDJFCB SYSCTLG       READ JFCB OF SYSCTLG              GP12338
         CLC   =C'SYSCTLG',JFCBDSNM    VALID NAME ?             GP12338
         BNE   MSGBDDSN                   NO; TATTLE AND SKIP   GP12338
*NEXT*   B     READOK        PROCESS CVOL                       GP12338
         SPACE 1
READOK   MVC   CVOL,JFCBVOLS   MOVE VOLSER                       81151
 PRTDATA 'Processing',(CVOL,PAD),'catalog',(JFCBDSNM,PADL,DEB)  GP12338
         MVC   SYSCATVL,CVOL   COMPLETE CALL PARM                81117
         XR    R0,R0                                             81117
         LA    R1,SYSCATDD   POINT TO PARM LIST                  81117
         L     R15,@CVLREAD  GET READ ROUTINE                    83016
         BALR  R14,R15       REQUEST CATALOG OPEN                81117
         BXH   R15,R15,ABENDCAT  TOO BAD                        GP12338
         MVI   PRIVY,0       CLEAR CVOL RESTRICTION FLAG
         AIF   ('&LOCAL' NE 'PID').NOWYL                         93004
         CLC   =C'TSO001',CVOL   WYLBUR ?
         BNE   *+8           NO
         OI    PRIVY,PRIWYL  SET WYLBUR
         CLC   =C'LIBPA',CVOL    SPECIAL LIBRARY PACK ?
         BNE   *+8           NO
         OI    PRIVY,PRILIB  SET FLAG
.NOWYL   L     R2,CVTPTR     GET CVT                             81117
         USING CVTMAP,R2                                         81137
         L     R2,CVTSYSAD   GET SYSRES UCB                      81117
         USING UCBOB,R2                                          81137
         CLC   CVOL,UCBVOLI  SYSRES ?                            81117
         BNE   *+8           NO                                  81117
         OI    PRIVY,PRISYS  SET SYSRES FLAG
         DROP  R2,R3                                             81117
         SPACE 1                                                 81117
         L     R2,@STACK     R2 POINTS TO THE FIRST BLOCK
         CATCVOL =A(256),(R2)   READ FIRST BLOCK IN CATALOG      81117
         LA    R3,24(,R2) PASS THE COUNT & VICE
         SPACE 2
TESTENTR CLI   11(R3),X'07'  IS IT A DSPE OF A DSN OF 1 VOLUME?
         BE    DSPE              IF SO:GO CHECK THE ENTRY
         BH    DSNTEST       SEE IF RESTRICTED VOLUME
         CLC   0(8,R3),LAST  IS IT AN ILE?
         BE    ILE               IF SO:GO TAKE CARE OF THE ILE
         CLI   11(R3),X'00'  IS IT AN IPE ?
         BE    IPE               IF SO:GO TAKE CARE OF THE IPE
         CLI   11(R3),X'02'  IS IT A GIPE ?
         BE    IPE           YES; TREAT AS NORMAL INDEX
         SPACE 1
         CLI   11(R3),1      VCB POINTER ?                       81137
         BNE   TESTNVCB      NO                                  81137
         TM    PRIVY,PRIWYL+PRILIB+PRISYS  ANY RESTRICTIONS ?    81137
         BZ    TAPEVCB       NO; SEE IF TAPE ENTRY               81137
         TM    PRIVY,PRISYS  SYSTEM PACK ?                       79350
         BZ    WYLVCB        NO                                  79350
         CLC   =C'SYS1.',OUTDSN   SPECIAL NAME ?                 81117
         BE    NEXTENTR      YES; IGNORE THIS ONE                79350
         B     BADVCB        ELSE KILL IT                        79350
WYLVCB   TM    PRIVY,PRIWYL  WYLBUR ?                            79350
         BZ    TAPEVCB       NO; SEE IF TAPE ENTRY               81137
BADVCB   MVC   VOLSER,=CL6' MANY ' DON'T DO I/O TO GET VOLSER    79350
         XC    VOLTYPE,VOLTYPE  DITTO FOR TYPE
         B     PUTTYPE       DO UNCONDITIONAL UNCATALOG
TAPEVCB  LR    R4,R1         SAVE TTR                            81137
         MVC   TTR(3),8(R3)  COPY VCB TTR                        81137
         CATCVOL TTR,VCB     READ THE VCB                        81137
         ST    R4,TTR        RESET TTR                           81137
         CLI   VCB+4,X'80'   TAPE ENTRY ?                        81137
         BNE   NEXTENTR      NO; SKIP                            81137
         MVC   VOLSER,VCB+6                                      81137
         MVI   FLAG,FGTMCAT   SET TAPE ENTRY                     81137
         MVI   TAPEFG,FGTMCAT  DITTO FOR SUB-TYPE FLAG           81137
         MVI   MGFLAG,MGNTMC  SET PROVISIONAL ERROR CODE         81137
         B     DSNMOVE                                           81137
         SPACE 1
TESTNVCB C     R2,@STACK     ARE WE IN TOP LEVEL ?
         BNE   NEXTENTR      NO; SKIP
         MVC   OUT,OUT-1     CLEAR OUT
         XC    VOLSER,VOLSER  NO VOLSER
         XC    VOLTYPE,VOLTYPE  OR TYPE
         CLI   11(R3),3      LOWER THAN CVOL ?
         BL    NEXTENTR      YES - NO CODE YET FOR GDG
         BE    OLDCONN       OLD FORMAT CONNECT POINTER
         CLI   11(R3),5      HIGHER THAN CVOL ?
         BH    NEXTENTR      DON'T KNOW WHAT IT IS ?
         BL    TESTALIS      ALIAS ENTRY
         MVC   VOLTYPE,12(R3)  COPY CVOL DEVICE TYPE
OLDCONN  MVC   VOLSER,16(R3)   COPY VOLSER OF CVOL
         MVC   DSN,OUT       CLEAR DSNAME
         MVC   DSN(8),LAST   MAKE FF
         MVC   DSN+8(8),0(R3)  MAKE INDEX NAME
         TM    VOLTYPE+2,255-UCB3DACC  NON-DISK ?                81137
         BNZ   BCVLTYPE      YES; BAD ENTRY                      81137
         MVI   FLAG,FGTEST   SET TEST CODE
         MVI   MGFLAG,MGINDX  SHOW EMPTY INDEX
         TM    PRIVY,PRISYS  SYSRES CVOL ?
         BNZ   PUTDSNP       PUT OUT THE CVOL CHECK REQUEST
BCVLTYPE MVI   FLAG,FGUNCT   NOT SYSRES - RELEASE UNCONDITIONALLY
         MVI   MGFLAG,MGTYPE  SHOW BAD TYPE
         B     PUTDSNP       AND PUT IT OUT
         SPACE 1
TESTALIS CLC   =C'SYS1 ',12(R3)  ALIAS OF SYS1 FOR ANOTHER SYSTEM ?
         BE    NEXTENTR      YES; LEAVE IN UNCHECKED             80357
         CLC   =C'IPO1 ',12(R3)  DITTO FOR IPO1                  80357
         BE    NEXTENTR                                          80357
         MVC   DSN,OUT       CLEAR DSN                           80357
         MVC   DSN(8),0(R3)  DISPLAY ALIAS ENTRY
         MVC   DSN+24(8),12(R3)  SHOW INDEX NAME
         MVI   FLAG,FGDLTA   INDICATE ALIAS DELETE
         B     PUTDSNP       PUT OUT THE REQUEST
         SPACE 2
NEXTENTR XR    R4,R4               MOVE TO THE NEXT ENTRY
         IC    R4,11(,R3)      THE NUMBER OF HALF WORDS
         LA    R4,12(R4,R4)         THE LENGTH OF THE ENTRY
         LA    R3,0(R3,R4)   THE NEXT ENTRY
         B     TESTENTR          GO TEST THIS ENTRY
         SPACE 1
IPE      ST    R3,@VERENT    THE ADDRESS OF THE TESTED ENTRY
         MVC   257(3,R2),@VERENT+1 THE BLOCK IS KEPT IN THE LAST
         MVC   TTR(3),8(R3)  3 BYTES OF THE BLOCK IN THE STACK
         LA    R2,L'STACK(,R2) INCREMENT THE DEPTH OF THE STACK GP12338
         CATCVOL TTR,(R2)    READ INDEX BLOCK                    81117
         LA    R3,2(,R2)     PROVISIONALLY POINT TO INDEX CONTROL
         CLC   =XL8'1',0(R3)  IS IT INDEX CONTROL ENTRY ?
         BNE   ICECOLD       NO, ERROR IN CATLG; CHECK FIRST OTHER
         LA    R3,20(,R2)    SKIP THE COUNT & ICE
ICECOLD  CLC   INDEXEND,0(R3)   IS THIS AN EMPTY INDEX ?
         BNE   TESTENTR          NO,TEST THIS ENTRY
         MVC   OUT,OUT-1     CLEAR THE WORK AREA
         L     R4,@STACK     BUILD THE DSN TO BE TESTED IN OUT
         LA    R6,OUTDSN-1   POINT TO FAKE PERIOD                81117
ICEMOVE  MVI   0(R6),C'.'    PUT THE '.'                         81117
         LA    R6,1(,R6)
         L     R5,&BLKLEN.(,R4)   A NEW LEVEL                   GP12338
         LA    R7,8(,R5)
ICECHAR  MVC   0(1,R6),0(R5)
         LA    R5,1(,R5)
         LA    R6,1(,R6)
         CLI   0(R5),C' '         END OF LEVEL?
         BE    ICEDONE
         CR    R5,R7             END OF LEVEL?
         BNE   ICECHAR
ICEDONE  LA    R4,L'STACK(,R4)    THE NEXT LEVEL                GP12338
         CR    R4,R2          HAVE WE ALREADY MOVED THE LAST LEVEL?
         BL    ICEMOVE           GO MOVE THE NEXT LEVEL          81117
ICEPUT   MVC   DSN,OUTDSN                                        81117
         MVI   OUTDSN-1,C' '  JUST IN CASE                       81117
         TR    DSN,ICETAB    CONVERT TRAILING BLANKS TO FF FOR SORT
         XC    VOLSER,VOLSER  CLEAR THE VOL-SER
         MVI   FLAG,FGDLTX   INDICATE DELETE INDEX
         PUT   TEMPOUT,SORTREC   SAVE DATASET NAME AND VOLSER
         MVI   FLAG,FGTEST   RESTORE NORMAL FLAG
         B     TESTENTR      RESUME NORMAL PROCESSING
         SPACE 2
ILE      CLC   8(3,R3),ZERO  IS IT THE LAST BLOCK OF THE INDEX?
         BE    BACK              IF SO:GO TAKE CARE OF IT
         MVC   TTR(3),8(R3)  IT'S NOT THE LAST BLOCK IN THE INDEX
         CATCVOL TTR,(R2)    READ THE NEXT BLOCK                 81117
         LA    R3,2(,R2)  PASS THE COUNT
         B     TESTENTR          GO TEST THE NEW ENTRY
         SPACE 1
BACK     C     R2,@STACK     ARE WE IN THE VOLUME INDEX?
         BNP   LOOPTIOT        DONE; CLOSE AND DO NEXT CAT      GP12338
         SH    R2,=AL2(L'STACK)   POINT TO THE FORMER INDEX     GP12338
         L     R3,&BLKLEN.(,R2)   R3 HAS THE ADDRESS IN THAT INDEX
         B     NEXTENTR          GO GET THE NEXT ENTRY
         SPACE 2
DSPE     CLC   =C'AMASTCAT',0(R3)  SVS MASTER VSAM CATALOG ENTRY ?
         BNE   DSPENVS       NO                                  80357
         CLI   16(R3),UCB3DACC  ON DISK ?                        81117
         BNE   PUTTYPE       NO; UNCONDITIONAL UNCATALOG         80357
         B     NEXTENTR      ELSE LEAVE IT FOR NOW               80357
DSPENVS  CLI   16(R3),UCB3DACC    DISK ??                        81117
         BE    DSNMOVE       YES - PROCESS IN OBTAIN MODE
DSNTEST  TM    PRIVY,PRIWYL  WYLBUR CATALOG ?
         BNZ   PUTTYPE       YES; SET INVALID TYPE               81137
         CLI   16(R3),UCB3DACC  DISK ENTRY ?                     81137
         BE    DSNMOVE       YES; TEST FIRST VOLUME ONLY         81137
         CLI   16(R3),UCB3TAPE   TAPE ENTRY ?                    81137
         BNE   PUTTYPE       NEITHER DISK NOR TAPE - FAIL        81137
         TM    TMCFLAG,TMCNODD  RUNNING WITHOUT TMC CHECK ?      82032
         BNZ   NEXTENTR      YES; JUST IGNORE                    82032
         MVI   FLAG,FGTMCAT  SHOW TAPE CHECK ENTRY               81137
         MVI   TAPEFG,FGTMCAT  DITTO FOR SUB-TYPE FLAG           81137
         MVI   MGFLAG,MGNTMC  SET PROVISIONAL ERROR CODE         81137
         B     DSNMOVE       AND PUT IT OUT                      81137
PUTTYPE  MVI   FLAG,FGUNCT   SET FOR UNCONDITIONAL UNCATALOG
         MVI   MGFLAG,MGTYPE  SHOW BAD ENTRY TYPE
DSNMOVE  MVC   OUT,OUT-1     CLEAR THE WORK AREA
         ST    R3,@VERENT    THE ADDRESS OF THE TESTED ENTRY
         MVC   257(3,R2),@VERENT+1
         L     R4,@STACK     BUILD THE DSN IN OUT                81117
         LA    R6,OUTDSN-1                                       81117
MOVE     MVI   0(R6),C'.'    PUT THE '.'                         81117
         LA    R6,1(,R6)
         L     R5,&BLKLEN.(,R4)   A NEW LEVEL                   GP12338
         LA    R7,8(,R5)
CHAR     MVC   0(1,R6),0(R5)
         LA    R5,1(,R5)
         LA    R6,1(,R6)
         CLI   0(R5),C' '         END OF LEVEL?
         BE    DONE
         CR    R5,R7             END OF LEVEL?
         BNE   CHAR
DONE     LA    R4,L'STACK(,R4)    THE NEXT LEVEL                GP12338
         CR    R4,R2          HAVE WE ALREADY MOVED THE LAST LEVEL?
         BNH   MOVE              GO MOVE THE NEXT LEVEL          81117
PUTDSN   MVC   DSN,OUTDSN                                        81117
         MVI   OUTDSN-1,C' '  JUST IN CASE                       81117
         CLI   11(R3),7      ENTERED FOR A DSN ENTRY ?
         BL    PUTDSNT       NO; SKIP VOLSER/TYPE MOVE
         MVC   VOLSER,18(R3)
         MVC   VOLTYPE(4),14(R3)  SAVE VOLUME TYPE
PUTDSNT  CLI   FLAG,FGTEST   TEST MODE ?
         BNE   PUTDSNP       NO; PUT IT OUT
PUTDSNTP CLC   =C'MIGRAT',VOLSER  MIGRATED ?                    GP12338
         BE    WICHNEXT        YES; LET HSM MANAGE IT           GP12338
         AIF   ('&LOCAL' NE 'PID').PUTDSW                        93004
         TM    PRIVY,PRIWYL   WYLBUR MODE ?
         BZ    PUTDSNW       NO; CHECK OTHER
PUTWYLCH SERVCALL  DSWYC,DSN,ERR=SERVERR     CHECK WYLBUR NAME   85223
         B     PUTDSNP       OK; SEE IF IT EXISTS                81117
         SPACE 1                                                 81117
PUTDSNW  TM    PRIVY,PRILIB     LIBPAK ?
         BZ    PUTDSNS       NO; CHECK FOR SYSRES
         CLC   =C'LIBPA',VOLSER  LIBPAK RESIDENCE ?              79350
         BNE   PUTDSNP       CHECK IF IT EXISTS                  79350
         CLC   =C'SYS1.',DSN  OTHER SPECIAL ?
         BE    PUTDSNP
         SERVCALL  DSLIB,DSN,ERR=SERVERR  DO LIBPAK DSN CHECK    81151
         B     PUTDSNP       OK ?                                81117
         SPACE 1
PUTDSNS  TM    PRIVY,PRISYS   SYSRES CVOL ?
         BZ    PUTDSNX       NO; SEE IF RESTRICTED INDEX         81117
         CLC   =C'IPO1.',DSN  MVS CRUTTY SPECIAL ?               83016
         BE    PUTDSNP       YES; DO IN TEST MODE                83016
         CLC   =C'SYS1.',DSN   SYSTEM ENTRY ?                    79281
         BNE   PUTNAME       NO; GET RID OF IT                   79350
         AIF   (&MVS).NOEXECU                                    87036
         CLC   =C'EXECROLL ',DSN+5  REAL SPECIAL ?               79281
         BE    WICHNEXT      YES; LEAVE IN WITHOUT CHECKING
.NOEXECU B     PUTDSNP       TEST FOR PRESENCE
         SPACE 1                                                 81117
SERVERR  CH    R15,=H'16'    RETURN CODE 8 ?                     81117
         BH    ABENDCAT      NO; PROGRAM ERROR                   81117
         STC   R0,MGFLAG     SAVE RETURN FLAGS                   81117
         CLI   MGFLAG,VRPARM   BAD PARM ?                        81117
         BNH   ABENDCAT      YES; PROGRAM ERROR ?                81117
         TR    MGFLAG,VALLMAP   REMAP                            81117
         B     PUTDSND       SET FOR UNCATALOG                   81117
         SPACE ,                                                 81117
PUTDSNX  SERVCALL  DSCAT,DSN,ERR=SERVERR  SEE IF VALID NAME      81151
         SERVCALL WYLDX,DSN,CC0=PUTWYLCH  CHECK WYLBUR NAME      81151
.PUTDSW  B     PUTDSNP       CHECK IF DATASET EXISTS             81117
         SPACE 1                                                 80340
PUTNAME  MVI   MGFLAG,MGNAME  SIGNAL BAD NAME
PUTDSND  MVI   FLAG,FGUNCT   SET FOR DELETION
PUTDSNP  CLI   FLAG,FGTMCAT  DOING TAPE ENTRY ?                  81137
         BNE   PUTDSNPF      NO                                  81137
         LR    R4,R2         SAVE R2                             81137
         TRT   DSN(38),GDGTAB  LOOK FOR POSSIBLE GDG NAME        81137
         LR    R2,R4         RESTORE R2                          81137
         BZ    PUTDSNPF      NOT FOUND                           81137
         BCTR  R1,0          BACKUP ONE                          81137
         MVC   DB(8),GDGMASK  COPY GDG MASK                      81137
         NC    DB(8),0(R1)    AND IT                             81137
         CLC   DB(8),GDGREST  REALLY A GDG ?                     81137
         BNE   PUTDSNPF      NO ?                                81137
         XC    1(4,R1),LAST  FLIP THE BITS TO MAKE NAME          81137
PUTDSNPF PUT   TEMPOUT,SORTREC   SAVE DATASET NAME AND VOLSER    81137
WICHNEXT XC    SORTREC(CVOL-SORTREC),SORTREC   JUST IN CASE      81137
         MVI   FLAG,FGTEST   RESET FOR STANDARD                  81117
         AIF   (NOT &MVS).NOVSCAL                                83135
         TM    PRIVY,VSCAT   DOING VSAM CATALOG ?                83016
         BNZ   READVSAM      YES                                 83016
.NOVSCAL B     NEXTENTR                                          83135
         EJECT ,
ACTIEND  CATCVOL =A(0-1),0,ERR=   CALL CATALOG CLOSE            GP12338
         SPACE 1
ACTII    SERVCALL  FREEM,0   FREEMAIN/DELETE EXCEPT PRINTER      81151
         AIF   ('&LOCAL' NE 'PID').NVTMCRD                       93004
         TM    TMCFLAG,TMCNODD  DD CARD MISSING ?                82032
         BNZ   ACTIIFIX      YES; SKIP EVERYTHING                82032
         SERVLOAD @TMSREAD                                      GP12338
         READTMC OPEN,BUFNO=255,TEST=YES,EXT=YES  OPEN PLAIN     89296
         BH    ABENDTMC      MAJOR ERROR                         81137
         BE    ACTIIFIX      SKIP IF NO DD                       82032
         TIME  BIN                                               81137
         STCM  R1,7,CURDAT   SAVE CURRENT DATE                   81137
         OI    CURDAT+2,X'0F'  MAKE GOOD DATE                    81137
         XC    SORTREC,SORTREC   CLEAR                           81137
LOOPTMRD READTMC DSNB,TEST=YES    READ AND TEST                  81137
         BH    ABENDTMC      FAIL                                81137
         BE    ACTIINT       QUIT                                81137
         LR    R5,R1         SAVE ADDRESS                        81137
         USING TMRECORD,R5   DECLARE IT                          81137
         LA    R2,2          SET TO LOOP TWICE IF DSNB           81137
         CLI   TMDSN,X'FF'   DSNB ?                              81137
         BE    DODSNB        YES; DIFFERENT FORMAT               81137
         CLI   TMVOLSER,C'O'  VALID ?                            81137
         BL    LOOPTMRD      NO; SKIP                            81137
         TM    TMFLAG1,2     DELETED ?                           81137
         BNZ   CHAINING      YES; SKIP                           81137
         TM    TMFLAG1,4     SCRATCH ?                           81137
         BZ    PROCVOL       NO; PROCESS                         81137
         CLC   TMEXPDT,CURDAT  EXPIRATION DATE REACHED ?         81137
         BH    PROCVOL       NO; PROCESS IT                      81137
CHAINING OC    TM#DSNBS(6),TM#DSNBS                              81137
         BNZ   PROCVOL       LEAVE AS VALID ANYWAY               81137
         MVI   MGFLAG,MGTMEXP   ELSE SET EXPIRED                 81137
PROCVOL  MVC   DSN,TMDSN                                         81137
         MVC   VOLSER,TMVOLSER                                   81137
         LA    R2,1          'LOOP' ONCE FOR VOLUME RECORD       81137
PUTTMC   MVI   FLAG,FGTMCAT                                      81137
         MVI   TAPEFG,FGTMC                                      81137
         MVC   CVOL,BLANKO                                       81137
         TRT   DSN(38),GDGTAB  LOOK FOR POSSIBLE GDG NAME        81137
         BZ    PUTTMC1       NOT FOUND                           81137
         BCTR  R1,0          BACKUP ONE                          81137
         MVC   DB(8),GDGMASK  COPY GDG MASK                      81137
         NC    DB(8),0(R1)    AND IT                             81137
         CLC   DB(8),GDGREST  REALLY A GDG ?                     81137
         BNE   PUTTMC1       NO ?                                81137
         XC    1(4,R1),LAST  FLIP THE BITS TO MAKE NAME          81137
PUTTMC1  PUT   TEMPOUT,SORTREC                                   81137
         MVI   MGFLAG,0      CLEAR FLAG                          81137
BCTDSN   LA    R5,100(,R5)   SET FOR NEXT DSNB ENTRY             81137
         BCT   R2,DODSNB     GET SECOND RECORD                   81137
         B     LOOPTMRD                                          81137
         SPACE 1                                                 81137
DODSNB   CLI   DSNBVSN,C'O'                                      81137
         BL    BCTDSN                                            81137
         MVC   DSN,DSNBDSN                                       81137
         MVC   VOLSER,DSNBVSN                                    81137
         CLC   DSNBEXDT,CURDAT  EXPIRED ?                        81137
         BH    PUTTMC        UNEXPIRED ENTRY                     81137
         MVI   MGFLAG,MGTMEXP  SET EXPIRED                       81137
         B     PUTTMC        PUT IT OUT                          81137
         SPACE 1                                                 81137
ABENDTMC ABEND 251,DUMP      ABEND WITH (OUR) SVC NUMBER         81137
         SPACE 1                                                 81137
ACTIINT  READTMC CLOSE,TEST=NO   CLOSE/ NO TEST                  81137
ACTIIFIX LM    R15,R1,TMCBXLE  GET THE CREATED DISK POINTERS     82032
         SR    R1,R0         POINT TO NEXT TO LAST ENTRY         82032
         CR    R15,R1        ANY ENTRIES ?                       82032
         BL    *+12          YES                                 82032
         ST    R15,TMCBXLE+8  ELSE INVALIDATE TABLE - ERROR ?    82032
         OI    TMCFLAG,TMCNODD  SET TMC DD MISSING               82032
.NVTMCRD SPACE 1                                                 81151
ACTIII   MVI   SORTREC,FGEND    SEND AT LEAST ONE RECORD IN      81137
         MVC   SORTREC+1(SORTLEN-1),SORTREC                      81137
         PUT   TEMPOUT,SORTREC
         CLOSE TEMPOUT
         FREEPOOL TEMPOUT
         SERVCALL  FREEM,0   TEMPORARY CLOSE/FREEMAIN/DELETE     81151
         TESTAUTH FCTN=1     SEE IF WE ARE AUTHORIZED            81152
         BXH   R15,R15,NOAUTH   NO                               81152
         MODESET KEY=ZERO    ELSE GET INTO KEY ZERO              81152
         LA    R1,SORTPARM   INVOKE SORT WHILE IN KEY ZERO       81152
         L     R2,CVTPTR     GET CVT                             81152
         L     R2,CVTTCBP-CVTMAP(,R2)  GET TCB POINTER           81152
         L     R2,4(,R2)     GET CURRENT TCB                     81152
         L     R2,TCBJSCB-TCB(,R2)  GET JSCB                     81152
         AIF   (NOT &MVS).SVSAPF                                 82195
         NI    JSCBOPTS-IEZJSCB(R2),255-JSCBAUTH  RESET OUR APF  81167
         XC    DB,DB         JUST IN CASE                        81167
         LA    R3,DB                                            GP12338
         ATTACH EP=SORT,RSAPF=YES,ECB=(R3)                      GP12338
         BXH   R15,R15,ABEND69                                   81167
         ST    R1,DB+4       SAVE TCB ADDRESS                    81167
         WAIT  ECB=DB        WAIT FOR FINISH                     81167
         DETACH DB+4         GET RID OF IT                       81167
         ICM   R15,7,DB+1    TEST COMPLETION CODE                81167
         BNZ   ABEND69       TOO BAD                             81167
         AGO   .CMAPF                                            81167
.SVSAPF  LINK  EP=SORT                                           81167
         BXH   R15,R15,ABEND69   FAIL ON BAD SORT
.CMAPF   OI    JSCBOPTS-IEZJSCB(R2),JSCBAUTH  RESTORE APF        81167
         MODESET KEY=NZERO   RESTORE TCB KEY                     81152
         B     RESTAUTH      REJOIN COMMON CODE                  81152
NOAUTH   LA    R1,SORTPARM                                       81152
         LINK  EP=SORT
         BXH   R15,R15,ABEND69
RESTAUTH MVC   TEMPOUT+DCBDDNAM-IHADCB+4(3),=C'OUT'  SORTIN=>SORTOUT
         OPEN  TEMPOUT
         TM    TEMPOUT+DCBOFLGS-IHADCB,DCBOFOPN  REALLY OPEN ?   81151
         BZ    ABEND69
         SERVCALL LPALD,=CL8'@OBTAINS'  LOAD VTOC READER         81152
         ST    R0,@OBTAIN    SAVE FOR @OBTAIN MACRO              81152
         LR    R1,R0         COPY                                81152
         L     R1,28(,R1)    ADDRESS OF @OBSERVE                 81152
         MVC   0(4,R1),=AL4(16*1024)  LEAVE SOME SPACE FOR CATLG 79154
         EJECT ,
AGAIN    MVC   OUT,OUT-1     CLEAR OUT
AGAINRD  GET   TEMPOUT,SORTREC
         CLI   FLAG,FGTMCAT  TMC RECORD ?                        81137
         BL    AGAINST       NO; PROCESS                         81137
         BH    ENDJOB        ALL DONE                            81137
         TM    TMCFLAG,TMCNODD  NO DD CARD PRESENT ?             82032
         BNZ   ENDJOB        YES; JUST IGNORE TAPE CATALOG ENTRIES
         CLI   TAPEFG,FGTMC  TMC OR TMCCAT ?                     81137
         BH    TAPECATL      TAPE                                81137
         BL    AGAINRD       SKIP INVALID TYPE                   81137
         MVC   TMCSORT,SORTREC  SAVE TMC RECORD FOR COMPARE      81137
         B     AGAINRD                                           81137
TAPECATL CLC   TMCSORT+1(TAPEFG-VOLSER),SORTREC+1   DSN IN TMC ? 81137
         BNE   TAPEDELT      NO; UNCATALOG THIS ONE              81137
         CLI   MGFLAG-SORTREC+TMCSORT,0  ERROR FLAG ON ?         81137
         BE    AGAINRD       NO; GET NEXT RECORD                 81137
         MVC   MGFLAG,MGFLAG-SORTREC+TMCSORT  COPY FLAG          81137
TAPEDELT MVI   FLAG,FGUNCT   SET FOR UNCATALOG                   81137
*        B     AGAINST       JOIN COMMON PROCESSING              81137
         SPACE 1                                                 81137
AGAINST  MVC   OUTCVOL,CVOL  COPY THE CVOL                       81117
         CLI   FLAG,FGDLTX   DELETE INDEX REQUEST ?
         BE    INDDEL        YES - GO TO PROCESS
         MVC   OUTDSN,DSN                                        81117
         CLI   FLAG,FGDLTA   DELETE ALIAS ?
         BE    ALIDEL        YES
         CLI   FLAG,FGTEST   DSN TEST ?
         BE    DATATEST      YES, PROCESS
         CLI   FLAG,FGUNCT   UNCATALOG ?
         BNE   AGAIN         OTHERS NOT WRITTEN YET
DATATEST MVC   OUTVOL,VOLSER                                     81117
         TRT   OUTDSN(38),GDGTAB  LOOK FOR POSSIBLE GDG NAME     81117
         BZ    DATATYPE      NOT FOUND
         BCTR  R1,0          BACKUP ONE
         MVC   DB(8),GDGMASK  COPY GDG MASK
         NC    DB(8),0(R1)    AND IT
         CLC   DB(8),GDGREST  REALLY A GDG ?
         BNE   DATATYPE      NO ?
         XC    1(4,R1),LAST  FLIP THE BITS TO MAKE NAME
         MVC   DSN,OUTDSN    MAKE NAME FOR UNCATLG ALSO          81117
DATATYPE LM    R15,R1,=A(UNITNM,8,UNITNMND)                     GP12338
DATATLUP CLC   VOLTYPE+2(2),0(R15)  MATCHING TYPE ?             GP12338
         BE    DATATMOV               YES                       GP12338
         BXLE  R15,R0,DATATLUP                                  GP12338
DATATMOV MVC   OUTTYPE,2(R15)                                   GP12338
         CLI   FLAG,FGUNCT   UNCONDITIONAL UNCATALOG ?
         BE    UNCATL        YES - DO IT
         CLI   VOLSER,C' '      VALID FIRST DIGIT ?
         BNH   UNCAT         NO, KILL IT
         TRT   VOLSER,TRTSERNO                                  GP12338
         BNZ   UNCAT             INVALID CHARACTER FOUND
         LM    R15,R1,MISSVOLP    GET MISSING VOLUME POINTERS   GP12338
TESTVOL  BXH   R15,R0,OBTAIN      NO MORE - DO NORMALLY         GP12338
         CLC   VOLSER,0(R15)      COMPARE THE VOLUMES           GP12338
         BE    UNCATF            IF EQUAL:UNCATALOG THE DSN     GP12338
         B     TESTVOL           GO TEST THE NEXT VOLUME
OBTAIN   CLI   REPLY,C'D'   VOLUME GONE ?
         BNE   OBTAINO        NO; TRY OBTAIN
         CLC   VOLSER,WTORASK2+16  SAME AS BEFORE ?
         BE    UNCAT        YES; UNCATALOG
         MVI   REPLY,0      ELSE KILL REPLY
OBTAINO  CLC   =C'SYSCTLG.V',OUTDSN                              87036
         BNE   OBTAINC       MVS CVOL ENTRY ?                    87036
         MVC   OUTDSN,=CL44'SYSCTLG '  MAKE OS CVOL CATALOG NAME 87036
OBTAINC  @OBTAIN PARMOBT,OPT=LOADED  TEST THE DSN                83016
         MVC   OUTDSN,DSN    RESTORE NAME FOR LISTING            87036
         LTR   R15,R15           WAS THE FORMAT 1 DSCB FOUND ?
         BZ    ITEXISTS      NOW SEE IF DEVICE TYPE IS CORRECT
         ST    R15,SCODE
         CH    R15,=H'8'          TEST IF THE DSCB WAS NOT FOUND
         BE    UNCATCV           GO UNCATAL THE DSN
         CH    R15,=H'4'       NO VOLUME ?
         BNE   OBTSKIP       NOT VOLUME MISSING                  81117
         AIF   ('&LOCAL' NE 'PID').NOTMC                         93004
         LM    R15,R1,TMCBXLE   GET THE CURRENT VOLUMES          81117
         CR    R15,R1        ANY ENTRIES ?                       81117
         BNL   FOUNDTMC      NO; ASSUME DD CARD MISSING          81117
LOOPTMC  CLC   VOLSER,UVOLSER-UVOLSER(R15)    EXISTING VOLUME ?  89296
         BE    FOUNDTMC      FOUND - SEE IF WTOR REQUIRED        81152
         BXLE  R15,R0,LOOPTMC   TRY NEXT VOLUME SERIAL           81152
GONETMC  MVC   WTORASK2+16(6),VOLSER  SAVE VOLSER                81117
         MVI   REPLY,C'D'    SET DELETED VOLUME                  81117
         B     UNCAT         GO TO UNCATALOG                     81117
         SPACE 1                                                 81151
FOUNDTMC CLC   =C'WORK',VOLSER  IN-HOUSE WORK PACK ?             81117
         BE    GONETMC       YES; NOT PERMITTED                  81117
         TM    MODEFG,FGQUIET  QUIET MODE ?                      81117
         BZ    WTOR          NO; SEE IF OPSIE WISHES TO MOUNT    81117
.NOTMC   SPACE 1                                                 81151
OBTSKIP  CLC   =H'4',SCODE+2    VOLUME NOT MOUNTED ?             81117
         BNE   OBTSKIPS      NO; SOMETHING ELSE
         CLC   LAST,DSN      CONNECT ENTRY ?
         BNE   *+10          NO
         MVC   OUTDSN(8),=CL8'CONNECT '  INDICATE CONNECT POINTER
         MVC   WTORASK2+16(6),VOLSER  SAVE VOLSER
         MVC   OUTMSGL,SKIPMANY                                  81117
         XR    R2,R2         SET COUNT FOR SKIPS
OBTSKIPL LA    R2,1(,R2)     UP DATASET COUNT
         GET   TEMPOUT,SORTREC  READ ANOTHER RECORD
         CLI   FLAG,FGTEST   ALSO TEST MODE ?
         BNE   OBTSKIPP      NO; SKIP SKIP LOOP
         CLC   VOLSER,WTORASK2+16  SAME AS BEFORE ?
         BE    OBTSKIPL      YES; TRY AGAIN
OBTSKIPP CVD   R2,DB         CONVERT SKIP COUNT
         ED    OUTMSGL(6),DB+5  SHOW COUNT                       81117
         PRTV  VOUT                                              81137
         MVC   OUT,OUT-1
         B     AGAINST       DO NEXT VOLUME
OBTSKIPS MVC   OUTMSGL(45),ERROROBT OBTAIN ERROR MESSAGE         81117
         L     R15,SCODE
         BAL   R9,DECODE          TO DECODE THE RETURN CODE
         MVC   OUTERR,CODE                                       81117
         B     PUTC          PRINT AND GET NEXT ENTRY
         SPACE 2
UNCATCV  CLC   LAST,DSN      CONNECT ENTRY ?
         BNE   UNCATN        NO; UNCATALOG
         LA    R1,DSN+9
         LA    R2,7
UNCATCL  CLI   0(R1),C' '    TRAILING BLANK ?
         BE    UNCATCX       YES - ADD FAKE DSN
         LA    R1,1(,R1)
         BCT   R2,UNCATCL
UNCATCX  MVI   0(R1),C'.'    MAKE FAKE INDEX POINT
         MVI   1(R1),C'A'    MAKE FAKE DSN
         LOCATE CONLOOK      SEE IF INDEX EXISTS
         LTR   R15,R15       FOUND ?
         BZ    ITEXISTS      YES; BUT CHECK DEVICE TYPE
         CH    R15,=H'8'     TEST RETURN CODE
         BH    AGAIN         SERIOUS (?) ERROR - IGNORE
         BL    UNCATV        RC=4 - GARBAGE ?
         LTR   R0,R0         HOW MANY VALID LEVELS ?
         BP    ITEXISTS      AT LEAST ONE - LEAVE IT
UNCATV   MODETEST DEBUG1,0   SKIP RELEASE IN TEST MODE           81117
         INDEX RELEASE       RELEASE CONNECT                     81117
         BXH   R15,R15,AGAIN  SKIP IF BAD RC
DEBUG1   MVC   OUTDSN(8),=CL8'CONNECT '  SHOW WHAT               81117
         MVC   OUTCODE(8),=C'RELEASED'                           81117
         MVC   OUTMSG,REASDEX  SHOW EMPTY INDEX                  81117
         CLI   REPLY,C'D'    DELETED ?                           81117
         BNE   PUTC          NO; PRINT                           81117
         MVC   OUTMSG,PACKGONE  SHOW PACK DELETED                81117
         B     PUTC          AND PRINT
         SPACE 1
UNCATF   MVI   REPLY,C'D'    SET FOR DEFUNCT SERIAL             GP12338
UNCAT    CLC   LAST,DSN      CVOL CONNECT ENTRY ?
         BE    UNCATV        YES - UNCONDITIONAL RELEASE
UNCATN   MVI   MGFLAG,MGNULL SHOW SCRATCHED
         CLI   REPLY,C'D'    DELETED VOLUME ?                    81117
         BNE   UNCATL        NO; UNCATALOG                       81117
         MVI   MGFLAG,MGVSGONE   SET FOR VOLUME GONE             81117
         SPACE 1
UNCATL   XR    R15,R15       FOR TESTING
         IC    R15,MGFLAG    GET REASON CODE
         MH    R15,=AL2(L'REASONS)
         LA    R15,REASONS(R15)  GET MESSAGE
         MVC   OUTMSG,0(R15)  MOVE REASON                        81117
         CLC   LAST,DSN      CONNECT ENTRY ?
         BE    UNCATV        YES - RELEASE
         MODETEST DEBUG2,0   TEST FOR TEST MODE                  81117
         TM    PRIVY,VSCAT+PRISYS  VSAM MASTER CATALOG ?         83016
         BNO   UNCATOS       NO                                  83016
         CATALOG UNCATVSM    UNCATALOG FROM VSAM MASTER          83016
         B     UNCATOSV                                          83016
UNCATOS  CATALOG UNCATAL         UNCATAL THE DSN                 83016
UNCATOSV LTR   R15,R15           UNCATALOGED ?                   83016
         BNZ   NOTDONE           WAS NOT UNCATALOGED
DEBUG2   MVC   OUTCODE(L'UNCATLG),UNCATLG   MOVE THE MESSAGE     81117
         B     INDSTRIP      PUT OUT THE MESSAGE AND DELETE HI-HI INDX
NOTDONE  BAL   R9,DECODE          GO TO DECODE THE RETURN CODE
         MVC   OUTCODE(25),ERRORCAT MOVE THE MESSAGE             81117
         MVC   OUTERR,CODE   THE RETURN CODE                     81117
PUTC     PRTV  VOUT                                              81137
         B     AGAIN
         SPACE 2
ITEXISTS CLC   CATVOL,VOLSER  PRIOR CALCULATION ?
         BE    ITEXDEVT      YES - CHECK DEVICE TYPE
         SERVCALL UCBDK,VOLSER,ERR=AGAIN  LOCATE UCB BY VOLSER   81151
         LTR   R3,R0         FOUND ?                             81117
         BNP   AGAIN         NO; SKIP                            81117
         USING UCBOB,R3      DECLARE UCB                         81137
         MVC   CATDEV,UCBTBYT1  COPY DEVICE TYPE FROM UCB        81117
         NI    CATDEV+1,255-UCBVLPWR-UCBDVPWR-UCBRR              81159
         CLC   =X'200A',UCBTBYT3    3340 ?                       81155
         BNE   *+8           NO                                  81155
         NI    CATDEV+1,255-UCBRPS  WHY DOES MVS DO THIS ?       81155
         MVC   CATVOL,VOLSER SHOW DEVICE TYPE ENTERED
         SERVCALL UCBGN,(R3)  GET DEVICE NAME                   GP12338
         MVC   RECATYP,0(R1)      BLANKS OR NAME                GP12338
         DROP  R3                                                81117
         SPACE 1
ITEXDEVT CLC   VOLTYPE,CATDEV  IS DEVICE TYPE CORRECT ?
         BE    AGAIN         YES - OK
         CLC   LAST,DSN      CONNECT ENTRY ?
         BE    ITEXCONN      YES - RELEASE AND RECONNECT CORRECTLY
         MODETEST ITEXPUT,0   TEST FOR TEST MODE                 81117
         TM    PRIVY,VSCAT+PRISYS  VSAM MASTER CATALOG ?         87036
         BNO   ITEXREC       NO                                  87036
         CATALOG RECATLVS    RECATALOG IN VSAM MASTER            87036
         B     ITEXPUT                                           87036
ITEXREC  CATALOG RECATLG     RECATALOG IT CORRECTLY              81117
ITEXPUT  MVC   OUTMSGRE,RECATMG SHOW WHAT WAS DONE               81117
         LTR   R15,R15       SUCCESSFUL ?
         BNZ   NOTDONE       NO; SIGNAL ERROR
         B     PUTC          NOW PRINT IT
         SPACE 1
ITEXCONN MODETEST DEBUG4,0   TEST FOR DEBUG MODE                 81117
         INDEX RELEASE       RELEASE THE INDEX                   81117
         BXH   R15,R15,AGAIN  IGNORE IF BAD                      81117
DEBUG4   MODETEST DEBUG5,0   TEST FOR DEBUG MODE                 81117
         INDEX RECONN        RECONNECT                           81117
DEBUG5   MVC   OUTDSN(8),=CL8'CONNECT '  IDENTIFY                81117
         B     ITEXPUT       ADD RECAT MSG AND TEST RETURN
         SPACE 2
ALIDEL   MVC   OUTMSG,REASTYP                                    81117
         MODETEST PUTC,0    TEST FOR TEST MODE                   81117
         INDEX ALDELETE      DELETE THE ENTRY                    81117
         BXH   R15,R15,AGAIN   IGNORE IF ERROR
         B     PUTC          AND PRINT IF DELETED
         SPACE 2
INDDEL   TR    DSN,ICETAB    MAKE 'FF' INTO BLANKS AGAIN
         MVC   OUTDSN,DSN    COPY IT                             81117
INDLEVEL MVC   OUTMSG,REASDEX  SHOW EMPTY INDEX                  81117
         CLI   REPLY,C'D'    GONE ?                              81117
         BNE   *+10          NO                                  81117
         MVC   OUTMSG,PACKGONE  FLAG IT                          81117
         MVC   OUTVOL(OUTMSGL-OUTVOL),OUTVOL-1   CLEAR VOL FIELD 81117
         MVC   OUTCODE(L'UNCATLG),UNCATLG MOVE THE MESSAGE       81117
         MODETEST DEBUG7,0   FOR TESTING                         81117
         INDEX INDELETE      DELETE THE INDEX                    81117
DEBUG7   CH    R15,=H'4'     CHECK RETURN                        81117
         BE    AGAIN         BUT HOW ?
         BL    INDSTRIP      GOOD - STRIP A LEVEL
         CH    R15,=H'12'    ALIAS OR DSN ?
         BNL   AGAIN         YES - SKIP IT
INDSTRIP PRTV  VOUT          PRINT THE PREVIOUS MESSAGE          81137
         MODETEST AGAIN,(R15)  SKIP IF IN TEST MODE              81117
         LA    R1,OUTDSN+L'OUTDSN-1     POINT TO LAST BYTE       81117
         LA    R2,OUTDSN     POINT TO FIRST BYTE                 81117
INDSTROP CLI   0(R1),C' '    TRAILING BLANK ?
         BE    INSTRING      YES; SKIP
         CLI   0(R1),C'.'    END OF LEVEL ?
         MVI   0(R1),C' '    REPLACE BY BLANK UNCONDITIONALLY
         BE    INDSTEST      PROCESS NEXT LEVEL
INSTRING BCTR  R1,0          GET PREVIOUS BYTE
         CR    R1,R2         DONE ALL YET ?
         BNL   INDSTROP      NO; TRY MORE
INDSTEST CLI   0(R2),C' '    ANY LEFT ?
         BE    AGAIN         NO; DO NEXT REQUEST
         AIF   ('&LOCAL' NE 'CCSI').NOWYLX                       81151
         CLI   OUTDSN+3,C' '   POSSIBLE WYLBUR INDEX ?           81151
         BNE   INDLEVEL      NO; TRY A DELETE                    81151
         SERVCALL WYLDX,OUTDSN,LEN=3,CC0=AGAIN  SKIP IF WYLBUR LEVEL
.NOWYLX  B     INDLEVEL      TRY ANOTHER DELETE                  81151
         SPACE 2
EXIT16   MVICC 16            SET RETURN CODE                    GP12338
         TM    MODEFG,FGDUMP      DUMP ON ERROR ?               GP12338
         BZ    ENDJOB                                           GP12338
         @OBTAIN OPT=CLOSE   CLOSE/FREEMAIN VTOC SPACE          GP12338
         SERVTERM ,          CLOSE THE PRINTER AND WITHDRAW     GP12338
         ABEND 16,DUMP                                          GP12338
         SPACE 1                                                GP12338
ENDJOB   @OBTAIN OPT=CLOSE   CLOSE/FREEMAIN VTOC SPACE           81152
         SERVTERM ,          CLOSE THE PRINTER AND WITHDRAW      81152
         PGMEXIT RC=RETCODE       NORMAL END                    GP12338
         SPACE 2
DECODE   CVD   R15,DB            DECODING THE RETURN CODE
         UNPK  CODE(2),DB
         OI    CODE+1,X'F0'      X'F' IN THE RIGHTMOST BYTE
         BR    R9
WTOR     CLC   BLANKO,WTORASK2+16
         BE    STVOL
         CLC   VOLSER,WTORASK2+16
         BE    OBTSKIP
STVOL    MVC   WTORASK2+16(6),VOLSER
         MVC   WTORASK2+23(6),BLANKO
         CLI   OUTTYPE,C' '  ANY DEVICE TYPE ?                   81117
         BE    WTORASK       BUT HOW ?
         MVC   WTORASK2+23(6),OUTTYPE                            81117
WTORASK  XC    WTORECB,WTORECB
WTORASK2 WTOR  '      /TYPEXX - MOUNT VOL,REPLY U; ELSE S-SKIP, D-PACK *
               DELETED, Z-QUIET',REPLY,1,WTORECB,ROUTCDE=1
         WAIT  ECB=WTORECB
         OI    REPLY,X'40'
         CLI   REPLY,C'Z'   NO WTOR ??
         BE    NOWTOR
         CLI   REPLY,C'U'
         BNE   *+14
         MVC   WTORASK2+16,BLANKO
         B     OBTAIN
         CLI   REPLY,C'S'
         BE    OBTSKIP
         CLI   REPLY,C'D'   VOLUME DELETED ?
         BE    UNCAT        YES; UNCATALOG THE DATASET
         B     WTORASK
NOWTOR   OI    MODEFG,FGQUIET   SET NO WTOR MODE                 81117
         B     OBTSKIP
         SPACE 1                                                 81117
ABEND69  ABEND 69,DUMP
         SPACE 2
         LTORG ,
         EJECT ,                                                 81117
GDGMASK  DC    C'G0000V00'   MASK FOR GDG TEST
GDGREST  DC    C'G',X'00000000',C'V00'  REST AFTER NC TEST
TMCNODD  EQU   X'80'           TMC DD MISSING                    82032
TMCFLAG  DC    AL1(TMCNODD)  TMC PROCESSING FLAG                 82032
TMCBXLE  DC    A(*,8,*)      BAD DISK BXLE                       89296
SYSPRINT PRTWORK SYSPRINT,TITLE=5  DEFINE THE SYSPRINT OPTIONS   81137
@OBTAIN  DC    A(0)          ADDRESS OF @OBTAINS                 83016
@TMSREAD DC    A(0)          ADDRESS OF TMC READER               83016
WTORECB  DC    F'0'
CONLOOK  CAMLST NAME,DSN+8,CVOL,JFCB   LOCATE CONNECTED INDEX
RELEASE  CAMLST DRPX,OUTCONN,CVOL  RELASE CONNECT POINTER        81117
RECONN   CAMLST LNKX,OUTCONN,CVOL,CATDEV  RECONNECT              81117
UNCATAL  CAMLST UCATDX,DSN,CVOL  CATALOG PARAMETER LIST
UNCATVSM CAMLST UCATDX,DSN   UNCATALOG FROM VSAM MASTER          83016
INDELETE CAMLST DLTX,OUTDSN,CVOL  DELETE EMPTY(?) INDEX          81117
ALDELETE CAMLST DLTA,DSN,CVOL    DELETE ALIAS
PARMOBT  CAMLST SEARCH,OUTDSN,VOLSER,AREA                        81117
RECATLG  CAMLST RECAT,DSN,CVOL,CATLIST  TRY TO FIX THE ENTRY
RECATLVS CAMLST RECAT,DSN,,CATLIST  TRY TO FIX THE ENTRY         87036
CATLIST  DC    H'1'          ONE VOLUME (BY DESIGN)
CATDEV   DC    XL4'0'        DEVICE TYPE
CATVOL   DC    CL6' '        VOL-SER
         SPACE 1
SORTSEG  DS    0CL67' '      RECORD PASSED TO/FROM SORT          81137
FGTEST   EQU   1             TEST IF DATASET EXISTS ON PACK
FGUNCT   EQU   2             UNCATALOG UNCONDITIONALLY
FGDLTA   EQU   3             DELETE ALIAS ENTRY
FGDLTX   EQU   4             DELETE EMPTY INDEX
FGDLTC   EQU   5             RELEASE CVOL FOR MISSING INDEX
FGTMC    EQU   X'FD'         TMC DSN ENTRY                       81137
FGTMCAT  EQU   FGTMC+1       CATLG TAPE ENTRY                    81137
FGEND    EQU   X'FF'         DUMMY LAST ENTRY                    81137
FLAG     DC    AL1(FGTEST)   NORMAL FLAG SETTING
VOLSER   DC    CL6' '        VOLUME FOR OBTAIN CHECK
DSN      DC    CL44' '
TAPEFG   DC    X'0'          TAPE/TMC FLAG                       81137
SORTKEY  EQU   SORTSEG,*-SORTSEG,C'C'  KEY                      GP12338
DSCBTTR  DC    XL3'0'        VSAM CTLG - TTR OF DSCB             81137
VOLTYPE  DC    XL4'0'        DEVICE TYPE OF VOLUME
CVOL     DC    CL6' '        CATALOG VOLSER
PRIVY    DC    X'0'          CVOL RESTRICTION FLAGS
PRIWYL   EQU   128           WYLBUR CVOL
PRILIB   EQU   64            LIBRARY PACK
PRISYS   EQU   32            SYSRES
VSCAT    EQU   X'02'         ENTRY FROM VSAM CATALOG             81137
VSALIAS  EQU   X'01'         VSAM CTLG ALIAS ENTRY               81137
         SPACE 1
MGNULL   EQU   0             DATASET SCRATCHED
MGBACT   EQU   1             BAD ACCOUNT NUMBER
MGNPSW   EQU   2             NO WYLBUR PASSWORD
MGNAME   EQU   3             INVALID NAME
MGTYPE   EQU   4             INVALID ENTRY TYPE
MGINDX   EQU   5             EMPTY INDEX
MGNIND   EQU   6             WYLBUR INDEX LEVEL NOT VALID        81117
MGVSGONE EQU   7             DISK PACK NOT IN TMC                81117
MGPGMERR EQU   8                                                 81117
MGNTMC   EQU   9             TAPE ENTRY NOT IN TMC               81137
MGTMEXP  EQU   10            TMC ENTRY EXPIRED                   81137
MGFLAG   DC    X'0'          MESSAGE FLAG
SORTREC  EQU   SORTSEG,*-SORTSEG,C'C'  SORT RECORD              GP12338
SORTLEN  EQU   *-SORTREC     LENGTH OF SORT RECORD
         SPACE 1                                                 81137
TMCSORT  DC    XL(SORTLEN)'0'  TMC RECORD PRIOR TO CATALOG       81137
         SPACE 2
VALLMAP  DC    15AL1(MGPGMERR)   PROGRAM ERROR ?                 81117
         ORG   VALLMAP+VRSYNT                                    81117
         DC    AL1(MGNAME)                                       81117
         ORG   VALLMAP+VRACCT                                    81117
         DC    AL1(MGBACT)                                       81117
         ORG   VALLMAP+VRNTOS                                    81117
         DC    AL1(MGNAME)                                       81117
         ORG   VALLMAP+VRNWYL                                    81117
         DC    AL1(MGNAME)                                       81117
         ORG   VALLMAP+VRDLEN                                    81117
         DC    AL1(MGNAME)                                       81117
         ORG   VALLMAP+VRDLON                                    81137
         DC    AL1(MGNAME)                                       81117
         ORG   VALLMAP+VRNWYX                                    81137
         DC    AL1(MGNIND)                                       81137
         ORG   VALLMAP+VRNPSW                                    81117
         DC    AL1(MGNPSW)                                       81117
REPLY    DC    X'40'
SORTPARM DC    0A(0),X'80',AL3(PARMSORT)
         DC    H'0'
PARMSORT DC    AL2(SORTLISX-SORTLIST)
SORTLIST DC    A(SORTS1,SORTS2,SORTR1,SORTR2,0,0)
         DC    X'00',C'MAX',X'FF00',C'AP'
SORTLISX EQU   *
*        1/TYPE 6/VOLSER 44/DSN  1/TAPETYPE                      81137
SORTS1   DC    C' SORT FIELDS=(1,'                              GP12338
         EBCDIG L'SORTKEY,3            KEY LENGTH               GP12338
         DC    C',CH,A) ' ,FILSZ=E50000'    NO ESTIMATE IN MVT  GP12338
SORTS2   DC    0C' '
SORTR1   DC    C' RECORD TYPE=F,LENGTH=('                       GP12338
         EBCDIG L'SORTREC,3                                      81137
         DC    C')'                                             GP12338
SORTR2   DC    C' '
VOUT     DC    Y(137,0),C' '   PRINT RECORD                      81117
OUT      DC    CL132' '      NOTE BLANK AT OUT-1
         ORG   OUT+1                                             81117
OUTDSN   DC    CL44' '       DSN/INDEX                           81117
OUTCONN  EQU   OUTDSN+8,8,C'C'  CONNECT INDEX NAME               81159
         DC    CL1' '                                            81117
OUTVOL   DC    CL6' '        CATALOGED VOLUME                    81117
         DC    CL1' '                                            81117
OUTTYPE  DC    CL6' '        VOLUME DEVICE TYPE                  81117
         DC    CL2' '                                            81117
OUTMSGL  DC    0CL43' '      NNN ENTRIES SKIPPED - NOT MOUNTED   81117
         DC    CL3' '                                            81117
OUTMSG   DC    CL15' '       STATUS/REASON CODE                  81117
OUTMSGRE EQU   OUTMSG+5,20,C'C'  'RECATALOGED' MSG               81117
         ORG   OUTDSN+90                                         81117
OUTCODE  DC    CL25' '       ERROR STATUS                        81117
         ORG   OUT+110                                           81117
OUTERR   DC    CL2' '        ERROR CODE                          81117
         ORG   OUT+120                                           81117
OUTCVOL  DC    CL6' '        CVOL NAME                           81117
         ORG   ,                                                 81117
SYSCTLG  DCB   DDNAME=SYSCTLG,DSORG=PS,MACRF=RP,RECFM=U,BLKSIZE=&BLKLEN*
               ,EXLST=EXLST
SYSCATDD DC    CL8'SYSCTLG'   1 OF 2   DDNAME FOR @CATREAD CALL  81117
SYSCATVL DC    CL6' '         2 OF 2  VOLSER FOR @CATREAD CALL   81117
TEMPOUT  DCB   DDNAME=SORTIN,DSORG=PS,MACRF=(GM,PM),LRECL=L'SORTREC,   *
               RECFM=FB,BLKSIZE=((7294/L'SORTREC)*L'SORTREC),          *
               EODAD=ENDJOB
INDEXEND DC    0XL12'FFFFFFFFFFFFFFFF00000000'  END OF INDEX
LAST     DC    XL8'FFFFFFFFFFFFFFFF'
ZERO     DC    XL4'000000'
BLANKO   DC    C'      '
MASTVOL  DC    CL6' '        MASTER CATALOG VOLSER               83016
ERRORCAT DC    CL25'CATALOG ERROR CODE(DEC):'
ERROROBT DC    CL45'DSN NOT UNCATALOGED OBTAIN ERROR CODE(DEC):'
SKIPMANY DC    0CL43' ',X'402020202120',C' ENTRIES SKIPPED - VOLUME NOT*
                MOUNTED'
UNCATLG  DC    C'UNCATALOGED'
RECATMG  DC    C'RECATALOGED : DISK-?'                           81117
RECATYP  EQU   *-6,6,C'C'    DEVICE TYPE                        GP12338
REASONS  DC    CL15'DSN NOT FOUND'                               81117
         DC    CL15'INVALID ACCT'
         DC    CL15'NO PASSWORD'
         DC    CL15'INVALID NAME'
REASTYP  DC    CL15'INVALID TYPE'
REASDEX  DC    CL15'EMPTY INDEX'
         DC    CL15'INVALID INDEX'     UNDEFINED/OBSOLETE INDEX  81117
PACKGONE DC    CL15'DISK PACK GONE'                              81117
         DC    CL15'PROGRAM ERROR ?'                             81117
         DC    CL15'DSN NOT IN TMC'                              81278
         DC    CL15'EXPIRED IN TMC'                              81137
HEADER   VCON  '#                       ',END=HEADERND           81278
         DC    C'CATALOG CLEANUP ROUTINE   '                     81117
HEADTEST DC    C'*** DEBUG LISTING ***'                          81117
         VCON  ,                                                 81278
SUBHEAD  VCON  END=SUBHEADN                                      81278
         DC    CL46' DATASET NAME OR INDEX ENTRY'                81117
         DC    CL18'VOLUME/TYPE'                                 81117
         DC    CL24'CONDITION'                                   81117
SUBHTEST DC    C'NO'                                             81117
         DC    CL16' ACTION TAKEN'                               81117
         DC    CL14'ERROR CODE'                                  81117
         DC    C'CVOL'                                           81117
         VCON  ,                                                 81278
EXLST    DC    0F'0',X'87',AL3(JFCB)
JFCB     DS    0D,176X
         ORG   JFCB                                              81151
         IEFJFCBN ,          EXPAND THE JFCB                     81151
         ORG   ,                                                 81151
AREA     DS    0D
         DS    148C
         ORG   JFCB                                              81137
VCB      DC    0F'0'         VCB READ AREA                       81137
         ORG   ,                                                 81137
CODE     DS    H
*---------------------------------------------------------------------*
*   VALIDITY-CHECK SERIALS - NO SPECIAL CHARACTERS ALLOWED            *
*---------------------------------------------------------------------*
TRTSERNO DC    256X'4'                                          GP12338
         TRENT TRTSERNO,0,C' ',   ALLOW SHORT (<6 CHAR) SERS    GP12338
         TRENT ,0,C'@',C'#',C'$', ALLOW NATIONAL CHARACTERS     GP12338
         TRENT ,0,(C'A',9),(C'J',9),(C'S',8),    ALPHA          GP12338
 AIF ('&LOCAL' EQ 'CCSI' OR '&LOCAL' EQ 'PID' OR '&LOCAL' EQ 'AMS').NHM
         TRENT ,0,C'-',C'/',      SOME USERS ALLOW THESE        GP12338
.NHM     TRENT ,0,(C'0',10)       ALLOW DIGITS                  GP12338
         SPACE 1
*---------------------------------------------------------------------*
*   SORT INDICES BY LENGTH - CHANGE TRAILING BLANK TO X'FF' AND V.V.  *
*---------------------------------------------------------------------*
ICETAB   DC    256AL1(*-ICETAB)  TRANSLATE TRAILING BLANKS TO FF
         TRENT ICETAB,X'FF',C' ',                               GP12338
         TRENT ICETAB,C' ',X'FF'  SWAP FF AND BLANK             GP12338
         SPACE ,
*---------------------------------------------------------------------*
*   CONVERT GDG NAMES                                                 *
*---------------------------------------------------------------------*
GDGTAB   DC    256X'0'
         ORG   GDGTAB+X'06'
         DC    10X'01'       FLAG GDG LEVEL NUMBERS
         ORG   ,
*---------------------------------------------------------------------*
*   DELETEVOL(v1,v2 v3...) PARSING - STOP ON COMMA AND BLANK          *
*---------------------------------------------------------------------*
         SPACE 1
*---------------------------------------------------------------------*
*   DEFINE UCBTBYT3/4 IN PRINTABLE FORM - DASD AND TAPE ONLY          *
*---------------------------------------------------------------------*
UNITNM   UNITY FORMAT=26,DEVD=(DA,TA)                           GP12338
UNITNMND DC    X'FFFF',CL6'???? '                               GP12338
         SPACE 1
         AIF   (NOT &MVS).NOVSBUF                                83016
VSAMCAT  ACB   AM=VSAM,CATALOG=NO,DDNAME=CATALOGN,                     *
               MACRF=(KEY,DIR,SEQ),EXLST=VSAMEXLS               GP12338
VSAMRPL  RPL   ACB=VSAMCAT,AM=VSAM,AREA=VSAMRECD,                      *
               AREALEN=L'VSAMRECD,                                     *
               ARG=MCKEY,ECB=MCECB,KEYLEN=4,MSGAREA=MCMSG,             *
               MSGLEN=L'MCMSG,NXTRPL=0,OPTCD=(KEY,SEQ,GEN)       83016
VSAMEXLS EXLST AM=VSAM,EODAD=VSAMEOF                            GP12338
         SPACE 1
MCMSG    DC    CL16' '       ?                                   83016
MCKEY    DC    XL8'0'        CATALOG SEARCH KEY                  83016
MCECB    DC    F'0'          VSAM RPL ECB                        83016
MCRBP    DC    0F'0',AL1(S99RBPND),AL3(MCRB) MASTER CATALOG RE-  83016
*                                    QUEST BLOCK POINTER         83016
MCRB     DC    0F'0',AL1(20)  REQUEST BLOCK LENGTH               83016
         DC    AL1(S99VRBAL)  SPECIFY ALLOCATION                 83016
         DC    XL6'0'         FLAGS1, ERROR CODES, INFO CODES    83016
         DC    A(MCTPP)       POINTER TO TEXT POINTER LIST       83016
         DC    XL8'0'         RESERVED, FLAGS2                   83016
MCTPP    DC    A(MCT1)        MASTER CATALOG TEXT POINTER LIST   83016
         DC    A(MCT2)                                           83016
         DC    A(MCT3)                                           83016
         DC    A(MCT4)                                           83016
         DC    A(MCT5)                                           83016
         DC    X'80',AL3(MCT6)                                   83016
MCT1     DC    Y(DALDSNAM,1,44)  SPECIFY DSNAME                  90302
MCDSN    DC    CL44' '         VSAM CATALOG DSNAME               83016
MCT2     DC    Y(DALSTATS,1,1),X'08'  SPECIFY DISP=SHR           83016
MCT3     DC    Y(DALVLSER,1,6)  SPECIFY VOLUME SERIAL NUMBER     83016
MCVOL    DC    CL6' '         VOLUME SERIAL NUMBER               83016
MCT4     DC    Y(DALUNIT,1,3) SPECIFY UNIT ADDRESS               83016
MCUNIT   DC    CL3' '         UNIT ADDRESS                       83016
MCT5     DC    Y(DALCLOSE,0)  SPECIFY UNALLOCATE AT CLOSE        83016
MCT6     DC    Y(DALRTDDN,1,8) SPECIFY RETURN DDNAME             83016
MCDDN    DC    CL8' '         DDNAME RETURN FIELD                83016
         SPACE 1
VSAMRECD DS    XL512         VSAM CATALOG AREA                   83016
         ORG   VSAMRECD                                          83016
VCLINK   DS    XL4           LINK FIELD                          83016
         ORG   VSAMRECD+44                                       83016
VCTYPE   DS    X             A-DSN/CVOL X-INDEX                  83016
         ORG   VSAMRECD+49                                       83016
VCDSN    DS    CL44          DSN/CATALOG NAME                    83016
         DS    XL8           8X'FF' ?                            83016
VCDATE   DS    PL3           ENTRY DATE                          83016
         ORG   VSAMRECD+112  COUNT/RELOCATION SECTION            83016
VCNFD    DS    X             NUMBER OF SUBFIELD POINTERS         83016
VCFLD    EQU   *             START OF POINTERS                   83016
         ORG   ,                                                GP12338
         SPACE 1                                                 83016
VFSECT   DSECT ,             FIELD POINTER DESCRIPTION           83016
VF       DS    X             ?                                   83016
VFOFF    DS    XL2           OFFSET TO FIELD FROM POINTER END    83016
VFFLG    DS    X             FLAG FIELD                          83016
         DS    X             ?                                   83016
VFLEN    EQU   *-VF          LENGTH OF POINTER ENTRY             83016
         SPACE 1                                                 83016
VLSECT   DSECT ,             VOLUME ENTRY                        83016
VLLEN    DS    X'0012'       STANDARD VOLUME SECTION LENGTH      83016
VLTYPE   DS    XL4           DEVICE TYPE                         83016
VLVOL    DS    CL6           VOLUME SERIAL                       83016
VLFILE   DS    XL2           TAPE FILE SEQUENCE                  83016
         DS    X'80'         ?                                   83016
VLDSCB   DS    XL3           DSCB TTR                            83016
         SPACE 1
SAVE     DSECT ,             SAVE AND WORK AREA                 GP12338
DB       DS    2D                                               GP12338
         SERVDEFS ,          SERVICE ROUTINE ADDRESSES          GP12338
@STACK   DC    A(0)          ADDRESS OF STACK
MODEFG   DC    X'00'         LOCAL PROCESSING FLAGS              81117
FGDEBUG  EQU   X'80'           RUNNING IN TEST MODE ONLY         81117
FGQUIET  EQU   X'40'           SUPPRESS WTORS                    81117
FGDUMP   EQU   X'20'           ABEND AFTER ERROR                GP12338
FGSTEP   EQU   X'02'           FINAGLED PCCB W/STEPCAT FLAG     GP12338
FGMAST   EQU   X'01'           PROCESSING VSAM MASTER CATALOG   GP12338
CURDAT   DC    PL3'0'        CURRENT DATE                        81137
SCODE    DC    F'0'
TTR      DC    F'0'
MYPCCB   DS    A                                                GP12338
@CURTIOT DS    A                                                GP12338
@VERENT  DS    A
         MAPPARSE DSECT=NO                                      GP12338
         PARKEYBX MISSVOL    LIST OF DEFUNCT SERIALS            GP12338
STACK    DS    23XL(4+&BLKLEN)'0' STACK FOR CVOL INDICES        GP12338
         SPACE 1
MISSVOL  DS    CL6           SERIAL OF DELETED VOLUME           GP12338
MISSVOL2 DS    CL6           SERIAL OF DELETED VOLUME           GP12338
         DS    29CL6         SERIAL OF DELETED VOLUME           GP12338
MISSVOLN DS    CL6           SERIAL OF DELETED VOLUME           GP12338
SAVEEND  EQU   *                                                GP12338
         SPACE 1
         MAPPARST ,                                             GP12338
         SPACE 1
         IEFZB4D0                                                83016
         IEFZB4D2                                                83016
         SPACE 1
         IGGCAXWA ,          FROM MY PVTMACS                    GP12338
         SPACE 1                                                 82228
*        NO USABLE IBM AMCBS MACRO                               90302
*                                                                90302
AMCBS    DSECT ,                                                 90302
CBSID    DS    CL2           ID                                  90302
CBSSIZ   DS    AL2           LENGTH                              90302
CBSMCSTA DS    A             CCHH OF MASTER CATLG                90302
CBSACB   DS    A             MASTER CAT ACB                      90302
CBSCBP   DS    A             C B MANIP                           90302
CBSCMP   DS    0A            CAT RTNE                            90302
CBSMCUCB DS    A             MASTER CAT UCB                      90302
CBSCAXCN DS    A             CAXWA CHAIN                         90302
CBSCRACA DS    A             CRA CAXWA CHAIN                     90302
CBSCRTCB DS    A             CRA TASK TCB                        90302
CBSVSRT  DS    A                                                 90302
CBSVUSE  DS    A             VSRT USE COUNT                      90302
CBSVPTR  DS    A             VSRT                                90302
CBSFLAGS DS    X             FLAGS                               90302
CBSMICF  EQU   X'80'           MAST IS ICF CATALOG               90302
         DS    XL3           SPARE                               90302
CBSVVDSA DS    A             VVDS MANAGER                        90302
CBSDEVNT DS    A             DEVICE NAME TABLE                   90302
CBSVSICN DS    A             IDAVSI CHAIN                        90302
CBSFLG1  DS    X             AMCBS FLAGS                         90302
CBSCUVSI EQU   X'80'           VSI CHAIN CLEAN-UP REQUIRED       90302
         DS    XL3           SPARE                               90302
         SPACE 1
         IEFPCCB ,                                              GP12338
.NOVSBUF SPACE 2                                                 83016
         PRINT &PRTSYS                                           83016
CVT      DSECT ,                                                 81137
         CVT   DSECT=YES                                         81137
         SPACE 1                                                 89296
         IHAPSA ,                                                89296
         SPACE 1                                                 81152
         IKJTCB ,            GET TCB MAPPING                     81152
         SPACE 1                                                 81152
         IEZJSCB ,           AND JSCB                            81152
         SPACE 1                                                 81151
         IHACDE ,                                                81151
         SPACE 1                                                 81151
         DCBD  DEVD=DA,DSORG=PS                                  81151
         SPACE 1                                                 81137
UCB      DSECT ,                                                 81137
         IEFUCBOB ,                                              81137
         AIF   (&MVS).UCBRPS                                     82195
UCBRR    EQU   X'20'         UCBTBYT2 - SHARED DASD              81159
UCBRPS   EQU   X'10'         UCBTBYT2 - ROTTEN POSITION SENSING  81159
.UCBRPS  SPACE 1                                                 81159
MYTIOT   DSECT ,             TIOT MAPPING SANS DSECT            GP12338
         IEFTIOT1 ,          TIOT MAPPING SANS DSECT            GP12338
         SPACE 1
         AIF   ('&LOCAL' NE 'PID').NOTMCD                        93004
USERCVT  USERCVT ,                                               89296
USERVOLS USERVOLS ,          USER-VOLUME TABLE                   89296
TMSDSECT TMSREC ,                                                93004
         SPACE 1                                                 81137
         ORG   TMRECORD                                          81137
         TMSDSNB  ,                                              81137
         ORG   ,                                                 81137
.NOTMCD  SPACE 1                                                 81151
         END
