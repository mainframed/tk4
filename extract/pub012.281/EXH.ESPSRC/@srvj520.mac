J520     TITLE '@ S R V J 5 2 0  ***  JES2 5.2.0 SERVICE ROUTINES'
         MACRO ,
&L       SAVEX &R1,&R3,&LOC,&TYPE=*,&SETAM=                     GP02310
&L       SWAPR R14,R15       @SERVICE IS SCREWY
         AIF   ('&TYPE' EQ 'STM').STM
         AIF   ('&TYPE' EQ 'BSM').STM                           GP99120
         MNOTE 8,'TYPE=&TYPE UNKNOWN - TYPE=STM ASSUMED'
         AGO   .STM
.STM     ANOP  ,
         STM   &R1,&R3,&LOC
         ORG   &SYSECT+2                                         90274
         DC    X'E0'         MAKE 47F0E020 - BRANCH AROUND ID    90274
         ORG   ,                                                 90274
         MEND  ,
         SPACE 1                                                GP02318
         MACRO ,                                                GP02318
&NM      STOMP &IV=,&ADDR=                                      GP02318
         GBLA  &ZZSTMP#                                         GP02318
         GBLC  &ZZSTMP@                                         GP02318
         AIF   ('&IV' EQ '').NONUMB                             GP02318
&ZZSTMP# SETA  &IV                                              GP02318
.NONUMB  AIF   ('&ADDR' EQ '').NONADD                           GP02318
&ZZSTMP@ SETC  '&ADDR'                                          GP02318
.NONADD  ANOP  ,                                                GP02318
&ZZSTMP# SETA  &ZZSTMP#+1                                       GP02318
&NM      MVI   &ZZSTMP@,&ZZSTMP#                                GP02318
 MNOTE *,'***************** STOMP &ZZSTMP# ********************'
         MEND  ,                                                GP02318
         SPACE 2
         COPY  OPTIONGB
         SYSPARM LIST=YES    ASSEMBLE WITH 5.2.0 OPTIONS/LIBRARIES
         SPACE 1                                                 86251
         COPY  $HASPGBL                                          86251
         SPLEVEL TEST        SET JES2 AND SP LEVEL               90189
&MACSPLV SETC  '&SYSSPLV'                                        90189
         AIF   ('&VERSION' NE '').VERSET                         90288
&VERSION SETC  'SP 5.2.0'    GUESS WHO DOESN'T SET THIS ?       GP99026
.VERSET  SPACE 1
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 1990  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2002  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                     *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 1
         LCLB  &LOCO
&LOCO    SETB  ('&LOCAL' EQ 'PID')  SHORT-CUT FOR LOCAL TESTS    92287
         SPACE 2
***********************************************************************
*                                                                     *
*        @SRVJ520 CONTAINS ALL JES2 RELATED CODE FOR THE @SERVICE     *
*        ROUTINE (FOR VERSIONS FROM 1.3.5 THROUGH 5.2.0).             *
*                                                                     *
*        THE CODE HAS BEEN SPLIT OFF FROM @SERVICE IN ORDER TO        *
*        PROVIDE FOR MULTIPLE JES2 SUB-SYSTEMS RUNNING ON THE SAME    *
*        MACHINE, AND TO AFFORD IEFUJV, IEFUJI AND IEFUSI SOME DEGREE *
*        OF INDEPENDENCE FROM SUB-SYSTEM CONTROL BLOCKS.              *
*                                                                     *
***********************************************************************
*                                                                     *
*  NOTE: @SRV ROUTINES MAY COEXISTS ON THE SAME SYSTEM WITH DIFFERENT *
*        LEVELS OF JES2. REFERENCES ARE RESOLVED DYNAMICALLY BY       *
*        MATCHING THE JES2 SUBSYSTEM NAME TO THE MODULE ALIAS:        *
*                                                                     *
*        ASSUME PRIMARY JES2 IS LEVEL 5.2.0 - ASSIGN THE ALIAS        *
*        @SRVJES2 TO @SRVJ520. IF YOU ARE RUNNING AN ALTERNATE JES2   *
*        AT THE SAME LEVEL, ADD ITS NAME AS AN ALIAS (E.G., @SRVJESA) *
*                                                                     *
*        FOR A DIFFERENT LEVEL, ADD THE APPROPRIATE SS ID, E.G.       *
*        ALIAS @SRVJESB FOR @SRVJ530.                                 *
*                                                                     *
***********************************************************************
*                                                                     *
*        TO MAKE LIFE INTERESTING, THIS ROUTINE IS CALLED WITH THE    *
*        ENTRY POINT ADDRESS IN R14 AND THE RETURN ADDRESS IN R15     *
*                                                                     *
***********************************************************************
         SPACE 2
         PRINT &PRTSOR
         SPLEVEL SET         GET ASSEMBLY SP LEVEL               93242
@SRVJ520 PGMHEAD NOEQU,ZERO12,BASE=R12,PARM=R1,PFX=SAWE, ADDED ON 90281*
               AM=31,RM=24   CALLED BY @SERVICE IN AM31         GP99120
         AM31  WORK=R15      GET HIGH (JUST IN CASE)            GP99026
         LR    R6,R0         COPY ENTRY R0
         LR    R7,R1         COPY ENTRY R1
         STM   R0,R1,CALLR0  SAVE ENTRY CODES
         ST    R1,RETR1      PROVISIONALLY LEAVE R1 UNCHANGED    90274
         LA    R10,EXIT      POINT TO GOOD EXIT
         BIX   ERR=BADENTRY,SRL=4,PFX=,LOC=(GROUP0,GROUP1)
         SPACE 1
BADENTRY MVI   RC,16         SET RETURN CODE 16
         MVI   RV,VRPARM     INVALID PARM/ENTRY CODE
         B     EXIT          AND EXIT WITH ERROR
         SPACE 1
BADCHAR  MVI   RV,VRSYNT     SYNTAX ERROR OR BAD CHARACTERS IN PARM
BADRET8  MVI   RC,8          SET RETURN CODE 8
         B     EXIT
         SPACE 1
BADRET4  MVI   RC,4          SET RETURN CODE 4
*        B     EXIT
         SPACE 1
ENTEXIT  DS    0H            NORMAL RETURN                       90274
EXIT     PGMEXIT COPYRET=(RETCH-SAWE(R1),12),PFX=SAWE RETURN R15,R0,R1
         TITLE '@ S R V J 5 2 0  ***  ACCOUNT NUMBER PROCESSING'
GROUP1   BIX   ERR=BADENTRY,PFX=ENT,LOC=(ACGET)
         SPACE 2
*        ENTRY CODE 16 - GET ACCOUNT NUMBER OF CURRENT USER
*        RETURN R15 - ALWAYS ZERO (UNLESS R1 SUPPLIED AS ZERO)   81151
*        0(8,R1) - UPDATED WITH CURRENT ACCOUNT NUMBER (OR DEFAULT)
*        R0 - HIGH TWO BYTES - BINARY ACCOUNT EQUIVALENT         81151
*             LOW BYTE - PRIVILEGES (MAPPED BY VAA FLAGS)        81151
*
*        NOTE: LOCAL ACCOUNT NUMBERS ARE FOUR BYTES, PLUS A FOUR BYTE
*          REQUIRED SUBACCOUNT. FIRST BYTE IS ALPHANUMERIC (AT PRESENT
*          ONLY ALPHABETIC), FOLLOWED BY THREE DIGITS.           81151
*          THE SUBACCOUNT MUST BE ALPHANUMERIC.                  81151
*        A LOCAL MODIFICATION TO IEFUJV SAVES THE 8-BYTE NUMBER  81151
*          (FOR A VALID JOB) IN JMRUSEID.                        81151
*                                                                81151
*        MODE=0  GET CURRENT ACCOUNT FROM JMR / RETURN PRIVY FLAGS
*                WHEN THERE IS NO JMR, LOCATES $JCT ACCOUNT.     83254
*                SUPPLIED BY LOCAL HASPRDR CHANGE.               83254
         SPACE 1                                                 81151
ENTACGET LA    R1,0(,R7)     CLEAN HIGH BIT                      93242
         LTR   R1,R1         ANY PARM ADDRESS ?                  93242
         BZ    BADENTRY      NO; FAIL
         IC    R0,CALLMODE   GET SUB-FUNCTION                    81151
         BIX   ERR=BADENTRY,PFX=ENT,LOC=(GETAC)
ENTGETAC LA    R10,EXIT      JUST IN CASE - SET THE RETURN ADDRESS
GETACCT  LTCB  R2            GET CURRENT TCB                     86314
         USING TCB,R2                                            81137
         ICM   R2,15,TCBTCT  GET TCT                             93242
         BZ    ACCTGNON      NO TCT - START TASK ?               93242
         USING SMFTCT,R2                                         81137
         ICM   R2,15,TCTJMR  GET JMR                             93242
         BZ    ACCTGNON      NO SMF? USE DEFAULT                 93242
         USING JMR,R2
         CLI   JMRUSEID,C'A'  WAS ACCOUNT NUMBER FILLED IN ?     85021
         BNL   ACCTGGOT      PRESUMABLY - USE IT                 85021
ACCTGNON LA    R2,ACCT#DEF-(JMRUSEID-JMR)  POINT TO DEFAULT ACCOUNT
         LTCB  R15           GET TCB BACK                        83254
         ICM   R15,7,TCBJSCBB-TCB(R15)  GET JSCB                 83254
         BZ    ACCTJNON      OR SKIP                             83254
         USING IEZJSCB,R15                                       83254
         ICM   R15,15,JSCBSSIB  GET SUBSYSTEM INFORMATION BLOCK  83254
         BZ    ACCTJNON      OR OUT                              83254
         USING SSIBEGIN,R15                                      83254
         CLC   EXLOCNMS,SSIBSSNM  UNDER MASTER ?                 90287
         BE    ACCTJNON      YES; NO INFORMATION AVAILABLE       90287
         ICM   R15,15,SSIBSUSE  GET SJB ADDRESS                  83254
         BZ    ACCTJNON                                          83254
         USING SJB,R15                                           90288
         CLC   =C'SJB ',SJBID   REALLY SJB ?                     83254
         BNE   ACCTJNON      NO                                  83254
         ICM   R15,15,SJBJCT   GET $JCT                          83254
         BZ    ACCTJNON                                          83254
         USING JCT,R15                                           92287
         LA    R2,JCTUSEID-(JMRUSEID-JMR)  FAKE POINTER          92287
         CLI   JCTJOBID,C'S'   START JOB ?                       85097
         BNE   ACCTGGOT      NO; GO TO MOVER                     85097
         DROP  R15                                               83254
         SPACE 1                                                 83254
ACCTJNON OI    RV,VAASTC     SET START TASK FLAG                 83254
         MVC   0(8,R1),JMRUSEID  MOVE CANNED ACCOUNT NUMBER      82018
         B     ACCTPRIM      JOIN COMMON CODE                    82018
         SPACE 1                                                 82018
ACCTGGOT MVC   0(8,R1),JMRUSEID   SEND ACCOUNT NUMBER TO USER   GP99026
         SPACE 1
ACCTCOMM DS    0H            WAS LA R10,EXIT                     86314
ACCTPRIV NI    RV,255-VAASTC  MAKE SURE IT'S CLEAN               81215
         L     R15,PSAAOLD-PSA  GET ASCB ADDRESS                 85097
         ICM   R15,15,ASCBCSCB-ASCB(R15)  GET CSCB               85097
         BZ    ACCTPRIS      SET START TASK IF NO CSCB           85097
         USING CHAIN,R15     DECLARE IT                          85097
         CLI   CHVCD,X'0C'   MOUNT ?                             85097
         BE    ACCTPRIS      YES; TREAT AS START JOB             85097
         CLI   CHVCD,X'04'   START COMMAND ?                     85097
         BNE   ACCTPRIM      NO                                  85097
         TM    CHTRKID,CHINITID  INITIATOR ?                     85097
         BO    ACCTPRIM      YES; TREAT AS JOB                   85097
ACCTPRIS OI    RV,VAASTC     SET START TASK FLAG                 85097
ACCTPRIM DS    0H              PLANT A LABEL                     92287
         AIF   (NOT &LOCO).N1L1D                                 92321
         CLI   0(R1),C'Z'    SPECIAL ACCOUNT ?                   92297
         BE    ACCTPRSY      YES; CHECK FOR SYSTEMS PRIVILEGES   92297
         OI    RV,VAAINH     PROVISIONALLY SET IN-HOUSE          81215
         CLC   =C'G036',0(R1)    IN-HOUSE ODD-BALL ?             92297
         BE    ACCTPRCM      YES                                 82052
         CLC   =C'C90',0(R1)   OVERHEAD FOR TESTING ?            92297
         BE    ACCTPRCM      YES                                 81215
         CLC   =C'F500',0(R1)  BENCHMARKS ?                      92297
         BE    ACCTPRCM      YES                                 92297
         NI    RV,255-VAAINH                                     81215
         OI    RV,VAAUSER    ELSE SET USER                       81215
         B     ACCTPRCM      GET OUT                             92297
ACCTPRSY CLC   ACCT#SYS(3),0(R1)   SPECIAL ACCOUNT ?             81215
         BNE   ACCTPROH      NO; JUST SET OVERHEAD               81215
         CLI   3(R1),C'4'    SPECIAL RANGE ?
         BL    ACCTPROH      NO; OVERHEAD                        81215
         BE    ACCTGSYS      CHECK FOR SYSTEMS
         CLI   3(R1),C'8'
         BNH   ACCTPRSU      IN RANGE - TECHNICAL SUPPORT, ETC.  81215
         CLI   4(R1),C'0'    ANOTHER SPECIAL ?                   81215
         BE    ACCTPROH      YES; JUST SET OVERHEAD              81215
         LA    R1,4(,R1)     ACCT 0909 IS SPECIAL; REAL ACCT IN NEXT 4
         B     ACCTPRIV      TRY IT AGAIN WITH LOWER NUMBER      81215
ACCTPRSU TM    3(R1),1       905,907 ?                           86314
         BNZ   ACCTPROH      YES - JUST PLAIN OVERHEAD           86314
         OI    RV,VAASUP     SET TECHNICAL SUPPORT, ETC.         81215
         B     ACCTPROH      AND GO TO SET OVERHEAD ALSO         81215
ACCTGSYS CLC   ACCT#SUB(3),4(R1)  PROPER SUB-ACCOUNT ?           81151
         BNE   ACCTPROH      NO; SET OVERHEAD                    81215
         OI    RV,VAASYS     SET SYSTEM PRIVILEGES
ACCTPROH OI    RV,VAAOHD     SET OVERHEAD ACCOUNT                81215
.N1L1D   ANOP  ,             NO CLASSIFICATION UNLESS LOCAL      94039
ACCTPRCM BR    R10           RETURN TO CALLER                    81215
         DROP  R2
         TITLE '@ S R V J 5 2 0  ***  JES2 LOOK-UP FUNCTIONS'   GP99026
GROUP0   IC    R0,CALLFUN    RELOAD USER'S REQUEST
         BIX   ERR=BADENTRY,PFX=ENT,                                   *
               LOC=(,,,,,,,,,,,JESVC)
         SPACE 2                                                 90273
ENTJESVC LTR   R1,R7         USER PASSED PARAMETER LIST ?
         BZ    BADENTRY      NO; ERROR
         SERVINIT ,          GET THE @SERVICE ADDRESS
         MVC   EXWJ2NAM,0(R7)  MOVE SUBSYSTEM NAME
         CLI   EXWJ2NAM,C' '   SUB-SYSTEM NAME SET ?             85070
         BH    *+8           YES                                 85070
         MVC   EXWJ2NAM,EXLOCNAM  NO; MOVE DEFAULT               85070
         L     R6,CVTPTR     GET THE CVT                         85070
         USING CVTMAP,R6                                         85070
         ICM   R5,15,CVTJESCT  SUBSYSTEM VECTOR TABLE            85070
         BNP   BADRET4       MAJOR BOOBOO                        85070
         USING JESCT,R5      DECLARE IT                          85070
         CLI   EXWJ2NAM,C' '   SUBSYSTEM DEFAULTED ?             85070
         BH    EXLOCHSY      NO                                  87270
         MVC   EXWJ2NAM(L'JESPJESN),JESPJESN  COPY PRIMARY NAME  85070
         L     R4,PSATOLD-PSA  GET MY TCB BACK                   87270
         L     R4,TCBJSCB-TCB(,R4)  GET THE JSCB                 87270
         ICM   R4,15,JSCBSSIB-IEZJSCB(R4)  TEST SSIB             87270
         BZ    EXLOCHSY      LEAVE PRIMARY                       87270
         CLI   SSIBSSNM-SSIB(R4),C' '  SUBSYSTEM PRESENT ?       87270
         BNH   EXLOCHSY      NO; KEEP PRIMARY                    87270
         CLC   EXLOCNMS,SSIBSSNM-SSIB(R4)  RUNNING ALONE ?       87270
         BE    EXLOCHSY      YES; KEEP PRIMARY                   87270
         MVC   EXWJ2NAM(4),SSIBSSNM-SSIB(R4) USE STARTER'S NAME  87270
EXLOCHSY LA    R5,JESSSCT-(SSCTSCTA-SSCT)  CHEAT                 87270
         USING SSCT,R5                                           85070
EXLOCSLP ICM   R5,15,SSCTSCTA   GET NEXT SSCVT                   85070
         BNP   BADRET4       USER CHOSE BAD NAME ?               85070
         CLC   SSCTSNAM,EXWJ2NAM  DESIRED SUBSYSTEM ?            85070
         BNE   EXLOCSLP      NO                                  85070
         ICM   R4,15,SSCTSUS2  TEST FOR HCCT                    GP99026
         BNP   BADRET4       NONE ?                              85070
         L     R15,CVTASVT   GET THE ASCB VECTOR TABLE           85070
         USING ASVT,R15                                          85070
         L     R0,ASVTMAXU   GET HIGHEST VALID ASID              85070
         SLL   R0,2          *4 FOR FAST CHECK                   85070
         SLR   R1,R1         SET INDEX FOR NEXT ASID             85070
EXLOCSAD LA    R1,4(,R1)     GET NEXT ASID INDEX                 85070
         CR    R1,R0         VALID ?                             85070
         BH    BADRET4       NO; QUIT                            85070
EXLOCSOM LA    R3,ASVTFRST(R1)  POINT TO ASCB POINTER            85070
         ICM   R3,15,0(R3)   LOAD AND TEST                       85070
         BNP   EXLOCSAD      SKIP UNUSED/FREE SLOT               85070
         USING ASCB,R3                                           85070
         ICM   R2,15,ASCBJBNI  JOB CSCB ?                        85070
         BNZ   EXLOCSAD      YES; SKIP                           85070
         ICM   R2,15,ASCBCSCB  GET THE START CSCB                85070
         BZ    EXLOCSAD                                          85070
         USING CHAIN,R2      DECLARE THE CSCB                    85070
         CLI   CHVCD,X'04'   START CODE ?                        85070
         BNE   EXLOCSAD      NO                                  85070
         CLC   CHCLS,EXWJ2NAM   MATCHING ID ?                    85070
         BNE   EXLOCSAD      NO                                  85070
         LH    R0,ASCBASID   GET AS ID                           85070
         CH    R0,CHASID     JUST IN CASE                        85070
         BNE   EXLOCSAD                                          85070
         STM   R3,R4,RETR0   RETURN ASCB/$HCCT ADDRESSES        GP99026
         STM   R3,R4,EXWJ2AS# SAVE FOR SCHEDULE                  90274
         MVC   0(8,R7),EXWJ2NAM  RETURN SUB-SYSTEM NAME
         USING HCCT,R4       DECLARE THE $HCCT                   85070
*ZERO*   ICM   R2,15,CCTPIT  INITIATOR POINTER ?                 90281
*ZERO*   BZ    BADRET4       NO ?                                85070
         C     R3,CCTPOSTE+4  SAME ASCB IN $HCCT                 90281
         BNE   BADRET4       NO                                  85070
*OLD*    CLC   CCTQLOKE,EXWJ2NAM  RIGHT EXPANSION ?              90281
*OLD*    BNE   BADRET4       NO; ERROR ?                         85070
         IC    R0,CALLMODE   GET FUNCTION CODE                   90274
         BIX   ERR=BADENTRY,PFX=ENT,                                   *
               LOC=(J2INF,LOJOB,MDJOB)                           90288
EXLOCNAM DC    CL8' JES    '  INITIALIZATION PARAMETER FOR SSID  87270
EXLOCNMS DC    C'MSTR'                                           87270
         SPACE 2                                                 90274
***********************************************************************
*                                                                     *
*        ENTJ2INF LOCATES THE $HCCT AND $HCT AND RETURNS INFORMATION  *
*        NOT DEPENDENT ON JES2 MAPPINGS.                              *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90274
ENTJ2INF SERVCALL SSSET,EXWJ2NAM  SPECIFY SUBSYSTEM FOR SCHEDULE 90288
         BXH   R15,R15,BADRET4  HUH ?  SHOULD NEVER HAPPEN       90274
         USING SERVJES,R7    DECLARE USER'S PARAMETER LIST       90274
         LR    R15,R0        COPY ASCB ADDRESS                   90288
         LH    R0,ASCBASID-ASCB(,R15)  GET THE ASID FOR JES      90288
         STM   R0,R1,EXWJ2AS#   SAVE ASID AND $HCCT ADDRESS      90274
         SERVCALL SCHIN,CRXSIZE  GET CSA SPACE FOR OUR CODE      90288
         BXH   R15,R15,SCHEDERR  ERROR ?                         90288
         LR    R6,R0         COPY USER ADDRESS                   90288
         SERVCALL SCHMV,CRXCODE,CRXSIZE  INITIALIZE CSA CONTENTS 90288
         BXH   R15,R15,SCHEDERR                                  90288
         MODESET KEY=ZERO    UNLOCK CSA ACCESS                   90288
         MVC   ADDRCCT-CRXCODE(L'ADDRCCT,R6),EXWJ2CCT  PROPAGATE CCT
         MVI   FUNCODE-CRXCODE(R6),FUNINIT  REQUEST INFORMATION  90288
         MVC   COPYJES-CRXCODE(COPYINF,R6),SERVJES  COPY USER'S REQ.
         MODESET KEY=NZERO   RESTORE NORMAL KEY                  90274
         L     R1,EXWJ2AS#   GET JES2 ASID                       90274
         O     R1,=X'80000000'  REQUEST 31-BIT MODE              92297
         SERVCALL SCHED,(R1) SCHEDULE THE SRB                    90288
         MVC   SERVJES(COPYINF),COPYJES-CRXCODE(R6) RTRN USER'S REQ.
         MVC   RC,REQCODE-CRXCODE(R6)  PROPAGATE RETURN CODE     90274
         MVC   RV,REACODE-CRXCODE(R6)  PROPAGATE REASON CODE    GP02318
         BXH   R15,R15,SCHEDERR                                  90288
         B     POSTJES       RETURN VIA COMMON CLEAN-UP          90274
         EJECT ,                                                 90274
***********************************************************************
*                                                                     *
*        ENTLOJOB LOCATES A JOB AND RETURNS JOB INFORMATION IN A DATA *
*        AREA MAPPED BY SERVJES OPT=LOJOB                             *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90274
ENTMDJOB DS    0H            SHARE COMMON LOOK-UP                90288
ENTLOJOB SERVCALL SSSET,EXWJ2NAM  SPECIFY SUBSYSTEM FOR SCHEDULE 90274
         BXH   R15,R15,BADRET4  HUH ?  SHOULD NEVER HAPPEN       90274
         USING SERVJES,R7    DECLARE USER'S PARAMETER LIST       90274
         LR    R15,R0        COPY ASCB ADDRESS                   86251
         LH    R0,ASCBASID-ASCB(,R15)  GET THE ASID FOR JES      86251
         STM   R0,R1,EXWJ2AS#   SAVE ASID AND $HCCT ADDRESS      90274
         SERVCALL SCHIN,CRXSIZE  GET CSA SPACE FOR OUR CODE      86251
         BXH   R15,R15,SCHEDERR  ERROR ?                         86251
         LR    R6,R0         COPY USER ADDRESS                   86251
         SERVCALL SCHMV,CRXCODE,CRXSIZE  INITIALIZE CSA CONTENTS 86251
         BXH   R15,R15,SCHEDERR                                  86251
         SPACE 1                                                 90281
         CLC   =C'* ',SJ1JNAM  USER WANT INFORMATION ON SELF ?   90281
         BNE   LOJNOSLF      NO                                  90281
         LTCB  R2,USE=YES    GET TCB                             90281
         L     R1,TCBTIO     GET TIOT                            90281
         MVC   SJ1JNAM,0(R1) COPY TIOT JOB NAME                  90281
         MVI   SJ1QNAM,C'X'  SET FOR EXECUTION QUEUE             90281
         ICM   R2,7,TCBJSCBB GET JSCB                            90288
         BZ    LOJNOSLF      OR SKIP                             90288
         USING IEZJSCB,R2                                        90288
         ICM   R2,15,JSCBSSIB  GET SUBSYSTEM INFORMATION BLOCK   90288
         BZ    LOJNOSLF      OR OUT                              90288
         USING SSIBEGIN,R2                                       90288
         ICM   R2,15,SSIBSUSE  GET SJB ADDRESS                   90288
         BZ    LOJNOSLF                                          90288
         USING SJB,R2                                            90288
         CLC   =C'SJB ',SJBID   REALLY SJB ?                     90288
         BNE   LOJNOSLF      NO                                  90288
         MVC   SJ1JQOF,SJBJQOFF  COPY JQE OFFSET                 90288
         DROP  R2                                                90281
         SPACE 1                                                 90288
LOJNOSLF MODESET KEY=ZERO    UNLOCK CSA ACCESS                   90274
         MVC   ADDRCCT-CRXCODE(L'ADDRCCT,R6),EXWJ2CCT  PROPAGATE CCT
         MVC   COPYJES-CRXCODE(COPYLEN,R6),SERVJES  COPY USER'S REQ.
         CLI   CALLMODE,2    MDJOB ENTRY ?                       90288
         BNE   *+8           NO                                  90288
         MVI   FUNCODE-CRXCODE(R6),FUNMOD  SET MODIFY FUNCTION   90288
         MODESET KEY=NZERO   RESTORE NORMAL KEY                  90274
         L     R1,EXWJ2AS#   GET JES2 ASID                       90274
         O     R1,=X'80000000'  REQUEST 31-BIT MODE              92297
         SERVCALL SCHED,(R1) SCHEDULE THE SRB                    86251
         MVC   SERVJES(COPYLEN),COPYJES-CRXCODE(R6) RTRN USER'S REQ.
         MVC   RC,REQCODE-CRXCODE(R6)  PROPAGATE RETURN CODE     90274
         MVC   RV,REACODE-CRXCODE(R6)  PROPAGATE REASON CODE    GP02318
         BXH   R15,R15,SCHEDERR                                  86251
         B     POSTJES       RETURN VIA COMMON CLEAN-UP          90274
         SPACE 2                                                 86251
SCHEDERR WTO   'MSG881E ERROR IN SUB-SYSTEM INTERFACE',                *
               ROUTCDE=1,DESC=3  CONSIDER 1,1 ?                  89309
         LA    R10,BADENTRY  SET FOR MAJOR ERROR                 90274
POSTJES  SERVCALL SCHFR      FREE THE WORK AREA                  90274
         BR    R10           AND RETURN                          90274
         SPACE 1                                                 90274
         PRINT &PRTMAC                                           90274
SERVJES  SERVJES OPT=(J2INF,LOJOB)  USER'S CALLING PARAMETERS    90288
         PRINT &PRTSOR                                           90274
         SPACE 1                                                 86251
@SRVJ520 CSECT ,                                                 86251
         LTORG ,                                                 90274
         SPACE 3                                                 90274
***********************************************************************
*                                                                     *
*        CODE EXECUTED IN CSA (SRB MODE IN JES2 ADDRESS SPACE)        *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90274
         CRMHEAD ,           RUNS IN 31-BIT MODE                 86251
         MVI   REQCODE-CRXCODE(R13),20  SET FOR DISASTER         92321
         STOMP IV=0,ADDR=REACODE  MARK MY PLACE *****DEBUG***** GP02318
         L     R7,ADDRCCT    GET THE $HCCT                       90288
         USING HCCT,R7       DECLARE IT                         GP99026
         L     R8,CCTHCT     GET THE $HCT ADDRESS                86251
         USING HCT,R8                                            90288
         LA    R5,COPYJES    POINT TO COPIED USER DATA           90274
         USING SERVJES,R5    BUT DECLARE DSECT                   90274
         CLI   FUNCODE,FUNINIT  INFORMATION CALL ?               90288
         BNE   CRXLOJOB      NO; JOB LOCATION/MODIFICATION       90288
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
         MVC   SJSSCOM,$CCOMCHR  OS COMMAND CHARACTER            90288
         MVC   SJSSRCH,$RCOMCHR  READER COMMAND CHARACTER        90288
         MVC   SJSSTOK,CCTJSTKN  ALESERV TOKEN                  GP02327
         MVC   SJ2VERS,$VERSION  COPY $HCT VERSION               90288
         MVC   SJ2TON(L'CCTTONOD),CCTTONOD  SYSTEM NUMBER        90288
         MVC   SJ2TOQ,CCTTOQUL  SYSTEM NUMBER - EBCDIC           90288
         MVC   SJ2MRJE,CCTROUT  MAXIMUM REMOTE NUMBER            90288
         MVC   SJ2MSYS,CCTNONOD  MAXIMUM SYSTEM NBR.             90288
         ST    R8,SJ2$HCT    $HCT ADDRESS                       GP02327
         MVC   SJ2$CAT,$CATPTR                                  GP02327
         MVC   SJ2$DCT,$DCTPOOL                                 GP02327
         MVC   SJ2$JOB,$JOBQPTR                                 GP02327
         MVC   SJ2$JOT,$JOTABLE                                 GP02327
         LA    R0,$JQHEADS                                      GP02327
         ST    R2,SJ2$JQH                                       GP02327
         MVC   SJ2$QNX,$QINDEXA                                 GP02327
         B     CRXGOOD       ALL DONE ?                          92323
         SPACE 2                                                 90288
CRXLOJOB ICM   R6,15,SJ1JQOF DID USER PASS A NON-ZERO OFFSET ?   90288
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
         CLI   SJ1QNAM,C'I'  SCAN INPUT QUEUES ONLY ?            90274
         BE    CRXQINP       INPUT QUEUES ONLY                   90281
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
         CLI   SJ1QNAM,C'X'  EXECUTION ONLY ?                    90281
         BNE   HSCANALL      NO; SCAN ALL                        86251
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
CRXQINP  L     R11,$QINDEXA  GET ADDRESS OF QUEUE INDEX TABLE    86251
         SLR   R9,R9           CLEAR FOR IC                      86251
         IC    R9,X'40'(,R11)  GET FIRST INPUT-TYPE QUEUE        90287
         LA    R9,$JQHEADS(R9)  GET QUEUE HEADER ADDRESS         86251
         LA    R11,36+5      SET FOR NUMBER OF QUEUES TO SCAN    90287
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
         B     HSCANCOM      GO TO COMMON                        86251
HSCANALL LA    R9,$JQHEADS+$JQHEADL  POINT PAST DUMMY QUEUE      92324
         LA    R11,$JQTYPES-2  SET FOR NUMBER OF QUEUES EXC PURG 92321
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
HSCANCOM LM    R2,R3,SJ1JNAM  LOAD NAME TO BE FOUND              90274
         SLR   R0,R0                                             90274
         ICM   R0,3,SJ1J###  GET BINARY JOB NUMBER               90274
         CLI   SJ1JNAM,C' '  DID USER PASS A NAME ?              90274
         BH    HSQPTR        YES; PROCEED WITH SCAN LOOP         90274
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
         SLR   R3,R3         SET QUICK SIGNAL                    90274
         SPACE 1                                                 86251
HSQPTR   LTJQEP R7,0(R9)      TEST QUEUE HEAD                    86251
         BNZ   HSQNJQE       PROCESS THE JQE                     86251
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
HSQPBUMP LA    R9,$JQHEADL(,R9)   POINT TO NEXT QUEUE HEAD       92321
         BCT   R11,HSQPTR    TRY THE NEXT QUEUE                  86251
         MVI   REQCODE-CRXCODE(R13),8  SET RETURN CODE 8 - NO MATCH
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
         CRMEXIT 0           RETURN TO CALLER                    86251
         SPACE 1                                                 86251
         USING JQE,R7        DECLARE THE JQE MAPPING             90281
HSQNEXT  LTJQEP R7,JQENEXT    GET NEXT JQE                       90281
         BZ    HSQPBUMP      END OF QUEUE                        86251
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
HSQNJQE  LTR   R6,R6         WAS A JQE OFFSET PASSED ?           90288
         BZ    HSQNJQE@      NO                                  90288
         CR    R6,R7         IS THIS IT ?                        90288
         BE    HSQNJQE@      YES; USE IT                         90288
         AL    R7,$JOBQPTR   RELOCATE                            90288
         B     HSQNEXT       AND TRY AGAIN                       90288
HSQNJQE@ AL    R7,$JOBQPTR   RELOCATE                            86251
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
         LTR   R3,R3         ANY NAME TO TEST ?                  90274
         BZ    CRXNJTST      NO                                  90274
         CLM   R3,15,JQEJNAME+4  MATCHING NAME ?                 90189
         BNE   HSQNEXT       NO                                  86251
         CLM   R2,15,JQEJNAME  MATCHING NAME ?                   90189
         BNE   HSQNEXT       NO                                  86251
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
CRXNJTST LTR   R0,R0         JOB NUMBER TEST ?                   90274
         BZ    CRXNNTST      NO; DON'T TEST JOB NUMBER           90274
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
         CH    R0,JQEJOBNO   MATCHING NUMBER ?                   90274
         BNE   HSQNEXT       NO; TRY ANOTHER                     90274
CRXNNTST DS    0H                                                90274
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
         AIF   (NOT &LOCO).NOACCT1                               90274
         CLI   SJ1ACCT,C' '  DID USER PASS AN ACCOUNT ?          90274
         BNH   CRXNATST      NO                                  90274
         CLC   SJ1ACCT,JQEPACCT  SAME ACCOUNT ?                  90274
         BNE   HSQNEXT       NO; IGNORE                          86251
CRXNATST DS    0H                                                90274
.NOACCT1 CLI   SJ1QNAM,C'I'  LOOKING FOR JOB ON ANY OLD QUEUE ?  90274
         BE    CRXQINP2      NO; CHECK ON INPUT                  90281
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
         CLI   SJ1QNAM,C'X'  IN EXECUTION ?                      90281
         BNE   RETJOB        YES; RETURN WITH IT                 90274
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
         CLI   JQEBUSY,0     BUSY ?
         BE    HSQNEXT       NO; IGNORE                          90281
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
CRXQINP2 TM    JQETYPE,$XEQ  EXECUTING OR AWAITING EXECUTION ?   86251
         BZ    HSQNEXT       NO; IGNORE                          86251
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
         SPACE 1                                                 90274
***********************************************************************
*                                                                     *
*        MATCHING JOB LOCATED - RETURN DATA                           *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90274
RETJOB   CLI   FUNCODE,FUNMOD  CHANGE JQE INFORMATION ?          90288
         BNE   RETJOB2       NO                                  90288
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
         AIF   (NOT &LOCO).NOLFGS                                90288
         MVC   JQEESPF1,SJ1LFG1  UPDATE LOCAL FLAGS              92322
         MVC   JQEESPF2,SJ1LFG2    DITTO                         92322
         MVC   JQEESPF3,SJ1LFG3                                  92322
         MVC   JQEESPF4,SJ1LFG4                                  92322
.NOLFGS  MVC   LOOKDB-CRXCODE(1,R13),JQEFLAG1  GET PRIMARY FLAG BYTE
*OLD*    NI    LOOKDB-CRXCODE(R13),$SPIN LEAVE ONLY SYSTEM ID 90288
         MVC   LOOKDB+1-CRXCODE(1,R13),SJ1FLGS  USER'S REQUEST   90288
*OLD*    NI    LOOKDB+1-CRXCODE(R13),255-$SPIN GET USER'S FLAGS
         OC    LOOKDB-CRXCODE(1,R13),LOOKDB+1 MERGE              90288
         MVC   JQEFLAG1,LOOKDB  RESET                            90288
         SPACE 1                                                 90288
RETJOB2  MVC   SJ1JNAM,JQEJNAME  RETURN NAME                     90274
         STOMP ,             MARK MY PLACE *****DEBUG*****      GP02318
         ICM   R0,3,JQEJOBNO GET BINARY JOB NUMBER               90274
         STH   R0,SJ1J###    RETURN TO USER                      90274
         CVD   R0,LOOKDB-CRXCODE(R13)  MAKE PACKED               90274
         MVC   SJ1JNUM,JNUMASK  MAKE MASK                        90274
         SLR   R0,R0         CLEAR HIGH BYTES                    90288
         ICM   R1,3,JQEFLAG3 GET JOB FLAGS                       90288
         LA    R2,JOBTYPE    GET PREFIX LIST                     90288
         LA    R0,JQE3JOB    GET MASK AND MULTIPLIER             90288
         NR    R1,R0         MASK QUOTIENT                       90288
         MR    R0,R0         MAKE OFFSET INTO LIST               90288
         AR    R1,R2         GET LIST ENTRY ADDRESS              90288
         ED    SJ1JNUM+2(6),LOOKDB+5  MAKE PRETTY                90274
         MVC   SJ1JNUM(3),0(R1)  MOVE JOB/TSU/STC                90287
         MVC   SJ1PTY,JQEPRIO  CURRENT PRIORITY                  90287
         MVC   SJ1CLS,JQETYPE  COPY QUEUE TYPE                   90274
         MVC   SJ1FLGS,JQEFLAG1  AND HOLD/BUSY FLAGS             90288
         MVC   SJ1FLG2,JQEFLAG2  AND MORE FLAGS                  90288
         MVC   SJ1FLG3,JQEFLAG3  AND MORE FLAGS                  90288
         MVC   SJ1FLG4,JQEFLAG4  AND MORE FLAGS                  90288
         MVC   SJ1QUE,JQETYPE  COPY QUEUE TYPE                   90274
         LR    R15,R9        GET CURRENT QUEUE HEAD              86251
         LA    R4,$JQHEADS+$JQHEADL  POINT TO PURGE QUEUE        92321
         SR    R15,R4        GET QUEUE OFFSET                    86251
         SRL   R15,$JQHEADL/2   GET FINAGLED NUMBER              92321
         STCM  R15,3,SJ1QUE                                      90288
         LR    R15,R7        COPY JQE ADDRESS                    90288
         SL    R15,$JOBQPTR  GET JQE OFFSET                      90288
         ST    R15,SJ1JQOF   RETURN OFFSET                       90288
         TM    JQETYPE,X'40' EXEC/INPUT QUEUE ?                  90288
         BZ    RETQNAME      NO; DO NORMALLY                     90288
         LA    R1,QNAMES+4   SET FOR INPUT                       90288
         CLI   JQEBUSY,0     IN EXECUTION ?
         BE    *+8           NO                                  90288
         LA    R1,=C'XEQ'    SET FOR EXECUTION                   90288
         MVC   SJ1QNAM(3),0(R1)                                  90288
         MVC   SJ1QNAM+3(1),JQETYPE                              90288
         OI    SJ1QNAM+3,X'C0'  MAKE COMPLETE QUEUE NAME         90288
         B     RETQNAMD      DONE                                90288
RETQNAME SLR   R15,R15                                           90288
         IC    R15,JQETYPE                                       90288
         LA    R1,QNAMES     POINT TO QUEUE NAME LIST            90288
         SLL   R15,24-1      LEFT ADJUST (SAVE ONE)              90288
         LA    R0,8          TEST EIGHT BITS                     90288
         OR    R15,R8        MAKE LOW NON-ZERO                   90288
RETQNAML BXLE  R15,R15,RETQNAMV  SHIFT AND TEST NEXT BIT         90288
         LA    R1,4(,R1)     POSITION TO NEXT NAME               90288
         BCT   R0,RETQNAML   TRY AGAIN                           90288
RETQNAMV MVC   SJ1QNAM,0(R1) MOVE NAME OF QUEUE                  90288
RETQNAMD DS    0H            DONE                                90288
         AIF   (NOT &LOCO).NOACCT2                               90274
         MVC   SJ1ACCT,JQEPACCT  RETURN ACCT (USER IS AUTHORIZED)
         MVC   SJ1OCLS,JQEOCLAS  ORIGINAL JOB CLASS              90287
         MVC   SJ1OPTY,JQEOPRTY  ORIGINAL PRIORITY               90287
         MVC   SJ1LFG1,JQEESPF1  AND MORE FLAGS                  92322
         MVC   SJ1LFG2,JQEESPF2  AND MORE FLAGS                  92322
         MVC   SJ1LFG3,JQEESPF3                                  92322
         MVC   SJ1LFG4,JQEESPF4                                  92322
.NOACCT2 SPACE 1                                                 90288
CRXGOOD  MVI   REQCODE-CRXCODE(R13),0  CLEAR THE RETURN CODE     90274
         MVI   REACODE-CRXCODE(R13),0  CLEAR THE REASON CODE    GP02318
CRXDONE  CRMEXIT 0           RETURN TO CALLER                    90288
         SPACE 1                                                 90274
JOBTYPE  DC    C'STCSTCTSUJOB'  TYPE OF 'JOB'                    90288
QNAMES   DC    C'DUMYINP RDR XMITSET RCV OUT HCPYPURG'           90288
         AIF   ('&LOCAL' NE 'CCSI').NOFET                        90288
         ORG   QNAMES+16                                         90288
         DC    CL4'FET '     WYLBUR FETCH                        90288
         ORG   ,                                                 90288
.NOFET   ANOP  ,                                                 90288
         SPACE 1                                                 90274
JNUMASK  DC    X'D1D6402020202120'                               90274
         SPACE 1                                                 86251
LOOKDB   DS    D             WORK AREA                           86251
ADDRCCT  DC    A(0)          $HCCT ADDRESS                       86251
FUNCODE  DC    X'00'         FUNCTION REQUESTED                  86251
FUNGET   EQU   0               GET INFO ON A JOB                 86251
FUNINIT  EQU   4               INITIALIZE (GET CURRENT JOB)      86251
FUNMOD   EQU   8               MODIFY LOCATED JOB                90288
REQCODE  DC    X'00'         COMPLETION CODE                     86251
REACODE  DC    X'00'         REASON CODE (FOOTPRINT)            GP02318
         PRINT &PRTMAC                                           90274
COPYJES  SERVJES OPT=LOJOB,DSECT=NO,PFX=CJ                       90274
COPYLEN  EQU   *-COPYJES                                         90274
         ORG   COPYJES                                           90288
COPYINFO SERVJES OPT=J2INF,DSECT=NO,PFX=CI                       90288
COPYINF  EQU   *-COPYINFO    LENGTH OF INFORMATION SECTION       90288
         ORG   ,                                                 90288
         PRINT &PRTSYS                                           90274
         SPACE 1                                                 86251
         LTORG ,                                                 86251
         CRMEND ,            LENGTH OF CSA WORK AREA             86251
         TITLE '@ S R V J 5 2 0  ***  DATA AND WORK AREA'       GP99026
ACCT#DEF DC    C'ZZ04001S'   DEFAULT ACCOUNT FOR START JOBS      81151
ACCT#SYS DC    C'Z904'       SYSTEMS ACCOUNT                     81151
ACCT#FLP DC    C'Z909'       SPECIAL BYPASS NUMBER               81151
ACCT#SUB DC    C'0010'       DEFAULT SUBACCOUNT FOR SHORT ACCT CHECK
SAWE     DSECT ,
DB       DS    2D            WORK SPACE
@SERVICE DS    A             ADDRESS OF @SERVICE ROUTINE
RETCH    DC    0XL8'0'       RETURN VALUES
RETR15   DC    0A(0),XL3'0'                                      81165
RC       DC    X'0'          RETURN CODE
RETR0    DC    0A(0),XL3'0'                                      81165
RV       DC    X'00'         RETURN FLAGS
RETR1    DC    A(0)          ZERO OR MODIFIED R1 RETURN          81165
CALLR0   DC    A(0)          CALLER'S R0 (COPIED)
CALLMODE EQU   CALLR0+2,1,C'X'  FUNCTION MODIFIER CODE
CALLFUN  EQU   CALLR0+3,1,C'X'  FUNCTION CODE
CALLR1   DC    A(0)          CALLER'S R1 (COPIED)
EXWJ2ASC DC    F'0'          JES2 ASCB                           85070
EXWJ2NAM DC    CL8'JES2    ' JES2 START NAME+CL4                 85070
EXWJ2AS# DS    A             SUBSYSTEM ASID                      90274
EXWJ2CCT DS    A             SUBSYSTEM $HCCT                     90274
SAWEEND  EQU   *                                                GP99120
         PRINT &PRTSYS
         IHAASVT ,                                               85070
         IHASRB ,                                                85070
         IEFJESCT ,                                              85070
         IEFJSCVT ,                                              85070
CSCB     DSECT ,                                                 85070
         IEECHAIN ,                                              85070
         SPACE 1                                                 81137
         IKJTCB ,                                                81137
         SPACE 1                                                 81151
         IHACDE ,                                                81151
         SPACE 1                                                 81145
IEFTIOT1 DSECT ,                                                 81145
         IEFTIOT1 ,                                              81145
         SPACE 1                                                 81137
         IEFTCT ,                                                81137
         SPACE 1                                                 81137
         IEFJMR ,
         SPACE 1                                                 85202
*        IEZIOB ,            NO-OP - DEFINED IN $BUFFER          85202
         SPACE 1                                                 81359
         IEZJSCB ,                                               83254
         SPACE 1                                                 83100
@SRVJ520 CSECT ,                                                 83254
      $MODULE  TITLE='@ S R V J 5 2 0  ***  JES2 SERVICES',            *
               ASCB,               GENERATE MVS ASCB DSECT             *
               CVT,                GENERATE MVS CVT DSECT              *
               DCB,                GENERATE MVS DCB DSECT              *
               MLTE,               GENERATE MVS MLTE DSECT     @430P337*
               PSA,                GENERATE MVS PSA DSECT              *
               RPL,                GENERATE MVS RPL DSECT              *
               SAFP,               GENERATE MVS SAFP DSECT     @430P337*
               SDWA,               GENERATE MVS SDWA DSECT             *
               SMCA,               GENERATE MVS SMCA DSECT             *
               TOKEN,              GENERATE MVS ICHRUTKN DSECT         *
               UCB,                GENERATE MVS UCB DSECT              *
               $BUFFER,            GENERATE HASP BUFFER DSECT          *
               $CADDR,             GENERATE HASP CADDR DSECT           *
               $CAT,               GENERATE HASP CAT DSECT             *
               $CMB,               GENERATE HASP CMB DSECT             *
               $CPINDEX,           GENERATE HASP CPINDEX DSECT         *
               $DCT,               GENERATE HASP DCT DSECT             *
               $DSB,               GENERATE HASP DSB DSECT             *
               $DSSCB,             GENERATE HASP DSSCB DSECT   @430P337*
               $ERA,               GENERATE HASP ERA DSECT             *
               $ERPL,              GENERATE HASP ERPL DSECT            *
               $EST,               GENERATE HASP EST DSECT             *
               $HASB,              GENERATE HASP HASB DSECT            *
               $HASXB,             GENERATE HASP HASXB DSECT           *
               $HASPEQU,           GENERATE HASP EQUATES               *
               ($HCCT,GEN),        GENERATE HASP HCCT DSECT            *
               ($HCT,GEN),         GENERATE HASP HCT DSECT             *
               $HFAME,             GENERATE HASP HFAME DSECT           *
               $IOT,               GENERATE HASP IOT DSECT             *
               ($JCT,GEN),         GENERATE HASP JCT DSECT             *
               $JDSN,              GENERATE HASP JDSN DSECT      @520CK*
               ($JQE,GEN),         GENERATE HASP JQE DSECT             *
               $KIT,               GENERATE HASP KIT DSECT             *
               $LRC,               GENERATE HASP LRC DSECT             *
               $MIT,               GENERATE HASP MIT DSECT             *
               $MITETBL,           GENERATE HASP MTE DSECT     @430P270*
               $NHD,               GENERATE HASP NHD DSECTS            *
               $NIT,               GENERATE HASP NIT DSECT             *
               $NWR,               GENERATE HASP NWR DSECT             *
               $OCR,               GENERATE HASP OCR DSECT             *
               $OCT,               GENERATE HASP OCT DSECT             *
               $PADDR,             GENERATE HASP PADDR DSECT           *
               $PARMLST,           GENERATE HASP EXITPL DSECT          *
               $PCE,               GENERATE HASP PCE DSECT             *
               $PDDB,              GENERATE HASP PDDB DSECT            *
               $PIT,               GENERATE HASP PIT  DSECT            *
               $PSV,               GENERATE HASP PSV DSECT             *
               $QSE,               GENERATE HASP QSE DSECT             *
               $RAT,               GENERATE HASP RAT DSECT             *
               $RDRWORK,           GENERATE HASP RDRWORK DSECT         *
               $RESNAM,            GENERATE HASP RESNAM CSECT          *
               $RJCB,              GENERATE HASP RJCB DSECT    @430P062*
               $SAFINFO,           GENERATE HASP SAFINFO DSECT         *
               $SCAT,              GENERATE HASP SCAT DSECT            *
               $SCR,               GENERATE HASP SCR DSECT             *
               ($SJB,GEN),         GENERATE HASP SJB DSECT             *
               $SMF,               GENERATE HASP SMF DSECT             *
               $SQD,               GENERATE HASP SQD DSECT             *
               $TAB,               GENERATE HASP TAB DSECT             *
               $WAVE,              GENERATE HASP WAVE DSECT            *
               $WSA,               GENERATE HASP WSA DSECT             *
               $XECB,              GENERATE HASP XECB DSECT            *
               $XIT,               GENERATE HASP XIT DSECT     @420LTST*
               $XMAS,              GENERATE HASP XMAS DSECT    @OW12987*
               $XPL                GENERATE HASP XPL DSECT     @OW12987
         IEZDEB ,            FOR SP1.3.3 AND UP                  84316
         IEFJSSIB ,                                              83254
         SPACE 1                                                 83254
         END   ,                                                 84087
