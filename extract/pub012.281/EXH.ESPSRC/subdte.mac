SUBDTE   TITLE 'CURRENT DATE AND TIME FORMATTING ROUTINE'
   PUNCH ' ALIAS SUBDAT,SUBDJT '  DECLARE ENTRY POINTS
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=AYES
         SPACE 2                                                 79198
***********************************************************************
**                                                                   **
**  SUBDTE - ROUTINE TO PRODUCE CURRENT DATE AND TIME                **
**    RESULTS ARE MOVED TO THE PASSED OUTPUT LOCATIONS:              **
**                                                                   **
**  CALL SUBDTE,(@DATE,@TIME),VL                                     **
**  CALL SUBDTE,(@DATE),VL                                           **
**    RESULT DATE: YYYY-MM-DD  (ISO STANDARD)                        **
**    RESULT TIME: HH:MM:SS    (24-HOUR CLOCK)                       **
**                                                                   **
**  CALL SUBDAT,(@DATE,@TIME),VL                                     **
**    RESULT DATE: MM-DD-YY    (USA STANDARD)                        **
**                                                                   **
**  CALL SUBDJT,(@DATE,@TIME),VL                                     **
**    RESULT DATE: YYYY.DDD    (IBM STANDARD)                        **
**                                                                   **
***********************************************************************
*   MINOR (COMPLETE) REWRITE. OLD VERSION WRITTEN 68 OR SO, AND VERY
*     INELEGANTLY, AT THAT (OLD NAME GPPDAT/GPPTIM/EXORPDAT)
         SPACE 1                                                 79198
SUBDTE   PGMHEAD ZERO,ENTRY=(SUBDAT,SUBDJT),ENTNO=FLAGS,PARM=R9 GP03105
         LA    R11,DB2       POINT TO DUMMY TIME RETURN
         L     R10,0(,R9)    LOAD DATE ADDRESS
         LTR   R10,R10       ONLY PARAMETER ?
         BM    DOTIME
         L     R11,4(,R9)    LOAD ADDRESS OF TIME RETURN
         AIF   (NOT &MVSESA).OLDCODE                            GP05013
DOTIME   TIME  STCK,CLOKWORK,LINKAGE=SYSTEM,MF=(E,WORKTIME)
         LTR   R9,R15
         BNZ   PGMEXIT       RETURN ERROR
         L     R1,CVTPTR
         USING CVT,R1
         L     R1,CVTEXT2
         USING CVTXTNT2,R1
         A64D  CLOKWORK,CVTLDTO  CONVERT FROM GMT TO LOCAL
         DROP  R1
         BIX   VAL=FLAGS,LOC=(EURO,USA,JULIAN),ERR=RET20
         SPACE 1
*---------------------------------------------------------------------*
*  EURO:  RETURNS YYYY-MM-DD  TIME HH:MM:SS (24 HOUR CLOCK)           *
*---------------------------------------------------------------------*
EURO     STCKCONV STCKVAL=CLOKWORK,CONVVAL=SHOWTIME,                   *
               TIMETYPE=DEC,DATETYPE=YYYYMMDD,MF=(E,WORKCONV)
         LTR   R9,R15
         BNZ   PGMEXIT       RETURN ERROR
         MVC   WORKOUT(11),=X'F021202020602020602020'
         ED    WORKOUT(11),SHOWDATE
         MVC   0(10,R10),WORKOUT+1  RETURN TO USER
         B     FORMTIME      NOW DO THE TIME
         SPACE 1
*---------------------------------------------------------------------*
*  USA:   RETURNS MM-DD-YY    TIME HH:MM:SS (24 HOUR CLOCK)           *
*---------------------------------------------------------------------*
USA      STCKCONV STCKVAL=CLOKWORK,CONVVAL=SHOWTIME,                   *
               TIMETYPE=DEC,DATETYPE=MMDDYYYY,MF=(E,WORKCONV)
         LTR   R9,R15
         BNZ   PGMEXIT       RETURN ERROR
         MVC   WORKOUT(11),=X'F021206120206120202020'
         ED    WORKOUT(11),SHOWDATE
         MVC   0(6,R10),WORKOUT+1  RETURN MM/DD/ TO USER
         MVC   6(2,R10),WORKOUT+9  RETURN YY TO USER (NO CENTURY)
         B     FORMTIME      NOW DO THE TIME
         SPACE 1
*---------------------------------------------------------------------*
* JULIAN: RETURNS YYYY.DDD    TIME HH:MM:SS (24 HOUR CLOCK)           *
*---------------------------------------------------------------------*
JULIAN   STCKCONV STCKVAL=CLOKWORK,CONVVAL=SHOWTIME,                   *
               TIMETYPE=DEC,DATETYPE=YYYYDDD,MF=(E,WORKCONV)
         LTR   R9,R15
         BNZ   PGMEXIT       RETURN ERROR
         MVC   WORKOUT(10),=X'F021202020204B202020'
         ED    WORKOUT(10),SHOWDATE
         MVC   0(8,R10),WORKOUT+2  RETURN YYYY.DDD TO USER
*NEXT*   B     FORMTIME      NOW DO THE TIME
         AGO   .COMCODE
.OLDCODE SPACE 1                                                GP05013
DOTIME   TIME  DEC           GET TIME AND DATE PACKED           GP05013
         ST    R0,SHOWTIME
         OI    SHOWTIME+3,X'0F'  MAKE POSITIVE PLUS
         ST    R1,SHOWDATE
         MVC   INTAB,=C'-/'
         BIX   VAL=FLAGS,LOC=(EURO,USA,JULIAN),ERR=RET20
         SPACE 2
*---------------------------------------------------------------------*
*  EURO:  RETURNS YYYY-MM-DD  TIME HH:MM:SS (24 HOUR CLOCK)           *
*---------------------------------------------------------------------*
EURO     L     R1,SHOWDATE   CURRENT DATE PACKED
         L     R15,=V(SUBP2M)  USE EXISTING (OLD) FORMATTING
         BALR  R14,R15
         MVC   0(10,R10),=X'02,03,04,05,00,06,07,00,08,09'
         MVC   INTAB+2(8),0(R1)
         TR    0(10,R10),INTAB    FORMAT
         B     FORMTIME
         SPACE 1
*---------------------------------------------------------------------*
*  USA:   RETURNS MM-DD-YY    TIME HH:MM:SS (24 HOUR CLOCK)           *
*---------------------------------------------------------------------*
USA      L     R1,SHOWDATE   CURRENT DATE PACKED
         L     R15,=V(SUBP2M)  USE EXISTING (OLD) FORMATTING
         BALR  R14,R15
         MVC   0(08,R10),=X'06,07,01,08,09,01,04,05'
         MVC   INTAB+2(8),0(R1)
         TR    0(08,R10),INTAB    FORMAT
         B     FORMTIME
         SPACE 1
*---------------------------------------------------------------------*
* JULIAN: RETURNS YYYY.DDD    TIME HH:MM:SS (24 HOUR CLOCK)           *
*---------------------------------------------------------------------*
JULIAN   MVC   WORKOUT(10),=X'F0212020204B20202020'
         CLI   SHOWDATE,X'01'  ALREADY IN CC FORMAT ?
         BH    JULIAN2       YES (?)
         AP    SHOWDATE(4),=P'1900000'  MAKE CORRECT CENTURY
JULIAN2  ED    WORKOUT(10),SHOWDATE
         MVC   0(8,R10),WORKOUT+1  RETURN YYYY.DDD TO USER
.COMCODE SPACE 1
*---------------------------------------------------------------------*
* NOW FORMAT TIME: HH:MM:SS (24 HOUR CLOCK)                           *
*---------------------------------------------------------------------*
FORMTIME MVC   WORKOUT(9),=X'F021207A20207A2020'
         ED    WORKOUT(9),SHOWTIME  FORMAT TIME
         MVC   0(8,R11),WORKOUT+1   RETURN TO USER
         L     R9,RETCODE
PGMEXIT  PGMEXIT RC=(R9)
         SPACE 1
RET20    LA    R9,20         PROGRAM ERROR (?)
         B     PGMEXIT
         SPACE 1
SAVE     DSECT
DB2      DS    D
CLOKWORK DC    2D'0'         TOD / ZEROES
SHOWTIME DC    D'0'   1/2    PACKED TIME
SHOWDATE DC    2F'0'  2/2    PACKED DATE / ZERO
INTAB    DC    C'-/',CL10'YYYYMMDD'  TR WORK SPACE
RETCODE  DC    F'0'          PROGRAM RETURN CODE
WORKOUT  DS    CL16          INTERMEDIATE WORK AREA
         AIF   (NOT &MVSESA).OLDDATA
WORKTIME TIME  LINKAGE=SYSTEM,MF=L
WORKCONV STCKCONV MF=L
.OLDDATA ANOP  ,
FLAGS    DS    X'00'         ENTRY INDICATOR: 0-DTE 1-DAT 2-DJT
SAVEEND  EQU   *
         SPACE 1
         PRINT NOGEN
         CVT   DSECT=YES
         IHACDE ,
         END
