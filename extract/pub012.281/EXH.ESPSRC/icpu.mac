ICPU     TITLE 'CPU/CONNECT/TIME/DATE SUBROUTINE'
         PUNCH ' ALIAS ICON,ITIME,IDATE '
***********************************************************************
**                                                                   **
**                                                                   **
**   CPU TIME FOR THE CURRENT STEP (AS OF LAST DISPATCH)             **
**                                                                   **
**       I=ICPU(X)           WHERE X IS A DUMMY PARAMETER            **
**             RESULT IS IN 1/1000 SECOND (NOT 1/100)                **
**                                                                   **
**   CONNECT (STEP RESIDENCE) TIME:                                  **
**       I=ICON(X)           WHERE X IS A DUMMY PARAMETER            **
**             RESULT IS CONNECT/RESIDENCE TIME FOR STEP IN 1/100 SECS.
**                                                                   **
**   TIME OF DAY                                                     **
**       I=ITIME(X)          WHERE X IS A DUMMY PARAMETER            **
**             RESULT IS TIME OF DAY IN 1/100 SECONDS                **
**                                                                   **
**   JULIAN DATE                                                     **
**       I=IDATE(X)          WHERE X IS A DUMMY PARAMETER            **
**             RESULT IS DATE IN THE FORM YYYYDDD               GP03146
**                                                                   **
***********************************************************************
**  MAINTENANCE                                                      **
**  2003-05-26  GYP  FIXED Y2K; MADE RE-ENTRANT AND REFRESHABLE      **
**                                                                   **
**                                                                   **
***********************************************************************
         SPACE 2
         PRINT NOGEN
ICPU     START 0                                        ADDED ON 85198
         STM   R14,R12,12(R13)
         USING ICPU,R15
         L     R7,PSAAOLD-PSA   GET CURRENT ASCB
         USING ASCB,R7       DECLARE IT
         LM    R0,R1,ASCBEJST  GET JOB-STEP CPU TIME (TCB TIME)
         SRDL  R0,22         CONVERT TO 1/1000THS
         LR    R0,R1         PLACE IN RETURN REGISTER
         B     EXIT          AND RETURN
         SPACE 2
         ENTRY ICON
ICON     STM   R14,R4,12(R13)
         LR    R12,R15       LOAD BASE
         USING ICON,R12      PASSED BY CALLER
         TIME  BIN           GET CURRENT TIME IN R0
         L     R1,PSATOLD-PSA  GET OUR TCB
         USING TCB,R1
         ICM   R1,15,TCBTCT  ANY TCT ?
         BZ    EXIT          NO; RETURN TOD
         USING SMFTCT,R1
         S     R0,TCTPPST    GET START TIME
         BP    EXIT          OK ?
         A     R0,DAY        ADD A DAY
         B     EXIT
         SPACE 2
         ENTRY ITIME
ITIME    STM   R14,R12,12(R13)
         LR    R12,R15       LOAD BASE
         USING ITIME,R12
         TIME  BIN
         LR    R0,R1
         B     EXIT
         SPACE 2
         ENTRY IDATE
IDATE    STM   R14,R12,12(R13)
         LR    R12,R15       LOAD BASE
         USING IDATE,R12
         LA    R4,20+R6*4(,R13)  POINT INTO SAVE AREA           GP03146
         SRL   R4,3          CONVERT TO DOUBLE-WORD             GP03146
         SLL   R4,3          ADDRESS NOW ON DOUBLE WORD BOUNDARY
         XC    0(4,R4),0(R4)  CLEAR HIGH PORTION                GP03146
         TIME  ,             GET THE DATE
         ST    R1,4(,R4)     STASH IT                           GP03146
         AP    4(4,R4),=P'1900000'  CHANGE 01 TO 20             GP03146
         CVB   R0,0(,R4)     CONVERT TO INTEGER YYYYDDD         GP03146
         SPACE 2
*   COMMON EXIT - NO ADDRESSABILITY
*   RESULT RETURNED IN R0
EXIT     L     R14,12(,R13)  RELOAD RETURN REGISTER
         SLR   R15,R15       CLEAR ERROR INDICATOR
         LM    R1,R4,24(R13)  RELOAD SOME MORE
         BR    R14           RETURN
         SPACE 2
DAY      DC    A(24*60*60*100)  ONE DAY IN 1/100THS SECONDS
         SPACE 1
         IHAPSA ,
         IHAASCB ,
         IEFTCT ,
         IKJTCB ,
         REGEQU ,
         END
