POD      TITLE 'LIST ALL MEMBERS OF A PARTITIONED DADA SET'
         COPY  OPTIONGB                                         GP12049
         SPACE 1
         SYSPARM LIST=YES                                       GP12049
         SPACE 1
         PUNCH ' ORDER POD(P)'      MAKE DEBUGGING EASIER       GP02266
         SPACE 1
***********************************************************************
**                                                                   **
**  MODULE LISTPDS WRITTEN BY SEYMOUR (SHMUEL) METZ CIRCA 1969.      **
**  USE LIMITED AS DESCRIBED IN MEMBER $$RIGHTS IN THIS LIBRARY      **
**  I CHANGED NAME TO POD TO AVOID CONFLICT WITH THE LISTPDS BY      **
**  GENE CZARCINSKI (GODDARD SPACE FLIGHT CENTER) FROM CBT 429/F316  **
**                                                                   **
**  COPYRIGHT 1969 SEYMOUR J. METZ                                   **
**                                                                   **
**  ADDITIONAL CHANGES CPR 2002 GERHARD POSTPISCHIL                  **
**                                                                   **
***********************************************************************
**                                                                   **
**  THIS PROGRAM LISTS PARTITIONED DATA SETS FOR A SOURCE LIBRARY    **
**  (FIXED RECORD FORMAT WITH RECORD LENGTH 80) (SYSUT1 DD)          **
**  UPGRADED TO READ ANYTHING OTHER THAN SPANNED - PRINTS <81 CHARS  **
**                                                                   **
**  WHEN ORIGINALLY DEVELOPED, RUNNING ON SLOW(ER) S/360 MACHINES,   **
**  WE GOT ANTSY AND WANTED TO KNOW HOW FAR THE PROCESSING HAD COME. **
**  THE CSCB/CIB INTERFACE PERMITS USE OF 'MODIFY jobname,xxx' TO    **
**  GET THE NAME OF THE NEXT MEMBER BEING PROCESSED. WITH MODERN     **
**  MACHINES THIS IS DEPRECATED.                                     **
**                                                                   **
**  ENTRY WTORPOD SERVES A SIMILAR PURPOSE, BUT USES A WTOR INSTEAD. **
**                                                                   **
**  OUTPUT ON DD SYSPRINT (VARIABLE, ASA, REC.LEN=133)               **
**                                                                   **
**  PARM=name MAY BE USED TO BEGIN PROCESSING AT OR AFTER THE        **
**  SPECIFIED NAME.                                                  **
**                                                                   **
***********************************************************************
         SPACE 1
*        (0)   SCRATCH
*        (1)   SCRATCH
*        (2)   BASE
*        (3)   DIRECTORY ENTRY
*        (4)   DIRECTORY END
*        (5)   PSDCB BUFFER A
*        (6)   CONSTANT INCREMENT OF 80
*        (7)   COMPARAND FOR BXLE OF END PSDCB BUFFER A
*        (8)   PSDCB BUFFER B
*        (9)   LINES REMAINING ON PAGE
*        (10)  RETURN/SCRATCH
         SPACE 2
*              MESSAGE IDS:
*              POD0XX                   STATUS
*              POD1XX                   INSTRUCTIONS
*              POD2XX                   NORMAL TERMINATION
*              POD3XX                   ERROR
*---------------------------------------------------------------------*
*                                                                     *
*  ORIGINAL VERSION CLEANED UP FOR USE UNDER OS/390 AND Z/900         *
*  RUNS IN AMODE 31, RMODE 24                                         *
*  ABSOLUTE REGISTERS REPLACED BY SYMBOLICS                           *
*  ENTRY/EXIT MACROS USED                                             *
*  TEST FOR MVT RELEASE 20 REMOVED                                    *
*  SPELLING ERROR CORRECTED                                           *
*  DEVICE TYPE LOOKUP CHANGED TO USE MACRO                            *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         PRINT NOGEN
POD      PGMHEAD ZERO12,BASE=R12,AM=31,RM=24,PARM=R9,           GP02266*
               ENTRY=(WTORPOD),ENTNO=FLAGS
         LTR   R1,R9                    PARM OMITTED?           GP02266
         BZ    FRESH                     YES - START FROM SCRATCH
         L     R1,0(,R1)
         LH    R14,0(,R1)
         LTR   R14,R14                  IS THE PARM NULL?
         BNP   FRESH                     YES - SNUB IT          GP02266
         CH    R14,=H'8'                PARM TOO BIG?
         BH    BADPARM                   YES
*              AT THIS POINT, AN EIGHT CHARACTER MEMBER NAME IS SAVED
*              IT SHOULD BE THE LAST MEMBER PREVIOUSLY PRINTED
         MVI   TRIGGER,C' '       RENAMED FROM 'NAME'           GP12049
         MVC   TRIGGER+1(7),TRIGGER                             GP12049
         BCTR  R14,0                    JAIL IS TOO GOOD FOR HIM
         EX    R14,SAVEPARM              EXECUTE HIM
         MVC   RESTNAME,TRIGGER         COPY                    GP12049
         WTO   MF=(E,RESTINFO)          INFORM USER             GP12049
         B     FRESH                                            GP12049
RESTINFO WTO   'POD123I RESTARTING FROM MEMBER XXXXXXXX',MF=L   GP12049
RESTNAME EQU   *-8,8,C'C'                                       GP12049
         EJECT
*              IGNORE THE FOLLOWING FOMA.
*              THE ONLY REASON FOR THE WHOLE MUGILLAH WITH RDJFCB IS
*              TO READ THE DIRECTORY WITHOUT CLOBBERING THE DCB INFO
*              ALL I DO TO THE JFCB IS KEEP IT FROM BEING REWRITTEN
FRESH    RDJFCB MF=(E,OP1)
         MVC   DSN,JFCBDSNM             MOVE NAME TO TITLE
         OI    JFCBTSDM,X'0A'           HANDS OFF MY JOB QUEUE
         TM    FLAGS,FGWTOR  WTOR REQUESTED ?                   GP02266
         BZ    SKIPWTOR      NO; SKIP OVER                      GP02266
         XC    ECB,ECB
         WTOR  'POD100E REPLY ''STOP'' TO TERMINATE LISTING',          $
               REPLY,5,ECB,ROUTCDE=11,DESC=7
         SPACE 1
SKIPWTOR OPEN  MF=(E,OP1),TYPE=J
         OPEN  (PSDCB,INPUT)
         SR    R14,R14                                          GP12049
         IC    R14,PSDCB+DCBRECFM-IHADCB     GET INPUT RECFM    GP12049
         SRL   R14,6                    SHIFT OUT B,S, ETC.     GP12049
         ST    R14,RECFIX               STASH IT                GP12049
         TR    RECFIX+L'RECFIX-1(1),=X'08040008'   0,V,F,U      GP12049
         GETBUF PSDCB,R5
         GETBUF PSDCB,R8
         OPEN  (SYSPRINT,OUTPUT)
*              SEPARATE OPENS TAKE LONGER,
*              BUT THEY REDUCE THE SIZE OF THE OPEN WORK AREA.
*              SINCE THE DESIGN GOAL IS 8K ...
         TIME  DEC                      GET TIME AND DATE
         STM   R0,R1,TIME
         MVC   HOUR-1(9),TIMEMASK       HH:MM:SS
         ED    HOUR-1(9),TIME
         UNPK  YEAR(3),DATE+1(2)
         MVI   YEAR+2,C' '
         TM    DATE+1,1                 IS YEAR ODD?
         BO    *+16                      NO - THAT'S ODD, NO LEAP
         TM    DATE+1,X'12'             IS YEAR 2, 10,?
         BM    *+8                      YES - NOT 0, 12 - NOT LEAP
         MVI   FEB,29                   LEEP, LEEP, LEEP
*              WHO CARES ABOUT 2100 AD?
         ZAP   DATE(2),=P'1'            SET DATE TO JAN
         LA    R14,JAN
         ZAP   CROSS,DATE+2(2)          SET (0) TO DAY OF YEAR
         CVB   R0,CROSS
         SR    R1,R1                    SET FOR IC
JULIAN   IC    R1,0(,R14)               DAYS IN MONTH
         CR    R0,R1                    IS IT THIS MONTH
         BNH   GREGORY                  SCHAHERAZADE
         LA    R14,1(,R14)
         SR    R0,R1
         AP    DATE(2),=P'1'
         B     JULIAN
GREGORY  CVD   R0,CROSS
         UNPK  DAY,CROSS
         OI    DAY+1,C'0'
         UNPK  MON,DATE(2)
         OI    MON+1,C'0'
         IC    R2,PSDCB+DCBDEVT-IHADCB  GET THE DCB DEVICE TYPE GP02266
         N     R2,=X'0000001F'          KILL THE 20 BIT         GP02266
         LM    R15,R1,DEVBXLE           LOAD THE ADDRESSES      GP02266
TESTDT   CLM   R2,1,1(R15)              MATCHES ?               GP02266
         BE    FOUNDDT                   YES - MOVE IN GROUP NAME
         BXLE  R15,R0,TESTDT            TRY NEXT DEVICE TYPE    GP02266
*              IF NOT FOUND, 'DASD' WILL BE PRINTED AS DEVICE TYPE
FOUNDDT  MVC   DEVTP,2(R15)                                     GP02266
         MVC   VOLSER,JFCBVOLS          PUT THE CEREAL IN THE BOWL
CVTPTR   EQU   16
*MVT*    LA    R6,80
*MVT*    L     R1,CVTPTR
*MVT*    SR    R1,R6                    CVT-80
*MVT*    CLI   76(R1),C'2'              IS RELEASE EARLIER THAN 20.0?
*MVT*    BL    READIR
*MVT*    L     R1,80(,R1)               CVTTCBP
         L     R1,PSATOLD-PSA           CURRENT TCB             GP12049
         PUSH  USING                                            GP12049
         USING TCB,R1
         L     R1,TCBJSCB
         USING IEZJSCB,R1
         L     R7,JSCBCSCB
         USING CHAIN,R7
         AIF   (NOT &MVSXA).NOCSCX                              GP12049
         L     R2,CHCSCXP                                       GP02266
         USING CSCX,R2                                          GP02266
.NOCSCX  ST    R7,CSCBPTR
         QEDIT ORIGIN=CHCIBP            DELETE START CIB, IF ANY
         QEDIT ORIGIN=CHCIBP,CIBCTR=1   ALLOW 1 MODIFY AT A TIME
         POP   USING                                            GP12049
         EJECT
         USING DIRECTRY,R3
READIR   GET   PODCB                    READ A DIRECTORY BLOCK
         LR    R3,R1
         LH    R4,BLKLEN                FIND END OF DIRECTORY BLOCK
         AR    R4,R3
         LA    R3,MEMNAME
         USING MEMNAME,R3
USEDIR   CLI   MEMNAME,X'FF'            IS IT END OF DIRECTORY
         BE    ENDIR                     YES - TREAT AS EOF
         CLI   TRIGGER,0                ARE WE IN SKIP MODE
         BNE   SCAN
         PUSH  USING                                            GP12049
ENDSCAN  XC    TRIGGER,TRIGGER          PRINT TRIGGER AFTER MISSING
         L     R7,CSCBPTR
         USING CHAIN,R7
         LTR   R7,R7                    RELEASE 20 OR LATER?
         BZ    NOT20
         AIF   (NOT &MVSXA).NOCSCX2                             GP12049
         L     R2,CHCSCXP                                       GP02266
         USING CSCX,R2                                          GP02266
.NOCSCX2 TM    CHECB,X'40'              IS OPERATOR TALKING?
         BZ    NOT20                     NO STOP, NO MODIFY
         CLI   CHECB,X'50'              WAS STOP POSTED?
         BE    KISSOFF                   YES - IGNORE POSSIBLE MODIFY
         L     R10,CHCIBP
         USING CIB,R10
*              MOMMY, WHAT'S REDUNDANT MEAN?
*              SHUT UP AND PLAY SAFE.
         CLI   CIBVERB,CIBSTOP          IS OPERATOR UP-TIGHT?
         BE    KISSOFF                   YES - GET OFF
         SPACE 1
         MVC   WHEREAMI+12(8),CHKEY
         MVC   WHEREAMI+21(8),MEMNAME
         SR    R0,R0                    SET UP UCMID FOR WTO
         IC    R0,CIBCONID
         WTO   MF=(E,WHEREAMI)
         QEDIT ORIGIN=CHCIBP,BLOCK=(R10) DELETE MODIFY CIB
         POP   USING                                            GP12049
NOT20    TM    ECB,X'40'                IS OPERATOR UP-TIGHT?
         BO    KISSOFF                   YES - GET OFF
         MVC   MEMDES,MEMNAME           SET UP PAGE HEADER
         MVC   KSSI(12),=CL12'NO SSI'
         TM    ENTLEN,X'1F'-1           MUST BE AT LEAST 2 HALFWORDS
         BZ    NOSSI
*              BITS 1-2 SHOULD BE ZERO
*              I DON'T HANDLE USER TTR'S
         MVC   KSSI,=C'SSI='
         UNPK  SSI(9),USERDATA(5)       CONVERT SSI TO HEX
         NC    SSI,=X'0F0F0F0F0F0F0F0F'
         TR    SSI,=C'0123456789ABCDEF'
         MVI   SSI+8,C' '
NOSSI    MVC   FINDWORD(3),MEMTTR       SET UP FOR FIND
         FIND  PSDCB,FINDWORD,C
         LA    R9,1                     FORCE PAGE EJECT FIRST TIME
         ZAP   MEMPNUM,=P'0'
         READ  DECB,SF,,(R5),'S',MF=E    PRIME BUFFERS
NEWBLOCK L     R5,DECAREA               SWAP BUFFERS
         CHECK DECB
         NOTE  PSDCB
         ST    R1,CROSS
         UNPK  TT(5),CROSS(3)
         NC    TT,=X'0F0F0F0F'
         TR    TT,=C'0123456789ABCDEF'
         MVI   TT+4,C'.'
         UNPK  R(3),CROSS+2(2)
         NC    R,=X'0F0F'
         TR    R,=C'0123456789ABCDEF'
         MVI   R+2,C'.'
         ZAP   LINENO,=P'1'
         LH    R7,PSDCB+DCBBLKSI-IHADCB FIND BLOCK END FOR BXLE
         L     R1,DECIOBPT
         USING IOBSTDRD,R1
         SH    R7,IOBCSW+5
         AR    R7,R5
         BCTR  R7,0
         READ  DECB,SF,,(8),'S',MF=E
         LR    R8,R5                    SAVE BUFFER START LOC FOR SWAP
         CLI   RECFIX+L'RECFIX-1,4      VARIABLE ?              GP12049
         BNE   NEXTCARD                   NO                    GP12049
         LA    R5,4(,R5)                  YES; SKIP BDW         GP12049
NEXTCARD BCT   R9,NEXTLINE              WILL CARD FIT ON PAGE?
         LA    R9,50                    RESET LINE COUNT
         AP    MEMPNUM,=P'1'            PROCESS PAGE NUMBERS
         AP    TOTPNUM,=P'1'
         MVC   TOTPAGE+1(5),P5MASK      EDIT TOTAL PAGE NUMBER
         ED    TOTPAGE,TOTPNUM
         MVC   MEMPAGE+1(3),P5MASK+2    EDIT MEMBER PAGE NUMBER
         ED    MEMPAGE,MEMPNUM
         PUT   SYSPRINT,HEAD
         PUT   SYSPRINT,COLHEAD1
         PUT   SYSPRINT,COLHEAD2
         MVI   DATA+4,C'-'
NEXTLINE L     R1,RECFIX                GET RECORD FORMAT BR    GP12049
         LH    R6,DCBLRECL-IHADCB+PSDCB LENGTH FROM DCB         GP12049
         LR    R14,R6        LENGTH TO MOVE                     GP12049
         LR    R15,R5                   SET SOURCE ADDRESS      GP12049
         B     *+4(R1)                    BRANCH BY RECFM       GP12049
           B   NEXTMOVE                     FIXED - LEAST WORK  GP12049
           B   NEXTVAR                      VARIABLE - UNSPAN'D GP12049
NEXTUND  LA    R6,1(,R7)     GET TRUE END                       GP12049
         SR    R6,R5         UNDEFINED : RECORD = BLOCK LENGTH  GP12049
         LR    R14,R6        LENGTH TO MOVE                     GP12049
         B     NEXTMOVE        JUST MOVE                        GP12049
         SPACE 1
NEXTVAR  ICM   R6,3,0(R5)    LOAD RECORD LENGTH                 GP12049
         LR    R14,R6                                           GP12049
         SH    R14,=H'4'     LENGTH TO MOVE                     GP12049
         LA    R15,4(,R5)    SOURCE ADDRESS                     GP12049
*NEXT*   B     NEXTMOVE                                         GP12049
         SPACE 1
NEXTMOVE MVC   CARD,CARD-1   VARIABLE - CLEAR OUTPUT LINE       GP12049
         MINH  R14,=AL2(L'CARD)   MAX TO MOVE                   GP12049
         SH    R14,=H'1'     FOR EXECUTE                        GP12049
         BM    SKIPMOVE                                         GP12049
         EX    R14,EXMVCARD  MOVE TEXT                          GP12049
SKIPMOVE UNPK  LINENUM,LINENO
         AP    LINENO,=P'1'
         OI    LINENUM+2,C'0'
         PUT   SYSPRINT,DATA
         MVI   DATA+4,C' '
         BXLE  R5,R6,NEXTCARD
         B     NEWBLOCK
EXMVCARD MVC   CARD(0),0(R15)           MOVE TEXT               GP12049
         SPACE 1
SCAN     CLC   MEMNAME,TRIGGER          HAVE WE FOUND THE 0TH MEMBER?
         BL    ENDMEM                    IF AT FIRST YOU DON'T SUCCEED
         BH    ENDSCAN                  IF 0TH MISSING, RECOVER
         XC    TRIGGER,TRIGGER          START AT NEXT MEMBER
ENDMEM   IC    R1,ENTLEN                GET LENGTH OF USER DATA
*                                        IN HALFWORDS
         N     R1,=F'31'                MASK OUT FLAGS
         SLA   R1,1                     CONVERT LENGTH TO BYTES
         LA    R3,USERDATA(R1)          FIND NEXT ENTRY
         CR    R3,R4                    TEST EOB
         BL    USEDIR
         B     READIR
*              GET THY BEAK FROM OUT MY HEART,
*              AND GET THY FORM FROM OUT MY DOOR.
KISSOFF  PUT   SYSPRINT,TDMMDI
ENDIR    CLOSE (PODCB,,PSDCB,,SYSPRINT)  ENOUGH MEMORY IN MVS?  GP12049
         PGMEXIT RC=0
         SPACE 2
BPAMSIN  SYNADAF ACSMETH=BPAM
         B     SIN
BSAMSIN  SYNADAF ACSMETH=BSAM
         B     SIN
QSAMSIN  SYNADAF ACSMETH=QSAM
SIN      MVC   TSBUF,=CL22' '           ASSUME NO BUFFER ADDRESS
         ST    R0,SYNADCOD
         ST    R1,SYNADBUF
         LTR   R0,R0                    SUCCESSFUL?
         BZ    TSMNOBUF                  YES
         SH    R0,=H'8'                  MAYBE
         BL    TSBUFF                    YES
         MVC   TSBUF,=CL22'SYNADF R0=8'
         B     TSMNOBUF
TSBUFF   MVC   TSBUF,=CL22'BUF=XXXXXX, SIZE=XXXX.'
         UNPK  TSBUF+4(7),9(4,1)        MOVE IN BUFFER LOC
         NC    TSBUF+4(6),=X'0F0F0F0F0F0F'
         TR    TSBUF+4(6),=C'0123456789ABCDEF'
         MVI   TSBUF+10,C','
         UNPK  TSBUF+17(5),12(3,1)      MOVE IN BUFFER SIZE
         NC    TSBUF+17(4),=X'0F0F0F0F0F0F'
         TR    TSBUF+17(4),=C'0123456789ABCDEF'
         MVI   TSBUF+21,C'.'
TSMNOBUF MVC   TSERRMSG,50(1)           MOVE SYNADF BUFFER TO MSG
         WTO   MF=(E,TS)
         SPACE 2
*              THIS FEATURE WAS NEVER OFFICIALLY AVAILABLE
*              IT WAS NEVER IN MVT
*              THE ADVENT OF ROUTCDE=11 HAS PROBABLY KILLED IT FOR GOOD
         L     R1,=X'81001000'          DEAR ABBY
         LA    R0,TS                    I HAVE A MESSAGE FOR YOU
         SVC   R13                      GOODBY
SYNADBUF DS    F
SYNADCOD DS    F
TS       DC    Y(TSEND-TS),X'8000'
         DC    C'POD301I I/O ERROR'
TSERRMSG DC    CL78' '
TSBUF    DC    CL22' '
TSEND    DC    XL2'0200',XL2'0060'
         SPACE 1
WHEREAMI WTO   'POD003I JJJJJJJJ XXXXXXXX NEXT MEMBER',MCSFLAG=REG0,   *
               MF=L
         SPACE 1
BADPARM  WTO   'POD300I BAD PARM',ROUTCDE=11,DESC=7
*OLD*    ABEND 4,DUMP,,BADPARM+4        DAY: EVENING
TAG      ABEND 4,DUMP  BADPARM+4        DAY: EVENING            GP02266
CROSS    DS    D
         SPACE 1
TIME     DS    F                        MUST BE FOLLOWED BY DATE
DATE     DS    F                        MUST BE PRECEEDED BY TIME
         SPACE 1
ECB      DC    F'0'
REPLY    DS    CL5
DECB     DS    0F
DECSDECB DC    F'0'                     ECB
DECTYPE  DC    X'8080'
*              FIRST BYTE MEANS LENGTH 'S'
*              SECOND BYTE MEANS READ SF
DECLNGTH DC    H'0'
DECDCBAD DC    A(PSDCB)                 DCB ADDRESS
DECAREA  DC    A(0)                     BUFFER
DECIOBPT DC    A(0)                     IOB
         SPACE 2
TDMMDI   DC    Y(GTBFOMH-TDMMDI,0)
         DC    C'POD200I THE DEMON MADE ME DO IT.'
GTBFOMH  EQU   *
HEAD     DC    H'137',H'0',CL133'1'
         ORG   HEAD+5
         DC    C'PAGE'
MEMPAGE  DC    C' 123'       THREE DIGIT MEMBER PAGE NUMBER
         DC    C' FOR MEMBER '
MEMDES   DC    CL8' '
         ORG   HEAD+35
KSSI     DC    C'SSI='
SSI      DC    C'LLFFSSSS'
         ORG   HEAD+51
         DC    C'DSN='
DSN      DC    CL44' '
         ORG   HEAD+104
MON      DC    CL2' '
         DC    C'/'
DAY      DC    CL2' '
         DC    C'/'
YEAR     DC    CL2' '
         DC    CL3' '
HOUR     DC    CL2' '
         DC    C':'
MIN      DC    CL2' '
         DC    C':'
SEC      DC    CL2' '
         ORG   HEAD+125
         DC    C'PAGE'
TOTPAGE  DC    C' 12345'     5 DIGIT LISTING PAGE NUMBER
         ORG
COLHEAD1 DC    Y(ENDCH1-COLHEAD1),H'0',C'0TTTT.RR.### '
         DC    CL5' '
         DC    C'....:....'
         DC    C'1'
         DC    C'....:....'
         DC    C'2'
         DC    C'....:....'
         DC    C'3'
         DC    C'....:....'
         DC    C'4'
         DC    C'....:....'
         DC    C'5'
         DC    C'....:....'
         DC    C'6'
         DC    C'....:....'
         DC    C'7'
         DC    C'....:....'
         DC    C'8'
         DC    CL5' '
         DC    C'VOL='
DEVTP    DC    C'    '
         DC    C'='
VOLSER   DC    CL6' '
ENDCH1   EQU   *
         SPACE 1
COLHEAD2 DC    Y(ENDCH2-COLHEAD2),H'0',CL13' '
         DC    CL5' '
         DC    8C'1234567890'
ENDCH2   EQU   *
DATA     DC    Y(ENDATA-DATA),H'0',C' '
TT       DC    C'TTTT'
         DC    C'.'
R        DC    C'RR'
         DC    C'.'
LINENUM  DC    C'001'
         DC    C' '
         DC    CL5' '
CARD     DC    CL80' '
ENDATA   EQU   *
LINENO   DC    PL2'0'
MEMPNUM  DC    PL2'0'
TOTPNUM  DC    PL3'0'
PCI      EQU   32
OP1      OPEN  (PODCB,INPUT),MF=L
SAVEPARM MVC   TRIGGER(0),2(1)          REMOTE MOVE FOR PARM
NOPR     NOPR  0
FINDWORD DC    XL3'0',X'0',X'0',X'0'
TIMEMASK DC    C' ',X'2120',C':',X'2020',C':',X'2020'
P5MASK   DC    X'2020212020'            ALSO USED FOR 3 DIGIT MASK
         SPACE 1
JAN      DC    AL1(31)
FEB      DC    AL1(28)
MAR      DC    AL1(31)
APR      DC    AL1(30)
MAY      DC    AL1(31)
JUN      DC    AL1(30)
JUL      DC    AL1(31)
AUG      DC    AL1(31)
SEP      DC    AL1(30)
OCT      DC    AL1(31)
NOV      DC    AL1(30)
DEC      DC    AL1(31)
         SPACE 1
DEVTAB   UNITY FORMAT=24,DEVD=DA  EXPAND DASD UCBTBYT3/4 AND NAME
DEVTABND DC    X'FFFF',CL4'DASD'                                GP02266
DEVBXLE  DC    A(DEVTAB,6,DEVTABND)                             GP02266
         SPACE 1
SAVE     DSECT ,             SAVE AND WORK AREA
         SERVDEFS ,          FOR @SERVICE & DEBUGGING           GP12049
RECFIX   DS    F             INDEX FOR RECFM TYPE PROCESSING    GP12049
TRIGGER  DS    CL8           RESTART MEMBER OR X'00'            GP12049
CSCBPTR  DC    A(0)          CSCB PTR IF OS/360 R20.0 OR LATER
FLAGS    DC    X'00'         PROCESSING FLAGS                   GP02266
FGWTOR   EQU   X'01'           DO WTOR - SET BY PGMHEAD ENTNO   GP02266
SAVEEND  EQU   *             END OF WORK AREA
POD      CSECT ,             BACK TO NORMAL                     GP02266
         TITLE 'DATA CONTROL BLOCK AND DCB EXIT FOR SYSUT1 DIRECTORY'
PODCB    DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GL,                        *
               SYNAD=QSAMSIN,                                          *
               RECFM=F,LRECL=256,BLKSIZE=256,KEYLEN=8,BUFNO=1,         *
               EODAD=ENDIR,EXLST=DIREX
DIREX    DC    0A(0),X'87',AL3(INFMJFCB)
         SPACE 5
         TITLE 'DATA CONTROL BLOCK AND DCB EXIT FOR SYSUT1'
PSDCB    DCB   DDNAME=SYSUT1,DSORG=PO,MACRF=R,                         *
               SYNAD=BPAMSIN,BUFNO=2,NCP=1,                            *
               EODAD=ENDMEM,EXLST=MEMEX                         GP12049
*OBSOLETE*     RECFM=FB,LRECL=80                                GP12049
MEMEX    DC    0A(0),X'85',AL3(DCBMEMEX)
         SPACE 1
         PUSH  USING                                            GP02266
         DROP  ,                                                GP02266
         USING DCBMEMEX,R15                                     GP02266
         USING IHADCB,R1
DCBMEMEX N     R1,=X'00FFFFFF'     KILL HIGH BITS IN CASE AM31
         NI    DCBOPTCD,255-PCI
         OC    DCBBLKSI,DCBBLKSI
         BCR   7,R14
         MVC   DCBBLKSI,DCBLRECL
         BR    R14
         POP   USING                                            GP02266
         TITLE 'DATA CONTROL BLOCK AND DCB EXIT FOR SYSPRINT'
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,                      *
               SYNAD=QSAMSIN,                                          *
               RECFM=VBA,LRECL=137,OPTCD=WU,EXLST=SEX
SEX      DC    0A(0),X'85',AL3(DCBSEX)
         SPACE 1
         PUSH  USING                                            GP02266
         DROP  ,                                                GP02266
         USING DCBSEX,R15                                       GP02266
         USING IHADCB,R1
DCBSEX   STM   R14,R12,12(R13)
         N     R1,=X'00FFFFFF'     KILL HIGH BITS IN CASE AM31
         LH    R0,DCBBLKSI
         LTR   R0,R0
         BNZ   *+28          SUPPLIED BLOCKSIZE, DON'T TOUCH
         LH    R0,DCBLRECL
         CLI   DCBDEVT,X'40'            UNIT RECORD OUTPUT DEVICE?
         BE    *+8                       YES - LEAVE UNBLOCKED
         MH    R0,=H'5'                  NO - BLOCK 5 TO 1
         AH    R0,=H'4'                 ALLOW FOR BLOCK CONTROL WORD
         STH   R0,DCBBLKSI
         CLI   DCBBUFNO,0
         BNE   SARTRE
         SR    R2,R2
         LA    R3,2048
         DR    R2,0
         CH    R3,=H'2'
         BH    *+8
         LA    R3,2
         STC   R3,DCBBUFNO
SARTRE   LM    R14,R12,12(R13)
         BR    R14
         POP   USING                                            GP02266
         TITLE 'JOB FILE CONTROL BLOCK'
         IEFJFCBN
         TITLE 'PDS DIRECTORY DSECT'
DIRECTRY DSECT ,
BLKLEN   DS    H                        LENGTH OF DIRECTORY BLOCK
MEMNAME  DS    CL8                      MEMBER NAME
MEMTTR   DS    XL3                      RELATIVE ADDRESS OF MEMBER
ENTLEN   DS    XL1                      LENGTH OF USER DATA IN HALFWORD
USERDATA EQU   *
         TITLE 'TASK CONTROL BLOCK DSECT'
        IKJTCB
         TITLE 'JOB STEP CONTROL BLOCK DSECT'
       IEZJSCB
         TITLE 'COMMAND SCHEDULING CONTROL BLOCK DSECT'
CSCB     DSECT
      IEECHAIN
         TITLE 'COMMAND INPUT BUFFER'
CIB      DSECT
        IEZCIB
         TITLE 'DATA CONTROL BLOCK DSECT'
         DCBD  DSORG=PS
         TITLE 'I/O BLOCK DSECT'
         IEZIOB
         SPACE 1
         PRINT NOGEN                                            GP12049
         IHAPSA ,                                               GP12049
         TITLE 'LITERALS'
POD      CSECT ,                                                GP02266
         LTORG ,                                                GP02266
         END   ,
