BARXER   TITLE 'B A R X E R  ***  XEROX POSTAL BARCODE GENERATOR'
*        FORTRAN/COBOL CALLABLE ROUTINE
*           CALL BARXER(ZIP,BAR)
*           CALL BARXER USING ZIP BAR
*        ZIP IS 10-BYTE AREA:
*           NNNNN            FIVE DIGITS, FIVE BLANKS
*           NNNNN-           FIVE DIGITS, HYPHEN, BLANKS
*           NNNNN-NNNN       FIVE, HYPHEN, FOUR DIGITS
*           NNNNNNNNN        NINE DIGITS, BLANK
*        BAR IS 12-BYTE OUTPUT AREA
*
*        INVALID OR NON-NUMERICS RETURN BLANKS IN 'BAR'
*
*        OUTPUT CODES ARE FOR XEROX 4050 FONT MB041P AND RELATED.
*        XEROX USES STROKE (X'4F') FOR GUARD BARS, AND DIGITS
*        0 THROUGH 9 FOR THE CORRESPONDING BAR PATTERNS.
*
***********************************************************************
*                                                                     *
*        COPYRIGHT 1991-1992  EXPERT SYSTEM PROGRAMMING, INC.         *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 2
BARXER   START 0                                                 91228
         USING BARXER,R15    USE CALLER'S SUPPLIED BASE
         STM   R14,R12,12(R13)  SAVE CALLER'S REGISTERS
         LM    R11,R12,0(R1)   LOAD PASSED PARAMETERS
         USING ZIPIN,R11     DECLARE INPUT FIELD
         USING BAROUT,R12    DECLARE OUTPUT
         MVI   BAROUT,C' '   PREPARE TO BLANK USER'S FIELD
         MVC   BAROUT+1(L'BAROUT-1),BAROUT  BLANK IT
         SLR   R3,R3         CHECKSUM ACCUMULATOR
         LA    R5,9          PRESET LENGTH FOR 9
         CLI   ZIPIN+5,C'-'  ZIP 'PLUS' ?
         BE    BARC59        YES; MAY BE FIVE OR NINE
         MVC   ZIPPER(9),ZIPIN  MOVE NINE
         CLI   ZIPIN+9,C' '  TRAILING BLANK ?                    91256
         BE    BARCOM
         B     QUICKOUT      NO; DEFINITION ERROR                91256
BARC59   MVC   ZIPPER(5),ZIPIN  MOVE FIRST FIVE
         MVC   ZIPPER+5(4),ZIPIN+6  MOVE REMAINING FIVE
BARCOM   CLC   ZIPPER+5(4),BAROUT  SUBCODE BLANK ?
         BNE   *+8           NO
         LA    R5,5          SET FOR BASIC ZIP ONLY
         LR    R9,R5         COPY LENGTH
         LA    R4,ZIPPER     GET HOLD FIELD
         LA    R0,X'F0'      MAKE NUMERIC ZONE                   91256
         LA    R10,10        MAKE DIVISOR
         SLR   R6,R6         CLEAR FOR IC                        91256
BARNUM   IC    R6,0(,R4)     LOAD DIGIT
         XR    R6,R0         FLIP ZONE BITS (FN -> 0N)           91256
         CR    R6,R10        MORE THAN 9 ?                       91256
         BNL   QUICKOUT      YES; RETURN BLANKS
         AR    R3,R6         ADD TO CHECKSUM ACCUMULATOR
         LA    R4,1(,R4)     SPACE TO NEXT POSITION              91256
         BCT   R9,BARNUM     GO AGAIN
         SLR   R2,R2         PREPARE FOR CHECKSUM
         DR    R2,R10        GET UNITS DIGIT
         LTR   R3,R2         IS REMAINDER ZERO ?
         BZ    CHKZERO       YES; LEAVE AS IS
         LR    R2,R10        LOAD CONSTANT 10                    91256
         SR    R2,R3         MAKE RESIDUAL CHECK DIGIT
CHKZERO  OR    R2,R0         CONVERT TO ZONED DIGIT              92094
         STC   R2,0(,R4)     STASH AFTER DATA                    91256
         MVI   1(R4),X'4F'   MAKE TRAILING GUARD                 92094
         MVC   BAROUT,LEFTGARD  MOVE REULT TO USER'S AREA        92094
         SPACE 1                                                 92094
QUICKOUT LM    R14,R12,12(R13)  RESTORE USER'S REGISTERS
         SLR   R15,R15       SET GOOD RETURN (LIE?)
         BR    R14           GO BACK
         SPACE 1
LEFTGARD DC    X'4F'   1/4   LEFT GUARD BAR                      92094
ZIPPER   DC    CL10' ' 2/4   CONVERSION WORK SPACE
         DC    CL2' '  3/4   SPARE FOR ABC(ADVANCED BAR CODE)    92094
         DC    C' '    4/4   RIGHT GUARD BAR                     92094
         SPACE 1
ZIPSECT  DSECT ,             INPUT PARAMETER
ZIPIN    DS    CL10
         SPACE 1
BARSECT  DSECT ,             OUTPUT FIELD
BAROUT   DS    CL12          GUARDED AND CHECK-DIGITED OUTPUT
         SPACE 1
         PRINT NOGEN
         REGEQU ,            EXPAND REGISTER MNEMONICS
         END   ,
