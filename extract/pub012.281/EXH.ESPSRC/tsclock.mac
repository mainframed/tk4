TSCLOCK  TITLE 'T S O   C L O C K'
***********************************************************************
**                                                                   **
**   TSCLOCK DISPLAYS THE CURRENT TIME ON A TSO CRT, INCREMENTING    **
**   BY MINUTES (DEFAULT), OR 5 SECOND INTERVALS.                    **
**                                                                   **
**   MAY ALSO BE USED AS AN ALARM                                    **
**                                                                   **
***********************************************************************
         MACRO ,                                                GP96127
&NM      SCAD  &ADDR                                            GP96127
         GBLC  &MACPLAB                                         GP96127
&MACPLAB SETC  '&NM'                                            GP96127
         MACPARM R15,&ADDR,OP=LA                                GP96127
         MACPARM R14,SCRADD,OP=BAL                              GP96127
         MEND  ,                                                GP96127
         SPACE 1
         MACRO ,                                                GP96128
&NM      SHOW  &PTR                                             GP96128
&NM      LM    R0,R1,&PTR       LOAD LENGTH,FLAGS,ADDR OF MSG   GP96128
         MACPARM 93,OP=SVC,MODE=ONE                             GP96128
         MEND  ,                                                GP96128
         SPACE 1                                                GP96130
         MACRO ,                                                GP96130
&NM      SWAP  &A,&B,&LEN=   EXCHANGE TWO STRINGS          G.P.  86197
         LCLC  &L                                               GP96130
&L       SETC  'L'''                                            GP96130
         AIF   ('&LEN' EQ '').DFLT                              GP96130
&NM      XC    &A.(&LEN),&B                                     GP96130
         XC    &B.(&LEN),&A                                     GP96130
         XC    &A.(&LEN),&B                                     GP96130
         MEXIT ,                                                GP96130
.DFLT    ANOP  ,                                                GP96130
&NM      XC    &A.(&L&A),&B                                     GP96130
         XC    &B.(&L&A),&A                                     GP96130
         XC    &A.(&L&A),&B                                     GP96130
         MEND  ,                                                GP96130
         SPACE 1
         PUNCH ' ORDER TSCLOCK(P) '                             GP96127
         EJECT ,                                                GP96130
         PRINT NOGEN
TSCLOCK  START 0
R93      EQU   93   *** TEMP *** BYPASS MACPARM PROBLEM ***     GP02355
         B     TSCLOCK1-TSCLOCK(R15)  BRANCH AROUND
         DC    AL1(17),C'TSCLOCK &SYSDATE '
TSCLOCKS DC    18F'0'        CRUTTY SAVE AREA
TSCLOCK1 STM   R14,R12,12(R13)  SAVE ENTRY REGISTERS
         LR    R12,R15       MAKE LOCAL BASE
         USING TSCLOCK,R12   DECLARE LOCAL BASE
         LA    R9,TSCLOCKS   SET LOCAL SAVE AREA
         ST    R9,8(,R13)    MAKE LINK
         ST    R13,4(,R9)    AND THE OTHER WAY
         LR    R13,R9        MAKE LOCAL SAVE
         SPACE 1
*DEFER*  STAX  DEFER=YES
*DEFER*  PRINT GEN                                              GP96130
         SPACE 1
         L     R3,16         CVTPTR
         L     R3,0(,R3)     CVTTCBP                            GP96128
         L     R3,4(,R3)     ACT TCB                            GP96128
         L     R3,12(,R3)    TCBTIO                             GP96128
         MVC   PASSWORD,0(R3)  TIOJOB                           GP96127
         SPACE 2                                                GP96127
         GTSIZE
         STM   R0,R1,MAXROWS   SAVE CURRENT SCREEN SIZE         GP96127
         CH    R1,=H'1'      IS THIS A BATCH RUN?               GP96127
         BNH   2             YES; SOCK IT TO 'EM                GP96127
         GTTERM PRMSZE=DWORD+0,ALTSZE=DWORD+2,ATTRIB=DWORD+4    GP12050
         LM    R0,R1,MAXROWS RESTORE ROWS/COLUMNS               GP12050
         CLM   R1,1,DWORD+1    PRIMARY ROWS    = CURRENT ?      GP12050
         BNE   USEALT          NO; RUNNING ALTERNATE SIZE       GP12050
         CLM   R0,1,DWORD+0    PRIMARY COLUMNS = CURRENT ?      GP12050
         BE    USEPRI          NO; RUNNING ALTERNATE SIZE       GP12050
USEALT   MVI   CLRSCRN+1,X'7E'    CLEAR ALTERNATE SIZE          GP12050
USEPRI   LA    R5,CLOBBER1   POINT TO FIRST SBA REPLACEMENT REGION
         LA    R6,1          SET SCAN INCREMENT                 GP96127
         LA    R7,CLOB1END   SET END ADDRESS                    GP96127
CLOB1LUP CLI   0(R5),X'11'   BEGINNING OF SBA SEQUENCE?         GP96127
         BNE   CLOB1BMP      NO                                 GP96127
         SCAD  1(R5)                                            GP96127
         LA    R5,2(,R5)     SKIP SBA                           GP96127
CLOB1BMP BXLE  R5,R6,CLOB1LUP  FIND ALL SBAS                    GP96127
         SPACE 1                                                GP96127
         LA    R5,CLOBBER2   POINT TO FIRST SBA REPLACEMENT REGION
*DONE*   LA    R6,1          SET SCAN INCREMENT                 GP96127
         LA    R7,CLOB2END   SET END ADDRESS                    GP96127
CLOB2LUP CLI   0(R5),X'11'   BEGINNING OF SBA SEQUENCE?         GP96127
         BNE   CLOB2BMP      NO                                 GP96127
         SCAD  1(R5)                                            GP96127
         LA    R5,2(,R5)     SKIP SBA                           GP96127
CLOB2BMP BXLE  R5,R6,CLOB2LUP  FIND ALL SBAS                    GP96127
         EJECT ,
SHOWTIME MVI   ANSAID,X'0'                                      GP96128
         SHOW  CLEARSCN                                         GP96128
         SHOW  PUTCOLON                                         GP96128
         SHOW  THELP                                            GP96128
         SHOW  TCHIME                                           GP96128
         SHOW  DSPALRM                                          GP96128
         SPACE 1
         MVI   SOUNDED,X'99'
         LA    R3,255        SET FOR FORCED MISMATCH            GP96128
         STC   R3,CURDIGHT                                      GP96128
         STC   R3,CURDIGHU                                      GP96128
         STC   R3,CURDIGMT                                      GP96128
         STC   R3,CURDIGMU                                      GP96128
         SPACE 1
SHOWCHNG CLI   ANSAID,X'6C'  PA1?                               GP96128
         BE    SHOWTIME      YES; REFRESH                       GP96128
         SPACE 1
         BAS   R11,GETTIME
         CLI   NEWDIGS,0     IS IT 0-9 HOURS?                   GP96130
         BNE   *+8           NO                                 GP96130
         MVI   NEWDIGS,10    (FAKE) MAKE LEADING BLANK          GP96130
         LA    R8,DIGITUP    POINT TO DIGIT INFO TABLE
         LA    R7,NEWDIGS    POINT TO NEW DIGITS                GP96128
         LA    R10,4         HAVE FOUR DIGITS
DIGILOOP SLR   R0,R0         CLEAR FOR IC                       GP96128
         IC    R0,0(,R7)     LOAD NEXT DIGIT VALUE              GP96128
         LR    R1,R8         COPY ADDRESS
         BAS   R9,DISPDIG                                       GP96128
         LA    R8,3*4(,R8)   NEXT ADDRESS LIST
         LA    R7,1(,R7)     NEXT DIGIT ADDRESS                 GP96128
         BCT   R10,DIGILOOP
         SPACE 1
BLDMUNEX TM    OPTFLAGS,REQSEC  REQUESTED DISPLAY OF SECONDS?   GP96128
         BZ    DSPSECEX                                         GP96128
         MVN   DWORD(1),SECHEX                                  GP96128
         NI    DWORD,X'0F'   CLOBBER ZONE                       GP96128
*NI*     CLI   DWORD,X'00'      HEAVILY LOADED SYSTEM; DISPLAY  GP95193
         BE    DSPSECNW               ONLY ON 0 AND 5 SECONDS   GP95193
         CLI   DWORD,X'05'      HEAVILY LOADED SYSTEM; DISPLAY  GP95193
         BNE   DSPSECEX               ONLY ON 0 AND 5 SECONDS   GP95193
DSPSECNW UNPK  DWORD(3),SECHEX(2)                               GP96128
         MVC   SECSDISP,DWORD   DISPLAY SECONDS                 GP96128
         SHOW  SECSREGS                                         GP96128
         SPACE 1
DSPSECEX TM    OPTFLAGS,REQCHIME  CHIME THE QUARTERS?           GP96128
         BZ    CHIMEXIT                                         GP96128
         CLI   MINHEX,X'15'                                     GP96128
         BE    SOUNDQTR
         CLI   MINHEX,X'30'                                     GP96128
         BE    SOUNDQTR
         CLI   MINHEX,X'45'                                     GP96128
         BE    SOUNDQTR
         CLI   MINHEX,X'0'                                      GP96128
         BNE   CHIMEXIT                                         GP96128
*NEXT*   BE    SOUNDHR                                          GP96128
         SPACE 1
SOUNDHR  CLC   SOUNDED,MINHEX                                   GP96128
         BE    CHIMEXIT
         MVC   SOUNDED,MINHEX                                   GP96128
         ZAP   DWORD,=P'0'   JUST IN CASE                       GP96128
         MVO   DWORD,HOURS   ALWAYS SIGN YOUR WORK              GP96128
         CVB   R2,DWORD
         LA    R0,12         FAST CONSTANT                      GP96128
         LTR   R2,R2
         BP    *+6                                              GP96128
         LR    R2,R0         MIDNIGHT                           GP96128
         CR    R2,R0                                            GP96128
         BNH   SOUNDHRT                                         GP96128
         SR    R2,R0         CHANGE PM TO 1-12                  GP96128
         B     SOUNDHRS                                         GP96128
         SPACE 1
SOUNDHRT STIMER WAIT,BINTVL==A(100)  WAIT ONE SECOND            GP96128
SOUNDHRS SHOW  CHIMREG                                          GP96128
         BCT   R2,SOUNDHRT
         B     CHIMEXIT
         SPACE 1
SOUNDQTR CLC   SOUNDED,MINHEX                                   GP96128
         BE    CHIMEXIT
         MVC   SOUNDED,MINHEX                                   GP96128
         SHOW  CHIMREG                                          GP96128
         SPACE 1
CHIMEXIT TM    OPTFLAGS,REQALRM  AM I ALARMED?                  GP96128
         BZ    ALRMDON                                          GP96128
         CLC   HOURS(L'HOURS+L'MINHEX),ALRMHH                   GP96128
         BNE   ALRMDON
         NI    OPTFLAGS,255-REQALRM  TURN IT OFF                GP96128
         LA    R2,60
         B     ALRMSND
         SPACE 1
ALRMWT   SHOW  TGETREGS                                         GP96128
         BXLE  R15,R15,ALRMSNDX                                 GP96128
         STIMER WAIT,BINTVL==A(100)   WAIT ONE SECOND           GP96128
ALRMSND  SHOW  CHIMREG                                          GP96128
         BCT   R2,ALRMWT
ALRMSNDX XC    DALRMLIT,DALRMLIT
         SHOW  DSPALRM                                          GP96128
ALRMDON  DS    0H
         EJECT ,
         XC    ANSWER,ANSWER
         SHOW  TGETREGS                                         GP96128
         BXLE  R15,R15,INPUTOK                                  GP96128
         STIMER WAIT,BINTVL==A(100)   WAIT ONE SECOND           GP96128
         B     SHOWCHNG
         SPACE 1
INPUTOK  LA    R3,FOURNAID   GET ACTION TABLE                   GP96128
         LA    R4,FOURN2-FOURNAID  LENGTH OF ONE ENTRY          GP96128
         LA    R5,FOURNEND   LAST TABLE ENTRY                   GP96128
         IC    R0,ANSAID     LOAD RECEIVED AID FOR FASTER COMPARES
GETAID   CLM   R0,1,0(R3)    MATCHING AID?                      GP96128
         BE    GOTAID        YES; JUMP TO EXECUTIONER           GP96128
         BXLE  R3,R4,GETAID  TRY NEXT ENTRY                     GP96128
GOTAID   SLR   R15,R15       CLEAR FOR ICM                      GP96128
         ICM   R15,3,1(R3)   LOAD SERVICE ADDRESS               GP96128
         B     TSCLOCK(R15)  GO TO PROCESSOR                    GP96128
         SPACE 1                                                GP96128
FOURNAID DC    X'7D',AL2(EXECENT-TSCLOCK)  ENTER ACTION         GP96128
FOURN2   DC    X'6C',AL2(EXECEXIT-TSCLOCK)  PA1 - GET OUT       GP96128
         DC    X'6D',AL2(EXECEXIT-TSCLOCK)  CLEAR - GET OUT     GP96128
         DC    X'F9',AL2(EXECEXIT-TSCLOCK)  PFK 9 - GET OUT     GP96128
         DC    X'C9',AL2(EXECEXIT-TSCLOCK)  PFK 21 - GET OUT    GP96128
         DC    X'F1',AL2(EXECSECS-TSCLOCK)  PFK 1 - TOGGLE SECONDS
         DC    X'C1',AL2(EXECSECS-TSCLOCK)  PFK 13 - TOGGLE SECONDS
         DC    X'F2',AL2(EXECHIME-TSCLOCK)  PFK 2 - TOGGLE CHIMES
         DC    X'C2',AL2(EXECHIME-TSCLOCK)  PFK 14 - TOGGLE CHIMES
         DC    X'F3',AL2(EXECALRM-TSCLOCK)  PFK 3 - TOGGLE/SET ALRM
         DC    X'C3',AL2(EXECALRM-TSCLOCK)  PFK 15 - TOGGLE/SET ALRM
         DC    X'7C',AL2(EXEC12HR-TSCLOCK)  PFK 12 - TOGGLE 12/24 HOUR
         DC    X'4C',AL2(EXEC12HR-TSCLOCK)  PFK 24 - TOGGLE 12/24 HOUR
FOURNEND DC    X'6E',AL2(SHOWTIME-TSCLOCK)  PA2 - REDISPLAY     GP96128
         DC    X'FF',AL2(SHOWTIME-TSCLOCK)  WHATEVER ELSE - IGNORE IT
         SPACE 1
EXECENT  CLC   ANSFADD(2),BLANKS  UNFORMATTED INPUT?            GP96128
         BE    SHOWTIME
         OC    ANSTXT,BLANKS
         LA    R15,L'ANSTXT                                     GP96128
LEFTANS  CLI   ANSTXT,C' '   LEADING BLANK?                     GP96128
         BNE   HAVEANS       NO; EXAMINE INPUT                  GP96128
         MVC   ANSTXT,ANSTXT+1  SLIDE LEFT; BLANK FILL          GP96128
         BCT   R15,LEFTANS                                      GP96128
         B     RE#HELP      INPUT ALL BLANK                     GP96128
HAVEANS  TM    OPTFLAGS,REQHOLD                                 GP96128
         BZ    CHKPSWD                                          GP96128
         NI    OPTFLAGS,255-REQHOLD                             GP96128
         CLC   HOLDPASS,ANSTXT
         BNE   CHKPSWD
         MVC   PASSWORD,HOLDPASS
         B     RE#HELP
         SPACE 1
CHKPSWD  CLC   PASSWORD,ANSTXT
         BE    EXECEXIT
         OI    OPTFLAGS,REQHOLD                                 GP96128
         MVC   HOLDPASS,ANSTXT
RE#HELP  SHOW  THELP                                            GP96128
         B     SHOWCHNG
         SPACE 1
EXECSECS SWAP  DSHSEC,DSASEC   CHANGE HELP MESSAGE TEXT         GP96130
         SHOW  THELP         AND RE-DISPLAY IT                  GP96130
         XI    OPTFLAGS,REQSEC   TOGGLE REQUEST FOR DISPLAY OF SECS
         BNZ   SHOWCHNG                                         GP96128
         MVC   SECSDISP,BLANKS                                  GP96128
         SHOW  SECSREGS                                         GP96128
         B     SHOWCHNG
         SPACE 1                                                GP96130
EXEC12HR XI    OPTFLAGS,REQ12HR  TOGGLE 12-HOUR VS. 24-HOUR     GP96130
         SWAP  DSHAMPM,DSAAMPM   FIX HELP DISPLAY               GP96130
         MVC   DSPAMPM,BLANKS    CLEAR OUT THE DISPLAY          GP96130
         B     SHOWTIME          REDISPLAY                      GP96130
         SPACE 1
EXECHIME SWAP  DSHCHIME,DSACHIME   CHANGE HELP MESSAGE TEXT     GP96130
         SHOW  THELP         AND RE-DISPLAY IT                  GP96130
         XI    OPTFLAGS,REQCHIME  FLIP THE CHIME OPTION         GP96128
         BNZ   CHIMESHW      FLIP TEXT ALSO                     GP96128
         MVI   SOUNDED,X'99'  TIME LAST CHIME SOUNDED (?)       GP96128
CHIMESHW SWAP  CHIMEOFF,CHIMELIT                                GP96130
         SHOW  TCHIME                                           GP96128
         B     SHOWCHNG
         SPACE 1
EXECALRM TM    OPTFLAGS,REQALRM                                 GP96128
         BNZ   ALRMNOT                                          GP96128
ALRMPMT  SHOW  CHIMREG                                          GP96128
         UNPK  DWORD(3),ALRMHH(2)  UNPACK AND PAD HOURS         GP96128
         MVC   GALRMLIT(2),DWORD   SHOW IT                      GP96128
         MVI   GALRMLIT+2,C':'
         UNPK  DWORD(3),ALRMMM(2)  UNPACK AND PAD MINUTES       GP96128
         MVC   GALRMLIT+3(2),DWORD   SHOW IT                    GP96128
         SHOW  GETALRM                                          GP96128
         XC    ANSWER,ANSWER
         SHOW  TGETWAIT                                         GP96128
         BXH   R15,R15,SHOWALRM                                 GP96128
         CLI   ANSAID,X'7D'            ENTER
         BNE   SHOWALRM
         SPACE 1
         LA    R14,ANSTXT              ADDRESS OF ANSWER
         LA    R15,L'ANSTXT            LENGTH OF ANSWER
         BAL   R11,FINDDIG             FIND FIRST DIGIT IN ANSWER
         LTR   R15,R15                 NO DIGITS?
         BZ    ALRMNOT
         CLI   0(R14),C':'             NO HOURS?
         BE    ALRMHRCK                YES, CHECK CURRENT HOUR SETTING
         LR    R7,R14                  ADDRESS OF DIGIT
         LA    R14,1(,R14)             ADDRESS AFTER DIGIT      GP96128
         BCTR  R15,R0                  ONE LESS CHARACTER
         BAL   R11,FINDNDIG            FIND NON-DIGIT AFTER DIGIT
         LR    R9,R14                  ADDRESS OF NON-DIGIT
         SR    R9,R7                   NUMBER OF DIGITS
         EX    R9,PACKDB               PACK IN GENERIC FIELD
         MVC   ALRMHH,DWORD+6          MOVE TO SPECIFIC FIELD   GP96128
ALRMHRCK CLI   ALRMHH,X'24'                                     GP96128
         BH    ALRMPMT
         BL    ALRMHRDN
         MVI   ALRMHH,0                                         GP96128
         SPACE 1
ALRMHRDN MVI   ALRMMM,0                ZERO OUT ALRM MINUTES    GP96128
         LTR   R15,R15                 ANY MORE DIGITS?         GP96128
         BZ    ALRMSCDN                NO; GET OUT              GP96128
         LA    R14,1(,R14)             ADDRESS AFTER DIGIT      GP96128
         BCTR  R15,0                   ONE LESS CHARACTER       GP96128
         BAL   R11,FINDDIG             FIND FIRST DIGIT IN MINUTES
         LTR   R15,R15                 ANY MORE DIGITS?         GP96128
         BZ    ALRMSCDN                NO; GET OUT              GP96128
         LR    R7,R14                  ADDRESS OF DIGIT
         LA    R14,1(,R14)             ADDRESS AFTER DIGIT      GP96128
         BCTR  R15,0                   ONE LESS CHARACTER       GP96128
         BAL   R11,FINDNDIG            FIND NON-DIGIT AFTER DIGIT
         LR    R9,R14                  ADDRESS OF NON-DIGIT
         SR    R9,R7                   NUMBER OF DIGITS
         EX    R9,PACKDB               PACK IN GENERIC FIELD
         MVC   ALRMMM,DWORD+6          MOVE TO SPECIFIC FIELD   GP96128
ALRMSCDN CLI   ALRMMM,X'59'                                     GP96128
         BH    ALRMPMT
         OI    OPTFLAGS,REQALRM                                 GP96128
         MVC   DSHALRM,=C'OFF'    SET HELP TEXT                 GP96130
         UNPK  DWORD(3),ALRMHH(2)  UNPACK AND PAD HOURS         GP96128
         MVC   GALRMLIT(2),DWORD   SHOW IT                      GP96128
         MVI   GALRMLIT+2,C':'                                  GP96128
         UNPK  DWORD(3),ALRMMM(2)  UNPACK AND PAD MINUTES       GP96128
         MVC   GALRMLIT+3(2),DWORD   SHOW IT                    GP96128
         MVC   DALRMLIT,GALRMLIT
         B     SHOWALRM
PACKDB   PACK  DWORD,0(0,R7)
         SPACE 1
ALRMNOT  NI    OPTFLAGS,255-REQALRM                             GP96128
         MVC   DSHALRM,=C'ON '   SET HELP TEXT                  GP96130
         XC    DALRMLIT,DALRMLIT
SHOWALRM SHOW  DSPALRM                                          GP96128
         SHOW  THELP                                            GP96128
         B     SHOWCHNG
         SPACE 2
EXECEXIT SHOW  CLEARSCN                                         GP96128
         L     R13,4(,R13)   GET CALLER'S SAVE AREA             GP96127
         RETURN (14,12),RC=0  RETURN ALL                        GP96127
         EJECT ,
*  SCRADD - CONVERT SBA FROM LINE,POS TO 3270 VALUE             GP96127
*   R15 - ADDRESS OF ADDRESS BYTES (AFTER SBA)                  GP96127
SCRADD   STM   R0,R5,20(R13)  SAVE A FEW REGISTERS              GP96127
         SLR   R1,R1                                            GP96127
         SLR   R3,R3                                            GP96127
         SLR   R5,R5         SET PROVISIONAL RETURN CODE        GP96127
         ICM   R1,1,0(R15)   LOAD AND TEST LINE NUMBER          GP96127
         BZ    SCRADBLN      LINE BAD                           GP96127
         C     R1,MAXROWS    VALID LINE NUMBER?                 GP96127
         BNH   SCRADTPO      YES; CHECK POSITION                GP96127
SCRADBLN LA    R5,4          SET ERROR RETURN                   GP96127
         LA    R1,1          USE LINE 1 INSTEAD                 GP96127
SCRADTPO ICM   R3,1,1(R15)   LOAD AND TEST POSITION             GP96127
         BZ    SCRADBPO      LINE BAD                           GP96127
         C     R3,MAXCOLS    VALID COLUMN NUMBER?               GP96127
         BNH   SCRADSBA      YES; CHECK POSITION                GP96127
SCRADBPO LA    R5,4          SET ERROR RETURN                   GP96127
         LA    R3,41         USE COLUMN 41 INSTEAD              GP96127
SCRADSBA BCTR  R1,0          MAKE RELATIVE TO 0,0               GP96127
         BCTR  R3,0                                             GP96127
         M     R0,MAXCOLS    CONVERT LINE POSITION INTO BYTES   GP96127
         AR    R1,R3         TOTAL OFFSET                       GP96127
         CH    R1,=H'4096'   12-BIT ADDRESSING MODE?            GP96127
         BL    SCRADB12      YES                                GP96127
         STCM  R1,3,0(R15)   UPDATE WITH 14-BIT ADDRESS         GP96127
         B     SCRADEX       GO TO COMMON EXIT                  GP96127
SCRADB12 D     R0,=F'64'     LAZY SHIFT                         GP96127
         STC   R1,0(,R15)    SET QUOTIENT                       GP96127
         STC   R0,1(,R15)    SET REMAINDER                      GP96127
         TR    0(2,R15),ADDR3270  CONVERT TO VALID ADDRESSES    GP96127
SCRADEX  LR    R15,R5        SET RETURN CODE                    GP96127
         LM    R0,R5,20(R13) RELOAD REGISTERS                   GP96127
         BR    R14           AND RETURN                         GP96127
ADDR3270 DC    X'40,C1,C2,C3,C4,C5,C6,C7,C8,C9,4A,4B,4C,4D,4E,4F'
         DC    X'50,D1,D2,D3,D4,D5,D6,D7,D8,D9,5A,5B,5C,5D,5E,5F'
         DC    X'60,61,E2,E3,E4,E5,E6,E7,E8,E9,6A,6B,6C,6D,6E,6F'
         DC    X'F0,F1,F2,F3,F4,F5,F6,F7,F8,F9,7A,7B,7C,7D,7E,7F'
MAXROWS  DC    F'0'    1/2   MAXIMUM LINES ON CURRENT SESSION SCREEN
MAXCOLS  DC    F'0'    2/2   MAXIMUM COLUMNS ON CURRENT SCREEN  GP96127
         EJECT ,
GETTIME  TIME  DEC           GET PACKED TIME IN R0
         LR    R15,R0        TRANSFER TIME TO SECOND PAIRED REGISTER
         LA    R4,4          EXTRACT FOUR NIBBLES (H*10,H,M*10,M)
         STCM  R0,8+4,HOURS    SAVE HOURS AND MINUTES           GP96128
         STCM  R0,2,SECHEX   SAVE SECS                          GP96128
         TM    OPTFLAGS,REQ12HR DISPLAY IN 12-HOUR FORMAT ?     GP96130
         BZ    GETTIM24      NO                                 GP96130
         MVC   DSPAMPM,=X'8194'  SHOW AM                        GP96130
         CLI   HOURS,X'12'   PM?                                GP96130
         BNH   GETTIM24      NOT YET                            GP96130
         SL    R15,=X'12000000'   MAKE PM HOURS                 GP96130
         MVC   DSPAMPM,=X'9794'  SHOW PM                        GP96130
GETTIM24 LA    R3,NEWDIGS    GET FIRST VALUE TO BE EXTRACTED    GP96128
GETTIMEL SLR   R14,R14       CLEAR FOR SHIFTING                 GP96128
         SLDL  R14,4         EXTRACT NEXT NIBBLE                GP96128
         STC   R14,0(,R3)    SAVE INTO HOURS/MINUTES            GP96128
         LA    R3,1(,R3)     POINT TO NEXT DESTINATION          GP96128
         BCT   R4,GETTIMEL   EXTRACT ALL FOUR                   GP96128
         BR    R11
         SPACE 2                                                GP96128
FINDDIG  LTR   R15,R15                                          GP96128
         BZR   R11
FINDDLUP CLI   0(R14),C':'                                      GP96128
         BER   R11                                              GP96128
         CLI   0(R14),C'0'
         BL    FINDDSKP                                         GP96128
         CLI   0(R14),C'9'
         BNHR  R11                                              GP96128
FINDDSKP LA    R14,1(,R14)                                      GP96128
         BCT   R15,FINDDLUP                                     GP96128
         BR    R11
         SPACE 2                                                GP96128
FINDNDIG LTR   R15,R15                                          GP96128
         BZR   R11
FINDNLUP CLI   0(R14),C'0'                                      GP96128
         BLR   R11                                              GP96128
         CLI   0(R14),C'9'
         BHR   R11                                              GP96128
         LA    R14,1(,R14)                                      GP96128
         BCT   R15,FINDNLUP                                     GP96128
         BR    R11
         SPACE 2                                                GP96128
*  DISPLAY ONE DIGIT (AT A TIME)                                GP96128
*   R1 -> A(PRIOR VALUE, OUTPUT ADDR, TPUT A/L ADDR)            GP96128
*   R0 =  CURRENT DIGIT                                         GP96128
*                                                               GP96128
DISPDIG  LM    R2,R4,0(R1)   LOAD PASSED PARAMETERS             GP96128
         CLM   R0,1,0(R2)    SAME AS PRIOR VALUE?               GP96128
         BER   R9            YES; JUST RETURN                   GP96128
         STC   R0,0(,R2)     NO; UPDATE IT AND DISPLAY          GP96128
         MH    R0,=Y(LDIGIT) CONVERT DIGIT TO OFFSET IN DIGIT MATRIX
         LA    R14,DIGITS    POINT TO TABLE OF DIGITS           GP96128
         AR    R14,R0        POINT TO THIS DIGIT                GP96128
         LA    R15,LDIGIT/LDIGSEG   # OF DIGIT LINES            GP96128
DISPDIGL MVC   3(LDIGSEG,R3),0(R14) MOVE ONE LINE               GP96128
         LA    R3,LSEGHT1(,R3)     NEXT OUTPUT LINE             GP96128
         LA    R14,LDIGSEG(,R14) NEXT PATTERN LINE              GP96128
         BCT   R15,DISPDIGL  DO ALL LINES IN PATTERN            GP96128
         SHOW  0(R4)         LOAD TPUT LENGH, FLAGS, ADDRESS    GP96128
         BR    R9            RETURN TO CALLER                   GP96128
DIGITUP  DC    A(CURDIGHT,SEGHT1,PUTHOUR1)     HOUR - TENS POSITION
         DC    A(CURDIGHU,SEGHU1,PUTHOUR2)     HOUR - UNITS     GP96128
         DC    A(CURDIGMT,SEGMT1,PUTMIN1)   SECONDS - TENS POSITION
         DC    A(CURDIGMU,SEGMU1,PUTMIN2)   SECONDS - UNITS     GP96128
         EJECT ,
         YREGS ,
         SPACE 1
         LTORG
         SPACE 1
NO       EQU   0
YES      EQU   255
         SPACE 1
DB       DC    D'0'                                             GP12050
DWORD    DC    0D'0',PL8'0'                                     GP96128
CLEARSCN DC    A(L'CLRSCRN),X'03',AL3(CLRSCRN)                  GP96128
PUTHOUR1 DC    A(LHOUR1),X'03',AL3(HOUR1)
PUTHOUR2 DC    A(LHOUR2),X'03',AL3(HOUR2)
PUTCOLON DC    A(LCOLON),X'03',AL3(COLON)
PUTMIN1  DC    A(LMIN1),X'03',AL3(MIN1)
PUTMIN2  DC    A(LMIN2),X'03',AL3(MIN2)
SECSREGS DC    A(LSECONDS),X'03',AL3(SECONDS)
THELP    DC    A(LHELP),X'03',AL3(HELP)
TCHIME   DC    A(LDCHIME),X'03',AL3(DCHIME)
CHIMREG  DC    A(LRCHIME),X'03',AL3(RCHIME)
DSPALRM  DC    A(LDSPALRM),X'03',AL3(TDSPALRM)
GETALRM  DC    A(LGETALRM),X'03',AL3(TGETALRM)
PASSWORD DC    CL8' '
HOLDPASS DC    CL8' '
OPTFLAGS DC    AL1(0)        PROCESSING OPTIONS                 GP96128
REQSEC   EQU   X'80'           SECONDS TO BE DISPLAYED          GP96128
REQCHIME EQU   X'40'           CHIME REQUESTED                  GP96128
REQALRM  EQU   X'20'           ALRM SET                         GP96128
REQHOLD  EQU   X'08'           HOLDING INPUT                    GP96128
REQ12HR  EQU   X'04'           DISPLAY AM/PM                    GP96130
         SPACE 1
HOURS    DC    X'00'   1/2   LAST DISPLAYED HOUR (UNSIGNED PACKED)
MINHEX   DC    X'00'   2/2                                      GP96128
ALRMHH   DC    X'00'   1/2   ALRM TIME: HOURS                   GP96128
ALRMMM   DC    X'00'   2/2                                      GP96128
SECHEX   DC    X'0'                                             GP96128
SOUNDED  DC    X'99'
CURDIGHT DC    X'FF'         1/4                                GP96128
CURDIGHU DC    X'FF'         2/4                                GP96128
CURDIGMT DC    X'FF'         3/4                                GP96128
CURDIGMU DC    X'FF'         4/4                                GP96128
NEWDIGS  DC    4X'FE'        CORRESPONDING NEW DIGITS           GP96128
         SPACE 1
CLOBBER1 EQU   *             START OF SBA STORAGE REPLACEMENT   GP96128
         SPACE 1                                                GP96130
CLRSCRN  DC    X'27F5C3'                                        GP96130
SECONDS  DC    X'27F142'
         DC    X'11',AL1(13,72),X'1D68'
SECSDISP DC    C'  ',X'1D60'
LSECONDS EQU   *-SECONDS
         SPACE 1
HELP     DC    X'27F142'
         DC    X'11',AL1(23,17),X'1D68'                         GP96130
         DC    C'PF1:SECONDS '                                  GP96130
DSHSEC   DC    CL3'ON ',C'  '                                   GP96130
         DC    C'PF2:CHIME '                                    GP96130
DSHCHIME DC    CL3'ON ',C'  '                                   GP96130
         DC    C'PF3:ALRM '                                     GP96130
DSHALRM  DC    CL3'ON ',C' '                                    GP96130
         DC    X'11',AL1(24,17),X'1D68'                         GP96130
         DC    C'PF9:QUIT    '                                  GP96130
         DC    CL3'   ',C'  '                                   GP96130
         DC    C'          '                                    GP96130
         DC    CL3'   ',C'  '                                   GP96130
         DC    C'PF12:'                                         GP96130
DSHAMPM  DC    CL2'12',C'-HOUR  ',X'1D4C13'                     GP96130
         DC    C'        ',X'1D60'                              GP96130
LHELP    EQU   *-HELP
DSASEC   DC    CL3'OFF'                                         GP96130
DSACHIME DC    CL3'OFF'                                         GP96130
DSAAMPM  DC    CL2'24'                                          GP96130
         SPACE 1
DCHIME   DC    X'27F142'
         DC    X'11',AL1(1,2),X'1D68'
CHIMEOFF DC    C'     ',X'1D60'
LDCHIME  EQU   *-DCHIME
CHIMELIT DC    C'CHIME'
         SPACE 1
RCHIME   DC    X'27F146'
LRCHIME  EQU   *-RCHIME
         SPACE 1
TDSPALRM DC    X'27F142'
         DC    X'11',AL1(1,73),X'1D68'
DALRMLIT DC    C'      ',X'1D60'
LDSPALRM EQU   *-TDSPALRM
         SPACE 1
TGETALRM DC    X'27F142'
         DC    X'11',AL1(1,73),X'1D4013'
GALRMLIT DC    C'06:00 ',X'1D60'
LGETALRM EQU   *-TGETALRM
CLOB1END EQU   *-3           POSITION OF LAST POSSIBLE SBA SEQUENCE
         SPACE 1
TGETWAIT DC    A(80),X'81',AL3(ANSWER)
TGETREGS DC    A(80),X'91',AL3(ANSWER)
ANSWER   DS    0CL80
ANSAID   DC    X'0'                    KEY STRUCK
ANSCADD  DC    AL1(0,0)                CURSOR ADDRESS
ANSSBA   DC    X'11'                   SET BUFFER ADDRESS
ANSFADD  DC    AL1(0,0)                FIELD ADDRESS
ANSTXT   DC    CL74' '      1/2        CHARACTERS TYPED
BLANKS   DC    CL74' '      2/2        BLANKS FOR UPPER CASING
         SPACE 1
CLOBBER2 EQU   *             START OF SECOND SBA REPLACEMENT REGION
HOUR1    DC    X'27F142'
SEGHT1   DC    X'11',AL1(07,10),X'1D60',CL12' '                 GP96128
LSEGHT1  EQU *-SEGHT1                                           GP96128
         DC    X'11',AL1(08,10),X'1D60',CL12' '
         DC    X'11',AL1(09,10),X'1D60',CL12' '
         DC    X'11',AL1(10,10),X'1D60',CL12' '
         DC    X'11',AL1(11,10),X'1D60',CL12' '
         DC    X'11',AL1(12,10),X'1D60',CL12' '
         DC    X'11',AL1(13,10),X'1D60',CL12' '
         DC    X'11',AL1(14,10),X'1D60',CL12' '
         DC    X'11',AL1(15,10),X'1D60',CL12' '
         DC    X'11',AL1(16,10),X'1D60',CL12' '
         DC    X'11',AL1(17,10),X'1D60',CL12' '
         DC    X'11',AL1(18,10),X'1D60',CL12' '
LHOUR1   EQU   *-HOUR1
         SPACE 1
HOUR2    DC    X'27F142'
SEGHU1   DC    X'11',AL1(7,24),X'1D60',CL12' '
         DC    X'11',AL1(8,24),X'1D60',CL12' '
         DC    X'11',AL1(9,24),X'1D60',CL12' '
         DC    X'11',AL1(10,24),X'1D60',CL12' '
         DC    X'11',AL1(11,24),X'1D60',CL12' '
         DC    X'11',AL1(12,24),X'1D60',CL12' '
         DC    X'11',AL1(13,24),X'1D60',CL12' '
         DC    X'11',AL1(14,24),X'1D60',CL12' '
         DC    X'11',AL1(15,24),X'1D60',CL12' '
         DC    X'11',AL1(16,24),X'1D60',CL12' '
         DC    X'11',AL1(17,24),X'1D60',CL12' '
         DC    X'11',AL1(18,24),X'1D60',CL12' '
         DC    X'11',AL1(12,5),X'1D68'  DISPLAY AM OR PM        GP96130
DSPAMPM  DC    C'  ',X'1D60'                                    GP96130
LHOUR2   EQU   *-HOUR2
         SPACE 1
COLON    DC    X'27F142'
COLONLIT DC    X'11',AL1(10,37),X'1D60',CL4' .. '
LCOLLINE EQU *-COLONLIT
         DC    X'11',AL1(11,37),X'1D60',CL4'....'
         DC    X'11',AL1(12,37),X'1D60',CL4' .. '
         DC    X'11',AL1(14,37),X'1D60',CL4' .. '
         DC    X'11',AL1(15,37),X'1D60',CL4'....'
         DC    X'11',AL1(16,37),X'1D60',CL4' .. '
LCOLON   EQU   *-COLON
         SPACE 1
MIN1     DC    X'27F142'
SEGMT1   DC    X'11',AL1(7,44),X'1D60',CL12' '                  GP96128
         DC    X'11',AL1(8,44),X'1D60',CL12' '
         DC    X'11',AL1(9,44),X'1D60',CL12' '
         DC    X'11',AL1(10,44),X'1D60',CL12' '
         DC    X'11',AL1(11,44),X'1D60',CL12' '
         DC    X'11',AL1(12,44),X'1D60',CL12' '
         DC    X'11',AL1(13,44),X'1D60',CL12' '
         DC    X'11',AL1(14,44),X'1D60',CL12' '
         DC    X'11',AL1(15,44),X'1D60',CL12' '
         DC    X'11',AL1(16,44),X'1D60',CL12' '
         DC    X'11',AL1(17,44),X'1D60',CL12' '
         DC    X'11',AL1(18,44),X'1D60',CL12' '
LMIN1    EQU   *-MIN1
         SPACE 1
MIN2     DC    X'27F142'
SEGMU1   DC    X'11',AL1(7,58),X'1D60',CL12' '                  GP96128
         DC    X'11',AL1(8,58),X'1D60',CL12' '
         DC    X'11',AL1(9,58),X'1D60',CL12' '
         DC    X'11',AL1(10,58),X'1D60',CL12' '
         DC    X'11',AL1(11,58),X'1D60',CL12' '
         DC    X'11',AL1(12,58),X'1D60',CL12' '
         DC    X'11',AL1(13,58),X'1D60',CL12' '
         DC    X'11',AL1(14,58),X'1D60',CL12' '
         DC    X'11',AL1(15,58),X'1D60',CL12' '
         DC    X'11',AL1(16,58),X'1D60',CL12' '
         DC    X'11',AL1(17,58),X'1D60',CL12' '
         DC    X'11',AL1(18,58),X'1D60',CL12' '
LMIN2    EQU   *-MIN2
CLOB2END EQU   *-3           POSITION OF LAST POSSIBLE SBA SEQUENCE
         EJECT ,
DIGITS   DS    0X
ZEROSEG1 DC    CL12' 0000000000 '
ZEROSEG2 DC    CL12'000000000000'
         DC    CL12'00        00'
         DC    CL12'00        00'
         DC    CL12'00        00'                               GP96127
         DC    CL12'00   00   00'
         DC    CL12'00   00   00'
         DC    CL12'00        00'                               GP96127
         DC    CL12'00        00'
         DC    CL12'00        00'
         DC    CL12'000000000000'
         DC    CL12' 0000000000 '
LDIGSEG  EQU   ZEROSEG2-ZEROSEG1
LDIGIT   EQU   *-ZEROSEG1
         SPACE 2
ONELIT   DC    CL12'     11     '
         DC    CL12'    111     '
         DC    CL12'   1111     '
         DC    CL12'     11     '
         DC    CL12'     11     '
         DC    CL12'     11     '
         DC    CL12'     11     '
         DC    CL12'     11     '
         DC    CL12'     11     '
         DC    CL12'     11     '
         DC    CL12' 1111111111 '
         DC    CL12' 1111111111 '
TWOLIT   DC    CL12' 2222222222 '
         DC    CL12'222222222222'
         DC    CL12'22        22'
         DC    CL12'          22'
         DC    CL12'          22'
         DC    CL12'         22 '
         DC    CL12'       22   '
         DC    CL12'     22     '
         DC    CL12'   22       '
         DC    CL12' 22         '
         DC    CL12'222222222222'
         DC    CL12'222222222222'
THREELIT DC    CL12' 3333333333 '
         DC    CL12'333333333333'
         DC    CL12'33        33'
         DC    CL12'          33'
         DC    CL12'          33'
         DC    CL12'       3333 '
         DC    CL12'       3333 '
         DC    CL12'          33'
         DC    CL12'          33'
         DC    CL12'33        33'
         DC    CL12'333333333333'
         DC    CL12' 3333333333 '
FOURLIT  DC    CL12'       444  '
         DC    CL12'      4444  '
         DC    CL12'     44 44  '
         DC    CL12'    44  44  '
         DC    CL12'   44   44  '
         DC    CL12'  44    44  '
         DC    CL12' 44     44  '
         DC    CL12'444444444444'
         DC    CL12'444444444444'
         DC    CL12'        44  '
         DC    CL12'        44  '
         DC    CL12'        44  '
FIVELIT  DC    CL12'555555555555'
         DC    CL12'555555555555'
         DC    CL12'55          '
         DC    CL12'55          '
         DC    CL12'5555555555  '
         DC    CL12'55555555555 '
         DC    CL12'          55'
         DC    CL12'          55'
         DC    CL12'          55'
         DC    CL12'55        55'
         DC    CL12'55555555555 '
         DC    CL12' 555555555  '
SIXLIT   DC    CL12' 6666666666 '
         DC    CL12'666666666666'
         DC    CL12'66          '
         DC    CL12'66          '
         DC    CL12'66          '
         DC    CL12'66666666666 '
         DC    CL12'666666666666'
         DC    CL12'66        66'
         DC    CL12'66        66'
         DC    CL12'66        66'
         DC    CL12'666666666666'
         DC    CL12' 6666666666 '
SEVENLIT DC    CL12'777777777777'
         DC    CL12'777777777777'
         DC    CL12'         77 '
         DC    CL12'        77  '
         DC    CL12'       77   '
         DC    CL12'      77    '
         DC    CL12'     77     '
         DC    CL12'    77      '
         DC    CL12'   77       '
         DC    CL12'  77        '
         DC    CL12' 77         '
         DC    CL12' 77         '
EIGHTLIT DC    CL12' 8888888888 '
         DC    CL12'888888888888'
         DC    CL12'88        88'
         DC    CL12'88        88'
         DC    CL12'88        88'
         DC    CL12' 8888888888 '
         DC    CL12' 8888888888 '
         DC    CL12'88        88'
         DC    CL12'88        88'
         DC    CL12'88        88'
         DC    CL12'888888888888'
         DC    CL12' 8888888888 '
NINELIT  DC    CL12' 9999999999 '
         DC    CL12'999999999999'
         DC    CL12'99        99'
         DC    CL12'99        99'
         DC    CL12'99        99'                               GP96127
         DC    CL12'999999999999'
         DC    CL12' 99999999999'                               GP96127
         DC    CL12'          99'
         DC    CL12'          99'
         DC    CL12'99        99'                               GP96127
         DC    CL12'999999999999'                               GP96127
         DC    CL12' 9999999999 '                               GP96127
BLANKSEG DC    12CL12' '     FOR LEADING ZERO DISPLAY OF NOTHING
         END   TSCLOCK
