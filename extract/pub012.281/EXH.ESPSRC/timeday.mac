TIMEDAY  TITLE 'T I M E D A Y  ***  TIME AND DATE CONVERSION ROUTINE'
***********************************************************************
**                                                                   **
**   WRITTEN IN THE DARK AGES BY DICK OXLEY, THEN AT N.I.S.          **
**   USED BY CoBOL AND ForTran PROGRAMS TO GET DATE AND TIME IN      **
**   VARIOUS FORMATS (SEE CSECT USERSECT)                            **
**                                                                   **
**   CALL TIMDAY(string59)   CURRENT TIME AND DATE                   **
**   CALL TIMDAY(STRING59,TIME,DAY) time in binary; date julian form **
**     use VL option for ASM calls.                                  **
**                                                                   **
**   Day of week is right justified, month is left justified.        **
**   Tables changed to lower case names.                             **
**   Note that save area isn't necessary, but allows for later mods. **
**                                                                   **
**-------------------------------------------------------------------**
**   Maintenance:                                                    **
**  2012-06-16  GYP  Fixed day of week code for Y2K.                 **
**                   Labels have been changed to be more meaningful? **
**                                                                   **
***********************************************************************
         PRINT NOGEN
         ENTRY TIMDAY
TIMEDAY  START 0                                                GP12168
TIMDAY   EQU   TIMEDAY                                          GP12168
         SPACE 1
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING TIMEDAY,R12
         LR    R9,R13
         LA    R13,SAVE
         ST    R9,4(,R13)
         ST    R13,8(,R9)
         LR    R4,R1                   SAVE CALLING LIST PTR.
         L     R10,0(,R4)              GET USERS AREA ADDRESS
         USING USERSECT,R10            MAP FOR USER AREA
         MVI   USERAREA,C' '           CLEAR IT
         MVC   USERAREA+1(L'USERAREA-1),USERAREA
         TM    0(R4),X'80'             USE SYSTEM DATE AND TIME ?
         BO    SYSTIME                 BRANCH IF SO
         TM    4(R4),X'80'             2-WORD LIST ?             82006
         BO    BADPARM                  YES - ERROR              82006
         LM    R2,R3,4(R4)             POSSIBLE TIME/DATE PTRS.
         TM    8(R4),X'80'             USER TIME AND DATE ?
         BO    HISTIME
BADPARM  WTO   '* TIMEDAY * - ERROR IN PARAMETER LIST',          82006 *
               ROUTCDE=11,DESC=7                                 82006
         LA    R15,16                  ERROR RETURN CODE
         B     COMMEXIT                                         GP12168
         SPACE 1
HISTIME  L     R0,0(,R2)               LOAD USER TIME           GP12168
         L     R1,0(,R3)               LOAD USER DATE           GP12168
         B     DOTIME                  BRANCH TO COMMON CODE
         SPACE 1
SYSTIME  TIME  BIN                     GET TIME
DOTIME   ST    R0,BINTIME              SAVE TIME
         ST    R1,PACKDAY              SAVE DATE
         LA    R8,TIME24               OUTPUT AREA
         BAL   R14,FORMTIME            CONVERT TIME (24 HR. CLOCK)
         L     R0,BINTIME              RE-INIT. TIME
         LA    R8,OUTTIME              OUTPUT AREA
         LCR   R8,R8                   INDICATE 12 HR. CLOCK
         BAL   R14,FORMTIME            CONVERT TIME             GP12168
*NEXT*   B     DODATE                  GO DO DATE CONVERSIONS
         SPACE 1
*              DATE PROCESSING ROUTINE
         SPACE 1
DODATE   CLI   PACKDAY,X'19'           CENTURY SET ?            GP12168
         BNL   DODATEOK                  YES, LEAVE IT          GP12168
         AP    PACKDAY,=P'1900000'     MAKE PROPER CENTURY      GP12168
DODATEOK UNPK  WORKJUL,PACKDAY         JULIAN IN PRINTABLE FORM GP12168
         OI    WORKJUL+L'WORKJUL-1,C'0'   BE POSITIVE           GP12168
         MVC   OUTJUL(2),WORKJUL+2     YEAR SANS CENTURY        GP12168
         MVI   OUTJUL+2,C'.'           SEPARATOR                GP12168
         MVC   OUTJUL+3(3),WORKJUL+4   JULIAN DAY               GP12168
         MVC   OUTDATE+4(4),WORKJUL    MOVE YEAR TO USERS AREA  GP12168
         LM    R15,R1,MONBXLE          LOAD BXLE POINTERS       GP12168
         LA    R4,MODAYR
         ZAP   MONTAB2+1(2),=P'28'     SET DEFAULT - NO LEAP    GP12168
         TM    PACKDAY+1,1             ODD YEAR ?
         BNZ   LOOPMON                 IF YES, NOT LEAP
         TM    PACKDAY+1,X'12'         IF EVEN, LEAP YEAR ?
         BM    LOOPMON                 BRANCH IF NOT
         ZAP   MONTAB2+1(2),=P'29'     SET FOR LEAP YEAR        GP12168
* Nota Bene: no century tests; valid 1901 - 2099
LOOPMON  SP    PACKDAY+2(2),1(2,R15)   SUB DAYS FROM TOTAL      GP12168
         BNP   HAVEMON                 IF NOT POSITIVE, FOUND   GP12168
         BXLE  R15,R0,LOOPMON          BUMP TO NEXT MONTH       GP12168
         B     LOOPMON                 GO CHECK NEXT MONTH
         SPACE 1
HAVEMON  SR    R2,R2
         IC    R2,0(,R15)              GET L'MONTH NAME         GP12168
         EX    R2,EXMVCMON             MOVE NAME OF MONTH       GP12168
         MVC   OUTDATE(2),3(R15)       MOVE MONTH NUMERIC       GP12168
         AP    PACKDAY+2(2),1(2,R15)   ADD DAYS BACK IN         GP12168
         MVC   DOUBLE(4),=X'F0202120'                           GP12168
         ED    DOUBLE(4),PACKDAY+2     EDIT DAY
         MVC   OUTDATE+3(2),DOUBLE+2   MOVE DAY
         LA    R4,2(R2,R4)             BUMP OUTPUT ADDRESS
         CLI   DOUBLE+2,X'F0'          BLANK LEADING DIGIT ?    GP12168
         BNE   TWODAY                  BRANCH IF NOT
         BCTR  R4,0                    GET RID OF LEADING ZERO
         MVI   DOUBLE+2,C' '           REPLACE LEADING ZERO     GP12168
TWODAY   MVC   0(2,R4),DOUBLE+2        MOVE DAY
         MVC   2(2,R4),=C', '                                   GP12168
         MVC   4(4,R4),WORKJUL         MOVE YEAR                GP12168
         SPACE 1
*              DAY OF WEEK ROUTINE
         SPACE 1
         PACK  DOUBLE,OUTDATE(2)
         CVB   R1,DOUBLE               MONTH
         PACK  DOUBLE,OUTDATE+3(2)
         CVB   R2,DOUBLE               DAY
         PACK  DOUBLE,OUTDATE+4(4)
         CVB   R3,DOUBLE               YEAR
         CH    R1,=H'2'                JAN. OR FEB. ?
         BH    *+10                    NO
         LA    R1,12(,R1)              YES, USE
         BCTR  R3,0                    PREV. YEAR
         LA    R4,2(,R2)               FINAGLE CONSTANT
         AR    R4,R1                   TWO TIMES
         AR    R4,R1                   THE MONTH
         LA    R7,1(,R1)
         MH    R7,=H'3'
         SR    R6,R6                   3*MONTH/5
         D     R6,=F'5'
         AR    R4,R7
         AR    R4,R3                   +YEAR
         SRA   R3,2                    +YEAR/4
         AR    R4,R3
         SR    R2,R2
         D     R2,=F'25'               -YEAR/100
         SR    R4,R3
         SRA   R3,2
         AR    R4,R3                   +YEAR/400
         LR    R5,R4
         SR    R4,R4                   MONTHS/7
         D     R4,=F'7'
         SPACE 1
*              REG. 4 = 0,SAT.  1,SUN.  2,MON.  ETC.
         SPACE 1
         MH    R4,=H'9'
         LA    R5,DAYNAME(R4)
         MVC   OUTWEEKD,0(R5)
         MVI   OUTDATE+2,C'/'
         MVI   OUTDATE+5,C'/'
         MVC   OUTWEEKD+L'OUTWEEKD(2),=C', '                    GP12168
         SR    R15,R15                 GOOD RETURN
COMMEXIT L     R13,SAVE+4
         L     R14,R12(,R13)                                    GP12168
         LM    R0,R12,20(R13)                                   GP12168
         BR    R14
EXMVCMON MVC   0(0,R4),5(R15)          NAME OF MONTH            GP12168
         SPACE 1
*              TIME CONVERSION ROUTINE
         SPACE 1
FORMTIME SR    R4,R4                   SET FOR 24 HR. CLOCK
         LTR   R8,R8                   12 HR. CLOCK ?
         BP    FORMTM24                BRANCH IF NOT
         LCR   R8,R8
         LA    R4,=C' a.m.'            SET FOR A.M.
         CL    R0,=A(12*3600*100)      PAST NOON ?
         BL    FORMTM24                BRANCH IF NOT
         LA    R4,=C' p.m.'            SET FOR P.M.
         CL    R0,=A(13*3600*100)      PAST ONE ?
         BL    *+8                     BRANCH IF SO
         SL    R0,=A(12*3600*100)      MINUS 12 HRS.
FORMTM24 LR    R3,R0
         SR    R2,R2
         D     R2,=F'100'              DISPOSE OF 100THS.
         SR    R2,R2
         D     R2,=F'60'               DISPOSE OF SECONDS
         SR    R2,R2
         D     R2,=F'60'               GET MINUTES
         MH    R2,=H'10'               SHIFT
         CVD   R2,DOUBLE
         UNPK  3(3,R8),DOUBLE+6(2)     MOVE TO USERS AREA
         MH    R3,=H'10'               SHIFT HOURS
         CVD   R3,DOUBLE
         UNPK  0(3,R8),DOUBLE+6(2)     MOVE TO USERS AREA
         MVI   2(R8),C':'
         LTR   R4,R4                   12 HR. CLOCK ?
         BZR   R14                     BRANCH IF NOT            GP12168
         CLI   0(R8),C'0'              LEADING ZERO ?
         BNE   FORMAMPM                BRANCH IF NOT
         MVI   0(R8),C' '              GET RID OF ZERO
FORMAMPM MVC   5(5,R8),0(R4)           ADD AM PM
         BR    R14                     RETURN                   GP12168
         SPACE 2
DOUBLE   DS    D
SAVE     DS    18F
BINTIME  DS    F             FROM USER OR TIME SVC
PACKDAY  DS    F             FROM USER OR TIME SVC
WORKJUL  DC    CL7'ccyyddd'                                     GP12168
         SPACE 2
MONBXLE  PARKEYBX MONTAB                                        GP12168
MONTAB   DC    AL1(6),PL2'31',C' 1',CL9'January'                GP12168
MONTAB2  DC    AL1(7),PL2'28',C' 2',CL9'February'               GP12168
         DC    AL1(4),PL2'31',C' 3',CL9'March'                  GP12168
         DC    AL1(4),PL2'30',C' 4',CL9'April'                  GP12168
         DC    AL1(2),PL2'31',C' 5',CL9'May'                    GP12168
         DC    AL1(3),PL2'30',C' 6',CL9'June'                   GP12168
         DC    AL1(3),PL2'31',C' 7',CL9'July'                   GP12168
         DC    AL1(5),PL2'31',C' 8',CL9'August'                 GP12168
         DC    AL1(8),PL2'30',C' 9',CL9'September'              GP12168
         DC    AL1(6),PL2'31',C'10',CL9'October'                GP12168
         DC    AL1(7),PL2'30',C'11',CL9'November'               GP12168
MONTABN  DC    AL1(7),PL2'31',C'12',CL9'December'               GP12168
         SPACE 2
DAYNAME  DC    CL9' Saturday'
         DC    CL9'   Sunday'
         DC    CL9'   Monday'
         DC    CL9'  Tuesday'
         DC    CL9'Wednesday'
         DC    CL9' Thursday'
         DC    CL9'   Friday'
         SPACE 2
USERSECT DSECT ,
TIME24   DS    CL5'hh:mm'
OUTTIME  DS    CL10'hh:mm x.m.'
OUTJUL   DS    CL6'yy.ddd'
OUTDATE  DS    CL8'mm/dd/yy'
OUTWEEKD DS    CL9'      day',C', '
MODAYR   DS    CL19'    month dd, ccyy'
USERAREA EQU   TIME24,*-TIME24,C'C'                             GP12168
         SPACE 1
         YREGS ,                                                GP12168
         END
