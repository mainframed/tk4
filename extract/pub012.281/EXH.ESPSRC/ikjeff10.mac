EFF10    TITLE 'I K J E F F 1 0  ***  SUBMIT EXIT'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES    SET AND SHOW OPTIONS
         SPACE 1
***********************************************************************
*                                                                     *
*     THIS IS A QUICK AND DIRTY EXIT FOR AUTHORIZING USERS TO RUN     *
*     JOBS WITH JOBNAMES OTHER THAN THEIR USERID, AND TO USE ANY      *
*     ACCOUNT NUMBER AUTHORIZED IN THEIR (LOCAL) USER DEFINITION.     *
*                                                                     *
*     AS AN ADDED GOODY, EMBEDDED COMMANDS ARE FAILED                 *
*                                                                     *
*     ONLY A LIMITED AMOUNT OF WORK WAS INVESTED IN THIS, AS ANY      *
*     LITERATE KLUTZ CAN USE SYSOUT=(A,INTRDR) TO SUBMIT JOBS         *
*     WHILE BYPASSING THIS EXIT.                                      *
*                                                                     *
***********************************************************************
         SPACE 1
         PRINT &PRTSOR
IKJEFF10 SAVEM ZERO12,BASE=R12,PARM=R11                    ADDED 92340
         L     R11,0(,R11)   LOAD PARAMETER LIST ADDRESS
         USING IEDSECTD,R11  DECLARE CALLING PARAMETERS
         SPACE 1
         L     R10,IESUBCTP  GET SWITCH POINTER, ETC.
         USING IESUBCTD,R10
         ICM   R1,15,IEMSGP  RE-ENTRY FOR MESSAGE DELETION ?
         BNZ   FREEMESS
         SLR   R5,R5         SET PROVISIONAL GOOD RETURN CODE
*FAILS   TM    IESTMTYP,IESCMD  COMMAND ?
*FAILS   BNZ   HAVECMND      YES
         TM    IESTMTYP,IESJOB  JOB STATEMENT ?
         BZ    ENDPROC       NO; GET OUT
         ICM   R4,15,IEEXITWD  HAVE WE BEEN HERE BEFORE ?
         BNZ   ENDPROC       YES; JUST PERMIT
         OI    IEEXITWD,X'80'  PROV. NO LOUD FLAG
         EXTRACT @PSCB,'S',FIELDS=PSB,MF=(E,EXTLIST)
         L     R7,@PSCB      LOAD POINTER TO PSCB
         USING PSCB,R7
         TM    PSCBATR1,PSCBCTRL   OPERATOR AUTHORITY ?
         BO    ENDPROC       YES; ALLOW THIS REQUEST
         SERVICE ACGET,MYACCT   GET THE CURRENT USER'S ACCOUNT
         BXH   R15,R15,ENDPROC  MAJOR FAILURE - ALLOW THIS
         STC   R0,MYPRIV     ALSO SAVE ACCOUNT PRIVILEGES
         TM    MYPRIV,VAASYS+VAASUP  SYSTEMS OR TECH.SUPPORT ?
         BNZ   ENDPROC       YES; ALLOW THIS REQUEST
         TM    PSCBATR1,PSCBJCL  ALLOWED JOB MANIPULATION ?
         BNZ   HAVEJCL       YES; PROCEED
         DROP  R7
BADAUTH  L     R0,GETMINFO   SUBPOOL 0, LENGTH DECIMAL 84
         GETMAIN R,LV=(0)    GET MESSAGE BUFFER
         ST    R1,IEMSGP     STORE POINTER TO MESSAGE BUFFER
         MVC   REJMSG-REJMSG(MSGUNALN,R1),MSGUNAUT  UNAUTHORIZED
         LA    R5,IEMSG      ISSUE MESSAGE AND RETURN
         B     ENDPROC       REJECT AFTER MESSAGE
         SPACE 1
HAVEJCL  OI    IETAKEEX,IETCMD+IETJOB  INTERCEPT COMMANDS AND JOBS
         L     R8,PSAAOLD-PSA    GET MY ASCB
         USING ASCB,R8
         ICM   R8,15,ASCBTSB GET THE TSB
         BZ    ENDPROC       BATCH JOB - LET IT BE
         USING TSB,R8
         XC    A$SVCRB(A$SLENRB),A$SVCRB  CLEAR
         MVC   A$SID,=C'A$RB'  IDENTIFY
         MVC   A$SFCTN(3),=AL1(A$SFLOUD,A$STSO,A$SUKEY+A$SBUILD)
         MODESET EXTKEY=ZERO,SAVEKEY=(2)  SWAP KEYS
         MVC   A$SACCT,TSBTRMID  SET TERMINAL ID
         MODESET KEYADDR=(2) RESTORE KEY (1)
         DROP  R8
         L     R15,IEUSRIDP  GET USER-ID POINTER
         MVC   A$SUID,0(R15)  MOVE TO PARM LIST
         ACCTSVC A$SVCRB,ERR=BADJOBN  IF SVC NOT AVAILABLE
         BXH   R15,R15,ENDPROC  MAJOR ERROR - ALLOW
         ICM   R4,15,A$SBUFR  GET LOUD ADDRESS
         BZ    BADAUTH       TOO BAD
         USING A$LOUD,R4
         ST    R4,IEEXITWD   SAVE FOR NEXT ENTRY
         L     R1,IECARDP    GET JOB CARD IMAGE
         LA    R14,1
         LA    R15,70(R1)    LAST USABLE COLUMN
         LA    R1,2(,R1)     ASSUME FIRST TWO COLUMNS ARE //
         CLI   0(R1),C'0'    INVALID NAME ?
         BNL   BADJOBN       YES
         CLI   0(R1),C' '    NO NAME AT ALL ?
         BE    BADJOBN       HOW DID THIS COME TO PASS ?
         SLR   R2,R2         CLEAR FOR TRT RESULT BYTE
         TRT   0(9,R1),CHARTAB  LOOK FOR BLANK OR GARBAGE
         CH    R2,=H'4'      CHECK RESULT
         BL    BADJOBN       NINE OR MORE CHARACTERS IN NAME
         BH    BADJOBN       INVALID CHARACTER
LOOKJOB  CLC   =C'JOB ',0(R1)  LOOK FOR JOB
         BE    HAVEJOB
         CLI   0(R1),C' '    LEADING BLANK ?
         BNE   BADFORM       NO; ERROR
         BXLE  R1,R14,LOOKJOB  SKIP MORE BLANKS
         B     BADFORM       ERROR IN SCAN ?
HAVEJOB  SLR   R1,R1         NOW LOOK FOR ACCOUNT
         ICM   R1,1,IEOPRAND  GET OPERAND COLUMN                 92340
         BNP   BADFORM       HUH ?                               92340
         BCTR  R1,0          MAKE RELATIVE                       92340
         AL    R1,IECARDP    POINT TO PRESUMED ACCOUNT FIELD     92340
*DEFER*  CLI   0(R1),C''''   QUOTED STRING ?
*DEFER*  BE    HAVEACCP      YES
         CLI   0(R1),C'('    PARENTHESIZED ?
         BE    HAVEACCP      YES
    B BADFORM           *FIX AFTER JES2 EXITS AND IEFUJV UPGRADED*
         MVC   SJ1ACCT,0(R1)  MOVE AS IS
         B     HAVEACCT
HAVEACCP MVC   SJ1ACCT,1(R1)  MOVE AS IS
         CLI   5(R1),C','    FOUR,FOUR FORMAT ?
         BNE   HAVEACCT      NO; ASSUME 8
         MVC   SJ1ACCT+4(4),6(R1)  MOVE REST
HAVEACCT TRT   SJ1ACCT,CHARTAB  VALIDITY CHECK CHARACTERS
         BNZ   BADFORM       MALFORMED ACCOUNT FIELD
         LM    R14,R15,SJ1ACCT  LOAD ACCOUNT
         N     R14,=X'C0F0F0F0'  ALPHA+3 DIGITS ?
         C     R14,=X'C0F0F0F0'  REALLY ?
         BNE   BADFORM       NO; FAIL
         N     R15,=X'C0C0C0C0'  SUBACCOUNT MUST BE
         C     R15,=X'C0C0C0C0'    ALPHAMERIC
         BNE   BADFORM
         CLC   A$UACCT(4),SJ1ACCT  SAME MAJOR ACCOUNT ?
         BNE   VALUEXAC      NO; CHECK OTHER ACCOUNTS
         CLC   A$UACCT+4(4),SJ1ACCT+4  SAME SUBACCOUNT ?
         BE    ENDPROC       YES; REWRITE THE RECORD
         TM    A$UPRIVY,A$UPFSUB  MAY USE DIFFERENT SUBACCOUNT ?
         BNZ   ENDPROC       YES; UPDATE THE RECORD
VALUEXAC LA    R6,A$UACACC  POINT TO EXTRA ACCOUNTS
         LA    R7,(A$UPROUT-A$UACACC)/(L'A$UACCFG+L'A$UACCNO)
VALUEXLP LA    R14,A$UACCNO-A$UACACC(,R6)  POINT TO FULL ACCOUNT
         TM    A$UACCFG-A$UACACC(R6),A$UAFJCL  JCL PRIVILEGES ?
         BZ    VALUEXLB      NO; SKIP THIS ENTRY
         CLI   A$UACCNO-A$UACACC(R14),0      EMPTY SLOT ?
         BE    VALUEXLB      YES; SKIP
         CLI   A$UACCNO-A$UACACC+4(R14),0 HEX ZERO (BAD DEFINITION) ?
         BE    VALUECL4      YES; DO SHORT COMPARE
         TM    A$UACCFG-A$UACACC(R6),A$UAFSUB  SUBACCOUNT ?
         BZ    VALUECLC      YES; UPDATE THE RECORD
VALUECL4 LA    R0,4          COMPARE ACCOUNT ONLY
VALUECLC LA    R1,SJ1ACCT    POINT TO JOB'S ACCOUNT
         LA    R0,8          SET LENGTH OF ACCOUNT
         BAL   R9,CLCMASK    COMPARE MASKED ACCOUNT
         BE    ENDPROC       MATCH - ACCEPT
VALUEXLB LA    R6,L'A$UACCFG+L'A$UACCNO(,R6)
         BCT   R7,VALUEXLP   TRY AGAIN
         SPACE 1
BADACCT  L     R0,GETMINFO   SUBPOOL 0, LENGTH DECIMAL 84
         GETMAIN R,LV=(0)    GET MESSAGE BUFFER
         ST    R1,IEMSGP     STORE POINTER TO MESSAGE BUFFER
         MVC   REJMSG-REJMSG(MSGUNACN,R1),MSGUNACT  BAD ACCOUNT
         LA    R5,IEMSG      ISSUE MESSAGE AND RETURN
         B     ENDPROC       REJECT AFTER MESSAGE
         SPACE 1
BADJOBN  L     R0,GETMINFO   SUBPOOL 0, LENGTH DECIMAL 84
         GETMAIN R,LV=(0)    GET MESSAGE BUFFER
         ST    R1,IEMSGP     STORE POINTER TO MESSAGE BUFFER
         MVC   REJMSG-REJMSG(MSGUNJBN,R1),MSGUNJOB  BAD JOBNAME
         LA    R5,IEMSG      ISSUE MESSAGE AND RETURN
         B     ENDPROC       REJECT AFTER MESSAGE
         SPACE 1
BADFORM  L     R0,GETMINFO   SUBPOOL 0, LENGTH DECIMAL 84
         GETMAIN R,LV=(0)    GET MESSAGE BUFFER
         ST    R1,IEMSGP     STORE POINTER TO MESSAGE BUFFER
         MVC   REJMSG-REJMSG(MSGUNFRN,R1),MSGUNFRM  BAD ACCOUNT
         LA    R5,IEMSG      ISSUE MESSAGE AND RETURN
         B     ENDPROC       REJECT AFTER MESSAGE
         SPACE 2
***********************************************************************
*                                                                     *
*        UNAUTHORIZED USER HAS COMMITTED A COMMAND - INSTEAD OF       *
*        FAILING IT, CHANGE IT TO A COMMENT                           *
*                                                                     *
***********************************************************************
         SPACE 1
HAVECMND L     R1,IECARDP    GET CARD IMAGE
         MVC   0(3,R1),=C'//*'  MAKE COMMENT, DEFINITELY
         B     ENDPROC       PROCEED
         SPACE 1
***********************************************************************
*                                                                     *
*        SECOND TIME ENTRY (FREE MESSAGE BUFFER)                      *
*                                                                     *
***********************************************************************
         SPACE 1
FREEMESS L     R0,GETMINFO   LOAD SUBPOOL NUMBER AND LENGTH
         FREEMAIN  R,LV=(0),A=(1)      FREE MESSAGE BUFFER
         SLR   R6,R6                   ZERO OUT MESSAGE POINTER ENTRY
         ST    R6,IEMSGP               IN PARMLIST
         LA    R5,IEABORT    SET RETURN CODE TO 16 - DELETE
         SPACE 1
***********************************************************************
*                                                                     *
*        RETURN TO CALLER;  RETURN CODE IS IN R5                      *
*                                                                     *
***********************************************************************
         SPACE 1
ENDPROC  ENDM  RC=(R5)       RETURN WITH CC IN R5
         SPACE 1
*        SUBROUTINE TO COMPARE MASKED ACCOUNT NUMBERS:
*        MASK ADDRESS R14, TEST ADDRESS R1, LENGTH R0, RETURN R9
*
CLCMASK  CMASK ML=R0,L=R0,MA=R14,A=R1
         SPACE 2
CHARTAB  DC    256AL1(8)     DECLARE ALL AS INVALID
         TRENT CHARTAB,4,C' '   BLANK IS SPECIAL
         TRENT ,0,C'#',C'$',C'@'  NATIONAL GOODIES
         TRENT ,0,(C'A',9),(C'J',9),(C'S',8),(C'0',10)  GOODIES
*        READ-ONLY DATA
GETMINFO DC    A(REJSIZE)    SUBPOOL/LENGTH FOR MESSAGE GETMAIN
MSGUNAUT VCON  'MSG551 You are not authorized for JOB submission'
MSGUNALN EQU   *-MSGUNAUT    LENGTH OF MESSAGE
MSGUNJOB VCON  'MSG552 The jobname on the JOB card is malformed'
MSGUNJBN EQU   *-MSGUNJOB    LENGTH OF MESSAGE
MSGUNACT VCON  'MSG553 You are not authorized to use the JOB account'
MSGUNACN EQU   *-MSGUNACT    LENGTH OF MESSAGE
MSGUNFRM VCON  'MSG554 The JOB card is syntactically invalid'
MSGUNFRN EQU   *-MSGUNFRM    LENGTH OF MESSAGE
         SPACE 1
REJMSG   DSECT                         DSECT FOR RETURN MESSAGE
REJMLEN  DS    H
REJJOB   DS    CL4
REJSLOT  DS    CL(69+1+8)
REJSIZE  EQU   *-REJMSG
         SPACE 2
SAVE     DSECT ,             SAVE/WORK AREA
MYACCT   DS    CL8           CURRENT ACCOUNT
SJ1ACCT  DS    CL8           SUBMITTED JOB'S ACCOUNT
EXTLIST EXTRACT MF=L
@PSCB    DS    A             ADDRESS OF PSCB
A$SVCRB  A$SVCRB DSECT=NO,PFX=A$S
MYPRIV   DS    C             USER'S PRIVILEGES
SAVEND   EQU   *
         SPACE 2
         PRINT &PRTMAC
         IKJEFFIE IETYPE=SUBMIT
         SPACE 1
         PRINT &PRTSYS
A$LOUD   A$LOUD DSECT=YES
A$GDA    A$GDA DSECT
         IKJPSCB ,
         IKJTSB ,
         CVT   DSECT=YES
         IHAPSA ,
         IHAASCB ,
USERCVT  USERCVT ,
         END   ,
