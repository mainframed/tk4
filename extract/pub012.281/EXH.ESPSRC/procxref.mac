PXRF     TITLE 'P R O C X R E F  ***  JCL CROSS-REFERENCE PROGRAM'
***********************************************************************
*                                                                     *
*     DSNAME CROSS REFERENCE PROGRAM - 'NSP011'                       *
*        FROM CB&T MODS TAPE FILE 379                                 *
*                                                                     *
*     THIS PROGRAM WILL PRODUCE A DSNAME CROSS REFERENCE LISTING      *
*     FROM ANY NUMBER OF PROCLIBS - DDNAMES PROCXXXX             90232
*                                                                     *
*     PROC, EXEC AND DD STATEMENTS ARE USED.                          *
*     JOB, COMMENT AND PEND STATEMENTS ARE DISCARDED.                 *
*                                                                     *
*     PROGRAM 'NSP012' IS USED TO READ THE PROCLIBS.                  *
*                                                                     *
*     GENERAL FLOW:                                                   *
*                        IN  - PROCLIBS                               *
*                        OUT - SORTIN    - DSNAME RECORDS             *
*                        OUT - SYSPRINT  - EXCEPTION LIST             *
*                                                                     *
*          1. BUILD 1 JCL STATEMENT FROM 1 OR MORE INPUT RECORDS.     *
*                                                                     *
*          2. IF PROC STATEMENT, STORE SYMBOLICS IN TABLE.            *
*                                                                     *
*          3. IF EXEC OR DD STATEMENT,                                *
*                     RESOLVE SYMBOLICS,                              *
*                     SEARCH FOR KEYWORDS,                            *
*                     STORE IN OUTPUT RECORD.                         *
*                                                                     *
*          4. SORT BY  (DSN, O/I, PROC, STEP)                         *
*                                IN  - SORTIN                         *
*                                OUT - SORTOUT                        *
*                                                                     *
*          5. 'NSP013'  -  PRINT BY DSNAME                            *
*                                IN  - SORTOUT                        *
*                                OUT - A60NXDSN  (REPORT)             *
*                                                                     *
*          6. 'NSP014'  -  PRINT TAPES WITH NO EXPDT                  *
*                                IN  - SORTOUT                        *
*                                WRK - SYSUT1                         *
*                                OUT - A60NDSNC  (REPORT)             *
*                                                                     *
*          8. 'NSP015'  -  SORT BY PROGRAM AND PRINT PGM CROSS REF    *
*                                IN  - SORTIN   (OUTPUT FROM A60PDSNA)*
*                                OUT - SORTOUT                        *
*                                IN  - SORTOUT                        *
*                                OUT - A60NDSNE                       *
*                                                                90232
*        REWRITTEN TO MATCH LOCAL PREJUDICES AND CONVENTIONS BY  90232
*        G. POSTPISCHIL, EXPERT SYSTEM PROGRAMMING, INC.         90232
*                                                                90232
*        NOW SUPPORTS VARIABLE NUMBER OF PROCLIBS.               90232
*        SYMBOLIC PARAMETER SUBSTITUTION FIXED.                  90232
*        MINOR ERRORS FIXED.                                     90232
*        LOCAL SUBROUTINES AND SERVICES SUBSTITUTED FOR OLD CODE.
*                                                                90232
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90232
         COPY  OPTIONGB      GET GLOBAL SETS                     90232
         SPACE 1                                                 90232
         SYSPARM LIST=YES    EXPAND AND SET GLOBALS              90232
         EJECT ,                                                 90232
         PRINT &PRTSOR                                           90232
PROCXREF SAVEM ZERO31,BASE=(R12),PARM=R8,BNDRY=PAGE              90232
*                                                                     *
HEX00    EQU   X'00'                    EQUATES
HEX20    EQU   X'20'
HEX40    EQU   X'40'
HEX80    EQU   X'80'
HEXF0    EQU   X'F0'
HEXFF    EQU   X'FF'
*                                                                     *
CHR0     EQU   C'0'
CHR1     EQU   C'1'
CHRI     EQU   C'I'
CHRO     EQU   C'O'
*                                                                     *
AMPER    EQU   C'&&'
ASTR     EQU   C'*'
BLANK    EQU   C' '
COMMA    EQU   C','
EQUAL    EQU   C'='
LEFTP    EQU   C'('
MINUS    EQU   C'-'
NULL     EQU   X'00'
PLUS     EQU   C'+'
POINT    EQU   C'.'
QUOTE    EQU   C''''         THIS IS REALLY AN APOSTROPHE !
RIGHTP   EQU   C')'
         SPACE 1                                                 90232
SYMTABSZ EQU   6400          SIZE OF SYMBOL TABLE                90232
STATMTSZ EQU   6400          SIZE OF EXPANDED STATEMENT          90232
         SPACE 2                                                 90232
         SERVINIT ,          LOAD THE @SERVICE ROUTINE           90232
         LA    R10,DYNWORK   POINT TO COMMON WORK AREA           90232
         DROP  R13                                               90232
         USING DYNWORK,R10   DECLARE IT                          90232
         L     R11,=A(RENTWORK)  COMMON DATA AREA                90232
         USING RENTWORK,R11  DECLARE IT                          90232
         MVI   SORTREC-1,C' '   PREPARE FOR CLEARING             90232
         MVC   SORTREC,SORTREC-1   CLEAR                         90232
         LA    R7,SORTREC    POINT TO WORK AREA                  90232
         USING XREC,R7       DECLARE IT                          90232
         MVI   W2STAT-1,C' '  SET FOR BLANKING                   90232
         LA    R1,WKAREA1    GET STATEMENT WORK AREA ADDRESS     90232
         A     R1,WKAR1LEN   SPACE PAST IT                       90232
         ST    R1,ADDRSYMT   SET SYMBOL TABLE ADDRESS            90232
         LA    R1,OUTANDIN   GET DCB                             90232
         ST    R1,OPLSTIN                                        90232
         OI    OPLSTIN,X'80'  MAKE LIST FOR OPEN/CLOSE           90232
         MVC   OUTANDIN(PATFILEL),PATFILE  MAKE DCB              90232
         MVC   OUTANDIN+DCBDDNAM-IHADCB(L'DCBDDNAM),=CL8'SORTIN' 90232
         LA    R1,WRKFILE    GET WORKFILE DCB                    90232
         ST    R1,OPLSTWRK                                       90232
         OI    OPLSTWRK,X'80'  MAKE LIST FOR OPEN/CLOSE          90232
         MVC   WRKFILE(PATFILEL),PATFILE  MAKE WORK DCB          90232
         SERVCALL LPALD,=CL8'@PRINTER'                           90232
         ST    R0,@PRINTER   LOAD THE @PRINTER ROUTINE           90232
         SERVCALL LPALD,=CL8'@INPREAD'                           90232
         ST    R0,@INPREAD   GET THE @INPREAD ROUTINE            90232
         SPACE 1                                                 90232
         PRTOPEN SYSPRINT,DEV=1  OPEN THE OUTPUT FILE            90232
         PRTF  INHDR1,TITLE=1   MAKE PAGE HEADER                 90232
         OPEN  (,(OUTPUT)),MF=(E,OPLSTIN)                        90232
         SPACE 1                                                 90232
         AIF   ('&LOCAL' NE 'CCSI').NOPARM                       90232
         SERVCALL ACGET,DB   GET CURRENT ACCOUNT NUMBER          90232
         SERVCALL ACTST,DB                                       90232
         STCM  R0,12,ACCTMASK  STASH BINARY ACCOUNT EQUIVALENT   90232
         STC   R0,DB         SAVE PRIVILEGES FOR TESTING         90232
         TM    DB,VAASYS+VAASUP  SYSTEMS OR TECH SUPPORT ?       90232
         BZ    NEXTPROC      NO; PROCEED                         90232
         OI    PFLAG,FGPRIVY  PRIVILEGED USER                    90232
         LA    R8,0(,R8)     ANY PARM POINTER ?                  90232
         LTR   R8,R8                                             90232
         BZ    NEXTPROC      NO                                  90232
         ICM   R8,7,1(R8)    GET PARM                            90232
         BZ    NEXTPROC                                          90232
         CLI   1(R8),6       A=#### ?                            90232
         BNE   NEXTPROC      NO                                  90232
         CLC   =C'A=',2(R8)  ACCOUNT PARM ?                      90232
         BNE   NEXTPROC      NO                                  90232
         SERVCALL ACTST,4(R8),LEN=4  GET BINARY                  90232
         STCM  R0,12,ACCTMASK                                    90232
         NI    PFLAG,255-FGPRIVY  RESET GENERAL MATCH            90232
.NOPARM  SPACE 1                                                 90232
NEXTPROC MVC   PROCIN(PROCINL),PATINPWK  MAKE INPUT LIST         90232
         LA    R1,PDBXLE     GET MEMBER DATA                     90232
         XC    PDDTYPE(PDDCLRLN),PDDTYPE  CLEAR (IN CASE SEQ.)   90232
         MVC   PDDRALIS(PDDRBLNK),PDDRALIS  MEMBER DATA          90232
         ST    R1,PROCIN+20  SET MEMBER DATA AREA ADDRESS        90232
         L     R1,ADDRTIOT   GET PREVIOUS TIOT ADDRESS           90232
         SERVCALL TIOLK,(R1) GET NEXT TIOT ENTRY                 90232
         LTR   R1,R0         ANY FOUND ?                         90232
         BZ    LIBEODAD      NO; ALL DONE                        90232
         ST    R1,ADDRTIOT   SET FOR NEXT TIME                   90232
         USING TIOENTRY,R1   DECLARE IT                          90232
         CLC   =C'PROC',TIOEDDNM  PROCLIB SELECTION CARD ?       90232
         BNE   NEXTPROC      NO; IGNORE                          90232
         MVC   PROCIN(8),TIOEDDNM  MOVE DD NAME                  90232
         SERVCALL TIODD,PROCIN LOOK FOR DD BY NAME               90232
         C     R0,ADDRTIOT   SAME ENTRY ?                        90232
         BE    PROCOPEN      YES; USE IT                         90232
         PRTLIST MSGDUPDD    SET DUPLICATE DD                    90232
         OI    RETCH,4       SET MINOR ERROR                     90232
         B     NEXTPROC                                          90232
MSGDUPDD FDPRT 'DD CARD FOR'                                     90232
         FDPRT PROCIN,8,PAD                                      90232
         FDPRT 'IS A DUPLICATE - IGNORED'                        90232
         FDPRT *END                                              90232
         SPACE 2                                                 90232
PROCOPEN INPOPEN PROCIN      OPEN CURRENT PROCLIB                90232
         BXH   R15,R15,BADDDERR  SIGNAL OPEN PROBLEM             90232
         SPACE 1                                                 90232
NEXTCARD INPGET ,            GET NEXT INPUT RECORD               90232
         C     R15,=F'4'     IS IT END OF FILE ?                 90232
         BE    INEOF                    YES
         BH    INPERR        OR POSSIBLY INPUT ERROR ?           90232
         LA    R5,0(,R1)     SAVE INPUT ADDRESS; CLEAR SIGN      90232
         AIF   ('&LOCAL' NE 'CCSI').NOACCT                       90232
         LTR   R1,R1         NEW MEMBER ?                        90232
         BNM   NEXTSAME      NO                                  90232
         TM    PFLAG,FGPRIVY PRIVILEGED USER ?                   90232
         BNZ   NEXTSAME      YES; PROCESS                        90232
NEXTTEST CLI   PDDUDLEN,2    FOUR-BYTE PARM ?                    90232
         BNE   NEXTSAME      NO; NOT OUR PROCLIB FORMAT          90232
         CLC   PDDRDATE,BLANKS  IN CHANGE DATE FORMAT ?          90232
         BNE   NEXTSAME      YES; WYLBUR FORMAT - NOT ACCT       90232
         L     R15,PDDNEXT   GET ADDRESS OF DIRECTORY ENTRY      90232
         AH    R15,PDDOSSI   GET OFFSET TO SSI                   90232
         CLC   ACCTMASK,0(R15)  MATCHING ACCOUNT MASK ?          90232
         BE    NEXTSAME      YES; PROCESS THIS MEMBER            90232
         INPFEOV ,           SKIP TO NEXT MEMBER                 90233
NEXTFLSH INPGET ,            GET ANOTHER RECORD                  90232
         C     R15,=F'4'     TEST                                90232
         BE    INEOF         ALL DONE - EOF                      90232
         BH    INPERR        BOO                                 90232
         LTR   R5,R1                                             90232
         BNM   NEXTFLSH                                          90232
         B     NEXTTEST      CHECK NEXT MEMBER                   90232
.NOACCT  SPACE 1                                                 90232
NEXTSAME MVC   INREC,0(R5)   PRESERVE FOR ERROR MESSAGES         90232
         CLC   0(3,R5),=C'//*'          IS IT COMMENT
         BE    NEXTCARD                                          90232
         CLC   0(2,R5),=C'//'           IS IT '//'
         BNE   NEXTCARD                                          90232
         MVC   XPROC,PDDRNAME  SET MEMBER NAME                   90232
         BAL   R9,CLEAR10    CLEAR WORK AREA                     90232
         LA    R0,70         SET LOOP CTR TO 70                  90232
         LA    R5,2(,R5)     SKIP JCL                            90232
         CLI   0(R5),BLANK              IS CHAR BLANK
         BE    NOSTNAME                                          90232
         BAL   R14,SCAN30    FIND NAME                           90232
         C     R1,=F'8'                 IS NAME > 8
         BH    ERROR10       YES; EXCESSIVE NAME LENGTH          90232
         MVC   W1NAME,FOUND             MOVE FOUND NAME
         SPACE 1                                                 90232
NOSTNAME BAL   R14,SCAN20    SCAN FOR NON-BLANK                  90232
         C     R0,=F'54'                DID SCAN GO PAST COL 16
         BNH   ERROR20       YES - ERROR                         90232
         BAL   R14,SCAN30    SCAN FOR STAT IDNT                  90232
         C     R1,=F'4'                 IS IDNT > 4
         BH    ERROR30       YES - ERROR                         90232
         MVC   W1IDNT,FOUND             MOVE FOUND STAT IDNT
         BAL   R14,SCAN20    SCAN FOR NON-BLANK                  90232
         C     R0,=F'20'                DID SCAN GO PAST COL 50
         BNH   TESTSTMT      YES - END OF STAT                   90232
         BAL   R14,SCAN40    SCAN FOR '-----' OR BLANK           90232
         LTR   R15,R15                  DID SCAN GO PAST COL 71
         BNZ   ERROR60                  YES
         LA    R6,W1STAT
         BCTR  R1,0                     SUBTRACT 1 FROM LENGTH
         EX    R1,MVC10                 EX MOVE
* MVC10  MVC   0(0,R6),FOUND                                          *
         LA    R1,1(,R1)                                         90232
         AR    R6,R1                    BUMP TO END FOR NXT STAT
         BCTR  R5,0                     BACKUP 1 CHAR
         CLI   0(R5),COMMA              WAS LAST CHAR ','
         BNE   TESTSTMT      NO - END OF STAT                    90232
         SPACE 1                                                 90232
CONTCARD INPGET ,            GET NEXT RECORD                     90232
         C     R15,=F'4'     IS IT END OF FILE ?                 90232
         BE    INEOF                    YES
         BH    INPERR        IS IT I/O ERROR ?                   90232
         LTR   R5,R1         TEST ADDRESS                        90232
         BNM   CARDCONT      OK ?                                90232
         INPKEEP ,           RETURN THIS AGAIN ON NEXT CALL      90232
         B     ERROR40       MISSING CONTINUATION                90232
CARDCONT MVC   INREC,0(R5)   PRESERVE FOR ERROR MESSAGES         90232
         CLC   0(3,R5),=C'//*'          IS IT COMMENT
         BE    CONTCARD      YES                                 90232
         CLC   0(3,R5),=C'// '          IS IT //BLANK
         BNE   ERROR40       NO - ERROR                          90232
         LA    R5,2(,R5)     POINT R5 TO COL 3                   90232
         LA    R0,70         SET BCT CTR TO 70                   90232
         BAL   R14,SCAN20    SCAN FOR NON-BLANK                  90232
         C     R0,=F'54'                ARE WE PAST COL 16
         BNH   ERROR50       YES - ERROR                         90232
         BAL   R14,SCAN40    SCAN FOR '-----' OR BLANK           90232
         LTR   R15,R15                  DID SCAN GO PAST COL 71
         BNZ   ERROR60                  YES
         BCTR  R1,0
         EX    R1,MVC10                 MOVE FOUND TO STAT
         LA    R1,1(,R1)                                         90232
         AR    R6,R1                    BUMP TO END OF STAT
         BCTR  R5,0
         CLI   0(R5),COMMA              IS IT END OF STATEMENT
         BE    CONTCARD      NO                                  90232
         SPACE 1                                                 90232
TESTSTMT L     R0,WKAR1LEN   SET MAX LENGTH OF STAT              90232
         CLC   W1IDNT,=CL4'JOB'         IS IT JOB STATEMENT
         BE    A300
         CLC   W1IDNT,=CL4'PROC'        IS IT PROC STATEMENT
         BE    PROC10
         CLC   W1IDNT,=CL4'PEND'        IS IT PROC END STATEMENT
         BE    A300
         CLC   W1IDNT,=CL4'EXEC'        IS IT EXEC STATEMENT
         BE    EXEC10
         CLC   W1IDNT,=CL4'DD'          IS IT DD STATEMENT
         BE    DD10
         SPACE 1                                                 90232
A300     CLC   XDSN,BLANKS              WAS DSNAME FOUND
         BE    A304                     NO
A310     PUT   OUTANDIN,XREC  GIVE OUTPUT RECORD TO SORT         90232
         INC   NUMSORT       COUNT CARDS TO SORT                 90232
A304     MVC   PROC(L'XPROC+L'XSTEP+L'XPGM),XPROC  SAVE PROC AND STEP
         MVC   SORTREC,SORTREC-1    CLEAR RECORD                 90232
         MVC   XPROC(L'XPROC+L'XSTEP+L'XPGM),PROC RESTORE PROC AND STEP
         B     NEXTCARD                                          90232
         SPACE 1                                                 90232
BADDDERR PRTLIST MSGDDBAD                                        90232
         OI    RETCH,4       SET MINOR ERROR                     90232
         B     INEOF         TRY TO CLOSE, ANYWAY                90232
MSGDDBAD FDPRT 'OPEN FAILED FOR DD'                              90232
         FDPRT PROCIN,8,PAD                                      90232
         FDPRT *END                                              90232
         SPACE 1                                                 90232
MSGBADDD FDPRT 'UNCORRECTED ERROR ON FILE'                       90232
         FDPRT PROCIN,8,PAD                                      90232
         FDPRT *END                                              90232
INPERR   PRTLIST MSGBADDD                                        90232
         OI    RETCH,16      SET MAJOR ERROR                     90232
INEOF    INPCLOSE DEV=1      CLOSE THE INPUT FILE                90232
         B     NEXTPROC      GET ANOTHER LIBRARY                 90232
         SPACE 1                                                 90232
LIBEODAD CLOSE MF=(E,OPLSTIN)  CLOSE SORT INPUT FILE             90232
         L     R2,OPLSTIN                                        90233
         FREEPOOL (R2)       FREE BUFFERS                        90233
         ICM   R15,15,NUMSORT  DID WE DO ANY ?                   90232
         BNZ   SORTSOME      YES; PROCEED                        90232
         OI    RETCH,12      SET ERROR CODE                      90232
         PRTF  ' NO VALID JCL FOUND TO PROCESS'                  90232
         B     RETCHED                                           90232
         SPACE 1                                                 90232
SORTSOME CVD   R15,DB        MAKE PACKED COUNT                   90233
         UNPK  SORTEST1,DB+5(3)  MAKE 'ESTIMATE'                 90233
         OI    SORTEST1+L'SORTEST1-1,C'0'  CHANGE ZONE TO NUMERIC
         MVC   SORTEST2,SORTEST1                                 90233
         LA    R1,SORTLST1                                       90232
         LINK  EP=SORT                  SORT BY DSNAME
         LTR   R15,R15       CHECK RETURN CODE                   90233
         BZ    SORTGOOD      OK                                  90233
         ST    R15,RETCODE   USE IT AS RETURN CODE               90233
         PRTF  '0 ***** JOB TERMINATED DUE TO SORT ERROR *****'  90233
         B     RETCHED       QUIT                                90233
SORTGOOD MVC   OUTANDIN+DCBDDNAM-IHADCB(L'DCBDDNAM),=CL8'SORTOUT'
*                                                                     *
         SUBCALL N013        PRINT BY DSNAME                     90232
*                                                                     *
         SUBCALL N014        PRINT TAPES WITH NO EXPDT           90232
*                                                                     *
         SUBCALL N015        SORT AND PRINT BY PROGRAM           90232
RETCHED  L     R9,RETCODE    LOAD RETURN CODE                    90232
         ENDM  RC=(R9)                                           90232
         EJECT
***********************************************************************
*                                                                     *
*              //PS50    EXEC PGM=A50PWTRA,PARM='GO'                  *
*                                                                     *
***********************************************************************
EXEC10   EQU   *                        EXEC ROUTINE
         BAL   R9,SYM10                 RESOLVE SYMBOLICS        90232
         LA    R4,EXECTAB    GET TABLE OF EXEC KEYWORDS          90232
         BAL   R9,KEYWD10               LOOK FOR 'PGM='          90232
         XC    XDSN,XDSN     SET NON-BLANK SPECIAL NAME          90232
*                  NEED TO WRITE RECORD IN CASE NO DD CARDS      90232
         B     A310
         SPACE 2
***********************************************************************
*                                                                     *
*              //A60NXREF  DD DSN=SWL.APA60.ARAPXOUT,.... ETC.        *
*                                                                     *
***********************************************************************
DD10     EQU   *                        DD ROUTINE
         BAL   R9,SYM10                 RESOLVE SYMBOLICS        90232
         LA    R4,DDTAB      GET TABLE OF DD KEYWORDS            90232
         BAL   R9,KEYWD10               LOOK FOR KEYWORDS        90232
         B     A300
         SPACE 1                                                 90232
KEYWD10  L     R6,ADDRSYMT   (END OF STATEMENT TABLE)            90232
         BCTR  R6,0          POINT TO LAST BLANK                 90232
         ST    R4,KEYWDSV4   SAVE TABLE ADDRESS                  90232
         ST    R9,KEYWDSVE   SAVE RETURN ADDRESS                 90232
         LA    R5,W1STAT     POINT TO STEMENT TEXT               90232
KEYWD15  LA    R9,KEYWD20    SET RETURN ADDRESS FOR EXTRACTION   90232
         BLOOK T=(R4),R=R6,STRIP=NO  BLANKS ARE SIGNIFICANT      90232
KEYWD20  CLI   0(R5),C' '    TRAILING BLANK HIT ?                90232
         BE    KEYWD99       YES; EXIT SCAN                      90232
         LA    R5,1(,R5)     ADVANCE SCAN POINTER                90232
         L     R4,KEYWDSV4   RESTORE TABLE ADDRESS               90232
         B     KEYWD15       TRY AGAIN                           90232
KEYWD99  L     R9,KEYWDSVE   RESTORE RETURN                      90232
         BR    R9            RETURN                              90232
         SPACE 2                                                 90232
EXECTAB  BTAB  'PGM=',PGM10,BASE=*  EXEC PGM=                    90232
         BTAB  *END                                              90232
         SPACE 1                                                 90232
DDTAB    BTAB  'DSN=',DSN10,BASE=*   DD CARD OPERANDS            90232
         BTAB  'DSNAME=',DSN10                                   90232
         BTAB  'DISP=',DISP10                                    90232
         BTAB  'UNIT=',UNIT10                                    90232
         BTAB  'EXPDT=',EXPDT10                                  90232
         BTAB  'RETPD=',EXPDT10                                  90232
         BTAB  'SPACE=',SPACE10                                  90232
         BTAB  'DSORG=',DSORG10                                  90232
         BTAB  'DCB=',DCB10                                      90232
         BTAB  'RECFM=',RECFM10                                  90232
         BTAB  'LRECL=',LRECL10                                  90232
         BTAB  'BLKSIZE=',BLKSIZ10                               90232
         BTAB  *END          END OF TABLE                        90232
         EJECT
***********************************************************************
*                                                                     *
*              B U I L D    S Y M B O L I C    T A B L E              *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90232
PROC10   L     R8,ADDRSYMT                                       90232
         USING SYMTAB,R8                                         90232
         BAL   R9,CLEAR20               CLEAR SYM TABLE          90232
         MVI   SYML1,HEXFF              MARK END OF TABLE
         L     R0,WKAR1LEN   SET MAX LENGTH OF STAT              90232
         LA    R5,W1STAT
PROC20   EQU   *
         CLI   0(R5),BLANK              IS IT END OF PROC STAT
         BE    PROC90
         BAL   R14,SCAN115   SCAN FOR '=' OR BLANK               90232
         CLI   0(R5),EQUAL              IS IT '='
         BE    PROC30
*                                       NO - ERROR                    *
PROC30   EQU   *
         MVC   SYMNAME,FOUND            MOVE NAME TO TABLE
         STC   R1,SYML2                 MOVE NAME LENGTH TO TABLE
         LA    R5,1(,R5)                 BUMP PAST '='           90232
         CLI   0(R5),BLANK              IS IT END OF PROC STAT
         BE    PROC60
         CLI   0(R5),COMMA              IS IT END OF SYMBOLIC
         BE    PROC60
         CLI   0(R5),QUOTE              IS IT QUOTE
         BE    PROC50
         BAL   R14,SCAN125   SCAN FOR ',' OR BLANK               90232
PROC40   EQU   *
         STC   R1,SYML3                 MOVE LENGTH OF EQUATE TO TABLE
         BCTR  R1,0
         EX    R1,PROCMVC               MOVE EQUATE TO TABLE
         LA    R1,1(,R1)                                         90232
PROC42   EQU   *
         LA    R1,11(,R1)                COMPUTE TOTAL ENTRY LENGTH
         STC   R1,SYML1                 MOVE TO TABLE
         AR    R8,R1                    BUMP TO END OF TABLE     90232
         MVI   SYML1,HEXFF              MARK END OF TABLE
         CLI   0(R5),BLANK              IS IT END OF PROC STAT
         BE    PROC90
         LA    R5,1(,R5)                 BUMP PAST ','           90232
         B     PROC20
PROCMVC  MVC   SYMEQU(0),FOUND
PROC50   EQU   *
         LA    R5,1(,R5)                 BUMP PAST QUOTE         90232
         BAL   R14,SCAN140   SCAN FOR QUOTE                      90232
         STC   R1,SYML3                 MOVE LENGTH OF EQUATE TO TABLE
         BCTR  R1,0
         EX    R1,PROCMVC               MOVE EQUATE TO TABLE
         LA    R1,12(,R1)                COMPUTE TOTAL ENTRY LENGTH
         STC   R1,SYML1                 MOVE TO TABLE
         AR    R8,R1                    BUMP TO END OF TABLE     90232
         MVI   SYML1,HEXFF              MARK END OF TABLE
         LA    R5,1(,R5)                 BUMP PAST QUOTE         90232
         CLI   0(R5),BLANK              IS IT END OF PROC STAT
         BE    PROC90
         LA    R5,1(,R5)                 BUMP PAST COMMA         90232
         B     PROC20
PROC60   EQU   *
         SR    R1,R1                    NO SYMBOLIC EQUATE
         STC   R1,SYML3                 ( LIB=, ) OR ( LIB= )
         B     PROC42
PROC90   EQU   *
         B     A300
         DROP  R8                                                90232
         EJECT
***********************************************************************
*                                                                     *
*              R E S O L V E    S Y M B O L I C S                     *
*                                                                     *
*              &PGM10                                                 *
*              DUMMY.,DSN=SWL.APA60.                                  *
*              SWL.&LIB..USRLIB                                       *
*              UNIT=&DISK,                                            *
*              SPACE=(&SPC10)                                         *
*              PARM='&PARM10'                                         *
*              DSN=&OUTPUT(&OUTGEN),                                  *
*                                                                     *
***********************************************************************
SYM10    EQU   *
         L     R8,ADDRSYMT   POINT R8 TO SYMBOLIC TABLE          90232
         USING SYMTAB,R8                                         90232
         MVC   W2STAT,W2STAT-1          BLANK RECEIVING AREA
         MVC   W2STAT2,W2STAT
         LA    R4,W2STAT                POINT R4 TO RECEIVING AREA
         LA    R5,W1STAT                POINT R5 TO SENDING AREA
         LA    R0,500        SET MAX LOOP CTR                    90232
SYM20    EQU   *
         CLI   0(R5),AMPER              IS IT '&'
         BE    SYM40
         CLI   0(R5),BLANK              IS IT END OF STATEMENT
         BE    SYM110
SYM30    EQU   *
         MVC   0(1,R4),0(R5)            MOVE CHAR
         LA    R4,1(,R4)                 BUMP TO NXT CHAR        90232
         LA    R5,1(,R5)                                         90232
         BCT   R0,SYM20                 LOOP
         B     ERROR70                  ERROR - STAT TOO LONG
SYM40    EQU   *
         CLI   1(R5),AMPER              IS NXT CHAR '&'
         BNE   SYM50                    NO
         MVC   0(1,R4),0(R5)            YES - ASSUME TEMP DSN
         LA    R4,1(,R4)                 BUMP TO NXT CHAR        90232
         LA    R5,1(,R5)                                         90232
         BCT   R0,SYM30                 LOOP
         B     ERROR70                  ERROR - STAT TOO LONG
         SPACE 1                                                 90232
*        THE OLD CODE PERFORMED A LOOK-UP OF THE CURRENT CARD    90232
*        AGAINST THE SYMBOL TABLE, USING THE TABLE SYMBOL'S LENGTH.
*        WHEN THE PROC DEFINES A SHORTER SYMBOL PRIOR TO A LONGER
*        ONE, THAT RESULTS IN INCORRECT RESOLUTION (E.G.,        90232
*        // PROC P=IEFBR14,PRI='CYL,5'                           90232
*        //ANY DD SPACE=(&PRI)  WOULD BECOME SPACE=(IEFBR14)     90232
*                                                                90232
*        THE FIX CONSISTS OF GETTING THE CARD SYMBOL'S LENGTH FIRST,
*        AND COMPARING ONLY EQUAL LENGTH SYMBOLS.                90232
*                                                                90232
SYM50    LR    R15,R5        SAVE OLD CARD COLUMN FOR COMPARE    90232
         LR    R14,R15       ALSO SAVE FOR LENGTH CALCULATION    90232
SYM51    LA    R5,1(,R5)     SPACE TO NEXT POSITION              90232
         CLI   0(R5),BLANK   IS IT BLANK                         90232
         BE    SYM55                                             90232
         CLI   0(R5),COMMA   IS IT ','                           90232
         BE    SYM55                                             90232
         CLI   0(R5),LEFTP   IS IT '('                           90232
         BE    SYM55                                             90232
         CLI   0(R5),RIGHTP  IS IT ')'                           90232
         BE    SYM55                                             90232
         CLI   0(R5),AMPER   IS IT '&'                           90232
         BE    SYM55                                             90232
         CLI   0(R5),QUOTE   IS IT QUOTE                         90232
         BE    SYM55                                             90232
         CLI   0(R5),POINT   IS IT '.'                           90232
         BE    SYM54                                             90232
         BCT   R0,SYM51      LOOP                                90232
         B     ERROR70       ERROR - STAT TOO LONG               90232
SYMMVC   MVC   0(0,R4),SYMEQU                                    90232
SYM54    SR    R14,R5        PREPARE TO GET LENGTH               90232
         LA    R5,1(,R5)     SKIP AN EXTRA FOR PERIOD            90232
         B     SYM58         END OF SYMBOL NAME                  90232
SYM55    SR    R14,R5        GET INVERSE LENGTH                  90232
SYM58    LCR   R14,R14       GET LENGTH OF SYMBOL NAME + 1       90232
         SH    R14,=H'1'     GET SYMBOL LENGTH                   90232
         BNP   SYM20         END OF SYMBOLIC                     90232
         L     R8,ADDRSYMT   START AT BEGINNING OF TABLE         90232
SYM60    EQU   *
         CLI   SYML1,HEXFF              IS IT END OF TABLE
         BE    SYM30                    YES
         SR    R3,R3
         IC    R3,SYML2                 PICKUP LENGTH OF SYM
         CR    R3,R14        SAME AS CURRENT NAME ?              90232
         BNE   SYM68         NO; SKIP TO NEXT                    90232
         BCTR  R3,0
         EX    R3,SYMCLC                COMPARE TABLE ENTRY TO STAT
         BE    SYM70
SYM68    IC    R3,SYML1      PICKUP LENGTH OF ENTRY              90232
         AR    R8,R3                    BUMP TO NXT ENTRY        90232
         B     SYM60
SYMCLC   CLC   SYMNAME(0),1(R15)                                 90232
         SPACE 1                                                 90232
SYM70    ICM   R3,1,SYML3    LOAD AND TEST REPLACEMENT TEXT LENGTH
         BZ    SYM20         PROCESS NEXT                        90232
         BCTR  R3,0
         EX    R3,SYMMVC                MOVE EQUATE TO STATEMENT
         LA    R3,1(,R3)                                         90232
         AR    R4,R3                    BUMP TO END OF EQUATE
         B     SYM20         END OF SYMBOLIC                     90232
SYM110   EQU   *
         MVC   W1STAT,W2STAT            END OF STATEMENT
         MVC   W1STAT2,W2STAT2
         BR    R9                                                90232
         DROP  R8                                                90232
         EJECT
***********************************************************************
*                                                                     *
*              D S N A M E   R O U T I  N E                           *
*                                                                     *
*              DSN=SWL.APA60.ADAP2000                                 *
*              DSN=SWL.APA60.ADAP2000(PRIME)  INDEX OR OVFLOW         *
*              DSN=SWL.AXX.SYSIN(A60I2000)                            *
*              DSN=SWL.APA60.ADAP2000(+1)                             *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90232
DSN10    ST    R5,SCANSV5                                        90232
         BAL   R14,SCAN125   SCAN FOR ',' OR BLANK               90232
         MVC   XDSN,FOUND
         LA    R5,XDSN                  POINT R5 TO DSNAME
         BAL   R14,SCAN180   SCAN FOR '(' OR BLANK               90232
         CLI   0(R5),BLANK              IS IT BLANK
         BE    DSN90                    YES - DONE, GET OUT
         CLC   1(5,R5),=C'PRIME'        IS IT 'PRIME'
         BE    DSN50
         CLC   1(5,R5),=C'INDEX'        IS IT 'INDEX'
         BE    DSN50
         CLC   1(6,R5),=C'OVFLOW'       IS IT 'OVFLOW'
         BE    DSN50
         CLI   1(R5),PLUS               IS IT '+'
         BE    DSN40
         CLI   1(R5),MINUS              IS IT '-'
         BE    DSN40
         TM    1(R5),HEXF0              IS IT NUMERIC
         BO    DSN40
         CLI   1(R5),C')'    NULL MEMBER NAME ?                  90232
         BNE   DSN90         NO; LEAVE AS IS                     90232
         MVC   0(2,R5),BLANKS  CLEAR EMPTY MEMBER NAME           90232
         B     DSN90
DSN40    EQU   *
         MVC   DB,0(R5)               SAVE RELATIVE GEN          90232
         MVC   XDSN,FOUND               MOVE UP TO '(' TO XDSN
         LA    R5,DB                  POINT R5 TO RELATIVE GEN   90232
         LA    R5,1(,R5)                 BUMP PAST '('           90232
         BAL   R14,SCAN194   SCAN FOR ')' OR BLANK               90232
         MVC   XRELGEN,FOUND            SAVE RELATIVE GEN NO.
         LA    R5,1(,R5)                 BUMP PAST ')'           90232
         B     DSN90
DSN50    EQU   *
         B     DSN90
DSN90    EQU   *
         L     R5,SCANSV5                                        90232
         BR    R9                       RETURN                   90232
         EJECT
***********************************************************************
*                                                                     *
*              P R O G R A M    N A M E    R O U T I N E              *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90232
PGM10    BAL   R14,SCAN125   SCAN FOR ',' OR BLANK               90232
         MVC   XPGM,FOUND               MOVE TO XREC
         MVC   XSTEP,W1NAME             MOVE STEP NAME TO XREC
         BR    R9                                                90232
         EJECT
***********************************************************************
*                                                                     *
*              D I S P    R O U T I N E                               *
*                                                                     *
*              DISP=OLD                                               *
*              DISP=(OLD,KEEP)                                        *
*              DISP=(OLD,DELETE,KEEP)                                 *
*              DISP=(,CATLG,DELETE)                                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90232
DISP10   CLI   0(R5),LEFTP   IS IT LEFT PAREN                    90232
         BE    DISP20
         BAL   R14,SCAN125   SCAN FOR ',' OR BLANK               90232
         MVC   XDISP1,FOUND             MOVE TO DISP #1
         B     DISP60
         SPACE 1                                                 90232
DISP20   LA    R5,1(,R5)                 BUMP PAST LEFT PAREN    90232
         BAL   R14,SCAN190   SCAN FOR ','  RIGHT PAREN,BLK       90232
         MVC   XDISP1,FOUND             MOVE TO DISP #1
         CLI   0(R5),RIGHTP             IS IT RIGHT PAREN
         BE    DISP60
         LA    R5,1(,R5)                 BUMP PAST ','           90232
         BAL   R14,SCAN190   SCAN FOR ','  RIGHTP OR BLANK       90232
         MVC   XDISP2,FOUND             MOVE TO DISP #2
         CLI   0(R5),RIGHTP             IS IT RIGHT PAREN
         BE    DISP60                                            90232
         LA    R5,1(,R5)                 BUMP PAST ','           90232
         BAL   R14,SCAN194   SCAN FOR RIGHT PAREN OR BLK         90232
         MVC   XDISP3,FOUND             MOVE TO DISP #3
         LA    R5,1(,R5)                 BUMP PAST RIGHT PAREN   90232
         SPACE 1                                                 90232
DISP60   CLI   XDISP1,C' '   WAS PRIMARY DISPOSITION SET ?       90232
         BH    DISP65        YES                                 90232
         MVC   XDISP1,=CL5'NEW'  DEFAULT IS NEW                  90232
DISP65   LA    R1,=CL5'DELETE'  SET DEFAULT FOR 'NEW'            90232
         CLC   XDISP1,=CL5'NEW'         IS FIRST DISP = NEW
         BE    DISP70
         LA    R1,=CL5'KEEP'  SECOND DISP DEFAULT = KEEP         90232
DISP70   CLI   XDISP2,C' '   DID USER SPECIFY SECONDARY ?        90232
         BH    DISP80        YES; LEAVE IT                       90232
         MVC   XDISP2,0(R1)  SET APPROPRIATE SECONDARY           90232
         SPACE 1                                                 90232
DISP80   CLI   XDISP3,C' '   WAS THIRD DISPOSITION SET ?         90232
         BH    DISP90        YES; CHECK TYPE                     90232
         MVC   XDISP3,XDISP2            THIRD DISP DEFAULT = SECOND
         SPACE 1                                                 90232
DISP90   MVI   XIO,CHRO      PROVISIONALLY SET OUTPUT MODE       90232
         CLC   XDISP1,=CL5'NEW'         IS IT OUTPUT DATASET
         BER   R9            YES; RETURN                         90232
         CLC   =C'MOD',XDISP1  DISP=MOD ?                        90232
         BER   R9            YES; FLAGGED AS OUTPUT              90232
         MVI   XIO,CHRI                 NO - MARK AS INPUT
         BR    R9            RETURN                              90232
         EJECT
***********************************************************************
*                                                                     *
*              U N I T    R O U T I N E                               *
*                                                                     *
*              UNIT=SYSDA                                             *
*              UNIT=(SYSDA)                                           *
*              UNIT=(AFF=DDNAME)                                      *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90232
UNIT10   CLI   0(R5),LEFTP   IS IT LEFT PAREN                    90232
         BNE   UNIT20
         LA    R5,1(,R5)                 BUMP PAST RIGHT PAREN   90232
UNIT20   EQU   *
         CLC   =C'AFF=',0(R5)   IS IT UNIT=AFF=                  90232
         BE    UNIT90
         CLI   0(R5),COMMA              IS UNIT OMITTED
         BE    UNIT90
         BAL   R14,SCAN190   SCAN FOR ','  ')' OR BLANK          90232
         MVC   XUNIT,FOUND              MOVE TO XREC
         B     UNIT90
UNIT90   EQU   *
         BR    R9                                                90232
         EJECT
***********************************************************************
*                                                                     *
*              E X P D T / R E T P D    R O U T I N E                 *
*                                                                     *
*              LABEL=EXPDT=99000                                      *
*              LABEL=(EXPDT=99000)                                    *
*              LABEL=(,SL,,,EXPDT=99000)                              *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90232
EXPDT10  CLI   0(R5),LEFTP   DOES IT BEGIN WITH LEFT PAREN       90232
         BNE   EXPDT20                  NO
         LA    R5,1(,R5)                 YES - BUMP PAST RIGHT PAREN
EXPDT20  EQU   *
         BAL   R14,SCAN190   SCAN FOR ','  ')' OR BLANK          90232
         MVC   XEXPDT,FOUND             MOVE TO XREC
         BR    R9                                                90232
         EJECT
***********************************************************************
*                                                                     *
*              S P A C E    R O U T I N  E                            *
*                                                                     *
*              SPACE=(CYL,10)           SPACE=(CYL,10)                *
*              SPACE=(CYL,(10,5),RLSE,CONTIG)                         *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90232
SPACE10  CLI   0(R5),LEFTP   IS IT '('                           90232
         BE    SPACE20
         BAL   R14,SCAN125   SCAN FOR ',' OR BLANK               90232
         B     SPACE30       MOVE TEXT AND RETURN                90232
         SPACE 1                                                 90232
SPACE20  LA    R5,1(,R5)     BUMP PAST '('                       90232
         ST    R5,SCANSV5                                        90232
         BAL   R14,SCAN196   SCAN FOR '('  ')' OR BLANK          90232
         CLI   0(R5),RIGHTP             IS IT ')'
         BNE   SPACE40       NO; PARSE TO SECOND CLOSING ')'     90232
SPACE30  MVC   XSPACE,FOUND             MOVE TO XREC             90232
         B     SPACE100                 GET OUT
         SPACE 1                                                 90232
SPACE40  CLI   0(R5),LEFTP   IS IT '('                           90232
         BNE   SPACE60       ERROR - BLANK FOUND                 90232
         LA    R5,1(,R5)                 BUMP PAST 2ND '('       90232
         BAL   R14,SCAN194   SCAN FOR ')' OR BLANK               90232
         CLI   0(R5),RIGHTP             IS IT 1ST ')'
         BNE   SPACE60       NO - ERROR - MOVE WHAT WE HAVE      90232
         LA    R5,1(,R5)                 BUMP PAST ')'           90232
         BAL   R14,SCAN194   SCAN FOR ')' OR BLANK               90232
SPACE60  LR    R1,R5                    LOAD ADDR OF ENDING POS
         L     R2,SCANSV5               LOAD ADDR OF BEGINNNING POS
         SR    R1,R2                    COMPUTE LENGTH
         BCTR  R1,0
         EX    R1,SPCMVC                MOVE TO XREC
         B     SPACE100      USE AS IS                           90232
         SPACE 1                                                 90232
SPACE100 BR    R9                                                90232
         SPACE 1                                                 90232
SPCMVC   MVC   XSPACE(0),0(R2)
         SPACE 2
DCB10    EQU   *
         BR    R9                                                90232
         EJECT
***********************************************************************
*                                                                     *
*                                                                     *
*              D S O R G    R O U T I N  E                            *
*                                                                     *
*              DSORG=PS                                               *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90232
DSORG10  BAL   R14,SCAN190   SCAN FOR ','  ')' OR BLANK          90232
         MVC   XDSORG,FOUND             MOVE TO XREC
         BR    R9                                                90232
         EJECT
***********************************************************************
*                                                                     *
*                                                                     *
*              R E C F M    R O U T I N E                             *
*                                                                     *
*              RECFM=FB                                               *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90232
RECFM10  BAL   R14,SCAN190   SCAN FOR ','  ')' OR BLANK          90232
         MVC   XRECFM,FOUND             MOVE TO XREC
         BR    R9                                                90232
         SPACE 2
***********************************************************************
*                                                                     *
*              L R E C L    R O U T I N E                             *
*                                                                     *
*              LRECL=133                                              *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90232
LRECL10  BAL   R14,SCAN190   SCAN FOR ','  ')' OR BLANK          90232
         MVC   XLRECL,FOUND             MOVE TO XREC
         BR    R9                                                90232
         SPACE 2                                                 90232
***********************************************************************
*                                                                     *
*              B L K S I Z E    R O U T I N E                         *
*                                                                     *
*              BLKSIZE=1330                                           *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90232
BLKSIZ10 BAL   R14,SCAN190   SCAN FOR ','  ')' OR BLANK          90232
         MVC   XBLKSIZE,FOUND           MOVE TO XREC
         BR    R9                                                90232
         EJECT
CLEAR10  STM   R2,R5,CLEARSVE     CLEAR WORK AREA                90232
         LA    R2,WKAREA1    GET THE WORK AREA ADDRESS           90232
         L     R3,WKAR1LEN
         B     CLEAR28       GO TO COMMON CLEAR                  90232
*                                                                     *
CLEAR20  STM   R2,R5,CLEARSVE     CLEAR SYMTAB                   90232
         L     R2,ADDRSYMT                                       90232
         L     R3,SYMTLEN
CLEAR28  CLRL  (R2),(R3),FILL=C' '  FILL WITH BLANKS             90232
         B     CLEAR80                                           90232
*                                                                     *
CLEAR80  EQU   *                                                 90232
         LM    R2,R5,CLEARSVE
         BR    R9                                                90232
*                                                                     *
CLEARSVE DC    4F'0'
         SPACE 2                                                 90232
ERROR10  LA    R2,MESS10                STAT NAME > 8 CHARS      90232
         B     ERROR200
ERROR20  LA    R2,MESS20                STAT OP NOT FOUND
         B     ERROR200
ERROR30  LA    R2,MESS30                STAT OP > 4 CHARS
         B     ERROR200
ERROR40  LA    R2,MESS40                CONT STAT NOT FOUND
         B     ERROR200
ERROR50  LA    R2,MESS50                CONT DOES NOT START BEFORE 17
         B     ERROR200
ERROR60  LA    R2,MESS60     SCAN WENT PAST COLUMN 71
         B     ERROR200
ERROR70  LA    R2,MESS70     STATEMENT TOO LONG
         SPACE 1                                                 90232
ERROR200 PRTLIST ERRMSG      PRINT BAD CARD                      90233
         B     A300
         SPACE 1                                                 90233
ERRMSG   FDOPT NL,CC=C'0'    DOUBLE-SPACE                        90233
         FDPRT PDDRNAME      PRINT MEMBER NAME                   90233
         FDPRT ' ',PAD,LEN=3    SPACER                           90233
         FDPRT INREC         PRINT RECORD                        90233
         FDPRT ' ',PAD,LEN=3    SPACER                           90233
         FDPRT 0(R2),L'MESS10  ERROR MESSAGE                     90233
         FDPRT *END                                              90233
         SPACE 2                                                 90233
SCAN10   SLR   R1,R1         SCAN FOR A BLANK                    90233
SCAN12   CLI   0(R5),BLANK              IS IT BLANK
         BER   R14           YES; RETURN                         90233
         LA    R1,1(,R1)                                         90232
         LA    R5,1(,R5)                                         90232
         BCT   R0,SCAN12                LOOP
         BR    R14           HUH ?                               90232
         SPACE 1                                                 90233
SCAN20   SLR   R1,R1         SCAN FOR A NON-BLANK                90233
SCAN22   CLI   0(R5),BLANK
         BNER  R14           FOUND ONE; RETURN                   90233
         LA    R1,1(,R1)                                         90232
         LA    R5,1(,R5)                                         90232
         BCT   R0,SCAN22                LOOP
         BR    R14                                               90233
         SPACE 1                                                 90233
SCAN30   SLR   R1,R1         SCAN FOR BLANK AND MOVE TEXT        90233
         LA    R2,FOUND      RESULT AREA
         MVC   FOUND,BLANKS     CLEAR IT                         90232
SCAN32   CLI   0(R5),BLANK
         BER   R14           RETURN - PARM OR BLANKS IN 'FOUND'  90233
         MVC   0(1,R2),0(R5)
         LA    R1,1(,R1)                                         90232
         LA    R2,1(,R2)                                         90232
         LA    R5,1(,R5)                                         90232
         BCT   R0,SCAN32                LOOP
         BR    R14                                               90233
         SPACE 1                                                 90233
SCAN40   SLR   R1,R1         SCAN FOR BLANK, COPY ALL ELSE       90233
         LA    R2,FOUND
         MVC   FOUND,BLANKS                                      90232
         SLR   R15,R15       PRESET FOR GOOD RETURN CODE         90233
SCAN42   CLI   0(R5),QUOTE
         BE    SCAN46
         CLI   0(R5),BLANK
         BER   R14           RETURN WITH RC=0                    90233
         SPACE 1
SCAN44   MVC   0(1,R2),0(R5)
         LA    R1,1(,R1)                                         90232
         LA    R2,1(,R2)                                         90232
         LA    R5,1(,R5)                                         90232
         BCT   R0,SCAN42
         B     SCAN48
         SPACE 1                                                 90233
SCAN46   MVC   0(1,R2),0(R5)
         LA    R1,1(,R1)                                         90232
         LA    R2,1(,R2)                                         90232
         LA    R5,1(,R5)                                         90232
         CLI   0(R5),QUOTE
         BE    SCAN44
         BCT   R0,SCAN46
SCAN48   LA    R15,4         SET RC TO 4                         90232
         BR    R14                                               90232
         SPACE 1                                                 90233
SCAN100  ICM   R15,14,=AL1(BLANK,0,0)     BLANK                  90232
         B     SCAN200
SCAN110  ICM   R15,14,=AL1(EQUAL,0,0)     EQUAL                  90232
         B     SCAN200
SCAN115  ICM   R15,14,=AL1(EQUAL,BLANK,0) EQUAL,BLANK            90232
         B     SCAN200
SCAN120  ICM   R15,14,=AL1(COMMA,0,0)     COMMA                  90232
         B     SCAN200
SCAN125  ICM   R15,14,=AL1(COMMA,BLANK,0) COMMA,BLANK            90232
         B     SCAN200
SCAN130  ICM   R15,14,=AL1(POINT,0,0)     PERIOD                 90232
         B     SCAN200
SCAN140  ICM   R15,14,=AL1(QUOTE,0,0)     APOSTROPHE             90232
         B     SCAN200
SCAN150  ICM   R15,14,=AL1(BLANK,0,0)     BLANK                  90232
         B     SCAN200
SCAN160  ICM   R15,14,=AL1(AMPER,0,0)     AMPERSAND              90232
         B     SCAN200
SCAN170  ICM   R15,14,=AL1(POINT,COMMA,0) PERIOD,COMMA           90232
         B     SCAN200
SCAN180  ICM   R15,14,=AL1(LEFTP,BLANK,0) L.PARENTHESIS,BLANK    90232
         B     SCAN200
SCAN190  ICM   R15,14,=AL1(RIGHTP,COMMA,BLANK) R.PARENTH.,COMMA,BLANK
         B     SCAN200
SCAN194  ICM   R15,14,=AL1(RIGHTP,BLANK,0) R.PARENTHESIS,BLANK   90232
         B     SCAN200
SCAN196  ICM   R15,14,=AL1(RIGHTP,LEFTP,BLANK)  R.,L.PARENTHESIS,BLANK
         SPACE 1                                                 90232
SCAN200  SLR   R1,R1
         LA    R2,FOUND                 POINT R2 TO FOUND
         MVC   FOUND,BLANKS  BLANK FOUND                         90232
SCAN210  CLM   R15,8,0(R5)   IS IT CHAR 1                        90232
         BER   R14           YES; RETURN                         90233
         CLM   R15,4,0(R5)   IS IT CHAR 2                        90232
         BER   R14           YES; RETURN                         90233
         CLM   R15,2,0(R5)   IS IT CHAR 3                        90232
         BER   R14           YES; RETURN                         90233
         MVC   0(1,R2),0(R5)            MOVE CHAR TO FOUND
         LA    R1,1(,R1)                                         90232
         LA    R2,1(,R2)                BUMP CTRS                90232
         LA    R5,1(,R5)                                         90232
         BCT   R0,SCAN210               LOOP
         BR    R14           RETURN WITH ERROR                   90232
         SPACE 2
         LTORG
         SPACE 2
MVC10    MVC   0(0,R6),FOUND
WKAR1LEN DC    A(WKAREA1E-WKAREA1)      LENGTH OF WKAREA1
SYMTLEN  DC    A(SYMTABE-SYMTAB)        LENGTH OF SYMTAB
         SPACE 2                                                 90232
INHDR1   DC    C'#                      CROSS-REFERENCE EXCEPTION LIST'
MESS10   DC    CL30'STMT NAME LONGER THAN 8 CHAR'                90232
MESS20   DC    CL30'STMT VERB NOT FOUND'                         90232
MESS30   DC    CL30'STMT VERB LONGER THAN 4 CHARS'               90232
MESS40   DC    CL30'CONTINUATION NOT FOUND'
MESS50   DC    CL30'CONTINUATION PAST COL 16'
MESS60   DC    CL30'SCAN PAST COL 71'
MESS70   DC    CL30'STATEMENT TOO LONG'
         TITLE '*----NSP013----*  JCL XREF  --  DATA SET NAME CROSS-REF*
               ERENCE'
***********************************************************************
*                                                                     *
*                                                                     *
*     DSNAME XREF PRINT PROGRAM - 'NSP013'                            *
*                                                                     *
*                                                                     *
*     THIS PROGRAM PRINTS OUTPUT FROM PROGRAM 'NSP011'                *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90232
N013     SUBENT ,                                                90232
         LA    R1,DSNEOF     SET END-FILE ADDRESS                90232
         STCM  R1,7,OUTANDIN+DCBEODA-IHADCB  IN INPUT            90232
         OPEN  (,(INPUT)),MF=(E,OPLSTIN)                         90232
         OPEN  (,(OUTPUT)),MF=(E,OPLSTWRK)                       90232
         PRTF  DSNHDR1,TITLE=1  MAKE NEW TITLE                   90232
         PRTLIST DSNHDR2,TITLE=2                                 90232
         MVI   DSNAME,0      FORCE MISMATCH ON FIRST COMPARE     90232
         XC    SORTREC,SORTREC  CLEAR FOR WORKREC USE            90232
         LA    R5,SORTREC    USE IT FOR WORK FILE                90232
N13LOOP  GET   OUTANDIN      GET INPUT RECORD                    90232
         LR    R7,R1         COPY RECORD ADDRESS                 90232
         USING XREC,R7       DECLARE SORT RECORD                 90232
         CLI   XDSN,0        PROGRAM/STEP RECORD ?               90232
         BE    N13LOOP       YES; IGNORE                         90232
         CLC   DSNAME,XDSN              DID DSNAME CHANGE
         BE    N13OLDN       NO                                  90232
         CLI   DSNAME,0      FIRST TIME ?                        90232
         BE    N13COMM       YES                                 90232
         PRTF  SEPLINE,SEPLINEL             PUT SEPARATOR LINE   90232
         PRTROOM 3           LEAVE ROOM FOR AT LEAST 3 LINES     90232
         B     N13COMM                                           90232
         SPACE 1                                                 90232
N13OLDN  CLI   XIO,C'I'      INPUT FILE ?                        90232
         BNE   N13COMM       NO; PRINT IT REGARDLESS             90232
         CLC   XIO(XUNIT-XPROC),XIO-XREC+BACKREC  SAME ?         90232
         BE    N13LOOP       YES; SKIP IT                        90232
N13COMM  PRTLIST LISTDSN     PRINT RECORD                        90232
         MVC   BACKREC,XREC  SAVE FOR DUPLICATE SUPPRESSION      90232
         NI    PFLAG,255-NEWPAGE                                 90232
         MVC   DSNAME,XDSN              SAVE NEW DSNAME
         CLC   XDSN,XDSN-XREC(R5)       HAS THIS DSNAME BEEN WRITTEN
         BE    N13LOOP                  YES - DONT WRITE AGAIN   90232
         CLI   XIO,CHRO                 IS IT OUTPUT             90232
         BNE   N13LOOP                  NO                       90232
         CLC   =C'DEL',XDISP2  DELETE IMMEDIATELY ?              90232
         BE    N13LOOP       YES; DON'T PROCESS                  90232
         CLC   =C'TAPE',XUNIT   GENERIC TAPE ?                   90232
         BE    N13TAPE       YES                                 90232
         CLC   =C'2400',XUNIT  OLD DRIVE TYPE ?                  90232
         BE    N13TAPE       YES                                 90232
         CLC   =C'3400',XUNIT  NEWER TAPE DRIVE ?                90232
         BE    N13TAPE       YES                                 90232
         CLC   =C'3480',XUNIT  CARTRIDGE TAPE ?                  90232
         BNE   N13LOOP       NO                                  90232
N13TAPE  CLC   XEXPDT,BLANKS            IS EXPDT BLANK           90232
         BNE   N13LOOP                  NO                       90232
         PUT   WRKFILE,XREC  SAVE THIS RECORD ON WORK FILE       90232
         INC   NUMWORK       COUNT RECORDS WRITTEN               90232
         MVC   XDSN-XREC(L'XDSN,R5),XDSN     DON'T DUPLICATE     90232
         B     N13LOOP                                           90232
         SPACE 1
DSNEOF   CLOSE MF=(E,OPLSTWRK)  CLOSE TAPE FILE                  90232
         CLOSE MF=(E,OPLSTIN)                                    90232
         SUBEX ,                                                 90232
         SPACE 1
DSNHDR1  DC    C'#                    D S N   CROSS-REFERENCE LISTING'
         TITLE '*----NSP014----*  JCL XREF --  DATA SETS WITH NO EXPDT *
               OR RETPD SPECIFIED'
***********************************************************************
*                                                                     *
*     TAPE DATASETS WITH NO EXPDT/RETPD  -  'NSP014'                  *
*                                                                     *
*     THIS PROGRAM SELECT TAPE DATASETS WITH NO EXPDT/RETPD AND       *
*     PUTS THESE RECORD ON A WORK FILE.                               *
*     THEN MATCHES THESE RECORD AGAINST COMPLETE FILE TO PRINT ALL    *
*     RECORDS WITH THIS DSNAME.                                       *
*                                                                     *
***********************************************************************
         SPACE 1
N014     SUBENT ,                                                90232
         PRTF  EXPHDR1,TITLE=1  MAKE NEW TITLE                   90232
         MVI   DSNAME,0      FORCE MISMATCH ON FIRST COMPARE     90232
         ICM   R15,15,NUMWORK  ANYTHING TO DO ?                  90232
         BZ    N14EXIT       NO; GET OUT                         90232
         LA    R1,EXPEOF     SET NEW END-FILE ADDRESS            90232
         STCM  R1,7,OUTANDIN+DCBEODA-IHADCB  IN INPUT            90232
         STCM  R1,7,WRKFILE+DCBEODA-IHADCB  AND CONTROL          90232
         OPEN  MF=(E,OPLSTIN)                                    90232
         OPEN  (,(INPUT)),MF=(E,OPLSTWRK)                        90232
         SPACE 1                                                 90232
N14NREC  GET   WRKFILE       GET SELECTED RECORD                 90232
         LR    R5,R1         COPY RECORD ADDRESS                 90232
         SPACE 1                                                 90232
N14NDSN  GET   OUTANDIN      GET INPUT RECORD                    90232
         LR    R7,R1         COPY RECORD ADDRESS                 90232
         USING XREC,R7       DECLARE SORT RECORD                 90232
         SPACE 1                                                 90232
N14CMPR  CLC   XDSN,XDSN-XREC(R5)  DOES DSNAME MATCH ?           90232
         BL    N14NDSN                  LOW -LOOP TO GET NXT REC
         BE    N14PRNT                  EQUAL - GO TO PRINT
         GET   WRKFILE       HIGH - GET CONTROL REC              90232
         LR    R5,R1         COPY RECORD ADDRESS                 90232
         B     N14CMPR                         GO TO COMPARE
         SPACE 1
N14PRNT  CLC   =C'SYSDA',XUNIT  NON-TAPE FILE ?                  90232
         BE    N14NDSN       YES; GET NEXT RECORD                90232
         CLC   =C'33',XUNIT  EXPLICIT DASD TYPE ?                90232
         BE    N14NDSN       YES; DON'T PRINT IT                 90232
N14PROC  CLC   DSNAME,XDSN              DID DSNAME CHANGE
         BE    N14COMM       NO                                  90232
         CLI   DSNAME,0      FIRST TIME ?                        90232
         BE    N14COMM       YES                                 90232
         PRTF  SEPLINE,SEPLINEL             PUT SEPARATOR LINE   90232
         PRTROOM 3           LEAVE ROOM FOR AT LEAST 3 LINES     90232
N14COMM  PRTLIST LISTDSN     PRINT RECORD                        90232
         MVC   DSNAME,XDSN   SAVE NAME PRINTED                   90232
         NI    PFLAG,255-NEWPAGE                                 90232
         B     N14NDSN                                           90232
         SPACE 1                                                 90232
EXPEOF   CLOSE MF=(E,OPLSTIN)  CLOSE IN/OUT FILE                 90232
         CLOSE MF=(E,OPLSTWRK)  CLOSE WORK FILE                  90232
         L     R2,OPLSTWRK                                       90233
         FREEPOOL (R2)       FREE BUFFERS                        90233
N14EXIT  SUBEX RC=0                                              90232
         SPACE 1
EXPHDR1  DC    C'#                   TAPE DATASETS WITHOUT EXPDT/RETPD'
SEPLINE  DC    C' ',132C'-'                                      90232
SEPLINEL EQU   *-SEPLINE                                         90232
          TITLE '*----NSP015----*  JCL XREF -- PROGRAM NAME CROSS-REFER*
               ENCE'
***********************************************************************
*                                                                     *
*     'PGM'  CROSS REFERENCE PROGRAM  -  'NSP015'                     *
*                                                                     *
*                                                                     *
*     THIS PROGRAM SORTS AND PRINTS OUTPUT FROM 'NSP011'.             *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90232
N015     SUBENT ,                                                90232
         L     R2,OPLSTIN                                        90233
         FREEPOOL (R2)       FREE BUFFERS                        90233
         LA    R1,SORTLST2              SORT BY PROGRAM NAME
         LINK  EP=SORT
         LTR   R15,R15       GOOD SORT ?                         90233
         BZ    N15GOOD       YES                                 90233
         ST    R15,RETCODE   USE IT AS RETURN CODE               90233
         PRTF  '0 ***** JOB TERMINATED DUE TO SORT ERROR *****'  90233
         B     PGMEOF        QUIT                                90233
N15GOOD  LA    R1,PGMEOF     SET END-FILE ADDRESS                90232
         STCM  R1,7,OUTANDIN+DCBEODA-IHADCB  IN INPUT            90232
         PRTF  PGMHDR1,TITLE=1  MAKE NEW TITLE                   90232
         PRTF  PGMHDR2,PGMHDR2L,TITLE=2  MAKE SUB-TITLE          90232
         OPEN  MF=(E,OPLSTIN)                                    90232
         MVI   PROGRAM,0     FORCE MISMATCH ON FIRST COMPARE     90232
         SPACE 1                                                 90232
N15LOOP  GET   OUTANDIN      GET INPUT RECORD                    90232
         LR    R7,R1         COPY RECORD ADDRESS                 90232
         USING XREC,R7       DECLARE SORT RECORD                 90232
         CLC   PROGRAM,XPGM             DID PROGRAM CHANGE
         BE    N15OLDP       NO                                  90232
         CLI   PROGRAM,0     FIRST TIME ?                        90232
         BE    N15COMM       YES; SKIP TESTS                     90232
         PRTF  PGMSEP,PGMSEPL             PUT SEPARATOR LINE     90232
         PRTROOM 3           LEAVE ROOM FOR AT LEAST 3 LINES     90232
         B     N15COMM       PRINT                               90232
         SPACE 1                                                 90232
N15OLDP  CLC   PROC,XPROC    DON'T PRINT IF PROC AND STEP ALREADY DONE
         BNE   N15COMM       NOT THE SAME                        90232
         CLC   STEP,XSTEP
         BE    N15LOOP       DON'T PRINT DUPLICATES              90232
         SPACE 1                                                 90232
N15COMM  PRTLIST LISTPGM     PRINT RECORD                        90232
         NI    PFLAG,255-NEWPAGE  RESET                          90232
         MVC   PROGRAM,XPGM             SAVE NEW PROGRAM
         MVC   PROC,XPROC                                        90232
         MVC   STEP,XSTEP
         B     N15LOOP                                           90232
         SPACE 1                                                 90232
PGMEOF   CLOSE MF=(E,OPLSTIN)                                    90232
         SUBEX RC=0                                              90232
         SPACE 1                                                 90232
LISTPGM  FDPRT ' ',NL,LEN=5  INDENT                              90232
         FDCLC PROGRAM,XPGM,L'XPGM,BE=LISTPGMQ                   90232
         FDPRT XPGM,PADR                                         90232
         FDGOTO LISTPGMS                                         90232
LISTPGMQ FDPRT ' ',PADR,LEN=8  DON'T REPEAT PROGRAM NAME         90232
LISTPGMS FDPRT ' ',PAD,LEN=13  SPACER                            90232
         FDPRT XPROC,PAD       PROCEDURE NAME                    90232
         FDPRT ' ',PAD,LEN=5   SPACER                            90232
         FDPRT XSTEP,PAD       STEP NAME                         90232
         FDOPT NL                                                90232
         FDTM  PFLAG,NEWPAGE,BZ=LISTPGMZ  SKIP IF NOT NEW PAGE   90232
         FDCLC PROGRAM,XPGM,L'XPGM,BNE=LISTPGMZ  SKIP IF NEW     90232
         FDOPT CC=C'+'       OVERPRINT                           90232
         FDPRT ' ',LEN=5                                         90232
         FDPRT XPGM,PADR                                         90232
         FDPRT '*CONT'                                           90232
LISTPGMZ FDPRT *END                                              90232
         SPACE 2                                                 90232
PGMHDR1  DC    C'#                    P G M   CROSS REFERENCE LISTING'
PGMHDR2  DC    CL29'      PROGRAM NAME'
         DC    CL15'PROC'
         DC    CL5'STEP'
PGMHDR2L EQU   *-PGMHDR2                                         90232
PGMSEP   DC    CL15' ',40C'-'                                    90232
PGMSEPL  EQU   *-PGMSEP                                          90232
         SPACE 2                                                 90232
         DROP  ,                                                 90232
         USING DYNWORK,R10                                       90232
         USING RENTWORK,R11                                      90232
RENTWORK DS    0D            SHARED ADDRESSABILITY               90232
PATFILE  DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(PM,GL),EODAD=EXPEOF,      *
               RECFM=FB,LRECL=SORTLEN,BLKSIZE=20*SORTLEN         90232
PATFILEL EQU   *-PATFILE                                         90232
PATINPWK INPWORK PROCDD,WIDTH=80,PDS=MEM,PDE=1  PATTERN INPUT    90232
SYSPRINT PRTWORK SYSPRINT,TITLE=3,EXLST=SYSPREX                  90232
SYSPREX  DC    AL1(PUXFEND+PUXFTIT,0),AL4(HEADEXIT)              90232
BLANKS   DC    CL80' '
         SPACE 1                                                 90232
DSNHDR2  FDPRT ' DATA SET NAME',NL,LEN=28                        90232
         FDPRT 'UNIT',PAD,LEN=L'XUNIT                            90232
         FDPRT 'FMT',PAD,LEN=L'XRECFM                            90232
         FDPRT 'LRECL',PAD,LEN=L'XLRECL                          90232
         FDPRT 'BLKSI',PAD,LEN=L'XBLKSIZE                        90232
         FDPRT 'EXPDT',PAD,LEN=L'XEXPDT                          90232
         FDPRT 'SPACE',PAD,LEN=L'XSPACE-2                        90232
         FDPRT 'OUTPUT',PAD,LEN=L'XPROC                          90232
         FDPRT 'INPUT',PAD,LEN=L'XPROC                           90232
         FDPRT 'STEP',PAD,LEN=L'XSTEP                            90232
         FDPRT 'PROGRAM',PAD,LEN=L'XPGM                          90232
         FDPRT 'DISP',PAD,LEN=L'XDISP2+1+L'XDISP3                90232
         FDPRT 'GEN',PAD,LEN=L'XRELGEN                           90232
         FDPRT *END                                              90232
         SPACE 1                                                 90232
         USING XREC,R7       DECLARE SORT RECORD                 90232
LISTDSN  FDCLC DSNAME,XDSN,L'XDSN,BE=LISTDSNQ                    90232
         FDPRT XDSN,28,NL    TRUNCATED DSNAME                    90232
         FDGOTO LISTDSNC                                         90232
LISTDSNQ FDPRT ' ',PAD,NL,LEN=28    DON'T REPEAT DSN EVERY LINE  90232
LISTDSNC FDCLI XIO,CHRI,BE=LISTDSNI    IS IT INPUT               90232
         FDPRT XUNIT,PAD     NO                                  90232
         FDPRT XRECFM,PAD                                        90232
         FDPRT XLRECL,PAD                                        90232
         FDPRT XBLKSIZE,PAD                                      90232
         FDPRT XEXPDT,PAD                                        90232
         FDPRT XSPACE,L'XSPACE-2,PAD                             90232
         FDPRT XPROC,PAD     OUTPUT PROC                         90232
         FDPRT ' ',PAD,LEN=8     SPACE OVER INPUT PROC           90232
         FDGOTO LISTDSNN                                         90232
LISTDSNI FDPRT ' ',PAD,LEN=L'XUNIT     NO                        90232
         FDPRT ' ',PAD,LEN=L'XRECFM                              90232
         FDPRT ' ',PAD,LEN=L'XLRECL                              90232
         FDPRT ' ',PAD,LEN=L'XBLKSIZE                            90232
         FDPRT ' ',PAD,LEN=L'XEXPDT                              90232
         FDPRT ' ',PAD,LEN=L'XSPACE-2                            90232
         FDPRT ' ',PAD,LEN=8     SPACE OVER OUTPUT POSITION      90232
         FDPRT XPROC,PAD                                         90232
LISTDSNN FDPRT XSTEP,PAD                                         90232
         FDPRT XPGM,PAD                                          90232
         FDPRT XDISP2,PAD                                        90232
         FDPRT XDISP3,PAD                                        90232
         FDPRT XRELGEN,PADL                                      90232
LISTCONT FDOPT NL            FORCE NEW-LINE CLEARING             90232
         FDTM  PFLAG,NEWPAGE,BZ=LISTDSNZ  PAGE OVERFLOW?         90232
         FDCLC DSNAME,XDSN,L'XDSN,BNE=LISTDSNZ                   90232
         FDOPT CC=C'+'       OVERRPRINT                          90232
         FDPRT XDSN,21,NL    TRUNCATE EVEN MORE                  90232
         FDPRT ' * CONT'     INDICATE CONTINUATION               90232
LISTDSNZ FDPRT *END                                              90232
         SPACE 2                                                 90232
         PRINT &PRTMAC                                           90232
         MAPPRT EXPARM=YES   EXPAND PRTWORK MAPPING              90232
         SPACE 1                                                 90232
SORTSECT DSECT ,             MAPPING OF SORT RECORD              90232
XREC     DS    0C                       XREF RECORD              90232
XDSN     DC    CL44' '                  DSNAME
XIO      DC    CL1' '                   INPUT/OUTPUT
XPROC    DC    CL8' '                   PROC
XPGM     DC    CL8' '                   PROGRAM
XSTEP    DC    CL8' '                   STEP
XRELGEN  DC    CL4' '                   RELATIVE GEN
XDISP    DS    0CL9                     DISP
XDISP1   DC    CL3' '
XDISP2   DC    CL3' '
XDISP3   DC    CL3' '
XUNIT    DC    CL8' '                   UNIT
XEXPDT   DC    CL5' '                   EXPDT/RETPD
XSPACE   DC    CL25' '                  SPACE
XDCB     DS    0CL15                    DCB
XDSORG   DC    CL2' '                        DSORG
XRECFM   DC    CL3' '                        RECFM
XLRECL   DC    CL5' '                        LRECL
XBLKSIZE DC    CL5' '                        BLKSIZE
SORTLEN  EQU   *-SORTSECT    LENGTH OF SORT RECORD               90232
         SPACE 2
PROCXREF CSECT ,             GET BACK TO MAIN CSECT              90232
SORTLST1 DC    0A(0),X'80',AL3(SORTPRM1)                         90232
         DC    H'0'
SORTPRM1 DC    AL2(PARMEND1-*+2)                                 90233
         DC    A(SORT1,SORTEND1,SORTRCD,SORTRCDX,0,0)            90232
*ORTSIZ1 DC    F'100000'
SORTMSG1 DC    X'FF00',C'AP' MESSAGES TO SYSOUT                  90233
PARMEND1 EQU   *
         SPACE 1                                                 90233
*                              DSNAME    I/O       PROC      STEP     *
SORT1    DC    C' SORT FIELDS=(1,44,CH,A,45,1,CH,D,46,8,CH,A,62,8,CH,A'
         DC    C'),SIZE=E'                                       90233
SORTEST1 DC    C'40000'                                          90233
SORTEND1 DC    C' '                                              90233
         SPACE 1                                                 90232
SORTRCD  DC    C' RECORD LENGTH='                                90232
         EBCDIG SORTLEN,3    BUILD RECORD LENGTH                 90232
         DC    C',TYPE=F'                                        90232
SORTRCDX DC    C' '                                              90232
         SPACE 1
SORTLST2 DC    0A(0),X'80',AL3(SORTPRM2)                         90232
         DC    H'0'
SORTPRM2 DC    AL2(PARMEND2-*+2)                                 90233
         DC    A(SORT2,SORTEND2,SORTRCD,SORTRCDX,0,0)            90232
*ORTSIZ2 DC    F'100000'
SORTMSG2 DC    X'FF00',C'AP' MESSAGES TO SYSOUT                  90233
PARMEND2 EQU   *
         SPACE 1                                                 90233
*                              PGM       PROC      STEP               *
SORT2    DC    C' SORT FIELDS=(54,8,CH,A,46,8,CH,A,62,8,CH,A),'
         DC    C'SIZE=E'                                         90233
SORTEST2 DC    C'40000'                                          90233
SORTEND2 DC    C' '                                              90233
         SPACE 2                                                 90232
SAVE     DSECT ,             WORK/SAVE AREA                      90232
SAVEWK2  DS    18F           SUBROUTINE SAVE AREA                90232
SAVEWK3  DS    18F           THIRD LEVEL ROUTINE                 90232
DYNWORK  DS    0D            COMMON ADDRESSABILITY               90232
DB       DS    D             WORK WORDS                          90232
@SERVICE DS    A             @SERVICE ROUTINE                    90232
@PRINTER DS    A             @PRINTER ROUTINE                    90232
@INPREAD DS    A             @INPUT READER                       90232
ADDRSYMT DC    A(0)          ADDRESS OF SYMBOL TABLE             90232
ADDRTIOT DC    A(0)          ADDRESS OF CURRENT PROCLIB TIOT     90232
NUMSORT  DS    F             NUMBER OF RECORDS TO SORT           90232
NUMWORK  DS    F             NUMBER OF RECORDS IN WORK FILE      90232
KEYWDSVE DS    A             KEYWD ROUTINE - R9 SAVE             90232
KEYWDSV4 DS    A                           - SCAN TABLE SAVE     90232
SCANSV5  DS    A             SCANNER - START OF TEXT AFTER KEYWD 90232
PDBXLE   SERVPDS DSECT=NO,RETURN=YES  EXPAND DIRECTORY DATA      90232
         SPACE 1                                                 90232
ACCTMASK DC    H'0'          ACCOUNT NUMBER IN PROCLIB SSI       90232
         SPACE 1                                                 90232
PROCIN   INPWORK PROCDD,WIDTH=80,PDS=MEM,PDE=PDBXLE              90232
PROCINL  EQU   *-PROCIN      LENGTH TO MOVE                      90232
RETCODE  DC    F'0'          PROGRAM RETURN CODE                 90232
RETCH    EQU   RETCODE+L'RETCODE-1,1,C'B'  QUICKY DEFINITION     90232
PFLAG    DC    X'00'         PRINTER FLAGS                       90232
FGCONT   EQU   X'80'           CONTINUATION CARD EXPECTED        90232
NEWPAGE  EQU   X'40'           PAGE EJECTED IN RESPONSE TO REQUEST
FGPRIVY  EQU   X'01'           USER IS AUTHORIZED                90232
         SPACE 1                                                 90232
OPLSTIN  DC    A(0)                                              90232
OPLSTWRK DC    A(0)                                              90232
OUTANDIN DCB   DDNAME=SORTOUT,DSORG=PS,MACRF=(PM,GL),EODAD=DSNEOF
WRKFILE  DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(PM,GL),EODAD=EXPEOF,      *
               RECFM=FB,LRECL=SORTLEN,BLKSIZE=20*SORTLEN         90232
         SPACE 1
FOUND    DC    CL80' '
         DC    C' '
BACKREC  DS    0CL(SORTLEN)  N013 SAVE AREA                      90232
W2STAT   DC    CL256' '
W2STAT2  DC    CL256' '
         SPACE 1                                                 90232
DSNAME   DC    CL44' '
         ORG   DSNAME        RE-USE                              90232
PROC     DC    CL8' '
PROGRAM  DC    CL8' '
STEP     DC    CL8' '
         ORG   ,                                                 90232
         SPACE 1                                                 90232
INREC    DS    CL80          INPUT RECORD                        90232
         SPACE 1                                                 90232
         DC    C' '
SORTREC  DS    CL(SORTLEN)   SORT SEND AREA                      90232
         SPACE 2                                                 90232
*        STATEMENT PROCESSING AREA                               90232
WKAREA1  DS    CL(STATMTSZ)  INPUT STATEMENT                     90232
         ORG   WKAREA1                                           90232
W1NAME   DC    CL8' '                   JOBNAME-PROCNAME-STEP-DDNAME
W1IDNT   DC    CL4' '                   JOB-PROC-EXEC-DD
W1STAT   DS    CL256         STATEMENT                           90232
W1STAT2  DS    0C                                                90232
         ORG   ,                                                 90232
WKAREA1E EQU   *             END OF STATEMENT WORK AREA
*        SYMBOL TABLE                                                 *
SYMTAB   DS    CL(SYMTABSZ)  TABLE OF SYMBOLS AND VALUES         90232
         ORG   SYMTAB                                            90232
SYML1    DC    CL1' '                   ENTRY LENGTH
SYML2    DC    CL1' '                   NAME LENGTH
SYMNAME  DC    CL8' '                   ENTRY NAME
SYML3    DC    CL1' '                   EQUATE LENGTH
SYMEQU   DS    0C                       EQUATE                   90232
         ORG   ,                                                 90232
SYMTABE  EQU   *
SAVEND   EQU   *                                                 90232
         EJECT ,                                                 90232
PROCXREF CSECT ,                                                 90232
*        ITEM CONTINUATION PROCESSING - IF NEW PAGE EXIT IS TAKEN
*          SET THE CONTINUATION FLAG.                            90232
         PUSH  USING         SAVE ALL DEFINITIONS                90232
         DROP  ,                                                 90232
         USING HEADEXIT,R15                                      90232
         USING PUXPLIST,R1   PASSED PARAMETER LIST               90232
HEADEXIT L     R9,PUXPSAVE   GET OUR SAVE/WORK AREA BACK         90232
         SRL   R9,12         GETMAIN ON PAGE BOUNDARY            90232
         SLL   R9,12                                             90232
         USING SAVE,R9       DECLARE IT                          90232
         OI    PFLAG,NEWPAGE  SET NEW PAGE                       90232
         SLR   R15,R15       SET TO PRINT ALL LINES              90232
         BR    R14           RETURN TO CALLER                    90232
         POP   USING         RESTORE REGISTER MAPPINGS           90232
         LTORG ,                                                 90232
         SPACE 1                                                 90232
         PRINT &PRTSYS                                           90232
         CVT   DSECT=YES                                         90232
         IHACDE ,                                                90232
         IEFTIOT1 ,                                              90232
         DCBD  DEVD=DA,DSORG=PS                                  90232
         END   ,                                                 90232
