NOTCAT   TITLE 'CATALOG/VTOC COMPARISON PROGRAM'
         PUNCH '  ORDER NOTCAT(P) '                             GP12063
         PUNCH '   SETCODE AC(1) '  OPTIONAL, FOR VOLMOUNT ONLY GP14248
         COPY  ASMOPTS                                          GP14248
         ASMPARM  LIST=YES                                      GP14248
***********************************************************************
*                                                                     *
* NAME         NOTCAT, WAS SS0127                                     *
*                                                                     *
* FUNCTION     THIS PROGRAM WILL READ THE VOLUME TABLE OF CONTENTS    *
*              (VTOC) OF UP TO 99 DIRECT ACCESS VOLUMES TO OBTAIN     *
*              THE DATA SET NAME OF EACH DATA SET ON THE VOLUME.      *
*              THE CATALOG ENTRY FOR EACH DATA SET WILL BE READ TO    *
*              DETERMINE IF THE DATA SET IS CATALOGED, AND IF THE     *
*              CATALOG INFORMATION IS CORRECT.                        *
*                                                                     *
* ENVIRONMENT  OS/VS2                                                 *
*              OS/MVT                                                 *
*              BACKGROUND                                             *
*                                                                     *
* DESCRIPTION  THE VTOC IS OPENED AS A SEQUENTIAL DATA SET AND READ   *
*              USING BSAM.  THE PROGRAM MUST SUPPLY THE VTOC DSNAME   *
*              BY READING AND MODIFYING THE JFCB AND PERFORMING AN    *
*              OPEN TYPE=J.  EVERY ENTRY WHICH IS NOT A FORMAT 1      *
*              DSCB IS IGNORED.  THE DSNAME IS OBTAINED FROM THE      *
*              FORMAT 1 DSCB AND A LOCATE BY NAME IS ISSUED.  IF THE  *
*              RETURN CODE IS NOT ZERO, THE DATA SET IS NOT CATALOGED *
*              AND A MESSAGE IS ISSUED.                               *
*                                                                     *
*              IF THE RETURN CODE IS ZERO, THE VOLUME LIST RETURNED   *
*              BY LOCATE IS SEARCHED TO BE SURE THAT THE LIST         *
*              CONTAINS THE VOLUME SERIAL OF THE VOLUME BEING         *
*              PROCESSED.  IF NOT, A MESSAGE IS ISSUED.               *
*                                                                     *
*              IF BOTH TESTS ARE MET, THE NEXT ENTRY IS PROCESSED.    *
*              THIS CONTINUES UNTIL THE END OF DATA IS REACHED.       *
*                                                                     *
*              VSAM DATA SETS ARE IGNORED.                            *
*                                                                     *
* INPUT        THE INPUT CONSISTS OF A VTOC OF A VOLUME (DEFINED BY   *
*              A DD STATEMENT), AND THE CATALOG (NO DD STATEMENT      *
*              REQUIRED).                                             *
*                                                                     *
* OUTPUT       OUTPUT CONSISTS OF -                                   *
*                   1.  A PRINTED REPORT LISTING THE DATA SETS ON THE *
*                       VOLUME WHICH HAVE NO CATALOG ENTRIES, OR FOR  *
*                       WHICH THE CATALOG ENTRY DOES NOT POINT TO THE *
*                       VOLUME.                                       *
*                                                                     *
*                   2.  A FILE OF IEHPROGM SCRATCH STATEMENTS FOR     *
*                       EACH UNCATALOGED OR MISCATALOGED DATA SET.    *
*                                                                     *
* PARAMETERS   NONE.                                                  *
*                                                                     *
* EXECUTION    EXEC         SPECIFIES THE PROGRAM NAME.  NO PARM      *
*                           INFORMATION IS SPECIFIED.                 *
*                                                                     *
*              SYSPRINT DD  DEFINES THE OUTPUT MESSAGE DATA SET.      *
*                                                                     *
*              SYSUT2   DD  DEFINES THE OUTPUT IEHPROGM STATEMENTS.   *
*                                                                     *
*              VOLUMENN DD  DEFINES THE VOLUMES TO BE PROCESSED.      *
*                           VOLUME01 FOR THE FIRST VOLUME,            *
*                           VOLUME02 FOR THE SECOND VOLUME, ETC.      *
*                                                                     *
* EXAMPLE      LIST THE UNCATALOGED AND IMPROPERLY CATALOGED DATA     *
*              SETS ON WK1101 AND WK1102.                             *
*                                                                     *
*              //LIST   EXEC  PGM=NOTCAT                              *
*              //SYSPRINT DD  SYSOUT=*                                *
*              //SYSUT2   DD  SYSOUT=B (OR TEMP DASD DS)              *
*              //VOLUME01 DD  DISP=OLD,UNIT=SYSALLDA,VOL=SER=WORK01   *
*              //VOLUME02 DD  DISP=OLD,UNIT=SYSALLDA,VOL=SER=WORK02   *
*                                                                     *
*              THE PROGRAM WILL LIST ALL ON-LINE VOLUMES WHEN THE     *
*              USER SUPPLIES A                                        *
*                   //VOLMOUNT DD DUMMY                               *
*              DD CARD. HOWEVER, THE PROGRAM MUST BE AUTHORIZED TO    *
*              DO SO; OTHERWISE ONLY VOLUMES WITH ANY DD CARD WILL    *
*              BE PROCESSED.                                          *
*                                                                     *
* RETURN CODES                                                        *
*               0 - SUCCESSFUL COMPLETION                             *
*               4 - NOT USED                                          *
*               8 - NOT USED                                          *
*              12 - NOT USED                                          *
*              16 - UNABLE TO OPEN SYSUT2, NO OUTPUT PRODUCED         *
*                                                                     *
* ABEND CODES  USER 1403 - UNABLE TO OPEN SYSPRINT, NO OUTPUT         *
*                          PRODUCED.                                  *
*                                                                     *
* IBM MACROS USED -                                                   *
*                                                                     *
*              OPEN      CLOSE     DCB       DCBD                     *
*              READ      CHECK     PUT                                *
*              RDJFCB    IEFJFCBN  IECSDSL1                           *
*              LOCATE    OBTAIN    CAMLST                             *
*              GETMAIN   FREEMAIN                                     *
*              ABEND                                                  *
*              LINK                                                   *
*                                                 9/76 GORDON P. WEST *
***********************************************************************
*     USERS ARE INVITED TO SUBMIT SUGGESTIONS OR ERROR DOCUMENTATION  *
*     TO FPL, HOWEVER, NO PROMISE CAN BE MADE THAT SUCH SUGGESTIONS   *
*     WILL BE IMPLEMENTED OR ERRORS CORRECTED.  SUBMIT COMMENTS TO:   *
*              COORDINATOR OF TECHNICAL SYSTEMS                       *
*              COMPUTER OPERATIONS DEPARTMENT                         *
*              FLORIDA POWER & LIGHT COMPANY - GENERAL OFFICE         *
*              P. O. BOX  529100                                      *
*              MIAMI, FLORIDA  33152                                  *
*                                                                     *
*     THIS PROGRAM IS MADE AVAILABLE BY FPL WITHOUT CHARGE OR         *
*     CONSIDERATION.  RECIPIENTS ARE FREE TO MAKE THIS PROGRAM        *
*     AVAILABLE TO OTHERS IN LIKE MANNER.  IT MAY NOT BE SOLD.        *
*                                                                     *
         SPACE 2
***********************************************************************
*                                                                     *
* UPDATE LOG                                                          *
*                                                                     *
* 03/07/78  CORRECTED ERROR WHEN OVER 200 UNCATALOGED OR MISCATALOGED *
*                DATA SETS FOUND                                      *
NUMLINE  EQU   200           DESIRED MAXIMUM FOR HOLD ENTRIES   GP12063
*           ADDED 3350 TO DEVICE TABLE                                *
* 2010-12-15  CONVERTED TO USE LOCAL MACROS AND SERVICES.  ADDED      *
*             3380, 3390, AND 9345.                   G. POSTPISCHIL  *
* 2014-09-05  MORE CONVERSION; ADDED OPTION TO PROCESS ALL MOUNTED    *
*             VOLUMES.                                G. POSTPISCHIL  *
*                                                                     *
***********************************************************************
         EJECT
NOTCAT   PGMHEAD ZERO31,BASE=(R11,R12)   ENTRY CODING           GP12063
*BANDAID INIT,SPIE ****************DEBUG******************
         MVC   CURRDD,=CL8'VOLUMEnn'   PRIME THE DD NAME        GP14248
         SERVINIT ,          INITALIZE MY SERVICE STUFF         GP12063
         SERVLOAD @PRINTER,@VOLREAD,SUBXTSUM                    GP14248
         PRTOPEN SYSPRINT,OPT=ABEND    OPEN SYSPRINT OR BOMB    GP12063
         PRTF  HEADER0,TITLE=1                                  GP12063
         PRTF  HEADER1,TITLE=3                                  GP12063
         PRTOPEN SYSUT2,OPT=NOWTO,DEV=2  OPEN PUNCH             GP12063
         CH    R15,=H'8'               DID IT WORK?             GP12063
         BL    HAVEOUT                   YES; ELSE SET ERROR    GP12063
         PRTF  ERROR8                                           GP12063
         MVICC 16,2                    SET CONDITION CODE       GP12063
HAVEOUT  LA    R1,HOLDTAB    GET DYNAMIC TABLE ADDRESS          GP12063
         ST    R1,TABLEADR     AND SET IT                       GP12063
         SPACE 1
         DEVTYPE =CL8'VOLMOUNT',DEVCODE     ALL VOLUME OPTION?  GP14248
         BXH   R15,R15,NEXTVOL                NO                GP14248
         CLI   DEVCODE+2,0                  DUMMY?              GP14248
         BNE   NEXTVOL                        NO                GP14248
         OI    PROFLAGS,FGLOOP                                  GP14248
         EJECT
***********************************************************************
*                                                                     *
*        OPEN VTOC AND READ FORMAT 4 DSCB                             *
*                                                                     *
***********************************************************************
         SPACE 2
NEXTVOL  TM    PROFLAGS,FGLOOP    DO ALL MOUNTED VOLUMES ?      GP14248
         BZ    READJFCB             NO; USE OLD CODE            GP14248
         L     R1,@UCB            GET PREVIOUS UCB ADDRESS      GP14248
NEXTUCB  SERVCALL UCBUM           LOOP THROUGH UCBS             GP14248
         LTR   R1,R0              DID IT WORK ?                 GP14248
         BZ    RETURN               NO; END OF UNITS            GP14248
         USING UCBOB,R1           DECLARE IT                    GP14248
         CLI   UCBTBYT3,UCB3DACC  DASD?                         GP14248
         BNE   NEXTUCB                                          GP14248
         TM    UCBSTAT,UCBONLI    ON LINE ?                     GP14248
         BZ    NEXTUCB              NO; IGNORE                  GP14248
         CLI   UCBVOLI,C' '       PLAUSIBLE SERIAL ?            GP14248
         BNH   NEXTUCB              NO; SKIP                    GP14248
         ST    R1,@UCB            SAVE FOR NEXT TIME            GP14248
         MVC   CURVOL,UCBVOLI     COPY SERIAL                   GP14248
         MVC   DEVCODE(4),UCBTBYT1     COPY TYPE INFO (NO BLKL) GP14248
         DROP  R1                                               GP14248
         B     COMLOOP                                          GP14248
READJFCB AP    COUNTER,=P'1'           INCREMENT COUNTER
         CP    COUNTER,=P'99'          TEST FOR MAXIMUM
         BH    RETURN
         UNPK  CURRDD+6(2),COUNTER     COPY ORDINAL             GP14248
         OI    CURRDD+L'CURRDD-1,C'0'  FIX ZONE                 GP14248
         DEVTYPE CURRDD,DEVCODE   GET DEVICE CODE               GP14248
         BXH   R15,R15,RETURN       ALL DONE ?                  GP14248
         CLI   DEVCODE+2,UCB3DACC      DIRECT ACCESS?           GP14248
         BNE   READJFCB                  NO; IGNORE             GP14248
         SPACE 1
         SERVCALL RJFCB,CURRDD         GET THE JFCB             GP14248
         BXH   R15,R15,ERRDD             FAILED                 GP14248
         USING JFCBDSNM,R1             DECLARE IT               GP14248
         MVC   CURVOL,JFCBVOLS         SAVE VOLUME SERIAL
         DROP  R1                                               GP14248
         SPACE 1
COMLOOP  ZI    PROFLAGS,FGOUT          RESET INDICATOR          GP14248
         L     R1,TABLEADR             LOAD TABLE ADDRESS
         LA    R2,L'HOLDTAB            LOAD LENGTH OF AN ENTRY  GP12063
         L     R3,=A(L'HOLDTAB*NUMLINE)  LOAD LENGTH OF TABLE   GP12063
         SR    R3,R2                   SUBRACT LENGTH OF ONE ENTRY
         AR    R3,R1                   ADD TABLE ADDRESS
         SR    R1,R2                   SUBTRACT LENGTH FROM TABLE ADDR
         STM   R1,R3,SAVE#3            SAVE
         VOLREAD OPEN,CURVOL,USE=VTOC  OPEN THE VOLUME          GP14248
         CH    R15,=H'4'               DID IT WORK              GP14248
         BL    GOODOPEN                  YES                    GP14248
         PRTSPACE 1                                             GP14248
 PRTDATA 'VTOC Open for',(CURVOL,PAD),'failed'                  GP14248
         OICC  8,6                                              GP14248
         B     DONEVOL                   DO NEXT                GP14248
         SPACE 1
GOODOPEN PRTF  HEADER0,TITLE=1         UPDATE HEADER LINE       GP12063
         VOLREAD DSCB        GET THE FORMAT 4                   GP14248
         CH    R15,=H'4'               DID IT WORK              GP14248
         BH    ERRVTOC                   NO                     GP14248
         BE    EOD1                      END OF DATA??          GP14248
         LTR   R1,R0         COPY DSCB ADDRESS                  GP14248
         MVC   IECSDSL1(DS4END-IECSDSL1),0(R1) SAVE IT          GP14248
         MVC   TRKPRCYL,DS4DEVSZ+2     SAVE TRACKS PER CYLINDER
         SR    R9,R9                   CLEAR TOTAL TRACKS ALLOCATED
         SR    R2,R2                   CLEAR R2
         IC    R2,DEVCODE+3            LOAD DEVICE CODE
         SLA   R2,3                    MULTIPLY BY 8
         LA    R2,DEVTABLE(R2)         LOAD TABLE ADDRESS
         SR    R15,R15
         IC    R15,0(,R2)              INSERT DEVICE NAME LENGTH
         LTR   R15,R15                 TEST NAME LENGTH
         BNZ   BLANKOUT
         PRTF  WARN          PRINT WARNING MESSAGE              GP12063
         LA    R15,5                   SET LENGTH FOR SYSDA
BLANKOUT MVC   SCRATCHV(14),BLANKS     BLANK OUT DEVICE CODE & SERIAL
         MVC   SCRATCHV(7),1(R2)       MOVE IN DEVICE NAME
         LA    R1,SCRATCHV(R15)        LOAD ADDRESS FOR SERIAL
         MVC   1(6,R1),CURVOL          COPY SERIAL TO SCRATCH RECORD
FINDBLNK LA    R1,1(,R1)
         CLI   0(R1),C' '
         BNE   FINDBLNK
         MVI   0(R1),C','
         EJECT
***********************************************************************
*                                                                     *
*        READ VTOC RECORDS AND COMPARE TO CATALOG                     *
*                                                                     *
***********************************************************************
         SPACE 2
LOOPVTOC VOLREAD DSCB                  GET THE NEXT ENTRY       GP14248
         CH    R15,=H'4'               DID IT WORK              GP14248
         BE    EOD1                      END OF DATA            GP14248
         BH    ERRVTOC                   NO                     GP14248
         LR    R1,R0         COPY DSCB ADDRESS                  GP14248
         MVC   IECSDSL1(DS1END-IECSDSL1),0(R1) SAVE IT          GP14248
         CLI   DS1FMTID,C'1'           TEST FOR FORMAT 1 DSCB
         BNE   LOOPVTOC                                         GP14248
         CLI   DS1DSORG+1,0  VSAM OR OTHER FUNNY?               GP14248
         BNZ   LOOPVTOC        SKIP WITH NO NOTICE              GP14248
         MVC   DSNAME,DS1DSNAM         COPY DSNAME FOR LOCATE (LOCATE
*                                           WILL CHANGE ALIAS NAME TO
*                                           TRUE NAME).
         MVC   VOLONE,BLANKS           PRESET FOR 'OTHER'       GP12063
         LOCATE LOCLIST                READ CATALOG ENTRY
         CH    R15,=H'8'               TEST RETURN CODE         GP14248
         BE    NOTCTLGD                  NOT CATALOG            GP14248
         BH    LOCERR                    LOCATE FAILED          GP14248
         LA    R8,CATBLOCK+2      POINT TO FIRST ENTRY          GP14248
         LH    R1,CATBLOCK             LOAD VOLUME COUNT
         MVC   VOLONE,4(R8)            SAVE FIRST CATALOG VOL   GP12063
         MIN   R1,=F'20'     TEST VOL COUNT AGAINST VS MAX      GP14248
GETSER   CLC   CURVOL,4(R8)            TEST VOL SER IN CAT TO THIS VOL
         BE    LOOPVTOC                                         GP14248
         LA    R8,12(,R8)              INCREMENT TO NEXT VOLUME ENTRY
         BCT   R1,GETSER               DECREMENT COUNT AND LOOP
         EJECT
***********************************************************************
*                                                                     *
*        CATALOG DOES NOT POINT TO THIS VOLUME                        *
*                                                                     *
***********************************************************************
         SPACE 2
         MVC   DSN2,DS1DSNAM           INCORRECT CAT ENTRY, COPY DSNAME
         MVC   SER2,CURVOL             COPY VOLUME SERIAL TO MESSAGE
         MVC   UNONVOL,BLANKS          CLEAR                    GP12063
         CLI   VOLONE,C' '             HAVE CAT VOL ?           GP12063
         BNH   NOREALCT                                         GP12063
         MVC   UNONVOL(9),=CL9'CAT SHOWS'                       GP12063
         MVC   UNONVOL+10(6),VOLONE    SHOW WHERE IT'S AT       GP12063
NOREALCT LA    R4,ALLOC2               LOAD OUTPUT AREA ADDRESS GP14248
         MVC   ALLOC2,BLANKS           BLANK OUT OUTPUT AREA    GP14248
         BAL   R14,$ALLOC              GET ALLOCATION INFORMATION
         AR    R9,R6                   ACCUMULATE TRACKS ALLOCATED
         LA    R2,CRDATE2              LOAD OUTPUT AREA ADDRESS
         MVC   0(14,R2),BLANKS         BLANK OUT OUTPUT AREA    GP12063
         BAL   R14,$CRDATE             GET CREATION DATE
         LM    R1,R3,SAVE#3            LOAD POINTERS
         BXH   R1,R2,OVERFLOW          INCREMENT POINTERS
         MVC   0(133,R1),ERROR2        MOVE LINE INTO STORAGE
         STM   R1,R3,SAVE#3            SAVE POINTERS
         OI    PROFLAGS,FGOUT          INDICATE OUTPUT PRODUCED GP14248
         B     LOOPVTOC                GET ANOTHER ENTRY        GP14248
         EJECT
***********************************************************************
*                                                                     *
*        DATA SET IS NOT CATALOGED                                    *
*                                                                     *
***********************************************************************
         SPACE 2
NOTCTLGD MVC   DSN3,DS1DSNAM           NOT CATALOGED, COPY DSNAME
         LA    R4,ALLOC3               LOAD OUTPUT AREA ADDRESS GP14248
         MVC   ALLOC3,BLANKS           BLANK OUT OUTPUT AREA    GP14248
         BAL   R14,$ALLOC              GET ALLOCATION INFORMATION
         AR    R9,R6                   ACCUMULATE TRACKS ALLOCATED
         LA    R2,CRDATE3              LOAD OUTPUT AREA ADDRESS
         MVC   0(14,R2),BLANKS         BLANK OUT OUTPUT AREA    GP12063
         BAL   R14,$CRDATE             GET CREATION DATE
         LM    R1,R3,SAVE#3            LOAD POINTERS
         BXH   R1,R2,OVERFLOW          INCREMENT POINTER
         MVC   0(133,R1),ERROR3        MOVE LINE INTO STORAGE
         STM   R1,R3,SAVE#3            SAVE POINTERS
         OI    PROFLAGS,FGOUT          INDICATE OUTPUT PRODUCED GP14248
         B     LOOPVTOC                GET ANOTHER ENTRY
         EJECT
***********************************************************************
*                                                                     *
*        GET ALLOCATION  (ALLOCATED, USED, AND EXTENTS)               *
*                                                                     *
***********************************************************************
*                                                                     *
*        R2        - OUTPUT AREA ADDRESS                              *
*        R3        - RESERVED (DSNAME PDE POINTER)                    *
*        R4        - NUMBER OF EXTENTS (COUNTER)                      *
*        R5        - EXECUTE TABLE POINTER                            *
*        R6        - TRACKS ALLOCATED                                 *
*        R7        - EXTENT DESCRIPTION POINTER/TRACKS USED           *
*        R8 - R13  - RESERVED (POINTERS AND BASE REGISTERS)           *
*                                                                     *
***********************************************************************
         SPACE 2
$ALLOC   ST    R14,SAVER14   SAVE RETURN ADDRESS                GP14248
$ALLOC2  XC    IECSDSL3(DS3END-IECSDSL3),IECSDSL3  CLEAR        GP14248
         VOLREAD DSC3        READ FMT2 OR FMT3                  GP14248
         LTR   R15,R15                 TEST RETURN CODE
         BP    PROCESS                                          GP14248
         MVC   IECSDSL3(DS3END-IECSDSL3),0(R1)   COPY THE DSCB  GP14248
         CLI   DS3FMTID,C'3' THREE OR TWO?                      GP14248
         BE    PROCESS       THREE; USE IT                      GP14248
         CLI   DS3FMTID,C'2' THREE OR TWO?                      GP14248
         BE    $ALLOC2       TWO (ISAM); TRY AGAIN              GP14248
ERRFMT3  MVC   DSN7,DS1DSNAM           MOVE DSNAME INTO MESSAGE
         PRTF  ERROR7        DISPLAY ERROR MESSAGE              GP12063
         OICC  8,213           SET CONDITION CODE               GP12063
         B     EOD1              AND SKIP OUT                   GP14248
***********************************************************************
**                                                                   **
**  GET TRACK ALLOCATION                                             **
**  SUBXTSUM DOES SPLIT CYLINDER, TOO!                               **
**  R0 - NUMBER OF EXTENTS IN GROUP                                  **
**  R1 - ADDRESS OF FIRST EXTENT                                     **
**  R2 - NUMBER OF TRACKS PER CYLINDER FOR THIS DEVICE               **
**                                                                   **
**  R15 - 0 ON ERROR, ELSE NUMBER OF TRACKS                          **
**                                                                   **
***********************************************************************
PROCESS  LA    R0,3          EXTENTS IN DSCB 1
         LA    R1,DS1EXT1    POINT TO FIRST EXTENT (COULD BE LABEL)
         LH    R2,TRKPRCYL   CYLINDER SIZE
         SR    R6,R6                   CLEAR REGISTER 6
         SUBCALL SUBXTSUM    CALCULATE TRACK USED IN GROUP
         AR    R6,R15                  ACCUMULATE TOTAL
         LA    R0,4          EXTENTS IN DSCB 3 FRONT
         LA    R1,DS3EXTNT   POINT TO FIRST
         LH    R2,TRKPRCYL   CYLINDER SIZE
         SUBCALL SUBXTSUM    CALCULATE TRACK USED IN GROUP
         AR    R6,R15                  ACCUMULATE TOTAL
         LA    R0,9          EXTENTS IN DSCB 3 BACK
         LA    R1,DS3ADEXT   POINT TO FIRST
         LH    R2,TRKPRCYL   CYLINDER SIZE
         SUBCALL SUBXTSUM    CALCULATE TRACK USED IN GROUP
         AR    R6,R15                  ACCUMULATE TOTAL
DSATUTRK CVD   R6,DB                   CONVERT TRACKS TO DECIMAL
         MVC   DEC,=X'4020202020202120'      MOVE MASK
         ED    DEC,DB+4                EDIT
         MVC   0(5,R4),DEC+3           MOVE INTO MESSAGE
*--------GET TRACKS USED
         SLR   R7,R7                                            GP14248
         ICM   R7,7,DS1LSTAR      LOAD AND TEST                 GP14248
         BZ    DSATXNTS                                         GP14248
         SRL   R7,8               REMOVE RECORD NUMBER          GP14248
         LA    R7,1(,R7)               ADD 1
         CVD   R7,DB                   CONVERT TO DECIMAL
         MVC   DEC,=X'4020202020202120'  MOVE MASK
         ED    DEC,DB+4                EDIT
         MVC   6(5,R4),DEC+3           MOVE INTO MESSAGE
*--------GET NUMBER OF EXTENTS
DSATXNTS SR    R1,R1
         IC    R1,DS1NOEPV             LOAD EXTENTS             GP12063
         CVD   R1,DB                   CONVERT TO DECIMAL
         MVC   DEC,=X'4020202020202120'  MOVE MASK
         ED    DEC,DB+4                EDIT
         MVC   12(2,R4),DEC+6          MOVE INTO MESSAGE
         CLI   DS1EXT1,X'40'           USER LABEL TRACK ?       GP14248
         BNE   *+8                     NO                       GP14248
         MVI   12+2(R4),C'L'           FLAG LABEL TRACK         GP14248
         L     R14,SAVER14   LOAD RETURN ADDRESS                GP14248
         BR    R14                     RETURN
         EJECT
***********************************************************************
*                                                                     *
*        CREATION DATE                                                *
*                                                                     *
***********************************************************************
         SPACE 2
$CRDATE  L     R1,DS1CREDT-1           LOAD CREATION DATE
         BAL   R5,DATECONV             CONVERT IT
         MVC   0(8,R2),DB              MOVE DATE INTO MESSAGE
         BR    R14                     RETURN
         EJECT
***********************************************************************
*                                                                     *
*        DATE CONVERSION ROUTINE                                      *
*                                                                     *
*             THIS SECTION WAS ADAPTED FROM AN INSTALLATION           *
*             DATE CONVERSION ROUTINE TO AVOID HAVING MANY            *
*             DIFFERENT DATE CONVERSION ROUTINES.  IT IS              *
*             NECESSARY TO CONVERT THE DATE TO PACKED DECIMAL         *
*             FORM (AS IS THE DATE RETURNED BY TIME) TO               *
*             CONFORM TO THIS ROUTINE.                                *
*                                                                     *
***********************************************************************
         SPACE 2
DATECONV LA    R1,0(,R1)               ZERO HIGH BYTE
         TM    DS1DSORG+1,X'08'        TEST FOR VSAM
         BO    DATECON1
         LR    R0,R1                   SAVE DATE 00YYDDDD - BINARY
         N     R0,=X'0000FFFF'                   0000DDDD - BINARY
         CVD   R0,DB                             0000DDDS - DECIMAL
         L     R0,DB+4                           0000DDDS - DECIMAL
         N     R1,=X'00FF0000'                   00YY0000 - BINARY
         SRA   R1,16                             000000YY - BINARY
         CVD   R1,DB                             00000YYS - DECIMAL
         L     R1,DB+4                           00000YYS - DECIMAL
         SLL   R1,12                             00YYS000 - DECIMAL
         N     R1,=X'00FF0000'                   00YY0000 - DECIMAL
         OR    R1,R0                             00YYDDDS - DECIMAL
DATECON1 ST    R1,WORK                 SAVE DATE
         LA    R1,DB                   SAVE OUTPUT AREA ADDRESS
         USING OUTPUT,R1               DEFINE OUTPUT AREA BASE REG
         CP    DATE,=P'0'              TEST FOR ZERO DATE
         BE    NODATE
         UNPK  JULIAN,DATE             UNPACK DATE
         OI    JULDAY+2,X'F0'          SET ZONE
         MVI   BLANK,C' '              SET BLANK
         MVC   TARGET,=PL3'0'          00 00 0C
         MVO   YEAR2,YEAR              00 0Y YC
         DP    TARGET,=P'4'            0Q QC RC
         CP    REM,=P'0'               TEST REMAINDER
         BE    LEAP
         CP    DAY,=P'59'              TEST FOR BEFORE FEB 29
         BNH   LEAP
         AP    DAY,=P'1'               ADJUST TO LEAP YEAR
LEAP     CP    DAY,=P'1'               TEST MINIMUM DAY
         BL    BADDAY
         CP    DAY,=P'366'             TEST MAXIMUM DAY
         BH    BADDAY
         LA    R6,CALTABLE             LOAD TABLE ADDRESS
         USING CALENDAR,R6             SET BASE ADDRESS
         ZAP   MONTHP,=P'1'            SET MONTH TO 1
LOOP     CP    DAY,DAYS                SEE IF MORE DAYS THAN THIS MNTH
         BNH   OUT
         SP    DAY,DAYS                SUBTRACT DAYS IN THIS MONTH
         AP    MONTHP,=P'1'            ADD 1 TO MONTH
         LA    R6,LEN(,R6)             INCREMENT TO NEXT MONTH
         B     LOOP
OUT      UNPK  MO,MONTHP               UNPACK MONTH
         OI    MO+1,X'F0'              SET ZONE
         UNPK  DA,DAY                  UNPACK DAY
         OI    DA+1,X'F0'              SET ZONE
         MVI   SLASH1,C'/'             SET SLASH
         MVI   SLASH2,C'/'             SET SLASH
         B     DATERTRN
NODATE   MVC   0(12,R1),=CL12' '       BLANK OUTPUT FIELD
         B     DATERTRN
BADDAY   MVC   0(12,R1),=CL12'INVALID DATE'
DATERTRN BR    R5                      RETURN
         SPACE
***********************************************************************
*                                                                     *
*        CALENDAR TABLE - NUMBER OF DAYS IN EACH MONTH (LEAP YEAR)    *
*                                                                     *
***********************************************************************
         SPACE
CALTABLE DC    PL2'31'                 JANUARY
         DC    PL2'29'                 FEBRUARY
         DC    PL2'31'                 MARCH
         DC    PL2'30'                 APRIL
         DC    PL2'31'                 MAY
         DC    PL2'30'                 JUNE
         DC    PL2'31'                 JULY
         DC    PL2'31'                 AUGUST
         DC    PL2'30'                 SEPTEMBER
         DC    PL2'31'                 OCTOBER
         DC    PL2'30'                 NOVEMBER
         DC    PL2'31'                 DECEMBER
         EJECT
***********************************************************************
*                                                                     *
*   FINISHED PROCESSING VTOC AND CATALOG                              *
*                                                                     *
*   SORT AND PRINT MESSAGE RECORDS                                    *
*                                                                     *
***********************************************************************
         SPACE 2
OVERFLOW SR    R1,R2                   REDUCE POINTER ONE ENTRY
         STM   R1,R3,SAVE#3            SAVE POINTERS
         PRTF  ERROR9        DISPLAY ERROR MESSAGE              GP12063
         PRTF  HEADER0,TITLE=1  UPDATE HEADER                   GP12063
         SPACE 1
EOD1     VOLREAD TCLOSE                                         GP14248
         TM    PROFLAGS,FGOUT          SEE IF ANY RECORDS       GP14248
         BZ    NONE
         LM    R1,R3,SAVE#3            LOAD POINTERS
         LR    R3,R1                   RESET LIMIT TO LAST ENTRY
         L     R1,TABLEADR             LOAD TABLE ADDRESS
         STM   R1,R3,SAVE#3            SAVE POINTERS
         LR    R2,R1                   COPY FIRST TO R2
         #SORT FIELDS=(2,44,CH,A),FIRST=(R2),LAST=(R3),LENGTH=133
         ST    R9,NUMTRKS         SAVE TOTAL TRACKS             GP14248
         LM    R5,R7,SAVE#3            LOAD POINTERS
         SR    R5,R6                   SUBTRACT LENGTH OF ONE ENTRY
         EJECT
***********************************************************************
*                                                                     *
*        PRINT REPORT AND PUNCH SCRATCH STATEMENTS                    *
*                                                                     *
***********************************************************************
         SPACE 2
READLINE BXH   R5,R6,ENDVOL     POINT TO NEXT RECORD OR CLOSE   GP14248
         PRTF  (R5),L'HOLDTAB   PRINT LINE                      GP12063
         MVC   SCRATCHD,1(R5)          COPY DSNAME
         PRTF  SCRATCH1,DEV=2   PUT SCRATCH RECORDS             GP12063
         PRTF  SCRATCH2,DEV=2                                   GP12063
         B     READLINE                LOOP
         EJECT
***********************************************************************
*                                                                     *
*        NO UNCATALOGED OR MISCATALOGED DATA SETS ON VOLUME           *
*                                                                     *
***********************************************************************
         SPACE 2
NONE     PRTF  OK                                               GP12063
         B     DONEVOL                                          GP14248
         SPACE 2
***********************************************************************
*                                                                     *
*    UNRECOVERABLE ERROR PROCESSING VOLUME                            *
*                                                                     *
***********************************************************************
         SPACE 2
ERRVTOC  PRTLIST BADERR                                         GP12063
         OICC  8
         B     DONEVOL                                          GP14248
BADERR   FDOPT NL,CC=C'0'                                       GP14248
         FDPRT 'Unrecoverable processing error on volume'       GP14248
         FDPRT CURVOL,PAD                                       GP14248
         FD    *END                                             GP14248
         SPACE 2
***********************************************************************
*                                                                     *
*    ERROR READING SYSTEM CONTROL BLOCK (JFCB, ETC.)                  *
*                                                                     *
***********************************************************************
         SPACE 2
ERRDD    PRTLIST BADERDD                                        GP12063
         OICC  8
         B     DONEVOL                                          GP14248
BADERDD  FDOPT NL,CC=C'0'                                       GP14248
         FDPRT 'Unrecoverable error processing DD'              GP14248
         FDPRT CURRDD,PAD                                       GP14248
         FD    *END                                             GP14248
         SPACE 2
***********************************************************************
*                                                                     *
*        END OF VOLUME                                                *
*                                                                     *
***********************************************************************
         SPACE 2
ENDVOL   PRTLIST DONE                                           GP14248
DONEVOL  VOLREAD TCLOSE           CLOSE VOLUME                  GP14248
         B     NEXTVOL
         SPACE 2
***********************************************************************
*                                                                     *
*    LOCATE SVC ERROR                                                 *
*                                                                     *
***********************************************************************
         SPACE 2
LOCERR   STM   R15,R0,DB     SAVE RETURN CODES                  GP14248
         VOLREAD TCLOSE      CLOSE VOLUME                       GP14248
         PRTLIST LOCERRFD    SHOW WHAT AND WHO                  GP14248
         MVICC 16,22                                            GP14248
         B     PGMEXIT                                          GP14248
LOCERRFD FDOPT NL,CC=C'0'                                       GP14248
         FDPRT 'LOCATE SVC failed; R15='                        GP14248
         FDPRT DB,4,I,PADR,DEB,LADJ                             GP14248
         FDPRT 'R0='                                            GP14248
         FDPRT DB+4,4,I,DEB,LADJ                                GP14248
         FDPRT ' ('                                             GP14248
         FDPRT DB+4,4,HEX                                       GP14248
         FDPRT ')'                                              GP14248
         FD    *END                                             GP14248
         EJECT
***********************************************************************
*                                                                     *
*        OUTPUT COMPLETE - TERMINATE                                  *
*                                                                     *
***********************************************************************
         SPACE 2
RETURN   PRTLIST PGMEND      FINAL (W)RITES                     GP14248
PGMEXIT  VOLREAD CLOSE       CLOSE AND FREE                     GP14248
         SERVTERM ,          CLOSE AND CLEAN UP                 GP12063
         PGMEXIT COPYRET=(RETCODE,8)  RETURN TO CALLER WITH RC  GP12063
         SPACE 1
PGMEND   FDOPT NL,CC=C'0'                                       GP14248
         FDPRT 'Program complete; CC ='                         GP14248
         FDPRT RETCODE,I,LADJ,DEB,PAD                           GP14248
         FD    *END                                             GP14248
         EJECT
***********************************************************************
*                                                                     *
*        DATA                                                         *
*                                                                     *
***********************************************************************
         SPACE
         LTORG
         SPACE
SAVE     DSECT ,             DYNAMIC SAVE AND WORK SPACE        GP12063
DB       DS    D                       WORK AREA
DEC      DS    D                       WORK AREA
DEVCODE  DS    D                       DEVTYPE INFORMATION
CURRDD   DS    CL8'VOLUMEnn'      DD NAME FOR EXPLICIT VOLUMES  GP14248
         SERVDEFS ,          DEFINE ADDRESSES, ETC.             GP12063
SAVER14  DS    A             RETURN ADDRESS SAVER               GP14248
@UCB     DS    A             CURRENT UCB ADDRESS                GP14248
TABLEADR DS    A             ADDRESS OF MESSAGE STORAGE AREA
SAVE#3   DS    3A            POINTERS TO ENTRIES IN STORAGE
NUMTRKS  DS    F             USED, UNCATALOG TRACKS/VOLUME      GP14248
TRKPRCYL DS    H                       TRACKS PER CYLINDER
PROFLAGS DC    X'00'              PROCESSING FLAGS              GP14248
FGLOOP   EQU   X'80'                LOOP THROUGH ALL VOLUMES    GP14248
FGOUT    EQU   X'01'                OUTPUT LINE ON STACK        GP14248
VOLONE   DS    CL6                FIRST SERIAL IN CATALOG LIST  GP12063
HOLDTAB  DS    (NUMLINE)XL133     TABLE HOLD AREA               GP12063
SAVEEND  EQU   *                                                GP12063
         SPACE 1
NOTCAT   CSECT ,                       RESUME                   GP12063
BLANKS   DC    CL20' '                 SPACES                   GP12063
COUNTER  DC    PL2'0'                  VOLUME COUNTER
DEVTABLE DC    X'00',CL7'SYSDA='    00   UNSUPPORTED
         DC    X'04',CL7'2311='     01
         DC    X'04',CL7'2301='     02
         DC    X'04',CL7'2303='     03
*OLD*    DC    X'04',CL7'2302='     04                          GP12063
         DC    X'04',CL7'9345='     04                          GP12063
         DC    X'04',CL7'2321='     05
         DC    X'06',CL7'2305-1='   06
         DC    X'06',CL7'2305-2='   07
         DC    X'04',CL7'2314='     08
         DC    X'04',CL7'3330='     09
         DC    X'04',CL7'3340='     0A                          GP12154
         DC    X'04',CL7'3350='     0B
         DC    X'04',CL7'3375='     0C                          GP12154
         DC    X'06',CL7'3330-1='   0D
         DC    X'04',CL7'3380='     0E                          GP12063
         DC    X'04',CL7'3390='     0F                          GP12063
         SPACE
WORK     DS    0F                      DATE
         DS    C                       FILLER
DATE     DS    0CL3                    JULIAN DATE - PACKED DECIMAL
YEAR     DS    C                       YEAR - NO SIGN
DAY      DS    CL2                     DD DS
MONTHP   DS    PL2                     0M MS
TARGET   DS    0CL3                    00 0Y YS  -  0Q QS RS
         DS    C                       0Q         PART OF QUOTIENT
YEAR2    DS    0PL2                    0Y YS      YEAR AND SIGN
         DS    C                       QS         QUOTIENT AND SIGN
REM      DS    C                       RS         REMAINDER AND SIGN
         SPACE
DSNAME   DS    CL44
VTOCDSNM DC    44X'04'                 VTOC DSNAME
LOCLIST  CAMLST NAME,DSNAME,,CATBLOCK
OBTAIN   CAMLST SEEK,DS1PTRDS,CURVOL,FMT3DSCB
         EJECT
FMT1DSCB DS    0F                      ALIGNMENT
         IECSDSL1 1                                             GP12063
         EJECT
         ORG   DS1FMTID           F4 MAPPED SANS 44X'04'        GP14248
         IECSDSL1 4                                             GP12063
         ORG   DS1END                                           GP14248
         EJECT
FMT3DSCB DS    0F                      ALIGNMENT
         IECSDSL1 3                                             GP12063
         ORG   DS3END                                           GP14248
         EJECT
ERROR2   DC    CL1' '                  CARRIAGE CONTROL          1    1
DSN2     DC    CL44' '                 DSNAME                   44   45
         DC    CL5' '                                            5   50
ALLOC2   DC    CL15' '                 ALLOCATION               GP14248
         DC    CL4' '                                           GP14248
CRDATE2  DC    CL8' '                  CREATION DATE             8   77
         DC    CL5' '                                            5   82
         DC    CL21' IS NOT CATALOGED ON '                      21  103
SER2     DC    CL6' ',CL4' '           VOLUME SERIAL             6  113
UNONVOL  DC    CL20' '                 FILLER                   20  133
         SPACE 2
ERROR3   DC    CL1' '                  CARRIAGE CONTROL          1    1
DSN3     DC    CL44' '                 DSNAME                   44   45
         DC    CL5' '                                            5   50
ALLOC3   DC    CL15' '                 ALLOCATION               GP14248
         DC    CL4' '                                           GP14248
CRDATE3  DC    CL8' '                  CREATION DATE             8   77
         DC    CL5' '                                            5   82
         DC    CL17' IS NOT CATALOGED'                          17   99
         DC    CL34' '                 FILLER                   34  133
         SPACE 2
ERROR7   DC    CL1'-'                                            1    1
         DC    CL32'ERROR READING FORMAT 3 DSCB FOR '           32   33
DSN7     DC    CL44' '                                          44   77
         DC    CL56' '                                          56  133
         SPACE 2
ERROR8   DC    C'-UNABLE TO OPEN SYSUT2/SYSPUNCH - LIMPING ON'  GP12063
         SPACE 2
ERROR9A  DC    C'-LIST INCOMPLETE, '                            GP12063
         EBCDIG NUMLINE,5                                       GP12063
         DC    C' ENTRIES EXCEEDED'                             GP12063
ERROR9   EQU   ERROR9A,*-ERROR9A,C'C'                           GP12063
         SPACE 2
HEADER0A DC    C'#  CATALOG/VTOC COMPARISON FOR '               GP12063
CURVOL   DC    CL6' '
HEADER0  EQU   HEADER0A,*-HEADER0A,C'C'                         GP12063
         SPACE 2
HEADER1A DC    CL44' -------------------DSNAME-------------------'
         DC    CL5' '                                                50
         DC    CL16' Alloc  Used Ext'                                64
         DC    CL4' '                                                69
         DC    CL8'CR. DATE'                                         77
         DC    CL10' '
HEADER1  EQU   HEADER1A,*-HEADER1A,C'C'                         GP12063
         SPACE 2
OK       DC    CL133'-NO UNCATALOGED OR MISCATALOGED DATA SETS ON VOLUM+
               E'
         SPACE 2
WARN     DC    CL133'-** WARNING **  DEVICE NAME COULD NOT BE DETERMINE+
               D.  SYSDA USED.  SCRATCH RECORDS MAY REQUIRED MODIFICATI+
               ON FOR MVS SYSTEMS.'
         SPACE 2
DONE     FDOPT NL,CC=C'0'                                       GP14248
         FDPRT 'Total:',RADJ,LEN=47                             GP14248
         FDPRT NUMTRKS,I,RADJ,LEN=7                             GP14248
         FD    'used on',PAD                                    GP14248
         FD    CURVOL                                           GP14248
         FD    *END                                             GP14248
         SPACE 2
SCRATCHA DC    CL14'  SCRATCH VOL='                                  14
SCRATCHV DC    CL57' '                                               71
         DC    CL9'X'                                                80
SCRATCH1 EQU   SCRATCHA,*-SCRATCHA,C'C'                         GP12063
         SPACE 2
SCRATCHB DC    CL15' '                                               15
         DC    CL7'DSNAME='                                          22
SCRATCHD DC    CL44' '                                               66
SCRATCH2 EQU   SCRATCHB,*-SCRATCHB,C'C'                         GP12063
         EJECT
SYSPRINT PRTWORK SYSPRINT,TITLE=5      DDNAME IS SYSPRINT       GP12063
         SPACE 1
SYSUT2   PUNWORK SYSUT2,SYSPUNCH  DDNAME IS SYSUT2 OR SYSPUNCH  GP12063
         SPACE 2
CATBLOCK DS    0D                      ALIGNMENT
         DS    265C                    AREA FOR CATALOG BLOCK
         SPACE 2
***********************************************************************
*                                                                     *
*        CALENDAR TABLE DSECT                                         *
*                                                                     *
***********************************************************************
         SPACE
CALENDAR DSECT
DAYS     DS    PL2                     DAYS IN MONTH
LEN      EQU   *-DAYS                  LENGTH OF ENTRY
         SPACE 2
***********************************************************************
*                                                                     *
*        OUTPUT AREA DSECT                                            *
*                                                                     *
***********************************************************************
         SPACE
OUTPUT   DSECT                         OUTPUT DSECT
MO       DS    CL2                     MONTH
SLASH1   DS    C                       SLASH
DA       DS    CL2                     DAY
SLASH2   DS    C                       SLASH
JULIAN   DS    0CL5                    JULIAN DATE - EBCDIC
YR       DS    CL2                     YEAR
JULDAY   DS    CL3                     JULIAN DAY
BLANK    DS    C                       BLANK
         SPACE 2
MYJFCB   DSECT ,                  MAP JFCB                      GP14248
         IEFJFCBN ,
         SPACE 2
MYUCB    DSECT ,                  MAP UCB                       GP14248
         IEFUCBOB ,
         END
