TAPEMARK TITLE 'T A P E M A R K  ***  INITIALIZE NL TAPES'
***********************************************************************
**                                                                   **
**  TAPEMARK IS INTENDED TO INITIALIZE UNLABELED TAPES, USING AN     **
**  OPERATOR CONTROL START JOB.                                      **
**                                AUTHOR:  MICHAEL RADZYMSKI         **
**                                                                   **
***********************************************************************
TAPEMARK START 0
         REGEQU
         PRINT NOGEN
         STM   R14,R12,12(R13)
         LR    R12,R15       DECLARE A BASE REGISTER
         USING TAPEMARK,R12
         LA    R9,SAVEREGS   GET MY SAVE AREA
         ST    R9,8(,R13)
         ST    R13,4(,R9)
         LR    R13,R9
         SPACE
         USING IHADCB,R10
         LA    R10,TAPEDCB   FOR USING
        DEVTYPE DCBDDNAM,TAPEDATA    GET THE DEVICE TYPE
         RDJFCB TAPEDCB      GET THE JFCB TO SET THE DENSITY
         EJECT
WTOR     XC    WTORECB,WTORECB  CLEAR THE ECB
         TM    TAPEDATA+1,X'C0' 7 TRACK TAPE ???
         BZ    WTOR9         NO, PROMPT FOR  NINE TRACK ONLY
         SPACE
         LA    R3,DENSITY7   GET SEVEN TRACK DENSITY TABLE
         MVC   JFCBVOLS(6),=C'TAP777'  MOVE VOLSER INTO JFCB
WTOR7    WTOR  '**TAPEMARK** PROG ID: 72,75,78 -- END',                *
               REPLY,3,WTORECB
         B     WTORWAIT      WAIT FOR OPERATOR
         SPACE
WTOR9    LA    R3,DENSITY9   GET NINE TRACK DENSITY TABLE
         MVC   JFCBVOLS(6),=C'TAP999'
         WTOR  '**TAPEMARK** PROG ID: 98,96,62 -- END',                *
               REPLY,3,WTORECB
         SPACE
WTORWAIT WAIT  ECB=WTORECB
         OC    REPLY,=C'   ' MAKE UPPER CASE
         CLC   REPLY,=C'END' STOP ?????
         BE    EXIT          GOOD - BYE
         SPACE
DENLOOP  CLI   0(R3),X'FF'   TABLE END ???
         BE    BADDEN        YES, BAD REPLY PROBABLY ???
         CLC   REPLY(2),0(R3)
         BE    HAVEDEN
         LA    R3,3(,R3)     NEXT TABLE ENTRY
         B     DENLOOP       CHECK SOME MORE
HAVEDEN  XR    R2,R2         ZERO IC REGISTER
         IC    R2,2(,R3)     GET DENSITY FOR OI INTO JFCB
         OI    JFCBLTYP,JFCBLP TURN ON BLP FLAG
         OI    JFCBTSDM,JFCNWRIT  DONT WRITE JFCB BACK
         EX    R2,OIDEN      TURN PROPER DENSITY ON
         EJECT
HOWMANY  XC    WTORECB,WTORECB
         MVC   FINAL(6),TESTPAT
         WTOR  '**TAPEMARK**  HOW MANY ?  1-999',REPLY,3,WTORECB
         WAIT  ECB=WTORECB
MVC      MVC   FINAL(6),FINAL+1
         CLI   REPLY,255
         BNE   MVC
         MVC   MASKWORK,MASK
         NC    MASKWORK,FINAL
         CLC   MASKWORK,MASK
         BNE   HOWMANY
         PACK  DOUBLE,FINAL(3)
         CVB   R4,DOUBLE
         LTR   R4,R4
         BZ    EXIT
OPEN     OPEN  (TAPEDCB,(OUTPUT)),TYPE=J
         CLOSE TAPEDCB
         BCT   R4,OPEN
         B     WTOR
         SPACE 2
BADDEN   WTO   '**TAPEMARK**  INVALID DENSITY ???'
         B     WTOR
         SPACE
OIDEN    OI    JFCDEN,0      THIS IS THE EXECUTIONER
         SPACE 3
EXIT     L     R13,SAVEREGS+4
         LM    R14,R12,12(R13)
         XR    R15,R15
         BR    R14
         SPACE
         TITLE '*** CONSTANTS AND DATA AREAS ***'
DOUBLE   DC    D'0'          PACK WORK AREA
TAPEDATA DC    D'0'          DEVTYPE WORK AREA
SAVEREGS DC    18F'0'        SAVE AREA
EXLST    DC    X'87',AL3(INFMJFCB)
WTORECB  DC    F'0'          WTOR ECB
FINAL    DC    CL3' '
REPLY    DC    CL3' '        REPLY AREA
         DC    X'FFFFFF'
MASK     DC    CL3'000'
MASKWORK DC    CL3' '
TESTPAT  DC    X'F0F0F0FFFFFF'
         SPACE 2
DENSITY9 DC    C'96',X'C3'   NINE TRACK - 1600 BPI
         DC    C'98',X'83'   NINE TRACK - 800  BPI
         DC    C'62',X'D3'   NINE TRACK - 6250 BPI
         DC    X'FFFFFF'
         SPACE
DENSITY7 DC    C'78',X'83'   SEVEN TRACK - 800 BPI
         DC    C'75',X'43'   SEVEN TRACK - 556 BPI
         DC    C'72',X'03'   SEVEN TRACK - 200 BPI
         DC    X'FFFFFF'
         SPACE 2
       IEFJFCBN
         SPACE 2
TAPEDCB  DCB   DSORG=PS,MACRF=PM,DEVD=TA,DDNAME=TAPEDD,RECFM=F,        *
               BLKSIZE=20,EXLST=EXLST
         DCBD  DSORG=PS,DEVD=TA
         END
