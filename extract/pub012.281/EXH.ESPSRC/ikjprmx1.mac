PRMX1    TITLE 'I K J P R M X 1  ***  TSO/PARMLIB AUTHORIZATION'
         PRINT NOGEN
IKJPRMX1 START 0
IKJPRMX1 AMODE 31
IKJPRMX1 RMODE ANY
         STM   R14,R12,12(R13)  SAVE CALLER'S REGISTERS
         LR    R12,R15       COPY BASE
         LR    R8,R1         ADDRESS OF EXIT PARAMETER LIST
         USING IKJPRMX1,R12  DECLARE THE BASE
         LTCB  R2,USE=YES    LOAD THE TCB
         SLR   R9,R9         PRESET THE DEFAULT - USE RACF CHECKING
         ICM   R3,15,TCBTIO  RUNNING WITHOUT TIOT?
         BZ    EXIT          HOW DID WE MASTER THAT?
         LM    R5,R7,USERBXLE
SCANLOOP CLC   0(8,R3),0(R5)  NAME IN LIST ?
         BE    BINGO         YES - ALLOW ACCESS
         BXLE  R5,R6,SCANLOOP
         B     EXIT
         SPACE 1
BINGO    LA    R9,4          ALLOW ACCESS
EXIT     L     R1,36(,R8)    GET EXIT PARAMETER 10
         ST    R9,8(,R1)     SET RETURN VALUE
         LM    R14,R12,12(R13)  RESTORE REST
         SLR   R15,R15       PROCEED
         BR    R14           RETURN
         SPACE 1
USERBXLE DC    A(USER1,USER2-USER1,USERL)
USER1    DC    CL8'PAT     '
USER2    DC    CL8'PATM    '
         DC    CL8'GEORGEG '
         DC    CL8'GERHARD '
         DC    CL8'ZAP     '
         DC    CL8'ZAP     '
         DC    CL8'ZAP     '
         DC    CL8'ZAP     '
         DC    CL8'ZAP     '
         DC    CL8'ZAP     '
         DC    CL8'ZAP     '
         DC    CL8'ZAP     '
         DC    CL8'ZAP     '
         DC    CL8'ZAP     '
USERL    DC    CL8'IBMUSER '
         SPACE 1
         YREGS ,             EXPAND REGISTER DEFINITIONS
         IHAPSA ,            LOW STORAGE
         IKJTCB ,
         CVT   DSECT=YES
         END   ,
