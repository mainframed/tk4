RANDOM   TITLE 'RANDOM NUMBER GENERATOR, RETURNS VALUE BETWEEN 0 AND 1'
***********************************************************************
**                                                                   **
**  FORTRAN: Y=RANDOM(X) ; WHERE X IS DUMMY, Y IS RETURN VALUE       **
**  ASSEMBLY:  CALL RANDOM     ;   STE  0,Y                      81138*
**                                                                   **
**  FROM EITHER - IF RANDU IS USED, THE 'X' VALUE PROVIDED IS        **
**    THE SEED TO BE USED.                                       81138*
**                                                               81138*
***********************************************************************
         ENTRY RANDU                                             81138
RANDOM   START 0                                                 81138
         USING RANDOM,R12                                       GP03147
         STM   R14,R12,12(R13)                                   81138
         LR    R12,R15
         BAS   R12,UNCOMMON  GO TO COMMON AND SET BASE           82015
         SPACE 1                                                 81138
         USING RANDU,R12     ALTERNATE ENTRY                     81138
RANDU    STM   R14,R12,12(R13) SAVE REGISTERS                    81138
         LR    R12,R15       SET BASE                            81138
UFIRST   NOP   UNCOMMON      SET TO BRANCH AFTER FIRST CALL      81138
         OI    UFIRST+1,X'F0'                                    81138
         OI    FIRST+1,X'F0'                                     81138
         L     R1,0(,R1)     GET USER'S 'X' ADDRESS              81138
         L     R1,0(,R1)     GET USER'S X VALUE                  81138
         LA    R12,COMMON    MAKE NEW BASE                       81138
         B     ODDITY-COMMON(R12) BRANCH TO COMMON               81138
UNCOMMON BAS   R12,COMMON    USE COMMON CODE                     81138
         SPACE 1                                                 81138
         USING COMMON,R12    NEW BASE                            81138
COMMON   L     R1,U                                              81138
FIRST    NOP   NEXT          CHANGED TO BRANCH AFTER FIRST CALL  81138
         OI    FIRST+1,X'F0'   MAKE NOP INTO BRANCH              81138
         TIME  BIN
         AR    R1,R0
         A     R1,U
ODDITY   LA    R0,1                                              81138
         OR    R1,R0         ENSURE ODD SEED                     81138
NEXT     M     R0,Y
         SRL   R1,8
         ST    R1,U
         A     R1,EXP
         ST    R1,SAVE
         LE    R0,SAVE
         LM    R14,R12,12(R13)
         BR    R14
*
U        DC    F'262145'
Y        DC    F'13187'
EXP      DC    X'40000000'
SAVE     DC    F'0'          TEMP SAVE                           81138
         PRINT NOGEN                                            GP03147
         YREGS ,                                                GP03147
         END
