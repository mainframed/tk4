PROP     TITLE 'P R O C U P  ***  USER PROCLIB UPDATE PROGRAM'
         MACRO
&NM      $CLOSE &DCB
         GBLC  &MACPLAB                                          81144
&MACPLAB SETC  '&NM'                                             81144
         MACPARM R2,&DCB                                         81144
&MACPLAB BAL   R14,$CLOSER                                       81144
         MEND
         SPACE 2
         COPY  OPTIONGB                                          82315
         SPACE 2                                                 82315
         SYSPARM LIST=YES                                        82315
         EJECT ,                                                 81144
         PRINT NOGEN
*        ALL PROCLIBS IN OUR SYSTEM ARE PASSWORD PROTECTED.      81144
*        THIS PROGRAM IS PROVIDED TO ALLOW AND CONTROL USER UPDATES.
*        IEBUPDTE IS INVOKED FOR VALID REQUESTS.                 81144
*        PROGRAM MUST RUN AUTHORIZED.                            81144
*                                                                81144
*        A SPECIAL 'SYSHANG' DD/DSN IS REQUIRED TO INTERLOCK     81144
*        CONCURRENT UPDATE REQUESTS. THE PROCLIBS PROPER ARE     81144
*        DEFINED ON SYSUT1-SYSUT2 WITH DISP=SHR.                 81144
*                                                                86188
*        CODE HAS BEEN ADDED TO RESERVE ON THE PACK CONTAINING THE
*        UPDATED LIBRARY - THIS PERMITS CONSOLIDATION OF MULTIPLE
*        LIBRARIES (ONE FOR EACH SYSTEM). THE RESERVE WILL BE CONVERTED
*        TO A PLAIN ENQUEUE WHEN GRS IS ACTIVE.                  86188
         SPACE 2                                                 81144
PROCUP   SAVEM ZERO,BASE=(R11,R12),ENTRY=PROCLEAN,ENTNO=WHOFG,PARM=R8
         ICM   R0,1,WHOFG    PROCLEAN ENTRY ?                    92279
         BNZ   CLEANPRO      YES                                 92279
         SPACE 1
         BAL   R9,COMMINIT   DO COMMON INITIALIZATION            81144
         MVC   REASON,=CL8'JMR'                                  81144
         LTCB  R4,USE=YES    LOAD AND DECLARE TCB                92279
         LAT   R5,TCBTCT,BADSET  GET TCT OR OUT                  81324
         SERVICE PASON       DISABLE PASSWORD CHECKING           92279
         USING SMFTCT,R5                                         81324
         LAT   R4,TCTJMR,BADSET  GET JMR OR OUT
         USING JMR,R4
         SERVCALL ACTST,JMRUSEID   VALID ACCOUNT NUMBER ?        92279
         BXH   R15,R15,BADSET  NO; FAIL                          92279
         MVC   REASON,=CL8'JOBCLASS'
         CLI   JMRUCOM,C'E'  CLASS E JOB ?
         BE    NOSETUP       OK                                  92279
         CLI   JMRCLASS,C'E'  CURRENTLY NON-SETUP ?              92279
         BNE   BADSET
         MVC   REASON,=CL8'PARM'                                 81144
         DROP  R4,R5                                             81324
         SPACE 1
NOSETUP  LTR   R8,R8         WAS THERE A PARM POINTER ?
         BZ    BADSET        NO ADDRESS ?
         MVI   DOOMED,X'F0'  PERMIT MEMBER DELETION              81144
         LAT   R8,0(R8),NOPARM  IGNORE IF NO PARM SUPPLIED
         XR    R0,R0                                             81144
         ICM   R0,3,0(R8)    LOAD PARM LENGTH AND TEST           81144
         BZ    NOPARM        NONE; EXPECTED                      81144
         CLC   =C'TEST',2(R8)  TEST RUN REQUESTED ?              81144
         BNE   PARMNTST      NO                                  81144
         MVI   DOOMED,0      RESET DELETION REQUEST SWITCH       81144
         LA    R8,4(,R8)     SKIP OVER PARM                      81144
         SH    R0,=H'4'      ANYTHING ELSE ?                     81144
         BNP   NOPARM        NO                                  81144
PARMNTST CH    R0,=H'4'      CORRECT LENGTH ?                    81144
         BNE   BADSET        NO; FAIL                            81144
         TM    ACCTPRIV,VAASYS+VAASUP  PERMITTED OVERRIDE ?      81144
         BZ    BADSET        NO; FAIL                            81144
         MVC   ACCT,2(R8)    COPY REQUESTED ACCOUNT              81144
         MVC   ACCTWORK,ACCT                                     81144
         SERVCALL ACTST,ACCTWORK,ERR=   TEST IT                  81144
         CH    R15,=H'4'     VALID ?                             81144
         BH    BADSET        NO                                  81144
         SRDL  R0,32+16      PUT HIGH R0 INTO LOW R1             81144
         SERVCALL ACCON,(R1),ERR=   GET INTERNAL EBCDIC FORM     81144
         STCM  R0,15,ACCT    AND STASH IT BACK                   81144
NOPARM   MVC   REASON,=CL8'ACCOUNT'                              81144
         MVC   ACCTWORK,ACCT  COPY ACCOUNT                       81144
         SERVCALL ACTST,ACCTWORK,ERR=  VALIDITY CHECK IT         81144
         CH    R15,=H'4'     VALID ?                             81144
         BH    BADSET        NO; OR ERROR IN CODE ?              81144
         STCM  R0,12,ACCTOFF   SAVE ACCOUNT OFFSET               81144
         STC   R0,ACCTPRIV   AND ACCOUNT PRIVILEGE FLAGS         81144
         MVC   DDUT,SYSDD    SET FOR SYSTEMS LIBRARY             81144
         TM    ACCTPRIV,VAASYS+VAASUP  PRIVILEGED ?              81144
         BNZ   TESTJFCB      YES; GO TO TEST JFCBS               81144
         MVC   DDUT,USRDD    CHANGE TO USER LIBRARY SYSUT2       81144
         CLC   =C'Z90',ACCT  SPECIAL DUMMY ?                     92279
         BE    BADSET        YES; BAD ACCOUNT                    81144
         CLC   =C'C90',ACCT  SPECIAL DUMMY ?                     93006
         BE    BADSET        DITTO                               81144
         SPACE 1
TESTJFCB MVC   REASON,=CL8'JCL FOR'  SHOW BAD JCL SETUP          81144
         LA    R2,2          TRY TWO DATASETS
         LA    R4,SYSDSN     POINT TO NAME TO BE CHECKED         81144
         MVC   POD+DCBDDNAM-IHADCB(8),SYSDD                      81144
LOOPJFCB MVC   REASONDD,POD+DCBDDNAM-IHADCB                      81144
         RDJFCB POD
         BXH   R15,R15,BADSET                                    81144
         CLC   JFCBDSNM(14),0(R4)  CORRECT DSN ?
         BNE   BADSET
         CLC   JFCBELNM(8),JFCBELNM-8  MEMBER MISSING ?
         BNE   BADSET        NO
         TM    JFCBIND1,JFCRLSE+JFCPDS FUNNY BITS ON ?
         BNZ   BADSET        YES - FUNNY MESSAGE
         TM    JFCBIND2,JFCMOD+JFCTEMP
         BNZ   BADSET
         TM    JFCBIND2,JFCSHARE  DISP=SHR ?
         BZ    BADSET        HOW DID UJV LET THIS PASS ?
         OC    JFCBUFL,JFCBUFL
         BNZ   BADSET
         TM    JFCDSORG,255-JFCORGPO
         BNZ   BADSET
         TM    JFCRECFM,255-JFCFIX-JFCRFB  FUNNY RECFM ?
         BNZ   BADSET
         CLI   JFCOPTCD,0
         BNE   BADSET
         OC    JFCLRECL(2),JFCLRECL
         BNZ   BADSET
         OC    JFCBLKSI(2),JFCBLKSI
         BNZ   BADSET
         OC    JFCBUFSI(2),JFCBUFSI
         BNZ   BADSET
         OC    JFCBSQTY(3),JFCBSQTY  SECONDARY SPACE ?
         BNZ   BADSET        TOO BAD
*
*        ASSUME IT IS OK ?
*
         LA    R4,USRDSN     POINT TO USER PROCLIB               81144
         MVC   POD+DCBDDNAM-IHADCB(8),USRDD                      81144
         BCT   R2,LOOPJFCB   TRY SECOND ONE
         SPACE 1
         MVC   POD+DCBDDNAM-IHADCB(8),=CL8'SYSHANG'              81144
         MVC   REASONDD,POD+DCBDDNAM-IHADCB                      81144
         RDJFCB POD
         BXH   R15,R15,BADSET                                    81144
         CLC   =CL8'SYSHANG',JFCBDSNM
         BNE   BADSET
         TM    JFCBIND2,JFCSHARE DISP=SHR ?
         BNZ   BADSET        DEFEATS INTERLOCK
         SPACE 1
         MVC   DDUT2,DDUT
         MVC   POD+DCBDDNAM-IHADCB(8),DDUT  COPY TO DCB          81144
         MVC   PDS+DCBDDNAM-IHADCB(8),DDUT                       81144
         MVC   REASON,=CL8'OPEN FOR'                             81144
         MVC   REASONDD,SYSIN+DCBDDNAM-IHADCB                    81144
         OPEN  (SYSIN,INPUT)  TRY FOR SYSIN
         TM    SYSIN+DCBOFLGS-IHADCB,DCBOFOPN   OPEN ?           81144
         BZ    BADSET        NO
         MVI   PROCFLAG,0    CLEAR PROCESSING FLAG
         BAL   R10,DCBINIT   INITIALIZE DCBS AND WORK AREA
         MVI   FTYPE,FBAD    PROVISIONALLY SET BAD CARD
         MVI   NULLFLAG,3      BUT SET EXEC/DD FOUND             81144
         SPACE 2
GETLOOP  CLI   FTYPE,FEOF    WAS PREVIOUS CARD 'ENDUP' ?
         BE    GETEOF        YES; TREAT AS END FILE
         GET   SYSIN
         CLI   0(R1),0       SAME VALUE AS OUR ERROR MESSAGES ?  81144
         BNE   *+8           NO                                  81144
         MVI   0(R1),1       MAKE ANOTHER BAD VALUE              81144
         ST    R1,CARDADD    SAVE CARD ADDRESS
         LR    R5,R1         SAVE CARD ADDRESS
         BAL   R10,TYPE      GET CARD TYPE
         TM    PROCFLAG,PFINIT  FIRST TIME ?
         BNZ   GETMORE       NO
         CLI   FTYPE,FSCR    SCRATCH CARD ?
         BE    SCRATCHR      YES; DO SCRATCH CHECKING
         BH    BADSEQ        CARD OUT OF SEQUENCE
         OI    PROCFLAG,PFINIT  ELSE SET INIT FLAG ON
GETMORE  CLI   FTYPE,FSCR    SCRATCH CARD ?
         BE    SCRATCHR      YES; DO SCRATCH PROCESSING
         BL    GETCONT       CONTROL CARD
         CLI   FTYPE,FDAT    VALID DATA ?
         BNH   COPIER        YES; COPY IT
BADSEQ   L     R5,CARDADD    GET THE BAD CARD BACK
         BAL   R10,COPY      COPY THE CARD
         LA    R5,SEQBAD     GET ERROR MESSAGE
         CLI   FTYPE,FNNAM   MISSING NAME FIELD ?                81144
         BNE   SETMSG        NO                                  81144
         LA    R5,BADFIELD   SET FOR OMITTED FIELD               81144
SETMSG   BAL   R10,COPY      PUT IT OUT
SETERR   OI    RETCH+1,12    SET BAD INPUT CARD
         MVI   DOOMED,0      PROHIBIT SCRATCHES
         B     GETLOOP       GET MORE
         SPACE 1
SCRATCHR OI    NULLFLAG,3    SET (PHONY) EXEC/DD FOUND           81144
         BAL   R10,LOCOLD    LOCATE OLD MEMBER, IF ANY           81144
         CLI   MEMFLAG,C'N'  DOES THE MEMBER EXIST ?             81144
         BE    SCRBAD        NO                                  81144
         CLC   ACCT,MEMNO    BELONGS TO THIS USER ?
         BNE   SCRBADA       NO                                  81144
         L     R9,DELETADD   LOAD ADDRESS OF DELETE TABLE
         C     R9,DELETORG   TABLE FULL ?
         BNH   GETLOOP       YES; IGNORE THIS
         MVC   0(11,R9),AMEM  MOVE MEMBER NAME AND TTR
         SH    R9,=H'12'     SET FOR NEXT ENTRY
         ST    R9,DELETADD   SAVE THE ADDRESS
         OI    PROCFLAG,PFSCR  SHOW SCRATCHES TO BE DONE
         OI    DOOMED,X'0F'  ENABLE SCRATCHES
         B     GETLOOP       GET ANOTHER CARD
SCRBAD   L     R5,CARDADD    PUT CARD OUT
         BAL   R10,COPY
         LA    R5,BADMEM
         B     SETMSG        EXPLAIN WHY FAILED
SCRBADA  L     R5,CARDADD    PUT CARD OUT                        81144
         BAL   R10,COPY                                          81144
         LA    R5,BADMEMA                                        81144
         B     SETMSG        EXPLAIN WHY FAILED                  81144
         SPACE 1
GETCONT  BAL   R10,LOCOLD    LOCATE OLD MEMBER, IF ANY
         CLI   FTYPE,FREP    WHICH CONTROL TYPE IS IT ?
         BE    GETCREP       REPLACE
         BH    GETCHNG       CHANGE
         CLI   MEMFLAG,C'N'  WAS THE MEMBER FOUND ?
         BE    GETCNONE      NO; ALLOW THE ADDITION              81144
         L     R5,CARDADD    GET THE OLD CARD
         BAL   R10,COPY      COPY IT
         LA    R5,BADDUP     SHOW DUPLICATE
         B     SETMSG
GETCREP  CLI   MEMFLAG,C'N'  WAS THE MEMBER FOUND ?
         BNE   GETCREG       YES; CHECK ACCOUNT
GETCNONE TM    PDSOTH+DCBOFLGS-IHADCB,DCBOFOPN  OTHER PDS OPEN ? 81144
         BZ    GETCNON2      NO;  SKIP DOUBLE-CHECK              81144
         BLDL  PDSOTH,MEMBLDL  SEE IF IN ANOTHER LIBRARY         81144
         BXH   R15,R15,GETCNON2  NO                              81144
GETCDUPE L     R5,CARDADD    PUT THE CARD                        81144
         BAL   R10,COPY                                          81144
         LA    R5,BADOTH     SET FOR DUPLICATE                   81144
         B     SETMSG        FAIL IT                             81144
GETCNON2 TM    PDSSYS+DCBOFLGS-IHADCB,DCBOFOPN  SYS PDS OPEN ?   81144
         BZ    GETCADD       NO; PROCESS ADD REQUEST             81144
         BLDL  PDSSYS,MEMBLDL  SEE IF IN SYSTEM PROCLIB(S)       81144
         BXLE  R15,R15,GETCDUPE  YES; FAIL                       81144
GETCADD  MVC   OPCODE(4),=C'ADD '  CONVERT REPLACE TO ADD        81144
         B     GETCOM        AND PROCEED
GETCREG  CLI   MEMFLAG,C'G'  WITH GOOD ACCOUNT NUMBER ?
         BNE   GETCOM        NO; PERMIT REPLACEMENT
GETCACCT CLC   ACCT,MEMNO    MATCHING ACCOUNT ?
         BNE   SCRBADA       NO; GIVE ERROR                      81144
         B     GETCOM        ELSE ALLOW
         SPACE 1
GETCHNG  CLI   MEMFLAG,C'G'  MEMBER EXISTS ?
         BNE   SCRBAD        NO; TOO BAD
         B     GETCACCT      YES; CHECK ACCOUNT NUMBER
         SPACE 1
GETCOM   LA    R5,NEWCARD    POINT TO NEW CONTROL CARD
         OI    PROCFLAG,PFUP SHOW UPDATES TO BE DONE
         B     COPIEX        COPY IT AND GET MORE
         SPACE 1
COPIER   L     R5,CARDADD    GET TEXT CARD ADDRESS
COPIEX   LA    R10,GETLOOP   SET RETURN POINT AFTER COPY
COPY     TM    UPDTEIN+DCBOFLGS-IHADCB,DCBOFOPN  IS DCB OPEN ?   81144
         BNZ   COPOUT        YES
         MVC   REASONDD,UPDTEIN+DCBDDNAM-IHADCB                  81144
         OPEN  (UPDTEIN,OUTPUT)
         TM    UPDTEIN+DCBOFLGS-IHADCB,DCBOFOPN  OPEN NOW ?      81144
         BZ    BADSET        NO
COPOUT   PUT   UPDTEIN,(R5)  PUT OUT THE CARD
         BR    R10           RETURN TO CALLER
         SPACE 2
GETEOF   $CLOSE SYSIN
         CLI   NULLFLAG,0    ANY JCL IN MEMBER ?                 81144
         BNE   GETEOFF       YES                                 81144
         LA    R5,BADJCL     SET FOR MISSING DD/EXEC             81144
         BAL   R10,COPY      TACK IT IN                          81144
         OI    RETCH+1,12    SET BAD INPUT                       81144
GETEOFF  MVI   LINE,C' '     BUILD AN ENDUP CARD                 81144
         MVC   LINE+1(79),LINE
         MVC   LINE(2),=C'./'
         MVC   LINE+9(5),=C'ENDUP'
         LA    R5,LINE
         BAL   R10,COPY      PUT OUT
GETEOF1  $CLOSE UPDTEIN
         $CLOSE PDSOTH                                           81144
         $CLOSE PDSSYS                                           81144
         TM    DOOMED,X'FF'  ANY DELETIONS ?                     81144
         BZ    GETEOF2       NO (IF MIXED, PRINT LIST)           81144
         MVC   HEAD2VAR(L'HEADSCR),HEADSCR  MAKE NEW SUBTITLE
         PRTF  HEAD2,121,TITLE=3                                 81144
         MVI   CTLCHAR,C' '  SINGLE SPACE
         MVC   LINE,LINE-1   CLEAR PRINT LINE                    81144
GETEOF2  BAL   R10,DODELETE  DO DELETIONS, IF ANY; CLOSE DCBS
         TM    DOOMED,X'0F'  DELETE REQUESTED ?                  81144
         BNO   GETEOF3       NO; SKIP DELETE PROCESSING          81144
         L     R9,DELETORG   DELETE TABLE START ADDRESS
GETDLOOP TM    0(R9),X'FF'   CHECK TYPE OF LIST ENTRY
         BZ    GETDINC       ZERO - GET NEXT ENTRY
         BO    GETEOF3       ALL ONES - END OF LIST
         MVC   LINE+10(8),0(R9)  SHOW MEMBER NAME
         TM    DOOMED,X'FF'  FULL DELETE MODE ?                  81144
         BNO   GETNDEL       NO; JUST PRINT                      81144
         CLI   11(R9),0      DELETED ?
         BE    GETDELT       YES
GETNDEL  MVC   LINE+20(L'DCNOTD),DCNOTD                          81144
         B     GETDELM       GIVE COMMON MESSAGE
GETDELT  MVC   LINE+20(L'DCDELT),DCDELT  SHOW DELETED
GETDELM  PRTV  PLINE                                             81144
GETDINC  LA    R9,12(,R9)    POINT TO NEXT ENTRY
         B     GETDLOOP
         SPACE 1
GETEOF3  L     R0,SIZDEL     GET DELETE LIST CORE SIZE
     FREEMAIN  R,LV=(0),A=DELETORG
         TM    DOOMED,X'F0'  RUNNING IN TEST MODE ?              81144
         BNO   ERRLIST       YES; ECHO UPDTE SYSIN LISTING       81144
         ICM   R15,3,RETCH   CHECK RETURN CODE                   92279
         BZ    UPDTE         GOOD - CHECK FOR UPDATE INVOKATION
ERRLIST  MVC   HEAD2VAR(L'HEADERR),HEADERR                       81144
         PRTF  HEAD2,121,TITLE=3                                 81144
         MVI   CTLCHAR,C' '  SINGLE SPACE
         MVC   LINE,LINE-1   CLEAR PRINT LINE                    81144
         OPEN  (UPDTEIN,INPUT)
ERROUT   GET   UPDTEIN,LINE+20
         CLC   LINE+20(2),=C'./'  CONTROL CARD ?
         BNE   *+8           NO
         MVI   CTLCHAR,C'0'  DOUBLE SPACE
         CLI   LINE+20,X'00'   ERROR MESSAGE ?                   81144
         BNE   ERRPUT        NO
         PRTV  LINE+20,CC=NO                                     81144
         MVI   CTLCHAR,C'0'  SET DOUBLE SPACE NEXT TIME
         B     ERROUT
ERRPUT   PRTV  PLINE                                             81144
         MVI   CTLCHAR,C' '  SINGLE SPACE NEXT TIME
         B     ERROUT
         SPACE 1
UPDTE    $CLOSE UPDTEIN
         SERVTERM ,          DELETE ALL                          81151
         LH    R15,RETCH     GET RETURN CODE
         LTR   R9,R15        ANY INPUT ERRORS ?                  92279
         BNZ   COMEXIT       YES; SKIP UPDATE
         TM    PROCFLAG,PFUP  ANY UPDATES REQUESTED ?
         BZ    COMEXIT       NO; EXIT
         TM    DOOMED,X'F0'    TEST MODE ?                       81144
         BNO   COMEXIT       YES; QUIT NOW                       81144
UPDTSYN  MODESET KEY=ZERO,MODE=SUP  GO INTO KEY ZERO, SUPERVISOR 86188
         RESERVE MF=(E,RSVLIST),RET=HAVE  TRY TO RESERVE THE PACK
         MODESET KEY=NZERO,MODE=PROB  RESTORE NORMAL MODE        86188
         LINK  EP=IEBUPDTE,PARAM=(PARM,DDNAMES),VL=1   GO TO IT
         LR    R9,R15        PASS RETURN CODE                    92279
         DEQ   MF=(E,RSVLIST),RET=HAVE  DEQUEUE FROM PACK        86188
         SPACE 1
*        RETURN CODE SET IN R9                                   92279
*
COMEXIT  SERVTERM ,          JUST IN CASE                        92279
         SERVICE PASOF       RESET                               92279
         ENDM  RC=(R9)                                           92279
         SPACE 2
BADSET   WTO   MF=(E,NONOLIST)
         PRTV  NONOLIST,CC=NO                                    81144
         MVI   RETCH+1,20    SET MAJOR ERROR                     81144
         B     UPDTE         GO TO TERMINATION                   81144
         SPACE 2
TYPE     MVI   FTYPE,FBAD    PROVISIONALLY SHOW BAD CARD
         CLI   1(R5),C'/'    ANY CONTROL CARD ?
         BNER  R10           NO; RETURN ERROR
         CLI   0(R5),C'/'    JCL CARD ?
         BE    TYPEJCL       OK
         CLI   0(R5),C'.'    UPDTE CONTROL CARD ?
         BNER  R10           NO; ERROR
         CLI   71(R5),C' '   CONTINUATION ?
         BNE   SETBAD        YES - ERROR
         BAL   R9,SKIPNAME   SKIP NAME FIELD
         BAL   R9,GETVERB    GET OPERAND
         STC   R2,DB         SAVE VERB                           81144
         ICM   R10,8,DB      SAVE CARD TYPE                      81144
         CLI   NULLFLAG,0    ANY EXEC/DD IN PREVIOUS MEMBER ?    81144
         BNE   NONNULL       YES; JUST RESET                     81144
         ST    R5,SAVER14    SAVE CARD SCAN ADDRESS              81144
         LR    R2,R10        SAVE RETURN ADDRESS                 81144
         LA    R5,BADJCL                                         81144
         BAL   R10,COPY      ADD THE ERROR MESSAGE               81144
         LR    R10,R2        RESTORE RETURN ADDRESS              81144
         L     R5,SAVER14    RESTORE CARD ADDRESS                81144
         OI    RETCH+1,12    SET ERROR                           81144
NONNULL  MVI   NULLFLAG,0    RESET EXEC/DD FLAG                  81144
         BAL   R9,SCANNAME   SCAN FOR NAME= FIELD
         STCM  R10,8,FTYPE   REPLACE CARD TYPE AFTER NAME=       81144
         BAL   R9,COPYOPTS   COPY OTHER OPTIONS
         BR    R10           RETURN TO CALLER
TYPEJCL  MVI   FTYPE,FCOM    SET FOR COMMENTS CARD               81144
         CLI   2(R5),C'*'    COMMENTS CARD ?
         BER   R10           YES; RETURN
         MVI   FTYPE,FDAT    SHOW DATA (JCL) CARD
         BAL   R9,SKIPNAME  SKIP NAME FIELD                      81144
         CLC   =C'JOB ',0(R5)   JOB CARD ?                       81144
         BE    SETBAD        YES; FAIL                           81144
         CLC   =C'EXEC ',0(R5)  EXEC CARD ?                      81144
         BE    TYPEJCLX      YES; SET FLAG                       81144
         CLC   =C'DD ',0(R5)    DD CARD ?                        81144
         BNER  R10           NO; RETURN (MAY BE CONTINUATION)    81144
         TM    ACCTPRIV,VAASYS+VAASUP  PRIVILEGED USER ?         81144
         BNZ   TYPEJCLX      YES                                 81144
         L     R1,CARDADD    GET CARD ADDRESS BACK               81144
         CLC   =C'SYSABEND',2(R1)  IS THIS A WASTER ?            81144
         BNE   TYPEJCLX      NO                                  81144
         MVC   5(5,R1),=C'UDUMP'  FIX IT                         81144
TYPEJCLX OI    NULLFLAG,1    SHOW EXEC/DD CARD FOUND             81144
         BR    R10           RETURN OK                           81144
         SPACE 2
SKIPNAME LA    R14,9         LOOK FOR MAX OF 8 NON-BLANKS
SKIPLOOP CLI   2(R5),C' '    END OF FIELD ?
         BE    SKIPEND       OK
         LA    R5,1(,R5)     TRY AGAIN
         BCT   R14,SKIPLOOP
         BR    R10           ELSE BAD CARD
SKIPEND  MVC   OPCODE,OPCODE-1  BLANK THE OPCODE FIELD IN THE PATTERN
         LA    R5,2(,R5)     FIX POINTER
SKIPBLKS LA    R14,12        LOOK FOR SOMETHING ELSE
SKIPBL   CLI   0(R5),C' '    STILL BLANK ?
         BNER  R9            NO, RETURN
         LA    R5,1(,R5)     TRY AGAIN
         BCT   R14,SKIPBL
         BR    R10           SHOW BAD CARD
         SPACE 2
GETVERB  LA    R14,5         SET ANTICIPATED VERB LENGTH-1
         CLC   =C'NUMBER',0(R5)
         BE    GETDAT        SET FOR DATA CARD
         CLC   =C'DELETE',0(R5)
         BE    GETDAT        DITTO
         CLC   =C'CHANGE',0(R5)
         BE    GETVMOVE      MOVE VERB
         LA    R14,2
         CLC   0(3,R5),=C'ADD '  ADD CARD ?                      81144
         BE    GETVMOVE      OK
         LA    R14,3
         CLC   =C'REPL',0(R5)  REPLACE ?
         BE    GETVMOVE
         LA    R14,5
         CLC   =C'ENDUP',0(R5)  IS THIS ALL ?
         BNE   GETVSCR       NO; CHECK FOR SCRATCH
         MVI   FTYPE,FEOF    SET EOF
         BR    R10           RETURN OK
GETDAT   MVI   FTYPE,FDAT
         BR    R10
GETVSCR  CLC   =C'SCRATCH',0(R5)  ONE OF MY SPECIAL CARDS ?
         BNER  R10           NO; FAIL - REPRO/LABEL/ALIAS UNSUPPORTED
         LA    R14,6         SET PHONY LENGTH
GETVMOVE EX    R14,OPCMVC    MOVE OPCODE TO 'NEW' CARD
         LA    R5,1(R5,R14)  POINT TO NEXT INPUT COLUMN
         CLI   0(R5),C' '    BLANK ?
         BNER  R10           NO; ERROR
         LR    R2,R14        SAVE LENGTH-1 (=FTYPE)              81144
         MVI   FTYPE,FNNAM   SET NAME FIELD MISSING              81144
         B     SKIPBLKS      RETURN AFTER SKIPPING BLANKS
OPCMVC   MVC   OPCODE(0),0(R5)  MOVE OPCODE TO NEW CARD
         SPACE 2
SCANNAME MVI   CARDMEM,C' '  BLANK NEW CARD IMAGE
         MVC   CARDMEM+1(L'CARDMEM-1),CARDMEM
         ST    R5,SCANOP     SAVE SCAN START ADDRESS
         L     R4,CARDADD    GET CARD START
         LA    R4,65(,R4)    GET SCAN STOP ADDRESS
SCANNALP CLC   DCNAM,0(R5)   NAME= ?
         BE    SCANNAFN      YES
         LA    R5,1(,R5)
         CR    R5,R4
         BL    SCANNALP
SETBAD   MVI   FTYPE,FBAD    ELSE SHOW BAD
         BR    R10           AND RETURN
SCANNAFN LA    R1,CARDMEM    GET OUTPUT
         LA    R2,8          MAX OF 8
SCANNAMV CLI   5(R5),C','    END OF FIELD
         BE    SCANNAMD      YES
         CLI   5(R5),C' '
         BE    SCANNAMD
         MVC   0(1,R1),5(R5)  COPY A BYTE
         LA    R1,1(,R1)
         LA    R5,1(,R5)
         BCT   R2,SCANNAMV
         CLI   5(R5),C','    PROPER TRAILING SEPARATOR ?
         BH    SETBAD        NO
SCANNAMD CLI   CARDMEM,C','  AT LEAST ONE BYTE IN NAME ?
         BNH   SETBAD        NO
         MVC   AMEM,CARDMEM  SAVE FULL NAME WITH TRAILING BLANKS
         L     R5,SCANOP     RESTORE ORIGINAL R5 POINTER
         ST    R1,SCANOP     SAVE NEW MOVE STARTER
         BR    R9            RETURN OK
         SPACE 2
COPYOPTS L     R1,SCANOP     GET FIRST AVAILABLE OUTPUT COLUMN
COPYLOOP L     R6,CARDADD
         LA    R6,70(,R6)    SET END SCAN STOP
         LA    R4,VERBTAB    SET SUBOPERAND TABLE ADDRESS
         BAL   R14,VERBLOOK  LOOK FOR A MATCH
         B     SETBAD        ERROR - NOTHING FOUND
COPYDONE BR    R9            ALL DONE
COPYMORE LR    R5,R3         SET NEXT SCAN ADDRESS
         B     COPYLOOP      AND KEEP GOING
COPYSOME LA    R2,17         COPY MAX OF 17 BYTES
         MVI   0(R1),C','    MAKE SEPARATOR
         LA    R1,1(,R1)     BUMP ONE
COPYSLOP CLI   0(R5),C' '    END ?
         BE    COPYLOOP      YES; SCAN FOR MORE
         CLI   0(R5),C','    END ?
         BE    COPYLOOP      YES
         MVC   0(1,R1),0(R5)  COPY A BYTE
         LA    R1,1(,R1)
         LA    R5,1(,R5)
         BCT   R2,COPYSLOP   KEEP IT UP
         CLI   0(R5),C','    VALID SEPARATOR ?
         BH    SETBAD        TOO BAD
         B     COPYLOOP
COPYSKIP LA    R2,17         SKIP MAX OF 17 BYTES
COPYSKOP CLI   0(R5),C' '    END ?
         BE    COPYLOOP      YES; SCAN FOR MORE
         CLI   0(R5),C','    END ?
         BE    COPYLOOP      YES
         LA    R5,1(,R5)
         BCT   R2,COPYSKOP   KEEP IT UP
         CLI   0(R5),C','    VALID SEPARATOR ?
         BH    SETBAD        TOO BAD
         B     COPYLOOP
         SPACE 2
VERBLOOK XR    R15,R15       ZERO IC REGISTER
VERB1    CLI   0(R4),X'FF'   END OF TABLE ?
         BER   R14           YES; NO MATCH FOUND
         IC    R15,0(,R4)    GET LENGTH - 1
         EX    R15,VERBCLC   COMPARE
         BNE   VERB2         NO MATCH - TRY AGAIN
         LA    R3,1(R15,R5)  POINT R3 TO BYTE AFTER VERB
         ICM   R15,7,1(R4)   GET DISPLACEMENT                    81144
         B     PROCUP(R15)   BRANCH TO ROUTINE                   93006
VERB2    LA    R4,5(R15,R4)  POINT TO NEXT TABLE ENTRY
         B     VERB1         TRY AGAIN
VERBCLC  CLC   0(0,R5),4(R4)  COMPARE ENTRY TO TEXT
         EJECT
*        NOTE - SYSPRINT DCB MUST STAY COMPATIBLE WITH IEBUPDTE  81144
SYSPRINT PRTWORK SYSPRINT,RECFM=X'94',WIDTH=120,TITLE=5          81144
SUMPRINT PRTWORK SUMPRINT    ACCEPT ANYTHING FOR MESSAGE/SUMMARY 81144
RSVLIST  RESERVE (RSVQNAME,RSVRNAME,E,L'RSVRNAME,SYSTEMS),RET=HAVE,    *
               UCB=RSVUCB,MF=L    RESERVE ON PROCLIB UCB         86188
RSVQNAME DC    CL8'SYSDSN'   CHOSE A NAME KNOWN TO GRS           86188
RSVRNAME DC    C'PROCLIB.UPDATE'                                 86188
RSVUCB   DC    A(0)          UCB ADDRESS OF PROCLIB VOLUME       86188
NONOLIST DC    0H'0',AL2(NONOEND-*,X'8000')
         DC    C'PROCUP - FAILED DUE TO BAD '
REASON   DC    CL8' SETUP ',C' '
REASONDD DC    CL8' '        DD NAME                             81144
NONOEND  DC    X'00000020'   ROUTCDE=11
         CNOP  2,4
PARM     DC    H'3',C'MOD'   IEBUPDTE PARM FIELD
NULLFLAG DC    X'00'         EXEC/DD CARD FOUND FLAG             81144
         CNOP  2,4
DDNAMES  DC    0H'0',AL2(DDNAMEND-*-2)
         DC    4XL8'0'       NO OVERRIDES
         DC    CL8'UPDTEIN'
         DC    CL8'SYSPRINT'
         DC    XL8'0'
DDUT     DC    CL8'SYSUT2'
DDUT2    DC    CL8'SYSUT2'
DDNAMEND EQU   *
         SPACE 1                                                 81144
SYSDD    DC    CL8'SYSUT1'   SYSTEM PROCLIB                      81144
SYSDSN   DC    CL14'SYS1.BATPROC'   DSN                          92279
USRDD    DC    CL8'SYSUT2'   USER PROCLIB                        81144
USRDSN   DC    CL14'SYS1.USERPROC'                               81144
IBMDD    DC    CL8'SYSPROC'  SPECIAL DD                          81144
IBMDSN   DC    CL14'SYS1.PROCLIB'                                81144
         SPACE 2
VERBTAB  VERBTAB ' ',COPYDONE,BASE=PROCUP                        81144
         VERBTAB ',',COPYMORE                                    81144
         VERBTAB 'LIST=ALL',COPYSOME                             81144
         VERBTAB 'SEQFLD=',COPYSOME                              81144
         VERBTAB 'COLUMN=',COPYSOME                              81144
         VERBTAB 'NAME=',COPYSKIP        DONE ALREADY            81144
         VERBTAB 'LEVEL=',COPYSKIP       INCOMPATIBLE WITH OUR SSI
         VERBTAB 'SOURCE=',COPYSKIP                              81144
         VERBTAB 'SSI=',COPYSKIP                                 81144
*        FAIL NEW=,MEMBER=,UPDATE=,INHDR,INTLR,OUTHDR,OUTTLR AND TOTAL
         VERBTAB *END                                            81144
         SPACE 1
SEQBAD   WTU   'THE ABOVE CARD CONTAINS INVALID OR UNSUPPORTED TEXT, OR*
                IS OUT OF SEQUENCE',MF=L                         81144
BADFIELD WTU   'INVALID OR MISSING NAME= FIELD',MF=L             81144
BADJCL   WTU   'NO EXEC/DD CARDS IN ABOVE MEMBER',MF=L           81144
BADMEM   WTU   'REQUESTED MEMBER DOES NOT EXIST',MF=L            81144
BADMEMA  WTU   'REQUESTED MEMBER BELONGS TO ANOTHER ACCOUNT',MF=L
BADDUP   WTU   'SPECIFIED MEMBER NAME ALREADY EXISTS',MF=L       81144
BADOTH   WTU   'MEMBER ALREADY EXISTS IN ANOTHER LIBRARY',MF=L   81144
DCNOTD   DC    C'NOT DELETED DUE TO PROCESSING ERROR'
DCDELT   DC    C'SCRATCHED'
         SPACE 1
ACCTWORK DC    CL4' ',C'0010'  @SERVICE ACCT PARM                81144
ACCT     DC    CL4' '        CURRENT ACCOUNT NUMBER              81144
ACCTOFF  DC    H'0'          ACCOUNT OFFSET / SSI VALUE          81144
ACCTPRIV DC    X'00'         USER PRIVILEGES                     81144
         SPACE 1                                                 92279
SAVE     DSECT ,             SAVE/WORK AREA
@SERVICE DC    A(0)          ADDRESS OF @SERVICE ROUTINE         81144
@PRINTER DC    A(0)          ADDRESS OF @PRINTER ROUTINE         81144
CARDADD  DC    A(0)          ADDRESS OF CURRENT CARD
SCANOP   DC    A(0)          ADDRESS OF NEXT OUTPUT FIELD ON NEW CARD
RETCH    DC    H'0'          RETURN CODE
PROCFLAG DC    X'0'          PROCESSING FLAG
PFINIT   EQU   X'80'         FIRST TIME FLAG
PFSCR    EQU   X'40'         SCRATCHES TO BE DONE
PFUP     EQU   X'20'         INVOKE UPDTE
FTYPE    DC    X'0'          CURRENT CARD TYPE
FADD     EQU   2             ADD
FREP     EQU   3             REPL
FCHG     EQU   5             CHANGE
FSCR     EQU   6             SCRATCH
FEOF     EQU   8             ENDUP
FCOM     EQU   9             //*
FDAT     EQU   10            //  OR  ./ NUMBER, ETC.
FNNAM    EQU   254           MISSING NAME= FIELD                 81144
FBAD     EQU   255           MALFORMED CARD
WHOFG    DC    X'00'         ENTRY FLAG (0-PROCUP; 1-PROCLEAN)   92279
SAVEND   EQU   *                                                 92279
         TITLE 'PROCLEAN **** USER PROCLIB MAINTAINANCE PROGRAM ****'
**********************************************************************
*                                                                    *
*        THE PURPOSE OF THIS PROGRAM IS TO MAINTAIN USER PROCLIB.    *
*        EACH MEMBER ENTRY IN SYSUT1-SYSUT9 WILL BE SCANNED FOR SSI. *
*        THE FIRST TWO BYTES OF SSI ARE THE INTERNAL ACCOUNT NUMBER  *
*        THE ACCOUNT WILL BE CHECKED AGAINST THE SYSTEM ACCT TABLE.  *
*        INVALID ENTRIES ARE FLAGGED FOR DELETION.                   *
*                                                                    *
**********************************************************************
PROCUP   CSECT ,             BACK TO REALITY                     92279
CLEANPRO LA    R15,16        SET PROVISIONAL RETURN CODE         81144
         STH   R15,RETCH                                         81144
         BAL   R9,COMMINIT   DO COMMON INITIALIZATION            81144
         MVC   NONOLIST+8(4),=C'LEAN'  CHANGE PROGRAM NAME IN MESSAGE
         MVC   REASON,=CL8' PARM '     SET 1ST REASON
         MVI   DOOMED,0        JUST IN CASE
         SPACE 2
*        PARM ROUTINE - KEEP OR DELETE MEMBERS ?
         SPACE 2
         LTR   R8,R8         IS THERE A PARM ?                   81144
         BZ    ALL             NO, LIST MEMBERS ONLY
         L     R1,0(,R8)     MESS AROUND WITH THE..              81144
         LH    R14,0(,R1)    ADDRESSES UNTIL IT'S RIGHT
         LTR   R14,R14       IS THE PARM A NULL ?
         BZ    ALL           YES, IGNORE IT; LIST MEMBERS ONLY
         CH    R14,=H'6'     PARM TO BIG ?
         BNE   BADSET        YES, TO BAD
         CLC   =C'DELETE',2(R1)  CORRECT PARM FOR DELETION ?
         BNE   BADSET        TOO BAD
         SPACE 2
*        SETUP......GET TABLE AREAS
         SPACE 2
*        NOTE: RDJFCB IS DONE TO FORCE OPENING OF THE PDS
*        AS A SEQUENTIAL DATA SET
         SPACE 2
SET      TM    ACCTPRIV,VAASYS+VAASUP  PRIVILEGED USER ?         81144
         BZ    ALL           NO; JUST LIST                       81144
         MVI   DOOMED,X'0F'  SET HALF OF DELETE SWITCH           81144
ALL      BAL   R10,DCBINIT   INITIALIZE POD/PDS DCBS AND GETMAINS
         PRTF  HEAD2,121,TITLE=3                                 81144
         MVI   LIBTYPE,0     RESET LIBRARY TYPE
         CLC   SYSDSN,HEADDSN  IS IT SYSTEM LIB ?                81144
         BNE   *+8
         MVI   LIBTYPE,1     SET DTS PROC
         CLC   USRDSN,HEADDSN  OR USER PROC LIB ?                81144
         BNE   *+8
         MVI   LIBTYPE,2     SET USER PROC
         USING DIRECTRY,R3   DIRECTORY ENTRY MAP
READIR   GET   POD           TO GET THIS POOR DOG A BONE.....
         LR    R3,R1         MAKE DSECT ADDR. GOOD
         LH    R4,BLKLEN     FIND END OF...
         AR    R4,R3         DIRECTORY BLOCK
         LA    R3,MEMNAME
         USING MEMNAME,R3
USEDIR   CLI   MEMNAME,X'FF' END OF DIRECTORY ?
         BE    ENDIR         ....BUT THE CUBBARD WAS BARE
         SPACE 1
         BAL   R14,ACCTMEM   GET MEMBER ACCOUNT DATA
         B     LIST          VALID
*        B     BADACCT       INVALID
         SPACE 1
BADACCT  L     R9,DELETADD   LOAD ADDR. OF DELETE TABLE
         C     R9,DELETORG   ANY ENTRIES LEFT ?
         BH    BADACCT3      YES; ADD THIS ONE IN
         NI    MEMFLAG,255-X'40' DELETABLE BUT KEPT
BADFACT1 NOP   BADACCT2      ONLY ISSUE MESSAGE ONCE
         OI    BADFACT1+1,X'F0'  ONE TIME ONLY SWITCH
         PRTV  DELOFLOW      SHOW DELETE TABLE OVERFLOW          81144
BADACCT2 B     LIST          SKIP THIS ONE
BADACCT3 MVC   0(11,R9),MEMSAVE  MOVE MEMBER AND TTR
         SH    R9,=H'12'     GET PREVIOUS ENTRY
         OI    DOOMED,X'F0'  SET OTHER HALF OF DELETE SWITCH
         ST    R9,DELETADD   SAVE IT FOR NEXT TIME
         SPACE 1
LIST     TM    ACCTPRIV,VAASYS+VAASUP   PRIVILEGED ?             81144
         BNZ   LISTOK        YES                                 81144
         CLC   ACCT,MEMNO    MATCHING ACCOUNT ?                  81144
         BNE   BUMPDIR       NO; SKIP THIS ONE                   81144
LISTOK   L     R9,LISTADD    LOAD ADDR. OF LIST TABLE            81144
         C     R9,LISTEND    LIST FULL ?
         BL    LISTNT2       NO
BADFACT2 NOP   ENDIR         SKIP FURTHER PROCESSING
         OI    BADFACT2+1,X'F0'
         PRTV  LSTOFLOW      SHOW OVERFLOW                       81144
         B     ENDIR         EXIT
         USING LISTSECT,R9                                       81144
LISTNT2  MVC   LSTFLAG,MEMFLAG  COPY MEMBER PROCESSING FLAG      81144
         MVC   LSTNAME,MEMNAME   MEMBER NAME                     81144
         MVC   LSTTTR,MEMTTR     TTR OF MEMBER                   81144
         MVI   LSTZ,0           JUST IN CASE                     81144
         MVC   LSTACCT,MEMNO     ACCOUNT NUMBER                  81144
         MVC   LSTDATE,MEMDATE   ADD/CHANGE DATE                 81144
         LA    R9,LSTLEN(,R9)    BUMP LIST ADDRESS               81144
         ST    R9,LISTADD    SAVE IT FOR NEXT TIME
         SPACE 2
BUMPDIR  IC    R1,ENTLEN     GET LENGTH OF USER DATA (HALF WORDS)
         N     R1,=F'31'     MASK OUT FLAGS
         SLA   R1,1          CONVERT TO BYTES
         LA    R3,USERDATA(R1)   FIND NEXT ENTRY
         CR    R3,R4         BUFFER EXHAUSTED ?
         BL    USEDIR        NO
         B     READIR        YES
         SPACE 1
ENDIR    L     R9,LISTADD    GET MEMBER NAME LIST ADDRESS
         MVI   0(R9),X'FF'   MOVE IN EOF FLAG
         MVI   RETCH+1,4     SET FOR 'NO DELETE' RETURN CODE     81144
         BAL   R10,DODELETE  PERFORM DELETIONS, IF ANY
         L     R0,SIZDEL     GET DELETE LIST CORE SIZE
     FREEMAIN  R,LV=(0),A=DELETORG
         SPACE 2
PRTOK    L     R9,LISTORG    LOAD LIST TABLE ADDRESS
PRTLOOP  CLI   0(R9),X'FF'   END OF TABLE ?
         BE    DONE          YES
         MVC   LINE+26(4),LSTACCT  SHOW ACCOUNT                  81144
         MVC   LINE+40(5),LSTDATE  SHOW ACCESS DATE              81144
         MVC   LINE+55(8),LSTNAME  MEMBER NAME                   81144
         UNPK  LINE+73(7),LSTTTR(4)  UNPACK TTR                  81144
         TR    LINE+73(6),HEXTRTAB-C'0'  MAKE EBCDIC             81144
         MVI   LINE+79,C' '  CLEAR LAST BYTE                     81144
         MVC   LINE+88(4),=C'KEPT'  PROVISIONAL STATUS           81144
         CLI   0(R9),C'G'    GOOD ENTRY ?
         BE    PRTPRINT      YES                                 81144
         CLI   0(R9),C'B'    DELETE RECORD ?
         BE    DELETE        YES
         CLI   0(R9),X'82'   KEPT/DELETABLE MEMBER ?
         BE    DELETE          YES; SHOW IT
         CLI   0(R9),C'U'    UNDEFINED ?
         BE    UNDEFIND
         CLI   0(R9),X'A4'   KEPT/UNDEFINED MEMBER ?
         BE    UNDEFIND        YES; SHOW IT
         MVC   LINE+45(30),=C'*** TABLE PROCESSING ERROR ***'
         PRTV  PLINE                                             81144
         B     EOJ1
         SPACE 2
MOV$KEPT MVC   LINE+84(3),=C'***'
         MVC   LINE+93(3),=C'***'
         MVC   LINE+97(13),=C'TO BE DELETED'                     81144
PRTPRINT PRTV  PLINE                                             81144
         MVC   LINE,LINE-1   CLEAR PRINT LINE                    81144
         LA    R9,LSTLEN(,R9)    BUMP TO NEXT TABLE ENTRYD       81144
         B     PRTLOOP       DO IT AGAIN, AND AGAIN, AND AGAIN
         SPACE 2
UNDEFIND MVC   LINE+25(6),=C'*NOID*'
         MVC   LINE+40(5),LINE+39  CLEAR DATE                    81144
*        B     DELETE        CHECK FOR DELETION                  81144
         SPACE 1
DELETE   CLI   DOOMED,X'FF'  DELETIONS PERFORMED ?               81144
         BNE   MOV$KEPT      NO; SHOW AS KEPT
         TM    0(R9),X'40'   DELETE SHOT DOWN DUE TO TABLE OFLO?
         BZ    MOV$KEPT      YES; SHOW AS KEPT
GONE     MVC   LINE+86(7),=C'DELETED'
         B     PRTPRINT                                          81144
         SPACE 2
DONE     MVC   LINE+50(19),=C'*** END OF DATA ***'
         PRTV  PLINE                                             81144
         MVC   LINE,LINE-1   CLEAR PRINT LINE                    81144
EOJ1     L     R0,SIZMEM                   FREEMAIN MEMBER LIST
     FREEMAIN  R,LV=(0),A=LISTORG
PODNUP   CLI   POD+45,C'9'   END OF SYSUTN ?                     81144
         BE    EOJ
         XR    R15,R15
         IC    R15,POD+45    GET N FROM SYSUTN
         LA    R15,1(,R15)   UP BY ONE
         STC   R15,POD+45
         STC   R15,PDS+45    INTO BOTH DCBS
         DEVTYPE POD+DCBDDNAM-IHADCB,DB   LOOK FOR TIOT ENTRY    81144
         BXH   R15,R15,PODNUP  TRY NEXT IF OMITTED               81144
         CLI   DB+2,UCB3DACC   DISK ?                            81144
         BNE   PODNUP                                            81144
         NI    DOOMED,X'0F'  RESET DELETE SWITCH
         NI    BADFACT1+1,X'0F'  RESET LIST FULL SWITCH
         NI    BADFACT2+1,X'0F'  IN BOTH LISTS
         B     ALL           DO IT AGAIN FOR NEXT LIBRARY
         SPACE 1
EOJ      MVI   PROCFLAG,0    RESET PFUP (JUST IN CASE)           81144
         B     UPDTE         GO TO COMMON CLOSE                  81144
         SPACE 2
LOCOLD   XC    AMTRACK,AMTRACK  CLEAR RETURN INFO
         MVI   MEMFLAG,C'N'  INDICATE NO MEMBER
         BLDL  PDS,MEMBLDL   LOOK FOR THE MEMBER
         LTR   R15,R15       OK ?
         BNZ   LOCNOT        NO
         CLI   AMTRACK+2,0   VALID 'R' IN 'TTR' ?
         BE    LOCNOT        NO
         MVC   AMFLAG(5),AMFLAG+2  IGNORE BLDL DATA
         LA    R3,AMEM       POINT TO ENTRY
         BAL   R14,ACCTMEM   GET THE MEMBER'S ACCOUNT NUMBER
         B     LOCFND        FOUND AND VALID
*        DROP THROUGH - TREAT INVALID AS NON-EXISTENT
LOCNOT   MVC   MEMNO,ACCT    FAKE USER'S ACCOUNT
LOCFND   CLC   MEMNO,ACCT    SET CONDITION CODE ON RETURN
         BR    R10           RETURN TO CALLER
         SPACE 1
ACCTMEM  ST    R14,SAVER14   SAVE RETURN REGISTER                81144
         USING MEMNAME,R3                                        81144
         MVC   MEMSAVE,MEMNAME  COPY THE DIRECTORY INFO          81144
         MVC   MEMDATE,BLANKS   CLEAR DATE                       81144
         TM    ENTLEN,X'1E'  AT LEAST 2 HALF-WORDS FOR SSI ?     81144
         BZ    ENDMEM        NO; EXIT WITH ERROR                 81144
         XR    R1,R1                                             81144
         UNPK  DB(5),USERDATA+2(3)  GET THE DATE                 81144
         TR    DB(4),HEXTRTAB-C'0'     IN EBCDIC                 81144
         MVC   MEMDATE(1),HEADATE  GET TENS PORTION OF YEAR      81144
         MVC   MEMDATE+1(4),DB      GET UNITS AND JULIAN DAY     81144
         CLC   DB(4),NEWSSI+4   DID IT WRAP AROUND ?             81144
         BNH   ACCTNEWY       NO                                 81144
         IC    R1,MEMDATE     GET TENS BACK                      81144
         BCTR  R1,0                                              81144
         STC   R1,MEMDATE     USE PRIOR DECADE                   81144
ACCTNEWY ICM   R1,3,USERDATA   LOAD SSI                          81144
         SERVCALL ACCON,(R1),ERR=  CONVERT IT                    81144
         BXH   R15,R15,ENDMEM   INVALID                          81144
         STCM  R0,15,MEMNO   SAVE IT                             81144
         STCM  R0,15,ACCTWORK                                    81144
         SERVCALL ACTST,ACCTWORK,ERR=  TEST IT                   81144
         CH    R15,=H'4'                                         81144
         BH    BADAMEM       INVALID                             81144
         STC   R0,DB         SAVE PRIVILEGES                     81144
         MVI   MEMFLAG,C'G'  TENTATIVELY SET GOOD ACCOUNT        81144
         DROP  R3                                                81144
         CLI   NONOLIST+8,C'L'  DOING PROCLEAN ?                 81144
         BNE   ACCTMEMX      NO; EXIT OK                         81144
         CLI   LIBTYPE,1     WHICH LIBRARY ?                     81144
         BH    ACCTMUSR      USER                                81144
         BL    ACCTMEMX      SKIP - DON'T KNOW                   81144
         TM    DB,VAASYS+VAASUP   PRIVILEGED ?                   81144
         BNZ   ACCTMEMX      YES; OK                             81144
         B     BADAMEM       NO; TOO BAD                         81144
ACCTMUSR TM    DB,VAASYS+VAASUP   PRIVILEGED ?                   81144
         BNZ   BADAMEM       YES; WRONG LIBRARY                  81144
         CLC   =C'Z90',MEMNO   OTHER CRUD ?                      81144
         BE    BADAMEM       YES; FAIL                           81144
         CLC   =C'C90',MEMNO                                     81144
         BE    BADAMEM       YES; TOO BAD                        81144
ACCTMEMX XR    R14,R14       CLEAR RETURN OFFSET                 81144
ACCTMEMR AL    R14,SAVER14                                       81144
         BR    R14           RETURN TO CALLER                    81144
ENDMEM   MVI   MEMFLAG,C'U'      INDICATE UNDEFINED
         MVC   MEMNO,BLANKS  DON'T TRUST IN GARBAGE ?
BADCMEM  LA    R14,4         SET BAD RETURN CODE
         B     ACCTMEMR      GO TO COMMON RETURN
BADAMEM  MVI   MEMFLAG,C'B'  SHOW BAD ACCOUNT
         B     BADCMEM       GO TO COMMON
         SPACE 2
DODELETE $CLOSE POD
         CLI   DOOMED,X'FF'  DELETE REQUESTED AND ENABLED ?
         BNE   STOWEND       NO; SKIP DELETE PROCESSING
         CLOSE PDS
         LA    R1,PDS
         USING IHADCB,R1
         OI    DCBMACR+1,X'24'  CHANGE MACRF TO WRITE/POINT
         DROP  R1
         L     R9,DELETORG   DELETE TABLE START ADDRESS
         OPEN  (PDS,OUTPUT)  BPAM
         TM    PDS+48,X'10'  OPENED OK ?
         BO    STOWAGN       GO TO DELETE PROCESSING
         WTO   'UNSUCCESSFUL OPEN - PDS',ROUTCDE=11
         B     EOJ
         SPACE 2
STOWAGN  TM    0(R9),X'FF'   CHECK TYPE OF LIST ENTRY
         BZ    STOWINC       ZERO - GET NEXT ENTRY
         BO    STOWEND       ALL ONES - END OF LIST
         NI    PDS+DCBIFLGS-IHADCB,X'0F'  RESET ERROR FLAGS
         FIND  PDS,8(R9),C   RESET DCB TO TTR OF MEMBER
         STC   R15,11(,R9)   SAVE RETURN CODE
         LTR   R15,R15       FAILED ?
         BNZ   STOWINC       YES; SKIP DELETE
         STOW  PDS,(R9),D    DELETE THE MEMBER
         STC   R15,11(,R9)   SAVE RETURN CODE
         MVI   RETCH,0       RC=0 - SHOW COMPRESS REQUIRED       81144
STOWINC  LA    R9,12(,R9)    POINT TO NEXT ENTRY
         B     STOWAGN
         SPACE 2
STOWEND  $CLOSE PDS
         BR    R10           RETURN TO CALLER
         SPACE 1                                                 81144
COMMINIT LA    R8,0(,R8)     SAVE AND STRIP HIGH BYTE OF PARM    92279
         SERVINIT MAP=NO     LOAD THE @SERVICE ROUTINE           81144
         SERVCALL LPALD,=CL8'@PRINTER',ERR=  GET THE PRINTER ROUTINE
         ST    R0,@PRINTER      SAVE ADDRESS                     81144
         PRTOPEN SYSPRINT,DEV=1,OPT=ABE  ABEND IF NO SYSPRINT    81144
         SERVCALL ACGET,DB,ERR=  GET CURRENT ACCOUNT AND PRIVIES 81144
         MVC   ACCT,DB       SAVE HIGH FOUR BYTES                81144
         STC   R0,ACCTPRIV   SAVE PRIVILEGE FLAGS                81144
         STCM  R0,12,ACCTOFF   SAVE ACCOUNT OFFSET               81144
         PRTF  HEAD1,121,TITLE=1  PRELIMINARY TITLE              81144
         BR    R9            RETURN TO CALLER                    81144
         SPACE 2
DCBINIT  MVC   REASON,=CL8'OPEN FOR'                             81144
         MVC   REASONDD,POD+DCBDDNAM-IHADCB                      81144
         RDJFCB MF=(E,OP1)
         BXH   R15,R15,BADSET                                    81144
         OI    JFCBTSDM,X'0A'    SET FOR NO REWRITE
         MVC   HEADDSN,JFCBDSNM  COPY DSN TO HEADER
         OPEN  MF=(E,OP1),TYPE=J    QSAM
         TM    POD+48,X'10'  OPENED OK ?
         BZ    BADSET        NO; GIVE ERROR
         MVC   PDSOTH+DCBDDNAM-IHADCB(8),PDS+DCBDDNAM-IHADCB     81144
         XI    PDSOTH+DCBDDNAM-IHADCB+5,3  CHANGE SYSUT1=>2, 2=>1
         OPEN  (PDS,INPUT)   BPAM
         CLI   NONOLIST+8,C'L'  RUNNING PROCLEAN ?               81144
         BE    DCBNSYS       NO; SKIP OTHER GETMAIN              81144
         L     R1,PDS+DCBDEBAD-IHADCB  GET DEB ADDRESS           86188
         L     R1,DEBBASND-DEBBASIC(,R1)  GET UCB                86188
         LA    R1,0(,R1)     CLEAR IT                            86188
         ST    R1,RSVUCB     SAVE UCB ADDRESS FOR RESERVCE       86188
         OPEN  (PDSOTH,INPUT)  ONLY FOR DUPLICATE NAME CHECK     81144
         DEVTYPE PDSSYS+DCBDDNAM-IHADCB,DB  IS SYSPROC SUPPLIED ?
         BXH   R15,R15,DCBNSYS  NO                               81144
         CLI   DB+2,UCB3DACC  DISK ?                             81144
         BNE   DCBNSYS       NO                                  81144
         OPEN  (PDSSYS,INPUT)   OPEN FOR DUPLICATE NAME CHECK    81144
DCBNSYS  CLI   NONOLIST+8,C'L'  RUNNING PROCLEAN ?               81144
         BNE   DCBINOM       NO; SKIP OTHER GETMAIN
         L     R0,SIZMEM     GET SIZE FOR MEMBER NAME LIST
      GETMAIN  R,LV=(0)      GET CORE FOR MEMBER LIST
         ST    R1,LISTORG    SAVE THE ADDRESS
         ST    R1,LISTADD
         A     R1,SIZMEM1    ADD SIZE OF NEXT TO LAST ENTRY
         ST    R1,LISTEND    SET END OF LIST
DCBINOM  L     R0,SIZDEL     GET SIZE FOR DELETE LIST
     GETMAIN   R,LV=(0)      GET CORE FOR DELETE LIST
         ST    R1,DELETORG
         A     R1,SIZDEL1    ADD SIZE OF NEXT TO LAST ENTRY
         ST    R1,DELETEND   SET LAST ADDRESS
         L     R2,DELETORG   GET START AGAIN
DELINIT  XC    0(12,R2),0(R2)  CLEAR THE ENTRY
         LA    R2,12(,R2)    NEXT ENTRY
         CR    R2,R1         LAST ?
         BNH   DELINIT        NO; DO AGAIN
         MVI   0(R1),X'FF'   SET END OF LIST SWITCH
         SH    R1,=H'12'     POINT TO FIRST MEMBER ENTRY
         ST    R1,DELETADD   SAVE THE ADDRESS
         TIME  ,             GET THE DATE
         ST    R1,DB         STASH IT
         MVC   HEADATE-1(7),=X'4021204B202020'  MOVE EDIT MASK
         ED    HEADATE-1(7),DB+1      EDIT THE JULIAN DATE
         SRL   R1,4          ISOLATE YDDD IN LOW HALF            81144
         ICM   R1,12,ACCTOFF   GET ACCOUNT OFFSET IN TOP         81144
         ST    R1,DB                                             81144
         UNPK  NEWSSI(9),DB(5)  UNPACK                           81144
         TR    NEWSSI,HEXTRTAB-C'0'  MAKE EBCDIC                 81144
         MVI   NEWSSI+8,C','   RESTORE                           81144
         PRTF  HEAD1,121,TITLE=1                                 81144
         BR    R10           RETURN TO CALLER
         SPACE 2
         USING IHADCB,R2                                         81144
$CLOSER  TM    DCBOFLGS,DCBOFOPN  IS THE DCB OPEN ?              81144
         BZR   R14           NO; RETURN
         ST    R14,DB        SAVE RETURN
         CLOSE ((R2))        CLOSE THE DCB
         L     R14,DB        DON'T TRUST NO-BODY
         TM    DCBBUFCB+3,1  ANY BUFFER POOL ADDRESS ?           81144
         BNZR  R14           DEFINITELY NOT
         OC    DCBBUFCB+1(3),DCBBUFCB+1  OR ALL ZERO ?           81144
         BZR   R14           RETURN
         FREEPOOL (R2)       AND FREE BUFFERS
         L     R14,DB        DON'T TRUST NO-BODY
         BR    R14           RETURN
         DROP  R2                                                81144
         SPACE 2                                                 81144
LISTSECT DSECT ,             MAPPING OF MEMBER 'LIST' ENTRY      81144
LSTFLAG  DS    X             PROCESSING FLAG                     81144
LSTNAME  DS    CL8           MEMBER NAME                         81144
LSTTTR   DS    XL3           MEMBER TTR                          81144
LSTZ     DS    X             'Z' BYTE FOR FIND/STOW - ALWAYS ZERO
*                              AFTER STOW/DELETE - RETURN CODE   81144
LSTACCT  DS    CL4           ACCOUNT NUMBER                      81144
LSTDATE  DS    CL5           ADD OR LAST CHANGE DATE             81144
LSTLEN   EQU   *-LISTSECT    LENGTH OF ONE ENTRY                 81144
PROCUP   CSECT ,                                                 81144
         SPACE 2
*        CONSTANTS AND STUFF
         SPACE 2
BEBASE   DC    A(PROCUP+4096,PROCUP)  BASE REGISTERS
         SPACE 2
HEAD1    DS    0CL121
         DC    C'1'
BLANKS   DC    CL24' '
HEADSUP  DC    C'USER PROCLIB MAINTENANCE PROGRAM FOR '
HEADDSN  DC    CL44' ',CL9' '
HEADATE  DC    CL6' '        JULIAN DATE
HEAD2    DS    0CL121
         DC    CL26'0'
HEAD2VAR DC    CL15'ACCOUNT'                                     81144
         DC    CL14'DATE'                                        81144
         DC    CL19'MEMBER NAME'                                 81144
         DC    CL11'TTR'                                         81144
         DC    CL28'DISPOSITION'
HEAD2END DC    C'PAGE #P#'                                       81144
HEADSCR  DC    CL(HEAD2END-HEAD2VAR)'SCRATCH REQUEST LISTING'
HEADERR  DC    CL(HEAD2END-HEAD2VAR)'INPUT AND ERROR LISTING'
MEMLIST  DS    0CL13
MEMFLAG  DS    CL1
MEMSAVE  DS    0CL14         FULL ENTRY - NAME, TTR, FLAGS, CONC.
MEMSAV   DS    CL8
         DS    CL6           MEMSAV OVEFLOW DATA
MEMNO    DS    CL4
MEMDATE  DS    CL5           ADD/CHANGE DATE                     81144
PLINE    DC    Y(4+1+120,0)   V-FORMAT PRINT HEADER              81144
CTLCHAR  DC    C' '
LINE     DC    CL120' '
DOOMED   DC    X'00'
LIBTYPE  DC    X'0'          SET LIBRARY TYPE
DB       DC    D'0'
SAVER14  DC    A(0)                                              81144
DELETADD DC    A(0)
DELETORG DC    A(0)
LISTADD  DC    A(0)
LISTORG  DC    A(0)
MEMBLDL  DC    0A(0),AL2(1,20)  SET REASONABLE DIR. LENGTH
AMEM     DC    CL8' '        MEMBER NAME TO BE LOCATED
AMTRACK  DC    XL3'0'        TTR
AMFLAG   DC    X'0'          FLAG BYTE
DTSSI    DC    2XL4'0'       OTHER DATA
HEXTRTAB DC    C'0123456789ABCDEF'                               81144
DIREX    DC    0A(0),X'87',AL3(MYJFCB)
MEMEX    DC    0A(0),X'85',AL3(DCBMEMEX)
OP1      OPEN  (POD,INPUT),MF=L
         SPACE 2
DELETEND DC    A(0)          END OF DELETE LIST
LISTEND  DC    A(0)          END OF MEMBER LIST
SIZMEM   DC    A(LSTLEN*1300)    SIZE OF MEMBER LIST             81144
SIZMEM1  DC    A(LSTLEN*1300-26) SIZE FOR OVERFLOW CHECK         81144
SIZDEL   DC    A(12*200)     SIZE OF DELETE LIST
SIZDEL1  DC    A(12*200-12)  SIZE OF DELETE LIST FOR CHECKING
DELOFLOW WTU   '-   *****  OVERFLOW OF DELETE LIST - REASSEMBLE PROGRAM*
                OR RERUN  *****',MF=L                            81144
LSTOFLOW WTU   '-   *****  OVERFLOW OF MEMBER NAME LIST - REASSEMBLE TH*
               E PROGRAM FOR A LARGER SIZE  *****',MF=L          81144
         SPACE 1
NEWCARD  DC    C'./ '        PREFIX FOR NEW ADD/CHANGE/REPL CARD
OPCODE   DC    C'SCRATCH',C' '  OPERATION
         DC    C'SSI='
NEWSSI   DC    C'ACNNYDDD',C','
DCNAM    DC    C'NAME='
CARDMEM  DC    CL51' '       MEMBER NAME AND OPTIONS (SEQFLD,COLUMN)
         SPACE 2
MYJFCB   DS    0F
         IEFJFCBN ,          REQUEST IN-LINE JFCB EXPANSION
         SPACE 2
POD      DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GL,                        X
               RECFM=F,LRECL=256,BLKSIZE=256,KEYLEN=8,BUFNO=1,         X
               EODAD=ENDIR,EXLST=DIREX
         SPACE 2
PDS      DCB   DDNAME=SYSUT1,DSORG=PO,MACRF=R,                         X
               RECFM=FB,LRECL=80,BUFNO=1,NCP=1,EODAD=ENDMEM,           X
               EXLST=MEMEX
         SPACE 2                                                 81144
PDSOTH   DCB   DDNAME=SYSUT2,DSORG=PO,MACRF=R,   COMPLEMENT OF PDS     X
               RECFM=FB,LRECL=80,BUFNO=1,NCP=1,EODAD=ENDMEM,           X
               EXLST=MEMEX                                       81144
         SPACE 2                                                 81144
PDSSYS   DCB   DDNAME=SYSPROC,DSORG=PO,MACRF=R,                        X
               RECFM=FB,LRECL=80,BUFNO=1,NCP=1,EODAD=ENDMEM,           X
               EXLST=MEMEX                                       81144
         SPACE 2
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GL,EODAD=GETEOF,            *
               RECFM=FB,LRECL=80
         SPACE 2
UPDTEIN  DCB   DDNAME=UPDTEIN,DSORG=PS,MACRF=(GM,PM),EODAD=UPDTE,      *
               RECFM=FB,LRECL=80,BLKSIZE=3120
         SPACE 2
         LTORG
         SPACE 2
         DS    0H                                                81144
         USING *,R15
         DROP  R11,R12
         USING IHADCB,R1
DCBMEMEX NI    DCBOPTCD,255-32
         OC    DCBBLKSI,DCBBLKSI
         BNER  R14
         MVC   DCBBLKSI,DCBLRECL
         BR    R14
         DROP  R15
         SPACE 2
DIRECTRY DSECT
BLKLEN   DS    H
MEMNAME  DS    CL8
MEMTTR   DS    XL3
ENTLEN   DS    XL1
USERDATA EQU   *
         SPACE 2
         DCBD  DSORG=PS
         SPACE 2
CVTSECT  DSECT ,                                                 81144
         CVT   DSECT=YES                                         81144
         SPACE 1                                                 81151
         IHACDE ,                                                81151
         SPACE 1                                                 81144
         IKJTCB ,                                                81144
         SPACE 1                                                 81144
         IEFTCT ,                                                81144
         SPACE 2
         IEFJMR ,
         SPACE 2
         IEFUCBOB ,                                              81144
         SPACE 1                                                 86188
         IEZDEB ,                                                86188
         SPACE 2                                                 81144
         AIF   (NOT &MVS).NOPSA                                  92279
         IHAPSA ,                                                92279
.NOPSA   END
