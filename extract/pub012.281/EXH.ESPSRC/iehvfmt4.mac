FMT4     TITLE 'DSCB4'
         PRINT NOGEN
         MACRO
&NAME    TAB
         LCLC  &FL,&TL                                           81117
&FL      SETC  'L''&SYSLIST(1)'
         AIF   (N'&SYSLIST(1) LT 2).A
&FL      SETC  '&SYSLIST(1,2)'
.A       ANOP
&TL      SETC  'L''&SYSLIST(2)'
         AIF   (N'&SYSLIST(2) LT 2).B
&TL      SETC  '&SYSLIST(2,2)'
.B       ANOP
&NAME    DC S(&SYSLIST(1,1)),AL2(&FL),S(&SYSLIST(2,1)),AL2(&TL)  81117
         MEND
         SPACE 2                                                 81340
DSCB4    CSECT
         SAVE  (14,12),,*          SAVE REGS
         LR    12,15               SET BASE
         USING DSCB4,12
         LA    9,DSCB4OUT          SET OUTPUT BASE
         USING DSCB4OUT,9
         LR    11,13               FREE 13
         LA    13,D4SAVE           CHAIN SAVE AREAS
         ST    11,D4SAVE+4
         ST    13,8(11)
         LM    10,11,0(1)          GET INPUT PARMS
         USING DS4ADDR,10
         USING FMT4,11
         MVC   FM4IDFMT,DS4IDFMT   USE ACTUAL FORMAT ID
         CALL  PUTADDR,(CVHTAB4)        DO ADDRESS CONVERSIONS
         CALL  CVD,(CVDTAB4)       DO DEC CONVERSIONS
         MVC   FM4ADDR+10(2),=C'01'     SET RECORD 1
         MVC   FM4VTOCI,FM4VTOCI-1     CLEAR 'BAD' MESSAGE AREA
*        TM    DS4VTOCI,X'06'   CORRECTED VTOC ERRORS ?          81340
*        BO    DSCB4AA       YES    *** NOT ALWAYS TRUE ***      81340
         TM    DS4VTOCI,X'4'  ERRORS IN VTOC ?
         BZ    DSCB4AA       NO
         MVC   FM4VTOCI(L'VTOCERRM),VTOCERRM  VTOC ERRORS
         B     DSCB4A
DSCB4AA  TM    DS4VTOCI,X'08'  CORRECTED DOS PACK ?              81340
         BZ    DSCB4AB       OK; CHECK OTHERS                    81340
         MVC   FM4VTOCI(L'VTOCDOSC),VTOCDOSC   DOS CONTAMINATED  81340
         B     DSCB4A
DSCB4AB  TM    DS4VTOCI,X'81'  INDEXED VTOC ?                    90248
         BNO   DSCB4AC       NO                                  90248
         MVC   FM4VTOCI(L'VTOCINDX),VTOCINDX  INDICATE INDEXED   90248
         B     DSCB4A                                            90248
DSCB4AC  TM    DS4VTOCI,X'90'  ARE FORMAT 5 DSCBS VALID ?        90248
         BZ    DSCB4A        YES ?                               90248
         MVC   FM4VTOCI(L'VTOCNO5S),VTOCNO5S   NO DSCB 5 IN VTOC 81340
DSCB4A   MVC   FM4DEVFG,=C'DOES NOT'    SET NO-TOLERANCE FACTOR
         TM    DS4DEVFG,X'01'      DOES IT REALLY APPLY
         BZ    DSCB4B              NO                            81340
         MVC   FM4DEVFG,=C'  DOES  '    YES
         SPACE 1                                                 81340
DSCB4B   MVC   FM4INSER,FM4KBOX  MOVE ONE-BYTE OVERHEAD FIELD    81340
         TM    DS4DEVFG,8    TWO BYTE KEYED OVERHEAD ?           81340
         BZ    DSCB4C        NO; LEAVE ONE BYTE                  81340
         MVC   FM4INSER,FM4KBOH  MOVE TWO-BYTE OVERHEAD FIELD    81340
         SPACE 1                                                 85063
DSCB4C   TM    DS4DEVFG,X'20'  MODULO DEVICE ?                   85063
         BZ    DSCB4D        NO; PROCEED AS ALWAYS               85063
         MVI   FM4INSER,C' '                                     85063
         MVC   FM4INSER+1(FM4KBOL-1),FM4INSER  BLANK OUT         85063
         MVC   FM4INSER+FM4KBOL-2-L'MODMSG(L'MODMSG),MODMSG      85063
         MVC   FM4DEVK,=C'N/A'                                   85063
         SPACE 1                                                 81340
DSCB4D   OC    DS4AMTIM(DS4F6PTR-DS4AMTIM),DS4AMTIM  ANY VSAM STUFF ?
         BNZ   DSCB4C6       YES; PRINT FULL SIX LINES           81340
         CALL  PRINT,(DSCB4OUT,DSCB4OS1)  PRINT FIRST 2 LINES    81340
         CALL  PRINT,(FM4LINE4,DSCB4OS2)  AND LAST 2; SKIP VSAM  81340
         B     DSCB4EXT                                          81340
DSCB4C6  CALL  PRINT,(DSCB4OUT,DSCB4SIZ)     PRINT FULL ENTRY    81340
DSCB4EXT L     13,D4SAVE+4         RESTORE 13                    81340
         RETURN (14,12),T          RETURN
         SPACE 2                                                 81340
DSCB4SIZ DC    H'6,8'                                       *TSM*80204
DSCB4OS1 DC    H'2,8'        PRINT TWO LINES, RESERVE 8          81340
DSCB4OS2 DC    H'3,0'        PRINT THREE MORE                    81340
         SPACE 1                                                 81340
CVHTAB4  DS    0F
         TAB   (DS4VTOCE+2,4),FM4ADDR
         TAB   DS4F6PTR,FM4F6PTR
         TAB   DS4HCCHH,FM4HCCHH
         TAB   DS4HPCHR,FM4HPCHR
         TAB   DS4DEVSZ,FM4DEVSZ
         TAB   (DS4VTOCE+2,4),FM4VTOCL
         TAB   (DS4VTOCE+6,4),FM4VTOCH
         TAB   DS4AMTIM,FM4AMTIM                            *TSM*80204
         TAB   DS4VSIND,FM4VSIND                            *TSM*80204
         TAB   DS4VSCRA,FM4VSCRA                            *TSM*80204
         TAB   DS4R2TIM,FM4R2TIM                            *TSM*80204
         DC    H'0'
         SPACE 1                                                 81340
CVDTAB4  DS    0F
         TAB   DS4DSREC,FM4DSREC
         TAB   DS4DEVTK,FM4DEVTK
         TAB   DS4DEVTL,FM4DEVTL
         TAB   DS4DEVDT,FM4DEVDT
         TAB   DS4DEVDB,FM4DEVDB
         TAB   DS4NOATK,FM4NOATK
         TAB   DS4DEVI,FM4DEVI
         TAB   (DS4DEVI,2),FM4DEVH  2-BYTE OVERHEAD FIELD        81340
         TAB   DS4DEVL,FM4DEVL
         TAB   DS4DEVK,FM4DEVK
         DC    H'0'
         SPACE 1                                                 81340
VTOCERRM DC    CL28'** POSSIBLE VTOC ERRORS **'
VTOCNO5S DC    CL28'** NO VALID DSCB 5 **'
VTOCDOSC DC    CL28'** ''DOS'' CONTAMINATED **'
VTOCINDX DC    CL28'** INDEXED VTOC **'                          90248
D4SAVE   DC    18F'0'
         SPACE 2                                                 81340
*        VARIABLE PATTERN INSERTION DEPENDING ON ATTRIBUTES      81340
*              TWO 1-BYTE OR ONE 2-BYTE BLOCK OVERHEAD FIELD     81340
FM4KBOX  DC    C'  KEYED BLOCK OVERHEAD(NOT LAST BLOCK IS '      81340
FM4DEVI  DC    CL3' ',C', LAST BLOCK IS '
FM4DEVL  DC    CL3' ',C')               '
FM4KBOL  EQU   *-FM4KBOX     LENGTH OF INSERTION FIELD           81340
FM4KBOH  DC    C'  KEYED BLOCK OVERHEAD CONSTANT IS '            81340
FM4DEVH  DC    CL5' '                                            81340
         DC    CL(FM4KBOL+FM4KBOH-FM4DEVH-5)' '  PAD TO LENGTH   81340
MODMSG   DC    C'MODULO 32 DEVICE'                               85063
         SPACE 2                                                 81340
DSCB4OUT DC    6CL132' '                                    *TSM*80204
         ORG   DSCB4OUT
         DC    C'-'
FM4ADDR  DC    CL12' ',C'  FMT='
FM4IDFMT DC    C' ',C'   NUMBER OF AVAILABLE ENTRIES IN THE VTOC IS '
FM4DSREC DC    CL5' ',C'  '
FM4VTOCI DC    C'NO VALID FORMAT 5 DSCBS EXIST',CL20' '
         ORG   DSCB4OUT+103   OFFSET FIXED; TESTED IN IEHVMAIN   81340
         DC    C'DSCB EXTENDED AT '
FM4F6PTR DC    CL12' '
         SPACE 1                                                 81340
         ORG   DSCB4OUT+132
FM4LINE2 DC    CL6' ',C'THE FIRST OF '
FM4NOATK DC    CL5' ',C' AVAILABLE ALTERNATE TRACKS IS AT CCHH='
FM4HCCHH DC CL9' ',C'   LAST ACTIVE FORMAT 1 DSCB''S CCHHR='     81340
FM4HPCHR DC CL12' ',CL19' '                                      81340
         SPACE 1                                                 81340
         ORG   FM4LINE2+132
FM4LINE3 DC    C'      VSAM TIME STAMP',C' '                *TSM*80204
FM4AMTIM DC    CL16' ',C'  '                                *TSM*80204
         DC    C'VSIND '                                    *TSM*80204
FM4VSIND DC    CL02' ',C'  '                                *TSM*80204
         DC    C'VSCRA '                                    *TSM*80204
FM4VSCRA DC    CL04' ',C'  '                                *TSM*80204
         DC    C'VSAM TIME STAMP',C' '                      *TSM*80204
FM4R2TIM DC    CL16' ',C'  '                                *TSM*80204
         SPACE 1                                                 81340
         ORG   FM4LINE3+132                                 *TSM*80204
FM4LINE4 DC    C'      DEVICE SIZE MASK='                   *TSM*80204
FM4DEVSZ DC    CL9' ',C'  TRACK LENGTH IS '
FM4DEVTK DC    CL5' '                                            81340
FM4INSER DC    CL(FM4KBOL)' '  VARIABLE INSERTION                81340
         SPACE 1                                                 81340
         ORG   FM4LINE4+132                                 *TSM*80204
FM4LINE5 DC    C'      DEVICE TOLERANCE FACTOR IS '         *TSM*80204
FM4DEVTL DC    CL5' ',C'  ONE TRACK CONTAINS '
FM4DEVDT DC    CL3' ',C' DSCBS  OVERHEAD ADJ FOR UNKEYED BLOCKS IS '
FM4DEVK  DC    CL3' ',CL25' '
         SPACE 1                                                 81340
         ORG   FM4LINE5+132                                 *TSM*80204
FM4LINE6 DC    C'      THE TOLERANCE FACTOR '               *TSM*80204
FM4DEVFG DC    C'DOES NOT',C' APPLY  ONE TRACK CONTAINS '
FM4DEVDB DC    CL3' ',C' PDS DIRECTORY BLOCKS  VTOC EXTENT(LO='
FM4VTOCL DC    CL9' ',C',HI='
FM4VTOCH DC    CL9' ',CL20')'
         SPACE 2                                                 81340
DS4ADDR  DSECT
FMT4     DS    0CL96
DS4IDFMT DS    CL1
DS4HPCHR DS    CL5
DS4DSREC DS    CL2
DS4HCCHH DS    CL4
DS4NOATK DS    CL2
DS4VTOCI DS    CL1
DS4NOEXT DS    CL1
         DS    CL2
DS4DEVSZ DS    CL4
DS4DEVTK DS    CL2
DS4DEVI  DS    CL1
DS4DEVL  DS    CL1
DS4DEVK  DS    CL1
DS4DEVFG DS    CL1
DS4DEVTL DS    CL2
DS4DEVDT DS    CL1
DS4DEVDB DS    CL1
DS4AMTIM DS    XL8           VSAM TIME STAMP                *TSM*80204
DS4AMCAT DS    0XL3          VSAM CATALOG INFORMATION       *TSM*80204
DS4VSIND DS    X             VSAM INDICATORS                *TSM*80204
DS4VSCRA DS    XL2           RELATIVE TRACK LOCATION OF CRA *TSM*80204
DS4R2TIM DS    XL8           VOLUME/CATALOG MATCH TIME STAMP*TSM*80204
         DS    XL5           RESERVED                       *TSM*80204
DS4F6PTR DS    CL5
DS4VTOCE DS    CL10
         DS    CL25
         SPACE 2                                                 81340
         END
