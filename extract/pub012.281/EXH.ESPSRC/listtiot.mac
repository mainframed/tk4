LTIOT    TITLE 'L I S T T I O T  ***  LIST TIOT UNDER TSO'
***********************************************************************
*                                                                     *
*     NAME:      LISTTIOT (DTIOT)                                     *
*     WRITEN BY: GERHARD POSTPISCHIL                                  *
*     COPYWRITE: GPL                                                  *
*                OWNER RETAINS RIGHTS                                 *
*     PURPOSE:   TO DISPLAY THE TIOT OF A TSO SESSION.                *
*     INPUT:     NONE                                                 *
*     PARM:      NONE   - JUST DISPLAY THE TIOT ENTRIES               *
*     OUTPUT:    DISPLAY THE TIOT DATA                                *
*     NOTE(S):   INTENDED TO RUN ON A TSO TERMINAL                    *
*                WORKS UNDER MVS ONLY. NEEDS TOKEN RESOLUTION UNDER   *
*                LATER SYSTEMS. (TIOEJFCB)                            *
*     DATASETS:  NONE                                                 *
*     ATTRIBUTES: NOT REFRESHABLE, NOT REENTRANT, NOT REUSABLE.       *
*     REQUIREMENTS:  DURING ASSEMBLY, SYSLIB REQUIRES:                *
*                      SYS1.MACLIB                                    *
*                      SYS1.AMODGEN                                   *
*                PUT INTO A TSO ACCESSIBLE LIBRARY (E.G., SYS2.CMDLIB)*
*     CHANGE LOG:                                                     *
*        NEW MODULE.                                      ADDED GP15054
*                                                                     *
***********************************************************************
         SPACE 1
         COPY  ASMOPTS
         SPACE 1
         ASMPARM LIST=YES
         SPACE 1
         PUNCH '  ALIAS DTIOT '
         PUNCH '  ORDER LISTTIOT '
         SPACE 1
         PRINT &PRTSRC
LISTTIOT PGMHEAD ZERO15,BASE=(R12),BNDRY=PAGE,RM=24,AM=24,PARM=R9
         MVI   LINE-1,C' '
         MVC   LINE,LINE-1   PREPARE WORK LINE FOR PARM
         SPACE 1
         SERVINIT ,          GET @SERVICE ROUTINE
         SERVLOAD @PRINTER   LOAD PRINT/TERMINAL OUTPUT ROUTINE
         PRTOPEN TERMINAL,OPT=ABEND
         SPACE 1
*   PROCESS PARM (DIFFERENT FOR PROGRAM AND CP INVOCATION)
         PARMLOAD R4,PTR=R9  GET PARM ADDRESS AND LENGTH
         LTR   R5,R5  '      ANY OPTION?
         BNP   PARMDONE      NO; DEFAULT
         BCTR  R5,0          SET FOR EXECUTE
         EX    R5,EXMVCPRM   UPPERCASE AND SAVE PARM
         LA    R1,LINE            FIRST BYTE
         SLR   R2,R2
         EX    R5,EXTRTPRM   LOOK FOR ALL BLANK OR 'DETAIL'
         B     *+4(R2)       BRANCH BY STOPPER
          B    PARMDONE        ALL BLANK
          B    PARMTEST        DETAIL
*NEXT*    B    PARMFAIL        UNSUPPORTED
PARMFAIL PRTL  ' Option not recognized; use D or DETAIL'
         MVICC 12            SET ERROR                          GP15054
         B     PGMEXIT               GO END PROGRAM
         SPACE 1
PARMTEST OI    OPTFLAGS,FGLOTS    SET TO DISPLAY DETAIL
         CLI   1(R1),C' '         JUST 'D'?
         BE    PARMDONE           YES; CONTINUE
         CLC   =C'DETAIL ',0(R1)  DETAIL REQUESTED?
         BE    PARMDONE           YES; CONTINUE
         B     PARMFAIL
EXMVCPRM OC    LINE(0),0(R4)      SAVE PARAMETER                GP15054
EXTRTPRM TRT   LINE(0),TRTBLANK   TEST PARAMETER                GP15054
         SPACE 3
PARMDONE LTCB  R11,USE=
         L     R11,TCBTIO    GET THE TIOT
         ST    R11,DB        SAVE FOR FORMATTING
         PRTLIST JOBINFO
         USING TIOCNJOB,R11  USE ONCE
         LA    R10,TIOENTRY-TIOT1 SPACE TO FIRST DD
         USING TIOENTRY,R11  USE FOR ALL DDS
LOOPTIOT AR    R11,R10       NEXT DD
         ICM   R10,1,TIOELNGH     ANY MORE ?
         BZ    PGMEXIT
         TM    TIOESTTA,TIOSLTYP  IN USE?
         BNZ   LOOPTIOT             NO; IGNORE
         PRTLIST BASICDD
         SLR   R3,R3         JUST IN CASE
         ICM   R3,7,TIOEJFCB GET JFCB SQA ADDRESS, OR TOKEN
         BZ    LOOPTIOT        HUH?
         EX    R3,EXISODD    TOKEN (ODD ADDRESS)?
         BNZ   CONTOKEN
         LA    R1,16(,R3)    SKIP QUEUE HEADER
         B     HAVEJFCB
CONTOKEN SERVCALL SWARL,(R3) GET ADDRESS
HAVEJFCB MVC   LINE(JFCBLGTH),0(R1)    PRESERVE FOR A WHILE
         PRTLIST BASICDSN    APPEND THE DATA SET NAME
         B     LOOPTIOT
         SPACE 3
PGMEXIT  SERVTERM ,
         PGMEXIT COPYRET=RETCODE
         SPACE 1
EXISODD  TM    =X'01',*-*    TEST FOR ODD ADDRESS
         SPACE 1
         LTORG ,
         SPACE 1
TERMINAL PRTWORK *USER,SYSPRINT
         SPACE 1
         USING TIOT1,R11
JOBINFO  FDPRT '  TIOT at',NL
         FDPRT DB,4,HEX,PAD
         FDPRT ' Job/User'
         FDPRT TIOCNJOB,PAD
         FDPRT ' Step'
         FDPRT TIOCSTEP,8,PAD
         FDPRT TIOCSTEP+8,8
         FDOPT NL
         FDPRT *END
         SPACE 1
         USING TIOENTRY,R11
BASICDD  FDPRT TIOEDDNM,NL
         FDPRT TIOESTTA,HEX,PAD
         FDPRT TIOEWTCT,HEX,PAD
         FDPRT TIOELINK,HEX,PAD
         FDPRT TIOESTTC,HEX,PAD
         FDPRT TIOEJFCB,HEX,PAD
         FDPRT *END
         SPACE 1
BASICDSN FDPRT JFCBDSNM,DEB,PADL
         FDTM  JFCBIND1,JFCGDG+JFCPDS,BZ=BASICDSX ELEMENT NAME PRESENT?
         FDPRT '('
         FDPRT JFCBELNM,DEB
         FDPRT ')'
BASICDSX FDPRT *END
         SPACE 1
TRTBLANK TRENT FILL=8        SET EVERYTHING INVALID
         TRENT ,0,X'00',C' ',  EXCEPT NULL AND BLANK
         TRENT ,4,C'D'           AND D (FOR DETAIL)
         SPACE 1
SAVE     DSECT ,
DB       DS    D
         SERVDEFS ,          SERVICE ADDRESSES, ETC.
         SPACE 1
OPTFLAGS DC    X'0'
FGLOTS   EQU   X'80'         EXTRA OUTPUT
*   LINE USED AS WORK AREA, AS WELL AS OUTPUT
         DS    0F,XL3        WORD ALIGN FOR JFCB
         DS    C' '     1/2   FAST BLANKING
LINE     DS    CL255' ' 2/2
         ORG   LINE
         IEFJFCBN ,
         ORG   ,
         SPACE 1
SAVEEND  EQU   *
         PRINT &PRTSYS
         CVT   DSECT=YES,LIST=NO
         IHAPSA ,
         IKJTCB ,
MYTIOT   DSECT ,
         IEFTIOT1 ,
MYUCB    DSECT ,
         IEFUCBOB ,
         END
