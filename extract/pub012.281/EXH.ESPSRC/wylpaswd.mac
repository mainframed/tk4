WPDL    TITLE 'W Y L P A S W D  ***  WYLBUR PASSWORD LIST AND DELETION'
         MACRO ,
&NM      FLG   &TXT,&FON,&FOF
         AIF   ('&FOF' EQ '').NEGATE
&NM      DC    AL1(&FON,&FOF),CL6'&TXT '
         MEXIT ,
.NEGATE  ANOP  ,
&NM      DC    AL1(&FON,0),CL6'&TXT '                            79204
         MEND  ,
         SPACE 2
         COPY  OPTIONGB      DEFINE GLOBALS                      82010
         SPACE 1                                                 82010
         SYSPARM LIST=YES    SET GLOBALS                         82010
         SPACE 2                                                 81068
***********************************************************************
**                                                                   **
**       AUTHOR :  M. RADZYMSKI                                      **
**             MAINTENANCE MODS BY G. POSTPISCHIL                81068*
**                                                                   **
***********************************************************************
**                                                                   **
**  NOTE:  THIS MODULE REQUIRES CO-REQUISITE CHANGES TO THE PASSWORD **
**         SVC (FUBCTIONS MODIFIED AND ADDED)                        **
**                                                                   **
**  REPLACED BY NEW ACCOUNTING SYSTEM (SEE .ACCTSRC/.ACCTMAC)        **
***********************************************************************
         EJECT ,                                                 82010
         PRINT &PRTSOR                                           82010
WYLPASWD SAVEM ZERO12,BASE=R12,PARM=R9                           82010
         LA    R1,0(,R9)     PARM                                82010
         LTR   R1,R1         ANY ?
         BZ    OPEN          NO
         LAT   R1,0(R1),OPEN   GET PARM FIELD
         CLC   0(8,R1),DELPARM   IS IT DELETE ?
         BNE   OPEN          NO
         MVI   FLAG,FGREAL   SET REAL DELETE                     79204
         SPACE 1
OPEN     SERVINIT ,          INITIALIZE @SERVICE FUNCTIONS       82010
         SERVCALL ACGET,CURRACCT  GET CURRENT ACCOUNT AND PRIVILEGES
         STC   R0,ACCTPRIV   STASH ACCOUNT PRIVILEGES            82010
         TM    ACCTPRIV,VAASYS+VAASUP  PRIVILEGED USER ?         82010
         BNZ   LIVERUN       YES                                 82010
         NI    FLAG,255-FGREAL  FORCE TEST MODE                  82010
         OI    FLAG,FGUSER   SET USER RUN                        82010
LIVERUN  TESTAUTH FCTN=1     AUTHORIZED ?                        82010
         BXH   R15,R15,DEADRUN    NO; WILL 913 ON PASSWORD DSN   82010
         L     R4,CVTPTR     GET CVT                             82010
         USING CVTMAP,R4                                         82010
         L     R4,CVTTCBP    GET TCB POINTERS                    82010
         L     R4,4(,R4)     GET CURRENT TCB                     82010
         USING TCB,R4                                            82010
         MODESET KEY=ZERO    GET PRIVILEGED                      82010
         L     R3,TCBJSCB    GET JSCB ADDRESS                    82010
         USING IEZJSCB,R3                                        82010
         OI    JSCBSWT1,JSCBPASS  PERMIT READ ACCESS TO PASSWORD 82010
         DROP  R3,R4                                             82010
         MODESET KEY=NZERO   RESTORE                             82010
DEADRUN  SERVCALL LPALD,=CL8'@PRINTER'  LOAD THE PRINTER ROUTINE 82010
         ST    R0,@PRINTER                                       82010
         SERVCALL LPALD,=CL8'IGPVOLT'                            82010
         ST    R0,@IGPVOLT                                       82010
         LR    R2,R0         SAVE FOR PASSWORD VOLUME LOCATE     82010
         SERVCALL LPALD,=CL8'@PROTECS'  GET FAST PROTECT CODE    82010
         ST    R0,@PROTECT                                       82010
         PRTOPEN SYSPRINT,OPT=(ABEND)  OPEN THE PRINTER          82010
         USING USERVOLT,R2   DECLARE THE VOLUME TABLE            82010
         L     R3,VTWYIXAD   GET FIRST (PRIMARY) WYLBUR INDEX    82010
         AH    R3,VTWYIXLN   BUMP PAST INDEX                     82010
         LA    R1,6(,R3)     SKIP CVOL, POINT TO PASSWORD VOLUME 82010
         DROP  R2                                                82010
         SERVCALL UCBDK,(R1)  FIND THE PASSWORD VOLUME           82010
         LTR   R1,R0         SET FOR SPECIAL PROTECT CALL        82010
         BZ    UNITEXIT                                          82010
         LA    R2,PROTLIST   PROTECT PARAMETER LIST ADDRESS      82010
         STM   R0,R2,PROTUCB  SAVE UCB ADDRESS FOR PROTECT       82010
         MVI   DATA1+1,137   MAKE RDW                            82010
         MVI   DATA2+1,137                                       82010
         MVI   BDATA1-1,C' '                                     82010
         MVC   BDATA1,BDATA1-1  CLEAR PRINT LINE                 82010
         MVC   BDATA2-1(L'BDATA2+1),BDATA1-1                     82010
         LA    R1,BLANKS     SET FAKE ENTRY                      82010
         LA    R2,8          ENTRY LENGTH                        82010
         LA    R3,BLANKS-8                                       82010
         STM   R1,R3,TMCBXLE  SAVE ADDRESS AND BUILD LIST        89296
         L     R2,CVTPTR                                         89296
         USING CVTMAP,R2                                         89296
         ICM   R2,15,CVTUSER                                     89296
         BZ    NOVOLIST                                          89296
         USING USERCVT,R2                                        89296
         ICM   R2,15,UCLVOLS USER-VOLUME LIST ?                  89296
         BZ    NOVOLIST      NO                                  89296
         MVC   TMCBXLE(12),0(R2)  MOVE REAL BXLE POINTERS        89296
NOVOLIST TIME  ,             GET CURRENT DATE                    79204
         LR    R3,R1         SAVE DATE                           79204
         STH   R1,CURDATE+2  SAVE DAY                            79204
         OI    CURDATE+3,X'0F'  POSITIVE POSITIVE SIGN           79204
         ZAP   DOUBLE,CURDATE+2(2)  COPY DAY                     79204
         CVB   R2,DOUBLE     GET DAY IN BINARY                   79204
         SRA   R1,16         SHIFT OUT DAYS                      79204
         BCTR  R1,0          YEAR LESS ONE                       79204
         STH   R1,CURDATE    MAKE PREVIOUS YEAR                  79204
         SRL   R3,12         ISOLATE YEAR                        79204
         ST    R3,DOUBLE+4                                       79204
         XC    DOUBLE(4),DOUBLE                                  79204
         OI    DOUBLE+7,X'0F'  MAKE PLUS                         79204
         CVB   R3,DOUBLE     GET YEAR IN BINARY                  79204
         SH    R2,=H'182'    SUBTRACT HALFYEAR                   79204
         BP    *+10                                              79204
         BCTR  R3,0          DECREASE YEAR BY ONE                79204
         AH    R2,=H'366'    CHEAT                               79204
         MH    R3,=H'1000'   SCALE YEAR                          79204
         AR    R2,R3         YEAR+DAY                            79204
         CVD   R2,DOUBLE                                         79204
         MVC   SIXDATE+1(3),DOUBLE+5  SAVE SIX MONTHS            79204
         SPACE 2                                                 79204
         LM    R14,R0,@PROTECT  GET PROTECT UCB ADDRESSES        82010
         LA    R1,ADPRO      GET PHONY PROTECT REQUEST           79204
         LA    R6,UNITEXIT   GET ERROR EXIT ADDRESS              79204
         BALR  R14,R14       CALL @PROTECT                       79204
         BR    R6              PRINT NASTY IF ERROR              79204
         L     R1,@PROTECT   GET PASSWORD READ MODULE            82010
         L     R4,12(,R1)    GET START OF WORK AREA              83073
         LM    R1,R3,4(R4)   GET START OF AREA USED AND COUNT    83073
         L     R2,20(,R4)    POINT TO TOP USED ENTRY             83073
         ST    R1,BOTLIST                                        79204
         ST    R2,TOPLIST    SAVE FOR BXLE                       79204
         ST    R3,TOTALMAX   SET TOTAL RECORDS IN DATA SET       82010
         LA    R3,PLEN       LENGTH OF AN ENTRY                  82024
         STM   R1,R3,SORTPARM   MAKE SORT PARAMETER LIST         82024
         OI    SOPHIGH,SOPFAD   SHOW ADDRESS IN SECOND FIELD     82024
         MVI   SOPCOO,PSWACCT-AREASECT  POINT TO ACCOUNT         82024
         MVI   SOPCOL,L'PSWACCT+L'PSWUSER  SET COMBINED LENGTHS  82024
         MVI   SOPSEQ,C'A'   SET ASCENDING SEQUENCE              82024
         SERVCALL SORTH,SORTPARM   TRY A HEAP SORT               82024
         L     R11,BOTLIST                                       79204
         USING AREASECT,R11  DECLARE MAPPING                     79204
         PRTLIST DELHEAD,TITLE=1     HEADER FOR DELETIONS        82010
         PRTV  SUBHEAD,TITLE=3 AND SUBHEADER                     82010
DELLOOP  CLC   =CL9'WYLBUR',PSWDSN   WYLBUR ENTRY ?              79204
         BNE   DELINC        NO; SKIP                            79204
         TM    FLAG,FGUSER   UNPRIVILEGED USER MODE ?            82010
         BZ    DELUSER       NO                                  82010
         MVC   PSWPSWD,BLANKS  CLEAR PASSWORD                    82010
         CLC   PSWACT,CURRACCT  MATCHING ACCOUNT ?               82010
         BE    DELUSER       YES; PROCESS IT                     82010
         LA    R5,PSWWACTS   POINT TO EXTENDED ACCOUNTS          82010
         LA    R6,5          NUMBER TO CHECK                     82010
DELULOOP CLC   CURRACCT(4),0(R5)  EXTENDED ACCOUNT ACCESS ?      82010
         BE    DELUSER       YES; DISPLAY IT                     82010
         LA    R5,4(,R5)     TRY NEXT ENTRY                      82010
         BCT   R6,DELULOOP                                       82010
         MVI   PSWDSN,X'FF'  MAKE IT INVALID                     82010
         B     DELINC        AND SKIP THIS ENTRY                 82010
DELUSER  NI    FLAG,255-FFIX-FSUB-FVOL  TURN OFF REPLACE BITS    82010
         SERVCALL ACTST,PSWACCT  CHECK FULL ACCOUNT NUMBER       82010
         STC   R0,ACCTPRIV   SAVE ACCOUNT PRIVILEGES             82010
         CH    R15,=H'4'     VALID ?                             82305
         BNH   DELSUBA       VALID; NOW CHECK EXTENDED ACCOUNTS  82305
         BAL   R9,FORMAT     FORMAT BASIC ENTRY                  79204
         MVC   DSPPASSW,PSWPSWD  ALSO SHOW PASSWORD              79204
         PRTSPACE 1          DOUBLE-SPACE ENTRIES                82010
         PRTROOM 3           ALLOW FOR 3 CONTIGUOUS LINES        82010
         PRTV  DATA1          PRINT ALL LINES                    82010
         PRTV  DATA2                                             82010
         PRTV  INVACT                                            82010
         TM    FLAG,FGREAL   REAL DELETE OPTION ?                79204
         BNO   DELDCNT       NO; PROCESS NEXT ENTRY              82010
         LM    R14,R2,DEPAT   GET DELETE PATTERN                 79204
         ALR   R15,R11       RELOCATE                            79204
         ALR   R0,R11                                            79204
         STM   R14,R2,PROTLIST  MAKE PROTECT LIST                79204
         LM    R15,R1,PROTUCB  GET PROTECT REGISTERS             81068
         PROTECT (1)         ISSUE DELETE                        79204
         BXLE  R15,R15,DELDONE  DELETED                          79204
         PRTV  DELFAIL       SHOW DELETE FAILED                  82010
         B     DELINC        GET NEXT ENTRY                      82010
DELDONE  MVI   AREASECT,X'FF'                                    79204
         MVC   AREASECT+1(PLEN-1),AREASECT  CLEAR OUT            79204
DELDCNT  INC   TOTALDEL      COUNT DELETIONS                     82010
         B     DELINC        DO NEXT ENTRY                       79204
         SPACE 1                                                 79204
DELSUBA  BAL   R9,FORMAT     PROVISIONALLY FORMAT AS IS          79204
         MVC   INVSACT,BLANKS   CLEAR BAD ACCOUNT FIELD          82010
         MVC   INVSVOL,BLANKS                                    82010
         LA    R5,PSWWACTS    POINT TO EXTENDED ACCOUNTS         79204
         LA    R6,6          AND NUMBER                          79204
DELSLOOP CLC   0(4,R5),BLANKS  ALL BLANK ?                       82010
         BE    DELSINC       YES; CONTINUE                       79204
         CLC   PSWACCT(4),0(R5)  SAME AS MAJOR ACCOUNT ?         79204
         BE    DELSDEL       YES; DELETE                         79204
         LR    R1,R5         COPY ACCOUNT ADDRESS                82010
         ICM   R1,8,=AL1(4)  SET TO CHECK FOUR BYTES ONLY        82010
         SERVCALL ACTST,(R1) CHECK ACCOUNT                       82010
         CH    R15,=H'4'     VALID ACCOUNT ?                     82305
         BNH   DELSINC       GOOD; CONTINUE                      82305
         TM    ACCTPRIV,VAAINH  IN-HOUSE ACCOUNT ?               82010
         BZ    DELSDEL       NO; DELETE                          82010
         CLC   =C'0909',0(R5)  SPECIAL ACCOUNT ?                 82010
         BE    DELSINC       YES; PERMIT IT                      82010
DELSDEL  MVC   INVSACT(25),INVSACT+5  SHIFT OVER                 82010
         MVC   INVSACT+25(4),0(R5)  DISPLAY BAD ONE              79204
         MVC   0(4,R5),INVSACT+5  BLANK OUT BAD ONE              79204
         OI    FLAG,FFIX+FSUB     SHOW FIX REQUIRED              79204
DELSINC  LA    R5,4(,R5)     NEXT ACCT                           79204
         BCT   R6,DELSLOOP   TRY NEXT                            79204
         CLC   PSWWDVOL,BLANKS  ANY SAVE VOLUME ?                79204
         BE    DELNSAVE      NO; SKIP                            82010
         CLC   PSWWDVOL,=CL6'ANY'  ANY VOLUME ?                  82010
         BE    DELNSAVE      YES; PERMIT                         82010
         LA    R5,PSWWDVOL   POINT TO SAVE VOLUME                82010
         LA    R4,L'PSWWDVOL-1  SET EXECUTE LENGTH               82010
         BAL   R9,TESTTMC    SEE IF VALID                        82010
         B     DELNSAVE      YES                                 82010
         MVC   INVSVOL+30(6),PSWWDVOL  SHOW BAD ENTRY            82010
         MVC   PSWWDVOL,=CL6'ANY'  CLEAR IT OUT                  82010
         OI    FLAG,FFIX+FVOL  SHOW REPLACE REQUIRED             82010
DELNSAVE LA    R5,PSWWAVOL   POINT TO EXTENDED VOLUMES           82010
         LA    R6,5          AND NUMBER                          79204
DELSVLOP CLC   0(5,R5),BLANKS  EMPTY ENTRY ?                     82010
         BE    DELSVINC      YES; SKIP                           82010
         CLC   PSWWDVOL(5),0(R5)  DUPLICATE DEFINITION ?         82010
         BE    DELSVVOL      YES; KILL IT                        82010
         LA    R4,4          SET EXECUTE LENGTH FOR CLC          82010
         BAL   R9,TESTTMC    IN TMC ?                            82010
         B     DELSVINC      YES                                 82010
DELSVVOL OI    FLAG,FFIX+FVOL  SET BAD VOLUME                    82010
         MVC   INVSVOL(37),INVSVOL+6   SHIFT OVER                82010
         MVC   INVSVOL+31(5),0(R5)  ADD IT IN                    82010
         MVC   0(5,R5),BLANKS  CLEAR OUT                         79204
DELSVINC LA    R5,5(,R5)                                         79204
         BCT   R6,DELSVLOP   DO ALL                              79204
DELRTEST TM    FLAG,FFIX     REPLACE REQUIRED ?                  79204
         BZ    DELINC        NO; DO NEXT ENTRY                   79204
         PRTSPACE 1          DOUBLE-SPACE                        82010
         PRTROOM 3           ROOM FOR THREE LINES                82010
         PRTV  DATA1                                             82010
         PRTV  DATA2                                             82010
         TM    FLAG,FVOL     BAD VOLUME ?                        79204
         BZ    DELRVOL       NO                                  79204
         PRTLIST INVACVOL    ISSUE MESSAGE                       82010
DELRVOL  TM    FLAG,FSUB     BAD SUBACCOUNT ?                    79204
         BZ    DELRPRO       NO; SKIP MESSAGE                    79204
         PRTLIST INVSACCT    INVALID SUBACCOUNT                  82010
DELRPRO  TM    FLAG,FGREAL   REAL DELETE OPTION ?                79204
         BNO   DELRCNT       NO; PROCESS NEXT ENTRY              82010
         LM    R14,R4,REPPAT GET REPLACE PATTERN                 79204
         ALR   R15,R11       RELOCATE                            79204
         ALR   R0,R11                                            79204
         ALR   R3,R11                                            79204
         ALR   R4,R11                                            79204
         STM   R14,R4,PROTLIST                                   79204
         LM    R15,R1,PROTUCB  GET PROTECT REGISTERS             81068
         PROTECT (1)         REPLACE THE BAD ACCOUNT             79204
         BXLE  R15,R15,DELRCNT  CONTINUE IF OK                   82010
         PRTV  REPFAIL                                           82010
         B     DELINC        AND LEAVE AS IS                     82010
DELRCNT  INC   TOTALFIX      COUNT NUMBER FIXED                  82010
DELINC   LA    R11,PLEN(,R11)  SKIP TO NEXT ENTRY                79204
         C     R11,TOPLIST   DONE ?                              79204
         BNH   DELLOOP       NO; CONTINUE                        83073
         SPACE 2                                                 79204
         PRTLIST LISTHEAD,TITLE=1  HEADER FOR PLAIN LIST         82010
         L     R11,BOTLIST   POINT TO FIRST ENTRY                79204
         XR    R8,R8                                             79204
LISTLOOP CLC   =CL9'WYLBUR',PSWDSN  WYLBUR ENTRY ?               79204
         BNE   LISTINC       NO                                  79204
         BAL   R9,FORMAT     FORMAT THE DATA                     79204
         PRTROOM 2           TWO CONTIGUOUS LINES                82010
         PRTV  DATA1                                             82010
         PRTV  DATA2                                             82010
         LA    R8,1(,R8)     UP COUNT OF ACTIVE PASSWORDS        79204
         XC    PSWACCT,PSWUSER  FLIP THE ACCOUNT AND USERID      82024
         XC    PSWUSER,PSWACCT   TO KEEP THE USERID SORT IN      82024
         XC    PSWACCT,PSWUSER   ACCT SEQUENCE FOR EQUAL USER IDS.
LISTINC  LA    R11,PLEN(,R11)                                    79204
         C     R11,TOPLIST    DONE ?                             79204
         BNH   LISTLOOP                                          83073
         ST    R8,TOTAL      TOTAL #PASSWORDS                    82010
         SPACE 1
         PRTLIST EOFMSG                                          82010
         SPACE 2                                                 81068
*        NOW REPEAT WITH ENTRIES SORTED BY USER ID               81068
*                                                                81068
         SERVCALL SORTH,SORTPARM   DO A HEAP SORT                82024
         SPACE 1                                                 82024
         PRTLIST LUSTHEAD,TITLE=1  HEADER FOR USERID LIST        82010
         L     R11,BOTLIST   POINT TO FIRST ENTRY                81068
LUSTLOOP CLC   =CL9'WYLBUR',PSWDSN  WYLBUR ENTRY ?               81068
         BNE   LUSTINC       NO                                  81068
         XC    PSWACCT,PSWUSER  FLIP THE ACCOUNT AND USERID      82024
         XC    PSWUSER,PSWACCT   BACK THE WAY                    82024
         XC    PSWACCT,PSWUSER   THEY SHOULD BE                  82024
         BAL   R9,FORMAT     FORMAT THE DATA                     81068
         PRTROOM 2           TWO CONTIGUOUS LINES                82010
         PRTV  DATA1                                             82010
         PRTV  DATA2                                             82010
LUSTINC  LA    R11,PLEN(,R11)                                    81068
         C     R11,TOPLIST    DONE ?                             81068
         BNH   LUSTLOOP                                          83073
         PRTLIST EOFMSG                                          82010
EXIT     SERVTERM ,          CLOSE AND FREE EVERYTHING           82010
         L     R9,COMPCODE   GET COMPLETION CODE                 82010
         ENDM  RC=(R9)       EXIT WITH RETURN CODE               82010
         SPACE 1                                                 79204
UNITEXIT PRTV  PROFAIL       SIGNAL ERROR                        82010
         MVI   COMPCODE+3,16  SET ERROR                          79204
         B     EXIT                                              79204
         SPACE 2                                                 82010
*        TMC LOOKUP ROUTINE                                      82010
*        R5 POINTS TO VOLSER TO BE TESTED                        82010
*        R4 HAS EXECUTE LENGTH (4 OR 5)                          82010
*        RETURN 0(R9) - VALID VOLUME                             82010
*               4(R9) - INVALID VOLUME                           82010
TESTTMC  LM    R15,R1,TMCBXLE  GET TMC DISK ENTRIES              82010
         CR    R15,R1        ANY AT ALL ?                        82010
         BNL   TESTTMCP      NO; NOT INITIALIZED, DD CARD MISSING, ETC.
TESTTMCL EX    R4,TESTTMCC   MATCHING DISK ?                     82010
         BE    TESTTMCP      YES; VALID                          82010
         BXLE  R15,R0,TESTTMCL  TRY NEXT ENTRY                   82010
         B     4(,R9)        RETURN INVALID                      82010
TESTTMCC CLC   0(0,R5),UVOLSER-UVOLSER(R15)  EXECUTED CLC        89296
TESTTMCP CLC   =C'WYL00',0(R5)                                   82010
         BE    4(,R9)        INVALID SAVE GROUP                  82010
         CLC   =C'WYLBU',0(R5)                                   82010
         BE    4(,R9)                                            82010
         CLC   =C'WORK',0(R5)                                    82010
         BE    4(,R9)        INVALID                             82010
         CLC   =C'SVS',0(R5)                                     82010
         BE    4(,R9)                                            82010
         CLC   =C'MVS',0(R5)                                     82010
         BE    4(,R9)                                            82010
*        CLC   =C'LIBPA',0(R5)         **DUMMY UNTIL WYLBUR FIXED**
*        BE    4(,R9)                                            82010
         BR    R9            RETURN VALID                        82010
         EJECT
FORMAT   MVI   BDATA1,C' '   BLANK LINE                          79204
         MVC   BDATA1+1(L'BDATA1-1),BDATA1                       79204
         MVI   BDATA2,C' '                                       79204
         MVC   BDATA2+1(L'BDATA2-1),BDATA2                       79204
         MVC   DSPACCT,PSWACCT   MOVE ACCOUNT/SUBACCOUNT
         MVC   DSPUSER,PSWUSER
         MVC   DSPSUBSY,PSWWSSID       MILTEN SUBSYSTEM ID
         MVC   DSPSAVEV,PSWWDVOL
         CLI   PSWWCPU,(CPUTABLN-CPUTABLE)/4  VALID CPU ID ?
         BL    *+8
         MVI   PSWWCPU,0     ELSE KILL IT
         XR    R1,R1
         IC    R1,PSWWCPU    GET ID
         SLA   R1,2          *4
***      BZ    FORMNCPU
         LA    R1,CPUTABLE(R1)  GET ADDRESS OF ENTRY
         MVC   DSPCPU,1(R1)  SHOW CPU MNEMONIC
FORMNCPU XR    R1,R1         CLEAR FOR INDEX OFFSET IC           81068
         L     R15,@IGPVOLT  GET VOLUME TABLE ADDRESS            82010
         USING USERVOLT,R15                                      82010
         CLC   PSWHINDX,VTWYIXNO  VALID ?                        82010
         BL    FORMHIX       YES; GET IT                         81068
         MVI   PSWHINDX,0    ELSE USE DEFAULT INDEX              81068
FORMHIX  IC    R1,PSWHINDX   LOAD INDEX VALUE                    81068
         MH    R1,VTWYENLN   MAKE INTO INDEX LIST OFFSET         82010
         AL    R1,VTWYIXAD  SET ADDRESS OF THIS INDEX LEVEL      82010
         DROP  R15                                               82010
         MVC   DSPHIX,0(R1)  PUT IN DISPLAY LINE                 81068
         LA    R1,DSPRPRT    POINT TO HASP PRINT/PUNCH ROUTING   81068
         LA    R2,PSWWRJID
         LA    R3,2
         XR    R0,R0
FORMRMT  IC    R0,0(,R2)     GET PRT/PUN REMOTE
         CVD   R0,DB         MAKE PACKED
         MVC   DB(4),=X'40202020'  NO SIGNIFICANT ZERO
         ED    DB(4),DB+6    EDIT REMOTE #
         MVC   0(L'DSPRPRT,R1),DB+1  DISPLAY
         LA    R1,DSPRPUN-DSPRPRT(,R1)
         LA    R2,1(,R2)     NEXT INPUT FIELD
         BCT   R3,FORMRMT    DO SECOND FIELD
         LA    R1,DSPAUX-1   POINT TO EXTENDED ACCOUNT FIELD - 1
         LA    R2,PSWWACTS   POINT TO PROTECT ACCOUNTS
         LA    R3,6          SET FOR NUMBER
FORMACCT CLI   0(R2),C' '    ANOTHER ?
         BNH   FORMACTX      NO
         MVI   0(R1),C','    ADD SEPARATING COMMA
         MVC   1(4,R1),0(R2)  COPY THIS ONE
         LA    R1,5(,R1)     POINT TO NEXT OUTPUT FIELD
         LA    R2,4(,R2)     AND NEXT INPUT
         BCT   R3,FORMACCT
FORMACTX MVI   DSPAUX-1,C' '  RESTORE
         LA    R1,DSPAVOL-1   POINT TO EXTENDED VOLUME FIELD - 1
         LA    R2,PSWWAVOL   POINT TO PROTECT LIST
         LA    R3,5          SET FOR NUMBER
FORMVOLS CLI   0(R2),C' '    ANOTHER ?
         BNH   FORMVOLX      NO
         MVI   0(R1),C','    ADD SEPARATING COMMA
         MVC   1(5,R1),0(R2)  COPY THIS ONE
         LA    R1,6(,R1)     POINT TO NEXT OUTPUT FIELD
         LA    R2,5(,R2)     AND NEXT INPUT
         BCT   R3,FORMVOLS
FORMVOLX MVI   DSPAVOL-1,C' '  RESTORE
         LA    R1,DSPPRIV    POINT TO PRIVILEGE FLAG FIELD
         IC    R3,PSWWPRIV   INSERT FLAGS
         LA    R2,OPTTAB     POINT TO OPTION TEXT/FLAGS
         LA    R15,(OPTHASP-OPTTAB)/8  NUMBER IN FIRST BYTE
         LA    R14,3         NUMBER OF BYTES TO DO
FORMOPTS STC   R3,DB         SET FLAG TO BE TESTED
         NC    DB(1),0(R2)   MASK IT
         CLC   DB(1),0(R2)   MATCH ?
         BE    FORMOPTX      YES
         TM    1(R2),SKIPOF  SKIP DISPLAY IF OFF ?               79204
         BNZ   FORMOPTU      YES                                 79204
         MVC   0(2,R1),=C'NO'  ELSE NEGATE
         LA    R1,2(,R1)     AND BUMP
         B     FORMOPTY                                          79204
FORMOPTX TM    1(R2),SKIPON  SKIP IF FLAG ON ?                   79204
         BNZ   FORMOPTU      YES                                 79204
FORMOPTY MVC   0(6,R1),2(R2)  COPY TEXT
FORMOPTZ LA    R1,1(,R1)     SKIP ONE
         CLI   0(R1),C' '    TRAILING BLANK ?
         BNE   FORMOPTZ      NO; TRY MORE
         LA    R1,1(,R1)     LEAVE ONE TRAILING
FORMOPTU LA    R2,8(,R2)     POINT TO NEXT TABLE ENTRY           79204
         BCT   R15,FORMOPTS  DO ALL IN BYTE
         IC    R3,PSWWHASP   GET NEXT FLAG BYTE
         LA    R15,(OPTFLAG-OPTHASP)/8  AND NUMBER OF FLAGS
         CH    R14,=H'2'     DONE WITH SECOND FLAG ?
         BNE   FORMFLAX      NO
         IC    R3,PSWWFLAG   GET FLAG BITS
         LA    R15,(OPTEND-OPTFLAG)/8-1  NUMBER OF BITS-1
         TM    PSWWFLAG,PSWWAUA+PSWWAUL  CHECK FOR FULL LOGON/NOLOGON
         BNM   FORMFLAX      NOT MIXED, DO NORMALLY
         LA    R2,8(,R2)     SKIP FIRST TABLE ENTRY
FORMFLAX BCT   R14,FORMOPTS  FORMAT IT
         UNPK  DB(5),PSWWDATE  GET ACCESS DATE
         LA    R1,DSPDATA    GET ADDRESS
         LA    R2,2          DO TWO DATES
FORMDATE MVC   0(2,R1),DB    YEAR
         MVI   2(R1),C'.'
         MVC   3(3,R1),DB+2  DAY
         OI    5(R1),C'0'    ENSURE GOOD SIGN
         LA    R1,DSPDATC-DSPDATA(,R1) NEXT FIELD
         UNPK  DB(5),PSWWBIN  CREATED/MODIFY DATE
         BCT   R2,FORMDATE
         OI    PSWWBIN+2,X'0F'  MAKE GOOD SIGN                   79204
         OI    PSWWDATE+2,X'0F'  DITTO                           79204
         CLC   PSWWBIN,PSWWDATE  ACCESSED SINCE LAST CHANGE ?    79204
         BL    *+10          YES                                 79204
         MVC   DSPFLAG,=C'::>'  POINT IT OUT                     79204
         CLC   PSWWDATE,SIXDATE+1  ACCESSED IN LAST SIX MONTHS ? 79204
         BH    FORMTTR       YES                                 79204
         MVC   DSPFLAG,=C'-->'  POINT IT OUT                     79204
         CLC   PSWWDATE,CURDATE+1  ACCESSED IN LAST YEAR ?       79204
         BH    *+10          YES                                 79204
         MVC   DSPFLAG,=C'==>'  POINT TO IT                      79204
FORMTTR  UNPK  DB(7),AREATTR(4)  UNPACK TTR                      82010
         TR    DB(7),HEXTRTAB-C'0'   MAKE HEX                    82010
         MVC   DSPTTR,DB     MOVE TO DISPLAY LINE                82010
         BR    R9            RETURN TO CALLER                    79204
         EJECT
SYSPRINT PRTWORK SYSPRINT,TITLE=5                                82010
HEXTRTAB DC    C'0123456789ABCDEF'                               82010
ADPRO    DC    AL1(PSWFTTR),AL3(DB-SAVE),AL1(1),AL3(DB-SAVE),A(DB-SAVE)
DEPAT    DC    AL1(PSWFDEL),AL3(0),AL1(L'PSWDSN),AL3(PSWDSN-AREASECT)
         DC    A(PSWPSWD-AREASECT)     DELETE PATTERN            79204
REPPAT   DC    AL1(PSWFREP),AL3(0),AL1(L'PSWDSN),AL3(PSWDSN-AREASECT)
         DC    A(PSWPSWD-AREASECT,0,0),AL1(PSWTRW)               79204
         DC    AL3(PSWPSWD-AREASECT),AL1(L'PSWDATA-3)            79204
         DC    AL3(PSWDATA-AREASECT+3)                           79204
DELPARM  DC    H'6',CL6'DELETE'  DELETE PARM FIELD
         SPACE 2
CPUTABLE DC    C'  * '
         DC    C' MVS'                                           83220
         DC    C' SVS'                                           83220
         DC    C' NON'                                           83220
CPUTABLN DC    C' ZAP'
         DC    C' ZAP'
         SPACE
DELHEAD  FDOPT NL,CC=C'#'    TITLE WITH DATE/TIME/PAGE NUMBERS   82010
         FDPRT 'WYLBUR PASSWORD DELETION AND FIXUP',RADJ,LEN=50  82010
         FDTM  FLAG,FGREAL,BO=DELHEADE                           82010
         FDPRT 'TEST',PAD    ONLY IF TEST MODE                   82010
DELHEADE FDPRT *END                                              82010
         SPACE 1                                                 82010
LISTHEAD FDOPT NL,CC=C'#'                                        82010
         FDTM  FLAG,FGUSER,BZ=LISTHEAP                           82010
         FDPRT 'ACCOUNT',PAD                                     82010
         FDPRT CURRACCT,4                                        82010
LISTHEAP FDPRT 'WYLBUR PASSWORD LIST',RADJ,LEN=55                82010
LISTHEAX FDPRT *END                                              82010
         SPACE 1                                                 82010
LUSTHEAD FDOPT NL,CC=C'#'                                        82010
         FDTM  FLAG,FGUSER,BZ=LUSTHEAP                           82010
         FDPRT 'ACCOUNT',PAD                                     82010
         FDPRT CURRACCT,4                                        82010
LUSTHEAP FDPRT 'WYLBUR PASSWORD LIST     SORTED BY USER ID',RADJ,LEN=60
         FDPRT *END                                              82010
         SPACE 1                                                 82010
SUBHEAD  VCON  ' ',END=SUBHEADE                                  82010
         DC    CL4' '        FOR FLAG                            79204
         DC    CL9'ACCOUNT'                                      79204
         DC    CL9' USERID'                                      79204
         DC    CL7'ACCESS'                                       79204
         DC    CL7'CHANGED'                                      79204
         DC    CL9' SUB-SYS'                                     79204
         DC    CL4'CPU'                                          79204
         DC    CL4'PRT'                                          79204
         DC    CL4'PUN'                                          79204
         DC    CL4'IND'                                          79204
         DC    CL7' SAVE'                                        79204
         DC    CL30' AND ACCESS VOLUMES'                         79204
         DC    CL30'EXTRA ACCOUNTS'                              79204
         VCON  ,                                                 82010
         SPACE 1
EOFMSG   FDOPT NL,CC=C'-'    TRIPLE-SPACE                        82010
         FDPRT '***  END OF PASSWORDS  ***'                      82010
         FDPRT TOTAL,I,RADJ,LEN=10                               82010
         FDPRT 'ENTRIES LISTED',PADL                             82010
         FDTM  FLAG,FGUSER,BO=EOFMSGND  SKIP REST IF IN USER MODE
         FDPRT TOTALMAX,I,RADJ,LEN=10                            82010
         FDPRT 'ENTRIES MAXIMUM',PADL                            82010
         FDCLC TOTALFIX,ZEROES,4,BE=EOFMSGNF                     82010
         FDPRT TOTALFIX,I,RADJ,LEN=10                            82010
         FDPRT 'CORRECTED',PADL                                  82010
EOFMSGNF FDCLC TOTALDEL,ZEROES,4,BE=EOFMSGND                     82010
         FDPRT TOTALDEL,I,RADJ,LEN=10                            82010
         FDPRT 'DELETED',PADL                                    82010
EOFMSGND FDPRT *END                                              82010
         SPACE 1                                                 82010
INVACVOL FDPRT ' **** DUPLICATE OR UNDEFINED VOLUME :',NL,PADR   82010
         FDPRT INVSVOL,DEB                                       82010
         FDPRT *END                                              82010
REPFAIL  VCON  '************************** REPLACE FAILED'       82010
PROFAIL  VCON  '-**** @PROTECT FAILED / CALL SYSTEMS ****'       82010
INVACT   VCON  ' **** INVALID ACCOUNT ****'                      82010
DELFAIL  VCON  ' ************************* DELETE FAILED'        82010
         SPACE 1                                                 82010
INVSACCT FDPRT '*** INVALID EXTENDED ACCOUNT :',NL,PADR          82010
         FDPRT INVSACT,DEB                                       82010
         FDPRT *END                                              82010
         SPACE 2                                                 82010
SKIPON   EQU   1             SKIP IF FLAG ON                     79204
SKIPOF   EQU   2             SKIP IF FLAG OFF                    79204
SKIP2    EQU   3             SKIP BOTH                           79204
         SPACE 1                                                 82010
OPTTAB   FLG   OK,PSWWOK,SKIPON
         FLG   SYS,PSWWSYS,SKIPOF
         FLG   ACCT,PSWWACCT,SKIPOF
         FLG   OPER,PSWWOPER,SKIPOF
         FLG   FAIL,PSWWFAIL,SKIPOF
         FLG   DTS,PSWWLCL,SKIPOF                                82010
         FLG   TIME,PSWWTIME,SKIPOF
         FLG   SUB,PSWWSUB
OPTHASP  FLG   CHKEY,PSWWNKEY,SKIPOF
         FLG   BATCH,PSWWNBAT,SKIPON
         FLG   POCHK,PSWWNSEQ,SKIPOF                             79204
         FLG   WYCHK,PSWWNWYB+PSWWNSEQ,SKIPOF                    79204
OPTFLAG  FLG   LOGON,PSWWAUA+PSWWAUL   MUST BE FIRST
         FLG   PWMOD,PSWMODP
         FLG   LOG0,PSWWAUL                        MUST BE LAST
OPTEND   EQU   *
         SPACE 1                                                 79204
BLANKS   DC    CL38' '                                           82010
ZEROES   DC    XL4'0'                                            82010
         EJECT ,
SAVE     DSECT ,             WORK/SAVE AREA                      82010
DB       DS    0D'0'         ALIAS FOR DOUBLE                    79204
DOUBLE   DC    D'0'
BOTLIST  DC    A(0)          START OF PASSWORD ENTRIES           79204
TOPLIST  DC    A(0)           END OF ENTRIES                     79204
CURDATE  DC    A(0)          ONE YEAR AGO                        79204
SIXDATE  DC    A(0)          SIX MONTHS                          79204
         SPACE
@IGPVOLT DC    A(0)                                              82010
TMCBXLE  DS    A(*,8,*-8)    BXLE POINTERS FOR DISK VOLSERS      82010
@SERVICE DS    A                                                 82010
@PRINTER DS    A                                                 82010
COMPCODE DS    F             PROGRAM COMPLETION CODE             82010
TOTAL    DS    F             NUMBER OF WYLBUR ENTRIES            82010
TOTALMAX DS    F             NUMBER OF ENTRIES IN PASSWORD DATASET
TOTALFIX DS    F             NUMBER OF ENTRIES FIXED             82010
TOTALDEL DS    F             NUMBER OF ENTRIES DELETED           82010
@PROTECT DS    A             ADDRESS OF PHONEY PROTECT CODE      82010
PROTUCB  DC    F'0'          SAVE AREA FOR UCB ADDR. FROM UCBLOOP
         DC    A(0)          DITTO
         DC    A(PROTLIST)   PROTECT FUNCTION PARAMETER LIST     81068
PROTLIST DC    AL1(*-*),AL3(PSWDATA-AREASECT)                    79204
         DC    AL1(PSWWYLL),AL3(PSWWYLB-AREASECT)                79204
         DC    A(PSWPSWD-AREASECT,0,0)                           79204
         DC    AL1(PSWTRW),AL3(PSWCPSW-AREASECT)                 79204
         DC    AL1(77),AL3(PSWWPRIV-AREASECT)                    79204
SORTPARM SERVSORT DSECT=NO   EXPAND SORT PARAMETER AREA          82024
         SPACE 1                                                 82010
CURRACCT DS    CL8           CURRENT JOB'S ACCOUNT NUMBER        82010
ACCTPRIV DS    X             ACCOUNT ASSOCIATED PRIVILEGE FLAGS  82010
FLAG     DC    X'0'          PROCESSING FLAG                     79204
FGREAL   EQU   X'80'         PERFORM DELETES                     79204
FGUSER   EQU   X'40'         PROGRAM SUBMIITED BY UNPRIVILEGED USER
FFIX     EQU   X'01'         REPLACE BAD ENTRY                   79204
FSUB     EQU   X'02'         REPLACE BAD SUBACCOUNT              79204
FVOL     EQU   X'04'         REPLACE BAD ACCESS VOLUME           79204
INVSACT  DS    CL30                                              82010
INVSVOL  DS    CL38                                              82010
         SPACE 2
DATA1    DC    Y(137,0),C' '                                     79204
BDATA1   DC    0CL132' '                                         79204
DSPFLAG  DC    CL3' '        FLAG FOR UNDERUSED ENTRY            79204
         DC    C' '                                              79204
DSPACCT  DC    CL8' '
         DC    C' '                                              79204
DSPUSER  DC    CL8' '
         DC    C' '                                              79204
DSPDATA  DC    C'YY.DDD'
         DC    C' '                                              79204
DSPDATC  DC    C'YY.DDD'
         DC    C' '                                              79204
DSPSUBSY DC    CL8' '
         DC    C' '                                              79204
DSPCPU   DC    CL3' '
         DC    C' '                                              79204
DSPRPRT  DC    CL3' '
         DC    C' '                                              79204
DSPRPUN  DC    CL3' '
         DC    C' '                                              79204
DSPHIX   DC    CL3' '        USER DSN HIGH-LEVEL INDEX           81068
         DC    C' '                                              79204
DSPSAVEV DC    CL6' '
         DC    C' '                                              79204
DSPAVOL  DC    4CL6' ',CL5' '  VOLUME ACCESS CL5'VOLSE,'...
         DC    C' '                                              79204
DSPAUX   DC    6CL5' '       ACCT,ACCT,...
         ORG   BDATA1+132                                        79204
DATA2    DC    Y(137,0),C' '                                     79204
BDATA2   DC    0CL132' '                                         79204
         DC    CL4' '        FLAG ON LINE ABOVE                  79204
         DC    CL4' '                                            79204
DSPPASSW DC    CL8' '
         DC    C' '                                              79204
DSPTTR   DC    CL6' '        TTR OF ENTRY                        79204
         DC    CL3' '                                            79204
DSPPRIV  DC    CL121' '      USERS PRIVILEGES                    81068
         DC    CL40' '       FOR OVERRUN OF FLAGS                79204
SAVEND   EQU   *                                                 82010
         SPACE 2                                                 82010
         PRINT &PRTMAC                                           82010
         PSWSECT WYL=YES                                         82010
         ORG   PSWDSN-8      START OF MAPPING FOR @PROTECT ENTRY 79204
AREASECT DS    0XL8          CCHHR/FLAGS                         79204
         DS    CL5           CCHHR                               79204
AREATTR  DS    XL3           TTR OF ENTRY                        79204
         ORG   ,             BUMP AHEAD                          81068
         DS    0D            ALIGN                               79204
PLEN     EQU   *-AREASECT    LENGTH OF ONE ENTRY                 79204
         SPACE 2                                                 82010
USERVLST USERVOLT ,                                              82010
         EJECT ,
         PRINT &PRTSYS                                           82010
CVTDSECT DSECT  ,                                                82010
         CVT   DSECT=YES                                         82010
         SPACE 1                                                 82010
         IHACDE ,                                                82010
         SPACE 1                                                 82010
         IKJTCB ,                                                82010
         SPACE 1                                                 82010
         IEZJSCB ,                                               82010
         SPACE 1                                                 89296
USERCVT  USERCVT ,                                               89296
USERVOLS USERVOLS ,                                              89296
         END   ,
