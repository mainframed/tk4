RLSE     TITLE 'R E L E A S E  ***  RELEASE UNUSED DISK SPACE'   81221
         COPY  OPTIONGB                                          81221
         SPACE 2                                                 81221
         SYSPARM LIST=YES                                        81221
         SPACE 2                                                 81221
         PUNCH '  ALIAS PORLSE,PSRLSE,PXRLSE '                  GP06314
         PUNCH '   ORDER RELEASE(P) '       EASIER DUMPS        GP06314
         EJECT ,                                                 81221
         PRINT &PRTSOR                                           81221
RELEASE  PGMHEAD ZERO12,ENTRY=(PORLSE,PSRLSE,PXRLSE),AM=24,RM=24
*                                                                81062
*        AUTHOR :  M. RADZYMSKI
*          MINOR CHANGES :  G. POSTPISCHIL             81062 AND 81221
*          G.P.:  DCB CONVERTED FROM QSAM TO EXCP, RECFM/BLKSIZE 84150
*                 CHECKING REMOVED.  PXRLSE ENTRY ADDED.         84150
*                                                                81062
*        THIS PROGRAM RELEASES UNUSED DISK SPACE IN SEQUENTIAL   81062
*        DATASETS.  IT IS USED FOLLOWING AN FDRDSF RESTORE       81062
*        OR SIMILAR PROCESS WHERE THE DATASET IS OVERALLOCATED.  81062
*                                                                81062
*        THE SYSUT1/SYSLIB DD CARD MUST SPECIFY DISP=MOD; OTHERWISE
*        ALL DATA WOULD BE RELEASED.                             81062
*        THIS VERSION FORCES DISP=MOD.                           81221
*                                                                81221
*        ENTRY POINT PORLSE MAY BE USED TO RELEASE ALL UNUSED    81221
*        SPACE IN A PARTITIONED DATASET.  SHOULD NOT BE USED     81221
*        FOR SYSTEM DATASETS.                                    81221
*                                                                84150
*        ENTRY POINT PXRLSE IS USED TO FREE UNUSED EXTENTS ONLY. 84150
*        UNUSED TRACKS IN THE LAST EXTENT KEPT ARE NOT FREED.    84150
*        USED FOR PERMANENTLY ALLOCATED DATASETS THAT TEMPORARILY
*        EXCEED THEIR ALLOCATION.              G.P.              84150
*        6/86 - MVS RELEASE CHANGES CYLINDER ALLOCATION TO TRACKS;
*              NEW UPDATE=INPLACE OPTION OF VOLREAD USED TO FIX. 86177
*                                                                84150
*        ATTRIBUTES: RENT, REFR, REUS, PAGE, AC=0                84150
         SPACE 1
         SERVINIT ,          LOAD AND INITIALIZE SERVICE         86177
         SERVCALL LPALD,=CL8'@VOLREAD'  LOAD THE VOLUME READER   86177
         ST    R0,@VOLREAD   STASH IT                            86177
         MVC   SYSLIB(SYSLIBL),PATSYS  MOVE DCB                  81221
         LA    R8,SYSLIB                                         81221
         USING IHADCB,R8     DECLARE DCB                         81221
         LA    R1,EXLST      GET EXIT LIST                       81221
         STCM  R1,7,DCBEXLSA   SET EXIT LIST ADDRESS             81221
         LA    R0,INFMJFCB   GET JFCB ADDRESS                    81221
         ST    R0,EXLST      MAKE EXIT LIST                      81221
         MVI   EXLST,X'87'   LAST/JFCB                           81221
         LA    R1,SYSLIB                                         81221
         ST    R1,OPLIST     MAKE OPEN LIST                      81221
         MVI   OPLIST,X'80'  SET END LIST BIT                    81221
         LA    R1,PORLSE     GET ENTRY POINT FOR PDS SPACE RELEASE
         L     R15,4(,R13)   GET OLD SAVE AREA                   81221
         CLM   R1,7,SAVE15+1-SAVE(R15)  PORLSE ENTRY ?           81221
         BNE   *+8           NO                                  81221
         MVI   FLAG,FGPO     SET FOR PO RELEASE                  81221
         LA    R1,PXRLSE     EXTENT RELEASE ?                    84150
         CLM   R1,7,SAVE15+1-SAVE(R15)  PXRLSE ?                 84150
         BNE   NOTPXRL       NO                                  84150
         MVI   FLAG,FGPO+FGPX  SET EXTENT RELEASE                84150
         TESTAUTH FCTN=1     AUTHORIZED CALLER ?                 86177
         BXLE  R15,R15,NOTPXRL   YES                             86177
         SERVICE APFON       SET AUTHORIZATION ON                86177
         OI    FLAG,FGAUTH   REMEMBER TO TURN IT OFF             86177
         SPACE 1                                                 86177
*        INITIALIZATION COMPLETE - CHECK FOR DASD DATASET, DD, ETC.
*                                                                86177
NOTPXRL  DEVTYPE DCBDDNAM,DB SYSLIB DD PRESENT ?                 81221
         BXLE  R15,R15,HAVELIB                                   81062
         MVC   DCBDDNAM,=CL8'SYSUT1'  USE OLD NAME               81221
         DEVTYPE DCBDDNAM,DB  SYSUT1 DD PRESENT ?                84150
HAVELIB  CLI   DB+2,UCB3DACC DIRECT ACCESS DATASET ?             84150
         BNE   EXIT8         NO; THIS FAILS FOR TAPE ?           84218
         RDJFCB MF=(E,OPLIST)  GET THE SYSLIB JFCB               84150
         BXH   R15,R15,EXIT12  QUIT IF NOT SUPPLIED              84218
         TM    JFCBIND2,JFCDISP   DISP=MOD BIT ON                81221
         BNM   DISPNMOD      NO                                  81221
         TM    JFCBIND2,JFCMOD   DISP= MOD ?                     81221
         BNZ   DISPMOD       YES; OK                             81221
DISPNMOD WTO   'SYSUT1 DISP NOT MOD; MOD FORCED',ROUTCDE=11      81221
         NI    JFCBIND2,255-JFCDISP                              81221
         OI    JFCBIND2,JFCMOD   SET MOD                         81221
DISPMOD  OI    JFCBTSDM,JFCNWRIT+JFCNDCB  NO REWRITE             81221
         OI    JFCBIND1,JFCRLSE  RELEASE STORAGE                 81062
         VOLREAD OPEN,JFCBVOLS,MODE=UPDATE OPEN FOR PXRLSE UPD.  86177
         BXH   R15,R15,EXIT8                                     86177
         VOLREAD DSCB        GET THE NEXT DSCB                   86177
         BXH   R15,R15,EXIT8                                     86177
         LR    R1,R0         SWITCH DSCB ADDRESS                 86177
         MVC   FMT4DSN,0(R1)   SAVE 44X'04'                      86177
         LA    R1,DS1FMTID-DS1DSNAM(,R1)  F4 MAP NO NAME         86177
         USING IECSDSL4,R1   DECLARE FORMAT 4                    86177
         CLI   DS4IDFMT,C'4'   REALLY A FOUR ?                   86177
         BNE   EXIT8         NO; FAIL                            86177
         MVC   TRKPCYL,DS4DEVSZ+2  SAVE TRACKS PER CYLINDER      86177
         DROP  R1                                                86177
         VOLREAD SEARCH,JFCBDSNM  LOOK FOR THE USER'S DATASET    86177
         BXH   R15,R15,EXIT8    TOO BAD                          86177
         LR    R1,R0         COPY RETURNED ADDRESS               86177
         MVC   DS1DSNAM(LENDS1),0(R1)  COPY DSCB AND ADDRESS     86177
         BXH   R15,R15,EXIT8                                     84218
         TM    DS1DSORG,JFCORGPS+JFCORGPO  PS OR PO ?            81221
         BNM   EXIT8         NO; IGNORE                          84218
         TM    DS1DSORG,255-JFCORGPS-JFCORGPO-JFCORGU  ANY OTHERS ?
         BNZ   EXIT8         YES; SKIP                           84218
         CLI   DS1DSORG+1,0  ANY FUNNIES ?                       81062
         BNE   EXIT8                                             84218
*        TM    DS1RECFM,X'E0'  ANY RECFM ?                       84150
*        BZ    EXIT8         NO                                  84150
*        OC    DS1BLKL,DS1BLKL  ANY BLOCKSIZE ?                  84150
*        BZ    EXIT8         NO; SKIP                            84150
         SPACE 1                                                 86177
*        VERIFICATION COMPLETE. NOW OPEN DATASET, FAKE A WRITE AT THE
*        LSTAR LOCATION, AND CLOSE. THE SYSTEM CLOSE PROCESSOR WILL
*        ACT ON THE JFCB RELEASE FLAG AND RELEASE THE UNUSED SPACE.
*                                                                86177
         MVC   OLDLSTAR(L'DS1LSTAR),DS1LSTAR  SAVE CURRENT TTR   84150
         MVC   OLDTKBAL,DS1TRBAL   AND OLD TRACK BALANCE         84150
         TM    DS1DSORG,JFCORGPS   SEQUENTIAL ?                  81221
         BNZ   DOOPEN        YES; RELEASE                        81221
         OC    DS1LSTAR(7),DS1LSTAR  INITIALIZED ? (OR PDS/E)   GP09186
         BZ    EXIT4         NO; SKIP                           GP03028
         TM    DS1SMSFG,DS1PDSE   IS THIS A PDSE? (JUST TO BE SURE)
         BO    EXIT4         YES, DO NOT PROCESS                GP03028
         TM    FLAG,FGPO     PDS RELEASE ENABLED ?               81221
         BZ    EXIT4         NO; SKIP                            84218
DOOPEN   OPEN  (,OUTPUT),MF=(E,OPLIST),TYPE=J                    81221
         TM    DCBOFLGS,DCBOFOPN  OPEN ?                         86177
         BZ    EXIT8         NO; TOO BAD                         86177
         TM    FLAG,FGPX     EXTENT RELEASE ?                    84150
         BZ    DORLSE        NO; JUST CLOSE                      84218
         SPACE 1                                                 86177
*        FOR EXTENT RELEASE, WE HAVE TO SET THE FAKE LSTAR TO THE END
*        OF THE LAST USED EXTENT, FREE THE SUBSEQUENT ONES BY CLOSING,
*        AND OPEN/CLOSE AGAIN WITH THE TRUE LSTAR.               86177
*                                                                86177
         LH    R9,TRKPCYL    GET TRACKS PER CYLINDER             86177
         BCTR  R9,0          MAKE INTO HIGHEST TRACK IN CYLINDER 86177
         L     R5,DCBDEBAD   GET THE DEB                         84150
         USING DEBBASIC,R5   DECLARE IT                          84150
         SLR   R7,R7                                             84150
         ICM   R7,1,DEBNMEXT GET THE NUMBER OF EXTENTS           84150
         BZ    EXIT4         NONE ?                              84218
         LA    R6,DEBBASND   POINT TO THE DASD EXTENSION         84150
         USING DEBDASD,R6    DECLARE IT                          84150
         SLR   R0,R0         WORK REGISTER                       84150
         SLR   R4,R4                                             84150
         ICM   R4,3,OLDLSTAR GET LAST USED TRACK                 84150
         LA    R4,1(,R4)     CONVERT TO NUMBER OF LAST TRACK     86177
EXTNLOOP ICM   R0,3,DEBNMTRK GET TRACKS IN THIS EXTENT           84150
         CLM   R9,12,DEBSTRHH  START ON CYLINDER BOUNDARY ?      86177
         BNE   NOTCYL        NO                                  86177
         CLM   R9,3,DEBENDHH   ENDS ON CYLINDER BOUNDARY ?       86177
         BNE   NOTCYL                                            86177
         OI    FLAG,FGDSCB   DO DSCB FIX-UP                      86177
NOTCYL   SR    R4,R0         IN THIS EXTENT ?                    86177
         BM    EXTNFUND      YES; AND NOT LAST TRACK             86177
         BZ    DORLSE        YES; AND LAST TRACK IN USE          86177
         LA    R6,16(,R6)    UP TO NEXT EXTENT                   84150
         BCT   R7,EXTNLOOP   TRY AGAIN                           84150
         B     EXIT4         NOTHING TO RELEASE                  86177
         SPACE 1                                                 86177
EXTNFUND BCT   R7,EXTNFUNE   CONTINUE IF NOT LAST EXTENT         86177
         B     EXIT4         ELSE NOTHING TO RELEASE             86177
EXTNFUNE MVC   DCBFDAD+3(4),DEBENDCC  LAST TRACK IN THIS EXTENT  84150
         SLR   R0,R0                                             86177
         IC    R0,DEBNMEXT   GET NUMBER OF EXTENTS BACK          86177
         LA    R7,1(,R7)     CORRECT FOR BCT                     86177
         SR    R0,R7         GET RELATIVE EXTENT NUMBER          86177
         STC   R0,DCBFDAD    STASH EXTENT                        86177
         MVI   DCBFDAD+7,1   SET RECORD NUMBER TO ONE            84150
         XC    DCBTRBAL,DCBTRBAL  CLEAR BALANCE                  84150
         OI    DCBOFLGS,DCBOFLWR  SET WRITE DONE                 84150
         CLOSE MF=(E,OPLIST) CLOSE                               84150
         NI    JFCBIND1,255-JFCRLSE  RESET RELEASE BITS          84150
         OPEN  (,OUTPUT),MF=(E,OPLIST),TYPE=J  RE-OPEN           84150
         TM    DCBOFLGS,DCBOFOPN  OPEN AGAIN ?                   84150
         BZ    EXIT4         NO; TOO BAD                         84218
         SPACE 1                                                 86177
*        COMMON CLOSE. HERE WE SET THE DESIRED LSTAR AND CLOSE THE
*        DATASET, CAUSING UNUSED SPACE TO BE FREED - EXCEPT THAT FOR
*        PXRLSE, WE HAVE ALREADY FREED EVERYTHING WE MEANT TO, AND
*        ONLY NEED TO RESET LSTAR.                               86177
*                                                                86177
DORLSE   L     R1,CVTPTR     GET THE CVT                         84218
         USING CVTMAP,R1     DECLARE IT                          84150
         MVC   ABSCNVT,CVTPCNVT  TTR TO CCHHR CONVERSION ROUTINE 84150
         L     R0,DCBDEBAD   GET THE DEB ADDRESS                 84150
         LA    R1,DB         GET FULL DASD SEEK ADDRESS ON DBW BOUNDARY
         STM   R0,R1,PRLDEB    MAKE PARAMETERS FOR CONVERT       84150
         STM   R9,R13,CONTOSAV   SAVE REGISTERS OVER CONVERT CALL
         LR    R7,R13        SAVE SAVE                           84150
         LM    R15,R2,ABSCNVT   FOR TTR CONVERT                  84150
         BALR  R14,R15       INVOKE TTR TO MBBCCHHR CONVERSION   84150
         LR    R13,R7        RESTORE SAVE                        84150
         LM    R9,R13,CONTOSAV   RESTORE REGS                    84150
         BXH   R15,R15,EXIT4    LEAVE HIGH LSTAR                 84218
         MVC   DCBFDAD(8),DB  MOVE RESULT                        84150
         MVC   DCBTRBAL,OLDTKBAL  RESTORE TRACK BALANCE          84150
         OI    DCBOFLGS,DCBOFLWR  SET WRITE DONE                 84150
         B     EXIT          RETURN WITH CC=0                    84218
         SPACE 1                                                 84150
EXIT4    MVI   RETCC,4                                           84218
         B     EXITERR                                           86177
EXIT8    MVI   RETCC,8                                           84218
         B     EXITERR                                           86177
EXIT12   MVI   RETCC,12                                          84218
EXITERR  XC    DCBFDAD,DCBFDAD   CLEAR LAST WRITE ADDRESS        86177
         XC    DCBTRBAL,DCBTRBAL   AND TRACK BALANCE             86177
         NI    DCBOFLGS,255-DCBOFLWR  TURN WRITE MODE OFF        86177
EXIT     CLOSE MF=(E,OPLIST)                                     84218
         SLR   R7,R7                                             84218
         ICM   R7,1,RETCC    LOAD AND TEST RETURN CODE           86177
         BNZ   EXITTERM      SKIP ON ERROR                       86177
         TM    FLAG,FGDSCB   DSCB FIX-UP REQUIRED ?              86177
         BZ    EXITTERM      NO                                  86177
         SPACE 1                                                 86177
*        FOR PXRLSE UNDER MVS, IBM'S FREE-SPACE ROUTINES SET CYLINDER
*        EXTENT TYPES TO TRACK (TOO LAZY TO CHECK ?). THE EXTENTS ARE
*        CHECKED, AND ANY THAT ARE ON CYLINDER BOUNDARIES ARE FIXED.
*                                                                86177
         VOLREAD SEARCH,FMT4DSN  POSITION TO FORMAT 4 AGAIN      86177
         BXH   R15,R15,EXITFAIL  HUH ?                           86177
         LH    R9,TRKPCYL    GET TRACKS PER CYLINDER             86177
         BCTR  R9,0          MAKE INTO HIGHEST TRACK IN CYLINDER 86177
LOOPDSCB VOLREAD READ        GET A BLOCK                         86177
         BXH   R15,R15,EXITFAIL  TOO BAD                         86177
         LR    R1,R0         SWITCH DSCB ADDRESS                 86177
         CLC   DS1DSNAM(DS1FMTID-DS1DSNAM+1),8(R1)  SAME RECORD ?
         BNE   LOOPDSCB      NO; TRY AGAIN                       86177
         LA    R14,DS1EXT1-DS1DSNAM+8(,R1)   POINT TO FIRST EXTENT
         LA    R15,3         CHECK THREE EXTENTS                 86177
         USING XTWHOLE,R14   DECLARE MY MAPPING FOR IT           86177
CHECKEXT CLI   XTTYPE,XTTTRK   TRACK ALIGNED ?                   86177
         BNE   BUMPEXT       NO                                  86177
         CLM   R9,12,XTLOTRK   CYLINDER BOUNDARY ?               86177
         BNE   BUMPEXT       NO                                  86177
         CLM   R9,3,XTHITRK    STILL CYLINDER BOUNDARY ?         86177
         BNE   BUMPEXT       NO                                  86177
         OI    FLAG,FGUP     UPDATE DSCB                         86177
         MVI   XTTYPE,XTTCYL  SET CYLINDER BOUNDARY              86177
BUMPEXT  LA    R14,XTLEN(,R14)  BUMP TO NEXT ONE                 86177
         BCT   R15,CHECKEXT  GO BACK FOR MORE                    86177
         TM    FLAG,FGUP     UPDATE REQUIRED ?                   86177
         BZ    EXITTERM      NO                                  86177
         VOLREAD UPDATE,(R1) REWRITE RECORD IN R1                86177
EXITFAIL LTR   R7,R15        PASS RETURN CODE                    86177
*DEBUG*  BZ    EXITTERM                                          86177
*DEBUG*  EX    0,*           DEBUG POINT                         86177
EXITTERM TM    FLAG,FGAUTH   DID WE AUTHORIZE ?                  86177
         BZ    EXITWORM      NO                                  86177
         SERVICE APFOF       RESET AUTHORIZATION                 86177
EXITWORM SERVTERM ,          CLOSE AND FREE EVERYTHING           86177
         PGMEXIT RC=(R7)     RETURN                              84218
         EJECT ,                                                 81062
PATSYS   DCB   DDNAME=SYSLIB,DSORG=PS,DEVD=DA,MACRF=(E),EXLST=1  84150
SYSLIBL  EQU   *-PATSYS                                          81221
         SPACE 1                                                 81221
SAVE     DSECT ,             CONTINUE SAVE/WORK AREA DEFINITION  81221
@SERVICE DS    A             @SERVICE ROUTINE                    86177
@VOLREAD DS    A             VTOC READ/WRITE                     86177
DB       DS    D                                                 81221
CONTOSAV DS    5F            TTR/CCHHR CONVERT SAVE AREA         84150
OPLIST   DC    0A(0),X'80',AL3(SYSLIB)  OPEN/CLOSE LIST          81221
EXLST    DC    0A(0),X'87',AL3(INFMJFCB)                         81221
ABSCNVT  DS    A   1/4       TTR TO MBCCHHR CONVERSION ROUTINE   84150
OLDLSTAR DS    F   2/4       TTR BEFORE OPEN                     84150
PRLDEB   DS    A   3/4       ADDRESS OF DEB                      84150
PRLMBB   DS    A   4/4       ADDRESS OF IOBSEEK                  84150
OLDTKBAL DS    XL2           TRACK BALANCE BEFORE OPEN           84150
RETCC    DC    X'00'         RETURN CODE                         84218
FLAG     DC    X'00'                                             81221
FGPO     EQU   X'80'         RELEASE PDS SPACE                   81221
FGPX     EQU   X'40'           RELEASE UNUSED EXTENTS ONLY       84150
FGAUTH   EQU   X'04'           APF FUNNIES                       86177
FGUP     EQU   X'02'           EXTENT DESCRIPTOR WAS CHANGED     86177
FGDSCB   EQU   X'01'           DSCB EXTENT FIX-UP REQUIRED       86177
         SPACE 1                                                 86177
TRKPCYL  DC    H'0'          TRACKS PER CYLINDER                 86177
FMT4DSN  DS    CL44          NAME OF FORMAT 4 DSCB               86177
         IEFJFCBN  ,
         SPACE 1
SYSLIB   DCB   DDNAME=SYSLIB,DSORG=PS,DEVD=DA,MACRF=(E),EXLST=EXLST
         SPACE 1
         IECSDSL1 1          EXPAND FORMAT 1 DSCB                81221
SELFID   DS    XL5           CCHHR OF FORMAT 1 DSCB              86177
LENDS1   EQU   *-DS1DSNAM    LENGTH TO BE MOVED FROM @VOLREAD    86177
         AIF   (&MVSXA).SYSDEF                                  GP04234
DS1FLAG1 EQU   DS1NOBDB+1,1,C'X'  MORE FLAGS
DS1COMPR EQU   X'80'           COMPRESSABLE EXTENDED IF DS1STRP
DS1CPOIT EQU   X'40'           CHECKPOINTED D S
DS1SMSFG EQU   DS1FLAG1+17,1,C'X'  SMS FLAG
DS1SMSDS EQU   X'80'           SMS D S
DS1SMSUC EQU   X'40'           NO BCS ENTRY
DS1REBLK EQU   X'20'           MAY BE REBLOCKED
DS1CRSDB EQU   X'10'           BLKSZ BY DADSM
DS1PDSE  EQU   X'08'           PDS/E
DS1STRP  EQU   X'04'           EXTENDED FORMAT D S
DS1PDSEX EQU   X'02'           HFS D S
DS1DSAE  EQU   X'01'           EXTENDED ATTRIBUTES EXISY
DS1SCEXT EQU   DS1SMSFG+1,3,C'X'  SECONDARY SPACE EXTENSION
DS1SCXTF EQU   DS1SCEXT,1,C'X'  -"- FLAG
DS1SCAVB EQU   X'80'           SCXTV IS AVG BLOCK LEN
DS1SCMB  EQU   X'40'                 IS IN MEGBYTES
DS1SCKB  EQU   X'20'                 IS IN KILOBYTES
DS1SCUB  EQU   X'10'                 IS IN BYTES
DS1SCCP1 EQU   X'08'           SCXTV COMPACTED BY 256
DS1SCCP2 EQU   X'04'                 COMPACTED BY 65536
DS1SCXTV EQU   DS1SCXTF+1,2,C'X'  SEC SPACE EXTNSION VALUE
DS1ORGAM EQU   DS1ACBM         CONSISTENT NAMING - VSAM D S
DS1RECFF EQU   X'80'           RECFM F
DS1RECFV EQU   X'40'           RECFM V
DS1RECFU EQU   X'C0'           RECFM U
DS1RECFT EQU   X'20'           RECFM T   001X XXXX IS D
DS1RECFB EQU   X'10'           RECFM B
DS1RECFS EQU   X'08'           RECFM S
DS1RECFA EQU   X'04'           RECFM A
DS1RECMC EQU   X'02'           RECFM M
*   OPTCD DEFINITIONS   BDAM    W.EFA..R
*                       ISAM    WUMIY.LR
*             BPAM/BSAM/QSAM    WUCHBZTJ
DS1OPTIC EQU   X'80'  FOR DS1ORGAM - CATLG IN ICF CAT
DS1OPTBC EQU   X'40'           ICF CATALOG
DS1RACDF EQU   DS1IND40
DS1SECTY EQU   DS1IND10
DS1WRSEC EQU   DS1IND04
DS1SCAL1 EQU   DS1SCALO,1,C'X'    SEC. ALLOC FLAGS
DS1DSPAC EQU   X'C0'         SPACE REQUEST MASK
DS1CYL   EQU   X'C0'           CYLINDER BOUND
DS1TRK   EQU   X'80'           TRACK
DS1AVRND EQU   X'41'           AVG BLOCK + ROUND
DS1AVR   EQU   X'40'           AVG BLOCK LEN
DS1MSGP  EQU   X'20'
DS1EXT   EQU   X'10'           SEC. EXTENSION EXISTS
DS1CONTG EQU   X'08'           REQ. CONTIGUOUS
DS1MXIG  EQU   X'04'           MAX
DS1ALX   EQU   X'02'           ALX
DS1DSABS EQU   X'00'           ABSOLUTE TRACK
DS1SCAL3 EQU   DS1SCAL1+1,3,C'X'  SEC ALLOC QUANTITY
.SYSDEF  SPACE 1
SAVEEND  EQU   *             END OF GETMAINED WORK AREA          81221
         SPACE
CVTDSECT DSECT ,                                                 84150
         CVT   DSECT=YES                                         84150
         IHACDE ,                                                94234
         DCBD DEVD=DA,DSORG=PS                                   81221
         SPACE 1                                                 84150
         IEZDEB ,                                                84150
         IEFUCBOB ,                                              84150
         MAPEXTNT ,          EXPAND EXTENT MAPPING               86177
         IECSDSL1 4          MAP THE VTOC                        86177
         END
