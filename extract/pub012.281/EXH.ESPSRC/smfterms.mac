TERM     TITLE 'S M F T E R M S  ***  SMF LINE/TERMINAL/ETC. USAGE'
         MACRO ,                                                GP12066
&NM      FIXDATE &ITEM                                          GP12066
&NM      MACPARM R15,&ITEM                                      GP12066
         MACPARM R14,FIXDATE,OP=BAL                             GP12066
         MEND  ,                                                GP12066
         SPACE 1
         MACRO ,                                                 87026
&NM      TYPE  &BIN,&ADR                                         87026
&NM      DC    Y(&BIN,&ADR-SMFTERMS)                             87026
         MEND  ,                                                 87026
         SPACE 2                                                 87026
         COPY  OPTIONGB                                          81200
         SPACE 1                                                 81200
         SYSPARM LIST=YES                                        81200
         SPACE 1                                                 81200
***********************************************************************
*                                                                     *
*        COPYRIGHT 1981  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR                                           81200
SMFTERMS PGMHEAD ZERO31,BIGWORK,PARM=R9,BASE=(R11,R12)          GP12066
         SERVINIT ,          INITIALIZE SERVICE ROUTINE          81200
         SERVLOAD @PRINTER   LOAD THE PRINT ROUTINE             GP12066
         PRTOPEN SYSPRINT,OPT=(ABEND)  ABEND IF NO SYSPRINT DD   81200
         SPACE 1                                                 81200
         LA    R14,LENBXLE                                       81200
         LA    R15,FIRSTONE-LENBXLE                              81200
         LR    R0,R15                                            81200
         L     R1,=A(LASTONE-SAVE)                               81200
         AR    R1,R13        ADDRESS OF LAST ENTRY               81200
         STM   R14,R1,CURBXLE  SET BXLE POINTERS                 81200
         LA    R14,FIRSTSNA  GET BXLE FOR SNA SUMMARY            87190
         LA    R15,SECNDSNA-FIRSTSNA  LENGTH OF ONE ENTRY        87190
         LA    R0,LASTSNA    LAST ENTRY                          87190
         STM   R14,R0,SNABXLE  SET SNA POINTERS                  87190
         MVC   PSTART,=X'2099366C'  INITIALIZE START DATE       GP12066
         MVI   PEND+L'PEND-1,X'0C'  AND END DATE                 81200
         SPACE 1                                                 87026
         MVI   CPUCURR,C' '                                      87026
         MVC   CPUCURR+1(L'CPUCURR-1),CPUCURR  BLANK CPU IDS     87026
         MVC   CPUIDS,CPUCURR  DITTO                             87026
         LA    R14,CPUIDS    SET FOR DEFAULT LIST                87026
         LA    R15,4         SET LENGTH OF ONE ENTRY             87026
         LR    R0,R14        SET FOR LAST ENTRY                  87026
         STM   R14,R0,CPUDFLT  SET DEFAULT BXLE                  87026
         STM   R14,R0,CPUBXLE  SET CURRENT ENTRY                 87026
         LA    R1,0(,R9)     WAS THERE A PARM POINTER ?          87026
         LTR   R1,R1                                             87026
         BZ    NOPARM        NO                                  87026
         ICM   R1,7,1(R1)    DOES IT POINT TO ANYTHING ?         87026
         BZ    NOPARM                                            87026
         SLR   R2,R2                                             87026
         ICM   R2,3,0(R1)    VALID LENGTH ?                      87026
         BNP   NOPARM        NO                                  87026
         SRL   R2,2                                              87026
         SLL   R2,2          DIVISIBLE BY 4 ?                    87026
         CH    R2,0(R1)                                          87026
         BE    MOVEPARM      YES; MOVE PARM                      87026
         PRTF  '**** INVALID PARM ****',CC=NO                    87026
         SERVTERM ,                                              87026
         ABEND 16                                                87026
         SPACE 1                                                 87026
PARMMVC  MVC   CPUIDS(0),2(R1)  EXECUTED MOVE                    87026
MOVEPARM CLC   =C'VTAM',2(R1)  REQUEST FOR SNA DETAIL ?          87100
         BE    MOVEPANS      YES                                 87100
         CLC   =C'SOME',2(R1)  SOME SNA RECORDS ONLY ?           87225
         BNE   PSNADET       NO; CHECK FOR DETAIL                87225
         OI    FLAG,SNAFUS   SELECT SOME SNA RECORDS             87225
         B     MOVEPANS      SET MAIN SNA FLAG                   87225
PSNADET  CLC   =C'SNA',2(R1)   DITTO (SNAD, SNAG...)             87100
         BNE   MOVEPART      NO; MOVE EVERYTHING                 87100
         OI    FLAG,SNAFUL   SET FOR DETAIL PRINTING             87190
MOVEPANS OI    FLAG,SNAFU    SET SNA DETAIL RECORDS              87100
         CH    R2,=H'4'      REMOVE PARM                         87100
         BNP   NOPARM        NOTHING ELSE                        87100
MOVEPART BCTR  R2,0          SET LENGTH FOR EXECUTE              87026
         EX    R2,PARMMVC    MOVE PARM                           87026
         LM    R14,R0,CPUDFLT  GET DEFAULTS                      87026
         LA    R0,1(R14,R2)  GET LAST ADDRESS                    87026
         SR    R0,R15        GET LAST ENTRY                      87026
         ST    R0,CPUDFLT+8                                      87026
         ST    R0,CPUBXLE+8  DITTO                               87026
         SPACE 1                                                 81200
NOPARM   SERVCALL LPALD,=CL8'@INPREAD'                           87026
         ST    R0,@INPREAD                                       81200
         INPOPEN SYSIN,OPT=(DUMMY,NOWTO)                         81200
         BXH   R15,R15,SYNEOJ     SKIP IF DUMMY OR MISSING       81200
         XC    PARMSYS(PSTART-PARMSYS),PARMSYS  CLEAR            81200
SYNLOOP  INPGET ,            GET A CARD                          81200
         LR    R7,R1         COPY CARD ADDRESS                   81200
         LR    R9,R1         ALSO COPY FOR PROCESSING            90178
         LA    R7,71(,R1)    SET LAST CARD COLUMN                81200
         LA    R6,1          MAKE BXLE INCREMENT                 81200
         CLC   =C';CPU=',0(R9)  CPU ID CARD ?                    90178
         BE    SYNYCPU       YES; PROCESS IT                     85272
         CLC   =C'*CPU=',0(R9)  ALTERNATE FORM ?                 90178
         BNE   SYNNCPU       NO                                  81200
SYNYCPU  MVC   CPUBXLE(12),CPUDFLT  RESET FOR DEFAULT CPUS       87026
         MVI   PARMSYS,C' '  RESET SKIP FLAG                     87026
         CLI   5(R9),C'A'    ANY CPU ID SPECIFIED ?              90178
         BL    SYNLOOP       NO; GET ANOTHER CARD                87026
         MVC   CPUCURR,CPUIDS   MAKE DEFAULT LIST                87026
         LA    R3,CPUCURR                                        87026
         LA    R4,4                                              87026
         LA    R5,CPUCURR-4                                      87026
SYNCNCPU LM    R15,R1,CPUBXLE  GET DEFAULT CPU LIST              90178
         ICM   R14,15,5(R9)   LOAD CPU NAME                      90178
SYNCTEST CLM   R14,15,0(R15)   MATCHING NAME ?                   90178
         BE    SYNCADD       YES; ACCEPT                         87026
         BXLE  R15,R0,SYNCTEST  TRY AGAIN                        90178
         LA    R9,3(,R9)     PARTIAL SKIP                        90178
         BXLE  R9,R6,SYNCNCPU  SEE IF MORE                       90178
SYNCPUT  CR    R3,R5         DID WE FIND A MATCH ?               87026
         STM   R3,R5,CPUBXLE STASH ANYWAY                        87026
         BNH   SYNLOOP       YES; PROCEED                        87026
         MVI   PARMSYS,0     SET SKIP FLAG                       87026
         B     SYNLOOP                                           87026
SYNCADD  LA    R15,4(,R5)    SET NEW END ADDRESS                 87026
         LA    R2,CPUCURR+L'CPUCURR-4                            87026
         CR    R15,R2        WILL IT FIT ?                       87026
         BH    SYNCPUT       NO; JUST STASH                      87026
         STCM  R14,15,0(R15)  STASH THE NEW ID                   90178
         LR    R5,R15        SET NEW END POINTER                 87026
         B     SYNCPUT                                           87026
SYNNCPU  CLI   PARMSYS,0     SYSTEM GOTTEN ?                     87026
         BNH   SYNLOOP       NO; SKIP                            81200
         MVC   PARMNAME,BLANKS                                   81200
         MVC   PARMUCB,BLANKS                                    81200
         CLC   DCLINE,0(R9)  HASP LINE ?                         90178
         BE    SYNLINE       YES                                 81200
         CLC   =C'RMT',0(R9)  HASP REMOTE ?                      90178
         BE    SYNRMT        YES                                 81200
         CLC   =C'NET',0(R9)  WYLBUR LINE ?                      90178
         BNE   SYNLOOP       NO; GET ANOTHER                     81200
         LR    R2,R9                                             90178
SYNWYL   BXH   R2,R6,SYNLOOP   SKIP IF NO LINE                   81200
         CLC   =C'TERM=',0(R2)                                   81200
         BE    SYNWYLN                                           81200
         CLC   =C'LINE=',0(R2)                                   81200
         BNE   SYNWYL                                            81200
SYNWYLN  LA    R14,PARMNAME                                      81200
         LA    R15,5(,R2)                                        81200
         LA    R0,8                                              81200
SYNWYLL  CLI   0(R15),C' '                                       81200
         BE    SYNWYLU                                           81200
         MVC   0(1,R14),0(R15)                                   81200
         LA    R14,1(,R14)                                       81200
         LA    R15,1(,R15)                                       81200
         BCT   R0,SYNWYLL                                        81200
SYNWYLU  LR    R2,R9                                             90178
         MVC   PARMUCB,BLANKS                                    81200
SYNWYLUC BXH   R2,R6,SYNWYLUD                                    81200
         CLC   =C'UCB=',0(R2)                                    81200
         BNE   SYNWYLUC                                          81200
         MVC   PARMUCB+&MVSXA.(3+&MVSXA),4(R2)                  GP12066
SYNWYLUD BAL   R8,ADDUNDAT   BUILD BASIC ENTRY                   87026
         B     SYNLOOP       DO NEXT                             81200
         SPACE 1                                                 85272
SYNLINE  LA    R15,6(,R9)    POINT TO EARLIEST POSSIBLE START    90178
SYNLINTS CLC   =C'UNIT=',0(R15)  NEW-FANGLED (JES) CARD ?        85272
         BNE   SYNLINUP      NO; BUMP                            85272
         MVC   PARMUCB,5(R15)  COPY UCB NAME                     85272
         LA    R14,PARMNAME  GET DESTINATION                     85272
         LR    R15,R9        GET SOURCE                          90178
         LA    R0,8          SET MAXIMUM TO DO                   85272
SYNLINLP CLI   0(R15),C' '   TRAILING BLANK ?                    85272
         BE    SYNHASP       YES; ADD TO TABLE                   85272
         MVC   0(1,R14),0(R15)  MOVE A BYTE                      85272
         LA    R14,1(,R14)                                       85272
         LA    R15,1(,R15)                                       85272
         BCT   R0,SYNLINLP   TRY AGAIN                           85272
         B     SYNHASP       BIG SYSTEM                          85272
SYNLINUP BXLE  R15,R6,SYNLINTS TEST AGAIN                        85272
         SPACE 1                                                 85272
         CLI   13(R9),C'-'   DISABLED ?                          90178
         BE    SYNLOOP       YES; SKIP                           81200
         MVC   PARMNAME(6),0(R9)  IDENTIFY                       90178
         MVC   PARMUCB,7(R9)                                     90178
         CLI   PARMNAME+4,C'0'  LEADING ZERO ?                   81200
         BNE   SYNHASP       NO                                  81200
         MVC   PARMNAME+4(2),PARMNAME+5  EDIT                    81200
         B     SYNHASP                                           81200
SYNRMT   CLI   21(R9),C'-'   DISABLED ?                          90178
         BE    SYNLOOP       YES; SKIP                           81200
         MVC   PARMNAME(6),0(R9)  IDENTIFY                       90178
         CLI   PARMNAME+3,C'0'                                   87026
         BNE   SYNHASP                                           87026
         MVC   PARMNAME+3(3),PARMNAME+4                          87026
         CLI   PARMNAME+3,C'0'                                   81200
         BNE   SYNHASP                                           81200
         MVC   PARMNAME+3(3),PARMNAME+4                          87026
SYNHASP  BAL   R8,ADDUNDAT   BUILD BASIC ENTRY                   87026
         B     SYNLOOP                                           81200
         SPACE 1                                                 81200
SYNEOJ   SERVCALL LPADL,=CL8'@INPREAD'  CLOSE AND FREE           81200
         SPACE 1                                                 81200
         OPEN  (SMF,(INPUT,REREAD))                              81200
         TM    SMF+DCBOFLGS-IHADCB,DCBOFOPN  OPEN ?              81200
         BNZ   GOPREP                                            87100
CRASH    ABEND 666           INVALID JCL SETUP
         SPACE 1                                                 87100
GOPREP   TM    FLAG,SNAFU    SNA DETAIL PRINTING ?               87100
         BZ    GOGET         NO                                  87100
         PRTLIST SHEAD,TITLE=1  PRINT HEADER                     87100
         PRTLIST SSUBHEAD,TITLE=3  AND SUB-HEADER                87100
         SPACE 1
GOGET    GET   SMF           GET NEXT RECORD
         USING TIVSMF,R1     DECLARE COMMON SMF MAPPING
         MVC   PARMUCB,BLANKS  MAKE IT CLEAN                     85274
         LA    R14,4         SET INCREMENT                       87026
         LA    R15,TYPETABX  POINT TO LAST ENTRY                 87026
         LA    R2,TYPETAB    POINT TO FIRST ENTRY                87026
         IC    R0,5(,R1)     INSERT RECORD TYPE                  87026
GETLOOP  CLM   R0,1,1(R2)    MATCHING TYPE ?                     87026
         BE    GETGO         YES; PROCESS                        87026
         BXLE  R2,R14,GETLOOP  TRY AGAIN                         87026
         B     GOGET         ELSE SKIP THIS ONE                  87026
GETGO    LH    R15,2(,R2)    LOAD CODE OFFSET                    87026
         B     SMFTERMS(R15)   INVOKE IT                         87026
         SPACE 1                                                 87026
TYPETAB  TYPE  34,TIMESHAR   TSO OR WYLBUR STEP RECORD ?         87026
         TYPE  6,JESPRINT    JES (REMOTE) PRINT-OUT              87026
         TYPE  26,JESTERM    JES PURGE                           87026
         TYPE  47,JESSIGN    JES SIGNON/SIGNOFF                  87026
         AIF   ('&LOCAL' NE 'PID').NOLOTYP                       93012
         TYPE  VACREC#,VTACC   VTAM (LOCAL) ACCOUNTING RECORD    92058
.NOLOTYP TYPE  SEACSMF#,SEAC   COMTEN SEAC RECORD                87026
         TYPE  53,JESSNA     JES SNA LOGOFF                      87026
         TYPE  50,SNATUNE    SNA TUNING                          87100
TYPETABX EQU   *-4,4,C'C'    LAST TABLE ENTRY ADDRESS            87026
         SPACE 1                                                 87026
TIMESHAR MVC   PARMSYS,TIVCPUID   BUILD PARM LIST FOR ADD/UPDATE 87026
         MVC   PARMNAME,TIVINVNM                                 81200
         MVC   PARMUSER,TIVUID                                   81200
         MVC   PARMTGET,TIVINCT                                  81200
         MVC   PARMTPUT,TIVOUTCT                                 81200
         MVC   PARMDLOW,TIVONDTE                                GP12066
         MVC   PARMTLOW,TIVONTME                                 90178
         MVC   PARMDHI,TIVRCDTE                                 GP12066
         MVC   PARMTHI,TIVRCDTS                                  90178
         FIXDATE PARMDLOW                                       GP12066
         FIXDATE PARMDHI                                        GP12066
         BAL   R9,ADDDATED   ADD/UPDATE DATED ENTRY              81200
         B     GOGET         DO ANOTHER RECORD                   81200
         SPACE 1                                                 81200
JESSIGN  TM    SMF47EVT+1,5  SNA LOGON/BSC SIGNON ?              87026
         BNM   GOGET         NO; SKIP THIS RECORD                87026
         MVC   PARMSYS,SMF47SID   MAKE ADD/UPDATE PARM ENTRY     87026
         MVC   PARMNAME,SMF47LIN                                 81200
         XC    PARMTGET(8),PARMTGET   CLEAR COUNTS               81200
         MVC   PARMDLOW,SMF47DTE                                GP12066
         MVC   PARMTLOW,SMF47TME                                 90178
         MVC   PARMDHI,SMF47DTE                                 GP12066
         MVC   PARMTHI,SMF47TME                                  90178
         FIXDATE PARMDLOW                                       GP12066
         FIXDATE PARMDHI                                        GP12066
         MVC   PARMUSER,SMF47RMT       REMOTE NUMBER             81200
         BAL   R9,ADDDATED   ADD THE ENTRY                       81200
         MVC   PARMUCB,BLANKS                                    87026
         MVC   PARMUSER,SMF47LIN                                 81200
         MVC   PARMNAME,SMF47RMT                                 81200
         BAL   R9,ADDDATED                                       81200
         B     GOGET         GET ANOTHER                         81200
         SPACE 1                                                 81200
JESPRINT CLI   SMF6LEN+1,SMF6RTE+2-SMFRCD6  LONG ENOUGH FOR HASP ?
         BL    GOGET         NO; SKIP                            81200
         MVC   PARMTGET,ZEROES                                   81200
         MVC   PARMTPUT,SMF6NLR                                  81200
         MVC   PARMSYS,SMF6SID                                   81200
         MVC   PARMDLOW,SMF6WSD    WRITER START DATE            GP12066
         MVC   PARMTLOW,SMF6WST                                  90178
         MVC   PARMDHI,SMF6DTE     WRITER END TIME              GP12066
         MVC   PARMTHI,SMF6TME                                   90178
         FIXDATE PARMDLOW                                       GP12066
         FIXDATE PARMDHI                                        GP12066
         LA    R15,SMF6OUT   POINT TO DEVICE NAME                87295
         BAL   R14,BUILDRMT  CHECK FOR AND BUILD REMOTE NAME     87295
           B     IOADD         ENTRY IS FOR REMOTE               87295
         MVC   PARMUSER,SMF6JBN  COPY JOBNAME AS USER            87295
         BAL   R9,ADDDATED   ADD/UPDATE ENTRY                    87295
         B     GOGET         GET ANOTHER                         87295
         SPACE 1                                                 81200
JESTERM  TM    SMF26IND,(SMF26DES+SMF26ATU)/256  GENERAL+ACTUALS ?
         BNO   GOGET         NO; SKIP                            81200
         MVC   PARMTPUT,ZEROES                                   81200
         MVC   PARMDLOW,SMF26RSD    GET READER START TIME       GP12066
         MVC   PARMTLOW,SMF26RST                                 90178
         MVC   PARMDHI,SMF26RSD    PROVISIONALLY FOR END        GP12066
         MVC   PARMTHI,SMF26RST                                  90178
         LA    R2,SMF26LN1   POINT TO DESCRIPTIVE SECTION        87026
         AH    R2,SMF26LN1   POINT TO NEXT SECTION               87026
         USING SMF26LN2,R2   POINT TO SECOND SECTION             87026
         TM    SMF26IND,SMF26EVT/256  EVENT SECTION PRESENT ?    87026
         BZ    JESTERM3      NO                                  81200
         OC    SMF26RSD,SMF26RSD  ANY END DATE ?                 90182
         BNZ   JESTERM2      NO; USE START                       88292
         MVC   PARMDHI,SMF26RPD    SET READER STOP              GP12066
         MVC   PARMTHI,SMF26RPT                                  90178
JESTERM2 AH    R2,SMF26LN2   ADD SECTION LENGTH                  87026
         USING SMF26LN3,R2   DECLARE IT                          87026
JESTERM3 MVC   PARMTGET,SMF26ICD   GET INPUT COUNT               87026
         MVC   PARMSYS,SMF26RID  INPUT PROCESSOR ID              87026
         DROP  R2                                                87026
         LA    R15,SMF26DEV  POINT TO INPUT DEVICE NAME          87026
         BAL   R14,BUILDRMT  CHECK FOR AND BUILD REMOTE NAME     87295
           B     IOADD         ENTRY IS FOR REMOTE               87295
         MVC   PARMUSER,SMF26JBN  COPY JOBNAME AS USER           87295
         FIXDATE PARMDLOW                                       GP12066
         FIXDATE PARMDHI                                        GP12066
         BAL   R9,ADDDATED   ADD/UPDATE ENTRY                    87295
         B     GOGET         GET ANOTHER                         87295
         SPACE 1                                                 81200
IOADD    LM    R2,R4,CURBXLE                                     81200
IOADDLOP BXH   R4,R2,GOGET   SKIP IF NOT FOUND                   81200
         USING TERMINAL,R4                                       87026
         CLC   PARMSYS(12),TERMSYS  MATCHING ENTRY ?             87026
         BNE   IOADDLOP      NO                                  81200
         LM    R14,R15,PARMTGET   GET COUNTS                     81200
         ICM   R0,15,TERMTGET                                    81200
         AR    R14,R0                                            81200
         ICM   R0,15,TERMTPUT                                    81200
         AR    R15,R0                                            81200
         STM   R14,R15,TERMTGET        (0C6 ON 360)              81200
         CP    PARMDLOW,PSTART                                   87026
         BNL   *+10                                              87026
         ZAP   PSTART,PARMDLOW                                   87026
         CP    PARMDHI,PEND                                      87026
         BNH   *+10                                              87026
         ZAP   PEND,PARMDHI                                      87026
         CP    PARMDLOW,TERMDLOW                                 87026
         BNL   *+10                                              87026
         ZAP   TERMDLOW,PARMDLOW                                 87026
         CP    PARMDHI,TERMDHI                                   87026
         BNH   *+10                                              87026
         ZAP   TERMDHI,PARMDHI                                   87026
         B     GOGET                                             81200
         DROP  R4                                                87026
         SPACE 1                                                 87026
JESSNA   LH    R2,SMFPROFF   GET OFFSET TO PRODUCT SECTION       87026
         AR    R2,R1         RELOCATE                            87026
         USING SMFSUBID,R2   DECLARE IT                          87026
         CLC   =H'1',SMFSUBID  LOGOFF RECORD ?                   87026
         BNE   GOGET         NO; SKIP $PLNE RECORDS              87026
         LH    R2,SMFIDOFF   GET OFFSET TO ID SECTION            87026
         AR    R2,R1                                             87026
         USING SMF53RMT,R2   DECLARE IT                          87026
         MVC   PARMSYS,SMFHDSID  SYSTEM NAME                     87026
         MVC   PARMNAME,SMF53LIN  LINE NAME                      87026
         MVC   PARMDLOW,SMFHDDTE    TIME ON/OFF                 GP12066
         MVC   PARMTLOW,SMFHDTME                                 90178
         MVC   PARMDHI,SMFHDDTE        DITTO                    GP12066
         MVC   PARMTLOW,SMFHDTME                                 90178
         FIXDATE PARMDLOW                                       GP12066
         FIXDATE PARMDHI                                        GP12066
         MVC   PARMUSER,SMF53RMT  REMOTE NUMBER                  87026
         ICM   R14,15,SMF53CTR  NUMBER OF VTAM REQUESTS          87026
         LR    R15,R14       COPY                                87026
         SRA   R15,1         HALVE                               87026
         SR    R14,R15       GET REMAINDER                       87026
         STM   R14,R15,PARMTGET  SPLIT BETWEEN I AND O           87026
         BAL   R9,ADDDATED   ADD IN                              87026
         MVC   PARMUCB,BLANKS                                    87026
         XC    PARMUSER,PARMNAME  FLIP LINE                      87026
         XC    PARMNAME,PARMUSER    WITH                         87026
         XC    PARMUSER,PARMNAME      REMOTE                     87026
         BAL   R9,ADDDATED   ADD IT IN                           87026
         B     GOGET         GET ANOTHER                         87026
         DROP  R2                                                87026
         SPACE 1                                                 87026
BUILDRMT MVC   DB,0(R15)     SAVE NAME IN WORK AREA              87291
         CLI   0(R15),C'R'   REMOTE DEVICE ?                     87026
         BNE   BUILDRBD      DEFINITELY NOT                      87291
         MVC   PARMNAME,=CL8'RMT'                                87026
         LA    R2,8          MAXIMUM TO SCAN                     87026
         ICM   R0,15,BLANKS  CLEAR REMOTE NUMBER                 87026
BUILDRML CLI   0(R15),C'0'   REACHED A DIGIT YET ?               87026
         BH    BUILDRMI      YES                                 87026
         LA    R15,1(,R15)                                       87026
         BCT   R2,BUILDRML   TRY AGAIN                           87026
BUILDRBD MVC   PARMNAME,DB   ELSE USE (VTAM ?) NAME AS IS        87291
         B     4(,R14)       AND RETURN TO CALLER AS NON-RMT     87291
BUILDRMI SLL   R0,8                                              87026
         IC    R0,0(,R15)    MAKE NUMBER                         87026
         CLI   1(R15),C'0'   CHECK NEXT DIGIT                    87026
         BL    BUILDRMP      CHECK FOR END                       87026
         LA    R15,1(,R15)                                       87026
         BCT   R2,BUILDRMI   TRY AGAIN                           87026
         B     BUILDRBD      SKIP IT                             87291
BUILDRMP CLI   1(R15),C'.'   PROPER TERMINATION ?                87026
         BNE   BUILDRBD      NO                                  87291
BUILDRMN CLM   R0,8,BLANKS   LEADING BLANK ?                     87026
         BNE   BUILDRMS      NO; STORE                           87026
         SLL   R0,8          LEFT-ADJUST                         87026
         IC    R0,BLANKS     MAKE TRAILING BLANK                 87026
         B     BUILDRMN      TRY AGAIN                           87026
BUILDRMS STCM  R0,15,PARMNAME+3  MAKE RMTNN                      87026
         BR    R14           RETURN TO CALLER                    87026
         SPACE 1                                                 85272
*        CHECK FOR RECORDS PRODUCED BY THE NCR/COMTEN SEAC (STATISTICS
*              AND EXTENDED ACCESS CONTROL) CODE.                85272
*>>>>>* NOTE THAT THE CURRENT SEAC (RECORD V1) HAS TIME FIELDS  *<<<<<*
*>>>>>*   IN UNITS OF 1/1000 SECONDS (NOT 1/100) !!!!!          *<<<<<*
*                                                                85272
SEAC     CLI   SEACRTYP,SEACRTSR  IS IT A RECORD I KNOW ABOUT ?  85272
         BH    GOGET         NO; IGNORE                          85272
         MVC   PARMSYS,=C'COMT'  IDENTIFY COMTEN CPU             87026
         MVC   PARMNAME,SEACLSET  LINE GROUP                     85272
         MVC   PARMUCB+1(2),SEACTLN  COMTEN LINE NUMBER          85274
         MVI   PARMUCB,0     JUST IN CASE                        85274
         MVC   PARMUSER,SEACUSID                                 85272
         MVC   PARMTGET,SEACRDCT  READ/INHIBIT COUNT             85272
         MVC   PARMTPUT,SEACWTCT   WRITE COUNT                   85272
         MVC   PARMUSER,SEACTILS  SHOW SUB-SYSTEM                85273
         MVC   PARMDLOW,SEACIDAT                                GP12066
         MVC   PARMTLOW,SEACITIM                                 90178
         MVC   PARMDHI,SEACTDAT                                 GP12066
         MVC   PARMTHI,SEACTTIM                                  90178
         MVC   PARMTHI,SMF26RPT                                  90178
         FIXDATE PARMDLOW                                       GP12066
         FIXDATE PARMDHI                                        GP12066
         SLR   R14,R14                                           90182
         L     R15,PARMTLOW  GET START TIME                      90182
         D     R14,=F'10'    MAKE IBM UNITS                      90182
         ST    R15,PARMTLOW                                      90182
         SLR   R14,R14                                           90182
         L     R15,PARMTHI   GET END TIME                        90182
         D     R14,=F'10'                                        90182
         ST    R15,PARMTHI                                       90182
         CLI   SEACRTYP,SEACRTSR  NORMAL RECORD ?                90178
         BNH   SEACADD       YES                                 90178
         XC    PARMTLOW,PARMTLOW  CLEAR RESIDENCE TIME           90178
         XC    PARMTHI,PARMTHI                                   90178
SEACADD  BAL   R9,ADDDATED   ADD THE RECORD                      85272
         B     GOGET         PROCESS NEXT RECORD                 85272
         AIF   ('&LOCAL' NE 'PID').NOLOCOD                       93012
         SPACE 1                                                 92058
*    PROCESS DATA FROM LOCAL VTAM ACCOUNTING EXIT (SEE ISTAUCAG  92058
*      IN THIS LIBRARY)                                          92058
         SPACE 1                                                 87026
         PUSH USING          SAVE ASSIGNMENTS                    92058
VTACC    TM    FLAG,SNAFU    PROCESS VTAM RECORDS ?              92058
         BZ    GOGET         NO; SKIP THIS RECORD                92058
         LR    R7,R1         PRESERVE SMF ADDRESS                92058
         DROP  R1                                                92058
         USING VACRDW,R7     DECLARE OURS                        92058
         LA    R8,VACONOFF   SET EXPECTED ADDRESS                92058
         USING VACONOFF,R8                                       92058
         CLI   VACRDW+1,VACRLEN  EXPECTED LENGTH ?               92058
         BNL   *+8           YES                                 92058
         SH    R8,=H'4'      ADJUST FOR INCORRECT ACCOUNT FIELD  92058
         MVC   PARMSYS,VACSID     BUILD PARM LIST FOR ADD/UPDATE 92058
         MVC   PARMSYS(3),=C'VAP'   KILL CPU TYPE; KEEP INDEX BYTE
         MVC   PARMNAME,VACAPPL  MAKE ENTRY FOR APPLICATION USE  92058
         MVC   PARMUSER,VACTERM                                  92058
         XC    PARMTGET(8),PARMTGET  NO I/O COUNTS               92058
         MVC   PARMDLOW,VACDTE                                  GP12066
         MVC   PARMTLOW,VACTME                                   92058
         MVC   PARMDHI,VACDTE                                   GP12066
         MVC   PARMTHI,VACTME                                    92058
         FIXDATE PARMDLOW                                       GP12066
         FIXDATE PARMDHI                                        GP12066
         TM    FLAG,SNAFUL   PRINT DETAIL INFORMATION ?          92058
         BZ    VTACCSUM      NO                                  92058
         CLI   VACONOFF,C'0' LOGON RECORD ?                      92058
         BNE   VTACCOFF      NO                                  92058
         BAL   R9,ADDUNIQE   ADD/UPDATE DATED ENTRY              92058
         MVC   PARMNAME,VACTERM                                  92058
         MVC   PARMUSER,VACAPPL                                  92058
         MVC   PARMSYS(3),=C'VTM'   KILL CPU TYPE; KEEP INDEX BYTE
         BAL   R9,ADDUNIQE   ADD/UPDATE DATED ENTRY              92058
         B     GOGET         DO ANOTHER RECORD                   92058
VTACCOFF BAL   R9,FINDPARM   SEE IF PRIOR ENTRY                  92058
         B     VTACCSKP      NO; ERROR ?                         92058
         USING TERMINAL,R4   DECLARE LOCATED ENTRY               92058
         MVC   PARMDLOW,TERMDHI   SET START OF SESSION           92058
         MVC   PARMTLOW,TERMTHI                                  92058
         FIXDATE PARMDLOW                                       GP12066
         BAL   R9,ADDUNIQE   MAKE CONNECT TIME                   92058
         INC   TERMSESS,INC=-1  BUT FIX SESSION COUNT            92058
VTACCSKP MVC   PARMNAME,VACTERM                                  92058
         MVC   PARMUSER,VACAPPL                                  92058
         MVC   PARMSYS(3),=C'VTM'   KILL CPU TYPE; KEEP INDEX BYTE
         BAL   R9,FINDPARM   SEE IF PRIOR ENTRY                  92058
         B     GOGET         NO; ERROR ?                         92058
         MVC   PARMDLOW,TERMDHI   SET START OF SESSION           92058
         MVC   PARMTLOW,TERMTHI                                  92058
         FIXDATE PARMDLOW                                       GP12066
         FIXDATE PARMDHI                                        GP12066
         MVC   PARMSYS(3),=C'VTM'   KILL CPU TYPE; KEEP INDEX BYTE
         BAL   R9,ADDUNIQE   MAKE CONNECT TIME                   92058
         INC   TERMSESS,INC=-1  BUT FIX SESSION COUNT            92058
         B     GOGET         FINISHED ?                          92058
         SPACE 1                                                 92058
VTACCSUM BAL   R9,ADDDATED   ADD THE ENTRY                       92058
         BAL   R9,VTACCFIX   ADJUST COUNT IF LOGOFF RECORD       92058
         MVC   PARMNAME,VACTERM                                  92058
         MVC   PARMUSER,VACAPPL                                  92058
         MVC   PARMSYS(3),=C'VTM'  INDICATE TERMINAL ENTRY       92058
         CLI   VACONOFF,C'0'  LOGON RECORD ?                     92058
         BNE   VTACCSOF      NO                                  92058
         BAL   R9,ADDDATED   ADD THE ENTRY                       92058
         B     GOGET         AND GET ANOTHER                     92058
VTACCSOF BAL   R9,FINDPARM   FIND PRIOR ENTRY                    92058
         B     GOGET         NONE ?                              92058
         MVC   PARMDLOW,TERMDHI                                  92058
         MVC   PARMTLOW,TERMTHI                                  92058
         FIXDATE PARMDLOW                                       GP12066
         FIXDATE PARMDHI                                        GP12066
         BAL   R9,ADDDATED   ADD                                 92058
         INC   TERMSESS,INC=-1  BUT FIX SESSION COUNT            92058
         B     GOGET                                             92058
         SPACE 1                                                 92058
VTACCFIX LTR   R4,R4         ANY RECORD FOUND ?                  92058
         BZR   R9            NO; RETURN                          92058
         LR    R1,R7         RECOVER RECORD ADDRESS              92058
         CLI   VACONOFF,C'0'   LOGON ?                           92058
         BER   R9            YES; SESSION COUNTS                 92058
         INC   TERMSESS,INC=-1  FIX SESSION COUNT                92058
         BR    R9                                                92058
         SPACE 1                                                 92058
FINDPARM LM    R2,R4,CURBXLE GET SCAN POINTERS                   92058
         LA    R14,FINDPARL  SET TO COMPARE ONLY TERMID          92058
         TM    FLAG,SNAFUL   DOING DETAIL ?                      92058
         BZR   R14           NO; START LOOP                      92058
         BALR  R14,0         SET LOOP FOR EVERYTHING             92058
FINDPARN CLC   PARMUSER,TERMUSER  SAME USER ?                    92058
         BNE   FINDPART      NO                                  92058
FINDPARL CLC   PARMNAME,TERMNAME  SAME ENTRY ?                   92058
         BNE   FINDPART      NO                                  92058
         CLC   PARMSYS,TERMSYS  SAME ENTRY ?                     92058
         BNE   FINDPART      NO                                  92058
         CP    PARMDHI,TERMDHI  SAME OR LATER DAY ?              92058
         BH    4(,R9)        LATER; PROCESS                      92058
         BLR   R9            EARLIER - NO GOOD                   92058
         CLC   PARMTHI,TERMTHI  LATER TIME THAT DAY ?            92058
         BNL   4(,R9)        YES                                 92058
         BR    R9            NO                                  92058
FINDPART BXLE  R4,R2,0(R14)  TRY AGAIN                           92058
         BR    R9                                                92058
         POP   USING         RESTORE REGISTER ASSIGNMENTS        92058
.NOLOCOD SPACE 1                                                 87100
*        PROCESS SNA TUNING STATISTICS                           87100
*          PRINT DETAIL RECORDS IF PARM=VTAM OR PARM=SNAD        87100
*                                                                87100
SNATUNE  LR    R2,R1         PRESERVE SMF RECORD ADDRESS         87100
         DROP  R1                                                87100
         USING SMFRCD50,R2                                       87100
         TM    FLAG,SNAFU    DETAIL RECORDS TO BE PRINTED ?      87100
         BZ    SNASUM        NO; JUST SUMMARIZE THEM             87100
         LM    R15,R1,SNABXLE  GET SNA POINTERS                  87190
         USING SNASECT,R15                                       87225
SNAFLOOP ICM   R9,15,SNACPU  GET PRIOR SYSTEM ID                 87225
         BZ    SNAFADD       NONE; ADD AN ENTRY                  87190
         CLM   R9,15,SMF50SID  MATCHING SYSTEM ?                 87190
         BNE   SNAFBUMP      NO                                  87190
         CLC   SMF50NME,SNATERM MATCHING DEVICE ?                87225
         BE    SNAFSUM       YES; UP COUNTERS                    87190
SNAFBUMP BXLE  R15,R0,SNAFLOOP  TRY ALL ENTRIES                  87225
         B     SNAPRT        TABLE FULL                          87190
SNAFADD  MVC   SNACPU,SMF50SID  COPY CPU ID                      87225
         MVC   SNATERM,SMF50NME  AND DEVICE NUMBER               87225
SNAFSUM  LA    R0,4          SET INCREMENT                       87190
         LA    R1,SNASLOW-SNADUMP   SET LAST OFFSET              87225
         SLR   R9,R9         SET FIRST OFFSET                    87190
SNAFSUMS LA    R3,SMF50DLR(R9)  POINT TO NEXT ENTRY              90178
         ICM   R3,15,0(R3)   LOAD VALUE                          90178
         A     R3,SNADUMP(R9)  GET INDEX OLD TOTAL               90178
         ST    R3,SNADUMP(R9)  STASH NEW TOTAL                   87225
         BXLE  R9,R0,SNAFSUMS  DO FOR ALL                        87190
         INC   SNASESS,WORK=R1   COUNT NUMBER OF RECORDS         87225
         DROP  R15                                               87225
SNAPRT   TM    FLAG,SNAFUL+SNAFUS   DETAIL PRINT REQUESTED ?     87225
         BZ    SNASUM        NO; JUST SUMMARIZE                  87190
         TM    FLAG,SNAFUS   SELECTED PRINTING ?                 87225
         BZ    SNAPRT50      NO; PRINT ALL                       87225
         ICM   R1,15,SMF50SLD   GET SLOWDOWN COUNT               87225
         ICM   R15,15,SMF50DLR   AND DUMP/LOAD COUNT             87225
         AR    R15,R1        TEST TOTAL                          87225
         BNP   SNASUM        NOTHING; SKIP PRINT                 87225
SNAPRT50 PRTLIST MSGSNA50    PRINT DETAIL DATA                   87100
SNASUM   MVC   PARMSYS,SMF50SID  SYSTEM                          87100
         MVC   PARMNAME,SMF50NME  CONTROLLER NAME                87100
         MVC   PARMTGET,SMF50CRD  READ COUNT                     87100
         MVC   PARMTPUT,SMF50CWR  WRITE COUNT                    87100
         MVC   PARMDLOW,SMF50DTE    DATE                        GP12066
         MVC   PARMTLOW,SMF50TME                                 90178
         MVC   PARMDHI,SMF50DTE     TIME                        GP12066
         MVC   PARMTHI,SMF50TME                                  90178
         FIXDATE PARMDLOW                                       GP12066
         FIXDATE PARMDHI                                        GP12066
         LR    R1,R2         RESTORE STANDARD MAPPING            87100
         BAL   R9,ADDDATED   STACK                               87100
         B     GOGET         GET NEXT RECORD                     87100
         SPACE 1                                                 87100
MSGSNA50 FDPRT SMF50SID,NL,RADJ,LEN=5                            87100
         FDPRT SMF50NME,PAD                                      87100
         FDPRT SMF50DTE,DATE,PAD                                 87100
         FDPRT SMF50TME,TIME,PAD                                 87100
         FDPRT SMF50DLR,ICM,RADJ,LEN=10                          87100
         FDPRT SMF50CWR,ICM,RADJ,LEN=10                          87100
         FDPRT SMF50CRD,ICM,RADJ,LEN=10                          87100
         FDPRT SMF50ATN,ICM,RADJ,LEN=10                          87100
         FDPRT SMF50ATR,ICM,RADJ,LEN=10                          87100
         FDPRT SMF50PUI,ICM,RADJ,LEN=10                          87100
         FDPRT SMF50PUO,ICM,RADJ,LEN=10                          87100
         FDPRT SMF50BUF,ICM,RADJ,LEN=10                          87100
         FDPRT SMF50SLD,ICM,RADJ,LEN=10                          87100
         FDPRT *END                                              87100
         DROP  R2                                                87100
         USING TIVSMF,R1                                         87100
         EJECT ,                                                 87026
ADDUNDAT NI    FLAG,255-DODATE  USE NO DATES                     81200
         LM    R5,R7,CPUBXLE GET CPU ID SCAN                     87026
ADDSETUP MVC   PARMSYS,0(R5) MOVE CPU ID                         87026
         BAL   R9,ADDCOMON   ADD THE ENTRY                       87026
         BXLE  R5,R6,ADDSETUP  REPEAT FOR NEXT ONE               87026
         BR    R8            RETURN TO CALLER                    87026
         SPACE 1                                                 81200
ADDUNIQE OI    FLAG,DONIQUE  ALSO KEEP USER FIELD UNIQUE         92058
ADDDATED OI    FLAG,DODATE   SET DATES FOR ENTRY                 81200
ADDCOMON LM    R2,R4,CURBXLE                                     81200
         B     TERMBUMP                                          81200
         USING TERMINAL,R4   DECLARE MAPPING                     81200
TERMLOOP CLC   TERMSYS,PARMSYS   SAME CPU ?                      81200
         BNE   TERMBUMP      NO                                  81200
         CLC   TERMNAME,PARMNAME  SAME TERMINAL ?                81200
         BNE   TERMBUMP      NO                                  85272
         TM    FLAG,DONIQUE  UNIQUE USER ID ?                    92058
         BZ    TERMSUID      NO                                  92058
         CLC   TERMUSER,PARMUSER  SAME USER ?                    92058
         BNE   TERMBUMP      NO; SKIP                            92058
TERMSUID CLC   TERMUCB,PARMUCB  SAME LINE ?                      85272
         BE    TERMFUND      YES; UPDATE IT                      81200
         CLC   TERMUCB,BLANKS  ANY UCB IN THERE ?                87026
         BE    TERMFUND      NO; ACCEPT THIS AS A MATCH          87026
         CLC   PARMUCB,BLANKS  REQUEST FOR BLANK ?               87026
         BNE   TERMBUMP      NO; DO NEXT                         87026
         MVC   PARMUCB,TERMUCB  FAKE IT                          87026
         B     TERMFUND      AND PROCESS                         87026
TERMBUMP BXLE  R4,R2,TERMLOOP  TRY ANOTHER ENTRY                 81200
         C     R4,TOPBXLE    SEE IF REALLY AVAILABLE             81200
         BNH   TERMINIT      YES; INITIALIZE IT                  81200
         OI    FLAG,OFLOW    SET OVERFLOW FLAG                   81200
         SLR   R4,R4         SIGNAL NO RECORD                    92058
         BR    R9            GET ANOTHER RECORD                  81200
TERMINIT ST    R4,CURBXLE+4  SET NEW TOP LIMIT                   81200
         MVC   TERMSYS,PARMSYS   SET CPU ID                      81200
         MVC   TERMNAME,PARMNAME  SET TERMINAL NAME              81200
         MVC   TERMUSER,PARMUSER  SET USER ID                    81200
         MVC   TERMUCB,PARMUCB                                   81200
         ZAP   TERMDLOW,=P'2099366'                             GP12066
         ZAP   TERMDHI,=P'0'                                     90178
TERMFUND ICM   R0,15,PARMTGET  GET TGET COUNT                    81200
         A     R0,TERMTGET                                       81200
         ST    R0,TERMTGET                                       81200
         ICM   R0,R15,PARMTPUT  GET TPUT COUNT                   81200
         A     R0,TERMTPUT                                       81200
         ST    R0,TERMTPUT                                       81200
         TM    FLAG,DODATE                                       81200
         BZ    ADDSKDAT      NO; RETURN                          81200
         NI    FLAG,255-DODATE  RESET DATE FLAG                  92058
         LA    R0,1                                              81200
         A     R0,TERMSESS   ADD ONE TO SESSION COUNT            81200
         ST    R0,TERMSESS                                       81200
         CP    PARMDLOW,PSTART                                   81200
         BNL   *+10                                              81200
         ZAP   PSTART,PARMDLOW                                   81200
         CP    PARMDHI,PEND                                      81200
         BNH   *+10                                              81200
         ZAP   PEND,PARMDHI                                      81200
         CP    PARMDLOW,TERMDLOW                                 81200
         BNL   *+10                                              81200
         ZAP   TERMDLOW,PARMDLOW                                 81200
         CP    PARMDHI,TERMDHI                                   81200
         BNH   *+10                                              81200
         ZAP   TERMDHI,PARMDHI                                   81200
         MVC   TERMTHI,PARMTHI  SAVE TIME OFF                    92058
*        CALCULATE 'RESIDENCE' OR 'CONNECT' TIME (CRUDELY).      90178
*        THIS CODE ASSUMES THAT ALL DATES ARE IN 8X.YYY/9X/AX FORM
*        TOTALS WILL NOT EXCEED 3 MONTHS OF DATES                90178
         ZAP   DB,PARMDHI    COPY END DATE                       90178
         BAL   R14,BINDATE   CONVERT TO BINARY DAYS SINCE 1900   90178
         LR    R3,R15        SAVE                                90178
         ZAP   DB,PARMDLOW   COPY START DATE                     90178
         BAL   R14,BINDATE   CONVERT TO BINARY                   90178
         SR    R3,R15        GET DAY RANGE                       90178
         BM    ADDSKDAT      BAD INPUT ?                         90178
         M     R2,=A(24*60*60*100)  MAKE INTO 1/100THS SECONDS   90178
         A     R3,PARMTHI    PLUS END TIME                       90178
         S     R3,PARMTLOW   LESS START                          90178
         BM    ADDSKDAT      BAD INPUT ?                         90178
         AL    R3,TERMCON    ADD PRIOR CONNECT TIME              90178
         ST    R3,TERMCON    STASH IT BACK                       90178
ADDSKDAT TM    FLAG,DONIQUE  ADD UNIQUE ENTRY ?                  92058
         BNZ   ADDUQUSR      YES                                 92058
         CLC   PARMUSER,TERMUSER  SAME USER ?                    81200
         BER   R9            YES; LEAVE IT IN                    81200
         CLI   TERMUSER,0      EVER SET ?                        81200
         BNE   ADDSKUSR      YES; BLANK IT                       81200
ADDUQUSR NI    FLAG,255-DONIQUE  RESET                           92058
         MVC   TERMUSER,PARMUSER  ELSE SAVE THIS ONE             81200
         BR    R9            RETURN                              81200
ADDSKUSR MVC   TERMUSER,BLANKS  ELSE RESET IT                    81200
         BR    R9                                                81200
         SPACE 1                                                 90178
BINDATE  STM   R0,R3,DATESAVE  SAVE USER'S REGISTERS             90178
         SLR   R0,R0         INSERT CLEAR                        90178
         IC    R0,DB+5       INSERT YEAR                         90178
         SRDL  R0,4          PUT DECADES IN R0                   90178
         SRL   R1,28         AND UNITS IN R1                     90178
         SLL   R0,1          DECADES*2                           90178
         AR    R1,R0         ADD IN                              90178
         SLL   R0,2          DECADES*2*4                         90178
         AR    R1,R0         = UNITS + (2+8)*DECADES             90178
         XC    DB(6),DB      CLEAR YEAR                          90178
         LA    R1,6(,R1)     ADD FINAGLE FACTOR                  90178
         LA    R3,1(,R1)     SAVE YEAR + SECOND FINAGLE          90178
         SRA   R3,2          GET NUMBER OF LEAP YEARS SINCE 1900 90178
         MH    R1,=H'365'    DAYS SINCE 1900                     90178
         AR    R1,R3         ADD LEAP DAYS                       90178
         CVB   R0,DB         GET DAYS                            90178
         AR    R1,R0         ADD DAY IN THIS YEAR                90178
         LR    R15,R1        PASS BACK TO USER                   90178
         LM    R0,R3,DATESAVE  RESTORE USER'S REGISTERS          90178
         BR    R14           RETURN                              90178
         EJECT ,
         DROP  R4
         USING TERMINAL,R6
SMFEOF   CLOSE (SMF,LEAVE)                                       81200
         TM    FLAG,SNAFU    VTAM STATISTICS ?                   87190
         BZ    SMFEOFNA      NO                                  87190
         LM    R3,R5,SNABXLE GET POINTERS                        87190
         USING SNASECT,R3                                        87225
         ICM   R9,15,SNACPU  ANY ENTRIES AT ALL ?                87225
         BZ    SMFEOFNA      NO; SKIP                            87190
         TM    FLAG,SNAFUL+SNAFUS   DID WE DO DETAIL RECORDS ?   87225
         BZ    PRTSNA        NO; NO SPACER NEEDED                87190
         PRTSPACE 2          TRIPLE-SPACE                        87190
         PRTROOM 5           AT LEAST 2 LINES ?                  87190
PRTSNA   ICM   R9,15,SNACPU  ANY MORE ENTRIES ?                  87225
         BZ    SMFEOFNA      NO; QUIT                            87190
         PRTLIST TOTSNA50                                        87190
         BXLE  R3,R4,PRTSNA                                      87190
         DROP  R3                                                87225
SMFEOFNA PRTLIST VHEAD,TITLE=1  PRINT HEADER                     81200
         PRTLIST VSUBHEAD,TITLE=3                                81200
         LM    R4,R6,CURBXLE   GET TERMINALS AGAIN               81200
         B     RADJBUMP                                          81200
RADJTERM LM    R14,R15,TERMNAME                                  87026
         CLM   R15,1,BLANKS  SHORT ?                             87026
         BNE   RADJTER8      NO                                  87026
         SRDL  R14,8         RIGHT-ADJUST                        87026
         MVI   TERMNAME+3,C' '  MAKE A BLANK IN-BETWEEN          87026
RADJTER8 LA    R2,3                                              87026
RADJLOOP CLM   R15,1,BLANKS                                      81200
         BNE   RADJDONE                                          81200
         SRL   R15,8          SHIFT TRAILING BLANK OUT           81200
         ICM   R15,8,BLANKS   REPLACE BY LEADING BLANK           81200
         BCT   R2,RADJLOOP                                       81200
RADJDONE STCM  R15,15,TERMNAME+4                                 81200
         TR    TERMNAME+4(4),SORTTRAN  GARBLE IT                 81200
RADJBUMP BXLE  R6,R4,RADJTERM                                    81200
         LM    R4,R6,CURBXLE  GET LENGTH AND LAST ENTRY
         AR    R6,R4         ADDRESS OF FIRST ENTRY              81200
         LR    R2,R6         COPY ADDRESS OF FIRST ENTRY
         CR    R2,R5         COMPARE FIRST TO LAST ADDRESS
         BNL   GOPRINT       NONE TO SORT
         LR    R6,R4         LENGTH OF AN ENTRY
         LR    R7,R5         1ST BXLE
         SR    R5,R4         NEXT TO LAST ENTRY ADDR FOR
SORTBGIN LA    R3,0(R2,R6)   NEXT ENTRY
SORTCLC  CLC   TERMSYS-TERMINAL(15,R2),TERMSYS-TERMINAL(R3)
         BNH   SORTEND
SORTXC   XC    0(LENBXLE,R2),0(R3)
         XC    0(LENBXLE,R3),0(R2)
         XC    0(LENBXLE,R2),0(R3)
SORTEND  BXLE  R3,R6,SORTCLC
         BXLE  R2,R4,SORTBGIN
GOPRINT  LM    R4,R6,CURBXLE
         B     FORMSKIP                                          81200
FORMLOOP CLI   TERMSYS,C' '  PRINTABLE ENTRY ?
         BNH   FORMSKIP      NO
         CLC   OUTSYS,TERMSYS  SAME CPU/SYSTEM ?
         BE    FORMSAME      YES
         MVC   OUTSYS,TERMSYS                                    81200
         PRTLIST VHEAD,TITLE=1  SET NEW TITLE AND PAGE           81200
FORMSAME TR    TERMNAME+4(4),SORTTRAN  MAKE NAME PRINTABLE       81200
         CLI   TERMNAME,C' '  PRINTABLE NAME ?                   81200
         BH    *+10          YES                                 81200
         MVC   TERMNAME,=CL8'*FAILED*'  FAILED SIGN-ONS          81200
         CLI   TERMUCB,0     HEX UCB ENTRY ?                     85274
         BH    FORMNUCB      NO                                  85274
         UNPK  DB(5),TERMUCB+1(3)                                85274
         MVC   TERMUCB,DB+1                                      87026
         TR    TERMUCB,HEXTRAN  MAKE PRINTABLE                   85274
FORMNUCB CLI   TERMUSER,C' '  PRINTABLE NAME ?                   81200
         BNL   FORMPOUT      YES                                 81200
         L     R0,TERMTGET   GET INPUT                           87026
         A     R0,TERMTPUT   AND OUTPUT COUNTS                   87026
         BZ    FORMTBAD      NONE; TOO BAD                       87026
         MVC   TERMUSER,BLANKS  ELSE CLEAR IT                    87026
         B     FORMPOUT                                          87026
FORMTBAD MVC   TERMUSER,=CL8'*FAILED*'  FAILED SIGN-ONS          81200
FORMPOUT L     R1,TERMCON    GET CONNECT TIME                    90178
         SLR   R0,R0                                             90178
         D     R0,=F'100'    CONVERT TO SECONDS                  90178
         SLR   R0,R0         CLEAR REMAINDER                     90184
         LA    R14,60        MAKE NEXT DIVISOR                   90184
         DR    R0,R14        GET SECONDS/MINUTES                 90184
         LR    R3,R0         SAVE SECS                           90184
         SLR   R0,R0                                             90184
         DR    R0,R14        GET MINS/HOURS                      90184
         MH    R1,=H'100'    HOURS*100                           90184
         AR    R1,R0         HOUR/MINUTES                        90184
         MH    R1,=H'100'                                        90184
         AR    R1,R3         HOURS/MINUTES/SECS                  90184
         MVC   CONCONN,=X'4020202020207A21207A2020'  EDIT MASK   90178
         CVD   R1,DB         MAKE PACKED TIME                    90178
         ED    CONCONN,DB+3  MAKE EBCDIC                         90178
         PRTLIST OUTLINE     PRINT OUTPUT RECORD                 81200
FORMSKIP BXLE  R6,R4,FORMLOOP    REPEAT FOR ALL
         SLR   R9,R9         SET RETURN CODE                     81200
         TM    FLAG,OFLOW    TABLE OVERFLOW ?
         BZ    DONEALL       NO; QUIT
         PRTV  OVERFLOW      WARN OF OVERFLOW                    81200
         LA    R9,4          SET RETURN CODE - MINOR ERROR       81200
DONEALL  SERVTERM ,          CLOSE AND FREE EVERYTHING           81200
         PGMEXIT RC=(R9)       RETURN WITH 0 OR 4               GP12066
         SPACE 1
*   FIX UP DATE FIELDS PAST Y2K                                 GP12066
*   FIRST BYTE > 19 - LEAVE AS IS                               GP12066
*   FIRST BYTE >0   - ADD 19                                    GP12066
*   SECOND BYTE <64 - ADD 20                                    GP12066
*                                                               GP12066
FIXDATE  OI    3(R15),X'0F'   MAKE PREFERRED PLUS FOR PRINTING  GP12066
         CLI   0(R15),X'19'   ALREADY SET ?                     GP12066
         BNLR  R14             YES; RETURN                      GP12066
         CLI   0(R15),0       OLD OR NOT SET ?                  GP12066
         BE    FIXWIND         YES; USE WINDOWING               GP12066
FIXD19   AP    0(4,R15),P1900000     SCALE TO PROPER CENTURY    GP12066
         BR    R14                                              GP12066
FIXWIND  CLI   1(R15),X'64'   YOUNGER THAN OS/360 ?             GP12066
         BNL   FIXD19          NO; ASSUME 1964-1999             GP12066
         MVI   0(R15),X'20'   ASSUME 2000-2063                  GP12066
         BR    R14                                              GP12066
P1900000 DC    P'1900000'                                       GP12066
         SPACE 1                                                 87190
         USING SNASECT,R3                                        87225
TOTSNA50 FDPRT SNACPU,NL,RADJ,LEN=5                              87225
         FDPRT SNATERM,PAD                                       87225
         FDPRT SNASESS,I,PAD,RADJ,LEN=9  NUMBER OF RECORDS       87225
         FDPRT 'records',PAD                                     87225
         FDPRT SNADUMP,ICM,RADJ,LEN=10                           87225
         FDPRT SNAWRITE,ICM,RADJ,LEN=10                          87225
         FDPRT SNAREADS,ICM,RADJ,LEN=10                          87225
         FDPRT SNAATTN,ICM,RADJ,LEN=10                           87225
         FDPRT SNAATTRD,ICM,RADJ,LEN=10                          87225
         FDPRT SNAPIUIN,ICM,RADJ,LEN=10                          87225
         FDPRT SNAPIUOT,ICM,RADJ,LEN=10                          87225
         FDPRT SNABUFFS,ICM,RADJ,LEN=10                          87225
         FDPRT SNASLOW,ICM,RADJ,LEN=10                           87225
         FDPRT *END                                              87190
         DROP  R3                                                87225
         EJECT ,
SYSPRINT PRTWORK SYSPRINT,TITLE=5                                81200
SYSIN    INPWORK SYSIN,WIDTH=80,FILL=C' ',EODAD=SYNEOJ           81200
         SPACE 1                                                 81200
OUTLINE  FDPRT ' ',NL                                            81200
         FDPRT TERMSYS,LEN=5   SMCA ID                           81200
         FDPRT TERMUCB,LEN=5                                    GP12066
         FDPRT TERMNAME,4,PADL,DEB  TERMINAL NAME START          81200
         FDPRT TERMNAME+4,4,PADR,DEB  TERMINAL NAME TRAILER      87026
         FDOPT SBA=(*,22)    SKIP TO COUNTS                     GP12066
         FDPRT TERMSESS,I,RADJ,LEN=9  SESSION COUNT              81200
         FDCLC TERMTGET,ZEROES,4,BNE=OUTLINEN  IGNORE SESSION CT 87026
         FDCLC TERMTPUT,ZEROES,4,BNE=OUTLINEN                    87026
         FDCLC TERMSESS,ZEROES,4,BE=OUTLINEX                     81200
OUTLINEN FDCLC TERMNAME,DCLINE,4,BNE=OUTLINET                    81200
         FDPRT ' ',LEN=24                                        81200
         FDGOTO OUTLINEU                                         81200
OUTLINET FDPRT TERMTGET,I,RADJ,LEN=13  INPUT COUNT               81200
         FDPRT TERMTPUT,I,RADJ,LEN=11  OUTPUT COUNT              81200
OUTLINEU FDPRT TERMUSER,PAD,RADJ,LEN=12  USER ID IF ONE ONLY     81200
         FDPRT TERMDLOW,DATE,RADJ,LEN=10                         81200
         FDPRT TERMDHI,DATE,RADJ,LEN=9                           81200
         FDPRT CONCONN,DEB,RADJ,LEN=15                           90178
OUTLINEX FDPRT *END                                              81200
         SPACE 1
VHEAD    FDOPT NL,CC=C'#'    AUTOMATIC DATE/TIME/PAGE OPTION     81278
         FDPRT 'COMTEN, JES AND WYLBUR LINE UTILIZATION ON'      87026
         FDPRT TERMSYS,PAD                                       81200
         FDPRT 'FROM',PAD                                        81200
         FDPRT PSTART,DATE,PAD                                   81200
         FDPRT 'TO',PAD                                          81200
         FDPRT PEND,DATE                                         81200
         FDPRT *END                                              81200
         SPACE 1                                                 81200
VSUBHEAD FDPRT 'CPUID',NL,PAD                                    81200
         FDPRT ' DEV',PAD                                       GP12066
         FDPRT ' TERMINAL ',PAD                                  81200
         FDPRT 'SESSIONS  ',PAD                                  81200
         FDPRT '     READS ',PAD                                 81200
         FDPRT '   WRITES ',PAD                                  81200
         FDPRT '   USER-ID'                                      81200
         FDPRT '       DATE RANGE   ',PAD                        90178
         FDPRT '  CONNECT TIME'                                  90178
         FDPRT *END                                              81200
         SPACE 1                                                 87100
SHEAD    FDOPT NL,CC=C'#'    AUTO PAGE, DATE                     87100
         FDPRT 'SNA TUNING RECORDS',RADJ,LEN=57                  87100
         FDPRT *END                                              87100
         SPACE 1                                                 87100
SSUBHEAD FDPRT 'CPUID',NL                                        87100
         FDPRT 'TERMINAL',PAD                                    87100
         FDPRT 'DATE  ',RADJ,PAD,LEN=8                           87100
         FDPRT 'TIME  ',RADJ,PAD,LEN=8                           87100
         FDPRT 'DMP/LOAD',RADJ,LEN=10                            87100
         FDPRT 'WRITES',RADJ,LEN=10                              87100
         FDPRT 'READS',RADJ,LEN=10                               87100
         FDPRT 'ATTN',RADJ,LEN=10                                87100
         FDPRT 'ATTN/RD',RADJ,LEN=10                             87100
         FDPRT 'PIU IN',RADJ,LEN=10                              87100
         FDPRT 'PIU OUT',RADJ,LEN=10                             87100
         FDPRT 'BUFFERS',RADJ,LEN=10                             87100
         FDPRT 'SLOWDOWN',RADJ,LEN=10                            87100
         FDPRT *END                                              87100
         SPACE 1                                                 87100
BLANKS   DC    CL8' '                                            81200
ZEROES   DC    XL4'0'                                            81200
DCLINE   DC    C'LINE'                                           81200
OVERFLOW VCON  '-********** TABLE OVERFLOW **********'           81200
         SPACE 2
SMF      DCB   DDNAME=SMF,DSORG=PS,MACRF=(GL),EODAD=SMFEOF,            *
               RECFM=VBS,BFTEK=A
         SPACE 1                                                 88293
         LTORG ,                                                 88293
         SPACE 1                                                 81200
SORTTRAN DS    0X            TRANSLATE TABLE FOR HEX/SHORT NAMES 81200
         DC    256AL1(*-SORTTRAN)  DEFAULT - ONE TO ONE          81200
         ORG   SORTTRAN      GARBLED HEX                         81200
         DC    C' 0123456789ABCDEF'                              81200
         ORG   SORTTRAN+C' '   BLANK                             81200
         DC    X'00'                                             81200
         ORG   SORTTRAN+C'A'                                     81200
         DC    X'0B0C0D0E0F10'  GARBLED HEX                      81200
         ORG   SORTTRAN+C'0'                                     81200
         DC    X'0102030405060708090A'                           81200
         ORG   ,                                                 81200
HEXTRAN  EQU   SORTTRAN+1-C'0'  TRANSLATE UNPACKED HEX           85272
         SPACE 1                                                 81200
TERMINAL DSECT ,
TERMSESS DC    F'0'          SESSION COUNT
TERMTGET DC    F'0'          TGET / INPUT COUNT
TERMTPUT DC    F'0'          TPUT / OUTPUT COUNT
TERMCON  DC    F'0'          CONNECT TIME                        90178
TERMTHI  DC    F'0'          LAST TIME USED ON DHI DAY           92058
TERMSYS  DC    CL4' '        CPU ID
TERMNAME DC    CL8' '        TERMINAL NAME
TERMUCB  DC    CL3' '        UCB NAME                            81200
TERMDLOW DC    PL4'0'        LOWEST DATE                        GP12066
TERMDHI  DC    PL4'0'        HIGHEST DATE                       GP12066
TERMUSER DC    CL8' '        NAME OF SOLE USER, OR BLANKS
LENBXLE  EQU   *-TERMINAL
         SPACE 1                                                 87225
SNASECT  DSECT ,             SNA SUMMARY RECORDS                 87225
SNACPU   DS    CL4                                               87225
SNATERM  DS    CL8                                               87225
SNASESS  DS    F             NUMBER OF RECORDS                   87225
SNADUMP  DS    F                                                 87225
SNAWRITE DS    F                                                 87225
SNAREADS DS    F                                                 87225
SNAATTN  DS    F                                                 87225
SNAATTRD DS    F                                                 87225
SNAPIUIN DS    F                                                 87225
SNAPIUOT DS    F                                                 87225
SNABUFFS DS    F                                                 87225
SNASLOW  DS    F                                                 87225
SNAENTLN EQU   *-SNASECT                                         87225
         SPACE 1
SAVE     DSECT ,                                                 81200
DB       DS    D             WORK AREA                           85272
         SERVDEFS ,          SERVICES                           GP12066
@INPREAD DC    A(0)          ADDRESS OF INPUT READER             81200
DATESAVE DC    4A(0)         SAVE AREA FOR DATE CONVERSION       90178
OUTSYS   DC    CL4' '        PREVIOUS SMF CPU ID                 81200
CURBXLE  DC    A(LENBXLE,FIRSTONE-LENBXLE,FIRSTONE-LENBXLE)
TOPBXLE  DC    A(LASTONE)    LAST TABLE ENTRY
PARMSYS  DC    CL4' '        CPU ID                              81200
PARMNAME DC    CL8' '        TERMINAL NAME                       81200
PARMTGET DC    F'0'          TGET / INPUT COUNT                  81200
PARMTPUT DC    F'0'          TPUT / OUTPUT COUNT                 81200
PARMTLOW DC    F'0'          TIME ON                             90178
PARMTHI  DC    F'0'          TIME OFF                            90178
PARMDLOW DC    PL4'0'        LOWEST DATE                        GP12066
PARMDHI  DC    PL4'0'        HIGHEST DATE                       GP12066
PARMUCB  DC    CL4' '        UCB NAME                           GP12066
PARMUSER DC    CL8' '        NAME OF SOLE USER, OR BLANKS        81200
CONCONN  DC    C' NNNNN:NN:NN'  TIME ACCUMULATION FIELD          90178
PSTART   DC    PL4'2099366'                                     GP12066
PEND     DC    PL4'0'                                           GP12066
FLAG     DC    X'0'
OFLOW    EQU   X'80'         MORE ENTRIES THAN PROVIDED
SNAFU    EQU   X'40'           PRINT SNA RECORDS                 87100
SNAFUL   EQU   X'20'           PRINT DETAIL SNA RECORDS ALSO     87190
SNAFUS   EQU   X'10'           PRINT SELECTED DETAIL RECORDS ONLY
DONIQUE  EQU   X'02'           KEEP USER ID FIELD UNIQUE         92058
DODATE   EQU   X'01'         INCLUDE DATES IN ADD/UPDATE         81200
         SPACE 1                                                 87026
CPUBXLE  DC    3A(0)         BXLE POINTERS FOR CPU SCAN          87026
CPUDFLT  DC    3A(0)         BXLE POINTERS FOR DEFAULT           87026
CPUCURR  DC    CL100' '      CURRENT CPU IDS                     87026
CPUIDS   DC    CL100' '      DEFAULT CPU IDS                     87026
         SPACE 1                                                 87190
SNABXLE  DC    A(FIRSTSNA,SECNDSNA-FIRSTSNA,LASTSNA)             87190
FIRSTSNA DS    CL(SNAENTLN)  SYSTEM/DEVICE/COUNTERS              87225
SECNDSNA DS    32XL(SNAENTLN)                                    87225
LASTSNA  EQU   *-SNAENTLN                                        87225
         SPACE 2
FIRSTONE DC    300XL(LENBXLE)'0'  TABLE SPACE
         DC    300XL(LENBXLE)'0'  TABLE SPACE                    85272
         DC    150XL(LENBXLE)'0'  TABLE SPACE                    85272
LASTONE  EQU   *-LENBXLE
SAVEEND  EQU   *                                                GP12066
         SPACE 2
SMFDSECT DSECT ,
         IFASMFR 34          MAP TSO/WYLBUR SESSION
         ORG   SMFDSECT                                          81200
         IFASMFR 47          HASP SIGN-ON                        81200
         ORG   SMFDSECT                                          81200
         IFASMFR 48          HASP SIGN-OFF                       81200
         ORG   SMFDSECT                                          81200
*        IFASMFR 53          JES SNA LOGOFF                      87026
SMF53LEN DS    XL2           RDW                                 87026
SMF53SEG DS    XL2                                               87026
SMFHDFLG DS    X                                                 87026
SMFHDRTY DS    X             TYPE (53)                           87026
SMFHDTME DS    XL4                                               87026
SMFHDDTE DS    PL4                                               87026
SMFHDSID DS    CL4           SYSTEM ID                           87026
SMFPROFF DS    XL2           OFFSET TO PRODUCT SECTION           87026
SMFPRLEN DS    XL2           LENGTH OF PRODUCT SECTION           87026
SMFPRNUM DS    XL2           NUMBER -"-                          87026
SMFIDOFF DS    XL2           OFFSET TO IDENTIFICATION SECTION    87026
SMFIDLEN DS    XL2           LENGTH -"-                          87026
SMFIDNUM DS    XL2           NUMBER -"-                          87026
SMFSUBID DS    XL2           1 LOGOFF  2 $PLNE                   87026
SMFVERNO DS    CL2           RECORD VERSION                      87026
SMFSYSNM DS    CL4           JES2                                87026
SMF53RMT DS    CL8           REMOTE NAME                         87026
SMF53LIN DS    CL8           LINE NAME                           87026
SMF53PSW DS    CL8           LINE PASSWORD                       87026
SMF53CTR DS    XL4           VTAM REQUESTS PROCESSED             87026
         DS    XL4           EXCEPTION RESPONSES                 87026
         DS    XL4           # OF LUSTATS                        87026
         DS    XL4           # OF BID REJECTS                    87026
         DS    XL4           # OF TEMPORARY ERRORS               87026
SMF53ADP DS    CL3           'SNA'                               87026
         ORG   SMFDSECT                                          87026
         IFASMFR 26          HASP JOB SUMMARY                    81200
         ORG   SMFDSECT                                          81200
         IFASMFR 6           OUTPUT RECORD                       81200
         SPACE 1                                                 85272
         ORG   SMFDSECT                                          85272
         SMFSEAC ,           EXPAND NCR/COMTEN SEAC RECORD       85272
         ORG   SMFDSECT                                          87100
         PRINT GEN                                               87100
         IFASMFR 50          GET SNA DEVICE STATISTICS           87100
         AIF   ('&LOCAL' NE 'PID').NOLOMAP                       93012
         ORG   SMFDSECT ,                                        92012
VACRDW   DS    2Y            RECORD DESCRIPTOR WORD              92012
VACFLG   DS    BL1
VACTYPE  DS    BL1           RECORD TYPE                         92012
VACREC#  EQU   205       LOCAL VTAM ACCOUNTING RECORD NUMBER     92058
VACTME   DS    BL4           TIME (BINARY)                       92012
VACDTE   DS    PL4           DATE (PACKED DECIMAL)               92012
VACSID   DS    CL4           SYSTEM ID                           92012
VACJBN   DS    CL8           JOBNAME - LEAVE ROOM FOR STANDARD   92012
VACRST   DS    BL4           READER START        JOB STAMP       92012
VACRSD   DS    BL4           READER START                        92012
VACUIF   DS    CL8           ACCOUNT                             92012
VACONOFF DS    CL1           LOGON/LOGOFF FLAG                   92012
VACAPPL  DS    CL8           APPLICATION ID                      92012
VACTERM  DS    CL8           TERMINAL ID                         92012
VACRLEN  EQU   *-VACRDW      LENGTH OF RECORD                    92012
.NOLOMAP PRINT NOGEN                                             87100
         SPACE 2
CVTDSECT DSECT ,                                                 81200
         CVT   DSECT=YES                                         81200
         IHACDE ,                                                81200
         DCBD  DSORG=PS                                          81200
         END   ,
