REREAD   TITLE 'GU400000 - REREAD ROUTINE FOR FORTRAN'
         PRINT NOGEN
*              AUTHOR.   UNKNOWN.
*              DATE.     09/12/74.
*              REMARKS.  DISASSEMBLED AND RECODED VERSION OF
*                        FORTRAN REREAD SUBROUTINE WHICH HAS
*                        BEEN IN THE LIBRARY FOR AT LEAST
*                        SEVERAL YEARS AND PROBABLY CAME FROM AN
*                        IBM NEWSLETTER.
*                        IT WORKS ON IBM FORTRAN G, H, AND G1
*                        AS FOLLOWS.
*                             CALL REREAD
*                             READ(IUNIT,10)I
*                          10 FORMAT(I2)
*                             READ(99,20)X
*                          20 FORMAT(2X,F8.2)
*                        THE SECOND READ ABOVE WILL INPUT THE
*                        VALUE OF X FROM THE SAME RECORD USED
*                        TO INPUT THE VALUE OF I.  THIS ROUTINE
*                        ARBITRARILY USES 99 AS THE REREAD UNIT.
*        RESTRICTION :       THIS ROUTINE WILL FUNCTION ONLY FOR
*                            SINGLE RECORD I/O.  A READ FOR MORE THAN
*                            ONE RECORD FOLLOWED BY A REREAD WILL GET
*                            THE SEGMENT OF THE NEXT RECORD FOLLOWING,
*                            GARBAGE, OR THE NEXT TO LAST SEGMENT.
*                            READ (99..) MAY BE USED FORMATTED OR
*                            UNFORMATTED, REGARDLESS OF THE PREVIOUS
*                            READ TYPE,  BUT MAY NOT BE USED FOR
*                            DIRECT ACCESS I/O.
         TITLE 'REREAD CALL ROUTINE TO SET HOOKS IN IBCOM'
REREAD   CSECT
         EXTRN IBCOM#
         EXTRN FIOCS#
         STM   R14,R12,12(R13)          SAVE CALLERS REGS 14-12
         BALR  R8,0
         USING *,R8                     MAKE R8 BASE
         ST    R1,RSAVE12               SAVE R1 AGAIN
         L     R1,ADREAD                SET  R1 = A( MY READ ROUTINE)
         L     R15,ADIBCOM#             SET  R15= A( IBCOM# )
         MVI   IBLOAD(R15),STOREOP      CHANGE LOAD INTO STORE
         EX    R0,IBLOAD(R15)           EXECUTE STORE TO CHANGE IBCOM#
         MVI   IBLOAD(R15),LOADOP       RESTORE LOAD.  READ RTN ADDR
         L     R1,RSAVE12               RESTORE R1.
         LM    R2,R12,28(R13)           RESTORE REGS 2-12
         LM    R14,R15,12(R13)          RESTORE REGS 14-15
         MVI   12(R13),X'FF'            SET CALL COMPLETED FLAG
         BR    R14                      RETURN TO CALLER
         DROP  R8
         TITLE 'READ ROUTINE HOOKED INTO IBCOM'
READ     DS    0H
         USING READ,R1
         STM   R4,R15,MYSAVE            SAVE REGS 4-15
         LR    R11,R1                   MAKE R11 BASE
         DROP  R1
         USING READ,R11
         LR    R4,R0                    R4 = R0 = A(CALLERS IO LIST)
         CLC   0(2,R4),MYREAD           IS THIS MY KIND OF READ
         BE    CHKUNIT                  YES, GO CHECK IF REREAD UNIT #
         L     R1,ADFIOCS#              NO,  PASS CONTROL ON TO FIOCS
         LM    R4,R15,MYSAVE
         BR    R1                       EXIT
CHKUNIT  DS    0H
         CLC   0(4,R2),MYUNIT           IS THIS THE REREAD UNIT
         BE    RELOAD                   YES, GO REREAD
         L     R1,ADFIOCS#              NO,  ISSUE FIOCS READ FROM HERE
         BALR  R0,R1
         DC    AL2(READCODE)            I/O LIST READ OPERATION CODE
         B     ERREXIT                  EXIT IF READ ERROR
GOODREAD ST    R2,RSAVE12               ELSE SAVE R2-R3 BUFFER INFO
         ST    R3,RSAVE3
         B     GOODEXIT
RELOAD   L     R2,RSAVE12               RELOAD R2-R3 INFO FROM LAST
         L     R3,RSAVE3                REAL READ
GOODEXIT LA    R1,6(R4)                 GOOD EXIT TO CALLER
EXIT     LM    R4,R15,MYSAVE
         BR    R1
ERREXIT  LA    R1,2(R4)                 BAD  EXIT TO CALLER
         B     EXIT
         TITLE 'COMMON DATA AREA'
ADREAD   DC    A(READ)                  ADDRESS STORED IN IBCOM# READ
ADIBCOM# DC    A(IBCOM#)
MYSAVE   DC    18F'0'
ADFIOCS# DC    A(FIOCS#)
MYUNIT   DC    A(99)                    SET TO UNIT RESERVED FOR REREAD
RSAVE12  DC    F'0'                     R1 SAVE IN REREAD / R2-R3 SAVE
RSAVE3   DC    F'0'                     DURING READ EXITS FROM IBCOM
MYREAD   DC    AL2(READCODE)            I/O LIST READ OPERATION CODE
READCODE EQU   X'00F0'                  READ CODE VALUE
IBLOAD   EQU   74                       OFFSET TO FOLLOWING INSTRUCTION
*                                       IN IBCOM#      L  R1,=V(FIOCS#)
STOREOP  EQU   X'50'
LOADOP   EQU   X'58'
         REGEQU
         END
