DB401    TITLE 'I E F D B 4 0 1  ***  INSTALLATION ALLOCATION CHECKING'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*        COPYRIGHT 1983  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR
*
*        THIS MODULE IS A PREFACE TO THE LOCAL ALLOCATION CHECK
*        MODULE.
* NOTE: IN X/A THIS WAS REPACKAGED AS A SEPARATE MODULE - I'M LAZY
*        AND AM KEEPING THE EXISTING STRUCTURE.                  92300
*        NOTE THAT THE X/A VERSION HAS AMODE 31, RMODE ANY       92300
*
*        1) IF THE REQUEST IS UNINTERESTING, RETURNS OK.
*        2) IF THE REQUEST IS FOR A START TASK WITH AN ENTRY IN STCNMT
*              (IN USERVOLT), OR A TS TASK WITH NO MOUNT PRIVILEGE,
*              THE NOMOUNT BIT IS FORCED TO PREVENT CATALOG VOLUME
*              MOUNTING.
*        3) IF THE REQUESTING TASK HAS NO TCT, RETURNS OK.
*        4) IF MORE THAN ONE SVC 99 REQUEST HAS BEEN ISSUED, RETURNS
*              OK (I.E. RECURSION FROM SVC 99 EXIT). CATALOGS ?
*        5) ELSE MODULE LEXSVC99 IS LOCATED IN THE LPA. IF NOT FOUND,
*              THE REQUEST IS GRANTED. ELSE THE MODULE IS INVOKED
*              AS THOUGH IT HAD BEEN CALLED DIRECTLY.
*
*        USING AN EXTERNAL MODULE IS LESS EFFICIENT, BUT MAKES
*        PROGRAM MAINTENANCE MUCH EASIER.
         SPACE 1
IEFDB401 START 0                FIRST AND EARLY VERSION ADDED ON 83101
         AIF   (NOT &MVSXA).NO31AR                               92300
IEFDB401 AMODE 31                                                92300
IEFDB401 RMODE 24           LOCAL DEPENDENCIES                   92300
.NO31AR  STM   R14,R12,12(R13)    SAVE ALL REGISTERS
         LR    R12,R15
         USING IEFDB401,R12  SET LOCAL BASE
         L     R10,PSATOLD-PSA    LOAD OUR TCB ADRESS
         USING TCB,R10       DECLARE IT
         SPACE 1
         LR    R11,R1        SAVE CALL PARAMETER
         USING S99RBP,R11    DECLARE THE PARAMETER LIST
         L     R5,S99RBPTR                                       83136
         USING S99RB,R5      DECLARE REQUEST BLOCK PROPER
         CLI   S99VERB,S99VRBAL     DSN ALLOCATION ?
         BNE   NOCHECK       NO; ALLOW EVERYTHING ELSE
         SPACE 1
         L     R3,PSAAOLD-PSA  GET ASCB                          83136
         USING ASCB,R3                                           83136
         ICM   R1,15,ASCBOUCB  GET OUCB                          83136
         BZ    NOCHECK       ?                                   83136
         USING OUCB,R1                                           83136
         ICM   R0,15,ASCBTSB  TS USER ?                          83142
         BNZ   GETPSCB       YES; CHECK FOR PSCB                 83136
         TM    OUCBYFL,OUCBSTT  START TASK ?                     83136
         BZ    DONMOUNT      SKIP BATCH JOBS                     83136
         L     R2,CVTPTR                                         83136
         ICM   R2,15,CVTUSER-CVTMAP(R2)  GET USER CVT EXTENSION  92300
         BZ    DONMOUNT      HUH ?                               92300
         USING USERCVT,R2                                        83136
         ICM   R2,15,UCLVOLT    GET VOLUME LOOKUP                92300
         BZ    DONMOUNT                                          92300
         USING USERVOLT,R2                                       83136
         L     R2,VTSTCNMT   GET TABLE OF NO MOUNTS              83136
         ICM   R1,15,ASCBCSCB   GET THE CSCB OR ?                83136
         BZ    DONMOUNT      SHOULD NEVER HAPPEN ?               83136
         USING CHAIN,R1                                          83136
LOOPSTC  CLI   0(R2),C' '    END OF TABLE ?                      83136
         BNH   DONMOUNT      YES; PERMIT MOUNTS                  83136
         CLC   CHCLS,0(R2)   MATCH ?                             83136
         LA    R2,8(,R2)     UP TABLE POINTER                    83136
         BNE   LOOPSTC       TRY NEXT ENTRY                      83136
         B     KILMOUNT      ELSE KILL MOUNTS                    83136
         SPACE 1                                                 83136
GETPSCB  L     R1,TCBJSCB    GET JSCB                            83136
         USING IEZJSCB,R1                                        83136
         ICM   R1,15,JSCBPSCB  PSCB PRESENT ?                    83136
         BZ    DONMOUNT      NO                                  83136
         USING PSCB,R1                                           83136
         SPACE 1                                                 83136
*-------------------------------------------------------------------*
*                                                                   *
*    TEMPORARY KLUDGE UNTIL LOCAL LOGON PROCESSING IS DONE          *
*    USER'S DEFAULT SAVE (ALLOC) VOLUME IS STASHED IN UADS AS       *
*    UNIT(VOLSER).  THIS CODE DETECTS FIRST REFERENCE AND COPIES    *
*    IT TO PSCBU; PSCBGPNM (UNIT) IS THEN SET TO SYSALLDA TO        *
*    PERMIT CATALOG ALLOCATION TO WORK CORRECTLY.                   *
*                                                                   *
*-------------------------------------------------------------------*
         LA    R4,PSCBU      SET FOR SECONDARY ENTRY (RE-LOGON)  83142
         CLI   PSCBU,C' '    USER FIELD FILLED IN ?              83136
         BH    RELOGON       YES; REFRESH GROUP NAME             83142
         CLI   PSCBGPNM,C' '   ANYTHING IN UADS ?                83136
         BNH   DONEUNIT      NO                                  83136
         CLI   PSCBGPNM+6,C' '  TOO LONG FOR VOLSER ?            83136
         BNE   DONEUNIT      YES; LEAVE                          83136
         LA    R4,PSCBGPNM   SET TO MOVE UADS UNIT               83142
RELOGON  MODESET EXTKEY=ZERO  ASSUME KEY ZERO                    83142
         MVC   PSCBU,0(R4)   MOVE VOLUME SERIAL                  83142
         MVC   PSCBGPNM,=CL8'SYSALLDA'  MAKE INSTALLATION DEFAULT
         MODESET EXTKEY=SCHED    RESTORE KEY                     83136
         SPACE 1                                                 83136
DONEUNIT TM    PSCBATR1,PSCBVMNT  MOUNT ATTRIBUTE ?              83136
         BNZ   DONMOUNT      YES; PERMIT                         83136
KILMOUNT OI    S99FLG11,S99NOMNT   PREVENT UNDESIRED MOUNTS      83136
         SPACE 1                                                 83136
DONMOUNT ICM   R0,15,TCBTCT  ANY TCT ?                           92300
         BZ    NOCHECK       NO; RETURN OK
         LA    R4,2          SET FOR TWO OCCURRENCES             83136
         L     R6,TCBRBP     GET TOP RB
         LA    R0,RBBASIC-RBFLAGS1  SET DESIRED PREFIX LENGTH
         LA    R14,99        INTERRUPT CODE TO LOOK FOR          83136
         USING RBBASIC,R6
TESTRB   SR    R6,R0
         USING RBFLAGS1,R6
         CH    R14,RBINTCOD  SVC 99 ISSUED ?                     83136
         BNE   NEXTRB        NO
         BCT   R4,NEXTRB
NOCHECK  LM    R14,R12,12(R13)  RESTORE ALL REGISTERS
         SLR   R15,R15       PERMIT ALLOCATION REQUEST
         BR    R14           RETURN TO CALLER
         SPACE 1
NEXTRB   TM    RBSTAB2,RBTCBNXT  END OF CHAIN ?
         BNZ   LOOKLPA        YES; PROCEED                       83136
         ICM   R6,7,RBLINKB   SEE IF ANOTHER
         BNZ   TESTRB        CHECK IT
         SPACE 1
LOOKLPA  LM    R0,R1,MODNAME  SPECIFY EXIT NAME
         L     R3,CVTPTR     GET CVT IN R3 FOR IEAVVMSR
         ICM   R4,15,CVTUSER-CVTMAP(R3)  GET USER CVT            83136
         BZ    NOUSCVT                                           83136
         USING USERCVT,R4    DECLARE IT                          83136
         ICM   R15,15,UCXSVC99  ADDRESS ALREADY SET ?            83136
         BNZ   QUICKGO       YES; USE IT                         83136
NOUSCVT  L     R7,CVTLPDSR-CVTMAP(,R3)  IEAVVMSR                 83136
         BALSR R14,R7    NOTE THAT R7-R9 ARE CLOBBERED
         B     FOUNDIT       MODULE FOUND
         B     NOCHECK       PERMIT REQUEST WITHOUT CHECK
FOUNDIT  LR    R15,R0        COPY LPDE ADDRESS
         USING LPDE,R15
         L     R15,LPDENTP   LOAD ENTRY POINT
         LTR   R4,R4         USERCVT SET ?                       83136
         BZ    QUICKGO       NO                                  83136
         MODESET EXTKEY=ZERO  SET KEY ZERO FOR STORE             83136
         ST    R15,UCXSVC99  SAVE THE EXIT ADDRESS               83136
         MODESET EXTKEY=SCHED  RESTORE SCHEDULER KEY             83136
QUICKGO  L     R14,12(,R13)  RESTORE RETURN ADDRESS              83136
         LM    R0,R12,20(R13)  RESTORE ALL BUT R15
         BR    R15           TRANSPARENT CALL TO EXIT
         SPACE 1
MODNAME  DC    0F'0',CL8'LEXSVC99'   LOCAL EXIT NAME
         EJECT ,
         PRINT &PRTMAC
         IEFZB4D0 ,
         SPACE 2
         PRINT &PRTSYS
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IHALPDE ,
         IKJTCB ,
         IHARB ,
         IHAPSA ,
         IHAASCB ,                                               83136
         IEZJSCB ,                                               83136
         IKJPSCB ,                                               83136
         IEECHAIN ,                                              83136
         IRAOUCB ,                                               83136
USERCVT  USERCVT ,                                               83136
         USERVOLT LIST=NO                                        83136
         SPACE 1
         PRINT NOGEN
         REGEQU ,
         SPACE 1
         END   ,
