WCON     TITLE 'AMS/OSI/RAND WYLBUR EDIT LINE NUMBER CONVERSION TO OBS'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*        COPYRIGHT 1979, 1986  EXPERT SYSTEM PROGRAMMING, INC.        *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR
WYLCONV  SAVEM ZERO,BASE=R12,PARM=R9                             86269
         SPACE 1
         OPEN  (SYSUT1)
         TM    SYSUT1+48,X'10'  OPEN ?
         BZ    BOMB
         LA    R11,SYSUT1
         USING IHADCB,R11
         TM    DCBRECFM,X'C0'  RECFM=U ?
         BNO   BOMB
         CLC   DCBLRECL(2),DCBBLKSI
         BE    INBOK
         OC    DCBLRECL(2),DCBLRECL  ANY ?
         BNZ   BOMB
INBOK    CLC   DCBBLKSI(2),=H'3665'
         BH    BOMB
         LH    R1,DCBBLKSI
         STH   R1,DCBBLKSI-IHADCB+SYSUT2
         STH   R1,DCBLRECL-IHADCB+SYSUT2
         OPEN  (SYSUT2,(OUTPUT))
         TM    SYSUT2+48,X'10'
         BZ    BOMB
         SPACE 1
GET      GET   SYSUT1
         LR    R0,R1         SAVE BUFFER ADDRESS
         MVC   DCBLRECL-IHADCB+SYSUT2(2),DCBLRECL-IHADCB+SYSUT1
         LR    R6,R1         COPY AGAIN
         CLC   0(2,R1),DCBLRECL  VALID WYLBUR BLOCK ?
         BNE   BOMB
         LA    R1,2(,R1)     POINT TO DATA
         LA    R15,2         CURRENT SIZE PROCESSED
         SLR   R14,R14
LOOP     IC    R14,4(,R1)    GET LINE LENGTH
         CH    R14,=H'142'   VALID ?
         BH    BOMB
         LH    R7,0(,R1)     GET INTEGER PORTION
         MH    R7,=H'1000'
         AH    R7,2(,R1)     ADD FRACTION
         STCM  R7,15,0(R1)   STASH BACK OBS LINE NUMBER
         LA    R15,5(R14,R15)  NEW LENGTH
         LA    R1,5(R14,R1)  NEW RECORD
         CH    R15,DCBLRECL  DONE WITH BLOCK ?
         BL    LOOP
         BH    BOMB
         PUT   SYSUT2        PUT BLOCK IN R0
         B     GET
         SPACE 1
BOMB     ABEND 999,DUMP
         SPACE 1
EOF      CLOSE SYSUT1
         CLOSE SYSUT2
         ENDM  RC=0
         EJECT ,
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GL,EODAD=EOF,BUFNO=4
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=PM,BUFNO=4
         SPACE 1
         LTORG ,
         SPACE 1
SAVE     DSECT ,
SAVEND   EQU   *
         SPACE 1
         PRINT &PRTSYS
         DCBD  DEVD=DA,DSORG=PS
         END   ,
