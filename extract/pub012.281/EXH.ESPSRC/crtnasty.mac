MEAN     TITLE 'C R T N A S T Y  ***  WRITE NASTY MSG TO CRTS'
         COPY  OPTIONGB      INCLUDE GLOBAL FLAGS                82139
         SPACE 1                                                 82139
         PUNCH '   ORDER CRTNASTY(P) '      EASIER DUMPS        GP05084
         PUNCH '  SETCODE AC(1) '                               GP05084
         SPACE 1                                                 82139
         SYSPARM LIST=YES    DEFINE AND LIST GLOBALS             82139
         EJECT ,
         PRINT &PRTSOR                                           82139
***********************************************************************
**                                                                   **
**  THIS MODULE WRITES ONE PAGE TO ALL AVAILABLE 3270-TYPE           **
**  (LOCAL) CRTS.  THE DATA ARE SUPPLIED (MAX OF 23 CARDS) VIA       **
**  A SYSIN CARD FORMAT FILE.                                        **
**                                                                   **
**  MODULE RUNS IN MVS, OS/390 ONLY, AND REQUIRES AUTHORIZATION.     **
**                                                                   **
***********************************************************************
CRTNASTY PGMHEAD ZERO,BASE=R12                           UPDATED 82139
*DEFER*  BANDAID INIT                                           GP03074
         SERVINIT ,          LOAD AND INITIALIZE @SERVICE ROUTINE
         SERVCALL LPALD,=CL8'@PRINTER'   LOAD THE PRINTER MODULE 82139
         ST    R0,@PRINTER                                       82139
         SERVCALL LPALD,=CL8'@INPREAD'   LOAD THE INPUT READER   82139
         ST    R0,@INPREAD                                       82139
         PRTOPEN SYSPRINT,OPT=(DUMMY,NOWTO)  OK IF NO SYSPRINT FILE
         PRTOPEN CONSOLE,OPT=(DUMMY,NOWTO),DEV=2  FOR DATE/TIME FINAGLE
         INPOPEN SYSIN,OPT=(DUMMY,ABEND)  ABEND IF NO SYSIN      82139
         PRTLIST HEADER,TITLE=1  WRITE HEADER                    82139
         SPACE 1
         LA    R4,BUFFER     SET FOR BUFFER TEXT                 82139
         LA    R5,(BUFEND-BUFFER)/80   NUMBER OF LINES
SYNGET   INPGET ,            GET A LINE                          82139
         BXH   R15,R15,SYNEOF  EOF OR ERROR                      82139
         MVC   0(80,R4),0(R1)  MOVE TEXT                         82139
         LA    R4,80(,R4)    SPACE TO NEXT LINE
         BCT   R5,SYNGET
SYNEOF   INPCLOSE ,          CLOSE THE INPUT FILE                82139
         SERVCALL LPADL,=CL8'@INPREAD'  DELETE THE READER MODULE 82139
         PRTLIST MSGDATIM,DEV=2   GET DATE AND TIME              82139
         SPACE 1
         MODESET KEY=ZERO
         LM    R15,R0,DRDEBSP  GET STORAGE SUBPOOL/LENGTH       GP03074
         STORAGE OBTAIN,LENGTH=(0),SP=(15),LOC=BELOW            GP03074
         LR    R6,R1         SAVE IT
         USING DEBAVT,R6     DECLARE DEB PREFIX                  82139
         MVC   0(PATLEN,R6),PDEB  MOVE PATTERN DEB
         L     R11,CVTPTR
         USING CVTMAP,R11                                        82139
         LA    R9,DRDCB      GET DCB ADDRESS
         USING IHADCB,R9                                         82139
         MVI   DCBDEVT,X'19'   MAKE FUNNY DEVICE CODE
         MVI   DCBOFLGS,X'92'  SET DCB OPEN FOR OUTPUT
         MVC   DCBTIOT(4),=X'0018D008'   TIOT OFFSET / MACRF = E
         L     R8,CVTTCBP    GET TCB POINTER
         L     R8,4(,R8)     GET TCBOLD
         USING TCB,R8                                            82139
         LA    R8,0(,R8)     JUST IN CASE
         ST    R8,DEBTCBAD   ALSO SETS MODULES LOADED TO ZERO
         ST    R9,DEBDCBAD   SET DCB ADDRESS INT DEB
         MVI   DEBDEBID,X'0F'  SET DEB IDENTIFIER
         MVI   DEBNMEXT,1
         MVI   DEBLNGTH,(PATLEN+7)/8
         ST    R6,DEBAPPAD   SAVE APPENDAGE ADDR.
         MVI   DEBEXSCL,2     4 BYTES IN ACCESS METHOD SECTION
         LA    R5,DEBBASIC
         STCM  R5,7,DCBDEBAD+1   SAVE DEB ADDRESS
         L     R14,CVTXAPG   GET RESIDENT APPENDAGES
         MVC   DEBAVT(DEBPREFX-DEBAVT),0(R14) COPY CANNED ADDRESSES
         MODESET MODE=SUP
         LA    R4,TCBDEB-(DEBDEBAD-DEBBASIC)
         XR    R0,R0                                             82139
ADEBLOOP CLM   R0,7,DEBDEBB-DEBBASIC(R4)  END OF CHAIN ?         82139
         BE    ADEBLADD      YES; ADD OURS
         L     R4,DEBDEBAD-DEBBASIC(,R4)
         B     ADEBLOOP
ADEBLADD STCM  R5,7,DEBDEBB-DEBBASIC(R4)  PLACE DEB ON CHAIN
         DEBCHK DRDCB,TYPE=ADD,AM=EXCP
         MODESET MODE=PROB
         LA    R10,IOBLOCK
         USING IOBSTDRD,R10                                      82139
         SPACE 1
         XR    R1,R1         ZERO FIRST UCB ADDRESS              82139
         B     UNITBUMP+4                                        82139
UNITBUMP L     R1,ADDRUCB    GET PRIOR UCB ADDRESS               82139
         SERVCALL UCBUM      GET NEXT UCB                        82139
         BXH   R15,R15,UNITEXIT  STOP ON ERROR                  GP03074
         LTR   R3,R0         DID WE GET ONE ?                    82139
         BZ    UNITEXIT      NO; ALL DONE                        82139
         ST    R3,ADDRUCB    SAVE FOR NEXT TIME                  82139
         USING UCBOB,R3                                          82139
         SPACE 1                                                 82139
         CLI   UCBTBYT3,UCB3DISP  GRAPHICS ?                     82139
         BNE   UNITBUMP
         AIF   (NOT &MVS).NMVS78                                 82139
         CLI   UCBTBYT4,X'07'  3278 ?                            82139
         BE    ACCUCB        YES; PROCESS                        82139
.NMVS78  CLI   UCBTBYT4,X'09'  3277 ?                            82139
         BE    ACCUCB        YES; ACCEPT IT
         CLI   UCBTBYT4,X'0C'  3036/3158/WHATEVER ?
         BL    UNITBUMP      NO; PRINTER
         CLI   UCBTBYT4,X'0D'
         BH    UNITBUMP
ACCUCB   TM    UCBSTAT,UCBONLI  AVAILABLE ?                      82139
         BZ    UNITBUMP      DEFINITELY NOT; SKIP WITHOUT PRINTING
         CLI   UCBSTAT,UCBONLI  CONSOLE, ALLOC, ETC. ?           82139
         BNE   PUTUCBON      YES; SHOW IT                        82139
         AIF   (&MVS).MVSUSE                                     82139
*OLD*    TM    UCBFL5,UCBTICBT+UCBNALOC  TMCK/OLTEP ?           GP03026
         TM    UCBFL5,UCBNALOC  OLTEP ?                         GP03026
         BNZ   PUTUCBAC      EITHER; PRINT AS ACTIVE             82174
         CLI   UCBFL1,0      ANY ACTIVITY ?
         BNE   PUTUCBAC      BUT HOW ?                           82174
         AGO   .COMUSE                                           82139
.MVSUSE  L     R4,UCBEXTPT   POINT TO EXTENSION FOR ATI          82139
         N     R4,=X'00FFFFFF'  NEVER TRUST ANYBODY             GP03074
         USING UCBCMEXT,R4   DECLARE IT                          82139
         TM    UCBFL5,UCBNALOC  OLTEP OR SPECIAL USER ?          82139
         BNZ   PUTUCBAC      YES; PRINT                          82139
         CLI   UCBFLA,0      ANY I/O ACTIVITY ?                  82139
         BNE   PUTUCBAC      YES                                 82139
         CLI   UCBFLB,0                                          82139
         BNE   PUTUCBAC                                          82139
*OLD*    TM    UCBFLC,255-UCBUDE-UCBIVRR-UCBTICBT  FUNNIES ?     84184
         TM    UCBFLC,255-UCBUDE-UCBIVRR  FUNNIES ?             GP03026
         BNZ   PUTUCBAC                                          84184
.COMUSE  CLI   UCBATNCT,0    ATTENTION COUNT NON-ZERO ?          82139
         BNE   PUTUCBAC      YES; PRINT                          82139
*        TM    UCBGCB,255-UCBSKPFG-UCBRIPND                      84184
*        BNZ   PUTUCBAC      YES                                 84184
         ICM   R0,15,UCBIRB  ANY IRB/FLAGS ?                     82139
         BNZ   PUTUCBAC                                          82139
         ICM   R0,15,UCBIRLN  VTAM LDNCA/BTAM READYQ ?           82139
         BNZ   PUTUCBAC      YES; PRINT                          82139
         ICM   R0,15,UCBCTLNK  RLN/DEB,UCB ?                     82139
         BNZ   PUTUCBAC      YES; PRINT                          82139
         STCM  R3,7,DEBSUCBB
*OLD*    MVC   BUFCRT,UCBNAME  IDENTIFY CRT                     GP03026
         UNPK  DB(5),UCBCHAN(3)  GET DEVICE NUMBER              GP03026
         TR    DB(4),TRHEXTAB    MAKE PRINTABLE HEX             GP03026
         MVC   BUFCRT,DB     AND MOVE TO BUFFER                 GP03026
         SPACE 1
         MVI   ECB,0
         MVI   IOBFLAG1,IOBCMDCH+IOBUNREL
         MVI   DCBIFLGS,DCBIFIOE  NO ERROR RECOVERY MSGS
         EXCP  IOBLOCK
         WAIT  ECB=ECB
         PRTLIST MSGIO       PRINT RESULT                        82139
         B     UNITBUMP
         SPACE 1                                                 82139
PUTUCBON PRTLIST MSGUCBON    SHOW ACTIVE                         82139
         B     UNITBUMP      GET ANOTHER                         82139
PUTUCBAC PRTLIST MSGUCBAC    SHOW REALLY ACTIVE                  82139
         B     UNITBUMP                                          82139
         SPACE 1
UNITEXIT MODESET MODE=SUP
         DEBCHK DRDCB,TYPE=DELETE,AM=EXCP
         LA    R1,DEBBASIC   GET DEB ADDRESS BACK
         LA    R4,TCBDEB-(DEBDEBAD-DEBBASIC)
DDEBLOOP CLM   R1,7,DEBDEBB-DEBBASIC(R4)  OUR DEB NEXT ?
         BE    DDEBDELT
         ICM   R4,7,DEBDEBB-DEBBASIC(R4)  GET NEXT DEB
         BNZ   DDEBLOOP      LOOK AT IT
         B     DDEBDELX      HOW DID THIS HAPPEN ?
DDEBDELT MVC   DEBDEBB-DEBBASIC(3,R4),DEBDEBB  UNCHAIN OUR DEB
DDEBDELX MODESET KEY=NZERO,MODE=PROB
         SERVTERM ,          CLOSE AND FREE EVERYTHING           82139
         PGMEXIT RC=0        EXIT WITH CODE ZERO ANYWAY          82139
         SPACE 1                                                GP03026
         LTORG ,                                                GP03026
         SPACE 1                                                GP03026
HEXTAB   DC    C'0123456789ABCDEF'                              GP03026
TRHEXTAB EQU   HEXTAB-C'0',256,C'C'                             GP03026
         EJECT
SAVE     DSECT ,             WORK AREA                           82139
DB       DC    D'0'
@SERVICE DS    A             ADDRESS OF @SERVICE ROUTINE         82139
@PRINTER DS    A             ADDRESS OF @PRINTER ROUTINE         82139
@INPREAD DS    0A       1/2  ADDRESS OF INPUT READER             82139
ADDRUCB  DS    A        2/2  ADDRESS OF CURRENT UCB              82139
SAVEEND  EQU   *             END OF DYNAMIC WORK AREA            82139
CRTNASTY CSECT ,                                                 82139
         SPACE 2                                                 82139
DRDEBSP  DC    A(243-13*&MVS)  DEB GETMAIN SUBPOOL 243/230      GP03074
DRDEBLN  DC    A(PATLEN)     DEB GETMAIN 243/230 LENGTH         GP03074
         SPACE 1
         DC    XL40'0'       MAKE DUMP PRETTY
DRDCB    DCB   MACRF=(E),DDNAME=IEFRDER
         DC    XL12'0'       JUST IN CASE
         XDAP  ECB,RI,DRDCB,BUFPFX,BUFLEN,1,MF=L  ECB/IOB
IOBLOCK  EQU   ECB+4
         ORG   *-24
WCCW     DC    X'05',AL3(BUFPFX-CRTNASTY),X'20',AL3(BUFLEN)
PDEB     DC    8F'0'         DUMMY - NOT USED
         DC    AL1((PATLEN+7)/8,0,0,0)
PATDEB   DC    X'0F',AL3(0)  DEB LENGTH
         DC    F'0'          TCB ADDR
         DC    X'10',AL3(0)  DEB LINK PTR
         DC    X'68',AL3(0)    IRB PTR
         DC    F'0'          FLAGS
         DC    F'0'          IOB
         DC    X'FC',AL3(0) .    PRTY / ECB
         DC    X'0F',AL3(DRDCB)
         DC    X'02',AL3(0)  APP ADDR
         DC    X'33',AL3(0)  FILE MASK / UCB ADDR.
PATLEN   EQU   *-PDEB
         SPACE 1
SYSIN    INPWORK SYSIN,FILL=C' ',WIDTH=80,EODAD=SYNEOF           82139
SYSPRINT PRTWORK SYSPRINT,TITLE=3                                82139
         SPACE 1                                                 82139
HEADER   FDOPT NL,CC=C'#'    DATED TITLE REQUEST                 82139
         FDSPACE 25                                              82139
         FDPRT 'CRT MESSAGE WRITER'                              82139
         FDPRT *END                                              82139
         SPACE 1                                                 82139
MSGUCBON FDPRT 'CRT',NL                                          82139
         FDPRT UCBCHAN,PAD,HEX                                  GP03026
         FDPRT 'ALLOCATED',PAD                                   82139
         FDGOTO MSGUCBCM                                         82139
MSGUCBAC FDPRT 'CRT',NL                                          82139
         FDPRT UCBCHAN,PAD,HEX                                  GP03026
         FDPRT 'IN USE OR NOT CLEARED',PAD                       82139
MSGUCBCM FDPRT 'STAT=',PADL                                      82139
         FDPRT UCBSTAT,HEX,PADR                                  82139
         FDPRT 'FL5=',PADL                                       82139
         FDPRT UCBFL5,HEX,PADR                                   82139
         AIF   (&MVS).FORMMVS                                    82139
         FDPRT 'FL1=',PADL                                       82139
         FDPRT UCBFL1,HEX,PADR                                   82139
         AGO   .FORMCOM                                          82139
.FORMMVS FDPRT 'FLA/B/C',PAD                                     82139
         FDPRT UCBFLA,HEX                                        82139
         FDPRT UCBFLB,HEX,PAD                                    82139
         FDPRT UCBFLC,HEX,PAD                                    82139
.FORMCOM FDPRT 'ATI=',PADL                                       82139
         FDPRT UCBATI,HEX,PADR                                   82139
         FDPRT 'ATN/GCB',PAD                                     82139
         FDPRT UCBATNCT,2,HEX,PAD                                82139
         FDPRT 'IRB=',PADL                                       82139
         FDPRT UCBIRB,1,HEX  UCBGRAF                             82139
         FDPRT '/'                                               82139
         FDPRT UCBIRB+1,3,HEX,PADR                               82139
         FDPRT 'RDYQ=',PADL                                      82139
         FDPRT UCBIRLN,HEX                                       82139
         FDPRT '/'                                               82139
         FDPRT UCBIRLN+1,3,HEX,PADR                              82139
         FDPRT 'RLN/DEB/UCB',PAD                                 82139
         FDPRT UCBRLN,HEX                                        82139
         FDPRT '/'                                               82139
         FDPRT UCBCTLNA,HEX                                      82139
         FDPRT *END                                              82139
         SPACE 1                                                 82139
MSGIO    FDPRT 'CRT',NL                                          82139
         FDPRT BUFCRT,PAD                                        82139
         FDPRT 'ECB='                                            82139
         FDPRT ECB,1,HEX,PADR                                    82139
         FDCLI ECB,X'7F',BE=MSGIOX                               82139
         FDPRT 'IOB',PAD                                         82139
         FDPRT IOBSTDRD,4,HEX,PAD                                82139
         FDPRT IOBSTDRD+4,4,HEX,PAD                              82139
         FDPRT IOBSTDRD+8,4,HEX,PAD                              82139
         FDPRT IOBSTDRD+12,4,HEX,PAD                             82139
         FDPRT IOBSTDRD+16,4,HEX,PAD                             82139
         FDPRT IOBSTDRD+20,4,HEX,PAD                             82139
MSGIOX   FDPRT *END                                              82139
         SPACE 2                                                 82139
*        THE FOLLOWING CONSOLE STUFF IS SOLELY TO GET THE DATE   82139
*        AND TIME FORMATTED.                                     82139
CONSOLE  PRTWORK *CONSOLE                                        82139
MSGDATIM FDPRT DB,1,DATE,NL,LEN=10     GET CURRENT DATE (L<3)    82139
         FDPRT DB,1,TIME,LEN=8           DITTO FOR TIME (L<4)    82139
         FDUEX EXTDATIM      GO TO USER EXIT                     82139
         FDPRT *END                                              82139
         PUSH  USING                                             84184
         DROP  ,                                                 84184
         USING EXTDATIM,R15                                      84184
         USING PUXPLIST,R1   DECLARE PASSED PARM LIST            84184
EXTDATIM L     R2,PUXPLINE   GET ADDRESS OF @PRINTER VCON        84184
         MVC   BUFDAT(8+2+8),5(R2)  MOVE FROM LINE WORK AREA     84184
         LA    R15,4         SET LINE KILL IN RETURN             82139
         BR    R14           RETURN TO PRINTER                   82139
         POP   USING ,                                           84184
         SPACE 2
BUFPFX   SCRN  (RESTMDT,ALARM),SBA,(1,1),SF,(PROT,INT),SBA,(1,40)
BUFDAT   DC    CL8' ',CL2' '  DATE MM/DD/YY
BUFTIM   DC    CL8' ',CL9' '  TIME HH:MM:SS
         SCRN  SF,(PROT)
         DC    C'CRT'
         SCRN  SF,(PROT,INT)
BUFCRT   DC    C'1234'       WAS UCBNAME; NOW UCBCHAN           GP03026
         SCRN  SBA,(1,80),SF,(PROT)
BUFFER   DC    23CL80' '     SCREEN BUFFER
BUFEND   SCRN  SBA,(1,2),IC
BUFLEN   EQU   *-BUFPFX
         SPACE 2
         PRINT &PRTSYS                                           82139
CVTDSECT DSECT ,                                                 82139
         CVT   DSECT=YES                                         82139
         SPACE 1                                                 82139
         IHACDE ,                                                82139
         SPACE 1                                                 82139
         IKJTCB ,                                                82139
         SPACE 1                                                 82139
         IEZIOB ,                                                82139
         SPACE 1                                                 82139
         DCBD  DSORG=PS                                          82139
         SPACE 1                                                 82139
         IEZDEB ,                                                82139
         SPACE 1                                                 82139
         IEFUCBOB ,                                              82139
         SPACE 1                                                 84184
PRTMAP   MAPPRT EXPARM=      GET EXIT PARM LIST EXPANSION        84184
         END
