MCPI     TITLE 'M C P I N I T  ***  LOAD AND AUTHORIZE MILTEN MCP'
         COPY  OPTIONGB                                          82354
         SPACE 1                                                 82354
         SYSPARM LIST=YES                                        82354
         EJECT ,
         PRINT &PRTSOR                                           82354
MCPINIT  CSECT ,                                       ADDED ON  79244
         STM   R14,R12,12(R13) SAVE INPUT
         BAL   R15,MCPINITS-MCPINIT(,R15)                        82358
SAVEAREA DC    9D'0'
         DC    CL8'MCPINIT &SYSDATE'                             85092
DB       DC    D'0'          FOR SERVICE WORK SPACE              85092
MCPINITS ST    R15,8(,R13)                                       82358
         ST    R13,4(,R15)
         LR    R13,R15
         USING MCPINIT+8,R13
         SPACE 1
         AIF   ('&SYSTEM' EQ 'MFT' OR '&SYSTEM' EQ 'MVT').NOAPF
*        D.T.S. SVS CHANGES
*          IF INVOKED BY AUTHORIZED ACCOUNT NUMBER, TURN ON APF
*          BIT IN TASKLIB DEB (LOCAL IEAVLK01 MOD USES IT)
*        IF UNAUTHORIZED, TURN APF BIT OFF IN JSCB
         SPACE 1
         L     R11,CVTPTR    GET CVT
         USING CVTMAP,R11                                        82354
         L     R10,CVTTCBP
         L     R10,4(,R10)   GET CURRENT TCB
         USING TCB,R10                                           82354
         LAT   R9,TCBJSCB,BOOBOO       NO JSCB ?
         USING IEZJSCB,R9    DECLARE IT                          82354
         TM    JSCBOPTS,JSCBAUTH       APF AUTHORIZED ?
         BZ    BOOBOO        NO; SKIP ALL THIS
         MODESET KEY=ZERO    GET KEY ZERO
         SERVICE ACGET,DB    GET CURRENT ACCOUNT NUMBER          85092
         BXH   R15,R15,APFRESET  DISABLED OR INVALID ACCOUNT     85092
         STC   R0,DB         SAVE PRIVILEGE BYTE                 85092
         TM    DB,VAASTC+VAASYS  SYSTEMS OR START TASK ?         85092
         BZ    APFRESET      NO; FAIL                            85092
         LAT   R7,TCBJLB,APFQUIT       BRANCH IF NO STEPLIB
         USING IHADCB,R7                                         82354
         L     R8,DCBDEBAD   GET DEB                             79297
         USING DEBBASIC,R8                                       82354
         OI    DEBFLGS1,DEBAPFIN  TURN ON APF LIBRARY BIT        82354
         OI    JSCBSWT1,JSCBPASS  LET WYLBUR PDIO CHECK ACCESS   85235
         AIF   (NOT &MVS).NONTSWP                                82358
         SLR   R1,R1                                             84104
         SYSEVENT TRANSWAP   ELEGANT DONTSWAP                    84104
         BXLE  R15,R15,APFQUIT  OK IF IT WORKED                  84104
         SYSEVENT DONTSWAP   RESIDENT APPENDAGE CODE             82358
.NONTSWP B     APFQUIT
APFRESET NI    JSCBOPTS,255-JSCBAUTH   ELSE TURN OFF AUTHORIZATION
APFQUIT  MODESET KEY=NZERO   RESET ME
         DROP  R7,R8,R9,R10,R11
BOOBOO   DC    0H'0'         FORGOT SETCODE IN LKED ?
.NOAPF   SPACE 2
         L     R13,4(,R13)   GET SAVE BACK
         LM    R14,R12,12(R13)  RELOAD EVERYTHING
         DROP  ,
         USING MCPINIT,R15
         XCTL  EP=MCP        LOAD THE MCP PROPER
         SPACE 2                                                 82358
         PRINT &PRTSYS                                           82354
CVTDSECT DSECT ,                                                 82354
         CVT   DSECT=YES                                         82354
         SPACE 1                                                 82354
         IKJTCB ,                                                82354
         SPACE 1                                                 82354
         IEZJSCB ,                                               82354
         SPACE 1                                                 82354
         IEFTCT ,                                                82354
         SPACE 1                                                 82354
         IEZDEB ,                                                82354
         AIF   (&MVS).DFAPF                                      82354
DEBAPFIN EQU   2             APF AUTHORIZATION BIT               82354
.DFAPF   SPACE 1                                                 82354
         DCBD  DEVD=DA,DSORG=PS                                  82354
         SPACE 1                                                 82354
         PRINT NOGEN                                             82354
         REGEQU ,                                                82354
         END   MCPINIT
