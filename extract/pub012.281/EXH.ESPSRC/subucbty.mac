UCBTY    TITLE 'S U B U C B T Y  ***  UCB TYPE CONVERSION'
         COPY  OPTIONGB                                          82008
         SPACE 1                                                 82008
         SYSPARM LIST=YES                                        82008
         EJECT ,
         PRINT &PRTSOR                                           82008
*                                                                82008
*        ADAPTED FROM (SHARE?) PROGRAM UCBPRINT.                 82008
*        AUTHOR UNKNOWN - PROGRAM PRINTS LIST OF UNITS IN SYSTEM 82008
*
*   INPUT: R1 POINTS TO UCB      (OR FACSIMILE)
*   OUTPUT: R0-R1 CHARACTER EQUIVALENT OF DEVICE TYPE
*
*    ATTRIBUTES: RENT,REFR, AMODE ANY, RMODE ANY
*
         SPACE 1                                                 82008
SUBUCBTY START 0
         USING SUBUCBTY,R15
         USING USERSAVE,R13       USE CALLER'S SAVE AREA AS WORK
         B     BEGIN-*(R15)  SKIP AROUND ID
         BCON  'SUBUCBTY &SYSDATE'
BEGIN    STM   R14,R12,USR14      SAVE REGISTERS
         MVC   USR15,=F'8'        SET ERROR RETURN (PROVISIONALLY)
         MVC   USR0(8),BLANKS     CLEAR RETURN
         LTR   R3,R1         ANY ?
         BZ    SUBEXITS      RETURN ERROR
         USING UCBOB,R3           DECLARE UCB
         MVC   HOLDBYT1(4),UCBTBYT1  SAVE TYPE BYTES
*---------------------------------------------------------------------*
*    CHECK SPECIFIC COMMUNICATIONS ENTRIES FIRST                      *
*---------------------------------------------------------------------*
         CLI   HOLDBYT3,UCB3COMM  COM DEVICE ??
         BNE   SKIPCOMM
         LA    R8,COMMTBLE         GET COMM TYPE TABLE
LOOPCOMM MVC   HALF+0(1),HOLDBYT1   TEST SAVED UCBTBYT1 & 4
         MVC   HALF+1(1),HOLDBYT4                                80353
         NC    HALF,0(R8)                                        80353
         CLC   HALF,0(R8)                                        80353
         BE    HAVECOMM      ALL SET - MOVE TO PRTELE            80353
         LA    R8,8(,R8)           NEXT TABLE ENTRY              80353
         CLI   0(R8),X'FF'   ANY MORE ?                          80353
         BNE   LOOPCOMM      YES                                 80353
         B     SKIPCOMM                                          80353
HAVECOMM MVC   USR0(6),2(R8) PRINT TP DEVICE
         B     SUBEXIT0
         SPACE 1
*---------------------------------------------------------------------*
*    CHECK GENERAL TYPE TABLE                                         *
*---------------------------------------------------------------------*
SKIPCOMM LA    R8,TYPETBLE        DEVICE TYPE TABLE
         LA    R0,TYPENUM         NUMBER TO MATCH
LOOPTYPE CLC   HOLDBYT3(2),0(R8)  IF DEVICE FOUND
         BE    HAVETYPE           PRINT DESC.
         LA    R8,TYPELEN(,R8)    NEXT TABLE ENTRY
         BCT   R0,LOOPTYPE        LOOP
         SPACE 1
*---------------------------------------------------------------------*
*    NO MATCH - RETURN GENERAL CATEGORY ONLY                          *
*---------------------------------------------------------------------*
         LA    R8,MAINTYPE        DEVICE MAIN TABLE
         LA    R0,MAINNUM         NUMBER TO MATCH
LOOPMAIN CLC   HOLDBYT3(1),0(R8)  IF DEVICE FOUND
         BE    HAVEMAIN           PRINT DESC.
         LA    R8,MAINLEN(,R8)    NEXT TABLE ENTRY
         BCT   R0,LOOPMAIN        LOOP
HAVEMAIN MVC   USR0(7),1(R8) SET CATEGORY OR UNKNOWN
         B     SUBEXIT0
         SPACE 1
*---------------------------------------------------------------------*
*    MATCH - LOOK FOR SPECIAL CASES                                   *
*---------------------------------------------------------------------*
HAVETYPE CLI   HOLDBYT3,X'10'  GRAPHICS ?                        89022
         BNE   COPYTYPE      NO                                  89022
         TM    UCBAOF1,UCBOFPTR  PREPARE-TO-READ ?               89022
         BZ    COPYTYPE      NO                                  89022
         CLC   0(2,R8),TYPELEN(R8)   NEXT ONE HAVE SAME CODE?
         BNE   COPYTYPE
         LA    R8,TYPELEN(,R8)    YES; USE BETTER (?) VERSION
COPYTYPE MVC   USR0(6),2(R8)      COPY RESULT TO USER
         SPACE 1
         CLI   HOLDBYT3,UCB3DACC   DISK TYPE ??
         BNE   SUBEXIT0
         TM    HOLDBYT2,UCBRVDEV   REAL DEVICE ?                 82008
         BZ    SUBEXIT0      YES ?                               82008
         LA    R14,USR0+4
         LA    R0,3
TRAILBLK CLI   0(R14),C' '
         BE    TRAILV
         LA    R14,1(,R14)
         BCT   R0,TRAILBLK
TRAILV   MVI   0(R14),C'V'
         SPACE 1                                                 82008
SUBEXIT0 XC    USR15,USR15        CLEAR RETURN CODE
SUBEXITS XC    USNEXT,USNEXT      CLEAR (PHONEY) LINK WORD
         LM    R14,R12,USR14 RESTORE AND SET RETURN VALUES
         BR    R14           RETURN
         SPACE 2
BLANKS   DC    CL8' '        CLEARING
         LTORG ,
         SPACE 1                                                 82008
SUBUCBTY CSECT ,                                                 82008
COMMTBLE DS    0H
         DC    X'0810',CL6'2740X '
         DC    X'0790',CL6'BSC3  '
         DC    X'0710',CL6'1050X '
         DC    X'0690',CL6'BSC2  '
         DC    X'0610',CL6'2741P '
         DC    X'0590',CL6'BSC1  '
         DC    X'0510',CL6'2741C '
         DC    X'0410',CL6'2740  '
         DC    X'0310',CL6'2740CC'
         DC    X'0240',CL6'115A  '
         DC    X'0210',CL6'1060  '
         DC    X'0180',CL6'2260  '
         DC    X'0170',CL6'3270  '
         DC    X'0160',CL6'WTTA  '
         DC    X'0150',CL6'TWX   '
         DC    X'0140',CL6'83B3  '
         DC    X'0130',CL6'1050  '
         DC    X'0120',CL6'1030  '
         DC    X'0110',CL6'1050  '
         DC    X'FF'
         SPACE 2
TYPETBLE DC    X'0801',CL6'2540R'
TYPETBL2 DC    X'0802',CL6'2540P'
         DC    X'0803',CL6'1443'
         DC    X'0804',CL6'2501'
         DC    X'0805',CL6'2520'
         DC    X'0806',CL6'3505'
         DC    X'0808',CL6'1403'
         DC    X'0809',CL6'3211'
         DC    X'080A',CL6'1443'
         DC    X'080B',CL6'3203'
         DC    X'080C',CL6'3525'
         DC    X'080E',CL6'3800'
         DC    X'080F',CL6'AFP1'
         DC    X'0810',CL6'2671'
         DC    X'0811',CL6'4245'
         DC    X'0813',CL6'4248'
         DC    X'0816',CL6'3890'
         DC    X'0817',CL6'3886'
         DC    X'0818',CL6'2495'
         DC    X'0819',CL6'3895'
         DC    X'081A',CL6'1285'
         DC    X'081B',CL6'1287'
         DC    X'081C',CL6'1288'
         DC    X'081D',CL6'1419'
         DC    X'081E',CL6'1419'
         DC    X'081F',CL6'1275'
         DC    X'0820',CL6'1052'
         DC    X'0821',CL6'2150'
         DC    X'0822',CL6'3210'
         DC    X'0823',CL6'3215'
         DC    X'0830',CL6'3213'
         DC    X'083D',CL6'7443'
         DC    X'0842',CL6'3851'
         DC    X'0844',CL6'3540'
         DC    X'084C',CL6'3838'
         DC    X'8001',CL6'2400'
         DC    X'8003',CL6'3400'
         DC    X'8080',CL6'3480'
         DC    X'8081',CL6'3490'
*OLD*    DC    X'8081',CL6'9348'
         DC    X'8082',CL6'3423'
         DC    X'8083',CL6'3590'
         DC    X'2001',CL6'2311'
         DC    X'2002',CL6'2301'
         DC    X'2003',CL6'2303'
         DC    X'2004',CL6'2302'
         DC    X'2005',CL6'2321'
         DC    X'2006',CL6'2305'
         DC    X'2007',CL6'2305'
         DC    X'2008',CL6'2314'
         DC    X'2009',CL6'3330'
         DC    X'200A',CL6'3340'
         DC    X'200B',CL6'3350'
         DC    X'200C',CL6'3375'
         DC    X'200D',CL6'3330-1'
         DC    X'200E',CL6'3380'
         DC    X'200F',CL6'3390'
         DC    X'1002',CL6'2250'
         DC    X'1003',CL6'2260'
         DC    X'1004',CL6'1053'
         DC    X'1005',CL6'2280'
         DC    X'1006',CL6'2282'
*OLD*    DC    X'1007',CL6'MOD 85 CONSOLE'                       81047
         DC    X'1008',CL6'3066'
         DC    X'1009',CL6'3277'           IF PTREAD OPTION OFF
         DC    X'1009',CL6'3278'           IF PTREAD OPTION ON
         DC    X'100A',CL6'3284'
         DC    X'100B',CL6'3286'           IF PTREAD OFF
         DC    X'100B',CL6'3287'           IF PTREAD ON
         DC    X'100C',CL6'3158'
         DC    X'100D',CL6'3036'
         DC    X'100E',CL6'3138'
         DC    X'100F',CL6'3148'
         DC    X'4011',CL6'2702-1'
         DC    X'4012',CL6'2701-1'
         DC    X'4013',CL6'2703-1'
         DC    X'4014',CL6'2955'            CE TERMINAL'
         DC    X'401F',CL6'5098'
         DC    X'4015',CL6'3705-1'
         DC    X'4021',CL6'2702-2'
         DC    X'4022',CL6'2701-2'
         DC    X'4023',CL6'2703-2'
         DC    X'4025',CL6'3705-2'
         DC    X'4031',CL6'2702TTY'
         DC    X'4032',CL6'2701TTY'
         DC    X'4033',CL6'2703TTY'
         DC    X'4041',CL6'2702TTY1'
         DC    X'4042',CL6'2701TTY1'
         DC    X'4043',CL6'2703TTY1'
         DC    X'4051',CL6'2702TTY2'
         DC    X'4052',CL6'2701TTY2'
         DC    X'4053',CL6'2703TTY2'
         DC    X'4061',CL6'2702WTTA'
         DC    X'4062',CL6'2701WTTA'
         DC    X'4063',CL6'2703WTTA'
         DC    X'4071',CL6'2702-S1'
         DC    X'4072',CL6'2701-S1'
         DC    X'4073',CL6'2703-S1'
         DC    X'4081',CL6'2702-3'
         DC    X'4082',CL6'2701-3'
         DC    X'4083',CL6'2703-3'
         DC    X'4091',CL6'2702-S2'
         DC    X'4092',CL6'2701-S2'
         DC    X'4093',CL6'2703-S2'
         DC    X'40F1',CL6'3791L'
         DC    X'4100',CL6'CTCA'
         DC    X'4101',CL6'CTC-S'
         DC    X'4102',CL6'CTC-ES'
         DC    X'4103',CL6'CTC-6000'
         DC    X'4104',CL6'3172 CTC'
         DC    X'4105',CL6'OSA'
         DC    X'4106',CL6'OSA'
         DC    X'4107',CL6'OSA' RESERVED 07'                    GP97220
         DC    X'4108',CL6'OSA' RESERVED 08'                    GP97220
         DC    X'4109',CL6'OSA' RESERVED 09'                    GP97220
         DC    X'410A',CL6'OSA' RESERVED 0A'                    GP97220
         DC    X'410B',CL6'OSA' RESERVED 0B'                    GP97220
         DC    X'410C',CL6'OSA' RESERVED 0C'                    GP97220
         DC    X'410D',CL6'OSA' RESERVED 0D'                    GP97220
         DC    X'410E',CL6'OSA' RESERVED 0E'                    GP97220
         DC    X'410F',CL6'OSA' RESERVED 0F'                    GP97220
         DC    X'0000',CL6'Dummy'
         DC    X'0100',CL6'Dummy'
TYPETBLN DC    X'FFFF',CL6'UNKNOWN'
TYPELEN  EQU   (TYPETBL2-TYPETBLE)     LENGTH OF ONE ENTRY
TYPENUM  EQU   (TYPETBLN-TYPETBLE)/TYPELEN  NUMBER OF ENTRIES
         SPACE 2
MAINTYPE DC    X'80',CL7'Tape'
MAINTYP2 DC    X'41',CL7'CTC'
         DC    X'40',CL7'Comm'
         DC    X'20',CL7'DASD'
         DC    X'10',CL7'Display'
         DC    X'08',CL7'UnitRcd'
         DC    X'04',CL7'CharRdr'
MAINTYPN DC    X'FF',CL7'Unknown'
MAINLEN  EQU   (MAINTYP2-MAINTYPE)     LENGTH OF ONE ENTRY
MAINNUM  EQU   (MAINTYPN-MAINTYPE)/MAINLEN  NUMBER OF ENTRIES
         SPACE 2
USERSAVE DSECT ,
         DS    F             RESERVED
USPREV   DS    A             PREVIOUS SAVE AREA
USNEXT   DS    A             NEXT SAVE AREA
USR14    DS    A             RETURN ADDRESS
USR15    DS    F             RETURN CODE
USR0     DS    CL4           RETURN LEFT PORTION OF TYPE
USR1     DS    CL4           RETURN RIGHT PORTION OF TYPE
USR2     DS    11A             OTHER REGISTERS
         ORG   USNEXT        RE-USE SAVE AREA
HOLDBYT1 DC    0XL4'0',X'0'  SAVED UCBTBYT1
HOLDBYT2 DC    X'00'         SAVED UCBTBYT2
HOLDBYT3 DC    X'00'         SAVED UCBTBYT3
HOLDBYT4 DC    X'00'         SAVED UCBTBYT4
         ORG   USR15
HALF     DC    H'0'
         ORG   ,
         SPACE 2
         IEFUCBOB ,                                              82008
         AIF   (&MVS).UCBRR                                      82008
UCBRR    EQU   UCB2OPT2      SHARED DASD OPTION                  82008
UCBRPS   EQU   UCB2OPT3      RPS FEATURE                         82008
UCBRVDEV EQU   UCB2OPT4      VIRTUAL DEVICE                      82008
.UCBRR   SPACE 1
         YREGS ,
         END   ,
