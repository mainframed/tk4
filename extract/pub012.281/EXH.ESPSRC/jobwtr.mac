JOWT    TITLE 'J O B W T R  ***  OS QUEUE JOB PRINTER - MAIN PROCESSOR'
         COPY  OPTIONGB      DEFINE GLOBALS                      82095
         SPACE 1                                                 82095
         SYSPARM LIST=YES    SET AND DISPLAY GLOBALS             82095
         EJECT
*
*              THIS MODULE IS A SPECIALIZED WRITER MODULE
*
*                   IT USES QUEUE MANAGEMENT ROUTINES TO INTERFACE
*                        WITH THE SYSOUT QUEUES
*                   AND OUTPUTS THE LINES USING QSAM
*
         PRINT &PRTSOR                                           82095
JOBWTR   SAVEM ZERO12,BASE=R12,PARM=R9                           82095
*        AMS MODIFICATIONS :
*              JOBNAME OF %ALL WILL PROCESS ALL JOBS ON ONE QUEUE
*                            ADDITIONAL QUEUES ARE IGNORED
*              IF LINECOUNT REACHES 'MAXLINES' WILL TERMINATE AT END
*                   OF CURRENT JOB
*              SUPPORT FOR PRINTING/RE-INITIALIZING SYSVLOG DATASETS
*                   ADDED - REQUIRES STEP TO RUN IN KEY ZERO
*              INPUT FORMAT SUPPORT ADDED FOR RECFM M, V AND S
*
*        AUTHOR UNKNOWN - WE GOT IT FROM DCA
*
*        ASSEMBLE JOBWTR & JOBWTR1 - ENTRY JOBWTR
*              LKED.SYSLIB DD     =SYS1.LINKLIB
*        EXECUTION REQUIRES 'ASMANY3' SEPARATOR MODULE IN STEPLIB
*
*
*                   INITIALIZE TO DEFAULT SYSOUT QUEUES
*
         MVI   QUEUES,C'S'         CLASS S IS THE DEFAULT SYSOUT Q
         MVI   JOBCOUNT+1,1             DEFAULT COUNT = 1
         MVI   JOBNAMES,C' '            DEFAULT JOB NAME
         MVC   JOBNAMES+1(63),JOBNAMES       IS BLANK - TOP PRTY JOB
         MVI   JOBEMARK,C' '       MARK THE END OF THE JOBS
         MVI   QENDMARK,0          MARK THE END OF THE QUEUES
         LA    R2,LINE
         LA    R3,WTRCODE
         STM   R2,R3,PARMBUF
*
*              FIRST CHECK FOR PARMS AND TRANSLATE THEM
*
         LA    R1,0(,R9)     CLEAN ADDRESS OF PARM ADDRESS       82095
         LTR   R1,R1         ANY ?                               82095
         BZ    PARMERR       NO; TOO BAD                         82095
         ICM   R1,7,1(R1)    GET AND TEST PARM ADDRESS           82095
         LH    R2,0(,R1)     GET PARM LENGTH
         LTR   R2,R2
         BZ    PARMEND
         BM    PARMERR       JUST IN CASE                        82095
*              INSERT THE QUEUES, FIRST FIND A COMMA
         LA    R3,2(,R1)      POINTER TO PARMS
         LA    R6,2(R2,R1)    POINT TO CHARACTER PAST THE PARMS
         XR    R4,R4          QUEUE COUNTER
QCOMP    CLI   0(R3),C','     IS IT A COMMA?
         BE    ENDQPARM       IF SO WE'RE AT THE END OF THE CLASS LIST
         CH    R4,H8          CHECK FOR MAXIMUM NUMBER OF QUEUES
         BNL   PARMERR        ERROR - MORE THAN 8 SYSOUT Q'S
         IC    R5,0(,R3)      GET THE CLASS
         STC   R5,QUEUES(R4)  STORE IT INTO THE QUEUE LIST
         LA    R4,1(,R4)      BUMP QUEUE COUNTER
         LA    R3,1(,R3)      BUMP PARM COUNTER
         CR    R3,R6          ARE WE AT THE END OF THE PARMS
         BL    QCOMP          NO, GO CHECK FOR MORE QUEUES
         B     PARMEND        YES - NO MORE PARMS, DEFAULT JOB NAME
*
*              FINISHED WITH QUEUES, GET JOB NAMES, IF ANY
*
ENDQPARM LA    R3,1(,R3)      GET PAST THE COMMA, POINT TO START OF
         XR    R7,R7               COUNTER FOR JOBNAMES
         LR    R4,R3               JOB NAME
JCOMP    CLI   0(R4),C','     CHECK FOR END OF JOB NAME
         BE    ENDJOB
         LA    R4,1(,R4)      BUMP COUNTER
         CR    R4,R6          CHECK FOR END OF PARMS
         BL    JCOMP          GET MORE CHARS FOR THE JOB NAME
ENDJOB   LR    R5,R4          END OF JOB NAME
         SR    R5,R3            MINUS START OF JOB NAME GIVES COUNT
         CH    R5,H8          COMPARE TO 8
         BH    PARMERR        TOO MANY CHARS IN JOB NAME
         LTR   R5,R5          ZERO CHARACTERS
         BZ    PARMERR          ALSO AN ERROR
         CH    R7,H8          CHECK THE NUMBER OF JOB NAMES,
         BNL   PARMERR             8 IS THE MAXIMUM
         LR    R8,R7          JOB NAME COUNTER
         SLA   R8,3           * 8 - LENGTH OF JOB NAME
         LA    R8,JOBNAMES(R8)  RELOCATE
         BCTR  R5,0           DOWN 1 FOR EX
         EX    R5,MOVEJOBN    MOVE IN THE JOB NAME
         LA    R7,1(,R7)      BUMP THE JOB NAME COUNTER
         CR    R4,R6          CHECK FOR END OF PARMS
         BNL   PARMEND           ALL SET
         LA    R3,1(,R4)      GET PAST THE COMMA
         LR    R4,R3
         CR    R4,R6          CHECK FOR COMMA AS LAST CHAR OF PARMS
         BL    JCOMP             GET THE NEXT JOB
PARMEND  CLC   =CL8'%ALL',JOBNAMES  REQUEST FOR ALL JOBS ?
         BNE   PARMEND2      NO
         MVC   JOBNAMES+8(8),JOBNAMES  FAKE IT OUT AS BLANK JOBNAME
         MVC   JOBNAMES(8),JOBNAMES+16  + %ALL IN NEXT ONE
PARMEND2 MVC   SEPSYS(8),=CL8'&SYSTEM '  SET ASSEMBLED SYSTEM    82095
         L     R1,CVTPTR     GET CVT                             82095
         USING CVTMAP,R1     DECLARE IT                          82095
         L     R1,CVTSMCA    GET THE SMF WORK AREA               82095
         DROP  R1                                                82095
         MVC   SEPSYS+4(4),16(R1)  MOVE SMCASID                  82095
         EJECT
*
*              CONVERT SYSOUT CLASSES TO QUEUE NUMBERS
*
*              ALPHANUMERIC CHARACTER TO BINARY SYSOUT QUEUE
         LA    R6,QUEUES
TRANA    CLI   0(R6),0        CHECK FOR ZERO - NO Q SPECIFIED
         BE    ENDQTRAN       QUIT PROCESSING IF SO
         XR    R4,R4          CLEAR R4
         LA    R5,29          29 - BASE FOR 0-9 X'FX'
         TM    0(R6),X'30'    CHECK FOR 'FX'
         BO    TRANB
         LA    R5,2           2  BASE FOR A-I
         BZ    TRANB          ZERO - X'CX'
         LA    R5,11          11 BASE FOR J-R
         TM    0(R6),X'20'    CHECK FOR X'E0'
         BZ    TRANB
         LA    R5,19          BASE FOR S-Z
TRANB    IC    R4,0(,R6)      GET THE CHAR
         N     R4,BINARY      LEAVE ONLY 4 BITS
         AR    R4,R5          ADD THE BASE
         STC   R4,0(,R6)      AND STORE THE BINARY Q NUMBER
         LA    R6,1(,R6)      INCREMENT Q COUNTER
         LA    R3,QUEUES+7    END OF QUEUES
         CR    R6,R3          CHECK FOR END OF QUEUES
         BNH   TRANA          NOT THROUGH YET
ENDQTRAN EQU   *
         DEVTYPE   SYSOUTMD+DCBDDNAM-IHADCB,SYSOUT  LOOK FOR DD CARD
         LTR   R15,R15       ANY ?
         BNZ   RROR          COMPLAIN ABOUT IT
         CLI   SYSOUT+2,X'08'  UNIT RECORD OUTPUT DEVICE ?
         BNE   SETOPN        NO; LEAVE IT ALONE
         MVC   SYSOUTMD+DCBBLKSI-IHADCB(2),SYSOUTMD++DCBLRECL-IHADCB
         MVI   SYSOUTMD+DCBBUFNO-IHADCB,100    INCREASE BUFFERS  82095
SETOPN   MVC   SYSOUT(SYSOUTLN),SYSOUTMD  MOVE IN MODEL DCB
         LA    R1,SYSOUT           GET ADDRESS OF DCB
         ST    R1,SYSOPEN          STORE INTO OPEN PARM
         MVI   SYSOPEN,143         SET FLAGS- OUTPUT, LAST PARM
         LA    R1,SYSOPEN          GET ADDRESS OF LIST
         SVC   19                  ISSUE OPEN SVC
         LA    R9,16         SET PROVISIONAL RETURN CODE         82095
         TM    SYSOUT+DCBOFLGS-IHADCB,DCBOFOPN   DID IT OPEN ?   82095
         BNO   LEAVE
         EJECT
*
*                   NOW JOBNAMES AND QUEUES ARE SET
*                   FOR EACH JOB, PASS THROUGH THE SET OF QUEUES
*                        GETTING ALL SYSOUT FOR THAT JOB
*
         LA    R2,JOBNAMES         ADDRESS OF JOB NAME
         XR    R0,R0               INPUT / OUTPUT CODE
         LA    R1,QUEUES           START ADDRESS OF QUEUES
         ST    R1,SAVEQ            SAVE PRESENT Q ADDRESS
LINELOOP EQU   *
         MVC   LINE(8),0(R2)
         STC   R0,WTRCODE
         ST    R1,PARMQ
         LA    R1,PARMQ
         CALL  JOBWTR1
         XR    R0,R0               ZERO REG ZERO
         IC    R0,WTRCODE
         L     R1,PARMQ
         LTR   R0,R0               WAS A LINE RETURNED
         BNZ   NOLINE              NO
         SPACE 1
SEPLINE  NOP   PUTPUT        SEPARATE BETWEEN JOBS ONLY
         OI    SEPLINE+1,X'F0'    MAKE BR NEXT TIME
         MVC   SEPJOB,RETJOB  SET NAME OF RETURNED JOB
         MVI   SEPFG,0       SET PAGE EJECT
         MVC   SEPDDN,SYSOUTMD+DCBDDNAM-IHADCB  COPY DDNAME      82095
         MVC   SEPBLK,SYSOUT+DCBBLKSI-IHADCB  COPY BLKSIZE       82095
         MVC   SEPRECL,SYSOUT+DCBLRECL-IHADCB  COPY LRECL        82095
         MVC   SEPRECF,SYSOUT+DCBRECFM-IHADCB  COPY RECORD FORMAT
         LA    R1,SYSOUT     BUILD OPEN/CLOSE PARM LIST
         ST    R1,SYSOPEN
         MVI   SYSOPEN,X'80'  REQUEST CLOSE
         LA    R1,SYSOPEN
         SVC   20            CLOSE
         LA    R1,SEPARM     GET SEPARATOR PARAMETERS
         LINK  EP=ASMANY3    SEPARATE
         L     R15,CURLINES  GET CURRENT LINE COUNT
         LA    R15,80(,R15)  ADD SEPARATOR I/O
         ST    R15,CURLINES  SAVE
         MVI   SYSOPEN,143   MAKE OPEN LIST
         LA    R1,SYSOPEN
         SVC   19            REQUEST OPEN
         MVI   LINE,C'1'     FORCE ANOTHER PAGE EJECT
         SPACE 1
PUTPUT   PUT   SYSOUT,LINE         YES, WRITE IT
         L     R15,CURLINES  GET CURRENT LINE COUNT
         LA    R15,1(,R15)   UP BY ONE
         ST    R15,CURLINES  STASH BACK
         XR    R0,R0               ZERO REG ZERO
         L     R1,SAVEQ            POINT BACK TO Q
         B     LINELOOP            AND LOOP FOR MORE LINES
NOLINE   SLA   R0,2           MULTIPLY BY 4
         LR    R3,R0          GET INTO R3 FOR INDEX REG
         B     *(R3)          BRANCH TO APPROPRIATE CODE
         B     DELETE              01   PREVIOUS LINE = LAST OF DATA ST
         B     LASTDATA            02   PREVIOUS LINE = LAST DATA
         B     JOBNOTQ             03   JOB NOT ON Q
         B     JOBNOTQ             04   NO WORK ON Q
         B             RROR        05   NO ACTION
         B     LASTDS              06   DELETION
         B             RROR        07   HARDCOPY
         SPACE 3
LASTDS   XR    R0,R0          PREVIOUS LINE IS LAST OF DATA SET
         L     R1,SAVEQ       GO GET ANY MORE DATA SETS
         B     LINELOOP
*
*                  DELETE THIS DATA SET
*
DELETE   LA    R0,3
         LA    R1,SAVEQ            SAME Q
         B     LINELOOP
         SPACE 5
JOBNOTQ  CLC   =CL8'%ALL',JOBNAMES+8   REQUEST FOR ALL JOBS ?
         BE    WRAPUP        YES; WE ARE ALL DONE
         B     LASTDATB      GO SEE IF MORE QUEUES TO BE DONE
*
*                   FINISHED WITH THIS Q, CHECK FOR MORE
*
LASTDATA XI    LDP,1         HERE BEFORE ?
         BZ    LASTDATB      YES; SEE IF MORE QUEUES TO BE DONE
         LA    R0,4          SET FOR JOB DELETE/REQUEUE
         L     R1,SAVEQ      RESTORE JUST IN CASE
         B     LINELOOP
         SPACE 1
LASTDATB L     R1,SAVEQ       GET ADDRESS OF CURRENT Q
         CLI   1(R1),0        CHECK NEXT ONE FOR A VALID Q
         BE    NOMOREQS       NO MORE QUEUES
         LA    R1,1(,R1)      BUMP Q COUNTER
         ST    R1,SAVEQ       STORE THE ADDRESS
LASTDATE NI    SEPLINE+1,X'0F'    FORCE SEPARATOR OUT
         MVI   LDP,0         DON'T TRUST ANYONE
         L     R1,SAVEQ        RESTORE REG 1
         XR    R0,R0          CODE 0 - GET A LINE
         B     LINELOOP       AND LOOP TO GET LINES
         SPACE 5
NOMOREQS CLC   =CL8'%ALL',JOBNAMES+8   REQUEST FOR EVERYTHING ?
         BE    NOMOREQU      YES; DO SPECIAL
NOMOREQT LA    R2,8(,R2)      INCREMENT THE JOB COUNTER
         CLI   0(R2),C' '     VALID JOB
         BE    WRAPUP         NO, CLOSE FILE AND TERMINATE
NOMOREQU LA    R1,QUEUES      ADDRESS OF QUEUES
         ST    R1,SAVEQ       SAVE IT
         CLC   CURLINES,MAXLINES  AT OR OVER MAXIMUM LINES ?
         BNL   WRAPUPM       YES; GET OUT                        82095
         XR    R0,R0          CODE 0 - GET A LINE
         CLC   =CL8'%ALL',JOBNAMES+8   SPECIAL REQUEST ?
         BNE   LINELOOP         AND LOOP TO GET THEM
         B     LASTDATE      GO TO TRY FOR MORE JOBS
         EJECT
RROR     ABEND 18,DUMP        BAD ERROR - WHAT'S WRONG
         EJECT
WRAPUPM  WTO   '*** OUTPUT EXCEEDS 30K LINES - PLEASE RUN AGAIN ***',  *
               ROUTCDE=(1,11),DESC=2  (USUALLY A START JOB)      82095
WRAPUP   LA    R1,SYSOUT           CLOSE SYSOUT
         ST    R1,SYSOPEN
         MVI   SYSOPEN,X'80'
         LA    R1,SYSOPEN          ADDRESS OF OPEN LIST
         SVC   20                  ISSUE CLOSE SVC
FINI     XR    R9,R9         SET ZERO RETURN CODE                82095
         B     LEAVE
PARMERR  WTO   '*** PARM FIELD ERROR ***',ROUTCDE=11             82095
         LA    R15,16
LEAVE    ENDM  RC=(R9)       EXIT WITH RETURN CODE               82095
         SPACE 2                                                 82095
BINARY   DC    0A(0),XL4'F'                                      82095
MAXLINES DC    F'30000'      MAX LINES PUT PRIOR TO TERMINATION
         SPACE 1
SYSOUTMD DCB   DSORG=PS,MACRF=PM,DDNAME=SYSOUT,                        *
               RECFM=FBA,LRECL=133,BLKSIZE=1330,BUFNO=10,OPTCD=CZ
MOVEJOBN MVC   0(1,R8),0(R3)
H8       DC    H'8'
         EJECT
SAVE     DSECT ,             CONTINUE SAVEM WORK AREA            82095
QUEUES   DS    CL8
QENDMARK DS    C                   END OF QUEUES
LDP      DS    X             SWITCH FOR Q DELETE RECURSION
JOBCOUNT DS    H
CURLINES DC    F'0' .        CURRENT LINE COUNT
JOBNAMES DS    8CL8
JOBEMARK DS    C
WTRCODE  DS    C
RETJOB   DS    CL8           JOBNAME RETURNED BY Q SEARCH
LINE     DS    CL133
PARMQ    DS    F
PARMBUF  DS    F
PARMCODE DS    F
SAVEQ    DS    F
SYSOPEN  DS    F
SEPARM   DS    0F            SEPARATOR MODULE CALLING PARMS
SEPJOB   DC    CL8'JOB NAME'   JOB BEING PROCESSED
SEPSYS   DC    CL8'SYS SMCA'  IDENTIFY RUNNING SYSTEM / ID       82095
SEPFG    DC    X'0'          EJECT SUPPRESS FLAG
SEPLN2   DC    CL8' '         SECOND LINE - NOT USED
SEPDDN   DC    CL8'OUT DDNM'  OUTPUT DDNAME
SEPBLK   DC    AL2(0)        OUTPUT BLOCK SIZE
SEPRECL  DC    AL2(0)        OUTPUT RECORD LENGTH
SEPRECF  DC    X'0'          OUTPUT RECORD FORMAT
SYSOUT   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSOUT
SYSOUTLN EQU   *-SYSOUT
SAVEND   EQU   *             END OF DYNAMIC WORK AREA            82095
         SPACE 2                                                 82095
         PRINT &PRTSYS                                           82095
CVT      DSECT ,                                                 82095
         CVT   DSECT=YES                                         82095
         SPACE 1                                                 82095
         DCBD  DSORG=PS                                          82095
         END   JOBWTR
