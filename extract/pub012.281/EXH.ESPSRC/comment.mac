COMMENT  TITLE 'C O M M E N T  ***  PAUSE AND OPERATOR COMMENT ROUTINE'
         PUNCH ' ORDER COMMENT(P)'    MAKE DEBUGGING EASIER     GP02236
         PUNCH ' ALIAS PAUSE '                                  GP02236
         SPACE 1
***********************************************************************
*                                                                     *
*    EVERY DATA CARD ON SYSIN IS DISPLAYED ON THE CONSOLE             *
*                                                                     *
*    INPUT - A DATA SET ON SYSIN                                      *
*                                                                     *
*    OUTPUT - ON OPERATOR'S CONSOLE                                   *
*           - ON SYSPRINT IF A DD FOR IT IS PRESENT                   *
*                                                                     *
*      THIS ROUTINE IS RE-ENTRANT                                     *
*                                                                     *
*    IF COLUMN 1 OF A DATA CARD CONTAINS AN ASTERISK, THE             *
*      MESSAGE IS TYPED OUT WITH A WTOR REQUIRING A REPLY.            *
*      A REPLY OF 'CANCEL' WILL ABEND WITH SYSTEM CODE 222            *
*                                                                     *
*    A WTOR CARD OR AN END OF FILE CARD TERMINATE THE PROGRAM.        *
*                                                                     *
***********************************************************************
         SPACE 1
         COPY  OPTIONGB      DEFINE GLOBALS                      93212
         SPACE 1                                                 93212
         SYSPARM LIST=YES    SET AND LIST GLOBALS                93212
         SPACE 1                                                 93212
         AIF   (NOT &MVSSP).NOSP                                 93212
         SPLEVEL SET         SET SPLEVEL FROM MACLIB             93212
.NOSP    PRINT &PRTSOR       SAVE A TREE                         93212
COMMENT  PGMHEAD ZERO15,BASE=R12,BNDRY=PAGE,                           *
               ENTRY=PAUSE,ENTNO=FLAGS                          GP02236
         SERVINIT ,          LOAD THE SERVICE ROUTINE            93212
         LA    R1,REPLY      GET REPLY AND ECB ADDRESSES        GP06328
         LA    R0,ECB                                           GP06328
         STM   R0,R1,WTOR    AND STASH INTO WTOR                GP06328
         LA    R0,L'REPLY    MAX REPLY LENGTH                   GP06328
         STC   R0,WTOR+0     INTO REPLY ADDRESS WORK            GP06328
         LA    R1,L'INCARD   MAXIMUM WTO LENGTH                 GP06328
         STH   R1,WTO        INTO WTO                           GP06328
         LA    R1,DOMIDS
         ST    R1,DOMPTR     INITIALIZE DOM ID AREA POINTER
         SERVLOAD @INPREAD,@PRINTER    LOAD SERVICE ROUTINES    GP06328
         PRTOPEN SYSPRINT,OPT=NOWTO                              93212
         CH    R15,=H'4'     GOOD ?                              93212
         BNH   *+8           YES                                 93212
         OI    FLAGS,FGNOPRT  ELSE SET NO PRINTER                93212
         INPOPEN SYSIN,OPT=NOWTO                                 93212
         CH    R15,=H'4'     GOOD ?                              93212
         BH    NOINPUT       NO; FAIL                            93212
         INPGET ,            GET FIRST INPUT RECORD              93212
         CH    R15,=H'4'     WAS THERE ONE ?                     93212
         BNL   NOINPUT       NO                                  93212
         INPKEEP ,           PRESERVE INPUT RECORD               93212
*    START PROCESSING
*
         PRTLIST HEADER,TITLE=1  BUILD THE TITLE
         PRTSPACE 2          WRITE 2 BLANK LINES                GP02236
         BAS   R7,BOXLINE1    PRINT A LINE OF ASTERISKS         GP02236
         BAS   R7,BOXLINE2    INSERT A BLANK LINE WITH AN ** BORDER
*
*    LOOP HERE TILL INPUT IS ALL READ OR AN * CARD COMES UP.
*
LOOP     INPREAD ,           READ A CARD                         93212
         BXH   R15,R15,EODAD  END-FILE OR I/O ERROR              93212
         MVC   INCARD,0(R1)  MOVE TO LOCAL BUFFER                93212
         BAS   R7,INPECHO    PRINT IT                           GP02236
         TRT   INSEQ,TRTSEQ  SEE WHETHER EDIT OR WYLBUR SEQUENCE #
         BNZ   NSEQCARD      NON-NUMERICS                       GP02236
         MVC   INSEQ,BLANKS  CLEAR SEQUENCE FIELD               GP02236
NSEQCARD CLI   INCARD,C'*'   IS IT A WTOR REQUEST ?              93212
         BE    LOOPEND       YES. USE WTOR INSTEAD              GP02236
         BAS   R8,TYPE        TYPE THE CARD ON-LINE              93212
         INCH  MENE,WORK=R15 GET NUMBER OF NON-DELETABLE MESSAGES DONE
         CH    R15,=H'12'    REASONABLE ?                        93212
         BL    LOOP          YES (?)                             93212
         B     LOOP          AND GO TO READ NEXT CARD
         SPACE 2                                                GP02236
*    COME HERE FOR AN END OF INPUT
EODAD    TM    FLAGS,FGPAUSE  PGM=PAUSE NEEDS A WTOR ?          GP02236
         BZ    ENDJOB        ELSE WE'RE ALL DONE                GP02236
         MVC   INCARD,BLANKS
         MVC   INCARD(L'PAUSEMSG),PAUSEMSG                      GP02236
         L     R1,PSATOLD-PSA  GET MY TCB ADDRESS               GP02236
         L     R1,TCBTIO-TCB(R1)  GET TIOT ADDRESS              GP02236
         MVC   INCARD+4(8),0(R1)  MOVE TIOCNJOB TO MESSAGE      GP02236
LOOPEND  BAS   R8,RTYPE      ASK FOR A RESPONSE                 GP02236
         SPACE 1                                                GP02236
ENDJOB   BAS   R7,BOXLINE2    ANOTHER BORDERED BLANK LINE        93212
*HUH?*   BAS   R8,TYPE        TYPE IT                            93212
         BAS   R7,BOXLINE1                                      GP02236
         SPACE 2
*    EXIT PROCESSING
*
EXIT     SERVTERM ,          CLOSE AND FREE EVERYTHING           93212
         TM    FLAGS,FGCAN   ABEND REQUESTED ?
         BZ    EXITCC
         L     1,=X'00222000'
         ABEND (1)
         SPACE 2
EXITCC   L     R9,RETCODE    LOAD RETURN CODE                   GP06328
         PGMEXIT RC=(R9)                                        GP02236
         EJECT ,
*
*    COULD NOT OPEN SYSIN ??????
*
NOINPUT  PRTV  NOSYNMSG,CC=NO                                   GP02236
         SERVTERM ,                                              93212
         ABEND 2540          ABEND WITHOUT INPUT
         SPACE 2
*
*    SHOW THE CARD ON THE CONSOLE
*
TYPE     BAS   R14,HOWMANY   RESET THE NUMBER OF SIGNIFICANT CHARACTERS
         WTO   MF=(E,WTO)
         L     R14,DOMPTR    GET DOM ID POINTER
         LA    R15,DOMIDEND-4  ROOM FOR MORE ?
         CR    R14,R15
         BNLR  R8            BR NOT LOW
         ST    R1,0(,R14)    SAVE DOM ID
         LA    R14,4(,R14)   SET NEXT SAVE ADDRESS
         ST    R14,DOMPTR    INTO POINTER
         BR    R8
*
*    SEND TO OPERATOR, AND AWAIT REPLY
*
RTYPE    BAS   R14,HOWMANY   RESET THE NUMBER OF SIGNIFICANT CHARACTERS
         MVI   WTO+2,X'80'   SET DESC/ROUT PRESENT
         LH    R14,WTO       GET TEXT LENGTH
         LA    R14,WTO+4(R14)  BYTE AFTER TEXT
         MVC   0(4,R14),=X'40004000'  DESC=2, ROUTCDE=2
         WTOR  ,REPLY,L'REPLY,ECB,ROUTCDE=2,DESC=2,MF=(E,WTOR)
         WAIT  ,ECB=ECB
         OC    DOMIDS+1(3),DOMIDS+1
         BZ    RTYPEDOM      SKIP IF NO IDS AT ALL              GP02236
         L     R1,DOMPTR     ELSE GET POINTER
         SH    R1,=H'4'      POINT TO LAST ENTRY
         OI    0(R1),X'80'   SET END OF LIST BIT
         DOM   MSGLIST=DOMIDS   DELETE ALL
RTYPEDOM OC    REPLY,BLANKS  NASTY UPPER CASE                   GP02236
         BAS   R7,REPECHO    PRINT INPUT                         93212
         CLC   INCARD(6),=C'CANCEL'                             GP02236
         BNER  R8                                               GP02236
         OI    FLAGS,FGCAN   REQUEST CANCELLATION                93212
         BR    R8                                                93212
         SPACE 1
*    ELIMINATE THE RIGHT ADJUSTED BLANKS FROM THE ON-LINE PRINTOUT
*    BY DECREASING THE CHARACTER COUNT APPROPRIATELY
*
HOWMANY  LH    R2,=H'-1'     SET INCREMENT OF -1                GP02236
         LA    R1,INCARD+L'INCARD-1  START WITH LAST BYTE       GP02236
         LA    R0,C' '                                           93212
         LA    R3,VLINE-1    WHERE TO STOP                      GP02236
HOWLOOP  CLM   R0,1,0(R1)    TRAILING BLANK ?                   GP02236
         BNE   HOWDONE       NO.                                GP02236
         BXH   R1,R2,HOWLOOP TRY AGAIN                           93212
HOWDONE  SR    R1,R3         GET NUMBER OF NON-BLANKS THERE     GP02236
         STH   R1,VLINE      AND STORE CORRECT NUMBER IN WTO LIST
         STCM  R1,12,VLINE+2    ALSO ZERO FLAGS FIELD
         BR    R14                                               93212
         EJECT
INPECHO  PRTLIST ASTCARD                                        GP02236
         BR    R7                                               GP02236
REPECHO  PRTLIST OPREPMSG                                       GP02236
         BR    R7                                               GP02236
         SPACE 1                                                GP02236
*  BOXLINE1 - PRINT A SINGLE BLANK LINE OF ASTERISKS            GP02236
*  BOXLINE2 - PRINT A SINGLE BLANK LINE WITH ** ON EACH BORDER  GP02236
*
BOXLINE1 PRTLIST ASTFULL                                        GP02236
         BR    R7                                                93212
BOXLINE2 PRTLIST ASTLINE                                        GP02236
         BR    R7                                                93212
         EJECT
         PRINT NOGEN
*    READ-ONLY STORAGE
         SPACE 2
SYSIN    INPWORK SYSIN,EDIT=128,WIDTH=80                         93212
SYSPRINT PRTWORK SYSPRINT,TITLE=5
         SPACE 1                                                GP02236
ASTFULL  FDPRT '*',NL                                           GP02236
         FDREPT 131,C'*'                                        GP02236
         FDPRT *END                                             GP02236
         SPACE 1                                                GP02236
ASTLINE  FDPRT '**',NL                                          GP02236
         FDPRT '**',RADJ,LEN=130                                GP02236
         FDPRT *END                                             GP02236
         SPACE 1                                                GP02236
ASTCARD  FDPRT '**',NL,LEN=26                                   GP02236
         FDPRT INCARD                                           GP02236
         FDPRT '**',RADJ,LEN=26                                 GP02236
         FDPRT *END                                             GP02236
         SPACE 1                                                GP02236
PRTCARD  FDPRT ' ',NL,LEN=26                                    GP02236
         FDPRT INCARD                                           GP02236
         FDPRT *END                                             GP02236
OPREPMSG FDPRT '**  OPERATOR REPLY WAS: ''',NL                  GP02236
         FDPRT REPLY,DEB                                        GP02236
         FDPRT ''''                                             GP02236
         FDOPT SBA=(*,131)                                      GP02236
         FDPRT '**'                                             GP02236
         FDPRT *END                                             GP02236
         SPACE 2
NOSYNMSG VCON  'COULD NOT OPEN SYSIN DATA SET.'                 GP02236
PAUSEMSG DC    C'JOB XXXXXXXX NEEDS REPLY TO CONTINUE'          GP02236
HEADER   FDOPT NL,CC=C'#'    AUTO DATE AND PAGE NUMBERS
         FDTM  FLAGS,FGPAUSE,BNZ=HEADERP
         FDPRT 'COMMENT'
         FDGOTO HEADERC
HEADERP  FDPRT 'PAUSE'
HEADERC  FDPRT 'UTILITY PROGRAM',PADL
         FDPRT *END
         SPACE 1                                                GP02236
BLANKS   DC    CL80' '       LOTS OF SPACE                      GP02236
         SPACE 1
TRTSEQ   DC    256AL1(4)     PRESET ALL FOR NON-NUMERIC         GP02236
         TRENT TRTSEQ,0,C' '      ALLOW BLANK                   GP02236
         TRENT TRTSEQ,0,C'.'      ALLOW PERIOD (WYLBUR NNNN.NNN)
         TRENT TRTSEQ,0,(C'0',10) ALLOW DIGITS                  GP02236
         ORG   ,             JUST IN CASE                       GP02236
         SPACE 3                                                GP02236
SAVE     DSECT ,             SAVE/WORK AREA CONTINUED
         SERVDEFS ,          EXPAND SERVICE ROUTINE SUPPORT     GP06328
ECB      DS    F                                                GP02236
WTOR     DS    2A    1/6     START OF WTOR REPLY/ECB            GP06328
WTO      DS    0F    2/6     WTO START LIST ADDR                GP06328
VLINE    DS    2Y    3/6     V-FORMAT HEADER                    GP02236
INCARD   DS    CL80  4/6     INPUT CARD IMAGE                   GP02236
INSEQ    EQU   INCARD+72,8,C'C'  POTENTIAL SEQUENCE NUMBER      GP02236
         DS    XL4   6/6     DESC/ROUT CODE                     GP06328
         SPACE 1                                                GP02236
MENE     DC    H'0'          COUNTER OF NON-DELETED MESSAGES ISSUED
REPLY    DS    CL80
DOMPTR   DS    A             POINTER TO NEXT AVAILABLE DOM ID ENTRY
DOMIDS   DS    15A           UP TO 15 DOM IDS
DOMIDEND EQU   *             END OF DOM LIST
FLAGS    DS    X             PROCESSING FLAGS                    93212
FGCAN    EQU   X'80'           CANCEL INSTEAD OF END             93212
FGNOPRT  EQU   X'40'           NO PRINT DD FOUND                 93212
FGPAUSE  EQU   X'01'           SET BY PGMHEAD FOR PGM=PAUSE     GP02236
SAVEEND  EQU   *                                                 93212
         PRINT &PRTSYS                                           94215
         CVT   LIST=NO,DSECT=YES                                 94215
         IHACDE ,                                                94215
         IHAPSA ,                                               GP02236
         IKJTCB ,                                               GP02236
         END
