BLKP     TITLE 'B L O C K P R T  ***  TAPE TO PRINT UTILITY'     81365
         MACRO ,                                                 89142
&NM      SHEET &R=R7                                             89142
&NM      TM    MODEFG,FGXER  XEROX MODE ?                        89142
         BZ    *+4+4         NO; LEAVE AS IS                     89142
         N     &R,=F'-2'     TRUNCATE TO EVEN NUMBER             89142
         MEND  ,                                                 89142
         SPACE 1                                                 89142
         COPY  OPTIONGB                                          81365
         SPACE 1                                                 81365
         SYSPARM LIST=YES                                        81365
         SPACE 1                                                 89142
         GBLC  &DJDE                                             89142
         SPACE 1                                                 81365
***********************************************************************
*                                                                     *
*        COPYRIGHT 1981,1989-1993  EXPERT SYSTEM PROGRAMMING, INC.    *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 1                                                 81365
*        THIS UTILITY IS BASED ON PROGRAM BLOCKPRT, WRITTEN BY   81365
*        DICK OXLEY, AS SUBMITTED TO SHARE,INC. BY AMS IN 1974   81365
*        UPDATED FOR ON-LINE XEROX 4050 SUPPORT.  GERHARD P.     89142
         EJECT ,                                                 81365
         PRINT &PRTSOR                                           81365
BLOCKPRT PGMHEAD ZERO15,BASE=(R11,R12),PARM=R5,PFX=SAWE,BNDRY=PAGE,    *
               ENTRY=XEROXPRT,ENTNO=MODEFG                       89142
         ST    R13,WORKADDR  SAVE WORK AREA ADDRESS FOR EXITS    81365
         AIF   (NOT &MVSSP).NOSETPR                              92273
         SPACE 1                                                 92303
*                    *        DUE TO AN ERROR IN THE SP1.4 AND LATER
*     0C4            ***      VERSIONS OF THE SETPRT SVC (81), THIS
**************************    PROGRAM MUST BE RUN IN KEY 5. EITHER ZAP
***************************   IEFSDPPT TO 'BLOCKPRT 6850FFFF 00000000'
**************************    OR (X/A) DEFINE PARMLIB MEMBER SCHEDXX
*     0C4            ***      PPT PGMNAME(BLOCKPRT) CANCEL KEY(5) PRIV
*                    *            PASS AFF(NONE) NOSWAP          92273
.NOSETPR SPACE 1                                                 88304
MAXWIDTH EQU   204           DEFAULT PRINTER WIDTH (XEROX/3800)  89146
&DJDE    SETC  ' $DJDE$     '    XEROX IDEN PREFIX DEFINITION    89142
RSTACK   EQU   X'13'         USE 12 INSTEAD OF 13 UNTIL RSTACK FIXED
         SPACE 1                                                 81365
         SERVINIT ,          LOAD AND INITIALIZE SERVICES        89142
         SERVLOAD @INPREAD,@PRINTER                             GP03146
         LA    R14,MSG1                                          89142
         LA    R15,MSG2                                          89142
         LA    R0,MSG3                                           89142
         STM   R14,R0,@MSG   SET MESSAGE ADDRESSES               89142
         LA    R3,255        INITIALIZE TRANSLATE TABLE          89215
INITTRAN STC   R3,TRANSOUT(R3)   MAKE NO TRANSLATION             89215
         BCT   R3,INITTRAN   FOR ALL CHARACTERS                  89215
         MVC   TRANSCON(256),TRANSOUT  COPY FOR CARRIAGE CONTROL 90284
         MVI   PAGEJECT+1,7  INITIALIZE PAGE EJECT FOR ASA       93040
         MVI   PAGEJECT+4,C'1'                                   81365
         MVI   PAGEJECT+5,C' '   DATA OR FONT SELECTOR (TRC)     81365
         MVI   PAGEJECT+6,C' '                                   93040
         MVI   CCSINGLE,C' '                                     81365
         MVI   SMF6LEN+1,OVERSMF-SMFRCD6+S166RXSZ+SMF6LXSZ       94101
         MVI   SMF6FLG,2     SET VS SYSTEM                       81365
         MVI   SMF6RTY,166   SET RECORD TYPE - LOCAL 166         81365
         MVC   SMF6UIF,BLANKS  PRECLEAR                          81365
         MVI   SMF6OWC,C'P'  SET OUTPUT CLASS                    81365
         MVI   SMF6IOE,F6INTER     PRESET FOR 'OUTPUT ERROR'     81365
         MVI   SMF6NDS,1     ONE DATASET                         81365
         OI    SMF6PAD1,SMF6REXT+X'01'  ROUTE+LOCAL EXTENSION    92358
         MVI   SMF6SBS+1,1+&MVS   SET FOR HASP/JES2 FIELDS       83011
         MVC   SMF6JNM,BLANKS  SET JOB NUMBER                    81365
         MVC   SMF6OUT,BLOCKPRT+5  USE PROGRAM NAME FOR WRITER NAME
         MVC   SMF6FCB,=C'6LPI'  SET DEFAULT FCB                 81365
         MVC   SMF6UCS,=C'PN  '    AND UCS                       81365
         MVI   SMF6LN1+1,OVERSMF-SMF6LN1  LENGTH OF JES2 DATA    89178
         MVI   SMF6LXLN+1,SMF6LXSZ  LENGTH OF LOCAL EXTENSION    89178
         MVC   SMF6FMN,BLANKS  CLEAR FORM NUMBER                 81365
         MVI   S166LN3+1,SMF6LXLN-S166LN3  LEN OF 2ND EXTENSION  92358
         MVC   S166FMN,BLANKS  CLEAR FULL FORM NUMBER            92358
         TM    MODEFG,FGXER  XEROX MODE ?                        89149
         BZ    INITNXER      NO                                  89149
         MVI   SMF6LEN+1,SMF166LN  SET RECORD LENGTH             81365
         MVI   SMF6LN2+OFF3800+1,SMF6FEND-SMF6LN2 LENGTH OF 3800 EXT.
         MVI   SMF6OWC,C'X'  CHANGE CLASS                        89163
         MVC   SMF6MID+OFF3800(L'SMF6MID+L'SMF6FLI),BLANKS       89178
         OI    SMF6PAD1,SMF6FEXT  FIRST EXTENSION PRESENT        89178
         MVC   XERSETNM,BLANKS                                   89149
         MVC   XERPAPER,BLANKS                                   89149
         MVC   XERFLASH,BLANKS                                   89355
         MVC   SMF6OUT(5),=C'XEROX'                              89178
         MVC   WTOR1+25(5),=C'XEROX'                             89149
         MVC   WTOR3+25(5),=C'XEROX'                             89149
         MVC   BADOPEN+8(5),=C'XEROX'                            89149
         MVC   M1+25(5),=C'XEROX'                                89149
INITNXER LA    R14,AREA      POINT TO OUTPUT AREA (WITH CC)      81365
         LA    R15,L'AREA    SET MAXIMUM LENGTH                  81365
         STM   R14,R15,MOVETO                                    81365
         BAS   R10,CANDOM    INITIALIZE DOM PROCESSING           81365
         OI    FLAG1,FGPAGE  PAGE EJECT REQUIRED FOR FIRST MESSAGE
         MVI   NUMMSGS+1,(@MSGEND-@MSG)/(@MSG2-@MSG)  CANNED MSGS
         MVI   CCERR,X'0C'   SET CC ERROR COUNT TO ZERO          81365
         LA    R1,INFMJFCB                                       81365
         STCM  R1,7,OUTEXIT+1                                    81365
         SPACE 2
         LA    R5,0(,R5)     CLEAN ADDRESS                       89144
         LTR   R5,R5         PARM ADDRESS SUPPLIED ?             89144
         BZ    GETJOBNM      NO; USE DEFAULTS                    81365
         ICM   R4,7,1(R5)    GET PARM FIELD ADDRESS              89144
         BZ    GETJOBNM      USE DEFAULTS IF NONE                81365
         SR    R5,R5         CLEAR FOR IC                        89144
         ICM   R5,3,0(R4)    LOAD AND TEST LENGTH                89144
         BZ    GETJOBNM      NONE; USE DEFAULTS                  81365
         LA    R4,2(,R4)     SKIP LENGTH                         89144
         LA    R14,OPERMSG                                       89163
         LA    R15,L'OPERMSG+1                                   89163
         ICM   R5,8,BLANKS   SET FOR BLANK PADDING               89144
         MVCL  R14,R4        MOVE TEXT TO WORK AREA (NO 0C4)     89144
         LA    R5,OPERMSG                                        89163
LOOKPARM L     R4,=A(XERTAB) SET FOR XEROX OPERANDS              91043
         TM    MODEFG,FGXER  IS IT ?                             89144
         BNZ   *+8           YES                                 89144
         L     R4,=A(PRTTAB) ELSE USE NORMAL                     91043
         BLOOK T=(R4),R=OPERMSG  LOOK FOR INPUT                  89163
         CLI   0(R5),C' '    ALL DONE ?                          89144
         BE    GETJOBNM      YES                                 89144
BADPARM  WTO   'INVALID PARM FIELD',ROUTCDE=11  WRITE ERROR MESSAGE
         B     ABND          AND QUIT                            81365
         SPACE 1                                                 90296
PARMINT  CLI   0(R5),C' '    ALL DONE ?                          90296
         BE    LOOKPARM      YES; SKIP THIS                      90296
         CLI   0(R5),C','    ALTERNATE ?                         90296
         BE    LOOKPARM      YES; SKIP                           90296
         SR    R14,R14                                           90296
         LA    R15,C'0'                                          90296
         LA    R0,9          EIGHT DIGITS MAX                    90296
PARMINTL CLI   0(R5),C' '    END ?                               90296
         BE    PARMINTN      YES                                 90296
         CLI   0(R5),C','    ALTERNATE ?                         90296
         BE    PARMINTN                                          90296
         CLI   0(R5),C'0'    NUMERIC ?                           90296
         BL    BADPARM       NO; ERROR                           90296
         SLDL  R14,8         MAKE ROOM                           90296
         IC    R15,0(,R5)    GET NEXT DIGIT                      90296
         LA    R5,1(,R5)                                         90296
         BCT   R0,PARMINTL                                       90296
         B     BADPARM       TOO LONG                            90296
PARMINTN STM   R14,R15,EXSAVER  STASH ANYWHERE                   90296
         PACK  DB,EXSAVER(8)  MAKE PACKED                        90296
         CVB   R0,DB         MAKE BINARY                         90296
         BR    R4            RETURN TO CALLER                    90296
         SPACE 1                                                 89144
SETNOMSG OI    FLAG3,FGNOMSG  PARM=NOMSG                         88061
         B     LOOKPARM      TRY FOR MORE                        89144
         SPACE 1                                                 89144
SETNOCC  OI    FLAG2,FGNCC   NO CARRIAGE CONTROLS TO BE USED     89144
         LA    R0,AREA+1     SET DESTINATION FOR MOVE            81365
         LA    R1,L'AREA-1   SET MAXIMUM MOVE LENGTH             81365
         STM   R0,R1,MOVETO  INTO ADDRESS/LENGTH POINTERS        81365
         B     PRMCOM
         SPACE 1                                                 89144
SETCHAN  OI    FLAG2,FGUCC   INDICATE USER PAGE EJECT CHARACTER  81365
PRMCOM   BAS   R4,PARMINT    EXTRACT INTEGER                     90296
         TM    FLAG2,FGNCC+FGUCC  BOTH REQUESTED ?               89144
         BO    BADPARM       YES; ERROR                          89144
         STH   R0,PARMINFO   STORE VALUE FOR LATER TESTING       89144
         TM    FLAG2,FGNCC   NO CARRIAGE CONTROL ?               81365
         BZ    PRMNUCC       SEE IF USER CHANNEL VALUE VALID     81365
         LTR   R0,R0         OVERRIDE FOR LINES/PAGE ?           81365
         BZ    LOOKPARM      USE DEFAULT IF NOT                  81365
         CH    R0,=H'5'               LESS THAN 5 ?              81365
         BL    BADPARM       ERROR IF SO                         81365
         STH   R0,MAXLINES   SET USER LINES/PAGE                 81365
         CH    R0,=H'176'    GREATER THAN 176 ?                  81365
         BNH   LOOKPARM      NO; GET JOBNAME                     81365
         SPACE 1                                                 81365
PRMNUCC  LTR   R0,R0         AT LEAST CHANNEL 1 ?                81365
         BNP   BADPARM       NO                                  81365
         CH    R0,=H'12'     NOT TOO HIGH ?                      81365
         BH    BADPARM       TOO BAD                             81365
         B     LOOKPARM                                          89144
         SPACE 1                                                 91043
SETLPAGE BAS   R4,PARMINT    EXTRACT COUNT                       91043
         LTR   R0,R0         JUST IN CASE                        91043
         BM    BADPARM       NO WAY                              91043
         ST    R0,LIMPAGE    SAVE LIMIT                          91043
         B     LOOKPARM      GO FOR MORE                         91043
         SPACE 1                                                 89144
SETFORM  LA    R14,S166FMN   POINT TO FORM NUMBER                92358
         LA    R15,L'S166FMN  AND LENGTH                         92358
         B     PRMOVER       GO TO COMMON                        89144
         SPACE 1                                                 90296
SETFO#   BAS   R4,PARMINT    EXTRACT INTEGER                     90296
         TM    MODEFG,FGXUFONT  ALREADY REQUESTED ?              90296
         BO    BADPARM       YES; ERROR                          90296
         CH    R0,=H'48'     ONLY 0-2F SUPPORTED (BY US)         90296
         BNL   BADPARM       TOO HIGH, TOO BAD                   90296
         OI    MODEFG,FGXUFONT  SET FLAG                         90296
         STC   R0,XERUFONT   SAVE FOR XEROX ROUTINE              90296
         B     LOOKPARM      DO SOME MORE                        90296
         SPACE 1                                                 89144
SETFLA   LA    R14,XERFLASH  GET FLASH IMAGE NAME                89355
         LA    R15,L'XERFLASH  AND LENGTH                        89355
         B     PRMOVER       PROCESS IT                          89355
SETPAP   LA    R14,XERPAPER  GET PAPER NAME                      89149
         LA    R15,L'XERPAPER                                    89149
         B     PRMOVER       MOVE IT                             89149
SETSET   LA    R14,XERSETNM  GET SETUP NAME                      89144
         LA    R15,L'XERSETNM  AND LENGTH                        89144
         SPACE 1                                                 89144
PRMOVER  CLI   0(R5),C','    END ?                               89144
         BE    LOOKPARM      YES; LOOK FOR MORE                  89144
         CLI   0(R5),C' '    ALTERNATE ?                         89144
         BE    LOOKPARM                                          89144
         MVC   0(1,R14),0(R5)                                    89144
         LA    R14,1(,R14)   BUMP OUTPUT                         89144
         LA    R5,1(,R5)     BUMP INPUT                          89144
         BCT   R15,PRMOVER   TRY FOR MORE                        89144
         B     LOOKPARM                                          89144
         SPACE 2
GETJOBNM LTCB  R4            GET THE TCB ADDRESS                 89142
         USING TCB,R4        DECLARE IT                          83007
         L     R5,TCBTIO     GET TIOT                            81365
         USING TIOT1,R5      DECLARE THE TIOT                    81365
         LM    R14,R15,TIOCNJOB  GET JOBNAME                     81365
         LA    R0,7          RIGHT-ADJUST COUNTER                81365
GETJOBLP CLM   R15,1,BLANKS  TRAILING BLANK ?                    81365
         BNE   GETJOBST      NO; STASH IT                        81365
         SRDL  R14,8         SHIFT IT OVER                       81365
         ICM   R14,8,BLANKS  ADD LEADING BLANK                   81365
         BCT   R0,GETJOBLP   TRY AGAIN                           81365
GETJOBST STM   R14,R15,JOBNAME  STASH RIGHT-JUSTIFIED JOBNAME    81365
         MVC   M1+16(8),JOBNAME   MOVE TO MAIN WTOR              81365
         MVC   MGJOB,TIOCNJOB   COPY JOBNAME TO HEADER MESSAGE   81365
         MVC   MXGJOB,TIOCNJOB                                   89144
         MVC   SMF6JBN,TIOCNJOB   SET SMF 166 RECORD'S NAME      81365
         TM    TCBFLGS6,TCBRV  RUNNING ADDRSPC=REAL ?            81365
         BNZ   DOCHAIN       YES; CHAINED SCHEDULING OK          79313
         MVI   OIOPTCD+1,DCBOPTZ  NO CH. SCHED.                  79313
         MVI   MAXREC#+1,20  LIMIT RQE USE                       79313
DOCHAIN  L     R6,TCBTCT     GET TCT                             81365
         USING SMFTCT,R6                                         81365
         L     R7,TCTJMR     GET JMR                             81365
         USING JMR,R7                                            81365
         AIF   (&MVS).NOPCE                                      81365
         AIF   ('&LOCAL' NE 'PID').NONUM                         92302
         L     R10,JMRUCOM   GET USER AREA                       81365
         N     R10,=X'00FFFFFC'  REMOVE FLAG/CLASS BITS          79313
         BZ    NOPCE                                             79313
         TESTAUTH FCTN=1     MODESET ALLOWED ?                   79314
         BXH   R15,R15,NOPCE  NO; SKIP JOB NUMBER CODE           79314
         MODESET KEY=ZERO    PERMIT HASP PAGE ACCESS             79314
         USING PCEDSECT,R10  DECLARE $PCE MAPPING                81365
         ST    R10,LOCALPCE  SAVE PCE ADDRESS                    81365
         L     R10,XPCEJOB                                       79313
         ST    R10,LOCALJQE  AND JQE ADDRESS                     81365
         LH    R0,QUEJOBNO(,R10) GET JOB NUMBER                  79313
         CVD   R0,DB                                             81365
         UNPK  DB(5),DB+5(3)                                     81365
         OI    DB+4,C'0'                                         81365
         MVC   SMF6JNM,DB+1  MAKE JOB NUMBER                     81365
         MVC   MGJOB#,SMF6JNM  COPY TO LISTING ALSO              81365
         MVC   MXGJOB#+4(4),SMF6JNM                              89144
         MODESET KEY=NZERO   RESTORE                             79314
         DROP  R10                                               81365
         AGO   .DONPCE                                           83007
.NOPCE   SPACE 1                                                 81365
         LAT   R3,TCBJSCB,NOPCE,LA=34                            92303
         USING IEZJSCB,R3    DECLARE IT                          83007
         ICM   R2,15,JSCBSSIB  GET SSIB   83007                  92303
         BZ    NOPCE         ?                                   83007
         USING SSIB,R2                                           83007
         MVC   SMF6JNM,SSIBJBID+4  PROPAGATE JOBNUMBER           83007
         MVC   MGJOB#,SMF6JNM  COPY TO LISTING ALSO              83080
         MVC   MXGJOB#,SSIBJBID  MOVE JOB TYPE/NUMBER            89144
         DROP  R2,R3,R4                                          83007
.DONPCE  SPACE 1                                                 83007
NOPCE    SERVCALL ACGET,DB   GET CURRENT ACCOUNT                 89142
         STC   R0,DB         STASH FLAGS                         89142
         TM    DB,VAASYS+VAASUP  PRIVILEGED USER ?               89142
         BZ    *+8           NO                                  89142
         OI    MODEFG,PRIVY  SET PRIVILEGED                      89142
         DEVTYPE SYSUT2+DCBDDNAM-IHADCB,DB  CHEAT A BIT          89144
         CLI   DB+2,UCB3UREC  REAL PRINTER OUTPUT ?              89144
         BE    *+8           MAYBE                               89144
         OI    MODEFG,FGFAKE   SET FOR NOT ONLINE PRINTER        89142
         TM    MODEFG,FGXER+FGFAKE  BATCH RUN ?                  89144
         BO    FAKEUNIT      YES                                 89144
         TM    MODEFG,PRIVY  AUTHORIZED ?                        89144
         BZ    NOTPRIVY      NO                                  89142
FAKEUNIT MVI   FAKEUCB2+UCBID-UCBOB,X'FF'                        87167
         MVC   UCBTBYT3+FAKEUCB2-UCBOB(2),=X'0808' FAKE 1403     87167
         MVC   UCBNAME+FAKEUCB2-UCBOB,=C'0AF'                    87167
.NONUM   SPACE 1                                                 81365
NOTPRIVY MVC   SMF6SID(4),JMRCPUID  COPY SMCA ID                 81365
         MVC   SMF6RST(8),JMRENTRY   READER START TIME/DATE      81365
         MVC   SMF6UIF,JMRUSEID  ACCOUNT NUMBER                  81365
         MVC   MGACCT,JMRUSEID  COPY ACCOUNT                     81365
         MVC   MGSACT,JMRUSEID+4   AND SUB-ACCOUNT               81365
         MVC   MXGACCT(9),MGACCT  COPY SPLIT ACCT/SUBA           93008
         TIME  DEC           GET TIME AND DATE                   81365
         STCM  R1,15,SMF6DTE    SET CURRENT DATE                 81365
         ED    MGDATE-1(7),SMF6DTE+1  EDIT DATE INTO YY.DDD FORM 81365
         SRL   R0,4          REMOVE THOUSANDS BIT                81365
         ST    R0,DB         STASH TIME                          81365
         OI    DB+3,X'0F'    FORCE A POSITIVE SIGN               81365
         ED    MGTIME-1(10),DB  EDIT THE TIME                    81365
         MVI   MGTIME-1,C'E'  MAKE TIME                          81365
         MVC   SMF6TME,TCTPPST  USE PROGRAM START TIME           81365
         MVC   SMF6WST(8),SMF6TME  SET WRITER START TIME         81365
         AIF   ('&LOCAL' NE 'PID').NOJMRU                        92302
         CLI   JMRUCOM,C'O'   CLASS P JOB ?                      89163
         BNL   VALIDCLS      YES; OK. NORMAL OS OR HASP CLASS    89163
         CLI   JMRUCOM-1,C'O'  CHECK OTHER CLASS                 89163
         BNL   VALIDCLS                                          89163
         L     R0,=A(MFGCLASS)  SET FOR BAD CLASS                92303
         TM    MODEFG,PRIVY+FGFAKE PRIVILEGED USER ?             89142
         BZ    ABENDMSG      NO; TOO BAD                         81365
.NOJMRU  SPACE 1                                                 83011
VALIDCLS CLI   JMRSTEP,1     FIRST (AND ONLY) STEP ?             81365
         BNH   VALIDSTP      YES                                 81365
         L     R0,=A(MFGSTEP#)                                   92303
         TM    MODEFG,PRIVY+FGFAKE PRIVILEGED USER ?             89142
         BZ    ABENDMSG      NO; TOO BAD                         81365
VALIDSTP L     R6,TCTIOTBL   GET TCT I/O TABLE                   81365
         USING TCTTIOT,R6                                        81365
         PUSH  USING                                            GP99354
         AIF   (NOT &MVSXA).OLDTCT                               92303
         L     R2,TCTSZLKP   GET SIZE OF ENTRIES                 92303
         SRDA  R2,32         FLIP                                92303
         D     R2,=F'24'     GET NUMBER OF ENTRIES               92303
         LA    R2,1(,R3)     NUMBER OF ENTRIES + 1               92303
         LA    R4,TCTIODSP-8  POINT BEFORE LOOKUP SECTION        92303
         DROP  R6                                               GP99354
         USING TCTIODSP,R4   DECLARE IT                          92303
LOOKTIOT LA    R4,8(,R4)     SKIP TO NEXT ENTRY                  92303
         AGO   .COMTCT                                           92303
.OLDTCT  LH    R2,TCTSZLKP   GET SIZE OF ENTRIES                 83011
         SRA   R2,2+&MVS     CONVERT TO NUMBER OF ENTRIES        83011
         LA    R2,1(,R2)     MAKE LOOP FASTER                    92303
         LA    R4,TCTIODSP-4  POINT BEFORE 1ST DEVICE PTR        81365
         DROP  R6                                               GP99354
         USING TCTIODSP,R4   DECLARE IT                          81365
LOOKTIOT LA    R4,4(,R4)     POINT TO NEXT ENTRY                 81365
.COMTCT  BCT   R2,MORETIOT   SEE IF ANOTHER                      83011
         B     DONETIOT      NO                                  81365
MORETIOT SR    R1,R1                                             81365
         ICM   R1,3+&MVSXA*12,TCTDCBTD   GET TIOT DD OFFSET      92303
         BNP   DONETIOT                                          81365
         AR    R1,R5         GET TIOT ENTRY                      81365
         DROP  R5                                               GP99354
         USING TIOENTRY,R1                                       81365
         LA    R7,SMF6OUD    PROV. POINT TO OUTPUT ENTRY         81365
         SR    R8,R8                                             92303
         ICM   R8,3+&MVS*12,TCTIOTSD                             92303
         AR    R8,R6         POINT TO I/O DEVICE ENTRY           81365
         CLC   DCBDDNAM-IHADCB+SYSUT2,TIOEDDNM  OUTPUT DD NAME ? 81365
         BE    TESTTIOT      YES - MAKE ENTRY                    81365
         CLC   DCBDDNAM-IHADCB+SYSUT1,TIOEDDNM  IS IT INPUT ENTRY ?
         BNE   LOOKTIOT      NO; TRY ANOTHER                     81365
         AIF   (NOT &MVS).NODSABP                                83080
         ST    R1,DB         SAVE INPUT DD'S TIOT ADDRESS        83080
.NODSABP LA    R9,TCTDCTR-TCTDDENT(,R8)  GET ADDRESS FOR I/P ENTRY
         LA    R7,SMF6IND@   POINT TO INPUT ENTRY               GP99354
         ST    R9,ADUT1                                          81365
TESTTIOT CLI   0(R7),0       MULTIPLE DD ?                       81365
         L     R0,=A(MFGDUPDD)  SET FOR DUPLICATE DD CARD        92303
         BNE   ABENDMSG      YES; TOO BAD                        81365
         L     R0,=A(MFGUNITS)  INVALID NUMBER OF DEVICES        92303
         CLI   TCTSCTR-TCTDDENT(R8),1  ONE DEVICE ?              81365
         BNE   ABENDMSG      NO; ERROR                           81365
         ICM   R1,7,TIOEFSRT  GET THE UCB ADDRESS               GP03146
         BNZ   ACPTTIOT      PROCEED                             87167
         CLC   TIOEDDNM,DCBDDNAM-IHADCB+SYSUT2 OUTPUT ?          87167
         BNE   ABENDMSG      NO; KILL                            87167
         LA    R9,FAKEUCB2   POINT TO FAKE UCB                   87167
         CLI   UCBID-UCBOB(R9),X'FF'  PRIVILEGED USER ?          87167
         BNE   ABENDMSG      NO - DD DUMMY NOT SUPPORTED         87167
ACPTTIOT SERVCALL SWAAB,(R1)  GET THE UCB ADDRESS               GP03146
         LR    R9,R1                                            GP03146
         USING UCBOB,R9                                          81365
         CLI   TIOELNGH,20   SINGLE DD/SINGLE UCB ENTRY ?        81365
         BNE   ABENDMSG      NO; TOO BAD                         81365
         CLI   20+TIOELNGH,0    NEXT IS END OF TIOT ?            81365
         BE    LASTTIOT      YES; OK                             81365
         CLI   20+TIOEDDNM,C' '   NEXT IS CONCATENATION ?        81365
         BNE   LASTTIOT      NO                                  81365
         CLC   TIOEDDNM,DCBDDNAM-IHADCB+SYSUT2  OUTPUT DD ?      81365
         BE    ABENDMSG      YES - NOT ALLOWED                   81365
         CLI   20+TIOELNGH,20     SINGLE UCB ENTRY ?             81365
         BNE   ABENDMSG      NO                                  81365
         CLI   UCBTBYT3,UCB3TAPE  TAPE INPUT ?                   81365
         BNE   LASTTIOT      NO                                  81365
         CLC   TIOEFSRT,20+TIOEFSRT  SAME DRIVE ?                81365
         BNE   ABENDMSG      NO; ERROR                           81365
LASTTIOT MVC   0(2,R7),UCBTBYT3    COPY MAJOR DEVICE TYPES       81365
         MVC   DB2(3),UCBNAME  GET EBCDIC ADDRESS                92273
         NC    DB2(3),=3X'1F'  CLOBBER FOR HEX CONVERT           92273
         TR    DB2(3),TABTOHEX  CONVERT TO HEX                   92273
         PACK  2(3,R7),DB2(4)  PACK; CLOBBERS 4(R7)              92273
         ST    R9,4(,R7)     SAVE UCB ADDRESS FOR CHECK          81365
         B     LOOKTIOT      GET NEXT ENTRY                      81365
DONETIOT ICM   R9,7,SMF6OUD+5  GET PRINTER UCB ADDRESS           81365
         STCM  R9,7,OUTUCB@+1  SAVE FOR LATER                    89178
         L     R0,=A(MFGNODD)   SET FOR DD CARD MISSING          92303
         BZ    ABENDMSG      NONE - TOO BAD                      81365
         AIF   (NOT &MVS).NODSAB                                 83080
         L     R1,DB         GET TIOT ADDRESS OF SYSUT1 BACK     83080
         SR    R0,R0         SET FOR FAST IC                     83080
         IC    R0,TIOELNGH   GET LENGTH OF THIS ENTRY            83080
LOOPDSAT ICM   R1,7,TIOEFSRT  GET UCB TOKEN FOR THIS TIOT       GP03146
         BZ    SKIPDSAT      SKIP DUMMY                          83080
         SERVCALL SWAAB,(R1)                                    GP03146
         CLI   UCBTBYT3-UCBOB(R1),UCB3TAPE  TAPE ENTRY ?        GP03146
         BNE   SKIPDSAT      NO; SKIP TEST                       83080
         L     R1,DB         GET TIOT ADDRESS OF SYSUT1 BACK     83080
         TM    MODEFG,FGFAKE UNREAL UNIT ?                       89163
         BNZ   SKIPDSAT      YES                                 89163
         LTCB  R15           RELOAD CURRENT TCB ADDRESS          89142
         L     R15,TCBJSCB-TCB(,R15)  GET JSCB                   83080
         L     R15,JSCDSABQ-IEZJSCB(,R15)  GET QUEUE HEADER      83080
         USING QDB,R15                                           83080
         ICM   R15,15,QDBFELMP    GET FIRST DSAB                 83080
         USING DSAB,R15                                          83080
LOOPDSAB C     R1,DSABTIOT   MATCHING ENTRY ?                    83080
         BE    HAVEDSAB      YES                                 83080
         ICM   R15,15,DSABFCHN  GET NEXT ENTRY                   83080
         BNZ   LOOPDSAB                                          83080
         B     SKIPDSAT      SKIP OUT IF NONE ?                  83080
HAVEDSAB TM    DSABFLG3,DSABPASS  PASS USED ?                    83080
         BZ    FAILDSAB      NO; FAIL IT                         83080
         TM    TIOEJFCB+L'TIOEJFCB-1,1  SWA TOKEN ?              92303
         BNZ   SKIPDSAT      BYPASS CHECK FOR NOW                92303
         ICM   R14,7,TIOEJFCB   GET THE JFCB                     83080
         TM    JFCBIND2-INFMJFCB+16(R14),JFCMOD  DISP=NEW OR MOD ?
         BZ    SKIPDSAT      NO, OLD OR SHR - TEST NEXT TIOT     83080
         DROP  R15                                               83080
FAILDSAB L     R0,=A(MFGNOPAS)  SET MESSAGE FOR DISP=PASS MISSING
         B     ABENDMSG      AND ISSUE GREETINGS                 83080
SKIPDSAT AR    R1,R0         POSITION TO NEXT ENTRY              83080
         ICM   R0,1,TIOELNGH                                     83080
         BZ    DONEDSAT                                          83080
         CLI   TIOEDDNM,C' '   CONCATENATION ?                   83080
         BE    LOOPDSAT      YES; CHECK IT                       83080
DONEDSAT DS    0H                                                83080
.NODSAB  CLI   SMF6IND@,UCB3TAPE TAPE INPUT ?                   GP99354
         BE    *+8           YES; LEAVE REDUCED ERROR RECOVERY   81365
         NI    OIOPTCD+1,255-DCBOPTZ  RESET SEARCH DIRECT        81365
         SR    R2,R2                                             81365
         STCM  R2,15,SMF6OUD#   CLEAR I/O COUNTS                 92273
         STCM  R2,15,SMF6IND#                                    92273
         L     R0,=A(MFGBADDD)  UNSUPPORTED OUTPUT DEVICE        92303
         CLI   UCBTBYT3,UCB3UREC  UNIT RECORD ?                  81365
         BNE   ABENDMSG                                          81365
         CLI   UCBTBYT4,X'0B'  3203 ?                            85315
         BE    SUPDTYPE      YES                                 85315
         CLI   UCBTBYT4,X'30'  ?                                 85315
         BE    SUPDTYPE      BUT WHAT IS IT ?                    85315
         CLI   UCBTBYT4,X'0E'  3800 ?                            85315
         BE    MAKE3800                                          92303
         CLI   UCBTBYT4,X'08'  1403 ?                            81365
         BL    ABENDMSG      NO                                  81365
         CLI   UCBTBYT4,X'0A'  3211, 1443 ?                      81365
         BH    ABENDMSG                                          81365
         B     SUPDTYPE                                          92303
         SPACE 1                                                 92303
*   PRELIMINARY STC (SIEMENS AKTIENGESELLSCHAFT) 6100            92303
*                                                                92303
MAKE3800 OI    FLAG4,FG3800  INDICATE 3800 OUTPUT                92303
         MVI   SMF6LEN+1,SMF166LN  SET RECORD LENGTH             92303
         MVI   SMF6LN2+OFF3800+1,SMF6FEND-SMF6LN2 LENGTH OF 3800 EXT
         MVI   SMF6OWC,C'I'  CHANGE CLASS                        92303
         MVC   SMF6MID+OFF3800(L'SMF6MID+L'SMF6FLI),BLANKS       92303
         OI    SMF6PAD1,SMF6FEXT  FIRST EXTENSION PRESENT        92303
         MVC   XERSETNM,BLANKS                                   92303
         MVC   XERPAPER,BLANKS                                   92303
         MVC   XERFLASH,BLANKS                                   92303
         MVC   SMF6OUT(5),=C'LASER'                              92303
         MVC   WTOR1+25(5),=C'LASER'                             92303
         MVC   WTOR3+25(5),=C'LASER'                             92303
         MVC   BADOPEN+8(5),=C'LASER'                            92303
         MVC   M1+25(5),=C'LASER'                                92303
SUPDTYPE DS    0H                                                85315
         AIF   (&MVS).NOATI                                      81365
         TM    UCBATI,UCBHPDV  PSEUDO DEVICE ?                   81365
         BZ    READJFCB      NO; GET JFCB                        81365
         TM    MODEFG,PRIVY+FGXER  PRIVILEGED USER ?             89142
         BZ    ABENDMSG      NO; TOO BAD                         81365
.NOATI   SPACE 1                                                 81365
READJFCB RDJFCB MF=(E,OUTOPLST)   GET THE OUTPUT JFCB            81365
         L     R0,=A(MFGNOPEN)  SET FOR OPEN FAILED              92303
         BXH   R15,R15,ABENDMSG   TSK,TSK                        81365
         TM    MODEFG,FGFAKE  FAKE DD ?                          89145
         BNZ   HAVESET       YES; NOT MEANINGFUL                 89145
         CLI   XERPAPER,C' ' PAPER PRESET ?                      89149
         BH    HAVEPAP       YES                                 89149
         CLI   JFCBVOLS+12,C' '  ANY HERE ?                      89149
         BNH   HAVEPAP       NO                                  89149
         CLC   =C'DFAULT',JFCBVOLS+12  DEFAULT ?                 89149
         BE    HAVEPAP       YES; IGNORE                         89149
         MVC   XERPAPER(6),JFCBVOLS+12  USE OVERRIDE             89149
HAVEPAP  CLI   XERSETNM,C' ' WAS SETUP PROVIDED ON PARM ?        89145
         BH    HAVESET       YES                                 89145
         CLI   JFCBVOLS+6,C' '  ANY ON DD CARD ?                 89145
         BNH   HAVESET                                           89145
         CLC   =C'DFAULT',JFCBVOLS+6  DEFAULT (OUR PROC) ?       89145
         BE    HAVESET       YES; IGNORE IT                      89145
         MVC   XERSETNM(6),JFCBVOLS+6                            89145
HAVESET  CLI   JFCFCBID,X'40'  ANY USER FCB SPECIFIED ?          86092
         BH    *+8           YES                                 86092
         OI    FLAG3,FGFCB   SHOW DEFAULT NEEDED                 86092
         CLC   =C'STD3',JFCFCBID  IBM STANDARD ?                 89149
         BNE   *+10          NO                                  89149
         MVC   JFCFCBID,=C'8LPI'  REPLACE BY OURS                89149
         CLI   JFCUCSID,X'40'  ANYTHING IN UCS ?                 81365
         BH    HAVEUCS       YES; CHECK IT                       81365
         MVI   JFCUCSID,C'P'   LOCAL DEFAULT - PN/P11 TRAIN      81365
         OI    FLAG3,FGUCS   SHOW USE OF DEFAULT UCS             86092
HAVEUCS  MVC   MGBADUCS,JFCUCSID  COPY TO MESSAGE                81365
         LA    R0,MFGBADUC   SET TO BAD UCS MESSAGE              81365
         CLI   UCBTBYT4,X'0B'   3203 ?                           85315
         BNE   PRTN3203      NO                                  85315
         LA    R2,UCSU3203   SET 3203 UPPER CASE UCS TABLE       85315
         LA    R3,UCSL3203   LOWER CASE TABLE                    85315
         LA    R4,UCSX3203   NUMBER OF ENTRIES                   85315
         MVC   JFCUCSID+1(3),=C'N  '  SET FOR PN, ETC.           85315
         B     CHECKUCS      MATCH AGAINST TABLE                 85315
PRTN3203 LA    R2,UCSU1403   SET 1403 UPPER CASE UCS TABLE       85315
         LA    R3,UCSL1403   LOWER CASE TABLE                    81365
         LA    R4,UCSX1403   NUMBER OF ENTRIES                   81365
         MVC   JFCUCSID+1(3),=C'N  '  SET FOR PN, ETC.           81365
         TM    FLAG4,FG3800                                      92303
         BNZ   UPPERUCS      BYPASS FOLD TEST                    92303
         CLI   UCBTBYT4,X'09'  3211 ?                            81365
         BNE   CHECKUCS      NO                                  81365
         LA    R2,UCSU3211   SET TO 3211 UPPER CASE TRAINS       81365
         LA    R3,UCSL3211     LOWER CASE                        81365
         LA    R4,UCSX3211   NUMBER OF ENTRIES                   81365
         MVC   JFCUCSID+1(3),=C'11 '  MAKE P11, T11, ETC.        81365
CHECKUCS CLC   0(1,R2),JFCUCSID  MATCH ?                         81365
         BE    FOUNDUCS                                          81365
         LA    R2,1(,R2)                                         81365
         BCT   R4,CHECKUCS                                       81365
         B     ABENDMSG      UCS DEFINITION NOT FOUND            81365
FOUNDUCS OI    JFCUCSOP,JFCFOLD   SET FOLD OPTION                81365
         CR    R2,R3         LOWER CASE ?                        81365
         BL    UPPERUCS      NO                                  81365
         NI    JFCUCSOP,255-JFCFOLD  NO FOLD OPTION FOR LOWER CASE
UPPERUCS MVC   SMF6UCS,JFCUCSID   MOVE UCS TO SMF RECORD         81365
         CLI   S166FMN,C' '  FORM ALREADY SET BY PARM FIELD ?    92358
         BH    *+10          YES                                 89144
         MVC   S166FMN,JFCBVOLS  ELSE TREAT AS FORMS REQUEST     92358
         CLI   S166FMN,C' '  ANYTHING SUPPLIED ?                 92358
         BNH   NOVOLSER      NO                                  81365
         AIF   ('&LOCAL' NE 'PID').NOFORMS                       92302
         CLC   =C'STD. ',S166FMN  DEFAULT FORMS ?                92358
         BE    FORMFOR1                                          87167
         CLC   =C'STD ',S166FMN  NEW-FANGLED DEFAULT ?           92358
         BNE   FORMFOR2      NO                                  87167
FORMFOR1 MVC   S166FMN,=CL8'1671'  SET 1 PART, 6-LPI, LINED, PN  92358
FORMFOR2 BAS   R10,FORMCHEK  SEE IF VALID SPECIAL CASE           79313
         B     GOODFORM      YES; PROCESS                        81365
.NOFORMS CLC   S166FMN(4),=C'USER'    USER SUPPLIED FORMS ?      92358
         BNE   CHEKFORM      NO; CHECK FORM                      81365
         MVC   WTOR1+16(8),JOBNAME  COPY JOB NAME                81365
DOWTOR1  XC    WTORECB,WTORECB                                   81365
         MVI   OPREPLY,0     CLEAR REPLY                         81365
         TM    MODEFG,FGXER+FGFAKE  BATCH ?                      89144
         BO    GOODFORM      YES; GET LATER                      89144
WTOR1    WTOR  'XXXXXXXX BLOCKPRT - JOB REQUESTS ''USER'' FORMS, PLEASE*
                REPLY ''YES'' IF CORRECT',OPREPLY,3,WTORECB,           *
               ROUTCDE=(1,8)                                     81365
         WAIT  ECB=WTORECB                                       81365
         OI    OPREPLY,C' '  UPPER CASE, JUST IN CASE            81365
         CLI   OPREPLY,C'Y'  YES ?                               81365
         BE    GOODFORM      OK - ACCEPT USER FORM               81365
         CLI   OPREPLY,C'N'  NO ?                                81365
         BNE   DOWTOR1       ASK AGAIN                           81365
         LA    R0,MSGBADUF   SET FOR BAD USER FORMS              81365
         B     ABENDMSG        AND QUIT WITH MESSAGE             81365
MSGBADUF VCON  'OPERATOR REJECTED REQUEST FOR ''USER'' FORMS'    81365
MSGBADUQ VCON  'FORMS ',END=MSGUQBAD                             81365
MSGBADUU DC    CL(L'S166FMN)' ',C' INVALID'                      92358
         VCON  ,                                                 81365
FAILFORM MVC   MSGBADUU,S166FMN  COPY BAD FORM                   92358
         LA    R1,MSGBADUQ   BAD FORMS MESSAGE                   83019
         BAS   R10,PUTOPER   WRITE BAD FORMS TO OPERATOR         81365
NOVOLSER MVC   WTOR3+16(8),JOBNAME  COPY JOBNAME                 81365
         XC    WTORECB,WTORECB                                   81365
         XC    OPREPLY,OPREPLY   CLEAR REPLY                     81365
WTOR3    WTOR  'XXXXXXXX BLOCKPRT - SPECIFY FORMS',OPREPLY,8,WTORECB,  *
               ROUTCDE=(1,8)                                     92358
         WAIT  ECB=WTORECB                                       81365
         MVC   S166FMN,BLANKS                                    92358
         OC    S166FMN,OPREPLY   UPPER CASE FORMS                92358
         CLC   S166FMN(4),=C'USER'  FORM COMPLETE ?              92358
         BE    GOODFORM      YES; LOOK FOR FCB                   81365
         AIF   ('&LOCAL' NE 'PID').NOFORMT                       92302
         CLC   =C'STD. ',S166FMN  DEFAULT FORMS ?                92358
         BE    CHEKFOR1                                          87167
         CLC   =C'STD ',S166FMN  NEW-FANGLED DEFAULT ?           92358
         BNE   CHEKFOR2      NO                                  87167
CHEKFOR1 MVC   S166FMN,=C'1671'  SET 1 PART, 6-LPI, LINED, PN    92358
         CLI   SMF6FCB,C'8'  8-LPI ?                             89149
         BNE   *+10          NO                                  89149
         MVI   S166FMN+1,C'8'  PROPAGATE                         92358
CHEKFOR2 BAS   R10,FORMCHEK  SEE IF VALID SPECIAL CASE           79313
         B     GOODFORM      YES; PROCESS                        81365
.NOFORMT SPACE 1                                                 79313
CHEKFORM CLC   =C'XICS ',S166FMN  SPECIAL NAME ?                 92358
         BNE   *+10          NO                                  89144
         MVC   S166FMN,=CL8'4050'  MAKE REPLACEMENT              92358
*        MVC   OPREPLY(4),MASK  GET NUMERIC MASK BYTES           81365
*        NC    OPREPLY(4),S166FMN   MASK TO GET ZONES            92358
*        CLC   OPREPLY(4),MASK  ALL NUMERIC ?                    81365
*        BNE   FAILFORM      NO; TOO BAD                         81365
         CLI   S166FMN,X'F0' FIRST DIGIT ZERO ?                  92358
         BE    GOODFORM      YES - USER FORM                     81365
         CLI   S166FMN,X'F6' NO MORE THAN 6 PLY ?                92358
         BH    FAILFORM      WE DON'T STOCK IT                   81365
         MVC   OPREPLY(1),SMF6UCS  GET FIRST UCS BYTE BACK       81365
         TM    FLAG4,FG3800  10 AND 12 LPI ?                     93008
         BNZ   *+8           YES; NO OI VEH                      93008
         OI    OPREPLY,C'0'  MAKE INTO (HASP) NUMERIC            81365
         CLC   S166FMN+2(1),OPREPLY  MATCHES ?                   92358
         BE    GOODFORM      YES - CHECK FCB                     81365
MISHMASH MVC   MGBADUC4,SMF6UCS                                  81365
         MVC   MGBADFCB,SMF6FCB                                  81365
         MVC   MGBADFRM,S166FMN                                  92358
         LA    R1,MSGBADUI   SET INCONSISTENT                    81365
         BAS   R10,PUTOPER   SHOW OFF                            81365
         B     NOVOLSER                                          81365
MSGBADUI VCON  'UCS=',END=MSGUIBAD                               81365
MGBADUC4 DC    CL3' ',C', FCB='                                  81365
MGBADFCB DC    CL4' ',C' AND FORM '                              81365
MGBADFRM DC    CL(L'S166FMN)' ',C' ARE INCONSISTENT'             92358
         VCON  ,                                                 81365
         SPACE 1                                                 81365
GOODFORM LA    R1,S166FMN    SET LONG TO SHORT COPY POINT        92358
         CLI   S166FMN+6,C' '  SEVEN CHARACTER FORM ?            92358
         BE    GOODFORK      NO; COPY                            92358
         LA    R1,S166FMN+3  POINT PAST THREE-BYTE ACRONYM       92358
         CLI   0(R1),C'0'    NUMERIC ?                           92358
         BNL   GOODFORK      YES                                 92358
         LA    R1,S166FMN+4  SKIP ONE MORE                       92358
GOODFORK MVC   SMF6FMN,S166FMN  SAVE SHORT FORM                  92358
         ICM   R4,15,@IMGLIB   IMAGELIB OPEN ?                   89187
         BNZ   GOODFORT      YES                                 89187
         IMGLIB OPEN         OPEN IMAGELIB                       89163
         BXH   R15,R15,GOODFORT  HUH ?                           89163
         LR    R4,R1         SAVE DCB ADDRESS                    89163
         ST    R4,@IMGLIB    AND SAVE FOR LATER                  89187
GOODFORT TM    FLAG3,FGFCB+FGVOLT  FCB DEFAULTED ?               89187
         BO    USERFCB       YES; USE DEFAULT                    86092
         MVC   OPERMSG(8),=X'00010040C6C3C2F2'  FCB2             89163
         MVC   OPERMSG+8(4),JFCFCBID  MOVE FCB SUFFIX            89163
         TM    FLAG4,FG3800    3800 ?                            92303
         BZ    *+8           NO                                  92303
         MVI   OPERMSG+7,C'3'  FCB3----                          92303
         LTR   R4,R4         IMGLIB OPEN ?                       89187
         BZ    TESTFCB       NO, HUH ?                           89187
         BLDL  OPERMSG,(R4)  LOOK FOR FCB DEFINITION             89163
         LTR   R15,R15       VALID FCB ?                         89163
         BZ    TESTFCB       NO ?                                89163
         MVC   SMF6FCB,JFCFCBID  MOVE IT AS IS                   89163
         B     USERFCB       AND SKIP OTHER CHECKS               89163
TESTFCB  CLC   JFCFCBID+1(3),=C'LPI'  OUR STANDARD FCB SPECIFIED ?
         BNE   DFLTFCB       NO; LET IT DEFAULT                  81365
         LA    R1,FCB3211    GET VALID FCB PREFIX TABLE          81365
         LA    R2,FCBX3211     AND NUMBER OF ENTRIES             81365
LOOPFCB  CLC   JFCFCBID(1),0(R1)  IN TABLE ?                     81365
         BE    MOVEFCB       YES - COPY IT                       81365
         LA    R1,1(,R1)     GET ANOTHER                         81365
         BCT   R2,LOOPFCB                                        81365
         B     DFLTFCB       LET IT DEFAULT                      81365
MOVEFCB  MVC   SMF6FCB(1),JFCFCBID  COPY TO SMF                  81365
DFLTFCB  CLI   S166FMN,C'U'  USER FORM ?                         92358
         BE    USERFCB       YES - CAN'T CHECK                   81365
         CLI   S166FMN,X'F0'  DITTO                              92358
         BNH   USERFCB       VALID EXCEPTION                     81365
         CLC   SMF6FCB(1),S166FMN+1  CORRECT LPI ?               92358
         BNE   MISHMASH      NO; COMPLAIN ABOUT IT               81365
         CLC   SMF6UCS(1),S166FMN+2  CORRECT TRAIN ?             92358
         BE    USERFCB       YES; PASS IT                        81365
         MVC   DB(1),SMF6UCS                                     81365
         OI    DB,X'F0'      MAKE NUMERIC                        81365
         CLC   DB(1),S166FMN+2  DOES IT MATCH NOW ?              92358
         BNE   MISHMASH      NO; INCONSISTENT REQUEST            81365
         CLI   S166FMN+3,C'4'  VALID ?                           92358
         BNL   MISHMASH                                          81365
USERFCB  MVC   JFCFCBID(4),SMF6FCB   COPY TO PRINTER             81365
         ICM   R4,15,@IMGLIB   GET IMGLIB DCB                    89187
         BZ    USERFCBD      HUH ?                               89187
         MVC   OPERMSG(8),=X'00010040C6C3C2F2'  FCB2             89163
         MVC   OPERMSG+8(4),JFCFCBID  MOVE FCB SUFFIX            89163
         TM    FLAG4,FG3800    3800 ?                            92303
         BZ    *+8           NO                                  92303
         MVI   OPERMSG+7,C'3'  FCB3----                          92303
         LOAD  EPLOC=OPERMSG+4,DCB=(R4),ERRET=USERFCBF           89187
         LR    R1,R0         COPY MODULE ADDRESS                 89187
         SR    R0,R0                                             89187
         CLI   OPERMSG+7,C'3'  3800 FORMAT ?                     93008
         BNE   USERFPRT                                          93008
         ICM   R0,3,6(R1)    CL4'NAME',AL4(LPP)                  93008
         B     USERFNOX      NO INDEX                            93008
USERFPRT IC    R0,1(,R1)     GET FCB LINES PER PAGE              89187
         TM    2(R1),X'C0'   IS THERE AN INDEX BYTE ?            89187
         BZ    *+6           NO                                  89187
         BCTR  0,R0          YES; LENGTH IS ONE LESS             89187
USERFNOX TM    MAXLINES,X'7F'  DEFAULT MAXLINES ?                89187
         BO    USERFCBV      YES; USE FCB VALUE                  89187
         CH    R0,MAXLINES   DID USER PRESET A SMALLER VALUE ?   89187
         BNH   USERFCBF      YES; LEAVE IT ALONE                 89187
         TM    MODEFG,FGXER  XEROX MODE ?                        89187
         BZ    USERFCBF      NO; ALLOW IT                        89187
USERFCBV STH   R0,MAXLINES   SET FCB VALUE                       89187
USERFCBF IMGLIB CLOSE,(R4)   CLOSE IT                            89163
         XC    @IMGLIB,@IMGLIB  DELETE POINTER                   89187
USERFCBD TM    MAXLINES,X'7F'  DEFAULT MAX ?                     89187
         BNO   *+10          NO                                  89187
         XC    MAXLINES,MAXLINES  CLEAR                          89187
         OC    MAXLINES,MAXLINES  ANY LINE PER PAGE VALUE SET ?  89187
         BNZ   USERFCBS                                          89187
         MVI   MAXLINES+L'MAXLINES-1,60  SET JES2 DEFAULT        93008
         CLI   SMF6FCB,C'8'  8-LPI ?                             89187
         BNE   USERFCBS      NO                                  89187
         MVI   MAXLINES+L'MAXLINES-1,80  SET HIGHER MAXIMUM      89187
USERFCBS MVC   MSGSTUCB,UCBNAME   SHOW PRINTER ADDRESS IN SETUP  81365
         MVC   MSGSTFRM,S166FMN  FORM TO SETUP MESSAGE           92358
         CLI   MSGSTFRM,C'U'  USER FORM ?                        81365
         BE    *+8           YES; LEAVE IT                       79313
         OI    MSGSTFRM,C'0'  MAKE NUMERIC AGAIN                 81365
         MVC   MSGSTFCB,SMF6FCB  FCB                             81365
         MVC   MSGSTUCS,SMF6UCS  UCS                             81365
         MVC   MGOUT,UCBNAME  COPY UNIT ADDRESS                  81365
         MVC   MGFORMS,S166FMN  AND FORM                         92358
         CLI   MGFORMS,C'U'  USER FORM ?                         81365
         BE    *+8           YES; LEAVE IT                       79313
         OI    MGFORMS,C'0'  MAKE NUMERIC AGAIN                  81365
         MVC   MGUCS(3),SMF6UCS   AND TRAIN                      81365
         MVC   MGFCB(4),SMF6FCB   AND FCB                        81365
         TM    MODEFG,FGXER  XEROX PROCESSING ?                  89142
         BZ    *+12          NO                                  89142
         MVI   JFCUCSID,C'U' REPLACE BY UN/U11 FOR NO PROCESSING 89142
         NI    JFCUCSOP,255-JFCFOLD  RESET FOLD (RPAGE OFFSET)   89163
         TM    JFCUCSOP,JFCBEXTP  HAVE A 3800 JFCB EXTENSION ?   93008
         BZ    *+8           NO                                  93008
         NI    JEX3800-4,255-X'80'  ACTIVATE JFCBE EXIT          93010
         SPACE 1                                                 89216
***********************************************************************
*                                                                     *
*        READ SYSIN, IF ANY.  HONOR CONTROL CARDS:                    *
*        ..TR VAL REP        SET TRANSLATION                          *
*        DJDE TEXT           SUPPLY ADDITIONAL DJDE TEXT         90057
*        ANYTHING ELSE - TREAT AS WTO REQUEST                         *
*                                                                     *
***********************************************************************
         INPOPEN SYSIN,DEV=1,OPT=(DUMMY,NOWTO)   SYSIN ?         89142
         CH    R15,=H'4'     OPENED ?                            89142
         BH    DONEINP       NO; LEAVE EVERYTHING AS IS          89180
         L     R10,=A(INPROC)  GET INPUT PROCESSOR               89216
         BASR  R9,R10        INVOKE IT                           89216
DONEINP  SERVCALL LPADL,=CL8'@INPREAD'  CLEAR INPUT MODULE       89180
         ST    R15,@INPREAD                                      89180
         B     JFCBONCE      START PROCESSING                    81365
         POP   USING                                            GP99354
         SPACE 1                                                 79313
         AIF   ('&LOCAL' NE 'PID').NOFRMT                        92302
FORMCHEK MVC   DB,BLANKS                                         92357
         MVC   DB(L'S166FMN),S166FMN                             92358
         SERVICE GFORM,S166FMN  LOOK-UP FORM IN ACCT. TABLE      92358
         NI    FLAG3,255-FGVOLT  RESET FORM IN TABLE             86092
         CH    R15,=H'4'     WAS THE FORM FOUND ?                89178
         BH    4(,R10)       NO                                  89178
         USING A$GSFNO,R1    DECLARE RETURNED ENTRY              89178
*DEFUNCT MVC   S166FMN,A$GSFNO  USE PREFERRED FORM NUMBER        92358
         TM    FLAG3,FGUCS   DEFAULT UCS ?                       86092
         BZ    SFORMFCB      NO                                  86092
         MVC   SMF6UCS,A$GSFUCS  COPY UCS NAME                   89178
         MVC   SMF6UCS+1(3),=C'N  '  FORCE SUFFIX                89163
         L     R14,OUTUCB@   LOAD PRINTER UCB                    89178
         CLI   UCBTBYT4-UCBOB(R14),X'09'  3211 ?                 89163
         BNE   *+10          NO                                  89163
         MVC   SMF6UCS+1(2),=C'11 '                              89163
         MVC   JFCUCSID,SMF6UCS  AND TO JFCB                     86092
         OI    JFCUCSOP,JFCFOLD   SET FOLD OPTION                86092
         CLI   JFCUCSID,C'S'  SN TRAIN OR HIGHER ?               93008
         BL    SFORMFCB      NO                                  86092
         NI    JFCUCSOP,255-JFCFOLD  NO FOLD OPTION FOR LOWER CASE
SFORMFCB TM    FLAG3,FGFCB   DEFAULT FCB ?                       86092
         BZ    SFORMRET                                          86092
         MVC   SMF6FCB,A$GSFFCB   COPY DEFAULT FCB               89178
SFORMRET OI    FLAG3,FGVOLT  SHOW ENTRY FOUND IN VOLT            86092
         TM    MODEFG,FGXER  XEROX MODE ?                        89144
         BNZ   SFORMSET      YES; LOOK FOR SETUP                 89144
         TM    A$GSFFG1,X'80' SKIP (LOCAL) SEPARATOR ?           89178
         BZR   R10           NO                                  87330
         OI    FLAG3,FGNOSEP SET INHIBIT                         87330
         BR    R10           TAKE GOOD RETURN                    86092
SFORMSET CLI   XERSETNM,C' '  SETUP ALREADY SPECIFIED ?          89144
         BH    SFORMXEP      YES; KEEP IT                        89144
*   NOTE THAT WHEN THE USER OVERRIDES FORM'S DEFAULT SETUP, I ALSO
*        WNAT TO COPY THE PAPER (DEFAULT) FROM SETUP, NOT FORM.  89181
*GONE*   MVC   XERSETNM(6),A$GSFSET  COPY SETUP NAME             89178
*GONE*   CLI   XERPAPER,C' '  PAPER ALREADY SPECIFIED ?          89178
*GONE*   BH    *+10          YES                                 89178
*GONE*   MVC   XERPAPER,A$GSFPAP  USE DEFAULT                    89178
SFORMXEP TM    A$GSFFG1,X'10'  INHIBIT SEPARATOR ON XEROX ?      89178
         BZR   R10           NO                                  89144
         OI    FLAG3,FGNOSEP   YES                               89144
         BR    R10           RETURN                              89144
         DROP  R1                                                89178
.NOFRMT  SPACE 2
JFCBONCE SR    R7,R7         CLEAR RESTART PAGE NUMBER           81365
         AIF   (&MVS OR '&LOCAL' NE 'CCSI').NOREQ1               81365
         LM    R3,R4,LOCALPCE  GET PCE AND JQE BACK              81365
         LTR   R3,R3         STORED ?                            81365
         BZ    JFCB          NO                                  81365
         USING PCEDSECT,R3                                       81365
         MODESET KEY=ZERO                                        81365
*        OI    XPCESTAT,XREXREQ  HASP REQUEUE AT END OF JOB      81365
*        OI    FLAG1,FGREQ   SHOW REQUEUE DONE                   81365
         OI    QUEFLAG4(R4),QUERERUN  SHOW RERUN PERMITTED       81365
         DROP  R3                                                81365
         MODESET KEY=NZERO                                       81365
.NOREQ1  SPACE 1                                                 81365
JFCB     SR    R9,R9                    PAGE COUNTER            GP03146
         LH    R8,MAXLINES             FORCE EJECT IF NO CONTROLS
         NI    FLAG1,255-FGERR  RESET FLAGS                      81365
         OI    FLAG2,FGEJECT   FORCE INITIAL PAGE EJECT          81365
         SPACE 1
         BAS   R10,RGNCLEAN  UNFRAGMENT                          81365
         OPEN  (,(INPUT,REREAD)),MF=(E,INOPLIST)                 81365
         TM    SYSUT1+DCBOFLGS-IHADCB,DCBOFOPN   OPENED OK ?     81365
         BO    OPEN2
BADOPEN  WTO   'BLOCKPRT - UNSUCCESSFUL OPEN FOR SYSUT1',ROUTCDE=11
         B     ABND
OPEN2    TM    SYSUT2+DCBOFLGS-IHADCB,DCBOFOPN   RESTARTING ?    81365
         BO    MESSAGEA                DON'T OPEN THE PRINTER AGAIN
         BAS   R10,RGNCLEAN  UNFRAGMENT                          81365
         OPEN  MF=(E,OUTOPLST),TYPE=J  OPEN THE PRINTER          81365
         TM    SYSUT2+DCBOFLGS-IHADCB,DCBOFOPN                   81365
         BO    RGNCHECK      CHECK UNUSED STORAGE                81365
         MVI   BADOPEN+46,C'2'
         B     BADOPEN
         SPACE 1
RGNCHECK MVC   NULLBLK+4(1),CCSINGLE                             89144
         TM    MODEFG,FGXER  XEROX OUTPUT ?                      89142
         BZ    NOTXER                                            89142
         L     R10,=A(XERINIT)  GET INITIALIZATION               89145
         BASR  R14,R10       INITIALIZE XEROX                    89145
NOTXER   DS    0H                                                89142
         AIF   (&MVS).NOSTCHK                                    81365
         BAS   R10,RGNCLEAN  UNFRAGMENT AND CHECK STORAGE        81365
         BNZ   PROCMSGS      NONE FREE; PROCEED                  81365
         SRL   R5,12         TRUNCATE TO 4K BOUNDARY             81365
         SLL   R5,12                                             81365
         SH    R5,OHDWASTE   ALLOW FOR DUMP AND OTHER OVERHEAD   81365
         BNP   PROCMSGS      OK; SQUEEZED BY                     81365
         SRL   R5,10         MAKE UNITS OF HEX K                 81365
         CH    R5,=H'64'     TOO MUCH ?                          81365
         BL    PROCMSGS      NO; SKIP MESSAGE                    81365
         CVD   R5,DB                                             81365
         MVC   STOREBIG+8(6),=X'402020202120' MAKE EDIT MASK     81365
         ED    STOREBIG+8(6),DB+5                                81365
STOREBIG WTO   ' NNNNNK WASTED - RUN IN SMALLER REGION',ROUTCDE=11
.NOSTCHK SPACE 1                                                 79313
PROCMSGS TM    FLAG3,FGNOSEP+FGNOMSG   INHIBIT SEPARATORS ?      88061
         BNZ   PROCMSGW      YES; JUST DO WTO (IF NEEDED)        87330
         LA    R3,@MSG       GET MESSAGE LIST                    81365
         LH    R4,NUMMSGS    GET NUMBER TO DO                    81365
LOOPMSGS L     R1,0(,R3)     GET NEXT MESSAGE ADDRESS            81365
         BAS   R10,PUTMSG    WRITE IT OUT                        81365
         LA    R3,@MSG2-@MSG(,R3)     POINT TO NEXT              81365
         BCT   R4,LOOPMSGS   DO ALL                              81365
PROCMSGW LH    R4,NUMMSGS    GET NUMBER OF MESSAGES AGAIN        81365
         SH    R4,=Y((@MSGEND-@MSG)/(@MSG2-@MSG))  GET USER MESSAGES
         BNP   SETUPMSG      NONE                                81365
         LA    R3,@MSGEND    GET FIRST USER MESSAGE              81365
LOOPWTOS L     R1,0(,R3)     GET ADDRESS OF MESSAGE              81365
         BAS   R10,PUTOPER   WRITE TO OPERATOR                   81365
         LA    R3,@MSG2-@MSG(,R3)                                81365
         BCT   R4,LOOPWTOS   DO ALL                              81365
SETUPMSG LA    R1,MSGSETUP   GET SETUP MESSAGE                   81365
         TM    MODEFG,FGXER  XEROX MODE ?                        89145
         BZ    SETUPMXG      NO                                  89145
         L     R10,=A(XERINIT)  GET INITIALIZATION               91136
*FAILS*  BASR  R14,R10       REPEAT DJDE AFTER SEPARATOR (3.5)   91136
         LA    R1,MXGSETUP   USE ALTERNATE MESSAGE               89145
         MVC   MXGSTUCB,MSGSTUCB                                 89145
         MVC   MXGSTFRM,MSGSTFRM                                 89145
         MVC   MXGSTSET,XERSETNM  SETUP                          89145
         MVC   MXGSTPAP,XERPAPER  PAPER                          89145
SETUPMXG BAS   R10,PUTOPER   SHOW OFF                            81365
         TM    MODEFG,FGFAKE REAL PRINTER ?                      89142
         BNZ   REPLYGO       NO; NO OPERATOR CONTROL             89142
         SPACE 2
*         MESSAGE PROCESSOR
         SPACE 2
MESSAGE  TTIMER CANCEL                 CANCEL ANY REMAINING TIME
         TM    SMF6IOE,F6STOP+F6REST  STOP OR RESTART ISSUED ?   81365
         BNZ   ENDOJOB                                           81365
MESSAGEA MVC   FINAL(14),MASK          FOR REPLY SHIFT ROUTINE
         XC    WTORECB,WTORECB         CLEAR ECB                 81365
         MVI   OPREPLY+7,X'FF'  RESTORE SCAN STOPPER             92358
         TM    MODEFG,FGXER+FGFAKE                               89142
         BO    SETTIME                                           89142
M1       WTOR  'XXXXXXXX BLOCKPRT - COMMAND ?',OPREPLY,7,WTORECB,ROUTCD*
               E=(1,8)                                           81365
         TM    FLAG1,FGERR   ERROR IN PREVIOUS REPLY ?           81365
         BO    REPWAIT       WAIT IF SO                          81365
         TM    FLAG1,FGINIT  INITIAL MESSAGES DONE ?             81365
         BO    SETTIME                  SKIP WAIT IF SO
REPWAIT  WAIT  ECB=WTORECB   WAIT FOR REPLY                      81365
REPLYOP  BAS   R10,CANDOM    ISSUE DOM                           81365
         NI    FLAG1,255-FGERR  RESET REPLY ERROR FLAG           81365
         CLC   =C'GO',OPREPLY     GO COMMAND ?                   81365
         BNE   CHKSTOP                 IF NOT, CHECK OTHERS
REPLYGO  OI    FLAG1,FGINIT  INITIALIZATION COMPLETED            81365
         B     REPEOFT       CHECK FOR GO AFTER END-FILE         81365
         SPACE 1
CHKSTOP  CLC   =C'STOP',OPREPLY    STOP COMMAND ?                81365
         BE    REPSTOP       BRANCH IF SO                        81365
         CLC   =C'END',OPREPLY                                   81365
         BE    REPSTOP                                           81365
         CLC   =C'HELP',OPREPLY                                  81365
         BE    REPHELP                                           81365
         CLI   OPREPLY,C'?'                                      81365
         BE    REPHELP                                           81365
         AIF   (&MVS OR '&LOCAL' NE 'CCSI').NOLOC                81365
         CLC   =C'RERUN',OPREPLY  RERUN LATER ?                  81365
         BE    REPREQUE      YES; REQUEUE                        81365
         CLC   =C'CANCEL',OPREPLY  CANCEL RERUN ?                81365
         BNE   CHEKREST      NO; CHECK RESTART                   81365
REPREQUE LM    R3,R4,LOCALPCE   GET PCE AND JQE BACK             81365
         LTR   R3,R3         ANY ?                               81365
         BZ    REPREQBD      NO; TOO BAD                         81365
         USING PCEDSECT,R3                                       81365
         MODESET KEY=ZERO                                        81365
         CLI   OPREPLY,C'C'  CANCEL ?                            81365
         BNE   REPREQ        NO; SET RERUN                       81365
         NI    XPCESTAT,255-XREXREQ  DO NOT REQUEUE              81365
         NI    FLAG1,255-FGREQ  SHOW UNREQUEUED                  81365
         B     REPREQEN      GO TO COMMON                        81365
REPREQ   OI    XPCESTAT,XREXREQ  HASP REQUEUE AT END OF JOB      81365
         OI    FLAG1,FGREQ   SHOW REQUEUE DONE                   81365
         DROP  R3                                                81365
REPREQEN MODESET KEY=NZERO                                       81365
         LA    R1,MSGREQOK   SET DONE                            81365
         B     REPREQOP                                          81365
REPREQBD LA    R1,MSGREQBD   SHOW UNABLE TO HONOR                81365
REPREQOP BAS   R10,PUTOPER                                       81365
         OI    FLAG1,FGERR                                       81365
         B     MESSAGEA      SOLICIT MORE INPUT                  81365
MSGREQBD VCON  'UNSUPPORTED COMMAND'                             81365
MSGREQOK VCON  'RERUN STATUS CHANGED'                            81365
.NOLOC   SPACE 1                                                 81365
CHEKREST CLC   =C'RESTART',OPREPLY    SOFT RESTART ?             81365
         BNE   REPNUMB       SEE IF NUMERIC                      81365
         SR    R7,R7         SET RESTART PAGE NUMBER             81365
SETREST  OI    SMF6IOE,F6REST   INDICATE RESTART                 81365
         BAS   R10,WRITESMF    WRITE OUT RESTART RECORD          81365
         B     REPSTOP2      JOIN COMMON CODE                    81365
REPSTOP  OI    SMF6IOE,F6STOP  INDICATE STOP RECORD              81365
         TM    DCBOFLGS-IHADCB+SYSUT1,DCBOFTM  EOF ?             89145
         BNZ   MESSAGE       YES; MESSAGE ALREADY WRITTEN        89145
REPSTOP2 OI    FLAG1,FGPAGE  SET FOR NEW SHEET                   89142
         LA    R1,MSGINTER   DISPLAY INTERRUPT MESSAGE           81365
         BAS   R10,PUTMSG                                        81365
         B     MESSAGE
         SPACE 1
REPNUMB  CLI   OPREPLY,C'+'    FORWARD SPACE ?                   81365
         BE    REPFWD        YES                                 81365
         CLI   OPREPLY,C'-'    BACKSPACE ?                       81365
         BE    REPBACK                                           81365
         BAS   R10,NUMSCAN   CONVERT TO NUMERICS                 81365
         B     BADREPLY      NOT NUMERIC                         81365
         SHEET ,             ALIGN TO SHEET BOUNDARY             89142
         CR    R7,R9         FORWARD OR BACKWARD ?               81365
         BH    REPSKIP       FORWARD - JUST SKIP                 81365
         B     REPREST       BACKWARD                            81365
         SPACE 1                                                 81365
REPFWD   MVI   OPREPLY,C'0'    CHANGE PLUS TO ZERO               81365
         BAS   R10,NUMSCAN   CONVERT                             81365
         B     BADREPLY      NOT NUMERIC                         81365
         LTR   R7,R7         ANY POSITIONING ?                   81365
         BZ    MESSAGE       NO; JUST RESPOND AND CONTINUE       81365
         AR    R7,R9         GET RESTART PAGE                    81365
         SHEET ,             ALIGN TO SHEET BOUNDARY             89142
         CR    R7,R9         NEED TO BACK UP ?                   89142
         BNH   REPREST       YES; DRATS                          89142
REPSKIP  OI    FLAG1,FGSKIP+FGINIT  SET SKIP MODE                81365
         OI    FLAG1,FGPAGE  NEED EJECT / TRANSITION             89144
REPEOFT  TM    DCBOFLGS-IHADCB+SYSUT1,DCBOFTM   END-FILE HIT ?   81365
         BZ    MESSAGE       NO                                  81365
         OI    FLAG1,FGERR   SET ERRONEOUS REPLY                 81365
         LA    R1,MSGGOEOF                                       81365
         BAS   R10,PUTOPER   NAG, NAG                            81365
         B     MESSAGEA                                          81365
         SPACE 1                                                 81365
REPBACK  MVI   OPREPLY,C'0'    CHANGE MINUS TO ZERO              81365
         BAS   R10,NUMSCAN   CONVERT                             81365
         B     BADREPLY                                          81365
         LTR   R15,R7        ANY ?                               81365
         BZ    MESSAGE       NO                                  81365
         LR    R7,R9                                             81365
         SR    R7,R15        GET NEW PAGE NUMBER                 81365
         SH    R7,=H'3'      FOR GOOD MEASURE                    81365
         BP    *+8                                               81365
         LA    R7,1          SET RESTART PAGE                    81365
         SHEET ,             ALIGN TO SHEET BOUNDARY             89142
REPREST  OI    FLAG1,FGSKIP+FGINIT   FAKE A RESTART              81365
         B     SETREST       PROCESS IT                          81365
         SPACE 1
BADREPLY LA    R1,MSGNONUM                                       81365
         BAS   R10,PUTOPER   SHOW INVALID REPLY                  81365
         TM    FLAG1,FGHELP  HELP ALREADY DISPLAYED ?            81365
         BNZ   HELPOUT       YES; JUST SET FLAGS                 81365
         SPACE 1                                                 81365
REPHELP  LA    R3,@HELP                                          81365
         LA    R4,(@HELPEND-@HELP)/(@MSG2-@MSG)                  81365
HELPLOOP L     R1,0(,R3)                                         81365
         BAS   R10,PUTHELP   FOR HELP, SKIP JOBNAME AND PGM      83080
         LA    R3,@MSG2-@MSG(,R3)                                81365
         BCT   R4,HELPLOOP                                       81365
HELPOUT  OI    FLAG1,FGERR+FGHELP   SET ERROR FLAG               81365
         B     MESSAGEA                TRY AGAIN
         SPACE 2
*        PRINT, SKIP PROCESSING
         SPACE 2
SETTIME  STIMER REAL,TIMEX,BINTVL=TIME
         NI    FLAG1,255-FGTIME  RESET TIMER NEEDED              81365
         SPACE 1                                                 81365
         TM    FLAG1,FGSKIP  SKIP MODE ?                         81365
         BZ    GETLOOP       NO                                  81365
         LM    R4,R6,INPOFF  GET INPUT OFFSET AND MOVE ADDRESS   81365
SKIPLOOP TM    WTORECB,X'40'  WAS WTOR POSTED ?                  81365
         BNZ   REPLYOP       YES; SEE WHAT OPERATOR WANTS        81365
SKIPGET  GET   SYSUT1        GET A RECORD                        81365
         LH    R2,DCBLRECL-IHADCB+SYSUT1  GET INPUT LENGTH       81365
         SR    R2,R4         LESS RDW ADJUSTMENT                 81365
         BM    SKIPGET       SKIP IT                             89144
         BP    SKIPNNUL      ***INSERTION TO FIX                 90284
         LA    R1,DB         *****WYLBUR ERROR                   89144
         LA    R2,6          *******SAVE VAR DOESN'T WORK        89144
         MVC   DB(6),NULLBLK MAKE MINIMUM ACCEPTABLE RECORD      90284
SKIPNNUL LR    R3,R1         SAVE IT OVER TRT                    81365
         AR    R1,R4         ADD RDW OFFSET                      81365
         TM    FLAG2,FGEJECT   EJECT FORCED ?                    81365
         BNZ   SKIPGETP      YES; TREAT AS NEW PAGE              81365
         TM    DCBRECFM+SYSUT1-IHADCB,DCBRECCC  CONTROL ?        90284
         BZ    SKPMVNTR      NO                                  90284
         TM    FLAG3,FGTRCC  CARRIAGE CONTROL TRANSLATION ?      90284
         BZ    SKPMVNTR      NO                                  90284
         TR    0(1,R1),TRANSCON  REPLACE CONTROL CHARACTER       90284
SKPMVNTR SR    R2,R2                                             81365
         TRT   0(1,R1),CCTABLE  CHECK FOR NEW PAGE               81365
         CH    R2,=H'12'     NCC OR EJECT ?                      81365
         BL    SKIPGET       NEITHER                             81365
         BH    SKIPGETP      NEW PAGE                            81365
         LA    R8,1(,R8)     NEW LINE                            81365
         CH    R8,MAXLINES   PAGE FULL ?                         81365
         BL    SKIPGET       NO                                  81365
SKIPGETP SR    R8,R8         CLEAR LINE COUNT                    81365
         NI    FLAG2,255-FGEJECT  RESET EJECT REQUIRED           81365
         LA    R9,1(,R9)     INCREASE CURRENT PAGE NUMBER        81365
         CR    R7,R9         RESTART POINT REACHED ?             81365
         BH    SKIPLOOP      NOT YET                             81365
         OI    FLAG2,FGEJECT  FORCE EJECT ON NEXT TRT CALL       81365
         NI    FLAG1,255-FGSKIP   RESET SKIP MODE                81365
         BAS   R10,PUTCURR   DISPLAY CURRENT PAGE NUMBER FOR OPSIE
         LA    R1,MSGSPACE   SHOW PRINTING SPACED                81365
         BAS   R10,PUTMSG                                        81365
         LA    R1,MSGCURR    SHOW RESTART PAGE NUMBER            81365
         BAS   R10,PUTMSG                                        81365
         LA    R1,MXALIGN                                        89142
         BAS   R10,PUTXER    PUT A LINE IF XEROX                 89142
         BCTR  R9,0          FIX PAGE NUMBER FOR TRT UPDATE      81365
         LR    R1,R3         RESTORE LINE ADDRESS                81365
         B     GETNSKIP      START NORMAL PROCESSING             81365
         SPACE 1                                                 81365
GETLOOP  TM    WTORECB,X'40'    WAS WTOR POSTED ?                81365
         BNZ   REPLYOP       YES; SEE WHAT OPERATOR WANTS        81365
GET      GET   SYSUT1
GETNSKIP LR    R2,R1         COPY RECORD ADDRESS                 81365
         LH    R3,DCBLRECL-IHADCB+SYSUT1  GET INPUT LENGTH       81365
         TM    DCBRECFM-IHADCB+SYSUT1,DCBRECF  FIXED OR UNDEFINED ?
         BNZ   GETNFAKE      WHATEVER; NOT VARIABLE              93041
         CH    R3,=H'4'      ***BAD WYLBUR V RECORD ?            89144
         BNE   GETNFAKE      NO                                  89144
         LA    R3,7          FAKE RDW+CC+TRC                     93041
         LA    R2,NULLBLK                                        89144
GETNFAKE LM    R4,R6,INPOFF  GET OFFSET, ADDRESS AND LENGTH OF WORK
         SR    R3,R4         ADJUST FOR POSSIBLE RDW             81365
         BNP   GET           SKIP BAD FRAGMENT                   81365
         MVC   0(3,R5),BLANKS  MAKE BLANKS FOR SHORT RECORD      94110
         AR    R2,R4         POINT TO FIRST BYTE                 81365
         CR    R3,R6         NEED TO TRUNCATE ?                  81365
         BNH   *+6           NO                                  81365
         LR    R3,R6         TRUNCATE                            81365
         LR    R15,R3        COPY THE LENGTH                     81365
         SH    R15,=H'1'       LESS ONE                          81365
         LA    R14,0(R2,R15)  POINT TO LAST BYTE IN BUFFER       81365
         LTR   R15,R15       NULL RECORD ?                       88252
         BNP   GETNMVC       NO; MUST PROCESS CC                 93041
DEBLANK  CLI   0(R14),C' '   TRAILING BLANK ?                    81365
         BNE   GETMVC        NO; MOVE                            81365
         BCTR  R14,0         BACKSPACE                           81365
         BCT   R15,DEBLANK                                       81365
GETMVC   EX    R15,MVCTEXT   MOVE TEXT TO BUFFER                 81365
         TM    FLAG3,FGTRAN  TRANSLATE ?                         89215
         BZ    GETMVNTR      NO                                  89215
         EX    R15,TRANTEXT  TRANSLATE                           89215
GETNMVC  TM    DCBRECFM+SYSUT1-IHADCB,DCBRECCC  CONTROL ?        89215
         BZ    GETMVNTR      NO                                  89215
         EX    0,MVCTEXT     COPY CARRIOAGE CONTROL INTACT       89215
         TM    FLAG3,FGTRCC  CARRIAGE CONTROL TRANSLATION ?      90284
         BZ    GETMVNTR      NO                                  90284
         TR    0(1,R5),TRANSCON  REPLACE CONTROL CHARACTER       90284
GETMVNTR LA    R0,6          SET MINIMUM LENGTH                  81365
         TM    DCBOPTCD-IHADCB+SYSUT2,DCBOPTJ  OPTCD=J ?         93040
         BZ    *+8           NO                                  93040
         LA    R0,7          DATA CHARACTER AFTER TRC            93040
         LA    R15,4+1(R5,R15)  GET LENGTH +RDW +1 (EX) + AREA(+1)
         LA    R14,AREA                                          81365
         SR    R15,R14       ADD 1 IF NOCC IN INPUT              81365
         CR    R15,R0        TOO SHORT ?                         81365
         BNL   *+6           NO                                  81365
         LR    R15,R0        FIX TO MINIMUM ACCEPTABLE QSAM LENGTH
         STH   R15,RDW                                           81365
         LA    R3,GETLOOP    PRESET FOR NEW PAGE - TEST REPLY ECB
         BAS   R10,PAGECHK   PAGE EJECT ?                       GP03146
         LA    R3,GET        NO; SET FOR RETURN TO GET           81365
         ICM   R15,15,SMF6NLR   GET CURRENT LINE COUNT           81365
         AH    R15,=H'1'     UP BY ONE                           81365
         STCM  R15,15,SMF6NLR   STASH BACK                       81365
         PUT   SYSUT2,RDW    WRITE A RECORD                      81365
         TM    FLAG1,FGTIME  (RE)ISSUE TIMER ?                   81365
         BZR   R3            NOT YET                             81365
         SPACE 2
*        PUT OUT RESTART MESSAGE ON STIMER EXIT
         SPACE 2
PAGEMSG  L     R1,ADUT1      GET INPUT TCT COUNT FIELD           81365
         CLC   SMF6IND#,0(R1)                                    89178
         BE    SETTIME       IF NO CHANGE, SUPPRESS MSG          79314
         BAS   R10,CANDOM    CANCEL PREVIOUS MESSAGE             81365
         BAS   R10,PUTCURR                                       81365
         LA    R1,1                                              81365
         AH    R1,SMFINT     GET INTERVAL COUNTER                81365
         STH   R1,SMFINT     STASH BACK                          81365
         CH    R1,=H'2'      WRITE SMF EVERY 15 MINUTES          81365
         BNH   SETTIME       NOT YET; JUST RESET TIMER           81365
         BAS   R10,WRITESMF  WRITE SMF RECORD                    81365
         B     SETTIME
         SPACE 2                                                 81365
*        MESSAGE WTO AND PRINT PROCESSING                        81365
*                                                                81365
PUTMSG   TM    FLAG3,FGNOMSG KEEP IT CLEAN ?                     88061
         BNZ   PUTMSGX       YES; DON'T SMUDGE PAPER             89142
         TM    FLAG1,FGPAGE  PAGE EJECT REQUIRED ?               81365
         BZ    PUTMSGL       NO                                  81365
         ST    R1,DB         SAVE MESSAGE ADDRESS                81365
         ST    R10,DB+4      SAVE RETURN                         89144
         LA    R1,MXALIGN    SET FOR NEW SHEET                   89142
         BAS   R10,PUTXER                                        89142
         PUT   SYSUT2,PAGEJECT  EJECT                            81365
         LA    R1,MXAUXFD    PUT MESSAGES ON AUXILIARY PAPER     90112
*TEST*   BAS   R10,PUTXER    (SEPARATOR)                         90112
         L     R10,DB+4      RESTORE RETURN                      89144
         L     R1,DB         RESTORE MESSAGE ADDRESS             81365
         NI    FLAG1,255-FGPAGE  RESET EJECT REQUIRED            81365
PUTMSGL  LH    R15,0(,R1)    GET LENGTH OF USER TEXT             81365
         SH    R15,=H'5'     ALLOW FOR RDW AND 1 FOR EXECUTE     81365
         BM    PUTMSGX       NONE; SKIP                          89142
         LA    R14,L'PUTLINE-1-1  GET TEXT LENGTH                81365
         TM    DCBOPTCD-IHADCB+SYSUT2,DCBOPTJ  TRC ?             93040
         BZ    PUTMSGNJ      NO                                  93040
         BCTR  R1,0          START MOVING FROM BYTE BEFORE TEXT  93040
         LA    R15,1(,R15)   INCREASE USER'S LENGTH              93040
PUTMSGNJ CR    R15,R14       WILL IT FIT ?                       81365
         BNH   *+6           YES                                 81365
         LR    R15,R14       TRUNCATE                            81365
         MVC   PUTLINE(1),CCSINGLE  SET SINGLE-SPACE CONTROL     89286
         EX    R15,PUTMMVC   MOVE TEXT                           81365
         TM    DCBOPTCD-IHADCB+SYSUT2,DCBOPTJ                    93040
         BZ    *+8           SKIP TRC                            93040
         MVI   PUTLINE+1,C' '  MAKE FIRST CHARACTER SET          93040
         LA    R15,6(,R15)   LENGTH WITH RDW, ETC.               81365
         SLL   R15,16        CLEAR SEGMENT DESCRIPTOR            81365
         STCM  R15,15,OPERWTO  SET LENGTH                        81365
         PUT   SYSUT2,OPERWTO  WRITE THE PRINT LINE              81365
PUTMSGX  BR    R10           RETURN TO CALLER                    81365
PUTMMVC  MVC   PUTLINE+1(0),4(R1)  MOVE TEXT TO PRINT LINE       81365
         SPACE 1                                                 89142
PUTXER   TM    MODEFG,FGXER  XEROX MODE ?                        89142
         BZR   R10           NO; RETURN                          89142
         LR    R0,R1                                             89142
         PUT   SYSUT2 (0)    WRITE TO XEROX                      89142
         BR    R10           RETURN TO CALLER                    81365
         SPACE 1                                                 81365
PUTCURR  CVD   R9,DB         PACK CURRENT PAGE NUMBER            81365
         MVC   MSGPAGE,PATPAGE  MAKE EDIT MASK                   81365
         ED    MSGPAGE,DB+4  EDIT PAGE NUMBER                    81365
         LA    R1,MSGCURR    SET FOR CURRENT                     81365
*        B     PUTOPER       DROP THROUGH TO WTO                 81365
PUTOPER  LH    R15,0(,R1)    GET MESSAGE LENGTH                  81365
         SH    R15,=H'5'     MAKE EXECUTE LENGTH OF TEXT         81365
         BMR   R10           RETURN IF NONE                      81365
         LA    R14,L'OPERTEXT-1  GET AVAILABLE LENGTH            81365
         CR    R15,R14       WILL IT FIT ?                       81365
         BNH   *+6           YES                                 81365
         LR    R15,R14       ELSE TRUNCATE                       81365
         MVI   OPERMSG,C' '                                      81365
         MVC   OPERMSG+1(L'OPERMSG-1),OPERMSG  BLANK ALL         81365
         EX    R15,PUTOMVC   MOVE TEXT TO BUFFER                 81365
         MVC   OPERJOB,JOBNAME  COPY RIGHT-JUSTIFIED JOB NAME    81365
         MVC   OPERID,BLOCKPRT+5  ADD PROGRAM NAME               81365
         TM    MODEFG,FGXER  XEROX MODE ?                        89144
         BZ    *+10          NO                                  89144
         MVC   OPERID(5),=C'XEROX'  ALTER                        89144
         MVI   OPERCOL,C':'  MAKE IT PRETTY                      81365
         LA    R15,5+OPERTEXT-OPERMSG(,R15)  GET TOTAL LENGTH    81365
PUTOPERL LA    R1,OPERWTO(R15)  GET END OF MESSAGE               83080
         MVC   0(4,R1),=X'40008120'  DESC=2, ROUT=(1,8,11)       81365
         SLL   R15,16                                            81365
         STCM  R15,15,OPERWTO  SET LENGTH                        81365
         OI    OPERWTO+2,X'80'  SET DESC/ROUTCODE PRESENT        81365
         WTO   MF=(E,OPERWTO)  WRITE THE MESSAGE                 81365
         L     R14,DOMPOINT  GET THE DOM POINTER                 81365
         MVI   0(R14),0      CLEAR END OF LIST FLAG              81365
         ST    R1,4(,R14)    SET NEW ID                          81365
         MVI   4(R14),X'80'  SET END OF LIST                     81365
         LA    R14,4(,R14)   GET NEW ENTRY ADDRESS               81365
         LA    R15,DOMEND    GET END ADDRESS                     81365
         CR    R14,R15       END OF LIST ?                       81365
         BNL   *+8           YES                                 81365
         ST    R14,DOMPOINT  ELSE SET IT                         81365
         OI    DOMFLAG,X'80'   SET DOM PRESENT                   81365
         BR    R10           RETURN TO CALLER                    81365
PUTHELP  LH    R15,0(,R1)    GET MESSAGE LENGTH                  83080
         SH    R15,=H'5'     SET EXECUTE LENGTH                  83080
         BMR   R10           ?                                   83080
         LA    R14,L'OPERTEXT-1  MAXIMUM TEXT LENGTH             83080
         CR    R15,R14                                           83080
         BNH   *+6                                               83080
         LR    R15,R14       TRUNCATE                            83080
         EX    R15,PUTOMVT   MOVE TEXT                           83080
         LA    R15,5(,R15)   MAKE TOTAL LENGTH                   83080
         B     PUTOPERL      GO TO COMMON                        83080
PUTOMVT  MVC   OPERMSG(0),4(R1)  MOVE TEXT TO WTO MSG            83080
PUTOMVC  MVC   OPERTEXT(0),4(R1)  MOVE TEXT TO WTO               81365
         SPACE 1                                                 81365
CANDOM   TM    DOMFLAG,X'80' ANY DOM IDS STASHED ?               81365
         BZ    CANDOMI       NO; JUST INITIALIZE                 81365
         DOM   MSGLIST=DOMTAB  SET MESSAGES DELETABLE            81365
CANDOMI  MVI   DOMFLAG,0     CLEAR FLAG                          81365
         LA    R14,DOMTAB-4                                      81365
         ST    R14,DOMPOINT  SET POINTER                         81365
         BR    R10           RETURN TO CALLER                    81365
         SPACE 2
*        GET HIGH ORDER ZEROES, DO NUMERIC CHECK, AND CONVERT INPUT
         SPACE 2
NUMSCAN  MVC   FINAL(14),FINAL+1       SHIFT REPLY
         CLI   OPREPLY,255   SHIFTED ENOUGH ?                    81365
         BNE   NUMSCAN                 SHIFT AGAIN IF NOT
         MVC   MASKWK,MASK             NUMERIC MASK
         NC    MASKWK,FINAL            'AND' REPLY AGAINST MASK
         CLC   MASKWK,MASK             NUMERIC ?
         BNER  R10                     ERROR, NOT NUMERIC        81365
         PACK  DB,FINAL(7)   NUMBER TO SKIP                      81365
         CVB   R7,DB         CONVERT IT                          81365
         B     4(,R10)                 GOOD CONVERTED VALUE IN R7
         SPACE 2
*        ROUTINE TO CHECK FOR PAGE EJECTS
         SPACE 2
PAGECHK  SR    R2,R2         CLEAR TRT CHARACTER INSERT REGISTER 81365
         TRT   AREA(1),CCTABLE  CHECK CHARACTER                  81365
         B     *+4(R2)       BRANCH ACCORDING TO TYPE            81365
         B     CNTRLBAD      INVALID; COUNT AS ERROR             81365
         B     CNTRLBAR      NOT SUPPORTED; REPLACE              81365
         B     CNTRLOK       USE AS IS                           81365
         B     CNTRLNCC      SPECIAL ENTRY FOR NO CONTROLS       81365
*        B     CNTRLTOP      EJECT                               81365
CNTRLTOP MVI   CCERR,X'0C'   RESET ERROR COUNTER                 81365
CNTRLTO2 NI    FLAG2,255-FGEJECT   RESET EJECT NEEDED            81365
         ICM   R15,15,SMF6PGE  GET CURRENT PAGE COUNT            81365
         AH    R15,=H'1'     UP BY ONE                           81365
         STCM  R15,15,SMF6PGE   STASH BACK                       81365
         SR    R8,R8                   RESET LOCAL LINE COUNTER GP03146
         LA    R9,1(,R9)     YES, ADD TO PAGE COUNT
         OI    FLAG1,FGPAGE  EJECT REQUIRED ON NEXT MESSAGE      81365
         ICM   R15,15,LIMPAGE  PAGE LIMIT ?                      91043
         BZ    4(,R10)       NO; RETURN NEW PAGE                 91043
         CR    R9,R15        NEW PAGE PAST LIMIT ?               91043
         BH    EOJ           FAKE END OF JOB                     91043
         B     4(,R10)       RETURN WITH NEW PAGE INDICATION     81365
         SPACE 1                                                 81365
CNTRLOK  MVI   CCERR,X'0C'   RESET ERROR COUNTER                 81365
CNTRLOK2 TM    FLAG2,FGEJECT   NEW PAGE FORCED ?                 81365
         BZR   R10           NO; RETURN AS IS                    81365
         TM    FLAG2,FGMCC   MACHINE CHARACTERS ?                81365
         BZ    CNTRLOAS      NO; SET ASA                         81365
         PUT   SYSUT2,PAGEJECT  EJECT A PAGE                     81365
         B     CNTRLTO2      GO TO NEW PAGE CODE                 81365
CNTRLOAS MVI   AREA,C'1'     SET ASA PAGE EJECT                  81365
         B     CNTRLTO2      AND DO COMMON PAGE EJECT CODE       81365
         SPACE 1                                                 81365
CNTRLNCC LA    R8,1(,R8)     UP LINE COUNT ON THIS PAGE          81365
         CH    R8,MAXLINES   IS PAGE FULL ?                      81365
         BL    CNTRLBAR      NO; PROCESS                         81365
         OI    FLAG2,FGEJECT   FORCE EJECT                       81365
         SPACE 1                                                 81365
CNTRLBAR MVI   CCERR,X'0C'   RESET ERROR COUNTER                 81365
CNTRLBAT MVC   AREA(1),CCSINGLE  REPLACE BY SINGLE SPACE         81365
         B     CNTRLOK2      AND TREAT AS VALID REQUEST          81365
CNTRLBAD AP    CCERR,=P'1'   UP COUNTER                          81365
         BNO   CNTRLBAT      REPLACE BY SUBSTITUTE CHARACTER     81365
         WTO   '10 CONSECUTIVE BAD CARRIAGE CONTROLS',ROUTCDE=(1,11)
         BAS   R10,CANDOM    CLEAR SCREEN                        81365
         ABEND 001,DUMP                                          81365
         SPACE 2
*        END OF JOB, END OF FILE ROUTINE
         SPACE 2
EOJ      MVC   MSGEOFPG,PATPAGE                                  81365
         CVD   R9,DB                                             81365
         ED    MSGEOFPG,DB+4   DISPLAY FINAL PAGE NUMBER         81365
         LA    R1,MSGEOF     WRITE END-FILE MESSAGE              81365
         BAS   R10,PUTOPER   TO OPERATOR                         81365
         OI    FLAG1,FGPAGE  EJECT                               89144
         LA    R1,MSGEOF                                         81365
         BAS   R10,PUTMSG    AND LISTING                         81365
         TM    MODEFG,FGXER+FGFAKE  REAL PRINTER ?               89142
         BNO   REPWAIT       FORCE WAIT FOR RESPONSE             81365
         OI    SMF6IOE,F6STOP  QUIT NOW                          89142
         SPACE 1                                                 81365
ENDOJOB  TM    DCBOFLGS-IHADCB+SYSUT1,DCBOFTM  EOF ?             81365
         BNZ   ENDOFEND      YES; MESSAGE ALREADY WRITTEN        81365
         LA    R1,MXALIGN                                        89142
         BAS   R10,PUTXER                                        89142
         OI    FLAG1,FGPAGE  EJECT                               89144
         BAS   R10,PUTCURR   GIVE OPERATOR FINAL PAGE COUNT      81365
         LA    R1,MSGCURR    GIVE USER PAGE NO.                  81365
         BAS   R10,PUTMSG      ON LISTING                        81365
ENDOFEND CLOSE (,REREAD),MF=(E,INOPLIST)                         81365
*        FREEPOOL SYSUT1     THIS HURTS ME MORE THAN IT HURTS YOU . .
         NI    SMF6IOE,255-F6REST   RESET FLAG                   81365
         TM    SMF6IOE,F6STOP   STOP ?                           81365
         BZ    JFCB          NO; RESTART PROCESSING              81365
         MVC   MSG1+4(3),=C'END'  SET END OF LISTING             81365
         LA    R3,@MSG       GET MESSAGE LIST                    81365
         LA    R4,((@MSGEND-@MSG)/(@MSG2-@MSG))  GET FIXED MESSAGES
LEOJMSGS L     R1,0(,R3)     GET NEXT MESSAGE ADDRESS            81365
         BAS   R10,PUTMSG    WRITE IT OUT                        81365
         LA    R3,@MSG2-@MSG(,R3)     POINT TO NEXT              81365
         BCT   R4,LEOJMSGS   DO ALL                              81365
         LA    R1,MXFONTOF   RESET FONT INDEX                    89142
         BAS   R10,PUTXER                                        89142
         OI    MODEFG,FGXNFONT  NO FONT INDEXING                 89144
         NI    FLAG3,255-FGNOMSG  RESET NO MESSAGES              90112
         LA    R1,MXALIGN    GET SHEET FEED                      90112
*TEST*   BAS   R10,PUTXER    FEED ANOTHER SHEET                  90112
         PUT   SYSUT2,PAGEJECT   EJECT ONE LAST TIME             81365
         LA    R1,MXAUXFD    SET FOR AUXILIARY TRAY              90112
*DEFER*  BAS   R10,PUTXER    WASTE ANOTHER SHEET                 90112
         MVC   MXGSEPT,=CL5' END '                               89144
         LA    R2,8                                              89144
         LA    R1,MXG1       GET FAKE JES2 SEPARATOR             89144
         BAS   R10,PUTXER    PROCESS IT                          89144
         BCT   R2,*-8        WRITE ENOUGH                        89144
         LA    R1,NULLBLK    GET A DUMMY BLOCK                   89142
         BAS   R10,PUTXER    DO FINAL WRITES                     89142
         LA    R1,MXDELIM    SET DELIMITER                       90113
         BAS   R10,PUTXER    WASTE ANOTHER SHEET                 90112
         TM    MODEFG,FGXER  XEROX ?                             89142
         BZ    *+10                                              89142
         MVC   DCBPUTA-IHADCB+SYSUT2(L'DCBPUTA),REALPUT+1  RESTORE
         CLOSE MF=(E,OUTOPLST)    CLOSE THE PRINTER              81365
         NI    SMF6IOE,F6STOP  INDICATE FINAL RECORD             81365
         BAS   R10,WRITESMF  WRITE TRAILING SMF RECORD           81365
         BAS   R10,CANDOM    CLEAR SCREEN                        81365
         TM    MODEFG,FGXER+FGFAKE  FAKE XEROX ?                 89142
         BO    CLOSEOUT      YES                                 89142
         ABEND 0             THIS IS 'NORMAL' COMPLETION         81365
CLOSEOUT SERVTERM ,          CLOSE                               89142
         PGMEXIT PFX=SAWE,RC=0 TERMINATE WITH RC=0               89142
         SPACE 2                                                 81365
NO001    SR    R15,R15                 IF SYNAD EXIT, FORGET IT GP03146
         BR    R14
         SPACE 2                                                 81365
WRITESMF TIME  BIN           GET CURRENT TIME                    81365
         STCM  R0,15,SMF6TME   SET CURRENT TIME                  81365
         STCM  R1,15,SMF6DTE     AND CURRENT DATE                81365
         L     R15,ADUT1     GET INPUT TCT COUNT FIELD           81365
         MVC   SMF6IND#,0(R15)  MOVE INPUT COUNT                 89178
         MVC   SMF6OUD#,SMF6NLR  UPDATE OUTPUT COUNT ALSO        89178
         TM    SMF6PAD1,SMF6FEXT  3800 EXTENSION ?               89178
         BNZ   *+10          YES                                 89178
         MVC   OVERSMF(SMF6LXSZ+S166RXSZ),S166LN3  SHORTEN       94101
         LA    R1,SMFRCD6    GET ADDRESS OF RECORD               81365
         SR    R0,R0                                             81365
         STC   R0,SMFINT+1   REINITIALIZE                        81365
         AIF   ('&LOCAL' NE 'PID').NOSMFW                        92355
         TM    MODEFG,FGFAKE REAL PRINTER ?                      89144
         BNZR  R10           YES; NO BLOCKPRT BILLING            89144
         SVC   253           UNPRIVILEGED SVC 83 - SMFWTM        79313
.NOSMFW  BR    R10           RETURN TO CALLER                    83007
         SPACE 1                                                 81365
ABENDMSG LR    R1,R0         COPY MESSAGE FRAGMENT               81365
         BAS   R10,PUTOPER   WRITE TO LOG (ROUTCDE=11)           81365
         B     ABND          AND QUIT                            81365
         EJECT ,
         DROP  R11,R12                                           81365
         USING INDCBEXT,R15
INDCBEXT BSM   R14,0         REMEMBER CALLER'S AMODE            GP03146
         LM    R11,R12,=A(BLOCKPRT,BLOCKPRT+4096)  GET BASE      81365
         DROP  R15
         USING BLOCKPRT,R11,R12                                  81365
         AM31  ,             JUST IN CASE                       GP03146
         N     R1,=X'00FFFFFF'  DITTO                           GP03146
         L     R2,WORKADDR                                       81365
         LR    R7,R1         COPY DCB ADDRESS OVER
         USING IHADCB,R7     DECLARE IT
         STM   R13,R14,EXSAVER-SAWE(R2)  SAVE REGISTERS          81365
         LA    R13,EXSAVER-SAWE(,R2)  DECLARE NEW AREA           81365
         USING EXSAVER,R13                                       81365
         TM    DCBRECFM,DCBRECU  RECFM=U ?                       87167
         BNO   INDCBNU       NO                                  87167
         ICM   R0,3,DCBLRECL  ZERO RECORD LENGTH ?               87167
         BNZ   INDCBNUR      NO                                  87167
         MVC   DCBLRECL,DCBBLKSI                                 87167
         B     INDCBNU                                           87167
INDCBNUR ICM   R0,12,DCBBLKSI  ANY BLOCKSIZE ?                   87167
         BNZ   INDCBNU       NO                                  87167
         STCM  R0,3,DCBBLKSI  USE LRECL                          87167
INDCBNU  L     R0,=A(MFGNLREC)  SET FOR BAD LRECL                92303
         OC    DCBLRECL,DCBLRECL       LRECL SUPPLIED ?
         BZ    BADOPMSG                                          81365
         L     R0,=A(MFGNBLK)   SET FOR BAD BLOCKSIZE            92303
         OC    DCBBLKSI,DCBBLKSI       BLOCKSIZE SUPPLIED ?
         BZ    BADOPMSG                                          81365
         NI    DCBOPTCD,DCBOPTQ  IGNORE ALL BUT ASCII BIT        81365
OIOPTCD  OI    DCBOPTCD,DCBOPTC+DCBOPTZ SET CHAINED SCHED, RER   79313
         TM    FLAG2,FGNCC   SKIP CHECKING ?                     81365
         BNZ   INDNCC        YES
         L     R0,=A(MFGNCC)  SET FOR MISSING CARRIAGE CONTROL INFO
         TM    DCBRECFM,DCBRECCC       ANY CONTROL CHARS. ?
         BNM   BADOPMSG      NO; TELL USER ALL ABOUT IT          81365
INDNCC   TM    DCBRECFM,DCBRECU        RECFM SUPPLIED ?
         L     R0,=A(MFGNRECF)  SET FOR BAD RECFM                92303
         BO    INDVCOM       SKIP VARIABLE TESTS                 87167
         BNM   BADOPMSG                                          81365
         TM    DCBRECFM,DCBRECV        VARIABLE ?
         BZ    INDVCOM       NO; SKIP VARIABLE TESTS             81365
         MVI   INPOFF+L'INPOFF-1,4  SET FOR RDW OVERHEAD         81365
         TM    DCBRECFM,DCBRECSB       SPANNED ?                 81365
         BZ    INDVCOM                 NO
         NI    DCBBFTEK,255-DCBBFT  TURN OFF BFTEK
         OI    DCBBFTEK,DCBBFTA     SET LOGICAL RECORD INTERFACE
         LH    R1,DCBLRECL             GET RECORD LENGTH
         LTR   R1,R1         > 32K ?
         BNM   *+8           OK
         L     R1,=F'65536'  ALLOW FOR MAX
         LA    R1,23(,R1)    ALLOW FOR SOME OVERHEAD
         SRL   R1,3          /8
         SLL   R1,3          *8
         ST    R1,OHDREC     SAVE RECORD OVERHEAD
INDVCOM  TM    FLAG2,FGNCC   SKIP CHECKING ?                     81365
         BZ    INDVCON       NO; CHECK INPUT
         TM    DCBRECFM-IHADCB+SYSUT1,DCBRECCC  USER SUPPLIED ALSO ?
         BZ    INDVCOO       NO                                  81365
         WTO   'NOCC SPECIFIED; INPUT CARRIAGE CONTROLS IGNORED' 81365
         LA    R14,AREA      SET FOR MOVE TO AREA, NOT AREA+1    81365
         LA    R15,L'AREA    ALLOW MAX MOVE                      81365
         STM   R14,R15,MOVETO    STASH IN POINTERS               81365
INDVCOO  NI    DCBRECFM-IHADCB+SYSUT2,255-DCBRECCC  RESET        81365
         MVI   DCBRECFM-IHADCB+SYSUT2,DCBRECCA   FAKE ASA        81365
         NI    FLAG2,255-FGMCC   RESET MACHINE CONTROL CHARACTERS
         MVI   CCTABLE,12    SET FOR INVALID WITH COUNT          81365
         MVC   CCTABLE+1(255),CCTABLE  FOR ALL INPUT             81365
         B     INDACOM3      SKIP RECFM MERGE
INDVCON  TM    DCBRECFM,DCBRECCM       MACHINE CONTROL ?
         BZ    INDACOM2                BRANCH IF NOT
         OI    FLAG2,FGMCC   SET TO MACHINE CONTROLS             81365
         MVI   CCSINGLE,X'09'  SET SINGLE SPACE CODE             91044
         MVI   PAGEJECT+4,X'8B'  MAKE MACHINE CODE PAGE EJECT    81365
INDACOM2 MVC   DCBRECFM-IHADCB+SYSUT2(1),DCBRECFM  COPY RECFM
         NI    DCBRECFM-IHADCB+SYSUT2,DCBRECCC  SAVE TYPE OF CONTROL
INDACOM3 OI    DCBRECFM-IHADCB+SYSUT2,DCBRECV  BUT SET VARIABLE
         TM    FLAG2,FGNCC   BYPASS CARRIAGE CONTROL TABLE ?     81365
         BNZ   INDGC         YES                                 81365
         TM    FLAG2,FGUCC   USER CONTROL CHARACTER ?            81365
         BNO   INDACOM4      NO                                  81365
         LH    R1,PARMINFO            GET USER'S INDEX           81365
         LA    R2,VALIDASA-1           ASA TABLE
         TM    FLAG2,FGMCC   MACHINE CONTROL ?                   81365
         BNO   SETCC                   NO, ASA
         LA    R2,VALIDMAC-1           MACHINE CONTROL TABLE
SETCC    IC    R1,0(R2,R1)   GET PROPER CC                       81365
         STC   R1,PAGEJECT+4                                     81365
INDACOM4 LA    R14,AREA      ALL MOVES TO CC                     81365
         LA    R15,L'AREA    ALLOW FULL LENGTH                   81365
         STM   R14,R15,MOVETO   SET ADDRESS AND MOVE LENGTH      81365
         SR    R3,R3                                             81365
         SR    R14,R14                                           81365
         LA    R15,4         SET TRT VALUE                       81365
         LA    R0,2          REPEAT FOR CODES 4 AND 8            81365
         LA    R1,MCCTYPE4   POINT TO INVALID, BUT PASSED        81365
         LA    R4,2          SET OR VALUE FOR IMMEDIATE OPS      81365
         TM    FLAG2,FGMCC   MACHINE CODE ?                      81365
         BNZ   INDALOPT      YES                                 81365
         LA    R1,ASATYPE4   POINT TO ASA VERSION OF TABLE       81365
         SR    R4,R4         NO OR                               81365
INDALOPT ICM   R14,1,0(R1)   GET NUMBER OF ENTRIES IN THIS TABLE 81365
         BZ    INDALUMT      SKIP TABLE ENTRY                    81365
         L     R2,0(,R1)     GET LIST BASE                       81365
INDALOOP IC    R3,0(,R2)     GET NEXT BYTE                       81365
         STC   R15,CCTABLE(R3)   SET TRT VALUE                   81365
         OR    R3,R4         MAKE MCC IMMEDIATE OP               81365
         STC   R15,CCTABLE(R3)  SET IMMEDIATE VALUE ALSO         81365
         LA    R2,1(,R2)     POINT TO NEXT BYTE                  81365
         BCT   R14,INDALOOP  PROCESS NEXT BYTE                   81365
INDALUMT LA    R15,4(,R15)   UP TRT VALUE                        81365
         LA    R1,4(,R1)     GET NEXT POINTER ENTRY              81365
         BCT   R0,INDALOPT   DO IT                               81365
         IC    R3,PAGEJECT+4  GET USER SUPPLIED PAGE EJECT       81365
         LA    R15,16        SET EJECT CODE                      81365
         STC   R15,CCTABLE(R3)                                   81365
         TM    FLAG2,FGMCC   MACHINE CODE ?                      81365
         BZ    *+8           NO                                  81365
         STC   R15,CCTABLE+2(R3)  COPY FOR IMMEDIATE VALUE       81365
         SPACE 2
INDGC    TM    SYSUT2+DCBOFLGS-IHADCB,DCBOFOPN  RESTART ENTRY ?  81365
         BZ    INDGC2        NO - PROCESS STORAGE
INDGBUF  MVI   DCBBUFNO,*-*    SET OLD BUFFER NUMBER
         B     INDCBEXX      AND QUIT
         SPACE 2
INDGC2   LH    R5,OHDSYS     GET SYSTEM OVERHEAD                 89180
         A     R5,OHDREC     PLUS SYSTEM OVERHEAD                89180
         A     R5,GETMLIST   PLUS BASIC SIZE                     89180
         ST    R5,GETMLIST   SET MINIMUM REQUEST                 89180
         BAS   R10,RGNCLEAN  UNFRAGMENT AND CHECK STORAGE        81365
         BNZ   STORABND      NO; TOO BAD                         81365
         SH    R5,OHDSYS     SYSTEM OVERHEAD ALLOWANCE           81365
         S     R5,OHDREC     ALLOW FOR LOGICAL RECORD            81365
         BNP   STORABND      TOO BAD                             89180
         ST    R5,GETMADDR   SAVE AVAILABLE SIZE                 81365
         SRL   R5,1          HALVE FOR EACH DCB                  81365
         SR    R4,R4         FOR DIVIDE                          81365
         LH    R3,DCBBLKSI   GET INPUT BLOCKSIZE                 81365
         AH    R3,OHDIPBLK   ADD ICB/IOB OVERHEAD                81365
         LA    R3,7(,R3)     ROUND                               81365
         SRL   R3,3          TO DOUBLE-WORD                      81365
         SLL   R3,3          BOUNDARY                            81365
         C     R3,GETMLIST   IS THIS A LARGE BLOCK SIZE ?        81365
         BNH   INDGSMAL      NO; DO NORMALLY                     81365
         LA    R15,0(R3,R5)  GET HALF OF STORAGE+INPUT BLOCK     81365
         C     R15,GETMADDR  WILL IT FIT ?                       81365
         BNL   INDGSMAL      NO; WASTE SOME                      81365
         LR    R5,R15        ELSE ALLOW EXTRA                    81365
INDGSMAL SR    R4,R4         FOR DIVIDE                          81365
         DR    R4,R3         GET BUFFERS                         81365
         CH    R5,MAXREC#    MORE THAN MAX ?                     81365
         BNH   INDGMBUF      NO                                  81365
         LH    R5,MAXREC#    SET FOR MAX                         81365
INDGMBUF LTR   R5,R5         AT LEAST ONE BUFFER ?               81365
         BP    INDGBUFM      YES, OK                             81365
         LA    R5,1          ELSE SET FOR ONE                    81365
INDGBUFM STC   R5,DCBBUFNO   SET BUFFERS                         81365
         STC   R5,INDGBUF+1  REMEMBER THE BUFFER COUNT           81365
         MR    R4,R3         GET NEW STORAGE VALUE               81365
         LA    R5,4095(,R5)  PREPARE TO ROUND                    81365
         SRL   R5,12         TO 4K                               81365
         SLL   R5,12         BOUNDARY                            81365
         LNR   R5,R5         MAKE NEGATIVE                       81365
         A     R5,GETMADDR   GET SIZE LEFT FOR OUTPUT DCB        81365
         LH    R3,OHDOPBLK   GET OUTPUT ICB/BLKSIZE OVERHEAD     81365
         DR    R4,R3         GET OUTPUT BLOCKS THAT WILL FIT     81365
         CH    R5,MAXREC#    MORE THAN WILL FIT ?                81365
         BNH   INDGOBUF      NO                                  81365
         LH    R5,MAXREC#    SET FOR MAX                         81365
INDGOBUF LTR   R5,R5         AT LEAST ONE BUFFER ?               81365
         BP    INDGBUFO      YES, OK                             81365
         LA    R5,1          ELSE SET FOR ONE                    81365
INDGBUFO STC   R5,DCBBUFNO-IHADCB+SYSUT2  STASH OUTPUT SIZE      81365
         TM    JFCOPTCD,JFCOPTJ  OPTCD=J IN OUTPUT JFCB ?        93036
         BZ    *+8           NO                                  93036
         OI    DCBOPTCD-IHADCB+SYSUT2,DCBOPTJ  PROPAGATE         93036
         CLI   DCBBUFNO-IHADCB+SYSUT2,3  AT LEAST THREE BUFFERS ?
         BL    INDCBEXX      NO                                  81365
         OI    DCBOPTCD-IHADCB+SYSUT2,DCBOPTC  SET CHAINED SCHEDULING
INDCBEXX LM    R13,R14,EXSAVER   RESTORE REGS                    81365
         BSM   0,R14         RETURN TO OPEN                     GP03146
         USING SAWE,R13      RESTORE ORIGINAL MAPPING            81365
         SPACE 1
BADOPMSG L     R13,WORKADDR  RESTORE COMMON WORK AREA            81365
         B     ABENDMSG      ABEND WITH ERROR MESSAGE            81365
         SPACE 1                                                 81365
STORABND WTO   'INSUFFICIENT REGION SIZE',ROUTCDE=11             81365
         L     R13,WORKADDR  RESTORE WORK AREA                   81365
         SPACE 1
ABND     BAS   R10,CANDOM    CLEAR SCREEN                        81365
         ABEND 16,DUMP                                           81365
         SPACE 2
*        STIMER EXIT ROUTINE
         SPACE 1                                                 81365
TIMEX    DS    0H                                                81365
         DROP  R11,R12                                           81365
         USING TIMEX,R15                                         81365
         L     R1,WORKADDR                                       81365
         OI    FLAG1-SAWE(R1),FGTIME  REQUEST NEW STIMER         81365
         SR    R15,R15                                          GP03146
         BR    R14
         DROP  R15                                               81365
         USING BLOCKPRT,R11,R12                                  81365
         SPACE 2                                                 81365
RGNCLEAN GETMAIN VC,LA=GETMLIST,A=GETMADDR  GET MAX STORAGE      81365
         LTR   R5,R15        RETURN TOO LOW VALUE                89180
         BNZR  R10           TOO BAD ?                           81365
         LM    R4,R5,GETMADDR  ELSE GET AMOUNT                   81365
         FREEMAIN R,LV=(R5),A=(R4)  FREE IT UP                   81365
         SR    R15,R15       SET CONDITION CODE                  79313
         BR    R10           RETURN TO CALLER                    79313
         SPACE 2                                                 93008
***********************************************************************
*                                                                     *
*   3800 JFCBE EXIT ROUTINE - COMPENSATE FOR ERROR IN SETPRT -        *
*              MISSING CHARS IS NOT HANDLED CORRECTLY FROM UCS        *
*                                                                     *
***********************************************************************
         PUSH  USING                                             93008
         DROP  ,                                                 93008
         USING EJFCEXIT,R15                                      93008
         USING JFCBE,R2      PASSED IN R1                        93008
EJFCEXIT LR    R2,R0         COPY JFCBE ADDRESS                  93008
         L     R3,WORKADDR   GET WORK AREA ADDRESS               93008
         USING SAWE,R3       DECLARE DYNAMIC STORAGE             93008
         MVC   FCBJFCBE,SMF6FCB  UPDATE JFCB                     93008
         CLC   =C'XXXX',JFCBTRS1  STANDARD LASERPRT PROC CHARS ? 93010
         BE    EJFCEXIC                                          93010
         CLI   JFCBTRS1,C' '  DID USER SPECIFY CHARS ?           93008
         BNER  R14           YES; DON'T CREAM                    93008
EJFCEXIC MVC   JFCBTRS1,SMF6UCS  MOVE UCS                        93008
         OI    JFCBFLAG,JFCBEOPN  SHOW ALTERED                   93008
         BR    R14           RETURN                              93008
         POP   USING                                             93008
         EJECT ,
*        CONSTANTS AND STUFF
         SPACE 2
WORKADDR DC    A(0)          ADDRESS OF WORK AREA                81365
WTORECB  DC    F'0'                                              81365
TIME     DC    A(5*60*100)              SET FOR 5 MINUTES
INOPLIST DC    0A(0),X'80',AL3(SYSUT1)  INPUT DCB OPEN LIST      81365
INEXIT   DC    X'85',AL3(INDCBEXT)                               81365
OUTOPLST DC    0A(0),X'8F',AL3(SYSUT2)  OUTPUT DCB OPEN LIST     81365
OUTUCB@  DC    A(0)          ADDRESS OF OUTPUT OR FAKE UCB       89178
OUTEXIT  DC    X'87',AL3(0)  INFMJFCB  1/2                       81365
JEX3800  DC    X'95',AL3(EJFCEXIT)     2/2   3800 EXIT           93008
GETMLIST DC    A(4*1024,512*1024)      OPEN SIZE LIST
GETMADDR DC    A(0,0)        GETMAIN RETURN VALUES
         AIF   ('&SYSTEM' EQ 'SVS').OHDSVS                       79313
*        AIF   ('&SYSTEM' EQ 'MVS').OHDMVS                       81365
.OHDMVS  ANOP  ,             NOT DONE YET                        79313
.OHDSVS  ANOP  ,                                                 79313
OHDREC   DC    A(0)          LOGICAL RECORD OVERHEAD             79313
OHDIPBLK DC    H'56'         ICB INPUT OVERHEAD                  79313
OHDOPBLK DC    AL2(64+144)   OUTPUT BLOCKSIZE+ICB OVERHEAD       79313
OHDSYS   DC    AL2(12*1024)   AMOUNT TO RESERVE FOR SYSTEM       79313
OHDWASTE DC    AL2(60*1024)   MAXIMUM STORAGE PERMITTED TO WASTE 81365
.OHDCOM  ANOP  ,                                                 79313
NULLBLK  DC    Y(7,0),0CL7' ',C' '  DUMMY RECORD                 91043
BLANKS   DC    CL8' '        FOR CLEARING THINGS                 81365
TABTOHEX DC    X'000A0B0C0D0E0F',9X'00',X'00010203040506070809'  81365
MAXLINES DC    0H'0',X'7FFF'  DEFAULT TO FORCE RESETTING         89187
PARMINFO DC    H'0'
MAXREC#  DC    H'50'         20 IF VIRTUAL REGION                81365
MVCTEXT  MVC   0(0,R5),0(R2)   MOVE USER'S TEXT                  81365
TRANTEXT TR    0(0,R5),TRANSOUT  OUTPUT TRANSLATION              89215
         SPACE 1                                                 81365
MCCTYPE4 DC    0A(0),AL1(0),AL3(0)  IGNORED MCC CODES            81365
         DC    AL1(16),AL3(VALIDMAC)  VALID MCC CODES            81365
*        DC    AL1(0),AL3(0)   DUMMY ENTRY FOR NCC               81365
*        DC    AL1(1),AL3(PAGEJECT+4)  PAGE EJECT ENTRY          81365
ASATYPE4 DC    0A(0),AL1(0),AL3(0)  IGNORED ASA CODES            81365
         DC    AL1(16),AL3(VALIDASA)  VALID ASA CODES            81365
*        DC    AL1(0),AL3(0)   DUMMY ENTRY FOR NCC               81365
*        DC    AL1(1),AL3(PAGEJECT+4)  PAGE EJECT ENTRY          81365
VALIDASA DC    C'123456789ABC 0-+'  CHAN 1/12; SS, DS, TS, OP    81365
VALIDMAC DC    X'899199A1A9B1B9C1C9D1D9E109111901'  CH 1/12; OP,SS-TS
FINAL    DC    CL7' '
OPREPLY  DC    CL7' '                   MUST FOLLOW FINAL
PADBYTE  DC    X'FF'                    MUST FOLLOW REPLY
MASK     DC    C'0000000'
         DC    7X'FF'                  MUST FOLLOW MASK
MASKWK   DC    CL7' '
         SPACE 2                                                 81365
MSG1     VCON  'JOB ',END=MSG1E                                  81365
MGJOB    DC    CL8' ',C' '                                       81365
MGJOB#   DC    CL4' ',C'  ACCOUNT '                              81365
MGACCT   DC    CL4' ',C','                                       81365
MGSACT   DC    CL4' '        SUB-ACCOUNT                         81365
         VCON  ,                                                 81365
         SPACE 1                                                 89144
MXG1     VCON  ' JES2 ',END=MXG1E  PSEUDO-JES SEPARATOR LINE     89144
MXGCLASS DC    C'X',C' '     OUTPUT CLASS (FAKED)                89144
MXGSEPT  DC    C'START',C' '   START/END/CONT.                   89144
MXGJOB#  DC    CL8'JOB 2345',C' '  JOB NUMBER                    89144
MXGJOB   DC    CL8'JOB-NAME',C' '   JOB NAME                     89144
MXGPGM   DC    CL20' ',C' '  PROGRAMMER NAME ?                   89144
         DC    C'ACCT '                                          89144
MXGACCT  DC    C'1234',C' '  ACCOUNT                             89144
MXGSACT  DC    C'1234'       SUB-ACCOUNT                         89144
         VCON  ,             END                                 89144
MSG2     VCON  'DATE ',END=MSG2E                                 81365
MGDATE   DC    X'21204B202020',C'  TIM '  E ADDED LATER          81365
MGTIME   DC    X'2120207A20207A2020'                             81365
         VCON  ,                                                 81365
MSG3     VCON  'PRINTER ',END=MSG3E                              81365
MGOUT    DC    CL3' ',C'  FORM '                                 81365
MGFORMS  DC    CL(L'S166FMN)' ',C'  '                            92358
MGFCB    DC    CL4' ',C' '                                       81365
MGUCS    DC    CL3' ',C' TRAIN'                                  81365
         VCON  ,                                                 81365
MXFONTOF VCON  ' &DJDE.FONTINDEX=NONE,FORMS=NONE,END;'           92045
         ORG   MXFONTOF+5                                        89142
DJDE     DC    C'&DJDE'      DJDE IDENTIFIER                     89142
         ORG   ,                                                 89142
MXALIGN  VCON  ' &DJDE.COPIES=1,END;'  ALIGN TO ODD PAGE         89142
MXDELIM  VCON  ' ',END=MXDELIN  JOB DELIMITER                    90113
         DC    12AL1(RSTACK) XEROX 'JOB' DELIMITER               91168
         VCON  ,                                                 90113
MXAUXFD  VCON  ' ',END=MXAUXFT                                   90112
         DC    12X'11'       AUXILIARY TRAY FEED                 90112
         VCON  ,             END                                 90112
         SPACE 2                                                 81365
SYSIN    INPWORK SYSIN,WIDTH=80,FILL=C' ',EDIT=128               89142
         SPACE 1                                                 81365
*        VALID UCS AND FCB PARAMETERS                            81365
*          NOTE THAT ONLY THE FIRST CHARACTER APPEARS IN THE TABLE
*          (A LA HASP); UCS IS FORCED TO -N (1403) OR -11 (3211) 81365
*          FCB IS FORCED TO -LPI (LOCAL EQUIVALENTS FOR STD2/STD3)
UCSU1403 DC    C'PPPP'       PN TRAIN + ZAP SPACE                81365
UCSL1403 DC    C'TUTT'       TN, UN TRAIN + ZAP SPACE            89142
UCSX1403 EQU   *-UCSU1403    NUMBER OF DEFINITIONS               81365
UCSU3211 DC    C'POBZ'       P11 TRAIN, OCR-A, OCR-B + ZAP       81365
UCSL3211 DC    C'TSUT'       T11, LOCAL 'S11', UN + ZAP SPACE    89142
UCSX3211 EQU   *-UCSU3211    NUMBER OF DEFINITIONS               81365
FCB3211  DC    C'6868'       SUPPORTED FCBS                      81365
FCBX3211 EQU   *-FCB3211     NUMBER OF DEFINITIONS               81365
UCSU3203 DC    C'POBZ'       PN TRAIN, OCR-A, OCR-B, ZAP         88242
UCSL3203 DC    C'TUTT'       TN, UN TRAIN + ZAP SPACE            89142
UCSX3203 EQU   *-UCSU3203    NUMBER OF DEFINITIONS               85315
         SPACE 2
MSGCURR  VCON  END=MSGCURRN                                      81365
         DC    C'RESTART PAGE NUMBER ='                          81365
MSGPAGE  DC    CL10' '                                           81365
         VCON  ,                                                 81365
PATPAGE  DC    X'40206B2020206B202120'  EDIT PATTERN FOR PAGE    81365
         SPACE 1                                                 81365
MSGSPACE VCON  'PRINTER SPACED BY OPERATOR'                      81365
MSGINTER VCON  'INTERRUPTED BY OPERATOR'                         81365
MSGEOF   VCON  'END OF FILE ON INPUT; LAST PAGE IS',END=MSGEOFN  81365
MSGEOFPG DC    CL(L'MSGPAGE)' '                                  81365
         VCON  ,                                                 81365
MSGSETUP VCON  END=MSGSETUN                                      81365
         DC    C'SET '                                           81365
MSGSTUCB DC    C'CUU',C' FOR FORM '                              81365
MSGSTFRM DC    CL(L'S166FMN)'STD.',C', '                         92358
MSGSTFCB DC    CL4'6LPI',C', '                                   81365
MSGSTUCS DC    CL4'PN  ',C' TRAIN'                               81365
         VCON  ,                                                 81365
MXGSETUP VCON  END=MXGSETUN                                      89145
         DC    C'SET '                                           89145
MXGSTUCB DC    C'CUU',C' FOR FORM '                              89145
MXGSTFRM DC    CL(L'S166FMN)'STD.',C', SETUP '                   92358
MXGSTSET DC    CL8' ',C', PAPER '                                89145
MXGSTPAP DC    CL8' '                                            89145
         VCON  ,                                                 89145
MSGNONUM VCON  'NON-NUMERIC REPLY UNRECOGNIZED'                  81365
MSGGOEOF VCON  'END-FILE HIT; REPLY WITH STOP, RESTART OR PAGE NUMBER'
         SPACE 1                                                 81365
MFGBADUC VCON  'UNSUPPORTED UCS VALUE ',END=MFGUCBAD             81365
MGBADUCS DC    CL4' '                                            81365
         VCON  ,                                                 81365
@HELP    DC    A(HELP1,HELP2,HELP3,HELP4,HELP5,HELP6,HELP7)      81365
         AIF   (&MVS OR '&LOCAL' NE 'CCSI').NOHEL                81365
         DC    A(HELP8,HELP9)                                    81365
.NOHEL   ANOP  ,                                                 81365
@HELPEND EQU   *                                                 81365
         SPACE 2
         PRINT NOGEN
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=GL,EODAD=EOJ,DSORG=PS,EXLST=INEXIT
*              OPTCD=Z FORCED; BFTEK=A, OPTCD=C MAY BE SET       79313
         SPACE 2
SYSUT2   DCB   DDNAME=SYSUT2,MACRF=PM,DSORG=PS,SYNAD=NO001,            X
               RECFM=V,LRECL=MAXWIDTH+6,BLKSIZE=MAXWIDTH+10,           *
               EXLST=OUTEXIT    HAD OPTCD=C (EXTRANEOUS IN X/A)  93035
         SPACE 2
         LTORG ,
         SPACE 1                                                 92303
MFGSETER VCON  'ERROR PROCESSING SETUP (MISSING/BAD)'            89145
*FGACCT  VCON  'INVALID ACCOUNT NUMBER'                          81365
MFGCLASS VCON  'INVALID JOB CLASS'                               81365
MFGSTEP# VCON  'NOT SINGLE STEP JOB'                             81365
MFGDUPDD VCON  'DUPLICATE DD CARD FOUND'                         81365
MFGUNITS VCON  'UNSUPPORTED OR EXCESSIVE DEVICES'                81365
MFGNODD  VCON  'REQUIRED DD CARDS (SYSUT1/SYSUT2) NOT SUPPLIED'  81365
MFGNOPAS VCON  'DISP=(OLD,PASS) REQUIRED FOR TAPE INPUT'         83080
MFGBADDD VCON  'OUTPUT DEVICE NOT SUPPORTED'                     81365
*FGBADFM VCON  'INVALID FORM NAME/NUMBER'                        81365
MFGNOPEN VCON  'OPEN FAILED'                                     81365
MFGNLREC VCON  'INVALID LRECL'                                   81365
MFGNBLK  VCON  'INVALID BLKSIZE'                                 81365
MFGNRECF VCON  'UNSUPPORTED RECFM'                               81365
MFGNCC   VCON  'CARRIAGE CONTROL OPTIONS NOT SUPPLIED'           81365
         SPACE 1                                                 89144
XERTAB   BTAB  'SETUP=',SETSET,BASE=*  USER SPECIFIED SETUP      89144
         BTAB  'SET=',SETSET                                     89144
         BTAB  'S=',SETSET                                       89149
         BTAB  'PAPER=',SETPAP                                   89149
         BTAB  'P=',SETPAP                                       89149
         BTAB  'FLASH=',SETFLA                                   89355
         BTAB  'FL=',SETFLA  AVOID F= (CONFLICT WITH FORMS=/F=)  89355
         BTAB  'F#=',SETFO#  FONT SELECTOR                       90296
         BTAB  'FONT=',SETFO#                                    90296
PRTTAB   BTAB  'FORMS=',SETFORM  USER FORM OVERRIDE              89144
         BTAB  'FORM=',SETFORM                                   89144
         BTAB  'F=',SETFORM                                      89144
         BTAB  'PAGE=',SETLPAGE                                  91043
         BTAB  'LIM=',SETLPAGE                                   91043
         BTAB  'MSG',LOOKPARM   SKIP ASSERTION                   89144
         BTAB  'NOMSG',SETNOMSG  BUT SET NO MSG BIT              89144
         BTAB  'NOCC',SETNOCC   NO CHANNEL SKIPS ?               89144
         BTAB  'CHAN',SETCHAN   CHANNEL SPECIFICATION            89144
         BTAB  'CH',SETCHAN       ALTERNATE                      89144
         BTAB  ',',LOOKPARM  TRY MORE                            89144
         BTAB  *END                                              89144
         SPACE 2                                                 81365
SAWE     DSECT ,             EXTENSION OF SAVE/WORK AREA         81365
EXSAVER  DC    18F'0'        OPEN EXIT SAVE AREA                 81365
XERPSAVE DS    15F           XEROX SAVE AREA                     89142
XERPSAV2 DS    15F                                               89216
REALPUT  DS    A             SYSUT2 PUT ADDRESS (QSAM)           89142
DB       DS    D             WORK WORD                           81365
DB2      DS    D                 DITTO                           92273
         SERVDEFS ,          ADDRESS OF SERVICE ROUTINE, ETC.   GP03146
@IMGLIB  DS    A             ADDRESS OF IMGLIB DCB               89187
@SETUP   DS    A             ADDRESS OF X4SETUP MODULE           89145
LIMPAGE  DS    F             0 OR MAXIMUM PAGE # TO PROCESS      91043
INPOFF   DS    F             0 OFFSET FROM INPUT; OR 4 FOR V FORMAT
MOVETO   DS    A             A(AREA) MCC/ACC; OR A(AREA+1) FOR NCC
MOVELEN  DS    F             MAXIMUM OUTPUT MOVE LENGTH          81365
JOBNAME  DS    2A            RIGHT-JUSTIFIED JOBNAME             81365
ADUT1    DC    A(0)          TCT I/O COUNT ENTRY FOR SYSUT1      81365
LOCALPCE DS    A      1/2    ADDRESS OF PCE                      81365
LOCALJQE DS    A      2/2    ADDRESS OF JQE                      81365
DOMPOINT DC    A(DOMTAB-4)   POINTER TO CURRENT DOM ENTRY        81365
         DC    0F'0',X'0'    PREFIX FOR DOM LIST                 81365
DOMFLAG  DC    X'00'         X'80' IF DOM TO BE DONE             81365
NUMMSGS  DC    H'3'          NUMBER OF FIXED + USER MESSAGES     81365
DOMTAB   DC    16A(0)        TABLE OF DOM IDS; X'80' IN LAST ACTIVE
DOMEND   DS    A             END OF TABLE                        81365
FLAG1    DC    X'00'         PROGRAM FLOW CONTROL FLAGS          81365
FGINIT   EQU   X'80'           INITIALIZATION DONE               81365
FGSKIP   EQU   X'40'           RESTART - SKIPPING TO PAGE NUMBER 81365
FGREQ    EQU   X'20'           HASP $RESTART ISSUED FOR THIS JOB 81365
FGTIME   EQU   X'10'           (RE)ISSUE STIMER                  81365
FGPAGE   EQU   X'08'           PAGE EJECT REQUIRED FOR MESSAGES  81365
FGHELP   EQU   X'04'           HELP INFO DISPLAYED ON BAD REPLY  81365
FGERR    EQU   X'01'           ERROR IN NUMERIC REPLY            81365
FLAG2    DC    X'00'         CARRIAGE CONTROL FLAGS              81365
FGEJECT  EQU   X'80'           FORCE AN EJECT                    81365
FGUCC    EQU   X'08'           NON-STANDARD TOP-OF-PAGE CC IN USE
FGACC    EQU   X'04'           ASA CONTROL CHARACTERS PRESENT    81365
FGMCC    EQU   X'02'           MACHINE CONTROL PRESENT           81365
FGNCC    EQU   X'01'           NO CARRIAGE CONTROLS IN DATA      81365
FLAG3    DC    X'00'         INITIALIZATION FLAGS                86092
FGUCS    EQU   X'80'           DEFAULT UCS                       86092
FGFCB    EQU   X'40'           DEFAULT FCB                       86092
FGTRCC   EQU   X'10'           CONTROL TRANSLATION REQUIRED      90284
FGTRAN   EQU   X'08'           OUTPUT TRANSLATION REQUIRED       89215
FGNOMSG  EQU   X'04'           INHIBIT PRT MESSAGES              88061
FGNOSEP  EQU   X'02'           INHIBIT STARTING JOB SEPARATOR    87330
FGVOLT   EQU   X'01'           FORM FOUND IN VOLT TABLE          86092
CCERR    DC    P'0'          CC ERROR COUNTER                    81365
CCTABLE  DC    256X'00'      CARRIAGE CONTROL TRANSLATE AND TEST 81365
SMFINT   DC    H'0'          INTERVAL COUNTER FOR SMF RECORD     81365
PAGEJECT DS    0H'0',XL7'0'  PAGE EJECT SEQUENCE                 93040
CCSINGLE DS    C             CONTROL CHARACTER FOR SINGLE SPACE  81365
FLAG4    DS    X                                                 92303
FG3800   EQU   X'80'         IBM 3800, STC 6100                  92303
MODEFG   DS    C             MORE FLAGS                          89142
FGFAKE   EQU   X'80'           SYSUT2 IS NOT ON-LINE PRINTER     89142
PRIVY    EQU   X'40'           SYSTEMS PRIVILEGES FOR THIS RUN   81365
FGXIDONE EQU   X'20'           XERINIT CALLED ONCE               91136
FGXUFONT EQU   X'10'           USER FORCED FONT SELECTOR         90296
FGXNFONT EQU   X'08'           NO FONTINDEX PROCESSING           89144
FGXINIT  EQU   X'04'           OVERLAY INITIALIZED               89144
FGXNO    EQU   X'02'           BYPASS ALL XEROX PROCESSING (XICS)
FGXER    EQU   X'01'           XEROX MODE (SET BY SAVEM)         89142
XERCC    DC    X'00'         XEROX SAVED CC                      89142
XERFLASH DC    CL6' '        FLASH (FORMS OVERLAY) NAME          89355
XERUFONT DC    X'00'         USER SPECIFIED FONT SELECTOR        90296
TRANCHAR DC    X'00'         CHARACTER TO BE REPLACED            89216
TRANSAVE DC    2F'0'         SAVE AREA                           89216
UDJDE    DC    A(0)          HEAD OF DJDE QUEUE                  90057
UDJDEND  DC    A(0)          POINTER TO LAST ELEMENT ON DJDE QUEUE
INPSAVE  DC    8F'0'         INPUT PROCESSING SAVE AREA          89216
         SPACE 1                                                 81365
RDW      DC    F'0'          OUTPUT LINE                         81365
AREA     DC    CL(MAXWIDTH+1)' ',X'00'  MUST FOLLOW RDW          89142
         SPACE 1                                                 81365
OPERWTO  DS    Y(0,0)        MESSAGE WTO/PRINT WORK AREA         81365
PUTLINE  DS    0CL101        PRINT LINE                          81365
OPERMSG  DS    0CL100        OPERATOR MESSAGE                    81365
OPERJOB  DS    CL8           RIGHT-JUSTIFIED JOB NAME            81365
         DS    C                                                 81365
OPERID   DS    CL8           'BLOCKPRT'                          81365
         DS    C                                                 81365
OPERCOL  DS    C':'                                              81365
         DS    C                                                 81365
OPERTEXT DS    CL80          MESSAGE TEXT                        81365
         DS    XL4           DESCRIPTOR/ROUTING CODES            81365
         SPACE 1                                                 89142
@MSG     DC    A(MSG1)       JOB IDENTIFICATION MESSAGE TABLE    81365
@MSG2    DC    A(MSG2,MSG3)                                      81365
@MSGEND  DC    A(MSGU1,MSGU2,MSGU3,MSGU4,MSGU5,MSGU6,MSGU7)      81365
         DC    A(MSGU8,MSGU9,MSGU10,MSGU11,MSGU12)               81365
MSGU1    DC    Y(84,0),CL80' '   USER MESSAGE                    81365
MSGU2    DC    Y(84,0),CL80' '   USER MESSAGE                    81365
MSGU3    DC    Y(84,0),CL80' '   USER MESSAGE                    81365
MSGU4    DC    Y(84,0),CL80' '   USER MESSAGE                    81365
MSGU5    DC    Y(84,0),CL80' '   USER MESSAGE                    81365
MSGU6    DC    Y(84,0),CL80' '   USER MESSAGE                    81365
MSGU7    DC    Y(84,0),CL80' '   USER MESSAGE                    81365
MSGU8    DC    Y(84,0),CL80' '   USER MESSAGE                    81365
MSGU9    DC    Y(84,0),CL80' '   USER MESSAGE                    81365
MSGU10   DC    Y(84,0),CL80' '   USER MESSAGE                    81365
MSGU11   DC    Y(84,0),CL80' '   USER MESSAGE                    81365
MSGU12   DC    Y(84,0),CL80' '   USER MESSAGE                    81365
         SPACE 1                                                 89142
XERLINE  DS    XL(2+2+1+1+MAXWIDTH)                              89142
XERLINE2 DS    3XL(L'XERLINE)                                    89142
XERLINED DS    XL(L'XERLINE)                                     89142
         SPACE 1                                                 81365
         PRINT GEN                                               81365
PRESMF   EQU   *                                                 89178
         ORG   SAWE+((PRESMF-SAWE+255)/256*256)  DEBUG BOUNDARY  89178
         IFASMFR 6           DEFINE TYPE 6 SMF RECORD - WRITE AS 166
         SPACE 1                                                 89178
         ORG   SMF6FSIZ      LOCATION AFTER FIRST EXTENSION      92273
OFF3800  EQU   *-SMF6LN2     OFFSET TO 3800 EXTENSION            89178
OVERSMF  DC    XL(L'SMF6FSIZ)'0'  3800 EXTENSION                 94101
XERSETNM EQU   SMF6CHR+OFF3800,8,C'C'  XEROX SETUP NAME (JDL/JDE)
XERPAPER EQU   SMF6CHR+8+OFF3800,8,C'C'  PAPER USED              89178
XERFLISH EQU   SMF6FLI+OFF3800,4,C'C'  SHORT FORMS OVERLAY (FLASH)
         SPACE 1                                                 81365
S166LN3  DC    Y(SMF6REND-SMF6LN3)  LENGTH OF SECOND EXTENSION   92358
S166ROUT DS    FL4           ROUTE CODE                          92358
S166FMN  DS    CL8           REAL FORM NUMBER                    92358
S166RXSZ EQU   *-S166LN3     SIZE OF EXTENSION                   94101
         SPACE 1                                                 92358
SMF6LXLN DC    Y(18)         LENGTH OF LOCAL EXTENSION           81365
SMF6IND@ DS    AL4           INPUT DEVICE ADDRESS/TYPE          GP99354
SMF6IND# DS    AL4           INPUT I/O COUNTS                    81365
SMF6OUD  DS    AL4           OUTPUT DEVICE ADDRESS/TYPE          81365
SMF6OUD# DS    AL4           OUTPUT I/O COUNTS (SAME AS SMF6NLR) 81365
SMF6LXSZ EQU   *-SMF6LXLN    SIZE OF LOCAL EXTENSION             89178
SMF166LN EQU   *-SMFRCD6     LENGTH OF LOCAL RECORD              81365
         SPACE 1                                                 81365
*        LOCAL MAPPINGS OF SMF6IOE BITS                          81365
F6STOP   EQU   1             FINAL RECORD FOR JOB                81365
F6INTER  EQU   2             INTERMEDIATE RECORD                 81365
F6REST   EQU   4             RESTART ISSUED                      81365
         SPACE 1                                                 81365
         IEFJFCBN , LIST=YES                                     89142
         SPACE 2                                                 81365
         SPACE 1                                                 87167
         PRINT GEN                                               89216
FAKEUCB2 DC    12F'0'        FAKE UCB                            87167
TRANSOUT DC    256AL1(*-TRANSOUT)  OUTPUT TRANSLATION TABLE      89215
TRANSCON DC    256AL1(*-TRANSCON)  CONTROL CHARACTER TRANSLATION 90284
SAWEEND  EQU   *             END OF SAVE AREA                   GP03146
BLOCKPRT CSECT ,                                                 89142
         SPACE 1                                                 81365
HELP1    VCON  'VALID COMMANDS ARE :'                            89163
HELP2    VCON  'GO           CONTINUE FROM POINT OF INTERRUPTION'
HELP3    VCON  'RESTART      REPEAT FROM BEGINNING OF INPUT'     81365
HELP4    VCON  'nnnnnnn      A 1 TO 7 DIGIT STARTING PAGE NUMBER'
HELP5    VCON  '+nnnnnnn     SPACE FORWARD nnn PAGES'            81365
HELP6    VCON  '-nnnnnnn     BACKSPACE nnn PAGES'                81365
HELP7    VCON  'STOP         TERMINATE THIS JOB'                 81365
HELP8    VCON  'RERUN        SETS (DEFERRED) HASP RESTART'       81365
HELP9    VCON  'CANCEL       CANCELS HASP RESTART'               81365
         EJECT ,                                                 89216
         PUSH  USING                                             89216
         USING INPROC,R10                                        89216
***********************************************************************
*                                                                     *
*        READ SYSIN, IF ANY.  HONOR CONTROL CARDS:                    *
*        ..TR VAL REP        SET TRANSLATION                          *
*        TRCC VAL REP        SET CONTROL CHARACTER TRANSLATION        *
*        DJDE TEXT           SUPPLY ADDITIONAL DJDE TEXT         90057
*        ANYTHING ELSE - TREAT AS WTO REQUEST                         *
*                                                                     *
***********************************************************************
INPROC   STM   R2,R9,INPSAVE  SAVE CALLER'S REGISTERS            89216
         LA    R3,@MSGEND    POINT TO END OF MESSAGES            89142
         LA    R4,MSGU1      POINT TO FIRST USER MESSAGE         81365
         LA    R5,12         SET COUNTER                         81365
MSGULOOP INPGET DEV=1        GET A MESSAGE                       89142
         BXH   R15,R15,SYNEOF  END FILE (OR I/O ERROR)           89142
         SPACE 1                                                 89215
         CLI   4(R1),C' '    FOUR BYTES ?                        89216
         BNE   MSGUMSG       NO; NOT CONTROL CARD                89216
         ICM   R0,15,0(R1)   GET FIRST FOUR BYTES                89216
         ICM   R15,15,BLANKS   LOAD BLANKS                       91043
         OR    R0,R15        MAKE UPPER CASE                     91043
         CLM   R0,15,=C'..TR'  TRANSLATION REQUEST ?             89216
         BE    MSGUTRAN      YES                                 89215
         CLM   R0,15,=C'TRAN '  ALTERNATE FORM ?                 89216
         BE    MSGUTRAN                                          90057
         CLM   R0,15,=C'TRCC'   CONTROL TRANSLATION ?            90284
         BE    MSGUTRCC      YES; SET IT                         90284
         CLM   R0,7,=C'JDE'  DJDE OVERRIDE ?                     90057
         BNE   MSGUMSG       NO                                  89215
         TM    MODEFG,FGXER  XEROX MODE ?                        90057
         BZ    MSGULOOP      NO; WHO NEEDS A DJDE ?              90057
         SPACE 1                                                 90057
         STCM  R0,8,0(R1)    MAKE SURE UPPER CASE BYTE SAVED     90057
*        BUILD A SUBPOOL 0 GETMAIN CHAIN OF DJDE REQUESTS        90057
         LR    R2,R1         SAVE CARD ADDRESS                   90057
         GETMAIN R,LV=72     GET A CARD'S WORTH (LESS SEQUENCE)  90057
         MVC   0(72,R1),0(R2)  COPY INPUT LESS SEQUENCE          90057
         XC    0(4,R1),0(R1)   CLEAR CHAIN POINTER               90057
         ICM   R14,15,UDJDEND GET LAST DJDE                      90057
         BNZ   MSGUDJNF                                          90057
         ST    R1,UDJDE      SET HEAD OF QUEUE                   90057
         B     MSGUDJYF      SAVE TAIL                           90057
MSGUDJNF ST    R1,0(,R14)    SAVE TAIL POINTER                   90057
MSGUDJYF ST    R1,UDJDEND    SET LAST ENTRY ADDRESS              90057
         CLI   0(R2),C'D'    'DJDE' ?                            90057
         BE    MSGULOOP      YES; NORMAL SUPPLEMENTAL TEXT       90057
         OI    UDJDE,X'80'   INDICATE REPLACEMENT TEXT           90057
         B     MSGULOOP      GET ANOTHER CARD                    90057
         SPACE 1                                                 90057
MSGUTRAN STM   R4,R5,TRANSAVE  SAVE REGISTERS                    89216
         LA    R5,4(,R1)     SET SCAN AFTER VERB                 89216
         LA    R6,71(,R1)    SET SCAN STOP                       89216
         MVI   1(R6),C' '    MAKE TESTS WORK CORRECTLY           89216
         BLOOK T=,R=(R6)     LOOK FOR NON-BLANK                  89216
         CLI   0(R5),C' '    NON-BLANK ?                         89216
         BE    MSGUNONE      NO; SEE IF FIRST TIME               89216
         BAS   R14,CHARTEST  TEST FOR VALID CHARACTER            89216
         LR    R9,R8         SAVE RETURNED BYTE                  89216
         STC   R9,TRANCHAR   SAVE VALUE                          89216
MSGUTMOR BLOOK T=,R=(R6)     ANOTHER NON-BLANK ?                 89216
         CLI   0(R5),C' '                                        89216
         BNE   MSGUTDAT      MORE; PROCESS DATA                  89216
         CLM   R9,1,TRANCHAR FIRST TIME AND NO OPERAND ?         89216
         BE    BADCARD       YES; ERROR IN CARD                  89216
         B     MSGUTRET      RETURN                              89216
MSGUTDAT CH    R9,=H'255'    STILL VALID ?                       89216
         BH    BADCARD       NO; TOO MUCH INPUT                  89216
         BAS   R14,CHARTEST  GET NEXT CHARACTER                  89216
         STC   R8,TRANSOUT(R9)  SAVE THIS REQUEST                89216
         LA    R9,1(,R9)     SET FOR CHAINED ENTRY               89216
         B     MSGUTMOR      TRY AGAIN                           89216
MSGUNONE TM    FLAG3,FGTRAN  FIRST TIME FOR BLANK 'TRAN' CARD?   89216
         BNZ   BADCARD       NO; ERROR                           89216
MSGUTDFL MVI   TRANSOUT+X'6A',X'4F'  SPLIT TO SOLID STROKE       89215
         MVI   TRANSOUT+X'C0',X'8B'  LEFT BRACE                  89215
         MVI   TRANSOUT+X'D0',X'9B'  RIGHT BRACE                 89215
MSGUTRET OI    FLAG3,FGTRAN  SET TRANSLATION REQUEST             89215
         LM    R4,R5,TRANSAVE  RESTORE                           89216
         B     MSGULOOP      READ AGAIN                          89215
         SPACE 1                                                 90284
MSGUTRCC STM   R4,R5,TRANSAVE  SAVE REGISTERS                    90284
         LA    R5,4(,R1)     SET SCAN AFTER VERB                 90284
         LA    R6,71(,R1)    SET SCAN STOP                       90284
         MVI   1(R6),C' '    MAKE TESTS WORK CORRECTLY           90284
         BLOOK T=,R=(R6)     LOOK FOR NON-BLANK                  90284
         CLI   0(R5),C' '    NON-BLANK ?                         90284
         BE    BADCARD       NO; ERROR                           90284
         BAS   R14,CHARTEST  TEST FOR VALID CHARACTER            90284
         LR    R9,R8         SAVE RETURNED BYTE                  90284
         STC   R9,TRANCHAR   SAVE VALUE                          90284
MSGCTMOR BLOOK T=,R=(R6)     ANOTHER NON-BLANK ?                 90284
         CLI   0(R5),C' '                                        90284
         BNE   MSGCTDAT      MORE; PROCESS DATA                  90284
         CLM   R9,1,TRANCHAR FIRST TIME AND NO OPERAND ?         90284
         BE    BADCARD       YES; ERROR IN CARD                  90284
         B     MSGCTRET      RETURN                              90284
MSGCTDAT CH    R9,=H'255'    STILL VALID ?                       90284
         BH    BADCARD       NO; TOO MUCH INPUT                  90284
         BAS   R14,CHARTEST  GET NEXT CHARACTER                  90284
         STC   R8,TRANSCON(R9)  SAVE THIS REQUEST                90284
         LA    R9,1(,R9)     SET FOR CHAINED ENTRY               90284
         B     MSGCTMOR      TRY AGAIN                           90284
MSGCTRET OI    FLAG3,FGTRCC  SET TRANSLATION REQUEST             90284
         LM    R4,R5,TRANSAVE  RESTORE                           90284
         B     MSGULOOP      READ AGAIN                          90284
         SPACE 1                                                 89216
CHARTEST SR    R8,R8         CLEAR FOR IC                        89216
         IC    R8,0(,R5)     LOAD THIS CHARACTER                 89216
         LA    R5,1(,R5)     SKIP OVER IT                        89216
         CLI   0(R5),C' '    ONE BYTE ?                          89216
         BER   R14           YES; RETURN                         89216
         STC   R8,DB                                             89216
         MVC   DB+1(1),0(R5)  COPY                               89216
         LA    R5,1(,R5)     SKIP IT                             89216
         CLI   0(R5),C' '    VALID ?                             89216
         BNE   BADCARD       NO                                  89216
         LA    R15,DB                                            89216
         LA    R0,2                                              89216
         OC    DB(2),BLANKS  MAKE UPPER CASE                     89216
CHARTESL CLI   0(R15),C'A'   VALID HEX ?                         89216
         BL    BADCARD       NO                                  89216
         CLI   0(R15),C'9'   VALID ?                             89216
         BH    BADCARD                                           89216
         CLI   0(R15),C'0'   IN NUMERIC RANGE ?                  89216
         BNL   CHARTESB      YES; TRY NEXT                       89216
         CLI   0(R15),C'F'   VALID ALPHA ?                       89216
         BH    BADCARD       NO                                  89216
CHARTESB LA    R15,1(,R15)                                       89216
         BCT   R0,CHARTESL   TRY OTHER                           89216
         NC    DB(2),=X'1F1F'  MASK                              89216
      TR DB(2),=X'000A0B0C0D0E0F00000000000000000000010203040506070809'
         PACK  DB+4(2),DB(3)  PACK 0X0YSS INTO XYS               89216
         IC    R8,DB+4       GET HEX VALUE                       89216
         BR    R14           RETURN                              89216
         SPACE 1                                                 89216
BADCARD  WTO   'INVALID SYSIN CARD',ROUTCDE=11                   89216
         B     ABND          AND QUIT                            89216
         SPACE 1                                                 89216
MSGUMSG  MVC   4(80,R4),0(R1)  COPY TEXT                         89142
         ST    R4,0(,R3)     STORE MESSAGE ADDRESS               89142
         LA    R3,4(,R3)     ADVANCE                             89142
         MVI   1(R4),84      SET RDW LENGTH                      89142
         LA    R1,72+4(,R4)  POINT TO SEQUENCE FIELD             81365
         LA    R2,8          SCAN MAX OF EIGHT                   81365
MSGUTYPE CLI   0(R1),C' '    BLANK ?                             81365
         BE    MSGUTINC      YES                                 81365
         CLI   0(R1),C'.'    PERIOD ?                            81365
         BE    MSGUTINC      YES                                 81365
         CLI   0(R1),C'0'    NUMERIC ?                           81365
         BL    MSGUINC       NO; GET ANOTHER CARD                81365
MSGUTINC LA    R1,1(,R1)                                         81365
         BCT   R2,MSGUTYPE                                       81365
         MVC   72+4(8,R4),BLANKS   BLANK IT                      81365
MSGUINC  LR    R1,R4         COPY MESSAGE ADDRESS                81365
         LH    R2,0(,R4)     GET TOTAL LENGTH                    81365
         LA    R0,6          SET OVERHEAD                        81365
         AR    R1,R2         POINT TO LAST BYTE +1               81365
         SR    R2,R0         LEAVE MINIMUM                       81365
MSGUBLNK BCTR  R1,0          GET PRIOR BYTE                      81365
         CLI   0(R1),C' '    TRAILING BLANK ?                    81365
         BNE   MSGUSTOR      YES; STASH NEW LENGTH               81365
         BCT   R2,MSGUBLNK   ELSE CHECK AGAIN                    81365
MSGUSTOR AR    R2,R0         ADD MINIMUM LENGTH BACK             81365
         STH   R2,0(,R4)     SET NEW LENGTH                      81365
         LA    R4,MSGU2-MSGU1(,R4)  POINT TO NEXT MESSAGE        81365
         BCT   R5,MSGULOOP   TRY FOR ANOTHER MESSAGE             81365
SYNEOF   LA    R4,12+((@MSGEND-@MSG)/(@MSG2-@MSG))  LOAD MAX NUMBER
         SR    R4,R5         GET NUMBER STORED                   81365
         STH   R4,NUMMSGS    SET TOTAL MESSAGES                  81365
         INPCLOSE DEV=1      CLOSE THE SETUP CARD FILE           89142
         LM    R2,R9,INPSAVE  RESTORE CALLER'S REGISTERS         89216
         BR    R9            RETURN                              89216
         POP   USING                                             89216
         EJECT ,                                                 89142
         PRINT NOGEN                                             89145
***********************************************************************
*                                                                     *
*        THIS CODE INITIALIZES THE XEROX ACCORDING TO THE 'SETUP'     *
*        SPECIFIED BY THE USER (SETUP= PARM, OR SECOND VOL-SER).      *
*        MEMBERS WITH THE NAME X4SETUP RESIDE IN SYS1.IMAGELIB        *
*        (LOCAL CONVENTION - ALSO SEE MACROS X4050, X4CMD).           *
*        THE X4 MEMBER CONTAINS JDL, JDE AND OTHER SETUP DATA,        *
*        AS WELL AS OPTIONAL COMMANDS TO THE DEVICE.                  *
*                                                                     *
***********************************************************************
         SPACE 1                                                 89145
         PUSH  USING                                             89145
XERINIT  STM   R0,R14,EXSAVER  SAVE ENTRY REGISTERS              89145
         USING XERINIT,R9    LOCAL BASE                          89145
         LR    R9,R10        MAKE LOCAL BASE                     89145
         MVC   MXFONTOF+4(1),CCSINGLE                            89142
         MVC   MXALIGN+4(1),CCSINGLE                             89142
         MVC   MXAUXFD+4(1),CCSINGLE                             90112
         MVC   MXDELIM+4(1),CCSINGLE                             90113
         MVC   MXG1+4(1),CCSINGLE                                89144
         SPACE 1                                                 89145
         CLC   =C'X4',XERSETNM  SET-UP PROPERLY PREFIXED ?       89145
         BE    XERITDFL      YES                                 89145
         CLC   XERSETNM+6(2),BLANKS  SHORT ENOUGH ?              89145
         BNE   XERIFAIL      NO; FAIL                            89145
         MVC   DB2,XERSETNM  COPY SETUP NAME                     92273
         MVC   XERSETNM+2(6),DB2  SHIFT IT RIGHT                 92273
         MVC   XERSETNM(2),=C'X4' AND ADD XEROX 4050 PREFIX      89145
XERITDFL CLC   XERSETNM+2(6),BLANKS  NONE AT ALL ?               89145
         BNE   *+10                                              89145
         MVC   XERSETNM+2(6),=C'LAND61' DEFAULT DUPLEX, LANDSC.  89145
         IMGLIB OPEN         GET SYS1.IMAGELIB OPEN              89145
         BXH   R15,R15,XERIFAIL  NO GO ?                         89145
         LR    R4,R1         PRESERVE DCB                        89145
         LOAD  DCB=(R4),EPLOC=XERSETNM,ERRET=XERIFADE  GET DEFINITION
         ST    R0,@SETUP     SAVE SETUP ADDRESS                  89145
         LR    R7,R0         SAVE IT                             89145
         IMGLIB CLOSE,(R4)   CLOSE IMAGELIB                      89145
         USING X4DSECT,R7    DECLARE IT                          89145
         L     R6,X4@SET     GET ADDRESS OF BASIC INFORMATION    89145
         USING X4JDL,R6      DECLARE IT                          89145
         BAS   R2,PUTDJDE0   PREPARE FOR DJDE OUTPUT             89145
         TM    MODEFG,FGXIDONE  FIRST TIME ?                     91136
         BNZ   XERINSTK      NO; DON'T STACK AGAIN               91136
         MVI   AREA+1,RSTACK PREPARE A STACK COMMAND             91168
         MVC   AREA+2(L'AREA-3),AREA+1  MAKE STACK REQUEST       91135
         BAS   R2,PUTDJDE    PURGE PRIOR JOB                     91135
XERINSTK MVC   0(4,R1),=C'JDL='                                  89145
         MVC   4(6,R1),X4JDL  SET JDL                            89145
         LA    R1,10(,R1)                                        89145
         MVC   0(5,R1),=C',JDE='                                 89145
         MVC   5(6,R1),X4JDE                                     89145
         MVC   11(12,R1),=C',DUPLEX=NO ,'                        89149
         TM    X4F2,X4FDUPX  DUPLEX MODE ?                       89149
         BZ    *+10          NO                                  89149
         MVC   19(3,R1),=C'YES'                                  89149
         LA    R1,23(,R1)                                        89149
*DEFER*  MVC   0(5,R1),=C'DEPT='  BILLABLE DEPARTMENT            89149
*DEFER*  MVC   5(8,R1),SMF6UIF  ADD OUR ACCOUNT NUMBER           89149
*DEFER*  LA    R1,13(,R1)    SPACE PAST                          91176
         BAS   R2,PUTDJDL    WRITE IT OUT                        91176
         CLI   XERPAPER,C' ' PAPER ALREADY SET ?                 89149
         BH    *+10          YES; KEEP IT                        89149
         MVC   XERPAPER,X4PAPER  COPY PAPER TYPE                 89145
         CLI   XERFLASH,C' '  FLASH FORMS OVERLAY SET ?          89355
         BH    *+10          YES                                 89355
         MVC   XERFLASH,X4FLASH  MOVE IT                         89355
         MVC   XERFLISH,XERFLASH  COPY TO SMF RECORD             89355
         ICM   R4,15,X4@PRES  ANY PREFIX DATA TO PROCESS ?       89145
         BZ    XERICEND      NO; DONE                            89145
         USING X4CMD,R4      DECLARE IT                          89145
XERICBMS SR    R0,R0         CLEAR FOR IC                        89145
         IC    R0,X4CFG      LOAD THE CONTROL FLAG               89145
         SR    R3,R3                                             89145
         IC    R3,X4CMD      LOAD THE LENGTH                     89145
         BIX   PFX=XERIC,ERR=XERICBUM,                                 *
               LOC=(END,JDE,DAT,WTO)                             89145
         SPACE 1                                                 89145
XERICJDE TM    UDJDE,X'80'   DID USER ISSUE REPLACEMENT DJDE ?   90057
         BNZ   XERICBUM      YES; IGNORE THIS REQUEST            90057
         LA    R2,X4CTX      POINT TO COMMAND TEXT               89145
         LA    R15,AREA+L'AREA-1                                 89145
         LR    R14,R1                                            89145
         SR    R15,R14       LENGTH FREE                         89145
         B     XERICDMV      MOVE DJDE                           91177
         SPACE 1                                                 89145
XERICDAT LA    R2,X4CTX      POINT TO COMMAND TEXT               89145
         LA    R15,L'AREA-1                                      89145
         LA    R14,AREA+1                                        89145
XERICDMV ICM   R3,8,BLANKS   REQUEST BLANK FILL                  91177
         MVCL  R14,R2        MOVE DATA                           89145
         BAS   R2,PUTDJDL    EDIT, WRITE AND CLEAR               91176
         B     XERICBUM                                          89145
         SPACE 1                                                 89145
XERICWTO DS    0H            *****DEFER*****                     89145
XERICBUM SR    R2,R2                                             89145
         IC    R2,X4CMD                                          89145
         LA    R4,2(R2,R4)   SPACE TO NEXT COMMAND               89145
         B     XERICBMS      TRY IT                              89145
         SPACE 1                                                 89145
XERICEND MVC   REALPUT+1(3),DCBPUTA-IHADCB+SYSUT2  SAVE PUT      89180
         CLI   XERFLASH,C' '  FLASH OVERLAY ?                    89355
         BNH   XERICNFL      NO                                  89355
         MVC   0(6,R1),=C'FORMS='                                89355
         MVC   6(6,R1),XERFLASH  SET OVERLAY IMAGE NAME          89355
         MVI   12(R1),C','                                       89355
         LA    R1,13(,R1)                                        89355
XERICNFL MVC   0(10,R1),=C'FONTINDEX='                           91135
         TM    X4F1,X4FOFIX  NEED OVERPRINT/FONTINDEX FIX ?      89180
         BZ    XERICNFX      NO; WRITE 'NONE'                    91135
         MVC   10(9,R1),=C'(0,ZERO),'                            91135
         LA    R1,19(,R1)                                        89145
         MVC   DCBPUTA-IHADCB+SYSUT2(L'DCBPUTA),=AL3(XERPUT)     89142
         B     XERIDEND                                          91135
XERICNFX MVC   10(5,R1),=C'NONE,'                                91135
         LA    R1,15(,R1)                                        91135
XERIDEND ICM   R4,15,UDJDE   USER DJDE OVERRIDES ?               90057
         BZ    XERIUEND      NO; WRITE LAST                      90057
XERIUJDE MVC   0(68,R1),4(R4)  MOVE THE CARD IMAGE               90057
         BAS   R2,PUTDJDL    WRITE IT                            91176
         ICM   R4,15,0(R4)   ANY MORE TO DO ?                    90057
         BNZ   XERIUJDE                                          90057
XERIUEND MVC   0(4,R1),=C'END;'  TERMINATE THE DJDE SEQUENCE     89145
         BAS   R2,PUTDJDE    WRITE FINAL RECORD                  89145
         TM    MODEFG,FGXIDONE  BEEN HERE BEFORE ?               91136
         BNZ   XERIEXIT      YES; ALL DONE                       91136
         OI    MODEFG,FGXIDONE                                   91136
         TM    X4F2,X4FDUPX  DUPLEX ?                            90341
         BZ    XERIONCE      NO                                  90341
         LH    R2,MAXLINES   GET LINES PER PAGE (+...)           90341
         SH    R2,=H'10'     ALLOW FOR OTHER JUNK                90341
         BP    *+8           ROOM ?                              90341
         LA    R2,4          ELSE USE MINIMUM FOR BANNER DETECTIO
         LA    R1,MXG1                                           90341
         BAS   R10,PUTXER                                        90341
         BCT   R2,*-8        WRITE ALL                           90341
         LA    R1,PAGEJECT   FLIP TO BACK OF SHEET               90341
         BAS   R10,PUTXER                                        90341
XERIONCE LH    R2,MAXLINES   GET LINES PER PAGE (+...)           90341
         SH    R2,=H'15'     ALLOW FOR OTHER JUNK                90341
         BP    *+8           ROOM ?                              90341
         LA    R2,4          ELSE USE MINIMUM FOR BANNER DETECTION
         LA    R1,MXG1                                           89144
         BAS   R10,PUTXER                                        89144
         BCT   R2,*-8        WRITE ALL                           89144
         LA    R2,4          WRITE A FEW BLANK LINES             89145
         LA    R1,NULLBLK                                        89145
         BAS   R10,PUTXER                                        89145
         BCT   R2,*-8        SEPARATE IT A BIT                   89145
         NI    FLAG2,255-FGEJECT  (MOST OF MY) DJDES FORCE EJECT 89142
         NI    FLAG1,255-FGPAGE   SUPPRESS PAGE EJECT            89142
         SPACE 1                                                 89145
XERIEXIT LM    R0,R14,EXSAVER  RESTORE REGISTERS                 89145
         BR    R14           RETURN TO CALLER                    89145
         SPACE 1                                                 89145
XERIFADE IMGLIB CLOSE,(R4)   CLOSE IMAGELIB                      89145
XERIFAIL L     R0,=A(MFGSETER)                                   92303
         B     ABENDMSG      QUIT                                89145
         SPACE 2                                                 89145
*    EDIT THE CARD IMAGE - INSERT FINAL COMMA AND SEMI-COLON     91176
*                                                                91176
PUTDJDL  LA    R1,AREA+L'AREA                                    91176
         LA    R0,L'AREA-L'DJDE  NUMBER TO CHECK                 91176
PUTDJDLL BCTR  R1,0          GET PREVIOUS LOCATION               91176
         TM    0(R1),255-C' '   TRAILING BLANK/NULL ?            91176
         BNZ   PUTDJDLN      NO                                  91176
         BCT   R0,PUTDJDLL   TRY LOOP AGAIN                      91176
PUTDJDLN CLI   0(R1),C';'    TRAILING SEMI-COLON ?               91176
         BE    PUTDJDE       YES; NO ACTION                      91176
         CLI   0(R1),C','    TRAILING COMMA ?                    91176
         BE    PUTDJDLM      YES                                 91176
         TM    1(R1),255-C' ' ROOM TO INSERT ?                   91176
         BNZ   PUTDJDE       NO; LEAVE AS IS                     91176
         MVI   1(R1),C','    MAKE TRAILING COMMA                 91176
         LA    R1,1(,R1)     SKIP ONE                            91176
PUTDJDLM TM    1(R1),255-C' '  SPACE FOR SEMI-COLON ?            91176
         BNZ   PUTDJDE       NO; KEEP AS IS                      91176
         MVI   1(R1),C';'    APPEND SEMI-COLON                   91176
         SPACE 1                                                 91176
*    WRITE A DJDE OR CONTROL CARD IMAGE                          91176
*                                                                91176
PUTDJDE  PUT   SYSUT2,RDW    WRITE A DJDE RECORD                 89145
PUTDJDE0 MVC   RDW(2),DCBLRECL-IHADCB+SYSUT2  INITIALIZE LENGTH  89145
         MVC   AREA(1),CCSINGLE  PLAIN CARRIAGE CONTROL          89145
         MVC   AREA+1(L'DJDE),DJDE  MAKE PREFIX                  89145
         MVC   AREA+L'DJDE+1(L'AREA-L'DJDE-1),AREA+L'DJDE  BLANK 89145
         LA    R1,AREA+1+L'DJDE  FIRST INSERTION ADDRESS         89145
         BR    R2            RETURN TO CALLER                    89145
         SPACE 1                                                 89145
         PUSH  PRINT                                             89145
         PRINT GEN                                               89145
X4DSECT  X4050 MF=D          MAP THE SETUP MODULE                89145
         SPACE 1                                                 89145
         POP   PRINT                                             89145
         POP   USING                                             89145
BLOCKPRT CSECT ,                                                 89145
         EJECT ,                                                 89145
***********************************************************************
*                                                                     *
*        XEROX, FOR AN ON-LINE 4050, DOES NOT HONOR DARKENING BY      *
*              OVERPRINTING. THIS PUT INTERCEPT LOOKS FOR OVERPRINT   *
*              WITH THE SAME CHARACTER (AND UNDERLINES), AND CHANGES  *
*              THE FONT. OUR PDE ENTRIES DEFINE NORMAL/BOLD/ALTERNATE *
*              FONTS IN THAT ORDER.                                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                 89142
         PUSH  USING                                             89142
         DROP  ,                                                 89142
         USING SAWE,R13      COMMON SAVE/WORK AREA               89142
XERPUT   STM   R14,R12,XERPSAVE  SAVE ENTRY REGISTERS            89142
         LR    R10,R15       GET MY ADDRESS INTO THE BASE        89142
         USING XERPUT,R10    DECLARE THE BASE                    89142
         USING BLOCKPRT,R11,R12  OTHER DATA                      89142
         LR    R8,R0         PRESERVE THE ENTRY INFORMATION      89142
         LR    R9,R1                                             89142
         USING IHADCB,R9     DECLARE DCB                         89142
         SPACE 1                                                 89142
         ICM   R0,3,0(R8)    ANY DATA TO PROCESS ?               89142
         BZ    XERPDUMP      NO; CLEAR PRIOR                     89142
         TM    MODEFG,FGXNO+FGXINIT  BYPASS ?                    89144
         BO    PASSTHRU      YES                                 89144
         TM    MODEFG,FGXNFONT+FGXINIT  TEMPORARY BYPASS ?       89144
         BO    PASSTHRU                                          89144
         CLI   5(R8),X'30'   FONT SELECTOR ?                     89187
         BL    XERPDUMP      YES; FLUSH AND PASS THROUGH         89187
         TM    DCBRECFM,DCBRECCA  ASA ?                          89142
         BNZ   XERPCURA      YES                                 89142
         TM    4(R8),255-1   OTHER THAN PLAIN WRITE ?            89142
         BNZ   XERPDUMP      YES; DUMP PRIOR                     89142
         B     XERPMORE      ELSE APPEND                         89142
XERPCURA CLI   4(R8),C'+'    OVERPRINT ?                         89142
         BE    XERPMORE      YES; APPEND                         89142
         SPACE 1                                                 89142
XERPDUMP BAS   R14,XERPTRAN  CLEAR OLD PRINT BUFFERS             89216
         ICM   R6,3,0(R8)    LOAD RDW LENGTH                     89142
         BZ    XERPEXIT      SKIP IF NULL TRANSITION             89142
         MVC   XERCC,4(R8)   SAVE NEW CARRIAGE CONTROL           89142
         OI    MODEFG,FGXINIT  BUFFERS ARE CLEAN                 89144
         SPACE 1                                                 89144
         CLI   5(R8),X'30'   POSSIBLE OVERPRINT INDEX ?          89186
         BL    PASSTHRU      YES; PASS IT AS-IS                  89186
         CH    R6,=AL2(5+6)  LONG ENOUGH FOR OFFSET REQUEST ?    89142
         BL    XERPMORE      NO                                  89142
         CLC   =C'PGOFO',5(R8)  ODD PAGE OFFSET ?                89142
         BE    PASSTHRU      YES; PRINT AS IS                    89142
         CLC   =C'PGOFE',5(R8)  EVEN PAGE OFFSET ?               89142
         BE    PASSTHRU      YES; PRINT AS IS                    89142
         CLC   =4X'21',5(R8) **INTERIM OFFSET ?**                89149
         BE    PASSTHRU      YES                                 89149
         CLC   =X'11111111',5(R8)  XICS RAUX ?                   89144
         BE    PASSTHRU                                          89144
         CLC   =X'12121212',5(R8)  XICS OFFSET ?                 89144
         BE    PASSTHRU                                          89144
         CLC   =X'13131313',5(R8)  XICS STACK ?                  89144
         BE    PASSTHRU                                          89144
         CH    R6,=AL2(5+L'DJDE)  LONG ENOUGH FOR DJDE ?         89142
         BL    XERPMORE      NO                                  89142
         CLC   DJDE,5(R8)    IS IT A DJDE RECORD ?               89142
         BNE   XERPMORE      NO                                  89142
PASSTHRU BAS   R14,XERPTRAN  PROCESS PRIOR BUFFERS               89216
         L     R15,REALPUT   NO FONT INDEX ADJUSTMENT FOR DJDES  89142
         LR    R0,R8         COPY RECORD                         89142
         LR    R1,R9         COPY DCB                            89142
         BASR  R14,R15       PUT                                 89142
         B     XERPEXIT      AND RETURN                          89142
         SPACE 1                                                 89216
***********************************************************************
*                                                                     *
*        CHECK IF ACCUMULATION APPROPIATE                             *
*                                                                     *
***********************************************************************
XERPMORE SR    R6,R6                                             89142
         ICM   R6,3,0(R8)    ANY TEXT IN CURRENT BUFFER ?        89142
         BZ    XERPEXIT      NO (FINAL RITES ?)                  89142
         TM    MODEFG,FGXNO+FGXNFONT  ANY BYPASS ?               89144
         BNZ   PASSTHRU      YES                                 89144
         SH    R6,=H'5'      TRUE DATA LENGTH                    89142
         BM    XERPEXIT      ERROR                               89142
         BP    *+4+4+4                                           89142
         LA    R6,1                                              89142
         LA    R8,BLANKS-5   FAKE A BLANK INPUT RECORD           89142
         LA    R3,XERLINE    GET FIRST LINE                      89142
         LA    R4,XERLINE+WORKTEXT-WORKLINE  POINT TO NORMAL TEXT
         NI    MODEFG,255-FGXINIT  BUFFERS NO LONGER CLEAN       89144
         TM    MODEFG,FGXUFONT  USER SELECTED FONT ?             90296
         BZ    XERPMLUP      NO                                  90296
         BCTR  R6,0          FIX LENGTH FOR EXECUTION            90296
         MVC   WORKFONT-WORKLINE+XERLINE,XERUFONT  SET USER'S    90296
         EX    R6,XERPUMVC   MOVE USER'S DATA                    90296
         LA    R6,WORKTEXT-WORKLINE+1(,R6)  TRUE LENGTH          90296
         STH   R6,WORKRDW-WORKLINE+XERLINE                       90296
         BAS   R14,XERPTRAN  PRINT IT                            90296
         B     XERPEXIT      AND RETURN                          90296
XERPUMVC MVC   WORKTEXT-WORKLINE+XERLINE(0),5(R8) MOVE USER DATA 90296
         SPACE 1                                                 89142
***********************************************************************
*                                                                     *
*        ACCUMULATION LOGIC :                                         *
*                                                                     *
*        FIRST TIME IN A CHARACTER POSITION, SAVE CURRENT CHARACTER.  *
*        ELSE IF CURRENT IS BLANK, SKIP IT                            *
*        IF CURRENT MATCHES SECOND LINE, SKIP (NO TRIPLE PRINT)       *
*        IF CURRENT MATCHES FOURTH LINE, SKIP (NO TRIPLE PRINT)       *
*        IF CURRENT MATCHES FIRST LINE, ADD TO LINE 2                 *
*              OLD CHARACTER SET TO X'FF' (NO-OP ON 4050)             *
*        IF CURRENT MATCHES THIRD LINE, ADD TO LINE 4                 *
*              OLD CHARACTER SET TO X'FF' (NO-OP ON 4050)             *
*        ELSE IF FIRST, THIRD OR FIFTH LINE EMPTY, ADD THERE          *
*                                                                     *
*        ODD LINES ARE PRINTED WITH DEFAULT FONT (INDEX=0),           *
*        EVEN LINES WITH BOLD FONT (INDEX=1).                         *
*                                                                     *
***********************************************************************
         SPACE 1                                                 89142
XERPMLUP SR    R5,R5                                             89142
         IC    R5,5(,R8)     GET CURRENT CHARACTER TO WORK ON    89142
         CLI   0(R4),0       FIRST TIME ?                        89142
         BE    XERPMST1      YES; STASH INTO FIRST LINE          89142
         CLM   R5,1,BLANKS   IS THIS A BLANK ?                   89142
         BE    XERPMLOS      YES; IGNORE                         89142
         CLM   R5,1,L'XERLINE(R4)  DOUBLED ?                     89142
         BE    XERPMLOS      YES; NO TRIPLE PRINTING             89142
         CLM   R5,1,3*L'XERLINE(R4)  ALTERNATE DOUBLE ?          89142
         BE    XERPMLOS      YES; LOSE                           89142
         CLM   R5,1,0(R4)    IN FIRST LINE ?                     89142
         BE    XERPMD2       YES; MAKE DOUBLE                    89142
         CLM   R5,1,2*L'XERLINE(R4)  IN THIRD LINE ?             89142
         BE    XERPMD4       YES; MAKE SECOND DOUBLE             89142
         CLI   0(R4),C' '    AVAILABLE FIRST LINE ?              89142
         BH    XERPMTS3      NO                                  89144
         CLI   L'XERLINE(R4),C' '  REALLY FREE ?                 89144
         BNH   XERPMST1      YES; STASH IN LINE 1                89142
XERPMTS3 CLI   2*L'XERLINE(R4),C' '  AVAILABLE THIRD LINE ?      89142
         BH    XERPMTS5      NO                                  89144
         CLI   3*L'XERLINE(R4),C' '  REALLY AVAILABLE ?          89144
         BNH   XERPMST3      YES                                 89142
XERPMTS5 CLI   4*L'XERLINE(R4),C' '  AVAILABLE FIFTH LINE ?      89142
         BH    XERPMLOS      NO; IGNORE                          89142
         LA    R14,XERLINE+4*L'XERLINE                           89142
         LA    R15,4*L'XERLINE(,R4)                              89142
         B     XERPMADD      ADD ONE                             89142
XERPMST3 LA    R14,XERLINE+2*L'XERLINE                           89142
         LA    R15,2*L'XERLINE(,R4)                              89142
         B     XERPMADD                                          89142
XERPMD2  LA    R14,XERLINE+L'XERLINE                             89142
         LA    R15,L'XERLINE(,R4)                                89142
         MVI   0(R4),X'FF'   KILL OLD BYTE                       89360
         B     XERPMADD                                          89142
XERPMD4  LA    R14,XERLINE+3*L'XERLINE                           89142
         LA    R15,3*L'XERLINE(,R4)                              89142
         MVI   2*L'XERLINE(R4),X'FF'   KILL OLD BYTE             89360
         B     XERPMADD                                          89142
XERPMST1 LA    R14,XERLINE                                       89142
         LA    R15,0(,R4)                                        89142
XERPMADD STC   R5,0(,R15)                                        89216
         LA    R15,1(,R15)   EVERYTHING IS RELATIVE              89216
         SR    R15,R14       GET LINE LENGTH                     89216
         STMAX R15,WORKRDW-WORKLINE(R14),TYPE=H                  89286
XERPMLOS LA    R8,1(,R8)                                         89142
         LA    R4,1(,R4)                                         89142
         BCT   R6,XERPMLUP                                       89142
XERPEXIT LM    R14,R12,XERPSAVE  GET SAVED REGISTERS             89142
         BR    R14           RETURN TO CALLER                    89142
         SPACE 1                                                 89216
***********************************************************************
*                                                                     *
*        TRANSITION ROUTINE - PRINTS AND RE-INITIALIZES BUFFERS       *
*                                                                     *
***********************************************************************
XERPTRAN STM   R14,R12,XERPSAV2  SAVE REGISTERS                  89216
         LA    R3,XERLINE    GET INTERMEDIATE OUTPUT LINE        89142
         LA    R4,XERLINE2-XERLINE                               89142
         LA    R5,XERLINED   LAST LINE                           89142
         SR    R6,R6         FIRST LINE ADDRESS                  89142
         SR    R7,R7         LAST LINE ADDRESS                   89142
         USING WORKLINE,R3   DECLARE THE LINE ENTRY              89142
XERPTEST ICM   R0,3,WORKRDW  ANY DATA IN LINE ?                  89216
         BZ    XERPBUMP      NO                                  89142
         LTR   R6,R6         FIRST TIME ?                        89142
         BNZ   *+6           NO                                  89142
         LR    R6,R3         SAVE FIRST LINE                     89142
         LR    R7,R3         SAVE LAST LINE TO BE DONE           89142
XERPBUMP BXLE  R3,R4,XERPTEST  TRY AGAIN                         89142
         LTR   R3,R6         ANY DATA TO PROCESS ?               89142
         BZ    XERPINIT      NO; INITIALIZE FOR NEXT LINE        89142
         LR    R5,R7         SET LAST ADDRESS                    89142
XERPOUT  IC    R0,XERCC      GET SAVED CC                        89142
         TM    DCBRECFM,DCBRECCA  ASA MODE ?                     89142
         BNZ   XERPOUTA      YES                                 89142
         CR    R3,R7         IS THIS THE LAST LINE ?             89142
         BE    XERPOUTM      YES; USE CC AS IS                   89142
         N     R0,=F'2'      ELSE LEAVE ONLY THE NO-OP BIT       89142
         O     R0,=F'1'      AND MAKE A PLAIN WRITE              89142
         B     XERPOUTM      PUT THE LINE                        89142
XERPOUTA CR    R3,R6         FIRST LINE OF GROUP ?               89142
         BE    *+8           YES; USE CARRIAGE CONTROL           89142
         LA    R0,C'+'       ELSE SET OVERPRINT                  89142
XERPOUTM STC   R0,WORKCC     MAKE CC FOR THIS LINE               89142
         ICM   R0,3,WORKRDW  ANY DATA IN THIS LINE ?             89216
         BZ    XERPOUTP      NO; BUMP                            89142
         SLL   R0,16         LEFT-ADJUST                         89216
         STCM  R0,15,WORKRDW  SET LENGTH AND ZERO USE COUNT      89216
         L     R15,REALPUT   GET REAL PUT ROUTINE                89142
         LR    R1,R9         LOAD DCB                            89142
         LR    R0,R3         LOAD RDW ADDRESS                    89142
         BASR  R14,R15       CALL QSAM PUT ROUTINE               89142
XERPOUTP BXLE  R3,R4,XERPOUT DO IT AGAIN                         89142
XERPINIT LA    R3,XERLINE    GET FIRST LINE                      89142
         LA    R4,XERLINE2-XERLINE                               89142
         LA    R5,XERLINED   GET LAST LINE                       89142
         SR    R6,R6         FONT INDEX                          89142
         LA    R7,1          FLIP FLOP                           89142
XERPILUP XC    WORKRDW(L'XERLINE),WORKRDW  CLEAR ENTIRE LINE     89216
         STCM  R6,15,WORKUSE CLEAR USE COUNT, CC, SET FONT INDEX 89142
         XR    R6,R7         FLIP FONT INDEX                     89142
         BXLE  R3,R4,XERPILUP  AND DO ALL                        89142
XERPTRAX LM    R14,R12,XERPSAV2  RESTORE REGISTERS               89216
         BR    R14           RETURN TO CALLER                    89216
         SPACE 1                                                 89142
WORKLINE DSECT ,                                                 89142
WORKRDW  DS    H             RECORD LENGTH                       89142
WORKUSE  DS    H             0 - PUT; ELSE CHARACTER COUNT       89142
WORKCC   DS    C             CARRIAGE CONTROL                    89142
WORKFONT DS    X             FONT INDEX                          89142
WORKTEXT DS    CL(MAXWIDTH)  MAXIMUM TEXT WIDTH                  89142
         SPACE 2                                                 81365
         PRINT &PRTSYS                                           81365
CVT      DSECT ,                                                 81365
         CVT   DSECT=YES                                         81365
         SPACE 1                                                 81365
         IHACDE ,                                                81365
         SPACE 1                                                 81365
         IEFJMR ,                                                81365
         SPACE 1                                                 81365
         IEFTCT ,                                                81365
         SPACE 1                                                 81365
DUMTIOT  DSECT ,                                                GP99354
         IEFTIOT1 ,                                              81365
         SPACE 1                                                 81365
         IKJTCB ,                                                81365
         SPACE 1                                                 81365
         IEFUCBOB ,                                              81365
         SPACE 1                                                 81365
         DCBD  DSORG=PS                                          81365
         SPACE 1                                                 93008
         IEFJFCBE ,          EXPAND 3800 EXTENSION               93008
FCBJFCBE DS    CL4' '        PASSED TO JFCBE EXIT                93008
         AIF   (&MVS).MAPMVS                                     83007
         SPACE 1                                                 81365
BLOCKPRT CSECT ,                                                 81365
         $PCE  ,             PCE WORK AREA                       81365
SAVE     EQU   R13           NOT USED                            81365
         $XEQWORK ,          PCE WORK AREA FOR HASPXEQ           81365
         $JQE  ,                                                 81365
         AGO   .BIGEND                                           83007
.MAPMVS  ANOP  ,                                                 83007
         IEZJSCB ,                                               83007
         IEFJSSIB ,                                              83007
         IHAQDB ,                                                83080
         IHADSAB ,                                               83080
         IHAPSA ,                                                83080
.BIGEND  AIF   ('&LOCAL' NE 'PID').BOGEND                        92302
         SPACE 1                                                 86092
USERCVT  USERCVT ,                                               86092
         USERVOLT LIST=NO                                        86092
         A$GDA DSECT         EXPAND ACCOUNT CONTROL TABLE        89178
         SPACE 1                                                 81365
.BOGEND  END   ,                                                 83007
