VLNK     TITLE 'V A R L I N K  ***  VARIABLE LINK-EDIT INVOKATION PGM'
         MACRO
&NAME    WTP   &MSG
         LCLA  &I
&I       SETA  &SYSNDX
.P1      CNOP  0,4   ALIGN ON FULL WORD BOUNDARY
&NAME    BAL   R1,ZZWP&I.S   BRANCH AROUND MESSAGE
ZZWP&I.L DC    AL2(ZZWP&I.N-ZZWP&I.L,X'80'*256) LEN/FLAG
         DC    C&MSG WTP
ZZWP&I.N DC    AL2(0,32) DESC/ROUT
ZZWP&I.S SVC   35 .          WRITE-TO-PROGRAMMER
         MEND
         SPACE 1                                                 88269
         MACRO ,                                                 88269
&NM      EXSIZE &PFX=                                            88269
         LCLC  &P                                                88269
&P       SETC  '&PFX'                                            88269
         AIF   ('&P' NE '').HAVEPFX                              88269
&P       SETC  'LKP'                                             88269
.HAVEPFX AIF   ('&NM' EQ '').NONAME                              88269
&NM      DS    0C                                                88269
.NONAME  ANOP  ,                                                 88269
&P.SZ1   DC    C',SIZE=('                                        88269
&P.SZWK  EBCDIG 1234,4       WORK AREA SIZE                      89022
         DC    C'K,'                                             88269
&P.SZBF  EBCDIG 1234,4       BUFFER SIZE                         88269
         DC    C'K)'                                             88269
&P.SIZE  EQU   &P.SZ1,*-&P.SZ1,C'C'                              88269
         MEND  ,                                                 88269
         EJECT
**  AUTHOR :  ALAN FULFORD   DATE SUBMITTED :  25/7/73
**  INSTALLATION :  ALLIED BREWERIES, BURTON   PHONE :  0283-66322  X 3
**  THIS PROGRAM IS DESIGNED TO :
**    1) SELECT THE OPTIMUM F-LEVEL LINKAGE EDITOR FOR THIS PARTITION.
**    2) SELECT THE OPTIMUM SAFE VALUES FOR THE SIZE PARAMETER.
**    3) RESET THE PARM FIELD ACCORDINGLY.
**    4) XCTL TO THE SELECTED LINKAGE EDITOR.
**  ASSUMPTIONS :
**    1) F44, F88, & F128 LEVELS AVAILABLE.
**    2) ZLKD SHOULD NOT HAVE ALIASES IEWL & LINKEDIT IN SYS1.LINKLIB.
*              UNLESS THIS HAS BEEN CAREFULLY EVALUATED BY THE
*              INSTALLATION.  THE PROGRAM MAY GIVE ERRORS UNDER SMP,
*              AND THE CONTINUAL GETMAINS FOR A PARMLIST MAY RESULT
*              IN EVENTUAL 804 ABENDS.
**    3) 40 : 1 BLOCKING FACTORS USED FOR LINKAGE EDITOR.
**    4) IF PARTITION SIZE < 112K, SIZE ALREADY GIVEN IS USED FOR F44.
*
*        THIS PROGRAM HAS BEEN REWRITTEN BASED ON THE ORIGINAL CITED
*        ABOVE (DISTRIBUTED BY SHARE, INC. OS PROJECT).
*
*        THE ORIGINAL MFT DEPENDENCY HAS BEEN REMOVED, AND THE PROGRAM
*        MADE MORE FLEXIBLE BY REPLACING THE PARTITION SIZE LOOKUP
*        BY A VARIABLE CONDITIONAL GETMAIN, THUS ALLOWING SUPPORT
*        FOR DYNAMIC INVOKATION.
*
*        AT AMS, THIS PROGRAM IS LOADED INTO THE LINKPACK AREA TO
*        MAXIMIZE REGION UTILIZATION FOR BORDERLINE CASES.
*
*        ATTRIBUTES :   RE-ENTRANT, REFRESHABLE
*
*        CHANGES FOR MVS ENVIRONMENT:                            88269
*                                                                88269
*        IF PRIMARY SIZE > 256K, THEN BUFFER SIZE = (SIZE-64K)   88269
*           SECONDARY SIZE= .5 * (SIZE - 64K)                    88269
*        (HUGE SIZES NEEDED FOR NUCLEUS LINK-EDIT)               88269
*                                                                88269
*        CODE CHANGED TO USE NEW MACROS AND SERVICES             88269
         SPACE 2                                                 88269
         COPY  OPTIONGB      DEFINE GLOBALS                      88269
         SPACE 1                                                 88269
         SYSPARM LIST=YES    SET GLOBALS                         88269
         EJECT
         PRINT &PRTSOR                                           88269
VARLINK  SAVEM ZERO12,BASE=R12,PARM=R6,BNDRY=PAGE                88269
         SPACE 2
*        OBTAIN AMOUNT OF AVAILABLE STORAGE
         SPACE 1
         GETMAIN VC,LA=GMLEN,A=GETMAD,SP=0,MF=(E,GETWORK)  GET STORAGE
         BXLE  R15,R15,FREEMAIN  FREE IF IT WORKED               88269
NOCORE   WTP   'VARLINK - REGION TOO SMALL'
         L     R1,=X'80804000'   GIVE GREETINGS
         SR    R0,R0         JUST IN CASE
         SVC   13            ABEND
FREEMAIN LM    R2,R3,GETMAD   GET ADDRESS/LENGTH OF GOTTEN STORAGE
         FREEMAIN R,LV=(R3),A=(R2)     FREE IT UP; R3 HAS SIZE
         SRL   R3,11         TRUNCATE TO 2K MULTIPLE
         SLL   R3,1          MAKE INTO HEX K UNITS
         SH    R3,=Y(6+10*&MVS)  ALLOW FOR SYSTEM OVERHEAD       88269
         SPACE 2
**  DETERMINE REQUIRED LINKAGE EDITOR DESIGN LEVEL
         MVC   NEWSIZE,OLDSIZE  SET PATTERN SIZE LIST
         LA    R11,LKDTAB    GET ADDRESS FOR TABLE SCAN
         USING TABLE,R11
         LA    R2,2          DO COMPARE TWICE
         CH    R3,=Y(256-6-10*&MVS)  VERY LARGE ?                88269
         BNH   SEELOOP       NO; DO OLD-FASHIONED WAY            88269
         SH    R3,=Y(64-6-10*&MVS)  SUBTRACT 64K FROM MAXIMUM    88269
         MAX   R3,=Y(9999),TYPE=H  LIMIT TO SYNTACTIC MAXIMUM    89022
         LR    R5,R3         COPY FOR BUFFER SPACE               88269
         SRL   R5,1          AND HALVE IT                        88269
         B     CONVERT       USE IT                              88269
SEELOOP  CH    R3,VALUE1     FITS THIS ONE ?
         BNL   SETSIZE       OK, USE IT
         LA    R11,LKEDMED-LKDTAB(,R11)  TRY NEXT ONE
         BCT   R2,SEELOOP    TRY NEXT ENTRY
         B     F44           USE DEFAULT
         SPACE 2
**  COMPUTE VALUE2 OF SIZE PARAMETER
SETSIZE  SR    R4,R4
         LR    R5,R3         GET SIZE VALUE1
         SH    R5,VALUE1     COMPUTE EXCESS SIZE
         LA    R15,12
         DR    R4,R15        ROUND DOWN TO NEXT 12K
         MR    R4,R15
         AH    R5,VALUE2     COMPUTE SIZE VALUE2
         DROP  R11
         CH    R5,IH96+2     IS IT <= 96K
         BNH   CONVERT       YES, BRANCH
IH96     LA    R5,96         ELSE, USE 96K FOR VALUE2
CONVERT  CVD   R3,DWD        CONVERT VALUE1 TO DECIMAL
         OI    DWD+7,X'0F'   FORCE SIGN
         UNPK  NEWSZWK,DWD+4(4) SET IN NEW SIZE PARAMETER        88269
         CVD   R5,DWD        CONVERT VALUE2 TO DECIMAL
         OI    DWD+7,X'0F'   FORCE SIGN
         UNPK  NEWSZBF,DWD+4(4) SET IN NEW SIZE PARAMETER        88269
         SPACE 2
**  RESET PARM INFORMATION FOR THIS EXECUTION
         LA    R6,0(,R6)     CLEAR HIGH BYTE                     88269
         LTR   R6,R6         ANY PARM SUPPLIED ?
         BNP   F44           NO; SKIP PARM PROCESSING
         SLR   R7,R7                                             89022
         ICM   R7,7,1(R6)    LOAD AND TEST PARM POINTER          89022
         BZ    BUILDNEW      NONE; BUILD A NEW ONE               89022
         LH    R8,0(,R7)     GET SIZE OF CURRENT PARM
         LTR   R8,R8         IS IT ZERO
         BNZ   TACKTEST        NO, BRANCH
BUILDNEW GETMAIN R,LV=L'NEWSIZE-1+2  GET MAIN STORAGE FOR SIZE LIST
         LA    R9,L'NEWSIZE-1  SET LENGTH LESS COMMA             88269
         STH   R9,0(,R1)     SET LENGTH
         MVC   2(L'NEWSIZE-1,R1),NEWSIZE+1   ENTER NEW PARM DATA 88269
         B     RESET
         SPACE 2
*        IF EXISTING FIELD HAS SIZE, LEAVE IT AS IS
TACKTEST LR    R1,R7         POINT TO PARM FIELD LENGTH BYTE
         LA    R2,1          MAKE BXH INCREMENT
         LA    R3,0(R1,R8)   GET BYTE NEAR END OF DATA
         SH    R3,=H'6'      MAKE LAST REASONABLE LOCATION FOR 'SIZE='
         SR    R1,R2         FINAGLE FOR FIRST TIME              89022
TACKLOOP BXH   R1,R2,TACKON  SIZE NOT FOUND - BUILD NEW ONE
         CLC   1(5,R1),OLDSIZE+1  CALLER SUPPLIED SIZE ?
         BNE   TACKLOOP      NO; TRY AGAIN
         B     F44           YES - LEAVE AS IS
         SPACE 2
TACKON   LA    R0,L'NEWSIZE-1+2(,R8)    COMPUTE SIZE OF NEW PARM 88269
         GETMAIN R,LV=(0)    ACQUIRE MAIN STORAGE FOR IT
         BCTR  R8,0
         EX    R8,MOVEPARM   MOVE IN OLD PARM DATA
         LA    R9,L'NEWSIZE(,R8)    COMPUTE LENGTH OF NEW PARM DATA
         STH   R9,0(,R1)     ENTER INTO NEW PARM FIELD
         LA    R9,3(R8,R1)   LOCATE END OF OLD DATA
         MVC   0(L'NEWSIZE,R9),NEWSIZE     ADD ON NEW PARM DATA  88269
RESET    L     R0,0(,R6)     GET OLD PARM POINTER                88269
         N     R0,=X'80000000'  KEEP HIGH BIT                    88269
         OR    R0,R1         COMBINE WITH NEW ADDRESS            88269
         ST    R0,0(,R6)     STASH IT BACK                       88269
         SPACE 2
**  TRANSFER CONTROL TO LINKAGE EDITOR
F44      LR    R1,R13        COPY ADDRESS
         L     R13,4(,R13)   GET ORIGINAL SAVE AREA BACK
         L     R0,0(,R13)    AND LENGTH                          88269
         FREEMAIN R,LV=(0),A=(1)   FREE THE WORK AREA
         LR    R14,R11       SAVE NAME POINTER
         LM    R15,R12,16(R13)  RELOAD USER'S REGISTERS
         SPACE 1
*        XCTL WITH ALL REGISTERS INTACT EXCEPT R15
         XC    20(4,R13),20(R13)  XCTL LIST - DCB PARM=0
         ST    R14,16(,R13)                 - EPLOC
         LA    R15,16(,R13)  MAKE XCTL LIST ADDRESS
         L     R14,12(,R13)  RESTORE RETURN ADDRESS
         SVC   7             XCTL
         EJECT
**  REQUIRED CONSTANTS
LKDTAB   DS    0F                  PARAMETER TABLE
LKEDBIG  DC    C'IEWLF128'   NAME FOR F128
         DC    AL2(128)              VALUE1 FOR F128
         DC    AL2(24)               VALUE2 FOR F128
LKEDMED  DC    C'IEWLF880'   NAME FOR F88
         DC    AL2(104)              VALUE1 FOR F88
         DC    AL2(24)               VALUE2 FOR F88
LKEDNAME DC    C'IEWLF440'         LKED MODULE NAME
GMLEN    DC    A(52*1024,2048*1024)  GETMAIN REQUEST SIZE        88269
MOVEPARM MVC   2(0,R1),2(R7)  MOVE OLD PARM DATA
         EXSIZE PFX=OLD      SIZE PATTERN
         SPACE 2
         LTORG
         SPACE 2
**  REQUIRED DSECT
TABLE    DSECT
         DS    CL8           LINK-EDIT NAME
VALUE1   DS    H                   VALUE1
VALUE2   DS    H                   VALUE2
         SPACE 2
SAVE     DSECT ,             WORK AND SAVE AREA                  88269
DWD      DS    D                   WORK AREA
GETWORK  DC    3F'0'         GETMAIN WORK LIST
GETMAD   DC    2F'0'         GETMAIN RESULT ADDR/LEN
         EXSIZE PFX=NEW      NEW SIZE STRING
SAVEND   EQU   *                                                 88269
         END
