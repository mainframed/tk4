WYLDCOMP TITLE 'CONVERT WYLBUR EDIT TO FIXED/VARIABLE'           81051
*        THIS PROGRAM CONVERTS WYLBUR EDIT FORMAT DATA TO UNCOMPRESSED
*        OUTPUT (F OR V FORMAT).                                 80352
*        THE PROGRAM IS BASED ON THE ORIGINAL DISTRIBUTED BY N.I.H.
*          WITH CHANGES BY DICK OXLEY (COMNET) - QSAM, V-FORMAT  80352
*          CHANGES AND CORRECTIONS BY G. POSTPISCHIL (E.S.P.)    80352
         SPACE 1                                                 80352
*              MORE MUTILATION BY MAD MAN METZ (CNW)             81051
         SPACE 1                                                 89016
*        ADDED SUPPORT FOR NON-WYLBUR INPUT AND (UNLIKE) CONCATENATION
*          OF INPUT FILES.                                       89016
         SPACE 2                                                 81051
         COPY  OPTIONGB                                          81173
         SYSPARM LIST=YES                                        81194
         PRINT &PRTSOR                                           81121
         MTITL 'CONVERT WYLBUR EDIT TO FIXED/VARIABLE'           81051
         STITL 'MAINLINE'                                        81051
WYLDCOMP PGMHEAD BIGZERO,BASE=R12,PARM=R3,AM=24,RM=24
         SPACE 1                                                 88305
MAXWIDTH EQU   232           MAXIMUM LINE WIDTH ACCEPTED         88305
         SPACE 1                                                 88305
         SERVINIT ,          LOAD THE SERVICE ROUTINE            81194
         SERVCALL LPALD,=CL8'@DCBEXIT'  GET THE DCB EXIT         81194
         LR    R1,R0         COPY FOR STORES                     81194
         LA    R14,INDCB          INITIALIZE OPEN LISTS          81051
         LA    R15,OUTDCB                                        81051
         LA    R0,PRNTRDCB                                       81051
         ICM   R1,B'1000',=X'05'  1ST LEVEL DCB EXIT             81051
         LA    R2,I2LVL                                          81051
         ICM   R2,B'1000',=X'80'  NOP FOR DCBEXIT                81051
         STM   R14,R2,INOPL                                      81051
         MVI   INOPL,X'90'        INPUT,REREAD                   81194
         MVI   OUTOPL,X'8F'       OUTPUT                         81051
         MVI   PRNTROPL,X'8F'     OUTPUT                         81051
         LA    R2,O2LVL                                          81051
         ICM   R2,B'1000',=X'80'  NOP FOR DCBEXIT                81051
         STM   R1,R2,OUTEXLST                                    81051
         LA    R2,P2LVL                                          81110
         ICM   R2,B'1000',=X'80'                                 81110
         STM   R1,R2,PREXLST                                     81110
         SPACE 1                                                 81051
         MVC   OXDCBXID,=CL8'DCBEXITP'                           81051
         MVI   OXFLAG1,OX1TRUNC   TRUNCATE IF FB TO INT.*LRECL   81051
         MVI   OXDRECFM,DCBRECF   DEFAULT IS RECFM=F             81051
         MVI   OXLRECLF+1,80      DEFAULT LRECL (FB) - 80        81051
         MVI   OXLRECLV+1,255     DEFAULT LRECL (VB) - 255       89016
         MVI   OXFLAG3,OX3BLKTB+OX3BLKWY  USE CANNED BLOCKSIZES  81194
         MVI   OXOPLIST,X'80'  NO SECOND LEVEL CODE              81194
         SPACE 1                                                 81110
         MVC   P2LVL(PXOPLIST+3-PXOPLIST),O2LVL                  81110
         MVI   PXDRECFM,DCBRECF+DCBRECCA                         81110
         MVI   PXLRECLF+1,133                                    81110
         MVI   PXLRECLV+1,137                                    81110
         SPACE 1                                                 81051
         MVC   IXDCBXID,=CL8'DCBEXITP'                           81051
         MVI   IXFLAG1,IX1FULLT   DEFAULT BLKSIZE TO FULL TRACK  81051
         MVI   IXFLAG2,IX2CONCT   SUPPORT CONCATENATION          89016
         MVI   IXDRECFM,DCBRECU   DEFAULT IS RECFM=U             81051
         MVI   IXOPLIST,X'80'     NOP&LAST                       81051
         SPACE 1                                                 81051
         MVI   HEADER,C'1'        INITIALIZE HEADER              81051
         MVI   HEADER+1,C' '                                     81051
         MVC   HEADER+2(L'HEADER-2),HEADER+1                     81051
         LA    R11,PRNTRDCB
         USING IHADCB,R11         #####
         LA    R0,PREXLST                                        81110
         STCM  R0,B'0111',DCBEXLSA                               81110
       DEVTYPE DCBDDNAM,TEMP      SYSPRINT DD PRESENT ?          81051
         BXLE  R15,R15,PRNTOPEN    YES                           80352
         LA    R1,BR14       GET ADDRESS OF IMMEDIATE RETURN     80352
         STCM  R1,7,DCBPUT+1  SET DUMMY PUT ADDRESS              80352
         B     GOON          IGNORE MISSING OUTPUT               80352
PRNTOPEN OPEN  MF=(E,PRNTROPL)                                   81051
         TM    DCBOFLGS,DCBOFOPN  ERROR IN THE OPEN ?            81051
         BNZ   GOON               NO
PRNTNTOP ABEND 1403,,STEP    YES-ABEND WITH CC 1403              80352
*
*        PROCESS PARM FIELD
         SPACE 1                                                 81051
         AIF   ('&LOCAL' EQ 'CCSI').DEFCCSI                      81051
         AIF   ('&LOCAL' EQ 'CNW').DEFCNW                        81051
         AIF   ('&LOCAL' EQ 'EPA').DEFEPA                        81051
GOON     MVI   FLAG1,WORD# <---<< SET INSTALLATION DEFAULT HERE  81051
         AGO   .DEFCOM                                           81051
.DEFCCSI SPACE 1                                                 81051
GOON     MVI   FLAG1,WORD#        INSTALLATION DEFAULT           81051
         AGO   .DEFCOM                                           81051
.DEFCNW  SPACE 1                                                 81051
GOON     MVI   FLAG1,WORD#        INSTALLATION DEFAULT           81051
         AGO   .DEFCOM                                           81051
.DEFEPA  SPACE 1                                                 81051
GOON     MVI   FLAG1,0            INSTALLATION DEFAULT           81051
.DEFCOM  SPACE 1                                                 81051
         LTR   R3,R3              ANY PARM POINTER ?
         BZ    NOPARM             BRANCH IF NOT
         ICM   R3,7,1(R3)         SAVE ADDR OF PARM LIST         80352
         BZ    NOPARM             BRANCH IF NONE
         LH    R1,0(,R3)          GET LENGTH OF THE PARM INFO
         LTR   R1,R1              IS THERE ONE ?                 80352
         BNP   NOPARM             BRANCH IF NOT
         LA    R3,2(,R3)          BUMP POINTER PAST COUNT BYTES
         LR    R4,R3              PUT ADDR OF PARM LIST IN R4
         AR    R4,R1              SET END OF PARM ADDRESS
         B     COMASRCH      LOOK FOR LEADING COMMA AND BLANKS   80352
         SPACE 1                                                 80352
NEXTPARM LR    R14,R4                                            80352
         SR    R14,R3        GET RESIDUAL LENGTH                 80352
         CH    R14,=H'3'     LONG ENOUGH ?                       80352
         BL    BADPARM       NO                                  80352
         LA    R14,PARMTAB                                       80352
         LA    R15,(PARMTABE-PARMTAB)/(PARMTAB2-PARMTAB)         80352
PARMLOOP CLC   2(3,R14),0(R3)   MATCHING PARM ?                  80352
         BE    PARMFUND      YES                                 80352
         LA    R14,PARMTAB2-PARMTAB(,R14)                        80352
         BCT   R15,PARMLOOP  TRY AGAIN                           80352
         B     BADPARM                                           80352
         SPACE 1                                                 80352
PARMTAB  DC    AL1(0,255-NUMB-INT),C'UNN'   UNNUMBERED           80352
PARMTAB2 DC    AL1(NUMB,255),C'NUM'   NUMBERED (NNNN.DDD)        80352
         DC    AL1(INT,255),C'INT'   INTEGER NUMBERS             80352
         DC    AL1(INT+TSO,255),C'TSO'   INTEGER NUMBERS; VB RECFM
         DC    AL1(0,255-NUMB-INT),C'NON'   UNNUMBERED           80352
         DC    AL1(WORD#,255),C'WOR'   FULL-WORD LINE NUMBERS    80352
         DC    AL1(0,255-WORD#),C'HAL'   HALF-WORD LINE NUMBERS  80352
         DC    AL1(DEBUG,255),C'DEB'   DUMP ON NON-EDIT          80352
         DC    AL1(0,255-LIST),C'NOL'   NO LIST                  80352
         DC    AL1(LIST,255),C'LIS'   LIST OUTPUT ALSO           80352
PARMTABE EQU   *                                                 80352
         SPACE 1                                                 80352
PARMFUND NC    FLAG1,1(R14)  TURN UNWANTED BITS OFF              81051
         OC    FLAG1,0(R14)  TURN WANTED OPTIONS ON              81051
         LA    R3,3(,R3)          BUMP PARM LIST POINTER         80352
         B     TRAILDON      SEE IF MORE                         80352
TRAILSKP CLI   0(R3),C' '    TRAILING BLANK ?                    80352
         BE    COMASRCH      YES; QUIT                           80352
         CLI   0(R3),C','    TRAILING COMMA ?                    80352
         BE    COMASRCH                                          80352
         LA    R3,1(,R3)                                         80352
TRAILDON CR    R3,R4         ALL DONE ?                          80352
         BL    TRAILSKP      NO; SKIP TRAILING CRUD              80352
COMASRCH CR    R3,R4              ARE WE AT END OF LIST ?
         BNL   NOPARM             BRANCH IF SO
         CLI   0(R3),C' '    BLANK ?                             80352
         BE    COMASKIP      YES; SKIP                           80352
         CLI   0(R3),C','         IS NEXT CHAR A COMMA ?
         BNE   NEXTPARM      NO; SEE WHAT IT IS                  80352
COMASKIP LA    R3,1(,R3)          BUMP PARM LIST POINTER         80352
         B     COMASRCH      LOOK FOR NON-SEPARATOR              80352
         EJECT ,
*        OPEN FILES AND CHECK RECFM, LRECL ETC.
*
NOPARM   TM    FLAG1,NUMB+INT   WYLBUR AND INTEGER LINE NUMBERS ?81051
         BNO   NOPARM2       NO                                  80352
         NI    FLAG1,255-NUMB   USE ONLY INTEGER                 81051
NOPARM2  TM    FLAG1,LIST    LIST OPTION ?                       81051
         BZ    NOPARM3       NO; BEGIN PROCESSING                80352
         MVI   PREJECT+1,C'1'     SKIP TO TOP OF NEW PAGE        80352
         TM    PRNTRDCB+DCBOFLGS-IHADCB,DCBOFOPN  SYSPRINT OPEN ?
         BNZ   NOPARM3       YES; CONTINUE                       80352
         WTO   'LIST OPTION - SYSPRINT NOT SUPPLIED',ROUTCDE=11  80352
         B     PRNTNTOP      AND BOMB                            80352
NOPARM3  LA    R11,INDCB                                         81051
         LA    R0,INEXLST         SET EXLST=INEXLST IN INPUT     81051
         ST    R0,DCBEXLST                                       81051
         OPEN  MF=(E,INOPL)       OPEN THE INPUT DATA SET        81051
         TM    DCBOFLGS,DCBOFOPN  ERROR IN THE OPEN ?            81051
         BZ    OPENERRI            YES - PRINT MESSAGE & STOP    81051
*OLD*    TM    DCBRECFM,DCBRECU   RECFM=U ? (ELSE NOT EDIT)      81051
*OLD*    BNO   NOTEDIT             NO  - PRINT MESSAGE & STOP    81051
         TM    FLAG1,TSO          TSO OPTION SPECIFIED ?         81051
         BZ    *+8                 NO  - KEEP DEFAULT FB         81051
         MVI   OXDRECFM,DCBRECV    YES - MAKE DEFAULT VB         81051
         LA    R11,OUTDCB
         LA    R0,OUTEXLST        SET EXLST=OUTEXLST IN OUTPUT   81051
         ST    R0,DCBEXLST                                       81051
       DEVTYPE DCBDDNAM,TEMP      SYSUT2 DD PRESENT ?            81051
         BXLE  R15,R15,OPENOUT     YES                           80352
         TM    FLAG1,LIST    LIST OPTION ?                       81051
         BZ    OPENERRO      NO; EITHER SYSUT2 OR SYSPRINT/LIST REQ.
         LA    R1,BR14       POINT TO NO-OP                      80352
         STCM  R1,7,DCBPUT+1  MAKE DUMMY OUTPUT                  80352
         LR    R1,R11        GET DCB ADDRESS FOR OUTPUT          80352
         L     R15,INEXLST   GET @DCBEXIT ADDRESS BACK           81194
         BALR  R14,R15       FORCE DCB DEFAULT VALUES            80356
         B     NOPENOUT      SKIP OUTPUT OPEN                    80352
OPENOUT  OPEN  MF=(E,OUTOPL)      OPEN OUTPUT DATA SET           81051
         TM    DCBOFLGS,DCBOFOPN  ERROR IN THE OPEN ?            81051
         BZ    OPENERRO            YES - PRINT MESSAGE & ABEND   81051
NOPENOUT MVC   OUTRECFM,DCBRECFM  SAVE RECFM                     81051
         TM    OUTRECFM,DCBRECU   U FORMAT ?                     81051
         BO    UFORM               YES - ERROR MESSAGE           81051
         LH    R0,DCBLRECL        OUTPUT LRECL
         STH   R0,ORDW            SAVE LINE LENGTH
         TM    OUTRECFM,DCBRECV   V FORMAT ?                     81051
         BZ    *+12                 NO                           81051
         SH    R0,=H'4'      ALLOW FOR RDW AND MINIMUM ONE BYTE  80356
         BNP   TOOSMALL      NOT ENOUGH                          80356
         TM    FLAG1,NUMB+INT     NUMBERED ?                     81051
         BZ    LROKN              BRANCH IF NOT
         SH    R0,=H'8'           ALLOW FOR LINE NUMBER          81110
         BM    TOOSMALL           BRANCH IF LRECL TOO SMALL      81110
LROKN    CH    R0,=H'500'         TOO LONG ?                     81110
         BH    BADLRECL           BRANCH IF SO
*
*        GET AND UNPRESS RECORD
*
NEXTBUF2 NI    IXFLAG2,255-IX2REGET  RESET CONCATENATION FLAG    89016
NEXTBUF  GET   INDCB              GET NEXT BLOCK
         LR    R4,R1              COPY BUFFER POINTER
         TM    FLAG1,SYNADF       WAS THE READ OK ?              81051
         BO    INERR              NO-GO PRINT ERROR MESSAGE & TERMINAT
         TM    IXFLAG2,IX2REGET  CONCATENATION ?                 89016
         BNZ   NEXTBUF2      YES; RESET                          89016
         TM    DCBRECFM+INDCB-IHADCB,DCBRECU  WYLBUR ?           89016
         BNM   NEXTBUFU      YES; DEBLOCK HERE                   89016
         LA    R14,OBUF      ELSE MOVE TO OUR LINE BUFFER        89016
         LA    R15,L'OBUF                                        89016
         LR    R0,R1         COPY BUFFER START                   89016
         LH    R1,DCBLRECL+INDCB-IHADCB  GET PRESUMED RECORD LENGTH
         TM    DCBRECFM+INDCB-IHADCB,DCBRECF  FIXED ?            89016
         BNZ   NEXTBUFF      YES                                 89016
         ICM   R1,B'0011',0(R4)  GET LENGTH FROM RDW             89016
         SH    R1,=H'4'      LESS RDW LENGTH                     89016
         BNP   NEXTBUF       SKIP IF EMPTY                       89016
         LA    R0,4(,R4)     SET TO START OF TEXT                89016
NEXTBUFF CR    R1,R15        LONGER THAN LINE BUFFER ?           89016
         BNH   NEXTBUFM      NO                                  89016
         LR    R1,R15        TRUNCATE                            89016
         OI    RETCH+L'RETCH-1,4  SET TRUNCATION ERROR           89016
NEXTBUFM LA    R2,4(,R1)     GET LENGTH WITH RDW                 89016
         STH   R2,ORDW       STASH IT                            89016
         ICM   R1,B'1000',=C' '  SET BLANK FILL                  89016
         MVCL  R14,R0        MOVE LINE                           89016
         B     WRITE         AND PUT IT OUT                      89016
         SPACE 1                                                 89016
NEXTBUFU TM    0(R4),X'80'        NEW NIH FORMAT ?               81051
         BZ    *+12                NO                            81051
         OI    FLAG2,NIHNEW        YES - SET FLAG                81051
         NI    0(R4),X'7F'             - MASK BLOCK LENGTH       81051
         LH    R0,0(,R4)          GET TOTAL VALID BYTE COUNT IN RECORD
         LTR   R0,R0              CHECK IT OUT
         BNP   NOTEDITB           BRANCH IF BAD                  90110
         ST    R0,TOTALCNT        SAVE TOTAL BYTE COUNT
         CH    R0,DCBBLKSI-IHADCB+INDCB  LOGICALLY VALID ?       90110
         BH    NOTEDITB      NO; FAIL WITH MESSAGE               90110
         SPACE 1                                                 81051
*        REG   USE                                               81051
*        R2    RECORD BYTE COUNT FOR VALIDITY CHECK              81051
*        R3    IC/ICM FOR LENGTH                                 81051
*        R5    -> LINE #                                         81051
         SPACE 1                                                 81051
         LR    R6,R4         PRESERVE INPUT BUFFER ADDRESS       92358
SUMRSTRT LR    R4,R6         (RE)START BUFFER SCAN               92358
         LA    R4,2(,R4)          POINT R4 AT LINE NUMBER
         LR    R5,R4              POINTER TO INBUF IN R5
         LA    R2,2               SET RECORD BYTE COUNTER
SUMUP    XR    R3,R3               ASSUME 8-BIT LENGTH FIELD     81110
         IC    R3,4(,R5)                                         81110
         TM    FLAG2,NIHNEW        NEW NIH FORMAT ?              81110
         BZ    SUMUPOLD             NO  - CHECK OLD LIMIT        81110
         TM    FLAG2,NIHOSI  REALLY OSI/SSI FORMAT ?             92358
         BNZ   SUMUPOLD      YES; ONLY ONE-BYTE LENGTH           92358
         TM    0(R5),NIHLEN16       YES - 16-BIT LENGTH FIELD ?  81110
         BZ    SUMUPCOM                 - NO  - ACCEPT LENGTH    89313
         ICM   R3,B'0011',4(R5)         - YES - GET LENGTH       81110
         CH    R3,=Y((500/15+1)*16)                              81110
*                                  MAX. PREST COUNT EXCEEDED ?   81110
         BH    SUMISOSI             YES - ERROR                  92358
         TM    0(R5),NIHRAW        NIH UNCOMPRESSED ?            81110
         BZ    SUMUP1               NO  - PASSED CHECK           81110
         CH    R3,=H'500'           YES - VALID SIZE ?           81110
         BH    SUMISOSI                 - NO  - ERROR            92358
SUMUP1   LA    R3,1(,R3)           FUDGE                         81110
         B     SUMUPCOM            REJOIN SUMUP MAIN LINE        81110
SUMNTNIH TM    FLAG2,NIHNEW  NEW NIH FORMAT ?                    92358
         BZ    NOTPRESS      NO; NOT A RECOGNIZED FORMAT         92358
SUMISOSI TM    FLAG2,NIHOSI  BEEN HERE BEFORE ?                  92358
         BNZ   NOTPRESS      YES; NOT OSI/SSI FORMAT EITHER      92358
         OI    FLAG2,NIHOSI  TRY AGAIN WITH OSI/SSI FLAGS        92358
         B     SUMRSTRT      TRY A SECOND TIME                   92358
SUMUPOLD CH    R3,=Y((MAXWIDTH/15+1)*16)                         88305
*                                  MAX. PREST COUNT EXCEEDED ?   81110
         BH    SUMNTNIH           YES - ERROR                    92358
SUMUPCOM LA    R2,5(R2,R3)        BUMP RECORD COUNT BY PREST CT  81110
         LA    R5,5(R5,R3)        BUMP POINTER PAST THIS LINE
         C     R2,TOTALCNT        CUM COUNT > TOTAL BYTE COUNT ?
         BH    SUMNTNIH           YES - ERROR                    92358
         BL    SUMUP              GO ADD NEXT LINE COUNT
         SPACE 1                                                 81051
*        REG   USE IN DEBLOCKING PREST LINES                     81051
*        R1       NONBLANK MSK FOR PREST                         81069
*                 NONBLANK CNT FOR PREST                         81069
*        R2    -> OBUF DATA                                      81069
*        R3       LENGTH FIELD                                   81069
*        R4    -> LINE # IN CURRENT RECORD                       81051
*              -> PREST BYTE                                     81051
*              -> TEXT                                           81069
*        R5       LENGTH FIELD                                   81069
*                 BLANK  COUNT FOR PREST                         81069
*                 EX REG       FOR PREST                         81069
*        R7    BYTE COUNT FOR OUTPUT OVERFLOW TEST               81110
*        R9    BYTE COUNT FOR EOB TEST                           81051
         SPACE 1                                                 81051
         LA    R9,2               SET RECORD BYTE COUNTER        81051
NEXTLINE LA    R0,OBUF            BLANK OBUF                     81051
         LA    R1,L'OBUF                                         81051
         XR    R14,R14                                           81051
         XR    R15,R15                                           81051
         ICM   R15,B'1000',=C' '                                 81051
         MVCL  R0,R14                                            81051
         MVZ   FLAG2,0(R4)        PRESERVE NIH FLAGS             81051
         LA    R2,OBUF            R2-> 1ST OUTPUT BUFFER LOC.    81069
         TM    FLAG1,NUMB+INT     WAS NUMBERED PARM SPECIFIED ?  81051
         BZ    NONUM              NO
         MVC   LINE(4),0(R4)      YES-SAVE LINE # FOR LATER CONVERSION
         NI    LINE,X'7F'     STRIP OSI FLAG, IF ANY             81173
         TM    FLAG2,NIHNEW   NIH FORMAT ?                       81173
         BZ    *+8             NO  - LEAVE 2ND HW ALONE          81173
         NI    LINE,X'0F'         STRIP NIH FLAGS FROM LINE #    81051
         TM    OUTRECFM,DCBRECV   V FORMAT OUTPUT ?              81051
         BZ    *+8                  NO                           81051
         LA    R2,8(,R2)          ALLOW FOR LINE #               81069
NONUM    LA    R15,4              V-FORMAT OVERHEAD              81069
         XR    R3,R3              CLEAR FOR IC/ICM               81069
         IC    R3,4(,R4)          ASSUME 8-BIT LENGTH            81069
         TM    FLAG2,NIHNEW        NEW NIH FORMAT ?              83287
         BZ    NOTLEN16             NO  - 8-BIT LENGTH           83287
         TM    FLAG2,NIHOSI  NON-NIH FORMAT ?                    92358
         BNZ   NOTLEN16      YES; ONLY SINGLE-BYTE LENGTH        92358
         TM    0(R4),NIHLEN16       YES - 16-BIT LENGTH FIELD ?  83287
         BZ    NOTLEN16                 - NO  - ACCEPT LENGTH    89313
         ICM   R3,B'0011',0(R4)   GET 16-BIT LENGTH              81069
         LA    R4,1(,R4)          ADJUST PTR FOR 16-BIT LENGTH   81069
         LA    R9,1(,R9)          ADJUST CTR FOR 16-BIT LENGTH   81069
NOTLEN16 LTR   R5,R3              ANY TEXT ?                     81069
         BP    NONUML              YES                           81069
         TM    FLAG1,NUMB+INT     WILL LINE BE EMPTY ?           81069
         BNZ   NONUML              NO  - MINIMUM LENGTH IS OK    81069
         LA    R15,5              RDW + 1 BLANK                  81069
NONUML   AR    R15,R3             MAKE LENGTH WITHOUT NUMBERS    81069
         STH   R15,ORDW           SET NEW V-FORMAT LENGTH        81069
         AR    R9,R3              UPDATE INPUT BLOCK COUNT       81069
         XR    R7,R7              ZERO OUT OUTPUT COUNTER        81069
         LA    R4,5(,R4)          -> TEXT/1ST PREST BYTE         81069
         LA    R9,5(,R9)          BUMP INPUT BLOCK COUNT         81069
         LTR   R5,R3              ANYTHING TO MOVE ?             81069
         BNP   LINENO              NO  - GO WRITE LINE           81069
*                                  YES - PREST TEXT ?            81069
         TM    FLAG2,NIHRAW+NIHNEW                               81069
         BNO   MORE                    -  YES - DECOMPRESS IT    81069
         MVCL  R2,R4                   -  NO  - MOVE IT ALL      81069
         B     LINENO                  -        AND GO WRITE LINE81069
         SPACE 2                                                 81069
MOVER    MVC   0(,R2),1(R4)       EXECUTED FOR DECOMPRESS        81069
         SPACE 1                                                 81069
MORE     IC    R5,0(,R4)          LOAD PREST COUNTER BYTE        81069
         LA    R1,15              MASK FOR NONBLANK COUNT        81069
         NR    R1,R5              GET NONBLANK COUNT             81069
         SRL   R5,4               GET BLANK    COUNT             81069
         AR    R2,R5              SKIP OUTPUT PTR OVER BLANKS    81069
         AR    R7,R5              BUMP OUTPUT COUNT              81069
         AR    R7,R1                                             81110
         CH    R7,=H'500'          MAXIMUM LINE EXCEEDED ?       81110
         BH    NOTPRESS             YES - ERROR                  92358
         SR    R3,R1              DROP INPUT CNT BY NONBLANK CNT 81069
         BNP   NOTPRESS           MUST BE 1 LEFT FOR PREST BYTE  92358
         LTR   R5,R1              ANY NONBLANKS ?                81069
         BZ    CHECK               NO  - CHECK END-OF-LINE       81069
         BCTR  R5,0               DECR NONBLANK COUNT FOR EX     81069
         EX    R5,MOVER           MOVE NONBLANKS                 81069
         AR    R2,R1              MOVE OUTPUT PTR OVER NONBLANKS 81069
CHECK    LA    R4,1(R1,R4)        MOVE INPUT  PTR OVER NONBLANKS 81069
         BCT   R3,MORE            END-OF-LINE ?                  81069
*                                  YES - GO WRITE LINE           81069
         SPACE 1                                                 81110
*              FIX RDW IF RECFM=VB                               81110
         LA    R7,4(,R7)           CORRECT RDW                   81110
         STH   R7,ORDW                                           81110
         SPACE 2                                                 81110
*              PROCESS LINE NUMBERS                              81110
         SPACE 1                                                 81110
LINENO   TM    FLAG1,NUMB+INT     DID USER REQUEST 'NUMBERED'?   81051
         BZ    WRITE              NO - GO WRITE LINE
         TM    FLAG2,NIHNEW        NIH FORMAT ?                  81110
         BO    FULLWORD             YES - DO FULLWORD FORMAT     81110
         TM    FLAG2,X'80'          NO  - OSI STYLE FULL-WORD ?  81110
         BO    FULLWORD                 - YES - DO IT AS SUCH    81110
         TM    FLAG1,WORD#   FULL-WORD LINE NUMBER ?             81051
         BNZ   FULLWORD      YES                                 80352
         LA    R14,1000      MAKE SCALING FACTOR                 80352
         LH    R1,LINE       GET INTEGER PORTION                 80352
         MR    R0,R14        SCALE                               81014
         ICM   R0,3,LINE+2   GET SUPPOSED DECIMAL PORTION        80352
         CR    R0,R14        IS IT VALID ?                       80352
         BNL   NOTHALF       NO                                  80352
         AR    R1,R0         ELSE ADD IT IN                      80352
         B     COMMWORD      GO TO COMMON                        80352
FULLWORD L     R1,LINE       GET FULL-WORD VALUE                 81173
COMMWORD CVD   R1,TEMP       MAKE PACKED                         80352
         UNPK  LINE#(8),TEMP+3(5)  MAKE ZONED                    80352
         OI    LINE#+7,C'0'  MAKE GOOD ZONE                      80352
         TM    FLAG1,INT     INTEGER NUMBER ?                    81051
         BNZ   BACK          YES; SKIP FINAGLING                 80352
         MVC   LINE#(4),LINE#+1  SHIFT OVER                      80352
         MVI   LINE#+4,C'.'  MAKE DECIMAL POINT                  80352
         LA    R3,LINE#           SET POINTER TO 1ST DIGIT OF LINE #
NEXT     CLI   0(R3),C'0'         IS DIGIT ZERO ?
         BNE   LAST0              NO
         MVI   0(3),C' '          YES-CHANGE TO BLANK
         LA    R3,1(,R3)          BUMP  POINTER
         B     NEXT               ARE THERE MORE ?
*
LAST0    LA    R3,LINE#+7         POINT TO LAST DIGIT OF LINE #
STOP     CLI   0(R3),C'0'         IS DIGIT ZERO ?
         BNE   BACK               JOIN COMMON
         MVI   0(R3),C' '         YES-CHANGE TO BLANK
         BCT   R3,STOP            REDUCE TRAILING ZERO POINTER
*
BACK     TM    OUTRECFM,DCBRECV   V FORMAT OUTPUT ?              81051
         BZ    NOTV                 NO                           81051
         MVC   OBUF(8),LINE#      MOVE LINE# INTO FIRST 8 BYTES
         LA    R15,8         SET LENGTH OF LINE NUMBER           80352
         AH    R15,ORDW      GET V-FORMAT LENGTH                 80352
         STH   R15,ORDW      BUILD NEW RDW                       80352
         B     WRITE              GO WRITE RECORD
*
NOTV     LA    R11,OUTDCB         BETTER SAFE THAN SORRY
         LH    R8,DCBLRECL        FIXED - GET LRECL
         LA    R8,OBUF-8(R8)   GET INSERTION ADDRESS             80352
         MVC   0(8,R8),LINE#      MOVE LINE# INTO LAST 8 BYTES
*
*        OUTPUT RECORDS HERE
*
WRITE    TM    FLAG1,OUTERF       WAS THE LAST WRITE BAD ?       81051
         BO    OUTERR             YES-GO TO WRITE ERROR MESSAGE
         MVI   LINEOUT,0          CLEAR CC CHAR
         LA    R0,OBUF            SET UP BUFFER PTR
         TM    OUTRECFM,DCBRECV   V FORMAT OUTPUT ?              81051
         BZ    WRITENV              NO                           81051
         LA    R0,ORDW
         LH    R15,ORDW      GET CURRENT OUTPUT LENGTH           80352
         CH    R15,DCBLRECL-IHADCB+OUTDCB  FITS ?                80352
         BNH   WRITENV       YES                                 80352
         MVC   ORDW(2),DCBLRECL-IHADCB+OUTDCB  ELSE TRUNCATE     80352
         OI    RETCH+L'RETCH-1,4  SET TRUNCATION ERROR           89016
WRITENV  PUT   OUTDCB,(0)         OUTPUT LINE                    80352
         TM    FLAG1,LIST         DID USER REQUEST A LIST ?      81051
         BZ    NOLIST             NO                             80352
PREJECT  MVI   LINEOUT,C' '       SET CONTROL CHAR.              80352
         MVI   PREJECT+1,C' '   RESET FIRST-TIME PAGE-EJECT      80352
         PUT   PRNTRDCB,LINEOUT   YES-PRINT LINE
NOLIST   TM    DCBRECFM+INDCB-IHADCB,DCBRECU  WYLBUR ?           89016
         BM    NEXTBUF       NO; JUST GET ANOTHER LINE           89016
         C     R9,TOTALCNT        ARE THERE MORE LINES IN RECORD ?
         BNE   NEXTLINE           NO
         B     NEXTBUF            GO CHECK THE READ
         EJECT ,
SYNAD    OI    FLAG1,SYNADF       SET ERROR FLAG                 81051
BR14     BR    R14           DUMMY RETURN FOR UNOPENED SYSPRINT  80352
*
OUTER    OI    FLAG1,OUTERF       SET ERROR FLAG                 81051
         BR    R14
*
INERR    LA    R14,L'SYNADMSG-1   PRINT READ ERROR MESSAGE       80352
         LA    R15,SYNADMSG                                      80352
         B     ERRMSG                                            80352
*
OUTERR   LA    R14,L'OUTERMSG-1   PRINT WRITE ERROR MESSAGE      80352
         LA    R15,OUTERMSG                                      80352
         B     ERRMSG                                            80352
*
OPENERRO MVI   OPENMSG+33,C'2'    ERROR IN OPENING OUTPUT FILE   80352
OPENERRI LA    R14,L'OPENMSG-1    PRINT OPEN ERROR MESSAGE       80352
         LA    R15,OPENMSG                                       80352
ERRMSG   OI    RETCH+L'RETCH-1,8  SET ERROR RETURN CODE          89016
ENDMSG   EX    R14,MSGMOVE   MOVE MESSAGE TEXT                   80352
         PUT   PRNTRDCB,HEADER   PRINT ERROR OR COMPLETION MESSAGE
         MVI   INOPL,X'30'       RSET DISP=LEAVE                 81194
         MVI   OUTOPL,X'30'                                      81194
         MVI   PRNTROPL,X'B0'                                    81194
         CLOSE MF=(E,INOPL)                                      81051
         SERVTERM ,          DELETE, FREE, ETC.                  81194
         LH    R3,RETCH      LOAD RETURN CODE                    89016
         PGMEXIT RC=(R3)                                        GP03030
         SPACE 1                                                 81051
MSGMOVE  MVC   HEADER+1(0),0(R15)   MOVE MESSAGE TEXT            80352
*
DSEND    TM    FLAG1,OUTERF  ERROR ON LAST WRITE ?               81051
         BNZ   OUTERR                                            80352
         LA    R14,L'EODSMSG-1    PRINT TRANSLATION SUCCESSFUL   80352
         LA    R15,EODSMSG                                       80352
         SLR   R3,R3                                             80352
         B     ENDMSG                                            80352
*
NOTEDITB STM   R14,R1,TEMP   SAVE FOR DUMP                       90110
         LA    R15,BADBLOCK  SET MESSAGE                         90110
         LA    R14,L'BADBLOCK-1                                  90110
         B     NOTEDITM                                          90110
NOTPRESS STM   R14,R1,TEMP   SAVE FOR DUMP                       92358
         LA    R15,BADRECLN                                      90110
         LA    R14,L'BADRECLN-1                                  90110
NOTEDITM EX    R14,MSGMOVE                                       90110
         PUT   PRNTRDCB,HEADER  WRITE MESSAGE                    90110
         LM    R14,R1,TEMP   RESTORE REGS                        90110
NOTEDIT  TM    FLAG1,DEBUG        DEBUG OPTION ?                 81051
         BNO   *+8                BRANCH IF NOT
         EX    0,*                TELL THEM ALL ABOUT ERROR
         LA    R14,L'EDITBAD-1
         LA    R15,EDITBAD                                       80352
         B     ERRMSG                                            80352
*                                                                80352
NOTHALF  TM    FLAG1,DEBUG        DEBUG OPTION ?                 81051
         BNO   *+8                BRANCH IF NOT                  80352
         EX    0,*                TELL THEM ALL ABOUT ERROR      80352
         LA    R14,L'HALFNOT-1                                   80352
         LA    R15,HALFNOT                                       80352
         B     ERRMSG                                            80352
*
BADPARM  LA    R14,L'PARMMSG-1                                   80352
         LA    R15,PARMMSG                                       80352
         B     ERRMSG                                            80352
*
BADLRECL LA    R14,L'LRECLMSG-1                                  80352
         LA    R15,LRECLMSG                                      80352
         B     ERRMSG                                            80352
*
TOOSMALL LA    R14,L'SLRECLMS-1                                  80352
         LA    R15,SLRECLMS                                      80352
         B     ERRMSG                                            80352
         SPACE 1                                                 81051
UFORM    LA    R14,L'UFORMMSG-1                                  81051
         LA    R15,UFORMMSG                                      81051
         B     ERRMSG                                            81051
         SPACE 2
         DROP  R11                #####
         PRINT &PRTSYS                                           81121
         USING IHADCB,R1          #####
*
PRNTRDCB DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),RECFM=FBA,          *
               LRECL=133,EROPT=ACC,EXLST=123                     81194
         SPACE 1                                                 81051
INDCB    DCB   DDNAME=SYSUT1,DSORG=PS,EODAD=DSEND,MACRF=(GL),          *
               SYNAD=SYNAD,BFTEK=A                               81051
         SPACE 1                                                 81051
OUTDCB   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(PM),SYNAD=OUTERR    81051
         SPACE 2
*        ERROR MESSAGES AND STUFF
*
OUTERMSG DC    C'ERROR IN WRITING SYSUT2 DATA SET.'              80352
*
OPENMSG  DC    C'ERROR OCCURRED WHEN OPENING SYSUT1 DATA SET.'   80352
*
EODSMSG  DC    C'DATA SET SUCCESSFULLY CONVERTED FROM EDIT FORMAT'
*
SYNADMSG DC    C'ERROR IN READING SYSUT1 DATA SET.'              80352
*
EDITBAD  DC    C'THE INPUT DATA SET IS NOT IN EDIT FORMAT'       80352
*
PARMMSG  DC    C'ILLEGAL PARM SPECIFIED ON EXEC CARD'            80352
*
LRECLMSG DC    C'OUTPUT LINE SIZE GREATER THAN 500'              81110
*
SLRECLMS DC    C'OUTPUT LOGICAL RECORD LENGTH TOO SMALL'         80352
*
UFORMMSG DC    C'U-FORMAT OUTPUT NOT SUPPORTED'                  80352
*                                                                80352
HALFNOT  DC    C'LINE NUMBER IS NOT IN HALF-WORD FORMAT'         80352
*
BADBLOCK DC    C'BLOCK DESCRIPTOR IS ZERO OR LARGER THAN BLOCKSIZE'
BADRECLN DC    C'RECORD LENGTH BYTE(S) INCORRECT'                90110
         SPACE 1                                                 90110
         LTORG ,                                                 81051
         STITL 'WORKAREA'                                        81051
SAVE     DSECT ,                                                 81051
TEMP     DS    2D                                                90110
LINE     DS    F                                                 81051
TOTALCNT DS    F                                                 81051
@SERVICE DS    A             ADDRESS OF @SERVICE                 81194
RETCH    DC    H'0'          RETURN CODE                         89016
LINE#    DS    CL8                                               81051
         SPACE 1                                                 81051
OUTRECFM DS    X                                                 81051
         SPACE 1                                                 81051
FLAG1    DS    X             FLAG BYTE 1                         81051
LIST     EQU   X'80'         LIST OPTION                         81051
NUMB     EQU   X'40'         NUMBERED OPTION - NOT INT           81051
INT      EQU   X'20'         INTEGER  OPTION                     81051
TSO      EQU   X'10'         DEFAULT TO VB                       81051
WORD#    EQU   X'08'         FULL WORD LINE NUMBERS              81051
DEBUG    EQU   X'04'         ABEND OPTION                        81051
OUTERF   EQU   X'02'         SYSUT2 SYNAD ERROR                  81051
SYNADF   EQU   X'01'         SYSUT1 SYNAD ERROR                  81051
         SPACE 1                                                 81051
FLAG2    DS    X             FLAG BYTE 2                         81051
NIHRAW   EQU   X'80'         NIH - NOT COMPRESSED                81051
NIHLEN16 EQU   X'40'         NIH - 16 BIT LENGTH FIELD           81051
NIHNOSPC EQU   X'20'         NIH - NO SPECIAL CONTROL CHARACTERS 81051
NIHMARK  EQU   X'10'         NIH - LINE IS FLAGGED AS CHANGED    81051
NIHNEW   EQU   X'08'         NIH - NEW WYLBUR EDIT RECOGNIZED    81051
NIHOSI   EQU   X'04'         NOT NIH - HYBRID OSI/SSI STYLE      92358
         SPACE 1                                                 81051
HEADER   DC    CL133'1'      HEADER & MESSAGE AREA               81051
         SPACE 1                                                 81051
*              THE FOLLOWING THREE LINES MUST REMAIN TOGETHER.   81051
ORDW     DS    A                                                 81051
LINEOUT  EQU   *-1,1,C'C'                                        81051
OBUF     DS    CL500                                             81051
         DS    CL8                 FOR MAX NIH + LINE #          81110
         SPACE 3                                                 81051
INOPL    OPEN  (INDCB,INPUT),MF=L                                81051
OUTOPL   OPEN  (OUTDCB,OUTPUT),MF=L                              81051
PRNTROPL OPEN  (PRNTRDCB,OUTPUT),MF=L                            81051
INEXLST  DC    X'05',AL3(0)  @DCBEXIT                            81194
         DC    X'80',AL3(I2LVL)                                  81051
OUTEXLST DC    X'05',AL3(0)                                      81194
         DC    X'80',AL3(O2LVL)                                  81051
PREXLST  DC    X'05',AL3(0)                                      81194
         DC    X'80',AL3(P2LVL)                                  81110
         SPACE 2                                                 81051
         PRINT &PRTMAC                                           81121
I2LVL DCBEXITD DSECT=NO,PREFIX=IX                                81173
         SPACE 2                                                 81051
O2LVL DCBEXITD DSECT=NO,PREFIX=OX                                81173
         SPACE 1                                                 81110
P2LVL DCBEXITD DSECT=NO,PREFIX=PX                                81173
         SPACE 2                                                 81051
SAVEEND  EQU   *                                                 81051
         SPACE 2                                                 90110
         PRINT &PRTSYS                                           81121
         DCBD  DSORG=PS
         SPACE 2                                                 80352
         PRINT NOGEN                                             81194
CVTDSECT DSECT ,                                                 81194
         CVT   DSECT=YES                                         81194
         SPACE 1                                                 81194
         IHACDE ,                                                81194
         SPACE 1                                                 81194
         END
