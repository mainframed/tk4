DEBTR    TITLE 'D E B T R A C E  ***  QUICK AND DIRTY DEBUG OUTPUT'
***********************************************************************
*                                                                     *
*                                                                     *
*   THIS ROUTINE, DRIVEN BY THE DEBTRACE (DBO) MACRO, PRODUCES DEBUG  *
*   OUTPUT FOR ONE OR MORE OF THE OPTIONAL PARAMETERS:                *
*                                                                     *
*   CODE IS EXTREMELY NOT RE-ENTRANT, REUSEABLE, NOR REFRESHABLE.     *
*   CALLER MAY BE AM24 OR 31; ADDRESSES CALCULATED ACCORDINGLY.       *
*   MODULE OPERATES IN AM31.                                          *
*                                                                     *
*   LABEL XTRAP vars,OPT=(psw,regs),ID=name  EXHIBIT use              *
*   LABEL DBO TAG,REGS=,TEXT=,HEX=                                    *
*         "TAG" IS AN OPTIONAL 1-8 BYTE NAME (NO QUOTES)              *
*         "REGS=" PRODUCES REGISTERS 0-15 IN HEX                      *
*         "REGS=NO" PRODUCES NO REGISTERS (DEFAULT)                   *
*         "REGS=SHORT"  PRODUCES REGISTERS 14-1 ONLY                  *
*                                                                     *
*         "TEXT=NAME" PRODUCES EBCDIC TEXT USING L'NAME AS A LENGTH   *
*         "TEXT=(NAME,LEN)" PRODUCES TEXT OF LENGTH "LEN"             *
*             THE "NAME" IN THE SECOND FORM MAY BE IN REGISTER OR     *
*             BASE/DISPLACEMENT FORM:  TEXT=((R2),16)                 *
*         "CTEXT=... PRODUCES EBCDIC IF PRINTABLE, ELSE HEX           *
*                                                                     *
*         "HEX= " USES THE SAME SYNTAX AS TEXT=, BUT THE SPECIFIED    *
*             LENGTH IS THAT OF THE INPUT, AND THE OUTPUT WILL BE     *
*             TWICE THE INPUT LENGTH (PLUS SPACERS)                   *
*                                                                     *
*         "PACK=" USES THE SAME SYNTAX AS TEXT=, BUT THE SPECIFIED    *
*             LENGTH IS THAT OF THE INPUT, AND THE OUTPUT WILL BE     *
*             TWICE THE INPUT LENGTH. A LENGTH OF 0 IS VALID.         *
*                                                                     *
*         'LIST=(OP1,(OP2,LN2),(OP3,,TP3)...)                         *
*             PROCESSES A LIST OF ITEMS. EACH ITEM HAS THREE FIELDS:  *
*             NAME OF ITEM TO BE PROCESSED (NO DEFAULT)               *
*             LENGTH (DEFAULT IS L'NAME EXCEPT PACKED DECIMAL, L=0)   *
*             TYPE: HEX (DEFAULT), TEXT, CTEXT, PACKED                *
*                                                                     *
*   ALL NAME FIELDS, WHETHER IN A KEYWORD OR THE LIST OPERAND, MAY    *
*   BE FULLWORD POINTERS:   /NAME DISPLAYS THE DATA AT THE LOCATION   *
*   FOUND IN FIELD NAME. NOTE THAT THIS FORM REQUIRES A LENGTH.       *
*   THE FORM NAME% CAUSES 24-BIT ADDRESSING RESOLUTION WHEN IN 31-BIT *
*   MODE. BOTH / AND % MAY BE USED FOR THE SAME NAME FIELD.           *
*                                                                     *
*   THE CALLER MAY SET DBT@UDCB TO AN OPEN DCB ADDRESS, OR A PRINT    *
*   ADDRESS (EXORPRNT, @PRINTER) AND SET DBTFGMOD ACCORDINGLY.        *
*                                                                     *
*   WHEN THE PRODUCTION MODE FLAG IS ON, PROCESSING WILL BE BYPASSED  *
*   UNLESS A DEBTRACE DD IS SUPPLIED (WTO - DD DUMMY)                 *
*                                                                     *
*   INPUT:  R15 - BASE ADDRESS            R14 - RETURN ADDRESS        *
*           R0  - WORK AREA IN CALLER'S PROGRAM                       *
*           R1  - CALLER'S REQUEST:   CL8'TAG ' OR BLANKS, THEN       *
*     ONE OR MORE REQUEST UNITS OF THREE HALF-WORDS EACH:             *
*    X'000'    R1     R2    DUMP REGISTERS R1 TO R2                   *
*    X'100' S(TEXT) Y(LEN)  TEXT; INPUT "LEN"                         *
*    X'200' S(TEXT) Y(LEN)  CTEXT; TEXT IF PRINTABLE; HEX OTHERWISE   *
*    X'300' S(HEX@) Y(LEN)  HEX; INPUT "LEN"                          *
*    X'400' S(PAK@) Y(LEN)  PACKED DECIMAL; 0<=LEN<16                 *
*                                                                     *
*                                                                     *
*                                                                     *
*    X'50' OR HIGHER - END OF LIST (L, BAS, BAL IN MACRO EXPANSION)   *
*                                                                     *
*   THIS ROUTINE MAY BE CALLED IN 24-BIT AND 31-BIT ADDRESSING MODE,  *
*   AND MAY RESIDE ABOVE THE LINE WHEN LINKED WITH A 31-BIT MODE PGM. *
*   ALL ADDRESS EXPRESSIONS ARE RESOLVED IN THE CALLER'S MODE.        *
*                                                                     *
***********************************************************************
         COPY  OPTIONGB      DEFINE GLOBALS
         SPACE 1
         SYSPARM LIST=YES    DEFINE GLOBAL SETTINGS
         SPACE 1
DEBTRACE CSECT ,
         USING DEBTRACE,R15  DECLARE
         B     AROUND        BRANCH AROUND CPR AND ENTRY VECTOR
         DC    AL1(L'OWNTEXT+L'IDTEXT)
IDTEXT   DC    C'DEBTRACR &SYSDATE'
OWNTEXT  DC    C' COPYRIGHT 1995 EXPERT SYSTEM PROGRAMMING'
         ORG   DEBTRACE+64
         DC    A(DEBTRDCB)   EXTERNAL CLOSE ACCESS
AROUND   BSM   R14,0         PRESERVE CALLER'S AMODE            GP99029
         STM   R2,R15,DBTSVLOC  SAVE ESSENTIAL REGISTERS        GP99043
         LR    R12,R15       LOCAL BASE
         DROP  R15
         USING DEBTRACE,R12
         AM31  WORK=R15      GET HIGH                           GP99029
         SR    R15,R15                                          GP99116
         LTR   R14,R14       CALLER IN AM31?                    GP99116
         BNM   SETAMASK      NO                                 GP99116
         LA    R15,X'7F'     MAKE MASK FOR AMODE 31             GP99116
SETAMASK STC   R15,ADDRMASK                                     GP99116
         LR    R8,R0         KEEP OLD WORK AREA ADDRESSABLE     GP95235
         N     R8,ADDRMASK   CLEAN WORK AREA POINTER            GP99116
         LA    R13,SUBSAVE   POINT TO NEW SAVE AREA             GP95235
         NI    PROFLAGS,PFGBL  RESET ALL BUT GLOBAL FLAGS       GP99113
         USING DBTSAVE,R8    DECLARE OLD AREA                   GP99113
         LA    R6,DBTEXITS   SET FOR QUICK EXIT (LABEL ONLY)
*  NOTE: IN PRODUCTION MODE BZR IS CHANGED TO BR WHEN DD DEBTRACE HAS
*    NOT BEEN SUPPLIED (USES WTO FOR DD DUMMY)                  GP99113
         TM    DBTFLAG,DBTFLON  DEBUG ON ?
         BZR   R6            NO; JUST TAKE QUICK RETURN         GP99113
         TM    PROFLAGS,PFCANC  PERMANENT DISABLE?              GP00162
         BNZR  R6            YES; JUST TAKE QUICK RETURN        GP00162
         LR    R9,R1         COPY PARM REGISTER                 GP95235
         N     R9,ADDRMASK   CLEAN PARAMETER POINTER            GP99116
         SPACE 1                                                GP99113
         CLI   DBTFGMOD,DBTF@LCL   IS THIS THE FIRST CALL ?     GP99113
         BNE   DBTNONCE      NO                                 GP99113
         SPACE 1                                                GP00162
         MVI   DBTFGMOD,DBTF@WTO   FIRST TIME CHECKED; DEFAULT  GP99113
         DEVTYPE DEBDCB+DCBDDNAM-IHADCB,DB USER SUPPLY DEBTRACE DD?
         BXH   R15,R15,DBTBYPED   SEE WHETHER TO BYPASS?        GP99113
         CLI   DB+2,0        SOMETHING I CAN USE?               GP99113
         BE    DBTNONCE      NO; USE WTO                        GP99113
         OPEN  MF=(E,DEBTRDCB) OPEN IT
         TM    DEBDCB+DCBOFLGS-IHADCB,DCBOFOPN   OPEN NOW?
         BZ    DBTNONCE
         MVI   DBTFGMOD,DBTF@DCB  SIGNAL OPEN DCB               GP99113
         LA    R0,DEBDCB     GET DCB ADDRESS                    GP99113
         ST    R0,DBT@UDCB   AND SET IT                         GP99113
         CLC   DCBLRECL-IHADCB+DEBDCB,=H'132'                   GP99113
         BL    DBTNONCE                                         GP99113
         OI    DBTFLAG,DBTFLWID  SET WIDE MODE                  GP99113
         B     DBTNONCE                                         GP99113
DBTBYPED TM    DBTFLAG,DBTFLPRO  PRODUCTION MODE?               GP99113
         BZ    DBTNONCE      NO; USE WTO                        GP99113
         OI    PROFLAGS,PFCANC  DISABLE COMPLETELY              GP00162
         B     DBTEXITS      PROD & NO DD - BYPASS              GP99113
*   COUNT(3) - SKIP FIRST N CALLS                               GP95235
DBTNONCE ICM   R14,15,DBTCNT3 LOAD SKIP COUNT                   GP95235
         BNP   DBCNTC        LIMIT REACHED - PROCESS            GP95235
         BCTR  R14,0         DECREMENT                          GP95235
         STCM  R14,15,DBTCNT3 SAVE FOR NEXT TIME                GP95235
         BR    R6            AND SKIP CALL                      GP95235
DBCNTC   ICM   R14,15,DBTCNT2A  LOAD COUNTER                    GP95235
         BNP   DBCNTL        BAD - PROCESS CALL                 GP95235
         BCT   R14,DBCNTN    NON-ZERO; SAVE AND SKIP            GP95235
         MVC   DBTCNT2A,DBTCNT2  REFRESH COUNTER                GP95235
         B     DBCNTL        AND GO                             GP95235
DBTCNT2A DC    AL4(1)        INTERVAL COUNTER (DO FIRST ONE)    GP95235
DBCNTN   STCM  R14,15,DBTCNT2A  UPDATE COUNTER                  GP95235
         BR    R6            AND EXIT                           GP95235
DBCNTL   ICM   R14,15,DBTCNT1    LOAD LIMIT COUNT               GP95235
         BZ    DBTRACNM      NO COUNT, NO LIMIT                 GP95235
         BNPR  R6            IGNORE IF REACHED                  GP95235
         BCT   R14,DBTRACNM  DECREMENT                          GP95235
         BCTR  R14,0         MAKE NEGATIVE FOR NEXT TIME        GP95235
DBTRACNM STCM  R14,15,DBTCNT1    SAVE FOR NEXT TIME             GP95235
         USING REQWORK,R9    DECLARE MAPPING OF REQUESTS        GP95235
         TM    DBTFLAG,DBTFLWTO  IS WTO FORCED?                 GP99113
         BNZ   INITSHRT      YES; NARROW MODE                   GP99113
         TM    DBTFLAG,DBTFLWID  WIDE MODE REQUESTED?           GP99113
         BZ    INITSHRT      NO                                 GP99113
         OI    PROFLAGS,PFWIDE  SET WIDE MODE                   GP99113
         MVC   MSGLABL(MSGRTDS2-MSGLABL),MSGLABL-1   BLANK THE LINE
         MVC   MSGRDW(MSGLABL-MSGRDW),PATRDW   MAKE PATTERN     GP99113
         LA    R0,MSGRTDS2-MSGRDW  WIDE SIZE                    GP99113
         STH   R0,MSGRDW     SET WIDE                           GP99113
         TM    DBTFLAG,DBTFLTCB  INCLUDE TCB IN MESSAGE?        GP95235
         BZ    DBTNTCB       NO                                 GP95235
         TM    PROFLAGS,PFDUAL  DUAL MODE REQUEST?              GP00162
         BNZ   DBTNTCB       YES; NO ROOM FOR IT                GP00162
         UNPK  MSG@TCB2(9),PSATOLD-PSA(5)                        94011
         TR    MSG@TCB2,DBTHEXTR                                 94011
         MVI   MSG@TCB2+L'MSG@TCB2,C' '                          94011
         B     DBTNTCB       DONE                               GP99113
         SPACE 1                                                GP99113
INITSHRT MVC   MSGLABL(MSGRTDS-MSGLABL),MSGLABL-1   BLANK THE LINE
         MVC   MSGHEAD,=C'MSG666 '    DEBUG HEADER
         MVC   MSGRTDS,MSGRTDS2  REFRESH ROUTER/DESCRIPTOR CODES
         TM    DBTFLAG,DBTFLTCB  INCLUDE TCB IN MESSAGE?        GP95235
         BZ    DBTNTCB       NO                                 GP95235
         UNPK  MSG@TCB(9),PSATOLD-PSA(5)                         94011
         TR    MSG@TCB,DBTHEXTR                                  94011
         MVI   MSG@TCB+L'MSG@TCB,C' '                            94011
         SPACE 1                                                GP99113
DBTNTCB  CLI   REQTYPE,C'A'   IS THERE A LABEL?                 GP02364
         BNL   DBTLUP        YES; USE IT                        GP02364
         TM    REQTYPE,B'01111000'  ANY FUNNY BITS ON ?         GP02364
         BZ    DBTLOOP       NO; FORMATTING ITEM                GP02364
DBTLUP   MVC   MSGLABL,REQWORK   MOVE USER'S LABEL              GP95235
         LA    R9,REQWORK+8  BUMP PAST LABEL                    GP95235
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  LOOP TO PROCESS CURRENT REQUEST (POINTED TO BY R9)               **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DBTLOOP  IC    R2,REQTYPE    LOAD NEXT REQUEST                  GP97265
         LA    R0,RQTMSK     LOAD END-LIST MASK                 GP02364
         NR    R2,R0         IGNORE END-LIST BIT                GP02364
         CH    R2,=Y((PROCEND-PROCONE)/(PROCTWO-PROCONE))       GP97265
         BH    DBTLAST       TOO HIGH, TOO BAD                  GP97265
         SLL   R2,1          MAKE PROCESSOR ROUTINE OFFSET      GP97265
         LH    R7,PROCONE(R2)  LOAD PROCESSING ROUTINE          GP97265
         LA    R15,1                                            GP97265
         NR    R15,R7        SEE WHETHER ADDRESS WAS ODD        GP97265
         BZ    DEBTRACE(R7)  NO; INVOKE IT DIRECTLY             GP97265
         SRL   R7,1                                             GP97265
         SLL   R7,1          ELSE MAKE IT EVEN                  GP97265
         B     RESADDR                                          GP97265
         SPACE 1                                                GP97265
PROCONE  DC    Y(DOREGS-DEBTRACE)        REGISTER FORMATTING    GP97265
PROCTWO  DC    Y(FUNFTEXT-DEBTRACE+1)    TEXT                   GP97265
         DC    Y(FUNFCHEX-DEBTRACE+1)    TEXT OR HEX            GP97265
         DC    Y(FUNFHEX-DEBTRACE+1)     HEX                    GP97265
         DC    Y(FUNPACK-DEBTRACE+1)     PACKED                 GP97265
         DC    Y(FUNFHEX-DEBTRACE+1)       SPARE                GP97265
         DC    Y(FUNFHEX-DEBTRACE+1)       SPARE                GP97265
         DC    Y(FUNFHEX-DEBTRACE+1)       SPARE                GP97265
         DC    Y(DBTLIST-DEBTRACE)       LIST FORM              GP97265
PROCEND  EQU   *                                                GP97265
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  PROCESS ADDRESS, LENGTH, AND FLAG OPTION FOR COMMON FUNCTIONS    **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
RESADDR  SLR   R2,R2                                            GP97265
         IC    R2,REQADDR    GET S(ADDR)
         SRL   R2,4          DELETE LOW BITS OF OFFSET
         SLA   R2,2          *4 => INDEX INTO DBTSAVE
         BZ    *+8           R0 = 0
         L     R2,DBTSAVE(R2)  GET USER'S REGISTER
         LA    R0,4095       MAKE MASK
         ICM   R15,3,REQADDR  GET LOW PORTION OF ADDRESS        GP95235
         NR    R0,R15        MASK OFFSET OF ADDRESS             GP95235
         ALR   R2,R0         MAKE EFFECTIVE ADDRESS
         TM    REQFG,RFA24   FORCED 24-BIT ADDRESSING MODE?     GP96101
         BZ    *+8           NO                                 GP96101
         N     R2,=X'00FFFFFF'  FORCE 24-BIT ADDRESS            GP96101
         TM    REQFG,RFA31   FORCED 31-BIT ADDRESSING MODE?     GP99116
         BNZ   NOTBAD31      NO                                 GP99116
         N     R2,ADDRMASK   USE MASK IN USER'S AMODE           GP99116
NOTBAD31 TM    REQFG,RFIAD   INDIRECT ADDRESSING?               GP96101
         BZ    NOTIAD31      NO                                 GP96101
         L     R2,0(,R2)     LOAD ADDRESS FROM POINTER          GP96101
         TM    REQFG,RFA24   FORCED 24-BIT ADDRESSING MODE?     GP96101
         BZ    NOTIAD        NO                                 GP96101
         N     R2,=X'00FFFFFF'  FORCE 24-BIT ADDRESS            GP96101
NOTIAD   TM    REQFG,RFA31   FORCED 31-BIT ADDRESSING MODE?     GP99116
         BNZ   NOTIAD31      NO                                 GP99116
         N     R2,ADDRMASK   USE MASK IN USER'S AMODE           GP99116
NOTIAD31 SLR   R3,R3                                            GP96101
         IC    R3,REQLEN     GET S(LEN)
         SRL   R3,4          DELETE LOW BITS OF OFFSET
         SLA   R3,2          *4 => INDEX INTO DBTSAVE
         BZ    *+8           R0 = 0
         L     R3,DBTSAVE(R3)  GET USER'S REGISTER
         LA    R0,4095       MAKE MASK
         N     R3,=X'00FFFFFF'    MAKE SOME LIMIT               GP10020
         ICM   R15,3,REQLEN  GET LOW PORTION OF LENGTH          GP95235
         NR    R0,R15        MASK OFFSET OF LENGTH              GP95235
         ALR   R3,R0         MAKE EFFECTIVE ADDRESS
         B     DEBTRACE(R7)  GO TO PROCESSING ROUTINE           GP97265
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  PROCESS REGISTER REQUESTS                                        **
**    REGISTERS ARE MOVED TO REGWORK (TO HANDLE WRAP-AROUND) AND     **
**    THEN PROCESSED AS A HEX DISPLAY                                **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DOREGS   LA    R1,DBTSAVE    POINT TO R0                        GP96101
         LA    R0,15         MAKE MASK                          GP97261
         LH    R14,REQADDR   GET START REGISTER                 GP97261
         LH    R15,REQLEN    GET END REGISTER                   GP97261
         NR    R14,R0        KEEP ONLY LOW NIBBLE (START REG)   GP96101
         NR    R15,R0          END REGISTER                     GP96101
         LA    R1,DBTHEXTB(R14)  POINT TO PRINTABLE EQUIVALENT  GP97265
         MVC   MSGFLAG+1(1),0(R1)  IDENTIFY IT                  GP97265
         MVI   MSGFLAG,C'R'-X'40'  AS REGISTER                  GP97265
         CR    R14,R15       IS LOW SAME AS HIGH?               GP96101
         BNE   DOREGWRP      NO                                 GP96101
         BCTR  R15,0         MAKE RANGE OF 15 REGISTERS         GP96101
         NR    R15,R0        CLEAN IT UP                        GP96101
DOREGWRP SLDL  R14,2         CONVERT REGISTER NUMBER TO OFFSET  GP96101
         CR    R14,R15       IS LOW LESS THAN HIGH?             GP96101
         BH    DOREGSPL      NO; SPLIT                          GP96101
         LA    R2,DBTSAVE(R14)  POINT TO FIRST CONTIGUOUS REGISTER
         LR    R3,R15                                           GP96101
         SR    R3,R14                                           GP96101
         LA    R3,4(,R3)     MAKE HEX LENGTH                    GP96101
         B     DBTFHEX       FORMAT HEX                         GP96101
DOREGSPL LA    R3,(16+1)*4   MAX LENGTH TO FORMAT + ONE         GP96101
         AR    R3,R15        PLUS END OFFSET                    GP96101
         SR    R3,R14        LESS START OFFSET = REQ. LENGTH    GP96101
         LA    R4,DBTSAVE(R14)  POINT TO FIRST (LOW) SEGMENT    GP96101
         LA    R5,REGWORK    SET DESTINATION ADDRESS            GP96101
         LA    R1,64-1       MAX MOVABLE                        GP96101
         SR    R1,R14        LESS DISPLACEMENT TO LOW           GP96101
         EX    R1,DOREGMVC   MOVE FIRST PORTION                 GP96101
         LA    R4,DBTSAVE    SET FROM ADDRESS                   GP96101
         LA    R5,REGWORK+1(R1)  OFFSET TO SECOND PART          GP96101
         LA    R1,4-1(,R15)    LENGTH TO MOVE                   GP96101
         EX    R1,DOREGMVC   MOVE SECOND SECTION                GP96101
         LA    R2,REGWORK    POINT TO CONSOLIDATED FIELD        GP96101
         B     DBTFHEX       GO TO FORMAT                       GP96101
DOREGMVC MVC   0(0,R5),0(R4)  MOVE                              GP96101
         SPACE 1                                                GP97265
***********************************************************************
**                                                                   **
**  FOR PACKED DECIMAL, PERFORM VALIDITY CHECK. MAKE SURE LENGTH     **
**   MATCHES USER'S REQUEST IF NON-ZERO                              **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
FUNPACK  MVI   MSGFLAG,C'L'-X'40' PROVISIONALLY FLAG BAD LENGTH GP97265
         LA    R4,16         SET MAXIMUM LENGTH                 GP96081
         CR    R3,R4         IS USER'S LENGTH VALID?            GP96081
         BH    DBTFHEX       NO; FORMAT HEX WITH 'L' FLAG       GP96081
         MVI   MSGFLAG,C'P'-X'40' FLAG BAD PACKED (PRELIMINARY) GP97265
         LA    R1,0(,R2)     COPY ADDRESS                       GP96081
         LA    R7,9          CONSTANT                           GP96081
         LA    R0,X'0F'      MASK                               GP96081
         LA    R5,1(,R4)     REMEMBER THE LENGTH                GP96081
         SLR   R14,R14       CLEAR                              GP96081
         LTR   R6,R3         LOAD USER'S LENGTH                 GP96081
         BNZ   DBTPACKL      OK; UP AND AT 'EM                  GP96081
         LR    R6,R4         USE THIS ONE                       GP96081
DBTPACKL IC    R14,0(,R1)    LOAD NEXT BYTE                     GP96081
         LR    R15,R14       COPY                               GP96081
         SRL   R14,4         ISOLATE HIGH NYBBLE                GP96081
         CR    R14,R7        IS IT LEGAL ?                      GP96081
         BH    DBTFHEX       NO; DISPLAY AS HEX                 GP96081
         NR    R15,R0        ISOLATE LOW NYBBLE                 GP96081
         CR    R15,R7        IS IT NUMERIC?                     GP96081
         BH    DBTPACKD      NO; SIGN ENDS THE FIELD            GP96081
         LA    R1,1(,R1)     SPACE                              GP96081
         BCT   R6,*+8        OK IF USER'S COUNT NOT EXHAUSTED   GP96081
         B     DBTFHEX       ELSE INVALID PACKED                GP96081
         BCT   R4,DBTPACKL   TRY AGAIN                          GP96081
         B     DBTFHEX       TOO BAD                            GP96081
         SPACE 1                                                GP96081
*  COPY NUMBER TO MAXIMUM LENGTH PACKED WORK AREA AND CHECK SIGN
*    NOTE THAT SIGN IS STILL IN R15                             GP96081
DBTPACKD SR    R5,R4         INPUT LENGTH                       GP96081
         LTR   R3,R3         DID USER SPECIFY AN INPUT LENGTH?  GP96081
         BZ    DBTPACK0      NO; USE OURS                       GP96081
         MVI   MSGFLAG,C'L'-X'40' PROVISIONALLY FLAG BAD LENGTH AGAIN
         CR    R3,R5         SAME AS USER'S ?                   GP96081
         BNE   DBTFHEX       NO; FORMAT AS HEX                  GP96081
DBTPACK0 BCTR  R5,0          EXECUTE LENGTH                     GP96081
         XC    REGWORK(16),REGWORK  USE AS WORK SPACE           GP96081
         LA    R7,REGWORK+16-1                                  GP96081
         SR    R7,R5         LOCATION TO MOVE TO                GP96081
         EX    R5,DBTPACKM   MOVE TO WORK SPACE                 GP96081
         MVC   MSGTEXT-1(37),DBTPAMSK                           GP96081
         LA    R1,MSGTEXT+L'MSGTEXT-1   POINT TO SIGNIFICANCE   GP96081
         EDMK  MSGTEXT+L'MSGTEXT-32(32),REGWORK  FORMAT         GP96081
         BCTR  R1,0          INSIGNIFICANT LOCATION             GP96081
         STC   R15,0(,R1)    STORE HEX SIGN (0A-0F)             GP96081
         TR    0(1,R1),DBTPACKS-X'0A'  MAKE PRINTABLE SIGN      GP96081
         OI    MSGTEXT+L'MSGTEXT-1,C'0'  MAKE F ZONE            GP96081
         CLI   0(R1),C'?'    UNUSUAL SIGN?                      GP96081
         BE    DBTPACKF      NO                                 GP96081
         SLR   R3,R3         NO MORE DATA                       GP96081
         B     DBTLAST       PRINT THIS LINE                    GP96081
DBTPACKM MVC   0(0,R7),0(R2)  RIGHT ADJUST USER'S NUMBER        GP96081
DBTPACKS DC    C'??+-? '     F - BLANK, C - PLUS, D - MINUS     GP96081
DBTPAMSK DC    C' PD   ',30X'20',X'21,20'                       GP96081
DBTPACKF MVI   MSGTEXT-1,C' '                                   GP96081
         MVC   MSGTEXT,MSGTEXT-1  CLEAR TEST PORTION AGAIN      GP96081
         MVI   MSGFLAG,C'S'-X'40' SHOW BAD SIGN AND DO AS HEX   GP97265
         B     DBTFHEX                                          GP97266
         SPACE 1
***********************************************************************
**                                                                   **
**  CONDITIONAL TEXT :  COMPARE TEXT TO OUR TRANSLATE TABLE. WHEN    **
**  UNPRINTABLE CHARACTERS OCCUR, FORMAT THE STRING IN HEXADECIMAL,  **
**  OTHERWISE PRINT IT.                                              **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
FUNFCHEX MVI   MSGFLAG,C'C'-X'40'                               GP97266
DBTFCHEX LTR   R15,R3        COPY LENGTH                        GP96101
         BNP   FUNFTEXT      INVALID; GET OUT                   GP96101
         BCTR  R15,0         SET FOR EXECUTE                    GP96101
         LR    R4,R2         SAVE                               GP96101
         EX    R15,CHEXTRT   IS THIS A PRINTABLE TEXT STRING?   GP96101
         LR    R2,R4         RESTORE                            GP96101
         BZ    FUNFTEXT      YES; PRINT AS TEXT                 GP96101
         OI    PROFLAGS,PFDUAL  ENABLE DUAL HEX/TEXT DISPLAY    GP00162
         B     FUNFHEX       ELSE FORMAT IN HEX                 GP96101
CHEXTRT  TRT   0(0,R2),TRTCHAR  CHECK FOR PRINTABLES            GP96101
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  HEXADECIMAL FORMATTING                                           **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP96101
FUNFHEX  MVI   MSGFLAG+1,C'H'-X'40'    MAKE LOWER CASE H        GP97266
         OI    PROFLAGS,PFDUAL  ENABLE DUAL HEX/TEXT DISPLAY    GP00162
DBTFHEX  BASR  R6,(0)        SET RETURN ADDRESS                  92271
         SLR   R4,R4
         LA    R1,MSGTEXT    POINT TO TEXT
         LA    R5,16         MAX INPUT CHARACTERS ON LINE        92093
         TM    PROFLAGS,PFWIDE  DOING WIDE?                     GP99113
         BZ    DBTFHEX3      NO                                 GP99113
         LA    R5,32         PRINT TWO GROUPS                   GP99113
         TM    PROFLAGS,PFDUAL  DUAL MODE REQUEST?              GP00162
         BZ    DBTFHEX3      NO                                 GP00162
         LTR   R3,R3         IS LENGTH VALID ?                  GP00162
         BNP   DBTFHEX3      NO                                 GP00162
         LA    R15,L'MSGHTEX  MAX LENGTH ON ONE LINE            GP00162
         MIN   (R15),(R3)    USE SHORTER                        GP00162
         BCTR  R15,0         SET FOR EXECUTE                    GP00162
         EX    R15,EXMVCHTX  MOVE TO OUTPUT LINE                GP00162
         EX    R15,EXTRHTX   AND MAKE IT PRINTABLE              GP00162
         MVI   MSGHTEX-1,C'*'  FRAME THE TEXT                   GP00162
         MVI   MSGHTEX+L'MSGHTEX,C'*'                           GP00162
DBTFHEX3 LTR   R3,R3         VALID LENGTH?                      GP96081
         BP    DBTFHEXL      YES                                GP96081
         MVC   MSGTEXT(3),=C'L=?'   MAKE AN ERROR MESSAGE       GP96081
DBTFHEXL LTR   R3,R3         ANY MORE TO DO ?
         BNP   DBTLAST
         MVC   DB(1),0(R2)   MOVE SINGLE BYTE                   GP98222
         UNPK  0(3,R1),DB(2)  UNPACK ONE BYTE (NO 0C4)          GP98222
         TR    0(2,R1),DBTHEXTR   MAKE IT PRINTABLE             GP97265
         MVI   2(R1),C' '    BLANK NEXT
         LA    R2,1(,R2)
         LA    R1,2(,R1)
         BCTR  R3,0          ADJUST RESIDUAL COUNT
         LA    R4,1(,R4)     ADD ONE TO COUNT DONE
         LA    R15,3
         NR    R15,R4        END OF A WORD ?
         BNZ   *+8           NO
         LA    R1,1(,R1)     LEAVE A GAP BETWEEN WORDS
         CR    R4,R5         DONE ONE LINE ?                     92093
         BNL   DBTLAST       YES; PROCESS IT
         CH    R4,=H'16'     DONE ONE GROUP?                    GP99113
         BNE   DBTFHEXL      NO; NOT YET, OR NEVER              GP99113
         LA    R1,1(,R1)     LEAVE A LARGER GAP BETWEEN GROUPS  GP99113
         B     DBTFHEXL      SEE IF MORE TO DO
EXMVCHTX MVC   MSGHTEX(0),0(R2)  COPY USER'S TEXT               GP00162
EXTRHTX  TR    MSGHTEX(0),TRTABTN   LIMIT TO PRINTABLE STUFF    GP00162
         SPACE 1                                                GP97266
***********************************************************************
**                                                                   **
**  TEXT PRINTING                                                    **
**                                                                   **
***********************************************************************
FUNFTEXT MVI   MSGFLAG+1,C'T'-X'40'  L.C. T                     GP97266
DBTFTEXT LA    R4,L'MSGTEXT  SET MAXIMUM TEXT PRINTABLE
         TM    PROFLAGS,PFWIDE  DOING WIDE?                     GP99113
         BZ    DBTFTEX3      NO                                 GP99113
         LA    R4,2*(1+L'MSGTEXT)  WIDER IS BETTER?             GP99113
DBTFTEX3 LTR   R3,R3         VALID LENGTH?                      GP96081
         BP    DBTFTEXL      YES                                GP96081
         MVC   MSGTEXT(3),=C'L=?'   MAKE AN ERROR MESSAGE       GP96081
DBTFTEXL BASR  R6,(0)        SET RETURN POINT                    92271
         LTR   R3,R3         ANY MORE ?
         BNP   DBTLAST       NO; EXIT
         LR    R15,R4        SET TEXT LENGTH
         CR    R3,R15        IS REQUEST LONGER ?
         BNL   *+6           YES
         LR    R15,R3        ELSE USE SHORTER
         BCTR  R15,0         SET EXECUTE LENGTH
         EX    R15,DBTFTMVC  MOVE TO LINE
         EX    R15,DBTFTTR   MAKE PRINTABLE                     GP00162
         LA    R1,MSGTEXT+1(R15)  GET END OF LINE+1             GP00162
         MVI   0(R1),C'<'    SHOW WHERE TEXT ENDS               GP00162
         AR    R2,R4         UP TEXT ADDRESS
         SR    R3,R4         SET RESIDUAL PRINT LENGTH
         B     DBTLAST       PRINT IT
DBTFTMVC MVC   MSGTEXT(0),0(R2)  MOVE USER'S TEXT
DBTFTTR  TR    MSGTEXT(0),TRTABTN  LIMIT TO PRINTABLES          GP00162
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  ADJUST FOR MULTIPLE (LIST) ENTRIES IN PARAMETER LIST             **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DBTLIST  LA    R3,REQSIZE    SET POSITIVE                       GP95235
         BAS   R6,DBTLAST    PRINT PRIOR LINE                   GP95235
         LA    R9,REQWORK+2  SKIP OVER TYPE HEADER              GP95235
         B     DBTLUP        SHOW NAME                          GP95235
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  PROCESS THE CURRENT RECORD PER USER'S REQUEST                    **
**    SKIP COMPLETELY BLANK LINES                                    **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DBTLAST  CLC   MSGLABL(MSGRTDS-MSGLABL),MSGLABL-1  ANYTHING TO PRINT ?
         BE    DONEOUT       NO; SKIP WHEN NO DATA              GP99113
         TM    DBTFLAG,DBTFLWTO  USE WTO RATHER THAN PRINT?     GP95240
         BNZ   DOWTO         YES                                GP95240
         IC    R0,DBTFGMOD   HOW TO PRINT?                      GP99113
         BIX   BASE=DEBTRACE,ERR=DOWTO,                                *
               LOC=(DOWTO,DOWTO,     0,1 - WTO                         *
               DOPUT,                2 - PUT TO OPEN DCB               *
               DOEXIT,               3 - CALL USER EXIT                *
               DOXPRINT,             4 - OLD EXORPRNT/XPRINT I/F       *
               DO@PRINT)             5 - NEW @PRINTER INTERFACE GP99113
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  PRINT RECORD TO ANY DCB OPEN FOR QSAM OUTPUT                     **
**    (CURRENTLY DATA MODE IS NOT SUPPORTED)                         **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DOPUT    LA    R1,MSGRDW     GET OUTPUT RECORD                  GP99113
         BAS   R14,PRTDEB    GO TO PUT ROUTINE                  GP99113
         B     DONEOUT       JOIN COMMON                        GP99113
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  PASS RECORD TO SPECIFIED USER EXIT                               **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DOEXIT   LA    R1,MSGRDW     GET OUTPUT RECORD                  GP99113
         L     R15,DBT@UDCB  GET USER EXIT                      GP99113
         AIF   (NOT &MVSESA).OLDSYN                             GP04234
         SYNCH (R15),RESTORE=YES,AMODE=DEFINED  CALL USER EXIT  GP99113
         AGO   .COMSYN                                          GP04234
.OLDSYN  SYNCH (R15),RESTORE=YES                CALL USER EXIT  GP04234
.COMSYN  B     DONEOUT       JOIN COMMON                        GP99113
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  CALL EXORPRNT (XPRINT) TO PROCESS THE RECORD                     **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DOXPRINT LA    R1,MSGRDW     GET OUTPUT RECORD                  GP99113
         L     R15,DBT@UDCB  GET USER EXIT                      GP99113
         BASR  R14,R15       ANCIENT PRINT ROUTINE (CIRCA 1969) GP99113
         B     DONEOUT       JOIN COMMON                        GP99113
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  PASS THE RECORD TO @PRINTER (USE PRTV TYPE ENTRY)                **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DO@PRINT PRTV  MSGRDW,DEV==DBTF@DEV   PRINT ON REQUESTED FILE   GP95235
         BXLE  R15,R15,DONEOUT  IF IT FAILS, USE WTO            G922845
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  WRITE RECORD WITH WTO                                            **
**    NOTE THAT WE DON'T REMOVE THE CARRIAGE CONTROL CHARACTER       **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DOWTO    WTO   MF=(E,MSGRDW)   WRITE TO CONSOLE                  95067
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  DONE PRINTING.                                                   **
**                                                                   **
**  1) WHEN R3 IS NON-ZERO (RESIDUAL INPUT LENGTH), CONTINUE PROCESS **
**                                                                   **
**  2) BUMP ITEM POINTER (R9) AND FORMAT IT IF VALID                 **
**                                                                   **
**  OTHERWISE CLEAN UP AND RETURN TO CALLER                          **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DONEOUT  MVC   MSGLABL(MSGRTDS-MSGLABL),MSGLABL-1  BLANK IT      93357
         MVC   MSGHEAD,MSGLABL  CLEAR MESSAGE NUMBER             93357
         MVI   MSGHEAD+5,C'+' INDICATE CONTINUATION              93357
         MVC   MSGRDW(5),PATRDW   REFRESH RDW/FLAGS             GP99113
         LA    R0,MSGRTDS-MSGRDW    NARROW                      GP08240
         TM    PROFLAGS,PFWIDE   WIDE MODE?                     GP99113
         BZ    UPDTRDW       NO                                 GP99113
         MVC   MSGLABL(MSGRTDS2-MSGLABL),MSGLABL-1   BLANK THE LINE
         LA    R0,MSGRTDS2-MSGRDW  WIDE SIZE                    GP99113
UPDTRDW  STH   R0,MSGRDW     SET WIDE                           GP99113
         SPACE 1                                                GP99113
LASTSHRT LTR   R3,R3         ANY MORE TO DO ?                   GP95235
         BPR   R6            RESTART
DBTEXIT  TM    REQTYPE,RQTEOL  END OF LIST FLAG ?               GP02364
         BNZ   DBTEXITS      YES; STOP NOW                      GP02364
         LA    R9,REQSIZE(,R9)     SKIP TO NEXT OPTION ENTRY    GP95235
         B     DBTLOOP       AND DO IT                          GP03002
DBTEXITS TM    DBTFLAG,DBTFLEND  DID WE HAVE A CLOSE CALL ?     GP98222
         BZ    DBTEXITZ      NO                                 GP98222
         CLOSE MF=(E,DEBTRDCB)  CLOSE DCB                       GP98222
DBTEXITZ LM    R2,R15,DBTSVLOC  RESTORE BASE AND RETURN         GP99043
         BSM   0,R14         AND RETURN TO USER                 GP99029
         SPACE 2
***********************************************************************
**                                                                   **
**  PRTDEB :  SUBROUTINE TO WRITE THE CURRENT OUTPUT RECORD          **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
*  PRTDEB - PROCESS ONE PRINT LINE; TEXT ADDR IN R1             GP99113
*    CONTROL CHARACTERS, IF NEEDED, SET TO SINGLE SPACE         GP99113
*    RETURNS ON R14                                             GP99113
         PUSH  USING                                            GP99113
PRTDEB   STM   R14,R5,PRTDEBSV  SAVE REGISTERS                  GP99113
         L     R4,DBT@UDCB   LOAD DCB                           GP99113
         USING IHADCB,R4                                        GP99113
         LR    R2,R1         PRESERVE RECORD ADDRESS            GP99113
         LH    R3,0(,R2)     LOAD LENGTH                        GP99113
         SR    R15,R15                                          GP99113
         ICM   R15,3,MAXOUTLN  GET MAXIMUM TEXT LENGTH          GP99113
         BNZ   PRTNFRST                                         GP99113
         SPACE 1                                                GP99113
         TM    DCBRECFM,DCBRECCC  ANY CONTROL CHARACTERS?       GP99113
         BZ    PRT1NCC                                          GP99113
         MVI   CCINDEX,4     SET FOR CC                         GP99113
         TM    DCBRECFM,DCBRECCA  ASA ?                         GP99113
         BNZ   PRT1NCC                                          GP99113
         MVI   CCCHAR,X'09'  MACHINE CODE - SINGLE SPACE        GP99113
PRT1NCC  LH    R15,DCBLRECL  GET MAXIMUM RECORD LENGTH          GP99113
         STH   R15,MAXOUTLN  SAVE IT                            GP99113
         TM    DCBRECFM,DCBRECU  RECFM=U ?                      GP99113
         BO    PRT1RECU      YES; PROCESS                       GP99113
         BM    PRTQRECF      MIXED; F OR V                      GP99113
         TM    DCBRECFM,DCBRECD  RECFM=D?                       GP99113
         BNZ   PRT1RECV      YES; TREAT AS V                    GP99113
PRT1RECU MVC   MAXOUTLN,DCBBLKSI  RECFM=U USES REC=BLK          GP99113
         B     PRT1RECF      SET FLAF FOR F                     GP99113
PRTQRECF TM    DCBRECFM,DCBRECF  IS IT FIXED?                   GP99113
         BNZ   PRT1RECF      YES                                GP99113
PRT1RECV MVI   RFINDEX,4     BRANCH ON V/D                      GP99113
         B     PRTNFRST                                         GP99113
PRT1RECF MVI   RFINDEX,0     BRANCH ON F/U                      GP99113
         SPACE 1                                                GP99113
PRTNFRST MVC   MSGRDW+4(1),CCCHAR  SET APPROPRIATE CARRIAGE CONTROL
         TM    DCBMACF2,DCBMRLCP  PUT LOCATE MODE?              GP99119
         BNZ   PRTLOC                                           GP99113
         SR    R15,R15                                          GP99113
         ICM   R15,1,RFINDEX   SET RDW LENGTH OR 0              GP99113
         BNZ   PRTMV         RECFM=V OR D                       GP99113
         ICM   R5,1,CCINDEX  CARRIAGE CONTROL INDEX ?           GP99113
         BNZ   *+4+4         YES; REMOVE ONLY RDW               GP99113
         LA    R15,5(,R15)   ELSE REMOVE CC AND RDW             GP99113
         AR    R2,R15        ADJUST ADDRESS                     GP99113
         B     PRTMPUT                                          GP99113
PRTMV    SR    R1,R1                                            GP99113
         ICM   R1,12,0(R2)   GET RDW LENGTH                     GP99113
         MIN   R3,MAXOUTLN,TYPE=H  GET SMALLER OF RECORD AND LRECL
         CLI   CCINDEX,0     CC?                                GP99113
         BH    PRTMVAR                                          GP99113
         SL    R1,=X'00010000'  ADJUST FOR LACK OF CC           GP99113
         BCTR  R3,0          ADJUST LENGTH                      GP99113
         LA    R2,1(,R2)     COMPENSATE FOR CC                  GP99113
PRTMVAR  STCM  R1,15,0(R2)   UPDATE RDW LENGTH AND FLAGS        GP99113
PRTMPUT  PUT   (R4),(R2)     PRINT IT                           GP99113
         B     PRINTSEX      GO TO EXIT                         GP99113
         SPACE 1                                                GP99113
PRTLOC   SR    R15,R15                                          GP99113
         ICM   R15,1,RFINDEX  TEST F OR V                       GP99113
         BZ    PRTLPUT       FIXED                              GP99113
         CLI   CCINDEX,0     CC NEEDED?                         GP99113
         BH    PRTLCC                                           GP99113
         BCTR  R3,0          ELSE DECREASE CURRENT LENGTH       GP99113
PRTLCC   MIN   R3,MAXOUTLN,TYPE=H  GET SMALLER OF RECORD AND LRECL
         STH   R3,DCBLRECL                                      GP99113
PRTLPUT  PUT   (R4)          GET A RECORD AREA                  GP99113
         LR    R14,R1        SAVE THE RECORD ADDRESS            GP99113
         LH    R15,DCBLRECL  GET THE EXPECTED OUTPUT SIZE       GP99113
         ICM   R5,1,RFINDEX  F OR V?                            GP99113
         BNZ   PRTLMOVE                                         GP99113
         SR    R5,R5                                            GP99113
         ICM   R5,1,CCINDEX  CARRIAGE CONTROL INDEX ?           GP99113
         BNZ   *+4+4         YES; REMOVE ONLY RDW               GP99113
         LA    R5,5(,R5)     ELSE REMOVE CC ALSO                GP99113
         AR    R2,R5                                            GP99113
         SR    R3,R5         ADJUST ADDRESS AND LENGTH          GP99113
         ICM   R3,8,=C' '    SET PAD                            GP99113
         B     PRTLMVCL                                         GP99113
PRTLMOVE STCM  R3,3,0(2)     SET LENGTH                         GP99113
         STCM  R3,12,2(R2)   CLEAR FLAGS                        GP99113
         CLI   CCINDEX,0                                        GP99113
         BH    PRTLMVCL      MOVE RECORD WITH CC                GP99113
         LA    R5,5          ADJUSTMENT FOR RDW + CC            GP99113
         AR    R2,R5                                            GP99113
         SR    R3,R5                                            GP99113
         STCM  R3,3,0(R14)    SET LENGTH                        GP99113
         STCM  R3,12,2(R14)  CLEAR FLAGS                        GP99113
         SH    R15,=H'4'                                        GP99113
         LA    R14,4(,R14)   SKIP RDW                           GP99113
PRTLMVCL MVCL  R14,R2        MOVE DATA TO LOCATED RECORD AREA   GP99113
PRINTSEX LM    R14,R5,PRTDEBSV  RESTORE                         GP99113
         BSM   0,R14         RETURN IN CALLER'S ADDRESSING MODE GP99113
         SPACE 1                                                GP96109
MAXOUTLN DC    H'0' (0 IS FG) MAXIMUM OUTPUT LENGTH, INCL. RDW  GP99113
RFINDEX  DC    X'0'          INDEX FOR RECFM F/U VS V/D         GP99113
CCINDEX  DC    X'0'          INDEX FOR CARRIAGE CONTROL         GP99113
CCCHAR   DC    C' '          ASA SINGLE SPACE                   GP99113
PROFLAGS DC    X'00'         LOCAL PROCESSING FLAGS             GP99113
PFWIDE   EQU   X'80'           PROCESSING WIDE LINES            GP99113
PFDUAL   EQU   X'40'           SHOW HEX + TEXT                  GP00162
PFCANC   EQU   X'01'           GLOBAL CANCEL OF FUNCTION        GP00162
PFGBL    EQU   (PFCANC)        FLAGS RETAINED GLOBALLY          GP99113
         POP   USING                                            GP99113
         SPACE 2
DB       DC    D'0'          WORK SPACE
SUBSAVE  DC    18A(0)         SAVE AREA                         GP95235
DBTSVLOC DC    14A(0)        SAVE CRITICAL REGISTERS            GP99043
REGWORK  DC    16A(0)        WORK SPACE FOR REG OPTION          GP96101
PRTDEBSV DC    8A(0)         WORK SPACE FOR PRINTING
ADDRMASK DC    X'7FFFFFFF'   ADDRESS MODE MASK                  GP99116
         ENTRY DEBTRDCB      DECLARE EXTERNAL ACCESSIBILITY
DEBTRDCB OPEN  (DEBDCB,OUTPUT),MF=L
DEBDCB   DCB   DDNAME=DEBTRACE,DSORG=PS,MACRF=PM,RECFM=FB,            **
               LRECL=MSGRTDS2-MSGHEAD                           GP99113
CALLPRMS DC    0A(0)   1/2   COPY OF CALLER'S R0 + R1           GP95235
CALLR0   DC    A(0)    1/2   COPY OF CALLER'S R0                GP95235
CALLR1   DC    A(0)    2/2   COPY OF CALLER'S R1                GP95235
DBTHEXTB DC    C'0123456789ABCDEF'   1/2                        GP97265
DBTHEXTR EQU   DBTHEXTB-C'0'         2/2                        GP97265
PATRDW   WTO   ' MSG666 ',ROUTCDE=11,DESC=8,MF=L                GP99113
*OLD*    ORG   PATRDW                                           GP99113
MSGRDW   DC    Y(MSGRTDS-*),X'8000'   WTO HEADER                GP96081
MSGMAX   DC    CL133' '      ASSURE REQUIRED SPACE              GP99113
         ORG   MSGMAX        REDEFINE                           GP99113
MSGRTDSC DC    C' '          CARRIAGE CONTROL                   GP99113
MSGHEAD  DC    C'MSG666 '    DEBUG HEADER                        93357
MSGLABL  DC    CL8' ',C' '   USER'S LABEL                       GP97265
MSGFLAG  DC    CL2' ',C' '   ANNOTATION/ERROR MSGS              GP97265
MSGTEXT  DC    CL36' '       USER'S HEX OR EBCDIC TEXT          GP97265
         DC    C' '          EXTRA FOR UNPACK                    94011
MSG@TCB  DC    CL8' ',C' '   CURRENT TCB ADDRESS+PADDING FOR UNPK
MSGRTDS  DC    X'01000020'   DESCRIPTOR 8; ROUTCDE 11
         ORG   MSG@TCB         REDEFINE FOR WIDE OPTION         GP99113
         DC    C' '          GROUP SEPARATOR                    GP99113
MSGTEXT2 DC    CL36' '       ANOTHER 4 HEX WORDS                GP99113
         DC    C' '          EXTRA FOR UNPACK                    94011
MSG@TCB2 DC    CL8' ',C' '   CURRENT TCB ADDRESS+PADDING FOR UNPK
         ORG   MSG@TCB2      RE-USE FOR WIDE DUAL OPTION        GP00162
         DC    C' '          LEFT FRAME *                       GP00162
MSGHTEX  DC    CL32' '       TEXT FOR HEX DUAL OPTION           GP00162
         DC    C' '          RIGHT FRAME *                      GP00162
         ORG   ,                                                GP99113
MSGRTDS2 DC    X'01000020'   DESCRIPTOR 8; ROUTCDE 11           GP99113
         SPACE 1
PATCHAR  DC    30S(*)        MINI-PATCH                         GP95235
         SPACE 1                                                GP96101
*   THIS TABLE DEFINES ALL PN/P11 CHARACTERS PLUS LOWER CASE    GP99113
*                                                               GP96101
TRTCHAR  TRTAB CODE=TN,FILL=X'FF'  0 FOR VALID CHARS            GP00162
TRTABTN  TRTAB CODE=TN,FILL=X'40'  VALID CHARS                  GP00162
         SPACE 1                                                GP95235
REQWORK  DSECT ,             INPUT REQUEST LIST MAPPING         GP95235
REQTYPE  DS    X             REQUEST TYPE                       GP95235
RTREGS   EQU   0               REGISTER LISTING                 GP96101
RTTEXT   EQU   1               TEXT STRING                      GP95235
RTCHEX   EQU   2               PRINTABLE TEXT, ELSE HEX         GP96101
RTHEX    EQU   3               HEXADECIMAL                      GP95235
RTPACK   EQU   4               PACKED DECIMAL                   GP96081
RTVAR    EQU   8               VARIABLE NAME (+ DATA REQUEST)   GP95235
RQTEOL   EQU   X'80'           END OF LIST FLAG                 GP02364
RQTMSK   EQU   X'7F'           END OF LIST MASK                 GP02364
REQFG    DC    AL1(0)        OPTION FLAGS                       GP95235
RFIAD    EQU   1               INDIRECT ADDRESS SUPPLIED        GP96101
RFA24    EQU   2               FORCE 24-BIT MODE ADDRESSING     GP96101
RFA31    EQU   4               FORCE 31-BIT ADDRESSING MODE     GP96101
REQNAME  DS    0CL8          VARIABLE NAME FOR RTVAR TYPE       GP95235
REQADDR  DS    S             ADDRESS OF VARIABLE                GP95235
REQLEN   DS    Y             LENGTH OF VARIABLE                 GP95235
REQSIZE  EQU   *-REQWORK     LENGTH OF ONE ENTRY (EXC VARIABLE AND TAG)
         SPACE 1                                                GP95235
DBTWORK  DSECT ,                                                GP95235
         DEBTRACE MODE=D,CALL=TRC   EXPAND THE COMMON  AREA     GP99113
@PRINTER EQU   DBT@UDCB,4,C'A'  REDEFINED FOR PRTV MACRO        GP99113
         PRINT NOGEN                                            GP95235
         YREGS ,                                                GP95235
         IHAPSA ,                                               GP95235
         DCBD  DEVD=DA,DSORG=PS                                 GP95235
         IEFUCBOB ,                                             GP99113
         END   ,
