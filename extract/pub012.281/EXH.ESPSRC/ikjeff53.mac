EFF53    TITLE 'I K J E F F 5 3  ***  OUTPUT, STATUS, CANCEL EXIT'
         COPY  OPTIONGB      ESTABLISH GLOBAL DEFINITIONS        90337
         SPACE 1                                                 90337
         SYSPARM LIST=YES    SET GLOBAL OPTIONS                  90337
         SPACE 1                                                 90337
*        IKJEFF53 IS THE INSTALLATION EXIT FOR VERIFYING OUTPUT, 90337
*           STATUS AND CANCEL COMMANDS.  THIS CODE WAS ADAPTED FROM
*           IBM'S IPO SAMPLIB AT THE SP 1.3.5 LEVEL (NO COPYRIGHT).
*                                                                90337
*         STATUS:  IF JOBNAME IS NOT 'TSO' RETURN TO                  *
*         CALLER SINCE ANY JOBNAME WILL BE ALLOWED.                   *
*         IF JOBNAME IS 'TSO' PUT OUT LIST OF USERIDS                 *
*         WITH ADDRESS SPACE ID AND TERMINAL ADDRESS                  *
*         OR SYMBOLIC TERMINAL NAME (TSO/VTAM).                       *
*         RETURN TO CALLER WITH RETURN CODE OF 12 TO                  *
*         SKIP JOBNAME SEARCH FOR 'TSO'.                              *
*                                                                     *
*         OUTPUT:  FIRST CHECK IF THE USER HAS OPERATOR               *
*         AUTHORITY.  IF YES, ALLOW THE USER TO OUTPUT                *
*         ANY JOBNAME.  IF THE USER DOES NOT HAVE OPERATOR            *
*         AUTHORITY COMPARE THE USERID WITH THE JOBNAME               *
*         PASSED.  IF THE JOBNAME IS NOT THE USERID OR                *
*         DOES NOT START WITH THE USERID, THE JOBNAME                 *
*         IS REJECTED BY FIRST RETURNING TO THE CALLER                *
*         AN ERROR MESSAGE AND A RETURN CODE REQUESTING               *
*         THAT THE MESSAGE BE ISSUED VIA THE PUTLINE                  *
*         MECHANISM.  WHEN REENTERED FOR THE JOBNAME,                 *
*         THE EXIT ISSUES A RETURN CODE REJECTING THE                 *
*         JOBNAME.                                                    *
*                                                                     *
*         CANCEL:  FIRST CHECK IF THE USER HAS OPERATOR               *
*         AUTHORITY.  IF YES, ALLOW THE USER TO CANCEL                *
*         ANY JOBNAME (THE MVS SYSTEM WILL PREVENT THE                *
*         USER FROM CANCELLING STARTED TASKS AND TSO                  *
*         SESSIONS).  IF THE USER DOES NOT HAVE OPERATOR              *
*         AUTHORITY COMPARE THE USERID WITH THE JOBNAME               *
*         PASSED.  IF THE JOBNAME IS NOT THE USERID PLUS              *
*         AT LEAST ONE CHARACTER, THE JOBNAME IS REJECTED             *
*         BY FIRST RETURNING TO THE CALLER AN ERROR                   *
*         MESSAGE AND A RETURN CODE REQUESTING THAT THE               *
*         THAT THE MESSAGE BE ISSUED VIA THE PUTLINE                  *
*         MECHANISM.  WHEN REENTERED FOR THE JOBNAME,                 *
*         THE EXIT ISSUES A RETURN CODE REJECTING THE                 *
*         JOBNAME.                                                    *
*                                                                     *
*                ATTRIBUTES = PROTECT KEY 1, REENTRANT,               *
*                                                                     *
*                                                                     *
*             EXIT - ERROR = AT PROGRAM END VIA BRANCH REGISTER 14    *
*                                                                     *
*                OUTPUT = MESSAGEP SET IN THE INPUT PARAMETER LIST.   *
*                   IF RETURN IS FROM STATUS FOR JOBNAME 'TSO' THEN   *
*                   RETURN CODE IS 12 AND NO MESSAGE WILL BE ISSUED.  *
*                                                                     *
*                RETURN CODE =                                        *
*                   4  - ISSUE PROMPT AND RETURN REPLY   (IEPROMPT)   *
*                   8  - ISSUE MESSAGE AND RETURN        (IEMSG)      *
*                   12 - REJECT THIS JOB                 (IEREJECT)   *
*                   16 - TERMINATE THIS COMMAND          (IEABORT)    *
*                                                                     *
***********************************************************************
         EJECT
         PRINT &PRTSOR                                           90337
IKJEFF53 SAVEM ZERO12,BASE=R12,PARM=R11                          90337
         USING IEPARML,R11   PARAMETER LIST ADDRESS              90337
         SLR   R5,R5         SET DEFAULT (GOOD) RETURN CODE      90337
         SPACE 1
***********************************************************************
*                                                                     *
*        ALWAYS DO PROCESSING                                         *
*                                                                     *
***********************************************************************
         SPACE 1
         L     R6,IECODEP              LOAD COMMAND CODE POINTER
         CLI   0(R6),IECANCEL          SEE IF COMMAND IS CANCEL
         BE    PROCESS                 YES....CONTINUE PROCESSING
         CLI   0(R6),IEOUTPUT          SEE IF COMMAND IS OUTPUT
         BE    PROCESS                 YES....CONTINUE PROCESSING
         CLI   0(R6),IESTATUS          SEE IF COMMAND IS STATUS
         BNE   ENDPROC                 NO.....DO NO PROCESSING
         SPACE 1
***********************************************************************
*                                                                     *
*        SEE IF SECOND TIME ENTRY FOR A JOB -                         *
*           MESSAGE POINTER NOT ZERO                                  *
*                                                                     *
***********************************************************************
         SPACE 1
PROCESS  ICM   R1,15,IEMSGP  RE-ENTRY FOR MESSAGE DELETION ?     90337
         BNZ   FREEMESS                YES....GO FREE MESSAGE BUFFER
         L     R6,IECODEP    GET REQUEST POINTER                 90337
         CLI   0(R6),IESTATUS  STATUS REQUEST ?                  90337
         BE    STATUS        YES; PROCESS DIFFERENTLY            90337
         EXTRACT PSCBADDR,'S',FIELDS=PSB,MF=(E,EXTLIST)
         L     R7,PSCBADDR   LOAD POINTER TO PSCB                90337
         USING PSCB,R7                                           90337
         TM    PSCBATR1,PSCBCTRL   OPERATOR AUTHORITY ?          90337
         BO    ENDPROC       YES; ALLOW THIS REQUEST
         SERVICE ACGET,MYACCT   GET THE CURRENT USER'S ACCOUNT   90337
         STC   R0,MYPRIV     ALSO SAVE ACCOUNT PRIVILEGES        90337
         TM    MYPRIV,VAASYS+VAASUP  SYSTEMS OR TECH.SUPPORT ?   90337
         BNZ   ENDPROC       YES; ALLOW THIS REQUEST             90337
         TM    PSCBATR1,PSCBJCL  ALLOWED JOB MANIPULATION ?      90337
         BNZ   HAVEJCL       YES; PROCEED                        90337
         DROP  R7                                                90337
         L     R0,GETMINFO   SUBPOOL 0, LENGTH DECIMAL 84        90337
         GETMAIN R,LV=(0)    GET MESSAGE BUFFER                  90337
         ST    R1,IEMSGP     STORE POINTER TO MESSAGE BUFFER     90337
         MVC   REJMSG-REJMSG(MSGUNALN,R1),MSGUNAUT  UNAUTHORIZED 90337
         LA    R5,IEMSG      INDICATE MESSAGE EXIT/RETURN        90337
         B     ENDPROC       REJECT AFTER MESSAGE                90337
         SPACE 1                                                 90337
HAVEJCL  L     R4,IEIDLENP   GET USER'S NAME POINTER             90337
         SLR   R2,R2                                             90337
         ICM   R2,1,0(R4)    LOAD AND TEST NAME LENGTH           90337
         BZ    NOTSESS       NOT TIME-SHARING SESSION; TRY BATCH 90337
         SLR   R14,R14                 ZERO REGISTER 14
         L     R4,IENAMELP             LOAD JOBNAME LENGTH POINTER  CUJ
         SPACE 1
***********************************************************************
*                                                                     *
*        DETERMINE IF THIS IS FOR CANCEL                              *
*           NO.....GO TO OUTPUT ROUTINE                               *
*           YES....IF USER HAS OPERATOR AUTHORITY THEN O.K.           *
*              ELSE CHECK IF JOBNAME IS VALID FOR CANCEL              *
*              CANCEL NEEDS JOBNAME EQUAL TO USERID                   *
*              PLUS AT LEAST ONE CHARACTER                            *
*                                                                     *
***********************************************************************
         SPACE 1
         CLI   0(R6),IECANCEL  IS THIS CANCEL ?                  90337
         BNE   OUTPUT        NO; CHECK OUTPUT REQUEST            90337
         CH    R2,0(,R4)     SEE IF USERID LENGTH IS SUFFICIENT  90337
         BNL   NOTSESS       NO; SEE IF ACCESS VALID             90337
         L     R6,IEUSRIDP             LOAD USERID POINTER          CUJ
         L     R1,IEJOBNMP             LOAD JOBNAME POINTER         CUJ
         BCTR  R2,0          DECREMENT LENGTH                    90337
         EX    R2,JOBNCOMP    EXECUTE OF COMPARE                 90337
         BNE   NOTSESS       NOT MATCHED - CHECK ACCESS AUTHORITY
         B     ENDPROC                 GO ACCEPT JOBNAME
         SPACE 1
***********************************************************************
*                                                                     *
*        SEE IF THIS IS OUTPUT COMMAND                                *
*           NO.....GO TO STATUS ROUTINE                               *
*           YES....IF USER HAS OPERATOR AUTHORITY THEN O.K.           *
*              ELSE CHECK IF JOBNAME IS VALID FOR OUTPUT              *
*              OUTPUT NEEDS JOBNAME EQUAL TO USERID OR                *
*              EQUAL TO USERID PLUS AT LEAST ONE CHARACTER            *
*                                                                     *
***********************************************************************
         SPACE 1
OUTPUT   CH    R2,0(,R4)     SEE IF USERID LENGTH                92308
         BH    NOTSESS         NO; CHECK ACCESS AUTHORITY        90337
         L     R6,IEUSRIDP             LOAD USERID POINTER          CUJ
         L     R1,IEJOBNMP             LOAD JOBNAME POINTER         CUJ
         BCTR  R2,0          DECREMENT LENGTH                    90337
         EX    R2,JOBNCOMP    EXECUTE OF COMPARE                 90337
         BE    ENDPROC                 EQUAL..RETURN TO CALLER
         SPACE 1                                                 90337
***********************************************************************
*                                                                     *
*        USER IS RUNNING IN BATCH MODE, OR DOES NOT HAVE A USERID     *
*        MATCHING THE JOB NAME.                                       *
*        IF A LOCAL ON-LINE USER DATA ELEMENT EXISTS (TS ONLY), THEN  *
*        CHECK THE REQUESTED JOB'S ACCOUNT AND SUBACCOUNT AGAINST     *
*        THOSE THE USER IS ALLOWED ACCESS TO IN A$USER RECORD.        *
*          THE A$LOUD AND A$USER BLOCKS ARE BUILT AND VERIFIED IN     *
*        THE LOCAL PRE-LOGON EXIT LEXLOGON.                           *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90337
NOTSESS  XC    COPYJES(DB-COPYJES),COPYJES  CLEAR CALL PARM LIST 90337
         SERVINIT ,          INITIALIZE THE SERVICE ROUTINE      90337
         MVC   SJ1JNAM,BLANKS  MAKE ALL BLANK                    90337
         L     R1,IENAMELP   GET JOBNAME LENGTH                  90337
         LH    R15,0(,R1)    LOAD JOBNAME LENGTH                 90337
         BCTR  R15,0         LESS ONE FOR EXECUTE                90337
         L     R1,IEJOBNMP   GET JOBNAME ADDRESS                 90337
         LA    R6,SJ1JNAM                                        90337
         EX    R15,MOVEATE   MAKE 8-BYTE NAME                    90337
         ICM   R2,15,IEJIDLNP  IS THERE A JOB ID LENGTH POINTER ?
         BZ    NOTSID        NO                                  90337
         LH    R2,0(,R2)     LOAD JOB ID LENGTH                  90337
         LTR   R2,R2         ANY JOB ID ?                        90337
         BNP   NOTSID        NO                                  90337
         L     R1,IEJOBIDP   GET JOB ID                          90337
         SLR   R14,R14                                           90337
         LA    R15,C'0'                                          90337
MAKESID  CLI   0(R1),C'0'    NUMERIC ?                           90337
         BL    MAKESIDX      NO                                  90337
         SLDL  R14,8         ADJUST                              90337
         IC    R15,0(,R1)    COPY ONE DIGIT                      90337
MAKESIDX LA    R1,1(,R1)     NEXT BYTE                           90337
         BCT   R2,MAKESID                                        90337
         STM   R14,R15,PACK  STASH                               90337
         PACK  DB,PACK       MAKE PACKED                         90337
         CVB   R0,DB         GET JOB NUMBER                      90337
         LTR   R0,R0         VALID ?                             90337
         BNP   NOTSID                                            90337
         STH   R0,SJ1J###    ADD TO REQUEST                      90337
NOTSID   SERVCALL LOJOB,COPYJES,MODE=SYNCH   FIND JOB            90337
         BXLE  R15,R15,HAVEJES  HAVE JES ACCOUNT                 90337
         CH    R15,=Y(2*8)   JOB NOT FOUND ?                     90337
         BNE   BADJOBN       NO; ACCESS VIOLATION                90337
         B     ENDPROC       LET TSO ISSUE ERROR MESSAGE         90337
         SPACE 1                                                 90337
HAVEJES  CLC   MYACCT,SJ1ACCT  MATCHING ACCOUNT ?                90337
         BE    ENDPROC       YES; ALLOW                          90337
         L     R8,PSAAOLD-PSA    GET MY ASCB                     90337
         USING ASCB,R8                                           90337
         ICM   R8,15,ASCBTSB GET THE TSB                         90337
         BZ    BADJOBN       TOO BAD                             90337
         USING TSB,R8                                            90337
         XC    A$SVCRB(A$SLENRB),A$SVCRB  CLEAR                  90337
         MVC   A$SID,=C'A$RB'  IDENTIFY                          90337
         MVC   A$SFCTN(3),=AL1(A$SFLOUD,A$STSO,A$SUKEY+A$SBUILD) 90337
         MODESET EXTKEY=ZERO,SAVEKEY=(2)  SWAP KEYS              90337
         MVC   A$SACCT,TSBTRMID  SET TERMINAL ID                 90337
         MODESET KEYADDR=(2) RESTORE KEY (1)                     90337
         DROP  R8                                                90337
         MVC   A$SUID,BLANKS                                     90337
         LA    R6,A$SUID                                         90337
         L     R1,IEUSRIDP                                       90337
         L     R2,IEIDLENP                                       90337
         SLR   R14,R14                                           90337
         IC    R14,0(,R2)    GET NAME LENGTH                     90337
         BCTR  R14,0         FOR EXECUTE                         90337
         EX    R14,MOVEATE   MOVE NAME                           90337
         ACCTSVC A$SVCRB,ERR=BADJOBN  IF SVC NOT AVAILABLE       90337
         BXH   R15,R15,BADJOBN  TOO BAD                          90337
         ICM   R4,15,A$SBUFR  GET LOUD ADDRESS                   90337
         BZ    BADJOBN       TOO BAD                             90337
         USING A$LOUD,R4                                         90337
         CLC   A$UACCT(4),SJ1ACCT  SAME MAJOR ACCOUNT ?          90337
         BNE   VALUEXAC      NO; CHECK OTHER ACCOUNTS            90337
         CLC   A$UACCT+4(4),SJ1ACCT+4  SAME SUBACCOUNT ?         90337
         BE    ENDPROC       YES; REWRITE THE RECORD             90337
         TM    A$UPRIVY,A$UPFSUB  MAY USE DIFFERENT SUBACCOUNT ? 90337
         BNZ   ENDPROC       YES; UPDATE THE RECORD              90337
VALUEXAC LA    R6,A$UACACC  POINT TO EXTRA ACCOUNTS              90337
         LA    R7,(A$UPROUT-A$UACACC)/(L'A$UACCFG+L'A$UACCNO)    90337
VALUEXLP LA    R14,A$UACCNO-A$UACACC(,R6)  POINT TO FULL ACCOUNT 90337
         TM    A$UACCFG-A$UACACC(R6),A$UAFJCL  JCL PRIVILEGES ?  90337
         BZ    VALUEXLB      NO; SKIP THIS ENTRY                 90337
         CLI   0(R14),0      EMPTY SLOT ?                        90337
         BE    VALUEXLB      YES; SKIP                           90337
         CLI   4(R14),0      HEX ZERO (BAD DEFINITION) ?         90337
         BE    VALUECL4      YES; DO SHORT COMPARE               90337
         TM    A$UACCFG-A$UACACC(R6),A$UAFSUB  SUBACCOUNT ?      90337
         BZ    VALUECLC      YES; UPDATE THE RECORD              90337
VALUECL4 LA    R0,4          COMPARE ACCOUNT ONLY                90337
VALUECLC LA    R1,SJ1ACCT    POINT TO JOB'S ACCOUNT              90337
         LA    R0,8          SET LENGTH OF ACCOUNT               90337
         BAL   R9,CLCMASK    COMPARE MASKED ACCOUNT              90337
         BE    ENDPROC       MATCH - ACCEPT                      90337
VALUEXLB LA    R6,L'A$UACCFG+L'A$UACCNO(,R6)                     90337
         BCT   R7,VALUEXLP   TRY AGAIN                           90337
         SPACE 1
***********************************************************************
*                                                                     *
*        REJECT THIS INVALID JOBNAME AND BUILD INVALID JOBNAME        *
*           MESSAGE                                                   *
*                                                                     *
***********************************************************************
         SPACE 1
BADJOBN  DS    0H                      BUILD BAD JOBNAME MESSAGE
         L     R0,GETMINFO             SUBPOOL 0, LENGTH DECIMAL 84
         GETMAIN R,LV=(0)              GET MESSAGE BUFFER
         ST    R1,IEMSGP               STORE POINTER TO MESSAGE BUFFER
*                                      INTO PARAMETER LIST
         LR    R10,R1                  LOAD REGISTER 10 WITH ADDRESS OF
*                                      GETMAINED AREA
         USING REJMSG,R10              ESTABLISH REGISTER 10 AS BASE
*                                      FOR REJECT MESSAGE DSECT
         MVI   REJSLOT-1,BLANK  INITIALIZE BUFFER WITH BLANKS    90337
         MVC   REJSLOT,REJSLOT-1   MAKE ALL BLANKS               90337
         MVC   REJMLEN(2),=Y(REJSIZE)  PUT IN MAXIMUM MESSAGE LENGTH
         MVC   REJJOB(3),=C'JOB'       PUT IN WORD 'JOB'         90337
         L     R1,IENAMELP             LOAD JOBNAME LENGTH POINTER
         LH    R15,0(,R1)              LOAD JOBNAME LENGTH
         LR    R14,R15                 LOAD REGISTER 14 WITH LENGTH
         BCTR  R14,0                   DECREMENT REGISTER 14 FOR MOVE
         L     R1,IEJOBNMP             LOAD JOBNAME POINTER
         EX    R14,MOVEJOBN            INSERT THE BAD JOBNAME
         LA    R10,1(R10,R15)   INDEX PAST BLANK AFTER JOBNAME   90337
         L     R1,IECODEP              LOAD COMMAND POINTER
         LA    R5,IEMSG                SET RETURN CODE TO PROMPT
         CLI   0(R1),IECANCEL          SEE IF COMMAND IS CANCEL
         BNE   OUTPMESS                NO.....GO TO OUTPUT MESSAGE
         MVC   REJSLOT(66),REJTEXT1  MOVE IN TEXT FOR CANCEL     90337
         B     ENDPROC       RETURN WITH MESSAGE                 90337
OUTPMESS MVC   REJSLOT(69),REJTEXT2  MOVE IN TEXT FOR OUTPUT     90337
         B     ENDPROC                 GO TO RETURN
         SPACE 1
***********************************************************************
*                                                                     *
*        SEE IF THIS IS STATUS COMMAND                                *
*           NO.....GO TO END OF PROCEDURE                             *
*           YES....CHECK IF NAME IS 'TSO'                             *
*              NO.....GO TO END OF PROCEDURE                          *
*              YES....PUT OUT USERIDS, ASIDS, AND ADDRESSES           *
*                                                                     *
***********************************************************************
         SPACE 1
STATUS   L     R1,IENAMELP             LOAD POINTER TO JOBNAME LENGTH
         CLC   =H'3',0(R1)   SEE IF JOBNAME LENGTH IS 3          90337
         BNE   ENDPROC                 NO.....GO TO END OF PROCEDURE
         L     R1,IEJOBNMP             LOAD POINTER TO JOBNAME   90337
         CLC   =CL3'TSO',0(R1)  SEE IF JOBNAME IS 'TSO'          90337
         BNE   ENDPROC                 NO.....GO TO END OF PROCEDURE
         L     R6,CVTPTR     LOAD POINTER TO CVT
         USING CVTMAP,R6     ESTABLISH ADDRESSABILITY TO CVT     90337
         L     R6,CVTASVT              LOAD POINTER TO ASVT
         USING ASVT,R6                                           90337
         L     R5,ASVTMAXU   MAXIMUM NUMBER OF ADDRESS SPACES    90337
         LA    R6,ASVTENTY   LOAD ADDRESS OF FIRST ASVT ENTRY    90337
         DROP  R6                                                90337
         SLR   R3,R3         ZERO OUT USER COUNTER               90337
         TPUT  HEADER,L'HEADER         PUT OUT HEADER LINE
         SPACE 1
***********************************************************************
*                                                                     *
*        GO DOWN THE ADDRESS SPACE VECTOR TABLE                       *
*           SEE IF ADDRESS SPACE CONTROL BLOCK IS FOR A TSO USER      *
*              YES....GET ADDRESS SPACE ID, USERID, AND PHYSICAL      *
*                     TERMINAL ADDRESS AND OUTPUT LINE                *
*              NO.....GO CHECK NEXT ASCB                              *
*                                                                     *
***********************************************************************
         SPACE 1
LOOPASCB ICM   R7,15,0(R6)   GET ASCB FROM POINTER               90337
         BNP   BUMPASCB      INACTIVE; SKIP                      90337
         USING ASCB,R7                 ESTABLISH ADDRESSABILITY TO ASCB
         CLC   ASCBASCB(4),ASCBWORD    ENSURE CONTROL BLOCK IS VALID
         BNE   LOOPASCB      NO.....GO ON TO NEXT ASVT ENTRY     90337
         ICM   R8,15,ASCBTSB  LOAD ADDRESS OF TSB                90337
         BZ    BUMPASCB                                          90337
         USING TSB,R8                  ESTABLISH ADDRESSABILITY TO TSB
         MVI   LINE,C' '     BLANK OUT THE OUTPUT LINE           90337
         MVC   LINE+1(L'LINE-1),LINE
         MODESET EXTKEY=ZERO,SAVEKEY=(2)  MODESET AND SAVE OLD KEY
         TM    TSBFLG5,TSBVTAM         SEE IF THIS IS A TSO/VTAM TSB
         BO    GETTSBX                 YES....GO GET TSB EXTENSION
         LH    R8,TSBLINE              GET BINARY LINE ADDRESS
         STH   R8,PACK                 STORE ADDRESS IN WORKAREA
         UNPK  PACK+3(5),PACK(3)       UNPACK THE ADDRESS
         MVC   LINEADDR,PACK+4         MOVE UNPACKED ADDRESS INTO LINE
         TR    LINEADDR,TRT-240        TRANSLATE TO EBCDIC
         B     MODEREST                GO MODESET AND RESTORE OLD KEY
GETTSBX  DS    0H
         L     R9,TSBEXTNT             GET ADDRESS OF TSB EXTENSION
         USING TSBX,R9                 ESTABLISH ADDRESSABILITY TO TSBX
         TM    TSBXFLG1,TSBXWREC       SEE IF TERMINAL AWAITING RECON
         BNO   GETSYM                  NO.....GO GET SYMBOLIC NAME
         MVC   LINESYM(8),DISCON       MOVE IN DISCON'D
         B     MODEREST                GO MODESET AND RESTORE OLD KEY
GETSYM   DS    0H
         MVC   LINESYM(8),TSBTRMID     MOVE IN TERMINAL SYMBOLIC NAME
         DROP  R8,R9
MODEREST DS    0H
         MODESET KEYADDR=(2)           MODESET AND RESTORE OLD KEY
         ICM   R8,15,ASCBJBNS   LOAD POINTER TO JOBNAME FIELD    90337
         BZ    STARTING      MOVE IN 'STARTING'                  90337
         MVC   LINEUSID,0(R8)          MOVE USERID TO PRINT LINE
         B     EDITASID                GO EDIT ASID TO EBCDIC
STARTING MVC   LINEUSID,STRTWORD       MOVE 'STARTING' TO PRINT LINE
EDITASID DS    0H
         LH    R8,ASCBASID             LOAD ADDRESS SPACE ID
         CVD   R8,PACK                 CONVERT TO DECIMAL
         MVC   CHAR,EDMSK              MOVE EDIT MASK TO CHAR
         ED    CHAR,PACK+4             EDIT TO ASID TO EBCDIC
         MVC   LINEASID,CHAR+5         MOVE THREE CHARS TO PRINT LINE
         LA    R1,LINE                 LOAD POINTER TO LINE
         TPUT  (R1),L'LINE             PUT OUT OUTPUT LINE
         LA    R3,1(,R3)               ADD 1 TO USER COUNT
BUMPASCB LA    R6,4(,R6)     LOAD ADDRESS OF NEXT ASVT ENTRY     90337
         BCT   R5,LOOPASCB   TRY NEXT ENTRY                      90337
         DROP  R7
         SPACE 1
***********************************************************************
*                                                                     *
*        AT END OF ASVT CHAIN SO PUT OUT TSO USER COUNT               *
*           AND SET RETURN CODE FOR USERID REJECT                     *
*           TO AVOID SEARCH FOR JOBNAME 'TSO'                         *
*                                                                     *
***********************************************************************
         SPACE 1
         CVD   R3,PACK                 CONVERT USER COUNT TO DECIMAL
         MVC   CHAR,EDMSK              MOVE EDIT MASK TO CHAR
         ED    CHAR,PACK+4             CONVERT DECIAML COUNT TO EBCDIC
         MVC   LINE(4),CHAR+4          MOVE IN COUNT
         MVC   LINE+4(L'USERS),USERS   MOVE IN MESSAGE
         LA    R1,LINE                 LOAD POINTER TO OUTPUT LINE
         TPUT  (R1),L'LINE             PUT OUT COUNT MESSAGE
         LA    R5,IEREJECT             TREAT AS IF JOBNAME REJECTED
         B     ENDPROC                 GO TO END OF PROCEDURE
         SPACE 1
***********************************************************************
*                                                                     *
*        SECOND TIME ENTRY (FREE MESSAGE BUFFER)                      *
*                                                                     *
***********************************************************************
         SPACE 1
FREEMESS L     R0,GETMINFO   LOAD SUBPOOL NUMBER AND LENGTH      90337
         FREEMAIN  R,LV=(0),A=(1)      FREE MESSAGE BUFFER
         SLR   R6,R6                   ZERO OUT MESSAGE POINTER ENTRY
         ST    R6,IEMSGP               IN PARMLIST
         LA    R5,IEREJECT             SET RETURN CODE TO 12 - DELETE
         SPACE 1
***********************************************************************
*                                                                     *
*        RETURN TO CALLER (STATUS, CANCEL, OR OUTPUT COMMAND)         *
*                                                                     *
***********************************************************************
         SPACE 1
ENDPROC  SERVTERM ,          CLOSE AND FREE                      90337
         ENDM  RC=(R5)       RETURN WITH CC IN R5                90337
         SPACE 1                                                 90337
*        SUBROUTINE TO COMPARE MASKED ACCOUNT NUMBERS:           90337
*        MASK ADDRESS R14, TEST ADDRESS R1, LENGTH R0, RETURN R9 90337
*                                                                90337
CLCMASK  CMASK ML=R0,L=R0,MA=R14,A=R1                            90337
         EJECT
******** CONSTANTS AND DSECTS *********
GETMINFO DC    A(REJSIZE)    SUBPOOL/LENGTH FOR MESSAGE GETMAIN  90337
JOBNCOMP CLC   0(0,R6),0(R1)
MOVEJOBN MVC   REJSLOT(0),0(R1)
MOVEATE  MVC   0(0,R6),0(R1) MOVE UP TO 8 BYTES ANYWHERE         90337
BLANKS   DC    CL8' '        BLANKS                              90337
BLANK    EQU   C' '
         SPACE 1                                                 90337
REJTEXT1 DC    CL66'REJECTED - JOBNAME MUST BE YOUR USERID PLUS AT LEASC
               T ONE CHARACTER'
REJTEXT2 DC    CL69'REJECTED - JOBNAME MUST BE YOUR USERID OR MUST STARC
               T WITH YOUR USERID'
MSGUNAUT VCON  'YOUR USER-ID IS NOT AUTHORIZED FOR JOB MANIPULATION'
MSGUNALN EQU   *-MSGUNAUT                                        90337
HEADER   DC    CL21'  USERID  ASID  LINE '
EDMSK    DC    XL8'4020202020202120'
USERS    DC    CL20' USERS ARE LOGGED ON'
TRT      DC    CL16'0123456789ABCDEF'
ASCBWORD DC    CL4'ASCB'
STRTWORD DC    CL8'STARTING'
DISCON   DC    CL8'DISCON''D'
         SPACE 1                                                 90337
SAVE     DSECT ,             SAVE/WORK AREA                      90337
PACK     DS    D
@SERVICE DS    A             ADDRESS OF @SERVICE ROUTINE         90337
EXTLIST  EXTRACT MF=L
PSCBADDR DS    A
MYACCT   DS    CL8           ACCOUNT OF RUNNING JOB/SESSION      90337
MYPRIV   DS    X             USER'S ACCOUNT RELATED PRIVILEGE BITS
CHAR     DS    CL8
LINE     DS    CL26
LINEUSID EQU   LINE+1,8,C'C'                                     90337
LINEASID EQU   LINE+11,3,C'C'                                    90337
LINEADDR EQU   LINE+16,3,C'C'                                    90337
LINESYM  EQU   LINE+16,8,C'C'                                    90337
         DS    0A                                                90337
A$SVCRB  A$SVCRB DSECT=NO,PFX=A$S                                90337
COPYJES  SERVJES OPT=LOJOB,DSECT=NO                              90337
DB       DS    D                                                 90337
SAVEND   EQU   *                                                 90337
         SPACE 1                                                 90337
REJMSG   DSECT                         DSECT FOR RETURN MESSAGE
REJMLEN  DS    H
REJJOB   DS    CL4
REJSLOT  DS    CL(69+1+8)                                        90337
REJSIZE  EQU   *-REJMSG                                          90337
         SPACE 1                                                 90337
         PRINT &PRTMAC                                           90337
         IKJEFFIE IETYPE=OUTPUT
         SPACE 1                                                 90337
         PRINT &PRTSYS                                           90337
         SPACE 1                                                 90337
A$LOUD   A$LOUD DSECT=YES    MAP LOCAL ONLINE USER DATA          90337
A$GDA    A$GDA DSECT         MAP ACCOUNTING DATA                 90337
         SPACE 1                                                 90337
         IKJPSCB ,                                               90337
         IKJTSB   LIST=YES,EXT=NO
         IKTTSBX
         IHAASCB DSECT=YES
         IHAASVT DSECT=YES                                       90337
         CVT DSECT=YES
         IHACDE ,                                                94226
         IHAPSA ,                                                90337
USERCVT  USERCVT ,                                               90337
         END   ,                                                 90337
