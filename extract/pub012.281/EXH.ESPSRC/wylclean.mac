WSCR     TITLE 'DISK STORAGE CLEAN-UP PROGRAM'
         MACRO
&NM     VOLUME &A
         LCLA  &K
         AIF   ('&A' EQ '' OR K'&A GT 6).BAD
&K       SETA  K'&A-1
&NM      DC    AL1(&K),C'&A'
         MEXIT
.BAD     MNOTE 8,'VOL-SER MISSING OR TOO LONG.'
         MEND
         MACRO
&NM      DSNAME &A
         LCLA  &K
         AIF   ('&A' EQ '' OR K'&A GT 44).BAD
&K       SETA  K'&A-1
&NM      DC    AL1(&K),C'&A'
         MEXIT
.BAD     MNOTE 8,'DSNAME MISSING OR TOO LONG.'
         MEND
         MACRO
&NM      INDEX &A
         LCLA  &K
         AIF   ('&A' EQ '' OR K'&A GT 8).BAD
&K       SETA  K'&A
&NM      DC    AL1(&K),C'&A..'
         MEXIT
.BAD     MNOTE 8,'INDEX LEVEL MISSING OR TOO LONG'
         MEND
         EJECT ,                                                 79154
         COPY  OPTIONGB                                          81172
         SPACE 1                                                 81172
         SYSPARM LIST=YES                                        81172
         EJECT
         PRINT &PRTSOR                                           81172
         SPACE 1
         EXTRN SUBVTOCI      VTOC STATUS ROUTINE                GP05155
WYLCLEAN START 0
         USING *,R15                                             81075
         STM   R14,R12,12(R13)                                   81075
         MVI   FLAG,0        RESET ALL FLAGS                     81075
         BAL   R15,COMCLEAN                                      81075
         USING *,R15                                             81075
         ENTRY VOLCLEAN      USER VOLUMES ONLY                   81075
VOLCLEAN STM   R14,R12,12(R13)                                   81075
         MVI   FLAG,VAAUSER  SET FOR USER VOLUMES ONLY           81075
COMCLEAN L     R15,=A(WYLCLEAN)  SET FOR ORIGINAL BASE           81075
         DROP  R15                                               81075
         USING WYLCLEAN,R12,R11                                  81075
         LR    R12,R15
         LA    R11,4095(,R12)  START ON SECOND BASE
         LA    R11,1(,R11)   MAKE SECOND BASE
         LR    R9,R13
         LA    R13,SAVEAREA                                      81075
         ST    R13,8(,R9)
         ST    R9,4(,R13)
         SPACE 2
         NI    FLAG,255-FGLIVE  SET FOR TEST ONLY                81075
         LA    R1,0(,R1)                                         81075
         LTR   R1,R1         IS THERE A PARM ?
         BZ    ALLKEEP         NO, FAKE IT OUT FOR TESTING
         LAT   R1,0(R1),ALLKEEP  SEE IF ANY
         CLC   0(2,R1),ZEROES   ANY PARM ?                       81075
         BE    ALLKEEP       NO - TEST ONLY                      81075
         CLC   =H'6',0(R1)   PARM LENGTH OK ?
         BNE   BADPARM       NO, TOO BAD
         CLC   =C'DELETE',2(R1)  CORRECT CHOICE ?
         BE    SETLIVE
         SPACE 2
BADPARM  WTO   'INVALID PARM - EOJ',ROUTCDE=11
         ABEND 666                                               81075
         SPACE 1                                                 81075
BADVALID WTO   'ERROR IN ''@SERVICE'' MODULE',ROUTCDE=(1,11)     81116
         ABEND 666,DUMP                                          81075
         SPACE 2
SETLIVE  OI    FLAG,FGLIVE   SET DELETE SWITCH                   81075
         MVC   BUGHEAD,BUGHEAD-1   CLEAR 'DEBUG' PORTION OF TITLE
         MVC   SUBHED+49(2),SUBHED+47  REMOVE 'NO' FROM HEADER   81152
         SPACE 1                                                 81075
ALLKEEP  SERVINIT ,          LOAD SERVICE ROUTINE                86136
         TIME  TU            GET THE TIME QUICKLY                86136
         SLR   R0,R0                                             86136
         STM   R0,R1,DB      SAVE THE PACKED DATE                86136
         CVB   R5,DB         GET BINARY EQUIVALENT               86136
         SLR   R4,R4                                             86136
         D     R4,=F'1000'   SEPARATE YEAR FROM DAYS             86136
         STH   R4,CURDSDAT+1 SET DAYS                            86136
         STC   R5,CURDSDAT   SET YEAR IN DSCB FORMAT             86136
         SERVCALL LPALD,=CL8'@PRINTER',ERR=BADVALID  GET PRINTER 81151
         ST    R0,@PRINTER   SAVE ADDRESS                        81137
         PRTOPEN SYSPRINT,OPT=ABE OPEN PRINT/ABEND IF NONE OR DUMMY
         SERVCALL LPALD,=CL8'@OBTAINS'  LOAD VTOC READER         81152
         ST    R0,@OBTAIN    SAVE THE ADDRESS                    81152
         LR    R2,R0         COPY                                81152
         L     R2,28(,R2)    GET (FINAGLED) @OBSERVE ADDRESS     81152
         MVC   0(4,R2),=A(268*1024)  SPACE FOR @PROTECT CALL     81075
         SERVCALL  ACGET,DB,ERR=BADVALID  GET OUR ACCOUNT AND PRIVIES
         STC   R0,DB         STASH FLAGS                         81075
         NI    DB,255-FGLIVE   MAKE SURE LOCAL FLAG NOT TURNED ON
         OC    FLAG,DB       OR IN WITH OURS                     81075
         SERVCALL INITG,ERR=NO  FORCE GETMAIN/OPEN SERVICE       81151
         XC    SAVEUCB,SAVEUCB  INITIALIZE UCB SCAN              81151
UNITBUMP L     R1,SAVEUCB    RESTORE UCB ADDRESS                 81116
         SERVCALL UCBUM,(R1),ERR=UNITEXIT  QUIT IF END           81116
         USING UCBOB,R3      DECLARE UCB MAPPING                 81137
         ST    R0,SAVEUCB    SAVE UCB ADDRESS                    81116
         LTR   R3,R0         COPY UCB ADDRESS                    81151
         BZ    UNITEXIT      JUST IN CASE                        81151
         CLI   UCBTBYT3,UCB3DACC  DIRECT ACCESS UCB ?            81075
         BNE   UNITBUMP      NO, SKIP                            81075
         TM    UCBSTAT,UCBCHGS+UCBUNLD  COMING OFF ?             81075
         BNZ   UNITBUMP      YES; SKIP                           81075
         TM    UCBSTAT,UCBONLI   ONLINE ?                        81075
         BZ    UNITBUMP      NO; SKIP                            81075
         CLI   UCBVOLI,C' '   ANY VOLSER ?                       81075
         BNH   UNITBUMP      NO, GET ANOTHER ONE                 81075
         CLI   UCBVOLI,C'9'  VALID ?                             81075
         BH    UNITBUMP      NO; IGNORE                          81075
         CLI   UCBVOLI,X'84'   SPECIAL ?                         81075
         BE    UNITBUMP      YES; SKIP                           81075
         SERVCALL VSNFG,UCBVOLI,ERR=NO   GET FLAGS FOR VOLUME    81151
         XC    INFLTEST,INFLTEST  CLEAR RETURNED FLAGS           86336
         BXH   R15,R15,TESTTIOT  IF NOT FOUND - LOOK FOR DD CARD 81116
         STH   R0,INFLTEST   SAVE FLAGS RETURNED                 81116
         TM    FLAG,VAAUSER  ARE WE IN USER MODE ?               81075
         BZ    CHECKSYS      NO; CHECK SYSTEM PACKS              81075
         TM    FLAG,VAASTC+VAASYS+VAASUP  SPECIAL PRIVIES ?      81075
         BNZ   TESTTIOT      YES; PERMIT IF DD CARD SUPPLIED     81075
         TM    INFLTEST,VF1SYSP+VF1SYSG+VF1SYSD+VF1STOR  SPECIAL ?
         BNZ   UNITBUMP      YES; SKIP THIS VOLUME               81075
         CLI   INFLTEST+1,0   ANY OTHER RESTRICTION ?            81075
         BNE   UNITBUMP      YES; SKIP                           81075
         B     TESTTIOT                                          81075
CHECKSYS TM    INFLTEST+1,VF2DSNA+VF2DSNT SPECIAL NAMING ?       94240
         BNZ   SCANVTOC      YES; BYPASS DD CARD CHECK           81075
         SPACE 1                                                 81075
*        NOT SPECIAL PACK - SEE IF DD CARD PRESENT FOR IT        81075
*                                                                81075
TESTTIOT O     R3,=X'80000000'  SET 'NO DYNAMIC DD' FLAG         86336
         SERVCALL TIOUA,(R3),ERR=UNITBUMP  LOOK FOR DD ON UNIT   81151
         LTR   R2,R0         CHECK IT ANYWAY                     81151
         BZ    UNITBUMP      NONE; TRY ANOTHER UCB               81151
         SPACE 1                                                 81075
SCANVTOC MVC   OPVOL(6),UCBVOLI STASH VOL-SER                    81075
         MVC   VOLSER(6),UCBVOLI  COPY TO OBTAIN LIST            81075
         MVC   VOLTYP(4),UCBTBYT1  SAVE VOL-TYP FOR SCRATCH      81075
         IC    R1,VOLTYP+3   GET DISK SUB-TYPE                   81075
         LA    R15,15        MAKE MASK
         NR    R1,R15        MASK SUB-TYPE
         BZ    UNITBUMP      SKIP IF UNDEFINED                   81075
         SLL   R1,2          MAKE OFFSET
         LA    R1,BLKUCTAB-4(R1)  GET CORRESPONDING TABLE ENTRY
         MVC   BLKUCMIN(4),0(R1)  COPY VALID SIZES
         CLC   BLKUCMIN(4),ZEROES  UNDEFINED ?                   81075
         BE    UNITBUMP      YES; SKIP OUT                       81075
         EJECT
*        NOW FIND THE FIRST ( DSCB4 ) VTOC ENTRY
*
         PRTV  HEAD,TITLE=1  PRINT NEW PAGE HEADER               81278
         PRTV  SUBHED,TITLE=3,CC=NO  AND SUB-HEADER              81138
         @OBTAIN DSCB4
         CH    R15,=H'8'     DEFINED ?                           81075
         BH    IOERR         NO; SIGNAL ERROR                    81075
         B     *+4(R15)      CHECK RETURN
         B     FOUND4        OK
         B     NOMT          NOT MOUNTED
         B     FOUND4         DSCB 4 NOT FOUND; BUT FOUND ANYWAY 81075
IOERR    PRTV  MNO4                                              81137
         B     UNITBUMP                                          81075
         SPACE 1
NOMT     PRTV  MNOM                                              81137
         B     UNITBUMP                                          81075
         SPACE 1
FOUND4   CLI   DS4IDFMT,C'4'    IS IT TYPE 4 DSCB ANYWAY ?       81075
         BNE   IOERR         IF NOT TYPE 4 IT'S A VALID 'NOT FOUND'
         MVC   ATTR(4),DS4VTOCE+2  COPY VTOC LOW EXTENT          81075
         MVC   R(1),UCBVTOC+2   COPY RECORD NUMBER OF DSCB4      81075
         DROP  R3            DONE WITH UCB                       81075
         LA    R1,DS4VTOCI   POINTTO STATUS BITS                GP05155
         SUBCALL SUBVTOCI    EXAMINE THE STATUS                 GP05155
         CH    R15,=H'4'     USABLE HWM ?                       GP05155
         BNH   OBTSET        YES; SHORTEN SEARCH                GP05155
         MVC   DS4HPCHR(4),DS4VTOCE+6  USE VTOC END EXTENT       81172
         MVC   DS4HPCHR+4(1),DS4DEVDT   + HIGH DSCB/TRK          81075
OBTSET   MVI   OBTSORT,X'A2'   SET FOR SORTED @OBTAIN            81075
         TM    INFLTEST+1,VF2DSNT   WYLBUR/TSO PACK?             92344
         BNZ   OBTSHORT      SET FOR UNSORTED                    81075
         MVC   DB(4),DS4VTOCE+2  COPY START CYL/TRACK            81075
         MVC   DB+4(4),DS4HPCHR  AND END CYL/TRACK               81075
         LH    R14,DB+4      GET END CYLINDER                    81075
         SH    R14,DB        LESS START CYLINDER                 81075
         MH    R14,DS4DEVSZ+2  MAKE TRACKS                       81075
         AH    R14,DB+6      ADD END TRACK                       81075
         SH    R14,DB+2      LESS START                          81075
         CH    R14,=H'38'    MORE THAN 39 TRACKS ?               81075
         BNH   DSLOOP        NO; START PROCESSING                81075
OBTSHORT MVI   OBTSORT,0     SET UNSORTED                        81075
         EJECT
*        RETURN TO THIS POINT TO GET ANOTHER DS NAME
*
DSLOOP   SLR   R2,R2
         IC    R2,R          GET PREVIOUS RECORD NUMBER
         LA    R2,1(,R2)     BUMP BY ONE
         STC   R2,R
         CLC   R(1),DS4DEVDT     IS IT HIGHER THAN FITS ON DEVICE ?
         BNH   FIND1         NO, CHECK AGAINST LAST DSCB 1
         MVI   R,1           YES, RESET RECORD NO. TO 1
         LH    R2,HH         GET TRACK NUMBER
         LA    R2,1(,R2)     AND BUMP BY ONE
         STH   R2,HH
         CH    R2,DS4DEVSZ+2   IS IT HIGHER THAN DEVICE PERMITS ?
         BL    FIND1         NO, CHECK FOR VALID ADDRESS
         XC    HH(2),HH      YES, RESET TRACK TO 0
         LH    R2,CC         AND BUMP CYLINDER
         LA    R2,1(,R2)     NUMBER BY 1
         STH   R2,CC
FIND1    CLC   ATTR(5),DS4HPCHR  NEW ADDRESS PAST END ?          81075
         BNH   OBT1          NO, GET THE DS NAME
WEOV     PRTV  EOV                                               81137
         B     UNITBUMP      DO ANOTHER UCB                      81075
         SPACE 2
OBT1     MVC   DS1FMTID,OBTSORT   REQUESTED SORTED @OBTAIN       81075
         @OBTAIN DSCB1       SEEK THE DSCB AT THIS ADDRESS
         B     *+4(R15)
         B     OBOK          FOUND IT
         B     NOMT          VOLUME NOT MOUNTED ?
         B     IOERR         DSCB NOT FOUND ?
         B     IOERR         I/O ERROR
         B     IOERR         DS1DSNAM PTR BAD                    81075
         B     WEOV          BEYOND VTOC ? END OF VOLUME
         EJECT
OBOK     CLI   DS1FMTID,X'FF'   IS THIS @OBTAINS 'EOF' INDICATOR ?
         BE    WEOV          YES - TAKE EOV EXIT
         CLI   DS1FMTID,C'1'    FOUND A DSCB, IS IT TYPE 1 ?     81075
         BNE   DSLOOP        NO, IGNORE
         MVC   MDSN(MDSNE-MDSN),MDSN-1  CLEAR PRINT LINE
         MVC   MDSN(44),DS1DSNAM  PLACE DS NAME IN MESSAGE AREA  81075
         MVC   YN+4(9),=C'SCRATCHED'  REPLACE
         SPACE 2
VOLOK    LA    R1,DSNLIST    CHECK SPECIAL DSNAMES NOT TO BE SCRATCHED
         LA    R2,DSNEND
         BAL   R10,DSNCHK    GO CHECK FOR COMMON SPECIAL NAMES
         SPACE 1
         LA    R1,INDICES    CHECK FOR SPECIAL INDEX LEVELS
         LA    R2,INDEND
         BAL   R10,DSNCHK    SEE IF SPECIAL INDEX
*DEFUNCT TM    INFLTEST+1,VF2DSNW  WYLBUR PACK ?                 92344
*DEFUNCT BNZ   WYLSCR        YES - DO SPECIAL WYLBUR FUNCTIONS   92344
*NOT*    SERVCALL WYLDX,DS1DSNAM,ERR=NO  SEE IF WYLBUR NAME      81151
         TM    INFLTEST+1,VF2DSNT   TSO STORAGE VOLUME ?         85195
         BNZ   TSOTEST       YES; TEST FOR VALID TSO USERID      85195
         TM    INFLTEST+1,VF2DSNA  LIBRARY PACK ?
         BZ    USERVOL       NO; DO SPECIAL USER VOLUME CODE     81075
         CLI   DS1NOEPV,0    PATTERN DSCB ?                      81075
         BE    GDGPAT        YES; CHECK SOME MORE                81075
         LA    R1,LIBLIST    GET EXEMPT LIBPAK NAMES             79350
         LA    R2,LIBLEST    AND END
         BAL   R10,DSNCHK    SEE IF ANY
         SERVCALL  DSLIB,DS1DSNAM,ERR=SERVERR   CHECK LIBPAK DSN 81151
COMTEST  CLI   DS1DSORG+1,0     ANY REALLY FUNNY DSORG ?         85195
         BNE   BADORG                                            81075
         TM    DS1DSORG,255-JFCORGIS-JFCORGPS-JFCORGDA-JFCORGPO  ANY ?
         BNZ   BADORG        YES; NOT PERMITTED                  81075
         TM    DS1DSORG,JFCORGIS+JFCORGPS+JFCORGDA+JFCORGPO  ANYTHING ?
         BZ    BADORG        NONE; KILL IT                       81075
         TM    DS1DSORG,JFCORGPS+JFCORGPO   DS1LSTAR VALID ?     81075
         BNM   LIBFORM       NO; SKIP DS1LSTAR CHECK             81075
         CLC   DS1LSTAR(7),ZEROES  EVER USED ?                  GP09186
         BE    BADUSE        NO; KILL IT                         81075
LIBFORM  TM    DS1RECFM,JFCFMREC    RECFORM ?                    81075
         BZ    BADFORM       NO; KILL IT                         81075
         CLC   DS1BLKL(2),ZEROES   ANY BLOCKSIZE ?               81075
         BE    BADBLOCK      NO; TOO BAD                         81075
EXPTEST  ICM   R0,7,DS1EXPDT  ANY EXPIRATION ?                   86136
         BZ    DSLOOP        NONE; KEEP IT                       86136
         CLM   R0,7,DS1CREDT SAME AS CREATION ?                  86136
         BNH   DSLOOP        THIS IS IBM'S IDEA OF A JOKE        86136
*                            - SETTING AN UNUSABLE DEFAULT       86136
         CLM   R0,7,CURDSDAT CHECK DATE                          86136
         BNL   DSLOOP        NOT PASSED, YET; KEEP               86136
         MVC   EXP(7),=C'EXPIRED'                                86136
         SLR   R0,R0                                             86136
         IC    R0,DS1EXPDT   GET YEAR                            86136
         SLR   R1,R1                                             86136
         ICM   R1,3,DS1EXPDT+1  AND DAY                          86136
         MH    R0,=H'1000'   FINAGLE                             86136
         AR    R0,R1         MAKE YYDDD                          86136
         CVD   R0,DB         MAKE PACKED                         86136
         MVC   EXP+7(6),=X'F02020202120'  MAKE EDIT MASK         86136
         ED    EXP+7(6),DB+5   MAKE DATE PRINTABLE               86136
         MVI   EXP+7,C' '    RESTORE BLANK                       86136
         B     SCRTCH        GET RID OF IT                       86136
         SPACE 2                                                 81075
USERVOL  LA    R1,USERLIST   GET EXEMPT USER NAMES               81075
         LA    R2,USERLEST   SET LIST END                        81075
         BAL   R10,DSNCHK    SEE IF SPECIAL                      81075
         CLI   DS1DSNAM,C'*'   IEHMOVE TEMPORARY ?               81075
         BE    BADTEMP       YES; KILL IT                        81075
         CLC   =C'SYS',DS1DSNAM  POSSIBLE TEMPORARY DATASET ?    81075
         BNE   USERNTEM      NO                                  81075
         CLC   =C'.T',DS1DSNAM+8  SECOND INDEX ?                 81075
         BNE   USERNTEM      NO                                  81075
         MVC   DB(5),=5C'0'                                      81075
         NC    DB(5),DS1DSNAM+3  CHECK FOR FIVE NUMERICS         81075
         CLC   DB(5),=5C'0'  ALL NUMERIC ?                       81075
         BNE   USERNTEM      NO; NOT TEMPORARY                   81075
BADTEMP  MVC   EXP(9),=C'TEMPORARY'                              81075
         B     SCRTCH        SCRATCH IT                          81075
USERNTEM CLI   DS1NOEPV,0    ANY EXTENTS ?                       81075
         BE    GDGPAT        NO; CHECK GDG                       81075
         CLI   DS1DSORG+1,0  ANY DSORG ?                         81075
         BNE   USERNORG      YES                                 81075
         TM    DS1DSORG,255-JFCORGU  OTHER THAN JUST U ?         81075
         BZ    BADORG        NO; FAIL                            81075
         TM    DS1DSORG,JFCORGPS+JFCORGPO  SEQUENTIAL ?          81075
         BZ    USERNORG      NO; BYPASS LSTAR TEST               81075
         CLC   DS1LSTAR(7),ZEROES  EVER USED ?                  GP09186
         BE    BADUSE        NO; TOO BAD                         81075
USERNORG TM    DS1RECFM,JFCFMREC  ANY RECFM ?                    81075
         BZ    BADFORM       NO                                  81075
         CLC   DS1BLKL(2),ZEROES  ANY BLOCKSIZE ?                81075
         BE    BADBLOCK      NO                                  81075
         B     EXPTEST       CHECK RETENTION                     86136
         SPACE 1                                                 81075
GDGPAT   MVC   EXEMPT(8),=C'PAT DSCB'                            81075
         CLC   DS1RECFM(6),ZEROES  ANY RECFM/LRECL/BLKSIZE ?     81075
         BNE   GDGDCBS       YES; ALLOW                          81075
         TM    DS1RECFM,JFCFMREC   ANY RECFORM ?                 81075
         BZ    BADFORM       NO                                  81075
         CLC   DS1BLKL(2),ZEROES   ANY BLOCKSIZE ?               81075
         BE    BADBLOCK      NO; TOO BAD                         81075
GDGDCBS  MVC   HIDEX,HIDEX-1   CLEAR INDEX LEVEL                 81075
         LA    R0,9          DO FOR NINE                         81075
         LA    R1,DS1DSNAM                                       81075
         LA    R2,HIDEX                                          81075
         LA    R14,1         CONSTANT                            81075
GDGPATLP CLI   0(R1),C'.'    INDEX SEPARATOR ?                   81075
         BE    GDGPATCH      YES; CHECK IT                       81075
         MVC   0(1,R2),0(R1)  MOVE A BYTE                        81075
         AR    R1,R14                                            81075
         AR    R2,R14                                            81075
         BCT   R0,GDGPATLP   TRY NEXT                            81075
         B     NAMEBAD       UNINDEXED NAME IS INVALID           81075
GDGPATCH MVC   0(2,R2),=X'4B15'  MAKE BAD NAME                   81075
         MVI   DB+259,0      CLEAR OUTPUT BYTE                   81075
         LOCATE LOCDEX       LOOK FOR INDEX ENTRY                81075
         BXLE  R15,R15,GDGELSE   BAD ENTRY CATALOGUED ?          81075
         CLI   DB+259,C' '   WAS A CVOL SET ?                    81075
         BNH   GDGELSE       NO; KILL IT                         81075
         CLC   VOLSER,DB+259  SAME AS THIS VOLUME ?              81075
         BE    DSLOOP        YES; CHECK NEXT DATASET             81075
GDGELSE  MVC   EXP(8),=C'BAD CVOL'                               81075
         B     SCRTCH        SCRATCH IT                          81075
         SPACE 3
WYLSCR   LA    R1,WYLLIST    GET EXEMPT DSNAMES
         LA    R2,WYLLEST
         BAL   R10,DSNCHK    SEE IF EXEMPT
         SERVCALL  DSWYL,DS1DSNAM,ERR=SERVERR   CHECK WYLBUR NAME
         CLI   DS1DSORG+1,0  ANY REALLY FISHY DSORG ?            81075
         BNE   BADORG        NO; FAIL                            81075
         TM    DS1DSORG,JFCORGPO+JFCORGPS  PS/PO ?               81075
         BNM   BADORGW       NO; TOO BAD                         81075
         TM    DS1DSORG,255-JFCORGPO-JFCORGPS   OTHER THAN PS/PO ?
         BZ    OKDSORG       NO; USE IT
BADORGW  CLI   DS1DSORG,JFCORGDA  DSORG=DA ?                     81075
         BNE   BADORG        NO                                  81018
         TM    DS1RECFM,JFCUND  RECFM=U ?                        81075
         BNO   BADORG        NO                                  81018
         LH    R15,DS1LRECL   GET RECORD LENGTH                  81075
         LA    R15,4(,R15)    +4                                 81018
         CH    R15,DS1BLKL     MATCHES BLOCK SIZE ?              81075
         BNE   BADORG                                            81018
         MVC   EXEMPT(9),=CL9'DA - SAS?'  SPECIAL EXCEPTION      81075
         B     NOTS          LEAVE IT                            81018
         SPACE 1                                                 81075
BADORG   MVC   EXP(9),=C'BAD DSORG'    SET UNACCEPTABLE DSORG    81075
         CLC   DS1DSORG(2),ZEROES  ANY DSORG ?                   81075
         BNE   SCRTCH        YES; SCRATCH                        79350
BADTONO  MVC   EXP(3),DCNO   MOVE 'NO '                          81075
         B     SCRTCH
OKDSORG  CLC   DS1LSTAR(7),ZEROES  EVER USED ?                  GP09186
         BNE   ISUSED        YES                                 81075
BADUSE   MVC   EXP(8),=C'NOT USED'     NO LSTAR, NO SAVE         81075
         B     SCRTCH
ISUSED   TM    DS1RECFM,JFCFMREC  CHECK RECORD FORMAT            81075
         BNZ   OKRECFM       OK; CHECK MORE                      81075
BADFORM  MVC   EXP(9),=C'BAD RECFM'    NO MAJOR RECORD FORMAT    81075
         CLI   DS1RECFM,0       ANY ?                            81075
         BE    BADTONO       NO; CHANGE TO 'NO ...'              79350
         B     SCRTCH        YES; LEAVE 'BAD ...'                81075
OKRECFM  CLI   DS1NOEPV,0    PATTERN DSCB ?                      81075
         BE    BADUSE        YES; SET 'NOT USED'                 81075
*        CLC   DS1BLKL(2),BLKUCMIN  VALID BLOCKSIZE              81075
*        BL    BADBLOCK
*        CLC   DS1BLKL(2),BLKUCMAX   NOT EXCESSIVE               81075
*        BNH   OKBLOCK
         CLC   DS1BLKL(2),ZEROES  ANY BLOCKSIZE AT ALL ?         81075
         BNE   OKBLOCK       YES; OK                             81075
BADBLOCK MVC   EXP(9),=C'BAD BLKSZ'    SHOW INVALID SIZE         81075
         CLC   DS1BLKL(2),ZEROES  ANY BLOCKSIZE ?                81075
         BE    BADTONO       NO; CHANGE TO 'NO ...'              81075
         B     SCRTCH        ELSE LEAVE 'BAD ...'                81075
BADINDEX MVC   EXP(9),=C'BAD INDEX'  WRONG INDEX FOR ACCOUNT/USER
         B     SCRTCH        DELETE IT                           81075
OKBLOCK  EQU   *
*****    ADD MORE CHECKS HERE
         B     EXPTEST       CHECK RETENTION                     86136
         SPACE 2                                                 85195
TSOTEST  SERVCALL DSTSX,DS1DSNAM,ERR=  CHECK INDEX               86336
         CH    R15,=H'8'     BAD ?                               86336
         BNL   SERVERRL      YES; IDENTIFY SUB-CODE              86336
TSOTESTK CLI   DS1NOEPV,0    PATTERN DSCB ?                      85195
         BE    GDGPAT        YES; GO TO CHECK IT                 85195
         B     COMTEST       ELSE TEST FOR USED DATASET          85195
         SPACE 2
NAMEBAD  MVC   EXP(7),=C'BAD DSN'                                81075
         SPACE 1
*        SCRATCH THE DATA SET
*
SCRTCH   TM    DS1DSIND,PASSW   PASSWORD/RACF PROTECTED ?        81075
PASSW    EQU   X'54'         PASSWORD FLAGS                      81075
         BNZ   PASS
         XC    VOLSEQ(2),VOLSEQ      ZERO RETURN INDICATOR
         SLR   R15,R15       FOR TEST ONLY
         TM    FLAG,FGLIVE   TEST ONLY ?                         81075
         BZ    CATCHK        TEST MODE ONLY, SKIP SCRATCH ATTEMPT
         SLR R0,R0  ZERO UCB ADDRESS TO PREVENT SYSTEM 'MOUNT' REQUESTS
         SCRATCH SCRDSN
         B     *+4(R15)
         B     CATCHK        SCRATCHED, NOW CHECK IF UNCATLG NEEDED
         B     NOMT          NOT MOUNTED ?
         CLI   VOLSEQ+1,5    UNUSUAL CONDITION, CHECK IT
         BNL   NOMT          NOT MOUNTED OR NOT  MOUNTABLE
         CLI   VOLSEQ+1,3
         BL    CATCHK        SCRATCHED ALREADY; NEED TO UNCATLG ?
         BH    IOERR         I/O ERROR
*
PASS     MVC   EXEMPT(8),=C'PASSWORD'
NOTS     MVC   YN(3),=C'NOT USED'                                81075
DUMSKIP  EQU   *             FOR TESTING BRANCH AROUND SCRATCH *******
         PRTV  VDSN                                              81137
         B     DSLOOP
         SPACE 2
CATCHK   MVC   CATMSG(8),=C' NOT CAT'  PRELIMINARY - NO CATLG ENTRY
         LOCATE CATFND       IS IT CATALOGUED ?
         BXH   R15,R15,DUMSKIP    NO, OR ERROR                   81075
         MVC   CATMSG,=CL9'NOT UNCAT'   PROVISIONALLY INDICATE NOT
         CLC   =H'1',CATVOL#  ONE VOLUME ENTRY ?
         BNE   DUMSKIP       NO, FORGET IT
         CLC   CATVOL,VOLSER   SAME VOLSER  ?
         BNE   DUMSKIP       NO, SKIP IT
         TM    FLAG,FGLIVE   TEST MODE ?                         81075
         BZ    DUMSKIP       FOR DEBUGGING MAKE UNCONDITIONAL BYPASS
         CATALOG UNCAT       REMOVE IT FROM CATLG
         BXH   R15,R15,DUMSKIP  ON ERROR, LEAVE MSG AS IS        81075
         MVC   CATMSG,=CL9'UNCATLG''D'
         B     DUMSKIP       PRINT MESSAGE
         SPACE 3
UNITEXIT @OBTAIN OPT=CLOSE   CLOSE VTOC READER                   81152
         SERVTERM ,          CLOSE PRINT AND SERVICE             81151
         SLR   R15,R15       ZERO STEP CONDITION CODE            81075
         L     R13,4(,R13)
         LM    R0,R12,20(R13)
         L     R14,12(,R13)
         BR    R14
         SPACE 2
CLDSN    CLC   DS1DSNAM(0),1(R1)                                 81075
         SPACE 1
*        SPECIAL DSNAME CHECKER
DSNCHK   SLR   R3,R3         CLEAR FOR IC
DSNLP    IC    R3,0(,R1)
         EX    R3,CLDSN      IS IT SPECIAL ?
         BE    SPECIAL       YES, NO SCRATCH
         LA    R1,2(R1,R3)   CHECK NEXT ENTRY
         CR    R1,R2
         BL    DSNCHK
         BR    R10           RETURN TO CALLER
         SPACE 1
SPECIAL  MVC   EXEMPT(7),=C'SPECIAL'
         B     NOTS
         SPACE 2                                                 81075
SERVERRL SLL   R15,1         FAKE BXH ENTRY                      86336
SERVERR  CH    R15,=H'16'    RETURN CODE 8 ?                     81116
         BH    BADVALID      NO; PROGRAM ERROR                   82228
         STC   R0,DB         SAVE RETURN FLAGS                   81075
         CLI   DB,VRPARM     BAD PARM ?                          81075
         BE    BADVALID      YES; PROGRAM ERROR ?                81075
         SLR   R15,R15       CLEAR FOR IC                        81075
         IC    R15,DB        RELOAD CODE                         81075
         MH    R15,=Y(L'VALMSG)                                  81075
         LA    R15,VALMSG-2*L'VALMSG(R15)                        81075
         MVC   EXP(L'VALMSG),0(R15)                              81075
         B     SCRTCH        SCRATCH                             81075
         EJECT
*        LISTS FOR OBTAIN AND SCRATCH
*
DSCB1    CAMLST SEEK,ATTR,VOLSER,DS1DSNAM                        81075
DSCB4    CAMLST SEARCH,DSN4,VOLSER,DS4IDFMT                      81075
SCRDSN   CAMLST SCRATCH,DS1DSNAM,,VOLIST,,OVRD                   81075
CATFND   CAMLST NAME,DS1DSNAM,,DB  SEE IF IN CATALOG             81075
LOCDEX   CAMLST NAME,HIDEX,,DB   LOCATE CONNECT ENTRY            81075
UNCAT    CAMLST UCATDX,DS1DSNAM         REL. 21 (UNCATDX)        81075
         SPACE 1                                                 81137
SYSPRINT PRTWORK SYSPRINT,TITLE=5   SYSPRINT PARAMETERS          81137
         SPACE 2
         DC    C' '          HIDEX-1                             81075
HIDEX    DC    CL11' '       HIGH-LEVEL INDEX + DUMMY NAME       81075
         SPACE 1                                                 81075
VALMSG   DC    CL9'BAD NAME'           2                         81075
         DC    CL9'BAD ACCT'           3                         81075
         DC    CL9'BAD NAME'           4                         81075
         DC    CL9'INVLD DSN'          5                         81075
         DC    CL9'SHORT DSN'          6                         81137
         DC    CL9'DSN LONG'           7                         81137
         DC    CL9'BAD INDX'           8                         81137
DCNO     DC    CL9'NO PSWD'            9                         81075
         DS    0F
BLKUCMIN DC    H'0'          MINIMUM BLOCKSIZE VALID FOR THIS DEVICE
BLKUCMAX DC    H'0'          MAXIMUM PERMITTED
BLKUCTAB DC    H'2299,3625'  OBSOLETE DEVICE ?             2311
         DC    H'0000,0000'  UNSUPPORTED DEVICE ?          2301
         DC    H'0000,0000'  UNSUPPORTED DEVICE ?          2303
         DC    H'0000,0000'  UNSUPPORTED DEVICE ?          2302
         DC    H'0000,0000'  UNSUPPORTED DEVICE ?          2321
         DC    H'2299,3520'  UNSUPPORTED DEVICE ?          2305
         DC    H'2299,3520'  UNSUPPORTED DEVICE ?          2305
BLKU2314 DC    H'2299,3520'  SUPPORTED                     2314
         DC    H'2499,3156'  SUPPORTED                     3330
         DC    H'2299,4100'  UNSUPPORTED DEVICE ?          3340  81018
         DC    H'2299,3665'  UNSUPPORTED ?                 3350  81075
         DC    H'2299,4096'  SUPPORTED                     3375  86336
         DC    H'2499,3156'  SUPPORTED                     3330-11
         DC    H'2499,4096'  SUPPORTED                     3380  86336
         DC    H'2499,4096'  SUPPORTED                     3390
         EJECT
*        MESSAGES
*
MNO4     VCON  '0CAN''T READ DSCB'                               81278
EOV      VCON  '0EOV'                                            81278
MNOM     VCON  '0CAN''T MOUNT VOLUME'                            81278
VDSN     DC    Y(MDSNE-*,0),C'0 '  BLANK FOR CLEARING            81075
MDSN     DC    CL44' ',CL4' '
YN       DC    CL4' ',C'SCRATCHED   '
EXEMPT   DC    CL9' '                                            81075
         DC    CL1' '                                            81075
EXP      DC    CL13' '                                           86136
         DC    CL3' '                                            86136
CATMSG   DC    CL9' '        UNCATLGD/NOT UNCAT MSG
MDSNE    EQU   *
HEAD     VCON  '#  STORAGE VOLUME SCRATCH FOR ',END=HEADEND      81278
OPVOL    DC    CL6'VOLSER'   VOLUME CURRENTLY IN USE
         DC    CL5' '                                            81138
BUGHEAD  DC    C'***** DEBUG LISTING ONLY *****'                 81138
         VCON  ,                                                 81278
SUBHED   VCON  '     DATASET NAME                            NO ACTION *
               TAKEN     EXEMPT.   REASON          CATLG STATUS' 86138
OBTSORT  DC    X'A2'         REQUEST @OBTAINS DSN SORT           81075
FLAG     DC    X'0'          PROCESSING FLAGS MAPPED BY VALIDFLG 81075
FGLIVE   EQU   X'01'           SCRATCH BAD DATASETS (ELSE TEST)  81075
         SPACE 1
DSNLIST DSNAME PASSWORD
        DSNAME SYSCTLG
DSNEND   EQU   *
*
INDICES  INDEX SYS1
INDEND   EQU   *             END OF INDEX LIST
         SPACE 2
LIBLIST  INDEX CICS     MIKE DOESN'T KNOW WHERE THE BATHROOM IS  81116
         INDEX 4444          FOR ZAPPING                         81116
         INDEX 55555                                             81018
         INDEX 666666                                            81018
         INDEX 7777777                                           81018
         INDEX 88888888                                          81018
LIBLEST  EQU   *
         SPACE 2
WYLLIST  INDEX 4444          FOR ZAPPING                         81018
         INDEX 55555                                             81018
         INDEX 666666                                            81018
         INDEX 7777777                                           81018
         INDEX 88888888      ZAP ENTRY
WYLLEST  EQU   *
         SPACE 2                                                 81075
USERLIST INDEX VSAMDSPC      KEEP VSAM DATA                      81075
         DSNAME Z999999      DITTO WITH CATALOGS                 81075
         INDEX CICS          CICS MSG DSNS                       81075
         INDEX TASKMAST                                          81075
         DSNAME D557.ADA                                         81075
         INDEX 4444          FOR ZAPPING                         81075
         INDEX 55555                                             81075
         INDEX 666666                                            81075
         INDEX 7777777                                           81075
         INDEX 88888888      ZAP ENTRY                           81075
USERLEST EQU   *                                                 81075
         SPACE 2
@SERVICE DC    A(0)          ADDRESS OF '@SERVICE' MODULE        81116
@PRINTER DC    A(0)          ADDRESS OF @PRINTER MODULE          81137
         USERVOLT ,          DEFINE VOLT CALL/MAP                81075
WYLCLEAN CSECT ,                                                 81075
         SPACE 2
         LTORG
         SPACE 2
SAVEAREA DC    9D'0'         STANDARD SAVE AREA                  81075
ZEROES   DC    XL8'0'                                            81075
SAVEUCB  DC    F'0'          UCB POINTER SAVE AREA               81116
         DS    0H            ALIGNMENT FOR ATTR                  81075
ATTR     DC    XL5'0'        CCHHR OF THIS/NEXT DSCB             81075
CC       EQU   ATTR            CYLINDER
HH       EQU   ATTR+2            TRACK
R        EQU   ATTR+4              RECORD
CURDSDAT DS    XL3'0'        TODAY IN DS1 FORMAT YYDDDD          86136
VOLIST   DC    H'1'          NUMBER OF VOLUMES DATA SET RESIDES ON
VOLTYP   DC    XL4'0'        TYPE OF RESIDENCE VOLUME
VOLSER   DC    CL6' '        VOLUME SERIAL NUMBER
VOLSEQ   DC    H'0'          SEQUENCE OF VOL. FOR DS / RETURN CODE
         CNOP  4,8           ALIGNMENT FOR FORMAT 4 IECSDSL1     81075
DSN4     DC    44X'4'        DS NAME FOR TYPE 4 DSCB
         IECSDSL1 4          EXPAND FORMAT 4 DSCB                81075
         SPACE 1                                                 81075
         IECSDSL1 1          EXPAND FORMAT 1 DSCB                81075
DB       DC    35D'0'              WORK AREA
         ORG   DB
CATVOL#  DC    H'0'          NO. OF VOLUMES
CATDEV   DC    XL4'0'        DEVICE TYPE
CATVOL   DC    CL6' '        FIRST VOLUME
         SPACE 1                                                 81075
         PRINT &PRTSYS                                           81278
         REGEQU ,                                                81137
         SPACE 1                                                 81137
CVT      DSECT ,                                                 81137
         CVT   DSECT=YES                                         81137
         SPACE 1                                                 85195
USERCVT  USERCVT ,                                               85195
         SPACE 1                                                 81151
         IHACDE ,                                                81151
         SPACE 1                                                 81137
         IKJTCB ,                                                81137
         SPACE 1                                                 81137
         IEFUCBOB ,                                              81137
JFCBSECT DSECT ,                                                 81137
         IEFJFCBN ,          INCLUDE JFCB                        81137
         SPACE 1                                                 81075
         END
