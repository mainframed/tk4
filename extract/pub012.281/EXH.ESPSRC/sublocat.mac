SUBLOCAT TITLE 'S U B L O C A T  ***  LOCATE DATA SET IN CATALOG'
***********************************************************************
**                                                                   **
**   SUBROUTINE SUBLOCAT DOES A CATALOG LOOKUP FOR THE PASSED        **
**   DATA SET NAME, AND RETURNS THE RESULT IN THE USER'S WORK AREA.  **
**                                                                   **
**                                   AUTHOR: G. POSTPISCHIL          **
***********************************************************************
**
*   R1 POINTS TO PARAMETER LIST
*   0     POINTER TO CL44 - DATA SET NAME
*   4     POINTER TO RETURN WORK AREA (265 BYTES)
*
*   RETURN CODES AS DOCUMENTED FOR LOCATE
*
*
SUBLOCAT START 0
         STM   R14,R12,12(R13)
         LR    R8,R15        SET NEW BASE
         USING SUBLOCAT,R8
         LA    R7,20+9*4(,R13)  POINT INTO USER'S SAVE AREA (R9-R12)
         USING USERSAVE,R7   MAKE THIS CODE RE-ENTRANT/REFRESHABLE
         USING PRMDSECT,R1   DECLARE THE PARM LIST
         LM    R5,R6,PRM@DSN  LOAD DSN AND WORK AREA ADDRESSES
         LA    R6,0(,R6)     KILL POSSIBLE VL BIT
         USING WRKDSECT,R6
         MVC   DYNLOC(16),PATLOC   INITIALIZE LOCATE AREA
         MVC   WRKDSN,0(R5)  PRESERVE USER'S DSN
         ST    R6,DYNLOC+4   DSN ADDRESS
         LA    R6,WRKCATBK
         ST    R6,DYNLOC+12  SET RETURN WORK AREA
         LOCATE DYNLOC       LOOK FOR IT
         LM    R1,R8,24(R13)  RELOAD SOME REGISTERS
         L     R14,12(,R13)  RELOAD RETURN ADDRESS
         BR    R14           RETURN
         SPACE 2
PATLOC   CAMLST NAME,PRM@DSN-PRM@DSN,,PRM@WORK-PRM@WORK
         LTORG ,
         SPACE 1
         PRINT NOGEN
         SPACE 1
USERSAVE DSECT ,             MY WORK AREA MAPPED ON USER'S SAVE AREA
DYNLOC   CAMLST NAME,PRM@DSN-PRM@DSN,,PRM@WORK-PRM@WORK
         SPACE 1
PRMDSECT DSECT ,             CALLER'S PARMS
PRM@DSN  DS    A             REQUESTED DATA SET NAME
PRM@WORK DS    A             ADDRESS OF WORK AREA FOR RETURN DATA
         SPACE 1
WRKDSECT DSECT ,             CALLER'S PARMS
WRKDSN   DS    CL44          ALTERED (?) DATA SET NAME
WRKCATBK DS    CL265         RETURN DATA
WRKSIZE  EQU   *-WRKDSECT
         SPACE 1
         YREGS ,
         END   ,
