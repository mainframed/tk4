PPT      TITLE 'I E F S D P P T  ***  PROGRAM PROPERTIES TABLE'
         MACRO ,
&NM      MYPPT &PROPS,&KEY=,&CPU=FFFF
         GBLB  &P@KF
         GBLC  &P@U(16),&P@Q(16)
         GBLA  &P@N
         LCLA  &I,&J,&K
         LCLC  &FG1,&FG2,&C,&QUY
         AIF   (&P@KF).NOTONE
&P@KF    SETB  1
&P@N     SETA  11
&P@U(1)  SETC  'NOCAN'
&P@Q(1)  SETC  'PPTNCNCL'
&P@U(2)  SETC  ' KEY= '
&P@Q(2)  SETC  'PPTSKEY'
&P@U(3)  SETC  'NOSWAP'
&P@Q(3)  SETC  'PPTNSWP'
&P@U(4)  SETC  'SRMPRIV'
&P@Q(4)  SETC  'PPTPRIV'
&P@U(5)  SETC  'SYSTASK'
&P@Q(5)  SETC  'PPTSYSTK'
&P@U(6)  SETC  'NODSI'
&P@Q(6)  SETC  'PPTNDSI'
&P@U(7)  SETC  'NOPSWD'
&P@Q(7)  SETC  'PPTNOPAS'
&P@U(8)  SETC  ' RSVD'
&P@Q(8)  SETC  '        '
&P@U(9)  SETC  'LEVEL2'
&P@Q(9)  SETC  'PPT2LPU'
&P@U(10) SETC  'LEVEL1'
&P@Q(10) SETC  'PPT1LPU'
&P@U(11) SETC  'NOLEVEL2'
&P@Q(11) SETC  'PPTN2LP'
.NOTONE  AIF   ('&NM' NE '').HAVENM
         MNOTE 8,'PROGRAM NAME REQUIRED IN LABEL FIELD'
         MEXIT ,
.HAVENM  ANOP  ,
&K       SETA  N'&SYSLIST
&FG1     SETC  '0'
&FG2     SETC  '0'
&QUY     SETC  '0'
         AIF   (T'&KEY EQ 'O').LLOOP
&FG1     SETC  'PPTSKEY'     USER KEY
&QUY     SETC  'PPTKEY'.'&KEY'
.LLOOP   AIF   (&J GE &K).LISTEND
&J       SETA  &J+1
&C       SETC  '&SYSLIST(&J)'
         AIF   ('&C' EQ '').LLOOP
&I       SETA  0
.OLOOP   AIF   (&I GE &P@N).BADOPT
&I       SETA  &I+1
         AIF   ('&C' NE '&P@U(&I)').OLOOP
         AIF   (&I GT 8).FLAG2
         AIF   ('&FG1' NE '0').FLAG1
&FG1     SETC  '&P@Q(&I)'
         AGO   .LLOOP
.FLAG1   ANOP  ,
&FG1     SETC  '&FG1'.'+'.'&P@Q(&I)'
         AGO   .LLOOP
.FLAG2   AIF   ('&FG2' NE '0').FLAG2B
&FG2     SETC  '&P@Q(&I)'
         AGO   .LLOOP
.FLAG2B  ANOP  ,
&FG2     SETC  '&FG2'.'+'.'&P@Q(&I)'
         AGO   .LLOOP
.BADOPT  MNOTE 4,'OPTION INVALID : &SYSLIST(&J)'
         AGO   .LLOOP
.LISTEND ANOP  ,
         DC    CL8'&NM',AL1(&FG1,&QUY),XL2'&CPU',AL1(&FG2,0,0,0)
         MEND  ,
         SPACE 2
         PRINT ON,NODATA
         SGIEF0PT ,
         END   ,
