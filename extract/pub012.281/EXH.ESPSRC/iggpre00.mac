PRE32    TITLE 'I G G P R E 0 0  ***  DADSM PRE-PROCESSING EXIT'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 1
***********************************************************************
*                                                                     *
*        COPYRIGHT 1992  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         PRINT &PRTSOR
         EJECT ,
***********************************************************************
*                                                                     *
*    IGGPRE00 ENFORCES LOCAL STANDARDS IN DATASET NAMING AND          *
*       PLACEMENT.                                                    *
*                                                                     *
*    SYSDA/SYSTS/VIO/SYSSQ :  DATASET NAME MUST BE TEMPORARY          *
*       RACF, PASSWORD FLAGS AND EXPIRATION DATE WILL BE ZEROED       *
*                                                                     *
*    LIBPA? : DATASET NAME MUST BE ACCT.NAME OR BE PRIVILEGED USER'S  *
*                                                                     *
*    TSO??? : USER.ACCT.NAME OR USER.NAME                             *
*                                                                     *
*    USER PACK : ANY NAME AT ALL UNLESS THE DUIX FLAG IS ON, THEN THE *
*       NAME MUST HAVE A REGISTERED HIGH-LEVEL INDEX AND BE VALID OS  *
*                                                                     *
*    SYSTEM PACKS:  SYS1. TYPE DATASETS ONLY; CALLER MUST BE SYSTEMS  *
*                                                                     *
*                                                                     *
*    FOR PUBLIC AND STORAGE PACKS, THE AMOUNT OF SPACE IS LIMITED.    *
*       LIMIT IS FOUND IN MODULE LEXVOLT BY DEVICE CLASS              *
*       VIO LIBPAK TSO/WYLBUR SYSDA/SYSSQ/SYSTS                       *
*                                                                     *
*                                                                     *
*    ATTRIBUTES: RENT REFR REUS AM31 RM24 (BALR TO AM24 CODE)         *
*       LOCATED IN SYS1.XAPAKLIB; LOADED VIA IEALPA?? LIST            *
*                                                                     *
*                                                                     *
***********************************************************************
IGGPRE00 SAVEM ZERO12,BASE=R12,PARM=R11,BNDRY=PAGE  OLD MVSALLOC 92341
         AIF   (NOT &MVSSP).PRESP  HUH ?
         SPLEVEL SET         SET DEFAULT SP LEVEL
         AIF   (NOT &MVSXA).PRESP
IGGPRE00 AMODE 31            ABOVE THE LINE
IGGPRE00 RMODE 24            NEED TO BALR TO OLD LOCAL CODE
.PRESP   USING IEXPL,R11     PARM LIST POINTER
         NOP   MISOK      *****DEBUG SWITCH*****                 92341
         MVC   MISSAGE(MISSDSN-MISSAGE),MESSPAT  MAKE BASIC MESSAGE
         OI    MISSROUT+3,X'24'   SET ROUTCDE=11 AND 14
         L     R3,CVTPTR     LOAD CVT                            83122
         USING CVTMAP,R3                                         83122
         ICM   R4,15,CVTUSER   GET MY EXTENSION                  83122
         BZ    MISOK         BYPASS IF NOT FOUND                 83122
         USING USERCVT,R4                                        83122
         ICM   R2,15,UCLVOLT   GET VOLUME TABLE                  83122
         BZ    MISOK         BYPASS IF NOT FOUND                 83122
         ST    R2,@VOLT      SAVE IT FOR LATER                   83122
         SPACE 1                                                 93066
         LTCB  R5,USE=YES    GET MY TCB                          93066
         ICM   R1,15,TCBTIO  GET TIOT OR OUT                     93066
         BZ    MISOK         ?                                   93066
         MVC   MISSJOB,0(R1)   PROVISIONAL JOBNAME (COULD BE INIT)
         ICM   R1,IIII,TCBUSER  HAVE A USER EXTENSION ?          93066
         BNZ   SAVETUSE      YES                                 93066
         L     R5,TCBJSTCB   GET MAIN TCB                        93066
         ICM   R1,IIII,TCBUSER  HAVE A USER EXTENSION ?          93066
         BNZ   SAVETUSE      YES                                 93066
         ICM   R5,IIII,TCBOTC  GET ORIGINATING TCB               93066
         BNZ   SAVETUSE                                          93066
         ICM   R1,IIII,TCBUSER  LOAD PRESUMED EXTENSION NOW      93066
SAVETUSE ST    R1,@TCBUSER                                       93066
         USING USERTCB,R1    DECLARE IT                          93024
         ICM   R15,15,UTA@   ANY ACCOUNT POINTER ?               93024
         BZ    NORMTUSE      NO                                  93024
         MVC   ACCTNO,0(R15)  GET CALLER'S ACCOUNT               93066
         SERVICE ACTST,ACCTNO  CHECK THE ACCOUNT                 93066
         STC   R0,ACCTPRIV   SET POTENTIAL PRIVILEGES            93024
         CH    R15,=H'4'     VALID ACCOUNT ?                     93024
         BNH   LOCLTUSE      YES; USE IT INSTEAD OF MULTI-USER TASK
NORMTUSE SERVICE ACGET,ACCTNO  GET THE ACCOUNT PRIVILEGES        93066
         STC   R0,ACCTPRIV  SAVE FOR LATER TESTS                 84009
         DROP  R5                                                93066
LOCLTUSE ICM   R15,15,IEXDSN  GET DSN                            93066
         BNZ   MOVEDSN
         MVI   MISSDSN,C' '
         MVC   MISSDSN+1(L'MISSDSN-1),MISSDSN
         B     DONEDSN
MOVEDSN  MVC   MISSDSN,0(R15)   PROV. COPY DS NAME
DONEDSN  SLR   R0,R0
         IC    R0,IEXFUNC    GET FUNCTION CODE
         BIX   BASE=IGGPRE00,ERR=GETOUT,PFX=TEST,                      *
               LOC=(,ALLO,EXT,SCR,REL,REN,PREL,SAMX)
         SPACE 1
         USING INFMJFCB,R8   DECLARE JFCB/DSCB
TESTALLO L     R8,IEXPTR1    LOAD JFCB ADDRESS
         L     R7,IEXUCB     LOAD UCB (MAY BE ZERO)
         ICM   R6,15,IEXFMT1  POINT TO DATA PORTION OF DSCB
         BNZ   DODSCB
         LTR   R8,R8         HAVE A JFCB ?                       93188
         BZ    MISOK         SHOULD NEVER HAPPEN                 93188
         USING DS1FMTID,R6   DECLARE IT
DODSCB   TM    IEXFLAG,IEXMF1+IEXFDSCB  IS THERE A JFCB ?
         BNZ   LOADUCB       DSCB                                83122
         LTR   R8,R8         IS THERE REALLY A JFCB ?            92341
         BZ    MISOK         NO; NOTHING TO CHECK                92341
         TM    IEXFLAG,IEXVIO  VIO DATASET ?
         BZ    LOADUCB       NO                                  83122
         MVC   MISSUCB,=C'VIO'  FAKE UCB NAME                    83122
         OI    LCLFLAG,LCLVIO   INDICATE VIO PROCESSING          83122
NOUCB    MVI   STARTYPE,X'0E'  TREAT AS 3380
         MVI   STARFLGS,6      SHOW TYPE PRESENT                 83122
         MVI   PROFG1,VF1SYSP+VF1SYSD+VF1JOBE+VF1FREE  TREAT VIO 83122
         MVI   PROFG2,VF2SCR+VF2VTO                      AS SYSDA
         SLR   R7,R7         ENSURE UCB POINTER REALLY ZERO
         B     SKIPUCB       DON'T LOAD UCB                      83122
         SPACE 1
LOADUCB  LTR   R7,R7         IS THERE REALLY A UCB ?
         BZ    NOUCB         NO; TREAT AS 3380
         ST    R7,STARUCBA   SET UCB ADDRESS FOR TRKCALC         83122
         MVI   STARFLGS,4    SET UCB ADDRESS PROVIDED            83122
         USING UCBOB,R7                                          83122
         MVC   MISSVOL,UCBVOLI   PROVISIONALLY COPY UCB VOL-SER
         MVC   MISSUCB,UCBNAME    AND UCB NAME                   83129
SKIPUCB  L     R2,@VOLT      RESTORE USER VOLT
         USING USERVOLT,R2                                       83122
         SPACE 1                                                 83122
*        REGARDLESS OF USER'S PRIVILEGES, THE DATASET NAME IS    83122
*        CHECKED FOR AUTOMATIC PASSWORD PROTECTION. IF SO, THE   83122
*        TYPE BITS (R/W OR W) ARE SET IN THE DSCB OR JFCB        83122
*                                                                83122
MISNDEX  MVC   HINDEX,BLANKS8    BUILD EIGHT BLANKS              85189
         LA    R15,HINDEX    POINT TO INDEX OUTPUT               85189
         LA    R14,MISSDSN   GET DATASET NAME
*OLD*    CLC   =C'WYL.',MISSDSN  OLD WYLBUR DSN ?
*OLD*    BNE   *+8           NO                                  83136
*OLD*    LA    R14,4(,R14)   SKIP THE COMMON HIGH INDEX          83136
         LA    R1,7(,R14)    END OF MAXIMUM INDEX LEVEL          83122
         LA    R0,1          CONSTANT                            83122
INXGETLP CLI   0(R14),C'.'   END OF INDEX ?                      83122
         BE    INXGETND      YES                                 83122
         MVC   0(1,R15),0(R14)  MOVE A BYTE                      83122
         AR    R15,R0        UP THE OUTPUT LOCATION              83122
         BXLE  R14,R0,INXGETLP  UP INPUT                         83122
         CLI   0(R14),C'.'   REALLY END ?                        83122
         BNE   INXNOTFN      NO; NOT VALID INDEX                 83122
INXGETND L     R1,VTAUPSWD   POINT TO INDEX/PROTECT BIT TABLE    83122
INXTSTLP TM    0(R1),X'80'   ANOTHER NAME ?                      83122
         BZ    INXNOTFN      NO; NO MATCH                        83122
         CLC   HINDEX,0(R1)  MATCHING INDEX LEVEL ?              85189
         LA    R1,9(,R1)     SET TO NEXT ENTRY                   83122
         BNE   INXTSTLP      CHECK IT                            83122
         BCTR  R1,0          BACKSPACE TO FLAG                   83122
         MVC   DB(1),0(R1)   COPY PROTECT BITS                   83122
         L     R14,=X'24300054'  SET XI AND MASK BITS            83122
         LA    R15,DS1DSIND  POINT TO DS1DSIND                   83122
         TM    IEXFLAG,IEXMF1+IEXFDSCB  DSCB PASSED ?            83122
         BNZ   INXMASK       YES                                 93182
         SRL   R14,16        ELSE SHIFT TO JFCB MASKS            83122
         LA    R15,JFCBIND2  POINT TO JFCB PROTECTS              83122
INXMASK  STCM  R14,3,DB+1    STASH MASKS                         83122
         NC    DB(1),DB+2    MASK OUT UNDEFINED BITS             83122
         MVC   AUTOPASS,DB   SAVE PASSWORD BITS, IF ANY          83136
         NC    DB+2(1),0(R15)  DID THE  USER SPECIFY ANY ?       83122
         BNZ   INXNOTFN      YES; HONOR USER'S REQUEST           83122
         OC    JFCBIND2-JFCBIND2(1,R15),DB  SET IN REQUEST       83122
INXNOTFN DS    0H            END / BYPASS OF AUTO PSWD           83122
         DROP  R2,R3,R4                                          83122
         EJECT  ,
*        LOCAL VALIDITY CHECKING
*  MSG914E     SCRATCH 2314/3330/DISK INVALID
*  MSG915E     ALLOCATION RESTRICTED TO SYSTEMS (E.G. LIBPAK)
*  MSG916E     WORK03 UP VALID ONLY IF CLASS F JOB (OBSOLETE)    83122
*  MSG917E     WORK3X VALID ONLY FOR CLASS I JOB
*  MSG918E     STORAGE REQUEST (PERM DSN ON SYSTEM PACK)
*  MSG919E     PRIVATE VOLUME NOT PERMITTED FOR NON-SETUP JOB
         SPACE 1
         LTCB  R5,USE=YES   GET CURRENT TCB
         OI    LCLFLAG,LCLCUST   SET NON-PRIVILEGED ACCOUNT NO.  83129
         SLR   R4,R4
         ICM   R4,OIII,TCBTCTB  GET 24-BIT TCT ADDRESS
         BZ    NOTBATCH      TREAT AS STC
         SPACE 1
         USING SMFTCT,R4                                         83122
         LAT   R4,TCTJMR,NOTBATCH,LA=34  GET JMR OR SET STC FLAG
         USING JMR,R4        DECLARE JMR
         MVC   MISSJOB,JMRJOB  USE JOBNAME FROM JMR              84009
         MVC   DB(1),JMRUCLAS  COPY ORIGINAL JOBCLASS
         CLI   DB,C'A'       WAS IT SUPPLIED ?                   83122
         BNL   MISJMR        YES                                 83122
         MVC   DB(1),JMRCLASS  HOPE OPERATOR HASN'T CHANGED IT ? 83122
MISJMR   CLI   DB,C'F'       CLASS F JOB ?
         BNE   *+8           NO
         OI    PROPQTY,VF1JOBF  SET FOR SECOND LOAD              81074
         CLI   DB,C'I'       CLASS I ?
         BNE   *+8           NO
         OI    PROPQTY,VF1JOBI  SET FOR SECOND LOAD              81074
         CLI   DB,C'E'       CLASS E ?
         BNE   *+8           NO
         OI    LCLFLAG,LCLJOBE  SET NON-SETUP FLAG
         B     DOGBATCH      SKIP STC SETTING                    84009
NOTBATCH OI    ACCTPRIV,VAASTC   SET START TASK FLAG             84009
         TM    ACCTPRIV,VAASTC+VAASYS+VAASUP  PRIVILEGED ?       84009
         BZ    LOOKTEMP      NO                                  84009
         OI    PROPQTY,VF1JOBF+VF1JOBI  FAKE PRIVILEGES          83261
DOGBATCH TM    ACCTPRIV,VAASYS+VAASUP  PRIVILEGED ?              84009
         BZ    LOOKTEMP      NO                                  84009
         NI    LCLFLAG,255-LCLCUST   SHOW PRIVILEGED CALLER      84009
         CLC   =C'SYSCTLG.V',MISSDSN  CATALOG ?                  93182
         BE    MISOK         VALID EVERYWHERE                    93182
         CLC   =C'SYS1.',MISSDSN  WITH MISGIVINGS                93182
         BE    MISOK                                             93182
         SPACE 1                                                 84009
LOOKTEMP CLI   MISSDSN,C'*'      IEHMOVE TEMP DSN ?
         BE    MISDSNT       YES, ASSUME TEMPORARY
         CLC   DISDSN,MISSDSN    CHECK FOR TEMPORARY DATA SET
         BNE   MISDSNC       NO
         CLC   DISDST,MISSDSN+8
         BNE   MISDSNC
         LA    R1,MISSDSN+3      GET PRESUMED DATE
         LA    R2,5          LOOK FOR 5 NUMERICS
         BALS  R9,MISNUMBS   CHECK NUMERICS                      93006
         LA    R1,MISSDSN+10     GET PRESUMED TIME
         LA    R2,6
         LA    R9,MISDSNT    RETURN FROM IN-LINE SUBROUTINE BAL
         SPACE 1
MISNUMBS CLI   0(R1),C'0'    NUMERIC ?
         BL    MISDSNC       NO
         LA    R1,1(,R1)     NEXT
         BCT   R2,MISNUMBS   TRY AGAIN
         BR    R9            RETURN TO CALLER
         SPACE 1
MISDSNT  OI    LCLFLAG,LCLDTEMP -  ASSUME TEMP. DSN
MISDSNC  TM    LCLFLAG,LCLVIO  VIO REQUEST ?                     83136
         BNZ   MISNDEV       YES; NO UCB                         83122
         LTR   R7,R7         IS THERE A UCB POINTER ?
         BZ    MISNDEV       NO; SKIP UCB VOLI CHECK
         LA    R4,UCBVOLI    GET UCB VOLUME SERIAL, IF ANY       83122
         BALS  R9,MISVOLID   GO TO CHECK IT                      93006
MISNDEV  TM    IEXFLAG,IEXMF1+IEXFDSCB  ANY JFCB ?
         BNZ   MISDELT       NO; SKIP JFCB VOL-SER CHECK
         CLI   JFCBNVOL,1    ANY VOL-SER ENTRIES ?               83122
         BNL   MISJFLPI      YES
         OI    LCLFLAG,LCLVSTO   ASSUME SCRATCH DISK REQUEST
         B     MISDELT
         SPACE 1
MISJFLPI LA    R4,JFCBVOLS   POINT TO FIRST JFCB VOL-SER
         SLR   R5,R5         CLEAR
         LA    R0,5          SET CONSTANT - MAX VOLSERS IN UCB   84009
         ICM   R5,1,JFCBNVOL   GET NO. OF VOLUMES                84009
         BP    *+8           NON-ZERO                            84009
         LA    R5,1          ELSE FORCE ONE                      84009
         CR    R5,R0         SEE IF MORE THAN 5                  84009
         BNH   *+6           NO, OK                              84009
         LR    R5,R0         SET TO PROCESS ONLY FIRST 5 SERIALS 84009
         CLI   JFCBVOLS,X'40'   ANY VOL-SER IN JFCB ?
         BNH   LCLJFVL       NO, LEAVE UCB NAME / VOL AS IS
         CLI   JFCBVOLS,C'9'   SCRATCH ?
         BH    LCLJFVL       YES, LEAVE IT
         MVC   MISSVOL,JFCBVOLS   ELSE COPY JFCB VOL
LCLJFVL  BALS  R9,MISVOLID   CHECK THIS ONE                      93006
         LA    R4,6(,R4)
         BCT   R5,LCLJFVL    AND REPEAT FOR NEXT ONE
         SPACE 1
MISDELT  TM    ACCTPRIV,VAASTC   START TASK ?                    84009
         BZ    MISBATCH      NO                                  84009
*                                                                84009
*   THIS CODE SUPPLIED BY YOUR FRIENDLY LOCAL DIAPER SERVICE
*
         TM    PROFG2,VF2DUIX  USER'S CONTROLLED PACK ?
         BNZ   MISBATCH      YES; CHECK DATASET AND CALLER LATER 93011
         TM    LCLFLAG,LCLCUST   PRIVILEGED TASK ?               84009
         BZ    MISBATCH     YES; PERMIT VIRTUALLY EVERYTHING     84009
         TM    LCLFLAG,LCLDTEMP+LCLVSTO  TEMP/STORAGE REQUEST ?  84009
         BNZ   MISBATCH      YES; CHECK                          84009
         TM    PROFG1,VF1SYSP+VF1SYSD  CONTROLLED PACK ?         84009
         BZ    MISBATCH      NO                                  84009
         TM    PROFG2,VF2DSNA+VF2DSN1+VF2DSNT  RESTRICTED ?
         BNZ   MISBATCH      YES; CHECK                          84009
         TM    PROFG1,VF1JOBF+VF1JOBI  SPECIAL WORK PACK ?       84009
         BZ    MISCHEK2      NO; BYPASS MOST CHECKS              84009
         SPACE 1                                                 84009
MISBATCH LA    R1,BADSN+FAIL SET INVALID NAME                    84009
         TM    LCLFLAG,LCLDTEMP  TEMP DSN ?                      81082
         BNZ   MISNAMET      YES; SKIP CHECK                     81082
         TM    PROFG2,VF2DSNA+VF2DSN1+VF2DSNT+VF2DUIX CHECK NAME?
         BZ    MISNAMED      NO
         CLC   =C'SYS1.DFDSS.',MISSDSN  DEFRAG OR SOMETHING ?    93067
         BE    MISOK         BOO                                 93067
         SERVICE DSTST,MISSDSN  CHECK DSN FOR OS FORMAT
         LA    R1,BADSN+FAIL RESTORE ERROR CODE
         BXH   R15,R15,MISBOMB  FAIL IN BIG WAY
MISDSSU  LA    R14,MISSDSN   POINT TO START
         LA    R15,AINDEX    POINT TO LOCAL COPY
         MVC   AINDEX,BLANKS8  CLEAR                             92341
         LA    R0,8          MAX TO CHECK
MISDSNXL CLI   0(R14),C'.'   INDEX POINT ?
         BE    MISDSNXW      YES; QUIT
         MVC   0(1,R15),0(R14)  COPY ONE
         LA    R14,1(,R14)   ADVANCE
         LA    R15,1(,R15)
         BCT   R0,MISDSNXL   REPEAT
         CLI   0(R14),C'.'   INDEX POINT NOW ?
         BE    MISDSNXW      YES
         CLI   0(R14),C' '   END OF NAME ?
         BNE   MISBOMB       MALFORMED (DOS?) DATASET NAME
MISDSNXW L     R3,CVTPTR     MAKE SURE OF CVT ADDRESS
         ICM   R3,IIII,CVTUSER-CVTMAP(R3)  RELOAD USER CVT
         BZ    MISNAMED      UNINITIALIZED
         ICM   R3,IIII,UCLINDX-USERCVT(R3)  GET INDEX TABLE
         BZ    MISNAMED      ALLOW
         LM    R3,R5,UINDBXLE-USERINDX(R3)  GET BXLE POINTERS
         SR    R3,R4         BACK-UP ONE ENTRY
MISDSNXH BXH   R3,R4,MISBOMB  NO INDEX RECORD LOCATED
         USING UINDEX,R3     DECLARE USER RECORD
         CLC   AINDEX,UINDEX  REQUESTED INDEX ?
         BL    NOTHIX        ***MUST BE SORTED***
         BNE   MISDSNXH      NO
*DEFER*  LA    R2,UINDACT1   POINT TO FIRST ACCOUNT
*        SLR   R0,R0
*        ICM   R0,OOOI,UIND#ENT  GET ENTRIES IN THIS SLOT
*        BZ    MISDSNXH      INVALID ENTRY
*        MIN   R0,=H'4',TYPE=H  NOT OVER DESIGN LIMIT ?
*ISDSNLP CLI   UINDACT1-UINDACT1(R2),0  ANY ENTRY ?
*        BE    MISDSNMP      NO; SKIP
*        TM    UINDACF1-UINDACT1(R2),A$XAFUID  NAME ENTRY ?
*        BNZ   MISDSNID      YES; TEST IT
*        TM    A$SCNTL,A$SAKEY  ACCOUNT PASSED ?
*        BZ    MISDSNMP      NO; SKIP
*        LA    R1,A$SACCT    POINT TO PASSED ACCOUNT
*        BALS  R14,CMPACCT   GO TO COMPARE
*        BE    MISDSNCC      MATCH - NOW CHECK ACCESS REQUEST
*        B     MISDSNMP
*ISDSNID TM    A$SCNTL,A$SUKEY  USER-ID PASSED ?
*        BZ    MISDSNMP      NO; DON'T COMPARE
*        LA    R1,A$SUID     POINT TO PASSED ID
*        BALS  R14,CMPACCT   GO TO COMPARE
*        BNE   MISDSNMP      NO MATCH
*ISDSNCC MVC   DB(1),A$SACES  MOVE ACCESS REQUESTED
*        NI    DB,A$XAFRD+A$XAFRDW+A$XADELT+A$XALLOC  MASK
*        MVC   DB+1(1INDACF1-UINDACT1(R2)
*        NI    DB+1,A$XAFRD+A$XAFRDW+A$XADELT+A$XALLOC  MASK
*        NC    DB+1(1),DB    HAVE ALL DESIRED PRIVILEGES ?
*        CLC   DB+1(1),DB
*        BE    MISNAMED      EXIT WITH PERMISSION
*ISDSNMP LA    R2,UINDACT2-UINDACT1(,R2)  NEXT ENTRY
*        BCT   R0,MISDSNLP
*        B     MISDSNXH      TRY ANOTHER ENTRY IN THE TABLE
         B     MISNAMED      NAME PASSED; CONTINUE CHECKING
***********************************************************************
*   NOT REGISTERED HIGH-LEVEL INDEX; SEE IF IT IS A USER-ID           *
NOTHIX   L     R3,CVTPTR     JUST IN CASE                        85189
         L     R3,CVTUSER-CVTMAP(,R3)  GET USER EXTENSION        85189
         ICM   R3,15,UCLUADS-USERCVT(R3)  TEST FOR UADS ENTRY    85189
         BZ    MISNAMED      NONE; CONTINUE CHECKING             92342
         LM    R15,R1,0(R3)  LOAD BXLE POINTERS                  85189
         USING UUADUID,R15   YES; TOO BAD                        85189
LOOPUID  CLC   AINDEX,UUADUID  MATCH ?
         BE    MISNAMED      YES; PERMIT                         85189
         BL    FAILUID       ***SORTED***                        92342
         BXLE  R15,R0,LOOPUID   TRY AGAIN                        93011
FAILUID  CLI   AINDEX+3,C' '  AT LEAST FOUR ?                    92342
         BNH   FAILDSN       NO                                  92342
         CLI   AINDEX+4,C' '  ONLY FOUR ?                        92342
         BH    FAILDSN                                           92342
         MVC   DB(4),=X'C0F0F0F0'  ACCOUNT ZONE MASK             92342
         NC    DB(4),AINDEX  MASK                                92342
         CLC   DB(4),=X'C0F0F0F0'  ACCOUNT INDEX ?               92342
         BE    MISNAMED      YES; CONTINUE CHECKING              92342
FAILDSN  LA    R1,BADSN+FAIL INVALID DSN                         85189
         B     MISBOMB       FAIL IT                             85189
MISNAMET TM    PROFG1,VF1SYSD  TEMPORARY ON SYSDA ?              92341
         BNZ   SPACEK        YES; CHECK SPACE                    92341
         TM    PROFG1,VF1STOR    STORAGE VOLUME ?                81082
         BZ    MISNAMED      NO; CHECK FURTHER                   81082
         TM    PROFG2,VF2ALLO+VF2DSNA+VF2DSN1+VF2DSNT  RESTR.?
         BNZ   MISBOMB       YES; FAIL                           81082
         TM    PROFG1,VF1JOBF+VF1JOBI  SPECIAL WORK PACK ?       81082
         BZ    MISBOMB       NO; FAIL                            81082
MISNAMED TM    LCLFLAG,LCLVSTO    STORAGE REQUEST ?              79323
         BZ    MISCHEK1      NO
         TM    PROFG2,VF2DUIX  USER VOLUME ASSIGNED ?
         BNZ   MISXCTL       YES - GO TO NAME CHECK              93011
         TM    PROFG2,VF2DSNT  TSO NAME CHECKING ?               85189
         BNZ   MISUADS       YES; DO IT                          85189
         TM    PROFG1,VF1SYSP+VF1SYSD+VF1STOR  STORAGE PACK ?    81082
         BNZ   MISCHEK1      OK
MISBOM14 MVI   MISSVOL,C' '  SET FOR SCRATCH
         LA    R1,14+FAIL1   SET ERROR CODE
         B     MISBOMB       TOO BAD
MISCHEK1 TM    LCLFLAG,LCLCUST   SYSTEMS JOB ?
         BZ    MISOK         YES, ANYTHING ELSE GOES
         LA    R1,15+FAIL1   SET ERR. CODE
         TM    PROFG2,VF2ALLO  RESTRICTED ALLOCATION ?           81074
         BNZ   MISBOMB       YES, INVALID REFERENCE
MISCHEK2 TM    PROFG1,VF1SYSD  SYSDA REQUEST ?                   84009
         BNZ   MISXCTL       YES; GO TO CHECK SIZE
         TM    PROFG2,VF2DSNT  TSO/WYLBUR NAME CHECKING ?
         BNZ   MISUADS       YES; DO IT                          85189
         TM    PROFG2,VF2DSNA  LIBPAK TYPE (ACCT.NAME)
         BZ    MISOK         NO; ACCEPT THE REQUEST
MISXCTL  B     SPACEK        GO TO SYSDA/SPACE CHECKING          83122
         SPACE 2                                                 85189
*        CHECK TSO USERID                                        85189
*                                                                85189
MISUADS  L     R3,CVTPTR     JUST IN CASE                        85189
         L     R3,CVTUSER-CVTMAP(,R3)  GET USER EXTENSION        85189
         ICM   R3,15,UCLUADS-USERCVT(R3)  TEST FOR UADS ENTRY    85189
         BZ    MISOK         NONE; SKIP CHECKING                 85189
         LM    R15,R1,0(R3)  LOAD BXLE POINTERS                  85189
         USING UUADUID,R15   YES; TOO BAD                        85189
LOOPUADS CLC   HINDEX,UUADUID  MATCH ?
         BL    FAILUADS      ***SORTED***                        92342
         BE    SPACEK        YES; CHECK FOR SPACE REQUIRED       92342
         BXLE  R15,R0,LOOPUADS  TRY AGAIN                        85189
FAILUADS LA    R1,BADSN+FAIL INVALID DSN                         85189
         B     MISBOMB       FAIL IT                             85189
         DROP  R15
         SPACE 2
MISVOLID MVC   INVOL,0(R4)   MOVE VOL-SER
         L     R15,@VOLT     GET ENTRY POINT ADDRESS
         SLR   R0,R0         ANY MATCH
         STH   R0,INFLTEST   CLEAR FLAGS
         LA    R1,INVOL      POINT TO LIST
         BALSR R14,R15       LOOK FOR VOLUME
         OC    PROFG,INFLTEST  MERGE FLAGS                       81074
         LTR   R15,R15       0 NO MATCH; - SPECIFIC; + GROUP     92306
         BZ    MISUSER       NOT FOUND; SKIP LOCAL CHECKING
*        LTR   R1,R1         CHECK RETURN TYPE
*        BNM   LCLNOVS       NOT SPECIFIC VOLUME ENTRY
*        USING UVOLSER,R1    DECLARE IT
*        DROP  R1
LCLNOVS  TM    PROFG1,VF1JOBF  RESTRICTED TO CLASS F ?           81074
         BZ    LCLNOTF       NO
         LA    R1,16+FAIL1   SET ERROR CODE
         TM    PROPQTY,VF1JOBF   CLASS F JOB ?                   81074
         BZ    MISBOMB       NO; TOO BAD
LCLNOTF  TM    PROFG1,VF1JOBI  RESTRICTED TO CLASS I ?           81074
         BZ    LCLNOTI       NO
         LA    R1,17+FAIL1   SET ERROR CODE
         TM    PROPQTY,VF1JOBI   CLASS I JOB ?                   81074
         BZ    MISBOMB       NO
LCLNOTI  TM    LCLFLAG,LCLCUST   PRIVILEGED CALLER ?
         BZR   R9            YES; SKIP EXTRA CHECKS
         TM    PROFG1,VF1SYSP+VF1SYSG+VF1SYSD+VF1STOR+VF1JOBF+VF1JOBI
         BZ    LCLNOTK       SKIP IF NOT COMPUTER CENTER PACK
         TM    PROFG1,VF1SYSD  SYSDA REQUEST ?                   84327
         BZ    CHKEXPDA      NO                                  84327
         TM    LCLFLAG,LCLDTEMP  TEMP DATASET ?                  84327
         BNZ   KILLEXP       YES; ZERO EXPIRATION DATE           84327
CHKEXPDA TM    PROFG2,VF2DSNT  WYLBUR DATASET ON WYLBUR PACK ?   84327
         BZ    CHKEXPWY      NO                                  84327
KILLEXP  LA    R1,JFCBXPDT   POINT TO JFCB EXPIRATION DATE       84327
         TM    IEXFLAG,IEXMF1+IEXFDSCB  JSCB ?
         BZ    *+8           JFCB                                84327
         LA    R1,DS1EXPDT   DSCB                                84327
         XC    JFCBXPDT-JFCBXPDT(3,R1),DS1EXPDT-DS1EXPDT(R1)     84327
CHKEXPWY TM    ACCTPRIV,VAASTC   START TASK ?                    84327
         BZ    LCLBATCH      NO                                  84009
         TM    LCLFLAG,LCLDTEMP+LCLVSTO  TEMP/STORAGE REQUEST ?  84009
         BNZ   LCLBATCH      YES; CHECK                          84009
         TM    PROFG1,VF1SYSP+VF1SYSD  CONTROLLED PACK ?         84009
         BZ    LCLBATCH      NO                                  84009
         TM    PROFG2,VF2DSNA+VF2DUIX+VF2DSN1+VF2DSNT  RESTRICTED ?
         BNZ   LCLBATCH      YES; CHECK                          84009
         TM    PROFG1,VF1JOBF+VF1JOBI  SPECIAL WORK PACK ?       84009
         BZR   R9            NO; BYPASS MOST CHECKS              84009
LCLBATCH CLI   AUTOPASS,0    AUTOMATIC PASSWORD PROTECTION ?     84009
         BNE   LCLNOTK       YES; PSWD PERMITTED                 83136
         LA    R1,25+FAIL1   SET ERROR CODE - PSWD NOT PERMITTED
         LA    R15,JFCBIND2  POINT TO JFCB INDICATOR BYTE
         TM    IEXFLAG,IEXMF1+IEXFDSCB  JFCB ?
         BZ    *+8           YES
         LA    R15,DS1DSIND  POINT TO DS1DSIND
         TM    0(R15),X'10'  PASSWORD PROTECT REQUESTED ?
         BNZ   MISBOMB       YES - TOO BAD
LCLNOTK  TM    LCLFLAG,LCLDTEMP  PERMANENT DATASET REQUEST ?
         BNZ   LCLNOTP       NO
         LA    R1,18+FAIL    SET ERROR CODE
         TM    PROFG1,VF1STOR  STORAGE PERMITTED ?               81074
         BNZ   LCLNOTP       OK
         TM    PROFG1,VF1SYSP+VF1SYSG+VF1SYSD  STORAGE PROHIBITED ?
         BNZ   MISBOMB       TOO BAD
MISUSER  DS    0H
LCLNOTP  TM    LCLFLAG,LCLJOBE+LCLCUST  CLASS E/NON-SYS JOB ?
         BNO   LCLNOTE       NO
         LA    R1,19+FAIL    SET ERROR CODE
         TM    PROFG1,VF1SYSP+VF1SYSD+VF1JOBE  PERMITTED ?       81074
         BZ    MISBOMB       NO
LCLNOTE  EQU   *
         BR    R9
         DROP  R5,R4
         TITLE 'I G G P R E 0 0  ***  SYSDA AND SPACE AMOUNT CHECK'
*        LOCAL VALIDITY CHECKING
*  MSG921E     SPACE EXCEEDS MAXIMUM FOR SINGLE DSN
HIGH1    EQU   21            ERROR CODE FOR ABOVE
*  MSG922E     CUMULATIVE STEP SYSDA EXCEEDS MAXIMUM
HIGHM    EQU   22            ERROR CODE FOR ABOVE
*  MSG923E     INVALID DSNAME
BADSN    EQU   23            ERROR CODE FOR ABOVE
*  MSG924E     NON-SUPPORTED ACCESS METHOD (ISAM ON WYLXXX PACK)
BADORG   EQU   24            ERROR CODE FOR ABOVE
FAIL     EQU   8*256         FAIL THIS REQUEST FOR ALL VOLUMES
FAIL1    EQU   4*256         FAIL THIS REQUEST FOR CURRENT VOLUME
         SPACE 1
SPACEK   TM    LCLFLAG,LCLVIO   VIO REQUEST ?                    83122
         BZ    SPACGUCB      NO; GET UCB                         83122
         IC    R15,STARTYPE  LOAD SAVED TYPE                     83122
         B     SPACCUCB      REJOIN COMMON                       83122
SPACGUCB IC    R15,UCBTBYT4  GET THE DISK SUB-TYPE               83122
SPACCUCB LA    R14,X'0F'     MASK FOR SUB-TYPE INDEX             83122
         NR    R15,R14       GET INDEX
         L     R14,CVTPTR    GET CVT
         L     R14,CVTZDTAB-CVTMAP(,R14)    GET CVT DEVICE POINTER
         IC    R15,0(R15,R14)   GET INDEX TO DEVICE ENTRY
         AR    R14,R15       GET ENTRY FOR THIS TYPE
         MVC   DS4DEVSZ,0(R14)   MOVE CYL/PACK + TRK/CYL         84327
         SPACE 1
         LA    R5,MISSDSN    POINT TO NAME TO BE CHECKED
         LA    R4,L'JFCBDSNM AND NUMBER OF BYTES TO CHECK
         LA    R1,BADSN+FAIL PROVISIONALLY SET BAD DSN MESSAGE NUMBER
         LA    R10,VTSPOWYL   SET SIZE FOR WYLBUR                83122
         TM    PROFG2,VF2DSNT  REALLY TSO/WYLBUR REQUEST ?
         BNZ   SETWYLTS      YES; GO TO CHECK NAME
         LA    R10,VTSPOLIB   SET FOR LIBPAK                     83122
         TM    PROFG2,VF2DSNA  BUT IS IT ?                       81074
         BNZ   SETWYLTS      YES; DO COMMON NAME CHECK
         LA    R10,VTSPOSDA   ELSE SET FOR NORMAL BATCH          83122
         TM    LCLFLAG,LCLVIO    VIO REQUEST ?                   83122
         BZ    *+8           NO                                  83122
         LA    R10,VTSPOVIO-VTSPOSDA(,R10)  YES; BUMP            83122
         TM    PROPQTY,VF1JOBF+VF1JOBI  SPECIAL ?                83122
         BZ    SETSIZE       NO
         LA    R10,VTSPOSDI-VTSPOSDA(,R10)  YES; SET BIGGER      83122
         B     SETSIZE       SET SIZES
         SPACE 1
SETWYLTS LA    R15,JFCDSORG  POINT TO JFCB DSORG
         TM    IEXFLAG,IEXMF1+IEXFDSCB  JFCB ?
         BZ    *+8           YES
         LA    R15,DS1DSORG  ELSE USE DS1DSORG
         CLI   1(R15),0      VALID ?
         BNE   ORGBAD        NO
         TM    0(R15),X'BD'   ISAM/UNMOVABLE/ETC. ?
         BZ    SETWYLLP      NEITHER - PROCEED
         CLI   0(R15),JFCORGDA    DIRECT ACCESS ?                81082
         BNE   ORGBAD        NO; FAIL IT                         81082
         TM    JFCRECFM-JFCDSORG(R15),JFCUND  RECFM=U ?          82209
         BNO   ORGBAD        NO; FAIL IT                         81082
         LH    R0,JFCBLKSI-JFCDSORG(,R15)  GET BLOCKSIZE         81082
         SH    R0,=H'4'                                          81082
         CH    R0,JFCLRECL-JFCDSORG(,R15)  SAS CONVENTION ?      81082
         BE    SETWYLLP      YES; PERMIT IT                      81082
ORGBAD   LA    R1,BADORG+FAIL  SHOW BAD DSORG
         B     MISBOMB       AND EXIT WITH ERROR
SETWYLLP LA    R14,1         CONSTANT
         SLR   R0,R0         CLEAR INDEX LEVEL COUNTER
DSNCHK1  CLI   0(R5),C'0'    IS FIRST DIGIT OF INDEX LEVEL ALPHA ?
         BNL   MISBOMB       NO; TOO BAD
         CLI   0(R5),C'$'    PRESUMABLY VALID ?
         BL    MISBOMB       NO
         LA    R15,8         EIGHT BYTES PER INDEX
DSNCHK2  CLI   0(R5),C'.'    END OF INDEX ?                      79323
         BL    GETLEVEL      LOW; EMBEDDED GARBAGE OR BLANK      79323
         BH    DSNCHK3       DATA; COUNT BYTES
         AR    R0,R14        UP COUNTER OF INDEX LEVELS
         LA    R5,1(,R5)     NEXT INPUT
         CLI   0(R5),C'.'    DOUBLED UP ?
         BE    MISBOMB       YES; TOO BAD
         BCT   R4,DSNCHK1    SHOULDN'T FALL THROUGH
         B     MISBOMB       BUT IF IT DOES - FAIL IT
DSNCHK3  CLI   0(R5),C'A'    ALPHANUMERIC ?                      79323
         BNL   DSNCHK4       HOPE SO                             79323
         CLI   0(R5),C'$'    FANCY ?                             79323
         BE    DSNCHK4                                           79323
         CLI   0(R5),C'#'                                        79323
         BE    DSNCHK4                                           79323
         CLI   0(R5),C'@'                                        79323
         BNE   MISBOMB       TOO BAD                             79323
DSNCHK4  LA    R5,1(,R5)     NEXT INPUT                          79323
         SR    R15,R14       VALID LENGTH ?
         BM    MISBOMB       NO
         SR    R4,R14        MORE TO DO ?
         BP    DSNCHK2       YES, CONTINUE COUNTING
GETLEVEL LA    R15,7         SET MAXIMUM PERMITTED WYLBUR LEVELS -1
         TM    PROFG2,VF2DSNT  BUT IS THIS REALLY TSO/WYLBUR ?
         BNZ   *+8           YES; OK
         LA    R15,2         ELSE PERMIT ONLY TWO ON LIBPAK
         CR    R0,R15        VALID NUMBER OF LEVELS ?
         BH    MISBOMB       NO; TOO BAD (MINIMUM ALREADY CHECKED)
SETSIZE  L     R2,@VOLT      GET VOLUME TABLE BACK               83122
         USING USERVOLT,R2                                       83122
         A     R10,VTSPCLIM  GET SPACE LIMITS                    83122
         MVC   SPACTMAX(8),0(R10)  COPY MAX SIZE, # EXTENTS      83122
         TM    PROFG2,VF2DSN1  SYSTEMS ?                         93024
         BNZ   SETSIZE2      YES; ENFORCE                        93024
***********************************************************************
*                                                                     *
*   THE CURRENT VERSION OF DFDSS (MODULE ADRDSSU) ALLOCATES ALL       *
*      AVAILABLE SPACE ON A PACK. TO AVOID FAILING VALID REQUESTS,    *
*      THE SPACE CHECK IS BYPASSED (CRIME PAYS ?)                     *
*                                                                     *
***********************************************************************
         L     R15,PSATOLD-PSA  GET THE CURRENT TCB              93024
         USING TCB,R15                                           93024
         L     R15,TCBJSTCB  DFDSS USES SUBTASKING               93024
         LAT   R15,TCBRBP,SETSIZE2  GET RB CHAIN OR OUT          93024
         B     SETSRB2       LOOK AT IT                          93024
         USING RBBASIC,R15                                       93024
SETSRB1  TM    RBSTAB2,RBTCBNXT  DOES RB POINT TO TCB ?          93024
         BNZ   SETSIZE2      YES - EXIT                          93024
         LAT   R15,RBLINK,SETSIZE2 GET NEXT RB                   93024
SETSRB2  TM    RBSTAB1,RBFTP   IS THIS A PRB ?                   93024
         BNZ   SETSRB1       NO; SKIP IT                         93024
         LAT   R1,RBCDE,SETSRB1,LA=0   GET CDE, CLR HIGH, OR DO NEXT
         USING CDENTRY,R1                                        93024
         CLC   =CL8'FASTDSS ',CDNAME  LOCAL DSS FRONT-END ?      93182
         BE    MISOUT        YES; SKIP SPACE CHECK               93182
         CLC   =CL8'ADRDSSU ',CDNAME  DFDSS MODULE NAME PREFIX ? 93024
         DROP  R1,R15                                            93024
         BNE   SETSRB1       NO; LOOK AGAIN                      93024
         B     MISOUT        YES; SKIP SPACE CHECK               93024
         SPACE 2
SETSIZE2 TM    IEXFLAG,IEXMF1+IEXFDSCB  JFCB ENTRY ?
         BZ    HAVEJFCB
         L     R1,IEXFMT1    PRESUME PARTIAL DSCB1               93164
         SH    R1,=Y(DS1FMTID-DS1DSNAM)  MAKE PSEUDO-DSCB1 ADDR  93164
         L     R2,IEXFMT3    GET FORMAT-3 POINTER                93164
         STM   R1,R2,TRKALC+4  MAKE PARAMETER LIST               93164
         SERVICE DVEXT,TRKALC  GET SPACE NEEDED                  93164
         CH    R15,=H'4'     CODE WORSE THAN 4 (NEED DSCB3) ?    93164
         BH    MISOUT                                            93164
         B     HOWHOW        USE AMOUNT RETURNED IN R0           93164
         SPACE 1
HAVEJFCB TM    PROFG2,VF2DSNA   LIBPAK ALLOCATION ?              81074
         BZ    NOTWYLJF      NO
         XC    JFCBSQTY(3),JFCBSQTY    SECONDARIES NOT HONORED
NOTWYLJF LM    R0,R1,JFCBPQTY     LOAD PRIMARY AND SECONDARY REQUESTS
         SRL   R0,8          GET RID OF FLAGS
         SRL   R1,8          DITTO
         MH    R1,SPACTEXT+2   MULTIPLY BY PROB. # OF EXTENTS    92342
         AR    R0,R1         TOTAL REQUEST (?)
         LTR   R10,R0        SAVE FOR CALCULATION
         BZ    HOWMUCH       SKIP CALC IF ZERO
         SPACE 1
         TM    JFCBCTRI,X'C0'  CHECK REQUEST TYPE
         BZ    HOWMANY       ABSOLUTE - JUST CHECK
         BO    CYLCCALC      REQUEST IN CYLINDERS
         TM    JFCBCTRI,X'40'   AVERAGE BLOCK SIZE ?
         BZ    ROUNTEST      NO; SEE IF ROUND REQUESTED
*        BNZ   AVRBLOCK      YES; COMPUTE REQUIRED SPACE
         SPACE 2                                                 84009
*        THE ORIGINAL SPACE CALCULATIONS HAVE BEEN REPLACED BY A FAIRLY
*        EXPENSIVE SVC CALL TO PROVIDE DEVICE INDEPENDENCE.      84009
         SPACE 1                                                 84009
AVRBLOCK NI    STARFLGS,STARDTU   RESET OPTIONS                  85312
         OI    STARFLGS,STARFUNC  CALCULATE RECORDS/TRACK        84009
         MVC   STARKL,JFCKEYLE   SET KEY LEN                     84009
         MVC   STARDL,JFCBDRLH+1   SET DATA LENGTH               84009
         CLI   JFCBDRLH,0    VALID LENGTH FIELD ?                84009
         BNE   AVRBAD        NO; TREAT AS 1 PER TRACK            84009
         SERVICE DVCAP,TRKALC   GET RECORDS PER TRACK            84009
         BXH   R15,R15,AVRBAD   ERROR ?                          84009
         LTR   R0,R0         ANY RETURNED ?                      84009
         BP    AVRGOOD       YES; USE THE NUMBER                 84009
AVRBAD   LA    R0,1          FORCE TO ONE RECORD PER TRACK       84009
AVRGOOD  LR    R2,R10        GET THE NUMBER OF BLOCKS REQUESTED  84009
         SRDA  R2,32         PREPARE FOR DIVIDE                  84009
         DR    R2,R0         GET NUMBER OF TRACKS                84009
         LTR   R2,R2         ANY REMAINDER ?                     84009
         BZ    *+8           NO                                  84009
         AH    R3,=H'1'      UP TRACK COUNT                      84009
         LR    R0,R3         COPY BACK TO COMMON REGISTER        84009
*
* A TEST IS MADE TO SEE IF THE ROUND TO CYLINDER OPTION IS SPECIFIED
*
ROUNTEST TM    JFCBCTRI,X'01'    IS ROUND OPTION REQUESTED ?
         BNZ   ROUNDUP                BRANCH IF YES
         TM    JFCBSPTN,X'FF'           TEST FOR SHARED CYLINDERS
         BNM   HOWHOW        NOT SHARED
ROUNDUP  LR    R5,R0        LOAD NBR OF TRACKS REQUESTED
         SLR   R4,R4        CLEAR HIGH DIVIDEND REGISTER
         LH    R2,DS4DEVSZ+2      GET THE NBR OF TRKS/CYL
         AR    R5,R2         CHANGE TRUNCATE
         BCTR  R5,0          INTO ROUND
         DR    R4,R2        CONVERT TRK QTY TO CYLS/TRKS
         LR    R0,R5        MOVE REQUEST TO COMM REGISTER
         SPACE 1
CYLCCALC MH    R0,DS4DEVSZ+2  CONVERT CYLINDERS TO TRACKS
HOWHOW   LR    R10,R0        SAVE IT
         SPACE 2
HOWMANY  TM    JFCBSPTN,X'FF'           TEST FOR SHARED CYLINDERS
         BM    SHARDCYL               BRANCH IF SHARED
         ICM   R0,7,JFCBSBNM  IS THIS A SUBALLOC REQUEST ?       83122
         BNZ   MISOUT        YES; IGNORE IT - SHOULD HAVE BEEN DONE
         B     HOWMUCH
         SPACE 2
SHARDCYL LR    R1,R10       GET TRACKS REQUESTED
         SLR   R0,R0
         SLR   R2,R2        CLEAR HIGH DIVIDEND REGISTER
         SLR   R3,R3
         IC    R3,JFCBSPTN        PICK UP TRKS/PERCENT
         TM    JFCBCTRI,X'C0'           CHECK FOR N = TO NBR OF TRACKS
         BO    INTRACKS               BRANCH IF N IS IN TRACKS
         MH    R3,DS4DEVSZ+2      MULTIPLY BY TRACKS/CYLINDER
         LA    R4,100
         DR    R2,R4        CONVERT PERCENT TO TRACKS
INTRACKS LTR   R3,R3         AT LEAST ONE TRACK ?
         BZ    MISOUT        NO; DON'T DIVIDE BY ZERO
         LR    R0,R10        SET DATASET SIZE TO CHECK INTO 0
         LR    R10,R3        SET NEW CUMULATIVE SIZE
         B     HOWMUCHS      COMPARE TO LARGEST VALID SIZE
         SPACE 1
HOWMUCH  LR    R0,R10        GET AMOUNT REQUESTED IN TRACKS
HOWMUCHS C     R0,SPACTLIM   COMPARE TO SINGLE MAXIMUM           92342
         BNH   HOWMUCH1      OK
         LA    R1,HIGH1+FAIL SET ERROR CODE FOR SIZE EXCESSION
         B     MISBOMB       ISSUE ERROR MESSAGE                 83122
HOWMUCH1 TM    PROFG2,VF2DSNA+VF2DSNT OTHER THAN SYSDA ?         81074
         BNZ   MISOUT        YES; CHECK FOR TYPE OF EXIT
         LA    R1,HIGHM+FAIL SET MANY HIGHER
         LAT   R2,@TCBUSER,MISOUT,LA=34 TCB USER OR OUT
         LA    R14,UTDA-USERTCB(,R2)  GET POINTER                83122
         TM    LCLFLAG,LCLVIO   VIO REQUEST ?                    83122
         BZ    *+8           NO                                  83122
         LA    R14,UTVA-USERTCB(,R2)  SET VIO COUNTER            83122
         LR    R15,R10       COPY AMOUNT                         83122
         STM   R14,R15,SPACTADR   SAVE FOR LATER UPDATE          83122
         LA    R9,FAIL/256   SET FOR TOTAL FAILURE               92342
         L     R3,0(,R14)    GET CURRENT CUMULATIVE TOTAL        83122
         C     R3,SPACTMAX   ALREADY OVER LIMIT ?                83122
         BH    QUITTER       YES; FAIL WITHOUT MESSAGE           92342
         A     R3,SPACTAMT   ADD AMOUNT FOR THIS ONE             92342
         LA    R1,HIGHM+FAIL SET ERROR CODE                      83122
         C     R3,SPACTMAX   OVER LIMIT ?                        92342
         BH    MISBOMB       RETURN ERROR AND MESSAGE            92342
         TITLE 'I G G P R E 0 0  ***  TSO/WYLBUR ALLOCATION CHECK'
*        LOCAL VALIDITY CHECKING
*  MSG923E     INVALID DSNAME/VOLUME
*  MSG924E     INVALID DSORG
         SPACE
MISOUT   TM    PROFG2,VF2DSNT  TSO/WYLBUR REQUEST ?              93066
         BZ    MAKEMESS      NO; GET OUT NOW                     93066
         TM    IEXFLAG,IEXMF1+IEXFDSCB  HAVE A JFCB ?            93066
         BNZ   MAKEMESS      NO; SKIP DSCB CHECK (FOR NOW)       93066
         LA    R1,FAIL+BADORG    PRESET FOR BAD DSORG            93066
         TM    JFCDSORG,255-JFCORGPO-JFCORGPS  SEQUENTIAL ?      93066
         BNZ   MISBOMB       NO; TOO BAD                         93066
         TM    JFCDSRG2,255  ANY NONSENSE ?                      93066
         BNZ   MISBOMB                                           93066
         B     MAKEMESS      ***FOR NOW - BYPASS DSN CHECKS***
         SPACE 2
MISBOMB  STCM  R1,OOIO,RETCH+L'RETCH-1  SET ERROR                92341
         N     R1,=X'0000003F'  RETAIN ONLY LOW CODE             92341
         CVD   R1,DB         CONVERT CODE TO PACKED
         UNPK  DB(5),DB+6(3)   UNPACK WITH F ZONE
         MVC   MISSERR,DB+1  SHOW ERROR CODE
         ICM   R0,OOII,MISSERR  GET DECIMAL ERROR CODE           92341
         SRDL  R0,4          PLACE LOW NIBBLE INTO R1            92341
         SRL   R0,4          DISCARD ZONE                        92341
         SRDL  R0,4          PLACE LOW NIBBLE OF LEADING DIGIT   92341
         STCM  R1,IOOO,IEXREASN+L'IEXREASN-1  DISPLAYS IN HEX!   92341
MAKEMESS CLI   MISSERR,C'0'  WAS THIS ROUTINE CALLED FOR ERROR MSG ?
         BH    PROCERR       YES; PROCESS
         SPACE 2
MISOK    ST    R13,IEXRSVWD  PASS THE WORK AREA TO IGGPOST0
         L     R13,SAVE13-SAVE(,R13)  RELOAD HIGH LEVEL SAVE AREA
         LM    R14,R12,12(R13)  RESTORE CALLER'S REGISTERS
         SLR   R15,R15       CLEAR RETURN CODE
         BR    R14           RETURN WITHOUT FREEMAIN OF MY WORK AREA
         TITLE 'I G G P R E 0 0  ***  DATASET SCRATCH'           93066
         DROP  ,                                                 93066
         USING IEXPL,R11     DECLARE PARAMETER LIST              93066
         USING IGGPRE00,R12                                      93066
         USING SAVE,R13      SAVE/WORK AREA                      93066
TESTSCR  ICM   R6,15,IEXFMT1  POINT TO DATA PORTION OF DSCB      93066
         USING DS1FMTID,R6                                       93066
         TM    IEXFLAG,IEXVIO  VIO ?                             93066
         BNZ   SCROK                                             93066
         TM    DS1DSORG+1,DS1ORGAM  VSAM ?                       93066
         BNZ   SCROK         YES; PROCEED                        93066
         TM    ACCTPRIV,VAASYS+VAASUP  PRIVILEGED CALLER ?       93066
         BNZ   SCROK         YES; HOPE FOR THE BEST              93066
         SPACE 1                                                 93066
         CLI   MISSDSN,C'*'      IEHMOVE TEMP DSN ?              93066
         BE    SCROK         YES, ASSUME TEMPORARY               93066
         CLC   =C'SYS1.DFDSS.',MISSDSN  DEFRAG OR SOMETHING ?    93067
         BE    SCROK         BOO                                 93067
         CLC   DISDSN,MISSDSN    CHECK FOR TEMPORARY DATA SET    93066
         BNE   SCRNTEMP      NO                                  93066
         CLC   DISDST,MISSDSN+8                                  93066
         BNE   SCRNTEMP                                          93066
         LA    R1,MISSDSN+3      GET PRESUMED DATE               93066
         LA    R2,5          LOOK FOR 5 NUMERICS                 93066
         BALS  R9,SCRNUMBS   CHECK NUMERICS                      93006
         LA    R1,MISSDSN+10     GET PRESUMED TIME               93066
         LA    R2,6                                              93066
         LA    R9,SCROK      NO RETURN IF LOOKS LIKE TEMP        93066
         SPACE 1                                                 93066
SCRNUMBS CLI   0(R1),C'0'    NUMERIC ?                           93066
         BL    SCRNTEMP      NO                                  93066
         LA    R1,1(,R1)     NEXT                                93066
         BCT   R2,SCRNUMBS   TRY AGAIN                           93066
         BR    R9            RETURN TO CALLER                    93066
         SPACE 1                                                 93066
SCRNTEMP ICM   R1,15,IEXPTR2  GET POINTER TO CURRENT VOLUME      93066
         BZ    SCROK         HUH ?                               93066
         MVC   MISSVOL,4(R1)  MOVE TO LIST                       93066
         ICM   R15,15,IEXUCB  ANY UCB ?                          93067
         BZ    SCRNUCB       NO                                  93067
         MVC   MISSUCB,UCBNAME-UCBOB(R15)  SHOW IT               93067
SCRNUCB  MVC   INVOL,MISSVOL                                     93066
         XC    A$SVCRB(A$SLENRB),A$SVCRB  CLEAR FOR CALL         93066
         MVC   A$SID,=C'A$RB'  IDENTIFY                          93066
         MVC   A$SFCTN(3),=AL1(A$SFVALV,A$XADELT,A$SAKEY) ACCT/VOL
         MVC   A$SACCT,ACCTNO  PASS ACCOUNT                      93066
         MVC   A$SPSWD(6),MISSVOL PASS SERIAL TO BE CHECKED      93066
         LA    R0,ACCTERRM   GET ERROR MESSAGE ADDRESS           93066
         ST    R0,A$SERMSG   AND PASS IT                         93066
         L     R15,PSAAOLD-PSA  LOAD ASCB                        93066
         ICM   R15,IIII,ASCBTSB-ASCB(R15)  ANY TSB ?             93066
         BZ    SCRACTU       VALIDATE INDEX ACCESS               93066
         MVC   A$SUID,MISSJOB  ADD USER ID IN FORM OF JOB NAME   93066
         B     SCRACTY       SKIP USER TCB CODE                  93080
SCRACTU  ICM   R15,15,@TCBUSER  IS THERE A TCBUSER AREA ?        93080
         BZ    SCRACTX       NO                                  93080
         CLI   UTUD-USERTCB(R15),C' '  IS THERE A USER ID ?      93080
         BNH   SCRACTX       NO                                  93080
         MVC   A$SUID,UTUD-USERTCB(R15)  MOVE IT                 93080
SCRACTY  OI    A$SCNTL,A$SUKEY  SET UID PRESENT                  93080
         OC    A$SUID,BLANKS8  ENSURE TRAILING BLANK ETC.        93080
SCRACTX  SLR   R0,R0                                             93066
         STH   R0,INFLTEST                                       93066
         L     R15,@VOLT                                         93066
         LA    R1,INVOL                                          93066
         BALSR R14,R15       SEE IF VOLUME IS CONTROLLED         93066
         LTR   R3,R15        WAS IT FOUND ?                      93066
         BZ    SCROK         NO; NO FURTHER CHECKS               93066
         TM    INFLTEST,VF1JOBI+VF1JOBF  SPECIAL JOB CLASS?      93116
         BNZ   SCRCLAS       YES; JUST CHECK JOBCLASS            93116
         TM    INFLTEST,VF1SYSP+VF1SYSG+VF1SYSD+VF1STOR  SYS PACK ?
         BNZ   SCRACCT       YES; CHECK VOLUME ACCESS            93066
         TM    INFLTEST+1,VF2ALLO+VF2DSNA+VF2DUIX+VF2DSN1+VF2DSNT
         BZ    SCROK         NOT A CONTROLLED PACK               93066
SCRACCT  LTR   R3,R3         CHECK GROUP VS. SPECIFIC            93144
         BP    SCRNAME       GROUP ENTRY - CHECK DSN INSTEAD     93144
         ACCTSVC A$SVCRB,ERR=GETOUT  CHECK VOLSER ACCESS         93066
         CH    R15,=H'4'     DID IT WORK ?                       93080
         BL    SCRNAME       YES; NOW CHECK DSN                  93080
         BE    SCROK         YES; VOLUME NOT CONTROLLED          93080
         LA    R1,FAIL1(,R15)  GET ERROR CODE                    93066
         B     MISBOMB       AND PROCESS                         93066
         SPACE 1                                                 93116
SCRCLAS  LA    R0,C'I'       SET FOR REQUIRED CLASS              93116
         TM    INFLTEST,VF1JOBI  CLASS I RESTRICTION ?           93116
         BNZ   *+8           YES                                 93116
         LA    R0,C'F'       ELSE CLASS F                        93116
         LTCB  R2,USE=YES   GET CURRENT TCB                      93116
         SLR   R3,R3                                             93116
         ICM   R3,OIII,TCBTCTB  GET 24-BIT TCT ADDRESS           93116
         BZ    SCRNAME       TREAT AS STC                        93116
         USING SMFTCT,R3                                         93116
         LAT   R4,TCTJMR,SCRNAME,LA=34  GET JMR OR OUT           93144
         USING JMR,R4        DECLARE JMR                         93116
         CLM   R0,1,JMRUCLAS  TEST ORIGINAL JOBCLASS             93116
         BE    SCROK         MATCHESO                            93116
         CLM   R0,1,JMRCLASS  MATCHES ORIGINAL ?                 93116
         BE    SCROK         YES                                 93116
         DROP  R2,R3,R4                                          93144
         SPACE 1                                                 93066
SCRNAME  SLR   R14,R14       SET DSN PARM SIGNAL                 93080
         LA    R15,MISSDSN   POINT TO DATASET NAME               93080
         STM   R14,R15,A$SPSWD  PASS 0 AND DSN ADDRESS           93080
         MVI   A$SFCTN,A$SFVALX  CHECK INDEX FOR SCRATCH         93080
         ACCTSVC A$SVCRB,ERR=GETOUT  CHECK ACCESS                93066
         CH    R15,=H'4'     DID IT WORK ?                       93080
         BL    SCROK         YES; NOW GET OUT                    93080
         BE    SCROK         INDEX NOT CONTROLLED                93080
         CH    R15,=H'40'    MALFORMED DSNAME ?                  93080
         BE    SCROK         ALLOW DOS NAME SCRATCH              93080
         LA    R1,FAIL1(,R15)  GET ERROR CODE                    93066
         B     MISBOMB       AND PROCESS                         93066
SCROK    NI    DS1DSIND,255-DS1IND40-DS1IND10-DS1IND04  RESET PROT.
         B     GETOUT                                            93066
         SPACE 2                                                 92329
***********************************************************************
*        MASKED COMPARISON OF STANDARD ACCT/UID ENTRY            92329*
*        R1 - VALUE  R2  - MASK    R14 RETURN                    92329*
***********************************************************************
CMPACCT  STM   R14,R1,CONTOSAV  SAVE                             92329
         LA    R15,UINDACT1-UINDACT1(,R2)  MASK                  92329
         LA    R0,8          NUMBER TO SCAN                      92329
         TM    UINDACF1-UINDACT1(R2),A$XAFUID  USER ID PASSED ?  92330
         BNZ   CMPCOMM       YES; CHECK FULL 8 BYTES             92330
         TM    UINDACF1-UINDACT1(R2),A$XAFSUB SUB-ACCOUNT PRIVY? 92330
         BZ    CMPCOMM       NO; CHECK FULL LENGTH               92330
         LA    R0,4          DON'T CHECK SUB-ACCOUNT             92330
         B     CMPCOMM                                           92330
         SPACE 1                                                 87278
***********************************************************************
*        MASKED COMPARISON                                       87278
*        R1 - VALUE  R15 - MASK    R14 RETURN                    87278
*                                                                87278
***********************************************************************
CMPMASK  STM   R14,R1,CONTOSAV  SAVE                             87278
         LA    R0,8          NUMBER TO SCAN                      87278
CMPCOMM  LA    R14,1         INCREMENT                           87278
CMPLOOP  CLI   0(R15),C'*'   GENERAL COMPLETE MATCH ?            92329
         BE    CMPEXIT       YES; RETURN MATCHED                 92329
         CLI   0(R15),C'?'   CHARACTER MATCH ?                   92329
         BE    CMPBUMP       YES; BUMP                           92329
         CLC   0(1,R15),0(R1)  MATCH ?                           92329
         BNE   CMPEXIT       NO; RETURN UNMATCHED                92330
CMPBUMP  AR    R1,R14                                            92329
         AR    R15,R14                                           92329
         BCT   R0,CMPLOOP    TRY NEXT ENTRY                      92329
         LTR   R0,R0         SET CONDITION CODE =                92330
CMPEXIT  LM    R14,R1,CONTOSAV                                   92329
         BR    R14           RETURN WITH CONDITION CODE          92329
         TITLE 'I G G P R E 0 0  ***  I G G P O S T 0  ---  CLEANUP'
***********************************************************************
*                                                                     *
*   IGGPOST0 - RETURN IMMEDIATELY WITH CODE=0 IF THERE IS NO WORK     *
*      AREA, OR IF THE ALLOCATION FAILED.                             *
*                                                                     *
***********************************************************************
         ENTRY IGGPOST0
         DROP  ,
IGGPOST0 STM   R14,R12,12(R13)  SAVE CALLER
         USING IEXPL,R1      DECLARE PARAMETER LIST
         ICM   R15,15,IEXRSVWD  RELOAD MY GETMAINED WORK AREA
         BZR   R14           RETURN TO CALLER
         LR    R11,R1        COPY OVER
         DROP  R1
         USING IEXPL,R11     DECLARE IT AGAIN
         BALSR R12,0         MAKE NEW BASE
         USING IGGPOST1,R12
IGGPOST1 SL    R12,=A(IGGPOST1-IGGPRE00)  MAKE OLD BASE
         USING IGGPRE00,R12  NOW WE ARE BACK TO NORMAL
         ST    R13,SAVE13-SAVE(,R15)  SAVE BACK LINK
         LR    R13,R15       RESTORE GETMAINED AREA
         USING SAVE,R13      DECLARE IT AGAIN
         SLR   R9,R9         SET PROVISIONAL RETURN CODE
         ICM   R15,15,IEXDCC  DID IT WORK ?
         BNZ   QUITTER       NO; JUST GET OUT
         LM    R14,R15,SPACTADR  SEE IF ANYTHING TO UPDATE       83122
         LTR   R14,R14       ANY UPDATE ADDRESS ?                83122
         BZ    QUITTER       NO                                  83122
         LTR   R15,R15       ANY SIZE ?                          83122
         BNP   QUITTER       NO                                  83122
         L     R0,0(,R14)                                        83122
         AR    R0,R15                                            83122
         ST    R0,0(,R14)    UPDATE USERTCB HWM                  83122
         B     QUITTER       RETURN WITH CODE 0
         SPACE 1
TESTEXT  DS    0H            NOT WRITTEN, YET
TESTREL  DS    0H
TESTREN  DS    0H
TESTPREL DS    0H
TESTSAMX DS    0H
GETOUT   LH    R9,RETCH      LOAD RETURN CODE
QUITTER  ENDM  RC=(R9)       FREEMAIN AND RETURN
         EJECT ,
PROCERR  ICM   R0,OOII,RETCH  WAS A NON-ZERO RETURN CODE SET ?   92341
         BNZ   PROCERRW      OK                                  92341
         OI    RETCH+L'RETCH-1,4  FAIL FOR THIS VOLUME ONLY      92341
PROCERRW CLI   MISSVOL,C' '  ANY VOL-SER ?
         BNH   MISBUSCR      NO; ASSUME SCRATCH
         CLI   MISSVOL,C'9'  SCRATCH ?
         BNH   MISBUCB       NO; ISSUE MSG
MISBUSCR MVC   MISSVOL-1(8),=C'*SCRTCH*'  SHOW SCRATCH
MISBUCB  BALS  R9,PUTMISS    ISSUE MESSAGE FOR THIS UCB/DSN/VSN  93006
         ICM   R0,OOII,ACCTERRM  SPECIAL ERROR MESSAGE ?         93066
         BZ    MESSTWO       NO                                  93066
         LA    R1,ACCTERRM   POINT TO IT                         93066
         BALS  R9,PUTTER     DISPLAY IT                          93066
         B     GETOUT          AND EXIT                          93066
MESSTWO  MVC   MISSTEXT(MISSEND-MISSTEXT),MISSTEXT-1  CLEAR MESSAGE
         LA    R1,CANMESS    GET CANNED MESSAGES
         SLR   R2,R2         CLEAR IC REGISTER
LOOPMESS CLI   0(R1),X'FF'   END OF TABLE ?
         BE    GETOUT        YES; EXIT WITHOUT SECOND MESSAGE
         IC    R2,0(,R1)     LOAD TEXT LENGTH-1
         CLC   1(2,R1),MISSERR  TEXT FOR THIS ERROR ?
         BE    MOVEMESS      YES; PROCESS
         LA    R1,4(R1,R2)   POINT TO NEXT MESSAGE
         B     LOOPMESS
         SPACE 1
MVCTEXT  MVC   MISSTEXT(0),3(R1)  COPY FULL TEXT TO MESSAGE
MOVEMESS EX    R2,MVCTEXT    MOVE TEXT TO MESSAGE
         CLI   MISSERR,C'2'  SPECIAL TEXT INSERTION REQUIRED ?
         BNE   PUTMESS2      NO
         CLI   MISSERR+1,C'2'  DITTO ?
         BH    PUTMESS2
*        BE    TOTALS        TOTAL EXCEEDS SIZE
*        L     R0,SPACTLIM   GET MAX SINGLE DSN SIZE             81074
*        B     EDITOR
TOTALS   L     R0,SPACTMAX   GET CUMULATIVE JOB MAX              92342
EDITOR   CVD   R0,DB         PACK
         ED    MISSTEXT(8),DB+4  SHOW LIMITS
PUTMESS2 BALS  R9,PUTMISS                                        93006
         B     GETOUT        AND EXIT                            83122
         SPACE 2
PUTMISS  LA    R1,MISSAGE    POINT TO STANDARD MESSAGE
PUTTER   L     R15,PSAAOLD-PSA   GET ASCB                        83122
         USING ASCB,R15                                          83122
         ICM   R0,15,ASCBTSB  TSB POINTER PRESENT ?              83122
         BNZ   PUTPUT        YES; USE TPUT
         LAT   R15,@TCBUSER,PUTWTO  NO USER EXTENSION - USE WTO
         USING USERTCB,R15
         TM    UTMF,4+2+1    KERMIT, MILTEN OR WYLBUR ?
         BMR   R9            YES; IMMEDIATE RETURN               79251
PUTWTO   WTO   MF=(E,(1))    ISSUE WTO
         BR    R9            AND RETURN
PUTPUT   LH    R0,0(,R1)     GET LENGTH+4 OF MESSAGE
         LA    R1,4(R1,0)    POINT TO TEXT
         SH    R0,*-2        CORRECT LENGTH
         TPUT  (1),(0),EDIT  NOTIFY USER
         BR    R9            RETURN TO CALLER
         EJECT ,
*        LCL CONSTANTS
         SPACE 1
DISDST   DC    C'.T'         CONT.
DISACT   DC    C'Z90'        SPECIAL ACCOUNT NUMBERS
         SPACE 1
DISDSN   DC    0C'SYS'       TEMP. DSNAME START
SYSDSN   DC    C'SYS1.'      HIGH INDEX FOR AUTOMATIC WRITE PROT.
         SPACE 2
*        CONSTANTS
         SPACE
DC8      DC    0XL8'0',8X'C0'  ALPHA-NUMERIC ZONE COMMON BITS
         ORG   *-1           FIRST 'NUMERIC' MAY BE ALPHA
DC000    DC    X'C0F0F0'     NUMERIC ZONES
WYLSAVOL DC    C'TSO001'     WYLBUR SAVE VOLUME GROUP
WYLMAIL  DC    C'MAIL '      SPECIAL MAIL DSN
WYLMAILX DC    C'MAIL.'                                          79281
WYLRECOV DC    C'ACTIVE   '  RECOVERY DATA                       79251
WYLRECOX DC    C'ACTIVE.'                                        79281
WYLLIB   DC    C'LIB '       SPECIAL PDS                         79251
WYLLIBX  DC    0CL4'LIB.'                                        79281
WYLLIBXU DC    C'LIB.UNLD '                                      79281
WYLCARD  DC    C'CARD '      SINGLE SEQ. FOR PO ONLY USERS
         SPACE 2
MESSPAT  DC    AL2(MISSROUT-MISSAGE,X'80'*256)   MESSAGE PATTERN
 DC C'MSG900E JOB         , UCB=   , VOL=''      '', DSN='       79251
BLANKS8  EQU   MESSPAT+16,8,C'C'  8 BLANKS                       83136
         SPACE 1
CANMESS  EQU   *  MESSAGES AL1(L-1),CL2'ID',C'TEXT...'
         DC    AL1(MS15-*-4),C'14VOLUME SERIAL NOT SPECIFIED VIA JCL'
MS15     DC    AL1(MS16-*-4),C'15ALLOCATION RESTRICTED TO SYSTEMS'
MS16     DC    AL1(MS17-*-4),C'16ABOVE VOLUME RESTRICTED TO CLASS F'
MS17     DC    AL1(MS18-*-4),C'17ABOVE VOLUME RESTRICTED TO CLASS I'
MS18     DC    AL1(MS19-*-4),C'18PERMANENT DATASET NOT ALLOWED ON SYSDA*
               /VIO'                                             83122
MS19     DC    AL1(MS21-*-4),C'19PRIVATE VOLUME NOT PERMITTED FOR NON-S*
               ETUP CLASS E'
MS21     DC    AL1(MS22-*-4),C'21',X'4020202020202120',C' TRACKS FOR SI*
               NGLE DATASET EXCEEDED'
MS22     DC    AL1(MS23-*-4),C'22',X'4020202020202120',C' TRACK MAXIMUM*
                SYSDA/VIO SPACE EXCEEDED'                        83122
MS23     DC    AL1(MS24-*-4),C'23DSNAME MALFORMED OR NOT PERMITTED ON V*
               OLUME'
MS24     DC    AL1(MS25-*-4),C'24REQUESTED DSORG NOT PERMITTED ON ABOVE*
                VOLUME'
MS25     DC    AL1(MS26-*-4),C'25PASSWORD NOT ALLOWED ON THIS VOLUME'
MS26     DC    X'FF'         END OF LIST
         EJECT ,
SAVE     DSECT ,             GETMAINED SAVE AND WORK AREA
HINDEX   DC    CL8' '        DATASET'S HIGH INDEX                85189
AINDEX   DC    CL8' '        NEXT LOWER INDEX FOR ACCESS CHECKING
TRKALC   SERVCALC MF=D       SERVCALC (TRKCALC) PARAMETER LIST   83122
DB       DS    D             WORK AREA
SPACTADR DC    A(0)          ADDRESS IN USERTCB FOR CUMULATIVE SPACE
SPACTAMT DC    A(0)          AMOUNT TO ADD FOR SUCCESSFUL ALLOCATION
SPACTMAX DC    A(0)          LIMIT FOR THIS REQUEST TYPE         83122
SPACTEXT DC    A(0)          NUMBER OF EXTENTS TO COUNT          83122
SPACTLIM EQU   SPACTMAX,4,C'A'  SINGLE REQUEST LIMIT - SAME AS MAX
         SPACE
MISSAGE  DC    0A(0),AL2(MISSEND-*,0)  WILL BE OVERLAYED LATER
         DC    C'MSG9'       MESSAGE PREFIX
MISSERR  DC    C'NN',C'E '   ERROR NUMBER
MISSTEXT DC    C'JOB '
MISSJOB  DC    CL8' ',C', '
         DC    C'UCB='
MISSUCB  DC    C'123',C', VOL='''   UCB NAME
MISSVOL  DC    CL6' ',C''', DSN='    VOL-SER
MISSDSN  DC    CL44' '       DSNAME - GARBAGE IF IEHMOVE CALLS ?
MISSEND  EQU   *             END OF WTO TEXT
MISSROUT DC    XL4'0'        OPTIONAL DESC/ROUT CODES
         SPACE
LCLFLAG  DS    X             PROCESSING FLAG
LCLDTEMP EQU   128           TEMPORARY DSNAME
LCLVIO   EQU   64            VIO REQUEST                         83122
LCLJOBE  EQU   32            RESIDENT VOL-SER / VALID NON-SETUP
LCLVSTO  EQU   16            INVALID - STORAGE REQUEST - NO VOL-SER
LCLVLIB  EQU   8             LIBRARY PACK - RESTR. TO SYSTEMS
LCLVSCR  EQU   4             NON-RESIDENT WORK PACK REQUEST
LCLJCLS  EQU   2             OFF IF CLASS F OR PRIVILEGED JOB CLASS
LCLCUST  EQU   1             ON IF NOT SPECIAL ACCOUNT NUMBER
         SPACE 1                                                 84327
FLOWFG   DS    X             PROCESSING FLOW FLAGS               84327
FFGNOBT  EQU   X'80'           SKIP OBTAIN DURING ERROR RETURN   84327
         SPACE 1
PROFG    DC    0XL2'0'       LOCAL VOLUME FLAGS
PROFG1   DC    X'0'          PRIMARY VOLTAB FLAGS                81074
PROFG2   DC    X'0'          SECONDARY FLAGS                     81074
         SPACE 1
RETCH    DC    H'0'          RETURN CODE
@VOLT    DC    A(0)          ADDRESS OF VOLT (RESIDENT)
@TCBUSER DC    A(0)          ADDRESS OF TCBUSER EXTENSION OR 0
PROPQTY  DC    F'0'          PRIMARY Q / FINAL QUANT IN TRKS     81074
         SPACE 1
         PRINT &PRTMAC
         USERVOLT ,          EXPAND VOLT FLAGS, CALL AREA, MAP   81082
         PRINT &PRTSYS
SAVE     DSECT ,                                                 81082
         SPACE 1
A$SVCRB  A$SVCRB DSECT=NO    LOCAL ACCOUNTING PARAMETER LIST     93066
CONTOSAV DS    5A            SAVE AREA                           93066
ACCTNO   DS    CL8           CURRENT ACCOUNT NUMBER              93066
DS4DEVSZ DS    XL4           DEVICE SIZE
AUTOPASS DS    X             FLAGS FOR AUTOMATIC PASSWORD PROTECTION
ACCTPRIV DS    X             ACCOUNT PRIVILEGES                  84009
ACCTERRM DS    XL125         V-FORMAT ERROR MESSAGE FROM ACCT SVC
SAVEND   EQU   *             END OF WORK AREA
         SPACE 2
JFCBKEN  DSECT ,
JFCB     DS    0D            DEFINE FOR 'USE'
         IEFJFCBN
         SPACE 1
         IECSDSL1 1          EXPAND FORMAT 1
         EJECT ,
         PRINT &PRTMAC                                           83122
         SPACE 1                                                 83261
USERTCB  USERTCB ,                                               83122
         SPACE 2                                                 83122
USERCVT  USERCVT ,                                               83122
         SPACE 2
USERUADS USERUADS ,
         SPACE 2
USERINDX USERINDX ,
         SPACE 2
USERVOLS USERVOLS ,
         SPACE 2                                                 93066
A$INDX   A$INDX ,            EXPAND LOCAL INDEX RECORD           93066
         SPACE 2                                                 93066
A$GDA    A$GDA DSECT         MAP ACCOUNT GLOBAL AREA             93066
         SPACE 3                                                 83122
         PRINT &PRTSYS                                           83122
CVTDSECT DSECT ,
         CVT   DSECT=YES                                         83122
         SPACE 1                                                 83122
         IHARB ,                                                 83122
         SPACE 1                                                 93024
         IHACDE ,                                                93024
         SPACE 1
         IEFJMR  ,
         A$UCOM ,            APPEND LOCAL JMRUCOM DEFINITION
         SPACE 1
         IHAPSA ,
         SPACE 1                                                 83122
         IKJTCB ,                                                83122
         SPACE 1                                                 83122
         IEFTCT ,                                                83122
         SPACE 1                                                 83122
         IEFUCBOB ,                                              83122
         SPACE 1                                                 83122
         IEZJSCB ,                                               83122
         SPACE 1                                                 83122
         IHAASCB ,                                               83122
         SPACE 1                                                 83122
         PRINT &PRTMAC
IEXPL    IECIEXPL DSECT=YES
         END   ,
