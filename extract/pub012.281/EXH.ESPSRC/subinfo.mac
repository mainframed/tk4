INFO     TITLE 'S U B I N F O  ***  GET JOB AND DD INFORMATION'
***********************************************************************
*        SUBROUTINE TO OBTAIN SYSTEM INFORMATION                      *
*          FROM CBTTAPE                                               *
***********************************************************************
*                                                                     *
*   CHANGED ABSOLUTE TO MNEMONIC REGISTERS.                           *
*   CONVERTED TO USE LOCAL MACROS.                                    *
*   CHANGED TO RUN UNDER MVS ALSO.                                    *
*   CODE IS RENT/REFR UNDER MVS; ONLY REUS UNDER LATER SYSTEMS.       *
*   LINES ANNOTATED WITH \ CONTAIN CHANGES BY G. POSTPISCHIL          *
*   THEY ARE UNTESTED, AS ONLY MY MVS SYSTEM IS UP RIGHT NOW.         *
*   CREATED MAPSBINF MACRO TO MAP RETURN AREA.                        *
*                                                                     *
***********************************************************************
*                                                                     *
*   TO NOT HAVE EMPTY SLOTS RETURNED, CHANGE THE TIOSLTYP BRANCH      *
*   TO BUMPTIOT. IT MAY ALSO BE EXPEDIENT TO CHANGE THE DD COUNTER    *
*   TO OMIT THOSE ENTRIES - IF SO, MOVE TM/BNZ BEFORE THE INCH CODE.  *
*                                                                     *
*   A TIOT MAY HAVE CONSIDERABLE MORE THAN 500 ENTRIES, SO FOR GENERAL*
*   USE THE 512 SHOULD BE INCREASED, AND THE RETURN AREA ENLARGED     *
*                                                                     *
*   SOME OR ALL OF THE LISTSWA INITIALIZATION MAY BE MOVED BEFORE THE *
*   LOOP, BUT I WASN'T ABLE TO TEST THAT.                             *
*                                                                     *
***********************************************************************
         SPACE 1
         COPY  ASMOPTS                                          GP15029
         SPACE 1
         ASMPARM LIST=YES    SHOW ASSEMBLY OPTIONS              GP15029
         SPACE 1
SUBINFO  START 0                                                GP15029
SUBINFO  AMODE 31                                               GP15029
SUBINFO  RMODE 24                                               GP15029
         PRINT &PRTSRC                                          GP15029
         USING SUBINFO,R15   ENTRY BASE                         GP15029
         B     BEGIN         BRANCH AROUND ID                   GP15029
         DC    AL1(17),CL17'SUBINFO &SYSDATE'                   GP15029
BEGIN    BSM   R14,0         PRESERVE AMODE                     GP15029
         STM   R14,R12,12(R13)    PRESERVE REGISTERS            GP15029
         LA    R12,0(,R15)   CLEAN BASE ADDRESS FOR AM31        GP15029
         DROP  R15                                              GP15029
         USING SUBINFO,R12   DECLARE NEW BASE                   GP15029
         SAM31 ,             NEED 31-BIT FOR ESQA               GP15029
         L     R2,0(,R1)           GET PTR TO RETURN AREA       GP15029
         USING INFDSECT,2
         SPACE 1
*        ADDRESS KEY CBS
*        ----------------
         PUSH  USING                                            GP15029
         USING PSA,R0
         L     R3,PSAAOLD          LOCATE ASCB
         USING ASCB,R3
         L     R4,PSATOLD          LOCATE TCB
         USING TCB,R4
         L     R5,TCBTIO           LOCATE TIOT
         USING TIOT1,R5
         L     R6,TCBJSCB          LOCATE JSCB
         USING IEZJSCB,R6
         L     R7,ASCBASXB         LOCATE ASXB
         USING ASXB,R7
         L     R8,ASXBSENV         LOCATE ACEE
         USING ACEE,R8
         SPACE 1
*        SAVE SYSTEM INFO FIELDS
*        -----------------------
         XC    INFDSECT(256),INFDSECT
         MVC   INFJOBN,TIOCNJOB    GET JOB NAME
         MVC   INFSTEP,TIOCSTEP    GET STEP NAME
         LTR   R8,R8               HAVE AN ACEE?                GP15029
         BZ    SKIPACEE              NO                         GP15029
         MVC   INFUSER,ACEEUSRI    GET USERID
         MVC   INFGRPN,ACEEGRPN    GET GROUP
SKIPACEE MVC   INFPGMN,JSCBPGMN    GET PROGRAM NAME
         POP   USING                                            GP15029
         USING TIOT1,R5
         SPACE 1
*        BUILD THE DDNAME LIST
*        ---------------------
         XC    INF#DDS,INF#DDS     SET DDNAME COUNTER           GP15029
         LA    R7,PATDDN           PATTERN ENTRY FOR EMPTY SLOT GP15029
         LA    R8,INFDDS           POINT TO FIRST SLOT          GP15029
         LA    R9,TIOENTRY-TIOT1   POINT TO FIRST TIOT DD ENTRY
         USING TIOENTRY,R5
         USING DDSLOT,R8                                        GP15029
LOOPTIOT AR    R5,R9         NEXT TIOT ENTRY                    GP15029
         ICM   R9,1,TIOELNGH       LOAD AND TEST ENTRY LENGTH   GP15029
         BZ    PGMEXIT               END LOOP                   GP15029
         INCH  DDCONC,WORK=R15     INCREMENT COUNTER            GP15029
         CH    R15,=H'512'         ABOUT TO OVERFLOW?           GP15029
         BNL   PGMOFLO             YES - STOP B4 WE BUST LIMIT  GP15029
         MVC   DDENTRY,PATDDN      INIT SLOT IN CASE EMPTY      GP15029
         TM    TIOESTTA,TIOSLTYP   FREED DD ENTRY ?             GP15029
         BNZ   KEEPTIOT              YES; RETURN *** AS DD NAME GP15029
         MVC   DDNAME,TIOEDDNM     SAVE DDNAME                  GP15029
         CLI   TIOEDDNM,X'40'      IS THIS A CONCAT ENTRY?
         BH    NEWDD                 NO - SKIP
         MVC   DDNAME,DDNAME-DDSLOT(R7) YES - REPEAT DDNAME
         LA    R15,1                                            GP15029
         AH    R15,INFDDNUM(,R7)   INCREASE CONCATENATION COUNT GP15029
         STH   R15,DDCONC          AND SAVE IT BACK             GP15029
         SPACE 1
NEWDD    LR    R7,R8               SAVE PTR TO PRIOR ENTRY
         AIF   (&MVSXA).SWAREQ                                  GP15029
         SLR   R1,R1                                            GP15029
         ICM   R1,7,TIOEJFCB       GET JFCB BLOCK ADDRESS       GP15029
         LA    R1,16(,R1)          SKIP Q BLOCK HEADER          GP15029
         AGO   .COMREQ                                          GP15029
.SWAREQ  XC    LISTSWA(LISTSWAL),LISTSWA  CLEAR SWA REQUEST     GP15029
         MVC   LISTSVA+4(3),TIOEJFCB  SECOND WORD OF LIST       GP15029
         LA    R1,LISTSVA    GET SVA ADDRESS                    GP15029
         ST    R1,LISTEPA    STORE IN POINTER                   GP15029
         MVC   LISTSWA+8,=C'RL'  SET FUNCTION                   GP15029
         LA    R1,LISTSWA+8                                     GP15029
         ST    R1,LISTSWA+4  POINTER TO FUNCTION                GP15029
         SWAREQ MF=(E,LISTSWA),UNAUTH=YES                       GP15029
         BXH   R15,R15,LDDN80    UNEXPECTED FAILURE             GP15029
         L     R1,LISTSVA    GET STORAGE ADDRESS OF JFCB        GP15029
.COMREQ  MVC   DDDSN,JFCBDSNM-INFMJFCB(R1)                      GP15029
         SLR   R3,R3                                            GP15029
         ICM   R3,7,TIOEFSRT      GET UCB                       GP15029
         BZ    DEVDUMY                                          GP15029
         USING UCBOB,R3                                         GP15029
         UCBDEVN UCBPTR=(R3),DEVN=DDUNI,MODE=3   FORMAT DEVICE  GP15029
         TM    UCBTBYT3,UCB3TAPE+UCB3DACC   SERIAL?             GP15029
         BNM   KEEPTIOT                                         GP15029
         MVC   DDVOL,UCBVOLI                                    GP15029
         B     KEEPTIOT                                         GP15029
         SPACE 1
DEVDUMY  LA    R15,=CL4'SOUT'     SYSOUT                        GP15029
         TM    TIOELINK,TIOSYOUT  IS IT ?                       GP15029
         BNZ   DEVMVC               YES                         GP15029
         LA    R15,=CL4'TERM'     TERMINAL?                     GP15029
         TM    TIOELINK,TIOTTERM  IS IT ?                       GP15029
         BNZ   DEVMVC               YES                         GP15029
         LA    R15,=C'SSYS'                                     GP15029
         TM    TIOELINK,TIOESSDS  SUBSYSTEM?                    GP15029
         BNZ   DEVMVC               YES                         GP15029
         LA    R15,=C'REMT'                                     GP15029
         TM    TIOELINK,TIOTREM   REMOTE?                       GP15029
         BNZ   DEVMVC               YES                         GP15029
         LA    R15,=C' DMY'       OTHER DUMMY                   GP15029
DEVMVC   MVC   DDUNI,0(R15)  MOVE IDENTIFIER                    GP15029
         SPACE 1
KEEPTIOT LA    R8,INFSIZE(,R8)     BUMP SLOT POINTER            GP15029
BUMPTIOT B     LOOPTIOT
         SPACE 1
*        EXIT
*        ----
PGMOFLO  LA    R15,4         DD NAME OVERFLOW - RETURN WARNING  GP15029
         ST    R15,16(,R13)  RETURN WARNING CODE                GP15029
PGMEXIT  LM    R14,R12,12(R13)  RESTORE REGISTERS (EXCEPT R15)  GP15029
         BSM   0,R14         RETURN IN ORIGINAL AMODE           GP15029
         SPACE 1
         LTORG ,
         SPACE 1

PATDDE   DC    CL8'********'
         DC    XL2'0'                                           GP15029
         DC    CL44'*FREED*'
         DC    CL6' '                                           GP15029
         DC    CL4' '                                           GP15029
PATDDN   EQU   PATDDE,*-PATDDE,C'X'  DUMMY DD ENTRY             GP15029
         SPACE 1
SAVE     DS    0D            NON-RENT WORK AREA                 GP15029
*B       DS    D             CONVERSION WORK AREA               GP15029
*TEST    SERVDEFS ,                                             GP15029
         AIF   (NOT &MVSXA).SKIPSWA                             GP15029
LISTSWA  SWAREQ FCODE=RL,EPA=LISTEPA,UNAUTH=YES,MF=L            GP15029
LISTEPA  DS    A             POINTER TO EXPANSION AREA          GP15029
LISTSVA  DS    7A            EXPANSION FOR UNAUTHORIZED REQUEST GP15029
.SKIPSWA SPACE 1
SAVEEND  EQU   *                                                GP15029
         SPACE 1
         MAPSBINF ,          EXPAND MAPPING FOR WORK AREA       GP15029
DDSLOT   DSECT ,                MAP OF DD RETURN ENTRY          GP15029
DDNAME   EQU   INFDDNAM+DDSLOT,L'INFDDNAM,C'C'                  GP15029
DDCONC   EQU   INFDDNUM+DDSLOT,L'INFDDNUM,C'H'                  GP15029
DDDSN    EQU   INFDDDSN+DDSLOT,L'INFDDDSN,C'C'                  GP15029
DDVOL    EQU   INFDDVOL+DDSLOT,L'INFDDVOL,C'C'                  GP15029
DDUNI    EQU   INFDDUNI+DDSLOT,L'INFDDUNI,C'C'                  GP15029
DDENTRY  EQU   DDSLOT,INFSIZE,C'X'                              GP15029
         SPACE 1
         PRINT NOGEN                                            GP15029
         CVT   DSECT=YES                                        GP15029
MYJFCB   DSECT ,                                                GP15029
         IEFJFCBN ,                                             GP15029
MYTIOT   DSECT ,
         IEFTIOT1 ,
         SPACE 1
         YREGS ,             MNEMONIC REGISTERS                 GP15029
         IHAPSA ,
         IHAASCB ,
         IHAASXB ,
         IHAACEE ,
         IEZJSCB ,
         IKJTCB ,
         IEFJESCT ,                                             GP15029
         IEFUCBOB ,
         SPACE 1
         PUSH  PRINT                                            GP15029
         PRINT ON,GEN,DATA                                      GP15029
         SPACE 1
         IEFZB505 ,                                             GP15029
         POP   PRINT                                            GP15029
         SPACE 1
         AIF   (NOT &MVSXA).MEND                                GP15029
         IEFQMIDS ,                                             GP15029
.MEND    END   ,
