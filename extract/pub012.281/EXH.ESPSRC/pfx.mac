PREFIX   TITLE 'P R E F I X  ***  UPDATE THE PREFIX IN THE UPT'
***********************************************************************
**                                                                   **
**   THIS LITTLE GEM FOUND ON CBTCOV FILE 065; AUTHOR UNKNOWN        **
**                                                                   **
**   MINOR CHANGES TO USE LOCAL MACROS/SERVICES.      G. POSTPISCHIL **
**   CHANGED TO HANDLE PLAIN PROGRAM ENTRY, NOT JUST COMMAND PROC.   **
**                                                                   **
***********************************************************************
         COPY  ASMOPTS                                          GP14258
         SPACE 1
         ASMPARM LIST=YES    SHOW OPTIONS                       GP14258
         SPACE 1
         PUNCH '  ORDER PREFIX(P) '    EASIER DUMP READING      GP14258
         SPACE 1
         PRINT &PRTSRC                                          GP14258
         EXTRN SUBTPUT                                          GP14258
PREFIX   PGMHEAD ZERO12,BASE=R12,BNDRY=PAGE,PARM=R9             GP14258
         SERVINIT ,               INITIALIZE SERVICE ROUTINES   GP14258
         SERVLOAD SUBSTRIP                                      GP14258
         EXTRACT DB,FIELDS=(TSO),MF=(E,PATEXT)  RUNNING UNDER TSO?
         L     R3,DB              GET ADDRESS OF TSO FLAG       GP14258
         L     R8,PSATOLD-PSA          GET MY TCB               GP14258
         L     R8,TCBJSTCB-TCB(,R8)           GET THE JSTCB     GP14258
         L     R8,TCBJSCB-TCB(,R8) GET THE JSCB                 GP14258
         USING IEZJSCB,R8                                       GP14258
         ICM   R8,15,JSCBPSCB  IF THIS FAILS, WE'RE IN DEEP SH  GP14258
         BNZ   GETPARM            PSCB - RUNNING TSO            GP14258
         USING PSCB,R8                                          GP14258
         TM    0(R3),X'C0'        FORE OR BACK-GROUND?          GP14258
         BZ    NOTSO                NO; ERROR                   GP14258
GETPARM  PARMLOAD R4,PTR=R9       GET THE PARM ADDRESS/LENGTH   GP14258
         STM   R4,R5,DB           STASH IT                      GP14258
         SUBCALL SUBSTRIP,(DB,DB+4),VL,MF=(E,CALLPARM)  DEBLANK GP14258
         LM    R4,R5,DB           GET TRIMMED SIZE              GP14258
         LTR   R15,R5             ANY DATA AT ALL?              GP14258
         BNP   SKIPTRT                                          GP14258
         TRT   0(1,R4),TRTFIRST   IS FIRST BYTE VALID ?         GP14258
         BNZ   MALFORM              NO                          GP14258
         MINH  R15,=H'8'          NOT MORE THAN NECESSARY       GP14258
         LA    R1,0(R15,R4)       PUTATIVE END                  GP14258
         BCTR  R15,0              EX LENGTH                     GP14258
         SR    R2,R2                                            GP14258
         EX    R15,EXTRTPRM       LOOK FOR END                  GP14258
         CLM   R2,1,TRTREST+1     INVALID CHARACTER FOUND ?     GP14258
         BE    MALFORM              YES; GET OUT                GP14258
         SR    R1,R4              GET SIGNIFICANT LENGTH        GP14258
         CH    R1,=H'7'           OVER IBM'S DESIGN LENGTH?     GP14258
         BH    TOOLONG              YES; FAIL                   GP14258
         LR    R5,R1              SET NEW LENGTH; SKIP COMMENTS GP14258
         BCTR  R1,0               EXECUTE LENGTH                GP14258
         EX    R1,EXUPP           MAKE UPPER CASE               GP14258
         SPACE 1
*---------------------------------------------------------------------*
*  IN ORDER TO ACCOMMODATE BOTH COMMAND ENTRY AND CALL ENTRY, WE GET  *
*   THE UPT, AND THE USER'S PREFIX HERE                               *
*---------------------------------------------------------------------*
SKIPTRT  ICM   R8,15,PSCBUPT GET THE UPT                        GP14258
         BZ    NOTSO                                            GP14258
         USING UPT,R8                                           GP14258
         SPACE 1
         MVC   OFIX,=CL7'()'       ASSUME NO PREFIX         *HMD 05/82*
         SR    R1,R1               CLEAR                    *HMD 05/82*
         ICM   R1,1,UPTPREFL       PREFIX LENGTH            *HMD 05/82*
         BNP   OLDNULL             ZERO LENGTH              *HMD 05/82*
         BCTR  R1,0                DECREMENT BY 0           *HMD 05/82*
         EX    R1,EXOLDMVC         MOVE OLD PREFIX TO MSG LINE
OLDNULL  STC   R5,UPTPREFL         STORE NEW LENGTH             GP14258
         SH    R5,=H'1'            LENGTH FOR EXECUTE           GP14258
         BM    NEWNULL               NONE; SKIP MOVE            GP14258
         MVC   UPTPREFX,=CL7' '    CLEAR PREFIX
         EX    R5,EXNEWPFX         MOVE NEW PREFIX TO UPT       GP14258
         EX    R5,EXNEWMVC         MOVE NEW PREFIX TO MSG LINE  GP14258
         B     PUTLINE                                      *HMD 05/82*
         SPACE 1
NEWNULL  XC    UPTPREFX(8),UPTPREFX      CLEAR PREFIX       *HMD 05/82*
         MVC   NFIX(8),=CL8'()'          INDICATE NOPREFIX  *HMD 05/82*
PUTLINE  #TPUT STATMSG,TYPE=BCON                                GP14258
PGMEXIT  SERVTERM ,                FREE AND CLOSE EVERYTHING    GP14258
         PGMEXIT COPYRET=(RETCODE,4)   RETURN                   GP14258
         SPACE 1
NOTSO    WTO   'PREFIX: NOT TSO?',ROUTCDE=11,DESC=7             GP14258
         MVICC 16                                               GP14258
         B     PGMEXIT                                          GP14258
         SPACE 1
MALFORM  #TPUT 'PREFIX: OPERAND MALFORMED'                      GP14258
         MVICC 8                                                GP14258
         B     PGMEXIT                                          GP14258
         SPACE 1
TOOLONG  #TPUT 'PREFIX: OPERAND TOO LONG (>7 CHARS)'            GP14258
         MVICC 8                                                GP14258
         B     PGMEXIT                                          GP14258
         SPACE 2
         LTORG
         SPACE 2
STATMSG  BCON  'OLD PREFIX WAS         ; NEW PFX IS         '   GP14258
OFIX     EQU   STATMSG+16,8,C'C'                                GP14258
NFIX     EQU   STATMSG+37,8,C'C'                                GP14258
EXUPP    TR    0(*-*,R4),TRUPP    UPPER-CASE TEXT               GP14258
EXTRTPRM TRT   0(0,R4),TRTREST    STOP ON BLANK OR EOS          GP14258
EXNEWPFX MVC   UPTPREFX(0),0(R4)  NEW PROFILE PREFIX            GP14258
EXOLDMVC MVC   OFIX(0),UPTPREFX   MOVE OLD PREFIX TO MSG LINE   GP14258
EXNEWMVC MVC   NFIX(0),0(R4)      MOVE NEW PREFIX TO MSG LINE   GP14258
         SPACE 1
TRUPP    TRENT FILL=(*-TRUPP)     UPPER-CASE TRANSLATION        GP14258
         TRENT ,*-TRUPP+C' ',(X'81',9),(X'91',9),(X'A2',8)      GP14258
TRTFIRST TRENT FILL=8              FAIL ALL                     GP14258
         TRENT ,0,(C'A',9),(C'J',9),(C'S',8),C'@',C'#',C'$'     GP14258
TRTREST  TRENT FILL=8              FAIL ALL                     GP14258
         TRENT ,4,X'00',C' '       STOPPERS                     GP14258
         TRENT ,0,(C'0',10),                                    GP14258
         TRENT ,0,(C'A',9),(C'J',9),(C'S',8),C'@',C'#',C'$'     GP14258
         SPACE 2
SAVE     DSECT ,             SAVE AND WORK AREA                 GP14258
DB       DS    D                                                GP14258
         SERVDEFS ,                                             GP14258
PATEXT   EXTRACT DB,FIELDS=(TSO),MF=L                           GP14258
SAVEEND  EQU   *                                                GP14258
         SPACE 3
         IKJPSCB ,
         SPACE 2
         IKJUPT
         SPACE 2
         IHAPSA ,                                               GP14258
         SPACE 2
         IEZJSCB ,                                              GP14258
         SPACE 2
         IKJTCB ,                                               GP14258
         END  PREFIX
