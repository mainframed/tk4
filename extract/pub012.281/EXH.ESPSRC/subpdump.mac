PDUMP    TITLE 'S U B P D U M P  ***  STORAGE DUMP LINE FORMATTING'
***********************************************************************
*                                                                     *
*  SUBPDUMP    FORMAT HEX DATA LINE(S)                                *
*    R1 POINTS TO  OFFSET , START , LENGTH , @PRINTER  WORDS          *
*    LOADS:                                                           *
*    R2 : OFFSET     R3 : START ADDRESS     R4: LENGTH                *
*                                                                     *
***********************************************************************
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 1
SUBPDUMP PGMHEAD ZERO12,BASE=R12,PARM=R10
         LM    R2,R5,0(R10)  GET PASSED PARAMETERS
         STM   R2,R4,OFADLEN    SAVE INITIAL VALUES
         L     R5,0(,R5)     GET PRINTER ADDRESS
         ST    R5,@PRINTER   MAKE PRT MACROS WORK
         MVI   OUTLINE,C' '
         MVC   OUTDATA,OUTLINE    CLEAR FIRST TIME
         LA    R7,LINEDEF    POINT TO LINE DEFINITION
DUMPLING CLC   OUTDATA,OUTLINE  CLEAR ?
         BE    DUMPBLNK
         PRTF  OUTLINE       ELSE PRINT IT
         MVC   OUTDATA,OUTLINE    CLEAR FIRST TIME
DUMPBLNK LA    R9,OUTDATA    CLEAR ANY PARTIAL LINE IN BUFFER
         MVC   OUTDATA,OUTLINE
         LM    R2,R4,OFADLEN   RELOAD UPDATED PARAMETERS
         LR    R0,R2         COPY OFFSET FOR CONVERSION
         BAL   R14,HEX2EBC    CONVERT
         MVC   1(6,R9),HEXEDOUT+2  SHOW OFFSET
         LTR   R4,R4         ANY LENGTH?
         BNP   DUMPLINX      NO; QUIT
         LH    R14,2(,R7)    TEXT SIZE
         MIN   R14,(R4)      BUT NOT MORE THAN WE HAVE
         BCTR  R14,0         ADJUST FOR EXECUTE
         LR    R15,R9        GET LINE ADDRESS
         AH    R15,4(,R7)    GET TEXT POSITION
         MVI   0(R15),C'*'   LEFT FRAME
         LA    R2,2(R14,R15)   TRAILING POSITION
         MVI   0(R2),C'*'    RIGHT FRAME
         EX    R14,EXMVCTXT  MOVE TEXT
         LA    R5,HEXOFFS    POINT TO LIST OF HEX OFFSETS
         LH    R6,2(,R7)     GET CHAR COUNT FOR LINE
         LM    R14,R0,OFADLEN  GET OFFSET/ADDR/SIZE
         AR    R14,R6        BUMP OFFSET
         AR    R15,R6        BUMP ADDRESS
         SR    R0,R6         REMAINING LENGTH AFTER THIS LINE
         STM   R14,R0,OFADLEN  SET OFFSET/ADDR/SIZE
         MIN   R6,(R4)       BUT NOT MORE THAN BUFFER
         SR    R4,R6         SIZE FOR NEXT WORD
DMPLWORD LA    R2,4          SIZE FOR ONE OFFSET
         SR    R1,R1           CLEAR FOR IC
         IC    R1,0(,R5)     GET OFFSET TO NEXT WORD
         LA    R5,1(,R5)     NEXT TIME
         AR    R1,R9         SET OUTPUT LOCATION
         MIN   R2,(R6)       BUT NOT MORE THAN 4
         SR    R6,R2         SIZE FOR NEXT TIME
         LR    R15,R2        SAVE LENGTH
         BCTR  R15,0         EXECUTE LENGTH
         EX    R15,EXHEXWRK  MOVE TO WORK AREA
         BAL   R14,HEX2UNPK  UNPACK AND TRANSLATE
         LA    R15,1(R15,R15)   OUTPUT LENGTH
         EX    R15,EXHEXMVC  MOVE TO BUFFER
         AR    R3,R2         SET NEW FROM ADDRESS
         LTR   R6,R6         TEXT SIZE REMAINING
         BP    DMPLWORD      DO NEXT WORD
         LTR   R4,R4         ANY MORE TO DO ?
         BNP   DUMPLINX      NO; QUIT
         CLC   OUTDATA,OUTLINE  CLEAR ?
         BE    DUPECHEK
         PRTF  OUTLINE       ELSE PRINT IT
         MVC   OUTDATA,OUTLINE    CLEAR FIRST TIME
         SPACE 1
***********************************************************************
**   STORAGE COMPARE FOR DUPLICATE LINES IN BUFFER                   **
***********************************************************************
DUPECHEK LH    R5,2(,R7)     GET LINE CAPACITY
         SLA   R5,1          DOUBLE
         CR    R4,R5         AT LEAST THREE LINES (INCL.PRINTED)
         BL    DMPLNDUP      NO; SKIP DUPLICATE CHECK
         LR    R0,R3         LOAD START ADDRESS
         LR    R15,R4        LOAD LENGTH
         LH    R2,2(,R7)     RESTORE LINE SIZE
         SR    R14,R14       CLEAR FOR DIVIDE
         DR    R14,R2        GET QUOTIENT
         MR    R14,R2        GET MULTIPLE OF LINE SIZE
         LR    R14,R3        LOAD START ADDRESS
         SR    R14,R2        COMPARE JUST PRINTED LINE
         LR    R1,R15
         CLCL  R14,R0        LOOK FOR DUPLICATION
         SR    R0,R3         COMPUTE DIFF
         LR    R15,R0          FLIP
         LH    R2,2(,R7)     RESTORE LINE SIZE
         SR    R14,R14       CLEAR FOR DIVIDE
         DR    R14,R2        GET QUOTIENT
         MR    R14,R2        GET MULTIPLE OF LINE SIZE
         CR    R15,R5        AT LEAST THREE LINES ?
         BL    DMPLNDUP      NOT ENOUGH
         LM    R2,R4,OFADLEN   RELOAD OFFSET/ADDR/SIZE
         AR    R2,R15        ADJUST OFFSET
         AR    R3,R15        NEXT DISPLAY ADDRESS
         SR    R4,R15        RESIDUAL LENGTH
         STM   R2,R4,OFADLEN   SET NEW OFFSET/ADDR/SIZE
         LR    R6,R2
         LA    R9,OUTDATA    SAVE LINE ADDRESS
         MVC   09(21,R9),=C'SAME AS ABOVE THROUGH'
         LR    R0,R6         PASS ADDRESS
         BAL   R14,HEX2EBC    FORMAT END ADDRESS
         MVC   31(6,R9),HEXEDOUT+2
DMPLNDUP LTR   R4,R4         ADJUST RESIDUAL LENGTH
         BP    DUMPLING      FORMAT CONTINUATION LINE
         SPACE 1
DUMPLINX CLC   OUTDATA,OUTLINE  CLEAR ?
         BE    PGMEXIT
         PRTF  OUTLINE       ELSE PRINT IT
PGMEXIT  PGMEXIT ,           RESET SPIE RECOVERY
         SPACE 2
***********************************************************************
*                                                                     *
*  HEX2EBC     BINARY TO EBCDIC CONVERSION ROUTINE                    *
*                                                                     *
***********************************************************************
         SPACE 1
HEX2EBC  ST    R0,HEXEDIN    STORE THE WORD TO BE CONVERTED
HEX2UNPK UNPK  HEXEDOUT(9),HEXEDIN(5)
         TR    HEXEDOUT(8),HEXTRTAB  TRANSLATE HEX TO PRINTABLE
         BR    R14                     RETURN
         SPACE 1
         LTORG ,
         SPACE 1
HEXTABLE DC    C'0123456789ABCDEF'
HEXTRTAB EQU   HEXTABLE-C'0'  VIRTUAL BASE FOR HEX TRANSLATION
EXHEXWRK MVC   HEXEDIN(0),0(R3)  MOVE EXACT LENGTH TO AVOID 0C4
EXHEXUN  UNPK  0(0,R1),HEXEDIN(0)  UNPACK +1 EXTRA
EXHEXMVC MVC   0(0,R1),HEXEDOUT   COPY PRINTABLE CHARACTERS
EXMVCTXT MVC   1(0,R15),0(R3)     MOVE USER'S DATA
HEXOFFS  DC    AL1(9,18,27,36,46,55,64,73,83,92,101,110)
LINEDEF  DC    AL2(116,32,083)    REQUIRED WIDTH / TEXT SIZE / OFFSET
         SPACE 1
SAVE     DSECT ,             LOCAL SAVE AREA
@PRINTER DS    A             PRINT ROUTINE ADDRESS; FROM USER
OFADLEN  DS    F,A,F         OFFSET, ADDRESS, LENGTH
HEXEDIN  DC    F'0'          HEX INPUT
HEXEDOUT DC    CL8' ',C' '   PRINTABLE HEX OUTPUT
OUTLINE  DS    0CL133,C      CARRIAGE CONTROL
OUTDATA  DS    CL132         PRINT DATA
SAVEEND  EQU   *
         SPACE 2
         END   ,
