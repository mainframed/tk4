SUBDBLNK TITLE 'S U B D B L N K  ***  REMOVE EXTRANEOUS BLANKS'
*    RPF COMMAND PROCESSING DRIVES ME CRAZY
*      EVEN SO MUCH AS AN EXTRA SPACE BEFORE OR AFTER THE
*      COMMAND VERB CAUSES ERRORS OR THE COMMAND IS IGNORED
*      WITH NO ERROR OR WARNING MESSAGE
*
*    THIS SUBROUTINE WILL REMOVE EXTRA BLANKS BEFORE AND AFTER
*      THE COMMAND VERB, AND COPY IT UPPER-CASED TO THE RPFCOMM
*      AREA INTO CMDVERB (CL8); MORE THAN 8 NOT MOVED - LEFT IN BUFFER
*
*    ADJUSTED TEXT IS WRITTEN BACK INTO THE INPUT AREA
*
*    INPUT:  R0 - ADDRESS OF TEXT BUFFER    R1 - LENGTH TO PROCESS
*            R15 - ROUTINE ADDRESS   R14 - RETURN ADDRESS
*      LENGTH MUST BE LESS THAN 128
*
*    ATTRIBUTES: REUS, NOT REFR, NOT RENT, AMODE ANY, RMODE ANY.
*                                              AUTHOR: G. POSTPISCHIL
*    ROUTINE PLACED INTO THE PUBLIC DOMAIN
SUBDBLNK CSECT ,
         USING *,R15         NO EXTRA SAVE AREA, NO SPECIAL BASE
         B     BEGIN
         DC    AL1(17),CL17'SUBDBLNK &SYSDATE'   EYE CATCHER
BEGIN    STM   R14,R12,12(R13)    SAVE REGISTERS
         MVC   16(4,R13),=F'8'    SET BAD LENGTH
         LTR   R5,R1              COPY AND TEST LENGTH
         BNP   PGMEXIT            NOTHING USEFUL
         MVC   16(4,R13),=F'4'    SET OTHER ERROR
         MVC   CMDVERB,BLANKS     CLEAR COMMAND VERB
         CH    R5,=AL2(L'WORKBUFF)   NOT TOO LONG?
         BH    PGMEXIT            YES; DON'T MESS WITH IT
         LR    R8,R0              SAVE START ADDRESS
         LR    R9,R1              AND LENGTH
         BCTR  R5,0               ADJUST LENGTH
         EX    R5,EXMVCWRK        MOVE TO WORK BUFFER
         LA    R3,WORKBUFF
*EXTRA   LA    R1,WORKBUFF+L'WORKBUFF  IN CASE NO STOPPER
         SR    R2,R2              JUST IN CASE
         EX    R5,EXNBLNK         FIND A NON-BLANK
         BZ    PGMEXIT            ALL BLANK
         MVC   WORKBUFF,0(R1)     LEFT-JUSTIFY COMMAND
         LA    R1,WORKBUFF+L'WORKBUFF  IN CASE NO STOPPER
         EX    R5,EXBLANK         FIND A BLANK
         BZ    PGMEXIT            ALL VERB?
         LR    R4,R1
         SR    R4,R3              LENGTH OF VERB
         BNP   MOVEBACK           SHOULD NOT HAPPEN
         CH    R4,=AL2(L'CMDVERB)   NOT TOO LONG
         BNH   *+8
         LH    R4,=AL2(L'CMDVERB)   SET MAXIMUM
         BCTR  R4,0
         EX    R4,EXUPCMD         COPY AND UPPER CASE
         LA    R1,1(,R1)          EARLIEST START OF OPERAND
         LR    R3,R1              EARLIEST START OF OPERAND
         EX    R5,EXNBLNK         FIND START OF OPERAND
         MVC   0(L'WORKBUFF,R3),0(R1)  LEFT-JUSTIFY OPERAND
MOVEBACK EX    R5,EXWRKMVC        RETURN ADJUSTED BUFFER
         XC    16(4,R13),16(R13)  SET COMPLETE
         SPACE 1
PGMEXIT  LM    R14,R12,12(R13)    RESTORE ALL
         BR    R14                RETURN TO CALLER
EXMVCWRK MVC   WORKBUFF(0),0(R8)  COPY USER'S TEXT
EXBLANK  TRT   0(0,R3),TRTBLANK   FIND A BLANK
EXNBLNK  TRT   0(0,R3),TRTNBLNK   FIND A NON-BLANK
EXUPCMD  OC    CMDVERB(0),0(R3)   SAVE AND UPPER-CASE COMMAND
EXWRKMVC MVC   0(0,R8),WORKBUFF   RETURN USER'S TEXT
         SPACE 1
CMDVERB  DC    CL8' '        UPPER-CASED COMMAND
WORKBUFF DC    CL128' '
BLANKS   DC    CL128' '      BLANKS AND SCAN STOPPER
TRTBLANK DC    256AL1(0)     DON'T STOP ON ANYTHING
         ORG   TRTBLANK+C' '   EXCEPT A BLANK
         DC    AL1(4)
         ORG   TRTBLANK+X'11'  OR AN SBA (SHOULDN'T GET HERE)
         DC    AL1(8)
         ORG   ,
TRTNBLNK DC    256AL1(4)     DON'T STOP ON ANYTHING
         ORG   TRTNBLNK+C' '   EXCEPT A BLANK
         DC    AL1(0)
         ORG   ,
R0       EQU   0             PREFER YREGS
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   ,
