SIM3270  TITLE  'ACCESS VTAM APPLS WITHOUT LOGGING OFF TSO'
*   (NEEDS ACF/VTAM)                   SIMULATED TERMINALS
***********************************************************************
*             ELLIS HOLMAN --                                         *
*                                                                     *
*             MODULE NAME IS SIM3270                                  *
*                                                                     *
*             FUNCTION:                                               *
*                ALLOWS ACCESS TO OTHER VTAM APPLICATIONS             *
*                WITHOUT HAVING TO LOG OFF TSO.                       *
*  DEPENDANCIES:                                                      *
*    1) BASE REGISTER IS 12.                                          *
*    2) TABLE AT OF ACBS CALLED APPLTAB IS LOADED AND SCANNED.        *
*    3) FIRST FOUR BYTES OF APPLTAB INDICATE THE NUMBER OF ENTRIES    *
*       (AVAILABLE ACB'S).  THIS MUST MATCH THE NUMBER OF ACB NAMES   *
*       IN THE ACB NAME TABLE.                                        *
*    4) PARMS PASSED TO THE PROGRAMMING IN THE FORM OF:               *
*          SIM3270 APPL(APPLNAME)                                     *
*       SPECIFY THE APPL TO WHICH A SESSION WILL ATTEMPT              *
*       TO BIND TO.  THIS NAME IS MOVED INTO STORAGE AT               *
*       LABEL ECHO1.                                                  *
*                                                                     *
*   MODIFICATION HISTORY:                                             *
*                                                                     *
*   1993/11/NN  GYP  COMBINED APPL TABLE; MINOR REVISIONS;            *
*                    CHANGED APPL DEFAULT TO EXHI                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                 93309
         MACRO
         APPL   &FLD
         GBLA  &NUMAPPL                                          93319
         LCLA  &LEN                                             GP12045
&LEN     SETA  K'&FLD
         AIF   ('&FLD' EQ 'END' OR '&FLD' EQ '*END').APPLEND     93323
         AIF   (&LEN GT 8).APPL
         AIF   (&LEN LT 1).APPL                                  93309
&NUMAPPL SETA  &NUMAPPL+1                                        93319
         AIF   (&NUMAPPL GT 1).NOTONE                            93319
APPLTAB  DC    F'1'          PROVISIONAL SIZE                    93319
.NOTONE  DC    CL8'&FLD'
         MEXIT ,                                                 93319
.APPL    MNOTE 8,'APPL NAME MUST NOT EXCEED 8 CHARACTERS'        93309
         MEXIT ,                                                 93323
.APPLEND ORG   APPLTAB                                           93319
         DC    A(&NUMAPPL)   SET CURRENT COUNT                   93319
         ORG   ,                                                 93319
         MEND  ,                                                 93319
         SPACE 1                                                 93309
         COPY OPTIONGB       DEFINE OPTIONS                      93323
         SPACE 1                                                 93323
         LCLA  &MAXBUFF                                          93323
&MAXBUFF SETA  4096          MAXIMUM SINGLE BUFFER               93323
         SYSPARM LIST=YES    SET OPTIONS                         93323
         EJECT ,                                                 93323
         PRINT &PRTSOR                                           93323
SIM3270  SAVEM ZERO12,BASE=(R11,R12),PARM=R3                     93323
         SPACE 1                                                 93323
*---------------------------------------------------------------------*
*  BASIC SETUP -                         SCANS                        *
*  THE COMMAND BUFFER FOR THE EXPECTED PARMS.                         *
*  IF THE EXPECTED PARMS ARE NOT FOUND (SEE                           *
*  DEPENDENCIES ABOVE FOR COMMAND FORMAT),                            *
*  A DEFAULT APPLICATION OF 'EXHI    ' IS REQUESTED             GP02326
*  FOR SESSION INITIATION.                                            *
*---------------------------------------------------------------------*
         STM   R11,R12,BASESAVE  SAVE BASE REG                   93323
         XC    WAITECB,WAITECB              CLEAR WAIT ECB       93319
         USING CPPL,R3                      ESTAB. CPPL MAP
         L     R4,CPPLCBUF                  PICKUP CBUF ADDRESS
         LH    R1,0(,R4)                    LOAD LINE LENGTH TO SCAN
         SH    R1,=H'4'      LESS LENGTH OF LENGTH               93319
         SH    R1,2(,R4)     LESS LENGTH PARSED (COMMAND)        93319
         BNP   DEFAPPL       NONE; USE DEFAULT                   93319
         AH    R4,2(,R4)     SKIP LENGTH SCANNED                 93319
         LR    R15,R1        COPY LENGTH                         93319
         BCTR  R15,0         SET EXECUTE LENGTH                  93319
         EX    R15,EXTRAPPL  UPPER CASE TEXT                     93319
APPLSCAN CLI   4(R4),C' '    LEADING BLANK ?
         BE    APPLCORE                                          93319
         CLC   4(4,R4),=C'APPL'             SEARCH FOR KEY WORD
         BE    SETAPL                       WHEN FOUND SET UP APPLID
         B     SETNAPL       NO APPL=                            93319
APPLCORE LA    R4,1(,R4)                    INDEX FOR NEXT BYTE
         BCT   R1,APPLSCAN                  CHECK NEXT POSSIBLE
         B     DEFAPPL                      IF NO APPL KEYWRD=DEFAULT
EXTRAPPL TR    4(0,R4),UPCASE               XLATE CBUF TO UPPER CASE
EXMVAPPL MVC   APPLID(0),4(R4)                                   93319
         SPACE 1                                                 93319
SETAPL   LA    R4,5(,R4)     SKIP APPL=                          93319
         SH    R1,=H'5'      SUBTRACT 'APPL=' LENGTH             93319
SETNAPL  SH    R1,=H'1'      ANYTHING LEFT ?                     93319
         BM    DEFAPPL       NO; USE DEFAULT                     93319
         CH    R1,=H'7'      TOO LONG ?                          93319
         BNH   *+8           NO                                  93319
         LA    R1,L'APPLID-1  NOT TOO MUCH                       93319
         EX    R1,EXMVAPPL   MOVE WHATEVER WE HAVE               93319
         B     CHGNIB                       MODIFY NIB AS NEEDED
         SPACE 1                                                 93319
DEFAPPL  MVC   APPLID,=CL8'EXHI    '        OR USE DEFAULT       93315
*---------------------------------------------------------------------*
*  CHANGE THE NIB TO ACCESS THE REQUESTED APPLICATION                 *
*---------------------------------------------------------------------*
CHGNIB   DS    0H
         MODCB NIB=SIMNIB,                                             X
               AM=VTAM,                                                X
               NAME=(*,APPLID)
         USING IFGRPL,R4                         SET RPL DSECT
*---------------------------------------------------------------------*
*  TURN ON TSO FULL SCREEN MODE AND ACTIVATE TSO ATTENTION EXIT       *
*---------------------------------------------------------------------*
         STTMPMD ON,KEYS=ALL                     SET TERMINAL MOD ON
         STFSMODE ON,                                                  X
               INITIAL=YES,                                            X
               NOEDIT=YES
         LA    R3,STAXLIST                       LOAD ADDR OF STAX PARM
         STAX  ATTNEXIT,MF=(E,(3))               ISSUE STAX MACRO
*---------------------------------------------------------------------*
*  DO INITIAL CLEAR OF SCREEN AND ATTEMPT OPEN OF ACB                 *
*---------------------------------------------------------------------*
         TPUT  CLEAR,CLEARLN,FULLSCR             CLEAR SCREEN    93319
         L     R3,=A(APPLTAB)                                    93309
         LA    R4,4(,R3)    POINT TO FIRST ENTRY                 93315
         L     R3,0(0,R3)    LOAD SIZE OF TABLE                  93315
         LA    R5,SIMACB
         USING IFGACB,R5
*---------------------------------------------------------------------*
*  SCAN ACB TABLE TO FIND AN ACB NOT IN USE, IF UNABLE TO FIND ONE    *
*  THAT IS NOT IN USE SEND A MESSAGE TO THE TERMINAL THAT THE         *
*  REQUESTED SESSION COULD NOT BE ACTIVATED.                          *
*---------------------------------------------------------------------*
FINDACB  DS    0H
         MVC   ECHO1+1(8),0(R4)                  MOVE IN FIRST ACB NAME
         OPEN  SIMACB                            OPEN VTAM ACB
         LTR   R15,R15                           RC = 0
         BZ    ACBOK                             IF NOT EXIT
         CLI   ACBERFLG,X'58'                    OPEN ALREADY ??
         BNE   BADACB                            IF NOT LEAVE
         LA    R4,8(,R4)                         INDEX TO NEXT NAME
         BCT   R3,FINDACB                        LOOP FOR ACB
BADACB   DS    0H
         STFSMODE OFF                            SET FULL SCRN OFF
         MVC   ACBMSGLU,0(R4)   ECHO LU NAME                     93319
         UNPK  ACBMSGCD(3),ACBERFLG(2)  UNPACK ERROR CODE        93319
         TR    ACBMSGCD,HEXTAB-C'0'  MAKE PRINTABLE HEX          93319
         TPUT  ACBMSG,ACBMSGSZ  ALERT USER                       93319
         B     GETOUT                            TERMINATE AND EXIT
ACBOK    DS    0H
*---------------------------------------------------------------------*
*  UPON SUCCESSFULLY OPENING AN ACB OBTAIN INPUT BUFFER AREA VIA      *
*  GETMAIN, EXIT IF UNABLE TO GET STORAGE.  OTHERWISE, ISSUE VTAM     *
*  REQUEST SESSION MACRO TO START A SESSION BETWEEN THE REQUESTED     *
*  APPLICATION AND THIS PROGRAM'S ACB                                 *
*---------------------------------------------------------------------*
         GETMAIN RC,LV=3*&MAXBUFF   GET DYNAMIC STORAGE          93323
         ST    R1,MTSCMPI@                       STORE ADDRESS OF AREA
         LA    R15,2048                                          93323
         LA    R1,2048(R1,R15)  NEXT PAGE                        93323
         ST    R1,OUTAREA                        STORE THIS ADR TOO
         LA    R1,2048(R1,R15)  NEXT PAGE                        93323
         ST    R1,MTSCMPO@   COMPRESSION WORK AREA               93323
         LA    R14,MTSCMPBS  POINT TO BUFFER SIZE                93323
         LA    R15,MTSCMPFG  POINT TO OPTION FLAGS               93323
*DEFER*  LA    R0,           COLOR CONVERSION TABLE              93323
         STM   R14,R15,MTSCMPB@  COMPLETE PARAMETER LIST         93323
         LA    R14,MTSCMPOS  POINT TO OUTPUT LENGTH              93323
         ST    R14,MTSCMPOL                                      93323
         LA    R15,MTSCMPIS  POINT TO INPUT LENGTH               93323
         ST    R15,MTSCMPIL                                      93323
         MVI   MTSCMPFG,MTSCMCCW+MTSCMWCC  FULL CCW/WCC PREFIX   93323
         LA    R0,24*80      PROVISIONAL SCREEN SIZE             93323
         ST    R0,MTSCMPBS                                       93323
*GONE*   ICM   R15,15,COMPRESS  COMP3270 LINKED IN ?             93323
*GONE*   BNZ   GOTCOMP       YES                                 93323
GOTCOMP  LA    R5,SIATTRIB   SET OUTPUT ADDRESSES                87109
         LA    R2,SIPRMSZE                                       87109
         LA    R3,SIALTSZE                                       87109
         GTTERM PRMSZE=(R2),ALTSZE=(R3),ATTRIB=(R5)  GET ATTRIBUTES
         CH    R15,=H'8'     NON-DISPLAY ?                       93323
         BE    NOTACRT                                           93323
         LOAD  EP=COMP3270   ELSE LOAD IT                        93323
         ST    R0,COMPRESS                                       93323
*        LA    R0,CCWW78     SET ERASE/WRITE ALTERNATE           87110
         LR    R1,R3         SET FOR ALTERNATE SIZE              87109
         CLI   1(R1),80      80 BYTE WIDTH ?                     87109
         BE    SETSIZE       YES; USE IT                         87109
*        LA    R0,CCWW70     SET ERASE/WRITE                     87110
         LR    R1,R2         USE PRIMARY SIZE                    87109
SETSIZE  SLR   R14,R14                                           87109
         SLR   R15,R15                                           87109
         IC    R14,0(,R1)    GET NUMBER OF LINES                 87109
         IC    R15,1(,R1)    GET WIDTH                           87109
*        STM   R14,R15,DB                                        87109
*        ST    R0,MTSFEAT    SET MY CCW CODE                     89065
         LR    R0,R14                                            93323
         LR    R1,R15                                            93323
         TM    SIATTRIB+3,1  TPG SUPPORTED ?                     87110
         BZ    GETSIZE       NO                                  87110
         MVI   MTSCMPFC,MTSCMCO7  SHOW 3278/79 TYPE              93323
         MVI   MTSCMPFH,MTSCMHI7  SHOW 3274 CONTROLLER           93323
         B     GETSIZE       GET THE SIZE                        93323
         SPACE 1                                                 93323
NOTACRT  OI    TERMFLAG,TFNCRT  FLAG NOT CRT                     93323
         LOAD  EP=CONV3270   LOAD CONVERSION PROGRAM             93323
         ST    R0,COMPRESS                                       93323
*        GTSIZE              JUST A QUICK AND DIRTY CHECK FOR TSO
GETSIZE  GTSIZE
         CH    R0,=H'1'                                          93323
         BH    GETSIZED                                          93323
         LA    R0,24         DEFAULT                             93323
GETSIZED STH   R0,DEVLINE    LINES ON SCREEN                     93323
         STH   R1,DEVBYTE    SCREEN WIDTH                        93323
         LR    R3,R1         SAVE WIDTH                          87110
         MR    R0,R0         GET TOTAL SCREEN SIZE IN R1         93323
         ST    R1,MTSCMPBS   SCREEN SIZE                         93323
SETFEAT  DS    0H                                                93323
         SPACE 1                                                 93323
         SETLOGON RPL=SIMRPL1,                                         X
               OPTCD=(START,SYN)
         LTR   R15,R15                           RC = 0
         BNZ   GETOUT                            NO TERMINATE
         LA    R5,DATAAREA                       GET DATA ADDRESS
         LA    R4,8                              GET LENGTH      93319
         CLC   DATAAREA,=CL8' '                  ANY DATA
         BE    NORMREQ                           NO, CLEAR LENGTH
         B     HCFREQ                            YES, LEAVE LENGTH
NORMREQ  DS    0H                                    *
         SR    R4,R4                             CLEAR AREA LENGTH
HCFREQ   DS    0H                                    *
         REQSESS RPL=SIMRPL1,NIB=SIMNIB,                               X
               OPTCD=(SYN,NQ),                                         X
               RECLEN=(4),                                             X
               AREA=(5)
         LTR   R15,R15                           R C = 0
         BNZ   ERROR                             TERMINATE AND EXIT
         LTR   R0,R0                             REASON CODE = 0
         BZ    DORECV                            TERMINATE AND EXIT
ERROR    DS    0H
         STFSMODE OFF                            SET FULL SCRN OFF
         TPUT  EMSG,EMSGLN                       SEND ERROR MSG  93319
         B     GETOUT                            TERMINATE AND EXIT
DORECV   DS    0H
*---------------------------------------------------------------------*
* NOW THAT THE SESSION BETWEEN THIS PROGRAM AND THE REQUESTED         *
* APPLICATION IS BOUND, SEND A REQUEST TO THE APPLICATION TO          *
* RECEIVE DATA FROM IT.                                               *
*---------------------------------------------------------------------*
         L     R7,MTSCMPI@                                       93323
         RECEIVE RPL=SIMRPL2,                                          X
               EXIT=RECVEX,                                            X
               AREA=(7),                                               X
               AREALEN=&MAXBUFF,                                       X
               OPTCD=(ASY,ANY,Q,CA)
         WAIT  ECB=WAITECB                       WAIT FOR ECB TO POST
RESET    DS    0H
         MVI   RESETCNT,C'N'                     CLEAR RESET SWITCH
         LA    R8,LOOPVAL1                       WAIT FOR 14 MINS
         MVC   WAITINT,HALFSEC                   IN 1/2 SEC INTERVALS
LOOP     DS    0H
         STIMER WAIT,DINTVL=WAITINT              STIMER WAIT
         CLI   EXIT,C'Y'                         DO WE EXIT
         BE    GETOUT                            YES
         CLI   RESETCNT,C'Y'                     RESET COUNT
         BE    RESET                             YES GO RESET
         L     R7,OUTAREA                        GET IOAREA ADDRESS
*---------------------------------------------------------------------*
*  ATTEMPT TO RECEIVE INPUT FROM THE TERMINAL. RESET WAIT COUNTERS    *
*  IF NO DATA RECEIVED AND CONTINUE TO WAIT FOR TERMINAL INPUT        *
*---------------------------------------------------------------------*
         TM    TERMFLAG,TFNCRT  3270 ?                           93330
         BZ    *+4+6+4       YES                                 93330
         MVC   0(6,R7),=X'7D4040114040'                          93330
         LA    R7,6(,R7)     SKIP PREFIX                         93330
         TGET  (7),&MAXBUFF-1,ASIS,NOWAIT        GET INPUT FROM TERM
         C     R15,=F'4'                         ANY INPUT
         BNE   GOODIN                            PROCESS INPUT
         BCT   R8,LOOP                           DECRIMENT AND LOOP
         CLC   WAITINT,TWOSEC                    IN 2ND INTERVAL
         BE    GETOUT                            YES, TIMED OUT
         LA    R8,LOOPVAL2                       LOOP FOR 2 SEC
         MVC   WAITINT,TWOSEC                    IN TWO SEC INTERVAL
         B     LOOP                              GO LOOP
GOODIN   TM    TERMFLAG,TFNCRT  3270 ?                           93330
         BZ    *+8           YES                                 93330
         LA    R1,6(,R1)     ALLOW FOR PREFIX                    93330
         ST    R1,SLENGTH                        GET INPUT LENGTH
         L     R6,CID                            LOAD CID
         L     R5,SLENGTH                        LOAD LENGTH
         L     R7,OUTAREA                        LOAD I/O AREA ADDR
*---------------------------------------------------------------------*
*  AFTER GETTING INPUT FROM THE TERMINAL, FORWARD IT TO THE           *
*  APPLICATION                                                        *
*---------------------------------------------------------------------*
         CLI   BBSW,C'Y'                         BETWEEN BRACKET ?
         BE    BBSEND                            YES - START BRACKET
         SEND  RPL=SIMRPL3,                                            X
               AREA=(7),                                               X
               RECLEN=(5),                                             X
               ARG=(6),                                                X
               OPTCD=SYN,                                              X
               BRACKET=(NBB,NEB),                                      X
               CHNGDIR=(CMD),                                          X
               STYPE=REQ,                                              X
               CONTROL=DATA,                                           X
               RESPOND=(NEX,NFME,NRRN,NQRESP)
         B     LOOP
BBSEND   DS    0H
         SEND  RPL=SIMRPL3,                                            X
               AREA=(7),                                               X
               RECLEN=(5),                                             X
               ARG=(6),                                                X
               OPTCD=SYN,                                              X
               BRACKET=(BB,NEB),                                       X
               STYPE=REQ,                                              X
               CONTROL=DATA,                                           X
               RESPOND=(NEX,NFME,NRRN,NQRESP)
         MVI   BBSW,C'N'                         INDICATE IN BRACKET
         B     LOOP                                  *
GETOUT   DS    0H
         CLOSE SIMACB                            CLOSE VTAM ACB
         STTMPMD OFF                             SET TERM MODE OFF
         STFSMODE OFF                            SET FULLSCREEN OFF
         ICM   R1,15,MTSCMPI@  ANY GETMAIN FOR INPUT ?           93323
         BZ    GETOUTX                                           93323
         FREEMAIN R,LV=3*&MAXBUFF,A=(1)  FREE IT                 93323
GETOUTX  ENDM  RC=0                                              93323
         SPACE 2                                                 93323
         USING SCIPEXIT,R15                      ESTAB. BASE REG
SCIPEXIT LM    R11,R12,BASESAVE   LOAD OLD BASE REG              93323
         DROP  R15                               DROP REG 15
         USING SIM3270,R11,R12                                   93323
         LR    R3,R14                            LOAD RETURN ADDR
         LA    R13,SAVEAR2                       LOAD NEW SAVEAREA
         L     R4,4(,1)                          LOAD CID VALUE
         L     R5,16(,1)                         LOAD RPL ADDRESS
         USING IFGRPL,R5                         MAP RPL
         CLI   RPLCNTDC,RPLTBIND                 IS BIND REQUEST
         BE    BIND                              YES BIND SESSION
         CLI   RPLCNTDC,RPLTUNBD                 IS UNBIND REQUEST
         BE    UNBIND                            YES DROP SESSION
         CLI   RPLCNTSC,RPLCLEAR                 IS CLEAR REQUEST
         BE    DOCLEAR                           YES CLEAR
         CLI   RPLCNTSC,RPLSDT                   IS SDT REQUEST
         BE    DOSDT                             YES DO SDT
         ABEND 2,DUMP                            UNKOWN REQUEST - DUMP
UNBIND   DS    0H
         L     R6,RPLAREA                        GET RU AREA ADDR
         CLC   0(2,R6),=X'3202'                  RETAIN RESOURSES
         BE    HANGON                            YES KEEP THEM
         MVI   EXIT,C'Y'                         SET EXIT SWITCH ON
         TERMSESS  RPL=SIMRPL4,                                        X
               ARG=(4),                                                X
               OPTCD=(SYN,COND)
HANGON   DS    0H
         LR    R14,R3                            GET RETURN ADDR
         BR    R14                               RETURN TO VTAM
BIND     DS    0H
         ST    R4,CID                            SAVE CID VALUE
         ST    R4,SIMNIB+4                       SAVE CID IN NIB
         OPNSEC RPL=SIMRPL4,                                           X
               OPTCD=(SYN,CA),                                         X
               NIB=SIMNIB
         C     R15,=F'32'                        SYNAD DETECTED
         BE    SDTEX                             GET OUT WAIT FOR BIND
         LTR   R15,R15                           R C = 0
         BNZ   GETOUT                            TERMINATE AND EXIT
         POST  WAITECB                           POST ECB
         LR    R14,R3                            RESTORE RETURN ADDR
         BR    R14                               RETURN TO VTAM
DOCLEAR  DS    0H
         MVI   BBSW,C'Y'                         CLEAR BRACKET SWITCH
DOSDT    DS    0H
SDTEX    DS    0H
CLEAREX  DS    0H
         LR    R14,R3                            GET RETURN ADDRESS
         BR    R14                               RETURN TO VTAM
         SPACE 2                                                 93323
         USING LEREXIT,R15                       ESTABLISH BASE REG
LEREXIT  LM    R11,R12,BASESAVE   LOAD BASE VALUE                93323
         ST    R14,SAVEREG3                      SAVE RETURN ADDR.
         DROP  R15                               DROP R15 AS BASE
         USING SIM3270,R11,R12                   ESTAB ADDRESS
         LR    R5,R1                             LOAD RPL ADDR
         LR    R3,R14                            LOAD RTN ADDR
         USING IFGRPL,R5                         MAP RPL
         CLI   RPLFDB2,X'13'                     INVALID CID
         BER   R14                               YES IGNORE ERROR
         CLI   RPLFDB2,X'60'  USER TRIED CLSDST PASS ?           93319
         BER   R14           YES; SUPPORT LATER ?                93319
         ABEND 4095,DUMP                         ABEND OTHERWISE
         BR    R14                               RETURN TO VTAM
         SPACE 2                                                 93323
         USING SYNEXIT,R15                       EST. ADDR
SYNEXIT  LM    R11,R12,BASESAVE   LOAD BASES                     93323
         DROP  R15                               DROP REG 15
         ST    R0,SAVER0
         LR    R5,R1                             GET RPL ADDR
         LR    R3,R14                            LOAD RETURN ADR
         USING IFGRPL,R5                         MAP RPL DSECT
SYNTEST1 DS    0H                                LU NOT ACTIVE
         MVI   EXIT,C'Y'                         SET XIT SWITCH
         CLC   RPLRTNCD(2),=X'1000'
         BNE   SYNTEST2
         LA    R15,10                            SET RC          93319
         BR    R14                               RETURN TO VTAM
SYNTEST2 DS    0H                                SESSION TERMINATED
         CLC   RPLRTNCD(2),=X'100D'              SESS TERM
         BER   R14                               RETURN TO VTAM
         CLC   RPLRTNCD(2),=X'081E'              SESSION REF ERR
         BER   R14                               RETURN TO VTAM
         CLC   RPLRTNCD(2),=X'0C0B'              SESSION TERM
         BNE   SYNDUMP                           RETURN TO VTAM
         BR    R14                               RETURN TO VTAM
SYNDUMP  DS    0H                                DUMP PGM SYN ERR.
         CLC   SAVER0,=F'16'                     CHECK IF R0 SHOWS APPL
*                                                NOT AVAILABLE
         BNE   SYNERR                            IF R0 EQ X'10' THEN
         STFSMODE OFF                            SET FULL SCRN OFF
         TPUT  EMSG,EMSGLN                       SEND ERROR MSG  93319
         MVI   EXIT,C'Y'                         SET XIT SWITCH
         BR    R14                               RETURN TO VTAM
SYNERR   DS    0H
         ABEND 4094,DUMP
         BR    R14
         SPACE 1                                                 93323
         USING RECVEX,R15                                        93323
RECVEX   LM    R11,R12,BASESAVE   LOAD BASES                     93323
         DROP  R15                               DROP R15 BASE
         USING SIM3270,R11,R12                                   93323
         ST    R14,SAVEREG3                      SAVE RETURN ADDR
         LA    R13,SAVEAR3                       LOAD SAVE AREA ADDR
         MVI   BBSW,C'N'                         SET BRACKET SW OFF
         MVI   RESPSW,C'N'                       SET RESP SW OFF
         CHECK RPL=(1)                           CHECK RECV
         LTR   R15,R15                           CHECK OK
         BE    RECOK                             THEN PROCESS
         MVI   EXIT,C'Y'                         SET XIT SWITCH
         B     EXITRECV                          EXIT OUT
RECOK    DS    0H
         LA    R5,0(,R1)                         LOAD RPL ADDRESS
         TM    20(R5),X'02'                      TYPE 1 RESP
         BO    NORESP                            NOT TYPE 1
         TM    20(R5),X'04'                      DEF TYPE 1 RESP
         BO    NORESP                            NOT DEF TYPE 1 RESP
         MVI   RESPSW,C'Y'                       INDICATE RESP NEEDED
NORESP   DS    0H
         TESTCB RPL=(5),                                               X
               AM=VTAM,                                                X
               BRACKET=(BB,EB)
         BNE   BBTEST2                           NO TRY END BRACKET
         MVI   BBSW,C'Y'                         SET BETWEEN BRACKET ??
         B     NOBB                              AND BYPASS
BBTEST2  DS    0H
         TESTCB RPL=(5),                                               X
               AM=VTAM,                                                X
               BRACKET=(NBB,EB)
         BNE   NOBB                              IN BRACKET STATE
         MVI   BBSW,C'Y'                         BETWEEN BRACKETS
NOBB     DS    0H
         L     R7,MTSCMPI@                       GET I/O AREA ADDR
         CLI   0(R7),X'F3'                       STRUCTURED FIELD CMDS
         BE    COMMREJ                           NO CAN DO THAT
         SHOWCB RPL=(5),                                               X
               AM=VTAM,                                                X
               FIELDS=(RECLEN),                                        X
               AREA=SLENGTH,                                           X
               LENGTH=4
         L     R2,SLENGTH                        STORE LENGTH
         ST    R2,MTSCMPIS   SET INPUT LENGTH                    93323
         MVC   MTSCMPOS,=A(&MAXBUFF)  SET MAXIMIM OUTPUT LENGTH  93323
         LA    R1,MTSSPARM   POINT TO CONVERSION PARAMETER LIST  93323
         L     R15,COMPRESS  GET COMPRESSION ROUTINE ADDRESS     93323
         BASR  R14,R15       COMPRESS THE BUFFER                 93323
         CH    R15,=H'8'     PROBLEM ?                           93323
         BNL   COMMREJ       YES; REJECT IT                      93323
         LM    R1,R2,MTSCMPO@  LOAD OUTPUT BUFFER ADDRESS AND LENGTH
         LR    R7,R1         NEW BUFFER                          93323
         L     R2,0(,R2)     NEW LENGTH                          93323
*---------------------------------------------------------------------*
*  THIS CODE IS THE "HEART" OF THIS PROGRAM IT TPUTS DATA TO          *
*  THE TERMINAL, TGET'S ANY RESPONSE, THEN SENDS IT TO THE            *
*  APPLICATION.                                                       *
*---------------------------------------------------------------------*
SCROUT   TM    TERMFLAG,TFNCRT  3270 ?                           93330
         BZ    SCROUTC       YES                                 93330
         TPUT  (R7),(R2),CONTROL                                 93330
         L     R7,MTSCMPI@                       GET I/O AREA ADDR
         TGET  6(7),&MAXBUFF-1,ASIS,NOWAIT        GET TO UNLOCK KYBD
         MVC   0(6,R7),=X'7D4040114040'  FAKE 3270 PREFIX        93330
         B     SCROUTCM                                          93330
SCROUTC  TPUT  (R7),(R2),FULLSCR  1,NOEDIT                       93323
         L     R7,MTSCMPI@                       GET I/O AREA ADDR
         TGET  (7),&MAXBUFF-1,ASIS,NOWAIT        GET TO UNLOCK KYBD
SCROUTCM CLI   RESPSW,C'Y'                       RESPONSE NEEDED
         BNE   SKIPRESP                          NO SKIP IT
         L     R6,CID                            LOAD CID
         SEND  RPL=(5),                                                X
               ARG=(6),                                                X
               STYPE=RESP,                                             X
               CONTROL=DATA,                                           X
               BRACKET=(NBB,NEB),                                      X
               RESPOND=(NEX,FME,NRRN,NQRESP),                          X
               OPTCD=(SYN,CA)
         B     SKIPRESP
COMMREJ  DS    0H
         L     R6,CID
         SEND  RPL=(5),                                                X
               ARG=(6),                                                X
               STYPE=RESP,                                             X
               CONTROL=DATA,                                           X
               BRACKET=(NBB,NEB),                                      X
               SSENSEO=FI,                                             X
               SSENSMO=03,                                             X
               RESPOND=(EX,FME,NRRN,NQRESP),                           X
               OPTCD=(SYN,CA)
SKIPRESP DS    0H
         L     R6,CID                             LOAD CID
         L     R7,MTSCMPI@                        GET I/O AREA ADDR
         RECEIVE RPL=(5),                                              X
               EXIT=RECVEX,                                            X
               AREA=(7),                                               X
               AREALEN=&MAXBUFF,                                       X
               OPTCD=(ASY,ANY,CA,Q)
         MVI   RESETCNT,C'Y'                      RESET COUNTS
EXITRECV DS    0H
         L     R14,SAVEREG3                       LOAD RETURN ADDR
         BR    R14
         SPACE 2                                                 93323
         USING ATTNEXIT,R15    TSO ATTENTION EXIT                93323
ATTNEXIT LM    R11,R12,BASESAVE   LOAD BASES                     93323
         DROP  R15                                DROP R15
         USING SIM3270,R11,R12                                   93323
         STFSMODE ON,,NOEDIT=YES                  RESTORE FULLSCR MODE
         BR    R14                                RETURN TO TSO
         SPACE 1
SAVER0   DC    F'0'
WAITINT  DC    D'0'                               WAIT INTVL FOR STIMER
HALFSEC  DC    C'00000050'                        WAIT A .5 SEC.
TWOSEC   DC    C'00000200'                        WAIT 2 SECONDS
OUTAREA  DC    F'0'                               ADDR OF I/O AREA
DATAAREA DC    CL8' '                             AREA FOR REQSESS
SLENGTH  DC    F'0'                               MSG LENGTH
BASESAVE DC    2A(0)         BASE ADDRESSES                      93323
CID      DC    F'0'                               CID VALUE
WAITECB  DC    F'0'                               ECB FOR WAIT/POST
SAVEAR2  DC    18A(0)                             SAVEAREA FOR SCIP
SAVEREG3 DC    F'0'
SAVEAR3  DC    18A(0)
EXIT     DC    CL1'N'                           EXIT SWITCH
BBSW     DC    CL1'N'                           BRACKET SWITCH
RESPSW   DC    CL1'N'                           RESP SWITCH
LOOPVAL1 EQU   1680                               14 MIN OF HALF SEC
LOOPVAL2 EQU   1                                  2 SEC OF 2 SEC INTV
RESETCNT DC    C'N'                               RESET LOOP CTRS
         SPACE 1                                                 93323
         WXTRN COMP3270      MAY BE LINKED                       93323
COMPRESS DC    A(COMP3270)   COMPRESSION ROUTINE                 93323
MTSSPARM DC    0A(0)         COMPRESSION ROUTINE PARMS           90147
MTSCMPO@ DC    A(0)          OUTPUT BUFFER ADDRESS               90147
MTSCMPOL DC    A(0)          ADDRESS OF MAX/ACT OUTPUT LENGTH    90147
MTSCMPI@ DC    A(0)          INPUT BUFFER ADDRESS                90147
MTSCMPIL DC    A(0)          ADDRESS OF INPUT LENGTH             90147
MTSCMPB@ DC    A(0)          ADDRESS OF BUFFER SIZE              90147
MTSCMPF@ DC    A(0)          ADDRESS OF OPTION FLAGS             90147
MTSCMPC@ DC    A(0)          ADDRESS OF COLOR CONVERSION TABLE   90147
         DC    A(0)            SPARE                             90147
         DC    A(0)            SPARE                             90147
MTSCMPOS DC    A(0)          OUTPUT LENGTH (MAX, THEN RESULT)    90147
MTSCMPIS DC    A(0)          INPUT LENGTH                        90147
MTSCMPBS DC    A(0)          BUFFER SIZE                         90147
MTSCMPFG DC    X'00'         REQUESTED FUNCTIONS                 90147
MTSCMFSF EQU   X'80'           INSERT PROT. SF IF NO SF IN LINE 2
MTSCMNCM EQU   X'40'           BYPASS BUFFER COMPRESSION, ETC.   90147
MTSCMNCO EQU   X'20'           BYPASS COLOR SUBSTITUTION         90147
MTSCMESC EQU   X'10'           BUFFER MAY CONTAIN ESCAPE (BTAM)  90147
MTSCMCCW EQU   X'08'           BUFFER CONTAINS CCW               90147
MTSCMWCC EQU   X'04'           BUFFER CONTAINS WCC/PCC           90147
MTSCMPFA DC    X'00'         ADDRESSING MODE                     90147
MTSCMA16 EQU   X'02'           USE 16-BIT ADDRESSING ONLY        90147
MTSCMA14 EQU   X'01'           14-BIT ADDRESSING SUPPORTED       90147
MTSCMPFC DC    X'00'         COLOR MODE                          90147
MTSCMCO7 EQU   X'07'           7-COLOR MODE                      90147
MTSCMCOB EQU   X'70'           BACKGROUND COLOR AVAILABLE        90147
MTSCMPFH DC    X'00'         EXTENDED HIGH-LIGHTING              90147
MTSCMHI7 EQU   X'07'           ALL EXTENDED HIGH-LIGHTING        90147
MTSCMPFM DC    X'00'         MISCELLANEOUS OPTIONS               90147
MTSCMVAL EQU   X'80'           FIELD VALIDATION SUPPORTED        90147
MTSCMOUT EQU   X'40'           FIELD OUTLINING                   90147
MTSCMGE  EQU   X'08'           GRAPHICS ESCAPE PROCESSING        91101
         DC    X'00'         SPARE                               90147
         DC    X'00'         SPARE                               90147
         DC    X'00'         SPARE                               90147
SIATTRIB DC    F'0'          ATTRIBUTES AND OPTIONS              87110
SIPRMSZE DC    XL2'0'        PRIMARY SIZE                        87110
SIALTSZE DC    XL2'0'        ALTERNATE SIZE                      87110
DEVBYTE  DC    H'0'                                              93323
DEVLINE  DC    H'0'                                              93323
         SPACE 1                                                 93323
ACBMSG   DC    C'ACB FOR SIM3270 FAILED TO OPEN' NO SESSION ERR MSG
         DC    C' PLU '                                          93319
ACBMSGLU DC    CL8' ',C'  CODE '                                 93319
ACBMSGCD DC    CL2' ',C' '                                       93319
ACBMSGSZ EQU   *-ACBMSG-1    LENGTH OF MESSAGE                   93319
EMSG     DC    C'SESSION NOT AVAILABLE '        NO SESSION ERR MSG
APPLID   DC    CL8'A'        (INIT BLANKS)      APPLID FOR PLU
ECHO1    DC    X'08',CL8'12345678'              APPLID FOR SIM3270
EMSGLN   EQU   *-EMSG                                            93319
CLEAR    DC    X'F5'                            3270 CLEAR CMD
         DC    X'C1'                            WCC FOR CLEAR
*TCAM*   DC    X'115D7E'
         DC    X'114040'
         DC    X'3C404000'
*WHY?*   DC    X'114040'
         DC    X'13'
CLEARLN  EQU   *-CLEAR                                           93319
STAXLIST STAX  ATTNEXIT,MF=L                     STAX MACRO LIST FORM
         DC    C'REQSESS RPL FOLLOWS:'
SIMRPL1  RPL   ACB=SIMACB,                                             X
               AM=VTAM,                                                X
               NIB=SIMNIB
SIMRPL2  RPL   ACB=SIMACB,                                             X
               AM=VTAM,                                                X
               NIB=SIMNIB
SIMRPL3  RPL   ACB=SIMACB,                                             X
               AM=VTAM,                                                X
               NIB=SIMNIB
SIMRPL4  RPL   ACB=SIMACB,                                             X
               AM=VTAM,                                                X
               NIB=SIMNIB
SIMACB   ACB   AM=VTAM,                                                X
               APPLID=ECHO1,                                           X
               MACRF=LOGON,                                            X
               EXLST=SIMEXT
SIMNIB   NIB   NAME=XXXXXXXX,                                          X
               MODE=RECORD,BNDAREA=WANTBIND,                           X
               LISTEND=YES
SIMEXT   EXLST AM=VTAM,                                                X
               SCIP=SCIPEXIT,                                          X
               SYNAD=SYNEXIT,                                          X
               LERAD=LEREXIT
         SPACE 1                                                 93319
HEXTAB   DC    C'0123456789ABCDEF'                               93319
         SPACE 1                                                 93319
UPCASE   DC    256AL1(*-UPCASE)  UPPER CASE TRANSLATE (WHY?)     93323
         TRENT UPCASE,(*-UPCASE+X'40'),(X'81',9),(X'91',9),(X'A2',8)
         ORG   ,                                                 93323
         SPACE 1                                                 93323
         LTORG
         SPACE 1                                                 93323
WANTBIND DC    X'00'         NEGOTIABLE                          93323
         DC    X'02,02,71,40,20,00,00,80,00,00,80,00'            93323
WANTBLU2 DC    X'00'                                             93323
WANTBEDS DC    X'00,00,00,00,00'                                 93323
WANTPROW DC    X'00,00'                                          93323
WANTAROW DC    X'00,00'                                          93323
WANTFLAG DC    X'02'         MODEL 2                             93323
         DC    XL32'00'      PAD                                 93323
         SPACE 2                                                 93323
SAVE     DSECT ,             SAVE/WORK AREA                      93323
DB       DS    2D            CONVERSION                          93323
TERMFLAG DS    X                                                 93330
TFNCRT   EQU   X'80'           OUTPUT DEVICE IS NOT A 3270       93330
SAVEND   EQU   *             END SAVE                            93323
         SPACE 2                                                 93323
         PRINT &PRTSYS                                           93323
         IFGRPL AM=VTAM
         IFGACB AM=VTAM
         IKJCPPL                                 COMMAND PROCESSOR LIST
*DUPE*   YREGS ,                                                 93309
         EJECT ,                                                 93309
SIM3270  CSECT ,                                                 93309
***********************************************************************
*             ELLIS HOLMAN --                                         *
*                                                                     *
*             MODULE NAME IS APPLTAB                                  *
*                                                                     *
*             FUNCTION:                                               *
*                DESCRIBES THE ACB NAMES USED BY SIM3270.             *
*                                                                     *
*                                                                     *
*  NOTES:                                                             *
*    1) THE FIRST MACRO IN THIS ASSEMBLY DESCRIBES THE NUMBER OF      *
*       ENTRIES THAT FOLLOW.  THE NUMBER OF ENTRIES MUST MATCH.       *
*                                                                     *
*   MODIFICATION HISTORY:                                             *
*                                                                     *
***********************************************************************
         APPL  SIM0001                                           93319
         APPL  SIM0002                                           93319
         APPL  SIM0003                                           93319
         APPL  SIM0004                                           93319
         APPL  SIM0005                                           93319
         APPL  SIM0006                                           93319
         APPL  SIM0007                                           93319
         APPL  SIM0008                                           93319
         APPL  SIM0009                                           93319
         APPL  SIM0010                                           93319
         APPL  SIM0011                                           93319
         APPL  SIM0012                                           93319
         APPL  SIM0013                                           93319
         APPL  SIM0014                                           93319
         APPL  SIM0015                                           93319
         APPL  SIM0016                                           93319
         APPL  SIM0017                                           93319
         APPL  SIM0018                                           93319
         APPL  SIM0019                                           93319
         APPL  SIM0020                                           93319
         APPL  SIM0021                                           93319
         APPL  SIM0022                                           93319
         APPL  SIM0023                                           93319
         APPL  SIM0024                                           93319
         APPL  SIM0025                                           93319
         APPL  *END                                              93323
         END   ,                                                 93309
