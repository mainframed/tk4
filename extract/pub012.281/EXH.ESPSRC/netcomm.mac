NCOMM    TITLE 'N E T C O M M  ***  NATIVE VTAM CONTROL MODULE'
         MACRO ,
&LABEL   BINCVRT &REG,&AREA,&PKVAR                            ESP88150
.*
.*  CONVERT THE CONTENTS OF &REG TO DECIMAL AND EDIT INTO &AREA.
.*  &AREA IS A FIELD OF LENGTH SIX THAT WILL CONTAIN THE INTEGER
.*  STRING WITH LEADING BLANKS SUPRESSED.  &PKVAR IS A DOUBLE ESP88150
.*  WORK SPACE.
.*
&LABEL   CVD   &REG,&PKVAR                                    ESP88150
         MVC   &AREA.(6),=X'402020202120'
         ED    &AREA.(6),&PKVAR+5                             ESP88150
         MEND  ,
         SPACE 1                                              ESP88150
         MACRO ,                                              ESP88150
&NM      CHARB &CH,&B                                         ESP88150
         AIF   ('&CH' EQ 'END').END                           ESP88150
&NM      DC    AL1(&CH,0),AL2(&B-KERMIT)                      ESP88150
         MEXIT ,                                              ESP88150
.END     ANOP  ,                                              ESP88150
&NM      DC    X'FF',AL3(0+&B)                                ESP88150
         MEND  ,                                              ESP88150
         SPACE 1                                              ESP88150
         MACRO ,                                              ESP88150
&NM      NINC  &N                                             ESP88150
&NM      LA    R0,1                                           ESP88150
         A     R0,&N                                          ESP88150
         N     R0,=X'0000003F'                                ESP88150
         ST    R0,&N                                          ESP88150
         MEND  ,                                              ESP88150
         SPACE 1                                              ESP88150
         MACRO ,                                              ESP88150
&NM      INC   &N                                             ESP88150
&NM      LA    R0,1                                           ESP88150
         A     R0,&N                                          ESP88150
         ST    R0,&N                                          ESP88150
         MEND  ,                                              ESP88150
         SPACE 1                                              ESP88150
         MACRO ,                                              ESP88150
&NM      MSG   &T,&L                                          ESP88150
         LCLA  &N                                             ESP88150
&N       SETA  K'&T                                           ESP88150
&NM      LA    R1,=C&T       LOAD THE MESSAGE ADDRESS         ESP88150
         AIF   ('&L' EQ '').DFL                               ESP88150
         LA    R0,0-1+&L     LOAD THE MESSAGE LENGTH          ESP88150
         MEXIT ,                                              ESP88150
.DFL     LA    R0,&N-2-1     LOAD THE MESSAGE LENGTH FOR EX   ESP88150
         MEND  ,                                              ESP88150
         SPACE 2                                              ESP88150
         MACRO ,                                                 87159
&NM      VARFD &PFX,&FIELD,&TEXT,&OPT                            87159
         GBLA  &LINE                                             87159
         AIF   (&LINE GT 0).UPLINE                               87159
&LINE    SETA  5                                                 87159
.UPLINE  ANOP  ,                                                 87159
&LINE    SETA  &LINE+2                                           87159
&NM      FDOPT WHITE,SKIP,SBA=(&LINE,5)    RESET DISPLAY MODE    87166
         FDTM  &PFX.FG,&PFX.FTXT+&PFX.FERR,BM=&NM.2,BZ=&NM.1,BO=&NM.0
&NM.0    FDOPT RED,INTENSE                                       87159
&NM.1    FD    '==>',PREV                                        87159
&NM.2    FDOPT SBA=(&LINE,9)                                     87159
         FD    ' ',NONDISP,PINK  FINAGLE DISTINCT OPTIONS        87165
         FDIN  &FIELD,GREEN,DEB,&OPT,UP,LEN=8                    87159
         FD    ' ',INT,PINK  ANOTHER DISTINCT FINAGLE FIELD      87166
         AIF   ('&TEXT' EQ '').NOTAG                             87159
         FDOPT SBA=(&LINE,30)                                    87159
         FD    &TEXT,TURQ,SKIP                                   87159
.NOTAG   MEND  ,                                                 87159
         SPACE 1                                                 87357
         MACRO ,                                                 87357
&NM      LOFF  &R,&F,&NONE=VERFAIL                               87357
&NM      ICM   &R,15,&F                                          87357
         BNP   &NONE                                             87357
         CR    &R,R4         IN MEMORY ?                         87357
         BNL   VERFAIL       NO; FAIL                            87357
         LA    &R,UADSMHDR(&R)  RELOCATE                         87357
         MEND  ,                                                 87357
         SPACE 1                                                 87159
         EJECT ,
         MACRO ,
&NM      ORCH  &OFF,&VAL,&REP
         LCLC  &N
&N       SETC  '1'
         AIF   (T'&REP EQ 'O').ONCE
&N       SETC  '&REP'
.ONCE    ANOP  ,
         ORG   ORDERS+&OFF
&NM      DC    &N.AL1(&VAL)
         MEND  ,
         SPACE 1
         MACRO ,
&NM      OFFBR &LBL,&DEF
         AIF   (T'&DEF EQ 'O').NODEF
OFF&DEF  EQU   *-OFFBASE
.NODEF   ANOP  ,
&NM      B     &LBL
         MEND  ,
         SPACE 1
         MACRO ,
&NM      CHRET &RETRY=
         AIF   ('&RETRY' EQ '' OR '&RETRY' EQ '0').NORET
&NM      LA    R2,&RETRY     SET RETRY ADDRESS
         AGO   .COMBAL
.NORET   ANOP  ,
&NM      SLR   R2,R2         NO RETRY ADDRESS
.COMBAL  BAL   R9,CCNZVTAM   ANALYZE VTAM RETURN CODES
         MEND  ,
         SPACE 1
         MACRO ,
&NM      XHEAD &SAVE=,&EXH=NO,&RPL=,&LTORG=NO,&RENT=4*18,&MODE=  89328
         GBLC  &X@SAVE,&X@EXIT,&X@RENT
         LCLC  &SA
         AIF   ('&X@SAVE' EQ '').OLDSAVE
&X@SAVE  DC    18A(0)        PRIOR XHEAD SAVE AREA
&X@SAVE  SETC  ''
.OLDSAVE AIF   ('&LTORG' EQ 'NO').NOLIT
         LTORG ,
.NOLIT   DROP  ,
&X@EXIT  SETC  '&NM'         EXIT NAME
&X@RENT  SETC  ''
         USING &NM,R12
         AIF   ('&MODE' EQ 'OS').DOALL
&NM      LR    R12,R15       LOAD BASE
         AGO   .CMALL
.DOALL   ANOP  ,
&NM      STM   R14,R12,12(R13)  SAVE IN USER'S SAVE AREA
         LR    R12,R15       SET LOCAL BASE
.CMALL   L     R10,=A(WORKBASE)  AND COMMON WORK AREA
         USING WORKBASE,R10
         LR    R6,R1
         AIF   ('&RPL' EQ 'NO').NORPL
         USING IFGRPL,R6
         L     R7,RPLUSFLD   GET NIB (STASHED EARLIER)
         USING MTSWORK,R7    MTSWORK BEGINS WITH NIB
&X@SAVE  SETC  ''
.NORPL   AIF   ('&SAVE' EQ 'NONE').NOSAVE
&SA      SETC  '&SAVE'
         AIF   ('&SAVE' NE 'RENT').NORENT
&SA      SETC  ''
&X@RENT  SETC  '&RENT'
         LR    R5,R0         SAVE R0 IN LERAD/SYNAD              89131
         GETMAIN R,LV=&RENT
         STM   R13,R12,0(R1)
         LR    R0,R5         RESTORE R0                          89131
         LR    R13,R1
.NORENT  AIF   ('&SA' NE '').DFLT
&SA      SETC  '&NM'.'ZZZZZZZZ'
&SA      SETC  '&SA'(1,4).'SAVE'
.DFLT    AIF   ('&SAVE' EQ 'RENT').NDFLT
         STM   R13,R12,&SA   SAVE RETURN INFO IN OUR SAVE AREA
         LA    R13,&SA
.NDFLT   ANOP  ,
&X@SAVE  SETC  '&SA'
.NOSAVE  AIF   ('&EXH' NE 'YES' AND '&EXH' NE '').MEND           89328
         AIF   ('&RPL' EQ 'NO').MEND
.MEND    MEND  ,
         SPACE 1
         MACRO ,
&NM      XEXIT &LTORG=,&MODE=
         GBLC  &X@SAVE,&X@EXIT,&X@RENT
         LCLC  &XN,&CS
         AIF   ('&X@SAVE' EQ '').MEND
&XN      SETC  '&X@EXIT'.'ZZZZZZZZ'
&XN      SETC  '&XN'(1,4).'EXIT'
         AIF   ('&NM' EQ '' OR '&NM' EQ '&XN').NODC
&NM      DS    0H
.NODC    AIF   ('&X@RENT' NE '').FRENT
&XN      LM    R13,R14,&X@SAVE   RESTORE RETURN REGISTERS
         AIF   ('&MODE' NE 'OS').NOS
         LM    R15,R12,16(R13)  RESTORE ALL REGISTERS
.NOS     BR    R14           RETURN
&X@SAVE  DC    18A(0)
&X@SAVE  SETC  ''
         AGO   .LORG
.FRENT   ANOP  ,
&XN      LR    R1,R13
         LM    R13,R14,0(R1)
         FREEMAIN R,LV=&X@RENT,A=(R1)
         AIF   ('&MODE' NE 'OS').NOS2
         LM    R15,R12,16(R13)  RESTORE ALL REGISTERS
.NOS2    BR    R14
&CS      SETC  '&SYSECT'
&X@SAVE  DSECT ,
         DC    18A(0)
&X@SAVE  SETC  ''
&CS      CSECT ,
.LORG    AIF   ('&LTORG' EQ 'NO').MEND
         LTORG ,
.MEND    MEND  ,
         SPACE 1                                                 88348
         MACRO ,                                                 88348
&NM      UNPOST &ECB                                             88348
&NM      NI    &ECB,255-X'80'-X'40'                              88348
         MEND  ,                                                 88348
         SPACE 1
         COPY  OPTIONGB                                          89328
         GBLB  &ZZ@BLUK      'BLOOK' EXPANSION VARIABLE          89345
         SPACE 1                                                 89328
         SYSPARM LIST=YES                                        89328
         EJECT ,                                                 89328
*
*        THIS MODULE CONTAINS MULTI-TASKING VTAM SUPPORT FOR THE 89328
*        KERMIT FILE TRANSFER PROGRAM.   THE CONTROL CODE DERIVES
*        FROM PROGRAMS EXHABASE AND EXHCCSIG, COPYRIGHT 1986 BY  89328
*        EXPERT SYSTEM PROGRAMMING, INC.; THE KERMIT CODE IS A HIGHLY
*        MODIFIED VERSION OF THE COLUMBIA UNIVERSITY CODE (REL. 1)
         SPACE 2
***********************************************************************
*                                                                     *
*        COPYRIGHT 1986  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR                                           89328
NETCOMM  SAVEM ZERO,BASE=R12,PARM=R9                             89328
         B     BEGIN                                             89328
MYAPPLID DC    AL1(8),CL8'COMM    '
BEGIN    L     R10,=A(WORKBASE) GET COMMON WORK AREA
         USING WORKBASE,R10  MAKE SECOND BASE
         L     R1,=A(SUBVTAM)  LOAD INITIALIZATION CODE          90231
         IDENTIFY EPLOC=FROGFACE,ENTRY=(R1)  IDENTIFY THE SUBTASK CODE
         BXLE  R15,R15,CONTOPEN  PROCEED                         89328
IDENFAIL WTO   'COM412E SUBTASK IDENTIFY FAILED',ROUTCDE=1,DESC=1
         ABEND 412,DUMP
         SPACE 1
CONTOPEN LA    R9,0(,R9)     CLEAR HIGH BYTE
         LTR   R9,R9         ANY PARM POINTER ?
         BZ    PARMDONE      NO
         ICM   R9,7,1(R9)    ANY PARM ?
         BZ    PARMDONE      NO
         SLR   R8,R8
         ICM   R8,3,0(R9)    ANY TEXT ?
         BNP   PARMDONE      NO
PARMNEXT LTR   R8,R8         ANY MORE ?
         BNP   PARMDONE      NO
         CLI   2(R9),C' '    SPACE ?
         BE    PARMONE       YES; SKIP
         CLI   2(R9),C','    SEPARATOR ?
         BNE   PARMTEST
PARMONE  LA    R0,1          SET BUMP INCREMENT
PARMBUMP AR    R9,R0         BUMP
         SR    R8,R0
         BP    PARMNEXT
         B     PARMDONE
         SPACE 1
PARMTEST CLC   =C'APPLID=',2(R9)
         BE    PARMAPPL      YES
         CLC   =C'APPL=',2(R9)
         BE    PARMAPPL
         CLC   =C'LOCK',2(R9)  LOCKED MODE ?
         BE    PARMLOCK      YES
         CLC   =C'UNLOCK',2(R9)  UNLOCKED ?
         BE    PARMUNLK      YES
         CLC   =C'LOGON',2(R9)  RESTRICTED ACCESS ?              87350
         BE    PARMLOGN      YES                                 87350
         CLC   =C'KTEST',2(R9)  SPECIAL TEST MODE ?              91078
         BE    PARMKTST      YES                                 91078
         CLC   =C'DEBUG',2(R9)  DEBUG REQUEST ?                  87263
         BE    PARMDBUG      YES; SET DUMP FLAG                  87263
PARMBAD  WTO   'COM425E INVALID PARM FIELD'
         ABEND 425
         SPACE 1
PARMUNLK LA    R0,6          SET INCREMENT
         NI    FLAGS,255-FGSYS  RESET LOCK STATE                 87263
         B     PARMBUMP      AND BUMP OVER
PARMLOCK LA    R0,4          SET INCREMENT
         OI    FLAGS,FGSYS+FGLOGON   LOCK TO SYSTEMS ONLY        88004
         B     PARMBUMP      AND IGNORE FOR NOW
PARMLOGN LA    R0,5          SET INCREMENT                       87350
         OI    FLAGS,FGLOGON  REQUIRE LOGON TO USE               87350
         B     PARMBUMP      AND IGNORE FOR NOW                  87350
PARMKTST OI    FLAGS,FGTEST  SET TEST MODE                       91078
PARMDBUG LA    R0,5          PARM LENGTH                         87263
         OI    DUMPCODE,X'80'  TAKE A DUMP ON 522                87263
         B     PARMBUMP                                          87263
PARMAPPL LA    R9,1(,R9)     SKIP THE PARM
         BCT   R8,*+8
         B     PARMBAD
         CLI   1(R9),C'='    END OF PARM ?
         BNE   PARMAPPL      NO; KEEP SEARCHING
         LA    R1,MYAPPLID+1  SET OUTPUT TARGET
         CLI   2(R9),C'$'    VALID APPLICATION ID ?
         BL    PARMBUMP      NO; OMITTED FIELD ?
         MVC   MYAPPLID+1(8),=CL8' '   CLEAR IT
         LA    R0,8          MAXIMUM TO DO
PARMAMOV CLI   2(R9),C','    END OF PARM ?
         BNH   PARMONE       YES
         MVC   0(1,R1),2(R9)  MOVE ONE BYTE
         LA    R9,1(,R9)
         LA    R1,1(,R1)
         BCT   R8,*+8
         B     PARMONE
         BCT   R0,PARMAMOV
         B     PARMONE       SKIP NEXT
         SPACE 1                                                 91078
PARMDONE LA    R2,=CL8'KERMNET'  POINT TO NORMAL KERMIT          91078
         TM    FLAGS,FGTEST  TEST MODE ?                         91078
         BZ    *+8           NO                                  91078
         LA    R2,=CL8'KERMNEW'  LOAD TEST VERSION               91078
         LOAD  EPLOC=(R2)    GET KERMIT/NETKERM INTERFACE ENTRY  91078
         ST    R0,@KERMIT    SAVE FOR SUBTASKS                   90206
         AIF   (&SVC@SVC GT 0).NORESVC                           90186
         SERVINIT ,          LOAD THE SERVICE ROUTINE            90186
.NORESVC SPACE 1
RTRYOPEN SLR   R15,R15                 CLEAR RETURN-CODE REGISTER
         OPEN  MYACB         CONNECT KERMIT TO VTAM              89328
         SPACE 1
*  THE VALUES IN REG 15 ARE RETURN CODES AS SET BY VTAM.              *
*   ERREXIT IS NOT ENTERED FOR OPEN ERROR.                            *
         CLI   MYACB+X'31',X'52'  HALT IN PROGRESS ?
         BE    MAINQUIT                YES, END THE TASK         87263
         LTR   R15,R15                 SUCCESSFUL OPEN ?
         BZ    OPENOK                  YES, ALLOW LOGONS TO HAPPEN
         SPACE 1
*        CHECK FOR AN ACB ERROR CODE OF X'58' WHICH CAN HAPPEN IF     *
*        THE OPERATOR HAS STARTED A SECOND COPY OF NETCOMM. IN THIS
*        CASE, TELL THE OPERATOR ABOUT THE MISTAKE AND TERMINATE
*        EXECUTION.                                                   *
         CLI   MYACB+X'31',X'58'  APPLID ALREADY IN USE?
         BNE   OPENA5A                 NO => CHECK NEXT ERROR
         WTO   'COM402E NETCOMM ALREADY ACTIVE',DESC=1,ROUTCDE=1
         B     MAINQUIT      QUIT                                87263
         SPACE 1
*        CHECK FOR AN ACB ERROR CODE OF X'5A' WHICH CAN HAPPEN IF     *
*        VTAM IS JUST COMING UP AND NETCOMM'S APPLID IS NOT YET       *
*        ACTIVE. IF THIS IS THE CASE, ISSUE A STIMER WAIT FOR
*        ONE (1) SECOND AND RETRY THE OPEN ACB FUNCTION.              *
OPENA5A  CLI   MYACB+X'31',X'5A'  CAN'T OPEN ACB YET ?
         BE    OPENLOOP                NO, REAL OPEN ERROR
         SPACE 1
*        CHECK FOR AN ACB ERROR CODE OF X'5C' WHICH CAN HAPPEN IF     *
*        VTAM IS DOWN. IF THIS IS THE CASE ISSUE A STIMER WAIT FOR    *
*        ONE (1) SECOND AND RETRY THE OPEN ACB FUNCTION.              *
         CLI   MYACB+X'31',X'5C'  CAN'T OPEN ACB YET ?
         BE    OPENLOOP                NO, REAL OPEN ERROR
         SPACE 1
*        CHECK FOR AN ACB ERROR CODE OF X'70' WHICH CAN HAPPEN IF     *
*        NETCOMM ABENDS AND IS RE-ATTACHED BY VTAM AND VTAM HAS NOT   *
*        YET SUCCESSFULLY FINISHED CLEANUP PROCESSING FOR             *
*        NETCOMM'S ACB. IF THIS IS THE CASE ISSUE A STIMER WAIT FOR   *
*        ONE (1) SECOND AND RETRY THE OPEN ACB FUNCTION.              *
         CLI   MYACB+X'31',X'70'   CAN'T OPEN ACB YET ?
         BNE   OPENFAIL                NO, REAL OPEN ERROR
OPENLOOP STIMER WAIT,BINTVL==A(1*100)  WAIT 1 SECOND, TRY AGAIN
         B     RTRYOPEN                GO AND RETRY OPERATION
         SPACE 1
OPENFAIL WTO   'COM413E VTAM OPEN FAILED',ROUTCDE=(1,11)
         ABEND 201           ERROR  DURING OPEN/CONNECT
         SPACE 1
OPENOK   LA    R3,LOGRPL
         SETLOGON RPL=(R3),OPTCD=START BEGIN
         CHRET RETRY=OPENOK  CHECK RETURN CODE
REQSESS  REQSESS RPL=REQRPL                                      93263
         CHECK RPL=REQRPL                                        93263
         CHRET RETRY=REQSESS                                     93263
         SERVICE PASON       I DO MY OWN CHECKING                89337
         SERVICE SWAPN       MAKE LOGICALLY NON-SWAPPABLE        89337
         WTO   'COM468I NETCOMM READY FOR LOGONS'
         L     R15,=A(INITALIZ)  GET INITIALIZATION ROUTINE
         BALR  R14,R15       CALL IT
OPENGET  L     R2,=A(XREAD)
         RECEIVE RPL=READRPL,RTYPE=(DFSYN,DFASY),OPTCD=(ASY,ANY,CA),   *
               EXIT=(R2)     WAIT FOR COMPLETION
         CHRET RETRY=OPENGET CHECK RETURN CODE
         OI    FLAGS,FGREAD  SHOW I/O PENDING
         EXTRACT MYCSCB,FIELDS=(COMM)  GET THE CSCB ECB PTR
         L     R15,MYCSCB  GET THE POINTER
         MVC   MECBLIST,0(R15)  COPY THE ECB ADDRESS
         LA    R0,4(,R15)    CIB ADDRESS FIELD
         QEDIT ORIGIN=(0),CIBCTR=2  SUPPORT MODIFY
MAINLOOP UNPOST TIMEECB      RESET THE STIMER ECB                88348
         TM    FLAGS,FGDOWN  GOING DOWN ?
         BNZ   MAINQUET      TEST FOR EMPTY QUEUE
         STIMER REAL,EXITTIME,BINTVL==A(09*60*100)  WAIT SOME
         WAIT  ECBLIST=MECBLIST  WAIT FOR SOMETHING
         TM    TIMEECB,X'40' TIMER DONE ?
         BNZ   MAINLOON      YES
         TTIMER CANCEL       ELSE CANCEL IT
MAINLOON TM    MAINECB,X'40' SHUT-DOWN ?
         BNZ   MAINQUET      YES; WAIT FOR QUIESCE
         L     R1,MECBLIST   GET COMM. ECB
         TM    0(R1),X'50'   CHECK CONDITION
         BZ    MAINLOOP      NORMAL
         BM    MAINMODY      MODIFY ONLY
MAINPOST OI    FLAGS,FGDOWN  SHOW SYSTEM GOING DOWN
         LA    R7,WORKQUE-(MTSQLINK-MTSWORK)
         USING MTSWORK,R7
MPOSLOOP ICM   R7,15,MTSQLINK  GET NEXT TASK
         BZ    MPOSPOST      NONE
         OI    MTSFLGT,MTSFGONE  SHOW TERMINAL COMING DOWN
         ICM   R2,15,MTSTASK  ANY SUBTASK ?
         BZ    MPOSLOOP      NO
         L     R0,=X'00622000'  SET COMPLETION CODE
         OI    MTSFLGT,MTSFXEND  END REQUEST                     89328
         POST  MTSATECB,(0)  POST ATTENTION                      89328
         LA    R1,MTSCNECB   GET CANCEL ECB ADDRESS              89328
         POST  (1),(0)       AND CANCEL
         B     MPOSLOOP      TRY ANOTHER TASK
MPOSPOST L     R1,=A(MAINECB)  GET MAIN TASK ECB
         POST  (1),0         POST IT
         DROP  R7                                                89328
MAINQUET ICM   R0,15,WORKQUE  ANYTHING TO DO ?
         BZ    MAINEXIT      NO; QUIT
         LA    R2,30         WAIT FOR 30 SECONDS                 87013
MAINQUEX UNPOST TIMEECB                                          88348
         STIMER REAL,EXITTIME,BINTVL==A(100)  WAIT 1 SECOND 30 TIMES
         WAIT  ECB=TIMEECB
         ICM   R0,15,WORKQUE  DONE YET ?
         BZ    MAINEXIT      YES
*87321*  ICM   R0,15,MTV#CURS  ANY ACTIVE SUB-TASKS ?            87273
*87321*  BZ    MAINEXIT      NO; MAY HAVE A HUNG TERMINAL ?      87273
         BCT   R2,MAINQUEX                                       87013
         ICM   R1,15,DUMPCODE  GET DUMP OPTION AND ABEND CODE    87263
         SVC   13            TAKE A DIVE                         87263
DUMPCODE DC    X'00522000'   QUIT WITH SYSTEM 522                87263
         SPACE 1
MAINEXIT CLOSE (MYACB)       CLOSE THE VTAM ACB                  87263
         CLOSE (MYACB)       TELL ME TWICE (SEC. OPERATOR I/F)   87263
MAINQUIT L     R13,4(,R13)
         LM    R14,R12,12(R13)
         SLR   R15,R15       BE GENTLE
         BR    R14           RETURN TO CALLER
         SPACE 1
         PUSH  USING
         DROP  ,
         USING EXITTIME,R15
EXITTIME POST  TIMEECB,0
         BR    R14
         POP   USING
MAINECB  DC    F'0'          MAIN TASK TERMINATION ECB
TIMEECB  DC    F'0'          STIMER ECB
MECBLIST DC    A(0)          COMM. ECB
ECBLIST2 DC    A(TIMEECB),X'80',AL3(MAINECB)  OTHER ECBS
         SPACE 2
*        PROCESS A CIB HERE
*              START AND STOP CODES ARE IGNORED; ONLY MODIFY ACCEPTED
*        COMMANDS:
*        TERMID TEXT - QUEUE THE CIB ON THE TERMINAL'S CHAIN
*        START TERMID - ISSUE SIMLOGON
*        STOP TERMID - TERMINATE THE SUBTASK
*
MAINMODY L     R3,MYCSCB     GET CIB POINTER POINTER
         LA    R3,4(,R3)     GET CIB POINTER
         ICM   R2,15,0(R3)   GET NEXT CIB
         BZ    MAINLOOP      GO BACK TO WAIT
         USING CIB,R2        DECLARE THE CIB                     89328
         CLI   CIBVERB,CIBMODFY  MODIFY REQUEST ?
         BNE   MCIBFREE      NO; JUST FREE IT
         CLI   CIBCONID,0    ISSUED FROM SYSTEM TASK ?
         BE    *+4+6         YES; DON'T CHANGE ID
         MVC   CONID,CIBCONID  SET RESPONSE ID
         LA    R6,CIBDATA    POINT TO THE DATA
         LA    R4,1          MAKE INCREMENT
         LH    R5,CIBDATLN   GET TEXT LENGTH
         AR    R5,R6         GET END
         SR    R5,R4         GET LAST BYTE
         BAL   R14,SCANPARM  LOOK FOR VERB
         CLI   INITDDB,C' '  ANY ?
         BNH   MCIBFREE      NOTHING; JUST FREE IT
         CLC   =C'START',INITDDB  START A SUBTASK ?
         BE    MCIBSTAR      YES
         CLC   =C'STOP',INITDDB  STOP ONE ?
         BE    MCIBSTOP      YES
         CLC   =C'DUMP',INITDDB  STOP ONE WITH DUMP?             87018
         BE    MCIBDUMP      YES
         CLC   =C'ABEND',INITDDB  RECALCITRANT TERMINATION ?     87099
         BE    MCIBABND      YES; FORCE IMMEDIATE ABEND          87099
         CLC   =C'LOCK',INITDDB  SYSTEMS MODE                    87356
         BE    MCIBFLOK      YES                                 87356
         CLC   =C'UNLOCK',INITDDB                                87356
         BE    MCIBFUNK      YES                                 87356
         CLC   =C'LOGON',INITDDB  SET LOGON MODE ?               87356
         BE    MCIBFLOG      SET LOGON MODE                      87356
         CLC   =C'NOLOG',INITDDB  RESET LOGON ?                  87356
         BE    MCIBFUNG      YES                                 87356
         CLC   =C'SHOW',INITDDB   SHOW USERS ?                   90231
         BE    MCIBSHOW                                          90231
         CLC   =C'LIST',INITDDB   LIST ACTIVE ?                  90231
         BE    MCIBLIST                                          90231
         CLC   =C'DOWN',INITDDB   SHUT-DOWN                      90231
         BE    MCIBDOWN                                          90231
         CLC   =C'QUI',INITDDB  ALT. SHUT-DOWN (QUIT/QUIESCE)    90231
         BE    MCIBDOWN      YES                                 90231
         MVC   MCIBERMV,INITDDB  INDICATE ERROR
         LA    R1,MCIBDTXT   INVALID TEXT
MCIBERRM SLR   R0,R0
         IC    R0,CONID
         WTO   MF=(E,(1))    ISSUE THE ERROR MESSAGE
MCIBFREE QEDIT ORIGIN=(R3),BLOCK=(R2)  FREE THE CIB
         B     MAINMODY      LOOK FOR ANOTHER
         SPACE 1                                                 87356
MCIBFLOK OI    FLAGS,FGSYS   LOCKED FOR SYSTEM USE ONLY          87356
         B     MCIBOK                                            87356
MCIBFUNK NI    FLAGS,255-FGSYS  UNLOCK                           87356
         B     MCIBOK                                            87356
MCIBFLOG OI    FLAGS,FGLOGON  REQUIRE LOGON                      87356
         B     MCIBOK                                            87356
MCIBFUNG NI    FLAGS,255-FGLOGON  NO LOGON                       87356
         B     MCIBOK                                            87356
         SPACE 1                                                 90231
MCIBDOWN OI    FLAGS,FGQUIT  SHOW GRACEFUL QUIESCE               90231
         ICM   R0,15,WORKQUE ANY ACTIVE TASKS ?                  90231
         BZ    MAINPOST      NO; QUIT NOW                        90231
         LA    R7,WORKQUE-(MTSQLINK-MTSWORK)                     90231
         USING MTSWORK,R7                                        90231
MCIBDOWL ICM   R7,15,MTSQLINK  GET NEXT TASK                     90231
         BZ    MCIBOK        NONE                                90231
         OI    MTSFLGT,MTSFGOIN  SHOW TERMINAL COMING DOWN       90231
         B     MCIBDOWL      TRY ANOTHER TASK                    90231
         DROP  R7                                                90231
         SPACE 1
MCIBSTAR BAL   R14,SCANPARM  LOOK FOR TERMID
         CLI   INITDDB,C' '  ANY ?
         BH    MCIBSTAN      YES
MCIBSMSG LA    R1,MCIBDSID   BAD START ID
         B     MCIBERRM      ISSUE ERROR MESSAGE
MCIBSTAN BAL   R14,FINDTERM  LOCATE THE SUBTASK                  87018
         B     MCIBSTAQ      NOT FOUND - OK TO START             87018
         USING MTSWORK,R7
         B     MCIBSMSG      YES; BAD START
MCIBSTAQ MVC   NIBSYM+INITNIB-NIBST,INITDDB  SET ID
         SLR   R8,R8         CLEAR LOGON DATA LENGTH             87130
         LR    R11,R6         ANY OLD AREA                       87130
         BAL   R14,SCANPARM  ANOTHER PARAMETER ?                 87130
         CLI   INITDDB,C' '                                      87130
         BNH   MCIBSTAS      NO MORE                             87130
         L     R11,INITR6     GET PARAMETER START                87130
MCIBLLP  CLI   0(R5),C' '    TRAILING BLANK ?                    87130
         BE    MCIBLBK       YES, BACK-UP                        87130
         CLI   0(R5),C','    TRAILING COMMA ?                    87130
         BNE   MCIBLLN       NO; COMPUTE LENGTH                  87130
MCIBLBK  SR    R5,R4         BACK-SPACE                          87130
         CR    R5,R6         ALL ?                               87130
         BH    MCIBLLP       NO; TRY AGAIN                       87130
MCIBLLN  LA    R5,1(,R5)     ALLOW FOR RELATIVITY                87130
         SR    R5,R11         TEST LENGTH                        87130
         BNP   MCIBSTAS      NONE                                87130
         LR    R8,R5         SET PARM LENGTH                     87130
MCIBSTAS SIMLOGON RPL=INITRPL,NIB=INITNIB,OPTCD=(SYN,Q,NRELRQ),        *
               RECLEN=(R8),AREA=(R11)  REQUEST LOGON             87130
         CH    R15,=H'8'     RETRY ?
         BE    MCIBSTAS      YES
         CLI   RPLFDBK+INITRPL-IFGRPL,0  ACCEPTED ?              87013
         BNE   MCIBSTAF                                          87013
         LTR   R15,R15
         BZ    MCIBOK
MCIBSTAF LA    R1,MCIBSFEL   FAILED
         B     MCIBERRM
MCIBOK   LA    R1,MCIBOKMG
         B     MCIBERRM
         SPACE 1
MCIBSTOP BAL   R14,SCANPARM  GET NEXT PARM
         BAL   R14,FINDTERM  GET SUBTASK                         87018
         USING MTSWORK,R7
         B     MCIBSMSG      INVALID ID
         L     R0,=X'00622000'  SET COMPLETION CODE
MCIBSTMP OI    MTSFLGT,MTSFGONE  SHOW TERMINAL COMING DOWN
         ICM   R14,15,MTSTASK  ANY SUBTASK ?
         BZ    MCIBOK        NO; DONE
         OI    MTSFLGT,MTSFXEND  END REQUEST                     89328
         POST  MTSATECB,(0)  POST ATTENTION                      89328
         LA    R1,MTSCNECB   GET CANCEL ECB ADDRESS              89328
         POST  (1),(0)       AND CANCEL
         B     MCIBOK        RESPOND WITH OK
         SPACE 1
MCIBDUMP BAL   R14,SCANPARM  GET NEXT PARM
         BAL   R14,FINDTERM  GET SUBTASK                         87018
         USING MTSWORK,R7
         B     MCIBSMSG      INVALID ID
         L     R0,=X'00122000'  SET COMPLETION CODE FOR DUMP
         B     MCIBSTMP      GO TO COMMON TERMINATION            87018
         SPACE 1                                                 87099
MCIBABND BAL   R14,SCANPARM  GET NEXT PARM                       87099
         BAL   R14,FINDTERM  IS IT A VALID SUBTASK ?             87099
         B     MCIBSMSG      NO GOOD                             87099
         USING MTSWORK,R7    DECLARE TASK'S WORK AREA            87099
         ICM   R8,15,MTSTASK  GET TCB ADDRESS                    87099
         BZ    MCIBSMSG      NO GOOD                             87099
         MODESET MODE=SUP    ALREADY IN KEY ZERO                 87099
         CALLRTM TYPE=ABTERM,COMPCOD=X'F22',TCB=(R8),DUMP=YES,         *
               STEP=NO       ABEND THE SUB-TASK                  87099
         MODESET MODE=PROB   RETAIN KEY 0                        87099
         B     MCIBOK        FREE CIB, ETC.                      87099
         SPACE 1                                                 90231
MCIBLIST LA    R7,WORKQUE-(MTSQLINK-MTSWORK)                     90231
         USING MTSWORK,R7                                        90231
MCIBLISP ICM   R7,15,MTSQLINK  GET NEXT TASK                     90231
         BZ    MCIBSHOW      NONE; ADD SUMMARY                   90231
         ICM   R1,15,MTSTASK  ANY SUBTASK ?                      90231
         BZ    MCIBLISP      NO                                  90231
         MVC   MCIBLIMT,MTSNIB-NIBST+NIBSYM  COPY ID             90231
         MVC   MCIBLIMU,MTSUID  AND USER                         90231
         SLR   R0,R0                                             90231
         IC    R0,CONID                                          90231
         WTO   MF=(E,MCIBLIMG)    AND ISSUE MESSAGE              90231
         B     MCIBLISP      TRY ANOTHER TASK                    90231
         DROP  R7                                                90231
         SPACE 1                                                 90231
MCIBSHOW L     R0,MTV#CURS   GET CURRENT SESSIONS                90231
         MVC   MCIBSHUS,=X'4020202020202120'  MAKE EDIT MASK     90231
         CVD   R0,INITDDB    MAKE PACKED                         90231
         ED    MCIBSHUS,INITDDB+4  UNPACK                        90231
         LA    R1,MCIBSHMG   GET MESSAGE                         90231
         B     MCIBERRM      WRITE MESSAGE AND FREE CIB          90231
         SPACE 1
MCIBDTXT DC    Y(MCIBDTXX-*),X'C000',C'INVALID TEXT :'
MCIBERMV DC    CL8' '
MCIBDTXX DC    X'80008000'   DESC=1, ROUT=1
MCIBDSID DC    Y(MCIBDSIX-*),X'C000',C'INVALID TERMID'
MCIBDSIX DC    X'80008000'
MCIBOKMG DC    Y(MCIBOKMX-*),X'4000',C'MODIFY ACCEPTED'
MCIBOKMX EQU   *
MCIBSFEL DC    Y(MCIBSFEX-*),X'C000',C'START FAILED'
MCIBSFEX DC    X'80008000'
         SPACE 1                                                 90231
MCIBSHMG DC    Y(MCIBSHMX-*),X'4000',C'COM467I'                  90231
MCIBSHUS DC    CL8' ',C' USERS ON NETCOMM'
MCIBSHMX EQU   *                                                 90231
MCIBLIMG DC    Y(MCIBLIMX-*),X'4000',C'COM466I  USER '           90231
MCIBLIMU DC    CL8' ',C' ON '                                    90231
MCIBLIMT DC    CL8' '        TERM-ID                             90231
MCIBLIMX EQU   *                                                 90231
         SPACE 1                                                 87018
*        SUBROUTINE TO LOCATE THE WORK AREA FOR THE TERMID IN INITDDB
*              RETURN ON R14 - NOT FOUND, ELSE                   87018
*                 4(R14), WORK AREA IN R7                        87018
*                                                                87018
FINDTERM LA    R7,WORKQUE-(MTSQLINK-MTSWORK)
         USING MTSWORK,R7
FINDTERL ICM   R7,15,MTSQLINK  GET NEXT TASK
         BZR   R14           INVALID ID
         CLC   INITDDB,NIBSYM+MTSNIB-NIBST  SAME TASK ?
         BNE   FINDTERL      NO; CHECK AGAIN
         B     4(,R14)       RETURN FOUND
         EJECT ,
XLOGON   XHEAD RPL=NO,EXH=NO NO USER RPL PASSED TO THIS EXIT
         TM    FLAGS,FGDOWN  GOING BYE-BYE ?
         BNZ   XLOGNOLO      YES; NO REASON GIVEN TO USER
         BAL   R9,WORKINIT   GET OR INITIALIZE A WORK AREA
         USING MTSWORK,R7
         L     R9,20(,R6)    GET THE CID
         L     R2,4(,R6)     GET TERMINAL NAME POINTER
         MVC   NIBSYM-NIBST+MTSNIB,0(R2)  COPY TERMNAME TO NIB
         ST    R9,NIBCID-NIBST+MTSNIB  SET CID
         ST    R7,NIBUSER-NIBST+MTSNIB  SET WORK AREA ADDRESS
         ICM   R4,15,12(R6)  ANY START-UP DATA ?                 87130
         BZ    XLOGLU        NO; GET BIND INFORMATION            87130
XLOGPARM INQUIRE RPL=MTSCRPL,OPTCD=LOGONMSG,ARG=(R9),AREA=MTSSTADB+6,  *
               AREALEN=255   OVERLAP BUFFER                      87130
         CH    R15,=H'16'    OUT OF STORAGE ?                    87130
         BE    XLOGPARM      YES; RETRY                          87130
         BXH   R15,R15,XLOGLU  ERROR - DON'T USE                 87130
         LA    R14,MTSSTADB+6  POINT TO START OF PARM            91294
         LR    R0,R4         COPY LENGTH                         87214
         MVC   MTSUID,=CL8' '  CLEAR POTENTIAL USER ID           87214
         LA    R15,MTSUID    OUTPUT LOCATION                     87214
         LA    R5,8          MAX LENGTH                          87214
         MIN   (R5),(R0)     BUT NOT TOO LONG                    87214
XLOGUIDL CLI   0(R14),C'/'   ID/PSWD SEPARATOR ?                 87214
         BE    XLOGPSWL      YES                                 87214
         MVC   0(1,R15),0(R14)  COPY A BYTE                      87214
         LA    R15,1(,R15)                                       87214
         LA    R14,1(,R14)                                       87214
         BCTR  R0,0                                              87214
         BCT   R5,XLOGUIDL   GET NEXT BYTE                       87214
XLOGPSWL CLI   0(R14),C'/'   SEPARATOR ?                         87214
         BNE   XLOGNLOG      NO; NO LOGON TEXT                   87214
         LA    R14,1(,R14)                                       87214
         SH    R0,=H'1'      RESIDUAL INPUT LENGTH               87214
         BNP   XLOGNLOG      NONE                                87214
         LA    R5,8          MAX PSWD LENGTH                     87214
         MIN   (R5),(R0)                                         87214
         LA    R15,MTSACCT+8 STASH IN ACCOUNT FIELD              87214
         MVC   MTSACCT+8(8),=CL8' '  CLEAR IT                    87214
XLOGPSWD MVC   0(1,R15),0(R14)                                   87214
         LA    R14,1(,R14)                                       87214
         LA    R15,1(,R15)                                       87214
         BCT   R5,XLOGPSWD                                       87214
         AIF   ('&LOCAL' NE 'PID').NOLUID                        93263
         L     R5,CVTPTR     GET THE CVT                         87214
         USING CVTMAP,R5                                        GP13007
         ICM   R5,15,CVTUSER GET MY EXTENSION                    87214
         BZ    XLOGNLOG      NOT AVAILABLE                       87214
         USING USERCVT,R5    DECLARE IT                          87214
         ICM   R5,15,UCLUADS  GET THE LOCAL USER-ID TABLE        87214
         LM    R15,R1,0(R5)  LOAD THE BXLE POINTERS              87214
XLOGTIDL CLC   MTSUID,0(R15) MATCHING ID ?                       87214
*DEFER*  BNE   XLOGLUMP      YES                                 87214
*DEFER*  CLC   MTSACCT+8(8),24(R15) MATCHES ?                    87214
         BE    XLOGGAP       YES                                 87214
XLOGLUMP BXLE  R15,R0,XLOGTIDL  TRY AGAIN                        87214
         B     XLOGNLOG      TOO BAD                             87214
XLOGGAP  XC    MTSACCT+8(8),MTSUID  HIDE                         87214
         MVC   MTSACCT,8(R15)  COPY ACCOUNT NUMBER               87214
         B     XLOGLU        SKIP AROUND                         87214
         DROP  R5                                                87214
.NOLUID  ANOP  ,                                                 87214
XLOGNLOG XC    MTSUID(24),MTSUID  CLEAR NON-PASSWORD INPUT       87214
         LA    R4,5(,R4)     GET LENGTH WITH LENGTH AND PREFIX   87133
         STH   R4,MTSSTADB+2 STASH LENGTH                        87130
         STCM  R4,IIOO,MTSSTADB+4  CLEAR LENGTH PROCESSED        91294
XLOGLU   INQUIRE RPL=MTSCRPL,OPTCD=SESSPARM,ARG=(R9),AREA=MTSBINDS,    *
               AREALEN=L'MTSBINDS    GET NITTY GRITTY
         CH    R15,=H'16'    OUT OF STORAGE ?
         BE    XLOGLU        YES; RETRY
         BH    XLOGFAIL      ?
         LA    R4,MTSBINDS
         USING ISTDBIND,R4   I'M IN A BIND
         CLI   BINLUP,BINLUP2C  LU TYPE 2 (3270)
         BE    XLOG3270      YES
         CLI   BINLUP,BINLUP1C  LU TYPE 1 (3767)
         BE    XLOG3767      YES
         CLI   BINLUP,BINLUP0C  LU TYPE 0 ?
         BNE   XLOGFAIL      NO; IGNORE
         CLI   BINFM,X'02'   FM=02 (3270)
         BE    XLOG3270
         CLI   BINFM,X'03'   FM=03 (3767)
         BNE   XLOGFAIL      NO; REJECT IT
XLOG3767 B     XLOGOPEN      ACCEPT NTO TTY TERMINALS            89328
XLOG3270 B     XLOGFAIL      NO PROTOCOL CONVERTER SUPPORT, YET  89328
XLOGOPEN OPNDST RPL=MTSCRPL,OPTCD=(ASY,ACCEPT,SPEC,NQ),                *
               NIB=(R7),EXIT=XOPENEND
         CHRET RETRY=XLOGOPEN TEST RETURN CODE                   89328
         B     XLOGEXIT      AND EXIT
         SPACE 2
WORKINIT LH    R5,=Y(MTSWORKL)  GET LENGTH OF EXTENDED WORK AREA
WORKINCS ICM   R7,15,FREEQUE GET A FREE ELEMENT
         BZ    WORKMAIN      NONE AVAILABLE; GET A NEW ONE
         L     R15,MTSQLINK  LOAD ITS LINK
         CS    R7,R15,FREEQUE  TRY TO DEQUEUE IT
         BNZ   WORKINCS
         B     WORKHAVE
WORKMAIN GETMAIN RC,LV=(R5),SP=0,BNDRY=PAGE                      89328
         BXH   R15,R15,XLOG804         ERR. MSG IF STORAGE ^ OBTAINED
         LR    R7,R1         GET WORK AREA ADDRESS
         CSADD MTV#WA        COUNT WORK AREAS ALLOCATED
WORKHAVE LR    R4,R7         COPY START ADDRESS
         L     R14,=A(PATWORK)  GET PATTERNS FOR NIB, RPL, DCBS
         LA    R15,PATSIZE   SIZE TO COPY
         MVCL  R4,R14        COPY PATTERNS AND ZERO REST
         USING MTSWORK,R7    DECLARE EXTENDED WORK AREA
         LA    R1,MTSBUFF
         AH    R1,=Y(MTSBUFIN-MTSBUFF)   INPUT BUFFER ADDRESS    88243
         ST    R1,MTSINPAD   MAKE ADDRESS
         MVI   MTSXPART+2,MTSDSSIZ-MTSXPART-1  SET ORDER SIZE    88244
         MVI   MTSDSSIZ+2,X'40'  SET 3270DS ID                   88243
         MVI   MTSXPART+3,X'0C'  SET CREATE PARTITION REQUEST    88243
         MVI   MTSXPART,X'F3'  SET WSF COMMAND CODE              88243
         LA    R14,ATTPARM   POINT TO DEFAULT PARM
         LA    R15,MTVECT    POINT TO COMMON VECTOR
         STM   R14,R15,MTS@PARM  POINTERS USED TO LINK WORK AREA
         OI    MTS@PARM+4,X'80'  MTS@MTV
         MVC   MTSPTITL(137),SIMWH137  MOVE TITLE PATTERN
         MVI   MTSPLIN#,X'FF'  NEGATIVE LINE COUNT FORCES PAGE EJECT
         ZAP   MTSPPAG#,=P'0'  CLEAR PAGE COUNT
         MVC   MTSPVLIN(4),MTSPH137  SET PRINT LINE LENGTH
         MVI   MTSPVLIN+4,C' '  CLEAR PRINT LINE
         MVC   MTSPVLIN+5(132),MTSPVLIN+4
         MVC   MTSPVLN2(137),MTSPVLIN  ALSO SECOND LINE
         LA    R3,MTSWRPL
         ST    R7,RPLUSFLD-IFGRPL(,R3) SAVE WORK AREA POINTER
         MODCB AM=VTAM,RPL=(R3),NIB=(R7)
         LA    R3,MTSCRPL
         ST    R7,RPLUSFLD-IFGRPL(,R3) SAVE WORK AREA POINTER
         MODCB AM=VTAM,RPL=(R3),NIB=(R7)
         MVI   MTSFLGV,MTSFVONC   SET FIRST-TIME FLAG
         LA    R0,MTSRCEND-MTSRLEN  LENGTH OF SMF RECORD         89337
         STH   R0,MTSRLEN    SET SMF 34 RECORD LENGTH            89337
         MVI   MTSRFLG,2     SET MVS                             89337
         MVI   MTSRCDTY,34   TYPE 34 (SHORTENED)                 89337
         L     R1,CVTPTR                                         89337
         L     R1,CVTSMCA-CVTMAP(R1)                             89337
         MVC   MTSCPUID,16(R1)  SET SYSTEM/MACHINE ID            89337
         TIME  BIN                                               89337
         STCM  R0,15,MTSONTME  SET TIME ON                       89337
         STCM  R1,15,MTSONDTE  SET TIME ON/DATE ON               89337
         STCM  R0,15,MTSSIT  STEP INITIATION                     89337
         STCM  R0,15,MTSAST  ALLOCATION START                    89337
         STCM  R0,15,MTSPPST PROBLEM PROGRAM START               89337
         LA    R0,MTSVARA-MTSSMF  INCORRECT RELOCATE SECT. OFFSET
         STH   R0,MTSRLCT                                        89337
         LA    R0,MTSVARA-MTSVAR  LENGTH OF EXCP SECTION         90017
         STH   R0,MTSVAR                                         89337
         MVC   MTSEXCP(4),=X'80030181'  FAKE BILLING DISK INP.   89337
         MVC   MTSOXCP(4),=X'80030182'  FAKE DISK OUT            89337
         MVI   MTSVARA,4     CPU + NO ACCOUNT                    89337
         L     R1,=A(XPRABEND)  GET DCB ABEND EXIT ADDRESS
         ST@   R1,MTSPDCBX,MVI=X'91'  MAKE DCB ABEND EXIT LIST   89275
         LA    R1,MTSPDCBX   GET EXIT LIST ADDRESS
         STCM  R1,7,DCBEXLSA+MTSPDCB-IHADCB  MAKE DCB EXIT LIST
         ST    R7,MTSSAVER+56  ENSURE WORK BASE FOR DCB ABEND
         AIF   ('&SYSPARM' NE 'DEBUG').NOBUGP                    87040
         LR    R4,R9         SAVE RETURN REGISTER                87040
         BAL   R9,SOUTALOC   ALLOCATE A SYSOUT DD (DDNM=>MTSPRDD)
         LR    R9,R4         RESTORE RETURN                      87040
         BXH   R15,R15,WORKNPRT  ALLOCATION FAILED               87040
         MVC   DCBDDNAM+MTSPDCB-IHADCB,MTSPRDD  MAKE DCB DDNM    87040
         DEVTYPE MTSPRDD,MTSDDB  SEE IF EXHPRINT SUPPLIED        87040
         BXH   R15,R15,WORKNPRT  ALLOC OR PGM ERROR              87040
         LA    R1,MTSPDCB     GET DCB ADDRESS                    87040
         ST@   R1,MTSDDB,MVI=X'8F'  MAKE OPEN,OUTPUT LIST        89275
         OPEN  ,MF=(E,MTSDDB)       OPEN IT                      87040
         TM    MTSPDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPEN NOW ?      87040
         BZ    WORKNPRT      NO                                  87040
         OI    MTSFLGT,MTSFBUG  SET TRACE MODE                   87040
         B     WORKCHEN      JOIN COMMON                         87040
.NOBUGP  ANOP  ,                                                 87040
WORKNPRT MVI   MTSPRDD,C' '  NOT ALLOCATED                       87032
         LA    R1,XPRRET     MAKE DUMMY PUT ROUTINE ADDRESS      87032
         ST    R1,MTSPDCB+48      MAKE NOPS                      87032
WORKCHEN L     R15,WORKQUE   GET CURRENT TOP ELEMENT
         ST    R15,MTSQLINK  MAKE PROVISIONAL LINK
         CS    R15,R7,WORKQUE  TRY TO PLACE IT ON QUEUE
         BNZ   WORKCHEN      NO GOOD; TRY AGAIN
         BR    R9            RETURN TO CALLER
         SPACE 1                                                 89337
XLOG804  WTO   'COM408E REGION TOO SMALL',ROUTCDE=1,DESC=1
XLOGNOLO LA    R7,INPNIB     USE DUMMY NIB/WORK AREA
         L     R9,20(,R6)    GET THE CID
         L     R2,4(,R6)     GET TERMINAL NAME POINTER
         MVC   NIBSYM-NIBST+MTSNIB,0(R2)  COPY TERMNAME TO NIB
         ST    R9,NIBCID-NIBST+MTSNIB  SET CID
         ST    R7,NIBUSER-NIBST+MTSNIB  SET WORK AREA ADDRESS
XLOGFAIL L     R2,=A(XCLOSE)  GET REQUEUE EXIT
         CLSDST RPL=MTSCRPL,NIB=(R7),OPTCD=(SYN,RELEASE),EXIT=(R2)
         CSADD MTV#FAIL      COUNT LOGON FAILURES
         XEXIT ,
LOGRPL   RPL   AM=VTAM,ACB=MYACB,NIB=INPNIB,EXIT=XOPENEND,             *
               AREA=INPBUFF,AREALEN=INPBUFLN
REQRPL   RPL   AM=VTAM,ACB=MYACB,NIB=REQNIB,OPTCD=(ASY,NQ),            *
               AREA=REQBIND,AREALEN=REQBINDL                     93263
REQNIB   NIB   MODE=RECORD,LOGMODE=DSILGMOD,NAME=COMA            93263
REQBIND  DC    X'01020271,40200000,00000000,00000000'            93263
         DC    X'00000000,00000002,00000840',CL7' ',X'00'        93263
REQBINDL EQU   *-REQBIND                                         93263
         SPACE 1
XOPENEND XHEAD RPL=NO,EXH=NO   LOGON RPL IS NON-STANDARD
         USING IFGRPL,R6     DECLARE THE (LOG)RPL
         L     R0,RPLARG     GET THE CID
         LA    R7,WORKQUE-(MTSQLINK-MTSWORK)
         USING MTSWORK,R7    OK ?
XOPELOOP ICM   R7,15,MTSQLINK  GET NEXT TEMPORARY ENTRY
         BZ    XOPEEXIT      HUH ?
         C     R0,NIBCID-NIBST+MTSNIB  SAME CID ?
         BNE   XOPELOOP  NO
XOPECHEK CHECK RPL=(R6)      CHECK FOR ERRORS
         CHRET RETRY=XOPECHEK TEST RETURN CODE
         L     R9,RPLARG     GET THE CID
         LA    R3,MTSWRPL
         MODCB AM=VTAM,RPL=(R3),ARG=(R9)
         LA    R3,MTSCRPL
         MODCB AM=VTAM,RPL=(R3),ARG=(R9)
         ICM   R0,3,MTSSTADB+2 PASSED PARAMETER ?                87130
         BZ    XOPEPARM                                          87130
         LA    R1,MTSSTADB+2 GET PARMLENGTH AND TEXT             87130
         ST    R1,MTS@PARM   SET TEXT POINTER                    87130
XOPEPARM LA    R1,MTS@PARM   POINT TO POINTER
         ATTACH SF=(E,MTSATT) EPLOC=FROGFACE   ATTACH THE SERVICE TASK
         BXLE  R15,R15,XOPEATOK  ATTACH TOOK
XOPEREL  L     R3,=A(XCLOSE)  MAKE RELEASE EXIT
         CLSDST RPL=MTSCRPL,OPTCD=(ASY,RELEASE),EXIT=(R3),             *
               RECLEN=0,AAREA=0      GIVE UP CONTROL
         CH    R15,=H'16'    NEED TO RE-ISSUE ?
         BE    XOPEREL       YES; RETRY IT
         B     XOPEEXIT      AND QUIT
XOPEATOK ST    R1,MTSTASK    SAVE THE ADDRESS FOR DETACH
         CSADD MTV#CURS,WK2=R15  ADD NUMBER OF CURRENT SESSIONS
         STMAX R15,MTV#MAXS  SET MAXIMUM CONCURRENT SESSIONS
         CSADD MTV#TOTS      ADD TOTAL SESSIONS SINCE START-UP
         SERVICE CANOF       DISALLOW OPERATOR CANCEL            90285
         XEXIT ,
ATTPARM  DC    H'4,0'        SUBTASK COMMAND BUFFER              91301
         SPACE 1
SIMWH137 DC    H'137,0',C'1'
SIMWTITX DC    CL60'     *****     NETCOMM TERMINAL ACTIVITY LOG     ***
               ***     '
SIMWTITE DC    C'RUN ON '
SIMWTIDT DC    CL6'YY.DDD',C' AT '
SIMWTITM DC    CL8'HH:MM:SS',CL4' '
         DC    C'PAGE'
SIMWTIPG DC    C'123456',CL38' '
SIMWTILE EQU   SIMWTITE-SIMWTITX
         ORG   SIMWH137+137
         SPACE 2
         DROP  ,
SUBVTAM  XHEAD RPL=NO,EXH=NO,SAVE=RENT,MODE=OS,RENT=XSUBSLEN     89328
         USING SAVE,R13      DECLARE EXTENDED SAVE AREA          89328
         L     R10,=A(WORKBASE) GET COMMON WORK AREA
         USING WORKBASE,R10  MAKE SECOND BASE
         SH    R6,=Y(MTS@PARM-MTSWORK)  GET WORK AREA BACK       89328
         USING MTSWORK,R6    DECLARE IT                          89328
         LTCB  R14           GET CURRENT TCB                     91058
         ICM   R15,15,TCBFSA-TCB(R14)  GET FSA                   91058
         BZ    *+4+6         NONE ?                              91058
         XC    0(4,R15),0(R15)  CLEAR @SERVICE ANCHOR            91058
         OI    MTSSIMFG,MTSSICRT+MTSSIFSC  SET CRT (PROVISIONALLY)
         TM    FLAGS,FGSYS   SYSTEMS ONLY ?                      87350
         BZ    *+8           NO                                  87350
         OI    MTSSECFG,MTSSECRQ  NEEDS SYSTEMS PRIVILEGES       89328
*FORCE*  TM    FLAGS,FGLOGON  MUST LOGON TO USE ?                87350
*LOGON*  BZ    *+8           NO                                  87350
         OI    MTSSECFG,MTSSECMD NEEDS LOGON FOR COMMANDS        89328
         ICM   R0,3,MTSSTADB+2  ANY CANNED COMMAND ?             87350
         BZ    DONESEC       NO                                  87350
         CLI   MTSUID,C' '   CANNED USER ?                       87350
         BH    DONESEC       YES (WILL DO AUTO-LOGON)            87350
         OI    MTSSECFG,MTSSECON  ELSE FAKE LOGON COMPLETE       89328
DONESEC  OI    MTSSAVPO,MTSSAMUL  INDICATE MULTIPLE TASKS IN ADDRES
         CLI   MTSUID,C' '   IS THERE A USER ID ?                87214
         BNH   SUBNLOG       NO                                  87214
         AIF   ('&LOCAL' NE 'PID').NOSUID                        93263
         CLC   =C'Z90',MTSACCT  IN-HOUSE ACCOUNT ?               93263
         BNE   SUBNLOG       NO                                  87214
         CLI   MTSACCT+3,C'4'  IS IT ?                           87214
         BE    SUBYLOG       YES                                 87214
         CLI   MTSACCT+3,C'8'  OR CUSTOMER SUPPORT ?             87214
         BNE   SUBNLOG       NO                                  87214
SUBYLOG  OI    MTSSAVPR,X'F7'  GLOBAL SUPER-DUPER UNLOCK         89328
.NOSUID  SPACE 1
SUBNLOG  LA    R14,MTSTMECB  GET FIRST ECB IN LIST               89328
         LA    R15,MTSATECB  GET SECOND ECB                      89328
         LA    R0,MTSCNECB   GET THIRD                           89328
         LA    R1,MTSWECB    GET I/O ECB LAST                    89328
         STM   R14,R1,MTSTLIST  MAKE LONG ECB LIST               89328
         OI    MTSELIST+8,X'80'   MAKE END OF LIST BIT           89328
         L     R14,=A(GETLINE)  GET MY (LOCAL) GETLINE ROUTINE   89328
         L     R15,=A(PUTLINE)  AND PUTLINE                      89328
         SLR   R0,R0                                             89334
*DEFER*  L     R0,=A(FULLSCRN)  AND FULL-SCREEN PUT              89328
         SLR   R1,R1         NOT DONE YET                        89334
*DEFER*  L     R1,=A(FULLWSF)   AND STRUCTURED FIELD WRITE/RESPONSE
         STM   R14,R1,MTSGETLN  SET PROCESSING ADDRESSES         89328
         SLR   R14,R14                                           89337
*DEFER*  L     R14,=A(LOFFCODE)  SPECIAL LOGOFF EXIT             89337
         L     R15,=A(COMMANDS)  COMMAND PRE-PROCESSING          89345
         L     R0,=A(DDACCESS)   ACCESS CONTROL CHECK            89337
         STM   R14,R0,MTSLOGOF                                   89337
         L     R3,=A(STABLE) GET STAE EXIT ADDRESS
         STAE  (R3),CT,PARAM=(R6),XCTL=YES,MF=(E,MTSSTAXP)       89328
         LA    R4,MTSBINDS
         USING ISTDBIND,R4   I'M IN A BIND
         CLI   BINLUP,BINLUP2C  LU TYPE 2 (3270)
         BE    XSUB3270      YES
         CLI   BINLUP,BINLUP1C  LU TYPE 1 (3767)
         BE    XSUB3767      YES
         CLI   BINLUP,BINLUP0C  LU TYPE 0 ?
         BNE   XSUBFAIL      NO; IGNORE
         CLI   BINFM,X'02'   FM=02 (3270)
         BE    XSUB3270
         CLI   BINFM,X'03'   FM=03 (3767)
         BNE   XSUBFAIL      NO; REJECT IT
XSUB3767 L     R9,=A(FEAT3767)  FAKE
         MVI   BINCMNP2,X'80'  SET HALF-DUPLEX
         MVI   BINPRIP,BINPCHN+BINPCHNR+BINPSEB
         MVI   BINSECP,BINSCHN+X'10'  EXC. RESP ONLY, NO END
         MVI   BINCMNP,BINBRAK+BINBKTR
         MVI   BINUSEL,4
         MVC   BINUSE(4),=X'FF000020'  NTO INST MANUAL APP. D
         MVC   MTSSCRSZ,=Y(23,80)  FAKE 23*80 CRT
         NI    MTSSIMFG,255-MTSSICRT-MTSSIFSC-MTSSIF78-MTSSIF79  89328
         B     XSUBFEAT      JOIN COMMON
XSUB3270 L     R14,=A(CCWW70)  POINT TO DEFAULT CCW CHAIN
         LA    R15,=Y(12,80) SET FOR MIDGET SIZE
         CLI   BINPRESZ,1    MODEL 1 ?
         BE    XSUBTERM      YES
         LA    R15,=Y(24,80) POINT TO DEFAULT SIZE
         CLI   BINPRESZ,2    MODEL 2 ?
         BE    XSUBTERM      YES
         BL    XSUBTERM      WHAT IS IT ?  ASSUME 24*80
XSUBFEAT ICM   R0,1,BINPRUSZ  PRESUMED SNA DEVICE ?              87350
         BZ    *+8           NO                                  87350
         OI    MTSSAVPO,MTSSAPRM  YES; RUN WITH PROMPTS          89328
         LA    R15,MTSSCRSZ  SKIP MOVE
         MVC   MTSROWS+1(1),BINSPRIR  DEFAULT ROWS               89328
         MVC   MTSCOLS+1(1),BINSPRIC  DEFAULT COLUMNS            89328
         CLI   BINPRESZ,BINPSFX  FIXED SIZE ?
         BE    XSUBSIZE      YES; USE IT
         BL    XSUBFAIL      NOT DEFINED
         CLI   BINPRESZ,BINPSZRC  TWO SIZES DEFINED ?
         BNE   XSUBFAIL      NO; WHAT IS IT ?
         MVC   MTSROWS+1(1),BINSALTR                             89328
         MVC   MTSCOLS+1(1),BINSALTC  USE ALTERNATE SIZE         89328
         LA    R14,CCWW78-CCWW70(,R14)  USE ALTERNATE CCW
         B     XSUBSIZE
XSUBFAIL LA    R15,=Y(24,80) POINT TO DEFAULT SIZE
XSUBTERM MVC   MTSSCRSZ(4),0(R15)  SET TERMINAL SIZE
         MVI   MTSUCB+UCBTBYT4-UCBOB,X'09'  SET 3277 TYPE
XSUBSIZE LH    R1,MTSCOLS    GET LINE WIDTH                      89328
         LH    R0,MTSROWS    AND NUMBER OF LINES                 89328
         LA    R9,MTSFEAT    BUILD A FEATURE ENTRY
         USING DEVMAP,R9
         ST    R14,DEVCCW    SET CCW ADDRESS
         MVC   DEVLINE(L'DEVLINE+L'DEVBYTE),MTSSCRSZ  COPY LINES/COLS
         LH    R1,DEVLINE
         MH    R1,DEVBYTE    GET SCREEN SIZE
         STH   R1,DEVPAGE    SET PAGE SIZE
         LA    R0,4          SET FOR FOUR PREFIX BYTES
         STH   R0,DEVPREF
         STCM  R0,12,DEVSUFF NO SUFFIX BYTES
         MVC   DEVPFX(4),SCRNDEF   MOVE DEFAULT PREFIX DATA
         CLI   DEVBYTE+1,40  FORTY-BYTE LINE ?
         BNE   *+10          NO
         MVC   DEVPFX(1),SCRN40  SET FOR FORTY-BYTE LINE
         CLI   DEVBYTE+1,80  EIGHTY ?
         BNE   *+10          NO
         MVC   DEVPFX(1),SCRN80  SET FOR 80
         ICM   R14,1,BINPRUSZ  TEST PRIMARY RU SIZE
         BZ    *+8           NONE; DON'T BOTHER TO SIZE THIS ONE UP
         OI    RPLO7-IFGRPL+MTSWRPL,RPLLMPEO  REQUEST VTAM TO SPLIT
         TM    MTSPDCB+DCBOFLGS-IHADCB,DCBOFOPN  PRT OPEN NOW ?  87040
         BZ    *+8           NO                                  87040
         OI    MTSSIMFG,MTSSIPRT  SET PRINT REQUEST              89328
         SPACE 1
*DEFER*  L     R1,DEVCCW     GET CCW ADDRESS
*FIX*    MVC   SQWCCW(16),0(R1)    COPY BASIC CCW CHAIN
         MVC   MTSSCRSZ(L'DEVSIZ),DEVSIZ   COPY SIZE VALUES      89328
         MVC   MTSPTITX+14(8),NIBSYM-NIBST+MTSNIB  FIX TITLE
         SPACE 1                                                 89328
***********************************************************************
*                                                                     *
*        IF USER HAS GLOBAL LOGON PRIVILEGES, OR HAS PREVIOUSLY DONE  *
*        A NETSOL LOGON, COPY THE USER'S INFORMATION NOW.             *
*              IF NOT, WILL INVOKE SIGN-ON CODE LATER.                *
*                                                                     *
***********************************************************************
         AIF   ('&LOCAL' NE 'PID').NOLOUTD                       93263
         XC    X$SVCRB(X$SLENRB),X$SVCRB  CLEAR ACCT SVC PARM AREA
         MVC   X$SID,=C'A$RB'                                    88217
         MVC   X$SFCTN(3),=AL1(X$SFFIND,0,X$SAKEY) FIND LOUD     89328
         MVC   X$SACCT,NIBSYM-NIBST+MTSNIB  COPY TERMINAL NAME   88217
         LA    R1,X$SVCRB                                        88217
         SVC   249           CANCEL SESSIONS; KEEP USER          88217
         BXH   R15,R15,XSUBSIGN  NOT FOUND; REQUIRE LOGON (LATER)
         MVC   MTSLOUD,X$SBUFR  PASS THE LOUD ADDRESS            89328
         L     R2,MTSLOUD                                        89328
         USING A$LSECT,R2    DECLARE IT                          89328
*DEFER*  TM    A$LFLG1,A$LFGOK  USER ON ?                        89328
*DEFER*  BZ    XSUBSIGN      NO; REQUIRE SIGN-ON                 89328
*DEFER*  MVC   MTSUID,A$UUID  COPY USER ID                       89328
*DEFER*  MVC   MTSACCT,A$LCACT  AND ACCOUNT                      89328
*DEFER*  B     XSUBCALL      GO TO INVOKE KERMIT                 89328
.NOLOUTD SPACE 1                                                 90186
XSUBSIGN LR    R1,R6         PASS MTS ADDRESS                    89334
         L     R15,=A(SIGNPROC)  GET SIGN-ON ROUTINE             89334
         BALR  R14,R15       CALL IT                             89334
         CH    R15,=H'4'     CHECK RESULT                        89334
         BL    XSUBCALL      PROCEED - USER LOGGED ON            89334
         AIF   ('&LOCAL' NE 'PID').GOLOUTD                       93263
         BE    XSUBEXIT      USER WISHES TO QUIT                 89334
         MODESET KEY=ZERO    GET PRIVILEGED                      89334
         OI    A$LFLG2,A$LFLOG  DISCONNECT THIS DEVICE           89334
         MODESET KEY=NZERO   RESTORE                             89334
.GOLOUTD B     XSUBEXIT      AND GET OUT                         89334
         SPACE 1                                                 89328
***********************************************************************
*                                                                     *
*        INVOKE KERMIT PROPER; USE SPECIAL CALLING PARAMETER LIST AND *
*        SPECIAL ENTRY POINT.                                         *
*                                                                     *
***********************************************************************
XSUBCALL MVC   MTSUIF,MTSUID SET SMF USER ID                     89337
         MVC   MTSUDATA,MTSACCT  SET SMF USER DATA (OUR ACCOUNT) 89337
         MVI   MTSINVSQ,1    STEP 1                              89337
         MVI   MTSPRI,X'4F'  PRTY=4                              89337
         MVC   MTSPRGNM,=CL8'*WYLKERM'  FAKE NAME                89337
         MVC   MTSINVNM,=CL8'*WYLKERM'  FOR BILLING              89337
         LA    R0,64         SET REGION=64K                      89337
         STH   R0,MTSEFRGN                                       89337
         STH   R0,MTSMCRE                                        89337
         MVI   MTSSPK,X'80'  PROTECT KEY                         89337
         LA    R14,=F'0'     POINT TO NULL CMD BUF               91294
         LA    R15,MTVECT    POINT TO MTV                        89328
         LA    R0,MTSWORK    POINT TO MTS                        89328
         STM   R14,R1,MTSSPARM  SAVE                             89328
         OI    MTSSPARM+8,X'80'  SET END OF LIST BIT             89328
         LA    R1,MTSSPARM   POINT TO PARM LIST                  89328
         L     R15,@KERMIT   GET KERMNET ENTRY POINT             90206
         BALR  R14,R15       INVOKE KERMIT WITH OVERRIDE LIST    90206
         SPACE 1                                                 89337
         STCM  R15,3,MTSSTAT TERMINATION STATUS                  89337
         TIME  BIN                                               89337
         STCM  R0,15,MTSRCDTS  SET TIME OFF                      89337
         STCM  R1,15,MTSRCDTE  SET DATE OFF                      89337
         LA    R1,MTSSMF                                         89337
         SMFWTM (1)          WRITE THE RECORD                    89337
         XC    MTSUIF,MTSUIF SIGNAL RECORD WRITTEN               89337
         SPACE 1                                                 89328
XSUBEXIT XEXIT MODE=OS                                           89328
         ORG   *-2                                               89328
XPRRET   BR    R14           RETURN TO CALLER
         SPACE 1
SCRNDEF  SCRN  (RESTKBY,RESTMDT),SBA,(1,1)                       89328
SCRN40   SCRN  (CHAR40,RESTKBY,RESTMDT)                          89328
SCRN80   SCRN  (CHAR80,RESTKBY,RESTMDT)                          89328
         SPACE 1
DEVMAP   DSECT ,
DEVCCW   DS    A             CCW ADDRESS
DEVSIZ   DS    0XL10         SIZE VALUES
DEVPAGE  DS    H             PAGE SIZE
DEVLINE  DS    H             LINES PER PAGE
DEVBYTE  DS    H             BYTES PER LINE
DEVPREF  DS    H             PREFIX LENGTH OF CONTROL BYTES
DEVSUFF  DS    H             SUFFIX CONTROL BYTES
DEVPFX   DS    CL6           PREFIX CONTROL BYTES, IF ANY
         SPACE 1                                                 89328
SAVE     DSECT ,             SAVE / WORK SPACE FOR MAIN TASK     89328
SAVEND   EQU   *             LAST ENTRY                          89328
         AIF   ('&LOCAL' NE 'PID').SOLOUTD                       93263
X$SVCRB  A$SVCRB DSECT=NO,PFX=X$S                                89328
.SOLOUTD ANOP  ,                                                 90186
XSUBSLEN EQU   *-SAVE        LENGTH OF XSUBSAVE SAVE/WORK AREA   89328
NETCOMM  CSECT ,
         DROP  ,                                                 89328
         EJECT ,
XLERAD   XHEAD EXH=NO,SAVE=RENT   LERAD ROUTINE
         MVC   XSYNWHO,=C'LER'
         LR    R4,R13        PRESERVE SAVE AREA                  88019
         BAL   R9,XLERSYN    EXECUTE COMMON CODE
         LR    R13,R4        RESTORE SAVE AREA                   88019
         XEXIT ,
XSYNAD   XHEAD EXH=NO,SAVE=RENT
         MVC   XSYNWHO,=C'SYN'
         LR    R4,R13                                            88019
         BAL   R9,XLERSYN    EXECUTE COMMON CODE
         LR    R13,R4        RESTORE SAVE AREA                   88019
         XEXIT ,             RETURN
XLERSYN  BALR  R8,0          NEED LOCAL BASE
         DROP  R12
         USING *,R8
         TM    FLAGS,FGDOWN  VTAM GOING ?
         BNZ   XSYNRET0      YES; IGNORE THE ERROR               88019
         LTR   R7,R7         ANY MTS WORK AREA ?                 88019
         BZ    XLERADX       NO; SET FUNNY RETURN CODE           88021
         CLM   R0,1,=X'18'   INVALID RPL ?
         BNE   XLERVRPL      NO; PROCEED                         87258
         LA    R14,WORKQUE-(MTSQLINK-MTSWORK)  POINT TO QUEUE HEAD
         USING MTSWORK,R14                                       87258
XLERLOOP ICM   R14,15,MTSQLINK  GET NEXT WORK AREA               87258
         BZ    XLERADX       NOT VALID; EXIT WITH ERROR          88021
         CR    R7,R14        KNOWN WORK AREA ?                   87258
         BNE   XLERLOOP      NO; TRY NEXT ONE                    87258
         DROP  R14                                               87258
XLERVRPL TM    MTSFLGT,MTSFGONE  TERMINAL WENT BYE-BYE ?
         BNZ   XSYNRET0      RETURN OK
         CLM   R0,1,=X'08'   STORAGE SHORTAGE ?                  87279
         BNE   XLERNSTO      NO                                  87279
         LA    R15,16        SET EXPECTED RETURN CODE            87279
         BR    R9            AND GO BACK                         87279
XLERNSTO CLC   =X'100D',RPLRTNCD  SHUT-DOWN IN PROGRESS ?        87326
         BNE   XLERNCLO      NO                                  87326
         OI    FLAGS,FGDOWN  SHOW GOING DOWN                     87326
         OI    MTSFLGT,MTSFGONE                                  87326
         B     XSYNRET0      FAKE IT                             87326
XLERNCLO CLI   RPLFDBK2,8    SNA SENSE ?                         88019
         BNE   XLERNLOP      NO                                  88019
         CLI   RPLSSMI,X'1B'  HAM-HANDED USER ?                  88019
         BE    XLERADX       YES                                 88019
         CLI   RPLSSMI,X'2B'                                     88019
         BE    XLERADX                                           88019
         CLI   RPLSSMI,X'2D'                                     88019
         BE    XLERADX                                           88019
         CLI   RPLSSMI,X'31'                                     88019
         BE    XLERADX                                           88019
XLERNLOP TM    MTSFLGT,MTSFBUG  TRACE MODE ?
         BZ    XLERNBUG      NO
         LR    R2,R0         SAVE ENTRY CODE
         MVC   MTSPVLIN(XSYNWTON-XSYNWTO),XSYNWTO  COPY MSG
         CVD   R0,MTSDDB     CONVERT R0
         MVC   XSYNR0-XSYNWTO+MTSPVLIN,=X'40202120'  REFRESH MASK
         ED    XSYNR0-XSYNWTO+MTSPVLIN,MTSDDB+6  DISPLAY ENTRY CODE
         ST    R6,MTSDDB     STASH RPL ADDRESS
         MVC   XSYNSYM-XSYNWTO+MTSPVLIN,NIBSYM-NIBST+MTSNIB  TERM. NAME
         UNPK  XSYNRPL-XSYNWTO+MTSPVLIN(L'XSYNRPL+1),MTSDDB+1(4)
         TR    XSYNRPL-XSYNWTO+MTSPVLIN,MDBHEX-C'0'
         MVI   XSYNRPL+L'XSYNRPL-XSYNWTO+MTSPVLIN,C' '
         UNPK  XSYNCC-XSYNWTO+MTSPVLIN(L'XSYNCC+1),RPLRTNCD(3)
         TR    XSYNCC-XSYNWTO+MTSPVLIN,MDBHEX-C'0'
         MVI   XSYNCC+L'XSYNCC-XSYNWTO+MTSPVLIN,C' '
         UNPK  XSYNFDBK-XSYNWTO+MTSPVLIN(L'XSYNFDBK+1),RPLFDBK2(5)
         TR    XSYNFDBK-XSYNWTO+MTSPVLIN,MDBHEX-C'0'
         MVI   XSYNFDBK+L'XSYNFDBK-XSYNWTO+MTSPVLIN,C' '
         L     R3,0(,R13)    GET OLD R13
         UNPK  XSYNAT-XSYNWTO+MTSPVLIN(L'XSYNAT+1),13(4,R3) RETURN
         TR    XSYNAT-XSYNWTO+MTSPVLIN,MDBHEX-C'0'
         MVI   XSYNAT+L'XSYNAT-XSYNWTO+MTSPVLIN,C' '
         LA    R1,MTSPVLIN   POINT TO PRINT LINE
         L     R15,=A(XPRNT) GET PRINT ROUTINE
         BALR  R14,R15       INVOKE IT
         LR    R0,R2         RESTORE THE ENTRY CODE
XLERNBUG CLC   MTSERRCT,MAXERRCT  TOO MANY TIMES IN HERE ?
         BH    XLERADX+2     YES; DUMP
         LA    R14,MTSCRPL    OPEN OR CLOSE ?
         CR    R6,R14
         BNE   XLERNCLS      NO
         SLR   R15,R15
         SLR   R0,R0
         IC    R15,RPLFDBK   LOAD PRIMARY FEEDBACK CODE
         IC    R0,RPLFDBK2   AND SECONDARY
         CH    R15,=H'16'    VTAM ERROR ?
         BNER  R9            NO; JUST RETURN
         LA    R15,X'14'     PREVENT RETRY
         BR    R9            LET CALLER HANDLE
XLERNCLS CH    R0,=H'8'      RETRY ?
         BE    XSYNEXEC      YES; JUST RETRY
         CLC   =X'0C0B',RPLRTNCD  LOST THE SESSION ?             89124
         BE    XLERHANG      YES; DISCONNECT                     89124
         CLC   =X'0A0A',RPLFDBK2+2  PROTOCOL CONVERTER PHONE HANG-UP ?
         BNE   XLERONHK      NO                                  88217
XLERHANG OI    MTSFLGT,MTSFGONE  YES; SHOW LINE DEAD             88217
         STM   R0,R15,MTSSAVES  SAVE A BIT                       88217
         PUSH  USING                                             88217
         ICM   R2,15,MTSTASK  ANY SUBTASK ?                      88217
         BZ    XLEROFHK      NO                                  88217
         L     R0,=X'00622000'  SET COMPLETION CODE              88217
         OI    MTSFLGT,MTSFXEND  END REQUEST                     89328
         POST  MTSATECB,(0)  POST ATTENTION                      89328
         LA    R1,MTSCNECB   GET CANCEL ECB ADDRESS              89328
         POST  (1),(0)       AND CANCEL                          88217
         AIF   ('&LOCAL' NE 'PID').NOLOUD                        93263
         XC    M$SVCRB(M$SLENRB),M$SVCRB  CLEAR ACCT SVC PARM AREA
         MVC   M$SID,=C'M$RB'                                    88217
         MVC   M$SFCTN(3),=AL1(M$SFFIND,0,M$SAKEY+M$SBUILD) FIND LOUD
         MVC   M$SACCT,NIBSYM-NIBST+MTSNIB  COPY TERMINAL NAME   88217
         LA    R1,M$SVCRB                                        88217
         SVC   249           CANCEL SESSIONS; KEEP USER          88217
         BXH   R15,R15,XLEROFHK  NOT FOUND; OK ?                 88217
         MVI   M$SFCTN,M$SFLOGF  LOGOFF USER AND SESSIONS        88217
         MVI   M$SCNTL,M$SUKEY+M$SBUILD                          88217
         XC    M$SUID(L'M$SUID+L'M$SACCT),M$SUID  KILL USER AND ACCT
         LA    R1,M$SVCRB    ...BUT USERID IS GARBAGE            88217
         SVC   249           ...SO THIS CALL KILLS THE DATA      88217
         B     XLEROFHK      SKIP AROUND LIST                    88217
M$SVCRB  A$SVCRB DSECT=NO,PFX=M$S                                89328
.NOLOUD  POP   USING                                             88217
XLEROFHK LM    R0,R15,MTSSAVES  RESTORE                          88217
XLERONHK CLI   RPLRTNCD,4    DEVICE PROBLEM ?
         BNE   XLERSYN2      NO
         CLC   =X'1000',RPLFDBK2  USER SENSE ONLY ?
         BE    XLERUSEN      YES; RETURN USER SENSE
         CLI   RPLFDBK2,0    USER SENSE ONLY ?
         BNE   XLERSYN2      NO; PROCEED WITH ERROR
XLERUSEN LA    R14,MTSWRPL   IS THIS A WRITE REQUEST ?
         CR    R14,R6        WRITE ?
         BE    XLERUS88      YES; PASS TO CALLER
         LA    R14,READRPL   GENERAL READ ?
         CR    R14,R6
         BNE   XLERSYN2      NO
XLERUS88 LA    R15,88        SET ERROR RETURN
         ICM   R0,15,RPLFDBK2  RETURN SENSE, MI, USER SENSE
         CLC   =X'0811',RPLFDBK2  BREAK ?
         BNER  R9            NO; RETURN WITH ERROR
         XC    RPLFDBK2(2),RPLFDBK2  PREVENT XRITE ERROR         87041
         OI    MTSFLGM,MTSFATTN  SIGNAL ATTENTION                87032
         B     XSYNRET0      RETURN OK
XLERSYN2 CVD   R0,MTSDDB     CONVERT R0
         CLC   =X'0813',RPLFDBK2  BRACKET ERROR ?                88024
         BE    XLERCLIR      YES; NO WTO                         88024
         CLC   =X'2003',RPLFDBK2  OTHER BRACKET ERROR ?          88024
         BE    XLERCLIR      YES; SKIP MESSAGE ALSO              88024
         MVC   XSYNR0,=X'40202120'  REFRESH MASK
         ED    XSYNR0,MTSDDB+6  DISPLAY ENTRY CODE
         ST    R6,MTSDDB     STASH RPL ADDRESS
         MVC   XSYNSYM,NIBSYM-NIBST+MTSNIB  TERM. NAME
         UNPK  XSYNRPL(L'XSYNRPL+1),MTSDDB+1(4)
         TR    XSYNRPL,MDBHEX-C'0'
         MVI   XSYNRPL+L'XSYNRPL,C' '
         UNPK  XSYNCC(L'XSYNCC+1),RPLRTNCD(3)
         TR    XSYNCC,MDBHEX-C'0'
         MVI   XSYNCC+L'XSYNCC,C' '
         UNPK  XSYNFDBK(L'XSYNFDBK+1),RPLFDBK2(5)
         TR    XSYNFDBK,MDBHEX-C'0'
         MVI   XSYNFDBK+L'XSYNFDBK,C' '
         L     R3,0(,R13)    GET OLD R13
         UNPK  XSYNAT(L'XSYNAT+1),13(4,R3) GET RETURN ADDRESS
         TR    XSYNAT,MDBHEX-C'0'
         MVI   XSYNAT+L'XSYNAT,C' '
         WTO   MF=(E,XSYNWTO)
XLERCLIR CLI   RPLRTNCD,4    DEVICE PROBLEM ?
         BNE   XLERADX       NO
         MVC   MTSSAVE4(8),0(R13)  SAVE R13, R14 FOR RETURN
         LA    R13,MTSSAVE4  GET ANOTHER SAVE AREA
         CLC   =X'0813',RPLFDBK2  BRACKET ERROR ?                87214
         BE    XLERFLIP      YES; FLIP BEGIN BRACKET             87214
    SESSIONC RPL=MTSCRPL,CONTROL=CLEAR,STYPE=REQ,OPTCD=SYN,ECB=INTERNAL
    SESSIONC RPL=MTSCRPL,CONTROL=SDT,STYPE=REQ,OPTCD=SYN,ECB=INTERNAL
         CLC   =X'2003',RPLFDBK2  BRACKET ERROR ?
         BNE   XLERFLIT      NO
         NI    RPLRH3,255-RPLBB  FIRST OF TWO FLIPS              87214
         OI    RPLRH3,RPLEB  CAUSES BB AND EB TO BE ON           87214
XLERFLIP XI    RPLRH3,255-RPLBB  FLIP BRACKET
XLERFLIT NI    FLAGS,255-FGREAD  SHOW READ COMPLETE
XSYNEXEC LR    R5,R9         SAVE RETURN                         88019
XSYNEXEQ EXECRPL RPL=(R6)    RETRY THE REQUEST
         CHRET RETRY=XSYNEXEQ TEST FOR ACCEPTANCE
         LR    R9,R5         RESTORE RETURN                      88019
XSYNRET0 SLR   R15,R15       CLEAR RETURNED R15 AND R0
         SLR   R0,R0
         BR    R9            RETURN TO CALLER
XLERADX  DS    0H
         LA    R15,69        SET MY ERROR CODE
         LA    R0,88         LERAD/SYNAD
         BR    R9            AND RETURN
XSYNWTO  DC    Y(XSYNWTON-XSYNWTO),X'8000'
         DC    C' VTAM '
XSYNWHO  DC    C'SYN',C'AD ENTRY '
XSYNSYM  DC    CL8' ' ,C' '
XSYNR0   DC    X'40202120',C' (R0)  RPL='
XSYNRPL  DC    C'XXXXXX',C' R/F='
XSYNCC   DC    C'XXXX',C' SENSE='
XSYNFDBK DC    C'XXXXXXXX',C' @ '
XSYNAT   DC    C'XXXXXX',C' '
XSYNWTON DC    X'00000020'   ROUTCDE=11
         EJECT ,
XREAD    XHEAD ,
         TM    FLAGS,FGDOWN  VTAM GOING ?                        88047
         BNZ   XREAEXIT      YES; JUST QUIT                      88047
         LTR   R7,R7                                             88019
         BZ    XREATRY2      NO MTS; NO PROCESSING               88019
         ICM   R0,15,MTSTASK STILL HERE ?                        88047
         BZ    XREATRY2      NO; JUST REREAD                     88047
         LTR   R8,R8         IMPATIENT USER ?                    89131
         BZ    XREATRY2      YES; IGNORE                         89131
         LTR   R11,R11       DITTO ?                             89131
         BZ    XREATRY2      DITTO                               89131
*OOPS*   STATUS STOP,TCB=(0) BETTER THAN NOTHING                 88047
XREACHK  CHECK RPL=(R6)
         L     R3,RPLRLEN    GET LENGTH READ
         TM    MTSFLGT,MTSFBUG  DEBUG MODE ?                     87032
         BZ    XREADBBG      NO                                  87032
         STM   R15,R3,MTSSAVCK  SAVE SOME REGISTERS              87032
         MVC   MTSPVLIN(4),MTSPH137  SET PRINT LINE LENGTH       87032
         MVI   MTSPVLIN+4,C' '  CLEAR PRINT LINE                 87032
         MVC   MTSPVLIN+5(132),MTSPVLIN+4                        87032
         MVC   MTSPFLIN+1(5),=C'XREAD'                           87032
         LA    R2,INPBUFF    LOAD THE INPUT ADDRESS              87158
         STM   R2,R3,MTSDDB                                      87032
         UNPK  MTSPFLIN+7(9),MTSDDB(5)                           87032
         UNPK  MTSPFLIN+16(9),MTSDDB+4(5)  LENGTH                87032
         UNPK  MTSPFLIN+25(5),RPLRTNCD(3)  RETURN CODE           87032
         UNPK  MTSPFLIN+29(5),RPLFDBK2(3)  FEED-BACK             87032
         TR    MTSPFLIN+7(9+9+8),MDBHEX-C'0'  MAKE PRINTABLE     87032
         MVI   MTSPFLIN+15,C' '                                  87032
         MVI   MTSPFLIN+24,C' '                                  87032
         MVI   MTSPFLIN+33,C' '                                  87032
         LTR   R15,R3        CHECK LENGTH                        87032
         BNP   XREADBBP                                          87032
         MIN   R15,=Y(130-34),TYPE=H  NOT TOO LONG               87032
         BCTR  R15,0                                             87032
         EX    R15,XREADBBM  MOVE TEXT                           87032
XREADBBM MVC   MTSPFLIN+34(0),0(R2)  MOVE SOME TEXT              87032
XREADBBP LA    R1,MTSPVLIN   POINT TO OUTPUT                     87032
         L     R15,=A(XPRNT)                                     87032
         BALR  R14,R15       PRINT THE LINE                      87032
         LTR   R0,R3         COPY AND CHECK LENGTH               87165
         BNP   XREADBBU                                          87165
         LR    R1,R2         COPY START ADDRESS                  87165
         L     R15,=A(HEXPRINT)                                  87165
         BALR  R14,R15       CALL THE HEX DUMP ROUTINE           87165
XREADBBU LM    R15,R0,MTSSAVCK  RESTORE CHECK RESPONSE           87032
XREADBBG LA    R14,88        SET I/O ERROR CODE
         CR    R15,R14       I/O ERROR ?
         BNE   XREACHEK      NO; CHECK OTHERS
XREADBAD L     R1,=X'01000000'  SET FOR 41 POST CODE
         SLR   R3,R3         NO DATA TRANSFERRED                 87154
         B     XREAD2        POST; SKIP ERROR ABEND
XREACHEK CHRET RETRY=XREACHK CHECK RETURN
         L     R14,MTSINPAD  GET SUBTASK BUFFER
         TM    RPLCHN,RPLFIRST+RPLONLY NEW CHAIN ?               87151
         BNZ   XREACHON      YES                                 87151
         L     R15,=A(L'MTSBUFIN+27*132)  LOAD MAX LENGTH        87151
         ICM   R0,15,MTSINPLN  GET PRIOR LENGTH                  87151
         BZ    XREADBAD      TREAT AS UNSPECIFIED ERROR          87154
         SR    R15,R0        GET MAXIMUM MOVABLE                 87151
         AR    R14,R0        DESTINATION                         87151
         LR    R1,R3         COPY CURRENT LENGTH                 87151
         CR    R1,R15        WILL IT FIT ?                       87151
         BNH   *+6           YES                                 87151
         LR    R1,R15        ELSE TRUNCATE                       87151
         LR    R3,R1         GET LENGTH TO BE MOVED              87151
         AR    R3,R0         PLUS OLD LENGTH = NEW SIZE          87151
         LA    R0,INPBUFF    GET INPUT BUFFER                    87151
         MVCL  R14,R0        APPEND TEXT                         87151
         SLR   R1,R1         NO MORE MOVES                       87151
         SLR   R15,R15                                           87151
         B     XREACHOF                                          87151
XREACHON LR    R15,R3        COPY THE LENGTH                     87151
         LA    R0,INPBUFF    GET COMMON BUFFER
         LR    R1,R3         COPY THE LENGTH; ZERO FILL
XREACHOF TM    RPLCNTDC,RPLSIGNL   ATTENTION ?                   87032
         BZ    XREADATR      NO                                  87155
         LA    R3,1          SET FAKE LENGTH                     87032
         MAX   (R1),(R3)     USE LARGER LENGTH TO MOVE           87155
         MAX   (R15),(R3)    DITTO FOR OUTPUT SIDE               87158
         MVI   INPBUFF,X'6C' POINT TO PA1                        87041
         L     R14,MTSINPAD  GET SUBTASK BUFFER
         TM    MTSSIMFG,MTSSICRT  REAL CRT ?                     89328
         BNZ   XREADATA      YES; JUST USE PA1                   87032
         MVCL  R14,R0        COPY ANYWAY                         87041
         OI    MTSFLGM,MTSFATTN  SIGNAL ATTENTION                87032
         B     XREAD2        NO EDITING                          87041
XREADATR OI    MTSFLGV,MTSFRSVP  NEXT WRITE IS A RESPONSE        87155
         TM    MTSSIMFG,MTSSICRT  REAL CRT ?                     89328
         BZ    XREADATA      NO; NOTHING SPECIAL                 87214
         CH    R1,=H'3'      MORE THAN THREE BYTES ?             87214
         BNH   XREADATA      NO                                  87214
         LR    R2,R0         COPY INPUT ADDRESS                  87214
         CLI   3(R2),X'11'   IS INPUT UNFORMATTED OR SPECIAL ?   87214
         BE    XREADATA      NO; PROCEED                         87214
         CLC   =X'7D4040',0(R2)   WYLBUR PASS-THROUGH BUFFER ?   87214
         BNE   XREADATA      NO                                  87214
         MVC   0(3,R14),=X'7DC370'  FORCE AID AND EXCESSIVE CURSOR
         LA    R14,3(,R14)                                       87214
         AH    R3,=H'3'      ADJUST OUTPUT LENGTH                87214
         MVC   0(3,R2),=X'1140C1'  FAKE FORMATTED FIELD          87214
XREADATA MVCL  R14,R0        PRESERVE COMMON IN SPECIFIC BUFFER
XREAD2   ST    R3,MTSINPLN   SAVE FOR READ ROUTINE
         LA    R2,MTSATECB   GET NORMAL ATTENTION ECB            89328
XREAD3   NI    FLAGS,255-FGREAD  NO LONGER HAVE A READ PENDING   87151
         TM    RPLCHN,RPLLAST+RPLONLY  CHAIN END ?               87151
         BZ    XREATRY       NO; SKIP POST                       87151
         L     R3,MTSINPAD   GET INPUT BUFFER                    88243
*DEFER*  CLI   0(R3),X'6D'   CLEAR KEY USED ?                    88243
*DEFER*  BNE   XREAD4        NO                                  88243
*DEFER*  OI    EXWXPFLG-EXHBWORK(R3),EXWXPRQ  REFRESH PARTITION  88243
XREAD4   POST  (R2),0        SIGNAL COMPLETION                   87151
XREATRY  ICM   R0,15,MTSTASK GET TCB BACK                        88047
         BZ    XREATRY2                                          88047
*OOPS*   STATUS START,TCB=(0)  START IT UP                       88047
XREATRY2 L     R2,=A(XREAD)
         RECEIVE RPL=READRPL,RTYPE=(DFSYN,DFASY),OPTCD=(ASY,ANY,CA),   *
               EXIT=(R2)     WAIT FOR COMPLETION
         CHRET RETRY=XREATRY2 CHECK RETURN CODE
         OI    FLAGS,FGREAD  SHOW I/O PENDING
         XEXIT ,
         SPACE 1
XRITE    XHEAD SAVE=RENT
XRITCHK  CHECK RPL=(R6)
         CLC   =X'0404',RPLFDBK  DEVICE PROBLEM ?                87018
         BNE   XRITEST       NO                                  87018
         CLC   =X'2003',RPLFDBK2  OTHER BRACKET ERROR ?          87263
         BE    XRITBRAK      YES; RETRY ONCE                     87263
         CLI   RPLFDBK2,X'08'  DEVICE PROBLEM ?                  87257
         BNE   XRITNRYE      NO                                  87257
         CLI   RPLSSMI,X'11' LOGICAL BREAK ?                     89328
         BE    XRITBRAK      YES; TREAT AS BRACKET ERROR         89328
         CLI   RPLSSMI,X'13' BRACKET ERROR ?                     87257
         BE    XRITBRAK      YES                                 87257
         CLI   RPLSSMI,X'1B' HAM-HANDED USER (KEY DEPRESSED)?    87257
         BE    XRITFAKE      YES; IGNORE                         87257
         CLI   RPLSSMI,X'2B' USER HIT SETUP KEY (OR SOMETHING) ? 87257
         BE    XRITFAKE      YES; IGNORE                         87257
         CLI   RPLSSMI,X'2D' USER DID SOMETHING ELSE ?           87263
         BE    XRITFAKE      YES; IGNORE                         87263
         CLI   RPLSSMI,X'31'   TERMINAL POWERED OFF ?            87257
         BNE   XRITNRYE      NO                                  87146
XRITFAKE SLR   R15,R15       FAKE NON-ERROR                      87146
         SLR   R0,R0                                             87146
         B     XRITCHOK                                          87146
XRITBRAK TM    MTSFLGV,MTSFBKER PRIOR BRACKET ERROR ?            87257
         BNZ   XRITNRYE      YES; FAIL                           87257
         OI    MTSFLGV,MTSFBKER SET BRACKET RECOVERY TRIED       87257
         SESSIONC RPL=MTSCRPL,OPTCD=SYN,ECB=INTERNAL,                  *
               CONTROL=CLEAR,STYPE=REQ  RESET LU SESSION         87257
         SESSIONC RPL=MTSCRPL,OPTCD=SYN,ECB=INTERNAL,                  *
               CONTROL=SDT,STYPE=REQ  ENABLE DATA TRAFFIC AGAIN  87257
         OI    RPLRH3,RPLBB+RPLEB  SET BRACKETS                  87257
XRITEXEC EXECRPL RPL=(R6)    TRY IT AGAIN                        87257
         CHRET RETRY=XRITEXEC  RE-WRITE                          87257
         B     XRITEXIT      GO BACK TO COMPLETE THE REQUEST     87257
XRITNRYE CLC   =X'1000',RPLFDBK2  USER SENSE ONLY ?              87018
         BE    XRITUSEN      YES; RETURN USER SENSE              87018
         CLC   =X'0811',RPLFDBK2  BREAK ?                        87044
         BNE   XRITUSF0      NO                                  87044
         XC    RPLFDBK2(2),RPLFDBK2  PREVENT XRITE ERROR         87044
         OI    MTSFLGM,MTSFATTN  SIGNAL ATTENTION                87044
         B     XRITCHEK      RETURN OK                           87044
XRITUSF0 CLI   RPLFDBK2,0    USER SENSE ONLY ?                   87018
         BNE   XRITBAD       NO; PROCEED WITH ERROR              87018
XRITUSEN ICM   R0,15,RPLFDBK2  RETURN SENSE, MI, USER SENSE      87018
XRITBAD  LA    R15,88        SET (LOCALLY DETECTED) ERROR        87018
XRITEST  LA    R14,88        SET I/O ERROR CODE
         CR    R15,R14       I/O ERROR ?
         BNE   XRITCHEK      NO; CHECK OTHERS
         L     R0,=X'01000000'  SET FOR 41 POST CODE
         LA    R15,1
         A     R15,MTSERRCT
         ST    R15,MTSERRCT  STASH BACK
         B     XRITPOST      POST; SKIP ERROR ABEND
XRITCHEK CHRET RETRY=XRITCHK
         XC    MTSERRCT,MTSERRCT  RESET ERROR COUNT
XRITCHOK L     R0,=X'3F000000'  SET FOR 7F POST CODE
         NI    MTSFLGV,255-MTSFBKER  RESET BRACKET ERROR FLAG    87257
XRITPOST POST  MTSWECB,(0)
         TM    RPLCNTDC,RPLSIGNL  HUH ?                          87032
         BZ    *+8                                               87032
         OI    MTSFLGM,MTSFATTN  SIGNAL ATTENTION                87032
         XEXIT ,
         EJECT ,                                                 89337
*        THIS EXIT IS INVOKED BY KERMIT AFTER DATASET ALLOCATION, BUT
*        PRIOR TO OPENING THE DCB. R1 POINTS TO AN OPEN TYPE (0-IN,
*        80-OUT) AND THE ADDRESS OF THE DDNAME. USER INFO IS IN THE
*        MTS FOR THE SUBTASK.                                    89337
*          A HEX 1 IN THE TYPE INDICATES A DSNAME IS BEING PASSED.
*        HEX 40 IN THE TYPE REQUESTS VOLUME SERIAL ACCESS CHECKING.
*                                                                89337
DDACCESS XHEAD SAVE=RENT,EXH=NO,RPL=NO,MODE=OS,RENT=DDACWRKL     89337
         L     R7,PSATOLD-PSA  GET CURRENT TCB
         L     R7,TCBFSA-TCB(,R7)  LOAD THE SAVE AREA
         L     R7,24(,R7)    LOAD ATTACH'S R1
         SH    R7,=Y(MTS@PARM-MTSWORK)  GET WORK AREA BACK
         USING MTSWORK,R7    DECLARE IT
         LM    R9,R10,0(R6)  LOAD OPEN TYPE, DDNAME ADDRESS      89337
         USING DDACWORK,R13  DECLARE WORK AREA                   89337
         TM    3(R6),X'40'   VOLUME SERIAL ?                     91294
         BNZ   DDACVOLS      YES                                 91294
         TM    3(R6),1       IS THIS DSNAME OR DDNAME ENTRY ?    91084
         BZ    DDACDDNM      DDNAME                              91084
         MVC   JFCBDSNM,0(R10)  COPY PASSED PARAMETER            91084
         B     DDACDSN       AND CHECK THE NAME                  91084
DDACDDNM MVC   RDDUMDCB(RDOPLIST-RDDUMDCB),PARDCB  MAKE PHONY DCB
         LA    R2,RDDUMDCB                                       81359
         LA    R3,INFMJFCB   POINT TO NEW JFCB                   89337
         STM   R2,R3,RDOPLIST  MAKE LISTS                        81359
         MVI   RDOPLIST,X'80'  SET END OF LIST BIT FOR DCBS      81359
         MVI   RDEXLIST,X'87'  SET JFCB EXIT ADDRESS             81359
         LA    R2,RDEXLIST                                       81359
         STCM  R2,7,RDDUMDCB+DCBEXLSA-IHADCB  SET EXIT LIST ADDRESS
         MVC   DCBDDNAM-IHADCB+RDDUMDCB,0(R10) COPY USER'S DDNAME
         RDJFCB MF=(E,RDOPLIST)  READ THE JFCB                   81359
         LA    R8,12         SET CONTROL BLOCK ERROR             89337
         BXH   R15,R15,DDACOUCH  EXIT IF NOT READ                89337
DDACDSN  CLC   =C'WYL.',JFCBDSNM  OLD STYLE WYLBUR ?             89337
         BE    DDACWOLD      YES                                 89337
         CLC   MTSUID(7),JFCBDSNM  USERID. FORM ?                89337
         BNE   DDACHACC      CHECK ACCT. FORM                    89337
         CLI   JFCBDSNM+7,C'.'  UID. ?                           89337
         BE    DDACPASS      YES; ALLOW                          89337
DDACHACC MVC   RDOPLIST,MTSACCT  COPY ACCOUNT                    89337
         NI    RDOPLIST,X'CF'  MAKE 1-9 INTO A-I                 89337
         CLI   RDOPLIST,X'C0'  SYSTEMS ?                         89337
         BNE   *+8           NO                                  89337
         MVI   RDOPLIST,C'Z'   SYSTEMS SPECIAL                   89337
         MVI   RDOPLIST+4,C'.' MAKE INDEX DOT                    89337
         CLC   RDOPLIST(5),JFCBDSNM  ACCOUNT INDEX ?             89337
         BE    DDACPASS                                          89337
         B     DDACFAIL      NO OTHER CHECKS AT THIS TIME        89337
         SPACE 1                                                 89337
DDACWOLD CLC   MTSUID(7),JFCBDSNM+13  WYLBUR USER ?              89337
         BE    DDACPASS      YES; PERMIT                         89337
         MVC   RDOPLIST,MTSACCT  COPY ACCOUNT                    91254
         NI    RDOPLIST,X'CF'  MAKE 1-9 INTO A-I                 91254
         CLI   RDOPLIST,X'C0'  SYSTEMS ?                         91254
         BNE   *+8           NO                                  91254
         MVI   RDOPLIST,C'Z'   SYSTEMS SPECIAL                   91254
         MVI   RDOPLIST+4,C'.' MAKE INDEX DOT                    91254
         CLC   RDOPLIST,JFCBDSNM+4  SAME ACCOUNT ?               91254
         BE    DDACPASS      YES; ALLOW                          89337
DDACFAIL LA    R8,4          DENY ACCESS                         89337
         TM    3(R6),X'80'   IS THIS AN OUTPUT FILE ?            90050
         BNZ   DDACOUCH      YES; FAIL IT                        90050
         TM    MTSSAVPR,PRFSYS  SYSTEMS PRIVILEGES ?             90050
         BNZ   DDACPASS      YES; ALLOW DIAGNOSTIC READS         90050
         CLC   =C'PUBLIC.',JFCBDSNM  PUBLIC WYLBUR FILES ?       91091
         BE    DDACPASS      YES; PERMIT                         91091
         CLC   =C'SUPPORT.',JFCBDSNM  ALTERNATE 'PUBLIC' ?       91254
         BE    DDACPASS      YES; PERMIT                         91254
         CLC   =C'WYL.DTPUBLIC',JFCBDSNM  OLD WYLBUR PUBLIC ?    91091
         BE    DDACPASS      YES                                 91091
         CLC   =C'SYS1.PROCLIB',JFCBDSNM  SYSTEM PROCS ?         91091
         BE    DDACPASS                                          91091
         CLC   =C'SYS1.DTSPROC',JFCBDSNM  BATCH PROCS ?          91091
         BE    DDACPASS      YES                                 91091
         CLC   =C'SYS1.CLIST',JFCBDSNM  LOGON PROCS ?            91091
         BNE   DDACOUCH      NOTHING ELSE, YET                   91254
         B     DDACPASS      ALLOW ACCESS                        91294
         SPACE 1                                                 91294
***********************************************************************
*                                                                     *
*        DASD VOLUME ACCESS CHECK                                     *
*                                                                     *
*        EACH LEGAL VOLUME HAS AN ENTRY IN THE USERVOL TABLE, WHICH   *
*        IS POINTED TO BY THE USER CVT, AND BUILT BY THE LOCAL SMF    *
*        INITIALIZATION ROUTINE A$GINIT (USING THE ACCOUNTING SVC).   *
*                                                                     *
*        THE USERVOL ENTRY HAS A LIST OF ONE TO FOUR ACCOUNT NUMBERS  *
*        ALLOWED TO ACCESS THE PACK. THE ACCOUNT NUMBER CORRESPONDS   *
*        TO THE RACF GROUP (SOMEWHAT). THE ENTRY MAY BE IN THE FORM   *
*        OF A MASK, USING '?' FOR CHARACTER MATCH, AND '*' (AND HEX   *
*        ZERO) FOR A COMPLETE MATCH.                                  *
*                                                                     *
***********************************************************************
         SPACE 1                                                 91294
DDACVOLS LTR   R1,R10        ANY POINTER PASSED ?                91294
         BZ    DDACPASS      NO; PASS WITH TREPIDATION           91294
         CLI   0(R10),C' '   VALID ?                             91294
         BNH   DDACPASS      YES ?                               91294
         L     R6,CVTPTR     GET CVT                             91294
         ICM   R6,15,CVTUSER-CVTMAP(R6)  TEST USER EXTENSION     91294
         BZ    DDACPASS      HUH ?                               91294
         ICM   R6,15,UCLVOLS-USERCVT(R6)  VOLUME TABLE ?         91294
         BZ    DDACPASS      NO ?                                91294
         LM    R15,R1,0(R6)  LOAD VOLSER BXLE TABLE              91294
         SR    R1,R0         ADJUST FOR FIRST TEST               91294
         CR    R15,R1        IS THERE AT LEAST ONE ENTRY ?       91294
         BH    DDACPASS      NO; DISASTER - ALLOW THIS ACCESS    91294
VOLCHLUP BXH   R15,R0,VOLNOACC  VOLUME NOT DEFINED               91294
         USING UVOLSER,R15   DECLARE SINGLE VOLUME ENTRY         91294
         CLC   0(L'UVOLSER,R10),UVOLSER  IS THIS THE CORRECT ENTRY ?
         BH    VOLCHLUP      NOT YET; TRY AGAIN                  91294
         BL    VOLNOACC      NO; FAIL - DEFINITION MISSING       91294
         SPACE 1                                                 91294
VOLCHMOR SLR   R5,R5         (RESTART FOR MULTIPLE ENTRIES)      91294
         ICM   R5,1,UVOL#ENT  GET NUMBER OF ACCOUNT NUMBERS      91294
         BZ    VOLNOACC      NO ACCESS PERMITTED                 91294
         LA    R4,UVOLACT1   POINT TO FIRST ENTRY                91294
         LA    R0,4          SET NUMBER OF ENTRIES               91294
         MIN   (R5),(R0)     USE SMALLER VALUE TO LOOP           91294
         DROP  R15                                               91294
         LR    R3,R15        SAVE DEFINITION ADDRESS             91294
         SPACE 1                                                 91294
         USING UVOLACT1,R4   DECLARE THE ENTRY ADDRESS           91294
VOLACCLP TM    UVOLACF1,A$VAFRDW+A$VALLOC  ALLOCATE/WRITE PRIVY? 91294
         BNO   VOLACCUP      NO; TRY ANOTHER                     91294
         LA    R1,MTSACCT    POINT TO ACCOUNT TO BE CHECKED      91294
         LA    R0,L'MTSACCT    AND ITS LENGTH                    91294
         LA    R14,UVOLACT1  POINT TO MASK                       91294
         CLI   UVOLACT1,0    ANY ENTRY AT ALL ?                  91294
         BE    VOLACCUP      NO; DO NOT TEST                     91294
         LA    R15,L'UVOLACT1  SET MASK LENGTH                   91294
         TM    UVOLACF1,A$VAFSUB  ANY SUB-ACCOUNT ?              91294
         BZ    *+8           YES                                 91294
         LA    R15,4         SET LENGTH FOR MAJOR ACCOUNT, ONLY  91294
         BAL   R9,COMPMASK   CHECK FOR MATCH                     91294
         BE    DDACPASS      MATCH; PERMIT ACCESS                91294
*DEFER*  B     VOLNOACC      NOT AUTHORIZED                      91294
*DEFER BECAUSE THERE MAY BE ANOTHER MATCHING ENTRY IN THE LIST   91294
         SPACE 1                                                 91294
VOLACCUP LA    R4,UVOLACT2   POINT TO NEXT ENTRY IN LIST         91294
         BCT   R5,VOLACCLP   CHECK IT                            91294
         DROP  R4            NO LONGER NEED SINGLE ENTRY         91294
         USING UVOLSER,R3    RESTORE SAVED ADDRESS               91294
         CLI   UVOL#ENT,4    MORE THAN FOUR ENTRIES ?            91294
         BL    VOLNOACC      NO; UNAUTHORIZED REQUEST            91294
*        WHEN MORE THAN 4 MASKS ARE DEFINED FOR A VOLUME, THE NEXT
*        FOUR (MORE OR LESS) ARE PLACED INTO AN ADJACENT ENTRY.  91294
         LM    R0,R1,4(R6)   RELOAD BXLE LENGTH/END ADDRESSES    91294
         LR    R15,R3        COPY CURRENT ENTRY ADDRESS          91294
         BXH   R15,R0,VOLNOACC  NOT AUTHORIZED FOR ACCESS        91294
         CLC   0(L'UVOLSER,R10),UVOLSER-UVOLSER(R15)  CONTINUATION ?
         BE    VOLCHMOR      YES; DO IT                          91294
*NEXT    B     VOLNOACC      ELSE ACCESS IS DENIED               91294
         SPACE 1                                                 91294
VOLNOACC LA    R8,4          ACCESS DENIED                       91294
         B     DDACOUCH      EXIT WITH ERROR                     91294
         SPACE 1                                                 91294
*        COMPARE ROUTINE WITH MASK OPTION                        91294
*          TO MAKE THE CODE FASTER, MASK LENGTH AND STRING LENGTH
*          ARE DEFINED AS THE SAME REGISTER. TO MAKE IT EASIER TO
*          REMOVE THIS CODE IF NOT DESIRED, ALL CALLERS MAINTAIN 91294
*          BOTH LENGTH REGISTERS (15 AND 0).                     91294
*                                                                91294
COMPMASK CMASK L=R15,MASKEQU='?',MASKEND='*
         SPACE 1                                                 91294
DDACPASS SLR   R8,R8         PERMIT ACCESS                       89337
DDACOUCH L     R1,0(,R13)    GET OLD SAVE AREA (OUR STYLE)       89328
         ST    R8,16(,R1)    PASS RETURN CODE                    89328
         XEXIT MODE=OS       RETURN TO CALLER                    89328
         SPACE 1                                                 81359
PARDCB   DCB   DDNAME=ANY,MACRF=(E),DSORG=PS,EXLST=2             81359
         SPACE 1                                                 89337
DDACWORK DSECT ,                                                 89337
         DS    18F           SAVE AREA                           89337
RDDUMDCB DCB   DDNAME=ANY,MACRF=(E),DSORG=PS,EXLST=2             89337
RDOPLIST DS    A                                                 89337
RDEXLIST DS    A                                                 89337
DDACJFCB IEFJFCBN ,          EXPAND AN IN-LINE JFCB              89337
DDACWRKL EQU   *-DDACWORK                                        89337
         SPACE 1                                                 91294
         PRINT &PRTMAC                                           91294
         SPACE 1                                                 91294
         SPACE 1                                                 91294
USERVOLS USERVOLS ,                                              91294
         SPACE 1                                                 91294
A$VOLUME A$VSER ,            EXPAND ACCOUNT RECORD FOR VOL-SER   91294
         SPACE 1                                                 91294
         PRINT &PRTSYS                                           91294
NETCOMM  CSECT ,
         EJECT ,                                                 89328
*        THIS CODE SERVES AS A REPLACEMENT FOR THE TSO 'GETLINE' 89328
*        FUNCTION, EXCEPT THAT ONLY TERMINAL INPUT IS PROCESSED, 89328
*        AND CURRENTLY 'ASIS' AND 'EDIT' REQUESTS ARE IGNORED.   89328
*                                                                89328
GETLINE  XHEAD SAVE=RENT,EXH=NO,RPL=NO,MODE=OS                   89328
         L     R7,PSATOLD-PSA  GET CURRENT TCB
         L     R7,TCBFSA-TCB(,R7)  LOAD THE SAVE AREA
         L     R7,24(,R7)    LOAD ATTACH'S R1
         SH    R7,=Y(MTS@PARM-MTSWORK)  GET WORK AREA BACK
         USING MTSWORK,R7    DECLARE IT
         LA    R8,28         SET TERMINAL DISCONNECTED           89328
         TM    MTSFLGT,MTSFGONE  SHUTTING DOWN ?                 89328
         BNZ   GETLOUCH      YES; QUIT RAPIDLY                   89328
         LA    R8,20         SET PARAMETER LIST ERROR            89328
         L     R6,12(,R6)    GET CONTROL AREA                    89328
         TM    2(R6),X'80'   GET ?                               89328
         BZ    GETLOUCH      NO; INVALID GTPB OR IOPL            89328
         UNPOST MTSWECB      DON'T WAIT FOR WRITE                89328
         XC    MTSTMECB,MTSTMECB  CLEAR TIMER ECB                89345
         STIMER REAL,STIMEXIT,BINTVL==A(15*60*100)  WAIT 15 MINUTES
         WAIT  ECBLIST=MTSTLIST  WAIT FOR ATTN, INPUT OR TIMER   89328
         LA    R8,28         SET DISCONNECTED                    89328
         TM    MTSTMECB,X'40'  TIMER EXPIRED ?                   89345
         BZ    GETLBUF       NO                                  89345
GETLLOST OI    MTSFLGT,MTSFGONE  SET TERMINAL GONE               89345
         B     GETLOUCH      QUIT                                89345
GETLBUF  TTIMER CANCEL       CANCEL TIMER REQUEST                89345
         TM    MTSCNECB,X'40'  CANCEL POSTED ?                   89328
         BNZ   GETLLOST      YES; QUIT NOW                       91127
         SLR   R8,R8         SET FOR NORMAL COMPLETION           89328
         LM    R2,R3,MTSINPAD  GET INPUT BUFFER/LENGTH           89328
         LTR   R0,R3         ANY INPUT ?                         89330
         BNP   NONEDIT       HUH ?                               89330
         TM    2(R6),X'03'   ASIS, CONTROL OR FULL-SCREEN MODE ? 89330
         BNZ   NONEDIT       YES; SKIP EDITING                   89330
         LR    R14,R2        GET OUTPUT ADDRESS BACK             89330
         LA    R15,0(R3,R14) GET LAST BYTE +                     89330
LOOK3767 BCTR  R15,0         LAST BYTE                           89330
         CLI   0(R15),EOT    CONTROL-D TYPE BREAK ?              89330
         BH    DONE3767      DONE WITH BACK-SCAN                 89330
         BE    ATTN3767      CONTROL-D INTERRUPT                 89330
         CLI   0(R15),X'16'  BACKSPACE ?                         89330
         BE    DONE3767      YES; NO EDITING                     89330
         CLI   0(R15),X'02'  SOH OR STX ?                        89330
         BNH   DONE3767      MAYBE; STOP EDITING                 89330
         BCT   R3,LOOK3767   DON'T PASS ON                       89330
         B     NONEDIT       DONE EDITING                        89330
ATTN3767 OI    MTSFLGM,MTSFATTN  SET ATTENTION BIT               89330
DONE3767 L     R14,MTSINPAD  GET TEXT START                      89330
         LR    R15,R14       DITTO                               89330
         SLR   R2,R2         CLEAR FOR CR                        89330
         LA    R1,X'40'      CONSTANT BLANK                      89330
LOOP3767 IC    R2,0(,R14)    GET NEXT CHARACTER FOR INSPECTION   89330
         CR    R2,R1         PRINTABLE CHARACTER ?               89330
         BNL   PUTS3767      YES; PROCESS IT                     89330
         CLM   R2,1,=X'01'   SPECIAL CHARACTER ?                 89330
         BE    PUTS3767      YES; COPY AS IS                     89330
         CLM   R2,1,=X'02'   SOH ?                               89330
         BE    PUTS3767                                          89330
         CLI   0(R14),X'16'  BACKSPACE ?                         89330
         LR    R2,R1         BUT REPLACE BY BLANK                89330
         BNE   PUTS3767      NO; JUST PROCESS AS BLANK           89330
         STC   R1,0(,R14)    CLEAR IT OUT                        89330
         C     R15,MTSINPAD  POSSIBLE TO BACK-UP ?               89330
         BNH   BUMP3767      NO; JUST BUMP 14                    89330
         BCTR  R15,0         BACK-UP ONE SPACE                   89330
         STC   R1,0(,R15)    CLEAR OLD BYTE                      89330
         SH    R3,=H'2'      SET NEW LENGTH                      89330
         BNM   BUMP3767      PROCESS                             89330
         SLR   R3,R3         HUH ?                               89330
         B     BUMP3767      AND AVOID R15 INCREMENTATION        89330
PUTS3767 STC   R1,0(,R14)    CLEAR THE SOURCE LOCATION           89330
         STC   R2,0(,R15)    STORE THE NEW BYTE                  89330
         LA    R15,1(,R15)   SET NEW OUTPUT LOCATION             89330
BUMP3767 LA    R14,1(,R14)   SET NEW INPUT                       89330
         BCT   R0,LOOP3767   REPEAT FOR ALL                      89330
NONEDIT  L     R2,MTSINPAD   RELOAD START ADDRESS                89330
         LA    R0,4                                              89328
         SR    R2,R0         SPACE TO 4 BEFORE TEXT              89328
         AR    R3,R0         SET LENGTH INCLUDING LENGTH OF LENGTH
         SLL   R3,16         MALIGN                              89328
         STCM  R3,15,0(R2)   MAKE RDW                            89328
         ST    R2,4(,R6)     SET BUFFER POINTER                  89328
         UNPOST MTSATECB     RESET ATTENTION ECB                 89328
         OI    MTSFLGM,MTSFREAD   SHOW LAST I/O WAS READ         91310
         TM    MTSFLGM,MTSFATTN  ATTENTION ?                     89328
         BZ    GETLOUCH      NO                                  89328
         LA    R8,8          SET ATTENTION INTERRUPT             89328
         NI    MTSFLGM,255-MTSFATTN  RESET ATTENTION             89330
         CLC   0(2,R2),=H'4' ANY TEXT READ ?                     91091
         BH    GETLOUCH      YES; LEAVE 'READ' STATUS            89345
         NI    MTSFLGM,255-MTSFREAD   RESET                      91310
         SPACE 1                                                 89328
GETLOUCH L     R1,0(,R13)    GET OLD SAVE AREA (OUR STYLE)       89328
         ST    R8,16(,R1)    PASS RETURN CODE                    89328
         XEXIT MODE=OS       RETURN TO CALLER                    89328
         SPACE 1                                                 89345
*        STIMER EXIT FOR CONDITIONAL WAIT ON INPUT               89345
*                                                                89345
         USING STIMEXIT,R15                                      89345
STIMEXIT L     R1,PSATOLD-PSA  GET CURRENT TCB                   89345
         L     R1,TCBFSA-TCB(,R1)  LOAD THE SAVE AREA            89345
         L     R1,24(,R1)    LOAD ATTACH'S R1                    89345
         SH    R1,=Y(MTS@PARM-MTSWORK)  GET WORK AREA BACK       89345
         POST  MTSTMECB-MTSWORK(R1),0  POST TIME ECB             89345
         BR    R14           RETURN TO OS                        89345
         EJECT ,                                                 89328
*        THIS CODE SERVES AS A REPLACEMENT FOR THE TSO 'PUTLINE' 89328
*        FUNCTION, EXCEPT THAT ONLY TERMINAL INPUT IS PROCESSED, 89328
*        AND CURRENTLY 'ASIS' AND 'EDIT' REQUESTS ARE IGNORED.   89328
*                                                                89328
PUTLINE  XHEAD SAVE=RENT,EXH=NO,RPL=NO,MODE=OS                   89328
         L     R7,PSATOLD-PSA  GET CURRENT TCB
         L     R7,TCBFSA-TCB(,R7)  LOAD THE SAVE AREA
         L     R7,24(,R7)    LOAD ATTACH'S R1
         SH    R7,=Y(MTS@PARM-MTSWORK)  GET WORK AREA BACK
         USING MTSWORK,R7    DECLARE IT
         LA    R8,20         SET TERMINAL DISCONNECTED           89328
         TM    MTSFLGT,MTSFGONE  SHUTTING DOWN ?                 89328
         BNZ   PUTLOUCH      YES; QUIT RAPIDLY                   89328
         LA    R8,12         SET PARAMETER LIST ERROR            89328
         L     R6,12(,R6)    GET CONTROL AREA                    89328
         L     R4,4(,R6)     GET BUFFER POINTER                  89328
         LH    R5,0(,R4)     GET LENGTH                          89328
         LA    R4,4(,R4)     POINT TO TEXT                       89328
         SH    R5,=H'4'      ALLOW FOR RDW LENGTH                89328
         BM    PUTLOUCH      SKIP                                89328
         SLR   R8,R8         SET FOR NO ERROR                    89328
         NI    MTSFLGM,MTSFATTN+MTSFREAD   KEEP ATTN AND READ    91310
         TM    2(R6),X'02'   CONTROL OR FULL-SCREEN ?            91310
         BNZ   PUTLRCOM      YES; DON'T MOVE, DON'T ALTER        89328
         LA    R14,MTSBUFF   GET OUTPUT BUFFER                   89328
         TM    MTSFLGM,MTSFREAD   LAST I/O WAS READ ?            91310
         BZ    PUTLNRED      NO                                  89345
         MVC   0(2,R14),=X'0D25'  MAKE LEADING CR/LF             89345
         LA    R14,2(,R14)   AND BUMP START                      89345
PUTLNRED LR    R15,R5        COPY LENGTH                         89328
         LR    R0,R4         COPY INPUT BUFFER                   89328
         LR    R1,R5         AND LENGTH AGAIN                    89328
         MVCL  R14,R0        COPY TO NEW BUFFER                  89328
         LA    R4,MTSBUFF    SET NEW BUFFER START                89345
         TM    2(R6),X'03'   SPECIAL WRITE ?                     91310
         BNZ   PUTLRINL      YES; DON'T LINE FEED A PROMPT       91310
         MVC   0(2,R14),=X'0D25'  CARRIAGE RETURN/LINE FEED      89328
         LA    R14,2(,R14)   SKIP OVER                           89345
         TM    MTSTRMFG,MTSFAUCR  AUTO CR TERMINAL ?             89328
         BZ    PUTLRINL      NO                                  89328
         CH    R5,MTSCOLS    SAME AS TERMINAL WIDTH ?            89328
         BNE   PUTLRINL      NO; LF NECESSARY                    89328
         BCTR  R14,0         SET LENGTH FOR CR, NO NL            89328
         B     PUTLRICR      DON'T DELETE BOTH                   91091
PUTLRINL TM    MTSTRMFG,MTSFAULF  DOES CR INCLUDE LF FUNCTION ?  89328
         BZ    *+6           NO                                  89328
         BCTR  R14,0         SEND ONLY CR                        89328
PUTLRICR LR    R5,R14        GET LAST BYTE USED +1               89345
         SR    R5,R4         SET BUFFER LENGTH                   89345
PUTLRCOM NI    RPLRH3-IFGRPL+MTSWRPL,255-RPLCMD-RPLCHREQ  NO CHG 89328
PUTLRCON TM    RPLRH3-IFGRPL+MTSWRPL,RPLBB  BEGIN BRACKET ?      89328
         B     PUTLPROM      YES; MUST AWAIT COMPLETION          89328
         UNPOST MTSWECB      CLEAR RESPONSE ECB                  89328
         L     R6,=A(XRITE)  GET THE WRITE COMPLETION EXIT       89328
         SEND  RPL=MTSWRPL,POST=SCHED,STYPE=REQ,CONTROL=DATA,    89328 *
               EXIT=(R6),AREA=(R4),RECLEN=(R5),RESPOND=(EX,FME,NRRN)
         CHRET RETRY=PUTLRCON  TEST FOR ACCEPTANCE               89328
         B     PUTLRRET                                          89328
PUTLPROM UNPOST MTSWECB      CLEAR RESPONSE ECB                  89328
         L     R6,=A(XRITE)  GET THE WRITE COMPLETION EXIT       89328
         SEND  RPL=MTSWRPL,POST=RESP,STYPE=REQ,CONTROL=DATA,     89328 *
               EXIT=(R6),AREA=(R4),RECLEN=(R5),RESPOND=(NEX,FME,NRRN)
         CHRET RETRY=PUTLPROM  TEST FOR ACCEPTANCE               89328
PUTLRRET WAIT  ECB=MTSWECB   WAIT FOR COMPLETION                 89328
         TM    MTSFLGT,MTSFBUG  DEBUG MODE ?                     89330
         BZ    PUTLPOST      NO                                  89330
         MVC   MTSPVLIN(4),MTSPH137  SET PRINT LINE LENGTH       89330
         MVI   MTSPVLIN+4,C' '  CLEAR PRINT LINE                 89330
         MVC   MTSPVLIN+5(132),MTSPVLIN+4                        89330
         MVC   MTSPFLIN+1(5),=C'PUTLN'                           89330
         STM   R4,R5,MTSDDB  STASH ADDRESS/LENGTH                89330
         UNPK  MTSPFLIN+7(9),MTSDDB(5)                           89330
         UNPK  MTSPFLIN+16(9),MTSDDB+4(5)  LENGTH                89330
         UNPK  MTSPFLIN+25(5),RPLRTNCD-IFGRPL+MTSWRPL(3)  RET.CD 89330
         UNPK  MTSPFLIN+29(5),RPLFDBK2-IFGRPL+MTSWRPL(3)  FEEDBK 89330
         TR    MTSPFLIN+7(9+9+8),MDBHEX-C'0'  MAKE PRINTABLE     89330
         MVI   MTSPFLIN+15,C' '                                  89330
         MVI   MTSPFLIN+24,C' '                                  89330
         MVI   MTSPFLIN+33,C' '                                  89330
         LTR   R15,R5        CHECK LENGTH                        89330
         BNP   PUTLDEB0                                          89330
         MIN   R15,=Y(130-34),TYPE=H  NOT TOO LONG               89330
         BCTR  R15,0                                             89330
         EX    R15,PUTLMVC   MOVE TEXT                           89330
PUTLMVC  MVC   MTSPFLIN+34(0),0(R4)  MOVE SOME TEXT              89330
PUTLDEB0 LA    R1,MTSPVLIN   POINT TO OUTPUT                     89330
         L     R15,=A(XPRNT)                                     89330
         BALR  R14,R15       PRINT THE LINE                      89330
         LTR   R0,R5         COPY AND CHECK LENGTH               89330
         BNP   PUTLPOST                                          89330
         LR    R1,R4         COPY START ADDRESS                  89330
         L     R15,=A(HEXPRINT)                                  89330
         BALR  R14,R15       CALL THE HEX DUMP ROUTINE           89330
PUTLPOST NI    RPLRH3-IFGRPL+MTSWRPL,255-RPLBB                   89328
         TM    MTSWECB,X'7F' NORMAL RETURN ?                     89328
         BO    PUTLOUCH      YES                                 89328
         LA    R8,16         SET (INCORRECT) ERROR CODE          89328
         SPACE 1                                                 89328
PUTLOUCH L     R1,0(,R13)    GET OLD SAVE AREA (OUR STYLE)       89328
         ST    R8,16(,R1)    PASS RETURN CODE                    89328
         NI    MTSFLGM,255-MTSFREAD-MTSFATTN  RESET ATTN, READ   91310
         XEXIT MODE=OS       RETURN TO CALLER                    89328
         EJECT ,                                                 89345
*        THIS EXIT IS INVOKED BY KERMIT PRIOR TO PARSING THE INPUT
*        BUFFER.  IT INTERCEPTS FUNCTIONS DONE HERE, AND SOME DONE IN
*        BOTH (E.G., SIGNOFF).                                   89345
*        ENTRY:  R0/R1  LENGTH/ADDRESS OF INPUT                  89345
*              (MOVED TO R5/R6 BY XHEAD MACRO)                   89345
*        RETURN: R15 =0 PROCESS  =4 SKIP AND READ AGAIN   =8 QUIT
*                                                                89345
COMMANDS XHEAD SAVE=RENT,EXH=NO,RPL=NO,MODE=OS                   89345
         L     R7,PSATOLD-PSA  GET CURRENT TCB                   89345
         L     R7,TCBFSA-TCB(,R7)  LOAD THE SAVE AREA            89345
         L     R7,24(,R7)    LOAD ATTACH'S R1                    89345
         SH    R7,=Y(MTS@PARM-MTSWORK)  GET WORK AREA BACK       89345
         USING MTSWORK,R7    DECLARE IT                          89345
         SWAPR R5,R6         EXCHANGE LENGTH AND ADDRESS         89345
         AR    R6,R5         ADD ADDRESS TO LENGTH               89345
         BCTR  R6,0          LAST BYTE                           89345
         TM    MTSFLGT,MTSFGONE+MTSFXEND+MTSFGOIN  QUIT ?        90231
         BNZ   COMMQUIT      YES                                 90231
         SLR   R8,R8         PRESET FOR KERMIT EXECUTION OF COMMAND
         LA    R4,COMMCMDS   GET CRT COMMAND TABLE               89345
         TM    MTSSIMFG,MTSSICRT  CRT MODE ?                     89328
         BNZ   *+8           YES
         LA    R4,COMMCMTY   INCLUDE TTY COMMAND
&ZZ@BLUK SETB  0             REQUEST LOCAL EXPANSION OF 'BLOOK' CODE
         BLOOK T=(R4),B=(R12),R=R6  SCAN INPUT                   89345
         B     COMMOUCH      RETURN WITH R15=0 (PROCESS COMMAND) 89345
*    CHANGE OF PRINT STATUS REQUESTED
COMMQPRT TM    MTSSIMFG,MTSSINPR    PRINTER DISABLED ?           89328
         BNZ   COMMNPRT      YES; ISSUE MESSAGE                  89345
         BLOOK T=COMMCMPR,B=(R12),R=R6 FOR ON OR OFF             89345
         B     COMMOUCH      NEITHER ON NOR OFF - LET CALLER DO IT
COMMPRTR OI    MTSFLGT,MTSFBUG  SET TRACE MODE
COMMPRON CLI   MTSPRDD,C' '  ALLOCATION PERFORMED ?              87032
         BH    COMMPROQ      YES; SEE IF OPEN                    87032
         BAL   R9,SOUTALOC   ALLOCATE A SYSOUT DD (DDNM=>MTSPRDD)
         BXH   R15,R15,COMMNPRT  ALLOCATION FAILED               87032
         MVC   MTSPDCB+48(4),PATPDCB+48  OVERLAY XPRRET          87032
         MVC   DCBDDNAM+MTSPDCB-IHADCB,MTSPRDD  MAKE DCB DDNM    87032
         DEVTYPE MTSPRDD,MTSDDB  SEE IF EXHPRINT SUPPLIED        87032
         BXH   R15,R15,COMMNPRT  ALLOC OR PGM ERROR              87032
COMMPROQ TM    MTSSIMFG,MTSSINPR  PRINTER AVAILABLE ?            89328
         BNZ   COMMNPRT      NO                                  87032
         TM    MTSPDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPEN NOW ?      87032
         BNZ   COMMPRQO      OK                                  87032
*        OPEN  (MTSPDCB,(OUTPUT)),MF=(E,MTSDDB)                  87032
         LA    R1,MTSPDCB     GET DCB ADDRESS                    87032
         ST@   R1,MTSDDB,MVI=X'8F'  MAKE OPEN,OUTPUT LIST        89275
         OPEN  ,MF=(E,MTSDDB)       OPEN IT                      87032
         TM    MTSPDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPEN NOW ?      87032
         BNZ   COMMPRQO      OK                                  87032
COMMNPRT L     R1,=A(XPRRET)    MAKE DUMMY PUT ROUTINE ADDRESS   87032
         ST    R1,MTSPDCB+48      MAKE NOPS                      87032
         OI    MTSSIMFG,MTSSINPR  PRINT NO LONGER AVAILABLE      89328
         NI    MTSFLGT,255-MTSFBUG  RESET TRACE MODE             87032
         NI    MTSSIMFG,255-MTSSIPRT  CANCEL PRINT REQUEST       89328
         LA    R2,COMMNOMG   POINT TO MESSAGE
         B     COMMSKIP      ISSUE 'NO PRINTER' MESSAGE
COMMPRQO OI    MTSSIMFG,MTSSIPRT    INDICATE PRINT REQUEST       89328
COMMSGOK LA    R2,COMMOKMG     GET OK MESSAGE
COMMSKIP LA    R8,4          SET FOR COMMAND PROCESSED HERE      89345
         XC    MTSUCBX(6*4),MTSUCBX  BUILD FAKE IOPL             89345
         ST    R2,MTSUCBX+5*4  MAKE LINE ADDRESS                 89345
         LA    R0,MTSUCBX+4*4  MAKE POINTER                      89345
         ST    R0,MTSUCBX+3*4  MAKE POINTER POINTER              89345
         LA    R1,MTSUCBX    POINT TO 'IOPL'                     89345
         L     R15,MTSPUTLN  GET PUT ADDRESS                     89345
         BALR  R14,R15       DISPLAY IT                          89345
         CH    R15,=H'4'     ERROR ?                             89345
         BH    COMMQUIT      YES; QUIT                           89345
         B     COMMOUCH      PUT IT OUT AND GET MORE INPUT       89345
COMMPROF NI    MTSSIMFG,255-MTSSIPRT    TURN PRINTING OFF        89328
         B     COMMSGOK      GO TO OK MESSAGE
COMMQTTY BLOOK T=COMMCMTS,B=(R12),R=R6   TTY SUB-COMMANDS        89345
         CLI   0(R5),C' '    ANY OPERAND ?
         BNE   COMMOUCH      YES; LET IT FAIL                    89345
         LA    R2,MSGTTYON
         TM    MTSTRMFG,MTSFAUCR  BUT IS IT ?
         BNZ   COMMSKIP      YES; RETURN WITH MESSAGE
         LA    R2,MSGTTYOF   AUTOCR OFF
         B     COMMSKIP      NOT ON; RETURN
COMMYAUT OI    MTSTRMFG,MTSFAUCR  SET AUTO CR
         B     COMMSGOK      SET OK
COMMYNAC NI    MTSTRMFG,255-MTSFAUCR  RESET AUTO CR
         B     COMMSGOK
COMMYAU2 OI    MTSTRMFG,MTSFAUCR  SET BOTH AUTO CR AND AUTO LF
COMMYAUL OI    MTSTRMFG,MTSFAULF  SET AUTO LF
         B     COMMSGOK      SET OK
COMMYNAL NI    MTSTRMFG,255-MTSFAULF  RESET AUTO LF
         B     COMMSGOK
COMMYNAU NI    MTSTRMFG,255-MTSFAUCR-MTSFAULF  RESET ALL
         B     COMMSGOK
         SPACE 1
COMMDISC L     R2,MTSLOUD    USER WISHES SESSION TO BE DISCONNECTED
         AIF   ('&LOCAL' NE 'PID').GODISCN                       93263
         USING A$LSECT,R2    DECLARE IT                          89345
         MODESET KEY=ZERO    GET PRIVILEGED                      89345
         OI    A$LFLG2,A$LFLOG  DISCONNECT THIS DEVICE           89345
         MODESET KEY=NZERO   RESTORE                             89345
.GODISCN ANOP  ,                                                 90186
COMMLOFF XC    MTSSTADB,MTSSTADB  CLEAR FOR CLSDST               89345
         OI    MTSFLGT,MTSFXEND REQUEST TERMINATION              89345
COMMQUIT LA    R8,8          RETURN AND QUIT                     89345
         SPACE 1                                                 89345
COMMOUCH L     R1,0(,R13)    GET OLD SAVE AREA                   89345
         ST    R8,16(,R1)    SET R15 RETURN VALUE                89345
         XEXIT MODE=OS       RETURN TO KERMIT                    89345
         SPACE 1
COMMCMTY BTAB  'TTY',COMMQTTY,BASE=COMMANDS  TTY OPTIONS         89345
COMMCMDS BTAB  'LOGOFF',COMMLOFF  TERMINATE SESSION
         BTAB  'SIGNOFF',COMMDISC  TERMINATE CONNECTION          90008
         BTAB  'BYE',COMMDISC   TERMINATE CONNECTION             90008
         BTAB  'EXIT',COMMLOFF    ALTERNATE FORM
         BTAB  'PRINT ',COMMQPRT  PRINT REQUEST
         BTAB  'PRI ',COMMQPRT  PRINT REQUEST
         BTAB  *END
COMMCMPR BTAB  'ON',COMMPRON  SET PRINT ON
         BTAB  'OFF',COMMPROF   SET OFF
         BTAB  'TRACE',COMMPRTR  TRACE MODE
         BTAB  *END
COMMCMTS BTAB  'AUTOCR',COMMYAUT  SET AUTOCR
         BTAB  'AUTOLF',COMMYAUL  SET AUTOLF
         BTAB  'AUTO',COMMYAU2  BOTH
         BTAB  'NOAUTOCR',COMMYNAC  OFF
         BTAB  'NOAUTOLF',COMMYNAL  OFF
         BTAB  'NOAUTO',COMMYNAU
         BTAB  *END
COMMOKMG VCON  ' OK  '       PRINTER STARTED                     89345
COMMNOMG VCON  'No printer '  UNAVAILABLE MSG TEXT               89345
MSGTTYON VCON  'TTY AUTOCR set '                                 89345
MSGTTYOF VCON  'TTY no AUTOCR '                                  89345
         EJECT ,
         DROP  ,
         USING COMP3270,RBASE       REGISTER 15 USED AS PROGRAM BASE
         USING MTSWORK,R7    PASSED BY CALLER
EXWCOLMP DS    0F            DUMMY FUNCTION FOR NOW              89328
COMP3270 STM   0,14,MTSSAVER+4  SAVE INPUT REGISTERS
*
*        PTF 75283 PROVIDES FOR SHORTER AND FEWER INPUT BUFFERS
*        BY FORCING A PROTECTED FIELD AT FIRST BLANK AFTER LOGICAL
*        INPUT AREA (THIRD LINE).
         OI    MTSFLGM,MTSFNCOL  NO COLOR CONVERSION YET         89328
         OI    MTSFLGM,MTSFFSF   REQUEST FAKE SF ADDITION
         LH    RSIZE,MTSBUFSZ  CRT SCREEN SIZE                   89328
         LM    RIN,RLEN,0(R1) LOAD BUFFER ADDRESSES AND SIZE PARM
         USING INPUT,RIN     DECLARE 'INPUT' MAPPING
         USING OUTPUT,ROUT   AND 'OUTPUT' MAPPING
*                     BUILD CONSTANTS
         LA    RON,1         PLACE CONSTANT ONE INTO A REGISTER
         LA    RTHREE,3      CONSTANT THREE
         LA    RFOUR,4       CONSTANT FOUR
         LA    RMASK,X'3F'   CURSOR ADDRESS MASK
*
         AR    RLEN,RIN      GET LAST INPUT ADDRESS + 1
*
         MVC   OUTPUT,INPUT  COPY PRESUMED WCC TO OUTPUT
         AR    RIN,RON       UP INPUT ADDRESS
         AR    ROUT,RON      UP OUTPUT ADDRESS
         TM    MTSFLGM,MTSFNCMP  BYPASS COMPRESSION ?
         BNZ   COMPNCMP      YES; JUST COPY AS IS
         CLI   INPUT,SBA     VALIDITY CHECK FOR ONLY REQUIRED START
         BE    COMPSBA       NEED INITIAL SBA FOR BUFFER LOGIC
COMPNCMP LR    R8,RIN        COPY INPUT ADDRESS
         LR    R0,ROUT       COPY OUTPUT ADDRESS
         SR    RLEN,RIN      GET LENGTH TO MOVE
         LR    R9,RLEN
         LR    R1,RLEN
         AR    ROUT,RLEN     SET POSITION AFTER MOVE
         MVCL  R0,R8         COPY AS IS
         B     EXIT          AND RETURN
         SPACE 1
COMPSBA  BAL   RET,GETSBA    GET CURRENT BUFFER ADDRESS FROM SBA ORDER
         AR    RIN,RTHREE    POINT TO NEXT INPUT BYTE
         SPACE 2
LOOPSBA  CR    RIN,RLEN      END OF INPUT ?
         BNL   LEAVESBA      YES - MAKE TRAILER
         CLI   INPUT,SBA     IS IT SBA FOLLOWED BY ANOTHER SBA ?
         BE    COMPSBA       YES - CONSOLIDATE INTO SINGLE SBA
         SPACE 1
LEAVESBA BAL   RET,SETSBA    PLACE NEW SBA ORDER INTO THE OUTPUT BUFFER
         SPACE 3
LOOPDATA CR    RIN,RLEN      END OF INPUT ?
         BNL   EXIT          YES - GET OUT
         LR    RET,R2        SAVE OVER TRT
         SLR   R2,R2         CLEAR FOR TRT'S IC
         TRT   0(1,RET),ORDERS  GET BRANCH OFFSET FOR THIS BYTE
         XR    R2,RET
         XR    RET,R2
         XR    R2,RET        RESTORE R2
         B     *+4(RET)      BRANCH BY TYPE
         B     COMPTBLK      CHECK FOR BLANK
         B     MOVEDATA      UNPRINTABLE CHARACTER
         B     COMPPT        PROGRAM TAB
         B     COMPSBA       SET BUFFER ADDRESS
         B     COMPIC        SET CURSOR
         B     COMPEUA       ERASE UNPROTECTED
         B     COMPSF        START FIELD
         B     COMPSA        SET ATTRIBUTE
         B     COMPSFE       START FIELD EXTENDED
         B     COMPMF        MODIFY FIELD
         B     COMPRA        REPEAT TO ADDRESS
         SPACE 1
COMPTBLK CLI   INPUT,BLANK   BLANK OR DATA ?
         BNE   MOVEDATA      NOT BLANK; PROCESS DATA BYTE
         TM    MTSFLGM,MTSFFSF    FALSE SF TO BE INSERTED ?
         BZ    MOVEDATA      NO; PROPAGATE BLANK
         CH    RBUF,FSFAD    REACHED THIRD LINE YET ?
         BL    MOVEDATA      NO
         MVI   OUTPUT,SF     YES - MAKE SF FIELD INSTEAD OF BLANK
         MVI   OUTPUT+1,X'60'  MAKE PLAIN OLD PROTECTED FIELD
         TM    MTSFLGM,MTSFNCOL  INHIBIT COLOR MAPPING ?         87172
         BNZ   COMPTBLX      YES; SKIP AROUND                    87172
         MVC   OUTPUT(6),=X'2902C0604220'  SET DEFAULT PROT. FLD 87172
         L     ROD,EXWCOLMP  GET COLOR TABLES                    87172
         TR    OUTPUT+5(1),0(ROD)  MAKE FIELD COLOR              87172
         NI    OUTPUT+5,X'07'  KILL ZONE BITS                    87172
         TR    OUTPUT+5(1),64(ROD)  GET REPLACEMENT COLOR        87172
         AR    ROUT,RFOUR    ADVANCE OUTPUT POINTER              87172
COMPTBLX LA    ROUT,2(,ROUT) SET NEXT OUTPUT LOCATION
         AR    RIN,RON       NEXT INPUT
         AR    RBUF,RON      SET NEW BUFFER ADDRESS
         NI    MTSFLGM,255-MTSFFSF   TURN FAKE SF OFF
         B     LOOPDATA      LOOK FOR MORE DATA
         SPACE 1
COMPSF   TM    MTSFLGM,MTSFFSF  FAKE SF ACTIVATED ?
         BZ    COMPSFNS      NO
         CH    RBUF,=H'80'   ON SECOND LINE ?
         BL    COMPSFNS      NO
         CH    RBUF,FSFAD    OR PAST IT ?
         BH    COMPSFNS
         NI    MTSFLGM,255-MTSFFSF KLUDGE FOR FULL-SCREEN I/O
COMPSFNS MVC   OUTPUT(2),INPUT         COPY TO OUTPUT BUFFER
         TM    MTSFLGM,MTSFNCOL  INHIBIT COLOR MAPPING ?         87172
         BNZ   COMPSFNX      YES; SKIP AROUND                    87172
         TM    1+INPUT,X'0C' NON-DISPLAY FIELD ?
         BO    COMPSFNX      YES; NO COLOR                       87172
         MVC   OUTPUT(5),=X'2902C0604220'  SET DEFAULT PROT. FLD 87172
         MVC   OUTPUT+3(1),1+INPUT  GET INPUT ATTRIBUTE
         MVC   OUTPUT+5(1),1+INPUT
         NI    OUTPUT+5,X'3F'  KILL ZONES                        87172
         L     ROD,EXWCOLMP  GET COLOR TABLES                    87172
         TR    OUTPUT+5(1),0(ROD)  MAKE FIELD COLOR              87172
         NI    OUTPUT+5,X'07'  KILL ZONE BITS                    87172
         TR    OUTPUT+5(1),64(ROD)  GET REPLACEMENT COLOR        87172
         AR    ROUT,RFOUR    ADVANCE OUTPUT POINTER              87172
COMPSFNX LA    RIN,2(,RIN)   SKIP OVER INPUT BYTES
         LA    ROUT,2(,ROUT) AND OVER OUTPUT
         AR    RBUF,RON      BUT BUFFER ONLY INCREASES BY ONE BYTE
         B     LOOPDATA      PROCESS NEXT INPUT BYTE
         SPACE 1
COMPMF   DS    0H
COMPSFE  SLR   ROD,ROD
         IC    ROD,1+INPUT  GET LENGTH BYTE
         LR    REV,ROD       SAVE REPEAT COUNT                   87172
         LA    ROD,1(ROD,ROD)  EXECUTE LENGTH OF ORDER
         EX    ROD,INMVCOUT   MOVE THE ORDER
         TM    MTSFLGM,MTSFNCOL  SKIP CONVERSION ?               87172
         BNZ   COMPSFEX      YES                                 87172
         LTR   REV,REV       ANY AT ALL ?                        87172
         BNP   COMPSFEC      NO; TACK ON A COLOR ORDER           87172
         LR    ROD,ROUT      COPY STARTING ADDRESS               87172
COMPSFEL CLI   2(ROD),X'42'  COLOR ORDER ?                       87172
         BE    COMPSFET      YES; JUST TRANSLATE                 87172
         LA    ROD,2(,ROD)                                       87172
         BCT   REV,COMPSFEL  NO COLOR ORDER                      87172
         IC    REV,1+INPUT   GET COUNT BACK
         LR    ROD,ROUT      GET START AGAIN                     87172
COMPSFEN CLI   2(ROD),X'C0'  ATTRIBUTE ?                         87172
         BE    COMPSFEA      YES; USE IT FOR COLOR               87172
         LA    ROD,2(,ROD)                                       87172
         BCT   REV,COMPSFEN  TRY AGAIN                           87172
COMPSFEC IC    REV,1+INPUT   REGAIN INPUT COUNT
         LR    ROD,REV       COPY                                87172
         LA    ROD,1(ROD,ROD)  L'1                               87172
         LA    ROD,1(ROD,ROUT) NEXT OUTPUT BUFFER POSITION       87172
         MVI   1(ROD),X'20'  SET DEFAULT ATTRIBUTE               87172
         B     COMPSFEB                                          87172
COMPSFEA TM    3(ROD),X'0C'  NON-DISPLAY FIELD ?                 87172
         BO    COMPSFEZ      YES; NO CHANGE                      87172
         IC    RFOUR,3(,ROD) SAVE ATTRIBUTE                      87172
         IC    REV,1+INPUT   REGAIN INPUT COUNT
         LR    ROD,REV       COPY                                87172
         LA    ROD,1(ROD,ROD)  GET EXECUTE LENGTH                87172
         LA    ROD,1(ROD,ROUT) NEXT OUTPUT POSITION              87172
         STC   RFOUR,1(,ROD)                                     87172
         NI    1(ROD),X'3F'  MASK                                87172
COMPSFEB L     RFOUR,EXWCOLMP                                    87172
         TR    1(1,ROD),0(RFOUR) GET ATTRIBUTE COLOR             87172
         NI    1(ROD),X'07'  MASK                                87172
         TR    1(1,ROD),64(RFOUR)  GET MAPPED COLOR              87172
         MVI   0(ROD),X'42'  MAKE COLOR                          87172
         AR    REV,RON                                           87172
         STC   REV,1+OUTPUT                                      87172
         B     COMPSFEZ      COMMON                              87172
COMPSFET L     RFOUR,EXWCOLMP                                    87172
         NI    3(ROD),X'07'                                      87172
         TR    3(1,ROD),64(RFOUR)  MAP COLOR                     87172
COMPSFEZ LA    RFOUR,4                                           87172
         IC    REV,1+OUTPUT                                      87172
         LR    ROD,REV                                           87172
         LA    ROD,1(ROD,ROD)                                    87172
COMPSFEX AR    ROD,RON       MAKE TRUE LENGTH
         AR    RBUF,RON      INCREMENT BUFFER BY ATTRIBUTE BYTE
         B     COMROD        GO TO COMMON BUMPER
         SPACE 1
COMPPT   DS    0H            INCORRECT TAB PROCESSING
COMPIC   MVC   OUTPUT,INPUT  COPY IC ORDER
         LR    ROD,RON       SET TO UP INPUT/OUTPUT BY ONE; BUFFER
         B     COMROD        ADDRESS IS UNCHANGED
         SPACE 1
COMPSA   MVC   OUTPUT(3),INPUT  MOVE SA ORDER
         LR    ROD,RTHREE    SET INCREMENT
         TM    MTSFLGM,MTSFNCOL  SUPPRESS COLOR MAPPING ?        87172
         BNZ   COMROD        YES                                 87172
         CLI   1+OUTPUT,X'42'  COLOR ORDER ?                     87326
         BNE   COMROD        NO; LEAVE AS IS                     87326
         L     ROD,EXWCOLMP  GET TRANSLATION TABLE               87172
         NI    2+OUTPUT,X'07'                                    87172
         TR    2+OUTPUT(1),64(ROD) MAP COLOR                     87172
         LR    ROD,RTHREE    RESTORE SEQUENCE LENGTH             87172
         B     COMROD
         SPACE 1
COMPRA   MVC   OUTPUT(4),INPUT     COPY RA ORDER
         BAL   RET,GETSBA    SET NEW BUFFER ADDRESS INTO RBUF
         LR    ROD,RFOUR     UP IN/OUT BY FOUR BYTES
         B     COMROD        GO TO INCREASER
         SPACE 1
COMPEUA  MVC   OUTPUT(3),INPUT   COPY EUA ORDER
         BAL   RET,GETSBA    SET NEW BUFFER ADDRESS INTO RBUF
         LR    ROD,RTHREE    INCREASE BY 3
         B     COMROD        GO TO UP IN/OUT
         SPACE 1
MOVEDATA CLC   INPUT+1(4),INPUT  FIVE OR MORE REPETITIONS ?
         BE    MAKERTA       YES - CONVERT TO RTA ORDER
INMVCOUT MVC   OUTPUT,INPUT  ELSE COPY ONE BYTE
         AR    RBUF,RON      INCREASE BUFFER POSITION
         LR    ROD,RON       SET INCREMENT FOR IN/OUT
         SPACE 1
COMROD   AR    RIN,ROD       INCREASE INPUT ADDRESS
         AR    ROUT,ROD      AND OUTPUT ADDRESS
         B     LOOPDATA      GO TO LOOK FOR MORE INPUT
         SPACE 1
BADEXIT  L     R1,MTSSAVER+8    RESTORE CALLER'S R1
         L     ROUT,4(,R1)   FORCE ZERO LENGTH ON ERROR
*        B     EXIT
EXIT     L     R1,MTSSAVER+8  GET ADDRESS TO STORE LENGTH AT
         S     ROUT,4(,R1)   GET CORRECT BUFFER LENGTH (OR 0)
         ST    ROUT,8(,R1)   GIVE TO USER
         LM    R0,R14,MTSSAVER+4  RELOAD ORIGINAL REGISTERS
         SLR   R15,R15       CLEAR RETURN CODE
         BR    R14           RETURN TO CALLER
         SPACE 1
MAKERTA  MVC   OUTPUT,INPUT  SAVE THE DATA BYTE SOMEWHERE
         SPACE 1
MAKERTAL AR    RIN,RON       GET NEXT INPUT BYTE
         AR    RBUF,RON      SET BUFFER ADDRESS UP TO PREVIOUS ONE
         CR    RIN,RLEN      END OF BUFFER ?
         BNL   MAKERTAX      YES - TERMINATE
         CLC   INPUT,OUTPUT  SAME CHARACTER AGAIN ?
         BNE   MAKERTAX      FORCE EXPANSION
         TM    MTSFLGM,MTSFFSF  FAKE SF TO BE DONE ?
         BZ    MAKERTAL      NO; LOOP SOME MORE
         CH    RBUF,FSFAD    REACHED THIRD LINE YET ?
         BL    MAKERTAL      NO
         CLI   INPUT,BLANK   DOING BLANKS ?
         BNE   MAKERTAL      NO, CONTINUE
         SPACE 1
MAKERTAX IC    RFOUR,OUTPUT   SAVE THE REPEATED BYTE
         MVI   OUTPUT,RTA    PLACE THE RTA ORDER
         BAL   RET,SETBA     BUILD THE BUFFER ADDRESS
         STC   RFOUR,OUTPUT   ADD THE REPEATED CHARACTER TO THE ORDER
         LA    RFOUR,4       RELOAD CONSTANT
         AR    ROUT,RON      CORRECT THE OUTPUT ADDRESS
         B     LOOPDATA      PROCESS NEXT INPUT BYTE
         SPACE 2
GETSBA   SLR   RBUF,RBUF     CLEAR THE NEW BUFFER ADDRESS
         TM    INPUT+1,X'C0'  12 OR 14 BIT ADDRESS ?
         BNZ   GETSBA12      12-BIT                              87154
         ICM   RBUF,3,INPUT+1  GET 14-BIT ADDRESS
         BR    RET           RETURN                              87154
GETSBA12 LR    ROD,RBUF      CLEAR THE WORKING REGISTER
         IC    RBUF,INPUT+1  GET HIGH BYTE OF BUFFER ADDRESS
         IC    ROD,INPUT+2   AND LOW BYTE
         NR    RBUF,RMASK    STRIP HIGH BITS OFF
         NR    ROD,RMASK               BOTH BYTES
         SLL   RBUF,6        SHIFT HIGH BYTE *64
         AR    RBUF,ROD      SET NEW BUFFER ADDRESS
         BR    RET           RETURN TO CALLER
         SPACE 2
SETSBA   MVI   OUTPUT,SBA    PREPARE AN SBA ORDER
         SPACE 1
SETBA    LR    ROD,RBUF      BUILD A BUFFER ADDRESS SEQUENCE
         SLR   REV,REV       CLEAR EVEN REGISTER FOR DIVIDE
         DR    REV,RSIZE     ONLY REMAINDER IN REV COUNTS
         CH    REV,=H'4096'  DOES 12-BIT ADDRESS SUFFICE ?       87154
         BL    SETBA12       YES                                 87154
         STCM  REV,3,OUTPUT+1  NO; STASH AS IS                   87154
         B     SETBACOM                                          87154
SETBA12  SRDL  REV,6         PLACE HIGH BUFFER BYTE IN REV
         SRL   ROD,26                  AND LOW BYTE INTO ROD
         STC   REV,OUTPUT+1  PLACE INTO OUTPUT BUFFER
         STC   ROD,OUTPUT+2
         TR    OUTPUT+1(2),EBCDIC      CONVERT TO CORRECT EBCDIC
SETBACOM AR    ROUT,RTHREE   SET NEW OUTPUT LOCATION
         BR    RET           RETURN TO CALLER
         DROP  RBASE
         SPACE 2
MOVER    MVC   OUTPUT+1(0),OUTPUT      SLIDING MOVE - SUBJECT OF EX
FSFAD    DC    H'160'        POSITION FOR FAKE SF INSERT
         SPACE 1
         LTORG ,
         EJECT ,
*
*      THIS ROUTINE PRODUCES PRINTED OUTPUT SIMULATING THE SCREEN
*        ASSUMES THAT PROGRAM IS WELL-BEHAVED
*      INPUT :  R2 - ADDRESS OF PHYSICAL WRITE BUFFER (WITH CONTROL CH)
*                            FOR WRITE; ELSE ANY AREA
*               R3 - LENGTH TO BE PROCESSED
*              MTSFLGM - FLAG OPTIONS CONTROLLING PROCESSING
*              MTSPPARM - CL8' '  DUMP TITLE
*
         SPACE 1
         PUSH  USING         RESTORE AFTER XPRINT
         USING MTSWORK,R7
BUFPRINT TM    MTSSIMFG,MTSSIPRT  PRINT REQUESTED ?              89328
         BZR   R14           NO; EXIT
EXCPRINT TM    MTSSIMFG,MTSSINPR  AVAILABLE ?                    89328
         BNZR  R14           NO; IGNORE REQUEST
         STM   R14,R12,MTSSAVER   SAVE REGS JUST IN CASE
         BALR  R12,0         MAKE LOCAL BASE
         USING *,R12         AND DECLARE IT (WATCH XPRNT)
         LTR   R3,R3         ANY LENGTH AT ALL ?
         BNP   BUFPREX
         SPACE 2
         LR    R4,R2         COPY OVER (R2 USED BY TRT)
         MVI   MTSPFLIN,C' '
         MVC   MTSPFLIN+1(L'MTSPFLIN-1),MTSPFLIN  CLEAR OUTPUT LINES
         MVC   MTSPFLN2,MTSPFLIN
         TM    MTSFLGM,MTSFREAD    WRITE MODE ?
         BNZ   BUFFROOM      NO; CALCULATE PRINT LINES NEEDED
         XC    MTSPLINE,MTSPLINE  CLEAR INITIAL OFFSET
         XC    MTSCURSA,MTSCURSA  CLEAR CURSOR ADDRESS
         SH    R3,=H'1'      ALLOW FOR WCC
         BNP   BUFPREX       NO TEXT ?
         LA    R4,1(,R4)     SKIP WCC/LNE CONTROL CHARACTER
         LA    R14,MTSBUFF    GET SIMULATION BUFFER
         LA    R15,=A(MTSBUFLN+L'MTSBUFND)                       93263
         SLR   R0,R0
         SLR   R1,R1
         ICM   R1,8,XPCC     LOAD A BLANK FOR PADDING
         MVCL  R14,R0        CLEAR THE BUFFER
         LA    R14,MTSBUFF    GET THE BUFFER ADDRESS AGAIN
         AH    R14,MTSBUFSZ    GET LOGICAL END                   89328
         ST    R14,MTSBFEND   SET FOR LATER WRAP-AROUND TEST
         SPACE 1
BUFFWLOP LTR   R3,R3         ANYTHING LEFT TO DO ?
         BNP   BUFFCURS      NO; PROCESS SCREEN
         LH    R14,MTSBUFSZ    GET BUFFER SIZE                   89328
         SH    R14,MTSPLINE  LESS CURRENT POSITION = SIZE TO END
         CR    R14,R3        LARGER THAN SIZE LEFT ?
         BNH   *+6           NO
         LR    R14,R3        USE AVAILABLE SIZE ONLY
         LA    R0,256        MAX ALLOWED ON TRT INSTRUCTION
         CR    R14,R0        TOO LONG ?
         BNH   *+6           NO
         LR    R14,R0        SET MAX ONLY
         LR    R1,R4         COPY START ADDRESS
         SLR   R2,R2         CLEAR FOR TRT'S IC
         AR    R1,R14        SET LAST BYTE+1 OF SUCCESSFUL TRT
         BCTR  R14,0         MAKE EXECUTE LENGTH
         L     R15,=A(ORDERS)  GET TRT TABLE ADDRESS
         EX    R14,TEXTTRT   GET TYPE
         LR    R15,R1        GET STOP BYTE
         SR    R15,R4        GET NUMBER OF VALID BYTES
         BNP   BUFFNMOV      NOTHING TO MOVE
         SR    R3,R15        SET RESIDUAL LENGTH AFTER MOVE
         LA    R14,MTSBUFF   GET BUFFER START
         LH    R0,MTSPLINE   GET BUFFER OFFSET
         AR    R14,R0        GET CURRENT START
         AR    R0,R15        NEW BUFFER OFFSET
         BCTR  R15,0
         EX    R15,TEXTMVC   MOVE TEXT TO BUFFER
         LA    R4,1(R4,R15)  SKIP DATA BYTES
         CH    R0,MTSBUFSZ     BUFFER FULL ?                     89328
         BL    *+6           NO
         SLR   R0,R0         MAKE IT WRAP AROUND
         STH   R0,MTSPLINE   SAVE NEW OFFSET
BUFFNMOV LTR   R2,R2         WAS THIS A TEXT MOVE ONLY ?
         BZ    BUFFWLOP      DO AGAIN; AND CHECK BUFFER END
         LA    R14,MTSBUFF   START OF BUFFER
         LH    R0,MTSPLINE   CURRENT LOGICAL OFFSET
         AR    R14,R0        NEXT BYTE
BUFFWRAP C     R14,MTSBFEND   AT BUFFER END ?
         BL    OFFBASE       NO
         SH    R14,MTSBUFSZ    WRAP-AROUND                       89328
         B     BUFFWRAP      CHECK AGAIN
OFFBASE  B     *(R2)         BRANCH BY FUNCTION
         OFFBR BUFFBAD,BAD   UNPRINTABLE CHARACTER
         OFFBR BUFFPT,PT     PROGRAM TAB
         OFFBR BUFFSBA,SBA   SET BUFFER ADDRESS
         OFFBR BUFFIC,IC     SKIP
         OFFBR BUFFEUA,EUA   ERASE UNPROTECTED TO ADDRESS
         OFFBR BUFFSF,SF     START FIELD
         OFFBR BUFFSA,SA     SET ATTRIBUTE
         OFFBR BUFFSFE,SFE   START FIELD EXTENDED
         OFFBR BUFFMF,MF     MODIFY FIELD
         OFFBR BUFFRA,RTA    REPEAT TO ADDRESS
         SPACE 1
BUFFPT   LA    R2,1          SKIP ONE BYTE - IGNORE PT REQUEST
         B     BUFFBUMP
BUFFRA   LA    R2,4          REPEAT TO ADDRESS - 4 BYTE SEQUENCE
         B     BUFFSAD       SET NEW ADDRESS
BUFFEUA  DS    0H            ERASE UNPROTECTED - TREAT AS NO-OP
BUFFSBA  LA    R2,3          SET BUFFER ADDRESS
BUFFSAD  TM    1(R4),X'C0'   12 OR 14-BIT ADDRESS ?              87154
         BNZ   BUFFSAD2      12                                  87154
         SLR   R0,R0                                             87154
         ICM   R0,3,1(R4)    PROCESS 14-BIT                      87154
         B     BUFFWSTB                                          87154
BUFFSAD2 LA    R15,X'3F'
         IC    R0,1(,R4)     FIRST ADDRESS BYTE
         NR    R0,R15
*WHY?*   LA    R15,X'3F'
         IC    R1,2(,R4)     SECOND ADDRESS BYTE
         NR    R1,R15
         SLL   R0,6
         OR    R0,R1         MAKE FULL BUFFER ADDRESS
BUFFWSTB CH    R0,MTSBUFSZ     EXCEEDS SIZE OF SCREEN ?          89328
         BL    BUFFWSTC      NO, USE IT
         SH    R0,MTSBUFSZ     TRY AGAIN                         89328
         B     BUFFWSTB      WITH REDUCED VALUE
BUFFWSTC STH   R0,MTSPLINE   SAVE IT
         B     BUFFBUMP      AND SKIP THIS ORDER SEQUENCE
         SPACE 1
BUFFIC   ST    R14,MTSCURSA  SET CURSOR POSITION
         LA    R2,1          SET ORDER LENGTH
         B     BUFFBUMP      INCREMENT BUFFER
         SPACE 1
BUFFSF   OI    MTSFLGM,MTSFSFO   SHOW SF FOUND
         MVC   0(1,R14),1(R4)  COPY SF OPTION BYTE               87165
         NI    0(R14),X'3D'  MASK PERMITTED OPTIONS              87165
         LA    R2,2          SET LENGTH OF ORDER
         B     BUFFADV1      GO TO INCREMENT
BUFFBAD  MVI   0(R14),C'.'   REPLACE GARBAGE CHARACTER
         LA    R2,1          SET 'ORDER' LENGTH
         B     BUFFADV1      JUST INCREMENT
         SPACE 1
BUFFSA   LA    R2,3          SET ATTRIBUTE LENGTH
         B     BUFFBUMP      NO ANALYSIS AT PRESENT
         SPACE 1
BUFFMF   DS    0H            MODIFY FIELD
BUFFSFE  OI    MTSFLGM,MTSFSFO  SHOW SF FOUND                    87165
         MVI   0(R14),X'00'  SET FOR LATER FLAGGING              87165
         IC    R2,1(,R4)     NUMBER OF OPERAND PAIRS
         LA    R2,2(R2,R2)   ORDER LENGTH
         SLR   R15,R15                                           87165
         ICM   R15,1,1(R4)   GET NUMBER OF OPERAND PAIRS         87165
         BZ    BUFFADV1      NONE                                87165
         LR    R1,R4         START OF SEQUENCE                   87165
BUFFSFEL CLI   2(R1),X'C0'   ATTRIBUTE DEFINITION ?              87165
         BNE   BUFFSFEM      NO                                  87165
         MVC   0(1,R14),3(R1)  COPY ATTRIBUTE                    87165
         NI    0(R14),X'3D'  MASK SUPPORTED OPTIONS              87165
BUFFSFEM LA    R1,2(,R1)     BUMP                                87165
         BCT   R15,BUFFSFEL  TRY AGAIN                           87165
*        B     BUFFADV1      BUMP ADDRESS BY ONE
         SPACE 1
BUFFADV1 LA    R1,1
         AR    R1,R0         GET NEXT BUFFER POSITION
         CH    R1,MTSBUFSZ     REACHED END OF BUFFER ?           89328
         BL    *+6           NO
         SLR   R1,R1         WRAP AROUND
         STH   R1,MTSPLINE   SET NEW OFFSET
BUFFBUMP AR    R4,R2         SKIP ORDER BYTES
         SR    R3,R2         GET RESIDUAL LENGTH
         BP    BUFFWLOP      CONTINUE
         SPACE 1
BUFFCURS OC    MTSCURSA,MTSCURSA  WAS A CURSOR ORDER FOUND ?
         BNZ   BUFFWH        YES; HONOR IT
         TM    MTSFLGM,MTSFSFO   FORMATTED BUFFER ?
         BNZ   *+8           YES; CURSOR FOLLOWS LAST BYTE I/O'D
         LA    R14,MTSBUFF    ELSE IT GOES INTO FIRST SCREEN POSITION
         ST    R14,MTSCURSA  SET CURSOR ADDRESS
BUFFWH   LA    R4,MTSBUFF     SET REGISTERS TO PRINT NEW BUFFER
         LH    R3,MTSBUFSZ     AND ONLY SCREEN LENGTH            89328
         SPACE 2
BUFFROOM LA    R1,3          PRINT OVERHEAD = 2 LINES + FRACTION
         LH    R15,MTSCOLS   GET SIMULATION LENGTH               89328
         BCTR  R15,0          - 1 FOR EX
         MVC   MTSSAVE4(6),PUFFRML  MOVE INSTRUCTION PATTERN
         STC   R15,MTSSAVE4+1  MAKE CORRECT INSTRUCTION
         LR    R14,R4        COPY START ADDRESS
         MIN   R3,MTSBUFSZ,TYPE=H  DON'T ABEND BEYOND BUFFER     89328
         LR    R15,R3        COPY LENGTH
         B     BUFFRMI       FIRST LINE ALWAYS PRINTED
BUFFRML  EX    0,MTSSAVE4    EX TEST FOR BLANK LINE
         BE    BUFFRMI       YES; WON'T GET PRINTED
         LA    R1,1(,R1)     UP NON-BLANK LINE COUNT
BUFFRMI  SH    R15,MTSCOLS   GET REMAINING LENGTH                89328
         AH    R14,MTSCOLS   AND NEXT ADDRESS                    89328
         CH    R15,MTSCOLS   ROOM FOR ANOTHER FULL TEST ?        89328
         BNL   BUFFRML       YES, TEST IT
         BAL   R14,XPRES     RESERVE ENOUGH SPACE ON THIS PAGE
         MVC   MTSPLNDC(8),MTSPPARM  COPY LINE HEADER
         MVI   MTSPFLIN,C'0'     SET DOUBLE SKIP FIRST LINE
         SLR   R6,R6         CLEAR RELATIVE LINE COUNT
BUFFPRL  LH    R15,MTSCOLS   GET LINE LENGTH                     89328
         CR    R3,R15        MORE THAN REMAINDER ?
         BNL   *+6           NO; PRINT FULL LINE
         LR    R15,R3        ELSE DO ONLY REMAINING COUNT
         BCTR  R15,0         CHEAT
         EX    R15,PUFFRMT   COPY THE TEXT
         TM    MTSFLGM,MTSFREAD   NON-WRITE MODE ?
         BNZ   BUFFPRN       YES; SKIP OVERPRINT CHECK
         LA    R2,MTSPLNTX       GET TEXT LINE
         LH    R5,MTSCOLS    NUMBER OF BYTES TO CHECK            89328
BUFFPRS  CLI   0(R2),X'3F'   START FIELD BYTE ?                  87165
         BH    BUFFPRC       NO; TRY NEXT ONE                    87165
         MVC   MTSPFLN2-MTSPFLIN(1,R2),0(R2)  COPY THE ATTRIBUTE 87165
         NI    0(R2),X'30'   KILL NUMERICS                       87165
         PACK  0(1,R2),0(1,R2)  FLIP ZONE TO NUMERICS            87165
         NI    MTSPFLN2-MTSPFLIN(R2),X'0F'  KILL ZONES           87165
         TR    0(1,R2),BUFATTR  MAKE ONE BYTE OF ATTRIBUTE       87165
         TR    MTSPFLN2-MTSPFLIN(1,R2),BUFATTR2  AND OVERPRINT   87165
BUFFPRC  LA    R2,1(,R2)     TRY NEXT ONE
         BCT   R5,BUFFPRS
         C     R4,MTSCURSA   IS THE CURSOR ON THIS LINE ?
         BH    BUFFPRN       NO
         LR    R14,R4        GET ADDRESS OF THIS LINE
         AH    R14,MTSCOLS   GET END OF THIS LINE                89328
         C     R14,MTSCURSA  BUFFER ON THIS LINE ?
         BNH   BUFFPRN       NO
         L     R14,MTSCURSA  GET CURSOR ADDRESS
         SR    R14,R4        GET OFFSET INTO LINE
         LA    R14,MTSPLNTX(R14)  GET ADDRESS
         CLI   0(R14),C' '   EMPTY ?                             87158
         BNH   *+8           YES                                 87158
         LA    R14,MTSPFLN2-MTSPFLIN(R14)  ELSE USE SECOND LINE  87158
         MVI   0(R14),C'_'   UNDERLINE FOR CURSOR
         SPACE 1
BUFFPRN  LA    R6,1(,R6)     INCREASE RELATIVE LINE NUMBER
         CLC   MTSPFLIN+1(L'MTSPFLIN-1),MTSPFLIN   IS THIS LINE BLANK ?
         BE    BUFFPRI       YES; SKIP IT
         TM    MTSFLGM,MTSFREAD  READ MODE ?
         BZ    BUFFPNR       NO; PROCESS LINE NUMBER
         CH    R6,=H'1'      LINE 1 ?
         BNE   BUFFPRR       NO; SKIP TEXT LENGTH
         L     R6,MTSINPLN   GET AND DISPLAY INPUT LENGTH
BUFFPNR  CVD   R6,MTSDDB        CONVERT
         MVC   MTSPLNAD-4(6),=X'402020202120'
         ED    MTSPLNAD-4(6),MTSDDB+5  AND EDIT THE LINE
BUFFPRR  MVC   MTSPLNTX-2(2),=C'**'   FRAME THE TEXT
         LA    R14,MTSPLNTX       GET TEXT START
         AH    R14,MTSCOLS   AND TEXT END                        89328
         MVC   0(2,R14),=C'**'    END FRAME
         LA    R1,MTSPVLIN        SHOW OFF
         BAL   R14,XPRNT     GO TO PRINT
         MVI   MTSPFLIN,C' '    CLEAR THE PRINT LINE AGAIN
         MVC   MTSPFLIN+1(L'MTSPFLIN-1),MTSPFLIN CLEAR ALL
         CLC   MTSPFLN2+2(L'MTSPFLN2-2),MTSPFLN2+1  OVERPRINT BLANK ?
         BE    BUFFPRI       YES; DO NEXT LINE
         MVI   MTSPFLN2,C'+'    SET OVERPRINT
         LA    R1,MTSPVLN2        PRINT CONTROL CHARACTERS
         BAL   R14,XPRNT     GO TO PRINT
         MVI   MTSPFLN2+1,C' '   CLEAR AGAIN
         MVC   MTSPFLN2+2(L'MTSPFLN2-2),MTSPFLN2+1
BUFFPRI  AH    R4,MTSCOLS    GET NEXT INPUT LINE                 89328
         SH    R3,MTSCOLS    ANYTHING LEFT TO DO ?               89328
         BP    BUFFPRL       YES - GO DO IT
BUFPREX  LM    R14,R12,MTSSAVER   RESTORE REGS
         BR    R14           RETURN
         SPACE 1
PUFFRML  CLC   0(0,R14),MTSPFLN2     BLANK LINE ?
PUFFRMT  MVC   MTSPLNTX(0),0(R4)   COPY THE TEXT
TEXTMVC  MVC   0(0,R14),0(R4)
TEXTTRT  TRT   0(0,R4),0(R15)  SCAN FOR ORDERS
BUFATTR  DC    C'=#_-'       INPUT/NUMERIC/PROTECTED/SKIP        87165
BUFATTR2 DC    C'.,.,:;:;//****' NONE/MDT/LPEN/INTENSE/NON-DSP 87165
         EJECT ,                                                 87165
         PUSH  USING                                             87165
         DROP  ,                                                 87165
         USING MTSWORK,R7                                        87165
HEXPRINT STM   R14,R12,MTSSAVER  SAVE CALLER'S REGISTERS         87165
         BALR  R12,0         MAKE LOCAL BASE                     87165
         USING *,R12                                             87165
         TM    MTSSIMFG,MTSSINPR  PRINTER AVAILABLE ?            89328
         BNZ   HEXPEXIT      NO                                  87165
         LTR   R3,R0         COPY AND TEST LENGTH                87165
         BNP   HEXPEXIT      HUH ?  QUIT                         87165
         MVC   MTSPVLIN(4),MTSPH137  SET PRINT LINE LENGTH       87165
         MVI   MTSPVLIN+4,C' '  CLEAR PRINT LINE                 87165
         MVC   MTSPVLIN+5(132),MTSPVLIN+4                        87165
         LA    R6,0(,R1)     COPY AND CLEAN START ADDRESS        87165
         LR    R10,R6        SAVE START ADDRESS                  87165
         AR    R3,R6         MAKE END ADDRESS+1                  87165
         MIN   R3,=X'01000000'  PREVENT OVERFLOW                 87165
HEXPLINE MVI   MTSPFLIN+98,C'*'                                  87165
         MVI   MTSPFLIN+131,C'*'   FRAME EBCDIC                  87165
         LR    R9,R3         GET END ADDRESS                     87165
         SR    R9,R6         LESS CURRENT START                  87165
         BNP   HEXPEXIT      ?                                   87165
         LA    R0,32         MAXIMUM ON ONE LINE                 87165
         CR    R9,R0         TOO MUCH ?                          87165
         BNH   *+6           NO                                  87165
         LR    R9,R0         SET SINGLE LINE SIZE                87165
         BCTR  R9,0          LESS ONE FOR EXECUTE                87165
         EX    R9,HEXPMVC    MOVE THE TEXT PORTION               87165
         LA    R2,MTSPFLIN+8                                     87165
         LR    R1,R6         GET ABSOLUTE ADDRESS                87165
         SR    R1,R10        MINUS START = RELATIVE ADDRESS      87165
         BAL   R9,HEXTR                                          87165
         LA    R2,MTSPFLIN+2                                     87165
         LR    R1,R6         ABSOLUTE ADDRESS                    87165
         BAL   R9,HEXTR                                          87165
         MVC   MTSPFLIN+2(2),MTSPFLIN+20                         87165
         MVC   MTSPFLIN+10(2),MTSPFLIN+20    CLEAR               87165
         LA    R2,MTSPFLIN+18   FIRST HEX OUTPUT ADDRSS          87165
         LR    R1,R6         START INPUT ADDRSS                  87165
         LA    R4,8          DO EIGHT WORDS                      87165
         SPACE 1                                                 87165
HEXPWORD BAL   R9,HEXTV      SHOW HEX                            87165
         LA    R2,10(,R2)                                        87165
         LA    R1,4(,R1)     NEXT WORD                           87165
         CR    R1,R3         DONE ?                              87165
         BNL   HEXPLPRT      YES, JUST PRINT THIS ONE            87165
         BCT   R4,HEXPWORD                                       87165
         SPACE 1                                                 87165
HEXPLPRT MVC   MTSPFLIN+2(53),MTSPFLIN+4  GAP BETWEEN 16 BYTE CHUNKS
         MVC   MTSPFLIN+55(2),MTSPFLIN BLANK THE GAP             87165
         LA    R1,MTSPVLIN                                       87165
         BAL   R14,XPRNT     PRINT THE LINE                      87165
         MVI   MTSPFLIN,C' '                                     87165
         MVC   MTSPFLIN+1(132),MTSPFLIN     CLEAR                87165
HEXPBUMP LA    R6,32(,R6)    NEXT LINE                           87165
         CR    R6,R3         END ?                               87165
         BL    HEXPLINE      DO ANOTHER LINE                     87165
HEXPEXIT LM    R14,R12,MTSSAVER  RELOAD CALLER'S REGISTERS       87165
         BR    R14           RETURN                              87165
         SPACE 1                                                 87165
HEXTR    ST    R1,MTSDDB                                         87165
         B     HEXT+6        SKIP MOVE                           87165
HEXT     MVC   MTSDDB(4),0(R1)   MOVE TO SAFE AREA               87165
         UNPK  MTSSTADB(9),MTSDDB(5)  NO 0CX ON LAST BYTE IN STORAGE
         TR    MTSSTADB(8),HEXTAB-C'0'                           87165
         MVC   0(8,R2),MTSSTADB                                  87165
         BR    R9                                                87165
HEXTV    LR    R15,R3        COPY END ADDRESS                    87165
         SR    R15,R1        LESS CURRENT START                  87165
         CH    R15,=H'4'     FULL WORD TO DO ?                   87165
         BNL   HEXT          YES; USE NORMAL FORMATTING          87165
         BCTR  R15,0         MAKE LENGTH FOR EXECUTE             87165
         LA    R14,1(R15,R15)  DITTO FOR OUTPUT                  87165
         EX    R15,HEXPMVC1  MOVE PARTIAL WORD                   87165
         UNPK  MTSSTADB(9),MTSDDB(5)  NO 0CX ON LAST BYTE IN STORAGE
         TR    MTSSTADB(8),HEXTAB-C'0'                           87165
         EX    R14,HEXPMVC2  MOVE PARTIAL WORD'S TEXT            87165
         BR    R9                                                87165
HEXPMVC  MVC   MTSPFLIN+99(0),0(R6)  MOVE EBCDIC TEXT            87165
HEXPMVC1 MVC   MTSDDB(0),0(R1)  MOVE PARTIAL WORD                87165
HEXPMVC2 MVC   0(0,R2),MTSSTADB  MOVE PARTIAL WORD'S TEXT BACK   87165
         POP   USING                                             87165
         EJECT ,
         DROP  ,                                                 88024
         USING MTSWORK,R7    PASSED BY CALLER                    88024
*        PRINT ROUTINE,  (1) HAS ADDRESS OF V FORMAT RECORD
*
XPRNT    TM    MTSFLGT,MTSFGONE  SHUTTING DOWN ?                 87273
         BNZR  R14           YES; SKIP OUT                       87273
         TM    MTSPDCB+DCBOFLGS-IHADCB,DCBOFOPN  LOGIC ERROR ?   87273
         BZR   R14           THAT, OR SUBTASK WENT               87273
         STM   R0,R15,MTSSAVES
         BALR  R12,0         MAKE NEW LOCAL BASE
         USING *,R12         AND DECLARE IT
         LR    R6,R1
         USING VREC,R6
         CLC   LEN,MTSPH137      LENGTH EXCEED MAX ?
         BNH   *+10          NO, OK
         MVC   LEN,MTSPH137      SET TO MAX
         CLC   LEN,H6        SHORTER THAN MINIMUM ?
         BNL   *+10          NO, OK
         MVC   LEN,H6        YES, USE MIN
         BE    *+16
         LH    R2,LEN
         SH    R2,H5
         EX    R2,RECTR      MAKE PRINTABLE
         MVC   MTSPPRFX,LEN  SAVE ALL
XPRNTSB  LA    R3,XPCC       GET TABLE OF VALID CARRIAGE CONTROLS
         LA    R4,(XPCCE-XPCC)/2
         SLR   R5,R5
XPRCL    CLC   CC,0(R3)
         BE    XPRCV         FOUND IT
         LA    R3,2(,R3)
         BCT   R4,XPRCL
         MVC   MTSPPRCC,0(R3)
XPRCV    IC    R5,1(,R3)
         TM    MTSSIMFG,MTSSINPR  PRIOR ABEND ?                  89328
         BNZ   XPREX         YES; QUIT                           88024
         LH    R3,MTSPLIN#
         SR    R3,R5
         STH   R3,MTSPLIN#
         BP    XPRPUT
         MVI   CC,C'-'       SKIP AFTER TITLE
         MVC   MTSPLIN#,XPRMAX  RESET THE LINE COUNT
         AP    MTSPPAG#,P1
         MVC   MTSPTIPG,PGMASK
         ED    MTSPTIPG,MTSPPAG#
         TIME  DEC
         STM   R0,R1,MTSDDB  SAVE TIME/DATE
         MVC   MTSPTIDT-1(7),XPRDTM  MOVE DATE MASK
         ED    MTSPTIDT-1(7),MTSDDB+5    EDIT THE DATE
         MVC   MTSPTITM-1(9),XPRTMM   MOVE TIME MASK
         ED    MTSPTITM-1(9),MTSDDB      EDIT THE TIME
         TM    MTSPDCB+DCBOFLGS-IHADCB,DCBOFOPN  LOGIC ERROR ?   87273
         BZ    XPREX         SHUTTING DOWN ?                     87273
         PUT   MTSPDCB,MTSPTITL
XPRPUT   TM    MTSSIMFG,MTSSINPR  ABENDED ?                      89328
         BNZ   XPREX         YES - GET OUT
         MVC   VFG,MTSPH137+2    ZERO FLAG FIELD
         LH    R1,MTSPPRFX   GET USER SPECIFIED LENGTH
         LA    R2,VREC(R1)   POINT TO LAST BYTE + 1
         SH    R1,H6         MAKE SURE TO LEAVE AT LEAST 6
         BNP   XPRPUTS       NONE TO CHECK
XPRPUTL  BCTR  R2,0          POINT TO PRECEDING BYTE
         CLI   0(R2),C' '    IS IT A TRAILING BLANK ?
         BNH   XPRPUTS       NO, SCAN DONE
         BCT   R1,XPRPUTL    TRY THE ONE BEFORE
XPRPUTS  AH    R1,H6         USE AT LEAST 6 BYTES
         STCM  R1,3,LEN
         TM    MTSPDCB+DCBOFLGS-IHADCB,DCBOFOPN  LOGIC ERROR ?   87273
         BZ    XPREX         SHUTTING DOWN ?                     87273
         PUT   MTSPDCB,(R6)   PUT THE RECORD
         MVC   LEN(5),MTSPPRFX   RESTORE
         SPACE 1
XPREX    LM    R0,R15,MTSSAVES
         BR    R14
         SPACE 3
*        RESERVE SPACE ON A PAGE
*
XPRES    LTR   R1,R1         IS THIS A VALID REQUEST ?
         BNPR  R14           INVALID - RETURN
         LH    R15,MTSPLIN#  GET LINES LEFT ON PAGE
         SR    R15,R1        GET REMAINDER AFTER THIS REQUEST
         BNMR  R14           RETURN - FITS ON THIS PAGE
         MVI   MTSPLIN#,X'FF'   ELSE FORCE A PAGE EJECT
         BR    R14           RETURN
         SPACE 2
*        DATA
*
RECTR    TR    CC(0),PRTAB
H5       DC    H'5'
H6       DC    H'6'
XPRMAX   DC    H'56'   ***** MAX DATA LINES PER PAGE *****
XPRDTM   DC    X'4021204B202020'   JULIAN DATE MASK
XPRTMM   DC    X'4021207A20207A2020'  TIME MASK
PRTAB    TRTAB CODE=PN,OPT=FOLD
         SPACE 2
P1       DC    P'1'
XPCC     DC    AL1(C' ',1,C'1',255,C'0',2,C'-',3,C'+',0)
XPCCE    DC    AL1(C' ',1)   DEFAULT FOR BAD CODE
PGMASK   DC    X'402020202120'
         POP   USING         PUSHED PRIOR TO BUFPRINT
         EJECT ,
REV      EQU   0             EVEN WORK REGISTER
ROD      EQU   1             ODD  WORK REGISTER
RIN      EQU   2             INPUT BUFFER POINTER
ROUT     EQU   3             OUTPUT BUFFER POINTER
RLEN     EQU   4             INPUT SCAN STOP ADDRESS
RBUF     EQU   5             CURRENT BUFFER POSITION
RMASK    EQU   6             BUFFER ADDRESS CONVERSION MASK
RON      EQU   8             CONSTANT OF ONE /WAS R7 - NEED FOR SIMWK
RTHREE   EQU   9             CONSTANT OF THREE
RFOUR    EQU   10            CONSTANT OF FOUR
*RWK1    EQU   11            WORK REGISTER / CHGD TO FREE R7
RSIZE    EQU   12            SCREEN SIZE
RSAVE    EQU   13            SAVE AREA POINTER
RET      EQU   14            SUBROUTINE CALL RETURN ADDRESS
RBASE    EQU   15            PROGRAM BASE REGISTER
         SPACE 2
*    CHARACTER SET DEPENDENT DATA
*        3270  ORDER CODES
         SPACE 1
SBA      EQU   X'11'         SET BUFFER ADDRESS ORDER
SF       EQU   X'1D'         START FIELD ORDER / ATTRIBUTE BYTE
IC       EQU   X'13'         INSERT CURSOR ORDER
RTA      EQU   X'3C'         REPEAT TO ADDRESS ORDER / ADDR / DATA
EUA      EQU   X'12'         ERASE UNPROTECTED TO ADDRESS / ADDR
SFE      EQU   X'29'         START FIELD EXTENDED/ N / (N)PAIRS
MF       EQU   X'2C'         MODIFY ATTRIBUTE      -"-
SA       EQU   X'28'         SET ATTRIBUTE / TYPE / VALUE
ETX      EQU   X'03'         BTAM/TCAM END OF TEXT SIGNAL
EOT      EQU   X'37'         BTAM/TCAM END OF TRANSMISSION CONTROL
BLANK    EQU   C' '          EBCDIC BLANK
PT       EQU   X'05'         PROGRAM TAB ***** NOT SUPPORTED *****
STX      EQU   X'02'         START TEXT
ESC      EQU   X'27'         ESCAPE (DEVICE CONTROL)
EWR      EQU   X'F5'         ERASE/WRITE CCW CODE
EWA      EQU   X'7E'         ERASE/WRITE ALTERNATE
WSF      EQU   X'F3'         WRITE STRUCTURED FIELD
WRT      EQU   X'F1'         WRITE
EAU      EQU   X'6F'         ERASE ALL UNPROTECTED (CCW)
         SPACE 1
ORDERS   DC    256AL1(OFFBAD)   DEFAULT TO UNPRINTABLE CHARACTER
         ORCH  SF,OFFSF      START FIELD
         ORCH  SBA,OFFSBA    SET BUFFER ADDRESS
         ORCH  IC,OFFIC      INSERT CURSOR
         ORCH  PT,OFFPT      PROGRAM TAB
         ORCH  RTA,OFFRTA    REPEAT TO ADDRESS
         ORCH  EUA,OFFEUA    ERASE UNPROTECTED TO ADDRESS
         ORCH  SFE,OFFSFE    START FIELD EXTENDED
         ORCH  MF,OFFMF      MODIFY FIELD
         ORCH  SA,OFFSA      SET ATTRIBUTE
         ORCH  0,0           HEX ZERO=>BLANK
         ORCH  C' ',0        BLANK
         ORCH  C'',0,7      CENT THROUGH AMPERSAND
         ORCH  C'!',0,8      EXCLAMATION POINT THROUGH SLASH
         ORCH  X'6A',0,6     BROKEN STROKE THROUGH QUESTION MARK
         ORCH  X'79',0,7     REVERSE QUOTE THROUGH QUOTE
         ORCH  X'81',0,9     LOWER CASE A THROUGH I
         ORCH  X'91',0,9     LOWER CASE J THROUGH R
         ORCH  X'A1',0,9
         ORCH  X'C0',0,10    LEFT BRACE, A TO I
         ORCH  X'D0',0,10    RIGHT BRACE, J TO R
         ORCH  X'E0',0       REVERSE SLASH
         ORCH  C'S',0,8      S TO Z
         ORCH  C'0',0,10     DIGITS 0 TO 9
         ORG   ,
         SPACE 2
*        CHARACTER SET DEPENDENT CONSTANTS
         SPACE 1
*ETXEOT  DC    0AL2(0),AL1(ETX,EOT)    END OF BUFFER SIGNAL
         SPACE 1
*        BUFFER ADDRESS TRANSLATION TABLE
EBCDIC   DC    X'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'   0 - 15
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F'  16 - 31
         DC    X'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F'  32 - 47
         DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'  48 - 63
         EJECT ,
XETXR    XHEAD RPL=NO,EXH=NO,SAVE=RENT
         LA    R7,WORKQUE-(MTSQLINK-MTSWORK)  POINT TO QUEUE HEAD
         USING MTSWORK,R7
XETXLOOP ICM   R7,15,MTSQLINK  GET NEXT WORK AREA
         BZ    XETXEXIT      HUH ?
         C     R6,MTSTASK    SAME TCB ?
         BNE   XETXLOOP      NO; TRY NEXT ONE
         DETACH MTSTASK      DETACH IT
         XC    MTSTASK,MTSTASK  SIGNAL SUBTASK GONE
         CSDEC MTV#CURS      DECREASE ACTIVE SESSION COUNT
         LTR   R15,R15       ANY ACTIVE USERS LEFT ?             90231
         BP    XETXNZRO      YES                                 90231
         SERVICE CANON       ALLOW OPERATOR CANCEL AGAIN         90285
         TM    FLAGS,FGQUIT  SOFT SHUT-DOWN ?                    90231
         BZ    XETXNZRO      NO                                  90231
         OI    FLAGS,FGDOWN  SHOW GOING DOWN                     90231
XETXNZRO ICM   R0,15,MTSDDNIO  WAS FINAL DD FREED ?              90050
         BZ    XETXDECT      YES                                 90050
         SERVICE ALCFD,MTSDDNIO  REQUEST DE-ALLOCATION           90050
         BXLE  R15,R15,XETXDECT  WORKED                          90289
         NOP   0             *****DEBUG*****                     90289
XETXDECT ICM   R0,15,MTSDDNCT  CONTROL FILE FREED ?              90050
         BZ    XETXDEAL      YES                                 90050
         SERVICE ALCFD,MTSDDNCT  FREE CONTROL FILE               90050
         BXLE  R15,R15,XETXDEAL  WORKED                          90289
         NOP   0             *****DEBUG*****                     90289
XETXDEAL CLI   MTSUIF,C' '   USER LOGGED ON ?                    89337
         BNH   XETXNSMF      NO; OR ALREADY WROTE SMF            89337
         OI    MTSSTI,X'02'  SHOW ABEND                          89337
         OI    MTSSTAT,X'80'   DITTO                             89337
         TIME  BIN                                               89337
         STCM  R0,15,MTSRCDTS  SET TIME OFF                      89337
         STCM  R1,15,MTSRCDTE  SET TIME OFF                      89337
         LA    R1,MTSSMF                                         89337
         SMFWTM (1)          WRITE THE RECORD                    89337
         XC    MTSUIF,MTSUIF SIGNAL RECORD WRITTEN               89337
XETXNSMF CLC   NIBMODE-NIBST+PATNIB,NIBMODE-NIBST+MTSNIB  VTAM ?
         BE    XETXVTAM      YES; DO FINAL RITES
         DROP  R7
XETXNOCS LA    R1,WORKQUE-(MTSQLINK-MTSWORK) QUEUE
         USING MTSWORK,R1
XETXSLOC C     R7,MTSQLINK   IS THIS IT ?
         BE    XETXSDEQ      YES; REMOVE FROM QUEUE
         ICM   R1,15,MTSQLINK  POINT TO NEXT ONE
         BNZ   XETXSLOC
         B     XETXEXIT      ELSE QUIT
XETXSDEQ L     R15,MTSQLINK-MTSWORK(,R7)  GET POINTER TO NEXT
         CS    R7,R15,MTSQLINK  UNCHAIN THIS ONE
         BNZ   XETXNOCS      DIDN'T TAKE - RUN THE CHAIN AGAIN
         DROP  R1
         USING MTSWORK,R7
XETXNZCS L     R15,FREEQUE   GET CURRENT FREE ELEMENT
         ST    R15,MTSQLINK  MAKE TENTATIVE LINK
         CS    R15,R7,FREEQUE  ADD TO QUEUE
         BNZ   XETXNZCS      ELSE TRY AGAIN
         B     XETXEXIT      AND FINISH
XETXVTAM UNPOST MTSWECB                                          88348
         TM    MTSFLGV,MTSFRSVP  IN A BRACKET ?                  87279
         BZ    XETXCLTY      PROBABLY NOT                        87273
         TM    MTSFLGT,MTSFGONE  TERMINAL GONE ?                 87273
         BNZ   XETXCLTY      YES; SKIP                           87273
         TM    FLAGS,FGDOWN  TASK GOING DOWN ?                   87273
         BNZ   XETXCLTY      YES; SKIP ALSO                      87273
         SEND  RPL=MTSWRPL,RECLEN=0,ECB=MTSWECB,BRACKET=(,EB),         *
               POST=RESP,STYPE=REQ,CONTROL=DATA
         BXH   R15,R15,XETXCLTY  CHECK CLOSE TYPE
         WAIT  ECB=MTSWECB   WAIT FOR COMPLETION
XETXCLTY CLI   MTSNAPPL,C' '  GO ELSEWHERE ?
         BNH   XETXREL       NO; JUST RELEASE
         L     R2,MTSSTADB   GET DATA LENGTH                     87300
         CLSDST RPL=MTSCRPL,OPTCD=(ASY,PASS),EXIT=XCLOSE,              *
               AREA=MTSSTADB+4,RECLEN=(R2),AAREA=MTSNAPPL        87300
         LTR   R15,R15       ACCEPTED ?
         BZ    XETXEXIT      OK IF DONE
         CH    R15,=H'16'    NEED TO RE-ISSUE ?
         BE    XETXCLTY      YES; RETRY IT
         XC    MTSNAPPL,MTSNAPPL  JUST IN CASE                   87267
XETXREL  CLSDST RPL=MTSCRPL,OPTCD=(ASY,RELEASE),EXIT=XCLOSE,           *
               RECLEN=0,AAREA=0
         CH    R15,=H'16'    NEED TO RE-ISSUE ?
         BE    XETXCLTY      YES; RETRY IT
XETXEXIT XEXIT ,
         SPACE 2
XCLOSE   XHEAD EXH=NO,SAVE=RENT
         CHECK RPL=(R6)
         CLI   RPLFDBK,0     OK ?                                87018
         BNE   XCLOSCTR      NO; R15 AND R0 IRRELEVANT           87018
         LTR   R15,R15       ACCEPTED ?
         BZ    XCLOFIND      YES
XCLOSCTR LA    R1,1
         A     R1,MTSERRCT
         ST    R1,MTSERRCT   COUNT ERROR ENTRIES
         CH    R15,=H'16'    TRAUMATIC ERROR ?
         BNL   XCLOFIND      YES; DON'T TRY TO RECOVER
         CLI   MTSNAPPL,C' '  ANY ?
         BNH   XCLOEXEC      NO; JUST RE-EXEC
XCLOREL  CLSDST RPL=MTSCRPL,OPTCD=(ASY,RELEASE),EXIT=XCLOSE
         CH    R15,=H'16'    NEED TO RE-ISSUE ?
         BE    XCLOREL       YES; RETRY IT
         XC    MTSNAPPL,MTSNAPPL  JUST IN CASE                   87267
         B     XCLOEXIT      WAIT FOR RE-ENTRY
XCLOEXEC EXECRPL RPL=(R6)    RETRY THE REQUEST
         XC    MTSNAPPL,MTSNAPPL  JUST IN CASE                   87267
         B     XCLOEXIT      AND WAIT FOR RE-ENTRY
*        CHRET ,             DON'T CARE ABOUT COMPLETION
         DROP  R7
XCLOFIND LA    R1,WORKQUE-(MTSQLINK-MTSWORK) QUEUE
         USING MTSWORK,R1
XCLOSLOC C     R7,MTSQLINK   IS THIS IT ?
         BE    XCLOSDEQ      YES; REMOVE FROM QUEUE
         ICM   R1,15,MTSQLINK  POINT TO NEXT ONE
         BNZ   XCLOSLOC
         B     XCLOEXIT      ELSE QUIT
XCLOSDEQ L     R15,MTSQLINK-MTSWORK(,R7)  GET POINTER TO NEXT
         CS    R7,R15,MTSQLINK  UNCHAIN THIS ONE
         BNZ   XCLOFIND      DIDN'T TAKE - RUN THE CHAIN AGAIN
         DROP  R1
         USING MTSWORK,R7
XCLONZCS L     R15,FREEQUE   GET CURRENT FREE ELEMENT
         ST    R15,MTSQLINK  MAKE TENTATIVE LINK
         CS    R15,R7,FREEQUE  ADD TO QUEUE
         BNZ   XCLONZCS      ELSE TRY AGAIN
         XEXIT ,             AND RETURN TO VTAM
         SPACE 2
XTPEND   XHEAD RPL=NO,EXH=NO
         OI    FLAGS,FGDOWN  SHOW SYSTEM GOING DOWN
         LA    R7,WORKQUE-(MTSQLINK-MTSWORK)
         USING MTSWORK,R7
XTPELOOP ICM   R7,15,MTSQLINK  GET NEXT TASK
         BZ    XTPEPOST      NONE
         CLC   NIBMODE-NIBST+PATNIB,NIBMODE-NIBST+MTSNIB  VTAM ?
         BNE   XTPELOOP      NO; DON'T CLOSE DOWN EXCP TYPE
         OI    MTSFLGT,MTSFGONE  SHOW TERMINAL COMING DOWN
         ICM   R2,15,MTSTASK  ANY SUBTASK ?
         BZ    XTPELOOP      NO
         L     R0,=X'00622000'  SET COMPLETION CODE
         OI    MTSFLGT,MTSFXEND  END REQUEST                     89328
         POST  MTSATECB,(0)  POST ATTENTION                      89328
         LA    R1,MTSCNECB   GET CANCEL ECB ADDRESS              89328
         POST  (1),(0)       AND CANCEL
         B     XTPELOOP      TRY ANOTHER TASK
XTPEPOST L     R1,=A(MAINECB)  GET MAIN TASK ECB
         POST  (1),0         POST IT
         XEXIT ,             AND GET OUT
         SPACE 1
XLOSTERM XHEAD RPL=NO,EXH=NO,SAVE=RENT
         ICM   R7,15,8(R6)   GET NIB USERFLD (MTSWORK BASE)
         BZ    XLOSEXIT      HUH ?  LOGON RPL ?
         USING MTSWORK,R7
         TM    FLAGS,FGDOWN  VTAM GOING DOWN ?
         BNZ   XLOSEXIT      YES; JUST QUIT
         LA    R1,WORKQUE-(MTSQLINK-MTSWORK) VERIFY RPL/WORK
XLOSLOOP C     R7,MTSQLINK-MTSWORK(,R1)  ON CHAIN ?
         BE    XLOSGONE      YES
         ICM   R1,15,MTSQLINK-MTSWORK(R1)  GET NEXT ONE
         BNZ   XLOSLOOP      CHECK IT
         B     XLOSEXIT      NONE
XLOSGONE OI    MTSFLGT,MTSFGONE  SIGNAL TERMINAL GONE
         ICM   R0,15,MTSTASK ACTIVE SUBTASK ?
         BZ    XLOSEXIT      NO; ALREADY COMING DOWN
         L     R0,=X'00622000'  SET COMPLETION CODE
         OI    MTSFLGT,MTSFXEND  END REQUEST                     89328
         POST  MTSATECB,(0)  POST ATTENTION                      89328
         LA    R1,MTSCNECB   GET CANCEL ECB ADDRESS              89328
         POST  (1),(0)       AND CANCEL
XLOSEXIT XEXIT ,
         EJECT ,
         DROP  ,
         SPACE 2
*        DCB ABEND EXIT TO IGNORE TASK 013/B37 TYPE ABENDS
*
         USING XPRABEND,R15
XPRABEND MVI   3(R1),0       SET PROVISIONAL ABEND
         LAT   R7,4(R1)      GET DCB ADDRESS FOR MTSPDCB
         SH    R7,=AL2(MTSPDCB-MTSWORK) GET EXTENDED WORK AREA
         USING MTSWORK,R7
         OI    MTSSIMFG,MTSSINPR  PRINT NO LONGER AVAILABLE      89328
         NI    MTSSIMFG,255-MTSSIPRT  CANCEL PRINT REQUEST       89328
         CLI   MTSPMG14+25,C'X'  IS THIS AN ABEND RECURSION ?
         BNER  R14           YES - JUST ABEND
         MVI   3(R1),4       ELSE SET IGNORE CODE
         UNPK  MTSSTADB(7),0(4,R1)  UNPACK ABEND CODE
         TR    MTSSTADB+0(6),HEXTAB-C'0'  MAKE EBCDIC
         MVC   MTSPMG14+4+25(3),MTSSTADB    SET MAJOR ABEND CODE
         MVC   MTSPMG14+4+29(2),MTSSTADB+4   AND SUB-CODE
XPRABMSG WTO   MF=(E,MTSPMG14)   ABEND MESSAGE
         BR    R14           RETURN
         DROP  ,
         SPACE 2
         USING STABLE,R15
STABLE   CH    R0,=H'12'     WAS A WORK/SAVE AREA GOTTEN ?
         BL    STABOK        YES - CONTINUE
         SLR   R15,R15       IF NOT; LET TMP HANDLE IT
         BR    R14           GO TO TMP STAI
STABOK   LR    R12,R15       MAKE NEW BASE REGISTER
         DROP  ,
         USING STABLE,R12    DO DECLARE IT
         LR    R10,R14       SAVE RETURN ADDRESS, JUST IN CASE
         LR    R6,R1         SAVE INPUT ADDRESS
         L     R7,0(,R1)     GET WORK AREA ADDRESS FROM STAE PARM
         USING MTSWORK,R7
         CLC   =X'5220',5(R6)  CHECK SYSTEM ABEND CODE           91127
         BNE   STABN522      NOT 522                             91127
         MVC   MTSSTAVC(PAT522LN),PAT522MG  MOVE PATTERN         91127
         MVC   MTSSTAMG+33(8),MTSNIB+12  ADD TERM ID             91127
         WTO   MF=(E,MTSSTAVC)  SEND A MESSAGE                   91127
         B     STABEXIT                                          91127
         SPACE 1                                                 91127
STABN522 MVC   MTSSTAVC(PATSTALN),PATSTAMG  MOVE CANNED MESSAGE
         UNPK  MTSSTAMG+59(9),8(5,R6)  CURRENT PSW 1
         UNPK  MTSSTAMG+68(9),12(5,R6) CURR. PSW 2 / NOTE OVERLAP TO DB
         TR    MTSSTAMG+59(17),HEXTAB-C'0'  MAKE EBCDIC
         MVI   MTSSTAMG+67,C' '  PUT SPACER BACK
         UNPK  MTSSTAMG+36(9),16(5,R6) PROG PSW 1
         UNPK  MTSSTAMG+45(9),20(5,R6) PROG PSW 2
         TR    MTSSTAMG+36(17),HEXTAB-C'0'
         MVI   MTSSTAMG+44,C' '  SEPARATE
         MVI   MTSSTAMG+53,C';'
         MVC   MTSSTADB(2),5(R6)  MOVE COMPLETION CODE
         NC    MTSSTADB(2),=X'FFF0'  MASK OUT SYSTEM CODE
         BZ    STABUSER      USER CODE
         UNPK  MTSSTAMG+26(3),MTSSTADB(2)  UNPACK
         OI    MTSSTAMG+28,C'0'  FORCE ZONE
         TR    MTSSTAMG+26(3),HEXTAB-C'0'  MAKE EBCDIC
         MVI   MTSSTAMG+25,C'S'  IDENTIFY SYSTEM CODE
         B     STABCOM       SKIP USER CODE
STABUSER LH    R0,6(,R6)     GET USER CODE
         CVD   R0,MTSSTADB   CONVERT IT
         OI    MTSSTADB+7,X'0F'  FORCE PLUS SIGN FOR NUMERIC ZONE
         UNPK  MTSSTAMG+25(5),MTSSTADB+5(3)
         MVI   MTSSTAMG+25,C'U'  IDENTIFY AS USER CODE
         MVI   MTSSTAMG+30,C';'  SEPARATE
STABCOM  MVI   MTSSTAMG+76,C' '                                  89345
         MVC   MTSSTAMG+77(8),MTSNIB+12  COPY TERM-ID            89345
         L     R5,PSATOLD-PSA  GET THE CURRENT TCB
         USING TCB,R5
         LAT   R5,TCBRBP,STABPUT  GET RB CHAIN OR OUT
         B     STABRB2       LOOK AT IT
         USING RBSECT,R5
STABRB1  TM    RBSTAB2,RBTCBNXT  DOES RB POINT TO TCB ?
         BNZ   STABPUT       YES - EXIT
         LAT   R5,RBLINK,STABPUT  GET NEXT RB
STABRB2  TM    RBSTAB1,RBFTP   IS THIS A PRB ?
         BNZ   STABRB1       NO; SKIP IT
         LAT   R1,RBCDE,STABRB1  GET CDE OR NEXT
         USING CDENTRY,R1                                        89328
         CLC   =C'KER',CDNAME  MY MODULE NAME PREFIX ?           89328
         BNE   STABRB1       NO; TRY AGAIN
         MVC   MTSSTAMG+8(8),CDNAME  MOVE NAME TO MESSAGE
         DROP  R1,R5
STABPUT  WTO   MF=(E,MTSSTAVC)  SEND MESSAGE
         MVC   MTSSTADB(7),=CL7'GR 0-7'  IDENTIFY FIRST REGISTER LINE
         MVI   MTSSTADB+43,C' '          GAP BETWEEN FOUR
         LA    R5,2          SET FOR TWO LINES
STABRLP  LA    R4,MTSSTADB+7 SET FIRST OUTPUT ADDRESS
         LA    R3,2          SET FOR TWO GROUPS OF FOUR
STABRLP2 LA    R2,4          SET FOR FOUR REGISTERS
STABRLP3 UNPK  0(9,R4),24(5,R6)  UNPACK A REGISTER
         TR    0(8,R4),HEXTAB-C'0'     MAKE EBCDIC
         MVI   8(R4),C' '    SEPARATOR
         LA    R4,9(,R4)     NEXT OUTPUT ADDRESS
         LA    R6,4(,R6)     NEXT REGISTER
         BCT   R2,STABRLP3   DO NEXT REGISTER
         LA    R4,1(,R4)     INTERGROUP GAP
         BCT   R3,STABRLP2   DO NEXT GROUP
         LA    R0,79+4
         SLL   R0,16
         STCM  R0,15,MTSSTADB-4
         WTO   MF=(E,MTSSTADB-4)       GIVE TO USER
         MVC   MTSSTADB+3(3),=C'8-F'   IDENTIFY SECOND LINE
         BCT   R5,STABRLP    DO SECOND LINE
STABEXIT XC    MTSSTADB,MTSSTADB  CLEAR FOR CLSDST               87300
         LR    R14,R10       RELOAD EXIT ADDRESS
         LA    R15,16        IGNORE STAI TO FORCE DUMP
         BR    R14           RETURN
         DROP  ,
         SPACE 1
PAT522MG VCON  'COM522E NETCOMM  TIME OUT ON TERMIDXX'
PAT522LN EQU   *-PAT522MG                                        91127
PATSTAMG VCON   'COM513E NETCOMM  ABENDED SXXX; APSW=FF00CCCC FFAAAAAA;*
                PSW=FF00CCCC FFAAAAAA TERMIDXX'   STAE ABEND MESSAGE
PATSTALN EQU   *-PATSTAMG    LENGTH OF PATTERN
HEXTAB   DC    C'0123456789ABCDEF'  HEX CONVERSION TABLE
         SPACE 2
         LTORG
         SPACE 1
FEAT3767 DC    A(CCWW70)
         DC    AL2(1840,23,80,4,0)
         SCRN  (CHAR80,RESTKBY,RESTMDT),SBA,(1,1)                89328
CCWW70   CCW   X'05',0,X'20',0   ERASE/WRITE
         CCW   X'01',0,0,0   DUMMY
CCWW78   CCW   X'0D',0,X'20',0  ERASE/WRITE ALTERNATE
         CCW   X'0D',0,0,0
         SPACE 2
VREC     DSECT
LEN      DS    H
VFG      DS    H             PROC. FLAGS
CC       DS    C
TEXT     DS    CL132
         SPACE 2
INSECT   DSECT ,             INPUT BUFFER MAPPING
INPUT    DS    C             SINGLE INPUT CHARACTER AT A TIME
         SPACE 1
OUTSECT  DSECT ,             OUTPUT BUFFER MAPPING
OUTPUT   DS    C             SINGLE BYTE AT A TIME
         EJECT ,
NETCOMM  CSECT ,                                                 89328
INITALIZ XHEAD RPL=NO,EXH=NO,MODE=OS   INITIALIZATION ROUTINE(S)
         DEVTYPE SYNDCB+DCBDDNAM-IHADCB,INITDDB  SYSIN SUPPLIED ?
         BXH   R15,R15,INITEXIT  NO; JUST GET OUT
         OPEN  (SYNDCB,(INPUT,REREAD))  OPEN IT
         TM    DCBOFLGS-IHADCB+SYNDCB,DCBOFOPN  OPEN ?
         BZ    INITEXIT      NO; SKIP
INITGET  GET   SYNDCB        PICK A CARD; ANY CARD...
         LR    R6,R1         SAVE IT
         CLI   0(R6),C'*'    COMMENTS ?
         BE    INITGET       YES; SKIP
         CLC   1(71,R6),0(R6)  ALL BLANK OR SOMETHING ?
         BE    INITGET       YES; SKIP IT ALSO
         MVC   INITCARD,0(R6)  COPY CARD FOR ERROR MESSAGE       87012
         LA    R4,1          SET BXLE INCREMENT
         LA    R5,71(,R6)    SET LAST SCANNED COLUMN
         BAL   R14,SCANPARM  FIND COMMAND
         CLC   =C'LOGON',INITDDB+3  ONE OF OURS? (NETSOL COMPATIBILITY)
         BE    INITSLOG      YES
         CLC   =C'LOGON',INITDDB  ALTERNATE FORM ?
         BNE   INITCBAD      NO; FAIL IT
INITSLOG BAL   R14,SCANPARM  FIND TERMID
         CLI   INITDDB,C' '  ANY ?
         BNH   INITCBAD      NO; BAD CARD
         MVC   NIBSYM+INITNIB-NIBST,INITDDB  COPY TERMID
         BAL   R14,SCANPARM  GET THE NEXT PARAMETER
         SLR   R2,R2         CLEAR LOGON DATA LENGTH             87130
         LR    R3,R6         ANY OLD AREA                        87130
         CLI   INITDDB,C' '  FOR US ?
         BE    INITLOGS      YES
         L     R1,=A(MYAPPLID)  GET OUR ID
         CLC   1(8,R1),INITDDB  MATCH ?
         BNE   INITGET       NO
         BAL   R14,SCANPARM  ANOTHER PARAMETER ?                 87130
         CLI   INITDDB,C' '                                      87130
         BNH   INITLOGS      NO MORE                             87130
         L     R3,INITR6     GET PARAMETER START                 87130
INITLLP  CLI   0(R5),C' '    TRAILING BLANK ?                    87130
         BE    INITLBK       YES, BACK-UP                        87130
         CLI   0(R5),C','    TRAILING COMMA ?                    87130
         BNE   INITLLN       NO; COMPUTE LENGTH                  87130
INITLBK  SR    R5,R4         BACK-SPACE                          87130
         CR    R5,R6         ALL ?                               87130
         BH    INITLLP       NO; TRY AGAIN                       87130
INITLLN  LA    R5,1(,R5)     ALLOW FOR RELATIVITY                87130
         SR    R5,R3         TEST LENGTH                         87130
         BNP   INITLOGS      NONE                                87130
         LR    R2,R5         SET PARM LENGTH                     87130
INITLOGS SIMLOGON RPL=INITRPL,NIB=INITNIB,OPTCD=(SYN,Q,RELRQ),         *
               RECLEN=(R2),AREA=(R3)                             87130
         CH    R15,=H'8'     RETRY ?
         BE    INITLOGS      YES; TRY AGAIN
         CLI   RPLFDBK+INITRPL-IFGRPL,0  OK ?                    87013
         BNE   INITLOGF      NO                                  87013
         LTR   R15,R15                                           87013
         BZ    INITGET       OK                                  87013
INITLOGF MVC   INITSFID,NIBSYM+INITNIB-NIBST  TERMID             87013
         CVD   R15,INITDDB   RETURN CODE                         87013
         MVC   INITSFRC,=X'40202120'                             87013
         ED    INITSFRC,INITDDB+6  SHOW RETURN CODE              87013
         UNPK  INITSFBK(5),RPLFDBK+INITRPL-IFGRPL(3)             87013
         TR    INITSFBK,MDBHEX-C'0'                              87013
         WTO   MF=(E,INITSFVC)  DISPLAY                          87013
         B     INITGET       IGNORE RESPONSE
         SPACE 1
INITCBAD WTO   MF=(E,INITVCON)  ISSUE ERROR MESSAGE
         B     INITGET       GET ANOTHER CARD
         SPACE 1
INITEOF  CLOSE (SYNDCB)
*30A*    FREEPOOL SYNDCB                                         87013
         ICM   R1,7,SYNDCB+DCBBUFCA-IHADCB   DIRTY POOL ?        87013
         BZ    INITPOOL      YES; SKIP FREEPOOL AND 30A ABEND    87013
         SLR   R14,R14                 CLEAR REGISTER            87013
         IC    R14,5(,R1)    GET BUFNO                           87013
         MH    R14,6(,R1)      TIMES BUFLEN                      87013
         LA    R0,8(,R14)    PREFIX                              87013
         TM    4(R1),X'40'   SIXTEEN-BYTE PREFIX ?               87013
         BNO   *+8           NO                                  87013
         LA    R0,16(,R14)   SET LONG PREFIX                     87013
*DEFUNCT*ICM   R0,8,=AL1(252)  SET SUB-POOL                      87013
         SVC   10            FREE THE BUFFERS                    87013
INITPOOL XEXIT MODE=OS       RETURN
         SPACE 1
SYNDCB   DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GL),EODAD=INITEOF
INITVCON DC    Y(INITVCOX-*,0),C' INVALID CARD :'
INITCARD DC    CL80' '
INITVCOX EQU   *             END OF WTO
INITSFVC DC    Y(INITSFVX-*,0)                                   87013
INITSFID DC    CL8' ',C' SIMLOGON FAILED'                        87013
INITSFRC DC    CL4' ',C' '                                       87013
INITSFBK DC    CL4' '                                            87013
INITSFVX DC    C' '          SLOP-OVER FROM UNPACK               87013
         SPACE 1
         PRINT NOGEN
         IHASCB ,                                                *TSM*
         IHAPSA ,
         IKJTSB ,
NETCOMM  CSECT ,                                                 89328
WORKBASE DS    0D            COMMON ADDRESSABILITY
         USING WORKBASE,R10
         SPACE 1
*        SUBROUTINE TO PRINT RECORD OF VTAM NON-ZERO RETURN CODES
*
         DROP  ,
         USING MTSWORK,R7    START WITH SOMETHING
CCNZVTAM LTR   R15,R15       ANY ERROR ?
         BZR   R9            NO (LET CALLER LOOK AT R0)
         STM   R14,R2,MTSSAVCK  SAVE INFORMATION FOR FORMAT OR RETURN
         TM    MTSFLGT,MTSFGONE   ERROR DURING SHUT-DOWN OR RELEASE?
         BNZR  R9            YES; JUST RETURN
         BALR  R14,0
         USING *,R14         LOCAL BASE
CCNZVTAB SH    R14,CCNZ256   FINAGLE
         USING CCNZVTAB-256,R14
         TM    FLAGS,FGDOWN  DITTO ?
         BNZR  R9            DITTO
         LM    R15,R0,MTSSAVCK+4  GET R15-R0 BACK
         CH    R15,=H'16'    RETRY ?
         BNE   CCNZNO2       NO
         LTR   R2,R2         RESTART ADDRESS AVAILABLE ?
         BNZR  R2            YES; RETRY THE REQUEST
CCNZNO2  CVD   R15,MTSDDB    CONVERT R15 CODE
         ED    CCNZR15,MTSDDB+6
         CVD   R0,MTSDDB     CONVERT R0 CODE
         ED    CCNZR0,MTSDDB+6
         ST    R1,MTSDDB     STASH RPL ADDRESS
         UNPK  CCNZRPL(L'CCNZRPL+1),MTSDDB+1(4)
         TR    CCNZRPL,MDBHEX-C'0'   MAKE PRINTABLE HEX
         MVI   CCNZRPL+L'CCNZRPL,C' '
         ST    R9,MTSDDB     STASH BAL ADDRESS
         UNPK  CCNZR9(L'CCNZR9+1),MTSDDB+1(4)
         TR    CCNZR9,MDBHEX-C'0'   MAKE PRINTABLE HEX
         MVI   CCNZR9+L'CCNZR9,C' '
         WTO   MF=(E,CCNZWTO)  PRINT
         ABEND 202,DUMP      AND QUIT
CCNZWTO  DC    Y(CCNZWTON-CCNZWTO),X'8000'  WTO PFX/MCS ROUT/CDE
         DC    C'VTAM MACRO ERROR :'
CCNZR15  DC    X'40202120',C' (R15)'
CCNZR0   DC    X'40202120',C' (R0)'
CCNZRPL  DC    C'XXXXXX',C' @ '
CCNZR9   DC    C'XXXXXX',C' '
CCNZWTON DC    X'00000020'   ROUTCDE=11
CCNZ256  DC    H'256'        FAKE BASE OFFSET
         DROP  ,
         USING WORKBASE,R10  DECLARE (CALLER'S) SECOND BASE
         USING MTSWORK,R7
         SPACE 1
*        DYNAMIC ALLOCATION ROUTINE FOR PRINT FILES
*        RETURNS ON R9, ZEROES R15 IF OK, PUTS DDNM INTO MTSPRDD
*
SOUTALOC MVC   MTSSTADB(PATALOLN),PATALOC  MOVE ALLOCATE PATTERN
         LA    R14,DYNRB-DYNRBPTR+MTSSTADB
         STCM  R14,7,DYNRBPTR+1-DYNRBPTR+MTSSTADB
         LA    R14,DYNTXPL-DYNRBPTR+MTSSTADB
         ST    R14,DYNTXTPP-DYNRBPTR+MTSSTADB
         LA    R14,TXDDN-DYNRBPTR+MTSSTADB
         ST    R14,DYNTXPL-DYNRBPTR+MTSSTADB
         LA    R1,DYNRBPTR-DYNRBPTR+MTSSTADB
         DYNALLOC ,          ALLOCATE IT
         BXH   R15,R15,0(R9)  RETURN WITH ERROR
         MVC   MTSPRDD,TXDDN+6-DYNRBPTR+MTSSTADB  RETURN DDN
         XC    MTSSTADB,MTSSTADB  CLEAR FOR CLSDST               87300
         BR    R9            RETURN
TXSOUT   DC    Y(24,1,1),C'A'  READ-ONLY TEXT UNITS
TXFREE   DC    Y(28,0)
         SPACE 1
*        MOVED PATTERN
PATALOC  DS    0A
DYNRBPTR DC    X'80',AL3(DYNRB)  REQUEST POINTER
DYNRB    DC    AL1(20,S99VRBAL,S99NOCNV+S99NOMNT,0) +S99JBSYS FOR DEBUG
         DC    Y(0,0)        ERROR, INFO
DYNTXTPP DC    A(DYNTXPL)    TEXT UNIT LIST POINTER
         DC    A(0,0)        RESERVED
DYNTXPL  DC    A(TXDDN,TXFREE),X'80',AL3(TXSOUT)  FREE=CLOSE
TXDDN    DC    Y(85,1,8),CL8' '  RETURNED DDNAME
PATALOLN EQU   *-PATALOC     LENGTH TO MOVE
         SPACE 1
SCANPARM MVC   INITDDB,=CL8' '  PRE-BLANK RESULT
         LA    R15,INITDDB
         LA    R0,L'INITDDB
SCANPATB CLI   0(R6),C' '    LEADING BLANK ?
         BE    SCANPASB      YES; SKIP IT
         CLI   0(R6),C','    OR COMMA ?
         BNE   SCANPALS      NO; PROCESS
SCANPASB BXLE  R6,R4,SCANPATB  TRY FOR MORE
         BR    R14           NOTHING
SCANPALS ST    R6,INITR6     SAVE START OF SCAN                  87130
SCANPALM CLI   0(R6),C' '    TRAILING BLANK ?
         BER   R14           YES; RETURN
         CLI   0(R6),C','    OR COMMA ?
         BER   R14           YES
         MVC   0(1,R15),0(R6)  COPY ONE BYTE
         AR    R15,R4        UP ONE
         BXH   R6,R4,0(R14)  RETURN IF EXHAUSTED
         BCT   R0,SCANPALM   ELSE TRY AGAIN
         BR    R14           ALL DONE
INITDDB  DC    D'0'          WORK AND PARSE AREA
INITR6   DC    A(0)          START OF PARAMETER                  87130
         LTORG ,
         SPACE 2
         PRINT GEN
         DC    C'PAD1'       *****DEBUG*****                     88034
MTVECT   MAPMTV TYPE=        EXPAND BASIC CONTROL BLOCK
@SCREENS DC    A(0)          ADDRESS OF LOADED @SCREENS RTNE     89334
@SERVICE DC    A(0)          ADDRESS OF @SERVICE IF SVC=0        90186
         PRINT NOGEN
         DC    C'PAD2'       *****DEBUG*****                     88034
@KERMIT  DC    A(0)          KERMNET ENTRY POINT                 90206
         SPACE 1
MYACB    ACB   AM=VTAM,APPLID=MYAPPLID,MACRF=LOGON,EXLST=VTAMEXL
INPNIB   NIB   MODE=RECORD,PROC=TRUNC
READRPL  RPL   AM=VTAM,ACB=MYACB,AREA=INPBUFF,AREALEN=INPBUFLN,        *
               RTYPE=DFSYN,OPTCD=(ASY,ANY,CA),NIB=INPNIB,              *
               BRACKET=(NBB,NEB),EXIT=XREAD
CAN804   RPL   AM=VTAM,ACB=MYACB,STYPE=REQ,CONTROL=DATA,POST=RESP,     *
               OPTCD=SYN,RESPOND=(NEX,FME,NRRN),NIB=INPNIB
INITNIB  NIB   MODE=RECORD
INITRPL  RPL   ACB=MYACB,AM=VTAM,NIB=INITNIB,OPTCD=(SYN,Q)
         SPACE 1
VTAMEXL  EXLST AM=VTAM,LOGON=XLOGON,SYNAD=XSYNAD,LERAD=XLERAD,         *
               TPEND=XTPEND,LOSTERM=XLOSTERM
CONID    DC    AL1(1)        QEDIT CIB CONSOLE ID
MYCSCB   DC    A(0)          MY CSCB FOR QEDIT
MAXERRCT DC    F'1000'       MAXIMUM CONSECUTIVE ERRORS ALLOWED
FROGFACE DC    CL8'FROGFACE' SUBTASK INITIALIZATION INTERFACE
MDBHEX   DC    C'0123456789ABCDEF'
         SPACE 1
         PRINT GEN
*        ALL TEXT FROM PATWORK THROUGH PATSIZE IS COPIED TO MTSWORK
*        AT MTSNIB.
PATWORK  MAPMTS PFX=PAT,TYPE=,FLAVOR=KERMIT,EPLOC=FROGFACE       89328
PATSIZE  EQU   *-PATWORK     LENGTH TO COPY
         PRINT NOGEN
         SPACE 1
PATPRT   DC    0A(0),X'0000FF88',X'00AF0000',X'00000000',X'00',C'PRT'
         DC    X'1250100B',A(PATPRTX,0,0,0,0,0)
PATPRTX  DC    5A(0),A(PATPRTD)
PATPRTD  DC    2A(0)         DDT
         SPACE 1
FREEQUE  DC    A(0,0)        QUEUE HEAD FOR FREE WORK AREAS
WORKQUE  DC    A(0,0)        QUEUE HEAD FOR FULL FUNCTIONS
         SPACE 1
FLAGS    DC    X'00'         GLOBAL FLAGS
FGDOWN   EQU   X'80'           SHUT-DOWN IN PROGRESS
FGQUIT   EQU   X'40'           GRACEFUL SHUT-DOWN                90231
FGSYS    EQU   X'20'           RESTRICT TO SYSTEMS USERS
FGLOGON  EQU   X'10'           REQUIRE LOGON                     87350
FGTEST   EQU   X'08'           TEST MODE (KERMNEW, NOT KERMNET)  91078
FGREAD   EQU   X'02'           RECEIVE ISSUED IF ON
         SPACE 2
DCREAD   DC    CL8'        '       00
         DC    CL8'PFK 13  '       01
         DC    CL8'PFK 14  '       02
         DC    CL8'PFK 15  '       03
         DC    CL8'PFK 16  '       04
         DC    CL8'PFK 17  '       05
         DC    CL8'PFK 18  '       06
         DC    CL8'PFK 19  '       07
         DC    CL8'PFK 20  '       08
         DC    CL8'PFK 21  '       09
         DC    CL8'PFK 22  '       0A
         DC    CL8'PFK 23  '       0B
         DC    CL8'PFK 24  '       0C
         DC    CL8'        '       0D
         DC    CL8'        '       0E
         DC    CL8'        '       0F
         DC    CL8'        '       10
         DC    CL8'        '       11
         DC    CL8'        '       12
         DC    CL8'        '       13
         DC    CL8'        '       14
         DC    CL8'        '       15
         DC    CL8'        '       16
         DC    CL8'        '       17
         DC    CL8'        '       18
         DC    CL8'        '       19
         DC    CL8'        '       1A
         DC    CL8'        '       1B
         DC    CL8'        '       1C
         DC    CL8'        '       1D
         DC    CL8'        '       1E
         DC    CL8'        '       1F
         DC    CL8'NO AID  '       20
         DC    CL8'        '       21
         DC    CL8'        '       22
         DC    CL8'        '       23
         DC    CL8'        '       24
         DC    CL8'        '       25
         DC    CL8'OPID RDR'       26
         DC    CL8'MAG RDR '       27
         DC    CL8'NO AID  '       28
         DC    CL8'        '       29
         DC    CL8'        '       2A
         DC    CL8'PA 3 KEY'       2B
         DC    CL8'PA 1 KEY'       2C
         DC    CL8'CLEAR'          2D
         DC    CL8'CANCEL'         2E   CANCEL (PA2) KEY
         DC    CL8' '              2F
         DC    CL8'TEST-REQ'       30
         DC    CL8'PFK 1'          31
         DC    CL8'PFK 2'          32
         DC    CL8'PFK 3'          33
         DC    CL8'PFK 4'          34
         DC    CL8'PFK 5'          35
         DC    CL8'PFK 6'          36
         DC    CL8'PFK 7'          37
         DC    CL8'PFK 8'          38
         DC    CL8'PFK 9'          39
         DC    CL8'PFK 10'         3A
         DC    CL8'PFK 11'         3B
         DC    CL8'PFK 12'         3C
         DC    CL8'ENTER'          3D
         DC    CL8'LIGHTPEN'       3E
         DC    CL8' '              3F
         SPACE 1
INSPFSOH DC    A(INSPFSO1,INSPFSO9)  SOH TYPES
INSPFSTX DC    A(INSPFST1,INSPFST9)  STX TYPES
         SPACE 1
INSPFSO1 DC    X'C1F2C2F3C3F4C4F5C5F6C6F7C7F8C8F9C97A'  A-I   2-10
         DC    X'817C82C183C284C385C486C587C688C789C8'  A-I L.C.12-20
         DC    X'D17BD27CD3C1D4C291C9924A934B944C7CF1E07B797B'
         DC    X'D5C3D6C4D7C5D8C6D9C7E2C8E3C9E44AE54BE64C'  O-W
INSPFSO9 DC    X'617B'       /
INSPFST1 DC    X'C1F1C2F2C3F3C4F4C5F5C6F6C7F7C8F8C9F9'   A-I
         DC    X'D17AD27BD37CD4C1D5C2D6C3D7C4D8C5D9C6'   J-R
         DC    X'E2C7E3C8E4C9E54AE64BE74C'               S-X
         DC    X'81F182F283F384F485F586F687F788F889F9'   A-I
         DC    X'917A927B937C94C195C296C397C498C599C6'   J-R
         DC    X'A2C7A3C8A4C9A54AA64B'                   S-W
INSPFST9 DC    X'A74C'                                     X
         SPACE 1
INPBUFF  DC    28CL132' '    SPACE FOR MODEL 5 + OVERHEAD
INPBUFLN EQU   *-INPBUFF     SIZE OF BUFFER
         SPACE 2
         PRINT GEN
         MAPMTS MODE=VTAM,FLAVOR=KERMIT    EXPAND A WORK AREA    89328
         PRINT NOGEN
         SPACE 1
         ISTDNIB ,
         SPACE 1
         IFGRPL AM=VTAM
         SPACE 1
         PRINT NOGEN
         ISTDBIND ,
         SPACE 1
         CVT   DSECT=YES
         TITLE 'N E T C O M M  ***  SIGNON/SIGNOFF PROCESSING'   89328
NETCOMM  CSECT ,                                                 89328
         DROP  ,                                                 89334
&ZZ@BLUK SETB  0             REQUEST LOCAL EXPANSION OF 'BLOOK' CODE
SIGNPROC XHEAD SAVE=RENT,EXH=NO,RPL=NO,RENT=SIGNWRKL,MODE=OS     89334
         USING SIGNWORK,R13  DECLARE IT                          89334
         LR    R8,R6         COPY MTS START                      89334
         USING MTSWORK,R8    DECLARE IT                          89334
         AIF   ('&LOCAL' NE 'PID').NOLOUDS                       93263
         L     R11,MTSLOUD                                       89334
         USING A$LSECT,R11   DECLARE LOCAL USER DATA             89334
.NOLOUDS LA    R2,18*4(R13)  GET WORK AREA AFTER SAVE            89334
         LA    R3,SIGNWRKL-18*4  AND LENGTH                      89334
         SLR   R15,R15                                           87357
         MVCL  R2,R14        CLEAR ALL OF GOTTEN STORAGE         87357
         MVI   BLANKS-1,C' '                                     89334
         MVC   BLANKS,BLANKS-1  MAKE LOTS OF BLANKS              89334
         MVC   UADSTADB(SGNALOLN),SGNALOC  MOVE ALLOCATE PATTERN 87357
         LA    R14,DYSRB-SGNALOC+UADSTADB                        87357
         STCM  R14,7,DYSRBPTR+1-SGNALOC+UADSTADB                 87357
         LA    R14,DYSTUPL-SGNALOC+UADSTADB                      87357
         ST    R14,DYSTXTPP-SGNALOC+UADSTADB                     87357
         LA    R14,TUDDN-SGNALOC+UADSTADB                        87357
         ST    R14,DYSTUPL-SGNALOC+UADSTADB                      87357
         LA    R14,LISTFD    POINT TO FD LIST                    87159
         LA    R15,MAPFID    POINT TO INPUT AREA                 87159
         LA    R0,3*(MAPFAC-MAPFID)  WORK AREA LENGTH            87159
         STM   R14,R0,FDWFDA  MAKE CALLING PARAMETER LIST        87159
         SPACE 1
         BAL   R9,INIT       GO TO INITIALIZATION                87357
         B     *+4(R15)      BRANCH BY RETURN CODE               87357
           B   REPENT        0 - SOLICIT INPUT                   87357
           B   POSTPROC      4 - PROCESS MODIFIED DATA AND EXIT  87357
           B   MODEXIT       8 - EXIT WITHOUT CHANGE             87357
           B   GETFAIL      12 - LOCK AND EXIT                   89334
           B   GETLOST      16 - LOCK, EXIT AND LOGOFF           87357
         SPACE 1                                                 87357
REPENT   TM    FIDFG,FIDFTXT  DID INIT PRIME USERID ?            87357
         BNZ   ALLCOMON      YES; GO TO VERIFICATION             89334
         SPACE 1                                                 87350
SHOWFAIL DS    0H                                                87350
REPEAT   LA    R1,MTSCNECB                                       89328
         TM    0(R1),X'40'   CANCEL REQUESTED ?
         BNZ   GETFAIL       YES - EXIT                          89334
         TM    MTSSIMFG,MTSSICRT+MTSSIFSC  FULL-SCREEN SUPPORTED ?
         BO    FULLSHOW      YES; USE SCREEN MACROS              87159
         LA    R14,MTSBUFF+6  LOAD WORK BUFFER (ROOM FOR CR, RDW)
         MVC   0(L'CRLF,R14),CRLF  START ON A NEW LINE           89334
         LA    R14,L'CRLF(,R14)  ADVANCE                         89334
         ICM   R0,3,ERRMSGLN ANY ERROR MESSAGE ?                 89334
         BNP   REPSHOK                                           87357
         MVC   0(L'ERRMSG,R14),ERRMSG  DISPLAY MESSAGE           87357
         AH    R14,ERRMSGLN  SKIP PAST MESSAGE                   89334
         MVC   0(L'CRLF,R14),CRLF  MAKE NEW LINE                 89334
         LA    R14,L'CRLF(,R14)  ADVANCE                         89334
REPSHOK  MVC   0(REQ1LEN,R14),REQ1  MAKE REQUEST LINE            89334
         LA    R14,REQ1LEN(,R14)                                 89334
         MVC   0(L'CRLF,R14),CRLF  MAKE ANOTHER LINE             89334
         LA    R14,L'CRLF(,R14)  ADVANCE                         89334
         MVC   0(REQ2LEN,R14),REQ2  MAKE SECOND LINE             89334
         LA    R14,REQ2LEN(,R14)   ADVANCE                       89337
         MVC   0(L'CRLF,R14),CRLF  MAKE NEW LINE                 89334
         LA    R14,L'CRLF(,R14)                                  89334
         TM    MTSSECFG,MTSSECRQ  RESTRICTED TO SYSTEMS USERS ?  89328
         BZ    REPSHOW       NO                                  87357
         MVC   0(REQLESLN,R14),REQLESS  MAKE SECOND LINE         87357
         LA    R14,REQLESLN(,R14)  ADVANCE AGAIN                 89334
         MVC   0(L'CRLF,R14),CRLF  MAKE NEW LINE                 89334
         LA    R14,L'CRLF(,R14)                                  89334
REPSHOW  MVC   0(3,R14),=C' : '  MAKE PROMPT                     89334
         LA    R14,3(,R14)   SKIP PROMPT                         89334
         LA    R15,MTSBUFF+2  POINT TO RDW                       89334
         SR    R14,R15       GET BUFFER LENGTH                   89334
         SLL   R14,16        LEFT-JUSTIFY                        89334
         STCM  R14,15,0(R15)  MAKE RDW                           89334
         XC    MTSUCBX(6*4),MTSUCBX  BUILD FAKE IOPL             89334
         ST    R15,MTSUCBX+5*4  MAKE LINE ADDRESS                89334
         MVI   MTSUCBX+4*4+2,X'01'  MAKE 'ASIS'                  89334
         LA    R0,MTSUCBX+4*4  MAKE POINTER                      89334
         ST    R0,MTSUCBX+3*4  MAKE POINTER POINTER              89334
         LA    R1,MTSUCBX    POINT TO 'IOPL'                     89334
         L     R15,MTSPUTLN  GET PUT ADDRESS                     89334
         BALR  R14,R15       DISPLAY IT                          89334
         CH    R15,=H'4'     ERROR ?                             89334
         BH    GETFAIL       YES; QUIT                           89334
         MVI   MTSUCBX+4*4+2,X'80'  SET GET EDIT                 89334
         L     R15,MTSGETLN  GET GET ADDRESS                     89334
         BALR  R14,R15       GET SOME INPUT                      89334
         CH    R15,=H'4'     ERROR ?                             89334
         BH    GETFAIL       YES; GET OUT                        89334
         L     R1,MTSUCBX+5*4  GET INPUT LINE ADDRESS            89334
         MVC   REPLY,BLANKS  CLEAR INPUT ADDRESS                 89334
         SLR   R15,R15                                           89334
         ICM   R15,3,0(R1)   GET INPUT LENGTH                    89334
         SH    R15,=H'5'     ALLOW FOR LENGTH OF LENGTH          89334
         BNP   REPEAT        MAKE USER DO IT AGAIN               89334
         EX    R15,REPMVC    MOVE TEXT                           89334
         MVC   ERRMSG,BLANKS RESET MESSAGE                       87357
         XC    ERRMSGLN,ERRMSGLN                                 87357
         XC    MAPFID(TRYCOUNT-MAPFID),MAPFID  CLEAR INPUT       89337
         MVI   FDWOPT,0      RESET                               89337
         MVI   FDWFG,0       DITTO                               89337
         LA    R5,REPLY
         BLOOK T=QUICKSY,R=REPLY,B=(R12),PFX=HERB                89334
         BAL   R14,GETONE                                        87350
           B   SHOWFAIL      MISSING REQUIRED PARAMETER          89337
         CLC   MTSDDB(7),FASTOFF+4  'SIGNOFF' ?                  89349
         BE    GETLOST       YES                                 89349
         CLC   MTSDDB(7),FASTOFF2   'SIGNOUT' ?                  89349
         BE    GETLOST                                           89349
         CLC   =C'SIGN ',MTSDDB  SHORT FORM ?                    89349
         BE    GETLOST                                           89349
         CLC   =C'LOGOFF',MTSDDB  ALTERNATE FORM ?               89349
         BE    GETFAIL       YES; QUIT                           89349
         CLC   =C'END ',MTSDDB   JUST FORGET IT ?                89349
         BE    GETFAIL       YES                                 89349
         MVC   PSWUSER,MTSDDB   MOVE IT                          89334
         MVI   FIDFG,FIDFTXT                                     89337
         BAL   R14,GETONE                                        87350
           B   SHOWFAIL      MISSING REQUIRED PARAMETER          89337
         MVC   PSWPSWD,MTSDDB   MOVE ACCOUNT                     89334
         MVI   FPSFG,FPSFTXT                                     89337
         BAL   R14,GETONE                                        87350
           B   ALLCOMON      USE DEFAULT ACCOUNT NUMBER          89337
         MVC   PSWACCT,MTSDDB    GET ACCOUNT                     89334
         MVC   FACTEXT,MTSDDB  COPY TO WORK AREA                 90179
         MVI   FACLEN,8      SET MAX LENGTH                      90179
         MVI   FACFG,FACFTXT                                     89337
         CLI   PSWUSER,C'0'  NUMERIC USER-ID ?                   87159
         BL    ALLCOMON      NO                                  87159
         CLI   PSWACCT,C'0'  NON-NUMERIC ACCOUNT ?               87159
         BNL   ALLCOMON      NO                                  87350
         SWAP  PSWACCT,PSWUSER                                   87350
         B     ALLCOMON      GO TO VERIFY                        87350
REPMVC   MVC   REPLY(0),4(R1)  MOVE INPUT TEXT                   89334
         SPACE 2                                                 87159
FULLSOME MVI   FDWOPT,0      RESET DISPLAY                       87159
FULLSHOW SCRINIT FDW         (RE-)INITIALIZE THE PAGE            89328
         SCRLIST FDW         FORMAT THE PAGE                     89328
         LA    R1,MTSCNECB                                       89328
         TM    0(R1),X'40'   CANCEL REQUESTED ?                  87159
         BNZ   MODEXIT       YES - EXIT                          87159
         OI    MTSSEDFG,MTSSENCO  DON'T MUSS UP MY COLORS        89328
         SCRPAGE FDW         DISPLAY THE PAGE AND SOLICIT INPUT  89328
         BM    GETFAIL       FORCE LOGOFF - ERROR OR NO RESPONSE 89334
         LA    R5,FDWICOD    POINT TO CONVERTED AID              87159
         OI    FDWOPT,FDWOKEEP  KEEP INPUT                       87159
         MVC   ERRMSG,BLANKS RESET MESSAGE                       87357
         XC    ERRMSGLN,ERRMSGLN                                 87357
         BLOOK T=QUICKIES,R=FDWICOD,B=(R12),PFX=HERB  NON-DATA   89334
         NI    FDWFG,255-FIDFERR  RESET ERROR BITS               87166
         NI    FPSFG,255-FPSFERR  IN PASSWORD ALSO               87166
         NI    FACFG,255-FPSFERR                                 87166
         NI    FIDFG,255-FPSFERR                                 87166
         SCRANAL FDW         ANALYZE INPUT                       89328
         MVC   FDWCUR,FIDATB PUT CURSOR IN USERID FIELD          87159
         TM    FIDFG,FIDFTXT   INPUT AVAILABLE ?                 87159
         BZ    FULLSHOW      NO; SOLICIT IT                      87159
         MVC   FDWCUR,FPSATB   ELSE MOVE CURSOR TO PASSWORD      87357
         TM    FPSFG,FPSFTXT   PASSWORD ?                        87159
         BZ    FULLSHOW      NO                                  87159
         OI    FDWOPT,FDWMKEEP  RETAIN TEXT LENGTH AND FLAGS     87159
         TM    FDWFG,FIDFERR  ANY ERROR ?                        87165
         BNZ   FULLSHOW      YES; MAKE USER FIX IT               87165
         SCRMOVE FDW         MERGE                               89328
         SPACE 1                                                 87357
ALLCOMON BAL   R9,VERIFY     INVOKE INSTALLATION'S VERIFICATION  87357
         B     *+4(R15)      TAKE APPROPRIATE ROUTE              87357
           B   POSTPROC      0 - ACCEPT AND POST-PROCESS         87357
           B   MODEXIT       4 - LEAVE THINGS AS IS; EXIT        87357
           B   GETFAIL       8 - LOCK AND EXIT                   89334
           B   GETLOST      12 - LOCK, EXIT AND LOG OFF          87357
           B   REPEAT       16 - MAKE USER FIX BAD INPUT         87357
         SPACE 1                                                 87350
GETONE   MVC   MTSDDB,BLANKS CLEAR WORK WORD                     89334
         LR    R2,R14        SAVE RETURN                         87350
         BLOOK T=NULLBTAB,R=REPLY,B=(R12),PFX=HERB  NON-BLANK ?  89334
         CLI   0(R5),C' '    ANY ?                               87350
         BNHR  R2            NO; INVALID INPUT                   89337
         LR    R14,R2        RESTORE (BAD) RETURN ADDRESS        89349
         LA    R1,MTSDDB                                         89334
         LA    R0,8          NUMBER OF ENTRIES                   87350
GETONCE  CLI   0(R5),C','    SEPARATING COMMA ?                  87350
         BE    GETDONE       YES                                 89337
         CLI   0(R5),C'/'    SEPARATING SLASH ?                  89337
         BE    GETDONE       YES; ALTERNATE SEPARATOR            89337
         CLI   0(R5),C' '    TRAILING BLANK ?                    87350
         BE    GETDONE                                           87350
         OC    0(1,R1),0(R5)   COPY AND UPPER-CASE ONE BYTE      89337
         LA    R1,1(,R1)                                         87350
         LA    R5,1(,R5)                                         87350
         LA    R14,4(,R2)    SET FOR GOOD RETURN                 89349
         BCT   R0,GETONCE                                        87350
GETDONE  CLI   0(R5),C','    TRAILING COMMA ?                    87350
         BE    GETDONED      YES; SKIP IT                        89337
         CLI   0(R5),C'/'    ALTERNATE ?                         89337
         BNER  R14           NO; JUST RETURN                     87350
GETDONED LA    R5,1(,R5)     SKIP THE COMMA                      89337
         BR    R14           AND RETURN                          87350
         SPACE 1                                                 87166
POSTPROC TM    MTSSECFG,MTSSECRQ  SYSTEMS ONLY ?                 89328
         BZ    NOTREST       NO; GIVE IT A REST                  87350
         TM    PSWPRIVY,PRFSYS    DOES USER QUALIFY ?            90050
         BZ    GETLOST       NO                                  87350
NOTREST  MVC   MTSUID,PSWUSER                                    87166
         MVC   MTSACCT,PSWACCT                                   87166
         OI    MTSSECFG,MTSSECON  SHOW USER LOGGED ON            89328
         MVC   MTSSAVPR,PSWPRIVY  PROPAGATE PRIVILEGES           90050
         BAL   R9,TERMIN8    GO TO CLEAN-UP                      87357
         BXH   R15,R15,GETFAIL  KILL AND EXIT                    89334
         B     SIGNDONE      OK                                  89334
         SPACE 1                                                 87357
EOF      LA    R1,MSGEOF                                         87357
         B     COMERR                                            87357
OOPS     LA    R1,MSGERR                                         87357
COMERR   LA    R14,MTSBUFF+6                                     89334
         SLR   R15,R15                                           87357
         IC    R15,0(,R1)    GET MSG LENGTH                      87357
         EX    R15,MSGMVC                                        87357
         LA    R14,0(R15,R14)  ADVANCE                           89334
         MVC   0(L'CRLF,R14),CRLF  MAKE NEW LINE                 89334
         LA    R14,L'CRLF(,R14)                                  89334
         LA    R15,MTSBUFF+2  POINT TO RDW                       89334
         SR    R14,R15       GET BUFFER LENGTH                   89334
         SLL   R14,16        LEFT-JUSTIFY                        89334
         STCM  R14,15,0(R15)  MAKE RDW                           89334
         XC    MTSUCBX(6*4),MTSUCBX  BUILD FAKE IOPL             89334
         ST    R15,MTSUCBX+5*4  MAKE LINE ADDRESS                89334
         MVI   MTSUCBX+4*4+2,X'01'  MAKE 'ASIS'                  89334
         LA    R0,MTSUCBX+4*4  MAKE POINTER                      89334
         ST    R0,MTSUCBX+3*4  MAKE POINTER POINTER              89334
         LA    R1,MTSUCBX    POINT TO 'IOPL'                     89334
         L     R15,MTSPUTLN  GET PUT ADDRESS                     89334
         BALR  R14,R15       DISPLAY IT                          89334
         B     GETLOST       AND QUIT                            87357
MSGMVC   MVC   0(0,R14),1(R1)  MOVE MESSAGE                      87357
         SPACE 1                                                 87159
MODEXIT  TM    MTSSECFG,MTSSECON  USER LOGGED ON ?               89334
         BNZ   SIGNEXIT      YES; TAKE GOOD EXIT                 89334
         B     GETFAIL       ELSE LEAVE PROGRAM                  89334
         SPACE 1                                                 89334
GETLOST  OI    MTSFLGT,MTSFXEND  REQUEST SHUT-DOWN               89328
         L     R1,0(,R13)    GET OLD SAVE AREA                   89334
         LA    R0,8          SET LOGOFF RETURN CODE              89334
         ST    R0,16(,R13)   SET RC=8                            89334
         B     GETFAIL2                                          89334
GETFAIL  L     R1,0(,R13)    GET OLD SAVE AREA                   89334
         LA    R0,4          SET QUIT CODE                       89334
         ST    R0,16(,R13)   SET RC=4                            89334
GETFAIL2 XC    MTSUID,MTSUID  RESET USER-ID                      89334
         XC    MTSACCT,MTSACCT                                   87166
         MVI   MTSSAVPR,0    DENY SPECIAL PRIVILEGES             90050
         B     SIGNDON2                                          89334
         SPACE 1                                                 89334
SIGNDONE L     R1,0(,R13)    GET OLD SAVE AREA                   89334
         XC    16(4,R1),16(R1)  SET RC=0                         89334
SIGNDON2 BAL   R9,TERMIN8    GO TO CLEAN-UP                      89334
         BXH   R15,R15,GETFAIL  KILL AND EXIT                    89334
         LA    R15,CLEANUP   GET CLEANUP ROUTINE                 87357
         BALR  R14,R15       GO TO CLEANUP                       87357
         XEXIT MODE=OS                                           89334
         EJECT ,                                                 87159
FASTOFF  BTAB  SIGNOFF,GETLOST,BASE=SIGNPROC  QUICK EXIT REQUEST 89334
FASTOFF2 BTAB  SIGNOUT,GETLOST                                   87362
SLOWOFF  BTAB  OUT,GETLOST   'SIGN OUT'                          87362
         BTAB  OFF,GETLOST   'SIGN OFF'                          87362
NULLBTAB BTAB  *END                                              89334
         SPACE 1                                                 87362
         USING SIGNWORK,R13                                      89334
QUICKSY  BTAB  *CLEAR,REPEAT,BASE=SIGNPROC  NON-CRT TABLE        89334
QUICKIES BTAB  *PA1,GETFAIL,BASE=SIGNPROC   COMMON TABLE         89334
         BTAB  *PA2,GETLOST                                      87350
         BTAB  *CLEAR,FULLSOME         RE-INITIALIZE             87159
         BTAB  *PF3,GETFAIL            TSO-STYLE EXIT            89334
         BTAB  *PF6,GETLOST  EXIT WITH LOGOFF                    87350
         BTAB  *END                                              87159
         SPACE 1                                                 87159
LISTFD   FD    '> > > > > > > > > > > > >',NL,BLUE               87350
         FD    'N E T C O M M  ',WHITE,PAD                       89328
         FD    'Sign-on',RED,PAD                                 87350
         FD    '< < < < < < < < < < < < <',BLUE                  87350
         FDOPT SBA=(3,1)                                         87350
         FD    'Please supply your user-id',GREEN                87350
         AIF   ('&LOCAL' NE 'PID').NOWYLFD                       93263
         FD    '(Wylbur or TSO)',GREEN                           87350
.NOWYLFD FD    ', your password and account.',GREEN              87357
         FDOPT TURQ,SBA=(4,20)                                   87159
         FD    'To exit from this function, use PA1 or PFK 3.',PINK
         FD    'Use PA2 or PFK 6 to sign off',NL,YELLOW,RADJ,LEN=48
         FDCLI ERRMSGLN+1,0,BE=SKIPMSG                           87357
         FD    ERRMSG,DEB,RED,NL                                 87357
SKIPMSG  EQU   *                                                 87357
IDFD     VARFD FID,PSWUSER,'User-id'                             87159
         FDTM  MTSSECFG,MTSSECRQ,BZ=NOSYSFD                      89328
         FD    REQLESS,REQLESLN,RED                              87357
NOSYSFD  EQU   *                                                 87350
PSFD     VARFD FPS,PSWPSWD,'Password',NONDISP                    87159
ACFD     VARFD FAC,PSWACCT,'Account number'                      87159
         FD    *END                                              87159
         SPACE 1
CRLF     DC    X'0D25'       CARRIAGE RETURN / LINE FEED         89334
         SPACE 1                                                 87350
REQ1     DC    C'>>>>> NETCOMM sign-on <<<<<'                    87350
REQ1LEN  EQU   *-REQ1                                            87350
REQ2     DC    C'Please supply your user-id, password and account, sepa*
               rated by commas.'                                 87350
REQ2LEN  EQU   *-REQ2                                            87350
REQLESS  DC    C'  LOGON restricted to SYSTEMS GROUP'            87357
REQLESLN EQU   *-REQLESS                                         87357
MSGEOF   DC    AL1(28-1),C'Unexpected End-File on input'         87357
MSGERR   DC    AL1(37-1),C'System dataset or control block error'
         TITLE 'N E T C O M M  ***  FUNCTION INITIALIZATION'     89328
*        CALLED AT ENTRY, AFTER WORK AREA IS GETMAINED.          87357
*        R7-R13 ARE IN USE AND MUST NOT BE ALTERED.              87357
*        RETURN TO R9 WITH R15 SET TO:  0 - PROCEED              87357
*              4 - PROCESS MODIFIED OPTIONS AND EXIT             87357
*              8 - EXIT WITHOUT CHANGING ANYTHING                87357
*             12 - LOCK AND EXIT                                 87357
*             16 - LOCK, EXIT AND LOG OFF                        87357
*                                                                87357
INIT     DS    0H            INITIALIZATION                      87357
         AIF   ('&LOCAL' NE 'PID').INITOTH                       93263
         XC    A$SVCRB(A$SLENRB),A$SVCRB  CLEAR PARM             87357
         MVC   A$SID,=C'A$RB'  IDENTIFY                          87357
         MVC   A$SFCTN(3),=AL1(A$SFLOUD,A$SONL,A$SAKEY+A$SBUILD) 89337
         LA    R0,ERRMSGLN                                       87357
         ST    R0,A$SERMSG                                       87357
         MVC   A$SBUFR,MTSLOUD  SET LOUD ADDRESS                 89337
         TM    A$LFLG1,A$LFGOK+A$LFGAL  USER WITH AUTO-LOGON ?   88234
         BNO   INITCC0       NO                                  88234
         MVC   PSWUSER,A$UUID   MOVE USER ID                     87357
         LA    R14,A$LCACT   POINT TO CURRENT LOGON ACCOUNT      90231
         CLI   0(R14),C'0'   IS THERE ONE ?                      90231
         BNL   *+8           YES                                 90231
         LA    R14,A$UACCT   ELSE POINT TO USER'S DEFAULT        90231
         MVC   PSWACCT,0(R14)  USE APPROPRIATE ACCOUNT NUMBER    90231
         MVI   MTSSAVPR,0                                        90050
         TM    A$UPRIVY,A$UPFSYS  SYSTEMS PRIVILEGES ?           90050
         BZ    *+8           NO                                  90050
         OI    MTSSAVPR,PRFSYS  SET SYSTEMS                      90050
         MVC   PSWPRIVY,MTSSAVPR  SAVE OLD PRIVILEGES            90050
         LA    R15,4         SET TO ACCEPT OLD OPTIONS           87357
         BR    R9            TAKE ACCEPTANCE EXIT                87357
.INITOTH SPACE 1                                                 87357
INITCC0  SLR   R15,R15                                           87357
         STH   R15,TRYCOUNT  CLEAR USER'S ATTEMPT COUNTER        87357
         CLI   MTSUID,C' '   IS THERE A USER ID ?                89334
         BNHR  R9            NO; RETURN TO SOLICIT ONE           89334
         LA    R15,8         SET TO EXIT AS-IS                   87357
         TM    MTSSECFG,MTSSECON  LOGGED ON ?                    89328
         BNZR  R9            YES; NOTHING TO DO                  87357
         MVC   PSWUSER,MTSUID                                    89334
         MVC   PSWACCT,MTSACCT                                   89334
         MVI   FIDLEN,8      PRESET (WRONG) LENGTH               87357
         OI    FIDFG,FIDFTXT  SHOW ID PRESENT                    87357
         SLR   R15,R15       CONTINUE PROCESSING                 87357
         CLI   PSWACCT,C' '  ACCOUNT PRE-LOADED ?                87357
         BNHR  R9            NO; CONTINUE                        87357
         OI    FACFG,FACFTXT  SET TEXT PRESENT                   87357
         MVI   FACLEN,8      PRESET LENGTH                       87357
         BR    R9            RETURN TO CALLER                    87357
         TITLE 'N E T C O M M  ***  USER VERIFICATION'           89328
*        CALLED AFTER USER HAS SUPPLIED UID AND PSWD.            87357
*        R7-R13 ARE IN USE AND MUST NOT BE ALTERED.              87357
*                                                                87357
*        THIS FUNCTION IS ALSO CALLED WITHOUT A PASSWORD WHEN    87357
*        ENTRY IS TO MODULE EXHASIGN (ALTERNATE ENTRY TO THIS    87357
*        MODULE) FOR PURPOSE OF AUTOMATIC LOGON AND VERIFICATION.
*        THIS MODE IS TESTED FOR WITH TM FPSFG,FPSFTXT/BZ (AUTO) 87357
*        NO SCREEN I/O IS PERMITTED IN THIS MODE.                87357
*        RETURN TO R9 WITH R15 SET TO:  0 - VERIFIED (USE)       87357
*              4 - EXIT WITHOUT CHANGING ANYTHING                87357
*              8 - LOCK AND EXIT                                 87357
*             12 - LOCK, EXIT AND LOG OFF                        87357
*             16 - REQUEST CORRECT INFORMATION                   87357
*                                                                87357
VERIFY   DS    0H            VERIFICATION                        87357
         AIF   ('&LOCAL' NE 'PID').VEROTH                        93263
         TM    FACFG,FACFTXT IS ACCOUNT VALID ?                  87159
         BZ    NULLACCT      NO; CAN'T CHECK IT                  87159
         MVC   MTSDDB,=X'F0C0F0F0C0C0C0C0'  MAKE MASK            89334
         NC    MTSDDB,FACTEXT    TEST USER'S ACCOUNT             89334
         CLC   MTSDDB,=X'F0C0F0F0C0C0C0C0'  VALID ?              89334
         BE    NULLACCT      YES                                 87165
         OI    FACFG,FACFERR SET ERROR                           87165
         OI    FDWFG,FACFERR  DITTO                              87165
NULLACCT MVC   A$SUID,PSWUSER  MOVE USER-ID                      87357
         MVI   A$SFCTN,A$SFVALD   VALIDATE USER-ID               87357
         NI    A$SCNTL,A$SBUILD  KILL ALL BUT LOUD BIT           87357
         OI    A$SCNTL,A$SUKEY  USER-ID SUPPLIED                 87357
         TM    FACFG,FACFTXT ACCOUNT SUPPLIED ?                  87357
         BZ    NOTACCTS      NO                                  87357
         MVC   A$SACCT,PSWACCT  MOVE IT                          87357
         OI    A$SCNTL,A$SAKEY  SHOW ACCOUNT INCLUDED            87357
NOTACCTS MVC   A$SBUFL,=Y(A$USIZE)  SET BUFFER SIZE              87357
         MVC   A$SPSWD,PSWPSWD  COPY PASSWORD                    87357
         LA    R1,A$SVCRB                                        87357
         SVC   249           PERFORM LOGON                       87357
         BXLE  R15,R15,VERSETAT  ACCEPT                          87357
         CH    R15,=Y(2*68)  BAD PASSWORD ?                      87357
         BNE   VERFAIL       NO; FAIL                            87357
         OI    FPSFG,FPSFERR   SET PASSWORD IN ERROR             87357
         B     VERFAIL                                           87357
         SPACE 1                                                 87357
VERSETAT L     R1,A$SBUFR    GET LOUD                            90050
         MVI   PSWPRIVY,0    RESET PRIVILEGES                    90050
         TM    A$USUBFG-A$LSECT(R1),A$USFTSO+A$USFWYL  WYL/TSO USER?
         BNZ   VERSETSY      YES                                 90050
         OI    FIDFG,FIDFERR  ELSE SET INVALID USER ID           90050
         B     VERFAIL       AND FAIL                            90050
VERSETSY TM    A$UPRIVY-A$LSECT(R1),A$UPFSYS  SYSTEMS PRIVIES ?  90050
         BZ    VERSETAD      NO                                  90050
         MVI   PSWPRIVY,PRFSYS  SET SYSTEMS PRIVILEGES           90050
VERSETAD TM    FACFG,FACFTXT DID USER ACCOUNT ?                  90050
         BNZ   VERCC0        YES                                 87357
         LA    R14,A$LCACT   POINT TO CURRENT LOGON ACCOUNT      90231
         CLI   0(R14),C'0'   IS THERE ONE ?                      90231
         BNL   *+8           YES                                 90231
         LA    R14,A$UACCT   ELSE POINT TO USER'S DEFAULT        90231
         MVC   PSWACCT,0(R14)  USE APPROPRIATE ACCOUNT NUMBER    90231
         B     VERCC0        AND PASS                            87357
         SPACE 2                                                 87357
.VEROTH  CLI   UADSDCB+DCBDDNAM-IHADCB,C'*'  ALLOCATION DONE ?   87357
         BNE   VERDALLO      YES                                 87357
         BAL   R2,UADSALOC   ELSE TRY TO ALLOCATE UADS           87357
VERDALLO TM    UADSDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPEN ?          87357
         BNZ   VERDALOP      YES                                 87357
         LA    R2,UADSDCB                                        87357
         ST    R2,MTSDDB                                         89334
         OI    MTSDDB,X'80'                                      89334
         OPEN  MF=(E,MTSDDB)     OPEN IT                         89334
         TM    UADSDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPEN NOW ?      87357
         BZ    OOPS          NO                                  87357
VERDALOP LA    R1,MTSDDB                                         89334
         MVC   MTSDDB,PSWUSER    COPY THE ID (PLUS TRAILER)      89334
         LA    R2,7                                              87357
VERDALOB TM    0(R1),255-C' '    TRAILING BLANK/NULL ?           87357
         BZ    VERDALOZ      YES                                 87357
         LA    R1,1(,R1)                                         87357
         BCT   R2,VERDALOB   TRY AGAIN                           87357
VERDALOZ MVI   0(R1),C'0'    SET FOR MAIN MEMBER                 87357
         OI    FIDFG,FIDFERR  SET BAD USER-ID (FOR NOW)          87357
         FIND  UADSDCB,MTSDDB,D  FIND THE MEMBER                 89334
         CH    R15,=H'4'                                         87357
*        BL    VERFOUND      FOUND MEMBER                        87357
         BE    VERFAIL       NO SUCH MEMBER                      87357
         BH    OOPS          I/O ERROR OR STORAGE SHORT          87357
         SPACE 1                                                 87357
VERFOUND LA    R2,UADSDCB                                        87357
         LA    R5,UADSBUFF                                       87357
         LA    R3,SIGNWRKL-(UADSBUFF-SIGNWORK)  SPACE FOR UADS   89334
         MIN   R3,UADSDCB+DCBBLKSI-IHADCB,TYPE=H                 87357
         READ  READDECB,SF,(R2),(R5),(R3),MF=E                   87357
         CHECK READDECB      WAIT FOR SOMETHING                  87357
         LA    R4,SIGNWRKL-(UADSBUFF-SIGNWORK)  INPUT SIZE       89334
         MIN   R4,UADSDCB+DCBBLKSI-IHADCB,TYPE=H                 87357
         LA    R4,UADSBUFF(R4)  POINT TO LAST VALID BYTE         87357
         SPACE 1                                                 87357
         USING DHED,R5       DECLARE UADS HEADER                 87357
         CLC   PSWUSER(7),UADSUSER  CORRECT MEMBER ?             87357
         BNE   VERFAIL       NO; FAIL                            87357
         NI    FIDFG,255-FIDFERR  RESET UID BAD                  87357
         OI    FPSFG,FPSFERR   SET PASSWORD BAD                  87357
         MVI   PSWPRIVY,0    RESET PRIVILEGES                    90050
         TM    UADSATTR,PSCBCTRL+PSCBJCL+PSCBVMNT OPER+JCL+MOUNT?
         BNO   *+8           NO                                  90050
         OI    PSWPRIVY,PRFSYS  YES; ASSIGN SYSTEMS PRIVILEGES   90050
*        INSTALLATION BITS ARE IN 'UADSINST'                     87357
*                                                                87357
         LOFF  R6,UADSPWD1   GET FIRST PASSWORD (OR BOMB)        87357
         USING DPOB,R6       DECLARE IT                          87357
NEXTPSWD LOFF  R1,UADSPDAT   GET PASSWORD BLOCK                  87357
         USING DPOBD,R1      DECLARE IT                          87357
         TM    FPSFG,FPSFTXT AUTO-LOGON ?                        87357
         BZ    VERACCT       YES; NO PASSWORD TO CHECK           87357
         CLC   PSWPSWD,UADSPPWD  HAVE WE A MATCH ?               87357
         BE    VERACCT       YES; PROCESS ACCOUNT                87357
         LOFF  R6,UADSPFLG   LOOK FOR ANOTHER                    87357
         B     NEXTPSWD                                          87357
         SPACE 1                                                 87357
VERACCT  NI    FPSFG,255-FPSFERR  NO PASSWORD ERROR              87357
         OI    FACFG,FACFERR   SET ACCOUNT BAD                   87357
         LOFF  R6,UADSPSUB   ACCOUNT BLOCK                       87357
         USING DNOB,R6       DECLARE ACCOUNT BLOCK               87357
         TM    FPSFG,FPSFTXT  AUTO-LOGON ?                       87357
         BZ    VERDEFAC      YES; DEFAULT ACCOUNT                87357
         TM    UADSAFLG,AFLG01  ONE ACCOUNT ENTRY ONLY ?         87357
         BZ    NEXTACCT      NO; MORE THAN ONE                   87357
VERDEFAC CLI   PSWACCT,C' '  ANY USER SUPPLIED ACCOUNT ?         87357
         BH    NEXTACCT      YES; DON'T DEFAULT                  87357
         LOFF  R1,UADSADAT,NONE=VERGACCT  GET ACCT OR PASS       87357
         USING DNOBD,R1      DECLARE THE DATA                    87357
         SLR   R15,R15                                           87357
         ICM   R15,1,UADSALEN  GET ACCOUNT LENGTH                87357
         BZ    VERGACCT      INSTALLATION W/O ACCOUNTING ?       87357
         MVC   PSWACCT,BLANKS  PRE-FILL WITH BLANKS              87357
         MIN   (R15),=Y(L'PSWACCT),TYPE=H                        87357
         BCTR  R15,0                                             87357
         EX    R15,ACCTDEF   SET DEFAULT ACCOUNTING              87357
         B     VERGACCT      SET ACCOUNT GOOD                    87357
         SPACE 1                                                 87357
NEXTACCT LOFF  R1,UADSADAT   GET ACCOUNT DATA                    87357
         USING DNOBD,R1      DECLARE ACCOUNT BLOCK               87357
         SLR   R15,R15                                           87357
         IC    R15,UADSALEN  GET ACCOUNT NUMBER LENGTH           87357
         MIN   (R15),=Y(L'PSWACCT),TYPE=H                        87357
         SH    R15,=H'1'                                         87357
         BM    NEXTACCT                                          87357
         EX    R15,ACCTCLC   SAME ACCOUNT ?                      87357
         BE    VERCC0        OK                                  87357
         LOFF  R6,UADSAFLG   ELSE GET ANOTHER ENTRY              87357
         B     NEXTACCT                                          87357
ACCTCLC  CLC   PSWACCT(0),UADSANUM  MATCHING ACCOUNT ?           87357
ACCTDEF  MVC   PSWACCT(0),UADSANUM  USE ONLY ENTRY AS DEFAULT    87357
.VERDON  SPACE 1                                                 87357
VERGACCT NI    FACFG,255-FACFERR  SET ACCOUNT GOOD               87357
VERCC0   SLR   R15,R15       ACCEPT ENTRY                        87357
VEREXIT  BR    R9            RETURN TO CALLER                    87357
         SPACE 1                                                 87357
VERFAIL  INCH  TRYCOUNT,WORK=R15  COUNT FAILED ATTEMPTS          89271
         CH    R15,=H'4'     ENOUGH IS ENOUGH ?                  87357
         BL    VERCC16       NO; LET USER TRY AGAIN              87357
VERCC12  LA    R15,12        SET FOR LOGOFF                      87357
         B     VEREXIT       AND RETURN                          87357
         SPACE 1                                                 87357
VERCC16  LA    R15,16        SET FOR UNACCEPTABLE INPUT          87357
         MVC   FDWCUR,FIDATB SET CURSOR INTO USERID              87357
         TM    FIDFG,FIDFERR DID USER SIGNAL ERROR ?             87357
         BNZ   VEREXIT       YES; EXIT                           87357
         MVC   FDWCUR,FPSATB  SET CURSOR INTO PASSWORD           87357
         TM    FPSFG,FPSFERR  ERROR THERE ?                      87357
         BNZ   VEREXIT       YES                                 87357
         MVC   FDWCUR,FACATB PLACE IT INTO ACCOUNT FIELD         87357
         TM    FACFG,FACFERR  WAS ERROR THERE ?                  87357
         BNZ   VEREXIT       YES                                 87357
         MVC   FDWCUR,FIDATB   ELSE PLACE IT IN UID              87357
         B     VEREXIT                                           87357
         TITLE 'N E T C O M M  ***  TERMINATION AND CLEANUP'     89328
*        THIS CODE IS ENTERED PRIOR TO LEAVING THIS MODULE.      87357
*        THE CODE MAY EXIT WITH R15=0 (PROCEED WITH TERMINATION) 87357
*        OR R15=4 (OR GREATER) TO RESET ALL PRIVILEGES; IN WHICH 87357
*        CASE THIS EXIT IS RE-ENTERED.                           87357
*        TO KILL A USER AND FORCE A LOG-OFF, YOU MAY BRANCH TO   87357
*        'GETLOST'.                                              87357
*                                                                87357
TERMIN8  DS    0H            RE-ENTRY ?                          87357
         AIF   ('&LOCAL' NE 'PID').TERMOTH                       93263
         MVC   A$SACCT,=CL8'KERMIT'  SET SUB-SYSTEM              87357
         LA    R0,USERDATA   POINT TO MY DATA                    87357
         ST    R0,A$SPSWD    (BUT THERE AREN'T ANY?)             87357
         MVI   A$SCNTL,A$SUKEY+A$SBUILD  SHOW USER AND LOUD      87357
         ST    R1,A$SBUFR    SET LOUD                            87357
         MVI   A$SCNTL,A$SFLOGF  MAKE THIS A LOGOFF REQUEST      87357
         TM    MTSSECFG,MTSSECON  USER LOGGED ON ?               89328
         BZ    TERMLOFF      NO; LEAVE LOGOFF                    87357
         MVI   A$SFCTN,A$SFLOGN  SET FOR LOGON                   87357
TERMLOFF MVC   A$SUID,PSWUSER  COPY USER ID                      87357
         TM    MTSFLGT,MTSFXEND  TERMINATE WITH EXTREME PREJUDICE ?
         BZ    *+8           NO                                  87359
         MVI   A$SACES,0     ZERO PERMITTED SYSTEMS TO KILL LOUD 87359
         LA    R1,A$SVCRB                                        87357
         SVC   249           PERFORM THE REQUEST                 87357
TERMOTH  DS    0H                                                87357
.TERMOTH SPACE 1                                                 87357
         SLR   R15,R15       SET GOOD RETURN                     87357
         BR    R9            RETURN TO CALLER                    87357
         SPACE 1                                                 87357
         PUSH  USING                                             87357
         USING CLEANUP,R12                                       87357
CLEANUP  STM   R0,R15,UADSSAVE  SAVE REGISTERS                   87357
         LR    R12,R15       SET NEW BASE                        87357
         TM    UADSDCB+DCBOFLGS-IHADCB,DCBOFOPN  DCB OPEN ?      87357
         BZ    CLEANED       NO                                  87357
         LA    R1,UADSDCB                                        87357
         ST    R1,MTSDDB                                         89334
         MVI   MTSDDB,X'80'                                      89334
         CLOSE MF=(E,MTSDDB)     CLOSE AND FREE IT               89334
CLEANED  LM    R0,R15,UADSSAVE  RESTORE REGISTERS                87357
         BR    R14           AND RETURN                          87357
         POP   USING                                             87357
         EJECT ,                                                 87357
*        DYNAMIC ALLOCATION ROUTINE FOR UADS                     87357
*        RETURNS ON R9, ZEROES R15 IF OK, PUTS DDNM INTO UADSDCB 87357
*                                                                87357
UADSALOC LA    R1,DYSRBPTR-SGNALOC+UADSTADB                      87357
         DYNALLOC ,          ALLOCATE IT                         87357
         BXH   R15,R15,OOPS  RETURN WITH ERROR                   87357
         MVC   UADSDCB+DCBDDNAM-IHADCB(8),TUDDN+6-SGNALOC+UADSTADB
         BR    R2            RETURN                              87357
TUDSN    DC    Y(DALDSNAM,1,13),CL13'SYS1.UADS'                  87357
TUFREE   DC    Y(DALCLOSE,0) FREE=CLOSE                          87357
TUSHR    DC    Y(DALSTATS,1,1),X'08'  DISP=SHR                   87357
         SPACE 1                                                 87357
*        MOVED PATTERN                                           87357
SGNALOC  DS    0D                                                89334
PATDCB   DCB   DDNAME=*,DEVD=DA,DSORG=PO,MACRF=R,EODAD=EOF,            *
               SYNAD=OOPS                                        87357
         READ  PATDECB,SF,1,2,'S',MF=L                           87357
DYSRBPTR DC    X'80',AL3(DYSRB)  REQUEST POINTER                 87357
DYSRB    DC    AL1(20,S99VRBAL,S99NOCNV+S99NOMNT,0) +S99JBSYS FOR
         DC    Y(0,0)        ERROR, INFO                         87357
DYSTXTPP DC    A(DYSTUPL)    TEXT UNIT LIST POINTER              87357
         DC    A(0,0)        RESERVED                            87357
DYSTUPL  DC    A(TUDDN,TUFREE,TUSHR),X'80',AL3(TUDSN)            87357
TUDDN    DC    Y(DALRTDDN,1,8),CL8' '  RETURNED DDNAME           87357
SGNALOLN EQU   *-SGNALOC     LENGTH TO MOVE                      89334
         SPACE 1                                                 87357
SIGNWORK DSECT ,             GOTTEN STORAGE                      89334
         DS    18F           TOP SAVE                            89334
UADSSAVE DS    18F           SAVE AREA                           87357
         SPACE 1                                                 87159
FDW      MAPFDW DSECT=NO                                         87159
         SPACE 1                                                 87159
MAPFID   MAPFIW DSECT=NO,PFX=FID,LEN=8                           87165
MAPFPS   MAPFIW DSECT=NO,PFX=FPS,LEN=8                           87165
MAPFAC   MAPFIW DSECT=NO,PFX=FAC,LEN=8                           87165
TRYCOUNT DS    H             COUNT OF USER'S TRIES               87357
ERRMSGLN DS    2H       1/2  ERROR MESSAGE PREFIX                87357
ERRMSG   DS    CL121    2/2  ERROR MESSAGE                       87357
         SPACE 1                                                 87357
PSWPRIVY DC    X'00'         NEW PRIVILEGES                      90050
PRFSYS   EQU   X'80'           SYSTEMS PRIVILEGES                90050
PRFOPER  EQU   X'20'           OPERATOR PRIVILEGES               90050
PSWUSER  DC    CL8' '                                            89337
PSWACCT  DC    CL8' '                                            87357
PSWPSWD  DC    CL8' '                                            87357
         AIF   ('&LOCAL' NE 'PID').SOLOUDS                       93263
A$SVCRB  A$SVCRB DSECT=NO                                        89328
.SOLOUDS ANOP  ,                                                 90186
USERDATA DS    8F            AVAILABLE FOR MY USE (IN LOUD)      87357
REPLY    DS    CL80          INPUT BUFFER                        89334
BLANKS   DS    CL80          + BLANKS                            89334
         SPACE 1
UADSTADB DS    ((SGNALOLN+7)/8)D  PARAMETERS, DCB, ETC.          89334
UADSDCB  EQU   UADSTADB+PATDCB-SGNALOC  UADS DCB                 89334
READDECB EQU   UADSTADB+PATDECB-SGNALOC  READ DECB               89334
         SPACE 1                                                 87357
UADSBUFF DS    12CL256       BUFFER FOR UADS DATA                89334
SIGNWRKL EQU   *-SIGNWORK    LENGTH OF GETMAIN                   89334
         SPACE 1                                                 89334
         IKJEFUAD ALL        MAP UADS STRUCTURE                  87357
         IEFZB4D0
         IEFZB4D2
         IKJEFLWA ,                                           ESP88277
         IHAASCB ,                                            ESP88277
         IHAASXB ,                                            ESP88277
         IEECHAIN ,                                              89328
         DCBD  DSORG=PS,DEVD=DA                                  89328
         IKJPSCB ,                                               89328
         IHARB ,                                                 89328
         IHACDE ,                                                89328
         IKJTCB ,                                                89328
         IEFUCBOB ,                                              89328
CIB      IEZCIB ,                                                89328
         AIF   ('&LOCAL' NE 'PID').BIGEND                        93263
USERCVT  USERCVT ,                                               89328
A$LSECT  A$LOUD ,                                                89328
.BIGEND  END   ,                                                 89328
