MICR     TITLE 'CONVERT A PRINT TAPE TO MICROFICHE'              86111
         COPY  OPTIONGB      COPY GLOBAL DEFINITIONS             86111
         SPACE 1                                                 86111
         SYSPARM LIST=YES    SET GLOBALS                         86111
         SPACE 1                                                 86111
***********************************************************************
*                                                                     *
*        COPYRIGHT 1986  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,                                                 86111
         PRINT &PRTSOR                                           86111
MICRO    SAVEM ZERO,BASE=R12,PARM=R9                             86111
         MVI   TITCC,C' '    SET TO CLEAR
         MVC   TITLE(TITBLEN),TITCC   CLEAR IT OUT
         SERVINIT ,          LOAD THE @SERVICE ROUTINE           86111
         SPACE 1                                                 86111
         OPEN  (INTAPE,(INPUT,REREAD))  OPEN INPUT FILE          86111
         TM    INTAPE+DCBOFLGS-IHADCB,DCBOFOPN  OPENED ?         86111
         BZ    EXIT16        NO; TOO BAD                         86111
         SPACE 1                                                 86111
         LA    R9,0(,R9)     ANY PARM ADDRESS PASSED ?           86111
         LTR   R9,R9                                             86111
         BZ    NOPARM        NO                                  86111
         L     R9,0(,R9)     LOAD THE PARM                       86111
         LA    R9,0(,R9)                                         86111
         LTR   R9,R9         ANY ?                               86111
         BZ    NOPARM                                            86111
         SLR   R8,R8                                             86111
         ICM   R8,3,0(R9)    LOAD AND TEST PARM LENGTH           86111
         BNP   NOPARM                                            86111
         LA    R0,TITCL-1  GET MAXIMUM LENGTH                    86111
         MIN   R8,(R0)       USE SHORTER LENGTH                  86111
         EX    R8,MVCPARM    MOVE PARM                           86111
         CLC   TITLE(TITCL-1),TITLE+1  ANY TITLE SUPPLIED ?      86111
         BNE   HAVETIT       YES                                 86111
         SPACE 2
*        NOW GET TITLE
*
NOPARM   SERVCALL LPALD,=CL8'@INPREAD'  GET READ ROUTINE         86111
         INPOPEN SYSIN,OPT=(NOWTO)     TRY FOR CANNED CARD       86111
         INPGET ,            GET FIRST CARD                      86111
         BXH   R15,R15,ENDSIN  TOO BAD                           86111
         LR    R8,R0         COPY LENGTH                         86111
         LA    R0,TITCL-1  GET MAX LENGTH                        86111
         MIN   R8,(R0)       USE SHORTER                         86111
         EX    R8,MVCSIN     MOVE FROM SYSIN                     86111
ENDSIN   INPCLOSE ,          CLOSE AND FREEMAIN                  86111
         SERVCALL LPADL,=CL8'@INPREAD'  DELETE                   86111
         B     CHEKTIT       TRY FOR NON-BLANK                   86111
         SPACE 1
WTORTIT  XC    DB(4),DB      CLEAR ECB
         WTOR  'MICRO1 - SPECIFY TITLE :',TITLE,TITCL,DB         86111
         WAIT  ECB=DB
CHEKTIT  OC    TITLE(TITCL),INREC    MAKE UPPER CASE             86111
         CLC   TITLE(TITCL-1),TITLE+1 ALL BLANKS ?               86111
         BE    WTORTIT       YES ,  TRY AGAIN
         SPACE 1
HAVETIT  ICM   R15,15,@MICRO  WAS SUBROUTINE LINKED IN ?         86111
         BNZ   HAVESUB       YES                                 86111
         SERVICE LPALD,=CL8'MICROSUB'  ELSE LOAD IT              86111
         ST    R0,@MICRO     SET LOAD ADDRESS                    86111
         L     R15,@MICRO    GET SUBROUTINE ADDRESS              86111
HAVESUB  LA    R1,=A(TITCC)  POINT TO TITLE REQUEST              86111
         BALR  R14,R15       INITIALIZE                          86111
         SPACE 2                                                 86111
*        PROCESS INPUT RECORDS HERE
*
GETINIT  GET   INTAPE,INREC  READ THE FIRST RECORD
         L     R15,@MICRO    GET FORMATTER                       86111
         LA    R1,=A(INREC)  GET RECORD ADDRESS                  86111
         BALR  R14,R15       CALL IT                             86111
         B     GETINIT       GET ANOTHER                         86111
         SPACE 2
GETINEOF XC    INREC(8),INREC   SET EOF REQUEST                  86111
         L     R15,@MICRO    GET FORMATTER                       86111
         LA    R1,=A(INREC)  GET RECORD ADDRESS                  86111
         BALR  R14,R15       CALL IT                             86111
         SPACE 2
EXIT     SLR   R9,R9         ZERO RETURN CODE
EXITC    CLOSE (INTAPE,LEAVE)  CLOSE INPUT TAPE                  86111
         SERVTERM ,          CLOSE AND FREE                      86111
         ENDM  RC=(R9)       RETURN                              86111
         SPACE
EXIT16   LA    R9,16         SET BIG RETURN CODE
         B     EXITC         AND GET OUT
         SPACE 1                                                 86111
MVCPARM  MVC   TITLE(0),2(R9)   MOVE PARM FIELD                  86111
MVCSIN   MVC   TITLE(0),0(R1)   MOVE SYSIN CARD                  86111
         EJECT
*        DATA AND STUFF
INTAPE   DCB   DDNAME=SYSUT1,MACRF=GM,DSORG=PS,EROPT=ACC,              *
               EODAD=GETINEOF,RECFM=FB,LRECL=133,BLKSIZE=1330
         SPACE
DB       DC    D'0'                                              86111
         WXTRN MICROSUB      MAY BE LOADED DYNAMICALLY           86111
@MICRO   DC    A(MICROSUB)   EXTERNAL SUBROUTINE                 86111
SYSIN    INPWORK SYSIN,EODAD=ENDSIN                              86111
TITCC    DC    C'$',CL132' '   TITLE CONTROL                     86111
TITCL    EQU   3*16-19       3 TIT/FRAME; 16 FRAMES PER FICHE    86111
TITLE    DC    CL(TITCL)'TITLE'
         ORG   *-1
TITB1    DC    C' '
TITDAT   DC    CL8' DATED:'    DATE OF ORIGINAL RUN
TITB2    DC    C' '
TITNAM   DC    CL8'PROGRAM'  JOB NAME OF RUN
TITPAG   DC    C'PA'         PAGE NUMBER OF FICHE
TITB3    DC    CL3'GE'       TITLE - PAGE DC
TITFRAM  DC    CL5'FRAME'    DATA FRAME WITHIN FICHE
         DC    C'S'          PLURAL
TITBLEN  EQU   *-TITLE
         DC    CL3' '
         ORG   ,                                                 86111
INREC    DC    CL133' '                                          86111
         SPACE 2
SAVE     DSECT ,             SAVE/WORK AREA                      86111
@SERVICE DS    A                                                 86111
@INPREAD DS    A                                                 86111
SAVEND   EQU   *                                                 86111
         SPACE 1                                                 86111
         DCBD  DSORG=PS,DEVD=DA                                  86111
         END
