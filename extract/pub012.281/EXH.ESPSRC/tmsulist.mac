TLST     TITLE 'T M S U L I S T  ***  TMS USER ENTRY PRINTOUT'
         COPY  OPTIONGB                                          81278
         SPACE 1                                                 81278
         SYSPARM LIST=YES                                        81278
         SPACE 2                                                 81278
***********************************************************************
*                                                                     *
*        COPYRIGHT 1981  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,
*        THIS MODULE READS THE TMC AND SELECTS ENTRIES BY EITHER
*          ACCOUNT NUMBER ALONE (PARM=ACCT) OR BOTH ACCOUNT AND
*          SUBACCOUNT (PARM OMITTED OR =SUB).
*        IF PARM(1:3) IS NEITHER 'ACC' NOR 'SUB', AND IS LENGTH 4,
*          THE SUPPLIED VALUE IS USED AS A SUBACCOUNT.           81046
*
         PRINT &PRTSOR                                           81138
TMSULIST SAVEM ZERO,BASE=R12,PARM=R5    ADDED ON 80218           81138
         MVI   OUT-1,C' '    INIT BLANKS                         81138
         MVC   OUT(OUTLEN),OUT-1  MAKE BLANKS                    81138
         MVC   HEADACCT+4(5),OUT   BLANK HEADER                  81138
         MVC   SUBSUB,OUT  BLANK SUBACCT                         81138
         MVC   COMPACCT(6),PATCOMP  INITIALIZE ACCT COMPARE      81138
         SERVINIT ,          LOAD THE SERVICE ROUTINE            90085
         SERVCALL ACGET,SAVEACCT,ERR=ABEND69  GET ACCOUNT        81123
         STC   R0,DB         SAVE ACCOUNT'S PRIVILEGES           84349
         SERVCALL LPALD,=CL8'@PRINTER'  GET THE PRINTER          90085
         ST    R0,@PRINTER   SAVE ITS ADDRESS                    81138
         SERVCALL LPALD,=CL8'@INPREAD'  GET THE INPUT ROUTINE    90085
         ST    R0,@INPREAD   STASH IT                            90085
         SERVCALL LPALD,=CL8'@TMSREAD'  GET THE INPUT ROUTINE    92366
         ST    R0,@TMSREAD   STASH IT                            92366
         LTR   R5,R5         ANY PARM ?                          81123
         BZ    NOPARM        NO                                  81123
         LAT   R5,0(R5),NOPARM   R5 NOW POINTS TO THE PARAMETER  81123
         CLI   1(R5),4       FOUR BYTES OR MORE ?                81123
         BL    NOPARM        NO
         CLC   =C'A=',2(R5)  ACCOUNT OPTION ?                    84349
         BNE   NOTPACCT      NO                                  84349
         TM    DB,VAASYS+VAASUP  PRIVILEGED SUBMITTER ?          84349
         BZ    BADPACCT      NO                                  84349
         MVC   SAVEACCT(4),4(R5)                                 84349
         CLI   1(R5),6       SHORT ?                             84349
         BE    HAVPACCT                                          84349
         CLI   1(R5),10      LONG ?                              84349
         BNE   BADPACCT      NO; ERROR                           84349
         MVC   SAVEACCT+4(4),8(R5)  COPY                         84349
         B     NOPARM                                            84349
BADPACCT WTO   '*** INVALID OR UNAUTHORIZED PARM FIELD ***'      84349
         B     ABEND69                                           84349
NOTPACCT CLC   =C'ACC',2(R5)   ACCOUNT OPTION ?                  81123
         BNE   USPARM        NO                                  81046
HAVPACCT MVI   COMPACCT+1,X'03'   IN MOVE AND COMPARE
         MVC   SUBSUB,=C'SUB'
         B     NOPARM                                            81046
USPARM   CLC   =C'SUB',2(R5)   SUB=SUB ?                         81123
         BE    NOPARM        YES; USE JMRUSEID SUBACCOUNT        81046
         MVC   SAVEACCT+4(4),2(R5)  USE REQUESTED VALUE          81123
NOPARM   MVC   HEADACCT(4),SAVEACCT  COPY MAJOR ACCOUNT NUMBER   84349
         CLI   COMPACCT+1,7  DOING SUBACCOUNT ?                  84349
         BNE   DIDPARM
         MVI   HEADACCT+4,C','
         MVC   HEADACCT+5(4),SAVEACCT+4  TACK SUBACCOUNT ON      81046
DIDPARM  PRTOPEN SYSPRINT,OPT=(ABE,DUMMY)  OPEN THE PRINT FILE   81138
         PRTLIST HEADPRT,TITLE=1  FORMAT THE PAGE TITLE          81138
         PRTLIST SUBHEAD,TITLE=3  DO THE SUBTITLE                81138
         READTMC OPEN,BUFNO=255,TEST=NO,EXT=  ALL STORAGE FOR BUFFERS
         BXH   R15,R15,ABEND69   IF OPEN FAILED                  81123
         SPACE 1
ACTII    TIME  BIN
         STCM  R1,7,CURDAT   SAVE CURRENT DATE
         OI    CURDAT+2,X'0F'  MAKE GOOD DATE
         MVI   NEXTWEEK+L'NEXTWEEK-1,X'7C'  INIT P'7'            81138
         AP    NEXTWEEK,CURDAT
         CP    NEXTWEEK+1(2),=P'366'  OVERFLOW ?
         BL    DATEOK        NO
         BH    DATEYEAR      BUMP A YEAR
         TM    CURDAT,X'01'   ODD YEAR ?
         BNZ   DATEYEAR      YES; NOT LEAP
         TM    CURDAT,X'12'   MULTIPLE OF FOUR ?
         BNM   DATEOK        YES; LEAP YEAR (FOR A WHILE)
DATEYEAR AP    NEXTWEEK,=P'635'  REDO FOR NEXT YEAR
DATEOK   EQU   *
         SPACE 2                                                 81123
LOOP     READTMC VOL,TEST=YES  READ TMC ONLY; TEST RETURN        81123
         BH    ABEND69       ERROR                               81123
         BE    ACTIII        PSEUDO END-FILE WHEN DSNB HIT       81123
         LR    R5,R1         SAVE ADDRESS
         USING TMRECORD,R5   DECLARE IT
         CLI   TMVOLSER,C'$'  VALID ?                            90316
         BL    LOOP          NO; SKIP
         EX    0,COMPACCT    COMPARE ACCOUNTS                    81138
         BNE   LOOP          NO MATCH; SKIP
         TM    TMFLAG1,F1DELET  DELETED FROM LIB ?               92365
         BNZ   LOOP          YES; SKIP                           84349
         MVC   OUT(OUTLEN),OUT-1     BLANK THE WORK AREA         84349
         CP    TMOUTDAT,=P'0'  IS THE TAPE OUT OF AREA ?         84349
         BZ    NOTOUT        NO                                  84349
         MVI   VEXPDT,C'-'   MAKE PRETTY                         84349
         MVI   VEXPDT+1,C'-'                                     84349
         MVI   VEXPDT+2,C'>'  MAKE A POINT                       84349
         MVC   VEXPDT+3(4),TMOUTAR  SHOW WHERE IT IS             84349
         INCH  NUMOUTA       COUNT CHECKED-OUT VOLUMES           90085
         B     DOCRTDT       PROCEED                             84349
NOTOUT   TM    TMFLAG2,F2ETMS  EXPIRED ?                         92365
         BZ    NOTGONE       NO; DO NORMALLY
*        TM    FLAG,FGALL    DO LOTS OF OUTPUT ?                 84349
*        BZ    LOOP          NO                                  84349
*        MVC   VEXPDT(7),=C'=FREED='                             84349
         B     DOCRTDT       PROCEED                             84349
NOTGONE  MVC   VEXPDT+1(6),=X'21204B202020'                      84349
         ED    VEXPDT(7),TMEXPDT+L'TMEXPDT-3                     92366
         CP    TMEXPDT,CURDAT  EXPIRED ?
         BL    LOOP          YES
         BH    NOTTODAY
         MVC   VEXPDT(7),=C'*TODAY*'
         B     DOCRTDT                                           81138
NOTTODAY CP    TMEXPDT,NEXTWEEK  WITHIN A WEEK ?
         BNH   SETSOON       YES; SET FLAGS                      81067
         CLC   =X'9800',TMEXPDT  FUNNY ENTRY ?                   81067
         BNE   DOCRTDT       NO                                  81138
SETSOON  MVI   VEXPDT,C'*'  FLAG IT                              81138
         MVI   VEXPDT+L'VEXPDT-1,C'*'                            81138
DOCRTDT  MVC   VCRTDT(6),=X'21204B202020'  BLANK AT -1           81138
         ED    VCRTDT-1(7),TMCRTDT+L'TMCRTDT-3                   92366
         CLI   COMPACCT+1,3  DOING ACCOUNT ?                     81067
         BNE   NOSUBA        NO; SKIP SUB-ACCOUNT
         MVC   VSUB(4),PIDSUB
NOSUBA   CLC   =C'X00',TMVOLSER   DISK ENTRY ?                   81067
*TEST*   BE    DODISK        YES; DO SEPARATELY                  81067
         INCH  NUMTAPE       BUMP NUMBER OF TAPES                90085
         LA    R1,TABDEN                                         81067
         LA    R0,(TABDENS-TABDEN)/2
DENSLOOP CLC   TMDEN,0(R1)
         BNE   DENSBUMP
         MVC   VDEN,1(R1)
         B     DENSDONE
DENSBUMP LA    R1,2(,R1)
         BCT   R0,DENSLOOP
DENSDONE LA    R1,TABLAB
         LA    R0,(TABLABS-TABLAB)/4
LABSLOOP CLC   TMLTYPE,0(R1)
         BNE   LABSBUMP
         MVC   VLTYPE,1(R1)
         B     LABSDONE
LABSBUMP LA    R1,4(,R1)
         BCT   R0,LABSLOOP
LABSDONE INCH  TM#DSNBS      MAKE # OF FILES FROM # OF DSNBS     90085
         PRTLIST VOLPRT      FORMAT TAPE PRINT LINE              81138
         B     LOOP          GET ANOTHER                         81138
         SPACE 1
ACTIII   READTMC CLOSE,TEST=NO  CLOSE AND FREEMAIN TMC           81123
*        CHECK FOR TRANSIENT LIBRARY                             90085
         INPOPEN TRANSINT,OPT=(NOWTO)                            90085
         CH    R15,=H'4'     USABLE ?                            90085
         BH    ACTIV         NO; QUIT                            90085
         MVC   COMPACCT+4(2),SADACCT   POINT COMPARE TO S(TRACCT)
         OI    FLAGS,FGTRANO  INDICATE TRANSIENT LIBRARY OPENED  91015
         SPACE 1                                                 90085
TRANLOOP INPGET ,            GET A RECORD                        90085
         BXH   R15,R15,ACTIV   TREAT ERROR AS EOF                90085
         LR    R6,R1         COPY RECORD BASE                    90085
         USING TRANSECT,R6   DECLARE IT                          90085
         EX    0,COMPACCT    BELONGS TO THIS ACCOUNT ?           90085
         BNE   TRANLOOP                                          90085
         CLC   TRVOL,=CL6' '  BLANK (UNUSED) ENTRY ?             93166
         BE    TRANLOOP                                          93166
         INCH  NUMTRANS      COUNT TRANSIENT TAPES               90085
         PACK  DB,TROUTDT(2)  PACK THE MONTH                     93166
         CVB   R1,DB         MONTH                               93166
         PACK  DB,TROUTDT+3(2)  DAY                              93166
         CVB   R15,DB        DAY IN MONTH                        93166
         CH    R1,=H'3'      MARCH OR LATER ?                    93166
         BL    TRANOLIP      NO; DON'T CARE WHETHER LEAP         93166
         PACK  DB,TROUTDT+6(2)  PACK THE YEAR                    93166
         TM    DB+7,X'10'    ODD YEAR ?                          93166
         BNZ   TRANOLIP      YES; NOT LEAP                       93166
         SRP   DB+6(2),1,0        PUT BOTH NYBBLES IN SAME BYTE  93166
         TM    DB+6,X'12'    MULTIPLE OF 4 ?                     93166
         BM    TRANOLIP      NO; NOT LEAP                        93166
         LA    R15,1(,R15)   UP JULIAN DAY AFTER FEBRUARY        93166
TRANOLIP SLL   R1,1          CHANGE MONTH TO OFFSET              93166
         AH    R15,DAYMON-2(R1)  ADD DAYS IN PRIOR MONTHS        93166
         CVD   R15,DB        MAKE PACKED                         93166
         UNPK  TRCRTDT,DB+6(2)  MAKE UNPACKED                    93166
         OI    TRCRTDT+L'TRCRTDT-1,C'0'  MAKE PLUS POSITIVELY    93166
         MVC   TRCRTDT(2),TROUTDT+6  COPY YEAR                   93166
         CLI   COMPACCT+1,3  DOING ACCOUNT ?                     90085
         BNE   NOSUBT        NO; SKIP SUB-ACCOUNT                90085
         MVC   VSUB(4),TRSUBA                                    90085
NOSUBT   PRTLIST FDTRANS     PRINT ENTRY                         90085
         B     TRANLOOP                                          90085
SADACCT  DC    S(TRACCT)     POINT TO ACCOUNT FIELD              90085
ACTIV    PRTROOM 3           MAKE ROOM FOR THREE LINES           81138
         PRTSPACE 2            AND SPACE 2                       81138
         PRTLIST SUMMARY     PRINT SUMMARY LINE                  81138
         SERVTERM ,          CLOSE AND FREE EVERYTHING           90085
         ENDM  RC=0          TERMINATE WITH ZERO CODE            81138
         SPACE 1
ABEND69  ABEND 69,DUMP
         EJECT ,
PATCOMP  CLC   SAVEACCT(8),PIDACCT  CHECK ACCOUNT NUMBER(S)
H1       DC    H'1'          COMPARE CONSTANT                    81138
         SPACE 1                                                 81138
SYSPRINT PRTWORK SYSPRINT,TITLE=5                                81138
TRANSINT INPWORK TRANS,TRANSLIB,EDIT=128,WIDTH=80                93166
         SPACE 1                                                 93166
DAYMON   DC    H'0,31,59,90,120,151,181,212,242,273,303,334'     93166
         SPACE 1                                                 81138
TABDEN   DC    X'D3',C'4'
         DC    X'C3',C'3'
         DC    X'CB',C'2'
         DC    X'83',C'2'
         DC    X'E3',C'C'    CARTRIDGE                           90085
         DC    X'E7',C'C'    CARTRIDGE / COMPACTED               90316
TABDENS  EQU   *
TABLAB   DC    X'01',CL3'NL'
         DC    X'02',CL3'SL'
         DC    X'04',CL3'NSL'
         DC    X'0A',CL3'SUL'
         DC    X'10',CL3'BLP'
         DC    X'20',CL3'LTM'
         DC    X'40',CL3'AL1'                                    90316
         DC    X'4A',CL3'AU1'                                    90316
         DC    X'48',CL3'AU1'                                    90316
         DC    X'C0',CL3'AL3'                                    90085
         DC    X'C8',CL3'AU3'                                    90085
TABLABS  EQU   *
         SPACE 1                                                 81179
HEADPRT  FDOPT NL,CC=C'#'    AUTOMATIC DATE/TIME/PAGE OPTION     81278
         FDPRT '       USER TAPE LIBRARY LISTING FOR ACCOUNT',PADR
         FDPRT HEADACCT                                          81138
         FDPRT *END                                              81138
         SPACE 1                                                 81138
VOLPRT   FDPRT VSUB,PADR,NL            SUBACCOUNT                81138
         FDPRT TMVOLSER,PADR           VOLUME SERIAL             81138
         FDCLC TMVOLSEQ,H1,BE=NOSEQFD,BL=NOSEQFD  SKIP VOLSEQ<2  81138
         FDPRT TMVOLSEQ,I,RADJ,PADR,LEN=3   VOLUME SEQUENCE      84349
         FDPRT TMPRVVOL,PADR           PREVIOUS VOLUME           81138
         FDGOTO CRTFD                    SKIP TO CRDT            81138
NOSEQFD  FDPRT ' ',LEN=11              SPACE IF NO VOLSEQ        84349
CRTFD    FDPRT VCRTDT                  FORMATTED CREATION DATE   81138
EXPFD    FDPRT VEXPDT        *TODAY* OR EXPIRATION DATE          81138
         FDPRT VDEN,PADR               DENSITY                   81138
         FDPRT VLTYPE,PADR             LABEL TYPE                81138
         FDPRT TM#DSNBS,I,RADJ,PADR,LEN=4   NUMBER OF FILES      84349
         FDPRT TMCRUNI,HEX,RADJ,LEN=3   UNIT NAME (LAST 3 BYTES CUU)
         FDPRT TMJOBNM,PAD             CREATING JOB              81138
         FDPRT TMSTPNAM,PADR                    STEP             81138
         FDPRT PIDPGMNM                         PROGRAM          81138
         FDPRT TMBLKCNT,I,RADJ,PADR,LEN=9                        84349
         FDPRT TMDSN                   DATASET NAME OF FIRST FILE
         FD    *END                                              81138
         SPACE 1                                                 90085
FDTRANS  FDPRT VSUB,PADR,NL            SUBACCOUNT                90085
         FDPRT TRVOL,PADR           VOLUME SERIAL                93166
         FDPRT ' ',PAD,LEN=10   SPACER                           90085
         FDPRT TRCRTDT,2     YEAR OF CREATION                    90085
         FDPRT '.'           SEPARATOR                           90085
         FDPRT TRCRTDT+2,3   JULIAN DAY OF CREATION              90085
         FDPRT 'IN TRANSIENT LIBRARY SLOT',PAD                   93166
         FDPRT TRSLOT                                            93166
         FD    *END                                              90085
         SPACE 1                                                 81138
SUBHEAD  FDPRT SUBSUB,NL,PADR                                    81138
         FDPRT ' VOLUME SEQ FROM  CREATED EXPIRE DN/LB FILE',PADR
         FDPRT 'UNIT  JOB    STEP     PROGRAM    BLOCKS '        84349
         FDPRT 'DATASET NAME'                                    84349
         FD    *END                                              81138
         SPACE 1                                                 81138
SUMMARY  FDPRT NUMTAPE,I,RADJ,PADR,NL,LEN=8                      81138
         FDPRT 'TAPE'                                            84349
         FDCLC NUMTAPE,H1,BE=SUMMARYT                            84349
         FDPRT 'S'                                               84349
SUMMARYT FDPRT 'IN THE PID LIBRARY',PAD                          92366
         FDTM  FLAGS,FGTRANO,BZ=SUMMARYN  NO TRANSIENT INFO.     91015
         FDPRT NUMTRANS,I,PADR,RADJ,LEN=13                       90085
         FDPRT 'TAPE'                                            84349
         FDCLC NUMTRANS,H1,BE=SUMMARYD                           90085
         FDPRT 'S'                                               84349
SUMMARYD FDPRT 'IN THE TRANSIENT LIBRARY',PADL                   90085
SUMMARYN FDCLC NUMOUTA,H1,BL=SUMMARYZ                            91015
         FDOPT NL                                                84349
         FDREPT 20,C'*'                                          84349
         FDPRT ' '                                               84349
         FDPRT NUMOUTA,I,RADJ,PAD                                84349
         FDPRT 'VOLUME'                                          84349
         FDCLC NUMOUTA,H1,BE=SUMMARYO                            84349
         FDPRT 'S'                                               84349
SUMMARYO FDPRT 'CHECKED OUT OF THE PID LIBRARY',PAD              92366
         FDPRT ' '                                               84349
         FDREPT 20,C'*'                                          84349
SUMMARYZ FDPRT *END                                              81138
         SPACE 2
         LTORG ,
         EJECT ,
SAVE     DSECT ,             SAVE AREA CONTINUED FROM SAVEM      81138
DB       DC    D'0'          WORK WORD                           84349
@PRINTER DC    A(0)          ADDRESS OF PRINTER ROUTINE          81138
@SERVICE DC    A(0)          ADDRESS OF SERVICE ROUTINE          81123
@INPREAD DC    A(0)          ADDRESS OF INPUT READER             90085
@TMSREAD DC    A(0)          ADDRESS OF EXTERNAL TMC READER      92366
NUMOUTA  DC    H'0'          VOLUMES CHECKED OUT OF LIBRARY      84349
NUMTAPE  DC    H'0'          NUMBER OF USER TAPES                81138
NUMTRANS DC    H'0'          NUMBER OF TRANSIENT TAPES           90085
COMPACCT CLC   SAVEACCT(8),PIDACCT  CHECK ACCOUNT NUMBER(S)
CURDAT   DC    PL3'0'
NEXTWEEK DC    PL3'7'        ADD VALUE FOR NEXT WEEK
SAVEACCT DC    CL8' '
SUBSUB   DC    CL3' '         'SUB' IF FULL ACCT LIST, ELSE ' '  81138
TRCRTDT  DC    CL5' '        CONSTRUCTED JULIAN DATE             93166
         DC    C' '          BLANK AT OUT-1                      81138
OUT      DS    0C            AREA TO BLANK                       81138
VCRTDT   DC    CL6' '
VEXPDT   DC    CL8' '
VSUB     DC    CL4' '        SUB-ACCOUNT
VDEN     DC    C' '
VLTYPE   DC    CL3' '
         SPACE 1                                                 81067
DPTYPE   DC    C'3330-1'        DEVICE TYPE                      81067
DPCHG    DC    C'BILLED'                                         81067
DPOWN    DC    C'CUST'                                           81067
OUTLEN   EQU   *-OUT         SIZE TO BE BLANKED                  81138
HEADACCT DC    CL9' '
FLAGS    DC    X'00'         PROCESSING FLAGS                    91015
FGTRANO  EQU   X'80'           TRANSIENT LIBRARY OPENED          91015
SAVEND   EQU   *             END OF WORK AREA                    81138
         SPACE 1
         PRINT &PRTMAC                                           81278
         TMSREC ,            USE LOCAL VERSION OF MAPPING        90085
         SPACE 1                                                 90085
TRANSECT A$TRAN ,            TRANSIENT LIBRARY RECORD            93166
         SPACE 1
         PRINT &PRTSYS                                           81278
CVTDSECT DSECT ,                                                 81151
         CVT   DSECT=YES                                         81151
         SPACE 1                                                 81151
         IHACDE ,                                                81151
         SPACE 1
         END
