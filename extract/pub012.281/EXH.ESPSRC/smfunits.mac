UNIT     TITLE 'S M F U N I T S  ***  DAILY/MONTHLY SMF STATISTICS'
         COPY  OPTIONGB                                          90088
         SPACE 1                                                 90088
         SYSPARM LIST=YES                                        90088
         EJECT
         PRINT &PRTSOR                                           90088
SMFUNITS PGMHEAD ZERO31,BASE=(R11,R12),PARM=R9                  GP12066
         SERVINIT ,          INITIALIZE SERVICE INTERFACE        90088
         SERVLOAD @PRINTER,SUBUCBTY                             GP12066
         LA    R14,TABLE     POINT TO TABLE                      90088
         LA    R15,TABSIZE   TABLE ENTRY LENGTH                  90088
         SR    R0,R15        SET TABLE EMPTY                     90088
         LR    R0,R14        TWICE                               90088
         STM   R14,R0,TABXLE  MAKE BXLE POINTERS                 90088
         SPACE 1
         OPEN  (SMF,(INPUT,REREAD))                              90088
         TM    SMF+DCBOFLGS-IHADCB,DCBOFOPN  SMF OPENED ?        90088
         BZ    CRASH         NO; FAIL
         LM    R5,R7,TABXLE  GET TABLE POINTERS                  90088
         LR    R8,R5         SAVE START                          90088
         PRTOPEN SUMPRINT,DEV=2,OPT=(DUMMY,NOWTO)                90088
         PRTOPEN SYSPRINT,DEV=1,OPT=(ABEND)                      90088
         BXLE  R15,R15,OPENPRT                                   92062
CRASH    ABEND 666           INVALID JCL SETUP
         SPACE 1                                                 92062
OPENPRT  PRTLIST TITLE1,TITLE=1  MAKE TITLE                      92062
         PRTLIST TITLE3,TITLE=3    AND SUBTITLE                  92062
         SPACE 1
GOREAD   GET   SMF           GET NEXT RECORD                     90088
         USING SMFMAPS,R1    DECLARE COMMON SMF MAPPING
         CLI   SMF4RTY,4     STEP RECORD ?                       90088
         BE    E15STEP       YES; PROCESS                        90088
         CLI   SMF4RTY,34    TSO STEP ?                          90088
         BNE   GOREAD                                            90088
         SPACE 1
         USING TABSECT,R5    DECLARE IT                          90088
E15STEP  LA    R3,SMF4DEVC-8   POINT PRIOR TO FIRST I/O ENTRY    90088
         XR    R2,R2
         ICM   R2,3,SMF4LENN  GET LENGTH OF I/O PORTION
E15IOLP  LA    R3,8(,R3)     BUMP TO FIRST/NEXT ENTRY
         SH    R2,=H'8'      SUBTRACT ONE ENTRY
         BM    GOREAD        DO NEXT RECORD                      90088
         LR    R5,R8         REFRESH TABLE ADDRESS               90088
         LM    R15,R0,0(R3)  GET TYPE/ADDRESS//EXCP COUNT        90088
ENTLOOP  BXH   R5,R6,ENTADD  ADD AN ENTRY IF TABLE IS FULL       90088
         C     R15,0(,R5)    MATCHES TABLE ENTRY ?               90088
         BNE   ENTLOOP       NO; TRY AGAIN                       90088
         A     R0,4(,R5)     MAKE NEW TOTAL                      90088
         ST    R0,4(,R5)     AND STASH IT                        90088
E15USE   INC   TABUSE        INCREMENT USE COUNT                 92062
         B     E15IOLP                                           90088
ENTADD   L     R14,=A(TABEND-TABLE)  GET TABLE SIZE              90088
         LA    R14,TABLE-TABSIZE(R14)  POINT TO LAST VALID ENTRY 90088
         CR    R5,R14        WITHIN TABLE ?                      90088
         BH    E15IOLP       NO; TABLE OVERFLOW                  90088
         AR    R7,R6         SET FOR NEW LAST ENTRY              90088
         STM   R15,R0,0(R5)    STASH DEV.TYPE, ETC.              90088
         B     E15USE        SET USE COUNT                       92062
         SPACE 2                                                 90088
SMFEOF   LR    R5,R8         REFRESH TABLE START                 90088
         STM   R5,R7,TABXLE  SAVE THE POINTERS                   90088
         XR    R6,R7                                             90088
         XR    R7,R6                                             90088
         XR    R6,R7         SWAP R6 AND R7                      90088
         STM   R5,R7,SOPLOW  MAKE SKELETON SORT LIST             90088
         OI    SOPHIGH,SOPFAD  INDICATE END ADDRESS SET          90088
         MVI   SOPSEQ,C'A'   ASCENDING                           90088
         MVI   SOPCOO,TABCUU-TABSECT  OFFSET TO ADDRESS          90088
         MVI   SOPCOL,L'TABCUU   LENGTH OF CUU ENTRY             90088
*        MVI   SOPLEN,TABLEN LENGTH OF ENTRY (ALREADY SET)       90088
         SERVCALL SORTH,SOPLOW  CALL HEAPSORT                    90088
         LM    R5,R7,TABXLE  GET TABLE ENTRY BACK                90088
         USING TABSECT,R5    DECLARE IT                          90088
PRINTLOP BXH   R5,R6,LISTEND   QUIT IF DONE                      90088
         ICM   R0,15,TABUSE  CHECK AT LEAST ONE USE              92062
         BZ    PRINTLOP      AVOID ERROR DUE TO BAD SORT LIMITS? 92062
         XC    DB,DB                                            GP12066
         MVC   DB+2(2),TABTYP     MAKE FAKE UCBTBYT1-3          GP12066
         LA    R1,DB-(UCBTBYT1-UCBOB)  FAKE UCB                 GP12066
         SUBCALL SUBUCBTY    CONVERT                            GP12066
         STM   R0,R1,UNITTYPE     ADD TO DISPLAY LINE           GP12066
         UNPK  UNITNAME(5),TABCUU(3)  UNPACK                     92062
         TR    UNITNAME,HEXTRTAB-C'0'                           GP12066
         CLI   UNITNAME,C'0' LEADING ZERO ?                     GP12066
         BNE   *+8             NO                               GP12066
         MVI   UNITNAME,C' '   THREE BYTE ADDRESS ?             GP12066
         MVC   VOLSER,=CL6' '  BLANK                             92062
         CLI   TABTYP,X'20'  DASD DEVICE ?                       92062
         BNE   PRINTPRT                                          92062
         SERVCALL UCBNM,UNITNAME+1  FIND UCB WITH SPECIFIED ADDRESS
         LTR   R3,R0         FOUND ?                             92062
         BZ    PRINTPRT      NO                                  92062
         USING UCBOB,R3      DECLARE IT                          92062
         TM    UCBSTAT,UCBONLI  ONLINE ?                         92062
         BZ    PRINTPRT       NO                                 92062
         CLI   UCBVOLI,C' '  PRINTABLE ?                        GP12066
         BNH   PRINTPRT       NO; SKIP IT                       GP12066
         MVC   VOLSER,UCBVOLI  SHOW OFF                          92062
PRINTPRT PRTLIST SINGLE                                          90088
         B     PRINTLOP      REPEAT FOR ALL                      90088
LISTEND  CLOSE (SMF)                                             90088
         SERVTERM ,          CLASE AND FREE                      90088
         PGMEXIT RC=RETCODE  RETURN TO CALLER                   GP12066
         SPACE 1
         PRINT NOGEN
HEXTRTAB DC    C'0123456789ABCDEF'                              GP12066
SYSPRINT PRTWORK SYSPRINT,TITLE=5                                90088
SUMPRINT PRTWORK SUMPRINT,SYSTERM                                90088
TITLE1   FDOPT NL,CC=C'#'    AUTO TITLE                          92062
         FDPRT 'SMF DEVICE UTILIZATION REPORT',RADJ,LEN=70       92062
         FDPRT *END                                              92062
TITLE3   FDPRT 'DEVICE TYPE',NL,LEN=13                          GP12066
         FDPRT ' DEV',PAD                                       GP12066
         FDPRT 'I/O COUNT',RADJ,PAD,LEN=15                       92062
         FDPRT 'USE COUNT',RADJ,PAD,LEN=15                      GP12066
*DEFER*  FDPRT 'DD COUNT',RADJ,PAD,LEN=15                        92062
         FDPRT 'VOLSER',PAD                                      92062
         FDPRT *END                                              92062
SMF      DCB   DDNAME=SMF,DSORG=PS,MACRF=(GL),EODAD=SMFEOF,            *
               RECFM=VBS,BFTEK=A
         SPACE 1                                                 90088
SINGLE   FDPRT TABTYP,HEX,PAD,NL                                 90088
         FDPRT UNITTYPE                                         GP12066
         FDPRT UNITNAME,PAD,RADJ,LEN=4                          GP12066
         FDPRT TABEXCP,I,RADJ,LEN=15                             90088
         FDPRT TABUSE,I,RADJ,PAD,LEN=15                          92062
*DEFER*  FDPRT TABSTEP,I,RADJ,PAD,LEN=15                         92062
         FDPRT VOLSER,PAD                                        92062
         FDPRT *END                                              90088
         SPACE 1                                                 90088
TABSECT  DSECT ,                                                 90088
TABTYP   DS    0XL2          DEVICE TYPE                         90088
TABTYP3  DS    X             UCBTBYT3                            90088
TABTYP4  DS    X             UCBTBYT4                            90088
TABCUU   DS    XL2           DEVICE ADDRESS                      90088
TABEXCP  DS    XL4           I/O EXCP COUNT                      90088
TABUSE   DS    XL4           NUMBER OF USES                      92062
TABSTEP  DS    XL4           NUMBER OF STEPS                     92062
TABSIZE  EQU   *-TABSECT                                         90088
         SPACE 1
         PRINT GEN
SMFMAPS  DSECT ,
         IFASMFR 4
         ORG   SMFMAPS
         IFASMFR 34
         SPACE 1                                                 90088
SAVE     DSECT ,                                                 90088
DB       DS    D             WORK AREA                           90088
UNITTYPE DS    D             UNIT TYPE                          GP12066
         SERVDEFS ,                                             GP12066
TABXLE   DS    3F            BXLE FOR TABLE                      90088
         SERVSORT DSECT=NO   MAKE SORT EXPANSION                 90088
VOLSER   DS    CL6           CURRENT SERIAL                      92062
UNITNAME DS    CL4,C         EBCDIC UNIT NAME + UNPK SPACER      92062
         SPACE 2                                                 90088
TABLE    DS    4096XL8       MAKE ROOM FOR 4K DEVICES           GP12066
TABEND   EQU   *             PAST LAST ENTRY                     90088
SAVEEND  EQU   *                                                GP12066
         SPACE 2
         PRINT &PRTSYS                                           90088
         DCBD  DEVD=DA,DSORG=PS                                  90088
         IEFUCBOB ,                                              92062
         END
