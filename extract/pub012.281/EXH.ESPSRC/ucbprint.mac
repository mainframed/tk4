UPRT     TITLE 'U C B P R I N T  ***  LIST CURRENT DEVICES'
         COPY  OPTIONGB                                          82008
         SPACE 1                                                 82008
         SYSPARM LIST=YES                                        82008
         EJECT ,
         PRINT &PRTSOR                                           82008
*                                                                82008
*        AUTHOR UNKNOWN - PROGRAM PRINTS LIST OF UNITS IN SYSTEM 82008
*        REQUIRED JCL - SYSPRINT DD FOR OUTPUT                   82008
*        OPTIONAL - SYSIN DD; CARDS (SORTED) CONTAINING UNIT     82008
*              ANNOTATION (COMMENTS) TO BE PRINTED WITH UNIT.    82008
*        ATTRIBUTES - RENT,REFR                                  82008
*        DEPENDENCIES - LOADED MODULES @SERVICE, @PRINTER        82008
*        UPDATED FOR ESA (31-BIT + DYNAMIC DEVICES)             GP99014
         SPACE 1                                                 82008
UCBPRINT PGMHEAD ZERO15,BASE=(R11,R12),PARM=,BNDRY=PAGE,AM=24,RM=24
         SERVINIT ,          LOAD THE @SERVICE ROUTINE           82008
         SERVLOAD @PRINTER,@INPREAD                             GP06244
         PRTOPEN SYSPRINT,OPT=ABEND   ABEND IF NO SYSPRINT DD    82008
         LA    R14,COUNTLEN                                      82008
         L     R15,=A(COUNTEND-COUNTLEN-SAVE)                    82008
         AR    R15,R13       RELOCATE LAST ENTRY ADDRESS         82008
         LA    R0,1                                              82008
         LA    R1,COUNTAB                                        82008
         STM   R14,R1,COUNTERS  INIT BXLE POINTERS               82008
         DEVTYPE SYSIN+DCBDDNAM-IHADCB,DB    LOOK FOR SYSIN      82008
         BXH   R15,R15,GOTOSYN  MISSING; DON'T USE IT            82008
         CLI   DB+3,0        DD DUMMY ?                          82008
         BE    GOTOSYN       YES; DON'T USE                      82008
         INPOPEN SYSIN,OPT=(ABEND)   TRY TO OPEN IT              82008
         BXLE  R15,R15,TSTPRINT   OPEN OK                        82008
         WTO   '*UCBPRINT* --  SYSIN DD CARD UNUSABLE',ROUTCDE=11
GOTOSYN  OI    FLAGS,FGSYN                                       82008
TSTPRINT PRTOPEN SYSPRINT    OPEN SYSPRINT                       82008
         L     R10,CVTPTR          CVT POINTER
         USING CVTMAP,R10                                        82008
         LR    R2,R10
         SH    R2,=H'6'            BACK OFF TO RELEASE ##
         MVC   RELEASE,2(R2)       SAVE FOR PRINTING
         ICM   R2,B'0011',0(R2)
         SLL   R2,4
         ST    R2,DB                                             82008
         OI    DB+3,X'0F'    UNPACK IT                           82008
         UNPK  CPU,DB+1(3)                                       82008
         L     R2,CVTSMCA          GET SYSTEM ID
         MVC   SYSID,16(R2)
         AIF   (&MVSXA OR &MVSESA).NOLCH1                       GP97220
         L     R2,CVTILCH          LCH TABLE
         ST    R2,LCHADDR          SAVE FOR LATER USE
.NOLCH1  PRTLIST VHDR1,TITLE=1 MAKE TITLE                        82008
         PRTLIST VHDR3,TITLE=3  AND SUB-TITLE                    82008
         MVI   PRINT,C' '
         MVC   PRINT+1(L'PRINT-1),PRINT  CLEAR PRINT LINE
         MVI   VPRINT+1,L'PRINT+4  MAKE RDW                      82008
         MVI   COUNT+L'COUNT-1,X'0C'  INIT P CONSTANT            82008
         DROP  R10
         SPACE 2
VOFF10   L     R1,ADDRUCB    GET PRIOR UCB ADDRESS               90241
         AIF   (NOT &MVSESA).OLDTEMP                            GP99014
         AM31  WORK=R15      GET HIGH                           GP99014
LOOKUNIT LTR   R3,R1         ANY UCB ?                          GP99014
         BNZ   LOOKUNXT      YES; CONTINUE                      GP99014
         XC    UNITPREV,UNITPREV                                GP99014
LOOKNIOC IOCINFO IOCTOKEN=UNITOKEN  GET A NEW TOKEN             GP99014
         XC    UNITWORK(UNITCLR),UNITWORK  CLEAR WORK AREA      GP99014
LOOKUNXT UCBSCAN COPY,WORKAREA=UNITWORK,IOCTOKEN=UNITOKEN,             *
               RANGE=ALL,DYNAMIC=YES,UCBAREA=UNITCOPY,                 *
               UCBPAREA=UNITUPFX,DCEAREA=UNITUDEV,DCELEN=UNITUDEL
         LA    R0,UNITCOPY   POINT TO LOCAL UCB                 GP99014
         CH    R15,=H'12'    IOC CHANGED?                       GP99014
         BE    LOOKNIOC      RESTART SCAN                       GP99014
*DEFER*  USING UCBOB,R3      DECLARE IT                         GP99014
         LTR   R1,R3         ANY ?                              GP99014
         BNZ   SUBEXITS      RETURN OK                          GP99014
LOOKUEND SR    R1,R1                                            GP99014
         B     SUBEXITS      AND RETURN                         GP99014
         SPACE 1                                                GP99014
UNITUPAT UCBSCAN MF=(L,UNITPATS)                                GP99014
UNIT000  DC    C'000'        XC MASK FOR NUM/ALPHA COLLATING    GP99014
UNITPREV DC    CL3' ' .      SAVE CURRENT UCB                   GP99014
UNITWORK DC    XL100'0'      WORK AREA                          GP99014
UNITOKEN DC    XL48'0'       IOC TOKEN                          GP99014
UNITUPFX DC    XL48'0'       COPY OF UCB PREFIX                 GP99014
UNITCOPY DC    XL48'0'       COPY OF UCB                        GP99014
UNITPTR  DC    A(0)          RETURNED UCB ADDRESS               GP99014
UNITCLR  EQU   *-UNITWORK    SIZE TO CLEAR                      GP99014
UNITUDEL DC    Y(L'UNITUDEV)  LENGTH OF DEVICE SGEMENT          GP99014
UNITUDEV DC    XL256'0'      DEVICE DEPENDENT SEGMENT           GP99014
SUBEXITS AM24  ,             FIX LATER                          GP99014
         AGO   .OLDTCOM                                         GP99014
.OLDTEMP SERVCALL UCBUM      GET NEXT UCB                        90241
.OLDTCOM BXH   R15,R15,SUMMARY  ALL DONE IF ERROR                90241
         LTR   R9,R0         GET RETURNED ADDRESS                90241
         BZ    SUMMARY       NONE ?                              90241
         ST    R9,ADDRUCB    SAVE IT                             90241
         USING UCBOB,R9                                          82008
         AIF   (&MVSESA).DEFUCB   DO LATER, AFTER AUTHORIZED    GP99014
         STCM  R9,12,HALF    GET HIGH PORTION OF UCB ADDRESS     82008
         BAL   R14,CONVERT   IN EBCDIC                           82008
         MVC   PRUCB(4),TRANADDR  SHOW FIRST PORTION OF ADDRESS  90241
         STH   R9,HALF
         BAL   R14,CONVERT
         MVC   PRUCB+4(4),TRANADDR                               90241
.DEFUCB  MVC   HALF,UCBTBYT1       UCBTYPE FIELD                GP99014
         BAL   R14,CONVERT
         MVC   PRDVT1,TRANADDR
         MVC   HALF,UCBTBYT3
         BAL   R14,CONVERT
         MVC   PRDVT2,TRANADDR
         MVC   HOLDBYT1,UCBTBYT1  SAVE TYPE
         NI    FLAGS,255-FGSPARE  RESET 'SPARE' FLAG             82008
         AIF   (NOT &MVS AND NOT &SVS AND NOT &VS1).NOREP1       90241
         LA    R8,REPTABLE   GET REPLACEMENT TABLE
         LA    R1,(REPENT-REPTABLE)/4  NUMBER OF ENTRIES
LOCREP   CLC   1(2,R8),HOLDBYT3  MATCHING UNIT ?
         BNE   LOCREPX       NO; TRY NEXT
         CLC   0(1,R8),HOLDBYT1  MATCHING OPTION BITS ?
         BNE   LOCREPX       NO
         MVC   HOLDBYT3,3(R8)  REPLACE BY TRUE UNIT
         OI    FLAGS,FGSPARE  SET SPARE UCB FLAG                 82008
         B     LOCREPZ       AND GET OUT
LOCREPX  LA    R8,4(,R8)     BUMP
         BCT   R1,LOCREP     AND TRY AGAIN
LOCREPZ  DS    0H
.NOREP1  AIF   (&MVS).MVS1                                       82195
         MVC   HALF(1),UCBETI  COPY ERROR INDEX
         AGO   .MVS2
.MVS1    L     R10,UCBEXTPT  GET COMMON EXTENSION                80349
         USING UCBCMEXT,R10  DECLARE IT                          80349
         MVC   HALF(1),UCBETI  COPY ERROR INDEX
.MVS2    BAL   R14,CONVERT
         MVC   PRETI,TRANADDR
         MVC   HALF(1),UCBATI  COPY ATTENTION INDEX              90241
         BAL   R14,CONVERT                                       90241
         MVC   PRATI,TRANADDR                                    90241
         AIF   (NOT &MVS).MVS3A                                  82195
         DROP  R10                                               80349
         AIF   (&MVSXA OR &MVSESA).MVS3C SKIP ALT.CHANNEL LOGIC GP97220
         SLR   R2,R2
         IC    R2,UCBLCI           LOGICAL CHANNEL FOR THIS UCB
         SLL   R2,LCHELP2          MULTIPLY LCH-NUMBER FOR DISP.
         AL    R2,LCHADDR    MAKE LCH ENTRY ADDRESS              84148
         USING LCH,R2        DECLARE IT                          84148
         CLI   LCHCHCNT,1    IF ONLY 1 CHANNEL PATH              84148
         BNH   VOFF15              TO DEVICE - NO NEED TO CONTINUE
         SLR   R1,R1                                             84148
         IC    R1,LCHCHCNT   GET NUMBER OF CHANNELS FOR LOOP     84148
         L     R2,LCHTCH     CHANNEL/PATH MASK TABLE             84148
         DROP  R2            DONE WITH LCH                       84148
VOFF2000 MVC   PATHTABL,0(R2)      FIND ALTERNATE PATHS
         LA    R2,PATHTABL
VOFF2020 CLI   0(R2),X'FF'         TABLE END
         BE    VOFF15
         MVC   HALF,0(R2)
*MVT?*   TM    UCBFL5,UCBALTCU  ALTERNATE CONTROL UNIT ADDRESS ? 84148
*MVT?*   BNZ   VOFF2025      YES; JUST GET UNITS DIGIT           84148
         MVZ   HALF+1(1),UCBUA  COPY UCB CONTROL UNIT ADDRESS    84148
VOFF2025 MVN   HALF+1(1),UCBUA  COPY UNIT ADDRESS FROM UCB       84148
         BAL   R14,CONVERT
         CLC   TRANADDR+1(2),UCBNAME  IF PATH MATCHES PRIME      84148
         BE    VOFF2030            PATH - NO NEED TO RECORD
         MVI   PRASL,C'/'
         TM    UCBFL5,UCBALTCU  ALTERNATE CONTROL UNIT ?         89022
         BZ    *+8           NO                                  89022
         MVI   PRASL,C':'    SHOW DUAL ACCESS                    89022
         MVC   PRALT,TRANADDR+1  ELSE SAVE ALTERNATE PATH        84148
         B     VOFF15
VOFF2030 LA    R2,2(,R2)
         BCT   R1,VOFF2020   TRY NEXT ENTRY                      84148
         B     VOFF15        COUNT TOO LARGE ?                   84148
         AGO   .MVS3C
.MVS3A   TM    UCBFL5,UCBALTCU+UCBALTPH  ALTERNATE ADDRESS ?
         BZ    VOFF15        NO; OK AS IS
         SLR   R1,R1
         IC    R1,UCBLCI     GET THE CHANNEL TABLE OFFSET
         SLL   R1,3          *8
         AL    R1,LCHADDR    GET THE CHANNEL ENTRY
         SLR   R2,R2
         ICM   R2,3,6(R1)    GET THE CHANNEL SEARCH CODE
         SH    R2,=H'2'      GET THE CHANNEL MASK ADDRESS
         LR    R1,R2         SAVE IT
         SLR   R2,R2
         IC    R2,0(,R1)     GET THE NUMBER OF ENTRIES
         AR    R2,R2         *2 FOR OFFSET
         LNR   R2,R2
         AR    R2,R1         FIRST ALTERNATE CHANNEL ENTRY
         MVI   HALF,0        CLEAR FOR MVN
PATHLOOP MVN   HALF(1),0(R2)  COPY THIS CHANNEL ADDRESS
         MVC   HALF+1(1),UCBUA  COPY CONTROL UNIT AND UNIT ADDRESS
         TM    1(R2),X'F0'   ALTERNATE CONTROL UNIT ?
         BZ    PATHCON       NO
         MVZ   HALF+1(1),1(R2)  COPY CONTROL UNIT ADDRESS
PATHCON  BAL   R14,CONVERT   MAKE EBCDIC
         CLC   UCBNAME,TRANADDR+1  PRIMARY PATH ?
         BE    PATHBUMP      YES; TRY FOR ANOTHER
         MVI   PRASL,C'/'
         MVC   PRALT,TRANADDR+1  COPY SECOND PATH
         B     VOFF15        CONTINUE
PATHBUMP LA    R2,2(,R2)     TRY NEXT ENTRY
         CR    R2,R1
         BL    PATHLOOP      BUT ONLY IF THERE IS ANOTHER ONE
.MVS3C   SPACE 1
VOFF15   DS    0H
         AIF   (&MVS).MVS4                                       82195
         CLI   HOLDBYT3,X'08'  UNIT RECORD ?
         BNE   VOFF19        NO; NORMALLY
         CLC   =C'00',PRETI  NO ERROR INDEX ?
         BNE   VOFF19        NO; DO NORMALLY
         LA    R8,HASPTBLE   DO AS HASP UNIT
         B     VOFF20
.MVS4    SPACE 1
VOFF19   LA    R8,TYPETBLE         DEVICE TYPE TABLE
VOFF20   CLI   0(R8),X'FF'         TABLE END ??
         BE    VOFF30              PRINT UNKNOWN DEVICE
         CLC   HOLDBYT3(2),0(R8)   IF DEVICE FOUND
         BE    VOFF30              PRINT DESC.
         LA    R8,22(,R8)          NEXT TABLE ENTRY
         B     VOFF20              LOOP
VOFF30   CLI   HOLDBYT3,X'10'  GRAPHICS ?                        89022
         BNE   VOFF30C       NO                                  89022
         CLI   HOLDBYT4,X'09'  3277/78 ?                         89022
         BE    VOFF30T       YES; TEST CONTROLLER                89022
         CLI   HOLDBYT4,X'0B'  3286/87 ?                         89022
         BNE   VOFF30C                                           89022
VOFF30T  TM    UCBAOF1,UCBOFPTR  PREPARE-TO-READ ?               89022
         BZ    VOFF30C       NO                                  89022
         LA    R8,22(,R8)    CHANGE 3277 TO 3278; 3286 TO 3287   89022
VOFF30C  MVC   PROSE,2(R8)
         MVC   PRNAME,UCBNAME  DEVICE ADDR.
         AP    COUNT,=P'1'         SEQUENTIAL COUNT
         MVC   PRSEQ-1(L'PRSEQ+1),=X'402020202120'  MAKE EDIT MASK
         AIF   (&MVSXA OR &MVSESA).DEVNO1                       GP97246
         ED    PRSEQ-1(L'PRSEQ+1),COUNT  EDIT UNIT COUNTER
         MVC   PRSEQ,PRSEQ+1   MAKE IT PRETTY
         AGO   .DEVCM1                                          GP97246
.DEVNO1  SLR   R0,R0                                            GP97246
         ICM   R0,3,UCBCHAN  GET DEVICE NUMBER                  GP97246
         CVD   R0,DB         MAKE PACKED                        GP97246
         ED    PRSEQ-1(L'PRSEQ+1),DB+5  EDIT DEVICE NUMBER      GP97246
.DEVCM1  MVC   PRONF(2),=C'ON'  PROVISIONALLY SET ON-LINE
         TM    UCBSTAT,UCBONLI     ONLINE ??
         BO    VOFF36        YES
         MVC   PRONF,=C'OFF'
VOFF36   CLI   HOLDBYT3,UCB3COMM  COM DEVICE ??
         BNE   VOFF60
         LA    R8,COMMTBLE         GET COMM TYPE TABLE
VOFF40   MVC   HALF+0(1),HOLDBYT1   TEST SAVED UCBTBYT1 & 4
         MVC   HALF+1(1),HOLDBYT4                                80353
         NC    HALF,0(R8)                                        80353
         CLC   HALF,0(R8)                                        80353
         BE    VOFF45        ALL SET - MOVE TO PRTELE            80353
         LA    R8,8(,R8)           NEXT TABLE ENTRY              80353
         CLI   0(R8),X'FF'   ANY MORE ?                          80353
         BNE   VOFF40        YES                                 80353
         B     VOFF1010                                          80353
VOFF45   MVC   PRTELE,2(R8)  PRINT TP ATTRIBUTE                  80353
         B     VOFF1010
VOFF60   CLI   HOLDBYT3,UCB3TAPE   TAPE ??
         BNE   VOFF200
         CLI   UCBVOLI,X'84'   LOCAL DUMMY UNIT ?
         BNE   *+8
         OI    FLAGS,FGSPARE   FLAG AS SPARE UNIT                82008
         LA    R8,MAGTAPET         GET MAG TAPE ENTRIES
         TM    HOLDBYT4,X'80'  NEW-FANGLED ?                    GP99014
         BZ    VOFF100       NO                                 GP99014
         LA    R8,MAGTAPE8   SET FOR NEW DRIVES                  85016
VOFF100  MVC   HALF,HOLDBYT1    TEST SAVED UCBTBYT1 & 2          80353
         NC    HALF,0(R8)                                        80353
         CLC   HALF,0(R8)                                       GP99014
         BNE   VOFF110       NO MATCH - GET NEXT                GP99014
         SLR   R1,R1                                             80353
         IC    R1,22(,R8)    GET PRINT FIELD OFFSET              80353
         LA    R1,PRINT(R1)                                      80353
         MVC   0(20,R1),2(R8)  MOVE ATTRIBUTE TO PRINT           80353
         TM    HOLDBYT4,X'80'  NEW-FANGLED ?                    GP99014
         BNZ   VOFF1000      YES; NO MORE TO DO                 GP99014
VOFF110  LA    R8,23(,R8)    GET NEXT TABLE ENTRY                80353
         CLI   0(R8),X'FF'   ANY MORE ?                          80353
         BNE   VOFF100       YES; TRY IT                         80353
         B     VOFF1000                                          80353
VOFF200  TM    HOLDBYT3,UCB3DACC   DISK TYPE ??
         BNO   VOFF300
         CLI   UCBVOLI,X'84'   LOCAL DUMMY UNIT ?
         BNE   *+8
         OI    FLAGS,FGSPARE   FLAG AS SPARE UNIT                82008
         TM    UCBSTAT,UCBONLI     ONLINE ??
         BNO   VOFF210
         CLI   UCBVOLI,C' '  VOLID PRESENT AND PRINTABLE ?
         BNH   VOFF210       NO
         CLI   UCBVOLI,C'9'   SCRATCH VOLSER ?
         BH    VOFF210       YES; SKIP IT
         MVC   PRVOL,UCBVOLI SHOW VOLSER PRESENT
VOFF210  TM    HOLDBYT2,UCBRR      SHARABLE ???
         BNO   VOFF220                                           82008
         MVC   PRSHARE,=C'SHARABLE'
VOFF220  TM    HOLDBYT2,UCBRVDEV   REAL DEVICE ?                 82008
         BZ    VOFF300       YES ?                               82008
         MVC   PRSHARE,=CL8'VIRTUAL'                             82008
VOFF300  DS    0H
VOFF1000 DS    0H
VOFF1010 BAL   R14,VOFF7000        GET INPUT CARDS FOR COMMENTS
         AIF   (NOT &MVS).NMSSFK                                 85016
         CLC   =X'0842',UCBTBYT3   MSS ?                         85016
         BE    VOFFNSPR      YES; NO FUNNY SPARE                 85016
         TM    UCBJBNR,UCBOLDSM  OLTEP BIT 1 ?                   85016
         BZ    VOFFNSPR      NO                                  85016
         TM    UCBFL5,UCBNALOC   OLTEP BIT 2 ?                   85016
         BZ    VOFFNSPR                                          85016
         OI    FLAGS,FGSPARE  FAKE SPARE                         85016
.NMSSFK  SPACE 1                                                 85016
VOFFNSPR TM    FLAGS,FGSPARE   SPARE UCB ?                       82008
         BZ    PRINTUCB
         MVC   TEMPHOLD,PROSE   MOVE DEVICE DESCRIPTION
         MVC   PROSE(6),=C'SPARE '
         MVC   PROSE+6(L'TEMPHOLD),TEMPHOLD  OVERLAY PRVOL
PRINTUCB PRTV  VPRINT                                            82008
         SPACE 1                                                 80349
         LM    R14,R1,COUNTERS  GET TABLE OF COUNTERS            80349
         TM    FLAGS,FGSPARE   SPARE UNIT ?                      82008
         BZ    COUNTLOP      NO                                  80353
         MVI   PROSE,0       FINAGLE FOR SORT                    80353
COUNTLOP CLC   2(50,R1),PROSE   CORRECT ENTRY ?                  80349
         BE    COUNTIT       YES                                 80349
         ICM   R2,3,0(R1)    EMPTY ENTRY ?                       82008
         BNZ   COUNTUP       NO; TRY AGAIN                       82008
         MVC   2(50,R1),PROSE  SAVE THE ENTRY DESCRIPTION        80349
COUNTIT  AH    R0,0(,R1)     UP THE COUNT                        80349
         STH   R0,0(,R1)     STASH THE COUNT                     80349
         B     COUNTCLR      SKIP OUT                            80349
COUNTUP  BXLE  R1,R14,COUNTLOP  TRY AGAIN                        80349
COUNTCLR MVI   PRINT,C' '                                        80349
         MVC   PRINT+1(132),PRINT
VOFF1020 DS    0H                                                90241
         B     VOFF10              LOOP THRU UCB CHAIN
         SPACE 2
SUMMARY  PRTV  SUMSUB,TITLE=3  PRINT SUMMARY SUB-TITLE           82008
         LM    R4,R7,COUNTERS  GET POINTERS                      80353
         LR    R2,R7         ADDRESS OF FIRST ENTRY              80353
         LR    R6,R4         LENGTH OF AN ENTRY                  80353
         LR    R7,R5         1ST BXLE                            80353
         SR    R5,R4         NEXT TO LAST ENTRY ADDR FOR         80353
SORTBGIN LA    R3,0(R2,R6)   NEXT ENTRY                          80353
SORTCLC  CLC   2(50,R2),2(R3)                                    80353
         BNH   SORTEND                                           80353
SORTXC   XC    0(52,R2),0(R3)                                    80353
         XC    0(52,R3),0(R2)                                    80353
         XC    0(52,R2),0(R3)                                    80353
SORTEND  BXLE  R3,R6,SORTCLC                                     80353
         BXLE  R2,R4,SORTBGIN                                    80353
         LM    R2,R5,COUNTERS  GET TABLE OF COUNTER ADDRESSES    80349
SUMLOOP  LH    R0,0(,R5)     GET UNIT COUNT                      82008
         LTR   R0,R0         EMPTY ?                             82008
         BZ    SUMSKIP       YES; SKIP IT                        82008
         CVD   R0,DB         MAKE PACKED                         82008
         MVC   PROSE-8(6),=X'402020202120'  SET MASK             80349
         ED    PROSE-8(6),DB+5  EDIT UNIT COUNT                  82008
         MVC   PROSE(50),2(R5)  ADD DEVICE DESCRIPTION           80349
         CLI   PROSE,X'00'   FUNNY SPARE ?                       80353
         BNE   *+8           NO                                  80353
         MVI   PROSE,C'S'    RESTORE                             80353
         PRTV  VPRINT        PRINT IT                            82008
SUMSKIP  BXLE  R5,R2,SUMLOOP  REPEAT FOR NEXT DEVICE             80349
         SPACE 2                                                 80349
CLOSE    SERVTERM ,          CLOSE AND FREE EVERYTHING           82008
         PGMEXIT RC=0        RETURN WITH ZERO COMPETION CODE     82008
         SPACE 2
CONVERT  UNPK  DB(5),HALF(3)  UNPACK                             82008
         MVC   TRANADDR(4),DB  MOVE FIRST 4                      82008
         TR    TRANADDR(4),TRANSLAT-C'0'  MAKE PRINTABLE
         BR    R14
         SPACE 2
VOFF6000 STM   R2,R3,DB      SAVE REGS                           82008
         SLR   R1,R1
VOFF6020 SLL   R1,4
         IC    R3,0(,R2)
         N     R3,=F'15'
         OR    R1,R3
         CLI   0(R2),C'F'          IF CHAR BETWEEN A - F
         BH    VOFF6040            MUST ADD 9 TO THE VALUE
         LA    R1,9(,R1)           IN R5 TO GET CORRECT HEX
VOFF6040 LA    R2,1(,R2)           NEXT CHAR.
         CLI   0(R2),X'C1'
         BNL   VOFF6020
         LM    R2,R3,DB                                          82008
         BR    R14
         SPACE 2
VOFF7000 TM    FLAGS,FGSYN   END OF INPUT OR NO SYSIN ?          82008
         BNZR  R14           YES; JUST RETURN                    82008
         ST    R14,HOLD7000
         LA    R2,UCBNAME
         BAL   R14,VOFF6000
         ST    R1,UCBHEXAD
VOFF7010 CLC   UCBHEXAD,ENDADR     IF CURRENT UCB OUTSIDE CARD
         BH    VOFF7020
         CLC   UCBHEXAD,STARTADR
         BL    VOFF7099
         MVC   PRDATE,INDATE
         MVC   PRUSE,INUSE
         B     VOFF7099
VOFF7020 INPGET INPUT        GET A CARD INTO INPUT              GP03034
         BXH   R15,R15,INEOD  TREAT ERROR AS END-FILE            82008
         CLI   INDASH,C'-'
         BE    VOFF7022
         MVI   INDASH,C'-'
         MVC   INHIGH,INLOW
VOFF7022 MVI   INHIGH+L'INHIGH,C' '
         LA    R2,INLOW
         BAL   R14,VOFF6000
         ST    R1,STARTADR
         CLI   INDASH,C'-'
         BNE   VOFF7030
         LA    R2,INHIGH
         BAL   R14,VOFF6000
VOFF7030 ST    R1,ENDADR
         B     VOFF7010
INEOD    OI    FLAGS,FGSYN   SET END OF INPUT                    82008
VOFF7099 L     R14,HOLD7000
         BR    R14
         EJECT ,
SYSPRINT PRTWORK SYSPRINT,TITLE=5    PRINT FILE                  82008
SYSIN    INPWORK SYSIN,WIDTH=80,EODAD=INEOD,EDIT=128,FILL=C' '   82008
TRANSLAT DC    CL16'0123456789ABCDEF'
         SPACE 1                                                 82008
VHDR1    FDOPT NL,CC=C'#'    DEFINE TITLE (AUTOMATIC NUMBERING)  82008
         FDPRT '   SYSTEM UNIT CONTROL BLOCK DESCRIPTION    ',PAD
         FDPRT 'MODEL',PAD                                       82008
         FDPRT CPU,DEBZ,DEBL,PAD,LEN=6                           82008
         FDPRT 'SMCA ID='                                        82008
         FDPRT SYSID,DEB,PADR,LEN=6                              82008
         FDPRT 'RELEASE',PAD                                     82008
         FDPRT RELEASE                                           82008
         FDPRT *END                                              82008
         SPACE 1
         AIF   (&MVSXA OR &MVSESA).DEVNO2                       GP97246
VHDR3    FDPRT ' SEQ  ADR/ALT',NL,LEN=20                         82008
         AGO   .DEVCM2                                          GP97246
.DEVNO2  ANOP  ,                                                GP97246
VHDR3    FDPRT ' DEV# ADR/ALT',NL,LEN=20                        GP97246
.DEVCM2  FDPRT 'DEVICE TYPE AND SPECIAL ATTRIBUTES',LEN=60       82008
         FDTM  FLAGS,FGSYN,BO=VHDR3NS                            82008
         FDPRT 'INSTALLED  USAGE'                                82008
VHDR3NS  FDOPT SBA=(*,-23)                                       90241
         FDPRT ' UCBADDR UCB-TYPE ER AT'                         90241
         FDPRT *END                                              82008
         SPACE 1                                                 80349
SUMSUB   VCON  '  NUMBER OF UNITS BY DEVICE TYPE'                82008
         SPACE 2
         LTORG ,
         EJECT ,                                                 82008
SAVE     DSECT ,             WORK/SAVE AREA                      82008
DB       DS    D             WORK WORD                           82008
         SERVDEFS ,          ADDRESS OF SERVICE ROUTINES        GP06244
ADDRUCB  DS    A             UCB ADDRESS                         90241
HOLD7000 DS    F
UCBHEXAD DC    F'0'
STARTADR DC    F'0'
ENDADR   DC    F'0'
LCHADDR  DS    F
HOLDBYT1 DC    0XL4'0',X'0'  SAVED UCBTBYT1
HOLDBYT2 DC    X'00'         SAVED UCBTBYT2
HOLDBYT3 DC    X'00'         SAVED UCBTBYT3
HOLDBYT4 DC    X'00'         SAVED UCBTBYT4
PATHTABL DS    CL8                 CHANNEL PATHS
TRANADDR DS    0CL4
BYTE1    DC    C' '
BYTE2    DC    C' '
BYTE3    DC    C' '
BYTE4    DC    C' '
HALF     DC    H'0'
INPUT    DC    CL80' '             INPUT CARD FORMAT
         ORG   INPUT
INLOW    DC    CL3' '              BEGINNING UCB ADDRESS
INDASH   DC    C' '                DASH
INHIGH   DC    CL3' ',CL2' '       ENDING UCB ADDRESS
INDATE   DC    CL9' ',C' '         DEVICE INSTALLED
INUSE    DC    CL20' '             USAGE OF DEVICE
INCOMM   DC    CL40' '             COMMENTS
         ORG   ,
         SPACE 2
VPRINT   DC    Y(137,0)      RDW FOR PRINT
PRINT    DS    0CL133              PRINT IMAGE
CARRIAGE DC    CL1' '              CARRIAGE CONTROL
PRSEQ    DC    CL5' ',C' '   DEVICE SEQUENCE NUMBER
PRNAME   DC    CL3' '        UNIT NAME
PRASL    DC    C' '           SLASH IF ALTERNATE
PRALT    DC    CL3' ',C' '   ALTERNATE UNIT ADDRESS
PRONF    DC    CL3' ',C' '   ON/OFF-LINE STATUS
PROSE    DC    CL20' ',CL10' '  LONG DEVICE DESCRIPTION
         ORG   PROSE+14                                          80353
PRTAPE   DC    CL10' '       TAPE DESCRIPTION
         ORG   PROSE+30
PRTELE   DC    0CL6' '       TP DEVICE
PRSHARE  DC    0CL8' '       SHARED DEVICE
PRTAPE2  DC    CL20' '
PRVOL    DC    CL6' ',CL4' '  DISK VOLUME SERIAL
PRDATE   DC    CL9' ',C' '   INSTALLATION DATE
PRUSE    DC    CL20' ',C' '  USAGE/COMMENT                       82008
PRUCB    DC    CL8' ',C' '   UNIT ADDRESS                        90241
PRDVT1   DC    CL4' '        UCBTBYT1-UCBTBYT2
PRDVT2   DC    CL4' ',C' '   UCBTBYT3-UCBTBYT4
PRETI    DC    CL2' '        ERROR TABLE INDEX
         DC    C' '                                              90241
PRATI    DC    CL2' '        ATTENTION INDEX                     90241
         ORG   PRINT+L'PRINT                                     82008
         SPACE 1
TEMPHOLD DC    CL(PRVOL-PROSE)' '   TEMP HOLD AREA FOR 'SPARE'
FLAGS    DC    X'00'         PROCESSING FLAGS                    82008
FGSYN    EQU   X'80'           END OF INPUT OR NO SYSIN DD       82008
FGSPARE  EQU   X'40'           UNUSED UCB (SPARE DEVICE)         82008
CPU      DS    CL4           CPU MODEL                           82008
SYSID    DS    CL4           SMCA SYSTEM/MODEL ID                82008
RELEASE  DS    CL4           CVT RELEASE NUMBER                  82008
COUNT    DC    PL3'0'
         SPACE 2                                                 80349
*        STORAGE TABLE FOR COUNT FACILITY                        80349
*                                                                80349
COUNTERS DC    A(COUNTLEN,COUNTEND-COUNTLEN,1,COUNTAB)           80349
COUNTAB  DC    H'0',CL50' '  UCB COUNTER / DESCRIPTION           80349
COUNTLEN EQU   *-COUNTAB                                         80349
         DC    199CL52' '    EXTRA ENTRIES                       80349
COUNTEND EQU   *                                                 80349
SAVEEND  EQU   *             END OF GOTTEN STORAGE               82008
         SPACE 2
         PRINT &PRTSYS                                           82008
CVTDSECT DSECT ,                                                 82008
         CVT   DSECT=YES                                         82008
         SPACE 1                                                 82008
         IHACDE ,                                                82008
         SPACE 1                                                 82008
         IEFUCBOB ,                                              82008
         AIF   (&MVS).UCBRR                                      82008
UCBRR    EQU   UCB2OPT2      SHARED DASD OPTION                  82008
UCBRPS   EQU   UCB2OPT3      RPS FEATURE                         82008
UCBRVDEV EQU   UCB2OPT4      VIRTUAL DEVICE                      82008
         AGO   .NLCHMP                                           84148
.UCBRR   AIF   (&MVSXA OR &MVSESA).NLCHMP                       GP97220
         IECDLCH ,           COPY LOGICAL CHANNEL MAPPING        84148
.NLCHMP  DCBD  DSORG=PS                                          84148
         EJECT ,
UCBPRINT CSECT ,                                                 82008
COMMTBLE DS    0H
         DC    X'0810',CL6'2740X '
         DC    X'0790',CL6'BSC3  '
         DC    X'0710',CL6'1050X '
         DC    X'0690',CL6'BSC2  '
         DC    X'0610',CL6'2741P '
         DC    X'0590',CL6'BSC1  '
         DC    X'0510',CL6'2741C '
         DC    X'0410',CL6'2740  '
         DC    X'0310',CL6'2740CC'
         DC    X'0240',CL6'115A  '
         DC    X'0210',CL6'1060  '
         DC    X'0180',CL6'2260  '
         DC    X'0170',CL6'3270  '
         DC    X'0160',CL6'WTTA  '
         DC    X'0150',CL6'TWX   '
         DC    X'0140',CL6'83B3  '
         DC    X'0130',CL6'1050  '
         DC    X'0120',CL6'1030  '
         DC    X'0110',CL6'1050  '
         DC    X'FF'
         SPACE 2
PRTPOFF1 EQU   PRTAPE-PRINT                                      80353
PRTPOFF2 EQU   PRTAPE2-PRINT                                     80353
         SPACE 1                                                 80353
MAGTAPE8 DC    X'3300',CL20'E COMPATIBILITY MODE',AL1(PRTPOFF1)  90241
         DC    X'7804',CL20'E  COMPRESS OPTION',AL1(PRTPOFF1)   GP99014
         DC    X'7800',CL20'E  STANDARD MODE',AL1(PRTPOFF1)      90241
         DC    X'FF'                                             85016
MAGTAPET DC    X'0400',CL20'1600BPI             ',AL1(PRTPOFF1)  80353
         DC    X'0200',CL20'6250BPI             ',AL1(PRTPOFF1)  80353
         DC    X'0080',CL20'7-TRACK             ',AL1(PRTPOFF1)  80353
         DC    X'0040',CL20'DATA CONVERT        ',AL1(PRTPOFF2)  80353
         DC    X'0020',CL20'DUAL 800/1600       ',AL1(PRTPOFF2)  80353
         DC    X'0010',CL20'DUAL 1600/6250      ',AL1(PRTPOFF2)  80353
         DC    X'0004',CL20'ASP CTC PSEUDO-UNIT ',AL1(PRTPOFF2)  80353
         DC    X'FF'
         SPACE 1
         AIF   (&MVS).MVS5                                       82195
HASPTBLE DC    X'0801',CL20'HASP SYSIN'
         DC    X'0802',CL20'HASP PUNCH'
         DC    X'0805',CL20'HASP INTRDR'
         DC    X'0808',CL20'HASP PRINTER'
.MVS5    SPACE 1
TYPETBLE DC    X'0801',CL20'2540 CARD READER'
         DC    X'0802',CL20'2540 CARD PUNCH'
         DC    X'0803',CL20'1443 CARD READ/PUNCH'
         DC    X'0804',CL20'2501 CARD READER'
         DC    X'0805',CL20'2520 CARD READ/PUNCH'
         DC    X'0806',CL20'3505 CARD RDR'
         DC    X'0808',CL20'1403 PRINTER'
         DC    X'0809',CL20'3211 PRINTER'
         DC    X'080A',CL20'1443 PRINTER'
         DC    X'080B',CL20'3203 PRINTER'                        82130
         DC    X'080C',CL20'3525 PUNCH'
         DC    X'080E',CL20'3800 PRINT SUBSYSTEM'
         DC    X'080F',CL20'ADV FUNCT PRINTER 1 '                90241
         DC    X'0810',CL20'2671 PAPER TAPE RDR'
         DC    X'0811',CL20'4245 PRINTER        '                90241
         DC    X'0813',CL20'4248 PRINTER        '                90241
         DC    X'0816',CL20'3890 DOC. PROCESSOR'                 80349
         DC    X'0817',CL20'3886 OCR'                            80349
         DC    X'0818',CL20'2495 TAPE CART. RDR'
         DC    X'0819',CL20'3895 DOC. RDR/INS.'                  80349
         DC    X'081A',CL20'1285 OPTICAL READER'
         DC    X'081B',CL20'1287 OPTICAL READER'
         DC    X'081C',CL20'1288 OPTICAL READER'
         DC    X'081D',CL20'1419 PRIMARY CTL UNT'
         DC    X'081E',CL20'1419 OR 1275 SEC CTL'
         DC    X'081F',CL20'1275 PRIMARY CTL UNT'
         DC    X'0820',CL20'1052 CONSOLE'
         DC    X'0821',CL20'2150 CONSOLE'
         DC    X'0822',CL20'3210 CONSOLE'
         DC    X'0823',CL20'3215 CONSOLE'
         DC    X'0830',CL20'3213 CONSOLE PRINTER'
         DC    X'083D',CL20'7443 MODEL 1 SRF'                    80353
         DC    X'0842',CL20'3851 MASS STG. CON.'                 82005
         DC    X'0844',CL20'3540 DISKETTE'                       81047
         DC    X'084C',CL20'3838 ARRAY PROCESSOR'                80353
         DC    X'8001',CL20'2400 MAG TAPE'
         DC    X'8003',CL20'3400 MAG TAPE'
         DC    X'8080',CL20'3480 CART. TAPE'                     85016
         DC    X'8081',CL20'3490 CART. TAPE'                    GP99014
*OLD*    DC    X'8081',CL20'9348 TAPE DRIVE'                     90241
         DC    X'8082',CL20'3423 TAPE DRIVE'                    GP97220
         DC    X'8083',CL20'3590 CART. TAPE'                    GP99211
         DC    X'2001',CL20'2311 DISK'
         DC    X'2002',CL20'2301 DRUM'
         DC    X'2003',CL20'2303 DRUM'
         DC    X'2004',CL20'2302 DISK'                           80353
         DC    X'2005',CL20'2321 DATA CELL'
         DC    X'2006',CL20'2305 DISK, MODEL 1'                  82008
         DC    X'2007',CL20'2305 DISK, MODEL 2'                  82008
         DC    X'2008',CL20'2314 DISK'
         DC    X'2009',CL20'3330 DISK, MODEL 1'                  82008
         DC    X'200A',CL20'3340 DISK'
         DC    X'200B',CL20'3350 DISK'
         DC    X'200C',CL20'3375 DISK'                           82008
         DC    X'200D',CL20'3330 DISK, MODEL 11'                 82008
         DC    X'200E',CL20'3380 DISK'                           82008
         DC    X'200F',CL20'3390 DISK'
         DC    X'1002',CL20'2250 DISPLAY TUBE'
         DC    X'1003',CL20'2260 DISPLAY TUBE'
         DC    X'1004',CL20'1053 PRINTER'
         DC    X'1005',CL20'2280'
         DC    X'1006',CL20'2282'
*OLD*    DC    X'1007',CL20'MOD 85 CONSOLE'                      81047
         DC    X'1008',CL20'3066 SYSTEM CONSOLE'
         DC    X'1009',CL20'3277 DISPLAY'   IF PTREAD OPTION OFF
         DC    X'1009',CL20'3278 DISPLAY'   IF PTREAD OPTION ON  81047
         DC    X'100A',CL20'3284 PRINTER'
         DC    X'100B',CL20'3286 PRINTER'   IF PTREAD OFF
         DC    X'100B',CL20'3287 PRINTER'   IF PTREAD ON         89022
         DC    X'100C',CL20'3158 DISPLAY CONSOLE'
         DC    X'100D',CL20'3036 DISPLAY CONSOLE'
         DC    X'100E',CL20'3138 DISPLAY CONSOLE'                81047
         DC    X'100F',CL20'3148 DISPLAY CONSOLE'                81047
         DC    X'4011',CL20'2702 IBM TYPE1'
         DC    X'4012',CL20'2701 IBM TYPE1'
         DC    X'4013',CL20'2703 IBM TYPE1'
         DC    X'4014',CL20'2955 CE TERMINAL'
         DC    X'401F',CL20'5098'                                81047
         DC    X'4015',CL20'3705 CONTROLLER CA1'                 80353
         DC    X'4021',CL20'2702 IBM TYPE2'
         DC    X'4022',CL20'2701 IBM TYPE2'
         DC    X'4023',CL20'2703 IBM TYPE2'
         DC    X'4025',CL20'3705 CONTROLLER CA2'                 80353
         DC    X'4031',CL20'2702-IBM TTY'
         DC    X'4032',CL20'2701-IBM TTY'
         DC    X'4033',CL20'2703-IBM TTY'
         DC    X'4041',CL20'2702 TTY TYPE I'
         DC    X'4042',CL20'2701 TTY TYPE I'
         DC    X'4043',CL20'2703 TTY TYPE I'
         DC    X'4051',CL20'2702 TTY TYPE II'
         DC    X'4052',CL20'2701 TTY TYPE II'
         DC    X'4053',CL20'2703 TTY TYPE II'
         DC    X'4061',CL20'2702 WTTA'
         DC    X'4062',CL20'2701 WTTA'
         DC    X'4063',CL20'2703 WTTA'
         DC    X'4071',CL20'2702 SYNCH TYPE I'
         DC    X'4072',CL20'2701 SYNCH TYPE I'
         DC    X'4073',CL20'2703 SYNCH TYPE I'
         DC    X'4081',CL20'2702 IBM TYPE III'
         DC    X'4082',CL20'2701 IBM TYPE III'
         DC    X'4083',CL20'2703 IBM TYPE III'
         DC    X'4091',CL20'2702 SYNCH TYPE II'
         DC    X'4092',CL20'2701 SYNCH TYPE II'
         DC    X'4093',CL20'2703 SYNCH TYPE II'
         DC    X'40F1',CL20'3791 CONTROLLER'                     80353
         DC    X'4100',CL20'CHANNEL-TO-CHANNEL'
         DC    X'4101',CL20'SERIAL CTC'                         GP97220
         DC    X'4102',CL20'ESCON BASIC MODE CTC'               GP99014
         DC    X'4103',CL20'RS6000 CTC'                         GP97220
         DC    X'4104',CL20'3172 CTC'                           GP97220
         DC    X'4105',CL20'OPEN SYS ADAPTER'                   GP97220
         DC    X'4106',CL20'OSA DIAGNOSTIC DVC'                 GP97220
         DC    X'4107',CL20'OSA  RESERVED 07'                   GP97220
         DC    X'4108',CL20'OSA  RESERVED 08'                   GP97220
         DC    X'4109',CL20'OSA  RESERVED 09'                   GP97220
         DC    X'410A',CL20'OSA  RESERVED 0A'                   GP97220
         DC    X'410B',CL20'OSA  RESERVED 0B'                   GP97220
         DC    X'410C',CL20'OSA  RESERVED 0C'                   GP97220
         DC    X'410D',CL20'OSA  RESERVED 0D'                   GP97220
         DC    X'410E',CL20'OSA  RESERVED 0E'                   GP97220
         DC    X'410F',CL20'OSA  RESERVED 0F'                   GP97220
         DC    X'0000',CL20'DUMMY UCB'
         DC    X'0100',CL20'DUMMY UCB'                           81047
UNKNOWN  DC    X'FFFF',CL20'UNKNOWN DEVICE TYPE'
         SPACE 2
         AIF   (NOT &MVS AND NOT &SVS AND NOT &VS1).NOREP2       90241
REPTABLE DC    X'50400408'   SPARE 2501
         DC    X'10400808'   SPARE 1403
         DC    X'10400908'   SPARE 3211
         DC    X'30400820'   SPARE 2314
         DC    X'30400920'   SPARE 3330-1
         DC    X'30400D20'   SPARE 3330-11
         DC    X'32400380'   SPARE 6250 TAPE
         DC    X'34400380'   SPARE 1600 TAPE
REPENT   EQU   *
.NOREP2  END   ,
