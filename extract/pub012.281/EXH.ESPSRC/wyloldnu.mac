WYLOLDNU TITLE 'W Y L O L D N U  ***  RENAME OLD TO NEW WYLBUR'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         EJECT ,
         PRINT &PRTSOR
***********************************************************************
*                                                                     *
*    THIS ROUTINE IS A USER EXIT (OPTIONAL) FOR THE COPYVOL PROGRAM.  *
*        IT RENAMES OLD WYLBUR DATASETS (RAND/AMS/OBS/ASC FORMAT)     *
*    FROM:   WYL.ACCOUNT#.USERNAME.DANGLE                             *
*      TO:   USERNAM.ACCOUNT#.DANGLE                                  *
*         NOTE THAT THE USER ID IS CLIPPED FROM 8 TO 7 BYTES          *
*                                                                     *
***********************************************************************
WYLOLDNU START 0
         USING WYLOLDNU,R15
         STM   R14,R12,12(R13)  SAVE REGISTERS
         LM    R4,R7,0(R1)   LOAD PASSED DATA POINTERS
         USING OLDDSN,R4     POINT TO OLD ONE
         USING NEWDSN,R6       AND NEW NAME (CALLER PRE-BLANKED) 93002
         SLR   R9,R9         SET RETURN CODE
         CLC   =C'WYL.',OLDDSN  IS IT AN OLD NAME ?
         BNE   GETOUT        NO; COPY AS IS
         CLI   OLDDSN+12,C'.'  MAKE SURE
         BNE   GETOUT        NO
         CLI   OLDDSN+21,C'.'  OTHER POINT ?
         BNE   GETOUT
         LA    R9,4          SET RETURN CODE FOR RENAME
         MVC   NEWDSN(7),OLDDSN+13  MOVE DSN AND CLIP LAST BYTE
         MVC   NEWDSN+7(9),OLDDSN+3  MOVE PERIOD AND ACCOUNT
         MVC   NEWDSN+16(23),OLDDSN+21  MOVE PERIOD AND REST OF NAME
GETOUT   ST    R9,16(,R13)   SET RETURN CODE
         LM    R14,R12,12(R13)  RESTORE ALL REGISTERS
         BR    R14           RETURN
         SPACE 1
OLDDSN   DSECT ,
OLDNAME  DS    CL44
NEWDSN   DSECT ,
NEWNAME  DS    CL44
         SPACE 1
         PRINT &PRTSYS
         REGEQU ,
         END   ,
