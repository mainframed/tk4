DUMPER   TITLE 'D U M P E R  ***  DUMP FULL OR SELECTED DATA SET'
 PUNCH ' ORDER DUMPER(P) '   MAKE DUMPS EASIER TO READ
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
***********************************************************************
**                                                                   **
**  DUMPER   PRINTS ALL OR SELECTED RECORDS FROM A DATA SET IN       **
**           DUMP FORMAT      (new 2009-08-08 for RECV380 debugging) **
**  SYSUT1 - INPUT DATA SET                                          **
**  SYSTERM - PRINTED DUMP OUTPUT                                    **
**                                                                   **
**  PARM=LIMIT          (E.G., PARM=123 PRODUCES 123 RECORDS)        **
**  PARM='LIMIT,START'  (E.G., PARM='123,5' SKIPS 5, THEN PRINTS 123)**
**                                                                   **
***********************************************************************
**  THIS IS A QUICK AND DIRTY ADAPTATION OF 'METZCOPY'               **
**     WRITTEN BY SEYMOUR J. (SHMUEL) METZ CIRCA 1969                **
***********************************************************************
         PRINT &PRTSOR
         EXTRN @DCBXLST,SUBPDUMP,SUBTIODD                       GP09220
DUMPER   PGMHEAD ZERO12,BASE=R12,AM=31,RM=24, *O*L*D* CODE       90280 *
               PARM=R9
*DEFER*  BANDAID INIT                                           GP03114
         SERVINIT ,                                             GP03114
         SERVLOAD @PARSER,@PRINTER                              GP03114
         PRTOPEN PRINTER,OPT=(ABEND)   FAIL IF MISSING          GP09220
         SPACE 1
*---------------------------------------------------------------------*
*  INITIALIZE DYNAMIC AREA                                            *
*---------------------------------------------------------------------*
         LA    R0,SYSUT1
         ST    R0,OPL1                                           *TSM*
         OC    OPL1(MODEND-MODOPL),MODOPL                        *TSM*
         LA    R1,SYSUT1+DCBDDNAM-IHADCB                        GP09220
         SUBCALL SUBTIODD    GET TIOENTRY AND JFCB FOR SYSUT1   GP09220
         LTR   R1,R1         ANY?                               GP09220
         BZ    BADUTDD       NO                                 GP09220
         LR    R3,R0         COPY JFCB ADDRESS                  GP09220
         USING INFMJFCB,R3   DECLARE IT                         GP09220
         PRTLIST HEADER1,TITLE=1                                GP09220
         B     PREPUT1                                          GP09220
HEADER1  FDOPT NL,CC=C'#'    TIME/DATE/PAGE # IN HEADER         GP09220
         FD    'Data set dump of',RADJ,LEN=30                   GP09220
         FD    JFCBDSNM,PAD                                     GP09220
         FD    JFCBVOLS,6,PAD                                   GP09220
         FD    *END                                             GP09220
*---------------------------------------------------------------------*
*  ANALYZE PARM FIELD - OK IF NONE; ELSE WANT 1 OR 2 NUMERICS         *
*---------------------------------------------------------------------*
PREPUT1  MVC   LIMCNT,=X'7FFFFFFF'  NO END LIMIT (SKIP IS ZERO) GP03114
         LA    R9,0(,R9)                                        GP03114
         LTR   R9,R9                                            GP03114
         BZ    DONEPARM      NO PARM POINTER ADDRESS            GP03114
         L     R9,0(,R9)                                        GP03114
         LA    R9,0(,R9)                                        GP03114
         LTR   R9,R9                                            GP03114
         BZ    DONEPARM      NO PARM POINTER                    GP03114
         LH    R8,0(,R9)     GET PARM LENGTH                    GP03114
         BNP   DONEPARM                                         GP03114
         PARSE 2(,R9),(R8),OPT=COMMA  (NO KEYWORDS)             GP03114
         BXH   R15,R15,BADPARSE                                 GP03114
         ICM   R3,15,PAR@TABL  GET RESULT(S)                    GP03114
         BZ    DONEPARM      BLANK ONLY                         GP03114
         USING PRSDSECT,R3   DECLARE IT                         GP03114
         ICM   R2,15,PRS#TEXT  ANY TEXT AT ALL ?                GP03114
         BZ    PARM2         NO                                 GP03114
         TM    PRSFLAGS,PRSFGINT  VALID INTEGER ?               GP03114
         BZ    PARMBAD                                          GP03114
         ICM   R0,15,PRS#TINT  MORE THAN FOUR BYTES ?           GP03114
         BNZ   PARMBAD                                          GP03114
         MVC   LIMCNT,PRS#TINT+4  USE LOW FOUR BYTES            GP03114
PARM2    ICM   R3,15,PRSLINK   GET NEXT ENTRY                   GP03114
         BZ    DONEPARM      BLANK ONLY                         GP03114
         ICM   R2,15,PRS#TEXT  ANY TEXT AT ALL ?                GP03114
         BZ    DONEPARM      NO                                 GP03114
         TM    PRSFLAGS,PRSFGINT  VALID INTEGER ?               GP03114
         BZ    PARMBAD                                          GP03114
         ICM   R0,15,PRS#TINT  MORE THAN FOUR BYTES ?           GP03114
         BNZ   PARMBAD                                          GP03114
         MVC   SKIPCNT,PRS#TINT+4  USE LOW FOUR BYTES           GP03114
         B     DONEPARM                                         GP03114
PARMBAD  L     R4,PRS@TEXT   LOAD TEXT ADDRESS                  GP03114
         PRTDATA '*** Invalid PARM',(0(R4),L'PRS$TEXT,DEB,PAD),'***'
         PRTCLOSE ,          FORCE OUT MSG                      GP09220
         ABEND 123,DUMP                                         GP03114
BADPARSE PRTL  '0*** PARSE FAILED ***'
         PRTCLOSE ,          FORCE OUT MSG                      GP09220
         ABEND 122,DUMP                                         GP03114
*---------------------------------------------------------------------*
*  CHECK INPUT                                                        *
*---------------------------------------------------------------------*
DONEPARM L     R7,LIMCNT    SAVE START COUNT                    GP03114
         OPEN  MF=(E,OPL1)                                       80050
         LA    R2,SYSUT1+DCBDDNAM-IHADCB                        GP03114
         TM    DCBOFLGS+SYSUT1-IHADCB,DCBOFOPN                   90280
         BZ    BADUTDD                                          GP03114
*---------------------------------------------------------------------*
*  CHECK FOR RECORDS TO BE SKIPPED                                    *
*---------------------------------------------------------------------*
         ICM   R5,15,SKIPCNT    ANY RECORDS TO BE SKIPPED ?     GP03114
         BZ    DONESKIP                                         GP03114
         PRTDATA '--- Skipping',(SKIPCNT,I,DEB,PAD),'records ---' 09218
SKIPPING GET   SYSUT1        GET A RECORD                       GP03114
         BCT   R5,SKIPPING   AND IGNORE IT                      GP03114
         SPACE 1
DONESKIP MVC   NUMREC,SKIPCNT                                   GP09220
         SR    R4,R4         STARTING DUMP OFFSET               GP09220
         SPACE 1
LOOP     GET   SYSUT1
         LR    R2,R1
         INC   NUMREC                                           GP09220
         PRTDATA '*** Record',(NUMREC,I,PAD,DEB),'  length',(DCBLRECL-I*
               HADCB+SYSUT1,2,I,DEB,PAD),'***'                  GP09220
         LH    R3,DCBLRECL+SYSUT1-IHADCB                         *TSM*
         SUBCALL SUBPDUMP,((R4),(R2),(R3),@PRINTER),MF=(E,CALLPARM)
         BCT   R7,LOOP       WRITE UNDER COUNT CONTROL          GP03114
         SPACE 2
CLOSE    C     R7,LIMCNT     ANY ?                              GP03114
         BNE   CLOSEM                                           GP03114
         PRTL  '0*** End-File before dumping started ***'       GP09220
         MVICC 4,0                                              GP03114
         B     CLOSER                                           GP03114
CLOSEM   L     R0,LIMCNT                                        GP03114
         SR    R0,R7                                            GP03114
         CVD   R0,DB         CONVERT BINARY TO PACKED           GP03114
         SPACE 1                                                 78271
CLOSER   PRTLIST STATUS                                         GP09220
         CLOSE (,DISP),MF=(E,OPL1)                              GP09220
      FREEPOOL SYSUT1
         SERVTERM ,                                             GP03114
         PGMEXIT COPYRET=(RETCODE,8)                            GP03114
         SPACE 1                                                 90280
BADUTDD  PRTL  '*** UNABLE TO OPEN SYSUT1 DD ***'
         PRTCLOSE ,          FORCE OUT MSG                      GP09220
         ABEND 113                                               90280
         SPACE 1
         LTORG
         SPACE 1
STATUS   FDOPT NL,CC=C'0'                                       GP09220
         FD    DB,I,RADJ,LEN=10                                 GP09220
         FD    'records formatted',PADL                         GP09220
         FD    *END                                             GP09220
         SPACE 2
PRINTER  PRTWORK SYSTERM,SYSPRINT,TITLE=3                       GP09220
MODOPL   OPEN  (0,(INPUT,REREAD)),MF=L                           90280
MODUT1   DCB   DDNAME=SYSUT1,MACRF=GL,DSORG=PS,EODAD=CLOSE,      78271 *
               EXLST=@DCBXLST                                    90280
MODEND   EQU   *
         STITL 'WORK AREA'                                       *TSM*
SAVE     DSECT
DB       DS    D                                                GP03114
         SERVDEFS ,          DEFINE NEWFANGLED STUFF            GP03114
SKIPCNT  DC    F'0'          NUMBER OF RECORDS TO SKIP          GP03114
LIMCNT   DC    X'7FFFFFFF'   NUMBER OF RECORDS TO WRITE         GP03114
NUMREC   DC    F'0'          CURRENT RECORD NUMBER              GP09220
SUBSAVE  DS    16A           REGISTER SAVE AREA FOR DCB EXIT    GP03114
         SPACE 1                                                GP03114
         MAPPARSE DSECT=NO                                      GP03114
*DEFER*  PRINT &PRTMAC                                           80050
OPL1     OPEN  (SYSUT1,INPUT),MF=L
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=GL,DSORG=PS,EODAD=CLOSE
SAVEEND  EQU   *
         STITL 'WORK AREA - JFCB'                                80050
MYJFCB   DSECT ,
      IEFJFCBN ,                                                 80050
         SPACE 2                                                GP03114
         MAPPARST ,          DEFINE PARSE RESULTS               GP03114
         STITL 'SYSTEM CONTROL BLOCK MAPPINGS'                  GP03114
         PRINT &PRTSYS                                          GP03114
         DCBD  DSORG=PS,DEVD=PC                                  78271
         END   ,                                                GP03114
