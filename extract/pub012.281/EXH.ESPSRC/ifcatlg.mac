IFCAT    TITLE 'I F C A T L G  ***  CLIST DSN TESTER'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 1989  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,
***********************************************************************
*                                                                     *
*        THIS MODULE SETS A ZERO RETURN CODE IF THE PASSED DSNAME     *
*        EXISTS IN THE CATALOG.                                       *
*                                                                     *
*        TSO CMD:  IFCATLG FULLY.QUALIFIED.DSNAME                     *
*                                                                     *
         SPACE 1
         PRINT &PRTSOR
IFCATLG  PGMHEAD ZERO12,BASE=R12,PARM=R10               ADDED ON 89283
         SPACE 1
         SERVINIT ,          LOAD THE SERVICE ROUTINE
         SERVCALL LPALD,=CL8'@PRINTER'  LOAD THE PRINTER ROUTINE
         ST    R0,@PRINTER
         PRTOPEN CONSOLE     ENABLE TPUT OR WTO TO USER
         SPACE 1
***********************************************************************
*                                                                     *
*        EXAMINE THE INPUT BUFFER FOR THE DATASET NAME                *
*          MISSING PARAMETER OR TOO LONG - RETURN CODE 32             *
*                                                                     *
*        WHEN THE CALL IS DIRECT (CALL STATEMENT, ETC.) THERE WILL    *
*        BE ONE PARM ADDRESS (X'80' +ADDR), POINTING TO A V-FORMAT    *
*        ENTRY. IF X'80' IS NOT ON, THE ENTRY IS SUPPOSED TO BE IN    *
*        CP MODE, AND POINTER IS TO VCON + CURRENT OFFSET             *
*                                                                     *
***********************************************************************
         SPACE 1
         LA    R10,0(,R10)   CLEAR PARM POINTER POINTER
         LTR   R10,R10       ANY SUPPLIED ?
         BZ    NOPARM        NO
         ICM   R9,15,0(R10)  LOAD PARM POINTER
         BZ    NOPARM        NONE
         SLR   R8,R8
         ICM   R8,3,0(R9)    LOAD PARM LENGTH
         BNP   NOPARM        ALSO NONE ?
         TM    0(R10),X'80'  SINGLE PARAMETER POINTER ?
         BNZ   DELBLANK      YES; PROCESS NORMALLY
         SLR   R2,R2         ELSE SEE IF BUFFER
         ICM   R2,3,2(R9)    SEE IF COMMAND HALFWORD
         BZ    SKIPCMND      YES
         CR    R2,R8         NOT LONGER THAN TEXT ?
         BH    DELBLANK      LONGER - ASSUME IT IS TEXT
SKIPCMND LA    R2,2(,R2)     ALLOW FOR LENGTH OF COMMAND LENGTH
         AR    R9,R2         SKIP OVER COMMAND
         SR    R8,R2         GET RESIDUAL LENGTH
         BNP   NOPARM        NOTHING LEFT TO DO
DELBLANK CLI   2(R9),C' '    PRINTABLE CHARACTER ?
         BH    NONBLANK
         LA    R9,1(,R9)
         BCT   R8,DELBLANK
NOPARM   PRTLIST MSGBADPM    COMPLAIN ABOUT NULL OR BAD PARM
         LA    R5,32         SET BAD RETURN CODE
         B     EXIT
         SPACE 1
NONBLANK LA    R15,0(R8,R9)  POINT TO LAST BYTE+1
TRLBLANK BCTR  R15,0         LAST BYTE
         CLI   2(R15),C' '   TRAILING BLANK OR NULL ?
         BH    DONBLANK      NO; START PROCESSING
         BCT   R8,TRLBLANK   YES; ELIMINATE IT
         B     NOPARM        ELSE NULL PARM
TEXTOC   OC    TEXT(0),2(R9)  MAKE UPPER-CASE NAME
         SPACE 1
DONBLANK BCTR  R8,0          SET LENGTH FOR EXECUTE
         CH    R8,=Y(L'TEXT)  IS INPUT TOO LONG ?
         BNL   NOPARM        YES; FAIL IT
         MVI   TEXT,C' '
         MVC   TEXT+1(L'TEXT-1),TEXT
         EX    R8,TEXTOC
         SERVCALL LOCAT,TEXT  PERFORM CATALOG LOOK-UP
*        NOTE THAT CVOL MOUNTS ARE PERMITTED - USE
*              LOCMT TO RESTRICT TO CURRENTLY MOUNTED PACKS.
         LR    R5,R15        COPY RETURN CODE
EXIT     SERVTERM ,          CLOSE AND FREE EVERYTHING
         PGMEXIT RC=(R5)     RETURN WITH APPROPRIATE CC
         SPACE 1
CONSOLE  PRTWORK *USER,*CONSOLE  DEFINE PRINT WORK AREA          89283
MSGBADPM FD    'NULL OR EXCESSIVE PARM LENGTH',NL
         FD    *END
         SPACE 2
SAVE     DSECT ,             SAVE AND WORK AREA
@SERVICE DS    A
@PRINTER DS    A
DB       DS    D
TEXT     DC    CL44' '       UPPER CASE TEXT
TEXT2    DC    2XL10'0'      RETURN TYPE/VOL  TYPE/CVOL          89283
SAVEEND  EQU   *                                                GP03028
         SPACE 1
         PRINT &PRTSYS
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IHACDE ,
         SPACE 1
         END   ,
