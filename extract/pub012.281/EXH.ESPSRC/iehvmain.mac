VTOC     TITLE 'L I S T V T O C  -  MAIN DRIVER'
         COPY  OPTIONGB      DEFINE GLOBALS                      81340
         SPACE 1                                                 81340
         SYSPARM LIST=YES    SET AND LIST RUN OPTIONS            81340
         SPACE 1                                                 81340
         GBLB  &WX@OBT       SET @OBTAIN MACRO GLOBAL            81340
&WX@OBT  SETB  1             FORCE LOAD, NOT LKED INCLUDE        81340
         SPACE 1                                                 81340
*        THIS PROGRAM IS BASED ON A PROGRAM ORIGINALLY DISTRIBUTED BY
*          IBM VIA PID, NOW DISCONTINUED. IT HAS BEEN MODIFIED
*          BY SEYMOUR METZ AND GERHARD POSTPISCHIL.              81340
*
*        9/8/74 CHANGES
*          SUPPORT FOR 2321 DEVICES HAS BEEN DISCONTINUED
*        SUPPORT FOR 3330 AND 2305 DEVICES ADDED
*        SORTED OPTION ADDED / NOVTOC OPTION CORRECTED
*        MFT SUBTASKING ENQUEUE ADDED;  MFT WITHOUT SUBTASKING SUPP'D
*
*      DISCLAIMER - THE OUTPUT FROM THIS PROGRAM MAY CONTAIN ERRORS
*        WHEN IT IS RUN UNDER MFT WITHOUT SUB-TASKING, AND WHEN
*        A DATASET IS SCRATCHED (OR SPACE IS RELEASED) UNDER OTHERS.
*        SHARED DASD IS NOT SUPPORTED. NOTE COMMENTS AT LABEL IEHVENQU
*        CHECKED OUT UNDER MVT/21.8, SVS 1.7, MVS 3.8
*        SOME NEWFANGLED 'ISAM' DSCB2 FIELDS NOT FORMATTED
*        TCAM/QTAM DATASETS NOT COMPLETELY FORMATTED
*
*     >>> MVS <<<  LINK-EDIT AUTHORIZED INTO AUTHORIZED LIBRARY
*     >>> SVS <<<  APF AUTHORIZATION IS OPTIONAL, BUT RECOMMENDED.
         SPACE 2
*        JCL:  // EXEC PGM=IEHVTOC,REGION=60K+,PARM='OPTION,..,VOL1..,'
*              //SYSPRINT DD SYSOUT=A    OUTPUT DSN
*              //ANYNAME DD UNIT=DISK,VOL=SER=VOLSER,DISP=OLD - ONE PER
*    (SVS):    //VOLMOUNT DD DUMMY - DYNAMIC MOUNTING (IF APF AUTH.)
*
*        PARM OPTIONS:       %TERM  - SUPPRESS OVERPRINTING
*              %NOVTOC - VOLUME INDEX (SUMMARY) ONLY
*              %INDEX - DETAIL LISTING PLUS SUMMARY
*              %SORTED - DETAIL IN ALPHAMERIC SEQUENCE BY DSN; NO INDEX
*              %SORTIN - DETAIL SORTED + SUMMARY
*        DEFAULT (NONE OF THE ABOVE) - DETAIL IN UNSORTED MODE
*          NOTE: SORT OPTIONS ARE VERY SLOW - TO BE FIXED LATER
*
*        SYSTEM OPTIONS (NO VOLUME DD CARDS NEEDED) :
*              %SYSALL - LIST ALL VOLUMES MOUNTED WHOSE NAMES ARE IN
*                 SPECIAL TABLE
*              %UCBALL - LIST ALL VOLUMES CURRENTLY MOUNTED
*        VOLUMES TO BE PROCESSED ARE SPECIFIED EXPLICITLY IN THE
*        PARM FIELD IF NEITHER %SYSALL NOR %UCBALL IS USED:
*              PARM='%OPTION,DLIB01,SYSRES,...ETC'
*        DD CARDS FOR A VOLUME ARE REQUIRED ONLY TO FORCE MOUNTING,
*              BUT WILL PRODUCE FASTER EXEC TIME WHEN PRESENT.   81340
*
*      NOTE - THE %SYSALL OPTION REQUIRES INSTALLATION DEPENDENT
*       ENTRIES IN THE LIST STARTING AT 'SYSPACK'; FORMAT IS :
*        AL1(LENGTH-1),C'VOLSERMASK'  E.G. AL1(3),C'DLIB'
*
*                                                                81340
*        DEPENDENCIES :  LOCAL AND IBM MACRO LIBRARIES           81340
*        EXTERNAL MODULES @SERVICE, @PRINTER AND @OBTAINS        81340
*                                                                81340
*        LINK-EDIT WITH INCLUDES FOR MODULES IEHVFMT1, IEHVFMT2  81340
*          IEHVFMT3, IEHVFMT4, IEHVFMT5, IEHVFMT6 AND IEHVINDX   81340
         EJECT
         PRINT &PRTSOR                                           81340
IEHVTOC  SAVEM BASE=(R11,R12),PARM=R9                            90358
         SERVINIT ,          LOAD THE @SERVICE ROUTINE           81340
         SERVCALL LPALD,=CL8'@PRINTER'  LOAD THE PRINTER ROUTINE 81340
         ST    R0,@PRINTER                                       81340
         SERVCALL LPALD,=CL8'@OBTAINS'  GET THE VOLUME READER    81340
         ST    R0,@OBTAIN                                        81340
         LTR   R1,R0                                             81340
         BZ    NO@OBT        NOT LOADED ?                        81340
         L     R1,28(,R1)    GET THE STORAGE RESERVE WORD        81340
         MVC   0(4,R1),=A(24*1024)  LEAVE SPACE FOR %INDEX OPTION
NO@OBT   PRTOPEN SYSPRINT,OPT=ABEND  FAIL IF NO SYSPRINT DD      81340
         LA    R1,0(,R9)     CLEAN THE PARM ADDRESS              81340
         LTR   R1,R1         ANY ?                               81340
         BZ    NOPARM        NO; TOO BAD                         81340
         ICM   R1,7,1(R1)    GET THE PARM                        81340
         BZ    NOPARM                                            81340
         SLR   R3,R3         CLEAR FOR IC                        81340
         ICM   R3,3,0(R1)    GET PARM LENGTH                     81340
         BNP   NOPARM                                            81340
         LA    R4,2(,R1)     1ST BYTE OF PARM                    81340
         L     R15,CVTPTR    GET CVT                             81340
         SLR   R1,R1         PROVISIONALLY ZERO SUBTASK TCB ADDRESS
         USING CVTMAP,R15                                        81340
         TM    CVTDCB,CVT4MS1+CVT6DAT  REASONABLY MODERN SYSTEM ?
         BNZ   VQTIDN        YES, SUBTASKING/IDENTIFY ALLOWED
         TM    CVTDCB,CVT2SPS  MFT ?                             81340
         BZ    VQTCSET       NO; CAN'T TELL WHAT IT IS ?
         TM    CVTOPTA,8    IS CVTOPTA SET FOR MFT W/SUBTASKING ?
         BZ    VQTCSET       NO; SKIP ENQ PROCESSING
         DROP  R15                                               81340
VQTIDN   IDENTIFY EP=IEHVENQU,ENTRY=IEHVENQU   SUBTASK FOR ENQ
         LA    R1,VQLIST     PASS ADDRESS OF LIST OF ECBS        81340
         ATTACH EP=IEHVENQU,ECB=VQDETACH   ATTACH IT
VQTCSET  ST    R1,VQTCBAD    SAVE SUBTASK ADDRESS OR ZERO IF NOT 81340
         LA    R5,0(R3,R4)   SET END SCAN ADDRESS FOR TRT, LAST BYTE+1
         STM   R3,R5,PARMSAVE   SAVE PARM POINTERS FOR RECURSION 90358
         B     ALLSCAN       BEGIN PROCESSING                    90358
ALLUCBS  MVI   LOOPFG,LFGUCB   SET AUTOMATIC UCB LOOKUP MODE     90358
ALLSCAN  XC    PRIORUCB,PRIORUCB   FORCE SCAN RESTART            90358
VLOOP    SLR   R0,R0         SET FOR DEQ REQUEST
         BAL   R2,SUBPOST    POST SUB-TASK AND WAIT              80340
         LM    R3,R5,PARMSAVE  RESTORE                           81340
         TM    LOOPFG,LFGUCB   IN UCB LOOP ?                     90358
         BZ    VLOOPP        NO
NEXTUCB  L     R1,PRIORUCB   GET PREVIOUS UCB ADDRESS            81340
         SERVCALL UCBUM,(R1)  GET NEXT UCB                       81340
         LTR   R3,R0         COPY AND TEST IT                    81340
         BZ    LOOPEXIT      ALL DONE                            90358
         USING UCBOB,R3      DECLARE THE UCB                     81340
         ST    R0,PRIORUCB   SAVE FOR NEXT TIME                  81340
         CLI   UCBTBYT3,UCB3DACC   DIRECT ACCESS UCB ?           81340
         BNE   NEXTUCB       NO, SKIP                            81340
         TM    UCBSTAT,UCBCHGS+UCBUNLD   COMING OFF ?            81340
         BNZ   NEXTUCB       YES; SKIP                           81340
         TM    UCBSTAT,UCBONLI  ON-LINE ?                        81340
         BZ    NEXTUCB       NO; SKIP                            81340
         CLI   UCBVOLI,C'$'  VALID VOL SER ?                     89121
         BL    NEXTUCB       NO, IGNORE                          81340
         ICM   R0,3,UCBVOLI  DOS OR BAD VTOC ?                  GP13227
         BZ    NEXTUCB         YES; IGNORE IT                   GP13227
         MVC   VOL,UCBVOLI   SAVE VOLUME SERIAL                  81340
         SPACE 1                                                 90358
         TM    LOOPFG,LFGMASK  SEARCH BY MASK ?                  90358
         BZ    UCSYST        NO; CHECK FOR SYSTEMS PACKS ?       90358
         LA    R14,VOLMASK   POINT TO SAVED MASK                 90358
         LA    R15,L'VOLMASK LENGTH OF MASK = LENGTH OF SERIAL   90358
         LA    R1,UCBVOLI    POINT TO SERIAL TO BE CHECKED       90358
         BAL   R9,CLCMASK    DO MASKED COMPARE                   90358
         BNE   NEXTUCB       NO MATCH                            90358
         MVC   VOL,UCBVOLI   ELSE SET PACK                       90358
         B     GOTCHA        AND PROCESS                         90358
         SPACE 1                                                 90358
UCSYST   TM    LOOPFG,LFGSYS  SYSTEM PACKS ONLY ?                90358
         BZ    GOTCHA        NO, DO ALL PACKS
         LA    R14,SYSPACK   GET TABLE OF SYSTEM PACKS           81340
UCSYSP   SLR   R15,R15       CLEAR IC REGISTER                   81340
         ICM   15,1,0(R14)   GET LENGTH - 1 OF THIS VOL-SER MASK 81340
         BM    NEXTUCB       NEXT UCB IF NO MATCH                81340
         EX    R15,UCSYSCLC  SEE IF IT MATCHES                   81340
         BE    GOTCHA        YES - PROCESS THIS VOLUME
         LA    R14,2(R14,R15)   ELSE TRY THE NEXT VOLUME ON THE LIST
         B     UCSYSP        TRY MATCHING NEXT LIST ENTRY
UCSYSCLC CLC   1(0,14),UCBVOLI   MATCHING VOLUME NAME ?          81340
         DROP  R3                                                81340
         SPACE 1                                                 90358
LOOPEXIT TM    LOOPFG,LFGHIT DID WE FIND A MATCH ?               90358
         MVI   LOOPFG,0      RESET FOR NEXT PARAMETER            90358
         BNZ   VLOOP         YES; CONTINUE                       90358
         PRTV  BADLOOP       SHOW LOOP FAILED                    90358
         OI    RETCH+L'RETCH-1,4  SET MINOR ERROR                90358
         B     VLOOP         PROCESS LOOP                        90358
BADLOOP  VCON  END=BADLOOPX                                      90358
         DC    C'-LOOP/SEARCH REQUEST FOR '                      90358
VOLMASK  DC    CL6' ',C' WAS UNSUCCESSFUL'                       90358
         VCON  ,                                                 90358
         SPACE 2                                                 81340
VLOOPP   MVC   VOL,=CL6' '
VLOOPA   LTR   R3,R3         GET RESIDUAL PARM LENGTH            81340
         BNP   EXIT          NO MORE; EXIT                       81340
         L     R1,PARMSAVE+8  GET LAST BYTE + 1 FOR SCAN STOP    81340
         EX    R3,TRT        SCAN FOR END OF VOLUME SERIAL       81340
         LR    R5,R1         SET STOP LOCATION                   81340
         SR    R5,R4           LESS START                        81340
         BNP   NOVOLSER      CONTIGUOUS COMMAS                   90358
VLOOPB   LA    R1,1(,R1)     SET FOR NEXT SCAN                   81340
         BCTR  R3,0          - 1 FOR COMMA                       81340
         SR    R3,R5         SET RESIDUAL COUNT                  81340
         BP    *+6           LEAVE IF POSITIVE                   81340
         SLR   R3,R3         ELSE ZERO FOR NEXT TIME             81340
         ST    R3,PARMSAVE                                       81340
         ST    R1,PARMSAVE+4       SAVE START FOR NEXT SCAN      81340
         CH    R5,=H'6'      IS LENGTH GE 6 ?                    81340
         BL    VLOOPCB       NO, CHECK FOR TERMINAL OPTION
         BE    VLOOPCA       CHECK FOR %INDEX
         CLI   0(R4),C'%'    COMMAND WORD ?                      81340
         BNE   VOL2BIG       NO, INVALID VERB
         MVC   VOLMASK,1(R4) SAVE REQUEST TYPE                   90358
         CLC   =C'SYSALL',1(R4)   DO ALL SYSTEM PACKS ?          81340
         BNE   *+12          NO, CHECK OTHER STUFF
         MVI   LOOPFG,LFGUCB+LFGSYS   SET FOR SYSTEM PACKS ONLY  90358
         B     ALLSCAN       PROCESS ALL SYSTEM VOLUMES          90358
         CLC   =C'UCBALL',1(R4)  DO ALL MOUNTED PACKS ?          81340
         BE    ALLUCBS       YES, SET IT                         81340
         LA    R15,XFSORT    DETAIL LISTING IN ALPHABETIC SEQUENCE
         CLC   =C'SORTED',1(R4)   IS SORTED OUTPUT REQUESTED ?   81340
         BE    VLOOPSXA      YES, GO TO SET IT                   81340
         LA    R15,XFSORT+XFINDEX  SORTED DETAIL + INDEX         82031
         CLC   =C'SORTIN',1(R4)  BUT WAS IT REQUESTED ?          81340
         BE    VLOOPSXA      YES, SET IT                         81340
         LA    R15,XFNODET   SET FOR INDEX ONLY, NO DETAIL       82031
         CLC   =C'NOVTOC',1(R4)  SKIP DETAIL LISTING ?           81340
         BNE   VOL2BIG       NO, ERROR
VLOOPSXA STC   R15,INDXFG    SET TYPE OF FORMATTING FLAG         81340
         B     VLOOP              DO NEXT VOLSER
VLOOPCA  LA    R15,XFNODET   SKIP DETAIL LISTING; DO INDEX       82031
         CLC   =C'%INDON',0(R4)  ALTERNATE FORM OF INDEX ONLY ?  81340
         BE    VLOOPSXA      YES; STASH                          81340
         CLC   =C'%INDEX',0(R4)   VTOC+INDEX ?                   81340
         BNE   VLOOPD        NO, TREAT AS VOL-SER
         MVI   INDXFG,XFINDEX  SET INDEX REQUEST FLAG            82031
         B     VLOOP         DO NEXT VOL-SER
VLOOPCB  CLC   =C'%TERM',0(R4)  TERMINAL OPTION ?                81340
         BNE   VLOOPD        NO; ALLOW OVERPRINTING
         OI    PUTPUT1+1,X'80'   MAKE NOP INTO BE TO SKIP OVERPRINTS
         B     VLOOP         GET NEXT PARM
VLOOPD   BCTR  R5,0          FOR EXECUTE                         81340
         EX    R5,MOVOL      MOVE VOLUME SERIAL                  81340
         EX    R5,TRTMASK    MASKED COMPARE ?                    90358
         BZ    GOTCHA        NO                                  90358
         MVC   VOLMASK,=6C'*'  MAKE TRAILING MATCH               90358
         EX    R5,MVOLMSK    MOVE VOLUME MASK                    90358
         MVI   LOOPFG,LFGUCB+LFGMASK  SET FOR MASKED COMPARE     90358
         B     ALLSCAN       GO TO START OF UCB LOOP             90358
TRTMASK  TRT   VOL(0),MASKTRT  CHECK AGAINST MASK TABLE          90358
MVOLMSK  MVC   VOLMASK(0),VOL  MOVE SERIAL TO MASK               90358
         SPACE 1                                                 90358
GOTCHA   CLC   TVOL,VOL      UNCHANGED ?                         81340
         BE    GOTCHAT       YES; SKIP SECOND EJECT              81340
         MVC   TVOL,VOL      MOVE NEW VOLSER TO TITLE            81340
         PRTV  TITLE,TITLE=1  PRINT NEW TITLE                    81340
GOTCHAT  MVI   VREADB1+1,X'70'     ALLOW FMT 5 FORMATTING        81340
         MVI   VOLABEL+1,1   SET PROVISIONAL FLAG FOR LABEL TRACK
         MVI   VOLIPL+1,1    SET PROVISIONAL FLAG FOR IPL TRACK
         XC    VOLUSE,VOLUSE   ZERO TRACKS ON VOLUME IN USE
         XC    VOLFREE,VOLFREE  ZERO TRACKS ON VOLUME FREE (FMT5)
         MVI   MOUNTRY,0     RESET MOUNT RETRY BYTE
         OI    LOOPFG,LFGHIT SHOW MATCHING VOL-SER FOUND         90358
         LA    R2,OBTAIN     SET RETURN POINT FROM IN-LINE       80340
         LA    R0,VOL        PASS A(VOLSER) TO ENQUEUE UPON      80340
         SPACE 1                                                 80340
SUBPOST  OC    VQTCBAD,VQTCBAD   HAVE A SUBTASK ?                80340
         BZR   R2            NO, SKIP WAIT                       80340
         MVI   VQREADY,0     RESET ECB                           80340
         TM    VQDETACH,X'40'  PREMATURE TERMINATION ?           80353
         BNZR  R2            YES; RETURN NORMALLY                80353
         POST  VQECB,(0)     POST ENQ TASK TO ENQ
         WAIT  ECBLIST=VQSUBLST  WAIT ON TCBECB AND REQUEST ECB  80340
         BR    R2            KEEP GOING ANYWAY                   80340
VQSUBLST DC    A(VQDETACH),X'80',AL3(VQREADY)  WAIT LIST         80340
         SPACE 1                                                 80340
OBTAIN   @OBTAIN FMT4CAM       READ VTOC                         81340
         B     *+4(15)             INDEXED BRANCH ON RETURN CODE
         B     FOUROK              GOOD RETURN
         B     MOUNTER       GO TO MOUNT RETRY ROUTINE
         B     FOUROK        NORMAL 'ERROR' CODE FOR FMT4 SEARCH
         NOP   *                   I/O ERROR
         B     IOERROR       AND GET OUT WITH ERROR MESSAGE
         B     IOERROR       PROGRAM ERROR                       82031
         SPACE
FOUROK   MVC   FMT4(FMT4LEN),CURDSCB   SAVE FORMAT 4 DSCB        81340
         MVC   VCHR4(4),DS4VTOCE+2 GENERATE DSCB4 CCHHR
         MVI   VCHR4+4,1     SET FOR RECORD 1                    81340
         MVC   DS5FLG(1),DS4VTOCI  SAVE VTOC INDICATOR FOR FMT 5 SCAN
         MVC   CYLTRK(2),DS4DEVSZ+2   SAVE CYLINDERS/TRACK FOR FREE SPA
         TM    INDXFG,XFNODET         IS THIS INDEX ONLY?        82031
         BO    SKIPFMT4
         CALL  DSCB4,(VCHR4,FMT4)
SKIPFMT4 TM    DS4VTOCI,255-DS4DICVT  FLAG OTHER THAN RECLAIM ?  81340
         BZ    SKIPFM4A      NO; LEAVE AS IS                     81340
         MVC   DS4HPCHR(4),DS4VTOCE+6  READ ENTIRE VTOC          81340
         MVC   DS4HPCHR+4(1),DS4DEVDT   IN CASE HWM INCORRECT    81340
SKIPFM4A MVC   C1(4),DS4DEVSZ   SAVE CYL/TRK MAXIMA
CHEKND   TM    INDXFG,XFNODET   INDEX ONLY ?                     82031
         BO    VACCNT2       SKIP DETAIL LISTING
         CLC   DS4F6PTR,ZEROES   IS THERE A FMT6 DSCB ?          81340
         BE    NOFMT6                   BRANCH IF NOT
         MVC   CCHHRF6,DS4F6PTR         YES, GET ADDRESS FOR SEEK
GET6     @OBTAIN FMT6CAM     READ THE FORMAT 6 DSCB              81340
         BXH   R15,R15,IOERROR   TERMINATE VOLUME ON ANY ERROR   81340
         CALL  DSCB6,(DS4F6PTR,CURDSCB) CALL FORMATTING SUBROUTINE
         MVC   CCHHRF6,DS1PTRDS   SET UP NEXT SEEK               81340
         CLC   DS1PTRDS,ZEROES   IS THERE ANOTHER FMT 6 DSCB ?   81340
         BNE   GET6                     BRANCH IF YES
         SPACE 1                                                 81340
NOFMT6   MVC   CCHHR(4),DS4VTOCE+2                               81340
         MVI   CCHHR+4,1
         MVI   SORTFLAG,0    PRESET FOR NO SORT OPTION           81340
         TM    INDXFG,XFSORT  DO WE NEED TO SORT ?               82031
         BZ    VBUMP         NO; READ THROUGH FORMAT 5 CHAIN     81340
         MVI   SORTFLAG,X'A0'  SET HIGH PORTION OF SORT FLAG     81340
         SPACE 1                                                 81340
VBUMP    SLR   R2,R2                                             81340
         IC    R2,CCHHR+4    GET RECORD NUMBER                   81340
         LA    R2,1(,R2)                                         81340
         STC   R2,CCHHR+4                                        81340
         CLM   R2,1,DS4DEVDT   STILL ON TRACK ?                  81340
         BNH   VREAD         YES; USE IT                         81340
         MVI   CCHHR+4,1     ELSE RESET TO RECORD ONE            81340
         LA    R2,2          SET FOR TWO HALF-WORDS              81340
         LA    R3,CCHHR+2    SECOND HALFWORD                     81340
         LA    R4,C1+2       LAST H OF DEV CAP                   81340
STEPADDR LH    R5,0(,R3)                                         81340
         LA    R5,1(,R5)     UP BY ONE                           81340
         STH   R5,0(,R3)     STASH BACK                          81340
         CH    R5,0(,R4)     LEGAL VALUE ?                       81340
         BL    VREAD         YES, USE IT
         XC    0(2,R3),0(R3)  ELSE ZERO IT AND UP NEXT ONE       81340
         SR    R3,R2         SUBTRACT INDEX (=2 FIRST TIME)      81340
         SR    R4,R2         DITTO                               81340
         BCT   R2,STEPADDR   REPEAT FOR ALL                      81340
VREAD    CLC   CCHHR(5),DS4HPCHR  PAST LAST FORMAT 1 DSCB ?      81340
         BH    VACCNT        YES; SEE IF INDEX REQUIRED          81340
         MVC   DS1FMTID,SORTFLAG  PRESET @OBTAINS SORT OPTION BYTE
         MVC   FMT1ADDR,CCHHR  PRESET FEEDBACK CCHHR FIELD       81340
         @OBTAIN FMT1CAM      GET NEXT OR A SPECIFIC FORMAT      81340
         B     VREADA(R15)   BRANCH ON RETURN CODE               81340
VREADA   B     VREADB              GOOD, NORMAL RETURN
         B     NOMOUNT             VOLUME COULD NOT BE MOUNTED
         B     IOERROR             BLANK DSCB
         B     IOERROR             IO ERROR OCCURRED
         B     IOERROR                                           82031
         B     IOERROR       PROGRAM ERROR                       82031
         SPACE 1                                                 81340
VREADB   CLI   DS1FMTID,X'FF'   END OF SORTED VTOC ?             81340
         BE    VACCNT        YES; CHECK FOR INDEX                81340
         CLI   DS1FMTID,C'5' IS IT A FORMAT 5 DSCB ?             81340
VREADB1  BNE   VREADC        NOP AFTER FMT 5 SCAN COMPLETE       81340
VREADB2  CALL  DSCB5,(CCHHR,CURDSCB)    DSCB5 FORMATTING ROUTINE 81340
         CLC   DS1PTRDS,ZEROES   IS THERE ANOTHER                81340
         BE    VREADB9             NO
         MVC   CCHHRF3,DS1PTRDS   YES, SET ADDRESS               81340
         @OBTAIN FMT3CAM     READ NEXT DSCB                      81340
         BXH   R15,R15,IOERROR   TERMINATE VOLUME ON ANY ERROR   81340
         B     VREADB2             LOOP
VREADB9  MVI   VREADB1+1,X'F0'     FMT 5-S ALL DONE
VREADC   OI    SORTFLAG,X'02'  SET LOW NYBBLE OF SORT OPTION     81340
         CLI   DS1FMTID,X'FF'  @OBTAINS EOF SIGNAL ?             81340
         BE    VACCNT        YES; CHECK FOR INDEX OPTION         81340
         CLI   DS1FMTID,C'1'   FORMAT 1 DSCB ?                   81340
         BNE   VBUMP         NO; READ ANOTHER DSCB               81340
VREADP1  XC    XTENTSIZ,XTENTSIZ   CLEAR IT
         MVC   RELDSO,DS1DSORG   SAVE DSORG                     GP09186
         SR    R1,R1                                            GP09186
         ICM   R1,7,DS1LSTAR                                    GP09186
         BZ    STUSE                                            GP09186
         SRL   R1,8                                             GP09186
         LA    R1,1(,R1)                                         81340
STUSE    STH   R1,RELUSE                                         81340
         CALL  DSCB1,(FMT1ADDR,CURDSCB)  FORMAT THE DSCB 1       81340
VREADD   CLC   DS1PTRDS,ZEROES   IS THERE A DSCB EXTENSION ?     81340
         BE    SIZIT         NO
         MVC   CCHHRF3,DS1PTRDS      YES, SETUP FOR SEEK         81340
         @OBTAIN FMT3CAM     GET DSCB3                           81340
         CLI   DS1FMTID,C'2'     IS IT A FORMAT 2 DSCB           81340
         BNE   VREADE              NO, CHECK FOR FORMAT 3
         CALL  DSCB2,(CCHHRF3,CURDSCB)  CALL FORMATTING ROUTINE  81340
         B     VREADD              GO TEST FOR NEXT
VREADE   CLI   DS1FMTID,C'3'     IS IT A FORMAT 3 DSCB ?         81340
         BNE   SIZIT               BRANCH IF NOT
         CALL  DSCB3,(CCHHRF3,CURDSCB)  CALL FORMATTING ROUTINE  81340
SIZIT    CALL  CVD,(TOTSIZ)        GET TOTAL EXTENT SIZE OF DATA SET
         MVC   USEPCNT,USEPCNT-1   BLANK OUT PERCENT USED FIELD
         CLI   RELDSO+1,0    VSAM, ETC.?                        GP09186
         BNE   TOTPR         YES                                GP09186
         TM    RELDSO,X'42'  'PS' OR 'PO' DATA SET
         BZ    TOTPR         NO, PRINT AS IS
         OC    XTENTSIZ,XTENTSIZ   PATTERN DSCB ?
         BZ    TOTPR         YES, FIX MY STUPID ERROR
         MVC   USEPCNT,USEPMSK   MOVE EDIT MASK
         LH    R1,RELUSE     GET TRACKS USED                     81340
         MH    R1,=H'100'    CONVERT TO PERCENT                  81340
         SLR   R0,R0                                             81340
         D     R0,XTENTSIZ   DIVIDE BY TRACKS IN EXTENT          81340
         CVD   R1,WORKD      CONVERT                             81340
         ED    USEPCNT(7),WORKD+5  AND EDIT FOR OUTPUT
TOTPR    CALL  PRINT,(TOTALOUT,FAKE1)
         CALL  PRINT,(TOTALOUT,FAKE2)
         B     VBUMP         GO TO READ ANOTHER DSCB             81340
         SPACE 3
NOPARM   PRTV  NOPRMERR      PRINT 'BAD PARM' MSG                81340
         OI    RETCH+L'RETCH-1,8  SET ERROR                      89121
EXIT     OC    VQTCBAD,VQTCBAD   IS THERE A SUB-TASK ?           81340
         BZ    EXITQ         NO, NEVER GOT AROUND TO IT
         TM    VQDETACH,X'40'  ALREADY TERMINATED ?              80353
         BNZ   DODETACH      YES; JUST DETACH                    80353
         MVI   VQREADY,0     RESET JUST IN CASE
         LA    R0,X'10'      POST CODE TO TERMINATE ENQ TASK     81340
         SLL   R0,24         PUT IT INTO HI BYTE                 81340
         POST  VQECB,(0)     TELL USER ABOUT IT
         WAIT  ECB=VQDETACH  WAIT TILL IT'S DONE
DODETACH DETACH VQTCBAD      GET RID OF IT                       80353
EXITQ    @OBTAIN OPT=CLOSE   CLOSE THE VTOC PROCESSOR            81340
         SERVTERM ,          CLOSE AND FREE ALL ELSE             81340
         LH    R9,RETCH      GET RETURN CODE                     89121
         ENDM  RC=(R9)       RETURN TO CALLER                    89121
         SPACE 3
*        THIS CODE RETRIES A MOUNT IF A 'VOLUME NOT MOUNTED' OCCURS.
*        THIS MAY HAPPEN IN SYSTEMS WITHOUT AVR IF THE OPERATOR
*        HAS NOT MOUNTED THE VOLUME.
*
*        THE TIOT IS SCANNED, IF A JFCB IS AVAILABLE POINTING TO
*        THAT VOLUME, AN OPEN IS FORCED
*
*
MOUNTER  XI    MOUNTRY,1     IS THIS THE SECOND ENTRY TO RETRY ? 81340
         BZ    NOMOUNT       YES, ASSUME CAN'T MOUNT VOLUME      81340
         SLR   R0,R0                                             81340
         ST    R0,PRIORTIO                                       81340
         SERVCALL UCBDK,VOL  SEE IF VOLUME ALREADY UP            81340
         LTR   R5,R0                                             81340
         BNZ   OBTAIN        YES; TRY AGAIN                      81340
MOUNTIOT L     R1,PRIORTIO   GET PREVIOUS TIOT ADDRESS OR ZERO   81340
         SERVCALL TIOLK,(R1)  LOOK FOR NON-SPECIAL ENTRIES       81340
         LTR   R3,R0         END OF TIOT ?                       81340
         BZ    NOMOUNT       YES; UNABLE TO MOUNT                81340
         ST    R3,PRIORTIO   REMEMBER THE LAST ENTRY             81340
         USING TIOENTRY,R3   MAP A DEVICE ENTRY                  81340
         CLC   =C'SYS0',TIOEDDNM  DYNAMIC DD ?                   87242
         BE    MOUNTIOT      YES; SKIP IT                        87242
         ICM   R5,7,TIOEFSRT   GET UCB ADDRESS                   81340
         BZ    MOUNTIOT      NONE; TRY ANOTHER                   81340
         USING UCBOB,R5                                          81340
         CLI   UCBTBYT3,UCB3DACC  DASD ENTRY ?                   81340
         BNE   MOUNTIOT      NO; TRY AGAIN                       81340
         MVC   MOUNTDCB+40(8),TIOEDDNM   COPY DDNAME             81340
         RDJFCB (MOUNTDCB)                                       81340
         BXH   R15,R15,MOUNTIOT  FAILED; TRY ANOTHER             81340
         CLC   JFCBVOLS(6),VOL   DESIRED VOLUME ?                81340
         BNE   MOUNTIOT      NO, GET NEXT                        81340
         MVC   JFCBDSNM,DSNAME04  SET TO VTOC NAME               81340
         OI    JFCBTSDM,JFCNWRIT+JFCNDSCB+JFCNDCB  NO REWRITE    81340
         MVI   JFCBIND2,JFCOLD+JFCSHARE  OLD, SHARE              81340
         OPEN  (MOUNTDCB,(INPUT)),TYPE=J
         TM    MOUNTDCB+48,X'10'
         BZ    NOMOUNT       NOT OPENED
         CLOSE (MOUNTDCB)
         B     OBTAIN        TRY AGAIN
         DROP  R3,R5                                             81340
         SPACE 2
MOUNTRY  DC    X'0'          MOUNT RETRY BYTE
PRIORUCB DC    A(0)          UCB LOOP POINTER                    81340
PRIORTIO DC    A(0)          TIOT LOOP POINTER                   81340
MTEXL    DC    0A(0),X'87',AL3(JFCB)   EXIT LIST
JFCB     DC    0A(0)         RJFCB TO FORCE MOUNTING             79213
         IEFJFCBN ,          JFCB DEFINITION                     79213
MOUNTDCB DCB   DDNAME=DUMMY,DSORG=PS,EXLST=MTEXL,MACRF=(E)       81340
         SPACE 2
NOMOUNT  MVC   NOVOL,VOL           PUT VOLUME IN MESSAGE ABOUT NOMOUNT
         CLC   TVOL,VOL      WAS TITLE WRITTEN YET ?             81340
         BE    NOMOUNTT      YES                                 81340
         MVC   TVOL,VOL      MOVE VOLSER TO TITLE                81340
         PRTV  TITLE,TITLE=1   UPDATE TITLE                      81340
NOMOUNTT PRTV  NOVOLUME      PRINT UNABLE TO MOUNT               81340
         OI    RETCH+L'RETCH-1,8  SET ERROR                      89121
         B     VLOOP
IOERROR  MVC   IOVOL,VOL
         PRTV  IOVOLERR      SHOW ERROR ON VOLUME                81340
         OI    RETCH+L'RETCH-1,8  SET ERROR                      89121
         B     VBUMP
NOVOLSER BCT   R3,EXIT       SKIP THE COMMA                      81340
         LA    R4,1(,R4)           SKIP THE COMMA                81340
         B     VLOOPA
VOL2BIG  MVC   TVOL,0(R4)    SET VOLSER IN TITLE                 81340
         PRTV  TITLE,TITLE=1   UPDATE THE TITLE                  81340
         LA    R5,6          SET MAXIMUM LENGTH OF VOLSER        81340
         MVC   TRUNCVOL,0(R4)  COPY VOLSER TO MESSAGE            81340
         PRTV  TRUNC         PRINT ERROR MESSAGE                 81340
         OI    RETCH+L'RETCH-1,8  SET ERROR                      89121
         B     VLOOPD
TRT      TRT   0(0,R4),SCANTAB                                   81340
MOVOL    MVC   VOL(0),0(R4)                                      81340
TOTSIZ   DC    S(XTENTSIZ),AL2(L'XTENTSIZ),S(TOTALSIZ),AL2(L'TOTALSIZ)
         DC    H'0'                THIS IS PART OF TOTSIZ
         EJECT
*        THIS SECTION OF CODE ACCOUNTS FOR ALL THE TRACKS ON A VOLUME
*        FMT 1,3 ENTRIES ARE SUMMED IN 'VOLUSE', FMT 5 ENTRIES IN
*        'VOLFREE'.  THE VTOC IS ADDED, AS ARE THE IPL AND LABEL
*        TRACK, WHERE APPLICABLE.
*
VACCNT   CALL  COMPSIZE,(COMPTAB)  ADD VTOC TO TRACKS IN USE
VACCNT2  LH    R1,DS4DEVSZ   TOTAL CYLINDERS                     81340
         LH    R2,DS4DEVSZ+2   TRACKS/CYLINDER                   81340
         MR    R0,R2         TOTAL TRACKS                        81340
         ST    R1,VOLTOT      + ALTERNATES                       81340
         LA    R14,VOLDEVT   GET DEVICE ENTRY TABLE              81340
         LA    R2,VOLDEVTE    AND END ADDR. OF TABLE             81340
VACUUM   CLC   0(2,R14),DS4DEVTK   SAME NO. OF BYTES / TRACK ?   81340
         BE    VACIT         YES, USE THIS ENTRY
         LA    R14,12(,R14)  NO, BUMP                            81340
         CR    R14,R2        END OF TABLE ?                      81340
         BL    VACUUM        NO, KEEP ON
         B     VLOOP         NO, SKIP TRACK ACCT. AND POSS. INDEX
         SPACE
VACIT    MVC   VOLATOT,2(R14) GET TOTAL TRACKS AVAILABLE AS ALTERNATES
         CH    R1,6(,R14)    CORRECT TRACK COUNT ?               81340
         BE    VACOS         YES; LET'S HOPE IT'S A REAL DISK    81340
         CH    R1,8(,R14)    DITTO FOR RPS ALTERNATE SIZE        81340
         BE    VACOS         YES; PROCESS IT                     81340
         MVC   VOLAFREE,10(R14)  COPY ALTERNATES FOR MINI-DISK   81340
         B     VACOS2                                            81340
VACOS    MVC   VOLAFREE,DS4NOATK   SET FREE ALTERNATES           81340
VACOS2   LH    R0,VOLATOT    GET TOTAL ALTERNATES                81340
         SH    R0,VOLAFREE   SUBTRACT FREE ONES                  81340
         STH   R0,VOLAUSE    STASH NO. OF ALTERNATES IN USE      81340
         NC    VOLIPL(2),4(R14)  AND IPL FLAG BIT IF IPL SUPPORTED
         L     R0,VOLUSE     GET TRACKS IN USE                   81340
         AH    R0,VOLABEL    ADD ONE FOR LABEL TRACK, IF ANY     81340
         AH    R0,VOLIPL     ADD ONE FOR SEPARATE IPL TRACK      81340
         ST    R0,VOLUSE                                         81340
         L     R0,VOLTOT     GET TOTAL TRACKS ON VOLUME          81340
         S     R0,VOLUSE     SUBTRACT TRACKS IN USE              81340
         S     R0,VOLFREE     - TRACKS FREE                      81340
         SH    R0,VOLATOT    SUBTRACT ALTERNATE TRACKS           81340
         ST    R0,VOLMISS    SHOULD BE ZERO                      81340
         TM    INDXFG,XFNODET  NO DETAIL LISTING ?               82031
         BO    INDEXF        CORRECT - NEED TO SKIP STATISTICS
         CALL  CVD,(VOLLIST)  GET VOLUME STATISTICS IN BCD
         CALL  PRINT,(VOLDES,FAKE1)   WRITE VOLUME TRACK ACCOUNTING
         OC    VOLMISS,VOLMISS   ANY ERROR IN TOTAL ?
         BZ    INDEX         NO, GET INDEX
         MVI   VOLERR,C' '   SET FOR LINE SKIP
         LA    R2,3          OVERPRINT THREE TIMES               81340
VOLBOOB  CALL  PRINT,(VOLERR,FAKE1)  PRINT ERROR MESSAGE
         MVI   VOLERR,C'+'   SET FOR OVERPRINTING
         BCT   R2,VOLBOOB    AND REPEAT TWO MORE TIMES           81340
         B     INDEXF        ON ERROR FORCE THE INDEX LISTING
         SPACE 2
INDEX    TM    INDXFG,XFNODET+XFINDEX  INDEX REQUESTED ?         82031
         BZ    VLOOP         NO, DO NEXT VOLUME                  82031
INDEXF   PRTV  TITLE,TITLE=1   FORCE PAGE EJECT                  81340
         L     R5,VOLMISS    SAVE CURRENT BAD TRACK COUNT        81340
         CALL  VTOCINDX      CALL INDEX ROUTINE
         TM    INDXFG,XFNODET  IS NO-VTOC OPTION ON ?            82031
         BNZ   VLOOP         YES, SKIP MESSAGES AND STUFF
         CALL  CVD,(VOLLIST)  TRANSLATE STATISTICS AGAIN
         CALL  PRINT,(VOLDES,FAKE1)  PRINT THEM FOLLOWING INDEX
         OC    VOLMISS,VOLMISS   ANY TRACKS BAD NOW ?
         BNZ   INDPERR       YES, PRINT ERROR MESSAGE AGAIN
         LTR   R5,R5         CHECK IF A PREVIOUS ERROR           81340
         BZ    VLOOP         NO, RETURN
         CALL  PRINT,(VOLALNOW,FAKE1)   INDICATE ERROR WENT AWAY 81340
         B     VLOOP         RETURN
INDPERR  MVI   VOLERR,C' '   SKIP
         LA    R2,3          OVERPRINT                           81340
VOLBOOBI CALL  PRINT,(VOLERR,FAKE1)  OVERPRINT ERROR MESSAGE
         MVI   VOLERR,C'+'   SET FOR OVERPRINT
         BCT   R2,VOLBOOBI   DO 3 TIMES                          81340
         OI    RETCH+L'RETCH-1,4  SET ERROR                      89121
         B     VLOOP         NOW DO NEXT VOLUME
         SPACE 2
*        SCAN THROUGH VTOC FOR INDEX ROUTINE
*
*
         ENTRY OBTAINIT,OBTAINS
         DROP  ,             DELETE ALL BASE DECLARATIONS        81340
OBTAINIT STM   R14,R12,12(R13)                                   81340
         USING OBTAINIT,R15  USE CURRENT BASE                    81340
         LM    R11,R12,VTOCBASE  GET PROGRAM BASES BACK          81340
         USING IEHVTOC,R11,R12   DECLARE MAIN BASES AGAIN        81340
         DROP  R15                                               81340
         LA    R9,PRSAVE     GET PRINT SAVE AREA                 81340
         ST    R9,8(,R13)    LINK SAVE AREAS                     81340
         ST    R13,4(,R9)                                        81340
         LR    R13,R9        SET NEW SAVE AREA                   81340
         L     R6,0(,R1)     GET CCHHR PARM ADDRESS              81340
         MVC   CCHHR(5),0(R6)  COPY IT                           81340
         OC    CCHHR(5),CCHHR   FIRST TIME ?
         BNZ   OBINC         NO, INCREMENT TO NEXT ONE
         MVC   CCHHR(4),DS4VTOCE+2   COPY VTOC CYLINDER AND TRACK
         MVI   CCHHR+4,1     SET FIRST RECORD TO FMT4, WILL BE SKIPPED
         SPACE
OBINC    SLR   R2,R2                                             81340
         IC    R2,CCHHR+4    GET RECORD NO.                      81340
         LA    R2,1(,R2)     BUMP UP                             81340
         STC   R2,CCHHR+4    STASH IT BACK                       81340
         CLM   R2,1,DS4DEVDT    STILL ON TRACK ?                 81340
         BNH   OBREAD        YES                                 81340
         MVI   CCHHR+4,1     NO, RESET
         LA    R2,2          SET FOR TWO HALFWORDS               81340
         LA    R3,CCHHR+2    SECOND                              81340
         LA    R4,C1+2       MAX                                 81340
OBSTEP   LH    R5,0(,R3)     VALUE                               81340
         LA    R5,1(,R5)     UP BY ONE                           81340
         STH   R5,0(,R3)                                         81340
         CH    R5,0(,R4)     MAX ?                               81340
         BL    OBREAD        OK
         XC    0(2,3),0(3)   CLEAR
         SR    R3,R2         SUBTRACT 2 (BCT INDEX IS 2 FIRST TIME)
         SR    R4,R2          DITTO                              81340
         BCT   R2,OBSTEP                                         81340
         SPACE 2
OBREAD   MVC   0(5,R6),CCHHR   FEED IT BACK                      81340
         CLC   CCHHR(5),DS4HPCHR  PASSED LAST FMT 1 ?
         BH    OBEX0         YES, RETURN EOF CODE
         @OBTAIN FMT1CAM     READ THE DSCB                       81340
         B     *+4(R15)                                          81340
         B     OBEX8         OK, NORMAL RETURN
         B     OBEX4         NOT MOUNTED ?
         B     OBINC         EXTENT BAD ? BLANK DSCB ?
         B     OBEX4         I/O ERROR
         B     OBINC         BAD EXTENT ???
         B     OBEX4         PROGRAM ERROR ?                     82031
         SPACE 3
*        THIS SECTION OF CODE READS A SPECIFIC DSCB FOR INDEX
*
         DROP  ,                                                 81340
OBTAINS  STM   R14,R12,12(R13)  SUBSIDIARY ENTRY POINT           81340
         USING OBTAINS,R15   USE CURRENT BASE                    81340
         LM    R11,R12,VTOCBASE  GET PROGRAM BASES BACK          81340
         USING IEHVTOC,R11,R12   DECLARE MAIN BASES AGAIN        81340
         DROP  R15                                               81340
         LA    R9,PRSAVE     GET PRINT SAVE AREA                 81340
         ST    R9,8(,R13)    LINK SAVE AREAS                     81340
         ST    R13,4(,R9)                                        81340
         LR    R13,R9        SET NEW SAVE AREA                   81340
         MVC   CCHHR(5),DS1PTRDS  COPY THE CCHHR                 81340
         @OBTAIN FMT1CAM     READ THE DSCB                       81340
         BXH   R15,R15,OBEX0  FUNNY RETURN CODE IF BAD           81340
OBEX4    LA    R15,4         THIS IS THE GOOD RETURN CODE !      81340
OBEXALL  L     R13,4(,R13)                                       81340
         LM    R0,R12,20(R13)                                    81340
         L     R14,12(,R13)                                      81340
         B     0(R14,R15)    RETURN                              81340
OBEX0    SLR   R15,R15                                           81340
         B     OBEXALL
OBEX8    LA    R15,8                                             81340
         B     OBEXALL
         SPACE 2
VTOCBASE DC    A(IEHVTOC,IEHVTOC+4096) PROGRAM BASES             81340
         EJECT
CCHHRF3  DC    XL5'0'
CCHHRF6  DC    XL5'0'                                            81340
FMT3CAM  CAMLST SEEK,CCHHRF3,VOL,CURDSCB                         81340
FMT6CAM  CAMLST SEEK,CCHHRF6,VOL,CURDSCB                         81340
COMPTAB  DC    S(DS4VTOCE+2),S(TOTALSIZ),H'0'  ADD VTOC SIZE TO TOTAL
VOLLIST  DS    0H            CONVERSION LIST FOR VOLUME TRACK LIST
         DC    S(VOLTOT),H'4',S(VTTOT),H'7'    TRACKS ON VOLUME
         DC    S(VOLUSE),H'4',S(VTUSE),H'7'  TRACKS IN USE
         DC    S(VOLFREE),H'4',S(VTFREE),H'7'   TRACKS FREE
         DC    S(VOLATOT),H'2',S(VTATOT),H'3'  ALTERNATE TRACKS TOTAL
         DC    S(VOLAUSE),H'2',S(VTAUSE),H'3'   ALT TKS IN USE
         DC    S(VOLAFREE),H'2',S(VTAFREE),H'3'  ALT TRKS FREE
         DC    S(VOLMISS),H'4',S(VTMISS),H'7'  TRKS MISSING ?
         DC    H'0'          END OF TABLE
         SPACE
*        FORMAT IS :
*              H - BLOCKSIZE OF DEVICE
*              H - NO.OF ALTERNATE TRACKS
*              H - NO. OF TRACKS NEEDED FOR IPL TEXT - 1
*              H - NO. OF TRACKS ON PACK(1 - RPS 1 BLOCK)  OS/VS 81340
*              H - NO. OF TRACKS ON PACK(2 - RPS TRACK)  OS/VS   81340
*              H - NO. OF ALTERNATES FOR VM VOLUMES              81340
*
VOLDEVT  DC    Y(7294,60,0,4060,4060,20)       2314              81340
         DC    Y(3625,30,1,2030,2030,0)        2311              81340
         DC    Y(14138,1,0,384,384,0)          2305-1            81340
         DC    Y(14568,1,0,384,384,0)          2305-1            81340
         DC    Y(14660,1,0,848,848,0)          2305-2            81340
         DC    Y(14858,1,0,848,848,0)          2305-2            81340
         DC    Y(13030,133,0,7809,15485,0)     3330-X            81340
         DC    Y(13165,133,0,7809,15485,0)     3330-X            81340
         DC    Y(20483,0,0,200,200,0)          2301              81340
         DC    Y(4984,184,1,11500,11500,0)     2302              81340
         DC    Y(4892,0,1,800,800,0)           2303              81340
         DC    Y(19069,150,0,16800,16800,0)    3350              81340
         DC    Y(19254,150,0,16800,16800,0)    3350       *TSM*  81340
         DC    AL2(35616,12,0,11520,11520,0)     3375            90358
         DC    AL2(36000,12,0,11520,11520,0)     3375            90358
*OLD*    DC    AL2(47476,15,0,13290,39870,0)     3380,  3380E    90358
         DC    AL2(47968,15,0,13290,39870,0)     3380,  3380E    90358
         DC    AL2(58786,15,0,16695,33390,0)     3390 M1, M2     90358
VOLDEVTE EQU   *
         SPACE
VOLDES   DC    CL132'0'
         ORG   VOLDES+1
         DC    C'***** VOLUME HAS '
VTTOT    DC    CL7' ',C' TRACKS, '
VTUSE    DC    CL7' ',C' IN USE, '
VTFREE   DC    CL7' ',C' AVAILABLE; OF '
VTATOT   DC    CL3' ',C' ALTERNATE TRACKS '
VTAUSE   DC    CL3' ',C' ARE IN USE, '
VTAFREE  DC    CL3' ',C' ARE AVAILABLE *****'
         ORG
VOLERR   DC    CL132' '
         ORG   VOLERR+29
         DC    C'*****>>>>> ERROR  -- '
VTMISS   DC    CL7' ',C' TRACKS NOT ACCOUNTED FOR <<<<<*****'
         ORG
         SPACE 2
VOLALNOW DC    CL132' '
         ORG   VOLALNOW+50
         DC    C'***  ALL TRACKS NOW ACCOUNTED FOR  ***'
         ORG
         SPACE 3                                                 90358
         PUSH  PRINT                                             90358
         PRINT GEN                                               90358
CLCMASK  CMASK L=R15         SET MASK LENGTH = SER LENGTH        90358
         POP   PRINT                                             90358
         DROP  ,             DONE WITH MAIN BASES                81340
         EJECT
*        THIS CODE ENQUEUES AND DEQUEUES ON 'SYSVTOC' AND 'VOLSER'
*        THIS BUNK IS NECESSARY BECAUSE THE SYSTEM 'OBTAIN' CODE, AFTER
*        CONDITIONALLY ENQUEUEING ON THE VOL-SER ISSUES A DEQ.  IN
*        ORDER TO PRESERVE CORRECT VOL-STATUS IN THE PROGRAM, IT IS
*        NECESSARY TO USE A SUB-TASK TO DO THE ENQ SINCE OBTAIN
*        CANNOT DEQ THAT ONE.
*
***********************************************************************
*
*    SYSTEMS WITH SHARED DASD SHOULD USE RESERVE RATHER THAN ENQ, AND
*        SHOULD DO A UCB LOOKUP AT LABEL VQENQ
*
*    THIS CODE, UNDER MFT, REQUIRES ATTACH AND IDENTIFY AT SYSGEN TIME
*
***********************************************************************
*
         PUSH  USING                                             90358
IEHVENQU STM   R14,R12,12(R13)                                   81340
         USING IEHVENQU,R12                                      81340
         LR    R12,R15       VERY BASE                           81340
         LA    R9,VQSAVE                                         81340
         ST    R9,8(,R13)                                        81340
         ST    R13,4(,R9)                                        81340
         LR    R13,R9                                            81340
         LR    R4,R1         SAVE ENTRY PARM LIST                81340
         USING VQLIST,R4                                         81340
         TESTAUTH FCTN=1     ARE WE AUTHORIZED ?                 90358
         BXLE  R15,R15,VQWAIT  YES; PROCEED                      90358
         MVC   VQMAJ(3),=C'IEH'  MAKE UNPRIVILEGED MAJOR NAME    90358
         SPACE 2
VQWAIT   WAIT  ECB=VQECB     WAIT FOR MAIN PROGRAM REQUEST
         SPACE
         CLI   VQMIN,C' '    ARE WE ENQUEUED ON ANYTHING ?
         BE    VQCHK         NO, BRANCH AROUND
         L     R5,VQRESAD    GET RESERVE/ENQ LIST ADDRESS        81340
         DEQ   MF=(E,(R5))   DEQ FROM VOL-SER                    81340
         MVI   VQMIN,C' '    INDICATE NO CURRENT ENQ
         SPACE
VQCHK    TM    VQECB,X'10'   WAS THIS A TERMINATION REQUEST ?
         BZ    VQENQ         NO, GO TO ENQ CHECK
         L     R13,4(,R13)   RESTORE                             81340
         LM    R14,R12,12(R13)                                   81340
         SLR   R15,R15                                           81340
         BR    R14           RETURN                              81340
         SPACE 2
VQENQ    OC    VQECB+1(3),VQECB+1  WAS THIS A DEQ ONLY REQUEST ?
         BZ    VQPOST        YES, DONE WITH CURRENT REQUEST
         LA    R5,VQRES      PROVISIONALLY SET FOR RESERVE       81340
         L     R2,VQECB      GET ADDRESS OF VOL-SER              81340
         SERVCALL UCBDK,(R2)  LOCATE THE UCB FOR THIS VOLUME     81340
         LTR   R3,R0         FOUND                               81340
         BZ    VQPOST        NO; BYPASS ENQUEUE                  81340
         USING UCBOB,R3                                          81340
         TM    UCBTBYT2,UCBRR   SHARED DASD ?                    81340
         BZ    VQUCBSTS      NO; RESERVE NOT NECESSARY           81340
         TM    UCBSTAT,UCBRESV+UCBPRES+UCBSYSR  PERMANENT PACK ? 81340
         BNZ   VQPOST        YES; SKIP RESERVE - DON'T HANG OTHER CPU
VQUCBSTS ST    R3,VQUCBAD    STASH UCB ADDRESS                   81340
         MVC   VQMIN,0(R2)   COPY VOL-SER                        81340
         SERVCALL TIOUA,(R3)   SEE IF TIOT ENTRY FOR THIS UCB    81340
         LTR   R1,R0         ANY ?                               81340
         BZ    VQENQQ        NO; USE ENQ, NOT RESERVE            81340
VQENQQ   LA    R5,VQREQ      SET FOR ENQUEUE                     81340
VQRESRV  RESERVE MF=(E,(R5))  RESERVE/ENQ ON VOLUME              81340
         ST    R5,VQRESAD    AND SAVE ADDRESS FOR DEQ            81340
         SPACE
VQPOST   XC    VQECB,VQECB   CLEAN OUR ECB
         POST  VQREADY       SHOW WE'RE READY FOR MORE
         B     VQWAIT        WAIT FOR NEXT REQUEST
         POP   USING                                             90358
         SPACE 2
VQLIST   DS    0F            START OF PASSED ECB LIST
VQECB    DC    F'0'          ECB TO REQUEST SERVICE (ENQ/DEQ)
VQREADY  DC    F'0'          ECB AFTER REQUEST IS SATISFIED
VQSAVE   DC    18F'0'        SAVE AREA FOR SUB-TASK              81340
VQDETACH DC    F'0'          SUBTASK ATTACH ECB
VQTCBAD  DC    F'0'          SUBTASK TCB ADDRESS
VQUCBAD  DC    F'0'          ADDRESS OF UCB FOR RESERVE          81340
VQRESAD  DC    F'0'          ADDRESS OF ENQ/RESERVE PARAMETER LIST
VQMAJ    DC    CL8'SYSVTOC'  SYSTEM VTOC ENQUEUE NAME
VQMIN    DC    CL6' '        ENQ VOL-SER
VQMINC   DC    X'C0'         ALTERNATE ENQ MINOR NAME
VQREQ    ENQ   (VQMAJ,VQMIN,S,6,SYSTEM,    ENQ/DEQ LIST                *
               VQMAJ,VQMINC,S,1,SYSTEM),RET=HAVE,MF=L  NORMAL ENQUEUE
VQRES  RESERVE (VQMAJ,VQMIN,S,6,SYSTEMS),  RESERVE PARAMETERS          *
               UCB=VQUCBAD,RET=HAVE,MF=L     FOR ENQ/DEQ         81340
         TITLE ' L I S T V T O C  -  PRINT SUBROUTINE'
*
*        CALL  PRINT,(DATALOC,LINES)
*
         SPACE 3
         ENTRY PRINT
         DROP  ,             DELETE ALL BASE DECLARATIONS        81340
PRINT    STM   R14,R12,12(R13)                                   81340
         USING PRINT,R12     USE CURRENT BASE                    81340
         LR    R12,R15       FIX IT UP                           81340
         LA    R9,PRSAVE     GET PRINT SAVE AREA                 81340
         ST    R9,8(,R13)    LINK SAVE AREAS                     81340
         ST    R13,4(,R9)                                        81340
         LR    R13,R9        SET NEW SAVE AREA                   81340
         LM    R2,R3,0(R1)   GET PASSED PARAMETERS               81340
         SLR   R4,R4         CLEAR IC REGISTER                   81340
         ICM   R4,3,2(R3)    GET NUMBER OF LINES TO RESERVE ON PAGE
         BNP   PUTNUM        NONE; SKIP                          81340
         PRTROOM (R4)        REQUEST CONTIGUOUS LINES ON ONE PAGE
PUTNUM   ICM   R4,3,0(R3)    GET NUMBER OF LINES TO PRINT        81340
         BNP   PUTEXIT       NONE; EXIT                          81340
PRTLOOP  CLI   0(R2),C'+'    OVERRPRINT REQUEST ?                81340
PUTPUT1  NOP   PUTBUMP       CHANGED TO 'BE' WITH %TERM OPTION   81340
         CLC   103(29,R2),=C'DSCB EXTENDED AT 0000.0000.00'      81340
         BE    PUTSHORT      SKIP NULL EXTENSION MESSAGE         81340
         PRTF  (R2),132      PRINT 132 BYTES WITH CARRIAGE CONTROL
         B     PUTBUMP       DO ANOTHER                          81340
PUTSHORT PRTF  (R2),102      PRINT SHORT LINE                    81340
PUTBUMP  LA    R2,132(,R2)   SKIP LINE SIZE                      81340
         BCT   R4,PRTLOOP    GO BACK FOR MORE                    81340
PUTEXIT  L     R13,4(,R13)   RESTORE OLD SAVE AREA               81340
         LM    R14,R12,12(R13)  RELOAD REGISTERS                 81340
         XR    R15,R15       SET UNNECESSARY GOOD RETURN CODE    81340
         BR    R14           RETURN TO CALLER                    81340
PRSAVE   DC    18F'0'                                            81340
         DROP  ,                                                 81340
         TITLE ' L I S T V T O C  -  DATA SET SIZE SUBROUTINE'
         ENTRY COMPSIZE
COMPSIZE L     R1,0(,R1)           GET LOC OF PARMS              81340
         USING COMPSIZE,R15                                      81340
         SLR   R0,R0         CLEAR ACCUMULATOR                   81340
COMPLOOP CLC   0(2,R1),ZEROES                                    81340
         BE    SAVESIZE            DONE
         MVC   *+8(2),0(R1)  COPY S-FORMAT INPUT LOCATION        81340
         LA    R2,*-*              COMPUTE INLOC                 81340
         MVC   COMPTEMP,0(R2)      MOVE DATA TO PROPER BOUNDARIES
         SLR   R5,R5               SET TRACKS TO ZERO            81340
         MVC   *+8(2),2(R1)  GET S-FORMAT OUTPUT ADDRESS         81340
         LA    R3,*-*              COMPUTE OULOC                 81340
         CLC   COMPTEMP+4(4),ZEROES     IS IT A NULL EXTENT      81340
         BE    COMPADD             IF YES, SKIP ARITH
         CLC   COMPTEMP(4),ZEROES    IS IT TRACK ZERO ?          81340
         BNE   *+8           NO, SKIP
         MVI   VOLABEL+1,0   SET LABEL TRACK ALLOCATED           81340
         CLC   COMPTEMP(4),=AL4(1)   IS IT TRACK 1 ?
         BH    COMPNIPL      NO                                  81340
         CLC   COMPTEMP+4(4),=AL4(1)   IS TRACK 1 ALLOCATED ?
         BL    COMPNIPL      NO, SKIP                            81340
         MVI   VOLIPL+1,0    RESET 'POSSIBLE' IPL TRACK ALLOCATED
COMPNIPL BCTR  R2,0          BUMP BACK TO EXTENT TYPE INDICATOR  81340
         BCTR  R2,0          BUMP BACK TO EXTENT TYPE INDICATOR  81340
         LH    R5,COMPTEMP+4    HI CYL                           81340
         SH    R5,COMPTEMP    - LO CYL                           81340
         LH    R6,COMPTEMP+6  HI TRK                             81340
         SH    R6,COMPTEMP+2  - LO TRK                           81340
         AH    R6,=H'1'      COMPENSATE                          81340
         CLI   0(R2),X'80'   SPLIT EXTENT ?                      81340
         BE    COMPSPL       YES - DO DIFFERENTLY                81340
         MH    R5,C1+2       TKS/CYL                             81340
         AR    R5,R6         TOTAL TRKS IN EXTENT                81340
         B     COMPADD       SUM IT IN
COMPSPL  AH    R5,=H'1'      # OF CYLS                           81340
         MR    R4,R6         TOTAL TRACKS                        81340
COMPADD  AR    R0,R5               ADD EXTENT SIZE               81340
         CVD   R5,DWORK            CNVRT TO DEC                  81340
         MVC   CVDB,CVDM
         ED    CVDB,DWORK
         MVC   0(4,R3),CVDB+12                                   81340
         LA    R1,4(,R1)           STEP PARM POINTER             81340
         B     COMPLOOP            LOOP TILL DONE
         SPACE 2
SAVESIZE LR    R1,R0         COPY EXTENT SIZE                    81340
         A     R0,XTENTSIZ         ADD LAST                      81340
         ST    R0,XTENTSIZ         SAVE FOR NEXT                 81340
         A     R1,VOLUSE     ADD TO TRACKS/VOLUME IN USE         81340
         ST    R1,VOLUSE     AND STASH BACK                      81340
         BR    R14                                               81340
         EJECT ,
         ENTRY VTOCCOMM
VTOCCOMM DS    0F
VOLFREE  DC    F'0'          FREE TRACKS FOR FMT 5
VOLABEL  DC    H'0'          FLAG FOR LABEL TRACK
VOLIPL   DC    H'0'          FLAG FOR IPL TRACK, IF APPLICABLE
CYLTRK   DC    H'0'          NO. OF TRACKS PER CYLINDER
DS5FLG   DC    X'0'          VTOC INDICATOR (I.E. BAD FMT 5'S)
INDXFG   DC    X'0'          DEFAULT - DETAIL LISTING, NO INDEX
XFINDEX  EQU   1             %INDEX REQUESTED                    82031
XFNODET  EQU   2             %INDON - NO DETAIL LISTING          82031
XFSORT   EQU   4             %SORT REQUESTED                     82031
F4ADDR   DC    A(FMT4)       ADDR. OF FORMAT 4 DSCB FOR INDEX
WKADDR   DC    A(CURDSCB)    ADDR OF WORK AREA FOR INDEX         81340
VOLTOT   DC    F'0'          TOTAL TRACKS ON VOLUME
VOLMISS  DC    F'0'          TRACKS NOT ACCOUNTED FOR
VOLUSE   DC    F'0'          TRACKS IN USE
VOLATOT  DC    H'0'          TOTAL ALTERNATE TRACKS ON VOLUME
VOLAUSE  DC    H'0'          ALTERNATE TRACKS IN USE
VOLAFREE DC    H'0'          ALT. TRACKS FREE
MAXLINE  DC    H'60'         MAX LINES PER PAGE                  89134
LCNT     DC    F'0'          CURRENT LINES LEFT                  89134
PAGE     DC    F'0'          CURRENT PAGE NUMBER                 89134
RETCH    DC    H'0'          RETURN CODE                         89121
         TITLE ' L I S T V T O C  -  CONVERT TO HEXADECIMAL SUBROUTINE'
         ENTRY CVH
CVH      L     R1,0(,R1)                                         81340
         USING CVH,R15                                           81340
CVHLOOP  MVC   WORKCL9,=XL9'F'
         USING TABLE,R1                                          81340
         LH    R2,INLNGTH                                        81340
         BCTR  R2,0                                              81340
         N     R2,=F'15'                                         81340
         MVC   MVO+4(2),INLOC
         EX    R2,MVO                                            81340
         LH    R2,OULNGTH                                        81340
         BCTR  R2,0                                              81340
         N     R2,=F'15'                                         81340
         MVC   HUNPK+2(2),OULOC
         MVC   HTR+2(2),OULOC
         LR    R3,R2                                             81340
         SLL   R2,4                                              81340
         EX    R2,HUNPK                                          81340
         EX    R3,HTR                                            81340
         LA    R1,8(,R1)                                         81340
         CLC   INLOC,ZEROES                                      81340
         BNE   CVHLOOP
         BR    R14                                               81340
HTR      TR    0(0,0),HEXTAB-240
HUNPK    UNPK  0(0,0),WORKCL9
MVO      MVO   WORKCL9,0
         TITLE ' L I S T V T O C  -  CONVERT TO DECIMAL SUBROUTINE'
         ENTRY CVD
CVD      L     R1,0(,R1)                                         81340
         USING CVD,R15                                           81340
CVDLOOP  XC    WORKF,WORKF
         USING TABLE,R1                                          81340
         LH    R2,INLNGTH                                        81340
         CH    R2,=H'4'                                          81340
         BHR   R14                                               81340
         LTR   R2,R2         ZERO ?                              81340
         BLR   R14           YES; DONE                           81340
         LA    R3,WORKF+4                                        81340
         SR    R3,R2                                             81340
         MVC   MOVE+4(2),INLOC
         BCTR  R2,0                                              81340
         EX    R2,MOVE                                           81340
         L     R2,WORKF                                          81340
         LPR   R2,R2         JUST IN CASE - EVEN IF O/P BAD      81340
         CVD   R2,WORKD                                          81340
         MVC   CVDB(16),CVDM
         ED    CVDB(16),WORKD
         LH    R2,OULNGTH                                        81340
         LA    R3,CVDB+16                                        81340
         SR    R3,R2                                             81340
         BCTR  R2,0                                              81340
         MVC   CVDMVC+2(2),OULOC
         EX    R2,CVDMVC                                         81340
         LA    R1,8(,R1)                                         81340
         CLC   INLOC,ZEROES                                      81340
         BNE   CVDLOOP
         BR    R14                                               81340
MOVE     MVC   0(0,R3),0                                         81340
CVDMVC   MVC   OULOC(0),0(R3)                                    81340
CVDM     DC    XL16'40202020202020202020202020202120'
CVDB     DS    CL16
         TITLE ' L I S T V T O C  -  CONSTANTS AND WORK AREAS'
WORKD    DS    D
DWORK    DS    D
COMPTEMP DS    D
WORKF    DS    F
WORKCL9  DS    CL9
ZEROES   DC    XL8'0'                                            81340
VOL      DC    CL6'111111'
XTENTSIZ DC    F'0'                                              81340
C1       DC    2H'0'         CYL/PACK & TRKS/CYL
ONE      DC    H'1',H'3'
FAKE1    DC    H'1,0'
FAKE2    DC    H'0,2'
FMT4CAM  CAMLST SEARCH,DSNAME04,VOL,CURDSCB                      81340
DSNAME04 DC    44X'04'
CCHHR    DC    3H'0'
FMT1CAM  CAMLST SEEK,CCHHR,VOL,CURDSCB                           81340
@SERVICE DC    A(0)          ADDRESS OF SERVICE ROUTINE          81340
@PRINTER DC    A(0)          ADDRESS OF PRINTER ROUTINE          81340
@OBTAIN  DC    A(0)          ADDRESS OF @OBTAINS                 81340
SYSPRINT PRTWORK SYSPRINT,TITLE=3                                81340
CURDSCB  DC    35D'0'                                            81340
         ORG   CURDSCB                                           81340
         IECSDSL1 1          EXPAND THE FORMAT 1                 81340
FMT1ADDR DC    XL5'00'       DSCB ADDRESS FEEDBACK               81340
         ORG   ,                                                 81340
VCHR4    DC    XL5'0'
NOVOLUME VCON  END=NOVOLUMX                                      81340
         DC    C'-VOLUME '
NOVOL    DC    CL6' '
         DC    C' COULD NOT BE MOUNTED'
         VCON  ,                                                 81340
NOPRMERR VCON  '-NULL OR MISSING PARM= OPERAND ON EXEC CARD'     81340
IOVOLERR VCON  END=IOVOLERX                                      81340
         DC    C'-PERMANENT I/O ERROR ON VOLUME '
IOVOL    DC    CL6' '
         VCON  ,                                                 81340
TITLE    VCON  END=TITLEX                                        81340
         DC    C'#'          REQUEST PAGE NUMBER, TIME AND DATE  81340
         DC    C'FORMATTED DUMP OF VTOC ON VOLUME '
TVOL     DC    CL6' '
         VCON  ,                                                 81340
HEXTAB   DC    CL16'0123456789ABCDEF'
PARMSAVE DC    3F'0'                                             81340
LOOPFG   DC    X'0'                                              90358
LFGUCB   EQU   X'80'           UCB SCAN MODE                     90358
LFGSYS   EQU   X'40'           SYSTEMS PACKS ONLY                90358
LFGMASK  EQU   X'20'           MASKED VOLUME SERIAL              90358
LFGHIT   EQU   X'01'           MATCH FOUND                       90358
         SPACE 1                                                 90358
TOTALOUT DC    CL132' '
         ORG   TOTALOUT
         DC    C'0THERE ARE '
TOTALSIZ DC    CL5' '
         DC    C' TRACKS IN THIS DATA SET.'
         DC    CL2' '        SPACER
USEPCNT  DC    C' 101.99 %  USED.'
         ORG
USEPMSK  DC    X'40402020202120',C' %  USED.'
TRUNC    VCON  END=TRUNCX                                        81340
         DC    C'-VOLUME SERIAL NUMBER TRUNCATED TO '''          81340
TRUNCVOL DC    CL6' ',C''''                                      81340
         VCON  ,                                                 81340
SORTFLAG DC    X'00'         @OBTAINS SORTS VTOC WHEN SET TO X'A2'
RELDSO   DC    H'0'          DATA SET ORGANIZATION              GP09186
RELUSE   DC    H'0'          SAVE REL. TRACKS USED BY PS OR PO DS
SYSPACK  DC    X'02',C'SVS',X'03',C'DLIB',X'05',C'IGP001' SYSTEM PACKS
         DC    X'02',C'MVS',X'03',C'LIBP',X'03',C'SYST'          83156
         DC    4X'FFFFFFFFFFFFFF'    EXTRA ROOM FOR ZAPPING
         SPACE 2                                                 81340
         LTORG ,
         SPACE 1                                                 81340
FMT4     DS    0D                                                81340
         IECSDSL1 4          EXPAND THE FORMAT 4 DSCB            81340
FMT4LEN  EQU   *-FMT4        LENGTH TO MOVE                      81340
SCANTAB  DC    256X'0'
         TRENT SCANTAB,X'FF',C','  STOP PARM SCAN ON COMMA       90358
         ORG   ,
         SPACE 1                                                 90358
MASKTRT  DC    256X'00'      MASK CHARACTER TABLE                90358
         TRENT MASKTRT,4,C'-'  MINUS IS TRAILING MATCH           90358
         TRENT ,4,C'*'       ASTERISK IS TRAILING MATCH          90358
         TRENT ,8,C'?'       QUESTION MARK IS CHARACTER MATCH    90358
         TRENT ,8,C'"'       AS IS QUOTE                         90358
         ORG   ,                                                 90358
         TITLE ' L I S T V T O C  -  CONVERT DSCB ADDRESS SUBROUTINE'
         ENTRY PUTADDR
PUTADDR  STM   R14,R12,12(R13)                                   81340
         LR    R4,R15                                            81340
         USING PUTADDR,R4                                        81340
         L     R5,0(,R1)                                         81340
PUTLOOP  MVC   INADDR(2),0(R5)                                   81340
         CALL  CVH,(INADDR)
         MVC   *+8(2),4(R5)                                      81340
         LA    R1,*-*                                            81340
         LA    R2,PUTASPC                                        81340
         LA    R3,PUTASNO                                        81340
         LA    R6,PUTWORK                                        81340
PUTSUBLP CLC   7(1,R5),0(R2)                                     81340
         BE    PUTOURS       IT'S ONE OF OUR SPECIAL LENGTHS
         LA    R2,1(,R2)                                         81340
         BCT   R3,PUTSUBLP   CHECK NEXT                          81340
         SLR   R2,R2                                             81340
         IC    R2,7(,R5)                                         81340
         BCTR  R2,0                                              81340
         EX    R2,PUTMVC     MOVE AS IS                          81340
         B     PUTINC
PUTOURS  CH    R3,H5                                             81340
         BE    PUTCCHH
         CH    R3,=H'3'                                          81340
         BH    PUTHR         NECESSARY LATER ON
         BL    PUTMB
         LA    R2,3                                              81340
         EX    R2,PUTMVC                                         81340
         MVI   4(R1),C'.'                                        81340
         LA    R1,5(1,0)                                         81340
H5       EQU   *-2
         LA    R6,4(,R6)                                         81340
PUTHR    LA    R2,3                                              81340
         EX    R2,PUTMVC                                         81340
         MVI   4(R1),C'.'                                        81340
         LA    R1,5(,R1)                                         81340
         LA    R6,4(,R6)                                         81340
PUTR     LA    R2,1          PUTS RECORD .RR ONLY                81340
         EX    R2,PUTMVC                                         81340
PUTINC   LA    R5,8(,R5)     GET NEXT LIST ITEM                  81340
         CLI   0(R5),0       LAST ?                              81340
         BNE   PUTLOOP
         LM    R14,R12,12(R13)                                   81340
         BR    R14                                               81340
PUTCCHH  LA    R7,2                                              81340
         B     PUTCH
PUTMB    LA    R2,1          MM.                                 81340
         EX    R2,PUTMVC                                         81340
         MVI   2(R1),C'.'                                        81340
         LA    R1,3(,R1)                                         81340
         LA    R6,2(,R6)                                         81340
         LA    R7,3          LOOP THREE TIMES FOR BBBB. CCCC. HHHH.
PUTCH    LA    R2,3                                              81340
PUTMBLP  EX    R2,PUTMVC                                         81340
         CH    R7,=H'1'                                          81340
         BE    *+8
         MVI   4(R1),C'.'                                        81340
         LA    R1,5(,R1)                                         81340
         LA    R6,4(,R6)                                         81340
         BCT   R7,PUTMBLP                                        81340
         CH    R3,=H'1'      WITHOUT .RR ?                       81340
         BNE   PUTINC        RIGHT, DO NEXT
         BCTR  R1,0                                              81340
         MVI   0(R1),C'.'                                        81340
         LA    R1,1(,R1)                                         81340
         B     PUTR          ADD .RR
PUTMVC   MVC   0(0,R1),0(R6)                                     81340
PUTASPC  DC    AL1(9,7,12,17,20)
PUTASNO  EQU   *-PUTASPC
INADDR   DC    S(*),H'8',S(PUTWORK),H'16',H'0'
PUTWORK  DS    CL16
TABLE    DSECT ,
INLOC    DS    H
INLNGTH  DS    H
OULOC    DS    H
OULNGTH  DS    H
         SPACE 2                                                 81340
SAVE     DSECT ,             WORK AND SAVE AREA FOR MAIN TASK    81340
SAVEND   EQU   *             NOTHING ELSE                        81340
         SPACE 2                                                 81340
         PRINT &PRTSYS                                           81340
CVT      DSECT ,                                                 81340
         CVT   DSECT=YES                                         81340
         SPACE 1                                                 81340
         IHACDE ,                                                81340
         SPACE 1                                                 81340
         IKJTCB ,                                                81340
         SPACE 1                                                 81340
         IEFTIOT1 ,                                              81340
         SPACE 1                                                 81340
         IEFUCBOB ,                                              81340
         AIF   (&MVS).NOURR                                      82195
UCBRR    EQU   UCB2OPT2      SHARED DASD FLAG                    81340
.NOURR   SPACE 1                                                 81340
         END
