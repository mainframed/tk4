SUBVTOCI TITLE 'S U B V T O C I  ***  EXAMINE DS4VTOCI ATTRIBUTES'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
*    CALLER PASSES DS4VTOCI ADDRESS IN R1 & VALID SAVE AREA IN R13
*    OUTPUT R0 CONTAINS INPUT WITH EXTRANEOUS BITS TURNED OFF
*          R15 - 00  UNINDEXED VTOC WITH VALID FORMAT 5 DSCBS
*          R15 - 04  INDEXED VTOC - NO VALID FORMAT 5 DSCBS
*          R15 - 08  DOS VTOC OR ERR - NO VALID FORMAT 5 DSCBS
*          R15 - 12  VTOC WITH ERRORS
         SPACE 1
SUBVTOCI START 0
         USING SUBVTOCI,R15  SET BY CALLER
         USING USAVE,R13     DECLARE USER'S SAVE AREA
         B     BEGIN
         DC    AL1(ENDHD-*),C'SUBVTOCI &SYSDATE'
ENDHD    EQU   *
BEGIN    STM   R14,R12,RETREG14
         DROP  R15
         LR    R12,R15       COPY BASE
         USING SUBVTOCI,R12  DECLARE IT
         SR    R3,R3         CLEAR FOR INSERT
         ST    R3,RETCODE    ZERO HIGH 3 BYTES OF RETURN CODE
         IC    R3,0(,R1)     LOAD DS4VTOCI
         ST    R3,RETREG0    STORE
         MVICC 12,RESULT=RETCODE  PROVISIONALLY SET ERROR
         USING DS4VTOCI,R1   PASSED BY CALLER
         TM    DS4VTOCI,DS4DVTOC  DISABLED INDEX ?
         BNZ   PGMEXIT       YES; TOO BAD - RETURN AS DOS
         TM    DS4VTOCI,DS4DIRF  VTOC UPDATE INTERRUPTED ?
         BZ    NOTDIRF       NO
         TM    DS4VTOCI,DS4DICVT  VTOC UPDATE FIXED ?
         BZ    PGMEXIT       NOT FIXED; MAJOR ERROR
NOTDIRF  NI    3+RETREG0,255-DS4DIRF-DS4DICVT  RESET IF BOTH ON
         MVICC 4             PRESET INDEXED VTOC
         TM    DS4VTOCI,DS4DOSBT+DS4IVTOC  GOOD INDEXED VTOC?
         BO    PGMEXIT       YES
         MVICC 8             PRESET DOS CODE
         TM    DS4VTOCI,DS4DSTKP  STACKED DOS ?
         BNZ   PGMEXIT       YES; TOO BAD - RETURN AS DOS
         TM    DS4VTOCI,DS4DOSBT  DOS ONLY ?
         BNZ   PGMEXIT
         NI    3+RETREG0,255-DS4DOCVT  DON'T NEED?
         MVICC 0             SET GOOD RETURN
PGMEXIT  LM    R14,R12,RETREG14  RELOAD ALTERED REGISTERS
         BR    R14           RETURN TO USER WITH NEW R15/R0
         SPACE 1
         PRINT &PRTSYS
         IECSDSL1 4
         AIF   (&MVSXA).OLD4                                    GP05085
*S4VTOCI DS    XL1           VTOC INDICATORS
*S4DOSBT EQU   X'80'           VSE CONTAMINATED
DS4DVTOC EQU   X'40'           INDEX DISABLED
DS4EFVLD EQU   X'20'           FMT 7 FREE SPACE
*S4DSTKP EQU   X'10'           VSE STACKED
*S4DOCVT EQU   X'08'           VSE CONVERTED
*S4DIRF  EQU   X'04'           INTERRUPTED
*S4DICVT EQU   X'02'           RECLAIMED
*S4IVTOC EQU   X'01'           INDEXED
         ORG DS4NOEXT+1
DS4SMSFG DS    XL1           SMS FLAGS
DS4SMSTS EQU   X'C0'           SMS VOLUME
DS4SMS   EQU   X'C0'           SMS VOLUME
DS4SMSCV EQU   X'40'           SMS INITIALIZING
DS4NTSMS EQU   X'00'           NON-SMS VOLUME
DS4DEVAC DS    AL1           # ALT CYLS
         ORG   DS4DEVSZ      REDEFINE DS4DEVSZ
DS4DSCYL DS    XL2             # CYLINDERS
DS4DSTRK DS    XL2             # TRKS PER CYLINDER
DS4DEVAV EQU   X'10'           ALT.CYL # VALID
.OLD4    SPACE 1
         PRINT &PRTSOR
USAVE    DSECT ,             USER'S SAVE AREA MAPPING
         DS    A
REG13PRV DS    A             PRIOR SAVE
REG13NXT DS    A             LOWER SAVE
RETREG14 DS    A             RETURN ADDRESS
RETCODE  DS    F             RETURN CODE
RETREG0  DS    F             RETURNED R0 = UPDATED DS4VTOCI
RETREG1  DS    A             ADDRESS OF DS4VTOCI
         DS    11A           OTHER REGISTERS
         SPACE 1
         YREGS ,
         END   ,
