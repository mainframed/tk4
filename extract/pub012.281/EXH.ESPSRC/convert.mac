CONV     TITLE 'C O N V E R T  ***  CHARACTER SET CONVERSION'    91081
         PUNCH '  ORDER CONVERT(P) '
         PUNCH ' ALIAS TCSGENER,TRANSLAT '                       92083
         SPACE 1                                                 91081
         COPY  OPTIONGB                                          91081
         SPACE 1                                                 91081
         SYSPARM LIST=YES                                        91081
*        THIS PROGRAM WILL CONVERT BCD PUNCHES FROM AN 026 KEYPUNCH
*        TO THE 029 PUNCH EQUIVALENTS..
*        NO SYNTAX ANALYSIS IS DONE. IF A FORTRAN PROGRAM PASSES
*        STATEMENT NUMBERS IN A CALL SEQUENCE, THEN THIS WILL BE
*        INCORRECTLY CONVERTED; I.E., BCD REQUIRES AN AMPERSAND
*        PRECEDING THE STATEMENT NUMBER, EBCDIC A DOLLAR SIGN.
*
***********************************************************************
*                                                                     *
*        CONVERT WAS WRITTEN IN 196? AT APPLIED DATA RESEARCH.        *
*        EXCEPT FOR THE (RENAMED) TRANSLATE TABLE, NOTHING OF THE     *
*        ORIGINAL REMAINS.  OTHER ENTRY POINTS/TRANSLATE TABLE PAIRS  *
*        ADDED FOR ADDITIONAL FUNCTIONS.                              *
*                                                                     *
*        THIS PROGRAM PLACED IN THE PUBLIC DOMAIN; HOWEVER, RELATED   *
*        MACROS, SUBROUTINES AND SUBPROGRAMS ARE PROTECTED AS         *
*        DOCUMENTED IN MEMBER $$RIGHTS.                               *
*                                                                     *
*        INPUT:  SYSUT1 - FORMAT F, V OR D FILE                       *
*        OUTPUT:  SYSUT2 - SAME FORMAT                                *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT ,                                                 91081
         PRINT &PRTSOR                                           91081
CONVERT  PGMHEAD ZERO12,BASE=(R12),BNDRY=PAGE,PARM=R9,                 *
               ENTRY=(TCSGENER,TRANSLAT),                              *
               ENTNO=TABINDEX                                    91081
         MVC   SYSUT1(PATDCBL),PATDCB  MOVE DCB PATTERN          91081
         MVC   SYSUT2(PATDCBL),PATDCB  MOVE DCB PATTERN          91081
         MVC   SYSUT1+DCBDDNAM-IHADCB(L'DCBDDNAM),=CL8'SYSUT1'   91081
         MVC   SYSUT1DD,=CL8'SYSUT1'                             91081
         MVC   SYSUT2+DCBDDNAM-IHADCB(L'DCBDDNAM),=CL8'SYSUT2'   91081
         MVC   SYSUT2DD,=CL8'SYSUT2'                             91081
         LA    R14,SYSUT1                                        91081
         LA    R15,SYSUT2                                        91081
         STM   R14,R15,OPENLIST                                  91081
         OI    OPENLIST,X'80'  LIST STOPPER                      91081
         OI    OPENLIST+4,X'80'  SET LIST STOPPER                91081
         SERVINIT ,          LOAD THE @SERVICE ROUTINE           91081
         SERVLOAD @INPREAD,@PRINTER   LOAD SUPPORT CODE         GP08204
         SERVCALL LPALD,=CL8'@DCBEXIT'  GET THE DCB EXIT         91081
         LA    R1,OUTEXOPT   POINT TO OPTION LIST                91081
         STM   R0,R1,OUTEXLST                                    91081
         MVI   OUTEXLST,X'05'  IDENTIFY AS OUTPUT EXIT           91081
         MVI   OUTEXLST+4,X'80'  END OF LIST/PARAMETER AREA      91081
         LA    R1,OUTEXLST   GET EXIT LIST ADDRESS               91081
         STCM  R1,7,DCBEXLSA-IHADCB+SYSUT2  MAKE EXIT LIST       91081
         MVC   OUTEXOPT(PATEXOPL),PATEXOPT  MOVE OPTION LIST     91081
         LA    R1,BLOCKTAB   GET BLOCKSIZE ENTRY                 91081
         ST@   R1,OXOPLIST,MVI=X'82'  BLOCKSIZE BY DEVICE        91081
         SPACE 1                                                 91081
         SLR   R15,R15                                           91081
         IC    R15,TABINDEX   GET INDEX TO TRANSLATE TABLE       91081
         SLL   R15,2         CONVERT TO OFFSET                   91081
         L     R15,TABLOOK(R15)  GET TRANSLATE TABLE ADDRESS     91081
         MVC   USERTRAN(256),0(R15)  MAKE TABLE (PATTERN)        92083
         SPACE 1                                                 92083
         INPOPEN SYSIN,OPT=(NOWTO)                               92083
         BXH   R15,R15,NOSYN  NO OVERRIDES                       92083
INPLOOP  INPGET DEV=1                                            92083
         BXH   R15,R15,SYNEOF  I/O ERROR OR END-FILE             92083
         LR    R2,R1         SAVE CARD ADDRESS                   92083
         PRTLIST INCARD      ECHO THE CARD IMAGE                 92083
         CLI   0(R2),C'*'    COMMENTS CARD ?                     92083
         BE    INPLOOP       YES; DO ANOTHER                     92083
         ICM   R0,15,0(R2)   GET FIRST FOUR BYTES                92083
         ICM   R15,15,BLANKS   LOAD BLANKS                       91043
         OR    R0,R15        MAKE UPPER CASE                     91043
         CLM   R0,15,=C'..TR'  TRANSLATION REQUEST ?             92083
         BE    SYNUTRAN      YES                                 89215
         CLM   R0,15,=C'TRAN '  ALTERNATE FORM ?                 92083
         BE    SYNUTRAN                                          90057
         SPACE 1                                                 92083
BADCARD  PRTL  'INVALID SYSIN CARD',CC=NO
         OICC  8             SET ERROR                          GP08204
         B     INPLOOP       CONTINUE                            92083
SYNUTRAN LA    R5,4(,R2)     SET SCAN AFTER VERB                 92083
         LA    R6,71(,R2)    SET SCAN STOP                       92083
         MVI   1(R6),C' '    MAKE TESTS WORK CORRECTLY           92083
         BLOOK T=,R=(R6)     LOOK FOR NON-BLANK                  92083
         CLI   0(R5),C' '    NON-BLANK ?                         92083
         BE    SYNUNONE      NO; SEE IF FIRST TIME               92083
         BAL   R14,CHARTEST  TEST FOR VALID CHARACTER            92083
         LR    R9,R8         SAVE RETURNED BYTE                  92083
         STC   R9,TRANCHAR   SAVE VALUE                          92083
SYNUTMOR BLOOK T=,R=(R6)     ANOTHER NON-BLANK ?                 92083
         CLI   0(R5),C' '                                        92083
         BNE   SYNUTDAT      MORE; PROCESS DATA                  92083
         CLM   R9,1,TRANCHAR FIRST TIME AND NO OPERAND ?         92083
         BE    BADCARD       YES; ERROR IN CARD                  92083
         B     SYNUTRET      RETURN                              92083
SYNUTDAT CH    R9,=H'255'    STILL VALID ?                       92083
         BH    BADCARD       NO; TOO MUCH INPUT                  92083
         BAL   R14,CHARTEST  GET NEXT CHARACTER                  92083
         STC   R8,USERTRAN(R9)  SAVE THIS REQUEST                92083
         LA    R9,1(,R9)     SET FOR CHAINED ENTRY               92083
         B     SYNUTMOR      TRY AGAIN                           92083
SYNUNONE TM    FLAGS,FGTRAN  FIRST TIME FOR BLANK 'TRAN' CARD?   92083
         BNZ   BADCARD       NO; ERROR                           92083
SYNUTDFL MVI   USERTRAN+X'6A',X'4F'  SPLIT TO SOLID STROKE       92083
         MVI   USERTRAN+X'C0',X'8B'  LEFT BRACE                  92083
         MVI   USERTRAN+X'D0',X'9B'  RIGHT BRACE                 92083
SYNUTRET OI    FLAGS,FGTRAN  SET TRANSLATION REQUEST             92083
         B     INPLOOP       READ AGAIN                          92083
         SPACE 1                                                 92083
INCARD   FD    ' ',NL,LEN=20                                     92083
         FD    0(R2),80      SHOW CARD IMAGE                     92083
         FD    *END                                              92083
         SPACE 1                                                 92083
CHARTEST SLR   R8,R8         CLEAR FOR IC                        92083
         IC    R8,0(,R5)     LOAD THIS CHARACTER                 92083
         LA    R5,1(,R5)     SKIP OVER IT                        92083
         CLI   0(R5),C' '    ONE BYTE ?                          92083
         BER   R14           YES; RETURN                         92083
         STC   R8,DB                                             92083
         MVC   DB+1(1),0(R5)  COPY                               92083
         LA    R5,1(,R5)     SKIP IT                             92083
         CLI   0(R5),C' '    VALID ?                             92083
         BNE   BADCARD       NO                                  92083
         LA    R15,DB                                            92083
         LA    R0,2                                              92083
         OC    DB(2),BLANKS  MAKE UPPER CASE                     92083
CHARTESL CLI   0(R15),C'A'   VALID HEX ?                         92083
         BL    BADCARD       NO                                  92083
         CLI   0(R15),C'9'   VALID ?                             92083
         BH    BADCARD                                           92083
         CLI   0(R15),C'0'   IN NUMERIC RANGE ?                  92083
         BNL   CHARTESB      YES; TRY NEXT                       92083
         CLI   0(R15),C'F'   VALID ALPHA ?                       92083
         BH    BADCARD       NO                                  92083
CHARTESB LA    R15,1(,R15)                                       92083
         BCT   R0,CHARTESL   TRY OTHER                           92083
         NC    DB(2),=X'1F1F'  MASK                              92083
      TR DB(2),=X'000A0B0C0D0E0F00000000000000000000010203040506070809'
         PACK  DB+4(2),DB(3)  PACK 0X0YSS INTO XYS               92083
         IC    R8,DB+4       GET HEX VALUE                       92083
         BR    R14           RETURN                              92083
SYNEOF   INPCLOSE DEV=1      CLOSE INPUT                         92083
         PRTSPACE 1          DOUBLE-SPACE NEXT LINE              92083
         ICM   R0,15,RETCODE TEST RETURN CODE                   GP08204
         BNZ   EXITTERM      NO COPY - INPUT ERRORS              92083
NOSYN    PRTOPEN SYSPRINT,OPT=(NOWTO)  OPEN SYSPRINT             91081
         PRTLIST TITLE1,TITLE=1        WRITE PAGE HEADER         91081
         SPACE
         OPEN  (SYSUT1,(INPUT,REREAD)),MF=(E,OPENLIST)           91081
         LA    R3,SYSUT1     LOAD INPUT ADDRESS                  91081
         BAL   R9,OPENCHEK   CHECK THAT IT OPENED                91081
         NI    OPENLIST,255-X'80'  MAKE CLOSE LIST (2 DCBS)      91081
         MVC   OXDRECFM,DCBRECFM-IHADCB+SYSUT1  SET DEFAULT RECFM
         MVC   BLOCKTAB+2(2),DCBBLKSI-IHADCB+SYSUT1  DEFAULT BLOCK
         MVC   BLOCKTAB(2),=X'FFFF'  ALL DEVICES                 91081
         SLR   R1,R1                                             91081
         ICM   R1,3,DCBLRECL-IHADCB+SYSUT1  GET LRECL            91081
         TM    DCBRECFM-IHADCB+SYSUT1,DCBRECU  U OR D ?          91081
         BO    OLDRECU       OLD IS U                            91081
         BZ    OLDVAR        OLD IS D                            91081
         TM    DCBRECFM-IHADCB+SYSUT1,DCBRECF  F OR V ?          91081
         BNZ   OLDFIX        FIXED                               91081
OLDVAR   SH    R1,=H'4'      SET EQUIVALENT FIXED SIZE           91081
         B     OLDSIZE                                           91081
OLDRECU  PRTL  '***** RECFM=U IS UNSUPPORTED - MAY NOT WORK *****',    *
               CC=NO
         OICC  2             SET SMALL EROR                     GP08204
OLDFIX   DS    0H            NOTHING SPECIAL FOR FIXED SIZE      91081
OLDSIZE  LA    R2,4(,R1)     GET VARIABLE SIZE                   91081
         MIN   R2,=H'32760',TYPE=H  CLIP TO OS MAXIMUM           91081
         STH   R1,OXLRECLF   SET FIXED SIZE                      91081
         STH   R1,OXLRECLU                                       91081
         STH   R2,OXLRECLV   SET VARIABLE                        91081
         STH   R2,OXLRECLD                                       91081
         OPEN  (SYSUT2,OUTPUT),MF=(E,OPENLIST+4)                 91081
         LA    R3,SYSUT2     LOAD OUTPUT ADDRESS                 91081
         BAL   R9,OPENCHEK   OPEN ?                              91081
         SPACE 1                                                 91081
READLOOP GET   SYSUT1        GET AN INPUT RECORD OR OUT          91081
         INC   NUMREAD       COUNT RECORDS READ                  91081
         LR    R6,R1         PRESERVE RECORD ADDRESS             91081
         SLR   R7,R7                                             91081
         ICM   R7,3,DCBLRECL-IHADCB+SYSUT1  GET RECORD LENGTH    91081
         BZ    READZERO      HUH ?                               91081
         LR    R4,R6         COPY FOR TRANSLATE                  91081
         LR    R5,R7                                             91081
         TM    DCBRECFM-IHADCB+SYSUT1,DCBRECF  FIXED OR UNDEFINED ?
         BNZ   TRANALL       YES                                 91081
         LA    R0,4          RDW ADJUSTMENT                      91081
         AR    R4,R0         SKIP OVER RDW                       91081
         SR    R5,R0         ADJUST TO DATA LENGTH               91081
         BM    READLOOP      SHOULD NOT HAPPEN ?                 91081
         BP    TRANALL       GO TO TRANSLATION                   91081
READZERO OICC  4             SET MINOR ERROR                    GP08204
         INC   NUMZERO       COUNT ZERO LENGTH RECORDS           91081
         B     READLOOP      AND SKIP                            91081
         SPACE 1                                                 91081
TRANALL  LA    R0,256        MAXIMUM TRANSLATABLE                91081
TRANLOOP LR    R1,R0         COPY MAX                            91081
         MIN   R1,(R5)       LOAD SMALLER OF DATA OR TR LENGTH   91081
         BCTR  R1,0          EXECUTE LENGTH                      91081
         EX    R1,TRANTEXT   TRANSLATE                           91081
         AR    R4,R0         UP ADDRESS BY SEGMENT LENGTH        91081
         SR    R5,R0         ADJUST LENGTH BY SEGMENT            91081
         BP    TRANLOOP      GO FOR MORE                         91081
         SPACE 1                                                 91081
PUTOUT   TM    DCBRECFM-IHADCB+SYSUT2,DCBRECU  U OR D ?          91081
         BNM   PUTLEN        YES; STASH LENGTH IN DCB            91081
         TM    DCBRECFM-IHADCB+SYSUT2,DCBRECF  RECFM=F ?         91081
         BNZ   PUTPUT        YES; LEAVE LENGTH AS IS             91081
PUTLEN   STH   R7,DCBLRECL-IHADCB+SYSUT2  UPDATE DCB RECORD LENGTH
PUTPUT   PUT   SYSUT2,(R6)   WRITE THE RECORD                    91081
         B     READLOOP
TRANTEXT TR    0(0,R4),USERTRAN  TRANSLATE TO USER'S CODE        92083
READBAD  OICC  8             SET MODERATE ERROR                 GP08204
         INC   NUMBAD        COUNT BAD RECORDS                   91081
         B     READLOOP      IGNORE THIS ONE                     91081
         SPACE 1                                                 91081
EODAD    PRTLIST MSGCOUNT    PRINT STATISTICS                    91081
         SPACE 1
EXIT     CLOSE (,DISP,,DISP),MF=(E,OPENLIST)                     91081
EXITTERM SERVTERM ,          CLOSE AND FREE EVERYTHING           91081
         L     R9,RETCODE    GET THE RETURN CODE                GP08204
         PGMEXIT RC=(R9)     RETURN WITH APPROPRIATE CODE       GP08204
MSGCOUNT FD    NUMREAD,I,RADJ,LN,LEN=15                          91081
         FD    'RECORDS READ',PAD                                91081
         FDCLC NUMBAD,ZEROES,BE=MSGCOUNY                         91081
         FD    NUMBAD,I,RADJ,LEN=15                              91081
         FD    'BAD RECORDS NOT COPIED',PAD                      91081
MSGCOUNY FDCLC NUMZERO,ZEROES,BE=MSGCOUNZ                        91081
         FD    NUMZERO,I,RADJ,LEN=15                             91081
         FD    'ZERO LENGTH RECORDS',PAD                         91081
MSGCOUNZ FDCLC RETCODE,ZEROES,BE=MSGCOUN0                       GP08204
         FD    ' ',NL                                            91081
         FD    '>>>>> RETURN CODE'                               91081
         FD    RETCODE,I,PAD                                    GP08204
         FD    '<<<<<'                                           91081
MSGCOUN0 FD    *END                                              91081
         SPACE 2
         USING IHADCB,R3                                         91081
OPENCHEK TM    DCBOFLGS,DCBOFOPN  DCB OPEN ?                     91081
         BNZR  R9            YES; RETURN                         91081
         OICC  16            SET MAJOR ERROR                    GP08204
         PRTLIST BADOPEN                                         91081
         B     EXIT          GET OUT                             91081
BADOPEN  FD    '***** OPEN FAILED FOR',NL                        91081
         FD    SYSUT1DD-SYSUT1(R3),L'SYSUT1DD,PAD                91081
         FD    '*****'                                           91081
         FD    *END                                              91081
         EJECT ,
TABLOOK  DC    A(BCDTAB)     INDEXED TRANSLATE TABLE TABLE       91081
         DC    A(TCSTAB)     T.C.S. CONVERT REVERSE QUOTE TO QUOTE
         DC    A(VANTAB)     VANILLA TABLE FOR USER OVERRIDES    92083
         DC    A(VANTAB)     VANILLA TABLE FOR USER OVERRIDES   GP08204
*        NOTHING ELSE, YET                                       91081
         SPACE 1                                                 92083
SYSIN    INPWORK SYSIN,WIDTH=80,EDIT=128                         92083
BLANKS   DC    CL8' '                                            92083
SYSPRINT PRTWORK SYSPRINT,TITLE=3                                91081
PATDCB   DCB   DDNAME=ANY,MACRF=(GL,PM),DSORG=PS,EODAD=EODAD     91081
PATDCBL  EQU   *-PATDCB                                          91081
PATEXOPT DCBEXITD FLAG1=OX1FULLT+OX1NOFBS+OX1NOPCI+OX1NOSD,            *
               FLAG2=OX2TARER,   ALLOW OPTCD=Z ON TAPE                 *
               FLAG3=0,      NO WYLBUR/BLOCKSIZED OPTIONS              *
               LEVEL2=1      ONE OPTIONAL ENTRY - BLOCKSIZE      91081
PATEXOPL EQU   *-PATEXOPT    LENGTH OF PATTERN                   91081
TITLE1   FDOPT CC=C'#'       SET AUTOMATIC PAGE NUMBERING        91081
         FD    '***** DATA SET CONVERSION *****',RADJ,LEN=82     91081
         FD    *END                                              91081
         SPACE 1                                                 91081
SAVE     DSECT ,             SAVE/WORK AREA                      91081
DB       DS    D             WORK WORD                           91081
ZEROES   DS    F             CONSTANT ZERO                       91081
         SERVDEFS ,          EXPAND SERVICE DATA                GP08204
NUMBAD   DC    F'0'          BAD RECORDS                         91081
NUMZERO  DC    F'0'          ZERO LENGTH                         91081
NUMREAD  DC    F'0'          NUMBER OF RECORDS READ              91081
INPEXLST DS    2A            INPUT EXIT LIST                     91081
OUTEXLST DS    2A            OUTPUT EXIT LIST                    91081
OPENLIST DS    2A            INPUT/OUTPUT OPEN                   91081
TABINDEX DC    X'00'         TRANSLATE TABLE INDEX               91081
FLAGS    DC    X'00'         PROCESSING FLAGS                    91081
FGTRAN   EQU   X'80'           TRAN OR ..TR CARD HAS BEEN FOUND  92083
TRANCHAR DS    X             SYSIN CHARACTER STORE               92083
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=GL,DSORG=PS,EODAD=EODAD       91081
SYSUT1DD DC    CL8'SYSUT1'                                       91081
SYSUT2   DCB   DDNAME=SYSUT2,MACRF=PM,DSORG=PS,EXLST=OUTEXLST    91081
SYSUT2DD DC    CL8'SYSUT2'                                       91081
         SPACE 1                                                 91081
         PRINT &PRTMAC                                           91081
OUTEXOPT DCBEXITD PREFIX=OX                                      91081
BLOCKTAB DC    XL4'0'        BLOCK SIZE TABLE                    91081
USERTRAN DC    256AL1(*-USERTRAN)  USER TRANSLATION TABLE        92083
SAVEEND  EQU   *             END OF DYNAMIC WORK AREA           GP08204
         SPACE 1                                                 91081
CONVERT  CSECT ,             BACK TO REALITY                     91081
*    BCD TO EBCDIC TRANSLATION TABLE                             91081
BCDTAB   DC    256AL1(*-BCDTAB)  DEFAULT - NO TRANSLATIONS       91081
         TRENT BCDTAB,C')',C'<'   REPLACE LESS THAN BY RIGHT PAR 91081
         TRENT ,C'+',C'&&'        AMPERSAND INTO PLUS            91081
         TRENT ,C'(',C'%'         PERCENT INTO LEFT PARENTHESIS  91081
         TRENT ,C'=',C'#'         POUND INTO EQUAL               91081
         TRENT ,C'''',C'@'         AT SIGN TO QUOTE              91081
         ORG   ,             END OF TABLE
         SPACE
TCSTAB   DC    256AL1(*-TCSTAB)  NO TRANSLATION                  91081
         TRENT TCSTAB,C'''',X'79'  REVERSE QUOTE TO QUOTE        91081
         ORG   ,                                                 91081
         SPACE 1
VANTAB   DC    256AL1(*-VANTAB)  NO TRANSLATION                  92083
         SPACE 1                                                 91081
         PRINT &PRTSYS                                           91081
         DCBD  DEVD=DA,DSORG=PS                                  91081
         END   ,
