BLKOP    TITLE 'S U B B L K O P  ***  GET OPTIMUM BLOCK SIZE'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES    SET A FEW GLOBALS
         SPACE 1
         PRINT &PRTSOR
***********************************************************************
**                                                                   **
**    SUBBLKOP CALCULATES THE OPTIMUM BLOCK SIZE GIVEN A UCB IN R0   **
**    AND A KEY AND RECORD LENGTH IN R1 (00kkrrrr).                  **
**       RETURNS OPTIMAL BLOCK SIZE IN R15 (OR 0 IF NO FIT)          **
**                                                                   **
***********************************************************************
SUBBLKOP PGMHEAD ZERO,BASE=R12,PARM=R1   DECLARE A FEW THINGS
         LR    R3,R0         COPY UCB
         LR    R7,R1         COPY KEY LENGTH
         SRL   R7,16           AND SHIFT RIGHT
         LR    R6,R1           AND RECORD LENGTH
         N     R6,=X'0000FFFF'    IS IT VALID?
         BZ    PGMEXIT       NO; SKIP ALL THIS
         USING UCBOB,R3      DECLARE IT
         L     R4,CVTPTR
         USING CVTMAP,R4
         L     R4,CVTZDTAB   GET DEVICE CHARACTERISTICS TABLE
         CLI   UCBTBYT3,UCB3DACC       DASD?
         BE    HAVEDASD
         LM    R1,R3,=A(UNITTABL,UNITTAB2-UNITTABL,UNITTABN)
         ICM   R0,3,UCBTBYT3
LOOPOTH  CLM   R0,3,0(R1)
         BE    HAVEOTH
         BXLE  R1,R2,LOOPOTH
HAVEOTH  LH    R15,2(,R1)    LOAD MAX SIZE
         CR    R6,R15        WILL IT FIT ?
         BNH   PGMEXIT       YES
         B     EXIT0         RETURN IT
         SPACE 1
HAVEDASD SR    R15,R15
         IC    R15,UCBTBYT4  GET DASD TYPE
         N     R15,=A(DVCTYPMK)  MASK FUNNY BITS
         SR    R14,R14
         IC    R14,0(R15,R4) GET OFFSET TO DEVICE ENTRY
         LA    R3,0(R14,R4)  GET DEVICE ENTRY
         DROP  R3,R4
         SR    R5,R5
         ICM   R5,3,DVCTRKLN-DVCT(R3)  GET MAX RAW SIZE
         MIN   R5,=F'32760'  LARGEST ALLOWED SIZE
         SR    R7,R7         KEY LENGTH
         LR    R15,R5
         SR    R14,R14
         DR    R14,R6        GET COUNT OF RECORDS/BLOCK
         LTR   R8,R15
         BNP   EXIT0
         BAL   R9,NUMMAX
         LR    R6,R1         RETURN SIZE
         SPACE 1
         PRINT &PRTSOR
PGMEXIT  PGMEXIT RC=(R6)     RETURN SIZE IN R15
EXIT0    SR    R6,R6
         B     PGMEXIT
         SPACE 2
*        SUBROUTINE TO GET OPTIMUM BLOCKSIZE FOR SOME RECORD LENGTH
*        LEN IS PASSED IN R6, STARTING COUNT IN R8.     R15-R2 WORK
*        RETURN ON R9; SIZE IN R1
NUMMAX   LR    R5,R8         COPY RECORD COUNT
         MR    R4,R6         MAKE BLOCK SIZE
         TRKCALC FUNCTN=TRKCAP,DEVTAB=(R3),                            *
               R=1,K=(R7),DD=(R5),  RECORD # ONE, KEY(R7),DL(R5)       *
               REGSAVE=YES,MF=(E,MAPCALC)   GET # PER TRACK
         LTR   R15,R0        ANY AT ALL?
         BNP   NUMMAXUP      NO; DECREASE BLOCK SIZE
         MR    R14,R8        RECORDS PER TRACK
         C     R15,DB        LARGER THAN BEFORE?
         BNH   NUMMAXUP      NO
         ST    R15,DB        REMEMBER THE RECORD COUNT
         ST    R5,DB+4         AND CORRESPONDING BLOCK
NUMMAXUP BCT   R8,NUMMAX     TRY WITH SMALLER SIZE
         LM    R0,R1,DB      RETURN COUNT/LARGEST SIZE FOUND
         BR    R9
         SPACE 1
UNITTABL DC    X'0801',AL2(80)    2540 R                        GP09217
UNITTAB2 DC    X'0802',AL2(80)    2540 P                        GP09217
         DC    X'0802',AL2(80)    2540 P                        GP09217
         DC    X'0803',AL2(80)    1443 R/P                      GP09217
         DC    X'0804',AL2(80)    2501 P                        GP09217
         DC    X'0805',AL2(80)    2520 R/P                      GP09217
         DC    X'0806',AL2(80)    3505 P                        GP09217
         DC    X'0808',AL2(132)   1403 PRT                      GP09217
         DC    X'0809',AL2(132)   3211 PRT                      GP09217
         DC    X'080A',AL2(132)   1443 PRT                      GP09217
         DC    X'080B',AL2(132)   3203 PRT                      GP09217
         DC    X'080C',AL2(132)   3525 PRT                      GP09217
         DC    X'080E',AL2(132)   3800 PRT                      GP09217
         DC    X'080F',AL2(132)   A.F. PRT                      GP09217
         DC    X'0811',AL2(132)   4245 PRT                      GP09217
         DC    X'0813',AL2(132)   4248 PRT                      GP09217
         DC    X'0820',AL2(120)   1052 CON                      GP09217
         DC    X'0821',AL2(120)   2150 CON                      GP09217
         DC    X'0822',AL2(120)   3210 CON                      GP09217
         DC    X'0823',AL2(120)   3215 CON                      GP09217
UNITTABN DC    X'0830',AL2(120)   3213 CON                      GP09217
         DC    X'FFFF',AL2(32760)      ALL ELSE (TAPE, COMM, ETC.)
         SPACE 1
SAVE     DSECT ,
DB       DS    D
         SERVDEFS ,
MAPCALC  SERVCALC MF=D
SAVEEND  EQU   *
         SPACE 1
         CVT   DSECT=YES
         IEFUCBOB ,
         IHADVCT ,
         END   ,
