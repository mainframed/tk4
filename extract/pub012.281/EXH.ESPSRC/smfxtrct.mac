         TITLE 'PROGRAM TO EXTRACT SMF DATA'
*********************************************************************
*                                                                    *
*        THE PURPOSE OF THIS PROGRAM IS TO EXTRACT SMF RECORDS FROM  *
*        AN SMF MASTER TAPE. IT IS USEFUL WHEN SMF TAPES ARE CREATED *
*        ON A MONTHLY BASIS. ALL RECORDS AS OF THE FIRST OF THE      *
*        MONTH CAN BE EXTRACTED ONTO A NEW TAPE TO START THE NEW     *
*        MONTHLY TAPE.                                               *
*                                                                    *
*        THIS PROGRAM WRITTEN BY DICK OXLEY                          *
*                                                                    *
*********************************************************************
EXTRACT  CSECT
         USING *,15
         STM   14,12,12(13)
         L     12,=A(EXTRACT)
         DROP  15
         USING EXTRACT,12
         LR    9,13
         LA    13,SAVE
         ST    13,8(9)
         ST    9,4(13)
         SPACE 3
         LTR   1,1           IS THERE A PARM ?
         BNZ   PARMYES       YES
PARMNO   WTO   'MISSING OR INCORRECT PARM FIELD'
         B     EOJ           TO BAD
PARMYES  L     1,0(1)        LOAD PARM LENGTH-ADDRESS
         LH    14,0(1)       GET PARM LEN. INTO REG.14
         LTR   14,14         NULL PARM ?
         BZ    PARMNO        NONO
         CH    14,=H'5'      CORRECT LENGTH ?
         BNE   PARMNO        NONO ALSO
         BCTR  14,0          L'PARM -1 FOR EXECUTE
         EX    14,GETPARM    GET THE PARM ENTRY
         MVC   WORK(5),MASK  MOVE IN MASK FIELD
         NC    WORK(5),DATE  'AND' DATE TO MASK
         CLC   WORK(5),MASK  IS VALUE ALL NUMERIC ?
         BNE   PARMNO        NON-NUMERIC...ERROR
         PACK  DATEP(4),DATE(5)
         OPEN  (TAPEIN,INPUT)
         OPEN  (TAPEOUT,OUTPUT)
         OPEN  (SYSPRINT,OUTPUT)
         L     0,=F'7000'    SET FOR 7K INPUT AREA
      GETMAIN  R,LV=(0)      GET IT
         LR    10,1          SAVE ADDR. IN REG. 10
         SPACE 2
         SR    8,8
         SR    9,9
         SR    6,6
         MVI   FLAG,X'00'
LOOP     GET   TAPEIN,(10)   GET A RECORD
         TM    13(10),X'0F'       IS THERE A SIGN ?
         BNO   BADONE             NO, BAD SIGN DIGIT
         CLI   13(10),X'9F'       LAST DIGIT HIGHER THAN 9 ?
         BH    BADONE             YES, THATS A NONO
         CLC   10(3,10),=X'999999'  OTHER 6 NUMBERS HIGHER THAN 9'S ?
         BH    BADONE             YES, TO BAD
         CP    10(4,10),DATEP
         BL    LOOP
PUT      PUT   TAPEOUT,(10)
         B     LOOP
         SPACE 3
BADONE   MVI   CCC,C'1'
         MVI   LINE,C' '
         MVC   LINE+1(131),LINE
         PUT   SYSPRINT,PRINT
         LR    2,10          SWAP BUFF. ADD. OVER
         L     3,=F'6500'          NUMBER OF BYTES TO PRINT
LOOP2    LA    5,WORK        WORK AREA FOR UNPK
         LA    6,50          DO 50 CHARS. AT A TIME
         LA    8,100         UNPACKS INTO 100
         UNPK  0(15,5),0(8,2)
         UNPK  14(15,5),7(8,2)
         UNPK  28(15,5),14(8,2)
         UNPK  42(15,5),21(8,2)
         UNPK  56(15,5),28(8,2)
         UNPK  70(15,5),35(8,2)
         UNPK  84(15,5),42(8,2)
         UNPK  98(3,5),49(2,2)
         TR    0(100,5),TABLE-240   7AKE READABLE
         CR    3,6           LESS THAN 50 REMAINING ?
         BL    TD3           YES, DO REMAINDER
         LR    4,8           DID 100 THIS TIME
TD4      BCTR  4,0           -1 FOR EXECUTE
         EX    4,MOVLIN      MOVE TO PRINT LINE
         MVI   CCC,C' '      SINGLE SPACE
         PUT   SYSPRINT,PRINT
         MVC   LINE(132),CCC     CLEAR FOR NEXT TIME
         SR    3,6           TOTAL BYTES - NUMBER PRINTED
         BZ    LOOP          ALL DONE WITH BAD RECORD
         AR    2,6           BUMP INPUT BUFF ADD.
         B     LOOP2
         SPACE 2
TD3      LR    6,3
         LR    4,3           LOAD REMAINING LENGTH
         AR    4,3           DOUBLE BECAUSE ITS PACKED
         B     TD4           GO PRINT IT
         SPACE 3
MOVLIN   MVC   LINE(0),0(5)      REMOTE MOVE
         SPACE 3
COMPARE  MVC   DOUBLE(4),6(10)      TIME FIELD
         IC    8,5(10)             RECORD TYPE
         L     7,DOUBLE
         CR    7,6
         BNH   BAD
PUTPUT   LR    9,8
         LR    6,7
         B     PUT
         SPACE 2
BAD      CR    9,8
         BNE   PUTPUT
         B     LOOP
         SPACE 2
EOJ1     L     0,=F'7000'
         LR    1,10          SWAP ADDRESS BACK
     FREEMAIN  R,LV=(0),A=(1)
EOJ      CLOSE TAPEIN
         CLOSE TAPEOUT
         CLOSE SYSPRINT
         L     13,SAVE+4
         LM    14,12,12(13)
         SR    15,15
         BR    14
         SPACE 3
GETPARM  MVC   DATE(0),2(1)
DOUBLE   DS    D
SAVE     DS    18F
LIST     DS    2F
MASK     DC    X'F0F0F0F0F0'
TABLE    DC    C'0123456789ABCDEF'
WORK     DS    CL101
PRINT    DS    0CL133
CCC      DS    CL1
LINE     DS    CL132
DATE     DS    CL5
DATEP    DC    PL4'0'
FLAG     DS    CL1
         SPACE 3
TAPEIN   DCB   DSORG=PS,MACRF=(GM),DDNAME=TAPEIN,DEVD=TA,EODAD=EOJ1
TAPEOUT  DCB   DSORG=PS,MACRF=(PM),DDNAME=TAPEOUT,DEVD=TA
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),RECFM=FBA,          X
               LRECL=133,BLKSIZE=665
         END
