PASS     TITLE 'WRITE/RESET PASSWORD PROTECTION IN DSCBS.'
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*  This program was written prior to RACF, TopSecret, and similar     *
*  schemes. In our system data sets were restricted by setting the    *
*  password protect bits in the DSCB1 (either no Write or no Read/W). *
*  The password data set did not contain any passwords, and none      *
*  could be added via IEHPROGM or SVC 98 (user data sets had full     *
*  password support). Data set access was controlled by local mods to *
*  system facilities.                                                 *
*  In order to do systems maintenance (data set compression, SMP      *
*  maintenance, etc.) this program allows the password flags in the   *
*  DSCBs to be set/reset on a data set or a volume basis.             *
*  Back in the late sixties we had a member of the systems group who  *
*  could not be bothered to read, and ruined many a systems pack by   *
*  applying PTFs that didn't belong, and otherwise making more work.  *
*  The design of the non-password password facility was the response  *
*  to protect us from him. As an added goody, if this program is not  *
*  used precisely as documented, it will terminate with random system *
*  codes.                                                             *
*                                                                     *
         SPACE 1
*    FOR INPUT SEE THE WRITE-UP FOR 'SCRATCH'.
*    THE ADDITIONAL REPLIES ARE:
*
*    WHEN NO FLAG SETTING/RESETTING OPTIONS ARE SPECIFIED (THE
*    DEFAULT CONDITION), THE PROGRAM SIMPLY LISTS DATASETS.
*
*    DSNAMES ---       DISPLAY EACH DS NAME BEFORE TAKING ACTION
*    NONAMES ---       CANCEL DISPLAY OPTION.
*
*    WITH THIS DISPLAY OPTION THERE WILL BE A REPLY OF Y OR N TO
*    INDICATE WHAT ACTION IS TO BE TAKEN, I.E. BYPASS OR DO IT.
*    THE OVERRIDE OPTION NOW HAS THE EFFECT OF RESETTING THE
*    PROTECT BIT, WITHOUT OVERRIDE THE BIT WILL BE SET.
*
*    THE REPLY IS OF THE FORM: REPLY NN,'XXXX,YYYYYY'
*    WHERE: XXXX IS AN EXPECTED DEVICE TYPE (I.E. 2311,...)
*    YYYYYY IS A VALID 6 CHARACTER VOLUME SERIAL OF THE VOLUME TO
*
*    INVALID VOLUME SERIALS DO NOT PRESENT ANY PROBLEM -
*    THEY ARE IGNORED AFTER A SUITABLE ERROR MESSAGE IS PRINTED -
*    THERE NEVER WILL BE A MOUNT REQUEST FOR A NON-EXISTENT VOLUME
*
*
*    OTHER REPLIES AVAILABLE TO THE OPERATOR ARE:
*    'END'      ---   TERMINATES PASSWORD PROGRAM
*    'XXXX,ALL'        CHECKS ALL TYPE XXXX VOLUMES IN THE UCB'S AND
*    LOOPS THROUGH THEM.  (E.G.  3350,ALL)
*
*    REPLY OVERRIDE RESETS RATHER THAN SETS THE PROTECT BITS.
*    REPLY 'NONORIDE' CANCELS THE OVERRIDE OPTION.
*
*    P.S.      SINCE THE PROGRAM IS SMALL AND FAST NO EFFORT HAS BEEN
*    MADE TO MAKE IT EFFICIENT OR TO OTHERWISE CLEAN IT UP.
*
         MACRO
&NM      WOT   &STR
         CNOP  0,4
&NM      BAL   1,WTO&SYSNDX
         DC    AL2(WTO&SYSNDX.A-*,0)
         DC    C&STR
WTO&SYSNDX.A EQU *
WTO&SYSNDX BALS  R9,WTOWOT                                       94115
         MEND
         SPACE 1                                                 94115
         COPY  OPTIONGB      COPY GLOBAL OPTION SWITCHES         94115
         SPACE 1                                                 94115
         SYSPARM LIST=YES    SET AND SHOW GLOBAL OPTIONS         94115
         EJECT ,
         PRINT &PRTSOR                                           94115
SETPASS  PGMHEAD ZERO12,BASE=(R11,R12),BNDRY=PAGE,PARM=R9,RM=24 GP99162
         L     R14,PATDSCB1  INITIALIZE DSCB1 CAMLST             94115
         LA    R15,ATTR      ADDRESS                             94115
         LA    R0,VOLSER     CEREAL                              94115
         LA    R1,DSN        NAME                                94115
         STM   R14,R1,DSCB1  BUILD CAMLST                        94115
         L     R14,PATDSCB4  DSCB 4 LIST                         94115
         LA    R15,DSN4      FORMAT 4 NAME                       94115
*        LA    R0,VOLSER     CEREAL - ALREADY IN BOWL            94115
         LA    R1,WORK4      RETURN AREA                         94115
         STM   R14,R1,DSCB4  BUILD CAMLST                        94115
         MVI   DSN4,X'04'                                        94115
         MVC   DSN4+1(L'DSN4-1),DSN4  ALL HEX 4S                 94115
         MVI   VOLIST+1,1    # VOLUMES FOR CATLIST               94115
         MVI   LINENO,X'7F'  FORCE INITIAL EJECT                 94115
         LA    R0,SEQLIST    SET DEFAULT - LIST ONLY            GP99162
         ST    R0,@ACTION                                        94115
         MVC   YNACTION,YNACTLST                                GP99163
         SPACE 1                                                 94115
         SERVINIT ,          LOAD THE SERVICE ROUTINE            94115
         SERVICE APFON                                           94115
         TESTAUTH FCTN=1     ARE WE AUTHORIZED?                 GP99162
         BXH   R15,R15,NOTAUTH  NO                              GP99162
         OI    PROGFLAG,PFAUTH  YES; SAY SO                     GP99162
         SPACE 3
NOTAUTH  DEVTYPE DCBDDNAM-IHADCB+INDCB,DB  LOOK FOR A SYSIN DD  GP99162
         BXH   R15,R15,NONEIN  NONE FOUND                       GP99162
         ICM   R0,15,DB      DD DUMMY?                          GP99162
         BZ    NONEIN        YES; SKIP IT                       GP99162
         OPEN  (INDCB,(INPUT))                                  GP99162
         TM    INDCB+DCBOFLGS-IHADCB,DCBOFOPN                    94115
         BZ    NONEIN                                           GP99162
*OLD*    MVC   WRITE(2),WRITRET                                 GP99162
*OLD*    MVC   WTOWOT(2),WTORET                                 GP99162
         OI    PROGFLAG,PFSYSIN                                 GP99162
       RDJFCB  (INDCB)                                          GP99162
         CLC   =C'SYS1.JOBJCL ',JFCB2                            94115
         BE    NONEIN                                           GP99162
*DEFER*  OI    PROGFLAG,PFSPEC                                  GP99162
         SPACE 2
NONEIN   DEVTYPE DCBDDNAM-IHADCB+OUT,DB    LOOK FOR A SYSIN DD  GP99162
         BXH   R15,R15,NONEOUT NONE FOUND                       GP99162
         ICM   R0,15,DB      DD DUMMY?                          GP99162
         BZ    NONEOUT       YES; SKIP IT                       GP99162
         OPEN  (OUT,(OUTPUT))  OPEN PRINT DATA SET
         OI    PROGFLAG,PFPRINT  SHOW PRINT OPEN                GP99162
         TM    OUT+DCBOFLGS-IHADCB,DCBOFOPN    OPENED ?          94115
         BNZ   NONEOUT       YES                                GP99162
         WOT   ' '                                              GP99162
         WOT   'CAN''T OPEN SYSPRINT.'
         B     EXIT16
         SPACE 2
NONEOUT  WOT   'TYPE ''END'' TO TERMINATE SETPASS'              GP99162
         LA    1,FUNNY
         BALS  R9,WTOWOT                                         94115
         EJECT
*    THIS SECTION GETS TIME AND DATE IN BCD PRINT AND COMPARE FORM.
*
         TIME  DEC                                              GP99162
         SRL   R0,4           TRUNCATE TO 1/10 SEC'S
         ST    R0,DB
         ED    TIME(10),DB     UNPACK AND EDIT TIME TO HH.MM.SS
         ST    R1,WORK4
         SP    WORK(4),=PL1'7'  SUBTRACT A WEEK
         CLI   WORK+2,X'99'   DID WE GET INTO A PREVIOUS YEAR ?
         BNE   UNP           NO.
         SP    WORK(4),=PL2'635' MAKE MODULO 365 INSTEAD OF 1000.
         TM    WORK+1,X'03'  WAS IT A LEAP YEAR?
         BNZ   UNP           NO, OK
         AP    WORK(4),=PL1'1'  ADD ONE FOR LEAP YEAR
UNP      OI    WORK+4,X'0F'    BETTER + SIGN
         UNPK  JULDAT(6),WORK4+1(3)   BCD JULIAN DATE _YYDDD
         XC    DB(8),DB
         LR    R2,R1
         STH   R1,DB+6
         CVB   R1,DB         JULIAN DAY
         SRA   R2,12
         STH   R2,DB+6
         OI    DB+7,X'0F'
         CVB   R2,DB         YEAR
         SR    R3,R3
         SR    R0,R0         WILL BE LEAP YEAR ADD, IF ANY
         TM    DB+7,X'30'    CHECK FOR LEAP YEAR
         BNZ   YD3           NO, NOT LEAP YEAR
         CH    R1,=H'60'     IS IT FEB 29 ?
         BL    YD3           NO - LOW. IGNORE LEAP YEAR
         BH    YD2  NO - HI. DECREASE JUL. DAY BY ONE TO FIT TABLE
         LA    R0,1          SET LEAP DAY FLAG
YD2      BCTR  R1,0          DECREASE JUL. DAY TO COMPENSATE FOR LEAP
YD3      LA    R4,MTAB       DAYS/MONTH CUM. TABLE
         LR    R5,R4
YLP      CH    R1,2(,R4)     DOES JUL. DAY FALL INTO THIS MONTH
         BNH   YF            YES, DO IT
         LA    R4,2(,R4)     NO, CHECK NEXT MONTH
         B     YLP
YF       SH    R1,0(,R4)     = DAY OF MONTH
         AR    R1,R0         + 1 IF LEAP DAY
         LA    R4,2(,R4)
         SR    R4,R5
         SRL   R4,1          MONTH
         MH    R4,=H'100'    PACK MONTH, DAY
         AR    R1,R4
         MH    R1,=H'100'    PACK MONTH, DAY, YEAR
         AR    R1,R2
         CVD   R1,DB
         ED    DATE(10),DB+4   CONVERT AND EDIT TO MM/DD/YY
         TM    PROGFLAG,PFSYSIN                                 GP99162
         BO    MAINTEST                                         GP99162
         AIF   ('&LOCAL' NE 'PID').NOUTCB                        94115
         SERVCALL ACGET,DB                                       94115
         STC   R0,DB                                             94115
         TM    DB+3,VAASTC+VAASYS  START TASK OR SYS.STAFF       94115
         BNZ   MAINTEST                                          94115
         LTCB  R1,USE=                                           94115
         ICM   R1,15,TCBUSER  GET LOCAL USER EXTENSION           94115
         BZ    NOUTCB                                            94115
         USING USERTCB,R1                                        94115
         TM    UTF1,X'80'+TUFZERO+TUFPASS (80-TUFSHORT) SPECIAL TASK?
         BNZ   MAINTEST      YES; PRIVY                          94115
NOUTCB   DS    0H                                                94115
         OI    PROGFLAG,PFSPEC                                  GP99162
.NOUTCB  ANOP  ,                                                GP99163
         EJECT
*    MAIN LOOP -- ENTERED TO GET OPERATOR REPLY, OR AUTOMATICALLY
*              IF IN UCB LOOKUP LOOP
*
MAINTEST TM    PROGFLAG,PFUCBLUP   IN UCB LOOKUP LOOP ?         GP99162
         BNZ   UNITLOOP      BUMP TO NEXT ENTRY AND CHECK        94115
MAINLOOP TM    PROGFLAG,PFSYSIN                                 GP99162
         BZ    MAINWTOR                                         GP99162
MAINREAD GET   INDCB,REPLY                                      GP99162
         LA    R3,MREP                                          GP99162
         LA    R2,L'MREP+L'REPLY                                GP99162
         BAS   R9,WRITE      ECHO THE INPUT REQUEST             GP99162
         CLI   REPLY,C'*'    COMMENT?                           GP99162
         BE    MAINREAD      YES; READ ANOTHER                  GP99162
         B     SCVCHK
         SPACE 1
*    MAX LENGTH IS 11, BUT ALLOW 16 SO OPERATOR WILL GET MORE
*    MEANINGFUL MESSAGE TO A TOO LONG REPLY
*
*    CHECK FOR FOLLOWING CONDITIONS:
*    LENGTH 3 - 'END' ?
*    LENGTH 8 - 'OVERRIDE, NONORIDE, OR TYPE,ALL'
*    LENGTH 11 - EXPECTED REPLY LENGTH
*    ALL ELSE IN ERROR
*
MAINWTOR MVI   ECB,0         RESET ECB                          GP99162
         MVC   REPLY,REPLY-1 BLANK OUT REPLY AREA                94115
         WTOR  MF=(E,NEXTW)   REMOTE WTOR
         WAIT  ,ECB=ECB      WAIT FOR REPLY
         OC    REPLY,BLNKS   CONVERT REPLY TO UPPER CASE         94115
         LA    R3,MREP                                          GP99162
         LA    R2,L'MREP+L'REPLY                                GP99162
         BAS   R9,WRITE      ECHO THE INPUT REQUEST             GP99162
SCVCHK   LA    R5,REPLY      POINT TO INPUT                      94115
         BLOOK T=COMMANDS,R=REPLY                                94115
         CLI   0(R5),C' '    ANY INPUT AT ALL ?                  94115
         BE    MAINTEST      NO; IGNORE                          94115
         MVC   REPLY,0(R5)   LEFT-JUSTIFY UNCANNED COMMAND       94115
         LA    R2,L'REPLY    MAX CHARS                           94115
         MIN   R2,=H'72',TYPE=H  ALLOW FOR SEQUENCE NUMBERS     GP99162
         LA    R1,REPLY(R2)  LAST REPLY ADDRESS
SCVRL    BCTR  R1,0          PREVIOUS POSITION                   94115
         CLI   0(R1),C' '    IS IT BLANK ?
         BNE   SCVRL1        NO, FOUND LENGTH
         BCT   R2,SCVRL      AND GO CHECK IT
SCVRL1   ST    R2,RL         REPLY LENGTH
         CLI   REPLY+4,C','  TYPE/VOLSER FORMAT ?                94115
         BNE   SCVNDVS       NO; FAIL (FOR NOW)                  94115
         CLC   =C'ANY',OPVOL  UNSPECIFIC REQUEST ?               94115
         BE    SCVSRCH       YES; SET SEARCH                     94115
         CLC   =C'ALL',OPVOL  ALL OF ONE TYPE ?                  94115
         BNE   SCVNF         NO, INCORRECT REPLY !
SCVSRCH  OI    PROGFLAG,PFUCBLUP   SET FLAG FOR AUTOMATIC UCB SCAN
         B     SCVTYPE       CONTINUE AT 'TYPE' ANALYSIS
         SPACE 1                                                 94115
SCVNDVS  CH    R2,=H'6'      POSSIBLY A VOLUME SERIAL ?          94115
         BH    SCVNES        NO; NOTHING ELSE, YET               94115
         SERVCALL UCBDK,REPLY  SEE IF ENTRY IS VALID VOL-SER     94115
         LTR   R7,R0         VALID UCB ?                         94115
         BZ    SCVNVS        SUPPOSED SERIAL NOT FOUND           94115
SCVMVS   MVC   REPLY(5),=C'DISK,'  FAKE IT                       94115
         MVC   REPLY+5(6),UCBVOLI-UCBOB(R7)  MOVE CURRENT SERIAL 94115
         B     UNITLONT      AND PROCESS IT                      94115
SCVNVS   CH    R2,=H'3'      POSSIBLE UNIT NAME ?                94115
         BNE   SCVNES        NO                                  94115
         SERVCALL UCBNM,REPLY  SEE IF DEFINED UNIT               94115
         LTR   R7,R0         UNIT FOUND ?                        94115
         BNZ   SCVMVS        YES; GO TO VERIFY                   94115
*
SCVNF    CH    R2,=H'11'     IS IT LENGTH 11 ?                   94115
         BNH   SCVTYPE       CORRECT LENGTH
SCVNES   WOT   ' REPLY NOT RECOGNIZED'                           94115
         LA    R3,SCVNES+8   SET TEXT                           GP99162
         LA    R2,21                                            GP99162
         BAS   R9,WRITE      ALSO TO SYSPRINT                   GP99162
         B     MAINTEST      GET ANOTHER REPLY                  GP99162
         SPACE 1                                                 94115
SETVER   MVI   NAMFG,1       VERIFY DSN BEFORE CHANGING
         B     MAINTEST                                         GP99163
         SPACE 1                                                 94115
SETNOVER MVI   NAMFG,0       NO VERIFY; LOOP THROUGH DSCBS
         B     MAINTEST                                         GP99163
         SPACE 1                                                 94115
SETLIST  LA    R0,SEQLIST    SET DEFAULT - LIST ONLY            GP99162
         ST    R0,@ACTION                                        94115
         MVC   YNACTION,YNACTLST                                GP99163
         B     MAINTEST      PRINT OPERATOR REPLY               GP99163
         SPACE 1                                                 94115
SETUNPRO MVC   YN+4(2),=C'UN'
         MVI   OVER,1        SET FOR OVERRIDE AND
         LA    R0,SEQNONE                                        94115
         B     SETACTN       PRINT OPERATOR REPLY
         SPACE 1                                                 94115
SETWPRO  MVC   YN+4(2),YN+3
         MVI   OVER,0        RESET OVERRIDE FLAG
         LA    R0,SEQRITE                                        94115
         B     SETACTN                                           94115
SETRPRO  MVC   YN+4(2),=C'RD'
         MVI   OVER,2        SET FOR READPSWD AND
         LA    R0,SEQREAD                                        94115
SETACTN  ST    R0,@ACTION                                        94115
         MVC   YNACTION,YNACTPRO                                GP99163
         B     MAINTEST      PRINT OPERATOR REPLY               GP99163
         SPACE 1                                                 94115
SETDSN   SLR   R2,R2         RESET LENGTH                        94115
         CLC   BLNKS,0(R5)   DSN= WITHOUT A NAME ?               94115
         BE    SETMSKLN      YES; RESET DSN COMPARE REQUEST      94115
         MVC   MASKNAME,0(R5)  MOVE ENTIRE USER'S INPUT          94115
         LA    R2,L'MASKNAME SET FULL LENGTH                     94115
SETMSKLN ST    R2,MASKLEN    SET NAME LENGTH                     94115
         B     MAINTEST      CONTINUE                            94115
SETMASK  MVC   MASKNAME,0(R5)  MOVE ENTIRE USER'S INPUT          94115
         LA    R2,L'MASKNAME SET MAX LENGTH                      94115
         LA    R1,MASKNAME(R2)  POINT PAST END                   94115
SETMASKL BCTR  R1,0                                              94115
         CLI   0(R1),C' '    TRAILING BLANK ?                    94115
         BNE   SETMSKLN                                          94115
         BCT   R2,SETMASKL                                       94115
         B     SETMSKLN      TURN OFF MASK SEARCH                94115
         SPACE 1                                                 94115
COMMANDS BTAB  'OVERRIDE ',SETUNPRO,BASE=*  UNPROTECT DSCBS      94115
         BTAB  'UNPROTECT ',SETUNPRO                             94115
         BTAB  'NOPASSWORD ',SETUNPRO                            94115
         BTAB  'NOPASS ',SETUNPRO                                94115
         BTAB  'NOPSWD ',SETUNPRO                                94115
         BTAB  'NONORIDE ',SETWPRO  WRITE PROTECT                94115
         BTAB  'PASSWORD ',SETWPRO                               94115
         BTAB  'PSWD ',SETWPRO                                   94115
         BTAB  'READPASS ',SETRPRO                               94115
         BTAB  'READPSWD ',SETRPRO                               94115
         BTAB  'DSNAMES ',SETVER    VERIFY DSN BEFORE CHANGING   94115
         BTAB  'VERIFY ',SETVER                                  94115
         BTAB  'NONAMES ',SETNOVER                               94115
         BTAB  'NOVERIFY ',SETNOVER                              94115
         BTAB  'NOVER ',SETNOVER                                 94115
         BTAB  'END ',DONE                                       94115
         BTAB  'EOJ ',DONE                                       94115
         BTAB  'QUIT ',DONE                                      94115
         BTAB  'LIST',SETLIST                                   GP99163
         BTAB  'MASK=',SETMASK                                   94115
         BTAB  'DSN=',SETDSN                                     94115
         BTAB  'DSNAME=',SETDSN                                  94115
         BTAB  *END                                              94115
         SPACE 2
*    AT THIS POINT CHECK THE VOLUME TYPE
*
SCVTYPE  LA    R14,UNITTAB   POINT TO TABLE OF UNITS             94115
         LA    R0,6          ENTRY LENGTH (FORMAT 2/4)           94115
         LA    R1,UNITTABX-6   LAST ENTRY                        94115
SCVTLOOP CLC   2(4,R14),TYPE  MATCHING TYPE ?                    94115
         BE    SCVTDONE                                          94115
         BXLE  R14,R0,SCVTLOOP  TRY AGAIN
SCVNET   WOT   ' INVALID DEVICE TYPE'                           GP99162
         LA    R3,SCVNET+8   SET TEXT                           GP99162
         LA    R2,20                                            GP99162
         BAS   R9,WRITE      ALSO TO SYSPRINT                   GP99162
         B     MAINTEST                                         GP99162
*
SCVTDONE MVC   VOLTYP+2(2),0(R14)  COPY TYPE FLAGS               94115
         EJECT
*    IF A UCB LOOKUP HAS BEEN REQUESTED THE PROGRAM WILL SCAN
*    ALL UCB ENTRIES FOR THE REQUIRED DEVICE TYPE WHICH IS ONLINE
*    AND MOUNTED.
*
SCVA     TM    PROGFLAG,PFUCBLUP   UCB LOOK UP REQUESTED ?      GP99162
         BZ    SCVB          NO                                 GP99162
         MVC   MREP+1(6),=C'UCBVOL'    DISTINGUISH FROM OPER. REPLY
         SLR   R7,R7         CLEAR UCB POINTER                   94115
         ST    R7,@UCB       CLEAR IN CASE OF FAILURE            94115
UNITLOOP TM    PROGFLAG,PFUCBLUP   AUTOMATIC LOOKUP ?            94115
         BZ    SCVNES        NO; VOLUME NOT FOUND                94115
         L     R1,@UCB       GET PREVIOUS UCB                    94115
         SERVCALL UCBUM      GET ADDRESS OF NEXT UCB             94115
         BXH   R15,R15,UCBDON   SKIP ON ERROR                    94115
         ST    R0,@UCB       SAVE FOR NEXT TIME                  94115
         LTR   R7,R0         CHECK RETURN                        94115
         BZ    UCBDON        DONE; FINISHED SCANNING             94115
         USING UCBOB,R7      DECLARE IT                          94115
         CLI   UCBTBYT3,UCB3DACC  DASD ?                         94115
         BNE   UNITLOOP      NO, SKIP                            94115
         CLI   VOLTYP+3,X'FF'   NON-SPECIFIC REQUEST?            94115
         BE    UNITLONT      YES; NO TYPE CHECK                  94115
         CLC   UCBTBYT4,VOLTYP+3  SAME SUBTYPE AS REQUEST ?      94115
         BNE   UNITLOOP      NO; SKIP                            94115
UNITLONT TM    UCBSTAT,UCBCHGS+UCBUNLD  GOING OFF OR UNLOADED?   94115
         BNZ   UNITLOOP      YES                                 94115
         TM    UCBSTAT,UCBONLI   REALLY ON-LINE ?                94115
         BZ    UNITLOOP
         CLC   UCBVOLI,BLNKS   HAS A VOL-SER BEEN STORED FOR IT ?
         BNH   UNITLOOP      NO, GET ANOTHER ONE
         MVC   OPVOL(6),UCBVOLI  STASH VOL-SER                   94115
         MVC   WTO+8(6),OPVOL LET OPERATOR KNOW WHICH DISK WE'RE DOING
WTO      WOT   'VOLSER'
         EJECT
*    THIS SECTION IS COMMON TO UCB LOOKUP AND REPLY ANAYSIS
*    A VOLUME SERIAL IS AVAILABLE, THE TYPE IS CORRECT
*
*    ALL OUR VOLUME SERIALS ARE 6 CHARACTERS LONG
*    REQUESTS WITH SHORTER ONES ARE REJECTED AT SCVNF, ONES
*    WITH IMBEDDED BLANKS ARE REJECTED HERE
*
*    THESE CHECKING FEATURES MAY BE ELIMINATED WITHOUT AFFECTING
*    CORRECT PROGRAM OPERATION - AN INCORRECT REPLY OR AN UNMOUNTED
*    OR UNREADY VOL-SER WILL RESULT IN A CAN'T MOUNT MESSAGE AND
*    THE PROGRAM WILL CONTINUE WITH THE NEXT VOL-SER.
*
SCVB     LA    R2,OPVOL      GET VOL-SER ADDRESS
         LA    R3,6          NO. OF CHAR'S TO BE CHECKED
SCVL     CLI   0(R2),C' '    IS IT A BLANK ?
         BE    SCVLB         YES - REJECT IT
         LA    R2,1(,R2)     BUMP FOR NEXT CHARACTER
         BCT   R3,SCVL       AND CHECK THAT
         MVC   VOLSER(6),OPVOL    MOVE VOL-SER INTO SCRATCH LIST
         B     FIND4         AND GET THE TYPE 4 DSCB
         SPACE 1
SCVLB    WOT   'INCORRECT VOL-SER'
         B     MAINTEST                                         GP99162
         SPACE 2
UCBDON   NI    PROGFLAG,255-PFUCBLUP   IF WE FELL THROUGH WE'RE DONE
         MVC   MREP+1(6),=C'REPLY:'    RESTORE MESSAGE
         B     MAINTEST      AND GET OPERATOR REPLY             GP99162
         EJECT
*    NOW FIND THE FIRST ( DSCB4 ) VTOC ENTRY
*
FIND4 OBTAIN   DSCB4
         B     *+4(15)       CHECK RETURN
         B     FOUND4        OK - BUT TAKEN FOR DSCB 1 ONLY
         B     NOMT          NOT MOUNTED
         B     FOUNDOOP  SAYS DSCB 4 NOT FOUND; BUT FOUND IT ANYWAY !
         NOP   0             I/O ERROR ?
         NOP   0             INVALID WORK-AREA ? COME ON ?
IOERR    WOT   'CAN''T READ DSCB'
 ORG IOERR *****DEBUG*****                                       94115
 DC H'0'   *****DEBUG*****                                       94115
         LA    R2,MNO4E-MNO4
         LA    R3,MNO4
         BALS  R9,WRITE                                          94115
         LA    R2,1
         LA    R3,SPACER     GIVE A DOUBLE SKIP
         BALS  R9,WRITE                                          94115
         B     CLOSE
*
NOMT     WOT   'CAN''T MOUNT VOLUME'
         LA    R2,MNOME-MNOM
         LA    R3,MNOM
         BALS  R9,WRITE                                          94115
         LA    R2,1
         LA    R3,SPACER     GIVE A DOUBLE SKIP
         BALS  R9,WRITE                                          94115
         B     CLOSE
*
FOUNDOOP CLI   WORK4,C'4'    IS IT TYPE 4 DSCB ANYWAY ?
         BNE   IOERR         IF NOT TYPE 4 IT'S A VALID 'NOT FOUND'
         MVC   ATTR(4),EXT4    GET VTOC LOW EXTENT ( CCHH )
         MVI   R,1           DSCB 4 IS AT RECORD 1
         B     FOUNDCOM
*
FOUND4   MVC   ATTR(5),DS4AD    USE CCHHR RETURNED BY OBTAIN
FOUNDCOM EQU   *
         MVC   DISK+DCBDDNAM-IHADCB(6),VOLSER                    94115
         MVC   DISK+DCBDDNAM-IHADCB+6(2),BLNKS                   94115
       RDJFCB  (DISK)
         MVC   JFCB2(44),DSN4  SET DS NAME FOR VTOC
         OI    JFCB2+52,X'40'
         TM    PROGFLAG,PFAUTH  AUTHORIZED?                     GP99162
         BNZ   OPENUP                                           GP99162
         OPEN  (DISK,(INPUT,REREAD)),TYPE=J                     GP99162
         B     OPENCOM                                          GP99162
OPENUP   OPEN  (DISK,(UPDAT)),TYPE=J                            GP99162
OPENCOM  TM    DISK+DCBOFLGS-IHADCB,DCBOFOPN  OPENED ?           94115
         BO    DSLOOP
         WOT   'NO DD CARD'
         LA    R2,MNODDE-MNODD
         LA    R3,MNODD
         BALS  R9,WRITE                                          94115
         B     WEOV
         EJECT
*    RETURN TO THIS POINT TO GET ANOTHER DS NAME
*
DSLOOP   SR    R2,R2
         IC    R2,R          GET PREVIOUS RECORD NUMBER
         LA    R2,1(,R2)     BUMP BY ONE
         STC   R2,R
         CLC   R(1),RMAX     IS IT HIGHER THAN FITS ON DEVICE ?
         BNH   FIND1         NO, CHECK AGAINST LAST DSCB 1
         MVI   R,1           YES, RESET RECORD NO. TO 1
         LH    R2,HH         GET TRACK NUMBER
         LA    R2,1(,R2)     AND BUMP BY ONE
         STH   R2,HH
         CH    R2,HHM        IS IT HIGHER THAN DEVICE PERMITS ?
         BNH   FIND1         NO, CHECK FOR VALID ADDRESS
         XC    HH(2),HH      YES, RESET TRACK TO 0
         LH    R2,CC         AND BUMP CYLINDER
         LA    R2,1(,R2)     NUMBER BY 1
         STH   R2,CC
FIND1    CLC   ATTR(5),HIDSCB  IS NEW ADDRESS BEYOND LAST TYPE 1 DSCB ?
*    CAUTION - IN NON-PCP ENVIRONMENT OR WITH SHARED DASD THIS
*    COULD HAVE CHANGED SINCE WE READ THE DSCB 4.
         BNH   OBT1          NO, GET THE DS NAME
*
WEOV     WOT   'EOV'         INDICATE THIS VOLUME IS DONE
         LA    R2,4
         LA    R3,EOV
         BALS  R9,WRITE                                          94115
         LA    R2,1
         LA    R3,SPACER     GIVE A DOUBLE SKIP
         BALS  R9,WRITE                                          94115
CLOSE    TM    DISK+DCBOFLGS-IHADCB,DCBOFOPN                     94115
         BZ    MAINTEST                                         GP99162
         CLOSE (DISK)
         B     MAINTEST                                         GP99162
         SPACE 2
OBT1  OBTAIN   DSCB1         SEEK THE DSCB AT THIS ADDRESS
         B     *+4(R15)
         B     OBOK          FOUND IT
         B     NOMT          VOLUME NOT MOUNTED ?
         B     IOERR         DSCB NOT FOUND ?
         B     IOERR         I/O ERROR
         B     IOERR         DSN PTR BAD
         B     WEOV          BEYOND VTOC ? END OF VOLUME
         EJECT
OBOK     CLI   FMTID,C'1'    FOUND A DSCB, IS IT TYPE 1 ?
         BNE   DSLOOP        NO, IGNORE
         CLI   DSN+DS1DSORG-IECSDSL1+1,0  VSAM OR OTHER FUNNY?  GP12045
         BNE   DSLOOP        YES; SKIP                          GP12045
         ICM   R15,15,MASKLEN   GET MASK LENGTH                  94115
         BNP   SELOK         IS THERE A MASK?                    94115
         LA    R14,MASKNAME  MASK (OR PREFIX) OR FULL DSN IF L=44
         LA    R1,DSN        NAME TO BE CHECKED                  94115
         BALS  R9,MASKCLC    GO TO COMPARE                       94115
         BNE   DSLOOP        NO MATCH; GET ANOTHER               94115
SELOK    MVC   MDSN+1(44),DSN  PLACE DS NAME IN MESSAGE AREA
         MVC   DDSN(44),DSN
         MVC   YN(3),YN-1    BLANK OUT 'NOT'
         MVC   EXP(12),EXP-1  AND 'EXP. DATE'
         L     R8,@ACTION    GET ADDRESS OF REQUESTED PROCESS    94115
         SPACE 1
         CLI   NAMFG,1       DISPLAY DS NAME ?
         BE    DISPLAY
*OLD*    CLC   DSN(5),=C'ATS.'
*OLD*    BE    READ
*OLD*    CLC   DSN(5),=C'SYS2.'  IS IT A SYSTEM DATA SET
*OLD*    BE    READ
*OLD*    CLC   DSN(5),=C'SYS1.'  IS IT A SYSTEM DATA SET
*OLD*    BNE   NSYSCHK       CHECK SPECIAL NON-SYS DATA SETS
*OLD*    CLI   OVER,1
*OLD*    BE    READ          NEED NOT CHECK FOR SPECIAL NAMES
         LA    R2,LIST
         LA    R3,LISTEND
OBLP     CLC   DSN(8),5(R2)  IS IT A SPECIAL DS ?                94115
         BE    EXPD          YES, SAY SO
         LA    R2,8(,R2)
         CR    R2,R3
         BL    OBLP
         B     READ
         SPACE 2
DISPLAY  LA    R2,L'DSN                                          94115
         LA    R3,DSN(R2)
DISLP    BCTR  R3,0
         CLI   0(R3),C' '
         BNE   DISW
         BCT   R2,DISLP
DISW     LA    R2,5(,R2)
         STH   R2,DISWTO
         LA    R1,DISWTO
         BALS  R9,WTOWOT                                         94115
DISQ     XC    ECB,ECB
         WTOR  'YES, OR N TO SKIP',RW,1,ECB
         WAIT  ,ECB=ECB
         L     R8,@ACTION    GET ADDRESS OF REQUESTED PROCESS    94115
         OI    RW,C' '
         CLI   RW,C'Y'
         BE    READ
         CLI   RW,C'N'
         BE    EXPD
         LA    R8,SEQNONE    SET FOR NO PROTECTION               94115
         CLI   RW,C'U'       UNPROTECT ?                         94115
         BE    READ                                              94115
         LA    R8,SEQREAD    SET FOR READ PROTECT                94115
         CLI   RW,C'R'                                           94115
         BE    READ                                              94115
         LA    R8,SEQRITE    ELSE TRY WRITE PROTECT              94115
         CLI   RW,C'W'                                           94115
         BE    READ                                              94115
         CLI   RW,C'Q'       QUIT THIS VOLUME ?                  94115
         BE    WEOV          FORCED QUIT                         94115
         B     DISQ          ELSE TRY AGAIN
         SPACE 3
NSYSCHK  LA    R2,NLIST
         LA    R3,NLISTEND
         SR    R1,R1
NSYSLP   IC    R1,0(,R2)
         EX    R1,NCLC
         BE    READ
         LA    R2,2(R1,R2)
         CR    R2,R3
         BL    NSYSLP
         B     NOTSYS
         EJECT
READ     DS    0H
         XC    RDECB,RDECB
         XDAP  RDECB,RK,DISK,DDDAT,96,(DDSN,44),MBB
         WAIT  ,ECB=RDECB
         CLI   RDECB,X'7F'
         BNE   PRERR
         CLC   DDSN(140),DSN
         BNE   PRERR
         BALS  R14,FORMSTAT  FORMAT CURRENT PROTECT STATUS       94115
         STC   R0,STATUS     STASH IT                            94115
         TM    PROGFLAG,PFSPEC                                  GP99162
         BZ    SETOFF                                            94115
*WHY     TM    DDSN+93,X'14'
*NOT     BZ    RITT
         NI    TIME+6,X'0F'
         NI    TIME+9,X'0F'
         NI    DATE+9,X'0F'
         NI    DATE+6,X'0F'
         SR    R0,R0
         SR    R1,R1
         SR    R2,R2
         SR    R3,R3
         IC    R0,DATE+9
         IC    R1,DATE+6
         IC    R2,TIME+6
         IC    R3,TIME+9
         LA    R1,0(R1,R2)
         LA    R1,0(R1,R3)
         AR    R1,R0
         SR    R0,R0
         D     R0,=F'17'
         LR    R2,R0
         SLL   R2,1
         LH    R1,TAB(R2)
         SLL   R1,12
         LA    R15,R15
         B     BB
TAB      DS    0H
         DC    X'806'
         DC    X'937'
         DC    X'140'
         DC    X'122'
         DC    X'422'
         DC    X'0F3'
         DC    X'100'
         DC    X'0C2'
         DC    X'0F1'
         DC    X'C13'
         DC    X'113'
         DC    X'106'
         DC    X'D37'
         DC    X'0B0'
         DC    X'001'
         DC    X'80A'
         DC    X'913'
         SPACE 1                                                 94115
SETOFF   MVC   DB(1),DDSN+93  SAVE ORIGINAL PROTECT BYTE         94115
         USING ACTSECT,R8                                        94115
         LH    R15,ACLEN     GET LENGTH (-1) OF ACTION ITEM      94115
         EX    R15,ACEXEC    GET EXEC FOR ACTION                 94115
         EX    R15,ACEXOR    AND ADD IN REQUESTED BITS           94115
         DROP  R8                                                94115
         CLC   DB(1),DDSN+93  WAS ANYTHING CHANGED ?             94115
         BE    RITT          NO; DON'T BOTHER                    94115
STASH    DS    0H
         XC    WTECB,WTECB
         XDAP  WTECB,WK,DISK,DDDAT,96,(DDSN,44),MBB
         WAIT  ,ECB=WTECB
         CLI   WTECB,X'7F'
         BNE   PRERR
RITT     BALS  R14,FORMSTAT  FORMAT CURRENT PROTECTION STATUS    94115
         STC   R0,STATUS+1                                       94115
         B     SCRT          SCRATCHED
         SPACE 1                                                 94115
FORMSTAT ICM   R0,15,=C'?WRU'   SET: UNPROTECT;READ PROTECT;READ/WRITE
         TM    DDSN+93,X'14'  ANY PROTECTION AT ALL ?            94115
         BZR   R14           NO; RETURN UNPROTECTED              94115
         SRL   R0,8          SET TO READ PROTECT                 94115
         BOR   R14           AND RETURN                          94115
         SRL   R0,8          SET TO WRITE PROTECT                94115
         TM    DDSN+93,X'10'  WRITE PROTECTED ?                  94115
         BNZR  R14           YES                                 94115
         SRL   R0,8          SET TO BAD STATUS                   94115
         BR    R14                                               94115
         SPACE 1                                                 94115
MASKCLC  CMASK L=R15         USE MASK LENGTH AS INPUT LENGTH     94115
         EJECT
*
NOTSYS   MVC   EXP(9),=C'NOT SYS1.'
         B     NOTS
EXPD     MVC   EXP(9),=C'- SPECIAL'
         B     NOTS
PRERR    MVC   EXP(9),=C'PROC. ERR'
NOTS     MVC   YN(3),=C'NOT'
SCRT     LA    R2,MDSNE-MDSN
         LA    R3,MDSN
         BALS  R9,WRITE                                          94115
         B     DSLOOP
         SPACE 3
DONE     WOT   ' '                                              GP99162
         WOT   'ALL DONE...'
         WOT   ' '
         CLOSE (OUT,,INDCB,,DISK)                               GP99162
         SR    R9,R9         ZERO STEP CONDITION CODE            94115
         B     EXIT
*
BB       ABEND (1)
WTOWOT   TM    PROGFLAG,PFSYSIN+PFPRINT  IN AND OUT BATCH?      GP99162
         BOR   R9            IF SO, SKIP WTO                    GP99162
         SVC   35                                               GP99162
WTORET   BR    R9                                                94115
EXIT16   LA    R9,16         CONDITION CODE 16 - NO PRINTER AVAILABLE
EXIT     SERVTERM ,          CLEAR THE SERVICE ROUTINE           94115
         PGMEXIT RC=(R9)                                         94115
         EJECT
*    SUBROUTINE TO OUTPUT TO PRINT FILE
*
*    REG 2 HAS LENGTH OF MESSAGE, REG 3 HAS ADDRESS
*
WRITE    LH    R4,LINENO     GET CURRENT LINE NO.
         LA    R4,2(,R4)     BUMP BY 2
         STH   R4,LINENO
         CH    R4,=H'54'     NEED A PAGE SKIP ?
         BL    WRT           NO, OUTPUT
         XC    LINENO(2),LINENO        RESET LINE NO.
         PUT   OUT
         MVI   0(R1),C' '
         MVC   1(120,R1),0(R1)
         MVC   0(HEADE-HEAD,R1),HEAD   MOVE IN PAGE HEADER
         LH    R4,PAGENO
         LA    R4,1(,R4)     GET NEW PAGE NUMBER
         STH   R4,PAGENO
         CVD   4,DB
         ED    PAGE-HEAD(5,1),DB+6     AND OUT IT IN HEADER
         PUT   OUT
         MVI   0(R1),C' '
         MVC   1(120,R1),0(R1)
         MVC   0(SUBHEDE-SUBHED,R1),SUBHED   PUT IN SUB HEADER
WRT      PUT   OUT
         MVI   0(R1),C' '
         MVC   1(120,R1),0(R1)  BLANK OUT RECORD
         BCTR  R2,0
         EX    R2,WRITMVC
WRITRET  BR    R9           AND RETURN                           94115
WRITMVC  MVC   0(0,R1),0(R3)   MOVE RECORD TO OUTPUT AREA
         EJECT
*    LISTS FOR OBTAIN AND SCRATCH
*
NEXTW    WTOR  'NEXT VOLUME TO BE PROTECTED:',REPLY,L'REPLY,ECB,MF=L
ECB      DC    F'0'
MREP     DC    C'0REPLY: '
REPLY    DC    CL80' '                                           94115
MREPE    EQU   *
BLNKS    DC    (L'REPLY)C' ' 80 BLANKS                           94115
RL       DC    F'11'
TYPE     EQU   REPLY
OPVOL    EQU   REPLY+5
MNO4     DC    C'0CAN''T READ DSCB'
MNO4E    EQU   *
EOV      DC    C'0EOV'
SPACER   EQU   *             USE FOR A DOUBLE SKIP ONLY
MNOM     DC    C'0CAN''T MOUNT VOLUME'
MNOME    EQU   *
MDSN     DC    CL48'0'
YN       DC    CL6' '                                           GP99163
YNACTION DC    C'PROTECTED',CL3'   '                            GP99163
EXP      DC    CL9' '
         DC    C' '
STATUS   DC    CL2' '
MDSNE    EQU   *
YNACTLST DC    CL(L'YNACTION)'LIST'                             GP99163
YNACTPRO DC    CL(L'YNACTION)'PROTECTED'                        GP99163
HEAD     DC    CL70'1PROTECT FUNCTION',C'RUN ON'
DATE     DC    X'40202120612020612020',C'  AT'
TIME     DC    X'402021204B20204B2020',C'   PAGE'
PAGE     DC    X'40202120'
HEADE    EQU   *
SUBHED   DC    CL50'        TYPE VOL-SER / SYSTEM DATA SET NAME'
         DC    C'ACTION TAKEN'
SUBHEDE  EQU   *
MNODD    DC    C'0NO DD CARD FOR THE ABOVE VOLUME'
MNODDE   EQU   *
         DS    0F
FUNNY    DC    AL2(FUNNYE-*,0)   REMOTE WTO LIST
         DC    C'INPUT IS: '
         DC    X'9940A7A76B407DA3A897856BA59693A285997D5E40894B854B'
* THAT IS:       R   X X ,   ' T Y P E , V O L S E R ' ;   E . G . ,
         DC    C' R 00,''2311,OSRES4'''
FUNNYE   EQU   *
MTAB     DC    H'0,31,59,90,120,151,181,212,243,273,304,334,365'
         DS    0F
EXLST    DC    X'87',AL3(JFCB2)
JFCB2    DS    CL176
DISWTO   DC    A(0)
DDSN     DS    CL44
DDDAT    DS    CL96
OVER     DC    X'0'          OVERRIDE FLAG
NAMFG    DC    X'0'
RW       DS    C
JULDAT   DC    CL6' '        JULIAN DATE
LIST     EQU   *
         DC    CL8'UT1'
         DC    CL8'UT2'
         DC    CL8'UT3'
LISTEND  EQU   *
NCLC     CLC   DSN(1),1(2)
NLIST    DC    AL1(7),C'SYSCTLG '
         DC    AL1(8),C'PASSWORD '
         DC    AL1(8),C'GPJOBJCL '
NLISTEND EQU   *
*
         PRINT &PRTSYS                                           94115
OUT      DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PL),RECFM=FA,LRECL=121, X
               EROPT=ACC                                        GP99162
INDCB    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GM),LRECL=80,EROPT=ACC,    X
               EXLST=EXLST,EODAD=DONE
DISK     DCB   MACRF=(E),EXLST=EXLST,EODAD=WEOV,DSORG=PS,DEVD=DA,      *
               DDNAME=A                                          94115
         SPACE 2                                                 94115
         PRINT &PRTMAC                                           94115
UNITTAB  UNITY FORMAT=24,DEVD=DA                                 94115
         DC    X'20FF',C'DASD'   GENERAL MATCH REQUEST           94115
         DC    X'20FF',C'DISK'   DITTO                           94115
UNITTABX EQU   *                                                 94115
PATDSCB4 CAMLST SEARCH,1,2,3 DSN4,VOLSER,WORK4                   94115
         ORG   PATDSCB4+4                                        94115
PATDSCB1 CAMLST SEEK,1,2,3   ATTR,VOLSER,DSN                     94115
         ORG   PATDSCB1+4                                        94115
SEQREAD  DC    Y(0)                                              94115
         NI    DDSN+93,255-X'14'  RESET ALL PROTECT BITS         94115
         DC    Y(0)                 PAD TO 6 BYTES               94115
         OI    DDSN+93,X'14'      SET READ PROTECT               94115
SEQRITE  DC    Y(0)                                              94115
         NI    DDSN+93,255-X'14'  RESET ALL PROTECT BITS         94115
         DC    Y(0)                 PAD TO 6 BYTES               94115
         OI    DDSN+93,X'10'      SET WRITE PROTECT              94115
SEQNONE  DC    Y(0)                                              94115
         NI    DDSN+93,255-X'14'  RESET ALL PROTECT BITS         94115
         DC    Y(0)                 PAD TO 6 BYTES               94115
         OI    DDSN+93,X'00'      SET NO PROTECT                 94115
SEQLIST  DC    Y(0)                                              94115
         OI    DDSN+93,X'00'      DEFAULT - DO NOTHING           94115
         DC    Y(0)                 PAD TO 6 BYTES               94115
         OI    DDSN+93,X'00'      DEFAULT - DO NOTHING           94115
         SPACE 2                                                 94115
SAVE     DSECT ,             SAVE/WORK AREA                      94115
@SERVICE DS    A             SERVICE ROUTINE                     94115
@PRINTER DS    A             PRINTER SERVICE                     94115
@INPREAD DS    A             READER SERVICE                     GP99162
@TRACE   DS    A             PGM TRACE FACILITY                 GP99162
@UCB     DS    A             CURRENT UCB                         94115
@ACTION  DS    A             ADDRESS OF 'SEQXXXX' ACTION SET     94115
DSCB4  CAMLST  SEARCH,DSN4,VOLSER,WORK4
DSCB1  CAMLST  SEEK,ATTR,VOLSER,DSN
         SPACE 1                                                 94115
MASKLEN  DC    F'0'          LENGTH OF SCAN MASK                 94115
MASKNAME DC    CL44' '       MASK (OR FULL DSN)                  94115
         SPACE 1
*    THE FOLLOWING CONSTANTS AND AREAS ARE IN A SEQUENCE TO FORCE
*       BOUNDARIES AND TO SHARE WORK AREAS - DO NOT ALTER THIS SEQUENCE
*
PAGENO   DC    H'0'          CURRENT OUTPUT PAGE NUMBER
LINENO   DC    H'55'   LINE COUNTER, SET FOR INIT. PAGE EJECT
         DS    CL5           ALIGNMENT SPACER
MBB      DC    XL3'0'
ATTR     DS    CL5' '        CCHHR OF THIS/NEXT DSCB
CC       EQU   ATTR            CYLINDER
HH       EQU   ATTR+2            TRACK
R        EQU   ATTR+4              RECORD
PROGFLAG DC    X'00'         PROCESSING FLAG                    GP99162
PFUCBLUP EQU   X'80'           LOOP THROUGH UCBS                GP99162
PFSYSIN  EQU   X'40'           INPUT FROM SYSIN RATHER THAN CONSOLE
PFSPEC   EQU   X'20'           INPUT FROM CANNED JOB LIBRARY    GP99162
PFAUTH   EQU   X'04'           RUNNING AUTHORIZED               GP99162
PFPRINT  EQU   X'01'           OUTPUT TO SYSPRINT INSTEAD OF WTO
VOLIST   DC    H'1'          NUMBER OF VOLUMES DATA SET RESIDES ON
VOLTYP   DC    XL4'0'        TYPE OF RESIDENCE VOLUME
VOLSER   DC    CL6' '        VOLUME SERIAL NUMBER
VOLSEQ   DC    H'0'          SEQUENCE OF VOL. FOR DS / RETURN CODE
DSN4     DC    XL44'0'       DS NAME FOR TYPE 4 DSCB             94115
WORK4    DC    96X'0'        DSCB 4 DATA / WORK AREA
WORK     EQU   WORK4
HIDSCB   EQU   WORK4+1       CCHHR OF HIGHEST DSCB 1
HHM      EQU   WORK4+20      HH MAX PER CC
RMAX     EQU   WORK4+30      MAX DSCB'S PER TRACK
EXT4     EQU   WORK4+63      CCHH OF FIRST EXTENT OF VTOC
DS4AD    DC    5X'0'         CCHHR OF DSCB 4
DSN      DC    CL44' '       D.S. NAME
FMTID    DS    CL96          DSCB DATA AREA
DB       DS    33D           WORK AREA
*
*    END OF FIXED SEQUENCE
*
SAVEEND  EQU   *                                                 94115
         SPACE 1                                                 94115
ACTSECT  DSECT ,             MAP OF ACTION REQUEST               94115
ACLEN    DS    H             LENGTH(-1) FOR EXECUTE              94115
ACEXEC   DS    CL6           FIRST EXECUTED INSTRUCTION          94115
ACEXOR   DS    CL6           LAST EXECUTED INSTRUCTION           94115
         EJECT
         PRINT &PRTSYS                                           94115
         CVT   DSECT=YES                                         94115
         IHACDE ,                                                94115
         IEFUCBOB ,                                              94115
         IECSDSL1 1                                              94115
         IECSDSL1 4                                              94115
         DCBD  DEVD=DA,DSORG=PS                                  94115
         AIF   (NOT &MVSXA).NOLO                                 94115
         IKJTCB ,                                                94115
         IHAPSA ,                                                94115
         AIF   ('&LOCAL' NE 'PID').NOLO                          94115
USERTCB  USERTCB ,                                               94115
.NOLO    END
