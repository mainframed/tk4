MIGR     TITLE 'M I G R A T E  ***  WYLBUR DATA SET MIGRATION PROGRAM'
         COPY  OPTIONGB                                          81360
         SPACE 1                                                 81360
         SYSPARM LIST=YES                                        81360
         SPACE 1                                                 81360
***********************************************************************
*                                                                     *
*        COPYRIGHT 1981  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,                                                 81360
         PRINT &PRTSOR                                           81360
MIGRATE  SAVEM ZERO12,BASE=R11,BASE2=R12,PARM=R9                 81360
         SPACE 1                                                 81360
         SERVINIT ,          LOAD THE SERVICE ROUTINE            81360
         SERVCALL LPALD,=CL8'@PRINTER'  LOAD THE PRINT ROUTINE   81360
         ST    R0,@PRINTER                                       81360
         SERVCALL LPALD,=CL8'@OBTAINS'  LOAD THE VTOC READER     81360
         ST    R0,@OBTAIN                                        81360
         L     R0,=A(100*1024)  STORAGE RESERVE SIZE AFTER @OBTAINS
         @OBTAIN (R0),OPT=SIZE   SET RESERVED STORAGE            81360
         PRTOPEN SYSPRINT,OPT=(ABEND)  1403 ABEND IF NO SYSPRINT 81360
         PRTOPEN ERRPRINT,OPT=(ABEND),DEV=2  DITTO               81360
         PRTV  HEADER1,TITLE=1  MAKE TITLES                      81360
         PRTV  ERRHEAD1,TITLE=1,DEV=2                            81360
         PRTV  HEADER2,TITLE=3                                   81360
         PRTV  ERRHEAD3,TITLE=3,DEV=2                            81360
         SPACE 1                                                 81360
         LA    R9,0(,R9)     CLEAN PARM POINTER                  81360
         LTR   R9,R9         ANY ?                               81360
         BZ    NOPARM        NO; ERROR                           81360
         ICM   R9,7,1(R9)    GET PARM FIELD                      81360
         BZ    NOPARM        NONE ?                              81360
         XR    R8,R8                                             81360
         ICM   R8,3,0(R9)    GET PARM LENGTH                     81360
         BNP   NOPARM        NONE ?                              81360
         CH    R8,=H'5'      AT LEAST MINIMUM LENGTH ?           81360
         BL    NOPARM
         MVC   DB(5),2(R9)   GET PARM                            81360
         NC    DB(5),=5C'0'  AND                                 81360
         CLC   DB(5),=5C'0'  NUMERIC ?                           81360
         BNE   NOPARM
         MVC   SLETDATE(2),2(R9)                                 81360
         MVC   SLETDATE+3(3),4(R9)                               81360
         SH    R8,=H'7'                                          81360
         BM    DONEPARM                                          81360
         EX    R8,PARMMOVE                                       81360
         STC   R8,CLCPARM+1                                      81360
         CLI   DSNMASK,C':'   GROUP REQUEST ?                    80205
         BNE   DONEPARM      NO                                  81360
         MVI   DSNMASK,X'FE'  SET GROUP INDICATOR                80205
         LA    R14,GROUPTAB                                      80205
         LA    R15,(GROUPEND-GROUPTAB)/(GROUPTAK-GROUPTAB)       80205
GROUPLUP CLC   DSNMASK+1(8),0(R14)  MATCHING ENTRY               80205
         BE    GROUPGOT      YES                                 80205
         LA    R14,GROUPTAK-GROUPTAB(,R14)                       80205
         BCT   R15,GROUPLUP                                      80205
         LA    R3,BADGRUP                                        81360
         B     FASTQUIT                                          81360
NOPARM   LA    R3,BADPARM                                        81360
FASTQUIT PRTV  (R3),DEV=2    WRITE ERROR MESSAGE                 81360
         SERVTERM ,                                              81360
         ABEND 666                                               81360
BADGRUP  VCON  '0REQUESTED GROUP NOT FOUND'                      81360
BADPARM  VCON  '0MISSING OR INVALID PARM FIELD'                  81360
PARMMOVE MVC   DSNMASK(0),8(R9)                                  81360
         SPACE 1                                                 81360
GROUPGOT MVC   DSNMASK+4(8),8(R14)  COPY GROUP ADDRESSES         80205
DONEPARM L     R1,CVTPTR     CVT POINTER                         81360
         USING CVTMAP,R1                                         81360
         MVC   DATE,CVTDATE  GET DATE FOR LATER                  80205
         L     R15,CVTSYSAD  GET SYSRES UCB                      80205
         MVC   SYSCVOL,UCBVOLI-UCBOB(R15)  COPY VOLSER           80205
         PRTV  HEADER1,TITLE=1  MAKE TITLES                      81360
         PRTV  HEADER2,TITLE=3                                   81360
         LA    R2,2                                              81360
         LA    R3,OUTDCB                                         81360
         LR    R8,R3         SAVE OUTDCB ADDRESS                 81360
         USING IHADCB,R3                                         81360
         SPACE 1
DEVTYPE  DEVTYPE DCBDDNAM,DB
         BXLE  R15,R15,DEVNEXT   GET NEXT ONE IF THERE           *GPP*
MISSDD   PRTLIST MGMISDD,DEV=2    WRITE MISSING/BAD DD           81360
         B     ABEND400                                          81360
MGMISDD  FDOPT NL,CC=C'0'                                        81360
         FDPRT 'REQUIRED DD CARD FOR',PAD                        81360
         FDPRT DCBDDNAM,DEB,PAD                                  81360
         FDPRT 'IS MISSING OR BAD',PAD                           81360
         FDPRT *END                                              81360
         SPACE 1                                                 81360
DEVNEXT  CLI   DB+2,UCB3DACC   DASD ?                            81360
         BNE   MISSDD        NO; ERROR                           81360
         LA    R3,INPDCB     REPEAT FOR INPUT DD                 81360
         BCT   R2,DEVTYPE
         USING TIOENTRY,R5   DECLARE TIOT ENTRY                  81360
         USING UCBOB,R4      DECLARE THE UCB                     81360
         LR    R3,R8         SET FOR OUTDCB                      81360
         SERVCALL TIODD,DCBDDNAM   FIND IT                       81360
         LTR   R5,R0         TIOT ENTRY FOUND ?                  81360
         BZ    MISSDD        NO; TOO BAD                         81360
         ICM   R4,7,TIOEFSRT   GET THE UCB ADDRESS               81360
         BZ    MISSDD        TOO BAD ?                           81360
         MVC   TO(6),UCBVOLI
         MVC   NEWVOLS,UCBVOLI  COPY VOL-SER TO RECAT
         MVC   NEWMASK,UCBTBYT1  MOVE DEVTYPE
         NI    NEWMASK+1,255-UCBRR-UCBVLPWR-UCBDVPWR  FIX FOR CATLG
         CLI   NEWMASK+3,X'0A'   3340 ?                          81360
         BNE   *+8           NO                                  81360
         NI    NEWMASK+1,255-UCBRPS                              81360
DDSEARCH LA    R3,INPDCB     NOW SET FOR INPUT DCB               81360
         SERVCALL TIODD,DCBDDNAM-IHADCB+INPDCB  GET INPUT DD     81360
         LTR   R5,R0         FOUND ?                             81360
         BZ    MISSDD        NO; ERROR                           81360
         ICM   R4,7,TIOEFSRT  GET THE UCB ADDRESS                81360
         BZ    MISSDD        SHOULD NOT HAVE GOTTEN HERE ?       81360
         MVC   VOLSER,UCBVOLI                                    81360
         MVC   FROM(6),UCBVOLI                                   81360
         MVC   OLDCCHHR+4(1),UCBVTOC+2  SAVE RECORD NUMBER OF FMT 4
         MVC   VMASK,UCBTBYT1  MOVE DEVTYPE                      81360
         NI    VMASK+1,255-UCBRR-UCBVLPWR-UCBDVPWR  MAKE CAT TYPE
         CLI   VMASK+3,X'0A'   3340 ?                            81360
         BNE   *+8           NO                                  81360
         NI    VMASK+1,255-UCBRPS                                81360
         PRTV  HEADER1,TITLE=1  MAKE TITLES                      81360
         PRTV  HEADER2,TITLE=3                                   81360
         EJECT ,                                                 81360
OBTAIN4  @OBTAIN FORMAT4,OPT=LOADED                              81360
         B     *+4(R15)
         B     FOUND4
         B     NOTFOUND      VOLUME NOT MOUNTED
         B     FOUND4        NO BUT YES
         B     NOTFOUND      I/O ERROR
         B     NOTFOUND      BAD WORKAREA
         B     NOTFOUND      ?????????
         SPACE 2
FOUND4   CLI   DS4IDFMT,C'4' REALLY A FORMAT 4 ?
         BNE   NOTFOUND      NO - SAY SO AND EXIT
         MVI   DS4IDFMT,0    CLOBBER FOR RE-SEARCH
         MVC   OLDCCHHR(4),DS4VTOCE+2  VTOC LOW EXTENT FOR 1ST IO
         SPACE
DSCBBUMP MVC   CCHHR(5),OLDCCHHR RESTORE LAST DSCB CCHHR
         XR    R2,R2                                             81360
         IC    R2,R                   LAST RECORD NUMBER         81360
         LA    R2,1(,R2)     BUMP                                81360
         STC   R2,R          SET FOR NEXT RECORD                 81360
         CLM   R2,1,DS4DEVDT   HIGHER THAN FITS ON DEVICE ?      81360
         BNH   TESTCCHH      NO, SEE IF HIGHER THAN LAST DSCB1
         MVI   R,1           YES, START WITH RECORD 1            81360
         LH    R2,HH         TRACK NO.                           81360
         LA    R2,1(,R2)                                         81360
         STH   R2,HH                                             81360
         CLM   R2,3,DS4DEVSZ+2   SAME CYLINDER ?                 81360
         BL    TESTCCHH      YES
         STCM  R2,12,HH      NO; RESET TRACK TO ZERO             81360
         LH    R2,CC         CYLINDER                            81360
         LA    R2,1(,R2)                                         81360
         STH   R2,CC                                             81360
TESTCCHH CLC   CCHHR(5),DS4HPCHR   PAST LAST DSCB 1 ?            81360
         BNH   GOOBT         NO; CONTINUE                        *GPP*
LOOPWYL  CLI   DCBDDNAM-IHADCB+INPDCB+5,C'9'  LAST ONE DONE ?    81360
         BNL   EXIT          YES; EXIT                           *GPP*
         IC    R1,DCBDDNAM-IHADCB+INPDCB+5  GET NUMBER           81360
         LA    R1,1(,R1)     UP BY ONE                           *GPP*
         STC   R1,DCBDDNAM-IHADCB+INPDCB+5  SET BACK             81360
         STC   R1,DCBDDNAM-IHADCB+DUMMYI+5  SET BACK             *GPP*
         DEVTYPE DCBDDNAM-IHADCB+INPDCB,DB  DOES DD CARD EXIST ? 81360
         BXLE  R15,R15,DDSEARCH   YES - REINITIALIZE NEXT VOLUME *GPP*
         B     LOOPWYL       ELSE TRY AGAIN                      *GPP*
         EJECT ,                                                 81360
GOOBT   @OBTAIN FORMAT1
         MVC   OLDCCHHR,CCHHR SAVE FOR LOOP
         BXH   R15,R15,DSCBIOER    TOO BAD ?                     81360
         CLI   DS1FMTID,C'1' FORMAT 1 ?
         BNE   DSCBBUMP      NO; GET ANOTHER                     81360
         CLI   DS1FMTID,X'FF'   @OBTAINS END OF VOLUME SIGNAL ?  81360
         BE    LOOPWYL       YES; QUIT                           81360
         CLI   DSNMASK,X'FE' HAVE A MASK ?
         BH    SYSCHECK      NO; SEE IF ONE OF OURS              80205
         BL    CLCPARM       YES; GO TO COMPARE DSN              80205
         LM    R14,R15,DSNMASK+4   GET GROUP START/END ADDRESS   80205
         LA    R14,MIGRATE(R14)  RELOCATE AND CLEAN              81360
         LA    R15,MIGRATE(R15)     DITTO                        81360
LUPGROUP CLC   DS1DSNAM+4(4),0(R14)  IN RANGE ?                  80205
         BL    BUMGROUP      NO                                  80205
         CLC   DS1DSNAM+4(4),4(R14)  IN RANGE ?                  80205
         BNH   SYSCD         YES; PROCESS                        80205
BUMGROUP LA    R14,8(,R14)                                       80205
         CR    R14,R15                                           80205
         BL    LUPGROUP      TRY NEXT ENTRY                      80205
         B     DSCBBUMP      ELSE SKIP IT                        80205
CLCPARM  CLC   DS1DSNAM,DSNMASK
         BNE   DSCBBUMP
         B     SYSCD
SYSCHECK CLC   DS1DSNAM(8),=C'WYL.Z904'
         BE    DSCBBUMP
SYSCD    BAL   R10,USECRD
         XC    TRKNUM,TRKNUM                                     81360
         CLC   ADATE,SLETDATE
         BH    DSCBBUMP
CKLSTAR  CLC   =C'ACTIVE ',DS1DSNAM+22   SPECIAL DSN ?           80205
         BNE   CKLSTAR2                                          80205
         CLC   =C'WYL00',FROM  FROM A WYLBUR VOLUME ?            80205
         BE    DSCBBUMP      YES - SKIP IT
CKLSTAR2 MVC   BADNO(12),=CL12'BAD DSORG'                        80205
         CLI   DS1DSORG+1,0       PDS ?                         GP09186
         BNE   NOTCOPID      NO; IGNORE                         GP09186
         TM    DS1DSORG,JFCORGPO+JFCORGPS  PS/PO DATASET ?       81360
         BZ    NOTCOPID      NO; SHOW NOT COPIED                 81360
         MVC   BADNO(9),=C' NO LSTAR'
         CLC   DS1LSTAR(7),ZEROES  EVER USED OR IN USE NOW ?    GP09186
         BE    NOTCOPID      NO - SKIP IT                        81360
         MVC   BADNO(9),=C'PSWD/RACF'                            80205
         TM    DS1DSIND,DS1IND40+DS1IND10  RACF OR PSWD PROTECTED ?
         BNZ   NOTCOPID                                          81360
         TM    DS1DSORG,JFCORGPS   SEQUENTIAL ?                  81360
         BNZ   *+14                                              80205
         MVC   BADNO(9),=CL9'   PDS'                             80205
         B     NOTCOPID                                          81360
         EJECT ,                                                 81360
ALLRETRY RDJFCB MF=(E,JFCBLIST)                                  80205
         BXH   R15,R15,ABEND400      CHANGE LATER FOR WTO        81360
         LM    R6,R7,JF1     INPDCB/OUTDCB JFCB ADDRESSES        81360
         OI    JFCBTSDM-INFMJFCB(R6),JFCNWRIT                    81360
         OI    JFCBTSDM-INFMJFCB(R7),JFCNWRIT                    81360
         MVC   JFCBDSNM-INFMJFCB(L'JFCBDSNM,R6),DS1DSNAM         81360
         MVC   JFCBDSNM-INFMJFCB(L'JFCBDSNM,R7),DS1DSNAM         81360
         MVC   JFCRECFM-INFMJFCB(1,R7),DS1RECFM                  81360
         MVC   JFCBLKSI-INFMJFCB(4,R7),DS1BLKL  COPY SIZES       81360
*  IF DATA SET IS VARIABLE - DO NOT PROCESS IT  *
         MVC   BADNO+1,=C'NO '                                   80205
         MVC   REASON,=CL8'RECFM'                                80205
         TM    DS1RECFM,JFCUND      IS IT U TYPE ?               81360
         BO    $UFORMAT        YES - PROCESS U TYPE
         BZ    NOTCOPID       NO - GET NEXT DSCB                 81360
         MVC   BADNO,=C'BAD'                                     80205
         TM    DS1RECFM,JFCFIX        IS IT FIXED ?              81360
         BZ    NOTCOPID      NO; PRINT AND SKIP                  81360
         MVC   REASON,=CL8'LRECL'                                80205
         XR    R8,R8                *        FOR DIVIDE
         LH    R5,DS1LRECL            DIVISOR - LRECL
         LTR   R5,R5            TEST FOR POS
         BNP   NOTCOPID             NOT - POS  BUMP              81360
         MVC   REASON,=CL8'BLKSIZE'                              80205
         LH    R9,DS1BLKL         DIVIDEND - BLKSI
         LTR   R9,R9
         BNP   NOTCOPID                                          81360
         MVC   REASON,=CL8'DCB'                                  80205
         DR    R8,R5        DIVIDE LRECL INTO BLKSI
         LTR   R8,R8           IS REMAINDER ZERO ?
         BNZ   NOTCOPID              NO - PRINT FILE NAME        81360
         SH    R9,=H'1'      CHECK QUOTIENT                      81360
         BM    NOTCOPID      SHOULDN'T HAVE GOTTEN HERE          81360
         BZ    *+12          UNBLOCKED OK                        81360
         TM    DS1RECFM,JFCRFB  FB REQUESTED ?                   81360
         BZ    NOTCOPID      NO; ERROR                           81360
         B     KILLEDIT           OK - ALLOCATE DS               81360
         SPACE 2                                                 81360
$UFORMAT OI    FLAG,EDIT
         XR    R9,R9
         IC    R9,NEWMASK+3
         SLL   R9,1
         LH    R9,NEWDEVTB(R9)
         LTR   R9,R9
         BZ    NODEVICE
         CH    R9,DS1BLKL
         BE    KILLEDIT                                          81360
         CH    R9,MAXBLKSI
         BH    KILLEDIT                                          81360
         CLC   DS1BLKL,MAXBLKSI  LARGER THAN MAXIMUM WYLBUR SIZE ?
         BH    KILLEDIT      YES; DON'T KNOW WHAT IT IS          81360
*   OUTPUT MAY BE DECREASED OR INCREASED DEPENDING UPON DEVICE  *
         STH   R9,JFCBLKSI-INFMJFCB(,R7)                         81360
         STH   R9,JFCLRECL-INFMJFCB(,R7)                         81360
         B     ALLOCATE
KILLEDIT NI    FLAG,255-EDIT   TURN OFF HIGH BIT                 81360
         MVC   JFCBLKSI-INFMJFCB(4,R7),DS1BLKL   REFRESH WITH ORIGINAL
ALLOCATE BAL   R10,GETSIZE   CALCULATE REQUIRED SPACE            81360
         B     NODSCB3       OK                                  81360
         MVC   BADNO(11),=C' NO EXTENTS'                         81360
         B     NOTCOPID      NO; FAIL                            81360
         SPACE 1                                                 81360
GETSIZE  LA    R1,3          DO MAX 3 EXTENTS FOR NOW - LAZY
         CLI   DS1NOEPV,3                                        80205
         BH    *+8
         IC    R1,DS1NOEPV   MAYBE LESS THAN 3                   80205
         LTR   R1,R1         CHECK FOR ANY AT ALL
         BNP   4(,R10)       TAKE ERROR RETURN                   81360
         LA    R2,DS1EXT1-10
         XC    TRKNUM,TRKNUM
ALLOCAT2 LA    R2,10(,R2)
         MVC   DB,2(R2)      MOVE EXTENTS TO HALF-WORD BOUNDARY  81360
         LH    R9,DB+4       UPPER CYLINDER                      80205
         SH    R9,DB         MINUS LOWER CYL                     80205
         MH    R9,DS4DEVSZ+2   INTO TRACKS                       81360
         AH    R9,DB+6       + HIGH TRACK                        80205
         SH    R9,DB+2       - START TRACK                       80205
         LA    R9,1(,R9)     NUMBER OF TRACKS REQUIRED           80205
         LR    R8,R9         SAVE SIZE OF LAST EXTENT            80206
         A     R9,TRKNUM     TOTAL IT
         ST    R9,TRKNUM
         BCT   R1,ALLOCAT2   LOOP MAX 3 TIMES
         XR    R2,R2                                             80205
         IC    R2,DS1NOEPV                                       80205
         SH    R2,=H'3'      ALLOW FOR THE THREE JUST TOTALED    80205
         BNP   GETSIZED                                          81360
         LR    R1,R8         COPY SIZE OF LAST ONE               80206
         MR    R0,R2         ASSUME 4-N SAME AS EXTENT 3         80205
         AR    R9,R1         ADD ESTIMATE IN                     80205
GETSIZED ST    R9,TRKNUM     SAVE FOR CALLER                     81360
         LTR   R9,R9         ANY ?                               81360
         BPR   R10           YES                                 81360
         B     4(,R10)       ELSE TAKE ERROR RETURN              81360
         SPACE 1                                                 81360
         USING INFMJFCB,R7                                       81360
NODSCB3  LA    R9,5(,R9)     MAKE PRIME AT LEAST 5 TRACKS
         XR    R14,R14                                           80206
         LR    R15,R14                                           80206
         IC    R14,VMASK+3   OLD TYPE                            80206
         IC    R15,NEWMASK+3  NEW TYPE                           80206
         LA    R1,15         MAKE MASK                           80206
         NR    R14,R1                                            80206
         NR    R15,R1                                            80206
         SLDL  R14,1         CONVERT TO INDEX                    80206
         LH    R14,NEWDEVTB(R14)                                 80206
         CH    R14,NEWDEVTB(R15)  SAME CAPACITY ?                80206
         BNH   SAMETYPE      YES; OR MORE ROOM (HENCE RELEASE)   80206
         LR    R14,R9        GET AMOUNT                          80206
         SRL   R14,1         HALVE IT                            80206
         AR    R9,R14        ADD IT IN                           80206
         LR    R14,R8        DITTO FOR SECONDARY                 80206
         SRL   R14,1                                             80206
         AR    R8,R14                                            80206
SAMETYPE ST    R9,TRKNUM
         MVC   JFCBPQTY(3),TRKNUM+1
         STCM  R8,7,JFCBSQTY   SET SECONDARY VALUE IN TRACKS     80206
         MVI   JFCBCTRI,JFCBTRK+JFCONTIG  FORCE TRACKS/CONTIG    80205
         XR    R0,R0         DON'T NEED TIME                     81360
         L     R1,DATE
         ST    R0,DB         SET FOR CVB
         SLDL  R0,16         SHIFT YEAR TO R0
         SRL   R1,16
         ST    R1,DB+4
         SLL   R1,28         SIGN FOR YEAR
         SLDL  R0,4
         CVB   R1,DB
         ST    R0,DB+4
         CVB   R0,DB         CONVERT YEAR
         STH   R0,DB
         STH   R1,DB+2
         MVC   JFCBCRDT(3),DB+1 CREATION DATE
         OI    JFCBIND1,JFCRLSE   SET RELEASE                    81360
         OI    JFCBIND2,JFCOLD  DISP=OLD
         SERVCALL ALCDS,INFMJFCB  ALLOCATE THE DATASET           81360
         LTR   R9,R15        SAVE FOR DUMP (IF ANY)              81360
         BZ    OPENCOPY      ZERO - RETURN TO COPY
         CH    R9,=H'8'                                          81360
         BL    DUPLICAT      ALREADY EXISTS ??                   81360
         BE    NOSPACE       NO SPACE LEFT IN VTOC ??
         CH    R9,=H'20'     VOLUME FULL ??                      81360
         BE    VOLFULL
         SERVTERM ,                                              81360
         ABEND 600,DUMP
         SPACE 1
USECRD   LA    R0,1000       MAKE YEAR MULTIPLIER                81360
         XR    R15,R15       CLEAR FOR IC                        81360
         IC    R15,DS1CREDT  YEAR                                81360
         MR    R14,R0        FINAGLE                             81360
         AH    R15,DS1CREDT+1   ADD DAYS                         81360
         CVD   R15,DB        PACK                                81360
         MVC   WORK(7),=X'F021204B202020'  MAKE DATE MASK        81360
         ED    WORK(7),DB+5    EDIT YY.DDD                       81360
         MVC   CDATE(6),WORK+1                                   81360
         MVC   WORK(7),=X'F021204B202020'   MAKE EDIT MASK AGAIN 81360
         CLI   DS1REFD,99    POSSIBLE DSCB FORMAT TIME ?         80205
         BNH   USECRDVS      YES; TRY IT                         80205
         TM    DS1REFD+2,X'0F'  VALID SIGN ?                     80205
         BNO   USECRDBA      BAD ACCESS DATE                     80205
         B     USECRDED      AND EDIT                            81360
USECRDBA MVC   ADATE,CDATE   LAST ACCESS = CREATION DATE         80205
         BR    R10           RETURN TO CALLER                    80205
USECRDVS XR    R15,R15       CLEAR FOR IC                        81360
         IC    R15,DS1REFD   YEAR                                81360
         MR    R14,R0        FINAGLE                             81360
         AH    R15,DS1REFD+1   ADD DAYS                          81360
         CVD   R15,DB        PACK                                81360
USECRDED ED    WORK(7),DB+5    EDIT YY.DDD                       81360
USECRDEX MVC   ADATE(6),WORK+1                                   81360
         BR    R10
         EJECT ,                                                 81360
OPENCOPY MVC   DECOMLST(PATLCOMP),PATDCOMP  REFRESH COMPRESS/DECOMP
         LH    R4,JFCBLKSI   GET OUTPUT BLOCK SIZE               81360
         STH   R4,WCMBUFMX   SET LARGEST POSSIBLE BLOCK SIZE     81360
         LM    R1,R2,MAINLY  GET GETMAIN PATTERN SIZES           81360
         TM    FLAG,EDIT     COMPRESSED FORMAT ?                 81360
         BNZ   OPENOWRK      YES                                 81360
         XR    R4,R4         CLEAR IT                            81360
         STH   R4,WCMBUFMX   AND SIZE                            81360
OPENOWRK AH    R4,=H'4095'                                       81360
         SRL   R4,12                                             81360
         SLL   R4,12         ROUND TO 4K (PAGE) MULTIPLE         81360
         AR    R1,R4         MAKE MINIMUM SIZE REQUIRED          81360
         STM   R1,R2,MAINREQ   MAKE MIN/MAX GETMAIN SIZES        81360
         GETMAIN VC,LA=MAINREQ,A=MAINRES                         81360
         BXH   R15,R15,OPENBBAD  USE DEFAULT IF NO SPACE         81360
         LM    R1,R2,MAINRES   GET STORAGE ADDRESS/SIZE GOTTEN   81360
         ST    R1,WCMBUFAD   SET BUFFER ADDRESS                  81360
         AR    R1,R4                                             81360
         SR    R2,R4         DON'T FREE UNTIL CLOSE              81360
         LR    R3,R2         SAVE STORAGE SIZE                   81360
         FREEMAIN R,LV=(R2),A=(1)  FREE IT
         S     R3,MAINLY     FREE MINIMUM FOR OS
         BNP   OPENBUFS      TREAT AS BUFFER SHORTAGE            81360
         SLR   R2,R2         FOR DIVIDE                          81360
         LH    R1,DS1BLKL    GET OLD BLOCK SIZE
         CH    R1,JFCBLKSI   COMPARE TO NEW SIZE
         BNL   *+8           HIGHER; USE IT
         LH    R1,JFCBLKSI   ELSE USE HIGHER VALUE
         LA    R1,128(,R1)   ALLOW IOB/ICB OVERHEAD
         DR    R2,R1         NUMBER OF FULL BLOCKS IN R3
         SRA   R3,1          HALF FOR EACH DCB
         B     OPENBUFS                                          81360
OPENBBAD XR    R3,R3                                             81360
         ST    R3,WCMBUFAD   SHOW NO BUFFER GOTTEN               81360
         NI    FLAG,255-EDIT   RESET EDIT MODE COPY              81360
OPENBUFS LTR   R3,R3         CHECK BUFFER NUMBER
         BP    OPENBUFM      NOT TOO LOW
         LA    R3,1          ELSE USE ONE BUFFER
OPENBUFM CH    R3,=H'255'    NOT TOO HIGH ?
         BNH   *+8
         LA    R3,255        USE MAX
         LA    R6,INPDCB                                         81360
         LA    R7,OUTDCB                                         81360
         STC   R3,DCBBUFNO-IHADCB(,R6)
         STC   R3,DCBBUFNO-IHADCB(,R7)  SET BOTH BUFFER NUMBERS
         OPEN  (INPDCB,(INPUT)),TYPE=J                           81360
         LA    R3,INPDCB                                         81360
         TM    INPDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPEN ?           81360
         BZ    MISSDD        NO; BAD DD ?                        81360
         OPEN  (OUTDCB,(OUTPUT)),TYPE=J                          81360
         LA    R3,OUTDCB     SET DCB ADDRESS FOR MESSAGE         81360
         TM    OUTDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPENED ?         81360
         BZ    MISSDD        NO ?                                81360
         SPACE
         L     R4,DCBGET-IHADCB(,R6)   GET GET
         L     R5,DCBPUT-IHADCB(,R7)   GET PUT
         TM    DCBRECFM-IHADCB+INPDCB,JFCUND  UNDEFINED ?        81360
         BO    GETU          YES; SPECIAL LOOP                   81360
         BALR  R3,0          MAKE LOOP RETURN ADDRESS            81360
*GET     GET   INPDCB                                            81360
GET      LR    R1,R6         DCB INPDCB                          81360
         LR    R15,R4        GET ROUTINE
         BALR  R14,R15       GET A RECORD
         LR    R0,R1                                             81360
*        PUT   OUTDCB,(R0)                                       81360
*        B     GET
         LR    R1,R7         DCB OUTDCB                          81360
         LR    R15,R5        PUT ROUTINE
         LR    R14,R3        GET ADDRESS
         BR    R15           PUT; RETURN TO GET
         SPACE 2                                                 81360
GETU     LA    R3,GETULP     POINT TO LOOP                       81360
         TM    FLAG,EDIT     COMPRESSED MODE ?                   81360
         BNZ   GETWYL        YES                                 81360
         BALR  R3,0          SET LOOP RETURN ADDRESS             81360
*GETULP  GET   INPDCB                                            81360
GETULP   LR    R1,R6         DCB INPDCB                          81360
         LR    R15,R4        GET ROUTINE                         81360
         BALR  R14,R15       GET A RECORD                        81360
GETGOT   LR    R0,R1                                             81360
         MVC   DCBLRECL-IHADCB(2,R7),DCBLRECL-IHADCB(R6)  SET LRECL
*        PUT   OUTDCB,(R0)                                       81360
*        B     GET                                               81360
         LR    R1,R7         DCB OUTDCB                          81360
         LR    R15,R5        PUT ROUTINE                         81360
         LR    R14,R3        GET ADDRESS                         81360
         BR    R15           PUT; RETURN TO GET                  81360
         SPACE
GETWYL   LR    R1,R6
         LR    R15,R4
         BALR  R14,R15
         STM   R1,R5,DB      SAVE IN CASE NOT EDIT FORMAT        81360
         ST    R1,DCMBUFAD   SET BUFFER ADDRESS                  81360
         MVC   DCMBUFMX,DCBLRECL-IHADCB+INPDCB  SET LENGTH       81360
         XC    DCMBUFLN,DCMBUFLN  SIGNAL NEW BLOCK TO BE PROCESSED
         SERVCALL WDCOM,DECOMLST  TRY TO DECOMPRESS IT           81360
         BXLE  R15,R15,GOTWYL   OK; APPEARS TO BE EDIT           81360
         LM    R1,R5,DB      RESTORE REGISTERS                   81360
         CLC   DCBBLKSI-IHADCB+INPDCB,DCBBLKSI-IHADCB+OUTDCB  FITS ?
         BNH   GETGOT        YES; USE RECFM=U COPY AS IS LOOP    81360
         B     NOTEDIT       ELSE SCRATCH AND RESTART            81360
         SPACE 1                                                 81360
GETWYL2  GET   INPDCB        GET A NEW BLOCK                     81360
         ST    R1,DCMBUFAD   SAVE THE ADDRESS                    81360
         MVC   DCMBUFMX,DCBLRECL-IHADCB+INPDCB  SET THE BLOCK LENGTH
         XC    DCMBUFLN,DCMBUFLN  SIGNAL NEW BLOCK TO BE PROCESSED
GETWYLN  SERVCALL WDCOM,DECOMLST  DECOMPRESS IT                  81360
         CH    R15,=H'4'     CHECK RETURN                        81360
         BL    GOTWYL        HAVE A NEW LINE                     81360
         BE    GETWYL2       GET A NEW BUFFER                    81360
         BH    NOTEDIT       ?                                   81360
GOTWYL   MVC   WCMRECAD,DCMRECAD  COPY THE RECORD ADDRESS        81360
         MVC   WCMRECLN,DCMRECLN  AND LENGTH                     81360
GOTWYL2  SERVCALL WCOMP,COMPLST   AND TRY TO SQUEEZE IT IN       81360
         CH    R15,=H'4'                                         81360
         BL    GETWYLN       DONE; GET ANOTHER LINE              81360
         BH    NOTEDIT       FUNNY ERROR                         81360
         STH   R0,DCBLRECL-IHADCB+OUTDCB  SET NEW BLOCK LENGTH   81360
         L     R0,WCMBUFAD   GET THE BUFFER ADDRESS              81360
         PUT   OUTDCB        COPY A BLOCK                        81360
         B     GOTWYL2       TRY IT AGAIN                        81360
         SPACE 1                                                 81360
NOTEDIT  BAL   R10,CLOSER    CLOSE AND FREE EVERYTHING           81360
         SCRATCH UNALLOC     SCRATCH UNUSED OUTPUT DSN           81360
         B     KILLEDIT      TRY ALL OVER AGAIN                  81360
         SPACE 1                                                 81360
EODAD    TM    FLAG,EDIT     COMPRESS MODE ?                     81360
         BZ    EODAD2        NO                                  81360
         XR    R1,R1                                             81360
         ST    R1,WCMRECAD   SIGNAL END                          81360
         SERVCALL WCOMP,COMPLST  SEE IF ANYTHING TO DO           81360
         LTR   R0,R0                                             81360
         BZ    EODAD2        NO                                  81360
         STH   R0,DCBLRECL-IHADCB+OUTDCB  SET BLOCK LENGTH       81360
         L     R0,WCMBUFAD   GET BLOCK ADDRESS                   81360
         PUT   OUTDCB        WRITE FINAL BLOCK                   81360
EODAD2   BAL   R10,CLOSER    CLOSE AND CLEAN-UP                  81360
         SPACE
BUGGER   B     CODEZERO                  BRANCH AROUND SCR/CAT
         MVC   CVOL,WYLCVOL  RESET TO MOST FREQUENT              80205
         CLC   =C'WYL.',DS1DSNAM  WYLBUR ENTRY ?                 81360
         BE    ITCH          YES; PROCESS                        80205
         MVC   CVOL,SYSCVOL  USE UNSPECIFIED CATALOG VOLUME      80205
ITCH     XR    R0,R0                                             80205
       SCRATCH SCRCAM
         BXLE  R15,R15,UNCATDSN                                  80205
         MVC   SNOT(3),=C'NOT'
UNCATDSN CATALOG RECATLG     TRY TO RECATALOGUE THE DSN
         BXLE  R15,R15,CODEZERO                                  80205
         CATALOG UCATCAM     TRY UNCATALOG                       80205
         CATALOG CATCAT      TRY TO CATALOG IT THEN
         BXLE  R15,R15,CODEZERO                                  80205
         MVC   SUCAT(4),=C'NOT '                                 81360
CODEZERO OBTAIN REFORM1      GET THE OUTPUT DSCB 1               81360
         SERVCALL DSFMT,DS1DSORG  FORMAT NEW STATISTICS          81360
         MVC   FMTAREA,0(R1)                                     81360
         BAL   R10,GETSIZE   GET NEW DATASET SIZE                81360
         NOP   0             IGNORE GOOD RETURN                  81360
         PRTLIST PRINTG      PRINT COPIED DATASET INFORMATION    81360
         MVC   SNOT(3),SNOT-1   REFRESH                          81360
         MVC   SUCAT(4),=C'  RE'                                 81360
         B     DSCBBUMP      DO ANOTHER DSCB                     81360
         SPACE 2                                                 81360
DUPLICAT MVC   BADNO(12),=CL12'DUPLICATE'                        80205
         SPACE 1                                                 81360
NOTCOPID SERVCALL DSFMT,DS1DSORG  FORMAT ATTRIBUTES              81360
         MVC   FMTAREA,0(R1)  SAVE FOR FORMATTING                81360
         BAL   R10,GETSIZE   GET DATASET SIZE                    81360
         NOP   0             IGNORE GOOD AND ERROR RETURN        81360
         PRTLIST PRINTX,DEV=2   PRINT ERROR MESSAGE              81360
         MVC   BADNO(L'BADNO+L'REASON+1),BADNO-1                 80205
         TM    INPDCB+DCBOFLGS-IHADCB,DCBOFOPN  DCBS OPEN ?      81360
         BZ    DSCBBUMP      NO; GET NEXT DSCB                   80205
         LA    R10,DSCBBUMP  SET RETURN POINT                    80205
         SPACE 1                                                 81360
CLOSER   LA    R2,INPDCB                                         81360
         LA    R3,DUMMYI                                         81360
         LA    R4,2                                              81360
CLOSEIN  TM    DCBOFLGS-IHADCB(R2),DCBOFOPN  DCB OPENED ?        81360
         BZ    CLOSED        NO; LEAVE IT                        81360
         CLOSE ((R2))        CLOSE IT                            81360
         CLC   DCBBUFCA-IHADCB(3,R2),=XL3'1'  BUFFER POOL ?      81360
         BNH   CLOSED        NO; LEAVE AS IS                     81360
         FREEPOOL (R2)       FREE THE BUFFER POOL                81360
CLOSED   MVC   0(DUMMYO-DUMMYI,R2),0(R3)  REFRESH THE DCB        81360
         LA    R2,OUTDCB                                         81360
         LA    R3,DUMMYO                                         81360
         BCT   R4,CLOSEIN                                        81360
         ICM   R1,15,WCMBUFAD   ANY BUFFER OBTAINED ?            81360
         BZR   R10           NO; RETURN                          81360
         XR    R0,R0                                             81360
         ICM   R0,3,WCMBUFMX   LENGTH SET ?                      81360
         BZR   R10           NO; RETURN                          81360
         FREEMAIN R,LV=(0),A=(1)  FREE THE STORAGE               81360
         XR    R0,R0                                             81360
         ST    R0,WCMBUFAD   SIGNAL FREED                        81360
         STH   R0,WCMBUFMX                                       81360
         BR    R10           RETURN TO CALLER                    81360
         EJECT ,                                                 81360
EXIT     SERVTERM ,          CLOSE AND FREE EVERYTHING           81360
         ENDM  RC=0          RETURN                              81360
         SPACE 2
NOTFOUND WTO   'ERROR PROCESSING FORMAT4 OR VOLUME NOT MOUNTED',ROUTCDE*
               =(1,11),DESC=1
         B     ABEND400                                          81360
DSCBIOER WTO   'I/O ERROR PROCESSING FORMAT1',ROUTCDE=(1,11),DESC=1
         B     ABEND400                                          81360
NOSPACE  MVC   FULLWTO+15(4),=C'VTOC'                            80205
         B     FULLCOMM                                          80205
VOLFULL  MVC   FULLWTO+15(4),=C'DISK'                            80205
FULLCOMM MVC   FULLWTO+8(6),NEWVOLS                              81360
FULLWTO  WTO   'OUTDCB VTOC FULL; WILL TRY NEXT PACK'            81360
LOOPOUT  CLI   DCBDDNAM-IHADCB+OUTDCB+5,C'9'  LAST ONE DONE ?    81360
         BNL   EXITNONE      YES; EXIT                           80205
         IC    R1,DCBDDNAM-IHADCB+OUTDCB+5  GET NUMBER           81360
         LA    R1,1(,R1)     UP BY ONE                           80205
         STC   R1,DCBDDNAM-IHADCB+OUTDCB+5  SET BACK             81360
         STC   R1,DCBDDNAM-IHADCB+DUMMYO+5  SET BACK             80205
         DEVTYPE DCBDDNAM-IHADCB+OUTDCB,DB  DOES DD CARD EXIST ? 81360
         BXH   R15,R15,LOOPOUT    NO; TRY NEXT                   80205
         CLI   DB+2,UCB3DACC   DASD ?                            81360
         BNE   LOOPOUT       NO; ERROR                           81360
         LA    R3,OUTDCB     SET FOR OUTDCB                      81360
         USING IHADCB,R3                                         81360
         SERVCALL TIODD,DCBDDNAM   FIND IT                       81360
         LTR   R5,R0         TIOT ENTRY FOUND ?                  81360
         BZ    LOOPOUT       NO; TOO BAD                         81360
         USING TIOENTRY,R5                                       81360
         ICM   R4,7,TIOEFSRT   GET THE UCB ADDRESS               81360
         BZ    LOOPOUT       TOO BAD ?                           81360
         USING UCBOB,R4                                          81360
         MVC   TO(6),UCBVOLI                                     81360
         MVC   NEWVOLS,UCBVOLI  COPY VOL-SER TO RECAT            81360
         MVC   NEWMASK,UCBTBYT1  MOVE DEVTYPE                    81360
         NI    NEWMASK+1,255-UCBRR-UCBVLPWR-UCBDVPWR  FIX FOR CATLG
         CLI   NEWMASK+3,X'0A'   3340 ?                          81360
         BNE   *+8           NO                                  81360
         NI    NEWMASK+1,255-UCBRPS                              81360
         B     ALLRETRY      NOW TRY THE ALLOCATION AGAIN        81360
         SPACE 1                                                 81360
EXITNONE WTO   'ALL OUTPUT PACKS EXHAUSTED',ROUTCDE=(1,11),DESC=1
         B     ABEND400                                          81360
         SPACE 2                                                 80205
ABEND400 SERVTERM ,          CLOSE AND FREE EVERYTHING           81360
         ABEND 400,DUMP
NODEVICE ABEND 433,DUMP
         EJECT ,                                                 81360
DATE     DC    F'0'          CVT DATE
MAINREQ  DC    2A(0)                                             81360
MAINRES  DC    2A(0)                                             81360
MAINLY   DC    A(8*1024,1024*1024)  GETMAIN MINIMUM/MAXIMUM
TRKNUM   DC    F'0'
MAXBLKSI DC    H'13030'      SET FOR TSM                         81360
NEWDEVTB DC    H'0'     0   0   0
         DC    H'0'     1   1   2
         DC    H'0'     2   2   4
         DC    H'0'     3   3   6
         DC    H'0'     4   4   8
         DC    H'0'     5   5   10
         DC    H'0'     6   6   12
         DC    H'0'     7   7   14
         DC    H'3520'  8   8   16  2314
         DC    H'3156'  9   9   18  3330 SINGLE
         DC    H'4100'  10  A   20  3340                         81360
         DC    H'3665'  11  B   22  3350
         DC    H'0'     12  C   24
         DC    H'3156'  13  D   26  3330 DOUBLE
         DC    H'0'     14  E   28
         DC    H'0'     15  F   30
FLAG     DC    X'00'
EDIT     EQU   X'80'
PQTY     DC    AL3(10)       PRIMARY ALLOCATION - 10 TRKS
         DC    AL4(05)       SECONDARY          - 20 TRKS
DSNMASK  DC    0F'0',X'FF',CL43' '                               80205
JF1      DS    0F            INPDCB JFCB ADDRESS                 81360
         DC    X'87',AL3(SYS1JFCB)
JF2      DS    0F            OUTDCB JFCB ADDRESS                 81360
         DC    X'87',AL3(SYS2JFCB)
         SPACE
*        RDJFCB LIST
         SPACE
JFCBLIST RDJFCB (INPDCB,,OUTDCB),MF=L                            81360
         SPACE 2                                                 80205
*        TABLE OF SPECIAL GROUP NAMES                            80205
         DS    0F            REQUIRED ALIGNMENT                  81360
GROUPTAB DC    CL8'NONIGP'                                       80205
         DC    A(NOTIGP-MIGRATE,IGP-MIGRATE)                     80205
GROUPTAK DC    CL8'IGP'                                          80205
         DC    A(IGP-MIGRATE,SYSTEMS-MIGRATE)                    80205
         DC    CL8'SYSTEMS'                                      80205
         DC    A(SYSTEMS-MIGRATE,SPARE1-MIGRATE)                 80205
         DC    CL8'SPARE1'                                       80205
         DC    A(SPARE1-MIGRATE,SPARE2-MIGRATE)                  80205
         DC    CL8'SPARE2'                                       80205
         DC    A(SPARE2-MIGRATE,SPARE3-MIGRATE)                  80205
         DC    CL8'SPARE3'                                       80205
         DC    A(SPARE3-MIGRATE,SPARE4-MIGRATE)                  80205
         DC    CL8'SPARE4'                                       80205
         DC    A(SPARE4-MIGRATE,SPARE5-MIGRATE)                  80205
         DC    CL8'SPARE5'                                       80205
         DC    A(SPARE5-MIGRATE,SPARE6-MIGRATE)                  80205
         DC    CL8'SPARE6'                                       80205
         DC    A(SPARE6-MIGRATE,SPARE7-MIGRATE)                  80205
GROUPEND EQU   *             END OF TABLE                        80205
         SPACE 2                                                 80205
NOTIGP   DC    C'F500FZZZ'                                       80205
         DC    C'Z000Z999'                                       80205
         DC    CL8' '        ZAP                                 80205
         DC    CL8' '        ZAP                                 80205
IGP      DC    C'AAAAE999'   IGP LOW RANGE                       80205
         DC    C'H000H000'   IGP HIGH RANGE                      82052
         DC    CL8' '        ZAP                                 80205
         DC    CL8' '        ZAP                                 80205
SYSTEMS  DC    CL8'Z904Z904'                                     80205
         DC    CL8' '                                            80205
         DC    CL8' '        ZAP                                 80205
SPARE1   DC    CL8'ZAP1ZAP1'                                     80205
         DC    CL8'ZAP1ZAP1'                                     80205
         DC    CL8'ZAP1ZAP1'                                     80205
SPARE2   DC    CL8'ZAP2ZAP2'                                     80205
         DC    CL8'ZAP2ZAP2'                                     80205
         DC    CL8'ZAP2ZAP2'                                     80205
SPARE3   DC    CL8'ZAP3ZAP3'                                     80205
         DC    CL8'ZAP3ZAP3'                                     80205
         DC    CL8'ZAP3ZAP3'                                     80205
SPARE4   DC    CL8'ZAP4ZAP4'                                     80205
         DC    CL8'ZAP4ZAP4'                                     80205
         DC    CL8'ZAP4ZAP4'                                     80205
SPARE5   DC    CL8'ZAP5ZAP5'                                     80205
         DC    CL8'ZAP5ZAP5'                                     80205
         DC    CL8'ZAP5ZAP5'                                     80205
SPARE6   DC    CL8'ZAP6ZAP6'                                     80205
         DC    CL8'ZAP6ZAP6'                                     80205
         DC    CL8'ZAP6ZAP6'                                     80205
SPARE7   EQU   *                                                 80205
         EJECT ,                                                 81360
HEADER1  VCON  END=HEADER1N                                      81360
         DC    C'#    WYLBUR DATASET MIGRATIONS FROM '           81360
FROM     DC    CL6' '
         DC    C' TO '                                           81360
TO       DC    CL6' '
         DC    CL2' '                                            81360
         DC    C'SELECTION DATE '                                81360
SLETDATE DC    CL6'  .   '
         VCON  ,                                                 81360
         SPACE 1                                                 81360
HEADER2  VCON  END=HEADER2X                                      81360
         DC    CL45'   DATASET NAME'                             81360
         DC    C'  CREDT'                                        81360
         DC    C'  ACCDT'                                        81360
         DC    C' DSO'                                           81360
         DC    C'  TRKS'                                         81360
         DC    C' RECFM '                                        81360
         DC    C' BLKSI'                                         81360
         VCON  ,                                                 81360
         SPACE
PRINTG   FDPRT DS1DSNAM,NL,PADR                                  81360
         FDPRT CDATE,PAD                                         81360
         FDPRT ADATE,PAD                                         81360
         FDPRT FMTDSORG,PAD                                      81360
         FDPRT TRKNUM,I,RADJ,PAD,LEN=5                           81360
         FDPRT FMTRECFM,PAD                                      81360
         FDPRT FMTBLKSI,PAD                                      81360
         FDPRT SNOT,PAD                                          81360
         FDPRT VOLSER,PAD,LEN=7                                  81360
         FDPRT SUCAT,PAD                                         81360
         FDPRT NEWVOLS,PADL                                      81360
         FDPRT *END                                              81360
         SPACE 1                                                 81360
CDATE    DC    CL6' '
ADATE    DC    CL6'  .   '
         DC    C' '          FOR BLANKING SNOT                   81360
SNOT     DC    C'    SCRATCHED ON'                               81360
SUCAT    DC    C'  RECATALOGED ON'                               81360
         SPACE 1                                                 81360
PRINTX   FDPRT DS1DSNAM,NL,PADR                                  81360
         FDPRT VOLSER,PAD                                        81360
         FDPRT CDATE,PAD                                         81360
         FDPRT ADATE,PAD                                         81360
         FDPRT FMTDSORG,PAD                                      81360
         FDCLC TRKNUM,ZEROES,4,BE=PRINTXPD                       81360
         FDPRT TRKNUM,I,RADJ,PAD,LEN=5                           81360
         FDGOTO PRINTXDP                                         81360
PRINTXPD FDPRT ' ',PAD,LEN=5                                     81360
PRINTXDP FDPRT FMTRECFM,PAD                                      81360
         FDPRT FMTLRECL,PAD                                      81360
         FDPRT FMTBLKSI,PAD                                      81360
         FDPRT 'NOT COPIED -',PAD                                81360
         FDPRT BADNO,L'BADNO+1+L'REASON                          81360
         FDPRT *END                                              81360
ERRHEAD1 VCON  '#   WYLBUR DATASET MIGRATION ERROR LISTING'      81360
         SPACE 1                                                 81360
ERRHEAD3 VCON  END=ERRHEND3                                      81360
         DC    CL45'   DATASET NAME'                             81360
         DC    C' VOLUME'                                        81360
         DC    C'  CREDT'                                        81360
         DC    C'  ACCDT'                                        81360
         DC    C' DSO'                                           81360
         DC    C'  TRKS'                                         81360
         DC    C' RECFM '                                        81360
         DC    C' LRECL'                                         81360
         DC    C' BLKSI'                                         81360
         VCON  ,                                                 81360
         SPACE 1                                                 81360
FMTAREA  DS    0CL27' '      RETURN FROM DSFMT SERVICE CALL      81360
FMTDSORG DS    CL3                                               81360
FMTRECFM DS    CL6                                               81360
FMTOPTCD DS    CL8                                               81360
FMTBLKSI DS    CL5                                               81360
FMTLRECL DS    CL5                                               81360
         DC    C' '          BADNO-1 BLANKING                    81360
BADNO    DC    CL3' ',C' '                                       80205
REASON   DC    CL8' '                                            80205
         DC    CL10' '       JUST IN CASE                        81360
         EJECT ,                                                 81360
FORMAT1 CAMLST SEEK,CCHHR,VOLSER,IECSDSF1                        81360
REFORM1  CAMLST SEARCH,DS1DSNAM,NEWVOL,DS1FMTID  REREAD FORMAT 1 81360
         SPACE
FORMAT4 CAMLST SEARCH,ID4MAT,VOLSER,DS4IDFMT                     81360
         SPACE
SCRCAM  CAMLST SCRATCH,DS1DSNAM,,VOLIST,,OVRD                    81360
UNALLOC CAMLST SCRATCH,DS1DSNAM,,NEWVOL,,OVRD  SCRATCH ON OUTPUT 81360
         SPACE
UCATCAM CAMLST UCATDX,DS1DSNAM,CVOL                              81360
RECATLG CAMLST RECAT,DS1DSNAM,CVOL,NEWVOL                        81360
CATCAT  CAMLST CATBX,DS1DSNAM,CVOL,NEWVOL                        81360
NEWVOL   DC    H'1'          NEW VOLUME LIST
NEWMASK  DC    X'3050200D'
NEWVOLS  DC    C'INF001'
         DC    H'0'          CREAMED BY RETURN CODE              80206
         SPACE 3
VOLIST   DC    H'1'
VMASK    DC    X'30502009'
VOLSER   DC    C'VOLSER'
         DC    H'0'
CVOL     DC    C'WYL001'
WYLCVOL  DC    C'WYL001'     WYL. CVOL                           80205
SYSCVOL  DC    CL6' '        SYSRES MAIN CATALOG VOLUME          80205
ID4MAT   DC    0CL44' ',44X'04'       VTOC DSNAME
OLDCCHHR DS    CL5
         DS    0H
CCHHR    DC    0CL5' ',5X'00'
         ORG   CCHHR
CC       DS    CL2
HH       DS    CL2
R        DS    C
         ORG
         SPACE
         DS    0D
SYS1JFCB DC    CL176' '      INPDCB JFCB                         81360
SYS2JFCB DC    CL176' '      OUTDCB JFCB                         81360
         SPACE 1                                                 81360
         DS    0F                                                81360
         IECSDSL1 4          EXPAND THE FORMAT 4                 81360
ADDRFMT4 DC    XL5'0'        ADDRESS RETURNED BY @OBTAIN         81360
         SPACE 2
         DS    0F                                                81360
         IECSDSL1 1          EXPAND A FORMAT 1                   81360
ADDRFMT1 DC    XL5'0'        ADDRESS RETURNED BY @OBTAIN         81360
         SPACE 1                                                 81360
         LTORG
         SPACE 2
SYSPRINT PRTWORK SYSPRINT,TITLE=5                                81360
ERRPRINT PRTWORK ERRPRINT,SUMPRINT,TITLE=5                       81360
         SPACE 2
INPDCB   DCB   DDNAME=SYSIN1,DSORG=PS,MACRF=GL,EODAD=EODAD,EXLST=JF1,  *
               OPTCD=C       CHAINED SCHEDULING IS FASTER
         SPACE 2
OUTDCB   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=PM,EXLST=JF2,              *
               OPTCD=C       CHAINED SCHEDULING IS FASTER
         SPACE 2
DUMMYI   DCB   DDNAME=SYSIN1,DSORG=PS,MACRF=GL,EODAD=EODAD,EXLST=JF1,  *
               OPTCD=C       CHAINED SCHEDULING IS FASTER
         SPACE
DUMMYO   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=PM,EXLST=JF2,              *
               OPTCD=C       CHAINED SCHEDULING IS FASTER
         SPACE 2                                                 81360
PATDCOMP SERVCOMP DSECT=NO,PFX=PDC,FG2=PDCF2NPR  LINE DECOMPRESSION
PATWCOMP SERVCOMP DSECT=NO,PFX=PCM,FG2=PCMF2NPR,FG1=PCMF1NIH  COMPRESS
PATLCOMP EQU   *-PATDCOMP    LENGTH TO MOVE ON REFRESH           81360
DECOMLST SERVCOMP DSECT=NO,PFX=DCM,FG2=DCMF2NPR  LINE DECOMPRESSION
COMPLST  SERVCOMP DSECT=NO,PFX=WCM,FG2=WCMF2NPR  LINE COMPRESSION
         SPACE 2                                                 81360
SAVE     DSECT ,             WORK/SAVE AREA                      81360
DB       DS    D             WORK WORD                           81360
WORK     DS    CL16          WORK SPACE                          81360
ZEROES   DS    D                                                 81360
@SERVICE DS    A             SERVICE ROUTINE                     81360
@PRINTER DS    A             PRINT ROUTINE                       81360
@OBTAIN  DS    A             VTOC READER                         81360
SAVEND   EQU   *                                                 81360
         SPACE 3
         PRINT &PRTSYS                                           81360
CVTDSECT DSECT ,                                                 81360
         CVT   DSECT=YES                                         81360
         SPACE 1                                                 81360
         IHACDE ,                                                81360
         SPACE 1                                                 81360
         IEZDEB ,                                                81360
         SPACE 1                                                 81360
         IEFUCBOB ,                                              81360
         AIF   (NOT &SVS).NOURR                                  81360
UCBRR    EQU   UCB2OPT2      SHARED DASD                         81360
UCBRPS   EQU   UCB2OPT3      RPS FEATURE                         81360
.NOURR   SPACE 1                                                 81360
         IEFTIOT1 ,                                              81360
         SPACE 1                                                 81360
         DCBD  DSORG=PS                                          81360
         SPACE 1                                                 81360
         IEFJFCBN ,                                              81360
         SPACE 1                                                 81360
         END
