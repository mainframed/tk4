S241     TITLE 'STORE CPUID SVC'
         PRINT NOGEN
*        THE FUNCTION OF THIS SVC IS TO PASS THE CPU IDENTIFICATION
*        (8 BYTES) TO THE UNPRIVILEGED USER.  THE OUTPUT IS STORED
*        IN A DOUBLE-WORD, THE ADDRESS OF WHICH IS SUPPLIED BY THE
*        USER IN REGISTER 1.
*          REGISTER 15 IS SET TO 0 ON NORMAL COMLETION.
*        ON ANY ERROR, R15 IS SET TO 8
*
IGC0024A CSECT ,
         BALR  12,0          MAKE A BASE
         USING *,12
         LA    1,0(,1)       CLEAN HIGH BYTE
         LR    7,1           COPY TO VALIDITY CHECK REGISTER
         SRL   7,3
         SLL   7,3           TRUNCAT TO DOUBLE WORD
         CR    7,1           SAME ?
         BNE   FAIL          NO; ERROR
         LR    9,4           COPY TCB ADDRESS
         LR    11,3          COPY CVT ADDRESS
         USING CVTMAP,3      DECLARE CVT MAPPING
         L     8,CVT0VL00    GET VALIDITY CHECK ROUTINE
         BALR  10,8          INVOKE IT
         BNZ   FAIL          ERROR
         STIDP 0(1)          STASH CPU ID FOR CALLER
         XR    15,15
         BR    14            RETURN
         SPACE 1
FAIL     LA    15,8
         BR    14            RETURN WITH ERROR
         SPACE 3
CVTSECT  DSECT ,
         CVT   DSECT=YES
         SPACE 1
         END
