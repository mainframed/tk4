ANSXT    TITLE 'I F G 0 1 9 3 G  ***  ANSI VALIDATION/ACCESS EXIT'
         MACRO ,                                                 90319
&NM      MGER  &ERR,&ACT,&SUB,&MSG                               90319
         LCLC  &CA,&CS                                           90319
&CA      SETC  '&ACT'                                            90319
         AIF   (K'&ACT GE 2).DEFACT                              90319
&CA      SETC  '&ACT'.'&ACT'                                     90319
         AIF   (K'&ACT EQ 1).DEFACT                              90319
&CA      SETC  'WE'          READ - WARN / WRITE - FAIL          90319
.DEFACT  AIF   ('&SUB' NE '').USESUB                             90319
&CS      SETC  '0'                                               90319
         AGO   .COMSUB                                           90319
.USESUB  ANOP  ,                                                 90319
&CS      SETC  '&SUB-&SYSECT'                                    90319
.COMSUB  ANOP  ,                                                 90319
&NM      DC    AL1(&ERR),CL2'&CA ',Y(&CS),CL19&MSG               90319
         MEND  ,                                                 90319
         SPACE 1                                                 90319
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*        THIS MODULE REPLACES IFG0193G/IFG0553G ANSIEXIT              *
*                                                                     *
*        ENTRY AND ACTION :                                           *
*                                                                     *
*        A) VOLUME ACCESS - RETURN CODE 4 UNLESS:                     *
*           1) USER/ACCT IS PRIVILEGED                                *
*           2) VOLUME HAS ACCESS AUTHORIZATION FOR RUNNING ACCT       *
*                                                                     *
*        B) FILE ACCESS EXIT - RETURN CODE 4 UNLESS:                  *
*           1) USER/ACCT IS PRIVILEGED                                *
*           2) VOLUME ACCESS PERMITTED FOR RUNNING ACCOUNT            *
*                                                                     *
*        C) VALIDATION SUPPRESSION EXIT - REQUEST VALIDATION          *
*                                                                     *
*        D) LABEL VALIDATION EXIT                                     *
*           1) FOR INPUT, PRODUCES WARNING MESSAGE AND CONTINUES      *
*           2) FOR OUTPUT, REQUEST IS FAILED UNLESS THE               *
*              PROBLEM CAN BE FIXED (E.G., NON-ZERO HDR1 BLKCT)       *
*                                                                     *
***********************************************************************
*                                                                     *
*        RE-ENTRANT, AMODE ANY, LPA RESIDENT                          *
*                                                                     *
*        ENTERED IN KEY 5 (DATAMGT)                                   *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR
IFG0193G SAVEM ZERO12,BASE=R12,PARM=R9,SP=230           ADDED ON 90309
         USING IECIEPRM,R9   PASSED PARAMETER LIST
         MVI   IECIEXRC,IECIERC4  SET DEFAULT TO FAIL THE REQUEST
         TM    IECIEFL1,IECIEVAL  VALIDATE LABEL ?
         BNZ   VALLAB        YES
         TM    IECIEFL1,IECIEFAE  FILE ACCESS ?
         BZ    FILACC        YES; CHECK
         TM    IECIEFL1,IECIEVAE  VOLUME ACCESS EXIT
         BNZ   VOLACC        YES; CHECK IT
         TM    IECIEFL1,IECIEVSP  VALIDATION SUPPRESSION EXIT ?
         BNZ   VALSUP        YES; CHECK IT
EXITCOM  SLR   R2,R2
         IC    R2,IECIEXRC   GET 'RETURN' CODE ALSO
         ENDM  RC=(R2)       PASS IT BOTH WAYS
         SPACE 1
***********************************************************************
*                                                                     *
*        VALIDATION SUPPRESSION EXIT                                  *
*                                                                     *
***********************************************************************
         SPACE 1
VALSUP   MVI   IECIEXRC,IECIERC4  PERFORM NORMAL VALIDATION
         B     EXITCOM       AND EXIT
         SPACE 1
***********************************************************************
*                                                                     *
*        VOLUME ACCESS EXIT                                           *
*        FILE ACCESS EXIT - TREAT EXACTLY LIKE VOLUME ACCESS          *
*                                                                     *
*        CHECK THAT THE USER OWNS OR CONTROLS THE VOLUME, IF IT IS    *
*        REGISTERED IN THE TMC.  IF TMS DOESN'T KNOW THE VOLUME,      *
*        ALLOW ACCESS.                                                *
*                                                                     *
***********************************************************************
         SPACE 1
VOLACC   MVI   IECIEXRC,IECIERC4  TEMPORARY ALLOWANCE
FILACC   B     EXITCOM       AND EXIT
         SPACE 1
***********************************************************************
*                                                                     *
*        LABEL VALIDATION EXIT                                        *
*                                                                     *
*          WRITE A WARNING OR ERROR MESSAGE, FIX OR IGNORE WHAT WE    *
*        CAN, AND RETURN ACCORDINGLY.                                 *
*                                                                     *
***********************************************************************
         SPACE 1
VALLAB   SLR   R8,R8         NO SPECIAL EXIT                     90320
         MVC   REALWTO,PATWTO  MAKE PATTERN
*OLD*    UNPK  REALWTO+28(3),IECIEERR(2)
*OLD*    MVI   REALWTO+30,C' '
*OLD*    TR    REALWTO+28(2),HEXTRTAB
         ICM   R1,15,IECIELBL  GET LABEL ADDRESS                 90319
         BZ    NOLABEL       NONE                                90319
         MVC   MSGLBL+REALWTO,0(R1)  MOVE LABEL TYPE             90319
NOLABEL  ICM   R1,15,IECIEUCB  GET UCB ADDRESS                   90319
         BZ    NOUCB         HUH ?                               90319
         SLR   R0,R0                                             90320
         ICM   R0,3,UCBFSCT-UCBOB(R1)  GET FILE COUNT            90319
         CVD   R0,DB         MAKE PACKED                         90319
         MVC   DB+8(6),=X'402020202120'                          90319
         ED    DB+8(6),DB+5                                      90319
         MVC   MSGSEQ+REALWTO,DB+9                               90319
         MVC   MSGCUU+REALWTO,UCBNAME-UCBOB(R1)  MOVE UCB "ADDRESS"
         CLI   UCBVOLI-UCBOB(R1),C'$'  PRINTABLE SERIAL ?        90319
         BL    NOUCB         PROBABLY NOT                        90319
         MVC   MSGVOL+REALWTO,UCBVOLI-UCBOB(R1)  MOVE CEREAL     90319
NOUCB    SLR   R0,R0         MAKE DEBUGGING EASIER               92240
         ICM   R0,1,IECIEERR  LOAD ERROR CODE                    90319
         LA    R1,MESSTAB     GET MESSAGE TABLE                  90319
         LA    R14,MESSTAB2-MESSTAB  GET ENTRY LENGTH            90319
         LA    R15,MESSTABN   GET LAST ENTRY                     90319
LOOKMSG  CLM   R0,1,0(R1)    MATCHING ENTRY ?                    90319
         BE    HAVEMSG                                           90319
         BXLE  R1,R14,LOOKMSG  TRY AGAIN                         90319
         ICM   R0,3,=C'RW'                                       90319
         B     DEFACT        TAKE DEFAULT ACTION                 90319
HAVEMSG  MVC   MSGMSG+REALWTO,OFFMSG(R1)  MOVE MESSAGE TEXT      90319
         ICM   R0,3,OFFACT(R1)  LOAD ACTION CODES                90319
         ICM   R8,3,OFFSUB(R1)  TEST FOR SPECIAL PROCESSING      90320
DEFACT   TM    IECIEFL1,IECIEWRT  WRITING ?                      90319
         BNZ   *+8           YES                                 90319
         SRL   R0,8          SHIFT READ ACTION CODE TO END       90319
         STC   R0,MSGTYPE+REALWTO                                90319
         LTR   R8,R8         PROCESSING ROUTINE ?                90320
         BNZ   IFG0193G(R8)    INVOKE IT                         90320
PUTWTO   CLI   REALWTO+MSGTYPE,C'N'  SUPPRESS WTO ?              90320
         BE    CODEZERO      YES; JUST PASS AWAY                 90320
         WTO   MF=(E,REALWTO)  WRITE TO JOB LOG
         CLI   REALWTO+MSGTYPE,C'E'  FAIL THIS REQUEST ?         90319
         BE    EXITCOM       YES; FAIL THE REQUEST               90319
CODEZERO MVI   IECIEXRC,IECIERC0  ALLOW WITH GRAVE DOUBTS        90310
         B     EXITCOM       RETURN
         SPACE 1                                                 90320
*        SPECIAL PROCESSING FOR UNSUPPORTED VALUES               90320
*                                                                90320
CHKVAL   ICM   R1,15,IECIELBL  LABEL ADDRESS ?                   90320
         BZ    PUTWTO        NO; NOTHING TO DO                   90320
         CLI   3(R1),C'2'    HDR2/EOF2/EOV2 ?                    90320
         BNE   PUTWTO        NOTHING ELSE, YET                   90320
         IC    R0,IECIEPOS   GET FIELD POSITION                  90320
         LA    R1,HDR2TAB    GET LBL2 TABLE                      90320
         LA    R14,HDR2TAB2-HDR2TAB  SIZE                        90320
         LA    R15,HDR2TABN  LAST ENTRY                          90320
LOOKHDR  CLM   R0,1,0(R1)    MATCH ?                             90320
         BE    HAVEMSG       REJOIN ORIGINAL CODE                90320
         BXLE  R1,R14,LOOKHDR  TRY AGAIN                         90320
         B     PUTWTO        AS BEFORE                           90320
         SPACE 1
PATWTO   WTO   'MSG305W ANSI-3 ERROR - cuu,volser,file#,lbl#,STANDARDS *
               violation',ROUTCDE=11,MF=L                        90319
         SPACE 2                                                 90320
PATWTOL  EQU   *-PATWTO
MSGTYPE  EQU   10,1,C'C'     OFFSET TO W/E IN MESSAGE            90319
MSGCUU   EQU   27,3,C'C'     UCB NAME                            90319
MSGVOL   EQU   31,6,C'C'     VOLUME SERIAL                       90319
MSGSEQ   EQU   38,5,C'C'     FILE SEQUENCE                       90319
MSGLBL   EQU   44,4,C'C'     LABEL TYPE                          90319
MSGMSG   EQU   49,19,C'C'    MESSAGE TEXT                        90319
         SPACE 1                                                 90319
MESSTAB  MGER  IECIEVRS,,,'VERSION CONFLICT'                     90319
OFFERR   EQU   0,1,C'X'      ERROR FLAG                          90319
OFFACT   EQU   1,2,C'C'      ACTION FLAGS                        90319
OFFSUB   EQU   3,2,C'Y'      OFFSET TO HANDLER                   90319
OFFMSG   EQU   5,19,C'C'     MESSAGE TEXT                        90319
MESSTAB2 MGER  IECIEUNK,,CHKVAL,'UNSUPPORTED VALUE'              90320
         MGER  IECIEADJ,,,'INVALID FIELD ALIGN'                  90319
         MGER  IECIESEQ,,,'LABEL SEQUENCE ERR.'                  90319
         MGER  IECIEDUP,WW,,'DUPLICATE FILE NAME'                90320
         MGER  IECIECHR,WW,,'INVALID CHARACTER'                  90319
         MGER  IECIEXPR,WW,,'INVALID EXPIRATION'                 90319
MESSTABN MGER  IECIESYM,WW,,'SYMMETRY CONFLICT'                  90319
         SPACE 1                                                 90320
HDR2TAB  MGER  4,WW,,'UNSUPPORTED RECFM'                         90320
HDR2TAB2 MGER  5,WW,,'INVALID BLKSIZE'                           90320
HDR2TABN MGER  10,WW,,'INVALID LRECL'                            90320
         SPACE 1
         ORG   *-X'F0'
HEXTRTAB DS    X
         ORG   ,
         DC    C'0123456789ABCDEF'
         SPACE 2
SAVE     DSECT ,             WORK AND SAVE AREA
DB       DS    2D            CONVERSION, ETC.
REALWTO  DS    CL(PATWTOL)   SPACE FOR WTO
SAVEND   EQU   *             END OF DYNAMIC AREA
         SPACE 2
         CVT   DSECT=YES
         SPACE 1
         PRINT &PRTMAC       EXPAND OPEN/CLOSE/EOV MAPPINGS
XYZZY    OPSYN EJECT         DON'T WASTE PAPER -                 90320
EJECT    OPSYN ANOP          IGNORE EJECTS                       90320
         IECANWA ,                                               92240
*        IECEQU ,
         SPACE 1
         PRINT &PRTSYS                                           90319
         IEFUCBOB ,                                              90319
         END   ,
