DPRO     TITLE 'DIPLOMACY - PROVINCE DEFINITIONS'
         MACRO
&PRO     DEF   &SUPP,&ARMY,&FLEET,&FLEET2
         GBLC  &DEFPROV(100),&DEFARMY(1200),&DEFLEET(1200)
         GBLC  &DEFNAT(100),&FNAME,&FPROV,&FCOAST
        GBLB  &TWO,&OCEAN,&SUP
         GBLA  &DEFLAG(100),&DEFCOUN,&SUPCOUN,&AL(12)
         GBLA  &DEFHOME
         LCLA  &I,&J,&K,&L,&M,&N
         AIF   ('&SUPP' EQ '').NOTEND
         AIF   ('&SUPP' EQ '*END').ENDIT
.NOTEND  AIF   (K'&PRO EQ 4).GOTPRO
         MNOTE 8,'PROVINCE DEFINITION; ''&PRO'' INVALID'
         AGO   .MEND
.GOTPRO  ANOP
&FPROV   SETC  '&PRO'
&I       SETA  0
.DUPL    AIF   (&I GE &DEFCOUN).PROK
&I       SETA  &I+1
         AIF   ('&FPROV' NE '&DEFPROV(&I)' ).DUPL
         MNOTE 8,'PROVINCE ''&PRO'' PREVIOUSLY DEFINED'
         AGO   .MEND
.PROK    ANOP
&DEFCOUN SETA  &DEFCOUN+1
&DEFPROV(&DEFCOUN) SETC '&FPROV'
         DEFSUP &SUPP
         AIF   (&OCEAN AND N'&ARMY EQ 0).SKIPARM
         AIF   (NOT &OCEAN).DOARMY
         MNOTE 4,'PROVINCE ''&FPROV''; INVALID ARMY ''&ARMY'''
         AGO   .SKIPARM
.DOARMY  AIF   (N'&ARMY GT 0).SETARM
         MNOTE 4,'PROVINCE ''&FPROV''; ARMY MOVES NOT SUPPLIED'
         AGO   .SKIPARM
.SETARM  ANOP
&J       SETA  1
&K       SETA  &DEFCOUN*12-11
&L       SETA  N'&ARMY
.LOOPARM AIF   ('&ARMY(&J)' EQ '').INCARM
.MAKEARM DEFNAME &ARMY(&J)
         AIF   ('&FNAME' EQ '').INCARM
&DEFARMY(&K) SETC '&FNAME'
&K       SETA  &K+1
         AIF   ('&FCOAST' EQ '').INCARM
         MNOTE 4,'PROVINCE ''&FPROV''; ARMY ''&ARMY(&J)'' - COAST IGNOR*
               ED'
.INCARM  ANOP
&J       SETA  &J+1
         AIF   (&J GT &L).ENDARM
         AIF   (&K LE (&DEFCOUN*12) ).LOOPARM
         MNOTE 4,'PROVINCE ''&FPROV''; EXCESSIVE ARMY MOVES ''&ARMY'''
.ENDARM  ANOP
.SKIPARM AIF   (N'&FLEET GT 0).DOFL
         MNOTE 4,'PROVINCE ''&FPROV''; FLEET MOVES NOT SUPPLIED'
         MEXIT
.DOFL    AIF   (&TWO AND N'&FLEET2 GT 0).SETFLEE
         AIF   (NOT &TWO).SETFLEE
     MNOTE 4,'PROVINCE ''&FPROV''; TWO COASTS, SECOND SET NOT SUPPLIED'
.SETFLEE AIF   ('&FLEET' EQ 'INLAND' AND NOT &TWO AND NOT &OCEAN).FLGIN
&J       SETA  1
&K       SETA  &DEFCOUN*12-11
&L       SETA  N'&FLEET
.LOOPNAV AIF   ('&FLEET(&J)' EQ '').INCNAV
         DEFNAME &FLEET(&J)
         AIF   ('&FNAME' EQ '').INCNAV
&DEFLEET(&K) SETC '&FNAME'.'&FCOAST'
&K       SETA  &K+1
.INCNAV  ANOP
&J       SETA  &J+1
         AIF   (&J GT &L).ENDONE
         AIF   (&K LE (&DEFCOUN*12) ).LOOPNAV
         MNOTE 4,'PROVINCE ''&FPROV''; EXCESSIVE FLEET MOVES'
.ENDONE  AIF   (N'&FLEET2 EQ 0).MEND
         AIF   (&TWO).GOTWO
.BADTWO  MNOTE 4,'PROVINCE ''&FPROV''; FLEET MOVES FOR TWO COASTS ?'
         AGO   .MEND
.FLGIN   ANOP
&DEFLAG(&DEFCOUN) SETA &DEFLAG(&DEFCOUN)+16
         AGO   .MEND
.GOTWO   AIF   (&OCEAN).BADTWO
         AIF   ('&FLEET' EQ 'INLAND').BADTWO
&M       SETA  &DEFCOUN*12-5
         AIF   (&K LT &M).OKTWO
         MNOTE 4,'PROVINCE ''&FPROV''; EXCESSIVE FLEET1 MOVES'
.OKTWO   ANOP
&K       SETA  &M
&J       SETA  1
&L       SETA  N'&FLEET2
.LOOPTWO AIF   ('&FLEET2(&J)' EQ '').INCTWO
         DEFNAME &FLEET2(&J)
         AIF   ('&FNAME' EQ '').INCTWO
&DEFLEET(&K) SETC '&FNAME'.'&FCOAST'
&K       SETA  &K+1
.INCTWO  ANOP
&J       SETA  &J+1
         AIF   (&J GT &L).MEND
         AIF   (&K LE (&DEFCOUN*12) ).LOOPTWO
         MNOTE 4,'PROVINCE ''&FPROV''; EXCESSIVE FLEET2 MOVES'
         MEXIT
.ENDIT   ANOP
         AIF   (&DEFCOUN GT 0).ENDOK
        MNOTE 8,'NO VALID DEFINITIONS'
         MEXIT
.ENDOK   ANOP
         EJECT
&I       SETA  0
.ELOOP   AIF   (&I GE &DEFCOUN).MOUT
&I       SETA  &I+1
&K       SETA  &I*12-11
&M       SETA  &I*12
         DEFDC &I
         ALZER
&L       SETA  1
.ECLP    AIF   ('&DEFARMY(&K)' EQ '').ECINC
         DEFIND &DEFARMY(&K),0,&I,&L
         AIF   (&AL(&L) EQ 0).ECINC
&L       SETA  &L+1
.ECINC   ANOP
&K       SETA  &K+1
         AIF   (&K LE &M).ECLP
         ALPUT
         ALZER
&N       SETA  1
&J       SETA  (&DEFLAG(&I)-&DEFLAG(&I)/64*64)/32
         AIF   (&J EQ 0).EF2
&M       SETA  &M-6
&N       SETA  2
.EF2     ANOP
&L       SETA  1
&K       SETA  &I*12-11
.EFLP    AIF   ('&DEFLEET(&K)' EQ '').EFINC
         DEFIND &DEFLEET(&K),1,&I,&L
         AIF   (&AL(&L) EQ 0).EFINC
&L       SETA  &L+1
.EFINC   ANOP
&K       SETA  &K+1
         AIF   (&K LE &M).EFLP
&N       SETA  &N-1
         AIF   (&N LT 1).EFPUT
&M       SETA  &M+6
&K       SETA  &I*12-5
&L       SETA  7
        AGO   .EFLP
.EFPUT   ALPUT
         AGO   .ELOOP
.MOUT    SPACE 2
      MNOTE *,'  &DEFCOUN PROVINCES;  &SUPCOUN SUPPLY CENTERS; AND &DEF*
               HOME HOME CITIES'
         SPACE 2
.MEND    MEND
         SPACE 2
         MACRO
         DEFNAME &ARG
         GBLC  &DEFPROV(100),&DEFARMY(1200),&DEFLEET(1200)
         GBLC  &DEFNAT(100),&FNAME,&FPROV,&FCOAST
         GBLA  &DEFLAG(100),&DEFCOUN,&SUPCOUN,&AL(12)
        GBLB  &TWO,&OCEAN,&SUP
&FNAME   SETC  ''
&FCOAST  SETC  ''
         AIF   ('&ARG' EQ '').MEND
         AIF   (K'&ARG EQ 4).FOUR
         AIF   (K'&ARG EQ 7).SEVEN
.BAD     MNOTE 'PROVINCE ''&FPROV''; MOVE TO ''&ARG'' INVALID'
.MEND    MEXIT
.SEVEN   AIF   ('&ARG'(5,1) NE '-').BAD
         AIF   ('&ARG'(7,1) NE 'C').BAD
         AIF   ('&ARG'(6,1) NE 'N' AND '&ARG'(6,1) NE 'S'              *
                 AND '&ARG'(6,1) NE 'E').BAD
&FCOAST  SETC  '&ARG'(6,2)
.FOUR    ANOP
&FNAME   SETC  '&ARG'(1,4)
         MEND
         SPACE 2
         MACRO
         DEFSUP &ARG
         GBLC  &DEFPROV(100),&DEFARMY(1200),&DEFLEET(1200)
         GBLC  &DEFNAT(100),&FNAME,&FPROV,&FCOAST
         GBLA  &DEFLAG(100),&DEFCOUN,&SUPCOUN,&AL(12)
        GBLB  &TWO,&OCEAN,&SUP
         LCLA  &I,&J,&K,&L
         LCLC  &WHICH
&OCEAN   SETB  0
&SUP     SETB  0
&TWO     SETB  0
&I       SETA  N'&ARG
&J       SETA  0
&DEFNAT(&DEFCOUN) SETC '0'
         AIF   ('&ARG' EQ '').MEXIT
.LOOP    AIF   (&J GE &I).MEND
&J       SETA  &J+1
&WHICH   SETC  '&ARG(&J)'
         AIF   ('&WHICH' EQ 'OC').DOC
         AIF   ('&WHICH' EQ 'SUP').DSUP
         AIF   ('&WHICH' EQ '2CO').DO2
&K       SETA  19
&L       SETA  7
.SEECO   AIF   ('&WHICH' EQ 'AUSENGFRAGERITARUSTUR'(&K,3)).COF
&K       SETA  &K-3
&L       SETA  &L-1
         AIF   (&L GT 0).SEECO
.BAD     MNOTE 4,'PROVINCE ''&FPROV''; OPTION ''&WHICH'' INVALID'
         AGO   .LOOP
.COF     AIF   ('&DEFNAT(&DEFCOUN)' NE '0').BAD
&DEFNAT(&DEFCOUN) SETC '001002004008016032064128'(&K,3)
         AGO   .LOOP
.DOC     AIF   (&TWO OR &OCEAN OR &SUP).BAD
&OCEAN   SETB  1
         AGO   .LOOP
.DO2     AIF   (&TWO OR &OCEAN).BAD
&TWO     SETB  1
         AGO   .LOOP
.OMSUP   MNOTE 4,'PROVINCE ''&FPROV''; COUNTRY, BUT NOT SUPPLY OPTION '
.DSUP    AIF   (&SUP OR &OCEAN ).BAD
&SUP     SETB  1
.UPSUP   ANOP
&SUPCOUN SETA  &SUPCOUN+1
         AGO   .LOOP
.MEND    AIF   ('&DEFNAT(&DEFCOUN)' NE '0' AND NOT &SUP).OMSUP
&DEFLAG(&DEFCOUN) SETA &OCEAN*64+&SUP*128+&TWO*32
.MEXIT   MEND
         SPACE 2
         MACRO
         ALZER
         GBLC  &DEFPROV(100),&DEFARMY(1200),&DEFLEET(1200)
         GBLC  &DEFNAT(100),&FNAME,&FPROV,&FCOAST
        GBLB  &TWO,&OCEAN,&SUP
         GBLA  &DEFLAG(100),&DEFCOUN,&SUPCOUN,&AL(12)
         LCLA  &I
.LOOP    AIF   (&I GE 12).MEND
&I       SETA  &I+1
&AL(&I)  SETA 0
         AGO   .LOOP
.MEND    MEND
         SPACE 2
         MACRO
         ALPUT
         GBLC  &DEFPROV(100),&DEFARMY(1200),&DEFLEET(1200)
         GBLC  &DEFNAT(100),&FNAME,&FPROV,&FCOAST
        GBLB  &TWO,&OCEAN,&SUP
         GBLA  &DEFLAG(100),&DEFCOUN,&SUPCOUN,&AL(12)
 DC AL1(&AL(1),&AL(2),&AL(3),&AL(4),&AL(5),&AL(6),&AL(7),&AL(8),&AL(9),*
               &AL(10),&AL(11),&AL(12))
         MEND
         SPACE 2
         MACRO
         DEFIND &MOVE,&SW,&FROM,&IN
         GBLC  &DEFPROV(100),&DEFARMY(1200),&DEFLEET(1200)
         GBLC  &DEFNAT(100),&FNAME,&FPROV,&FCOAST
        GBLB  &TWO,&OCEAN,&SUP
         GBLA  &DEFLAG(100),&DEFCOUN,&SUPCOUN,&AL(12)
         LCLA  &I,&J,&K,&L,&M,&N,&MOC,&M2
         LCLC  &WHO,&WHICH,&WHAT
&WHO     SETC  '&DEFPROV(&FROM)'
&N       SETA  6*&SW+1
&WHICH   SETC  'ARMY  FLEET FLEET2'(&N,6)
         AIF   ('&MOVE'(1,4) NE '&WHO').OK
         MNOTE 4,'PROVINCE ''&WHO'' MOVES TO ITSELF ?'
         MEXIT
.OK      AIF   (&I GE &DEFCOUN).NOTFOUN
&I       SETA  &I+1
         AIF   ('&DEFPROV(&I)' NE '&MOVE'(1,4) ).OK
&AL(&IN) SETA  &I
&MOC     SETA  (&DEFLAG(&I)-&DEFLAG(&I)/128*128)/64
&M2      SETA  (&DEFLAG(&I)-&DEFLAG(&I)/64*64)/32
&N       SETA  &I*12-12
         AIF   ('&SW' NE '0').SEE
         AIF   (&MOC EQ 0).FINDA
         MNOTE 4,'PROVINCE ''&WHO''; ARMY MOVES TO ''&MOVE'' OCEAN ?'
&AL(&IN) SETA  0
        MEXIT
.NOTFOUN MNOTE  4,'PROVINCE ''&WHO''; &WHICH DESTINATION ''&MOVE'' NOT *
               FOUND'
.MEND    MEXIT
.NOREF   MNOTE 4,'PROVINCE ''&WHO''; &WHICH MOVE TO ''&MOVE''; BACKWARD*
                REFERENCE MISSING'
         MEXIT
.FINDA   AIF   (&N GE &I*12).NOREF
&N       SETA  &N+1
         AIF   ('&WHO' NE '&DEFARMY(&N)').FINDA
         MEXIT
.SEE     AIF   (K'&MOVE NE 6 OR &M2 NE 0).SEESAW
         MNOTE 4,'PROVINCE ''&WHO''; &WHICH MOVE TO ''&MOVE''; COAST IG*
               NORED'
.SEESAW  AIF   (K'&MOVE NE 6 AND &M2 EQ 1).NOCO
&L       SETA  &I*12
         AIF   (K'&MOVE EQ 4).FINDS
         AIF   (&M2 NE 1).FINDS
&L       SETA  &L-6
&WHICH   SETC  'FLEET1'
         AIF   ('&MOVE'(5,2) NE 'SC').FINDS
&N       SETA  &N+6
&L       SETA  &L+6
&WHICH   SETC  'FLEET2'
&AL(&IN) SETA  &I+128
.FINDS   AIF   (&N GE &L).NOREF
&N       SETA  &N+1
&WHAT    SETC  '&DEFLEET(&N)'
         AIF   ('&WHAT' EQ '').FINDS
         AIF   ('&WHO' NE '&DEFLEET(&N)'(1,4) ).FINDS
         MEXIT
.NOCO    MNOTE 4,'PROVINCE ''&WHO''; &WHICH MOVE TO ''&MOVE''; COAST NO*
               T SPECIFIED'
         MEND
         SPACE 2
         MACRO
         DEFDC &IN
         GBLC  &DEFPROV(100),&DEFNAT(100)
         GBLA  &DEFLAG(100),&DEFCOUN
         GBLA  &DEFHOME
         LCLA  &I,&J
         LCLC  &WHICH,&K,&WHO
&K       SETC  '0'           DEFAULT
.AIF     AIF   (&I GE 22).NOSET
&I       SETA  &I+1
&J       SETA  4*&I-3
&WHICH SETC 'BUDATRIEVIENEDINLIVELONDBRESMARSPARIBERLKIELMUNINAPLROMEVE*
               NIMOSCSEVASTPEWARSANKACONSSMYR'(&J,4)
         AIF   ('&WHICH' NE '&DEFPROV(&IN)' ).AIF
&K       SETC  '&DEFNAT(&IN)'
&J       SETA  3*&I-2
&WHO SETC '001001001002002002004004004008008008016016016032032032032064*
               064064'(&J,3)
         AIF   ('&WHO' EQ '&K').OKK
         MNOTE 4,'HOME PROVINCE ''&DEFPROV(&IN)'' - ILLEGAL OCCUPATION'
.OKK     ANOP
&WHO     SETC  '1212122111212111261211'(&I,1)
&DEFLAG(&IN) SETA &DEFLAG(&IN)+&WHO
&DEFHOME SETA  &DEFHOME+1
.NOSET   ANOP
PROV&DEFPROV(&IN) DC CL4'&DEFPROV(&IN)',AL1(&IN,&K,&K,&DEFLAG(&IN),&DEF*
               NAT(&IN))
         MEND
         EJECT
         ENTRY PROV#BEG,PROV#END,PROV#NAV,PROV#ARM,PROV#INL
DIPLPROV START 0
         DC    XL256'0'      PREFIX FOR CHECKPOINT AREA
         SPACE 2
************************     OCEANS     *************************
*
ADRI     DEF   OC,,(VENI,TRIE,ALBA,IONI,APUL)
AEGE     DEF   OC,,(GREE,BULG-SC,CONS,SMYR,EMED,IONI)
ARCT     DEF   OC,,(BARE,NORW,NSEA,EDIN,CLYD,NATL)
BALT     DEF   OC,,(PRUS,LIVO,BOTH,SWED,DENM,KIEL,BERL)
BARE     DEF   OC,,(STPE-NC,NORW,ARCT)
BLAC     DEF   OC,,(SEVA,ARME,ANKA,CONS,BULG-EC,RUMA)
BOTH     DEF   OC,,(SWED,BALT,LIVO,STPE-SC,FINL)
CHAN     DEF   OC,,(LOND,WALE,IRIS,MIDA,BRES,PICA,BELG,NSEA)
EMED     DEF   OC,,(IONI,AEGE,SMYR,SYRI)
HELG     DEF   OC,,(NSEA,HOLL,KIEL,DENM)
IONI     DEF   OC,,(TUNI,TYRR,NAPL,APUL,ALBA,GREE,AEGE,EMED,ADRI)
IRIS     DEF   OC,,(NATL,MIDA,CHAN,WALE,LIVE)
LYON     DEF   OC,,(SPAI-SC,MARS,PIED,TUSC,TYRR,WMED)
MIDA     DEF   OC,,(NATL,IRIS,CHAN,BRES,GASC,SPAI-SC,SPAI-NC,PORT,AFRI,*
               WMED)
NATL     DEF   OC,,(ARCT,CLYD,LIVE,IRIS,MIDA)
NSEA     DEF   OC,,(NORW,ARCT,EDIN,YORK,LOND,CHAN,BELG,HOLL,HELG,DENM, *
               SKAG)
SKAG     DEF   OC,,(NORW,NSEA,DENM,SWED)
TYRR     DEF   OC,,(TUSC,ROME,NAPL,IONI,TUNI,WMED,LYON)
WMED     DEF   OC,,(SPAI-SC,MIDA,AFRI,TUNI,TYRR,LYON)
         SPACE 2
************************     TWOCOAST     ***********************
*
BULG     DEF   (SUP,2CO),(CONS,GREE,SERB,RUMA),(CONS,BLAC,RUMA),       *
               (GREE,AEGE,CONS)
SPAI     DEF   (SUP,2CO),(PORT,GASC,MARS),(GASC,PORT,MIDA),            *
               (MARS,LYON,MIDA,PORT,WMED)
STPE     DEF   (SUP,RUS,2CO),(NORW,FINL,LIVO,MOSC),(NORW,BARE),        *
               (FINL,BOTH,LIVO)
         SPACE 2
************************     COASTAL     ************************
*
AFRI     DEF   ,TUNI,(MIDA,WMED,TUNI)
ALBA     DEF   ,(TRIE,SERB,GREE),(TRIE,ADRI,IONI,GREE)
ANKA     DEF   (SUP,TUR),(CONS,SMYR,ARME),(CONS,BLAC,ARME)
APUL     DEF   ,(VENI,ROME,NAPL),(VENI,ADRI,IONI,NAPL)
ARME     DEF   ,(SEVA,ANKA,SMYR,SYRI),(ANKA,BLAC,SEVA)
BELG     DEF   SUP,(PICA,BURG,RUHR,HOLL),(PICA,CHAN,NSEA,HOLL)
BERL     DEF   (SUP,GER),(KIEL,MUNI,SILE,PRUS),(KIEL,PRUS,BALT)
BRES     DEF   (SUP,FRA),(PICA,PARI,GASC),(PICA,CHAN,MIDA,GASC)
CLYD     DEF   ,(EDIN,LIVE),(ARCT,NATL,LIVE,EDIN)
CONS     DEF   (SUP,TUR),(BULG,ANKA,SMYR),(BULG-EC,BULG-SC,BLAC,ANKA,  *
               SMYR,AEGE)
DENM     DEF   SUP,(SWED,KIEL),(SKAG,NSEA,HELG,KIEL,BALT,SWED)
EDIN     DEF   (SUP,ENG),(CLYD,LIVE,YORK),(ARCT,NSEA,YORK,CLYD)
FINL     DEF   ,(SWED,NORW,STPE),(SWED,BOTH,STPE-SC)
GASC     DEF   ,(BRES,PARI,BURG,MARS,SPAI),(BRES,MIDA,SPAI-NC)
GREE     DEF   SUP,(ALBA,SERB,BULG),(ALBA,IONI,AEGE,BULG-SC)
HOLL     DEF   SUP,(BELG,RUHR,KIEL),(BELG,NSEA,HELG,KIEL)
KIEL     DEF   (SUP,GER),(DENM,HOLL,RUHR,MUNI,BERL),(DENM,HELG,HOLL,   *
               BERL,BALT)
LIVE     DEF   (SUP,ENG),(CLYD,EDIN,YORK,WALE),(CLYD,NATL,IRIS,WALE)
LIVO     DEF   ,(PRUS,WARS,MOSC,STPE),(PRUS,BALT,BOTH,STPE-SC)
LOND     DEF   (SUP,ENG),(YORK,WALE),(YORK,NSEA,CHAN,WALE)
MARS     DEF   (SUP,FRA),(SPAI,GASC,BURG,PIED),(SPAI-SC,LYON,PIED)
NAPL     DEF   (SUP,ITA),(ROME,APUL),(ROME,TYRR,IONI,APUL)
NORW     DEF   SUP,(SWED,FINL,STPE),(BARE,STPE-NC,ARCT,NSEA,SKAG,SWED)
PICA     DEF   ,(BRES,PARI,BURG,BELG),(BRES,CHAN,BELG)
PIED     DEF   ,(MARS,TYRO,VENI,TUSC),(MARS,LYON,TUSC)
PORT     DEF   SUP,SPAI,(MIDA,SPAI-NC,SPAI-SC)
PRUS     DEF   ,(BERL,SILE,WARS,LIVO),(BERL,BALT,LIVO)
ROME     DEF   (SUP,ITA),(TUSC,VENI,APUL,NAPL),(TUSC,TYRR,NAPL)
RUMA     DEF   SUP,(SEVA,UKRA,GALI,BUDA,SERB,BULG),(SEVA,BLAC,BULG-EC)
SEVA     DEF   (SUP,RUS),(MOSC,UKRA,RUMA,ARME),(RUMA,BLAC,ARME)
SMYR     DEF   (SUP,TUR),(CONS,ANKA,ARME,SYRI),(CONS,AEGE,EMED,SYRI)
SWED     DEF   SUP,(NORW,DENM,FINL),(NORW,SKAG,DENM,BALT,BOTH,FINL)
SYRI     DEF   ,(SMYR,ARME),(SMYR,EMED)
TRIE     DEF   (SUP,AUS),(VENI,TYRO,VIEN,BUDA,SERB,ALBA),(VENI,ADRI,   *
               ALBA)
TUNI     DEF   SUP,AFRI,(WMED,TYRR,IONI,AFRI)
TUSC     DEF   ,(PIED,VENI,ROME),(PIED,LYON,TYRR,ROME)
VENI     DEF   (SUP,ITA),(TRIE,TYRO,PIED,TUSC,ROME,APUL),(TRIE,ADRI,   *
               APUL)
WALE     DEF   ,(LIVE,YORK,LOND),(LIVE,IRIS,CHAN,LOND)
YORK     DEF   ,(EDIN,LIVE,WALE,LOND),(EDIN,NSEA,LOND)
         SPACE 2
************************     INLAND     *************************
*
BOHE     DEF   ,(SILE,MUNI,TYRO,VIEN,GALI),INLAND
BUDA     DEF   (SUP,AUS),(VIEN,TRIE,SERB,RUMA,GALI),INLAND
BURG     DEF   ,(BELG,PICA,PARI,GASC,MARS,MUNI,RUHR),INLAND
GALI     DEF   ,(WARS,SILE,BOHE,VIEN,BUDA,RUMA,UKRA),INLAND
MOSC     DEF   (SUP,RUS),(SEVA,STPE,LIVO,WARS,UKRA),INLAND
MUNI     DEF   (SUP,GER),(KIEL,RUHR,BURG,TYRO,BOHE,SILE,BERL),INLAND
PARI     DEF   (SUP,FRA),(PICA,BRES,GASC,BURG),INLAND
RUHR     DEF   ,(HOLL,BELG,BURG,MUNI,KIEL),INLAND
SERB     DEF   SUP,(BUDA,TRIE,ALBA,GREE,BULG,RUMA),INLAND
SILE     DEF   ,(PRUS,BERL,MUNI,BOHE,GALI,WARS),INLAND
TYRO     DEF   ,(BOHE,MUNI,PIED,VENI,TRIE,VIEN),INLAND
UKRA     DEF   ,(MOSC,WARS,GALI,RUMA,SEVA),INLAND
VIEN     DEF   (SUP,AUS),(BOHE,TRIE,TYRO,BUDA,GALI),INLAND
WARS     DEF   (SUP,RUS),(PRUS,SILE,GALI,UKRA,MOSC,LIVO),INLAND
         SPACE 2
PROV#BEG EQU   *
         DEF   *END          END OF DEFINITIONS
         SPACE 2
PROV#END EQU   *
PROV#ARM EQU   PROVBULG      FIRST ARMY ENTRY
PROV#NAV EQU   PROVADRI      FIRST NAVY ENTRY
PROV#INL EQU   PROVBOHE
         SPACE 3
         ORG   DIPLPROV+7280
         ENTRY DIPLWORK
DIPLWORK DS    0D            FROM #END TO HRE IS WORK AREA
         END
