STAT     TITLE 'DISPLAY MEMBER STATUS FOR SOURCE/OBJECT/LOAD LIBS'
         MACRO ,
&NM      MAPREC &DSECT=,&PFX=
         LCLC  &P
&P       SETC  '&PFX'
&NM      MACMAPHD DSECT=&DSECT,PFX=&PFX  HEADER
&P.SECT  DS    0C            RECORD START
&P.SEG   DC    X'00'         0-MEM/CSECT   1-CSECT/MEM
&P.MAJ   DC    CL8' '        MEMBER, ALIAS, OR CSECT NAME
&P.MIN   DC    CL8' '        CSECT NAME, OR MEMBER
&P.LIB   DC    X'0'          LIBRARY NUMBER
&P.TYP   DC    X'0'          SOURCE DS TYPE
&P.TS    EQU   0             SOURCE
&P.TO    EQU   1             OBJECT
&P.TL    EQU   2             LOAD
&P.LS    EQU   4             LISTING
&P.AD    EQU   8             ADATA
&P.KEYL  EQU   *-&P.SECT     LENGTH OF SORT KEY
&P.DATE  DC    CL8'YYYY.DDD'  FULL CENTURY DATE                 GP99028
*  OR    DC    CL8'YYYYMMDD'  FULL CENTURY DATE                 GP99028
&P.AT1   DC    C' '          ALIAS FLAG FOR FIRST NAME
&P.AT2   DC    C' '          CSECT FLAG FOR SECOND NAME
&P.LEN   EQU   *-&P.SECT     LENGTH OF SORT &P.ORD
         MEND  ,
         SPACE 1
         PUNCH '  ORDER EXSTATIC(P)'   MAKE DUMPS EASIER        GP05105
         SPACE 1
         COPY  OPTIONGB                                          81194
         SPACE 1                                                 81194
         SYSPARM LIST=YES                                        81194
         EJECT ,
         PRINT &PRTSOR                                           81194
EXSTATIC PGMHEAD ZERO12,BASE=(R11,R12),PARM=R7,BNDRY=PAGE,AM=31,RM=24
*
*   THIS MODULE WILL PRODUCE COLLATED INFORMATION FOR LIBRARIES.
*   EACH LIBRARY PRESENTED BY A DD IS SCANNED FOR MEMBER
*   INFORMATION.  FOR SOURCE LIBRARIES, A DATE IS OBTAINED :
*     FOR SPF AND IEBUPDTX FILES, THE LAST CHANGE DATE.          82239
*     FOR OTHER FILES (WYLBUR, LOCAL), SSI IS ASSUMED TO BE
*     IN 0YYYYDDD OR YYYYDDDF FORM.                              82239
*     (OLD 000YYDDD AND 00YYDDDF ENTRIES ARE TREATED AS 19YY)
*   FOR OBJECT LIBRARIES, THE DATE EXTRACTED FROM THE ESD CARD
*   IS USED.                                                     82239
*   FOR LOAD-MODULE LIBRARIES, THE LINK-EDIT DATE AND THE        82239
*   IS USED.                                                     82239
*   ASSEMBLY/COMPILE DATES FOR EACH CSECT ARE EXTRACTED. THE
*   SSI (IF IN DATE FORMAT) IS USED AS A DEFAULT.                82239
*   SYSPRINT - VBA,133 LISTING WITH HEADINGS
*                                                                     *
*   SORTWK01-03;SYSOUT;SORTLIB - REQUIRED FOR SORT INTERFACE
*   SORTIN/SORTOUT - TEMPORARY SORT FILES                        82239
*---------------------------------------------------------------------*
*                                                                     *
*   CHANGED TO REPLACE SORT (0C4) BY SUBTREE ROUTINE                  *
*     NO 'SORT---' DD CARDS REQUIRED                            GP05105
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*   PARM TOPIONS ADDED:                                               *
*                                                                     *
*     DUPE    - ADDS :: FLAG FOR MORE THAN ONE ENTRY PER LINE         *
*                                                                     *
*     TERSE   - DO MEMBERS ONLY; NO ALII OR CSECTS                    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2                                                 81151
         SERVINIT ,          LOAD SERVICE ROUTINE                90303
         SERVLOAD @PRINTER,SUBTREE  LOAD REQUIRED MODULES       GP05105
         MVC   ROOTBALL(PATBALLN),PATBALL  INIT TREE WORK       GP05105
         SPACE 2                                                GP99028
         MVI   PROFLAGS,FCMEM  SET 'PREVIOUS DSN HAD MEMBERS'   GP99028
         L     R8,ESDTABLE   GET SPECIAL INITIALIZATION CODE     80340
         BALR  R10,R8        GO TO IT                            80340
         TIME  ,             GET THE DATE                        81144
         SRL   R1,4          DELETE THE SIGN DIGIT               81144
         ST    R1,HEXDATE    STASH IT                            81144
         SUBCALL SUBTREE,('INI',ROOTBALL),VL,MF=(E,CALLPARM)    GP05105
         SPACE 1
         XC    NEWREC(NEWLEN),NEWREC  CLEAR RECORD
         XC    NEXTTIOT,NEXTTIOT  CLEAR TIOT START               81151
         PARMLOAD R14        GET PARM                           GP10139
         LTR   R15,R15       GET PARM LENGTH                    GP10139
         BNP   SKIPTIOT        NONE                             GP10139
         SH    R15,=H'3'     LONG ENOUGH?                       GP10212
         BM    SKIPTIOT        NO                               GP10212
         LA    R15,1(,R15)   SET FOR BCT                        GP10212
PARMLOOP CLC   =C'TER',0(R14)  SKIP ALII AND CSECTS ?           GP10212
         BNE   PARMDUPE                                         GP10212
         OI    PROFLAGS,FGTERSE   REQUEST MINIMAL OUTPUT        GP10212
         BNE   PARMBUMP                                         GP10212
PARMDUPE CLC   =C'DUP',0(R14)  FLAG MULTIPLE ENTRIES?           GP10139
         BNE   SKIPTIOT                                         GP10139
         OI    PROFLAGS,FGDUPE    REQUEST FLAGGING              GP10139
PARMBUMP LA    R14,1(,R14)   TRY AGAIN                          GP10212
         BCT   R15,PARMLOOP                                     GP10212
         SPACE 1
         USING TIOENTRY,R2                                       81151
SKIPTIOT L     R1,NEXTTIOT   GET PREVIOUS TIOT ENTRY             81151
         SERVCALL TIOLK,(R1),ERR=ENDALL   GET NEXT ONE OR QUIT   81151
         LTR   R2,R0         COPY IT                             81151
         BZ    ENDALL        ALL DONE                            81151
         ST    R2,NEXTTIOT   SET FOR RESTART                     81151
         CLC   =C'SORT',TIOEDDNM
         BE    SKIPTIOT
         CLC   =C'SYS0',TIOEDDNM                                 87242
         BE    SKIPTIOT                                          87242
         CLC   =C'PRINT',TIOEDDNM+3  DEBPRINT, TRACE PRINT
         BE    SKIPTIOT
         CLC   =C'TRACE',TIOEDDNM+3  DEBPRINT, TRACE PRINT
         BE    SKIPTIOT
         CLI   TIOELNGH,20   ONE UCB ENTRY ?
         BNE   SKIPTIOT
         CLI   TIOELNGH+20,0  NEXT IS END OF TIOT ?
         BE    UNCNTIOT      YES, SKIP NEXT TEST
         CLI   TIOEDDNM+20,C' '  CONCATENATION ?
         BE    SKIPTIOT      NOT SUPPORTED
UNCNTIOT SR    R3,R3
         ICM   R3,7,TIOEFSRT  LOAD UCB ADDRESS
         BZ    SKIPTIOT      SKIP DUMMY
         USING UCBOB,R3                                          81151
         CLI   UCBTBYT3,UCB3DACC  DISK ?
         BNE   SKIPTIOT
         SERVCALL TIODD,TIOEDDNM  CHECK FOR TIOT ENTRY BY DDNAME
         CR    R0,R2         IF NOT EQUAL, USER HAS DUPLICATE DD 81151
         BNE   SKIPTIOT      IF SO, IGNORE SECOND ONE            81151
         MVC   PDS+DCBDDNAM-IHADCB(L'DCBDDNAM),TIOEDDNM
         MVC   POD+DCBDDNAM-IHADCB(L'DCBDDNAM),TIOEDDNM   COPY TO DCBS
         MVC   BADFORM+26(8),TIOEDDNM  SET IN ERROR MESSAGE      80340
         DROP  R2                                                81151
         SPACE 1
         RDJFCB MF=(E,OP1)
         BXH   R15,R15,SKIPTIOT  SKIP IF BAD
         OBTAIN FMT1         GET THE DATASET INFO
         BXH   R15,R15,SKIPTIOT  SKIP IF BAD
         LA    R1,OBTWORK-L'DS1DSNAM
         USING DS1DSNAM,R1   DECLARE IT
         CLI   DS1DSORG+1,0       PDS ?                         GP09186
         BNE   SKIPTIOT      NO; IGNORE                         GP09186
         TM    DS1DSORG,JFCORGPO  PDS ?
         BZ    SKIPTIOT      NO; IGNORE
         MVI   MEMFLAGS,0    RESET FLAGS                        GP05110
         TM    DS1RECFM,JFCUND  UNDEFINED ?
         BO    DOFRESH       YES; PROCESS FURTHER
         TM    DS1RECFM,DCBRECCA+DCBRECCM  CARRIAGE CONTROL ?
         BZ    DONOCC
         CLC   DS1LRECL,=H'121'  POSSIBLE (X)F ASM ?
         BL    DONOCC        NO
         CLC   DS1LRECL,=H'137'  OR UP TO HLASM ?
         BH    DONOCC        NO
         OI    MEMFLAGS,FLIST  ASSUME PROGRAM LISTING
         B     DOFRESH       JOIN COMMON
DONOCC   TM    DS1RECFM,JFCFIX  FIXED ?
         BZ    DONONF                                            94024
         CLC   =H'80',DS1LRECL  CARD FORMAT ?
         BE    DOFRESH                                           94024
DONONF   OI    MEMFLAGS,FGVBLK    SET NON-STANDARD DATASET       94024
         B     DOFRCOM                                           94024
DOFRESH  NI    MEMFLAGS,255-FGVBLK   RESET                       94024
DOFRCOM  CLI   DS1KEYL,0                                         94024
         BNE   SKIPTIOT
         TM    DS1SMSFG,DS1PDSE  PDS/E DATASET?                 GP99028
         BNZ   SKIPSTAR      YES; LSTAR NEVER SET               GP99028
         OC    DS1LSTAR,DS1LSTAR  INITIALIZED ?
         BZ    SKIPTIOT                                         GP99028
SKIPSTAR OC    DS1BLKL,DS1BLKL  ANY BLOCK SIZE ?                GP99028
         BZ    SKIPTIOT      NO; SKIP
         DROP  R1
         OI    JFCBTSDM,JFCNWRIT+JFCNDCB  HANDS OFF MY JOB QUEUE 81184
         NI    JFCBIND1,255-JFCBPMEM  TURN QSAM PDS MEMBER FLAG OFF
         MVC   JFCBELNM,BLANKS
         OPEN  MF=(E,OP1),TYPE=J
         TM    POD+DCBOFLGS-IHADCB,DCBOFOPN  OPENED OK ?         81048
         BZ    BADFORM       NO; COMPLAIN
         OPEN  (PDS,INPUT),TYPE=J
         TM    PDS+DCBOFLGS-IHADCB,DCBOFOPN  OPENED OK ?         81048
         BZ    BADFORM       NO; COMPLAIN
         MVC   OUT,OUT-1     CLEAR THE PRINT LINE                80340
         AIF   ('&LOCAL' NE 'PID').NOLOSSI                       94024
         NI    MEMFLAGS,255-FGLOCSSI   RESET FUNNY SSI FLAG      81144
         CLC   =C'SYS1.',JFCBDSNM   SYSTEM PREFIX ?              81144
         BNE   NOLOCSSI      NO                                  81144
         CLC   =C'USERPROC ',JFCBDSNM+5  OTHER PROCLIB ?         81144
         BNE   NOLOCSSI      NO                                  81144
STLOCSSI OI    MEMFLAGS,FGLOCSSI   SPECIAL SSI PROCESSING FLAG   81144
NOLOCSSI EQU   *                                                 81144
.NOLOSSI MVC   DSJDDNM,BADFORM+26  ADD DDNAME TO DETAIL          81144
         MVC   DSJDSN,JFCBDSNM  SHOW DSNAME                      80340
         MVC   DSJVOL,UCBVOLI   VOLUME SERIAL                    80340
         MVC   DSJUCB,UCBNAME   AND UNIT                         80340
         SERVCALL UCBGN,UCBOB  GET GENERIC NAME FOR DEVICE       82239
         MVC   DSJUTYP,0(R1) COPY GENERIC NAME (OR BLANKS)       82239
         DROP  R3                                                81151
         SR    R1,R1
         IC    R1,NEWLIB     GET LIBRARY COUNTER
         TM    PROFLAGS,FCMEM   MEMBER FOUND IN PREVIOUS LIBRARY ?
         BZ    SKIPEMTY      NO; SKIP INCREMENTING
         LA    R1,1(,R1)
SKIPEMTY NI    PROFLAGS,255-FCMEM  RESET MEMBER FOUND
         CH    R1,=AL2(MAXLIBS)  REACHED MAXIMUM SUPPORTED?     GP99028
         BH    LIBMAXEX      YES; TOO BAD
         STC   R1,NEWLIB     STASH BACK
         CVD   R1,CROSS      MAKE PACKED                         80340
         MVC   DSJNUM,=X'40202120'                               80340
         ED    DSJNUM,CROSS+6   EDIT COLUMN NUMBER               80340
         MH    R1,=AL2(OUTLEN)  MAKE OFFSET TO DSN              GP99028
         LA    R2,SUBDSN+1-OUTLEN(R1)  GET TARGET DSN ADDRESS   GP99028
      SERVCALL DSABB,JFCBDSNM      ABBREVIATED DSNAME            81184
         MVC   0(8,R2),0(R1)                                     81184
         MVC   DSJCOL,0(R1)  SHOW COLUMN HEADER                  81184
         MVC   SUBHEAD1-SUBHEAD2(8,R2),BADFORM+26  COPY DDNM     81184
         PRTV  ROUT                                              81144
         GETBUF PDS,R5                                           80340
         LR    R10,R5        SAVE FIRST BUFFER
         GETBUF PDS,R9
         STM   R9,R10,BUFSAVE   SAVE BUFFER ADDRESSES            80340
         MVC   PDRECFM,PDS+DCBRECFM-IHADCB  COPY RECFM           82239
         EJECT ,
         USING DIRECTRY,R3
READIR   GET   POD                      READ A DIRECTORY BLOCK
         LH    R2,0(,R1)     GET LOGICAL BLOCK LENGTH            82239
         BCTR  R2,0            LESS ONE FOR BXH                  82239
         LA    R3,0(R1,R2)   LOGICAL BLOCK END                   82239
         LA    R2,2          LENGTH OF FIRST ENTRY               82239
         STM   R1,R3,DEBXLE  SAVE POINTERS                       82239
USEDIR   LM    R1,R3,DEBXLE  GET DIRECTORY POINTERS              82239
         BXH   R1,R2,READIR  NEXT BLOCK IF THIS ONE EXHAUSTED    82239
         STM   R1,R3,DEBXLE  SAVE FOR NEXT TIME                  82239
         LR    R3,R1         COPY TO SAFE REGISTER               82239
         LA    R6,80         SET BXLE INCREMENT
         USING MEMNAME,R3
         CLI   MEMNAME,X'FF'   END OF DIRECTORY ?                82239
         BE    ENDIR                     YES - TREAT AS EOF
         SERVCALL PDSDE,DEBXLE  ANALYZE DIRECTORY ENTRY          82239
         LR    R15,R0        GET EXTENDED RETURN                 82239
         MVC   PDRALIS(PDRBLNK),0(R15)  COPY RETURN              82239
         TM    PROFLAGS,FGTERSE   SKIP ALIAS ENTRIES ?          GP10212
         BZ    KEEPALII        NO                               GP10212
         CLI   PDRALIS,C' '  IS THIS AN ALIAS ?                 GP10212
         BNE   USEDIR          YES; JUST SKIP IT                GP10212
KEEPALII MVC   NEWMAJ,PDRNAME  COPY EDITED NAME                  82239
         MVC   NEWAT1,PDRALIS  SET ALIAS FLAG                    82239
         LA    R0,PDRDATE    POINT TO RETURNED DATE, OR BLANKS  GP99028
         BAL   R14,OLDDATE   PROCESS 3-BYTE DATE FORM           GP99028
         XC    DECOMP(DEBXLE-DECOMP),DECOMP CLEAR WYLBUR         82239
         XR    R0,R0                                             82239
         BCTR  R0,0                                              82239
         ST    R0,DCMLINEP   SET PREVIOUS LINE                   82239
         LM    R9,R10,BUFSAVE   RELOAD BUFFER ADDRESSES          80340
         MVC   NEWMIN,NEWMAJ  PROVISIONALLY SET CSECT NAME
         MVI   NEWAT2,C' '   FLAG AS CSECT                      GP05110
         XC    IDRWORK,IDRWORK   CLEAR ASM IDR WORK SPACE        80340
         MVC   ESDPTR(12),ESDTABLE  RESTORE ESD TABLE POINTERS   80340
         AIF   ('&LOCAL' NE 'PID').NOACCT                        94024
         TM    ENTLEN,X'1F'-1           MUST BE AT LEAST 2 HALFWORDS
         BZ    NOSSI
         TM    MEMFLAGS,FGLOCSSI   SPECIAL SSI DATASET ?         81144
         BZ    NOSSI         NO                                  82239
         XR    R1,R1                                             81144
         ICM   R1,3,USERDATA   SAVE ACCOUNT NUMBER               81144
         ICM   R15,12,HEXDATE  GET TENS PORTION OF YEAR          81144
         ICM   R15,3,USERDATA+2  GET UNITS AND JULIAN DAY        81144
         CLC   USERDATA+2(2),HEXDATE+2  WRAP-AROUND ?            81144
         BNH   USELOSSI      NO                                  81144
         SL    R15,=X'00010000'  MAKE PRIOR DECADE               81144
USELOSSI STCM  R15,15,USERDATA   STASH ADJUSTED SSI BACK         81144
         UNPK  CROSS(7),USERDATA+1(4)                            82249
         TR    CROSS+1(5),HEXTAB   MAKE PRINTABLE                82249
         MVC   PDRDATE,CROSS+1  MAKE FAKE DIRECTORY INFO         82249
         LA    R0,PDRDATE    POINT TO RETURNED DATE, OR BLANKS  GP99028
         BAL   R14,OLDDATE   PROCESS 3-BYTE DATE FORM           GP99028
         SERVCALL ACCON,(R1)  CONVERT ACCOUNT TO EBCDIC          81151
         CH    R15,=H'4'     ERROR ?                             81144
         BH    NOSSI         YES; OMIT                           82239
         STCM  R0,15,NEWMAJ  FAKE ACCOUNT AS CSECT NAME          81144
         MVC   NEWMAJ+4(4),BLANKS  BLANK REST                    81144
.NOACCT  SPACE 1                                                 82239
NOSSI    MVC   FINDWORD(3),MEMTTR       SET UP FOR FIND
         FIND  PDS,FINDWORD,C
         NI    MEMFLAGS,FMEM     CLEAR PROCESSING FLAG
         TM    MEMFLAGS,FGVBLK   NON-STANDARD DATASET?           94024
         BNZ   ENDMEM2       YES; SKIP READS                     94024
         READ  DECB,SF,,(R10),'S',MF=E    PRIME BUFFERS
NEWBLOCK L     R5,DECAREA               SWAP BUFFERS
         LR    R10,R5        SAVE FOR LATER
         CHECK DECB
         TM    MEMFLAGS,FLOAD    PREVIOUS LOAD ENTRY ?
         BNZ   NEWBLOKT      YES; DON'T RE-INITIALIZE
         BAL   R8,DEBLOCK    INFORM OF NEW BLOCK
         NOP   0
NEWBLOKE TM    PDS+DCBRECFM-IHADCB,DCBRECU  WYLBUR OR LOAD ?     81048
         BO    NEWBLOKT      YES; SKIP BLOCK ADDRESS
         LH    R7,PDS+DCBBLKSI-IHADCB   FIND BLOCK END FOR BXLE
         L     R1,DECIOBPT   GET IOB
         SH    R7,14(,R1)    SUBTRACT RESIDUAL COUNT (ON RPS ??)
         AR    R7,R10
         BCTR  R7,0
NEWBLOKT READ  DECB,SF,,(R9),'S',MF=E
         LR    R9,R10                   SAVE BUFFER START LOC FOR SWAP
         TM    MEMFLAGS,FLIST  LISTING ?                        GP05110
         BNZ   LISTCONT      YES; PROCEED                       GP05110
         TM    MEMFLAGS,FLOAD    READ FOR LOAD MODULE ?
         BNZ   LOADCONT      YES; CONTINUE
         TM    MEMFLAGS,FOBJ     OBJECT DECK ?
         BNZ   NEXTCARD      YES; CONTINUE
         CLI   0(R5),X'02'   OBJECT DECK ?
         BNE   ISSOURCE
         OI    MEMFLAGS,FOBJ     SIGNAL OBJECT DECK
         MVI   NEWTYP,RECTO  SHOW OBJECT
NEXTCARD CLC   =X'02C5E2C4',0(R5)  ESD CARD ?                    80340
         BNE   LOOKEND       NO; GET ANOTHER                     80340
         STM   R1,R9,PRESSAVE  SAVE REGISTERS                    80340
         LH    R2,14(,R5)    GET STARTING ESD ID                 80340
         LA    R3,16(,R5)    POINT TO DATA                       80340
         LH    R5,10(,R5)    GET LENGTH OF DATA                  80340
         B     ESDOBJ        GO TO LOAD/OBJECT COMMON CODE       80340
LOOKEND  CLC   =X'02C5D5C4',0(R5)  END ?
         BNE   ENDCARD       NO; LOOK FOR MORE
         LA    R0,47(R5)     POINT TO ASSEMBLY DATE             GP99028
         BAL   R14,OLDDATE   PROCESS 3-BYTE DATE FORM           GP99028
         STM   R1,R9,PRESSAVE  SAVE REGISTERS                    80340
         B     ESDLAST       NOW WRITE THE ESD INFORMATION       80340
ENDCARD  BXLE  R5,R6,NEXTCARD
         BAL   R8,DEBLINE    UNBLOCK ANOTHER LINE
         B     NEXTCARD      IF BUFFER NOT EMPTY YET
         B     NEWBLOCK
         SPACE 1
LISTCONT LA    R6,1          SET SCAN INCREMENT                 GP05110
LISTLOOK CLC   =C'ASM ',0(R5)  LOOK FOR ASSEMBLER TIME/DATE STAMGP05110
         BE    LISTDATE                                         GP05110
LISTLUMP BXLE  R5,R6,LISTLOOK                                   GP05110
         B     NEWBLOCK                                         GP05110
LISTDATE CLI   17(R5),C'/'   POSSIBLE DATE ?                    GP05110
         BNE   LISTLUMP      NOT YET                            GP05110
         CLI   20(R5),C'/'   REALLY?                            GP05110
         BNE   LISTHLAS      NO                                 GP05110
         MVC   DB(2),=C'00'  DON'T KNOW CENTURY                 GP05110
         MVC   DB+2(2),21(R5)  COPY YEARS                       GP05110
         MVC   DB+4(2),15(R5)  COPY MONTH                       GP05110
         MVC   DB+6(2),18(R5)  COPY DAY                         GP05110
         B     LISTACOM                                         GP05110
LISTHLAS CLI   14(R5),C'/'   HIGH-LEVEL ASSEMBLER?              GP05110
         BNE   LISTLUMP      NO                                 GP05110
         MVC   DB+0(4),10(R5)  COPY YEARS                       GP05110
         MVC   DB+4(2),15(R5)  COPY MONTH                       GP05110
         MVC   DB+6(2),18(R5)  COPY DAY                         GP05110
LISTACOM MVC   NEWMIN,NEWMAJ  MEMBER=SECTION (LATER FIND SD/ETC?GP05110
         MVI   NEWAT2,C' '   FLAG AS CSECT                      GP05110
         LA    R1,DB                                            GP05110
         BAL   R8,CAL2JUL    CONVERT CALENDAR DATE TO JULIAN    GP05110
         MVC   NEWDATE,DB    ADD TO RECORD                      GP05110
         MVI   NEWTYP,NEWLS  IDENTIFY LISTING                   GP05110
         BAL   R8,PUTTER     MAKE AN ENTRY                      GP05110
         B     ENDMEM        NEXT MEMBER                        GP05110
         SPACE 1
ISSOURCE MVI   NEWTYP,NEWTS  SHOW SOURCE
         BAL   R8,PUTTER
ENDMEM   CHECK DECB          ENSURE COMPLETION OF I/O
ENDMEM2  TM    MEMFLAGS,FGPUT   ANYTHING PUT OUT FOR THIS MEMBER ?
         BNZ   ENDMEM3       YES                                 80340
         MVC   NEWMIN,NEWMAJ                                     80340
         MVI   NEWAT2,C' '   FLAG AS CSECT                      GP05110
         BAL   R8,PUTTER     PUT OUT A DUMMY ENTRY               80340
ENDMEM3  B     USEDIR        GET NEXT DIRECTORY ENTRY            82239
         SPACE 2
LIBMAXEX PRTV  TOOMANY       TELL ABOUT TOO MANY LIBRARIES       81144
         OI    PROFLAGS,FCMEM   PREVENT 'EMPTY' MESSAGE          80340
         B     ENDIR1        SKIP FREEPOOL                       82239
         SPACE 1
ENDIR    CLOSE MF=(E,OP1)                                        /
         CLOSE PDS
         FREEPOOL PDS
         FREEPOOL POD
ENDIR1   TM    PROFLAGS,FCMEM  ANY MEMBER DONE IN THIS LIBRARY?  82239
         BNZ   ENDIR2        YES                                 80340
         PRTV  LIBNULL       NO; SHOW EMPTY                      81144
ENDIR2   OI    PROFLAGS,DSFOUND  SHOW LIBRARY FOUND
         B     SKIPTIOT      GET ANOTHER LIBRARY
         SPACE 1                                                GP99028
OLDDATE  STM   R14,R12,12(R13)  SAVE A FEW REGISTERS            GP99028
         LR    R4,R0         COPY CALLER'S DATE ADDRESS         GP99028
         MVC   NEWDATE,BLANKS  CLEAR DATE FIELD                 GP99028
         CLI   0(R4),C' '    ANY DATE?                          GP99028
         BNH   OLDDATEX      OMITTED - LEAVE IT ALONE           GP99028
         CLI   0(R4),C'7'    BEFORE 1970 ? (YES; I HAVE 197X DATES!)
         BL    OLDDATE2      NO; WINDOW AS 20XX                 GP99028
         MVC   NEWDATE(2),=C'19' SET OLD DATE                   GP99028
         B     OLDDATEC                                         GP99028
OLDDATE2 MVC   NEWDATE(2),=C'20'                                GP99028
OLDDATEC MVC   NEWDATE+2(2),0(R4)  MOVE YY                      GP99028
         MVI   NEWDATE+2+2,C'.'  MAKE SEPARATOR                 GP99028
         MVC   NEWDATE+2+2+1(3),2(R4)  MODE DDD                 GP99028
*  LATER ADD CODE FOR YYYYMMDD FORMAT (PARM=CAL VS. PARM=JUL)   GP99028
OLDDATEX LM    R14,R12,12(R13)  RESTORE                         GP99028
         BR    R14           RETURN TO CALLER                   GP99028
         SPACE 2
ENDALL   TM    PROFLAGS,DSFOUND+FGMEM  ANYTHING OUT THERE ?
         BO    PHASE2
         PRTV  NOWORK                                            81144
         B     USERABND
         SPACE 1
PHASE2   PRTV  SUBHEAD1,TITLE=3,CC=NO                            81144
         PRTV  SUBHEAD2,TITLE=4,CC=NO                            81144
         XC    SORDATE,SORDATE                                   80340
         SUBCALL SUBTREE,('LIST',ROOTBALL),VL,MF=(E,CALLPARM)   GP05105
         LTR   R15,R15       ANY ?                              GP05105
         BNZ   ENDSORT       NO; DONE                           GP05105
         LR    R6,R1         PRESERVE RECORD ADDRESS            GP05105
         USING MEMREC,R6                                        GP05105
         B     GETSTAKK      AND FORCE PROCESSING
         SPACE 1
GETSORT  SUBCALL SUBTREE,('NEXT',ROOTBALL),VL,MF=(E,CALLPARM)   GP05105
         LTR   R15,R15       ANY ?                              GP05105
         BNZ   ENDSORT       NO; DONE                           GP05105
         LR    R6,R1         PRESERVE RECORD ADDRESS            GP05105
         USING MEMREC,R6                                        GP05105
         CLC   PRESEG,RECSEG  NEW PASS?                         GP05110
         BNE   GETBREAD      YES
         CLC   PREMEM,RECMAJ  SAME MODULE ?
         BNE   GETBREAD      NO                                  80340
         CLC   OUTMEM,RECMIN  SAME MEMBER ?
         BNE   GETBREAK      BREAK, BUT LEAVE DATE INTACT        80340
         TM    PROFLAGS,FGDUPE    FLAG MULTIPLE ENTRIES?        GP10139
         BZ    GETSTACK        NO                               GP10139
         MVI   OUTDUPE,C':'  SIGNAL MULTIPLE ENTRIES            GP10139
         MVI   OUTDUPE+1,C':'  SIGNAL MULTIPLE ENTRIES          GP10139
         B     GETSTACK                                         GP10139
GETBREAD XC    SORDATE,SORDATE                                  GP99028
GETBREAK CLC   OUTNAM,OUTMEM  SAME CSECT AND MEMBER?            GP05110
         BNE   GETBREAP       NO                                GP05110
         MVC   OUTMEM,BLANKS  SAME CSECT AND MEMBER?            GP05110
GETBREAP PRTV  ROUT          PRINT PREVIOUS ONE                  81144
         CLC   PRESEG,RECSEG  NEW PASS?                         GP05110
         BE    GETSTAKK      YES                                GP05110
         MVC   PRESEG,RECSEG  SET NEW PASS                      GP05110
         MVC   SUBDSNB,SUBDSN                                   GP05110
         PRTV  SUBHEADB,TITLE=4,CC=NO                           GP05110
GETSTAKK MVC   PREMEM,RECMAJ  MOVE PREVIOUS NAME
         MVC   OUT,OUT-1     CLEAR TEXT
GETSTACK MVC   OUTNAM,RECMAJ SET MODULE NAME
         MVC   OUTMEM,RECMIN SHOW MEMBER NAME
         MVC   OUTNMAL,RECAT1
         MVC   OUTALIAS,RECAT2
         CLI   RECTYP,RECTS  SOURCE ENTRY ?
         BNE   GETSTNSO      NO
         CLI   SORDATE,0     PREVIOUSLY SET ?                   GP99028
         BE    GETSTSDA      NO; SET DATE
         CLC   SORDATE,RECDATE  MULTIPLE SOURCE; CHECK DATES    GP99028
         BNL   GETSTNSO      EQUAL OR HIGH; LEAVE
         XR    R1,R1         PREVIOUS ONE IS BACK DATE OR BACKUP
         IC    R1,SORLIB     GET ITS INDEX
         MH    R1,=AL2(OUTLEN)                                  GP99028
         LA    R1,OUTONE-OUTLEN(R1)                             GP99028
         MVI   0(R1),C'<'    FLAG BACKDATE VERSION
GETSTSDA MVC   SORLIB,RECLIB SAVE LIBRARY NUMBER
         MVC   SORDATE,RECDATE  SAVE DATE                       GP99028
GETSTNSO XR    R4,R4
         IC    R4,RECLIB
         MH    R4,=AL2(OUTLEN)                                  GP99028
         LA    R4,OUTONE-OUTLEN(R4) POINT TO FIRST ONE          GP99028
         CLI   2(R4),C' '    EMPTY ENTRY ?                      GP99028
         BE    GETDUPBL      YES; USE IT                         80340
         CLC   2(L'OUT1DATE,R4),RECDATE  DUPLICATE ?            GP99028
         BE    GETDUPL       YES; JUST REDO                      80340
         CLC   OUTNAM,OUTMEM  SAME CSECT AND MEMBER?            GP05110
         BNE   GETDUPP        NO                                GP05110
         MVC   OUTMEM,BLANKS  SAME CSECT AND MEMBER?            GP05110
GETDUPP  PRTV  ROUT          ELSE FORCE A NEW LINE               81144
         MVI   OUTALIAS,C' '                                     80340
GETDUPBL MVI   0(R4),C' '    CLEAR BACK-LEVEL FLAG               80340
GETDUPL  MVC   2(L'OUT1DATE,R4),RECDATE  SHOW DATE              GP99028
         OC    OUTNMAL,RECAT1  PROPAGATE CSECT FLAG             GP05110
         CLC   SORDATE,RECDATE UP-TO-DATE MODULE ?              GP99028
         BNH   GETDUNFG      YES                                 80340
         MVI   0(R4),C'<'    NO; FLAG IT
GETDUNFG OC    OUTALIAS,RECAT2                                  GP05110
         CLC   2(L'OUT1DATE,R4),BLANKS ANY DATE AT ALL ?        GP99028
         BNE   GETSORT       YES, OK
         MVC   3(3,R4),=C'???'
         B     GETSORT
         SPACE 1
ENDSORT  CLC   OUTNAM,OUTMEM  SAME CSECT AND MEMBER?            GP05110
         BNE   ENDSORT2       NO                                GP05110
         MVC   OUTMEM,BLANKS  SAME CSECT AND MEMBER?            GP05110
ENDSORT2 PRTV  ROUT          PUT OUT LAST RECORD                 81144
ENDJOB   SUBCALL SUBTREE,('END',ROOTBALL),VL,MF=(E,CALLPARM)    GP05105
         SERVTERM ,          CLOSE PRINTER AND SERVICE           81151
         L     R9,RETCODE    GET RETURN CODE
         PGMEXIT RC=(R9)     AND GET OUT
         DROP  R6
         SPACE 1
         PUSH  USING
         DROP  ,
         USING BPAMSIN,R15
BPAMSIN  SYNADAF ACSMETH=BPAM
         STM   R14,R12,12(R13)
         BAL   R9,COMMSIN
         LM    R14,R12,12(R13)
         SYNADRLS ,
         BR    R14
         USING BSAMSIN,R15
BSAMSIN  SYNADAF ACSMETH=BSAM
         STM   R14,R12,12(R13)
         BAL   R9,COMMSIN
         LM    R14,R12,12(R13)
         SYNADRLS ,
         BR    R14
         USING QSAMSIN,R15
QSAMSIN  SYNADAF ACSMETH=QSAM
         STM   R14,R12,12(R13)
         BAL   R9,COMMSIN
         LM    R14,R12,12(R13)
         SYNADRLS ,
         BR    R14
         SPACE 1
         DROP  R15
COMMSIN  BALR  R11,0         MAKE COMMON BASE
         USING *,R11
         MVC   TSBUF,BLANKS             ASSUME NO BUFFER ADDRESS
         ST    R0,SYNADCOD
         ST    R1,SYNADBUF
         LTR   R0,R0                    SUCCESSFUL?
         BZ    TSMNOBUF                  YES
         SH    R0,=H'8'                   MAYBE
         BL    TSBUFF                    YES
         MVC   TSBUF,=CL22'SYNADF R0=8'
         B     TSMNOBUF
TSBUFF   MVC   TSBUF,=CL22'BUF=XXXXXX, SIZE=XXXX.'
         UNPK  TSBUF+4(7),9(4,R1)       MOVE IN BUFFER LOC
         TR    TSBUF+4(6),HEXTAB
         MVI   TSBUF+10,C','
         UNPK  TSBUF+17(5),12(3,R1)     MOVE IN BUFFER SIZE
         TR    TSBUF+17(4),HEXTAB
         MVI   TSBUF+21,C'.'
TSMNOBUF MVC   TSERRMSG,50(R1)          MOVE SYNADF BUFFER TO MSG
         WTO   MF=(E,TS)
*DEBUG*  BR    R9
         SPACE 2
*              THIS FEATURE WAS NEVER OFFICIALLY AVAILABLE
*              IT WAS NEVER IN MVT
*              THE ADVENT OF ROUTCDE=11 HAS PROBABLY KILLED IT FOR GOOD
         L     R1,=X'81001000'           DEAR ABBY
         LA    R0,TS                      I HAVE A MESSAGE FOR YOU
         SVC   13            GOODBYE                             82239
         POP   USING
         EJECT ,
*   THE DEBLOCK ROUTINE CHECKS FOR RECFM=U, THEN TRIES TO DEBLOCK
*   A WYLBUR BUFFER. IF IT IS NOT A WYLBUR BUFFER, AND THE DIRECTORY
*   ENTRY IS FOR A LOAD MODULE, THE CESD AND IDR RECORDS WILL BE READ.
         SPACE 1                                                 82239
DEBLOCK  TM    PDS+DCBRECFM-IHADCB,DCBRECU  WYLBUR FORMAT ?      81048
         BNO   4(,R8)        NO
         STM   R1,R9,PRESSAVE     SAVE REGISTERS                 82239
         TM    MEMFLAGS,FLOAD    LOAD MODULE ?
         BNZ   NOTEDIT       YES; SKIP OUT
         TM    PDTYPE,PDTLKED  LOAD-MODULE DIRECTORY ?           82239
         BNZ   NOTEDIT       YES; SKIP OUT                       82239
         ST    R10,DCMBUFAD  SET CURRENT BUFFER ADDRESS          82239
         MVC   DCMBUFMX,PDS+DCBBLKSI-IHADCB  SET BLOCKSIZE       83297
         B     DEBLINE2      TRY TO DECOMPRESS ONE LINE          82239
         SPACE 1
DEBLINE  TM    PDS+DCBRECFM-IHADCB,DCBRECU  WYLBUR FORMAT ?      81048
         BNO   4(,R8)        NO
         STM   R1,R9,PRESSAVE  SAVE REGISTERS
DEBLINE2 SERVCALL WDCOM,DECOMP  DECOMPRESS A LINE                82239
         CH    R15,=H'4'     CHECK RETURN CODE                   82239
         BL    DEBNLINE      NEW LINE AVAILABLE
         BH    NOTEDIT       NOT WYLBUR FORMAT
         LM    R1,R9,PRESSAVE  RESTORE
         B     4(,R8)        RETURN WITH NEW BLOCK REQUEST
DEBNLINE LM    R1,R9,PRESSAVE   RESTORE REGISTERS                82239
         L     R5,DCMRECAD   GET RECORD RETURNED                 82239
         LA    R7,79(,R5)    FAKE END ADDRESS                    82239
         BR    R8            RETURN WITH DATA
         SPACE 1                                                GP99028
BADFORM  WTO   'UNABLE TO PROCESS ddnamexx',ROUTCDE=11           80340
*NEXT*   B     USERABND                                         GP99028
         SPACE 1                                                GP99028
USERABND SERVTERM ,          CLOSE AND FREE                      82239
         ABEND 16,DUMP                                           82239
         SPACE 2
NOTEDIT  LM    R1,R9,PRESSAVE  GET ALL REGISTERS BACK
         TM    PDTYPE,PDTLKED   LOAD-MODULE DIRECTORY ENTRY ?    82239
         BZ    ENDMEM2       NO; SKIP THIS MEMBER                82239
         OI    MEMFLAGS,FLOAD    SET PRESUMED LOAD MODULE
         MVI   NEWTYP,RECTL
         CLI   0(R10),X'40'  SYM RECORD
         BE    LOADSKIP      OK
         CLI   0(R10),X'20'  CESD ?
         BE    LOADSKIP
         CLI   0(R10),X'10'  SCTR ?
         BE    LOADSKIP
         CLI   0(R10),X'80'  CSECT ID ?
         BNE   ENDMEM2       NO; SKIP THIS MEMBER
LOADSKIP MVC   ESDPTR(12),ESDTABLE  RESTORE ESD TABLE POINTERS   80340
         LA    R0,PDRDATE    POINT TO DATE OR BLANKS            GP99028
         BAL   R14,OLDDATE   PROCESS 3-BYTE DATE FORM           GP99028
         LM    R1,R9,PRESSAVE  RESTORE REGISTERS                 80340
         BR    R8            CONTINUE
         EJECT ,                                                 82249
*        CESD SCAN SUBROUTINE                                    80340
         SPACE 1                                                 80340
         USING DIRSECT,R10                                       80340
LOADCONT TM    DIRBUFF,X'0F'  END OF MODULE ?                    80340
         BNZ   ESDLAST       YES; SKIP                           80340
         CLC   DCBLRECL-IHADCB+PDS(2),DCBBLKSI-IHADCB+POD
         BH    ESDLAST       NOT CONTROL RECORD                  80340
         STM   R1,R9,PRESSAVE  SAVE EVERYTHING
         CLI   DIRBUFF,X'40'  TEST SYM RECORD?                   80340
         BE    NEWBLOCK       YES, SKIP RECORD                   80340
         CLI   DIRBUFF,X'80'   IDR DATA ?                        80340
         BE    ASIDR         YES; PROCESS THIS BLOCK             80340
         CLI   DIRBUFF,X'20'  CESD RECORD?                       80340
         BNE   NEWBLOCK       NO                                 80340
         LH    R2,DIRBUFF+4   RELATIVE # OF 1ST ESD ID           80340
         LA    R3,DIRBUFF+8   START OF ESD DATA                  80340
         LH    R5,DIRBUFF+6   LENGTH OF DATA IN BUFFER           80340
ESDOBJ   AR    R5,R3                                             80340
         LA    R4,16          LENGTH OF ONE ENTRY                80340
         SR    R5,R4                                             80340
         SPACE 1                                                 80340
         USING ESDNAME,R3                                        80340
ESDSCAN  CLI   ESDNAME+8,0   SD ENTRY ?                          80340
         BNE   NEXTESD       NO; IGNORE IT                       80340
         CLI   ESDNAME,C' '  PRINTABLE NAME ?                    80340
         BH    ESDUSE        YES; USE IT                         80340
NEXTESD  LA    R2,1(,R2)     UP ESDID                            80340
         BXLE  R3,R4,ESDSCAN                                     80340
NEWESDBK LM    R1,R9,PRESSAVE  RESTORE                           80340
         TM    MEMFLAGS,FOBJ     OBJECT CARD ?                   80340
         BNZ   ENDCARD       YES; DEBLOCK ANOTHER CARD           80340
         B     NEWBLOCK       GET NEXT ESD RECORD                80340
         DROP  R3                                                80340
         USING ESDENTRY,R6                                       80340
         SPACE 1                                                 80340
ESDUSE   L     R6,ESDPTR2    GET CURRENT ENTRY                   80340
         A     R6,ESDPTR+4   MAKE NEXT ENTRY                     80340
         C     R6,ESDTABLE+12 WILL IT FIT ?                      80340
         BH    NEWESDBK      EXIT WITHOUT ERROR MESSAGE          80340
         ST    R6,ESDPTR2    SET NEW LAST ENTRY                  80340
         XC    ESDDATE,ESDDATE                                   80340
         STH   R2,ESDID      SET ESD ID                          80340
         MVC   ESDNAME,0(R3)   SET ESD NAME                      80340
         B     NEXTESD                                           80340
         SPACE 1                                                 80340
ESDLAST  CLC   ESDPTR,ESDPTR2  ANY ENTRIES STASHED ?             80340
         BH    ESDQUIT        EXIT - NO ESD DATA AVAILABLE       80340
         MVC   CROSS,NEWDATE  SAVE DEFAULT DATE                 GP99028
         L     R6,ESDPTR     GET FIRST ENTRY                     80340
         LM    R4,R5,ESDPTR+4  GET LENGTH AND LAST ENTRY         80340
ESDPUTS  MVC   NEWDATE,CROSS   USE TO SAVED DATE                GP99028
         TM    PROFLAGS,FGTERSE   SHORT LISTING ?               GP10212
         BNZ   *+4+6         YES; DON'T COPY CSECT NAME         GP10212
         MVC   NEWMIN,ESDNAME  SET CSECT NAME                    80340
         MVI   NEWAT2,C' '   FLAG AS CSECT                      GP05110
         TM    ESDDATE,X'FF'  ANY DATE STORED ?                  80340
         BNM   ESDPUTP       NO; USE DEFAULT DATE                80340
         UNPK  DB,ESDDATE    MAKE DATE                          GP99028
         OI    DB+L'DB-1,C'0'   MAKE GOOD ZONE                  GP99028
         LA    R0,DB+3       POINT TO                           GP99028
         BAL   R14,OLDDATE     OLD DATE                         GP99028
ESDPUTP  BAL   R8,PUTTER     SEND TO SORT                        80340
         TM    PROFLAGS,FGTERSE   SHORT LISTING ?               GP10212
         BNZ   ESDPUTZ       YES; STOP AFTER FIRST ENTRY        GP10212
         BXLE  R6,R4,ESDPUTS  TRY NEXT ENTRY                     80340
ESDPUTZ  MVC   ESDPTR(12),ESDTABLE  CLEAR TABLE AGAIN            80340
*        B     ESDQUIT       DO ANOTHER MEMBER                   80340
         DROP  R6                                                80340
         SPACE 1                                                 80340
ESDQUIT  LM    R1,R9,PRESSAVE  RESTORE                           80340
         TM    MEMFLAGS,FOBJ     OBJECT CARD ?                   80340
         BNZ   ENDCARD       YES; DEBLOCK ANOTHER                80340
         B     ENDMEM        DO NEXT MEMBER                      80340
         SPACE 2                                                 80340
*        THIS CODE OBTAINS ASSEMBLY/COMPILE IDR DATES IN THE WORST
*        POSSIBLE FASHION.  BUT IT WORKS.                        80340
*                                                                80340
ASIDR    L     R5,IDRWORK    GET LENGTH OF EXISTING DATA         80340
         MVC   DIRBUFF(1),DIRBUFF+2  SAVE END IDR BIT            80340
         NI    DIRBUFF+2,X'7F'  TURN END BIT OFF                 80340
         CLI   DIRBUFF+2,IDRLKED LINKAGE EDITOR IDR RECORD?      80340
         BE    LKEDIDR        YES                                80340
         CLI   DIRBUFF+2,IDRASM  ASSEMBLY IDR ?                  80340
         BNE   ASIDRNXT      NO                                  80340
         LA    R3,DIRBUFF+3  POINT TO START OF DATA              80340
         XR    R4,R4           CLEAR FOR IC                      80340
         IC    R4,DIRBUFF+1  GET LENGTH                          80340
         SH    R4,=H'2'      LENGTH OF DATA                      80340
         BNP   ASIDRNXT      SKIP IF NONE                        80340
ASIDRLOP CH    R5,=H'18'     ENOUGH DATA FOR ONE ENTRY ?         80340
         BNL   ASIDRMSG      YES; PROCESS IT                     80340
         LTR   R4,R4         ANY DATA IN BUFFER ?                80340
         BNP   ASIDRNXT      NO; GO TO REFILL BUFFER             80340
         LA    R1,OBTWORK(R5)  FILL ADDRESS                      80340
         LA    R2,100        DESIRED FILL LENGTH                 80340
         CR    R4,R2         ENOUGH DATA FOR FULL FILL ?         80340
         BNL   *+6           YES                                 80340
         LR    R2,R4         USE ONLY WHAT'S AVAILABLE           80340
         SR    R4,R2         ADJUST LENGTH OF REMAINDER          80340
         EX    R2,ASIDRMVC   MOVE TEXT FROM BUFFER (L+1; TSK, TSK)
         AR    R3,R2         NEXT BUFFER ADDRESS AVAILABLE       80340
         AR    R5,R2         LENGTH OF AVAILABLE TEXT            80340
         B     ASIDRLOP      CHECK AGAIN                         80340
ASIDRMVC MVC   0(0,R1),0(R3)  FILL TEXT FROM BUFFER              80340
ASIDRMSG LM    R15,R1,ESDPTR  GET ESD POINTERS                   80340
         LH    R14,OBTWORK  GET SIGNED CESDID                    80340
         N     R14,=X'00007FFF'  STRIP SIGN                      80340
         USING ESDENTRY,R15  DECLARE IT                          80340
ASIDRTST CH    R14,ESDID     SAME CSECT ID ?                     80340
         BNE   ASIDRTSI      NO; TRY NEXT                        80340
         MVI   ESDDATE,X'FF'  FLAG AS DATE TO BE SET             80340
         LR    R15,R1        FORCE END OF LOOP                   80340
ASIDRTSI BXLE  R15,R0,ASIDRTST   TRY AGAIN                       80340
         TM    OBTWORK,X'80'  LAST LIST ENTRY ?                  80340
         BNZ   ASIDREOL      YES; GO TO FILL DATES               80340
         MVC   OBTWORK(116),OBTWORK+2  SHIFT LEFT                80340
         SH    R5,=H'2'      ADJUST RESIDUAL LENGTH              80340
         B     ASIDRLOP      CHECK LENGTH AGAIN                  80340
ASIDREOL LM    R15,R1,ESDPTR  GET LIST POINTERS AGAIN            80340
ASIDREOT CLI   ESDDATE,X'FF'   FILL DATE ENTRY ?                 80340
         BNE   ASIDREOX      NO; BUMP                            80340
         MVC   ESDDATE,OBTWORK+15  MOVE DATE                     80340
ASIDREOX BXLE  R15,R0,ASIDREOT  DO ALL                           80340
         CLI   OBTWORK+2,0  SINGLE ENTRY ?                       80340
         BNE   ASIDREOV      NO;  PROCESS VARIABLE LIST          80340
         SH    R5,=H'18'     ADJUST RESIDUAL LENGTH              80340
         MVC   OBTWORK(100),OBTWORK+18  SHIFT OVER               80340
         B     ASIDRLOP      DO NEXT ENTRY, IF ANY               80340
         SPACE 1                                                 80340
ASIDREOV MVC   OBTWORK+3(103),OBTWORK+18  CLOBBER PRIOR ENTRY    80340
         SH    R5,=H'15'     SUBTRACT LIST ENTRY LENGTH          80340
         XR    R1,R1                                             80340
         IC    R1,OBTWORK+2  GET NUMBER OF LIST ENTRIES          80340
         BCTR  R1,0                                              80340
         STC   R1,OBTWORK+2                                      80340
         B     ASIDRLOP      REPROCESS LAST ENTRY                80340
         SPACE 1                                                 80340
ASIDRNXT ST    R5,IDRWORK    SAVE REMAINING TEXT LENGTH          80340
         TM    DIRBUFF,X'80'  CHECK SAVED FLAG FOR LAST BLOCK    80340
         BZ    NEWESDBK      NOT YET; READ ANOTHER RECORD        80340
         B     ESDLAST       ELSE QUIT NOW                       80340
         DROP  R15                                               80340
         SPACE 1                                                 80340
*        LINKAGE EDITOR IDR RECORD PROCESSOR                     80340
         SPACE 1                                                 80340
LKEDIDR  UNPK  DB,DIRBUFF+15(3)  UNPACK THE LINK-EDIT DATE      GP99028
         OI    DB+L'DB-1,C'0'  ENSURE PROPER ZONE               GP99028
         LA    R0,DB+3       POINT TO DATE OR BLANKS            GP99028
         BAL   R14,OLDDATE   PROCESS 3-BYTE DATE FORM           GP99028
         B     NEWESDBK      GET ANOTHER BLOCK                   80340
         SPACE 2
PUTTER   CLI   NEWMIN,C' '   DUMMY ?                             80340
         BNHR  R8            YES, RETURN                         80340
         MVC   TSERRMSG(NEWLEN),NEWREC SAVE OVER XPRINT          81144
         CLC   NEWMAJ,NEWMIN  CSECT=MEMBER ?
*DEFER*  BNE   *+10
*DEFER*  MVC   NEWMIN,BLANKS  CLEAR CSECT
         SUBCALL SUBTREE,('ADD',ROOTBALL,NEWREC),VL,MF=(E,CALLPARM)
         CH    R15,=H'4'     ADDED ?                            GP10230
         BH    PUTREST         NO; OOOPS                        GP10230
*DEFER*  TM    MEMFLAGS,FOBJ+FLOAD  NON-SOURCE ?                 80340
*DEFER*  BZ    PUTREST       NO; LEAVE IT                        80340
         CLI   NEWMIN,C' '   SAME ?                              80340
         BNH   PUTREST       YES; LEAVE                          80340
         OI    NEWSEG,1      SET FOR CSECT/MEMBER LIST          GP05110
         SWAP  NEWMIN,NEWMAJ  SWAP MEMBER AND CSECT             GP05110
         SWAP  NEWAT1,NEWAT2  INDICATE SWAPPED                  GP05110
         SUBCALL SUBTREE,('ADD',ROOTBALL,NEWREC),VL,MF=(E,CALLPARM)
*IGNORE* CH    R15,=H'4'     ADDED ?                            GP10230
*ERROR?* BH    PUTREST         NO; OOOPS                        GP10230
PUTREST  MVC   NEWREC(NEWLEN),TSERRMSG RESTORE AFTER PRINT       80340
         OI    PROFLAGS,FGMEM+FCMEM  SHOW MEMBER FOUND           80340
         OI    MEMFLAGS,FGPUT  SHOW MEMBER FOUND                 80340
         BR    R8            RETURN
         SPACE 2
*   CAL2JUL - CONVERT 8 BYTE EBCDIC DATE (CCYYMMDD) TO JULIAN FORM
*     R1 -> CL8  REPLACE BY CCYY.DDD                            GP05110
CAL2JUL  STM   R14,R12,12(R13)                                  GP05110
         MVC   CDB1,=8X'F0'  TEST FOR NUMERIC ZONES             GP05110
         NC    CDB1,0(R1)    KEEP ONLY ZONE BITS                GP05110
         CLC   CDB1,=8X'F0'  ALL NUMERIC ZONES?                 GP05110
         BNE   CAL2JULR      NO; ERROR                          GP05110
         CLC   =C'00',0(R1)  CENTURY SUPPLIED?                  GP05110
         BNE   CAL2JULB      YES?                               GP05110
         ICM   R0,3,=C'20'   PRESET FOR CURRENT                 GP05110
         CLI   2(R1),C'7'    1970-1999 ?                        GP05110
         BL    CAL2JULA      NO                                 GP05110
         ICM   R0,3,=C'19'   YES?                               GP05110
CAL2JULA STCM  R0,3,0(R1)    COMPLETE CENTURY                   GP05110
CAL2JULB SR    R14,R14                                          GP05110
         ICM   R15,15,0(R1)  LOAD YEAR                          GP05110
         STM   R14,R15,CDB1  STASH                              GP05110
         PACK  CDB2,CDB1     PACK YEAR                          GP05110
         CVB   R2,CDB2       LOAD BINARY YEAR                   GP05110
         SR    R15,R15                                          GP05110
         ICM   R15,3,4(R1)   LOAD EBCDIC MONTH                  GP05110
         STM   R14,R15,CDB1  STASH                              GP05110
         PACK  CDB2,CDB1     PACK MONTH                         GP05110
         CVB   R3,CDB2       LOAD BINARY MONTH                  GP05110
         SR    R15,R15                                          GP05110
         ICM   R15,3,6(R1)   LOAD EBCDIC DAY                    GP05110
         STM   R14,R15,CDB1  STASH                              GP05110
         PACK  CDB2,CDB1     PACK DAY                           GP05110
         CVB   R4,CDB2       LOAD BINARY DAY                    GP05110
         LA    R15,DPMNORM-2 SET FOR NORMAL DAYS PER MONTH      GP05110
         EX    R2,EXLEAP     IS THIS A LEAP YEAR? 1901-2099     GP05110
         BNZ   CAL2JULC      NO                                 GP05110
         LA    R15,DPMLEAP-2 POINT TO LEAP TABLE                GP05110
CAL2JULC SLA   R3,1          CONVERT MONTH TO OFFSET            GP05110
         AH    R4,0(R3,R15)  GET JULIAN DAY                     GP05110
         CVD   R4,CDB1       MAKE PACKED                        GP05110
         UNPK  5(3,R1),CDB1  RETURN TO USER                     GP05110
         OI    7(R1),C'0'    MAKE F ZONE                        GP05110
         MVI   4(R1),C'.'    MAKE SEPARATOR                     GP05110
         B     CAL2JULX                                         GP05110
CAL2JULR MVC   0(8,R1),BLANKS  CLEAR NON-NUMERIC DATA           GP05110
CAL2JULX LM    R14,R6,12(R13)  RESTORE SOME                     GP05110
         BR    R8            RETURN TO CALLER                   GP05110
EXLEAP   TM    =X'03',*-*    TEST FOR LEAP YEAR                 GP05110
DPMNORM  DC    H'0,31,59,90,120,151,181,212,243,273,304,334'    GP05110
DPMLEAP  DC    H'0,31,60,91,121,152,182,213,244,274,305,335'    GP05110
         EJECT
SYSPRINT PRTWORK SYSPRINT,TITLE=6                                81144
         SPACE 1                                                 81144
MEMREC   MAPREC DSECT=YES,PFX=REC
EXSTATIC CSECT ,
         SPACE 1
PATBALL  SERVTREE PFX=PTB,KEYLEN=RECKEYL,KEYOFF=MEMREC-MEMREC,         *
               RECLEN=RECLEN,RECNUM=(64*1024-48)/RECLEN         GP05110
PATBALLN EQU   *-PATBALL                                        GP05110
         SPACE 1
SAVE     DSECT ,                                                 81144
DB       DS    D             WORK SPACE                         GP99028
CROSS    DC    D'0'                                             GP99028
CDB1     DC    D'0'          FOR CAL2JUL ONLY                   GP05110
CDB2     DC    D'0'          FOR CAL2JUL ONLY                   GP05110
HEXDATE  DC    F'0'          DATE LESS SIGN                      81144
         SERVDEFS ,          COMMON GOODIES                     GP05105
         PRINT &PRTMAC       SET LOCAL MACRO EXPANSION           82239
DECOMP   SERVCOMP DSECT=NO,PFX=DCM  WYLBUR DECOMPRESSION PARMS   82239
         SPACE 1                                                 82239
DEBXLE   SERVPDS PFX=PD,DSECT=NO,RETURN=YES,VER=1   D.E. PARMS  GP10139
         PRINT &PRTSOR                                           82239
         SPACE 1
ROOTBALL SERVTREE PFX=ROT,KEYLEN=RECKEYL,KEYOFF=MEMREC-MEMREC,         *
               RECLEN=RECLEN                                    GP05110
NEWREC   MAPREC DSECT=NO,PFX=NEW
         SPACE 1
PROFLAGS DS    X             PROCESSING FLAG
DSFOUND  EQU   X'01'           AT LEAST ONE DATASET PROCESSED
FGMEM    EQU   X'02'           AT LEAST ONE MEMBER DONE
FCMEM    EQU   X'04'           AT LEAST ONE MEMBER IN CURRENT DSN
FGDUPE   EQU   X'08'           FLAG MULTIPLE ENTRIES            GP10139
FGTERSE  EQU   X'10'           MEMBERS ONLY (NO ALII/CSECTS)    GP10212
MEMFLAGS DS    X             MEMBER FLAGS
FGPUT    EQU   X'80'           PUT WAS DONE FOR THIS MEMBER      80340
FGVBLK   EQU   X'40'           DATASET/MEMBER IS NOT FIXED/NOT WYLBUR
FGLOCSSI EQU   X'20'           SSI IS FUNNY LOCAL FORMAT         81144
FLOAD    EQU   X'10'           LOAD MODULE
FOBJ     EQU   X'08'           OBJECT DECK
FLIST    EQU   X'04'           ASSEMBLER LISTING                GP05110
FMEM     EQU   FGVBLK+FLIST    MEMBER RESET                     GP05110
SAVEEND  EQU   *             SHORT WORK AREA (TEMPORARY?)
EXSTATIC CSECT ,                                                 81144
         SPACE 2
SYNADBUF DC    A(0)
SYNADCOD DC    F'0'
TS       DC    Y(TSEND-TS),X'8000'
         DC    C'TSM I/O ERROR   '
TSERRMSG DC    CL78' '
TSBUF    DC    CL22' '
TSEND    DC    XL2'0200',XL2'0060'
BLANKS   DC    CL(RECLEN)' '                                    GP99028
FINDWORD DC    A(0)          MEMBER TTR
ESDTABLE DC    A(TABLEESD,LENESD,TABLEESD-LENESD,TABLEEND-LENESD)
ESDPTR   DC    A(0,LENESD)                                       80340
ESDPTR2  DC    A(0)                                              80340
IDRWORK  DC    A(0)          IDR WORK POINTER                    80340
BUFSAVE  DC    2A(0)         BUFFER ADDRESS SAVE                 80340
PRESSAVE DC    9F'0'
         SPACE 1
NEXTTIOT DC    A(0)          TIOT SCAN POINTER
FMT1     CAMLST SEARCH,JFCBDSNM,JFCBVOLS,OBTWORK  READ DSCB1
OBTWORK  DC    19D'0'
         SPACE 1
DECB     DS    0F
DECSDECB DC    F'0'                     ECB
DECTYPE  DC    X'8080'
*              FIRST BYTE MEANS LENGTH 'S'
*              SECOND BYTE MEANS READ SF
DECLNGTH DC    H'0'
DECDCBAD DC    A(PDS)                   DCB ADDRESS
DECAREA  DC    A(0)                     BUFFER
DECIOBPT DC    A(0)                     IOB
         SPACE 2
OP1      OPEN  (POD,INPUT),MF=L
         SPACE 1
ALPHAS   DC    C'0123456789ABCDEF'   HEXTAB                      80340
HEXTAB   EQU   ALPHAS-C'0'
         SPACE 1                                                 80340
ROUT     DC    Y(137,0),C' '
OUT      DC    CL132' '      OUTPUT RECORD
         ORG   OUT
OUTNMAL  DC    C' '          ALIAS/CSECT FLAG                    80340
OUTNAM   DC    CL8' '
         DC    C' '
OUTALIAS DC    C' '
OUTMEM   DC    CL8' '
OUTDUPE  DC    CL2' '                                           GP10149
OUTONE   DC    C' ',C' '     BACK-DATE FLAG
OUT1DATE DC    CL8' ',CL2' '  DATE, SPACER                      GP99028
OUTLEN   EQU   *-OUTONE      WIDTH OF ONE COLUMN                GP99028
OUTTWO   DC    CL(OUTLEN)' '                                    GP99028
MAXLIBS  EQU   ((132+2-(OUTONE-OUT))/OUTLEN)                    GP99028
         ORG   ,
PRESEG   DC    X'0'          PREVIOUS PASS
PREMEM   DC    XL8'0'        PREVIOUS MEMBER
SORLIB   DC    X'0'          LIBRARY FOR CORR. SOURCE DATE
SORDATE  DC    CL(L'RECDATE)' '   DATE OF SOURCE MODULE         GP99028
CURMEM   DC    XL8'0'        CURRENT MEMBER
         SPACE 2                                                 80340
         ORG   OUT           REDEFINE FOR DATASET INFO HEADER    80340
DSJNUM   DS    CL4           LIBRARY NUMBER                      80340
         DS    C                                                 80340
DSJDDNM  DS    CL8,CL2       DDNAME                              81144
DSJCOL   DS    CL8           COLUMN HEADER                       80340
         DS    CL2                                               80340
DSJDSN   DS    CL44          DATASET NAME                        80340
         DS    CL2                                               80340
DSJVOL   DS    CL6           VOLUME SERIAL                       80340
         DS    CL2                                               80340
DSJUCB   DS    CL3,C         UNIT NAME                          GP99028
         DS    C                                                 80340
DSJUTYP  DS    CL8           UNIT TYPE                          GP99028
         ORG   ,                                                 80340
         SPACE 2
POD      DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GL,                        *
               SYNAD=QSAMSIN,                                          *
               RECFM=U,LRECL=256,BLKSIZE=264,KEYLEN=8,BUFNO=2,         *
               EODAD=ENDIR,EXLST=DIREX
DIREX    DC    0A(0),X'87',AL3(INFMJFCB)
         SPACE 2
PDS      DCB   DDNAME=SYSUT1,DSORG=PO,MACRF=R,                         *
               SYNAD=BPAMSIN,BUFNO=2,NCP=1,EODAD=ENDMEM2,EXLST=MEMEX
MEMEX    DC    0A(0),X'05',AL3(DCBMEMEX),X'87',AL3(INFMJFCB)
         SPACE 2
         LTORG ,
         SPACE 2
         IEFJFCBN
         SPACE 1
NOWORK   VCON  ' *** NO PROCESSABLE DATA FOUND ***'              81278
HEADER   FDOPT NL,CC=C'#'    AUTOMATIC DATE/TIME/PAGE HEADER     81278
         FDPRT '    SOURCE/OBJECT/LOAD MODULE CONCORDANCE'       81278
         FD    *END                                              81144
SUBHEAD2 DC    Y(136,0),C' MODULE    CSECT      '                81144
SUBDSN   DC    13CL(OUTLEN)' '                                  GP99028
SUBHEADB DC    Y(136,0),C' CSECT     MODULE     '               GP05110
SUBDSNB  DC    13CL(OUTLEN)' '                                  GP05110
SUBHEAD1 DC    Y(136,0),CL132'              DDNAME:'             81144
LIBNULL  VCON  '     ***** NO MEMBERS PROCESSED IN ABOVE LIBRARY *****'
TOOMANY  VCON  '-*** MAXIMUM OF ',END=TOOMANYX                  GP99028
         EBCDIG MAXLIBS,2                                       GP99028
         DC    C' LIBRARIES EXCEEDED ***'                       GP99028
         VCON  *END                                             GP99028
         SPACE 1                                                 80340
         PUSH  USING                                            GP99028
         DROP  ,                                                GP99028
         USING DCBMEMEX,R15                                      94024
         USING IHADCB,R1
         USING SAVE,R13                                         GP99028
DCBMEMEX N     R1,=X'00FFFFFF'    NEED TO CLEAN IN AM31         GP05110
         NI    DCBOPTCD,255-PCI
PCI      EQU   32
         TM    DCBRECFM,DCBRECU  RECFM=U ?                       81048
         BOR   R14           YES, ASSUME WYLBUR
         TM    MEMFLAGS,FGVBLK+FLIST   FUNNY FORMAT ?            94024
         BNZR  R14           YES; ALLOW                          94024
         CLC   =H'80',DCBLRECL   CARD FORMAT ?
         BNE   NONFORM       NO, ERROR                           94024
         TM    DCBRECFM,X'40'  V ?
         BZ    CONFORM                                           94024
NONFORM  OI    MEMFLAGS,FGVBLK                                   94024
CONFORM  OC    DCBBLKSI,DCBBLKSI
         BNZR  R14
         MVC   DCBBLKSI,DCBLRECL
         BR    R14
         SPACE 1                                                 80340
         LTORG ,                                                 80340
         POP   USING                                            GP99028
         SPACE 2
         DS    0H
TABLEESD DS    500CL10       BIG ESD ID TABLE
TABLEEND EQU   *
         SPACE 2                                                 80340
         ORG   TABLEESD      INITIALIZATION CODE                 80340
INITPROG PRTOPEN SYSPRINT,OPT=ABEND                              81144
         PRTLIST HEADER,TITLE=1                                  81144
         PRTV  INIT1                                             81144
         PRTV  INIT2                                             81144
         PRTV  INIT3                                             81144
         PRTV  INIT4                                             81144
         PRTV  INIT5                                             81144
         PRTV  INIT6                                             81144
         PRTV  INIT7                                             81144
         PRTV  INIT8                                             81144
         BR    R10           RETURN AFTER INITIALIZATION MESSAGES
         SPACE 2                                                 80340
INIT1    VCON  ' This program produces dated cross-reference listings f*
               or source, object, listing, and load module libraries.'
INIT2    VCON  '0Each control section is listed for each member or alia*
               s name. It is listed again after the member or alias nam*
               e.'                                               81278
INIT3    VCON  ' Thus, CSECT in MEMBER is listed as CSECT/MEMBER, and a*
               gain as MEMBER/CSECT.'                            81278
INIT4    VCON  '0Entry dates for source members use either the ISPF/PDF*
                date, or local SSI. Other entries use the compile or li*
               nk-edit date.'                                    81278
INIT5    VCON  ' The source entry with the highest date on a line is us*
               ed as a reference value; lower dates on the line are fla*
               gged with < to show'                              81278
INIT6    VCON  ' that they may be obsolete.'                     81278
INIT7    VCON  '-Column headings for libraries processed are:'   81278
INIT8    VCON  '0',END=INIT8END                                  81278
         DC    C' NO. '                                          80340
         DC    CL10'DDNAME'                                      81144
         DC    CL10'COLUMN'                                      80340
         DC    CL46'   DATASET NAME'                             80340
         DC    CL8'VOLUME'                                       80340
         DC    C'UNIT TYPE'                                      80340
         VCON  ,                                                 81278
         LTORG ,                                                 80340
         ORG   ,                                                 80340
         SPACE 2
DIRBLOCK DSECT ,                                                 80340
         DS    3H            ALIGN IHAPDS ON DOUBLE-WORD BOUNDARY
DIRECTRY EQU   *                                                 80340
BLKLEN   DS    H                        LENGTH OF DIRECTORY BLOCK
MEMNAME  DS    CL8                      MEMBER NAME
MEMTTR   DS    XL3                      RELATIVE ADDRESS OF MEMBER
ENTLEN   DS    XL1                      LENGTH OF USER DATA IN HALFWORD
USERDATA EQU   *
         SPACE 1                                                 80340
         ORG   MEMNAME       REDEFINE FOR LOAD-MODULE            80340
         IHAPDS PDSBLDL=NO,DSECT=NO                              80340
         SPACE 2                                                 80340
ESDENTRY DSECT ,             TABLEESD ENTRY MAPPING              80340
ESDNAME  DS    CL8            NAME OF EXTERNAL SYMBOL            80340
ESDID    DS    H              RELATIVE ESD ID #                  80340
ESDDATE  DS    XL3           DATE OF ASSEMBLY/COMPILE            80340
         DS    0H            ALIGNMENT FOR 360 TYPES             80340
LENESD   EQU   *-ESDENTRY     LENGTH OF ENTRY                    80340
         SPACE 1                                                 80340
IDRZAP   EQU   X'01'                                             80340
IDRLKED  EQU   X'02'                                             80340
IDRASM   EQU   X'04'                                             80340
IDRUSER  EQU   X'08'                                             80340
         SPACE 1                                                 80340
DIRSECT  DSECT ,                                                 80340
DIRBUFF  DS    X             256 BYTE LKED CONTROL BUFFER        80340
         SPACE 2
         PRINT &PRTSYS                                           81144
CVTDSECT DSECT ,                                                 81144
         CVT   DSECT=YES                                         81144
         SPACE 1                                                 81151
         IHACDE ,                                                81151
         SPACE 1                                                 81144
         IKJTCB ,                                                81144
         SPACE 1                                                 81144
IEFTIOT1 DSECT ,                                                 81144
         IEFTIOT1                                                81144
         SPACE 1                                                 81144
FAKEUCB  DSECT ,                                                GP99028
         IEFUCBOB ,                                              81144
         SPACE 1                                                 81144
FAKEDS1  DSECT ,                                                GP99028
         IECSDSL1 1          DSCB 1                              81144
         AIF   (&MVSXA).SYSDEF                                  GP05110
DS1FLAG1 EQU   DS1NOBDB+1,1,C'X'  MORE FLAGS                    GP05110
DS1COMPR EQU   X'80'           COMPRESSABLE EXTENDED IF DS1STRP GP05110
DS1CPOIT EQU   X'40'           CHECKPOINTED D S                 GP05110
DS1SMSFG EQU   DS1FLAG1+17,1,C'X'  SMS FLAG                     GP05110
DS1SMSDS EQU   X'80'           SMS D S                          GP05110
DS1SMSUC EQU   X'40'           NO BCS ENTRY                     GP05110
DS1REBLK EQU   X'20'           MAY BE REBLOCKED                 GP05110
DS1CRSDB EQU   X'10'           BLKSZ BY DADSM                   GP05110
DS1PDSE  EQU   X'08'           PDS/E                            GP05110
DS1STRP  EQU   X'04'           EXTENDED FORMAT D S              GP05110
DS1PDSEX EQU   X'02'           HFS D S                          GP05110
DS1DSAE  EQU   X'01'           EXTENDED ATTRIBUTES EXISY        GP05110
DS1SCEXT EQU   DS1SMSFG+1,3,C'X'  SECONDARY SPACE EXTENSION     GP05110
DS1SCXTF EQU   DS1SCEXT,1,C'X'  -"- FLAG                        GP05110
DS1SCAVB EQU   X'80'           SCXTV IS AVG BLOCK LEN           GP05110
DS1SCMB  EQU   X'40'                 IS IN MEGBYTES             GP05110
DS1SCKB  EQU   X'20'                 IS IN KILOBYTES            GP05110
DS1SCUB  EQU   X'10'                 IS IN BYTES                GP05110
DS1SCCP1 EQU   X'08'           SCXTV COMPACTED BY 256           GP05110
DS1SCCP2 EQU   X'04'                 COMPACTED BY 65536         GP05110
DS1SCXTV EQU   DS1SCXTF+1,2,C'X'  SEC SPACE EXTNSION VALUE      GP05110
DS1ORGAM EQU   DS1ACBM         CONSISTENT NAMING - VSAM D S     GP05110
DS1RECFF EQU   X'80'           RECFM F                          GP05110
DS1RECFV EQU   X'40'           RECFM V                          GP05110
DS1RECFU EQU   X'C0'           RECFM U                          GP05110
DS1RECFT EQU   X'20'           RECFM T   001X XXXX IS D         GP05110
DS1RECFB EQU   X'10'           RECFM B                          GP05110
DS1RECFS EQU   X'08'           RECFM S                          GP05110
DS1RECFA EQU   X'04'           RECFM A                          GP05110
DS1RECMC EQU   X'02'           RECFM M                          GP05110
*   OPTCD DEFINITIONS   BDAM    W.EFA..R                        GP05110
*                       ISAM    WUMIY.LR                        GP05110
*             BPAM/BSAM/QSAM    WUCHBZTJ                        GP05110
DS1OPTIC EQU   X'80'  FOR DS1ORGAM - CATLG IN ICF CAT           GP05110
DS1OPTBC EQU   X'40'           ICF CATALOG                      GP05110
DS1RACDF EQU   DS1IND40                                         GP05110
DS1SECTY EQU   DS1IND10                                         GP05110
DS1WRSEC EQU   DS1IND04                                         GP05110
DS1SCAL1 EQU   DS1SCALO,1,C'X'    SEC. ALLOC FLAGS              GP05110
DS1DSPAC EQU   X'C0'         SPACE REQUEST MASK                 GP05110
DS1CYL   EQU   X'C0'           CYLINDER BOUND                   GP05110
DS1TRK   EQU   X'80'           TRACK                            GP05110
DS1AVRND EQU   X'41'           AVG BLOCK + ROUND                GP05110
DS1AVR   EQU   X'40'           AVG BLOCK LEN                    GP05110
DS1MSGP  EQU   X'20'                                            GP05110
DS1EXT   EQU   X'10'           SEC. EXTENSION EXISTS            GP05110
DS1CONTG EQU   X'08'           REQ. CONTIGUOUS                  GP05110
DS1MXIG  EQU   X'04'           MAX                              GP05110
DS1ALX   EQU   X'02'           ALX                              GP05110
DS1DSABS EQU   X'00'           ABSOLUTE TRACK                   GP05110
DS1SCAL3 EQU   DS1SCAL1+1,3,C'X'  SEC ALLOC QUANTITY            GP05110
.SYSDEF  SPACE 1
         DCBD  DSORG=PS
         SPACE 2
         END
