SUBVERB  TITLE 'S U B V E R B  ***  VERB TABLE LOOKUP'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*   SUBVERB COMPARES A VERB ENTRY (ADDRESS IN R0, LENGTH=8)           *
*     TO FIND A MATCH IN A TABLE (ADDRESS OF BXLE POINTERS IN R1:     *
*     A(FIRST ENTRY,ENTRY LENGTH,LAST ENTRY)                          *
*                                                                     *
*   THE VERB IS COMPARED TO EACH TABLE ENTRY, AND MAY BE ABBREVIATED  *
*     TO THE MINIMUM LENGTH SPECIFIED ON THE TABLE ENTRY              *
*   THE ROUTINE RETURNS 0 WHEN THERE IS NO MATCH, OTHERWISE THE       *
*     MATCHED TABLE ENTRY ADDRESS IS PASSED IN R15 (IND.AD. IN R1)    *
*     (NOTE THAT THE ADDRESS IN R1 IS MEANINGFUL PROVIDED THE OFFSETS *
*      0-12 MATCH IN PARKEYAD AND PARKEYFG EXPANSIONS)                *
*                                                                     *
*   THE TABLE BXLE POINTER MAY BE CREATED USING THE PARKEYBX MACRO.   *
*   TABLES MAY BE CREATED WITH PARKEYAD AND PARKEYFG MACROS,          *
*   PROVIDING ALL ENTRIES IN A TABLE ARE THE SAME LENGTH              *
*                                                                     *
***********************************************************************
*  MAINTENANCE:                                                       *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 1
SUBVERB  START 0
SUBVERB  AMODE ANY           USE CALLER'S AMODE
SUBVERB  RMODE ANY           USE CALLER'S RMODE
         SPACE 1
         B     BEGIN-SUBVERB(,R15)
         BCON  'SUBVERB &SYSDATE'
BEGIN    STM   R14,R12,12(R13)  SAVE A BIT
         LR    R12,R15       COPY BASE ADDRESS
         USING SUBVERB,R12   DECLARE IT
         SPACE 1
*---------------------------------------------------------------------*
*  CHECK INPUT POINTERS; ON ERROR R15=0, R0=100+ERROR CODE            *
*---------------------------------------------------------------------*
         LR    R6,R0         COPY KEYWORD ADDRESS
         LA    R6,0(,R6)     CLEAN IT
         LA    R0,100        SET REASON
         LTR   R6,R6
         BZ    BADRET15      NO KEYWORD ADDRESS
         LA    R0,101        SET REASON
         LA    R1,0(,R1)     CLEAN BXLE ADDRESS
         LTR   R1,R1
         BZ    BADRET15      NO TABLE ADDRESS
         LM    R3,R5,0(R1)   GET TABLE POINTERS
         LA    R0,103        SET REASON
         LA    R3,0(,R3)     CLEAN BXLE ADDRESS
         LTR   R3,R3
         BZ    BADRET15      NO FIRST ENTRY ADDRESS
         LA    R0,104        SET REASON
         LA    R4,0(,R4)     CLEAN BXLE ADDRESS
         LTR   R4,R4
         BZ    BADRET15      NO ENTRY LENGTH
         LA    R0,105        SET REASON
         LA    R5,0(,R5)     CLEAN BXLE ADDRESS
         LTR   R5,R5
         BZ    BADRET15      NO END ADDRESS
         LOCBYTE 0(R6),LEN=8,W2=(R7)  LOOK FOR BLANK IN KEYWORD
         SR    R7,R14        GET KEYWORD LENGTH
         SPACE 1
*---------------------------------------------------------------------*
*  LOOK FOR A MATCH                                                   *
*---------------------------------------------------------------------*
VERBLOON LOCBYTE OFO@TXT(R3),LEN=8,WK=R15,W2=R14  GET FIELD LENGTH
         SR    R14,R15       GET LENGTH
         BNP   VERBLOOX      SKIP BAD ENTRY
         CR    R14,R7        COMPARE TO ACTUAL
         BL    VERBLOOX        TOO LONG FOR MATCH
         LR    R14,R7        START WITH GOTTEN LENGTH
         BCTR  R14,0         CHANGE TO EXECUTE LENGTH
         CLM   R14,1,OFO@MIN(R3)  CHECK AGAINST DESIGN MINIMUM
         BL    VERBLOOX      TOO SHORT; DO ANOTHER WORD
VERBLOOP EX    R14,VERBLCLC  COMPARE TO REQUEST
         BE    VERBLOAD      FOUND
         SH    R14,=H'1'     DECREASE LENGTH
         BM    VERBLOOX        BUT NOT TOO MUCH
         CLM   R14,1,OFO@MIN(R3)  CHECK AGAINST DESIGN MINIMUM
         BNL   VERBLOOP      TRY WITH SHORTER VERSION >= MIN
VERBLOOX BXLE  R3,R4,VERBLOON  TRY ANOTHER WORD
         LA    R0,804        SET NOT FOUND
         B     BADRET15      RETURN UNMATCHED
VERBLCLC CLC   OFO@TXT(0,R3),0(R6)  CHECK FOR VERB MATCH
         SPACE 1
*---------------------------------------------------------------------*
*  MATCHED; PUT ENTRY ADDRESS IN R15; AND SET FLAG/BRANCH ADDR IN R1  *
*---------------------------------------------------------------------*
VERBLOAD SR    R1,R1         JUST IN CASE
         ICM   R1,3,OFO@ADD(R3)  LOAD FIELD OFFSET
         LR    R2,R1         MAKE A COPY
         N     R1,=X'00000FFF'  ISOLATE OFFSET
         SRL   R2,12-2       ISOLATE REGISTER OFFSET
         N     R2,=X'0000003C'  MAKE REGISTER OFFSET MASK
         BZ    VERBABSO      ABSOLUTE OFFSET (?)
         C     R2,=A(13*4)   IS IT REGISTER 13 ?
         BE    VERBAB13      YES; SPECIAL
         BH    VERBABSO      DON'T DO FOR R14-R15
         AL    R1,20(R2,R13)  RELOCATE R1-R12
         B     VERBABSO
VERBAB13 ALR   R1,R13        (IN DYNAMIC SAVE/WORK AREA?)
VERBABSO LR    R15,R3        RETURN MATCHED ENTRY
         B     COMMRET       VECTORED RETURN - SUCCESS
         SPACE 1
BADRET15 SR    R15,R15
COMMRET  L     R14,12(,R13)  LOAD RETURN ADDRESS
         LM    R2,R12,28(R13)  LOAD REMAINING REGISTERS
         BR    R14           RETURN TO CALLER
         SPACE 1
UNWANTED DSECT ,
         PARKEYAD 'KEYWORD',COMMRET       DEFINE OFFSETS
         PARKEYFG 'KEYWORD',COMMRET,1,2   DEFINE OFFSETS
         SPACE 1
         YREGS ,             REGISTER MNEMONICS
         END   ,
