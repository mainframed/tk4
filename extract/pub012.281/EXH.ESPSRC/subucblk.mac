SUBUCBLK TITLE 'S U B U C B L K  ***  LOOP THROUGH UCBS'
***********************************************************************
**                                                                   **
**   SUBROUTINE SUBUCBLK LOOPS THROUGH UCBS.                         **
**   INPUT:   R1=0 RETURNS FIRST UCB     R1=@ucb  RETURNS NEXT UCB.  **
**   RETURN:  R1=0,R15=4  END OF LIST    R1=@UCB,R15=0  FOUND UCB    **
**            R1=0,R15=16   INVALID R1 PASSED                        **
**                                                                   **
**   NOTE THAT THE CODE WILL USE THE APPROPRIATE METHOD DEPENDING    **
**   ON THE OPERATING SYSTEM.                                        **
**                                                                   **
**   XA AND ESA CODE ADAPTED FROM MODS TO SHOWDS BY GREG PRICE       **
**                                   AUTHOR: G. POSTPISCHIL          **
***********************************************************************
**
         SPACE 1
SUBUCBLK START 0                                         NEW ON GP09198
         B     BEGIN-SUBUCBLK(,R15)
         BCON  'SUBUCBLK &SYSDATE'
BEGIN    BSM   R14,0         REMEMBER CALLERS AMODE
         STM   R14,R12,12(R13)
         LR    R12,R15       SET NEW BASE
         USING SUBUCBLK,R12
         L     R3,CVTPTR     GET CVT
         USING CVTMAP,R3
         LR    R2,R1         SAVE USER'S REQUEST
         TM    CVTDCB,X'80'  X/A, ESA, OR LATER ?
         BZ    MVS370              NOT XA SO DO OS/370 UCB LOOK UP
         TM    CVTDCB,X'93'        CHECK OS BITS FOR MVS/XA SYSTEM
         BNO   MSPEXA              NOT XA SO DO MSP/EXA UCB LOOK UP
         ICM   R1,15,1252(R3)      FIRST UCB
         B     *+8                 BYPASS CHAIN JUMP
UCBLOOPX ICM   R1,15,8(R1)         POINT TO NEXT UCB            GP09131
         BZ    LASTEXIT            END OF CHAIN REACHED
         LTR   R2,R2         RESTART REQUEST ?
         BZ    GOODEXIT
         CR    R1,R2         HIT RESTART ?
         BNE   UCBLOOPX      NO; TRY AGAIN
         SR    R2,R2         RETURN NEXT ONE
         B     UCBLOOPX        AND FIND IT
         SPACE 1
MSPEXA   LA    R1,MSPIOCOM
         O     R1,=X'80000000'
*        BSM   0,R1                HANDLE 31-BIT ADDRESSES
         DC    X'0B01'             HANDLE 31-BIT ADDRESSES
MSPIOCOM L     R15,X'7C'(,R3)      IOS COUMMICATION AREA
         L     R15,X'184'(,R15)    UCB LOOK-UP TABLE
         B     *+8                 BYPASS INCREMENT
UCBLOOPE LA    R15,4(,R15)         POINT TO NEXT UCB POINTER
         ICM   R1,15,0(R15)        POINT TO UCB                 GP09131
         BZ    UCBLOOPE            NULL, IGNORE IT
         BM    LASTESA             END OF POINTER LIST
         LTR   R2,R2         RESTART REQUEST ?
         BZ    GOODESA
         CR    R1,R2         HIT RESTART ?
         BNE   UCBLOOPE      NO; TRY AGAIN
         SR    R2,R2         RETURN NEXT ONE
         B     UCBLOOPE        AND FIND IT
GOODESA  LA    R15,GOODEXIT        YES
*        BSM   0,R15               REVERT TO 24-BIT ADDRESSING
         DC    X'0B0F'             REVERT TO 24-BIT ADDRESSING
LASTESA  LA    R15,LASTEXIT        YES
*        BSM   0,R15               REVERT TO 24-BIT ADDRESSING
         DC    X'0B0F'             REVERT TO 24-BIT ADDRESSING
         SPACE 1
MVS370   L     R15,CVTILK2         UCB HALFWORD ADDRESS TABLE
         B     *+8                 BYPASS INCREMENT
UCBLOOP  LA    R15,2(,R15)         POINT TO NEXT UCB POINTER
         LH    R1,0(,R15)          POINT TO UCB
         N     R1,=X'0000FFFF'     16 BIT ADDRESS
         BZ    UCBLOOP             NULL, IGNORE IT
         C     R1,=X'0000FFFF'     LAST ENTRY ?
         BE    LASTEXIT            END OF POINTER LIST
         LTR   R2,R2         RESTART REQUEST ?
         BZ    GOODEXIT
         CR    R1,R2         HIT RESTART ?
         BNE   UCBLOOP       NO; TRY AGAIN
         SR    R2,R2         RETURN NEXT ONE
         B     UCBLOOP         AND FIND IT
         SPACE 1
FAILEXIT LA    R15,16        BOOBOO
         B     ZEROADDR
LASTEXIT LA    R15,4         SET END-LIST FLAG
ZEROADDR SR    R1,R1         NO MORE UCBS
         B     PGMEXIT
GOODEXIT SR    R15,R15       UCB IN R1
PGMEXIT  LM    R2,R12,28(R13)  RELOAD SOME REGISTERS
         L     R14,12(,R13)  RELOAD RETURN ADDRESS
         BSM   0,R14         RETURN
         LTORG ,
         SPACE 1
         PRINT NOGEN
         CVT   DSECT=YES     EXPAND CVT
         IEFUCBOB ,          EXPAND UCB
         SPACE 1
         YREGS ,
         END   ,
