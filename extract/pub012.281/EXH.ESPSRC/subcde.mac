SUBCDE   TITLE 'S U B C D E  ***  BUILD CDE FOR GOTTEN STORAGE'
         MACRO ,
&NM      KANT  &OP
         GBLA  &ZZKTCOD(4)
         LCLA  &I
         AIF   ('&OP' EQ '0').RESET
&I       SETA  1
         AIF   ('&OP' EQ '4').SET
&I       SETA  1
         AIF   ('&OP' EQ '8').SET
&I       SETA  1
         AIF   ('&OP' EQ '12').SET
&I       SETA  1
         AIF   ('&OP' EQ '16').SET
         MNOTE 8,'KANT: UNRECOGNIZED POSITIONAL: &OP'
.SET     ANOP  ,
&ZZKTCOD(&I) SETA  &ZZKTCOD(&I)+1
&NM      LA    R0,&ZZKTCOD(&I)   SET ERROR REASON CODE
         MEXIT ,
.RESET   ANOP  ,
&NM      SR    R0,R0         RESET REASON CODE
         MEND  ,
         SPACE 1
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         EJECT ,
***********************************************************************
**                                                                   **
**  SUBCDE  BUILDS A CDE ENTRY FOR GOTTEN STORAGE.                   **
**                                                                   **
**  PARAMETER LIST:  (MAPPED BY MACRO FETWORK)                       **
**                                                                   **
**  1) DCB FIELD IS USED BY SUBFETCH, AND IS IGNORED BY SUBCDE       **
**                                                                   **
**  2) POINTER TO THE NAME. WHEN THE HIGH BIT IS ON, POINTS TO       **
**     COMPLETE BLDL RETURN.                                         **
**                                                                   **
**  3) POINTER TO THE LOAD ADDRESS, OR 0. FOR A 0 ADDRESS, SUBFETCH  **
**     WILL OBTAIN THE INTEGRAL NUMBER OF PAGES TO HOLD THE MODULE.  **
**     SIZE IS PASSED, AND RETURNED IF LARGER.                       **
**                                                                   **
**  4) RELOCATION ADDRESS OR 0. IGNORED BY SUBCDE                    **
**                                                                   **
**  5) MODULE ENTRY POINT, WITH AMODE BIT, RETURNED IN @ENTRY        **
**                                                                   **
**  6) OPTION FLAGS 2 AND 3 TO SET (IF AUTH) CDEATTR/ATTR2           **
**                                                                   **
***********************************************************************
**                                                                   **
**  ERROR RETURNS:                                                   **
**                                                                   **
**  16 - ERROR PROCESSING PARAMETER LIST, OR OTHER SEVERE PROBLEM    **
**   8 - ERROR IN IDENTIFY OR LOAD SERVICE                           **
**                                                                   **
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
* MACRO NAME = BUILDCDE                                               *
*                                                                     *
* DESCRIPTIVE NAME = BUILD A MAJOR CDE TO IDENTIFY A STORAGE AREA     *
*                                                                     *
* FUNCTION = MAKES A STORAGE AREA EASIER TO LOCATE IN A               *
*            DUMP BY MAKING IT APPEAR AS A LOAD_MODULE.               *
*                                                                     *
* STATUS = R200                                                       *
*                                                                     *
* AUTHOR = GILBERT SAINT-FLOUR <GSF@POBOX.COM>                        *
*          MODIFIED TO MATCH LOCAL SPECS.  GERHARD POSTPISCHIL        *
*                                                                     *
* ENVIRONMENT = SEE BELOW                                             *
*                                                                     *
*     AMODE = ANY                                                     *
*     SCP   = MVS THROUGH MVS/ESA                                     *
*     KEY   = USER                                                    *
*     MODE  = PROBLEM                                                 *
*     APF   = OFF                                                     *
*                                                                     *
* OPERATION = BUILDCDE USES THE "LOADER" FORM OF IDENTIFY TO CREATE   *
*             A MAJOR CDE AND CORRESPONDING XL, THEN ISSUES A LOAD    *
*             SVC TO CREATE AN LLE AND ASSOCIATE THE CDE WITH THE     *
*             CURRENT TCB.                                            *
*                                                                     *
* INVOCATION = SEE BELOW                                              *
*                                                                     *
*          (BEFORE)     GETMAIN RU,LV=20000                           *
*                                                                     *
*          (AFTER)      GETMAIN RU,LV=20000                           *
*                       BUILDCDE LENGTH=(0),ADDR=(1),EP=DYNAM         *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*        BUILDCDE MAY BE ISSUED MORE THAN ONCE, PROVIDED THE EP       *
*        KEY-WORD HAS A DIFFERENT VALUE EACH TIME.                    *
*                                                                     *
*        IF STORAGE WAS ALLOCATED IN SUB-POOL ZERO (SP=0 IS           *
*        SPECIFIED OR DEFAULTED TO IN THE GETMAIN MACRO),             *
*        THE DELETE MACRO MAY BE ISSUED AGAINST THE SPECIFIED EP      *
*        TO DELETE THE CDE AND FREE UP THE STORAGE.                   *
*                                                                     *
*        IF GETMAIN SPECIFIES A NON-ZERO SUBPOOL, THE DELETE MACRO    *
*        DELETES THE CDE BUT DOES NOT FREE UP THE STORAGE:  YOU       *
*        MUST ISSUE FREEMAIN YOURSELF.                                *
*                                                                     *
*        IN A MAJOR CDE CREATED BY THE "LOADER" FORM OF IDENTIFY,     *
*        THE "RENT" AND "REUS" ATTRIBUTES ARE OFF.  THIS MEANS        *
*        THAT YOU MAY NOT ISSUE LOAD AGAINST THE EP NAME (BUILDCDE    *
*        HAS ALREADY ISSUED LOAD), BUT YOU MAY ISSUE LINK, ATTACH     *
*        OR XCTL IF YOU WANT TO (ONCE ONLY).  YOU MAY ALSO ISSUE      *
*        THE STANDARD FORM OF IDENTIFY TO DEFINE SECONDARY ENTRY      *
*        POINTS WHICH HAVE THE "RENT" ATTRIBUTE.                      *
*                                                                     *
*        IF THIS MACRO IS USED BY AN AUTHORIZED PROGRAM, THEN         *
*        A S306 ABEND WILL OCCUR (THE CDE CREATED HAS ITS CDSYSLIB    *
*        BIT OFF).  LOOK FOR A SECOND VERSION OF THE MACRO THAT MAY   *
*        BE USED IN AN AUTHORIZED PROGRAM AT THE END OF THIS JOB.     *
*                                                                     *
*        THE IDENTIFY MACRO SUPPLIED IN SYS1.MACLIB SUPPORTS THE      *
*        "LOADER" FORM, BUT THE FORMAT OF THE PARAMETER LIST IS       *
*        NOT DOCUMENTED, AS FAR AS I KNOW.  THE RETURN CODES X'18'    *
*        OR X'1C' MAY BE RETURNED IN R15 WHEN THE PARAMETER LIST      *
*        IS INVALID.  THESE RETURN CODES ARE DOCUMENTED ALONG WITH    *
*        THE IDENTIFY MACRO.                                          *
*                                                                     *
*        WHEN THE LOAD-LIST IS EMPTY (I.E. NO LOAD HAS BEEN ISSUED    *
*        FOR THIS TCB YET), IDENTIFY FAILS WITH A RETURN CODE 12.     *
*        THIS CAN BE CIRCUMVENTED BY ISSUING THE FOLLOWING SEQUENCE:  *
*                                                                     *
*                       LOAD    EP=IEFBR14                            *
*                       GETMAIN RU,LV=20000                           *
*                       BUILDCDE LENGTH=(0),ADDR=(1),EP=DYNAM         *
*                       DELETE  EP=IEFBR14                            *
*                                                                     *
*        I'VE NOTICED THAT ON MVS SYSTEMS PRIOR TO MVS/ESA 4.3, THE   *
*        "LOADER" FORM OF IDENTIFY RETURNS THE ADDRESS OF THE CDE IT  *
*        CREATED IN REG1.  IN MVS/ESA 4.3 THIS IS NO LONGER TRUE.     *
*                                                                     *
*   N.B.: IN MVS 3.8J (HERCULES TURNKEY), THE IDENTIFY PARAMETER LIST *
*        IS ONE WORD SHORTER THAN IN ESA. DON'T KNOW WHEN IT CHANGED. *
*                                                                     *
***********************************************************************
*                                                                     *
*  FOUND IN CBT TAPE VERSION 451, FILE 183, MEMBER BUILDCDE           *
*                                                                     *
***********************************************************************
********* AUTHORIZED VERSION OF THE BUILDCDE MACRO ********************
******  NOTE THAT THE MACRO HAS BEEN EXPANDED, GETMAIN REMOVED,  ******
******  ONE ERROR FIXED (UNAUTH RETURN), AND ENTRY VS. LOAD SET. ******
***********************************************************************
         SPACE 1
         PRINT &PRTSOR
SUBCDE   PGMHEAD ZERO31,BASE=R12,PARM=R1,AM=ANY,RM=24,LOC=BELOW
         SPACE 1
         LA    R9,16         PROVISIONAL ERROR CODE             GP06308
         KANT  16            PROVISIONAL RETURN CODE; FOOTPRINT
         LA    R10,0(,R1)    DID USER PASS A PARM AREA ?
         LTR   R10,R10
         BZ    ERREXIT       NO; FATAL BOO-BOO                  GP06308
         USING PARMSECT,R10
         NI    FWOFLGS1,255-FWOFBCDF   RESET BUILD FAILURE      GP06308
         MVC   LIDENTRY,FW@ENTRY  COPY EP ADDRESS/MODE FLAG
         ICM   R2,15,FW@NAME  ENTRY NAME
         MVC   LIDNAME,0(R2)
         TM    FWOFLGS1,FWOFALTN  BUILD ALTERNATE NAME ?        GP02348
         BZ    NONALT        NO                                 GP02348
         LA    R15,LIDNAME+L'LIDNAME                            GP02348
         LA    R0,L'LIDNAME - 1                                 GP02348
LOOPNALT BCTR  R15,0         GET PRIOR CHARACTER                GP02348
         CLI   0(R15),C' '   NON-BLANK ?                        GP02348
         BH    HAVENALT      YES                                GP02348
         BCT   R0,LOOPNALT   ELSE TRY ANOTHER                   GP02348
HAVENALT XI    0(R15),X'40'  MAKE ALTERNATE NAME                GP02348
NONALT   MVC   LIDSIZE,=F'16'                                   GP02348
         MVC   LID#PAIR,=F'1'
         MVC   LIDLEN,FW#LOAD
         OI    LIDLEN,X'80'  LAST PAIR
         MVC   LIDLOAD,FW@LOAD  MODULE LOAD ADRESS
         LA    R1,LIDPARM    LOAD IDENTIFY PARM LIST
         MNOTE *,'       IDENTIFY EPLOC=0,ENTRY=(R1)'
         SLR   R0,R0                   LOADER-TYPE IDENTIFY
         SVC   41                      ISSUE IDENTIFY SVC
         LTR   R15,R15
         BNZ   EXITCC15      RETURN ERROR CODE
         SPACE 1
         PUSH  USING
         LA    R14,EXITCC15  SET RETURN FOR UNAUTHORIZED LOAD
         LTCB  R4,USE=R4     LOAD TCB ADDRESS                   GP02348
         L     R5,TCBJSCB    GET JSCB                           GP02348
         USING IEZJSCB,R5
         TM    JSCBOPTS,JSCBAUTH   CHECK AUTHORIZATION
         BZ    LOADER        JUMP IF NOT AUTHORIZED
         MODESET KEY=ZERO              SWITCH TO KEY ZERO
         NI    JSCBOPTS,255-JSCBAUTH   TURN APF FLAG OFF
         SYNCH LOADER        EXECUTE ROUTINE WITH TCB KEY
         LR    R9,R15        SAVE RETURN CODE
         OI    JSCBOPTS,JSCBAUTH       TURN APF FLAG BACK ON
         LR    R6,R0                   SAVE EP ADDRESS
         ICM   R0,3,FWOFLGS2  DOES USER WANT CDE ATTR BITS SET? GP02348
         BZ    NOFLAGS       NO                                 GP02348
         ICM   R4,15,TCBJPQ  GET JOB PACK QUEUE                 GP02348
         BZ    NOFLAGS                                          GP02348
         USING CDENTRY,R4    DECLARE THE CDE                    GP02348
LOOPCDE  CLC   LIDNAME,CDNAME  MATCHING MODULE ?                GP02348
         BE    SETFLAGS                                         GP02348
         ICM   R4,15,CDCHAIN ANOTHER CDE ON CHAIN ?             GP02348
         BNZ   LOOPCDE       YES; TRY IT                        GP02348
         B     NOFLAGS       OOPS                               GP02348
         SPACE 1                                                GP02348
SETFLAGS MVC   DB(2),FWOFLGS2   MOVE FLAGS 2 AND 3              GP02348
         NC    DB(2),=AL1(FWOFRENT+FWOFREUS,FWOFAPFL+FWOFAUTH)  GP02348
         OC    CDATTR(2),DB  SET SELECTED FLAGS ON              GP02348
         POP   USING
         SPACE 1                                                GP02348
NOFLAGS  MODESET KEY=NZERO             SWITCH TO TCB KEY        GP02348
         LR    R0,R6                   EP ADDRESS
         LR    R15,R9        RESTORE RETURN CODE
         B     EXITCC15
         SPACE 1
ERROR16  LA    R15,16
         B     EXITCC15
         SPACE 1
ERROR12  LA    R15,12
         B     EXITCC15
         SPACE 1
EXITCC15 LTR   R9,R15        SAVE OVER INTERNALS                GP06308
         BZ    ERREXIT       NO ERROR                           GP06308
         OI    FWOFLGS1,FWOFBCDF   TELL USE CDE BUILD FAILED    GP06308
ERREXIT  PGMEXIT RC=(R9)     RETURN WITH CODE
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*  LOADER - INVOKED BY BRANCH (UNAUTH) OR SYNC (AUTH)                 *
*                                                                     *
*---------------------------------------------------------------------*
         MNOTE *,'       LOAD EPLOC=LIDNAME,DCB=0'
         USING PARMSECT,R10                                     GP02348
LOADER   SR    R0,R0         SIGNAL NO LOAD                     GP02348
         TM    FWOFLGS1,FWOFNOLO  BYOASS LOAD ?                 GP02348
         BNZR  R14           YES; JUST RETURN                   GP02348
         LA    R0,LIDNAME    POINT TO EP NAME
         SLR   R1,R1                   NO DCB FOR LOAD
         SVC   8                       ISSUE LOAD SVC
         BR    R14
         SPACE 2
         PRINT &PRTMAC
         IHACDE ,
         SPACE 1
PARMSECT FETWORK PFX=FW      MAP CALLER'S PARM LIST
         SPACE 2
SAVE     DSECT ,             SAVE & WORK AREA
DB       DS    D
RETCODE  DS    F             RETURN CODE
         SPACE 1
*  IDENTIFY PARAMETER LIST IN LOADER FORMAT
LIDPARM  DS    0F
LIDENTRY DS    A             ADDRESS OF ENTRY POINT
LIDNAME  DS    CL8           ENTRY POINT NAME
         AIF   (NOT &MVSXA).NOFLAGS                             GP06309
         DS    F'0'          (FLAGS?)
.NOFLAGS ANOP  ,                                                GP06309
LIDSIZE  DS    F'16'         XL SIZE
LID#PAIR DS    F'1'          NUMBER OF LENGTH/ADDRESS PAIRS
LIDLEN   DS    A(X'80000000')  LAST FLAG/MODULE SIZE
LIDLOAD  DS    A             MODULE LOAD ADDRESS
SAVEEND  EQU   *             END OF DYNAMIC AREA
         SPACE 2
         PRINT &PRTSYS
         CVT   DSECT=YES
         SPACE 2
         IEZDEB ,
         SPACE 2
         IKJTCB ,
         SPACE 2
         IEZJSCB ,
         SPACE 2
         IHAPSA ,
         END   ,
