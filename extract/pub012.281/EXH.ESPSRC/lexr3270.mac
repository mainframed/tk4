LEXR70   TITLE 'L E X R 3 2 7 0  ***  RELAY/3270 USER EXIT'
         MACRO ,                                                 88270
&NM      SAVEX &R1,&R3,&LOC,&TYPE=*                              88270
&NM      STM   &R1,&R3,&LOC                                      88270
         LR    R8,R1         LOAD PARAMETER BASE                 88270
         USING RAYSECT,R8    DECLARE IT                          88270
         ICM   R9,15,RAYWORK  CHECK FOR WORK AREA                88270
         BNZ   TSM&SYSNDX-&SYSECT.(,R15)  OK                     88270
         LM    &R1,&R3,&LOC  RELOAD                              88270
         SLR   R15,R15       SET RETURN OK                       88270
         BR    R14           RETURN TO CALLER                    88270
TSM&SYSNDX ST  R9,8(,R13)    LINK                                88270
         ST    R13,4(,R9)    SAVE                                88270
         LR    R13,R9        AREAS                               88270
         USING SAVE,R13                                          88270
         USING LEXR3270,R12                                      88270
         MEND  ,                                                 88270
         SPACE 1                                                 88270
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 1
***********************************************************************
*                                                                     *
*        THIS EXIT MAINTAINS THE LOCAL USER DATA AREA (LOUD) FOR A    *
*        VIRTUAL TERMINAL (CREATED BY RELAY/3270) IN PARALLEL WITH    *
*        THE CORRESPONDING NTO OR DIAL-UP LINE                        *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR
LEXR3270 SAVEM NOSAVE,NOWORK,BASE=R12  INITIALIZE AND LINK ADDED 88269
***********************************************************************
*
*        PERFORM FUNCTION TABLE LOOK-UP AND BRANCH TO MATCHING ROUTINE
*
***********************************************************************
         LA    R5,RAYFUNNM   POINT TO ENTRY TYPE                 88269
         BLOOK T=FUNTAB,ERR=EXIT0,R=RAYFUNNM,STRIP=NO  BRANCH BY CODE
         SPACE 1
***********************************************************************
*                                                                     *
*        NEW USER OR SESSION - ESTABLISH OLD AND NEW LOUD             *
*                                                                     *
***********************************************************************
NEWUSER  L     R2,RAYLUBLU   POINT TO REAL LU NAME
         BAL   R9,FINDLOUD   BUILD OR GET LOUD
         ST    R0,OLDLOUD    SAVE IT
         L     R2,RAYLUNEW   POINT TO NEW LU NAME
         CLI   0(R2),C'R'    ACB FORM OF NAME ?                  88270
         BNE   NAMEGOOD      YES                                 88270
         L     R1,CVTPTR     POINT TO CVT                        88270
         L     R1,CVTSMCA-CVTMAP(,R1)  POINT TO SMF              88270
         MVC   0(1,R2),SMCASID+3-SMCABASE(R1)  CPU/MODEL ID      88270
NAMEGOOD BAL   R9,FINDLOUD   BUILD OR GET LOUD
         ST    R0,NEWLOUD    SAVE IT
         MVC   A$SFCTN(3),=AL1(A$SFCOPY,0,A$SBUILD)
         MVC   A$SBUFR,OLDLOUD  MAKE COPY 'FROM' ADDRESS
         ST    R0,A$SUID     PASS NEW LOUD'S ADDRESS             88270
         LA    R1,A$SVCRB
         SVC   249           CALL SVC TO COPY BLOCKS
         BXH   R15,R15,EXIT0 NOT USER'S FAULT ?
         ICM   R1,15,RAYAPPL TEST APPLICATION POINTER
         BZ    EXIT0         EXIT IF NONE
         CLI   0(R1),C'A'    VALID NAME ?
         BL    EXIT0         NO; EXIT
         SPACE 1
***********************************************************************
*                                                                     *
*        PERFORM APPLICATION VALIDITY CHECKING (BY APPLID)            *
*                                                                     *
***********************************************************************
NEWAPPL  XC    A$SVCRB(A$SLENRB),A$SVCRB  CLEAR PARM LIST
         MVC   A$SID,=C'A$RB'  MAKE BLOCK ID
         MVC   A$SFCTN(3),=AL1(A$SFLOGN,0,A$SAKEY+A$SBUILD)
         MVC   A$SBUFR,NEWLOUD  SET LOUD ADDRESS
         L     R1,RAYAPPL
         MVC   A$SACCT,0(R1) PASS APPLICATION NAME
         LA    R1,A$SVCRB    POINT TO PARM LIST
         SVC   249           INVOKE LOCAL SVC
         BXLE  R15,R15,EXIT0
         B     EXIT8         FAIL THIS REQUEST
         SPACE 1
***********************************************************************
*                                                                     *
*        USER IS TERMINATING TTY SESSION - KILL TTY'S LOUD ENTRY      *
*                                                                     *
***********************************************************************
KILLAPP1 ICM   R1,15,OLDLOUD HAVE A LOUD ?                       88270
         BZ    KILLAPPL      NO; CHECK 3270 ENTRY, JUST IN CASE  88270
         XC    A$SVCRB(A$SLENRB),A$SVCRB  CLEAR PARM LIST
         MVC   A$SID,=C'A$RB'  MAKE BLOCK ID
         MVC   A$SFCTN(3),=AL1(A$SFLOGF,0,A$SBUILD)  LOGOFF
         MVC   A$SBUFR,OLDLOUD  SET LOUD ADDRESS
         LA    R1,A$SVCRB
         SVC   249           INVOKE LOCAL SVC
         SPACE 1
***********************************************************************
*                                                                     *
*        USER IS TERMINATING 3270 SESSION - KILL ITS LOUD ENTRY       *
*                                                                     *
***********************************************************************
KILLAPPL ICM   R1,15,NEWLOUD HAVE A LOUD ?                       88270
         BZ    EXIT0         HUH ?                               88270
         XC    A$SVCRB(A$SLENRB),A$SVCRB  CLEAR PARM LIST
         MVC   A$SID,=C'A$RB'  MAKE BLOCK ID
         MVC   A$SFCTN(3),=AL1(A$SFLOGF,0,A$SBUILD)  LOGOFF
         ST    R1,A$SBUFR    SET LOUD ADDRESS
         LA    R1,A$SVCRB
         SVC   249           INVOKE LOCAL SVC
*        B     EXIT0         IGNORE RETURN CODE
         SPACE 1
EXIT0    SLR   R9,R9         SET GOOD RETURN CODE
         B     EXITCOM
         SPACE 1
EXIT8    LA    R9,8          FAIL USER'S REQUEST
EXITCOM  ENDM  RC=(R9),DISP=POP  RETURN WITH APPROPRIATE CODE
         SPACE 1
FINDLOUD XC    A$SVCRB(A$SLENRB),A$SVCRB  CLEAR PARM LIST
         MVC   A$SID,=C'A$RB'  MAKE BLOCK ID
         MVC   A$SFCTN(3),=AL1(A$SFLOUD,0,A$SAKEY+A$SBUILD)
         MVC   A$SACCT,0(R2) MOVE TERMINAL ID SUPPLIED BY CALLER
         LA    R1,MSGBUFF    POINT TO ERROR MESSAGE BUFFER
         ST    R1,A$SERMSG   AND STASH IT
         LA    R1,A$SVCRB    POINT TO PARM LIST
         SVC   249           INVOKE LOCAL SVC
         ICM   R0,15,A$SBUFR  LOAD RETURNED LOUD ADDRESS (OR 0)  88270
         BXLE  R15,R15,0(R9) RETURN IF OK
         WTO   MF=(E,MSGBUFF) WRITE THE MESSAGE
         B     EXIT0         BUT ALLOW USER TO PROCEED
         SPACE 1
FUNTAB   BTAB  'RECEIVE',EXIT0,BASE=*  3270 APPL OUTPUT DATA
         BTAB  'SEND',EXIT0            3270 APPL INPUT DATA
         BTAB  'END3270',KILLAPPL      SESSION TERMINATION
         BTAB  'LOGOFF',KILLAPPL       COMPLETE USER TERMINATION
         BTAB  'LOGON',NEWUSER         INITIAL CONNECTION
         BTAB  'BEG3270',NEWUSER       SESSION START
         BTAB  *END
         SPACE 1
SAVE     DSECT ,
OLDLOUD  DS    A     1/2     LOCAL ON-LINE USER DATA FOR TTY LU
NEWLOUD  DS    A     2/2     L.O.U.D. FOR VIRTUAL 3270
A$SVCRB  A$SVCRB DSECT=NO    DEFINE ACCOUNTING SVC PARAMETER LIST
MSGBUFF  DS    CL125         SVC 249 ERROR MESSAGE BUFFER
*AVEND   EQU   *
         SPACE 1
RAYSECT  DSECT ,             RELAY/3270 PARAMETER LIST
RAYMODNM DS    CL8           EXIT MODULE NAME
RAYFUNNM DS    CL8           FUNCTION CODE NAME
RAYLUBLU DS    A             PTR TO TTY LU NAME
RAYLUNEW DS    A             PTR TO VIRTUAL 3270 LU NAME
RAYAPPL  DS    A             PTR TO APPLICATION NAME
RAYTRAN  DS    A             PTR TO TRANSLATE TABLE NAME
RAYIDCHN DS    A             PTR TO APPLICATION ID CHAIN
RAYWORK  DS    A             PTR TO WORK AREA FOR LINE
RAYPCID  DS    A             PTR TO PC IDENTIFIER
RAYPCNUM DS    A             PTR TO PC SERIAL NUMBER
RAYBUFMX DS    A             PTR TO MAXIMUM BUFFER LENGTH
RAYBUFLN DS    A             PTR TO BUFFER LENGTH
RAYBUFAD DS    A             PTR TO BUFFER ADDRESS
RAYFUN   DS    C             FUNCTION CODE
         SPACE 1
         PRINT &PRTSYS
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IEESMCA ,
         END   ,
