FCBLIST  TITLE 'LIST FCB IMAGES - FROM LA FILE OF MVS MODS 80-007'
         COPY  OPTIONGB                                          83094
         SPACE 1                                                 83094
         SYSPARM LIST=YES                                        83094
         SPACE 2                                                 83094
*   THIS PROGRAM WAS WRITTEN BY :
*        ART  BROWN
*        SOUTHERN CALIFORNIA EDISON
*        (213) 572-2589
*   THIS PROGRAM LISTS THE FCBS IN SYS1.IMAGELIB FOR 3211 AND 3800.
*   SYSUT1 POINTS TO SYS1.IMAGELIB.
*   SYSPRINT IS USED FOR SYSOUT.
*   IF THERE IS NO PARM FIELD, ALL THE FCBS WILL BE LISTED.
*   IF A PARM FIELD EXISTS, IT CONSISTS OF FCB NAMES SEPARATED BY
*        COMMAS AND ONLY THOSE FCBS WILL BE LISTED.
*                                                                83094
*        MINOR CHANGES BY SEYMOUR J. METZ                        83094
*        MORE CHANGES BY GERHARD POSTPISCHIL                     83094
         SPACE 5                                                 83094
         PRINT &PRTSOR                                           83094
FCBLIST  PGMHEAD ZERO12,BASE=R12,PARM=R11                        83094
         MVC   IMAGSIZE(PATLEN),PATSTART                        GP03027
         SERVINIT ,          LOAD THE SERVICE ROUTINE            83094
         SERVCALL LPALD,=CL8'@PRINTER'  GET THE PRINTER ROUTINE  83094
         ST    R0,@PRINTER                                       83094
         PRTOPEN SYSPRINT,OPT=(ABEND)  FAIL IF NO OUTPUT DD      83094
         MVI   PRNTLINE-1,C' '   SET PRINT CLEAR BYTE            83094
         BAL   R10,PRNTCLER  BLANK PRINT LINE                    83094
         LA    R1,INFMJFCB   GET THE JFCB ADDRESS                83094
         STCM  R1,7,EXLIST+1   COMPLETE THE EXIT LIST            83094
         SPACE 1
* PARAMETER FIELD PROCESSING
*
         LA    R11,0(,R11)   CLEAR PRESUMED PARM POINTER         83094
         LTR   R11,R11       VALID POINTER ?                     83094
         BZ    NULLPARM      NO; DO ALL                          83094
         ICM   R1,7,1(R11)   LOAD PARM ADDRESS                   83094
         BZ    NULLPARM      NONE                                83094
         XR    R2,R2                                             83094
         LR    R3,R1          SAVE 1ST PARM ADDR
         LA    R5,PARMTBL     ADDR OF MEMBER NAME TABLE
         ICM   R2,3,0(R1)    LENGTH OF PARM                      83094
         BP    LOOP           YES, PROCESS THEM
NULLPARM OI    PARMBR+1,X'F0' FORCE ALL MEMBERS                  83094
         B     PARMEND        BEGIN PROCESSING
LOOP     DS    0H
         CLI   2(R1),C','     DELIMITER CHARACTER ?  ASF  ,8-29-79
         BE    TBLSTORE       YES, GO MOVE MEM NAME  ASF  ,8-29-79
         LA    R1,1(,R1)      INCR PARM POINTER
         BCT   R2,LOOP        DECR PARM CTR & BRANCH
         LA    R2,1           CAUSE NEXT BCT TO FALL THRU
TBLSTORE DS    0H
         LR    R4,R1          END OF MEM NAME +1
         SR    R4,R3          LENGTH OF NAME
         BCTR  R4,R0          DECR 1 FOR MOVE INSTR
         EX    R4,PARMOVE     MOVE NAME TO TABLE
         LA    R5,8(,R5)      POINT TO NEXT TABLE ELEMENT
         LA    R1,1(,R1)      POINT TO NEXT MEM NAME
         LR    R3,R1          SAVE IT
         BCT   R2,LOOP        GO GET NEXT NAME
         MVI   0(R5),X'FF'    INDICATE END OF TABLE
         SPACE 1                                                 83094
PARMEND  RDJFCB (SYSUT1)     GET THE JFCB                        83094
         BXH   R15,R15,OPENFAIL                                  83094
         OI    JFCBTSDM,JFCNWRIT+JFCNDSCB+JFCNDCB  KEEP IT CLEAN 83094
         NI    JFCBIND1,255-JFCPDS  NOT A MEMBER OPEN            83094
         OPEN  (SYSUT1),TYPE=J                                   83094
         TM    DCBOFLGS-IHADCB+SYSUT1,DCBOFOPN  OPENED ?         83094
         BNZ   DIREADF       YES; READ THE DIRECTORY             83094
OPENFAIL PRTV  MSGOPBAD                                          83094
         SERVTERM ,                                              83094
         ABEND 666,DUMP                                          83094
MSGOPBAD VCON  '-***** UNABLE TO OPEN SYSUT1 *****'              83094
         EJECT ,
* READ SYS1.IMAGELIB DIRECTORY ENTRIES
*
DIREADF  FIND  SYSUT1,DIRTTRK,C  FIND DIRECTORY BLOCK            83094
         MVI   DIRTTRK+3,1   SET 'Z' TO 1 FOR POINT TTR+1        83094
         MVI   PDDRECFM,X'C0'  SET RECFM=U                       85353
DIREAD   READ  RDECB,SF,SYSUT1,DIRAREA,256 READ DIRECTORY        83094
         CHECK RDECB
         NOTE  SYSUT1                                            83094
         STCM  R1,14,DIRTTRK  SET TTR OF CURRENT DIRECTORY ENTRY 83094
         LA    R3,DIRAREA+2   BEGINNING OF DIRECTORY ADDR
         USING PDS2,R3       DECLARE DIRECTORY ENTRY MAPPING     83094
DIR      DS    0H
         CLI   PDS2NAME,X'FF'    END OF DIRECTORY ?              83094
         BE    RETURN         YES, FINISH UP
         LA    R5,PARMTBL     ADDR OF NAME TABLE
PARMSCH  DS    0H
         CLC   PDS2NAME,0(R5)  IS THIS MEM IN TABLE ?            83094
PARMBR   BE    GETMBR         YES, GO PROCESS IT
         CLC   0(8,R5),=CL8'ALL' WAS ALL REQUESTED ?
         BE    GETMBR         YES, GO PROCESS
         LA    R5,8(,R5)      POINT TO NEXT ELEMENT IN TBL
         CLI   0(R5),X'FF'    END OF TABLE ?
         BNE   PARMSCH        NO, CONTINUE CHECKING TABLE
         SPACE 2                                                 85353
* READ SYS1.IMAGELIB MEMBERS
*
NXTENT   MVI   MEMAREA,X'FF'  CLEAR IMAGE AREA...
         MVC   MEMAREA+1(L'MEMAREA-1),MEMAREA ...TO AN END IDENTIFIER
         SLR   R4,R4          CLEAR WORK REG
         NI    PDS2INDC,X'1F'   CLEAR UN-WANTED BITS             83094
         IC    R4,PDS2INDC   GET NUMBER OF HALF WORDS            83094
         SLL   R4,1           CONVERT TO BYTES
         LA    R3,PDS2USRD(R4)  ADDR OF NEXT DIR ENTRY           83094
         LH    R4,DIRAREA     LENGTH OF THIS DIR BLOCK
         LA    R4,DIRAREA(R4) ADDR OF END OF BLOCK
         CLR   R3,R4          FINISHED WITH THIS BLOCK ?
         BL    DIR            NO, KEEP PROCESSING
         POINT SYSUT1,DIRTTRK   POINT TO NEXT DIRECTORY BLOCK    83094
         B     DIREAD         GO GET NEXT BLOCK
         SPACE 2                                                 85353
GETMBR   CLC   =C'FCB',PDS2NAME IS THIS AN FCB IMAGE ?           85353
         BNE   NXTENT         NO, DON'T PROCESS IT
         ST    R3,PDDNEXT    SET MEMBER DESCRIPTION ADDRESS      85353
         SERVCALL PDSDE,SERVPDS  PASS BLOCK                      85353
         LR    R15,R0        COPY EXTENDED INFORMATION POINTER   85353
         MVC   PDDRALIS(PDDRBLNK),0(R15)  GET EXTENDED DATA      85353
         MVC   MEMTTRK(3),PDS2TTRT   DISK ADDR OF MEMBER TEXT    83094
         FIND  SYSUT1,MEMTTRK,C  FIND MEMBER
         READ  MDECB,SF,SYSUT1,MEMAREA,256 READ MEMBER           83094
         CHECK MDECB          WAIT FOR READ TO FINISH
         LA    R6,MEMAREA     POINT TO BEGINNING OF MEMBER
         EJECT ,
* PRINT 3203/3211 IMAGES
*
         MVC   JFCBELNM,PDS2NAME  SET UP IMAGE NAME              83094
         CLI   PDS2NAME+3,C'3'     3800 IMAGE?                   83094
         BE    PR3800         YES, GO FORMAT A 3800
         CLI   PDS2NAME+3,C'2'     3203/3211 IMAGE ?             83094
         BNE   NXTENT         NO, DON'T PRINT IT
         MVC   IMAGTYPE+6(11),=CL11'DEFAULT' ASSUME DEFAULT
         TM    0(R6),X'80'    CORRECT ?
         BO    INDEX          YES, CHECK INDEXING
         MVC   IMAGTYPE+6(11),=C'NON-DEFAULT' SHOW NON-DEFAULT
INDEX    MVC   IMAGINDX+7(10),=CL10'NONE' ASSUME NO INDEXING
         TM    2(R6),X'C0'   IS INDEX BYTE PRESENT ?             85353
         BZ    LENGTH        NO; JUST PROCEED                    85353
         MVC   IMAGINDX+7(L'LMASK),LMASK ASSUME LEFT INDEXING
         TM    2(R6),X'40'    CORRECT ?                          85353
         BZ    CVTINDX        YES, GO CONVERT IT                 85353
         MVC   IMAGINDX+7(L'RMASK),RMASK SHOW RIGHT INDEXING
CVTINDX  MVC   BYTE,2(R6)     GET INDEX BYTE
         NI    BYTE,X'1F'     KEEP ONLY INDEX VALUE
         SLR   R4,R4          CLEAR WORK REG
         IC    R4,BYTE        BINARY VALUE
         CVD   R4,DEC         CVT TO PACKED
         ED    IMAGINDX+7(4),DEC+6    AND EDIT
LENGTH   SLR   R9,R9          CLEAR LENGTH REG
         IC    R9,1(,R6)      GET LENGTH
         STC   R9,IMAGLEN     SAVE FOR DUMP
         LA    R6,2(,R6)      POINT TO 1ST IMAGE BYTE
         TM    0(R6),X'C0'    INDEXING ?                         85353
         BZ    CVTLNGTH       NO, LENGTH IS GOOD
         BCTR  R9,0           DECR 1 FOR INDEX BYTE
         LA    R6,1(,R6)      POINT 1 PAST INDEX BYTE
CVTLNGTH LA    R7,C'6'        ASSUME 6 LINES/INCH                85353
         TM    0(R6),X'10'    CORRECT ?
         BZ    SPACING        YES, SET UP SPACING
         LA    R7,C'8'        SHOW 8 LINES/INCH                  85353
SPACING  STC   R7,IMAGSPAC+10 MOVE SPACING TO PRINT
         LA    R15,15        SET MASK TO KILL ZONE               85353
         NR    R7,R15        KILL ZONE                           85353
         BAL   R10,INCHCVT    FIND FORM LENGTH
         MVC   IMAGSIZE+14(14),IMAGCHAN+23 AND MOVE IT
         PRTLIST HEAD32,TITLE=1   MAKE PAGE HEADER               83094
         PRTLIST SUBHEAD,TITLE=3  MAKE SUB-TITLE                 83094
         PRTF  IMAGSIZE                PRINT SIZE                83094
         PRTF  IMAGSPAC                AND SPACING               83094
         PRTF  IMAGTYPE                AND TYPE                  83094
         PRTF  IMAGINDX                AND INDEXING              83094
         MVI   IMAGCHAN,C'0'  CAUSE DOUBLE SPACE
         LTR   R4,R9         COPY AND TEST IMAGE LENGTH          85353
         BNP   ERROR         INVALID LENGTH BYTE                 85353
         LA    R9,1           START WITH LINE 1
CHANNELS DS    0H
         TM    0(R6),X'0F'    CHAN STOP THIS LINE ?
         BZ    CHANINCR       NO, GO INCR
         SLR   R8,R8          CLEAR WORK REG
         MVC   BYTE(1),0(R6)  GET CHANNEL
         NI    BYTE,X'0F'     CLEAR UNWANTED
         IC    R8,BYTE        LOAD BINARY CHANNEL
         CVD   R8,DEC         CVT TO PACKED
         OI    DEC+7,X'0F'    FIX SIGN
         UNPK  IMAGCHAN+10(2),DEC+6(2) MOVE TO PRINT
         CVD   R9,DEC         PACKED LINE NUMBER
         MVC   IMAGCHAN+17(4),=X'40202020' EDIT MASK
         ED    IMAGCHAN+17(4),DEC+6    MOVE LINE NUMBER
         BAL   R10,INCHCVT    DETERMINE INCHES FROM LINE
         MVC   PRNTLINE(L'IMAGCHAN),IMAGCHAN MOVE CHANNEL
         BAL   R10,PRINT      ...AND PRINT IT
         MVI   IMAGCHAN,C' '  CAUSE SINGLE SPACING
CHANINCR DS    0H
         LA    R6,1(,R6)      INCR POINTER
         LA    R9,1(,R9)      INCR LINE COUNTER
         BCT   R4,CHANNELS    DECR LNGTH AND BRANCH
CHANEND  DS    0H
         LTR   R4,R4          SHOULD BE ZERO
         BNZ   ERROR          NO, PRINT ERROR MSG
         BCTR  R6,R0          BACK UP 1
         TM    0(R6),X'10'    SHOULD BE END BYTE
         BZ    ERROR          NO, PRINT ERROR MSG
         EJECT ,
* FORMATTED DUMP OF IMAGE IN HEX
*
DUMP     DS    0H
         LA    R6,MEMAREA     BEGINNING OF IMAGE
         L     R4,FOXZERO     ADDRESS COUNTER
         SLR   R7,R7          ENSURE ONLY LENGTH
         IC    R7,IMAGLEN     ...AND LOAD IT
         AR    R7,R6          ENDING ADDRESS OF IMAGE
         LA    R7,2(,R7)      TO BE SURE
         LA    R9,PRNTLINE+8  BEGINNING OF DUMP
         MVI   PRNTLINE-1,X'EF' CLEAR PRNTLINE TO 0 - 1          83094
         MVC   PRNTLINE(L'IMAGDUMP),IMAGDUMP MOVE DUMP HEADING
         BAL   R10,PRINT      ...AND PRINT IT
         MVI   PRNTLINE,C'0'  CAUSE DOUBLE SPACE
AGAINEF  LA    R14,PRNTLINE+52   HALF-WAY MARK                   83094
         LA    R15,PRNTLINE+98   END OF DUMP LINE                83094
AGAIN    UNPK  0(9,R9),0(5,R6)  UNPK 9 BYTES                     83094
         MVI   8(R9),X'EF'    CLEAR THE BAD ONE
         LA    R6,4(,R6)      POINT TO NEXT EIGHT BYTES
         CLR   R7,R6          END OF IMAGE ?
         BL    DUMPDONE       YES, FINISH UP
         LA    R9,11(,R9)     FORMAT LIKE A ZAP DUMP
         CR    R9,R14        END OF PRINT LINE ?                 83094
         BL    AGAIN          NOT YET, KEEP GOING
         BH    HALF2          GO CHECK FOR END
         LA    R9,2(,R9)      CENTER GETS A WIDER MARGIN
         B     AGAIN          KEEP GOING
         SPACE 1                                                 83094
HALF2    CR    R9,R15        END OF PRINT LINE ?                 83094
         BL    AGAIN          NO, KEEP GOING
         STCM  R4,7,PRNTLINE+2 ADDR COUNTER
         LA    R4,512(,R4)    ADD 20 TO ADDR CTR
         TR    PRNTLINE,HEXTBL TRANSLATE EF THRU FF
         BAL   R10,PRINT      GO PRINT IT
         LA    R9,PRNTLINE+8  POINT TO BEGINNING OF LINE
         B     AGAINEF       ...AND CONTINUE                     83094
DUMPDONE DS    0H
         STCM  R4,7,PRNTLINE+2 ADDR FOR FINAL LINE
         TR    PRNTLINE,HEXTBL FINAL TRANSLATION
         LA    R9,PRNTLINE+98  PREPARE TO TRUNCATE BUFFER FILL CHARS
DUMPTRNK CLI   0(R9),C' '      IS IT A BLANK?
         BE    DUMPDECR        YES, GET NEXT BYTE
         CLI   0(R9),C'F'      IS IT A BUFFER FILL CHARACTER?
         BNE   DUMPRNT         NO, SCAN IS DONE
         MVI   0(R9),C' '      DROP BUFFER FILL CHARACTERS
DUMPDECR BCT   R9,DUMPTRNK     GET NEXT BYTE
DUMPRNT  DS    0H
         MVI   PRNTLINE-1,C' ' CLEAR TO SPACES AGAIN             83094
         CLI   PRNTLINE+8,C' '  EMPTY LINE ?                     85353
         BE    DUMPEXIT      YES; DON'T PRINT OFFSET ONLY        85353
         BAL   R10,PRINT      PRINT IT
DUMPEXIT BAL   R10,PRNTCLER  CLEAR PRINT BUFFER                  85353
         B     NXTENT         ...AND GET NEXT IMAGE
         EJECT ,
* PRINT 3800 IMAGES
*
PR3800   DS    0H
         MVI   LASTBYTE,X'FF' CAUSE FIRST LINE TO PRINT
         MVC   IMAGIDNT+11(4),0(R6) SETPRT IMAGE IDENT
         SLR   R9,R9          CLEAR WORK REG
         IC    R9,7(,R6)      LENGTH OF MODULE
         LA    R9,8(,R9)      PLUS EXTRA LENGTH OF 3800 HEADER
         STC   R9,IMAGLEN     SAVE FOR DUMP
         IC    R9,7(,R6)      GET LENGTH AGAIN
         BAL   R10,SIZE3800   GO ADD UP ALL THE LINE THICKNESSES
         ST    R9,ENDLEN      FORM LENGTH IN 1/24 INCHES
         LA    R7,24          SET A DENOMINATOR
         BAL   R10,INCHCVT    GO CONVERT TO INCHES
         MVC   IMAGSIZE+14(19),IMAGCHAN+23 MOVE INTO TEXT
         PRTLIST HEAD38,TITLE=1   MAKE PAGE HEADER               83094
         PRTLIST SUBHEAD,TITLE=3  MAKE SUB-TITLE                 83094
         PRTF  IMAGIDNT                SETPRT ID                 83094
         PRTF  IMAGSIZE                 AND SIZE                 83094
         MVI   IMAGCHAN,C'0'  CAUSE DOUBLE SPACE FOR NEXT LINE
         LA    R4,1           START WITH LINE 1
         SLR   R9,R9          START AT 0 LENGTH
         LA    R6,8(,R6)      GET BEGINNING OF LINES DATA
CH3800   DS    0H
         MVC   BYTE,0(R6)     SPACING AND CHANNEL FOR A LINE
         SLR   R8,R8          CLEAR WORK REG
         IC    R8,BYTE        PUT IT IN A WORK REG
         SRL   R8,4           KEEP ONLY SPACING INFO
         STC   R8,BYTE        SAVE SPACING INFO
         CLC   BYTE,LASTBYTE  HAS SPACING CHANGED?
         BE    SAMESPAC       NO, DON'T PRINT IT
         OI    PRSW,X'01'     CAUSE SPACING INFO TO PRINT
         STC   R8,LASTBYTE    SAVE FOR NEXT TIME AROUND
         IC    R7,LPI(R8)     CONVERT INFO TO LINES/INCH
         CVD   R7,DEC         MAKE THAT DECIMAL
         MVC   IMAGSPAC+8(4),=X'40202020'  EDIT MASK
         ED    IMAGSPAC+8(4),DEC+6 CONVERT SPACING TO EBCDIC
         MVI   IMAGSPAC+8,C'=' PUT THE "=" BACK
         MVC   PRNTLINE+42(L'IMAGSPAC-1),IMAGSPAC+1 SPACING TO PRNT
SAMESPAC SLR   R0,R0          CLEAR WORK REG
         IC    R0,THICKNS(R8) GET LINE THICKNESS FOR THIS SPACING
         AR    R9,R0          ADD IT TO TOTAL FROM PAGE TOP
         ST    R9,TEMP9       SAVE THE TOTAL
         LA    R7,24          SET LOWEST COMMON DENOM FOR 6 8 12
         BAL   R10,INCHCVT    GO CONVERT TO INCHES AND FRACTION
         CVD   R4,DEC         MAKE LINE NUMBER DECIMAL
         MVC   IMAGCHAN+17(4),=X'40202020' EDIT MASK
         ED    IMAGCHAN+17(4),DEC+6 EBCDIC LINE NUMBER
         MVC   BYTE,0(R6)     GET CHANNEL DATA
         NI    BYTE,X'0F'     PURIFY THE CHANNEL DATA
         BZ    TST3800        NO CHANNEL DATA GO TST FOR PRNT
         OI    PRSW,X'10'     CAUSE THIS LINE TO PRINT
         IC    R8,BYTE        GET CHANNEL ID IN HEX
         CVD   R8,DEC         CONVERT TO DECIMAL
         OI    DEC+7,X'0F'    FIX THE SIGN
         UNPK  IMAGCHAN+10(2),DEC+6(2) MAKE PRINTABLE
TST3800  CLI   PRSW,X'00'     ANYTHING WORTH PRINTING?
         BE    INC3800        NO, DON'T PRINT
         MVC   PRNTLINE(L'IMAGCHAN),IMAGCHAN MOVE CHANNEL TO PRNT
         TM    PRSW,X'10'     ANY CHANNEL DATA
         BO    PRNTITAL       YES, PRINT IT
         MVC   PRNTLINE+1(12),=CL12' ' BLANK OUT CHANNEL INFO    83094
PRNTITAL BAL   R10,PRINT      GO PRINT THIS LINE
         MVI   IMAGCHAN,C' '  FORCE SINGLE SPACING
         MVI   PRSW,X'00'     TURN OFF PRINT SWITCH
INC3800  DS    0H
         LA    R6,1(,R6)      INCR BUFFER PTR
         LA    R4,1(,R4)      INCR LINECTR
         CL    R9,ENDLEN      END OF BUFFER?
         BL    CH3800         NO, GO DO NEXT LINE
         LA    R4,7(,R4)      ADJUST BUFF LEN WITH HEADER LENGTH
         STC   R4,BYTE        AND SAVE IT FOR COMPARE
         CLC   BYTE,IMAGLEN   DID THEY COME OUT THE SAME?
         BE    DUMP           YES, GO PRINT THE IMAGE DUMP
         SPACE 1                                                 83094
ERROR    PRTV  ERRLINE                 SHOW ERROR                83094
         B     DUMP           GO PRINT MODULE DUMP
         SPACE 2                                                 85353
SIZE3800 DS    0H
         STM   R5,R9,REGS     SAVE THE CHANGED REGISTERS
         SLR   R5,R5          CLEAR WORK REGISTER
         SLR   R7,R7          CLEAR ANOTHER ONE
         LA    R6,8(,R6)      POINT TO FIRST LINE DATA
FORMSIZE DS    0H
         IC    R5,0(,R6)      GET SPACING AND CHANNEL DATA
         SRL   R5,4           MAKE SPACING INDEX
         IC    R5,THICKNS(R5) GET LINE THICKNESS IN 1/24 INCH
         AR    R7,R5          ADD IT TO TOTAL
         LA    R6,1(,R6)      POINT TO NEXT BYTE
         BCT   R9,FORMSIZE    GO THROUGH WHOLE IMAGE
         ST    R7,REGS+16     SAVE TOTAL LENGTH IN R9'S PLACE
         LM    R5,R9,REGS     RESTORE THE REGS, R9 GETS LENGTH
         BR    R10            RETURN
         EJECT ,
* CONVERTS A NUMERATOR IN R9 AND A DENOMINATOR IN R7 TO A
* MIXED FRACTION REDUCED TO THE LOWEST TERMS.
*
INCHCVT  DS    0H
         STM   R5,R9,REGS     SAVE THE REGISTERS USED
         LTR   R7,R7          DENOMINATOR CAN'T BE ZERO
         BZR   R10            SOMETHING'S WRONG JUST RETURN
         SLR   R8,R8          CLEAR FOR DIVISION
         DR    R8,R7          DIVIDE NUMERATOR BY DENOMINATOR
         CVD   R9,DEC         CONVERT INTEGER QUOTIENT TO DEC
         MVC   IMAGCHAN+22(20),FRACTMSK  INITIALIZE EDIT AREA
         ED    IMAGCHAN+22(4),DEC+6 EDIT INTEGER PART OF FRACTION
         LTR   R9,R8          PREPARE NUMERATOR FOR REDUCTION
         BZ    INCHEVEN       NO FRACTION JUST MOVE OVER "INCHES"
         ST    R9,TEMP9       SAVE INITIAL VALUE OF NUMERATOR
         ST    R7,TEMP7       SAVE INITIAL VALUE OF DENOMINATOR
         LR    R5,R9          SET REDUCTION COUNTER
INCHLOOP DS    0H
         C     R5,=F'1'       LOWEST POSSIBLE?
         BE    INCHDONE       YES, MIGHT AS WELL QUIT
         SLR   R8,R8          PREPARE FOR DIVISION
         DR    R8,R5          TRY TO REDUCE WITH ALL POSSIBLE VALUES
         LTR   R8,R8          EVENLY DIVISIBLE BY R5?
         BZ    INCHDENM       YES, GO TRY DENOMINATOR
INCHDECR DS    0H
         L     R9,TEMP9       RESTORE NUMERATOR
         BCT   R5,INCHLOOP    GET NEXT DIVISOR AND TRY AGAIN
         B     INCHDONE       ALL DONE GO MAKE EBCDIC
INCHDENM DS    0H
         SLR   R6,R6          PREPARE FOR DIVISION
         DR    R6,R5          SEE IF DENOM IS ALSO DIVISIBLE
         LTR   R6,R6          IS IT?
         BZ    INCHRDUC       YES, GO REDUCE THE SAVED VALUES
         L     R7,TEMP7       NULLIFY THE DIVISION
         B     INCHDECR       GO TRY ANOTHER DIVISOR
INCHRDUC DS    0H
         ST    R7,TEMP7       SAVE NEW DENOMINATOR
         ST    R9,TEMP9       SAVE NEW NUMERATOR
         LR    R5,R9          RESET THE DIVISOR
         B     INCHLOOP       GO START FROM THE TOP
INCHDONE DS    0H
         CVD   R9,DEC         CONVERT NUMERATOR TO DECIMAL
         MVC   IMAGCHAN+26(4),=X'40202020' MOVE EDIT MASK
         ED    IMAGCHAN+26(4),DEC+6 EDIT NUMERATOR
         CVD   R7,DEC         CONVERT DENOMINATOR TO DECIMAL
         MVC   IMAGCHAN+31(4),=X'40202020' MOVE EDIT MASK
         ED    IMAGCHAN+31(4),DEC+6 EDIT DENOMINATOR
         MVI   IMAGCHAN+30,C'/' PUT SLASH IN FOR FRACTION
INCHEVEN LA    R5,8           SET SCAN LIMIT
         LA    R6,IMAGCHAN+27 SET SCAN START
         LR    R8,R6          START WITH SAME VALUE IN R8
INCHCPRS DS    0H
         IC    R7,0(,R6)      GET A CHARACTER FROM EBCDIC FRACTION
         CLI   0(R6),C' '     IS IT A BLANK?
         BE    INCHDROP       YES, COMPRESS IT OUT
         STC   R7,0(,R8)      OTHERWISE MOVE IT OVER
         LA    R8,1(,R8)      INCREMENT STORE POINTER
INCHDROP DS    0H
         LA    R6,1(,R6)      INCREMENT LOAD POINTER
         BCT   R5,INCHCPRS    CONTINUE SCANNING FOR BLANKS
         MVC   0(7,R8),INCHES MOVE OVER THE WORD "INCHES"
         LM    R5,R9,REGS     RESTORE THE REGISTERS
         BR    R10            RETURN
         SPACE 2                                                 83094
* COMMON PRINT ROUTINE
*
PRINT    PRTF  PRNTLINE                                          83094
PRNTCLER MVC   PRNTLINE,PRNTLINE-1  CLEAR PRINT LINE             83094
         BR    R10            RETURN
         SPACE 2                                                 83094
RETURN   CLOSE (SYSUT1)                                          83094
         SERVTERM ,          CLOSE AND FREE                      83094
         PGMEXIT RC=0
         SPACE 2                                                 83094
         LTORG ,
         EJECT ,                                                 83094
MEMTTRK  DC    F'0'           MEMBER CSECT ADDR
DIRTTRK  DC    X'00000100'    DIRECTORY BLK ADDR
DIRAREA  DC    XL256'00'      DIRECTORY AREA
MEMAREA  DC    XL256'00'      MEMBER AREA
PARMOVE  MVC   0(0,R5),2(R3)  FOR LOADING TABLE                  83094
HEAD32   FDOPT NL,CC=C'#'    AUTO PAGE/DATE/TIME                 83094
         FDPRT '3203/3211'                                       85353
         FDGOTO HEADCOM                                          83094
HEAD38   FDOPT NL,CC=C'#'                                        83094
         FDPRT '3800'                                            83094
HEADCOM  FDPRT 'FORMS CONTROL BUFFER DESCRIPTION',PADL           83094
         FDPRT *END                                              83094
SUBHEAD  FDPRT 'NAME=',NL                                        83094
         FDPRT JFCBELNM,DEB                                      83094
         FDCLI PDDRMAIN,C' ',BE=SUBHEADN                         85353
         FDPRT ', ALIAS OF'                                      85353
         FDPRT PDDRMAIN,PADL                                     85353
SUBHEADN FDCLI PDDRDATE,C' ',BE=SUBHEADD                         85353
         FDPRT ', AS OF'                                         85353
         FDPRT PDDRDATE,PADL                                     85353
SUBHEADD FDOPT SBA=(*,78)                                        85353
         FDPRT JFCBDSNM,DEB,RADJ,LEN=44                          85353
         FDPRT 'ON',PAD                                          83094
         FDPRT JFCBVOLS,6                                        83094
         FDPRT *END                                              83094
HEXTBL   EQU   *-X'EF'        UNUSED PORTION OF TABLE
         DC    C' 0123456789ABCDEF' = HEX EF THRU HEX FF
RMASK    DC    X'4020202140D9C9C7C8E3'
LMASK    DC    X'4020202140D3C5C6E340'
FRACTMSK DC    X'40202020',CL16' '  INCH INTEGER MASK
INCHES   DC    C' INCHES'
BYTE     DC    X'00'          SINGLE BYTE WORKAREA
LASTBYTE DC    X'00'          PREVIOUS SPACING AND CHNL 3800
PRSW     DC    X'00'          PRINT SWITCH FOR 3800
ENDLEN   DC    F'0'           3800 BUFFER LENGTH IN 1/24 INCHES
FOXZERO  DC    X'00F0F0F0'    BEGINNING DUMP ADDRESS
IMAGLEN  DC    X'0'
LPI      DC    AL1(6,8,8,12)  LINES PER INCH TABLE
THICKNS  DC    AL1(4,3,3,2) TBL OF LINE THICKNESS'S PER LPI
         SPACE 2                                                 83094
SYSPRINT PRTWORK SYSPRINT,SYSTERM,TITLE=5                        83094
EXLIST   DC    0A(0),X'87',AL3(0)   INFMJFCB                     83094
SYSUT1   DCB   DDNAME=SYSUT1,                                          *
               DSORG=PO,                                               *
               RECFM=U,                                                *
               BLKSIZE=256,                                            *
               LRECL=256,                                              *
               EODAD=RETURN,                                           *
               EXLST=EXLIST,                                           *
               MACRF=(R)
         SPACE 1                                                GP03027
PATSTART DS    0F            MOED TO SAVE AREA                  GP03027
PMAGSIZE DC    C'0FORM LENGTH= *** */* INCHES    '              GP03027
PMAGSPAC DC    C'0SPACING= *  LINES PER INCH    '               GP03027
PMAGTYPE DC    C'0TYPE=NON-DEFAULT       '                      GP03027
PMAGINDX DC    C'0INDEX= *** RIGHT      '                       GP03027
PMAGCHAN DC    C'  CHANNEL **=LINE ***, *** ***/ *** INCHES'    GP03027
PMAGIDNT DC    C'0SETPRT ID=**** '                              GP03027
PERRLINE VCON  '0  **IMAGE LENGTH ERROR'                        GP03027
PMAGDUMP DC    C'-              ***IMAGE DUMP***'               GP03027
PATLEN   EQU   *-PATSTART                                       GP03027
         SPACE 2                                                 83094
SAVE     DSECT ,
DEC      DC    D'0'           DBL WRD WORK AREA
@SERVICE DS    A                                                 83094
@PRINTER DS    A                                                 83094
SERVPDS  SERVPDS DSECT=,RETURN=YES,VER=1                        GP12042
REGS     DS    5F             SAVE AREA FOR REGS R5-R9
TEMP7    DS    F              REG 7 SAVE AREA
TEMP9    DS    F              REG 9 SAVE AREA
         DC    C' '   1/2    BYTE TO CLEAR PRINTLINE             83094
PRNTLINE DS    CL133  2/2     PRINT LINE
         SPACE 1                                                GP03027
IMAGSIZE DC    C'0FORM LENGTH= *** */* INCHES    '              GP03027
IMAGSPAC DC    C'0SPACING= *  LINES PER INCH    '               GP03027
IMAGTYPE DC    C'0TYPE=NON-DEFAULT       '                      GP03027
IMAGINDX DC    C'0INDEX= *** RIGHT      '                       GP03027
IMAGCHAN DC    C'  CHANNEL **=LINE ***, *** ***/ *** INCHES'    GP03027
IMAGIDNT DC    C'0SETPRT ID=**** '                              GP03027
ERRLINE  VCON  '0  **IMAGE LENGTH ERROR'                        GP03027
IMAGDUMP DC    C'-              ***IMAGE DUMP***'               GP03027
         SPACE 1                                                GP03027
         IEFJFCBN ,                                              83094
PARMTBL  DC    100CL8' '      PARM TABLE                        GP03027
SAVEEND  EQU   *
         SPACE 1                                                 83094
         PRINT &PRTSYS                                           83094
CVTDSECT DSECT ,                                                 83094
         CVT   DSECT=YES                                         83094
         IHACDE ,                                                83094
         DCBD  DEVD=DA,DSORG=PS                                  83094
         IHAPDS PDSBLDL=NO,DSECT=YES                             83094
         END   ,
