CATEDSP  TITLE '   WRITES CATALOG ENTRY DATA TO SYSUT1                 X
               CATEDIT --- UICC/GTEL CATALOG EDIT PROGRAM'         DCL
*                                                                  DCL
*   CATEDSP                          -------- UICC/B.SCHRAGER      DCL
*                                                 /T.KOLE          DCL
*                                    -------- BOEING COMP SERV     DCL
*                                    07/16/76 UICC/E.KLEMENS       DCL
*                                    01/16/79 GTEL/D.C.LIVINGSTON  DCL
*                                                                  DCL
*   * WRITES CATALOG ENTRY DATA TO SYSUT1 *                        DCL
*                                                                  DCL
*                                                                  DCL
*   THIS CSECT CONTAINS FOUR SUBROUTINES WHICH PERFORM FOUR DIF-   DCL
*   FERENT OPERATIONS ON THE SYSUT1 TEMPORARY DATASET.  THE NAMES  DCL
*   AND FUNCTIONS OF THE SUBROUTINES ARE:                          DCL
*                                                                  DCL
*     DSPEO     OPENS SYSUT1 FOR OUTPUT                            DCL
*     DSPE      WRITES ONE DATA RECORD TO SYSUT1                   DCL
*     EMPINDX   FORMS DATA RECORD FOR INDEX TO BE DELETED,         DCL
*                 BRANCHES TO SUBROUTINE DSPE TO WRITE RECORD      DCL
*     DSPEC     CLOSES SYSUT1                                      DCL
*                                                                  DCL
*                                                                  DCL
         EJECT
*                                                                  DCL
*   THE FOLLOWING MACRO DEFINES PRIMARY ENTRY POINTS TO THE FOUR   DCL
*   SUBROUTINES SO THAT THE SAME SECTION OF CODE PERFORMS STAN-    DCL
*   DARD INWARD LINKAGE FOR ALL FOUR SUBROUTINES, AFTER WHICH      DCL
*   BRANCHES ARE TAKEN TO SECONDARY ENTRY POINTS FROM WHICH PRO-   DCL
*   CESSING FOR THE INDIVIDUAL SUBROUTINE FUNCTIONS BEGINS         DCL
*                                                                  DCL
         EJECT
         MACRO
         MENT  &A
         LCLA  &K,&L,&M
&K       SETA  N'&A
&L       SETA  0
.LOOP1   ANOP
&M       SETA  &L*4
&L       SETA  &L+1
         ENTRY &A(&L)
         USING *,15
&A(&L)   LA    0,&M
         B     MAINENT
         DROP  15
         AIF   (&L LT &K).LOOP1
MAINENT  STM   14,12,12(13)
         BALR  2,0
         USING *,2
         LA    15,SAVEAREA
         ST    13,4(15)
         ST    15,8(13)
         LR    13,15
         B     SAVEAREA+72
SAVEAREA DC    18A(0)
         DROP  2
         USING SAVEAREA,13
         LR    15,0
         B     *+4(15)
&L       SETA  0
.LOOP2   ANOP
&L       SETA  &L+1
         B     E&A(&L)
         AIF   (&L LT &K).LOOP2
         MEND
         EJECT
*                                                                  DCL
*   OPENS SYSUT1 FOR OUTPUT, BRANCHES TO LABEL 'RETURN' FOR STAN-  DCL
*   DARD OUTWARD LINKAGE BACK TO CALLING ROUTINE                   DCL
*                                                                  DCL
         SPACE 1                                                   DCL
CATEDSP  CSECT
         PRINT NOGEN                                             85230
         MENT  (DSPEO,DSPE,EMPINDX,DSPEC)
EDSPEO   OPEN  (SYSUT1,OUTPUT) OPEN SYSUT1 FOR OUTPUT              DCL
         B     RETURN         RETURN TO CATEDIT
         EJECT
*                                                                  DCL
*   RECEIVES DATA FROM CALLING ROUTINE FOR INSERTION INTO SYSUT1   DCL
*   RECORD, INSERTS DATA INTO APPROPRIATE FIELDS IN RECORD BUF-    DCL
*   FER, WRITES RECORD TO SYSUT1, BRANCHES TO LABEL 'RETURN' FOR   DCL
*   STANDARD OUTWARD LINKAGE BACK TO CALLING ROUTINE               DCL
*                                                                  DCL
         SPACE 1                                                   DCL
EDSPE    LM    3,6,0(1)       GET PARMS FROM ICE
         MVC   CARD,BLANK     CLEAR SYSUT1 BUFFER
         MVC   CDSN,0(3)      MOVE CATALOG ENTRY DSNAME, TTR,
         MVC   CDSCBTTR,0(4)    DEVTYPE, VOLSER FIELDS TO SYSUT1
         MVC   CVOLTYP,0(5)     BUFFER
         MVC   CVOLSER,4(5)
EDSP1    L     15,=V(CVOL)    MOVE SELECTED CATALOG VOLSER TO      DCL
         MVC   CCVOL(6),0(15)   SYSUT1 BUFFER                      DCL
         MVI   CCI,C' '      PROVISIONALLY SET NON-VSAM FLAG     85230
         TM    8(1),X'01'     MASTER CATALOG ENTRY?                DCL
         BZ    EPUT           NO; GO TO EPUT                       DCL
         MVC   CCI(4),0(6)    YES; MOVE MASTER CATALOG RECORD      DCL
*                               CONTROL INTERVAL NUMBER TO SYSUT1  DCL
*                               BUFFER                             DCL
EPUT     EQU   *
         PUT   SYSUT1,CARD    WRITE RECORD TO SYSUT1               DCL
         B     RETURN         RETURN TO ICE
         EJECT
*                                                                  DCL
*   RECEIVES NAME OF EMPTY INDEX FROM CALLING ROUTINE, FORMS SYS-  DCL
*   UT1 RECORD SPECIFYING THAT INDEX IS TO BE DELETED, BRANCHES    DCL
*   TO LABEL 'EDSP1' IN SUBROUTINE 'EDSPE' TO WRITE RECORD TO      DCL
*   SYSUT1 AND PERFORM STANDARD OUTWARD LINKAGE BACK TO CALLING    DCL
*   ROUTINE                                                        DCL
         SPACE 1                                                   DCL
EEMPINDX L     3,0(1)         GET PARM FIELD
         MVC   CARD,BLANK     BLANK OUT CARD
         MVC   CDSN,0(3)      MOVE INDEX LEVEL TO BE DELETED
         MVC   CVOLTYP,=X'0000FF00' SIGNAL TO PROCESS
         SPACE 1                                                   DCL
****************************************************************** DCL
*                                                                * DCL
*   MODIFICATION BY DCL:  NEXT THREE INSTRUCTIONS IN ORIGINAL    * DCL
*                         VERSION WERE:                          * DCL
*                                                                * DCL
*        L     6,16(1)        GET PARM5                          * DCL
*        MVC   CCVOL(6),0(6)  COPY OUT THE CURRENT CVOL          * DCL
*        B     EPUT           PUT CARD                           * DCL
*                                                                * DCL
*     THIS FAILED TO SET CVOL INTO PARM FIELD, THEREBY CAUSING   * DCL
*     DELETION OF EMPTY HIGH-LEVEL INDEXES TO FAIL.  REPLACING   * DCL
*     THESE THREE INSTRUCTIONS WITH A SINGLE BRANCH TO LABEL     * DCL
*     'EDSP1' CORRECTS THE PROBLEM.                              * DCL
*                                                                * DCL
****************************************************************** DCL
         SPACE 1                                                   DCL
         B     EDSP1          GO TO EDSP1                          DCL
         EJECT
*                                                                  DCL
*   CLOSES SYSUT1, PERFORMS STANDARD OUTWARD LINKAGE BACK TO       DCL
*   CALLING ROUTINE                                                DCL
*                                                                  DCL
         SPACE 1                                                   DCL
EDSPEC   CLOSE (SYSUT1)       CLOSE SYSUT1                         DCL
RETURN   L     13,4(13)       RELOAD OLD SAVE AREA ADDRESS
         LM    14,12,12(13)   PERFORM STANDARD OUTWARD LINKAGE,
         BR    14               AND RETURN TO DRIVER
BLANK    DC    CL80' '        BLANKS TO CLEAR BUFFER 'CARD'        DCL
         SPACE 4
         DS    0F             ALIGN CARD
CARD     DS    0CL80          SYSUT1 BUFFER
CDSN     DS    CL44           DATA SET NAME
CVOLSER  DS    CL6            VOLUME SERIAL
CVOLTYP  DS    XL4            DEVICE TYPE
CDSCBTTR DS    XL3            RELATIVE TTR OF DSCB IN VTOC
CCVOL    DS    CL6            CURRENT CVOL
CCI      DS    XL4            FOR VSAM MASTER CATALOG ENTRIES,     DCL
*                               CONTROL INTERVAL NUMBER OF NON-    DCL
*                               VSAM RECORD CONTAINING ENTRY       DCL
*                             FOR NONVSAM 'USER' CATALOG ENTRIES,  DCL
*                               BLANK                              DCL
         DS    CL13           FILLER                               DCL
         SPACE 3
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=(PM),RECFM=FB,                  DCL X
               LRECL=80,DSORG=PS
         END
