LISTUADS TITLE 'L I S T U A D S  ***  LIST SYS1.UADS'
*--------------------------------------------------------------------*
*
*        L I S T U A D S
*
*    LISTUADS (UADLIST) IS DESIGNED TO READ SYS1.UADS AND PRINT THE
*    FOLLOWING:
*    USERID, PASSWORD, ACCT INFO, LOGON PROCS, UNIT, PCF DATA,
*    AND ATTRIBUTES SUCH AS OPER, ACCT, JCL, AND MOUNT.
*
*    LISTUADS HANDLES 6 OR 7 CHARACTER USERIDS
*    LISTUADS HANDLES MULTIPLE LOGON PROCS
*    LISTUADS DOES PRINT THE PASSWORD                           GP12326
*
*    WRITTEN BY CAPT PAUL E. SCHAFER
*    AIR FORCE DATA SERVICES CENTER, PENTAGON, WASHINGTON, D.C.
*
*    LAST UPDATE BY AUTHOR  APRIL 1982 TO DOCUMENT AND ADD
*    CODE TO PRINT ATTRIBUTES.
*
*    UPDATED BY ME, JIM MARSHALL, AUGUST 1993.  DID A MAJOR
*    REWRITE TO PREMISE THERE MAY BE MULTIPLE PASSWORDS,
*    MULTIPLE ACCOUNT CODES, AND MULTIPLE LOGON PROCS. PLUS
*    I CHANGED THE WAY TO HOW END-OF-CHAINS ARE FOUND.  WITHOUT
*    PAUL'S MODEL OF HOW TO BEGIN, I WOULD HAVE PROBABLY TAKEN
*    A DIFFERENT APPROACH TO GETTING A LIST OF UADS.
*
*    JCL FOR USE:
*
*        //LISTUADS EXEC PGM=LISTUADS
*        //UADS      DD  DISP=SHR,DSN=SYS1.UADS
*        //LISTUADS  DD  SYSOUT=*
*
*    THE FOLLOWING ARE THE UADS POINTER ARRANGEMENTS AS CAN BE
*    FOUND IN GY28-6771-1, IBM S/360 OS TSO COMMAND PROCESSOR
*    PLM  VOLUME 1  ACCOUNT IF YOU CARE TO DIG.
*
*          HEADER BLOCK
*     *----------------------------------*
* + 4 * USERID                           *
* +16 * USER ATTRIBUTES (SYS)            *
* +18 * USER ATTRIBUTES (INSTALLATION)   *
* +24 * OFFSET FROM ANCHOR TO PASSWD BLK *
*     *----------------------------------*
*
*         PASSWORD OFFSET BLOCK
*     *--------------------------------------*
*  +0 * OFFSET TO NEXT PASSWORD OFFSET BLOCK *
*  +4 * PTR ACCOUNT NUMBER OFFSET BLOCK      *
*  +8 * PTR PASSWORD DATA BLOCK              *
*     *--------------------------------------*
*
*                              PASSWORD DATA BLOCK
*                           *------------------------*
*                        +0 *  # OF REF              *
*                        +4 *  PASSWORD  (8 BYTES)   *
*                           *------------------------*
*
*         ACCOUNT OFFSET BLOCK
*     *----------------------------------*
*  +0 * OFFSET TO NEXT ACCT OFFSET BLOCK *
*  +4 * OFFSET TO PROC NAME OFFSET BLOCK *
*  +8 * OFFSET TO ACCT DATA BLOCK        *
*     *----------------------------------*
*
*                            ACCOUNT NUMBER DATA BLK
*                           *-----------------------*
*                        +0 * # OF REF              *
*                       +44 * LENGTH OF ACCOUNT #   *
*                       +45 * ACCOUNT NUMBER        *
*                           *-----------------------*
*
*     PROCEDURE NAME OFFSET BLOCK
*     *--------------------------------*
*  +0 * OFFSET TO NEXT PROC OFFSET BLK *
*  +8 * OFFSET TO PROC DATA BLOCK      *
*     *--------------------------------*
*
*                              PROCEDURE DATA BLOCK
*                           *----------------------------*
*                        +0 * # OF REF                   *
*                        +4 * LOGON PROC NAME (8 BYTES)  *
*                       +16 * UNIT NAME (8 BYTES)        *
*                           *----------------------------*
*
*------------------------------------------------------------------*
         PRINT OFF
*          %GOTO  UADGO;
*/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*                       I K J E F U A D
*                UADS STRUCTURE MAPPING MACR0
*
*    USE OF THE COMPILE VARIABLES MAKES POSSIBLE THE SELECTIVE
*    INCLUSION OF THE INDIVIDUAL AREAS OF MAPPING
*
* STATUS -- CHANGE LEVEL --   02-000               12-15-1972
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * **/
*/*
*    ERRORS IN THIS MACRO HAVE BEEN FIXED BY:
*       BILL GODFREY, PLANNING RESEARCH CORPORATION
*       PRC COMPUTER CENTER INC, MCLEAN VA.
*       JANUARY 30 1976 & MAY 6 1981
*    THE ERRORS FIXED:
*       (1) THE 'AIF' STATEMENTS DID NOT HAVE THE
*           FIRST VARIABLE NAME IN APOSTROPHES. (APAR # Z07502).
*           EXAMPLE     AIF (&ALL EQ 'ALL').HDR1
*       (2) IN THE HEADER BLOCK, THE FIELD 'UADSUSER'
*           WAS DEFINED AS 0CL8, WHICH CAUSED THE SUBSEQUENT
*           FIELDS TO BE DEFINED WITH IMPROPER OFFSETS.
*           IT IS NOW CHANGED FROM 0CL8 TO CL8 TO AGREE
*           WITH THE PLS CODE.  (APAR # OZ35503).
*       (3) IN THE ACCOUNT NUMBER DATA BLOCK, THE TWO
*           COMMENT CARDS FOLLOWING THE CARD DEFINING THE
*           FIELD 'UADSANUM' DID NOT HAVE AN ASTERISK IN
*           COLUMN 1, THUS CAUSING ASSEMBLY ERROR
*           MSG IFO054 INVALID OPERATION CODE.
*       (4) THE SETC STATEMENT - THERE IS ONLY ONE - WAS FLAGGED
*           WITH ASSEMBLY MSG IFO031 NAME INCONSISTENT WITH TYPE.
*           (OR ASSEMBLER H MSG IEV124 ILLEGAL USE OF PARAMETER).
*           IT IS ONLY EXECUTED IF OPERANDS ARE NOT PROPERLY
*           SPECIFIED ANYWAY. IT IS COMMENTED OUT. (APAR # OZ35503).
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * **/
*%UADGO:     ;
*        %GOTO ACTMC1;                                               /*
         MACRO
         IKJEFUAD  &DSPL,&ALL,&HDR,&POB,&AOB,&ROB,&PDB,&ADB,&RDB,      S
               &FSQ,&GNL
         SPACE 1
         AIF   ('&ALL' EQ 'ALL').HDR1   CHK TO DO ALL
         AIF   ('&HDR' EQ 'HDR').HDR1   CHK TO DO HEADER BLOCK
         AIF   ('&GNL' EQ 'GNL').GOB1   CHK TO DO GENERAL OFFSET
         AIF   ('&POB' EQ 'POB').POB1   CHK TO DO PASSWD OFFSET
         AIF   ('&AOB' EQ 'AOB').AOB1   CHK TO DO ACCTNM OFFSET
         AIF   ('&ROB' EQ 'ROB').ROB1   CHK TO DO PROCNAME OFFSET
         AIF   ('&PDB' EQ 'PDB').PDB1   CHK TO DO PASSWD DATA
         AIF   ('&ADB' EQ 'ADB').ADB1   CHK TO DO ACCTNM DATA
         AIF   ('&RDB' EQ 'RDB').RDB1   CHK TO DO PROCNAME DATA
         AIF   ('&FSQ' EQ 'FSQ').FSQ1   CHK TO DO FREE SPACE ELEMENT
.*&ALL   SETC  'ALL'
         SPACE 1
.HDR1    ANOP
         AIF   ('&DSPL' NE 'DSPL').HDRA CHK TO DISPLAY FORMAT
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                     H E A D E R   B L O C K                         *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                                                                     *
*               I---------------------I---------------------I         *
*          0    I UADSBLNG            I UADSFSQP            I         *
*               I---------------------I---------------------I         *
*          4    I UADSUSER                                  I         *
*               I                                           I         *
*               I---------------------I---------------------I         *
*          12   I UADSBN01            I UADSMAXC            I         *
*               I---------------------I---------------------I         *
*          16   I UADSATTR            I UADSINST            I         *
*               I---------------------I---------------------I         *
*          20   I UADSUPTP                                  I         *
*               I-------------------------------------------I         *
*          24   I UADSPWD1                                  I         *
*               I-------------------------------------------I         *
*          28   I UADSPERP                                  I         *
*               I-------------------------------------------I         *
*          32   I UADSDEST                                  I         *
*               I                                           I         *
*               I-------------------------------------------I         *
*          40   I RESERVED                                  I         *
*               I                                           I         *
*               I                                           I         *
*               I                                           I         *
*               I                                           I         *
*               I                                           I         *
*               I-------------------------------------------I         *
*                                                                     *
         SPACE 1
.HDRA    ANOP
         SPACE 1
DHED     DSECT ,HEDBPTR
         SPACE 1
         DS    0F                      ALIGN TO WORD BOUNDARY
UADSMHDR DS    0CL16                   COMMON HEADER AREA
UADSBLNG DS    AL2                     BLOCK LENGTH
UADSFSQP DS    AL2                     OFFSET TO INITIAL FSQE (FREE
**                                     ..SPACE QUEUE ELEMENT)
UADSUSER DS    CL8                     USERID
UADSBN01 DS    0BL2                    FLAGS
         DS    BL1                     FIRST BYTE OF FLAGS -- RESERVED
         DS    BL1                     SECOND BYTE OF FLAGS
UADSNUSP EQU   X'80'                   ..0 -- NO NON-USABLE SPACE 1
**                                     ..1 -- ONLY NON-USABLE SPACE 1
**                                     .......EXISTS IN THIS BLOCK
UADSEXPF EQU   X'40'                   ..0 -- OLD FORMAT UADS
**                                     ..1 -- NEW EXPANDED FORMAT UADS
**                                     .......EXISTS IN THIS BLOCK
UADSMAXC DS    AL2                     MAXIMUM CORE SIZE ALLOTTABLE
**                                     ..TO THIS USER
UADSATTR DS    0BL2                    SYSTEM ATTRIBUTES OF USERID
         DS    BL1                     FIRST BYTE OF IBM FLAGS
USATR00  EQU   X'80'                   ..0 -- NO OPERATOR CAPABILITY
**                                     ..1 -- OPERATOR CAPABILITY
USATR01  EQU   X'40'                   ..0 -- NO ACCOUNT CAPABILITY
**                                     ..1 -- ACCOUNT CAPABILITY
USATR02  EQU   X'20'                   ..0 -- NO JCL CAPABILITY
**                                     ..1 -- JCL CAPABILITY
USATR03  EQU   X'10'                   ..0 -- NO MOUNT CAPABILITY
**                                     ..1 -- MOUNT CAPABILITY
USATR04  EQU   X'04'                   ..0 -- EDIT RECOVER CAPABILITY
**                                     ..1 -- NO RECOVER CAPABILITY
**                                     FLAGS 6 THROUGH 15 ARE
**                                     ..RESERVED FOR IBM USE
         DS    BL1                     SECOND BYTE OF FLAGS, 8 -- 15
         SPACE 1
UADSINST DS    BL2                     FOR INSTALLATION USE
UADSUPTP DS    AL4                     OFFSET TO CURRENT UPT
UADSPWD1 DS    AL4                     OFFSET TO FIRST PASSWORD OFFSET
UADSPERP DS    AL4                     OFFSET TO PERFORMANCE GROUP
UADSDEST DS    CL8                     DESTINATION
UADSSUBH DS    CL1                     HOLD CLASS                 .ADD.
UADSSUBC DS    CL1                     JOB CLASS                  .ADD.
UADSSUBM DS    CL1                     MESSAGE CLASS              .ADD.
UADSSOUT DS    CL1                     SYSOUT CLASS               .ADD.
         DS    CL20                    RESERVED                   .CHG.
         SPACE 1
         SPACE 1
         SPACE 1
         AIF   ('&ALL' EQ 'ALL').GOB1   CHK TO DO ALL
         AIF   ('&GNL' NE 'GNL').DOPOB  CHK TO DO GENERAL OFFSET
.GOB1    ANOP
         AIF   ('&DSPL' NE 'DSPL').GOBA CHK TO DISPLAY FORMAT
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*             G E N E R A L   O F F S E T   B L O C K                 *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                                                                     *
*               I----------I--------------------------------I         *
*          0    I UADSNNFL I UADSNNEX                       I         *
*               I----------I--------------------------------I         *
*          4    I UADSNSUB                                  I         *
*               I-------------------------------------------I         *
*          8    I UADSNDAT                                  I         *
*               I-------------------------------------------I         *
*                                                                     *
         SPACE 1
.GOBA    ANOP
         SPACE 1
UADSNODE DSECT ,UADSNPTR
         SPACE 1
UADSNNFL DS    BL1                    GENERAL OFFSET BLOCK
**                                     ..INDICATORS
UADSNFLG EQU   X'80'                   ..0 -- CONTINUE CHAINING
**                                     ..1 -- LAST OFFSET BLOCK FOR
**                                     .......THIS OFFSET CHAIN
**                                     .. FLAGS 1 THRU 7 ARE RESERVED
UADSNNEX DS    AL3                    OFFSET TO NEXT OFFSET BLOCK
UADSNSUB DS    AL4                    OFFSET TO ASSOCIATED OFFSET
UADSNDAT DS    AL4                    OFFSET TO DATA BLOCK
         SPACE 1
.DOPOB   ANOP
         AIF   ('&ALL' EQ 'ALL').POB1   CHK TO DO ALL
         AIF   ('&POB' NE 'POB').DOAOB  CHK TO DO PASSWORD OFFSET
.POB1    ANOP
         AIF   ('&DSPL' NE 'DSPL').POBA CHK TO DISPLAY FORMAT
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*            P A S S W O R D   O F F S E T   B L O C K                *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                                                                     *
*               I----------I--------------------------------I         *
*          0    I UADSPFLG I UADSPNEX                       I         *
*               I----------I--------------------------------I         *
*          4    I UADSPSUB                                  I         *
*               I-------------------------------------------I         *
*          8    I UADSPDAT                                  I         *
*               I-------------------------------------------I         *
*                                                                     *
         SPACE 1
.POBA    ANOP
         SPACE 1
DPOB     DSECT ,DPOBPTR
         SPACE 1
UADSPFLG DS    BL1                     PASSWORD BLOCK INDICATORS
PFLG01   EQU   X'80'                   ..0 -- CONTINUE CHAINING
**                                     ..1 -- LAST PASSWORD FOR THIS
**                                     .......USERID
**                                     ..FLAGS 1 THRU 7 ARE RESERVED
UADSPNEX DS    AL3                     OFFSET TO NEXT PASSWD OFFSET
**                                     ..BLOCK
UADSPSUB DS    AL4                     OFFSET TO ASSOCIATED ACCOUNT
**                                     ..NUMBER OFFSET BLOCK
UADSPDAT DS    AL4                     OFFSET TO PASSWORD DATA BLOCK
         SPACE 1
.DOAOB   ANOP
         AIF   ('&ALL' EQ 'ALL').AOB1   CHK TO DO ALL
         AIF   ('&AOB' NE 'AOB').DOROB  CHK TO DO ACCT NO. OFFSET
.AOB1    ANOP
         AIF   ('&DSPL' NE 'DSPL').AOBA CHK TO DISPLAY FORMAT
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                   A C C O U N T   N U M B E R                       *
*                     O F F S E T   B L O C K                         *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                                                                     *
*               I----------I--------------------------------I         *
*          0    I UADSAFLG I UADSANEX                       I         *
*               I----------I--------------------------------I         *
*          4    I UADSASUB                                  I         *
*               I-------------------------------------------I         *
*          8    I UADSADAT                                  I         *
*               I-------------------------------------------I         *
*                                                                     *
         SPACE 1
.AOBA    ANOP
         SPACE 1
DNOB     DSECT ,DNOBPTR
         SPACE 1
UADSAFLG DS    BL1                    ACCOUNT NUMBER OFFSET BLOCK
**                                     ..INDICATORS
AFLG01   EQU   X'80'                   ..0 -- CONTINUE CHAINING
**                                     ..1 -- LAST ACCOUNT NUMBER FOR
**                                     .......THIS PASSWORD CHAIN
**                                     .. FLAGS 1 THRU 7 ARE RESERVED
UADSANEX DS    AL3                     OFFSET TO NEXT ACCOUNT NUMBER
**                                     ..OFFSET BLOCK
UADSASUB DS    AL4                     OFFSET TO ASSOCIATED PROCNAME
**                                     ..OFFSET BLOCK
UADSADAT DS    AL4                     OFFSET TO ACCOUNT NUMBER DATA
**                                     ..BLOCK
         SPACE 1
.DOROB   ANOP
         AIF   ('&ALL' EQ 'ALL').ROB1   CHK TO DO ALL
         AIF   ('&ROB' NE 'ROB').DOPDB  CHK TO DO PROCNAME OFFSET
.ROB1    ANOP
         AIF   ('&DSPL' NE 'DSPL').ROBA CHK TO DISPLAY FORMAT
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                   P R O C E D U R E   N A M E                       *
*                     O F F S E T   B L O C K                         *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                                                                     *
*               I----------I--------------------------------I         *
*          0    I UADSRFLG I UADSRNEX                       I         *
*               I----------I--------------------------------I         *
*          4    I UADSRSUB                                  I         *
*               I-------------------------------------------I         *
*          8    I UADSRDAT                                  I         *
*               I-------------------------------------------I         *
*                                                                     *
         SPACE 1
.ROBA    ANOP
         SPACE 1
DROB     DSECT ,DROBPTR
         SPACE 1
UADSRFLG DS    BL1                     PROCNAME OFFSET BLOCK
**                                     ..INDICATORS
FLGR01   EQU   X'80'                   ..0 -- CONTINUE CHAINING
**                                     ..1 -- LAST PROCNAME FOR THIS
**                                     .......ACCOUNT NUMBER
**                                     ..FLAGS 1 THRU 7 ARE RESERVED
UADSRNEX DS    AL3                     OFFSET TO NEXT PROCNAME
**                                     ..OFFSET BLOCK
UADSRSUB DS    AL4                     RESERVED BY ACCOUNT
UADSRDAT DS    AL4                     OFFSET TO PROCNAME DATA BLOCK
         SPACE 1
.DOPDB   ANOP
         AIF   ('&ALL' EQ 'ALL').PDB1   CHK TO DO ALL
         AIF   ('&PDB' NE 'PDB').DOADB  CHK TO DO PASSWORD DATA
.PDB1    ANOP
         AIF   ('&DSPL' NE 'DSPL').PDBA CHK TO DISPLAY FORMAT
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*              P A S S W O R D   D A T A   B L O C K                  *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                                                                     *
*               I----------I--------------------------------I         *
*          0    I UADSPCTR I RESERVED                       I         *
*               I----------I--------------------------------I         *
*          4    I UADSPPWD                                  I         *
*               I                                           I         *
*               I-------------------------------------------I         *
*                                                                     *
         SPACE 1
.PDBA    ANOP
         SPACE 1
DPOBD    DSECT ,UADSPPTR
         SPACE 1
UADSPCTR DS    AL1                     COUNT OF REFERENCES TO THIS
**                                     ..DATA BLOCK
         DS    XL3                     RESERVED FOR ACCOUNT
UADSPPWD DS    CL8                     PASSWORD
         SPACE 1
.DOADB   ANOP
         AIF   ('&ALL' EQ 'ALL').ADB1   CHK TO DO ALL
         AIF   ('&ADB' NE 'ADB').DORDB  CHK TO DO ACCT NO. DATA
.ADB1    ANOP
         AIF   ('&DSPL' NE 'DSPL').ADBA CHK TO DISPLAY FORMAT
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                   A C C O U N T   N U M B E R                       *
*                      D A T A     B L O C K                          *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                                                                     *
*               I----------I--------------------------------I         *
*          0    I UADSACTR I RESERVED                       I         *
*               I----------I--------------------------------I         *
*          4    I UADSADRF (40 BYTES)                       I         *
*               I                                           I         *
*               I                                           I         *
*               I                                           I         *
*               I                                           I         *
*               I                                           I         *
*               I                                           I         *
*               I                                           I         *
*               I                                           I         *
*               I                                           I         *
*               I----------I--------------------------------I         *
*          44   I UADSALEN I UADSANUM (MAX OF 40 BYTES)     I         *
*               I----------I                                I         *
*               =                                           =         *
*               I-------------------------------------------I         *
*                                                                     *
         SPACE 1
.ADBA    ANOP
         SPACE 1
DNOBD    DSECT ,UADSAPTR
         SPACE 1
UADSACTR DS    AL1                     COUNT OF REFERENCES TO THIS
**                                     ..DATA BLOCK
         DS    XL3                     RESERVED FOR ACCOUNT
UADSADRF DS    CL40                    DRIVER DATA FIELD
UADSALEN DS    AL1                     LENGTH OF FOLLOWING ACCOUNT
**                                     ..NUMBER DATA FIELD
UADSANUM DS    CL40                    ACCT NMBR DATA FIELD
*                                      ..NOTE -- USE SPECIFIC
*                                      .....LENGTH REFERENCES
         SPACE 1
.DORDB   ANOP
         AIF   ('&ALL' EQ 'ALL').RDB1   CHK TO DO ALL
         AIF   ('&RDB' NE 'RDB').DOFSQ  CHK TO DO PROCNAME DATA
.RDB1    ANOP
         AIF   ('&DSPL' NE 'DSPL').RDBA CHK TO DISPLAY FORMAT
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                   P R O C E D U R E   N A M E                       *
*                      D A T A     B L O C K                          *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                                                                     *
*               I----------I--------------------------------I         *
*          0    I UADSRCTR I RESERVED                       I         *
*               I----------I--------------------------------I         *
*          4    I UADSRNAM                                  I         *
*               I                                           I         *
*               I----------I----------I---------------------I         *
*          12   I RESERVED            I UADSRSIZ            I         *
*               I----------I----------I---------------------I         *
*          16   I UADSUNAM                                  I         *
*               I                                           I         *
*               I-------------------------------------------I         *
*                                                                     *
         SPACE 1
.RDBA    ANOP
         SPACE 1
DROBD    DSECT ,UADSRPTR
         SPACE 1
UADSRCTR DS    AL1                     COUNT OF REFERENCES TO THIS
**                                     DATA BLOCK
         DS    XL3                     RESERVED FOR ACCOUNT
UADSRNAM DS    CL8                     LOGON PROCEDURE NAME
         DS    XL2                     RESERVED
UADSRSIZ DS    AL2                     REGION SIZE SPECIFIED IN THE
**                                     ..NAMED PROCEDURE
UADSUNAM DS    CL8                     ESOTERIC GROUP UNIT NAME
         SPACE 1
.DOFSQ   ANOP
         AIF   ('&ALL' EQ 'ALL').FSQ1   CHK TO DO ALL
         AIF   ('&FSQ' NE 'FSQ').DONE   CHK TO DO FSQE DATA
.FSQ1    ANOP
         AIF   ('&DSPL' NE 'DSPL').FSQEA CHK TO DISPLAY FORMAT
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                F R E E   S P A C E   Q U E U E                      *
*                         E L E M E N T                               *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                                                                     *
*               I---------------------I---------------------I         *
*          0    I FSQELNTH            I FSQENEXT            I         *
*               I---------------------I---------------------I         *
*                                                                     *
         SPACE 1
.FSQEA   ANOP
         SPACE 1
UADSFSQE DSECT ,FSQEPTR
         SPACE 1
FSQELNTH DS    AL2                     LENGTH OF FREE SPACE AREA
FSQENEXT DS    AL2                     OFFSET TO NEXT FSQE
.DONE    ANOP
         MEND  , */
         SPACE 1
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  MAINTENANCE/CHANGES:                                               *
*    CORRECTED PROBLEMS, EXPANDED OUTPUT, CONVERTED TO SUIT LOCAL     *
*      PREFERENCES AS TO MACROS, TITLES, ETC.          G. POSTPISCHIL *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         COPY  OPTIONGB                                         GP12326
         SPACE 1
         SYSPARM LIST=YES                                       GP12326
         SPACE 1
         PRINT ON,GEN
         PUNCH '  ORDER LISTUADS(P) '                           GP09251
         SPACE 1
LISTUADS PGMHEAD ZERO31,BASE=(R11,R12),BNDRY=PAGE,AM=24         GP12326
         SERVINIT ,          INITIALIZE @SERVICE ROUTINES       GP12326
         SERVLOAD @PRINTER                                      GP12326
         PRTOPEN SYSPRINT,OPT=ABEND    OPEN PRINT FILE OR BOMB  GP12326
         PRTV  HEADER1,TITLE=1                                  GP12326
         PRTV  HEADER2,TITLE=2                                  GP12326
         SPACE 1
         MVI   OUTRECD-1,C' '     SET FOR FAST BLANKING         GP12326
         MVC   OUTRECD,OUTRECD-1  BLANK OUTPUT LINE             GP12326
         SPACE 1
*------------------------------------------------------------------*
*        O P E N   D A T A   S E T S
*------------------------------------------------------------------*
         SPACE 1
         OPEN  (UADSPS,,UADSPO,)                                GP12326
         TM    UADSPS+DCBOFLGS-IHADCB,DCBOFOPN                  GP12326
         BZ    OPFAILIN                                         GP12326
         TM    UADSPO+DCBOFLGS-IHADCB,DCBOFOPN                  GP12326
         BNZ   OPGOODIN                                         GP12326
OPFAILIN PRTL  ' LISTUADS: UADS DD FAILED TO OPEN'              GP12326
         SERVTERM ,                                             GP12326
         ABEND 2540,DUMP                                        GP12326
         SPACE 1
OPGOODIN LA    R2,BUFFER          GET ADDR OF STORAGE AREA      GP12326
         USING DHED,R2            ADDRESSABULITY TO HEADER BLK
         SPACE 1
*------------------------------------------------------------------*
*        R E A D   T H E   D I R E C T O R Y
*------------------------------------------------------------------*
         SPACE 1
READDIR  EQU   *
         LA    R3,DIR                                           GP12326
         READ  DECB,SF,UADSPS,(R3),256                          GP12326
         CHECK DECB
         LA    R8,DIR              PT TO DIRECTORY
         LH    R7,0(R8)            LENGTH OF DIRECTORY USED
         BCTR  R7,0                SUBTRACT 1
         BCTR  R7,0                SUBTRACT 1 MORE FOR REAL LENGTH
         LA    R8,2(R8)            POINT TO A REAL PART OF DIR
         XR    R9,R9
         SPACE 1
DIRLOOP  EQU   *
         CLC   0(4,R8),=XL8'FFFFFFFFFFFFFFFF'   END OF DIRECTORY?
         BE    EOJ
         MVC   TTRC(4),8(R8)       SAVE TTR OF MEMBER
         MVI   TTRC+3,X'00'
         POINT UADSPO,TTRC
         SPACE 1
*------------------------------------------------------------------*
*        LOOP1:  USERID,  PASSWORD,  ACCOUNT NUMBER,  LOGON PROC.
*        R E A D   T H E   M E M B E R
*------------------------------------------------------------------*
         SPACE 1
         READ  PDSECB,SF,UADSPO,(R2)
         CHECK PDSECB
         SPACE 1
         CLI   UADSUSER+07,C'1'    8TH CHAR OF MEMBER END IN '1'?
         BNL   CONTDIR
         CLI   UADSUSER+07,C' '    8TH CHAR OF MEMBER END IN ' '?
         BNE   CONT7
         SPACE 1
         CLI   UADSUSER+06,C'1'    7TH CHAR OF MEMBER END IN '1'?
         BNL   CONTDIR
         CLI   UADSUSER+06,C' '    7TH CHAR OF MEMBER END IN ' '?
         BNE   CONT6
         SPACE 1
         CLI   UADSUSER+5,C'1'     6TH CHAR OF MEMBER END IN '1'?
         BNL   CONTDIR
         CLI   UADSUSER+5,C' '     6TH CHAR OF MEMBER END IN ' '?
         BNE   CONT5
         SPACE 1
         CLI   UADSUSER+4,C'1'     5TH CHAR OF MEMBER END IN '1'?
         BNL   CONTDIR
         CLI   UADSUSER+4,C' '     5TH CHAR OF MEMBER END IN ' '?
         BNE   CONT4
         SPACE 1
         CLI   UADSUSER+3,C'1'     4TH CHAR OF MEMBER END IN '1'?
         BNL   CONTDIR
         CLI   UADSUSER+3,C' '     4TH CHAR OF MEMBER END IN ' '?
         BNE   CONT3
         SPACE 1
         B     CONT3               ASSUME 3 CHAR ID,  CRAZY BUT...
         SPACE 1
CONT7    EQU   *
         LA    R15,7               TSO ID IS 7 CHARACTERS LONG
         ST    R15,IDLEN           SAVE IT FOR MOVE
         B     CONT                MOVE RIGHT ALONG
CONT6    EQU   *
         LA    R15,6               TSO ID IS 6 CHARACTERS LONG
         ST    R15,IDLEN           SAVE IT FOR MOVE
         B     CONT                MOVE RIGHT ALONG
CONT5    EQU   *
         LA    R15,5               TSO ID IS 5 CHARACTERS LONG
         ST    R15,IDLEN           SAVE IT FOR MOVE
         B     CONT                MOVE RIGHT ALONG
CONT4    EQU   *
         LA    R15,4               TSO ID IS 4 CHARACTERS LONG
         ST    R15,IDLEN           SAVE IT FOR MOVE
         B     CONT                MOVE RIGHT ALONG
CONT3    EQU   *
         LA    R15,3               TSO ID IS 3 CHARACTERS LONG
         ST    R15,IDLEN           SAVE IT FOR MOVE
         B     CONT                MOVE RIGHT ALONG
CONT     EQU   *
         MVC   ACCT#(9),HEX40      BLANK OUT ACCT FIELD
         SPACE 1
*------------------------------------------------------------------*
*        S T A R T   B Y   G E T T I N G   U S E R I D
*------------------------------------------------------------------*
         SPACE 1
         MVC   USID(8),HEX40       BLANK OUT TSO ID FIELD
         L     R15,IDLEN           GET THE LENGTH
         SH    R15,=H'1'           SUB 1 FOR EXECUTE            GP12326
         BM    *+4+4               BUT HOW ?                    GP12326
         EX    R15,MOVEID          SAVE THE USERID
         SPACE 1
*------------------------------------------------------------------*
*        U S E R   A T T R I B U T E S   (SYS)
*------------------------------------------------------------------*
ATTRIBS  EQU   *
         MVC   AUTHB(4),UADSATTR   SAVE AUTHORIZATIONS
         TM    UADSATTR,USATR00    OPER?
         BNO   TESTACCT
         MVC   OPER(4),=CL4'OPER'
TESTACCT EQU   *
         TM    UADSATTR,USATR01    ACCT?
         BNO   TESTJCL
         MVC   ACCOUNT(4),=CL4'ACCT'
TESTJCL  EQU   *
         TM    UADSATTR,USATR02    JCL?
         BNO   TESTMOUN
         MVC   JCL(3),=CL3'JCL'
TESTMOUN EQU   *
         TM    UADSATTR,USATR03       MOUNT?
         BNO   DOPCF
         MVC   MOUNT(5),=CL5'MOUNT'
         SPACE 1
*------------------------------------------------------------------*
*        U S E R   A T T R I B U T E S   (INSTALLATION)
*        P R O C E S S   P C F   D A T A
*------------------------------------------------------------------*
         SPACE 1
DOPCF    DS    0H                                               GP12326
         AIF   ('&LOCAL' EQ 'USAF').SHOWPCF                     GP12326
         MVC   COMMON(4),UADSSUBH COPY HOLD/JOB/MSG/OUT CLASSES GP12326
         OC    COMMON(4),HEX40    CHANGE HEX 0 TO SPACES        GP12326
         AGO   .SHOWCOM
.SHOWPCF MVC   COMMON(4),=C'0000'   RESET THE ENTRY
         MVN   DATA(1),18(R2)     .
         PACK  18(1,R2),18(1,R2)   .
         MVN   COMMON(1),18(R2)     .  FOR PCF DATA
         MVN   VOL2(1),19(R2)       .
         PACK  19(1,R2),19(1,R2)   .
         MVN   VOL1(1),19(R2)     .
.SHOWCOM SPACE 1
*------------------------------------------------------------------*
*        LOCATE AND RETRIEVE PASSWORD FROM ITS CHAIN
*------------------------------------------------------------------*
         SPACE 1
         L     R1,UADSPWD1         OFFSET TO PSWD OFFSET BLOCK
         LTR   R1,R1               ANY ?                        GP12326
         BZ    NULLPSWD            NO                           GP12326
         LR    R3,R2               SET UP FOR ADDITION
         AR    R3,R1               GET ADDR OF PW OFFSET BLK
         USING DPOB,R3
         SPACE 1
PW       EQU   *
         L     R1,UADSPDAT         GET OFFSET OF PASSWORD
         LTR   R1,R1               ANY ?                        GP12326
         BZ    NULLPSWD            NO                           GP12326
         AR    R1,R2               COMPUTE FROM ANCHOR
         USING DPOBD,R1            ADDRESSABILITY TO PW DATA BLK
         MVC   PASSWORD(8),UADSPPWD   MOVE IT INTO THE PRINT LINE
         DROP  R1
         SPACE 1
*------------------------------------------------------------------*
*        LOCATE AND RETRIEVE ACCOUNT NUMBER FROM ITS CHAIN
*------------------------------------------------------------------*
         SPACE 1
NULLPSWD L     R4,UADSPSUB         GET OFFSET TO ACCT OFFSET BLK
         LTR   R4,R4               ANY ?                        GP12326
         BZ    NULLACCT            NO                           GP12326
         AR    R4,R2               COMPUTE ADDR FROM ANCHOR
         USING DNOB,R4             ADDRESSABILITY TO ACCT OFFSET BLK
         SPACE 1
ACCT     EQU   *
         L     R1,UADSADAT         GET OFFSET TO ACCT DATA BLK
         LTR   R1,R1               ANY ?                        GP12326
         BZ    NULLACCT            NO                           GP12326
         AR    R1,R2               COMPUTE ADDR FROM ANCHOR
         USING DNOBD,R1            ADDRESSABILITY TO ACCT DATA BLK
         MVC   ACCT#(8),HEX40      BLANK OUT FIELD FIRST
         L     R15,UADSALEN        GET LENGTH ACCT # - IN LEFT BYTE
         SRL   R15,24              SHIFT OVER 3 BYTES
         BCTR  R15,0               SUB 1 PRIOR TO EXECUTE
         EX    R15,MOVE            MOVE IN ACCOUNT NUMBER
         B     ACCTC
         SPACE 1
MOVE     MVC   ACCT#(*-*),UADSANUM   MOVE ACCOUNT FIELD
         DROP  R1
         SPACE 1
ACCTC    EQU   *
NULLACCT L     R5,UADSASUB         GET OFFSET TO PROC NAME OFFSET BLK
         AR    R5,R2
         USING DROB,R5             ADDRESSABILITY TO PROC OFFSET BLK
         SPACE 1
NEXTPROC EQU   *
         L     R6,UADSRDAT         GET OFFSET TO PROC DATA BLK
         AR    R6,R2               COMPUTE ADDR FROM ANCHOR
         USING DROBD,R6            ADDRESSABILITY TO PROC DATA BLK
         MVC   PROC(8),UADSRNAM    SAVE LOGON PROC
         MVC   UNIT(8),UADSUNAM    SAVE UNIT NAME FOR THIS PROC
         SR    R0,R0
         ICM   R0,3,UADSRSIZ       GET REGION SIZE              GP12326
         CVD   R0,DB               CHANGE TO PACKED             GP12326
         MVC   REGION-1(6),=X'402020202120'                     GP12326
         ED    REGION-1(6),DB+5                                 GP12326
         SPACE 1
         PRTF  OUTRECD,CC=NO       PUT OUT RECORD NOW           GP12326
         SPACE 1
         TM    UADSRFLG,FLGR01     LAST OFFSET, PROC OFFSET BLK?
         BO    NEXTACCT            BUMP UP ONE LEVEL
         L     R6,UADSRNEX-1       GET OFFSET NEXT PROC OFFSET BLK
         AR    R6,R2               COMPUTE ADDR FROM ANCHOR
         LR    R5,R6               REENTER LOOP FOR NEXT PROC
         B     NEXTPROC
         SPACE 1
*------------------------------------------------------------------*
*        BUMP UP 1 LEVEL IN LOOP TO FIND NEXT ACCT NUMBER
*------------------------------------------------------------------*
         SPACE 1
NEXTACCT EQU   *
         TM    UADSAFLG,AFLG01     LAST OFFSET?
         BO    NEXTPW              BUMP UP NEXT LEVEL
         SPACE 1
         L     R1,UADSANEX-1
         AR    R1,R2               COMPUTE ADDR FROM ANCHOR
         LR    R4,R1               REENTER LOOP FOR NEXT PW
         B     ACCT
         SPACE 1
*------------------------------------------------------------------*
*        BUMP UP 1 LEVEL IN LOOP TO FIND NEXT PASSWORD
*------------------------------------------------------------------*
         SPACE 1
NEXTPW   EQU   *
         TM    UADSPFLG,PFLG01     LAST OFFSET?
         BO    CONTDIR             GET NEXT ENTRY IN DIR
         L     R1,UADSPNEX-1       GET OFFSET TO NEXT PW OFFSET BLK
         AR    R1,R2               COMPUTE ADDR FROM ANCHOR
         LR    R3,R1               REENTER LOOP FOR NEXT PW
         B     PW
         SPACE 1
*------------------------------------------------------------------*
*        CONTINUE DIRECTORY SEARCH
*------------------------------------------------------------------*
         SPACE 1
CONTDIR  EQU   *
         MVC   OUTRECD,OUTRECD-1  BLANK OUTPUT LINE             GP12326
         IC    R9,11(R8)           PICK UP INDICATORS
         LA    R8,12(R8)           POINT TO USER DATA
         SH    R7,=H'12'           DECREMENT AMT USABLE DIR LEFT
         SLL   R9,27               KILL HI ORDER BIT OF IND
         SRL   R9,26               MOVE BACK AND *2 FOR HALFWORDS
         AR    R8,R9               UPDATE DIRECTORY POINTER
         SR    R7,R9               DECREMENT AMT LEFT COUNTER
         BNP   READDIR
         B     DIRLOOP
         SPACE 1
*------------------------------------------------------------------*
*        LETS CLOSE UP AND GO HOME
*------------------------------------------------------------------*
         SPACE 1
EOJ      SERVTERM ,          CLOSE AND FREE STUFF               GP12326
         CLOSE (UADSPS,,UADSPO)
         PGMEXIT RC=0                                           GP12326
         SPACE 1
*------------------------------------------------------------------*
*        S Y N A D   E X I T
*------------------------------------------------------------------*
         SPACE 1
ERROREND EQU   *
         LA    R15,2008
         ABEND (15),DUMP
         SPACE 1
MOVEID   MVC   USID(*-*),UADSUSER  MOVE USER ID
         SPACE 1
         LTORG
         EJECT
*------------------------------------------------------------------*
*        S T O R A G E   A N D   C O N S T A N T S
*------------------------------------------------------------------*
HEX40    DC    CL80' '             BLANKS
*------------------------------------------------------------------*
*        D C B   D E F I N I T I O N S
*------------------------------------------------------------------*
         SPACE 1
UADSPS   DCB   MACRF=R,DSORG=PS,DDNAME=UADS,EODAD=EOJ,RECFM=U
UADSPO   DCB   MACRF=R,DSORG=PO,DDNAME=UADS,SYNAD=ERROREND
         SPACE 1
SYSPRINT PRTWORK LISTUADS,SYSPRINT,TITLE=3                      GP12326
HEADER1  VCON  '#                  U A D S   L I S T E R'       GP12326
HEADER2  VCON  END=HEADER2N,' '                                 GP12326
         DC    CL10'User Id'                                    GP12326
         DC    CL10'Password'                                   GP12326
         DC    CL10'Account'                                    GP12326
         DC    CL10'Log Proc'                                   GP12326
         DC    CL10'Unit'                                       GP12326
*OLD*    DC    CL5'PCF '     LOCAL INSTALLATION DATA ???        GP12326
         DC    CL5'HJMO'     JOB AND OUTPUT CLASSES             GP12326
         DC    CL5'Oper'                                        GP12326
         DC    CL5'Acct'                                        GP12326
         DC    CL4'JCL'                                         GP12326
         DC    CL6'Mount '                                      GP12326
         DC    CL6'Region'                                      GP12326
HEADER2N VCON  *END                                             GP12326
         SPACE 1
*------------------------------------------------------------------*
*   D Y N A M I C   S T O R A G E
*------------------------------------------------------------------*
SAVE     DSECT ,             SAVE AND WORK AREA                 GP12326
DB       DS    D                                                GP12326
         SERVDEFS ,          SUPPORT FUNCTION DATA              GP12326
IDLEN    DS    F                   USER ID LENGTH
TTRC     DS    F
AUTHB    DS    F                   BITS FOR AUTHORIZATIONS
         SPACE 1
         DC    C' '          FOR FAST BLANKING                  GP12326
USID     DC    CL7' '              01-07
         DC    CL3' '              08-10
PASSWORD DC    CL8' '              11-18
         DC    CL2' '              19-20
ACCT#    DC    CL8' '              21-28
         DC    CL2' '              29-30
PROC     DC    CL8' '              31-38
         DC    CL2' '              39-40
UNIT     DC    CL8' '              41-48
         DC    CL2' '              49-50
COMMON   DC    B'11110000'         51
DATA     DC    B'11110000'         52
VOL1     DC    B'11110000'         53
VOL2     DC    B'11110000'         54
         DC    CL1' '              55
OPER     DC    CL4' '              56-59
         DC    CL1' '              60
ACCOUNT  DC    CL4' '              61-64
         DC    CL1' '              65
JCL      DC    CL3' '              66-68
         DC    CL1' '              69
MOUNT    DC    CL5' ',CL2' '       70-74
REGION   DC    CL5' '              75-80
OUTRECD  EQU   USID,*-USID,C'C'    OUTPUT RECORD                GP12326
*OOPS*   ORG   OUTRECD+80    NEVER TRUST ANYONE                 GP12326
         SPACE 1
DIR      DS    0D,256X
         SPACE 1
BUFFER   DS    128XL256      UADS BUFFER                        GP12326
         SPACE 1
SAVEEND  EQU   *                  END OF DYNAMIC STORAGE        GP12326
         SPACE 1
         PRINT &PRTSYS                                          GP12326
         DCBD  DSORG=PO,DEVD=DA
         SPACE 1
         IKJEFUAD  DSPL,ALL
         END
