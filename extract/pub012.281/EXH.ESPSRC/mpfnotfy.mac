MPFNOTFY TITLE 'MPF EXIT TO SEND BATCH JOB MESSAGES TO TSO USER'
***********************************************************************
*                                                                     *
* JUSTIFICATION:                                                      *
*   USERS NEED TO BE NOTIFIED OF CERTAIN MESSAGES WHILE BATCH JOB IS  *
*   STILL RUNNING (SUCH AS IEF099I JOB XXXXXXX WAITING FOR DATASETS). *
*   THIS MPF EXIT WILL SEND THE CONTENTS OF THE MESSAGE THRU THE JES  *
*   NOTIFY FUNCTION TO THE USER IF A NOTIFY WAS SPECIFIED ON THE      *
*   JOBCARD.                                                          *
*                                                                     *
* FUNCTION:                                                           *
*   THIS MPF EXIT WILL CHECK THE PRESENCE OF A NOTIFY USERID/NODE     *
*   ON THE BATCH JOB ISSUING THE MESSAGE.  IF A NOTIFY USERID EXISTS  *
*   THEN THIS EXIT WILL USE THE JES NOTIFY USER FUNCTION TO SEND THE  *
*   MESSAGE ISSUED TO THE USER ON THE SPECIFIED NODE.                 *
*                                                                     *
* LOGIC:                                                              *
*   OBTAIN A WORKAREA IN SP230                                        *
*   FIND JES2 CONTROL BLOCKS THRU THE LIFE-OF-JOB SSIB                *
*   DO ACRONYM AND VERSION CHECK ON THE JES2 SJB                      *
*   CHECK FOR NOTIFY USERID IN THE JES JCT                            *
*   INITIALIZE SSOB AND SSNU                                          *
*   COPY MESSAGE ADDRESS AND LENGTH FROM CTXT TO SSNU                 *
*   CALL IEFJSREQ                                                     *
*   IF MESSAGE WAS SEND SUCCESSFULL THEN REQUEST MINOR LINES          *
*   RELEASE WORKAREA                                                  *
*   RETURN TO CALLER                                                  *
*                                                                     *
* REGISTER USAGE:                                                     *
*   R0    WORK                                                        *
*   R1    ENTRY PARAMETER + WORK                                      *
*   R2    TCB                                                         *
*   R3    JSCB                                                        *
*   R4    SSIB                                                        *
*   R5    SJB                                                         *
*   R6    JCT                                                         *
*   R7-R9 NOT USED                                                    *
*   R10   CTXTATTR                                                    *
*   R11   CTXT                                                        *
*   R12   PROGRAM BASE                                                *
*   R13   WORKAREA INCLUDING STANDARD OS SAVEAREA                     *
*   R14   RETURN ADDR + WORK                                          *
*   R15   ENTRY POINT ADDRESS + WORK                                  *
*                                                                     *
* ATTRIBUTES:                                                         *
*   AMODE(31),RMODE(ANY),REFR,AC(0),OL                                *
*   THE OL (ONLY LOADABLE) ATTRIBUTE IS NOT REQUIRED BUT RECOMMENDED  *
*   BECAUSE MPF EXITS ARE LOAD-ED INTO CSA AND SUBSEQUENTLY BRANCHED  *
*   TO.  THEY ARE NEVER LINK-ED ATTACH-ED OR XCTL-ED TO.              *
*                                                                     *
* HISTORY:                                                            *
*   DATE       BY              COMMENTS                               *
*   28/02/93   JAN JAEGER      INITIAL VERSION                        *
*                                                                     *
***********************************************************************
MPF$NTFY RSECT ,
MPF$NTFY AMODE 31
MPF$NTFY RMODE ANY
         B     SKIPHDR-*(,R15)
         DC    AL1(L'EYECATCH)
EYECATCH DC    C'MPF$NTFY-J.JAEGER-&SYSDATE.-&SYSTIME.'
SKIPHDR  DS    0H
         BAKR  R14,0                   STACK REGISTERS
         LR    R12,R15                 SET BASE REGISTER
         USING MPF$NTFY,R12
*
         L     R11,0(,R1)              POINT TO PARAMETER LIST
         USING CTXT,R11
*
         STORAGE OBTAIN,               OBTAIN WORKAREA                 X
               LENGTH=WORKLEN,                                         X
               SP=230
*
         MVC   4(4,R1),=CL4'F1SA'      INDICATE LINKAGE STACK USED
         LR    R13,R1
         USING WORKAREA,R13
         USING SSOB,WORKSSOB           THIS IS A NICE FEATURE OF HLASM
         USING PSA,R0                  THIS ALWAYS WORKS
*
         ICM   R2,B'1111',PSATOLD
         BZ    ERREXIT                 NOT IN TCB MODE
         USING TCB,R2
         SLR   R3,R3                   TCBJSCBB IS A 24 BIT ADDRESS
         ICM   R3,B'0111',TCBJSCBB     JSCB ADDRESS
         BZ    ERREXIT                 NO JSCB FOUND
         USING IEZJSCB,R3
         ICM   R3,B'1111',JSCBACT      POINT TO ACTIVE JSCB
         BZ    ERREXIT                 NO ACTIVE JSCB FOUND
         ICM   R4,B'1111',JSCBSSIB     SSIB ADDRESS
         BZ    ERREXIT                 NO LIFE-OF-JOB SSIB
         USING SSIB,R4
         ICM   R5,B'1111',SSIBSUSE     SJB ADDR
         BZ    ERREXIT                 NO SJB FOUND
*
         USING SJB,R5
         CLC   SJBID,=CL4'SJB'         CHECK ID OF SJB
         BNE   ERREXIT
         CLI   SJBVRSN,SJBCURVN        CHECK VERSION LEVEL
         BNE   ERREXIT
         ICM   R6,B'1111',SJBJCT       GET JCT ADDRESS
         BZ    ERREXIT                 NO JCT PRESENT
         USING JCT,R6
*
         TM    JCTNOTUS,X'BF'          CHECK PRESENCE OF NOTIFY USERID
         BZ    EXIT                    NO NOTIFY USERID
*
         LA    R1,SSOB                 INITIALIZE SSOB POINTER
         ST    R1,SSOBPTR
         OI    SSOBPTR,X'80'           SET HIGH ORDER BIT ON
         XC    WORKSSOB,WORKSSOB       CLEAR IEFSSREQ PARAMETER LIST
*
         MVC   SSOBID,=CL4'SSOB'       INITIALIZE HEADER
         MVC   SSOBLEN,=AL2(SSOBHSIZ)
         MVC   SSOBFUNC,=AL2(SSOBSSNU) NOTIFY USER FUNCTION
         LA    R1,SSNUBGN              POINT TO SSNU SECTION
         ST    R1,SSOBINDV             STORE IN SSOB
         MVC   SSNUID,=CL4'SSNU'       INITIALIZE HEADER
         MVC   SSNULEN,=AL2(SSNUSIZE)
         MVI   SSNUVER,SSNUCVER
         TM    JCTNNODE,X'BF'          CHECK PRESENCE OF NOTIFY NODE
         BZ    NOJCTNOD                NO NODE PRESENT
         MVC   SSNUNODE,JCTNNODE       NOTIFICATION NODE
NOJCTNOD DS    0H
         MVC   SSNUUSER,JCTNOTUS       NOTIFICATION USER
*
         ICM   R10,B'1111',CTXTTXPN    ADDRESS IF MINOR LINES
         BNZ   ISMINOR
         ICM   R10,B'1111',CTXTTXPJ    ADDRESS OF MAJOR LINES
         BZ    ERREXIT                 NO MESSAGE AT ALL
ISMINOR  DS    0H
         USING CTXTATTR,R10
         ICM   R9,B'0011',CTXTTLEN     PICK UP LENGTH OF MESSAGE
         BZ    ERREXIT                 NULL MESSAGE
         LA    R8,CTXTTMSG             ADDRESS OF MESSAGE
         STH   R9,SSNUMLEN             MESSAGE LENGTH
         ST    R8,SSNUMSG              MESSAGE ADDRESS
*
         LA    R1,SSOBPTR
         IEFSSREQ ,                    CALL IEFJSREQ FOR NU FUNCTION
         LTR   R15,R15
         BNZ   ERREXIT                 SUBSYSTEM INTERFACE ERROR
         CLC   SSOBRETN,=AL4(SSNUOKB)  RETURNCODE WORSE THEN WARNING
         BH    ERREXIT                 SUBSYSTEM FUNCTION ERROR
         CLC   SSNUERCD,=AL2(SSNUMSGT) TRUNCATED IS STILL ACCEPTABLE
         BH    ERREXIT                 NOTIFY USER ERROR
*
         OI    CTXTRFB1,CTXTRPML       REQUEST MINOR LINES FOR MLWTO
*
ERREXIT  DS    0H                      ON ERROR SIMPLY EXIT
EXIT     DS    0H
         LR    R1,R13
         STORAGE RELEASE,              RELEASE WORKAREA                X
               LENGTH=WORKLEN,                                         X
               SP=230,                                                 X
               ADDR=(1)
*
         PR    ,                       RETURN TO CALLER
*
         LTORG ,                       PUT LITERALS AT END OF CSECT
*
WORKAREA DSECT ,
SAVEAREA DS    18F                     SAVEAREA MUST BE FIRST
SSOBPTR  DS    A                       POINTER TO SSOB FOR IEFSSREQ
WORKSSOB DS    XL(SSNULEN8)            SSOB + SSNU
WORKLEN  EQU   *-WORKAREA
*
         PUSH  PRINT
         PRINT NOGEN
         IEZVX100 ,                    MPF EXIT PARAMETER LIST
         CVT   DSECT=YES               REQUIRED BY IEFSSREQ
         IEFJESCT ,                    REQUIRED BY IEFSSREQ
         IHAPSA ,                      POINTS TO TCB
         IKJTCB ,                      POINTS TO JSCB
         IEZJSCB ,                     POINTS TO SSIB
         IEFJSSIB ,                    POINTS TO SJB
         IEFSSOBH ,                    SSOB HEADER FOR IEFSSREQ
SSOBGN   EQU   *                       NU IS NOT SUPPORTED BY IEFJSSOB
         IAZSSNU ,                     NOTIFY USER SSOB EXTENSION
         $SJB  ,                       POINTS TO JCT
         $JCT  ,                       CONTAINS NOTIFY INFO FROM JES2
         $HASPEQU ,                    REQUIRED BY $SJB
         $BUFFER ,                     REQUIRED BY $JCT
         $TQE  ,                       REQUIRED BY $JCT
         IFGRPL ,                      REQUIRED BY $BUFFER
         POP   PRINT
*
         END   MPF$NTFY,(JAN JAEGER,0101,95059)
