FUTL     TITLE 'I E F U T L  ***  USER TIME LIMIT EXPIRATION'
         COPY  OPTIONGB                                          89222
         GBLC  &ZZIXMAK      CONTROL FOR MACRO INHEX            GP04035
&ZZIXMAK SETC  'OUT'                                            GP04035
         SPACE 1                                                 89222
         SYSPARM LIST=YES    SET GLOBAL OPTIONS                  89222
         EJECT ,                                                 79049
         PRINT &PRTSOR                                           89222
***********************************************************************
**                                                                   **
**   THIS ROUTINE HAS BEEN REWRITTEN FOR MVS. THE EARLIER VERSION    **
**   (MVT, SVS) IS IN THIS LIBRARY AS SVSUTL.                        **
**   -- AND CHANGED AGAIN FOR ESA AND OS/390.             2004-01-04 **
**                                                                   **
**   THIS ROUTINE IS DEPENDENT ON CODE IN IEFUJI / IEFUSI AND        **
**   OTHER LOCAL CHANGES (BUT IS USABLE WITHOUT THEM)                **
**         SOME OF THE REALLY OLD CODE FROM THE OLD, OLD, OLD MVT    **
**         VERSION COURTESY OF SEYMOUR J. METZ.                  89226*
**                                                                   **
***********************************************************************
         SPACE 2
***********************************************************************
**   THIS ROUTINE GETS CONTROL WITH THE FOLLOWING REGISTERS :        **
**                                                                   **
**   R0    ENTRY CONDITION                                           **
**         VALUE         REASON                                      **
**                                                                   **
**         0             JOB CPU TIME LIMIT                          **
**         4             STEP CPU TIME LIMIT                         **
**         8             WAIT TIME LIMIT                             **
**                                                                   **
**   R1    TCTJMR                                                    **
**                                                                   **
**                                                                   **
**         REGISTER USAGE IS :                                       **
**   R0    SCRATCH                                                   **
**   R1    SCRATCH                                                   **
**   R2    TCT                                                       **
**   R3    UCB, SMCA                                                 **
**   R4    TCB (PARM TO CVT VALIDITY CHECK ROUTINE)                  **
**   R5    JMR                                                       **
**   R6    CVT                                                       **
**   R7    UCM, IOSB                                                 **
**   R8    IOQ, BAS                                                  **
**   R9    WORK                                                      **
**   R10   USERTCB OR 0                                              **
**   R11   ORE, UCBLOCK                                              **
**   R12   BASE                                                      **
**   R13   GETMAIN WORK AREA                                         **
***********************************************************************
         EJECT ,
         SPLEVEL SET         USE MACLIB VERSION                  93175
IEFUTL   PGMHEAD ZERO,BASE=R12,PARM=R1,SP=253,AM=31,RM=31       GP04043
         LA    R13,12(,R13)  FIX FOR SETLOCK PROBLEM (STM 0(13)!)
         USING SAVE+12,R13   REDEFINE                            93175
         L     R6,CVTPTR                                         89222
         USING CVTMAP,R6     DECLARE CVT                         89222
         LTCB  R4,USE=YES    GET TCB POINTER                     89222
         L     R2,TCBTCT     GET TCT
         USING SMFTCT,R2                                         89222
         L     R5,TCTJMR     ASSUMES TCT ALREADY IN R2
         USING JMR,R5        DECLARE IT
         L     R10,TCBUSER   LOAD USERTCB (OR ZERO)              89226
         STC   R0,ENTRYTYP   SAVE ENTRY INDICATOR
         MVC   WTOPFX,CANPFX   PREPARE WTO AREA
         MVC   WTOJOB,JMRJOB
         SPACE 1                                                 89226
         LA    R9,15*60      15 MINUTES = OUR DEFAULT WAIT TIME  93175
         ICM   R3,15,CVTSMCA ANY SMF BASE ?                      89226
         BZ    NOSMCA        HUH ?                               89226
         USING SMCABASE,R3   DECLARE IT                          89226
         L     R8,SMCAJWT    GET JOB WAIT TIME IN ALMOST SECONDS 89226
         SLR   R9,R9         CLEAR                               89226
         SRDL  R8,22         SCALE IT                            89226
         D     R8,=F'976'    CONVERT TO SECONDS                  89226
NOSMCA   ST    R9,WAITTIME   STASH WAIT TIME IN SECONDS          89226
         CLI   ENTRYTYP,8    WAIT TIME ENTRY ?
         BNE   CPUGONE       NO, CPU
         SPACE 2                                                 89222
***********************************************************************
*                                                                     *
*    WAIT TIME EXPIRED:                                               *
*                                                                     *
*      0) EXTEND IF (NEW) PRIVILEGE FLAG ON IN JMRUCOM+1.             *
*      ALWAYS IF $JMRWMAX; ONCE IF $JMRWAIT.                          *
*                                                                     *
*      1) CHECK FOR UNFINISHED I/O (MAY BE INTERVENTION REQUEST,      *
*      RESERVED DEVICE ON ANOTHER SYSTEM, ETC.                        *
*      SEND FRIENDLY MESSAGE TO OPERATOR SHOWING UNIT ADDRESS.        *
*                                                                     *
*      2) CHECK FOR A WTOR THAT THE PROGRAM IS WAITING ON, BUT        *
*      THAT THE OPERATOR HAS NOT RESPONDED TO.                        *
*      SEND FRIENDLY MESSAGE TO OPERATOR SHOWING REPLY ID.            *
*                                                                     *
*      3) FOR A TIME-SHARING USER, ISSUE A WARNING MESSAGE ON         *
*      FIRST ENTRY, AND GRANT ONE EXTENSION.                          *
*                                                                     *
***********************************************************************
         SPACE 1                                                 89222
         PUSH  USING                                            GP04043
         AIF   ('&LOCAL' NE 'RTI').SKPJMR1                      GP04035
         TM    JMRUFLG,$JMRWMAX  ALLOW UNLIMITED WAIT EXTENSIONS ?
         BNZ   WAITEX        YES; EXTEND WITHOUT MESSAGE         89222
.SKPJMR1 SPACE 1                                                 89222
         NOP   DONEIO        LATER ADD OTHER TESTS ???           89222
***********************************************************************
*                                                                     *
*    SEARCH I/O QUEUES FOR REQUESTS FROM THIS ADDRESS SPACE           *
*                                                                     *
***********************************************************************
UNITINIT XC    UNITWORK(UNITCLR),UNITWORK  CLEAR WORK AREA      GP99014
         LA    R0,L'UNITUDEV  LENGTH OF DEVICE SEGMENT          GP99034
         STCM  R0,3,UNITUDEL  SET FOR CALL                      GP99034
         SPACE 1                                                GP04035
UNITLOOP UCBSCAN ADDRESS,WORKAREA=UNITWORK,NOPIN,UCBCXPTR=UEXTPTR,     *
               UCBPTR=UNITPTR,RANGE=ALL,LOC=ANY,DYNAMIC=YES,           *
               MF=(E,UNITUPAT)                                  GP99014
         CH    R15,=H'12'    IOC CHANGED?                       GP99015
         BE    UNITINIT      YES; START OVER                    GP04035
         CH    R15,=H'4'     OTHERWISE?                         GP99015
         BNL   DONEIO        LAST ONE DONE                      GP99014
         L     R3,UNITPTR    LOAD THE UCB POINTER               GP99014
         SH    R3,=Y(UCBPRFX)  POINT TO PREFIX                   93175
         USING UCBBGN,R3     DECLARE UCB PREFIX                  93175
         L     R2,UEXTPTR    GET EXTENSION ADDRESS              GP04043
         USING UCBCMEXT,R2   AND DECLARE IT                     GP04043
         SPACE 1                                                GP04043
*---------------------------------------------------------------------*
*   CHECK FOR DISK AND TAPE MOUNT REQUEST                             *
*---------------------------------------------------------------------*
         TM    UCBSTAT,UCBONLI                                  GP04043
         BZ    PROBIO        SKIP IF NOT ONLINE                 GP04043
         TM    UCBTBYT3,UCB3TAPE+UCB3DACC  DISK OR TAPE ?       GP04043
         BNM   PROBIO        NEITHER, SKIP                      GP04043
         L     R1,PSAAOLD-PSA    LOAD CURRENT ASCB ADDRESS      GP04043
         LH    R0,ASCBASID-ASCB(,R1)  LOAD ASCB ID              GP04043
         CH    R0,UCBASID    BELONG TO US ?                     GP04043
         BNE   PROBIO        NO                                 GP04043
         TM    UCBDMCT,UCBMOUNT  IS MOUNT PROCESSING FLAG ON ?  GP04043
         BZ    PROBIO        NO, TEST FURTHER                   GP04043
         TM    UCBSTAT,UCBRESV+UCBALOC+UCBPRES+UCBSYSR+UCBDADI  ALLOC ?
         BZ    PROBIO        NO, SKIP                           GP04043
         TM    UCBFL1,UCBNRY  UNIT READY ?                      GP04043
         BNZ   MIOWTO        YES, IGNORE DELAY IN PROCESSING    GP04043
         SPACE 1                                                 93175
*---------------------------------------------------------------------*
*   CHECK FOR OTHER DEVICE PROBLEM                                    *
*---------------------------------------------------------------------*
PROBIO   TM    UCBFLA,UCBNRY  NOT READY ?                       GP04043
         BNZ   CHEKOURS                                         GP04043
         TM    UCBFLC,UCBIVRR+UCBIVRS+UCBDDRSW  SWAP OR ?       GP04043
         BNZ   CHEKOURS                                         GP04043
         TM    UCBFLA,UCBSTRT+UCBHALT+UCBCLEAR   ANY ACTIVITY ? GP04035
         BZ    UNITLOOP      NO; IGNORE                         GP04035
         ICM   R15,15,UCBIOQ   I/O PENDING ?                     93175
         BZ    UNITLOOP      NO; DEFINITELY NOT OURS             93175
         SPACE 1                                                 93175
CHEKOURS LA    R11,UCBLOCK   POINT TO THE LOCK WORD              93175
         BAS   R14,SETSUP    GET VERY PRIVILEGED                GP04055
         SETLOCK OBTAIN,TYPE=IOSYNCH,ADDR=(11),MODE=UNCOND,REGS=SAVE,  *
               RELATED=(NIECE)   FREEZE THE UCB                  93175
         L     R1,PSAAOLD-PSA    LOAD CURRENT ASCB ADDRESS       93175
         LH    R0,ASCBASID-ASCB(,R1)  LOAD ASCB ID               93175
         USING IOQ,R8                                            93175
         LA    R8,UCBIOQ-(IOQLNK-IOQ)                            93175
NEXTIOQ  ICM   R8,15,IOQLNK  I/O PENDING ?                       93175
         BNP   FREEUCB       NO; DEFINITELY NOT OURS             93175
         ICM   R7,15,IOQIOSB GET THE ASSOCIATED IOSB             93175
         BNP   NEXTIOQ       NONE; SKIP                          93175
         USING IOSB,R7       DECLARE IT                          93175
         C     R3,IOSUCB     VERIFY THE UCB LINKAGE              93175
         BNE   NEXTIOQ       NO; SKIP  (?)SHOULD NEVER HAPPEN    93175
         CH    R0,IOSASID    CORRECT ADDRESS SPACE ?             93175
         BNE   NEXTIOQ       NO; TRY ANOTHER                     93175
         BAS   R8,RELIOS                                         93175
MIOWTO   TM    UCBTBYT3,UCB3DISP+UCB3COMM  TP/CTC/CRT ?          93175
         BNZ   WAITEX        YES; EXTEND TIME WITHOUT MESSAGE    89222
         MVC   WTOL(L'CANINT),CANINT  MOVE MESSAGE               89222
         INHEX WTOUNIT,UCBCHAN SHOW DEVICE NUMBER               GP04035
         CLI   WTOUNIT,C'0'  OLD THREE DIGIT NUMBER ?           GP04035
         BNE   NAMEFOUR                                         GP04035
         MVC   WTOUNIT(3),WTOUNIT+1 LEFT-JUSTIFY                GP04035
         MVI   WTOUNIT+L'WTOUNIT-1,C' '                         GP04035
NAMEFOUR LA    R15,LCANINT                                       89222
         B     WAITWTO       EXTEND WITH MESSAGE                 89222
         SPACE 1                                                 89222
FREEUCB  BAS   R8,RELIOS                                         93175
         B     UNITLOOP      TRY ANOTHER UCB                    GP04035
         SPACE 1                                                 93175
RELIOS   SETLOCK RELEASE,TYPE=IOSYNCH,ADDR=(11),REGS=SAVE,             *
               RELATED=(NEPHEW)    UNFREEZE                      93175
         BAS   R14,LETSUP    UN-SUPERVISE                       GP04055
         BR    R8            RETURN TO CALLER                    93175
         SPACE 1                                                 84100
         POP   USING                                            GP04043
DONEIO   DS    0H                                                84100
         EJECT ,                                                GP04035
***********************************************************************
*                                                                     *
*    FIND OUTSTANDING (OR MEDIOCRE) REPLY REQUESTS                   *
*                                                                     *
***********************************************************************
TESTWTOR NOP   SKIPWTOR       ** DEBUG ZAPPER**                 GP04056
         BAS   R8,SETLOCAL   GET LOCAL LOCK (FOR VALIDITY CHECKING)
         BAS   R8,SETLOCK    SET SPIN LOCK                       89222
         L     R7,CVTCUCB
         USING UCM,R7                                            89222
         LA    R11,UCMRPYQ-(ORELKP-OREF)   GET FIRST ORE ADDRESS 89222
         USING OREF,R11                                          89222
         L     R9,PSAAOLD-PSA      LOAD CURRENT ASCB ADDRESS     89222
         LH    R9,ASCBASID-ASCB(,R9)  LOAD ASCB ID               89222
NEXTORE  ICM   R11,15,ORELKP IS THERE ANOTHER ON THE CHAIN ?     89222
         BZ    DONEWTOR      NO                                  89222
         SPACE 1
*---------------------------------------------------------------------*
*   IS THE ORE FOR THIS SYSTEM, AND IS IT INCOMPLETE OR (DOM) PURGED? *
*---------------------------------------------------------------------*
         TM    OREXA,OREFORGN+OREINUSE+OREDMCMP ?               GP04035
         BNZ   NEXTORE       YES; SKIP IT                       GP04035
         SPACE 1                                                GP04035
*---------------------------------------------------------------------*
*   IS THE ORE BUFFER IN USE AND HAS THE BUFFER NOT BEEN PURGED ?     *
*---------------------------------------------------------------------*
         TM    OREXC,OREBUFA+OREBUFC  UNUSABLE ?                 89222
         BNZ   NEXTORE       YES; SKIP IT                        89222
*---------------------------------------------------------------------*
*   DOES THE ORE PERTAIN TO THIS JOB ?                                *
*     IF IT DOES, TEST WAIT BIT IN ECB                                *
*---------------------------------------------------------------------*
         CH    R9,OREASID    ASID MATCH ?                        89222
         BNE   NEXTORE       NO, IGNORE IT
         SPACE 1
*---------------------------------------------------------------------*
*   VALIDITY CHECK ECB UNLESS SPECIAL KEY=0                           *
*---------------------------------------------------------------------*
         BAS   R8,LETLOCK    MUST ALLOW PAGE FAULTS (ECB)        89222
         TM    OREXA,OREKEY0 SVCKEY SWITCH SET ?                 79049
         BO    VALIDORE      YES; BYPASS VALIDITY CHECK
         L     R1,OREECB     GET ADDRESS TO BE CHECKED
         SLR   R2,R2         SINGLE ENTRY TO BE DONE             89223
         L     R15,CVT0VL01  CALL VALIDITY CHECK ROUTINE         89222
*---------------------------------------------------------------------*
*   NOTE: IEAVEVAL REQUIRES LOCAL LOCK.                          89222*
*     R4 = TCB ADDRESS    (PRE-LOADED, USED GLOBALLY IN ROUTINE)      *
*---------------------------------------------------------------------*
         BASR  R14,R15
         BNZ   NEXTORE       IGNORE INVALID ECB
         SPACE 1
VALIDORE L     R1,OREECB     IS ANYONE WAITING ?
         SPACE 1
*---------------------------------------------------------------------*
*   DON'T BOTHER TO TEST IF IT IS WAITING FOR CORRECT           GP04056
*     ECB OR IF ECB HAS BEEN CLOBBERED.                         GP04056
*     JUST ACCEPT ECB AS BEING UNCLOBBERED                      GP04056
*     THE OPERATOR PROBABLY NEEDS AN ALARM CLOCK ANYHOW.        GP04056
*     THIS IS AN UNFAIR PROCEDURE :                             GP04056
*     IF YOU DON'T LIKE IT, TOUGH.                              GP04056
*---------------------------------------------------------------------*
         TM    0(R1),X'80'   WAITING ?
         BO    WAKEUP        YES
         BAS   R8,SETLOCK    FREEZE AGAIN (REALLY SHOULD RESTART?)
         B     NEXTORE       TRY AGAIN                           89222
         SPACE 2
WAKEUP   BAS   R8,LETLOCK    RELEASE SPIN LOCK                   89222
         BAS   R8,LETLOCAL   RELEASE LOCAL LOCK                  89222
         MVC   WTOL(L'CANORE),CANORE  MOVE MESSAGE              GP04053
         ICM   R0,3,OREID    OLD TWO-BYTE ID ?                  GP04053
         BNZ   REPID2        YES; JUST MOVE IT                  GP04053
REPID4   L     R0,ORERPIDB   GET BINARY ID                      GP04053
         CVD   R0,DB         PACK IT                            GP04053
         UNPK  WORK,DB       UNPACK IT                          GP04053
         OI    WORK+L'WORK-1,C'0'  MAKE F ZONE                  GP04053
         MVC   WTOL+DSPORE(4),WORK+L'WORK-4  4-BYTE ID          GP04053
         LA    R15,L'CANORE+2                                   GP04053
         B     WAITWTO                                          GP04053
REPID2   MVC   WTOL+DSPORE(2),OREID  ADD IN THE REPLY NUMBER    GP04053
         LA    R15,L'CANORE  SET THE LENGTH
         B     WAITWTO       SEND A MESSAGE AND EXTEND          GP04056
         DROP  R7,R11
         SPACE 2                                                 89222
*---------------------------------------------------------------------*
*   NO WTOR FOUND                                                     *
*   CHECK LOCAL EXTENSION BIT                                         *
*---------------------------------------------------------------------*
SKIPWTOR BAS   R8,LETLOCK    RELEASE LOCK                        89222
         BAS   R8,LETLOCAL   RELEASE LOCAL LOCK                  89222
DONEWTOR DS    0H                                               GP04056
         AIF   ('&LOCAL' NE 'RTI').SKPJMR2                      GP04035
         TM    JMRUFLG,$JMRWAIT  ALLOW ONE EXTENSION ?           89222
         BZ    TESTTSO       NO; CHECK I/O                       93175
         NI    JMRUFLG,255-$JMRWAIT  RESET                       89222
         B     WAITEX        AND EXTEND WITHOUT MESSAGE          89222
.SKPJMR2 SPACE 1                                                GP04035
***********************************************************************
*                                                                     *
*   FOR TIME-SHARING USER, CHECK FOR USERTCB AND PRIOR EXTENSION.     *
*      EXTEND SESSION BY 15 MINUTES ONCE AFTER ISSUING A WARNING      *
*                                                                     *
***********************************************************************
TESTTSO  ICM   R1,15,PSAAOLD-PSA  GET ASCB                       93175
         ICM   R1,15,ASCBTSB-ASCB(R1)  TSO SESSION ?             93175
         BZ    TS            NO; FAIL                            93175
         AIF   ('&LOCAL' EQ 'RTI').SESSEX                       GP04053
         LTR   R10,R10       IS THERE A USERTCB EXTENSION ?      93175
         BZ    TS            NO ?                                93175
         USING USERTCB,R10                                       93175
         TM    UTF2,TUFTWAIT  PRIOR TERMINAL WAIT ?              93175
         BNZ   TS            YES; FAIL NOW                       93175
         OI    UTF2,TUFTWAIT  SET IT                             93175
** OOPS ** TPUT IS QUEUED DESPITE BREAKIN. SOLVE LATER **        93177
*DEFER*  TPUT  TSOWAIT,L'TSOWAIT,EDIT,WAIT,HOLD,BREAKIN,HIGHP    93175
         DROP  R10                                               93175
.SESSEX  B     WAITEX        GRANT ONE EXTENSION                 93175
         SPACE 2                                                 89222
***********************************************************************
*                                                                     *
*    SETLOCK/LETLOCK : SUBROUTINES TO ACQUIRE/FREE SPIN LOCK          *
*        PROGRAM LOGIC ASSUMES THAT HOLDING THE DISP SPIN LOCK        *
*        IS ADEQUATE TO PRESERVE IOS AND CONSOLE QUEUES.              *
*                                                                     *
***********************************************************************
SETLOCK  TM    FLAGS,FGLOCK  ALREADY HOLD IT ?                   89223
         BNZR  R8            YES ?                               89223
         TM    FLAGS,FGLOCAL  ALREADY IN THE SUP ?               89223
         BNZ   SETLOCK2                                          89223
         BAS   R14,SETSUP    GET VERY PRIVILEGED                GP04055
SETLOCK2 SETLOCK OBTAIN,TYPE=DISP,MODE=UNCOND,REGS=SAVE,RELATED=(AUNT)
         OI    FLAGS,FGLOCK  SHOW LOCK GOTTEN                    89222
         BR    R8            RETURN                              89222
         SPACE 1                                                 89222
LETLOCK  TM    FLAGS,FGLOCK  HOLDING A LOCK ?                    89222
         BZR   R8            NO; JUST RETURN                     89222
         SETLOCK RELEASE,TYPE=DISP,REGS=SAVE,RELATED=(UNCLE)     89222
         NI    FLAGS,255-FGLOCK  RESET LOCK                      89222
         BR    R8            RETURN TO CALLER                    89222
         SPACE 1                                                 89222
SETLOCAL TM    FLAGS,FGLOCAL ALREADY HOLD IT ?                   89223
         BNZR  R8            YES ?                               89223
         TM    FLAGS,FGLOCK  ALREADY IN THE SUP ?                89223
         BNZ   SETLOCL2                                          89223
         BAS   R14,SETSUP    GET VERY PRIVILEGED                GP04055
SETLOCL2 SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=SAVE,RELATED=(MOM)
         OI    FLAGS,FGLOCAL SHOW LOCK GOTTEN                    89222
         BR    R8            RETURN                              89222
         SPACE 1                                                 89222
LETLOCAL TM    FLAGS,FGLOCAL HOLDING LOCAL LOCK ?                89222
         BZ    LETLOCAT      NO; JUST RETURN                    GP04055
         SETLOCK RELEASE,TYPE=LOCAL,REGS=SAVE,RELATED=(DAD)      89222
         NI    FLAGS,255-FGLOCAL RESET LOCK                      89222
LETLOCAT BAS   R14,LETSUP    UN-SUPERVISE                       GP04055
         BR    R8            RETURN TO CALLER                    89222
         SPACE 2                                                GP04055
*---------------------------------------------------------------------*
*                                                                     *
*   SWITCH TO AND FROM SUPERVISOR MODE.  RETURN ON R14                *
*                                                                     *
*---------------------------------------------------------------------*
SETSUP   ST    R14,SUBSAV14  SAVE RETURN                        GP04055
         TM    FLAGS,FGSUP   ALREADY IN THE SUP ?               GP04055
         BNZ   SETSUPX       YES; GET OUT                       GP04055
         MODESET MODE=SUP                                       GP04055
         OI    FLAGS,FGSUP   REMEMBER                           GP04055
SETSUPX  L     R14,SUBSAV14  RETURN TO CALLER                   GP04055
         BR    R14                                              GP04055
         SPACE 1                                                GP04055
LETSUP   ST    R14,SUBSAV14  SAVE RETURN                        GP04055
         TM    FLAGS,FGSUP   ALREADY IN THE SUP ?               GP04055
         BZ    LETSUPX       YES; GET OUT                       GP04055
         MODESET MODE=PROB                                      GP04055
         NI    FLAGS,255-FGSUP   REMEMBER                       GP04055
LETSUPX  L     R14,SUBSAV14  RETURN TO CALLER                   GP04055
         BR    R14                                              GP04055
         EJECT ,
***********************************************************************
*                                                                     *
*    THIS ROUTINE IS ENTERED WHEN CPU TIME IS GONE                    *
*    IT TESTS WETHER THIS JOB IS A WISE ASS                           *
*    IF IT IS, IT LETS THINGS SLIDE ANOTHER 1/2 HOUR OR SO            *
*                                                                     *
*    IF THE USER HAS SPECIFIED 'TIME' ON THE JOB CARD,                *
*    (ACCOUNT SUBFIELD #3), THEN AN EXTENSION WILL BE GRANTED ONCE    *
*    PER STEP AND ONCE PER JOB, AMOUNTING TO 0.5 TIMES THE MAXIMUM    *
*    CPU TIME FOR THAT JOB'S CLASS, BUT NOT EXCEEDING 60 MINUTES.     *
*                                                                     *
***********************************************************************
         USING JMR,R5        DECLARE IT
CPUGONE  MVC   WTOL(L'CANEXT),CANEXT  PREPARE MESSAGE
         LA    R9,8          EXTENSION IS IN SECONDS             89226
         L     R8,WAITTIME   GET WAIT TIME LIMIT                 89226
         LR    R15,R8        COPY
         SRL   R15,1         1/2 OF LIMIT
         AR    R8,R15        1.5 * WAIT LIMIT, SHOULD BE 15 MINUTES
*NUTS*   TM    JMRUFLG,$JMRTIME  UNCONDITIONAL TIME EXTENSION ?  89222
*NUTS*   BNZ   TATTLE        YES; EXTEND AND TELL OPERATOR       89222
         LTR   R10,R10       HAVE OUR LOCAL TCB EXTENSION ?      89226
         BZ    TS            SHOULD NOT HAVE HAPPENED ?          89222
         USING USERTCB,R10                                       89226
         TM    UTF1,TUFZERO+TUFPASS ?   WISE GUY ?               89222
         BNZ   TATTLE        YES, GIVE IT TIME . . . . . . . . . .
         TM    ENTRYTYP,4    STEP CPU EXCESSION ?
         BO    STEPCHEK      YES, CHECK IT
         TM    UTF1,TUFJOBEX  JOB EXTENSION ALLOWED ?            89222
         BZ    TS            NO, TOO BAD
         NI    UTF1,255-TUFJOBEX  ONLY ONCE PER JOB              89222
         MVC   WTOL(L'CANJOB),CANJOB   MAKE INTO JOB CPU TIME MESSAGE
         B     CLASSTIM      COMPUTE AMOUNT TO BE GRANTED BY CLASS
         SPACE 1
STEPCHEK LR    R7,R8         SAVE 1.5 * WAIT TIME VALUE          89226
         LH    R8,=H'7680'   LOAD .2 SECONDS WORTH OF TIMER UNITS
         TM    UTF2,TUFDIDEX  PRIOR EXTENSION GRANTED ?          93175
         BNZ   STEPTWO                                           93175
         OI    UTF2,TUFDIDEX  SET EXTENSION                      93175
         B     TU            ALLOW FOR IEFBR14                   93175
STEPTWO  DS    0H                                                93175
*NOT SET OC    TCTJSTX,TCTJSTX  WAS A PRIOR EXTENSION GRANTED ?  89226
*OOPS*   BZ    TU            NO; ALLOW FOR IEFBR14               89222
         TM    UTF1,TUFSTPEX  STEP TIME EXTENSION ALLOWED ?      89222
         BZ    TS            NO
         LR    R8,R7         SAVE 1.5 * WAIT TIME LIMIT          89226
         NI    UTF1,255-TUFSTPEX   ONLY ONCE PER JOB             89222
         SPACE 1
CLASSTIM CLC   =C'Z904001',JMRUSEID   SPECIAL ACCOUNT NUMBER ?
         BE    TATTLE        YES; USE 15 MINUTE EXTENSION
         SPACE 1                                                 93175
         AIF   ('&LOCAL' NE 'PID').CLASCOM                      GP04047
         ICM   R8,15,CVTUSER  USER EXTENSION THERE ?             93175
         BZ    CLASSLUK      NO; USE BUILT-IN TABLE              93175
         ICM   R8,15,UCLVOLT-USERCVT(R8)  VOLUME TABLE ?         93175
         BZ    CLASSLUK      NO; USE BUILT-IN TABLE              93175
         USING USERVOLT,R8   DECLARE IT                          93175
         L     R1,APCLASS    POINT TO CLASS TABLE                93175
UCLASLOP CLC   JMRUCLAS,0(R1)  MATCHING CLASS ?                  93175
         BE    UCLASCOM      YES; PROCESS                        93175
         LA    R1,3(,R1)     NEXT ENTRY (L'AMCLASS)              93175
         C     R1,APCLDFL    LAST ENTRY ?                        93175
         BNH   UCLASLOP      NO; CHECK AGAIN                     93175
         B     CLASSLUK      YES; USE TABLE                      93175
         SPACE 1                                                 93175
UCLASCOM SLR   R15,R15                                           93175
         IC    R15,1(,R1)    LOAD INDEX                          93175
         L     R1,APTIMDF    POINT TO DEFAULT TIME               93175
         L     R8,APTIMLM(R15)  LOAD TIME IN 1/100 SECONDS       93175
         DROP  R8                                                93175
         MIN   R8,0(R1)      DON'T EXCEED MAXIMUM                93175
         SRDL  R8,32         PREPARE FOR DIVIDE                  93175
         D     R8,=A(100*2)  HALVE AND MAKE INTO SECONDS         93175
         LTR   R8,R9         TEST QUOTIENT                       93175
         BP    SETSECS       GO TO EXTEND BY SECONDS             93175
         LA    R8,1          ONE SECOND ?                        93175
         B     SETSECS                                           93175
.CLASCOM SPACE 1                                                 93175
CLASSLUK LA    R1,CLASSTAB   ELSE POINT TO CLASS TABLE
         LA    R15,(CLASSEND-CLASSTAB)/4   AND NUMBER OF ENTRIES
CLASSLOP CLC   JMRUCLAS,0(R1) MATCHING CLASS ?                   89222
         BE    CLASSCOM      YES - GET VALUE
         LA    R1,4(,R1)     NEXT ENTRY
         BCT   R15,CLASSLOP  TRY AGAIN
CLASSCOM SLR   R8,R8         CLEAR FOR LOAD                      89222
         ICM   R8,7,1(R1)    GET EXTENSION TIME IN SECONDS       89222
SETSECS  LA    R9,8          EXTENSION IS IN SECONDS             89222
         SPACE 1
TATTLE   LA    R15,L'CANEXT  GET LENGTH
         MVC   WTONUM,INFO   CHANGE MESSAGE NUMBER TO INFORMATION ONLY
         BAS   R11,WTO       TELL OPSIE
*        R9:   4 - IN TIMER UNITS      8 - IN SECONDS            89222
         B     UPAND@EM      EXTEND AND RETURN                   89222
         SPACE 1                                                GP04056
*---------------------------------------------------------------------*
*   WAKE UP OPERATOR - SEND A WTO WITH BELLS                          *
*---------------------------------------------------------------------*
         SPACE 1                                                GP04056
WAITWTO  BAS   R11,WTO       WAKE OPERATOR                      GP04056
         SPACE 1                                                GP04056
*---------------------------------------------------------------------*
*   GRANT A TIME EXTENSION                                  89226     *
*---------------------------------------------------------------------*
WAITEX   L     R8,WAITTIME   GET WAIT TIME LIMIT  (SHOULD BE 10 MIN)
         LA    R9,8          EXTENSION IS IN SECONDS             89226
         B     UPAND@EM      RETURN TO SYSTEM                    89226
         SPACE 2
*---------------------------------------------------------------------*
*   THIS ROUTINE GIVES THE USER A BREAK                               *
*     R8 IS SET TO THE EXTENSION IN TIMER UNITS                       *
*---------------------------------------------------------------------*
TU       LA    R9,4          THIS IS COSTING MONEY               89222
         B     UPAND@EM
         SPACE 2                                                GP04035
*---------------------------------------------------------------------*
*   THIS ROUTINE IS ENTERED MORE IN SORROW THAN IN ANGER              *
*     IT HERTZ YOU MORE THAN IT HERTZ ME.                             *
*---------------------------------------------------------------------*
TS       SLR   R9,R9         NO EXTENSION; VALUE IRRELEVANT      89226
         SPACE 1
*---------------------------------------------------------------------*
*   EXIT WITH OUR WITHOUT EXTENSION                                   *
*---------------------------------------------------------------------*
         PUSH  USING                                             89222
UPAND@EM SH    R13,=H'12'    UNFIX SETLOCK PROBLEM               93175
         USING SAVE,R13      FIX MAPPING                         93175
         PGMEXIT RC=(R9),RC1=(R8) RETURN WITH CODE (AND VALUE IN R1)
         POP   USING                                             93175
         SPACE 2
***********************************************************************
*                                                                     *
*    COMMON WTO ROUTINE                                               *
*       IF USERTCB AVAILABLE, CLEAR PRIOR AND SAVE CURRENT DOM IDS.   *
*                                                                     *
***********************************************************************
WTO      LA    R15,L'WTOPFX(,R15)  GET TOTAL WTO LENGTH
         STH   R15,WTOMSG     STASH IT
         LA    R1,WTOMSG      GET MESSAGE ADDRESS
         LA    R14,0(R1,R15)    GET LAST BYTE FOLLOWING MESSAGE
         MVC   0(4,R14),DINGDONG  MOVE DESC/ROUTCDE
         LTR   R10,R10       USER TCB PRESENT ?                  89226
         BZ    WTO2          NO; NO DOM                          89226
         ICM   R1,15,UTDM    PRIOR DOM ?                         89226
         BZ    WTO2          NO                                  89226
         DOM   MSG=(1)       MAKE OLD ENTRY REMOVABLE            89226
         XC    UTDM,UTDM                                         89226
         SPACE 1                                                GP04035
WTO2     WTO   MF=(E,WTOMSG) CHATTER
         LTR   R10,R10       ANY USERTCB ?                       89226
         BZR   R11           NO; RETURN                          89226
         ST    R1,UTDM       ELSE SAVE DOM ID                    89226
         BR    R11                                              GP04035
         EJECT ,
*    THIS TABLE IS A BACKUP TABLE FOR USERVOLT                        *
*        ENTRIES ARE MINUTES OF CLASS LIMIT (EASIER TO COMPARE)       *
CLASSTAB DC    0A(0),C'E',AL3(10*30)  5 MINUTES - CLASS E        93175
         DC    C'D',AL3(10*30)        5 MINUTES                  93175
         DC    C'B',AL3(10*30)        5 MINUTES                  93175
         DC    C'C',AL3(60*30)       30 MINUTES                  93175
         DC    C'A',AL3(60*30)       30 MINUTES                  93175
         DC    C'S',AL3(60*30)       30 MINUTES                  93175
         DC    C'S',AL3(60*30)      ZAP ENTRY                    93175
         DC    C'S',AL3(60*30)      ZAP ENTRY                    93175
         DC    C'S',AL3(60*30)      ZAP ENTRY                    93175
         DC    C'S',AL3(60*30)      ZAP ENTRY                    93175
         DC    C'S',AL3(60*30)      ZAP ENTRY                    93175
         DC    C'S',AL3(60*30)      ZAP ENTRY                    93175
CLASSEND DC    C'?',AL3(120*30)      60 MINUTES ALL OTHER CLASSES
         SPACE 1
TSOWAIT DC C'MSG522W TIME-OUT EXIT: IMPENDING LOGOFF DUE TO INACTIVITY'
CANORE   DC    C'WAITING FOR REPLY XX'  OR XXXX EXTENDED
DSPORE   EQU   L'CANORE-2    POSITION FOR ORE ID INSERTION
CANINT   DC    C'INTERVENTION NEEDED ON '                       GP04035
DSPUNIT  DC    CL4'XXXX',C' '  INSERTION OF DEVICE NAME         GP04035
LCANINT  EQU   *-CANINT      LENGTH OF MESSAGE                  GP04035
CANEXT   DC    C'STEP CPU TIME EXTENDED'
CANPFX   WTO   'MSG999A JOB XXXXXXXX ',ROUTCDE=(1,11),DESC=1,MF=L
         ORG   *-4
DINGDONG DS    XL4           ROUTCDE, DESC
         ORG   ,
CANJOB   DC    CL4' JOB'     MAKE STP CPU INTO JOB CPU
INFO     DC    C'8I'         SUFFIX TO MSG99..'
HEXTRTAB DC    C'0123456789ABCDEF'                              GP04035
HEXTAB   EQU   HEXTRTAB-C'0'                                    GP04035
PATCH    DC    0F'0'         PATCH AREA                          89226
         DC    20S(*)        PATCH ENTRIES                       89226
         EJECT ,
SAVE     DSECT ,             DESCRIPTION OF SAVE/WORK AREA       89222
         DS    3A(0)         JUST IN CASE (SETLOCK PROBLEM)      93175
SUBSAV14 DS    A             SUBROUTINE RETURN SAVER            GP04055
DB       DS    D             WORK DOUBLE-WORD                   GP04053
WORK     DS    CL8           WORK SPACE                         GP04053
WAITTIME DC    A(15*60)      WAIT TIME LIMIT IN SECONDS          89226
WTOMSG   DS    0A            WTO MAPPING
         DS    AL2(0,0)      WTO LENGTH, FLAGS
         DS    C'MSG999A JOB XXXXXXXX '  COMMON PREFIX
WTOL     DS    CL45' '       ROOM FOR TEXT
         DS    XL4           ROOM FOR DESC / ROUTCDE
         ORG   WTOMSG
WTOPFX   DS    CL(WTOL-WTOMSG)   COMMON PREFIX FOR MOVE
         ORG   WTOL-9        ADDRESS OF JOB NAME INSERTION
WTOJOB   DS    CL8           COPY OF JOB NAME FROM JMR
         ORG   WTOMSG+9      INSERTION OF NEW MESSAGE NUMBER
WTONUM   DS    CL2           FOR CHANGE TO MSG998I
WTOUNIT  EQU   WTOL+DSPUNIT-CANINT,L'DSPUNIT,C'C'               GP04035
         ORG   ,
         SPACE 1
ENTRYTYP DS    X             ENTRY TYPE FLAG SAVE BYTE           89222
FLAGS    DC    X'00'         CONTROL FLAGS                       89222
FGLOCK   EQU   X'80'           LOCK OBTAINED                     89222
FGLOCAL  EQU   X'40'           LOCAL LOCK HELD                   89222
FGSUP    EQU   X'20'           SWITCHED TO SUPERVISOR MODE      GP04055
         SPACE 1                                                GP04035
UNITUPAT UCBSCAN MF=(L,UNITPATU)                                GP99014
UNITWORK DC    XL100'0'      WORK AREA                          GP99014
UNITPTR  DC    A(0)          RETURNED UCB ADDRESS               GP99014
UEXTPTR  DC    A(0)          RETURNED UCB COMMON PREFIX ADDRESS GP04043
UNITCLR  EQU   *-UNITWORK    SIZE TO CLEAR                      GP99014
UNITUDEL DC    Y(L'UNITUDEV)  LENGTH OF DEVICE SGEMENT          GP99014
UNITUDEV DC    XL256'0'      DEVICE DEPENDENT SEGMENT           GP99014
SAVEEND  EQU   *             END OF WORK AREA                   GP04035
         SPACE 2
         PRINT &PRTSYS                                           89222
         IEFJMR ,
         A$UCOM ,            DEFINE USER COMMUNICATIONS FIELD    92296
         SPACE 1                                                GP04035
CVTDSECT DSECT ,                                                 89222
         CVT   DSECT=YES                                         89222
         SPACE 1                                                 89222
         IKJTCB ,                                                89222
         SPACE 1                                                 89222
         IEFUCBOB PREFIX=YES                                     89222
         SPACE 1                                                 89222
         IEFTCT ,                                                89222
         SPACE 1                                                 89222
         IHAPSA ,                                                89222
         SPACE 1                                                 89222
         IHAASCB ,                                               89222
         SPACE 1                                                 89222
         IHAORE ,                                                89222
         SPACE 1                                                 84100
         AIF   (&MVSXA).NOLCHQ2                                  92296
         IECDLCH ,                                               84100
         AGO   .DOLCHQ2                                          93175
.NOLCHQ2 IEFJESCT ,                                              93175
.DOLCHQ2 SPACE 1                                                 84100
         IECDIOQ ,                                               84100
         SPACE 1                                                 84100
         IECDIOSB ,                                              84100
         SPACE 1                                                 84100
         IECDIOCM ,          EXPAND IOCOM                        84100
         SPACE 1                                                 89222
         IEECUCM FORMAT=NEW                                      89222
         SPACE 1                                                 89226
         IEESMCA ,                                               89226
         SPACE 1                                                 89222
         PRINT &PRTMAC                                           93175
USERTCB  USERTCB ,                                               89222
         SPACE 1                                                 89222
         AIF   ('&LOCAL' NE 'PID').NOMAP2                       GP04035
USERCVT  USERCVT ,                                               89222
         SPACE 1                                                 93175
         USERVOLT LIST=NO                                        93175
.NOMAP2  END   ,                                                GP04035
