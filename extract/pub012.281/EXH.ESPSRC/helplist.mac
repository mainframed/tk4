HELPLIST TITLE 'H E L P L I S T  ***  V E R S I O N   3 . 8   '
************************************************************
*                                                          *
*              'HELPLIST' PROGRAM AT FPC                   *
*                                                          *
************************************************************
         SPACE
*        PROGRAM. HELPLIST.
*        AUTHOR. UNKNOWN - FROM TOM GEE.
*        MODIFIER. BILL GODFREY,  PLANNING RESEARCH CORPORATION.
*        INSTALLATION. FEDERAL POWER COMMISSION.
*        DATE-UPDATED IN SOURCE LIBRARY. JANUARY 8 1974.
*        REMARKS.
*            DATE ADDED TO HEADING.
*            SUBCOMMAND NAMES PREFIXED WITH 'SUBCOMMAND - '.
*            INDENTATION CHANGED. CHECK FOR )X SYNTAX ADDED.
*            USER MACROS - EASYCOME,GEHEN,REGISTER
*
*        MODIFIER. JERRY PITTENGER, PLANNING RESEARCH CORPORATION.
*        INSTALLATION. OFFICE OF MANAGEMENT AND BUDGET (OMB).
*        DATE-UPDATED IN SOURCE LIBRARY. AUGUST 17 1977.
*        REMARKS:
*            DATE REMOVED FROM HEADING. MULTIPLE HEADING LINES.
*            CHANGE FROM )O OR )X OR )F HIGHLIGHTED BETTER.
*            ALLOW NO ALIAS TO PRINT BY USING BLDL AGAINST
*            THE SYSLIB LIBRARY/LIBRARIES DIRECTORY. INSERTED
*            SUBCOMMANDS ON SEPARATE PAGES WITH THE SUBCOMMAND
*            NAME IN THE HEADER BLOCK.
         EJECT
*            CURRENT REGISTER USAGE:
*
*            R0 = SYSTEM PARM REG
*            R1 = SYSTEM PARM REG
*            R2 = PROGRAM PARM REG + WORK REG
*            R3 = WORK REG
*            R4 = RECORD LENGTH
*            R5 = BLOCKSIZE
*            R6 = SPARE
*            R7 = SPARE
*            R8 = OFFSET INTO MEMBER DSECT
*            R9 = WORK REG
*           R10 = ADDRESS OF 'LINE'
*           R11 = I/O AREA BASE
*           R12 = PROGRAM BASE
*           R13 = SAVE AREA POINTER
*           R14 = SYSTEM RETURN ADDR
*           R15 = PROGRAM RETURN ADDR
*
*        NON-STANDARD MACROS USED: KOMMEN AND GEHEN
*
*---------------------------------------------------------------------*
*                                                                     *
*   IF SYSIN IS DD DUMMY OR MISSING, FORMAT ALL MEMBERS AND ALIAS     *
*     ENTRIES.                                                        *
*   CHANGED DD NAME FOR HELP PDS TO SYSLIB, AND CHANGED DCB NAMES TO  *
*     DDNAME (SYSIN/SYSPRINT).                                        *
*   USED LOCAL SERVICE ROUTINES (CBT FILE 860)                        *
*   USED LOCAL MACROS (CBT FILE 861)                   G. POSTPISCHIL *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         COPY  ASMOPTS                                          GP15039
         SPACE 1
         ASMPARM LIST=YES                                       GP15039
         SPACE 1
         PUNCH '  ORDER HELPLIST(P) '  MAKE DEBUGGING EASIER    GP15039
         SPACE 2
         PRINT &PRTSRC            SAVE A TREE                   GP15039
HELPLIST PGMHEAD ZERO15,BASE=(R12),BNDRY=PAGE,PARM=R1           GP15039
         SERVINIT ,                                             GP15039
         SERVLOAD @INPREAD,@PRINTER                             GP15039
         MVC   BLDLLIST,=AL2(1,8+4+2+31*2)                      GP15039
         MVI   OCLIST,X'80'                                     GP15039
         LA    R0,MYJFCB                                        GP15039
         STCM  R0,7,LIBXLIST+1    COMPLETE EXIT LIST            GP15039
         L     R2,0(,R1)          REG 2 --> PARM                GP15039
         MVI   LINE-1,C' '
         MVC   LINE,LINE-1
         LA    R10,LINE                POINT R10 TO 'LINE'
         SLR   R11,R11       NO BUFFER GOTTEN                   GP15039
         RDJFCB (SYSLIB),MF=(E,OCLIST) GET SYSLIB INFORMATION   GP15039
         PRTOPEN SYSPRINT,OPT=ABEND                             GP15039
         PRTLIST TITLE1,TITLE=1                                 GP15039
         INPOPEN SYSIN,OPT=NOWTO  CHECK SYSIN FILE              GP15039
         CH    R15,=H'8'     FOUND ?                            GP15039
         BNL   SYNEOF          NO; READ ENTIRE PDS              GP15039
PARMCHK  CLI   1(R2),0              PARM?
         BE    NOPARM
         LH    R3,0(,R2)           R3  =  LENGTH OF PARM
         CLI   1(R2),9             PARM LENGTH GREATER THAN 9?
         BNH   *+8                   NO - BRANCH
         LA    R3,9                YES - REDUCE TO 9
         BCTR  R3,0                LENGTH MINUS 1 FOR EX
NOPARM   OPEN  (SYSLIB,INPUT),MF=(E,OCLIST)                     GP15039
         TM    SYSLIB+DCBOFLGS-IHADCB,DCBOFOPN  OPEN?           GP15039
         BZ    OPENERR         NO; OOPS                         GP15039
         LH    R0,SYSLIB+DCBBLKSI-IHADCB    BLOCKSIZE           GP15039
         GETMAIN R,LV=(0)
         LR    R11,R1                   SAVE POINTER TO I/O AREA
         B     CTLLOOP       SKIP AROUND                        GP15039
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*   SYNEOF IS ENTERED WHEN SYSIN IS MISSING OR EMPTY (IMMEDIATE EOF). *
*   FOR THAT CASE WE USE THE @INPREAD FEATURE TO READ THE ENTIRED PDS *
*   AND OTHERWISE USE THE COMMON PROCESSING LOGIC.                    *
*                                                                     *
*---------------------------------------------------------------------*
SYNEOF   TM    PROFLAGS,FGPDS     BEEN HERE BEFORE ?            GP15039
         BNZ   PGMEXIT              YES; END OF PROGRAM         GP15039
         ICM   R0,15,#SYSIN  ANY CONTROL CARDS READ ?           GP15039
         BNZ   PGMEXIT         YES; NO NEED FOR ENTIRE PDS      GP15039
         INPCLOSE DEV=1      CLOSE SYSIN                        GP15039
         INPOPEN SYSPDS      OPEN PDS AS DEV=1                  GP15039
         CH    R15,=H'4'     WAS IT OPENED ?                    GP15039
         BH    OPENERR         NO                               GP15039
         OI    PROFLAGS,FGPDS    SET PDS READ MODE              GP15039
         SPACE 1
CTLLOOP  INPGET REC          READ CONTROL CARD                  GP15039
         CH    R15,=H'4'     TEST RETURN                        GP15039
         BH    IOERROR         OOPS                             GP15039
         BE    SYNEOF          SHOULD NOT GET HERE              GP15039
         INC   #SYSIN        KEEP TRACK OF INPUT COUNT          GP15039
         ZI    PROFLAGS,FGALI     RESET ALIAS FLAG              GP15039
         TM    PROFLAGS,FGPDS     READING PDS?                  GP15039
         BNZ   JUSTMOVE             YES; NO BLDL REQUIRED       GP15039
         XC    PDS2TTRP(16),PDS2TTRP    RESET A LITTLE          GP15039
         BLDL  SYSLIB,BLDLLIST          GO GET IT               GP15039
         CH    R15,=H'4'                TEST RETURN CODE        GP15039
         BL    MOVE                     GOOD MEMBER             GP15039
         BH    BLDLERR                  GO ISSUE ABEND..PERM I/O ERROR
         BAL   R14,TITLER                                       GP15039
         PRTDATA 'BLDL FAILED FOR ',MEMNAME                     GP15039
         B     CTLLOOP                  GO GET THE NEXT MEMBER
MOVE     TM    PDS2INDC,PDS2ALIS        ALIAS?                  GP15039
         BZ    *+8                        NO                    GP15039
         OI    PROFLAGS,FGALI             YES; SET INDICATOR    GP15039
         SPACE 1
COMPROC  BAL   R14,TITLER                                       GP15039
         FIND  SYSLIB,MEMNAME,D         FIND MEMBER
         CH    R15,=H'4'                TEST RETURN CODE        GP15039
         BE    BADNAME                  INVALID NAME            GP15039
         BH    BLDLERR                  I/O ERROR               GP15039
         NI    PROFLAGS,255-FGOPIND-FGCMDS TURN OFF INDENTATION & CMDS
         CLC   =CL8'COMMANDS',MEMNAME  COMMANDS?                GP15039
         BNE   REPORTD                  NO, ....
         OI    PROFLAGS,FGCMDS          YES, INDICATE SUCH
REPORTD  TM    PROFLAGS,FGPDS     READING PDS?                  GP15039
         BNZ   CTLLOOP              YES; GET ANOTHER CARD       GP15039
         READ  DECB,SF,SYSLIB,(R11),'S'
         CHECK DECB
         LH    R5,SYSLIB+DCBBLKSI-IHADCB   BLOCK SIZE           GP15039
         L     R1,DECB+16               A(IOB)
         SH    R5,14(,R1)               MINUS RESIDUAL CCW COUNT
         BCTR  R5,0                     DOWN ONE
         LR    R3,R11                   REC START
         LA    R4,80                    REC LENGTH
         AR    R5,R3                    REC END                 GP15039
         B     PROCESS                                          GP15039
         SPACE 1
JUSTMOVE CLC   =C'./',REC    (PSEUDO)CONTROL CARD?              GP15039
         BNE   JUSTFAKE        NO                               GP15039
         MVC   MEMNAME,REC+33  MOVE MEMBER NAME                 GP15039
         CLI   REC+70,C'A'   ALIAS ENTRY?                       GP15039
         BNE   *+8             NO                               GP15039
         OI    PROFLAGS,FGALI    SET IT                         GP15039
         BAL   R14,TITLER                                       GP15039
         B     CTLLOOP           AND DONE WITH CARD             GP15039
         SPACE 1
JUSTFAKE LA    R5,80-1       @INPREAD RETURNS SINGLE CARD       GP15039
         LA    R3,REC        INPUT RECORD                       GP15039
         LA    R4,80                    REC LENGTH              GP15039
         LA    R5,0(R3,R5)              REC END                 GP15039
         SPACE 2
PROCESS  TM    PROFLAGS,FGCMDS          CMDS?
         BZ    PROCESSA                 NO, CONTINUE
         MVC   LINEPL(72),0(R3)         YES, COPY IN RECORD
         B     LISTER                   GO LIST IT
         SPACE 1
PROCESSA CLI   0(R3),C')'    SPECIAL?                           GP15039
         BNE   PROCESSB
         CLI   1(R3),C'F'    FUNCTION?
         BE    MAJOR
         CLI   1(R3),C'S'    SUBCOMMANDS?
         BE    MAJOR
         CLI   1(R3),C'X'    SYNTAX?
         BE    MAJOR
         CLI   1(R3),C'O'    SUBCOMMAND?
         BE    INDENT
PROCESSB CLI   0(R3),C'='               SUB COMMAND
         BNE   CHEKOP                   NO
         OI    PROFLAGS,FGSUBCM         TELL EM SUBCOMMAND
* PRESUPPOSES THAT SUBCOMMAND HAS AN '=' SIGN IN FRONT OF IT
* NOTE THAT SUBCOMMAND MAY HAVE ALIAS NAMES FOLLOWING, SEPARATED
* BY EQUAL SIGNS - USE / TO SEPARATE                            GP15039
*
         TR    1(71,R3),TREQTAB   FIX MULTIPLE = TO /           GP15039
         PRTLIST TITLE4,TITLE=4                                 GP15039
         B     BXLE                     GO GET SOME MORE
         SPACE 1
CHEKOP   CLC   0(2,R3),=C'))'           YES - OPERAND?
         BE    NOTDUMB                  YES - BRANCH
         CLC   0(2,R3),=C'  '           NO, CHECK FOR LACK OF NEATNESS
         BE    NOTDUMB                  VERY GOOD - BRANCH
         MVC   LINEIND(72),0(R3)  SOME DUMMY DIDN'T LEAVE 1ST 2 BLANK
         B     LISTER
         SPACE 1
NOTDUMB  MVC   LINEIND(70),2(R3)        YES, SET LINE WITH INDENTATION
         B     LISTER
         SPACE 1
NORMAL   TM    PROFLAGS,FGMAJ           MAJOR SEPARATION???
         BNO   LISTER1                  NO....LIST IT
         MVC   LINEMAJ(70),2(R3)        MOVE IN DATA
         MVC   3(3,R10),=C'***'          INSERT HIGHLIGHTER
         MVC   18(3,R10),=C'***'         INSERT HIGHLIGHTER
         NI    PROFLAGS,255-FGMAJ       TURN OFF SWITCH
         B     LISTER
         SPACE 1
LISTER1  MVC   LINEPL(70),2(R3)
LISTER   TM    PROFLAGS,FGSUBCM          IS IT SUBCOMMANDS???
         BNO   PUTIT                     NO....LIST ASIS
         NI    PROFLAGS,255-FGSUBCM      TURN OFF SWITCH
         MVC   3(3,R10),=C'***'          INSERT HIGHLIGHTER
PUTIT    PRTF  LINE,CC=ASA        WRITE A LINE                  GP15039
         MVC   LINE,LINE-1         CLEAR LINE
BXLE     BXLE  R3,R4,PROCESS       SCAN ENTIRE BLOCK
         B     REPORTD                  READ ANOTHER BLOCK
         SPACE 1
MAJOR    NI    PROFLAGS,255-FGOPIND     TURN OFF INDENTATION
         B     COMMAJ                                           GP15039
INDENT   OI    PROFLAGS,FGOPIND         TURN ON INDENTATION
COMMAJ   MVI   CC,C'0'                  DOUBLE SPACE
         OI    PROFLAGS,FGMAJ           TURN ON SWITCH
         B     NORMAL
         SPACE 2
PGMEXIT  CLOSE (SYSLIB),MF=(E,OCLIST)  CLOSE AND FREE           GP15039
         SERVTERM ,                    CLOSE AND FREE           GP15039
         PGMEXIT COPYRET=(RETCODE,4)   RETURN WITH CODE         GP15039
         SPACE 2
BADNAME  BAL   R14,TITLER                                       GP15039
         PRTDATA MEMNAME,('- NOT FOUND IN SYSLIB',PAD)          GP15039
         B     CTLLOOP                                          GP15039
IOERROR  BAL   R14,TITLER                                       GP15039
         PRTDATA 'I/O ERROR WHILE READING ',MEMNAME             GP15039
         B     CTLLOOP
         SPACE 2
*   SUBROUTINE TO PRINT TITLES                                  GP15039
*
TITLER   STM   R14,R1,DB     SAVE A BIT                         GP15039
         PRTL  ' ',TITLE=4   CLEAR SUBCOMMAND                   GP15039
         TM    PROFLAGS,FGALI     ALIAS ?                       GP15039
         BZ    TITLEM                    NO                     GP15039
         PRTLIST TITLE3A,TITLE=3  EJECT AND NEW HEADER          GP15039
         B     TITLEX                                           GP15039
TITLEM   PRTLIST TITLE3,TITLE=3   EJECT AND NEW HEADER          GP15039
TITLEX   LM    R14,R1,DB     RESTORE                            GP15039
         BR    R14             AND RETURN                       GP15039
         SPACE 1
BLDLERR  BAL   R14,TITLER    BLDL ERROR                         GP15039
         PRTL  ' ERROR - UNABLE TO BLDL/FIND MEMBER',CC=ASA     GP15039
         SERVTERM ,                    CLOSE AND FREE           GP15039
         ABEND 2222,DUMP     BLDL ERROR
OPENERR  PRTL  ' UNABLE TO OPEN SYSLIB - TERMINATING'           GP15039
         SERVTERM ,                                             GP15039
         ABEND 1111,DUMP     CAN'T OPEN
         SPACE 1
TITLE1   FDOPT NL,CC=C'#'    DATE/TIME/PAGE#                    GP15039
         FDPRT ' ',LEN=10                                       GP15039
         FDPRT 'LISTING OF HELP DATA SET',PADR                  GP15039
         FDPRT JFCBDSNM,PAD,DEB                                 GP15039
         FDPRT 'ON'                                             GP15039
         FDPRT JFCBVOLS,6,PADL                                  GP15039
         FD    *END                                             GP15039
         SPACE 1
TITLE3   FDPRT ' ',,NL,LEN=10                                   GP15039
         FDPRT DASHES                                           GP15039
         FDPRT MEMNAME,DEB,PAD                                  GP15039
         FDPRT DASHES                                           GP15039
         FD    *END                                             GP15039
         SPACE 1
TITLE3A  FDPRT ' ',NL,LEN=10                                    GP15039
         FDPRT DASHES                                           GP15039
         FDPRT MEMNAME,DEB,PAD                                  GP15039
         FDPRT ' (ALIAS)',PAD                                   GP15039
         FDPRT DASHES                                           GP15039
         FD    *END                                             GP15039
TITLE4   FDOPT NL,CC=C'-'         DEFERRED PAGE SUBTITLE        GP15039
         FDPRT ' ',LEN=38                                       GP15039
         FDPRT 1(R3),71,DEB                                     GP15039
         FD    *END                                             GP15039
         SPACE 2
         LTORG ,                                                GP15039
SYSIN    INPWORK SYSIN,EDIT=128,EODAD=SYNEOF                    GP15039
SYSPDS   INPWORK SYSLIB,SYSUT,EDIT=128,EODAD=PGMEXIT,                  *
               PDS=(MEM,ALI,UPD)                                GP15039
SYSTERM  PRTWORK SYSTERM,SYSTSPRT,TITLE=3                       GP15039
SYSPRINT PRTWORK SYSPRINT,SYSUT2,TITLE=5                        GP15039
         SPACE
SYSLIB   DCB   DSORG=PO,MACRF=R,EODAD=CTLLOOP,DDNAME=SYSLIB,           *
               EXLST=LIBXLIST                                   GP15039
LIBXLIST DC    0A(0),X'87',AL3(*-*)                             GP15039
DASHES   DC    0CL27'-',27C'-'    SUBCOMMAND HIGHLIGHTER        GP15039
         SPACE 1
TREQTAB  DC    256AL1(*-TREQTAB)  NO CHANGES                    GP15039
         TRENT TREQTAB,C'/',C'='  SUBCOMMAND CHANGE = TO /      GP15039
         SPACE 2
SAVE     DSECT ,             DYNAMIC SAVE AND WORK AREA         GP15039
DB       DS    2D                                               GP15039
         SERVDEFS ,          SERVICE ROUTINE DATA               GP15039
OCLIST   OPEN  (SYSLIB),MF=L                                    GP15039
#SYSIN   DS    F             NUMBER OF CONTROL CARDS READ       GP15039
BLDLLIST DC    0F'0',H'1,72'  1/2                               GP15039
REC      DC    CL80'0'        2/2                               GP15039
MEMNAME  EQU   REC,8,C'C'     3/2                               GP15039
         ORG   REC                                              GP15039
         IHAPDS PDSBLDL=YES,DSECT=NO                            GP15039
         ORG   ,                                                GP15039
         DC    C'0'
LINE     DS    0CL133
CC       DC    CL1'0'
         DC    CL132'0'
LINEMAJ  EQU   LINE+6
LINEPL   EQU   LINE+8
LINEIND  EQU   LINEPL
PROFLAGS DC    X'0'          PROCESSING FLAGS                   GP15039
FGPDS    EQU   X'80'           READING SYSLIB WITH @INPREAD     GP15039
FGALI    EQU   X'40'           ALIAS FLAG                       GP15039
FGCMDS   EQU   X'20'
FGSUBCM  EQU   X'10'           SUBCOMMAND SEPARATION
FGMAJ    EQU   X'08'           MAJOR SEPARATION
FGOPIND  EQU   X'04'           INDENT TEXT                      GP15039
         SPACE 1
MYJFCB   DS    0D                                               GP15039
         IEFJFCBN ,                                             GP15039
SAVEEND  EQU   *
         SPACE
         PRINT &PRTSYS                                          GP15039
         DCBD  DEVD=DA,DSORG=PO                                 GP15039
         END   ,
