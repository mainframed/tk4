PWMD     TITLE 'TSO PASSWORD MODIFICATION COMMAND PROCESSOR'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*        COPYRIGHT 1986  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*        PSWDMOD (PWMOD) ALLOWS A TSO USER TO CHANGE THE PASSWORD     *
*              FOR ANY ENTRY CONTAINED IN THE FIRST BLOCK OF THE      *
*              USER'S UADS ENTRY.                                     *
*                                                                     *
*        THE USER IS PROMPTED FOR THE OLD PASSWORD (TO MAKE SURE      *
*              SOMEBODY ELSE ISN'T USING AN UNOCCUPIED TERMINAL);     *
*        THEN THE USER IS PROMPTED FOR THE NEW PASSWORD. THE NEW      *
*        PASSWORD MUST BE ALPHAMERIC, AND PASS PDS MEMBER NAME        *
*        RESTRICTIONS (EXCEPT NUMERIC ALLOWED AS FIRST BYTE).         *
*                                                                     *
*        THE @VOLREAD ROUTINE IS USED TO LOCATE AND UPDATE THE ENTRY. *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR
PSWDMOD  SAVEM ZERO12,BASE=R12,PARM=R9,ENTNO=ENTNO,     ADDED ON 86132 *
               ENTRY=(PWMOD,PSWDMOD1,CMDLOCK,CMDLOCK2)           88340
ENT#CMD  EQU   3                                                 88340
ENT#MOD1 EQU   2             ENTRY NUMBER FOR UNVERIFIED PSWDMOD 88340
         LA    R10,DYNWORK   POINT TO COMMON PORTION             88255
         USING DYNWORK,R10     OF DYNAMIC STORAGE                88255
         DROP  R13           DON'T NEED THIS                     88255
         L     R11,=A(RENTWORK)  SHARED WORK AREA                88255
         USING RENTWORK,R11  DECLARE IT                          88255
         SERVINIT ,          LOAD AND INITIALIZE @SERVICE
         SPACE 1                                                 88150
*        PREPARE TSO SPECIFIC FUNCTIONS                          88150
*                                                                88150
         USING CPPL,R9       DECLARE MAPPING                     88150
         TM    CPPLCBUF,X'80'   IS THIS A COMMAND PROCESSOR?     88150
         BZ    PROBCP        MAYBE                               88255
         WTERM ' This program must be invoked as a Command Processor'
         ABEND 106                                               88255
PROBCP   LOAD  EP=IKJGETL    GET THE GETLINE LPA ADDRESS         88150
         ST    R0,@GETLINE   SAVE IT                             88150
         LOAD  EP=IKJPUTL    GET THE PUTLINE LPA ADDRESS         88150
         ST    R0,@PUTLINE   SAVE IT                             88150
         LOAD  EP=IKJPTGT    LOAD PUTGET                         88255
         ST    R0,@PUTGET                                        88255
         L     R1,CPPLECT    GET THE ECT ADDRESS                 88150
         L     R8,CPPLCBUF   GET THE COMMAND BUFFER ADDRESS      88150
         ST    R1,MYIOPL+IOPLECT-IOPL  INTO THE IOPL             88150
         LA    R1,MYIOPECB   GET DUMMY ECB                       88150
         ST    R1,MYIOPL+IOPLECB-IOPL  DITTO                     88150
         L     R2,CPPLUPT    GET TO UPT                          88150
         ST    R2,MYIOPL+IOPLUPT-IOPL                            88150
         TCLEARQ INPUT       FLUSH TERMINAL INPUT BUFFERS        88256
         DROP  R9                                                88255
         SPACE 1                                                 88255
         TESTAUTH FCTN=1     CALLER AUTHORIZED ?
         BXLE  R15,R15,HAVEAUTH  YES
         OI    FLAGS,FGAUTH   SET UNAUTHORIZED CALLER
         SERVICE  APFON      AND AUTHORIZE HERE
HAVEAUTH SERVCALL LPALD,=CL8'@VOLREAD'  GET THE VOLUME READER
         ST    R0,@VOLREAD
         SPACE 1
         SERVCALL LPALD,=CL8'@PRINTER'
         ST    R0,@PRINTER
         MVC   DSNAME,PATDSN   COMPLETE UADS DSN
         AIF   ('&LOCAL' NE 'CCSI').LOCINIT                      88255
         XC    A$SVCRB(A$SLENRB),A$SVCRB  CLEAR PARM AREA        88255
         MVC   A$SID,=C'A$RB'  IDENTIFY THE BLOCK                88255
         MVC   A$SFCTN(3),=AL1(A$SFLOUD,A$STSO,A$SBUILD+A$SDEBUG)
         LA    R0,PUTRDW     GET RETURN MESSAGE                  88255
         ST    R0,A$SERMSG   SET FOR MESSAGE PROCESSING          88255
         L     R2,PSAAOLD-PSA  GET OUR ASCB                      88255
         ICM   R2,15,ASCBTSB-ASCB(R2)  GET TSB                   88255
         BZ    HAVELOUD      NONE ?                              88255
         USING TSB,R2                                            88255
         MODESET KEY=ZERO    ENABLE TSB READ                     88255
         MVC   A$SACCT,TSBTRMID  COPY THE TERMINAL ID            88255
         MODESET KEY=NZERO                                       88255
         DROP  R2                                                88255
         LA    R1,A$SVCRB    GET THE PARAMETER LIST ADDRESS      88255
         SVC   249           CALL THE SVC                        88255
         BXLE  R15,R15,HAVELOUD  OK; AS EXPECTED                 88255
         SLR   R0,R0                                             88255
         ICM   R0,OOII,PUTRDW    ANY MESSAGE RETURNED ?          88255
         SH    R0,=H'4'      ADJUST MESSAGE LENGTH               88255
         BNP   HAVELOUD      NO MESSAGE                          88256
         WTERM PUTBUFF,(R0) ISSUE MESSAGE AND PROCEED            88255
HAVELOUD MVC   @LOUD,A$SBUFR COPY ADDRESS OF DATA AREA           88255
.LOCINIT SPACE 1
         PRTOPEN SYSPRINT,OPT=(NOWTO,DUMMY)  OPEN PRINT FILE
         PRTOPEN USER,OPT=(DUMMY,NOWTO),DEV=2
         CLI   ENTNO,ENT#CMD  PSWDMOD OR CMDLOCK ENTRY ?         88340
         BNL   PRELOCK       CMDLOCK                             88340
         SUBCALL PSWMOD      GO TO PASSWORD MODIFY               88255
         STH   R15,RETCH     SET RETURN                          88255
         SPACE 1
ENDJOB   BAL   R9,TERMINAT
         LH    R9,RETCH      GET RETURN CODE
         SERVTERM ,
         ENDM  RC=(R9)       QUIT WITH RETURN CODE 0 OR 4
         SPACE 2
ABENDJOB BAL   R9,TERMINAT   CLEAN-UP
         PRTF  '0*** Unable to process UADS or member ***',DEV=(1,2)
         PRTCLOSE DEV=(1,2)
         ABEND 213,DUMP      TAKE A DUMP
         SPACE 1
TERMINAT TM    FLAGS,FGAUTH   WAS USER AUTHORIZED AT START ?
         BZR   R9            YES; RETURN
         NI    FLAGS,255-FGAUTH                                  88255
         SERVICE  APFOF      ELSE RESET IT
         BR    R9            AND RETURN
         TITLE 'TSO PASSWORD MODIFICATION COMMAND PROCESSOR - CMDLOCK'
***********************************************************************
*                                                                     *
*        CMDLOCK ALLOWS AN INSTALLATION TO LOCK A USER TO A SINGLE    *
*              APPLICATION. THE SESSION PROCEDURE (OR THE COMMAND     *
*              SLOT IN THE LOCAL A$USER ENTRY) MAY SPECIFY AN INITIAL *
*              COMMAND OF THE FORM :                                  *
*        CMDLOCK CMD TEXT                                             *
*                                                                     *
*        CMDLOCK STACKS THE COMMAND "CMD TEXT" ON THE COMMAND STACK,  *
*        AND ADDITIONALLY STACKS ANOTHER COMMAND REPLICATING THE      *
*        ENTRY PARM, BUT USING THE SECONDARY ENTRY POINT CMDLOCK2.    *
*        CMDLOCK2 PROMPTS THE USER FOR A) LOGOFF B) CMD RE-EXEC       *
*        C) PSWDMOD (IF AUTHORIZED). B) RE-EXECUTES THE CMDLOCK CODE. *
*        UNDER TSO/E 2 AND UP, THE COMMAND IS EXECUTED DIRECTLY USING
*        THE SERVICE FACILITY. NOTE THAT OUR OLD CVT HAS CVTTSVT ZAPPED
*        IN, AND MACRO IKJTSVT WAS COPIED FROM 1.3.6 TO 1.3.3    88340
*                                                                     *
***********************************************************************
         SPACE 1                                                 88255
PRELOCK  L     R7,CVTPTR     GET THE CVT                         88340
         ICM   R7,15,CVTTVT-CVTMAP(R7)  LOAD AND TEST THE TSVT   88340
         BZ    PRESTACK      OLD SYSTEM; FAKE IT                 88340
         ICM   R7,15,TSVTASF-TSVT(R15)  TEST FOR SERVICE ROUTINE 88340
         BNZ   CMDEXEC       HAVE IT - EXECUTE COMMAND           88340
PRESTACK STACK DELETE=ALL,MF=(E,MYIOPL),PARM=MYSTACK  CLEAR ?    88256
STACKS   CLI   ENTNO,ENT#CMD  CMDLOCK ENTRY ?                    88340
         BE    STACKER       YES                                 88255
STACKOUT WTERM ' '                                               88255
         WTERM ' '                                               88255
         WTERM '     You are no longer in your application program.'
         WTERM '       To stop processing completely, type BYE'  88255
         WTERM '       To continue, hit ENTER (Carriage RETURN)'
         AIF   ('&LOCAL' NE 'CCSI').NOPW                         88255
         ICM   R4,15,@LOUD   IS THERE A LOUD ?                   88255
         BZ    ASKHELP       NO                                  88255
         USING A$LOUD,R4     DECLARE IT                          88255
         TM    A$UADMIN,A$UAFPSW  PASSWORD MODIFICATION ALLOWED ?
         BZ    ASKHELP       NO                                  88255
         DROP  R4                                                88255
         WTERM '       Enter PASSWORD to change your password'   88255
.NOPW    ANOP  ,                                                 88255
ASKHELP  WTERM '       You may type HELP for more information'   88255
         SUBCALL TTGETC      READ A TEXT RESPONSE                88255
         BXH   R15,R15,GOODBYE  ERROR; LOGOFF                    88255
         LTR   R1,R1         NULL INPUT ?                        88255
         BZ    STACKER       YES; RE-EXECUTE LOCKED PROGRAM      88255
         CLC   =C'PSW',INPUT PASSWORD MOD ?                      88255
         BE    CALLPSW       YES                                 88255
         CLC   =C'PASS',INPUT  PSWMOD ?                          88255
         BE    CALLPSW       YES                                 88255
         CLC   =C'BYE',INPUT  LOGOFF ?                           88255
         BE    GOODBYE       YES                                 88255
         CLC   =C'LOG',INPUT  LOGOFF/LOGON ?                     88255
         BE    GOODBYE                                           88255
         CLC   =C'SIG',INPUT  SIGNOFF ?                          88255
         BE    GOODBYE                                           88255
         CLC   =C'? ',INPUT                                      88255
         BE    SHOWHELP      YES; HELP                           88255
         CLC   =C'HELP',INPUT                                    88255
         BE    SHOWHELP                                          88255
         WTERM ' Unrecognized input - please correct'            88255
         B     STACKS        RESTART                             88255
         SPACE 1                                                 88255
SHOWHELP WTERM ' Your user record has been defined so as to restrict'
         WTERM ' you to a single application program, which is no'
         WTERM ' longer being executed. To continue execution of this'
         WTERM ' program, just hit ENTER (or Carriage RETURN).'  88256
         WTERM ' For computer center assistance, call:'          88255
         WTERM '     (202) 872-5252  Technical support (Mon-Fri 8-6)'
         WTERM '     (202) 872-5236  Operations'                 88255
         WTERM ' '                                               88255
         B     STACKS        RE-DISPLAY                          88255
         SPACE 1                                                 88255
CALLPSW  SUBCALL PSWMOD      MODIFY PASSWORD                     88255
         B     STACKS        RE-DISPLAY                          88255
         SPACE 1                                                 88255
PREBAD   WTERM ' CMDLOCK requires a command'                     88340
         MVI   RETCH+L'RETCH-1,16  SET ERROR CODE                88340
GOODBYE  SUBCALL SYSBYE      STACK A LOGOFF COMMAND              88255
         B     ENDJOB                                            88255
         SPACE 1                                                 88255
STACKER  LTR   R7,R7         SERVICE ROUTINE PRESENT ?           88340
         BZ    STACKIT       NO; DO THE OLD, UNSAFE WAY          88340
CMDEXEC  BAL   R14,BUFFDEB   GET TEXT IN R0,R1                   88340
         B     PREBAD        TOO BAD                             88340
         LA    R14,PUTBUFF   POINT TO NEW COMMAND BUFFER         88340
         LA    R15,L'PUTBUFF   AND SET LENGTH                    88340
         ST    R15,SRCMDLEN  COMMAND LENGTH                      88340
         MVCL  R14,R0        BUILD NEW COMMAND BUFFER            88340
         LA    R15,L'PUTBUFF+4                                   88340
         STH   R15,PUTRDW    SET BUFFER LENGTH                   88340
         STCM  R15,IIOO,PUTRDW+2  CLEAR BUFFER OFFSET            88340
         MVI   SRFLAGS+3,X'05'  EXECUTE COMMAND OR CLIST         88340
         LA    R14,SRFLAGS   POINT TO FLAGS                      88340
         LA    R15,PUTBUFF   POINT TO COMMAND NAME               88340
         LA    R0,SRCMDLEN   POINT TO LENGTH OF COMMAND          88340
         LA    R1,SRRETCOD   POINT TO RETURN CODE                88340
         LA    R2,SRREASON   POINT TO RETURN REASON CODE         88340
         LA    R3,SRABEND    POINT TO ABEND CODE                 88340
         O     R3,=X'80000000'  SET END OF LIST BIT              88340
         STM   R15,R3,TSRPARM  BUILD PARAMETER LIST              88340
         LA    R1,TSRPARM    PASS PARAMETER LIST ADDRESS         88340
         LR    R15,R7                                            88340
         CALL  (15)          INVOKE THE SERVICE ROUTINE          88340
         B     STACKOUT      IGNORE THE RETURN CODE              88340
         SPACE 1                                                 88340
STACKIT  BAL   R14,BUFFDEB   CLEAN AND LOCATE TEXT               88340
         B     PREBAD        TOO BAD                             88340
         SUBCALL CMDSTAK2    STACK RE-INVOKATION                 88255
         SUBCALL CMDSTACK    STACK A COMMAND                     88255
         B     ENDJOB        AND QUIT                            88255
         SPACE 1                                                 88340
*        SUBROUTINE TO SET REGISTER R0 TO ADDRESS AND R1 TO LENGTH
*        OF SIGNIFICANT (UNSCANNED) PORTION OF INPUT.            88340
*        RETURNS TO R14 IF BUFFER IS NULL, 4(R14) OTHERWISE.     88340
BUFFDEB  LH    R1,0(,R8)     GET BUFFER LENGTH                   88340
         SH    R1,=H'4'      LESS LENGTH OF RDW                  88340
         LH    R2,2(,R8)     GET OFFSET PAST SCANNED TEXT        88340
         LA    R15,4(R2,R8)  POINT TO START OF TEXT              88340
         SR    R1,R2         GET UNSCANNED LENGTH                88340
         BNPR  R14           NOTHING SIGNIFICANT                 88340
BUFFDEBL TM    0(R15),255-C' '  NON-BLANK ?                      88340
         BNZ   BUFFDEBR      YES                                 88340
         LA    R15,1(,R15)                                       88340
         BCT   R1,BUFFDEBL   TRY AGAIN                           88340
         BR    R14           NOTHING SIGNIFICANT                 88340
BUFFDEBR LR    R0,R15        SAVE TEXT START                     88340
         LA    R15,0(R1,R15) POINT PAST TEXT                     88340
BUFFDEBN BCTR  R15,0                                             88340
         TM    0(R15),255-C' '  ANYTHING ?                       88340
         BNZ   4(R14)        YES; RETURN TEXT IN R0,R1           88340
         BCT   R1,BUFFDEBN   TRY AGAIN WITH SHORTER LENGTH       88340
         BR    R14           WON'T HAPPEN ?                      88340
         TITLE 'TSO PASSWORD MODIFICATION COMMAND PROCESSOR - PSWMOD'
PSWMOD   SUBENT ,            PASSWORD MODIFY ROUTINE             88255
         MVI   PTCHFLG,0     RESET ALL PROCESSING FLAGS          88255
         EXTRACT DB,FIELDS=TIOT,MF=(E,EXTRICT)  GET THE TIOT
         L     R1,DB         GET THE TIOT
         MVC   UUADUSER,0(R1)  JOBNAME IS USER NAME
         MVC   MEMBER,UUADUSER  COPY
         LA    R14,MEMBER+1
         LA    R15,7
TRAILBLK CLI   0(R14),C' '   TRAILING BLANK ?
         BNE   TRAILUP       NO
         MVI   0(R14),C'0'   MAKE INTO TRAILING ZERO
         B     TRAILDON
TRAILUP  LA    R14,1(,R14)
         BCT   R15,TRAILBLK  FAIL IF DROPS THROUGH ?
         SPACE 1
TRAILDON WTERM ' Specify current password : ',MODE=DARK          88255
         BXH   R15,R15,PWMEXIT8  SKIP IF ERROR ON INPUT          88255
         LTR   R1,R1         ANY INPUT ?
         BZ    PWMEXIT0      NO; SKIP OUT                        88255
         CH    R1,=Y(L'UUADPSWD)  NOT TOO LONG ?                 88255
         BNH   GETNEW                                            88255
         PRTF  'Input too long',CC=NO,DEV=2
         B     TRAILDON
GETNEW   MVC   UUADPSWD,INPUT  SAVE PRESUMED OLD PASSWORD        88255
         B     GETNEWS                                           88256
HELPNPSW WTERM ' Valid passwords are 5 to 8 characters long,'    88256
         WTERM ' must not be all numeric, and may only contain'  88256
         WTERM ' letters and numbers. Do not use names or dates.'
         WTERM ' To cancel this request, use ENTER (Carriage RETURN).'
GETNEWS  WTERM ' Specify new password : ',MODE=DARK              88255
         BXH   R15,R15,GETBDLEN  TOO LONG
         MVC   UUADNEWP,INPUT  COPY                              88255
         LTR   R1,R1         ANY INPUT AT ALL ?
         BZ    PWMEXIT0      NO; REQUEST TO CANCEL               88255
         CH    R1,=Y(L'UUADNEWP)                                 88255
         BH    GETBDLEN      LENGTH TOO HIGH                     88255
         CLC   =C'? ',INPUT                                      88256
         BE    HELPNPSW                                          88256
         CLC   =C'HELP ',INPUT                                   88256
         BE    HELPNPSW                                          88256
         MVC   DB,UUADNEWP   COPY
         CLI   DB,C'0'       FIRST BYTE NUMERIC ?
         BL    *+8           NO
         MVI   DB,C'A'       MAKE ANYTHING
         SERVCALL DSMEM,DB   CHECK AS THOUGH PDS MEMBER NAME
         BXLE  R15,R15,NEWGOOD  ACCEPT IT
GETBDLEN PRTF  'New password invalid or too long',DEV=2,CC=NO
         B     GETNEWS
         SPACE 1
NEWGOOD  CLI   ENTNO,ENT#MOD1  PSWDMOD1 ENTRY ?                  88340
         BE    NEWMATCH      YES; SKIP VERIFICATION              88340
         WTERM ' Redo for verification:',MODE=DARK               88319
         BXH   R15,R15,GETBDLEN  OOPS - MAKE USER DO IT ALL OVER 88319
         CLC   UUADNEWP,INPUT  VERIFY ?                          88319
         BE    NEWMATCH      YES                                 88319
         PRTF  'Password verification failed -',DEV=2,CC=NO      88319
         B     GETNEWS                                           88319
NEWMATCH DS    0H                                                88319
         AIF   ('&LOCAL' NE 'CCSI').MODUADS                      88255
         MVC   A$SUID,UUADUSER  MOVE USER ID                     88255
         ICM   R4,IIII,@LOUD GET LOUD                            88255
         BZ    ALLOWMOD      NONE; ALLOW CHANGE                  88255
         USING A$LOUD,R4     DECLARE IT                          88255
         MVC   UUADUSER,A$UUID  COPY UID (POSSIBLY 8-BYTES)      88255
         TM    A$UADMIN,A$UAFPSW  MAY USER CHANGE OWN PASSWORD ? 88255
         BNZ   ALLOWMOD      YES                                 88255
         WTERM ' Password change not allowed for this User-Id'   88255
         B     PWMEXIT8      AND QUIT                            88255
ALLOWMOD MVC   A$SUID,A$UUID MAY BE 8 BYTES                      88255
         DROP  R4                                                88255
         MVC   A$SPSWD,UUADPSWD  GET OLD PASSWORD                88255
         MVC   A$SFCTN(3),=AL1(A$SFVALD,A$STSO,A$SUKEY+A$SBUILD) 88255
         LA    R0,A$USIZE                                        88255
         STH   R0,A$SBUFL    SET LOUD USER LENGTH                88255
         LA    R1,A$SVCRB    POINT TO PARMS                      88255
         SVC   249           CHECK IT                            88255
         BXLE  R15,R15,OPSWDOK  VALID                            88255
         SLR   R0,R0                                             88255
         ICM   R0,OOII,PUTRDW  ANY MESSAGE ?                     88255
         SH    R0,=H'4'      FIX                                 88255
         BNP   PWMEXIT8                                          88255
         WTERM PUTBUFF,(R0)  WRITE ERROR MESSAGE                 88255
         B     PWMEXIT4                                          88255
OPSWDOK  MVC   A$SFCTN(3),=AL1(A$SFNPSW,A$STSO,A$SUKEY+A$SBUILD) 88255
         MVC   A$SPSWD,UUADNEWP  NEW PASSWORD                    88255
         LA    R1,A$SVCRB                                        88255
         SVC   249           UPDATE IT                           88255
         BXLE  R15,R15,OPSWDONE  WORKED                          88255
         SLR   R0,R0                                             88255
         ICM   R0,OOII,PUTRDW                                    88255
         SH    R0,=H'4'                                          88255
         BNP   PWMEXIT8                                          88255
         WTERM PUTBUFF,(R0)                                      88255
         CLC   =C'DTS780',PUTBUFF  MALFORMED ?                   88256
         BE    HELPNPSW      YES; DISPLAY HELP                   88256
         B     PWMEXIT4                                          88255
OPSWDONE OI    PTCHFLG,PTCHFIO  SHOW UPDATED                     88255
.MODUADS SPACE 1                                                 88255
         SERVCALL LOCAT,DSNAME  LOCATE THE CATALOG ENTRY FOR UADS
         BXH   R15,R15,PWMEXITC  ERROR IF CAN'T GET IT           88255
         SERVCALL ALCVS,VOLSER  ALLOCATE A DD FOR THE VOLUME
         BXH   R15,R15,PWMEXITC  ERROR IF CAN'T GET IT           88255
         VOLREAD OPEN,VOLSER,MODE=UPDAT  OPEN FOR UPDATE
         BXH   R15,R15,PWMEXIT8
         SPACE 1
         VOLREAD SEARCH,DSNAME   LOCATE THE DATASET
         BXH   R15,R15,PWMEXITC                                  88255
         SPACE 1
         VOLREAD FIND,MEMBER     LOCATE THE MEMBER
         BXH   R15,R15,PWMEXITC                                  88255
         SPACE 1
         VOLREAD READ        READ THE BLOCK
         BXH   R15,R15,PWMEXIT8
         LR    R3,R0         COPY BLOCK ADDRESS
         SLR   R4,R4
         IC    R4,5(,R3)     GET KEY LENGTH
         AH    R4,6(,R3)     PLUS DATA LENGTH
         LA    R4,8(,R4)     PLUS COUNT LENGTH
         STM   R3,R4,DB      STASH
         PRTLIST DUMP34
         SPACE 1
         LA    R2,8(,R3)     GET BUFFER ADDRESS
         ICM   R1,15,X'18'(R2)  GET PSWD/DATA/PROC CHAIN HEAD
         BZ    TESTNONE      NONE; SKIP
TESTFORK AR    R1,R2         RELOCATE
         ICM   R5,15,8(R1)   LOAD PASSWORD POINTER
         BZ    NEXTFORK      NONE ?
         AR    R5,R2         RELOCATE
         CLC   UUADPSWD,4(R5)  DOES PASSWORD MATCH ?
         BNE   NEXTFORK      NO
         MVC   4(8,R5),UUADNEWP  REPLACE BY NEW PASSWORD
         B     DOWNTREE      GO DOWN TO GET PROC AND ACCOUNT
NEXTFORK L     R1,0(,R1)     LOAD NEXT PASSWORD ENTRY
         N     R1,=X'7FFFFFFF'  KILL HIGH BIT
         BNZ   TESTFORK      TRY IT
TESTNONE PRTF  '0*** Old password not found in UADS***',DEV=(1,2)
         B     PWMEXIT4                                          88255
DOWNTREE ICM   R1,15,4(R1)   LOAD LINK TO DATA
         BZ    BLDUARIX
         AR    R1,R2         RELOCATE
         LR    R14,R1        SAVE FOR PROC LOAD
         ICM   R1,15,8(R1)   LOAD DATA POINTER
         BZ    BLDUARIP
         AR    R1,R2         RELOCATE
         CLI   X'2C'(R1),8   DTS ACCOUNT LENGTH ?
         BNE   BLDUARIP
         MVC   UUADACCT,X'2C'+1(R1)  MOVE ACCOUNT
BLDUARIP ICM   R1,15,4(R14)  PROC EXTENSION ?
         BZ    BLDUARIX      NO; SKIP
         AR    R1,R2         RELOCATE
         ICM   R1,15,8(R1)   TEST PROC DATA POINTER
         BZ    BLDUARIX      NONE ?
         AR    R1,R2         RELOCATE
         MVC   UUADPROC,4(R1)   MOVE PROCNAME
         MVC   UUADUNIT,16(R1)  MOVE UNIT FIELD
         SPACE 1
         VOLREAD UPDATE,(R3)  WRITE IT BACK
         BXH   R15,R15,PWMEXIT8
         PRTLIST MSGDONE,DEV=(1,2)                               86132
PWMEXIT0 SLR   R15,R15       CLEAR RETURN CODE                   88255
         B     PWMEXCOM      FINISH WITH CC=0                    88255
BLDUARIX PRTF  '0*** I/O or processing error ***',DEV=(1,2)
PWMEXIT4 LA    R15,4         SET RETURN CODE                     88255
         B     PWMEXCOM                                          88255
PWMEXITC TM    PTCHFLG,PTCHFIO  ALREADY CHANGED IN A$ACCT ?      88255
         BNZ   PWMEXIT0      YES; NO UADS SUPPORT AT CCSI        88255
PWMEXIT8 PRTF  '0*** Control block or environment error ***',DEV=(1,2)
         LA    R15,8                                             88255
PWMEXCOM SUBEX RC=(R15)      RETURN WITH CODE IN R15             88255
         TITLE 'TSO PASSWORD MODIFICATION COMMAND PROCESSOR - PTGT'
***********************************************************************
*                                                                     *
*        PUT OUTPUT ON THE TERMINAL (OR BATCH TMP SIMULATION FILE)    *
*        AND READ A RESPONSE (WITH 'DARK' PTBYPS OPTION)              *
*        ADDRESS SUPPLIED IN R1; LENGTH IN R0                         *
*        RESPONSE RETURNED IN R1 (DATA), R0 (LENGTH), R15 (RC)   88255
*        ALSO MOVED AND UPPER-CASED IN 'INPUT'                   88255
*                                                                     *
***********************************************************************
PTGTDARK OI    PTCHFLG,FGTCONT  DARK (PTBYPS) MODE               88255
PTGTPROM SUBENT ,            ENTRY TO PROMPT AND READ RESPONSE   88255
         LR    R5,R0         COPY TEXT LENGTH                    88255
         LR    R4,R1         COPY TEXT START                     88255
         ICM   R1,15,MYPGPB+PGPBIBUF-PGPB  TEST PRIOR BUFFER     88255
         BZ    PTGTT0        NONE                                88255
         LH    R0,0(,R1)     GET BUFFER LENGTH                   88255
         ICM   R0,8,=AL1(1)  SUBPOOL=1                           88255
         FREEMAIN R,LV=(0),A=(1)  FREE THE LINE                  88255
         XC    PGPBIBUF+MYPGPB-PGPB,PGPBIBUF+MYPGPB-PGPB         88255
PTGTT0   LA    R14,PUTBUFF   GET OUTPUT BUFFER                   88255
         LA    R15,L'PUTBUFF  MAXIMUM LENGTH                     88255
         MIN   (R15),(R5)    TRUNCATE IF IT WON'T FIT            88256
         LA    R0,4(,R15)    LENGTH WITH RDW LENGTH              88255
         STH   R0,PUTRDW                                         88255
         MVCL  R14,R4        MOVE TEXT TO OUTPUT                 88255
         LA    R1,PUTRDW     GET RDW ADDRESS                     88255
         LA    R0,1          SET FOR ONE TEXT LINE               88255
         STM   R0,R1,MYOLD   MAKE PARAMETER LIST                 88255
         L     R15,@PUTGET   LOAD THE IKJPTGT SERVICE ROUTINE ADDRESS
         LA    R1,MYIOPL     GET MY LIST ADDRESS                 88255
         LA    R0,MYPGPB     GET THE PUTGET PARAMETER BLOCK      88255
         ST    R0,MYIOPL+IOPLIOPB-IOPL  STASH IT IN IOPL         88255
*DEFER   TM    DCBOFLGS+DEBUG-IHADCB,DCBOFOPN  DEBUGGING ?       88255
*DEFER   BZ    PTGTTWB       NO                                  88255
*DEFER   STM   R15,R1,TEMP   SAVE REGISTERS                      88255
*DEFER   PUT   DEBUG,PUTRDW  WRITE BUFFER (NO EMBELLISHMENTS)    88255
*DEFER   LM    R15,R1,TEMP   RESTORE                             88255
PTGTTWB  TM    PTCHFLG,FGTCONT  PASSWORD-STYLE INPUT ?           88255
         BZ    PTGTTT        NO                                  88255
         PUTGET OUTPUT=(MYOLD,SINGLE,PTBYPS),TERMPUT=ASIS,             *
               TERMGET=EDIT,ENTRY=(15),MF=(E,(1))                88256
         B     PTGTTR        CHECK RETURN CODE                   88255
PTGTTT   PUTGET OUTPUT=(MYOLD,SINGLE,PROMPT),TERMPUT=ASIS,             *
               TERMGET=EDIT,ENTRY=(15),MF=(E,(1))                88256
PTGTTR   CH    R15,=H'4'     NORMAL RETURN ?                     88255
         BH    PTGTTS        NO; HANDLE SPECIAL PROCESSING       88255
         L     R5,MYPGPB+PGPBIBUF-PGPB  GET THE INPUT BUFFER     88255
         LA    R2,4(,R5)     POINT TO THE TEXT                   88255
         LH    R3,0(,R5)     GET THE TEXT LENGTH                 88255
         SH    R3,=H'4'      ALLOW FOR LENGTH OF LENGTH          88255
         BNP   PTGTTM0       RETURN AS IS                        88255
PTGTTB   TM    0(R2),255-C' '    LEADING BLANK OR NULL ?         88255
         BNZ   PTGTTM        NO; DONE                            88255
         LA    R2,1(,R2)     SKIP OVER IT                        88255
         BCT   R3,PTGTTB     AGAIN                               88255
PTGTTM0  SLR   R3,R3         MAKE NON-NEGATIVE                   88255
PTGTTM   ST    R2,@INCOME    SAVE THE TEXT ADDRESS               88255
         L     R15,4(,R13)   GET THE PREVIOUS SAVE AREA          88255
         STM   R2,R3,20(R15)  RETURN ADDRESS/LENGTH IN R0/R1     88255
         LA    R14,INPUT                                         88255
         LA    R15,L'INPUT                                       88255
         ICM   R3,8,BLANKS   BLANK FILL                          88255
         MVCL  R14,R2        MOVE TEXT TO INPUT BUFFER           88255
         TR    INPUT,UPPER   AND MAKE UPPER-CASE                 88255
*DEFER   TM    DEBUG+DCBOFLGS-IHADCB,DCBOFOPN  DEBUG OPEN ?      88255
*DEFER   BZ    PTGTTQ        NO                                  88255
*DEFER   PUT   DEBUG,INPUTRDW  WRITE INPUT LINE                  88255
PTGTTQ   SLR   R15,R15       CLEAR THE RETURN                    88255
         B     PTGTTEX       AND EXIT                            88255
PTGTTS   XC    PGPBIBUF+MYPGPB-PGPB,PGPBIBUF+MYPGPB-PGPB         88255
         CH    R15,=H'12'    END OF INPUT ?                      88255
         BE    PTGTTF        YES                                 88255
         CH    R15,=H'40'    BARRIER ?                           88255
         BNE   PTGTTBD       NO; ERROR                           88255
PTGTTF   LA    R2,=C'BYE '                                       88255
         LA    R3,4          SET FOR END REQUEST                 88255
         OI    FLAGS,FGBYE   SET TO QUIT                         88255
         B     PTGTTM        AND RETURN IT                       88255
PTGTTBD  LA    R15,8         SET MAJOR ERROR                     88255
         OI    FLAGS,FGBYE   SET TO QUIT                         88255
*LATER DO SOMETHING ABOUT ATTENTIONS ?                           88255
PTGTTAB  ABEND 2741,DUMP     MY TERMINAL IS BROKEN               88255
PTGTTEX  NI    PTCHFLG,255-FGTCONT  RESET MODE FLAG              88255
         SUBEX RC=(R15)      RETURN WITH ERROR CODE              88255
         TITLE 'TSO PASSWORD MODIFICATION COMMAND PROCESSOR - TGET'
***********************************************************************
*                                                                     *
*        GET INPUT FROM TERMINAL (OR BATCH TMP SIMULATION FILE)       *
*           ALSO NOTE THAT INPUT MAY BE FROM 'TAKE' FILE              *
*                                                                     *
*        INPUT ADDRESS RETURNED IN R0; LENGTH IN R1                   *
*           LEADING ZEROES AND BLANKS ARE SKIPPED                     *
*        FOR A TEXT MODE CALL (TTGETC), THE INPUT IS ALSO COPIED TO   *
*        'INPUT', CONVERTED TO UPPER CASE AND PADDED WITH BLANKS.     *
*                                                                     *
***********************************************************************
TTGETC   OI    PTCHFLG,FGTTEXT  GET TEXT (COMMAND)               88150
TTGET    SUBENT ,            ENTRY TO GET INPUT BUFFER           88150
         ICM   R1,15,MYGTPB+GTPBIBUF-GTPB  TEST PRIOR BUFFER     88150
         BZ    TTGET0        NONE                                88150
         LH    R0,0(,R1)     GET BUFFER LENGTH                   88150
         ICM   R0,8,=AL1(1)  SUBPOOL=1                           88150
         FREEMAIN R,LV=(0),A=(1)  FREE THE LINE                  88150
         XC    GTPBIBUF+MYGTPB-GTPB,GTPBIBUF+MYGTPB-GTPB         88150
TTGET0   L     R15,@GETLINE  LOAD THE IKJGETL SERVICE ROUTINE ADDRESS
         LA    R1,MYIOPL     GET MY LIST ADDRESS                 88150
         LA    R0,MYGTPB     GET THE GETLINE PARAMETER BLOCK     88150
         ST    R0,MYIOPL+IOPLIOPB-IOPL  STASH IT IN IOPL         88150
         TM    PTCHFLG,FGTTEXT  GETTING TEXT ?                   88150
         BNZ   TTGETT        YES                                 88150
         GETLINE INPUT=PHYSICAL,TERMGET=ASIS,  GET DATA                *
               ENTRY=(15),MF=(E,(1))                             88150
         B     TTGETR        CHECK RETURN CODE                   88150
TTGETT   GETLINE INPUT=ISTACK,TERMGET=EDIT,    GET TEXT                *
               ENTRY=(15),MF=(E,(1))                             88150
TTGETR   CH    R15,=H'4'     NORMAL RETURN ?                     88150
         BH    TTGETS        NO; HANDLE SPECIAL PROCESSING       88150
         L     R5,MYGTPB+GTPBIBUF-GTPB  GET THE INPUT BUFFER     88150
         LA    R2,4(,R5)     POINT TO THE TEXT                   88150
         LH    R3,0(,R5)     GET THE TEXT LENGTH                 88150
         SH    R3,=H'4'      ALLOW FOR LENGTH OF LENGTH          88150
         BNP   TTGETM0       RETURN AS IS                        88150
TTGETB   TM    0(R2),255-C' '    LEADING BLANK OR NULL ?         88150
         BNZ   TTGETM        NO; DONE                            88150
         LA    R2,1(,R2)     SKIP OVER IT                        88150
         BCT   R3,TTGETB     AGAIN                               88150
TTGETM0  SLR   R3,R3         MAKE NON-NEGATIVE                   88150
TTGETM   ST    R2,@INCOME    SAVE THE TEXT ADDRESS               88255
         L     R15,4(,R13)   GET THE PREVIOUS SAVE AREA          88150
         STM   R2,R3,20(R15)  RETURN ADDRESS/LENGTH IN R0/R1     88150
         TM    PTCHFLG,FGTTEXT  TEXT MODE ?                      88150
         BZ    TTGETQ        NO                                  88150
         LA    R14,INPUT                                         88150
         LA    R15,L'INPUT                                       88150
         ICM   R3,8,BLANKS   BLANK FILL                          88150
         MVCL  R14,R2        MOVE TEXT TO INPUT BUFFER           88150
         TR    INPUT,UPPER   AND MAKE UPPER-CASE                 88150
*DEFER   TM    DEBUG+DCBOFLGS-IHADCB,DCBOFOPN  DEBUG OPEN ?      88150
*DEFER   BZ    TTGETQ        NO                                  88150
*DEFER   PUT   DEBUG,INPUTRDW  WRITE INPUT LINE                  88150
TTGETQ   SLR   R15,R15       CLEAR THE RETURN                    88150
         B     TTGETEX       AND EXIT                            88150
TTGETS   XC    GTPBIBUF+MYGTPB-GTPB,GTPBIBUF+MYGTPB-GTPB         88150
         CH    R15,=H'12'    END OF INPUT ?                      88150
         BE    TTGETF        YES                                 88150
         CH    R15,=H'40'    BARRIER ?                           88150
         BNE   TTGETBD       NO; ERROR                           88150
TTGETF   LA    R2,=C'BYE '                                       88255
         LA    R3,4          SET FOR END REQUEST                 88255
         B     TTGETM        AND RETURN IT                       88150
TTGETBD  LA    R15,8         SET MAJOR ERROR                     88150
TTGETEX  NI    PTCHFLG,255-FGTTEXT  RESET TEXT MODE FLAG         88150
         SUBEX RC=(R15)      RETURN WITH ERROR CODE              88150
         TITLE 'TSO PASSWORD MODIFICATION COMMAND PROCESSOR - TPUT'
***********************************************************************
*                                                                     *
*        PUT OUTPUT ON THE TERMINAL (OR BATCH TMP SIMULATION FILE)    *
*                                                                     *
*        ADDRESS SUPPLIED IN R1; LENGTH IN R0                   *
*                                                                     *
***********************************************************************
TPUTCONT OI    PTCHFLG,FGTCONT  CONTROL MODE                     88150
TPUTASIS OI    PTCHFLG,FGTTEXT  ASIS (PROMPT) MODE               88150
TPUTEDIT SUBENT ,            ENTRY TO WRITE LINE TO TERMINAL     88150
         LR    R5,R0         COPY TEXT LENGTH                    88150
         LR    R4,R1         COPY TEXT START                     88150
         LA    R14,PUTBUFF   GET OUTPUT BUFFER                   88150
         LA    R15,L'PUTBUFF  MAXIMUM LENGTH                     88150
         MIN   (R15),(R5)    TRUNCATE IF NECESSARY               88256
         LA    R0,4(,R15)    LENGTH WITH RDW LENGTH              88150
         SPACE 1                                                 88204
TPUTNPC  STH   R0,PUTRDW                                         88150
         MVCL  R14,R4        MOVE TEXT TO OUTPUT                 88150
         L     R15,@PUTLINE  LOAD THE IKJPUTL SERVICE ROUTINE ADDRESS
         LA    R1,MYIOPL     GET MY LIST ADDRESS                 88150
         LA    R0,MYPTPB     GET THE GETLINE PARAMETER BLOCK     88150
         ST    R0,MYIOPL+IOPLIOPB-IOPL  STASH IT IN IOPL         88150
TTPUTW   DS    0H                                                88255
*DEFER   TM    DCBOFLGS+DEBUG-IHADCB,DCBOFOPN  DEBUGGING ?       88150
*DEFER   BZ    TTPUTWB       NO                                  88150
*DEFER   STM   R15,R1,TEMP   SAVE REGISTERS                      88150
*DEFER   PUT   DEBUG,PUTRDW  WRITE BUFFER (NO EMBELLISHMENTS)    88150
*DEFER   LM    R15,R1,TEMP   RESTORE                             88150
TTPUTWB  TM    PTCHFLG,FGTTEXT  DOING TEXT ?                     88150
         BZ    TTPUTT        YES                                 88150
         PUTLINE OUTPUT=(PUTRDW,DATA),TERMPUT=ASIS,                    *
               ENTRY=(15),MF=(E,(1))                             88150
         B     TTPUTR        CHECK RETURN CODE                   88150
TTPUTT   PUTLINE OUTPUT=(PUTRDW,DATA),TERMPUT=EDIT,                    *
               ENTRY=(15),MF=(E,(1))                             88150
TTPUTR   CH    R15,=H'4'     NORMAL RETURN ?                     88150
         BL    TTPUTEX       YES                                 88150
         BH    TTPUTAB       NO; TERMINATE WITH PREJUDICE        88150
*LATER DO SOMETHING ABOUT ATTENTIONS ?                           88150
TTPUTAB  ABEND 2741,DUMP     MY TERMINAL IS BROKEN               88150
TTPUTEX  NI    PTCHFLG,255-FGTTEXT-FGTCONT  RESET MODE FLAGS     88150
         SUBEX RC=(R15)      RETURN WITH ERROR CODE              88150
         TITLE 'TSO PASSWORD MODIFICATION COMMAND PROCESSOR'     88255
***********************************************************************
*                                                                     *
*        BYE COMMAND - REQUEST TSO LOGOFF AND QUIT                    *
*                                                                     *
***********************************************************************
SYSBYE   SUBENT ,                                                88150
         OI    FLAGS,FGBYE   SET LOGOFF REQUEST                  88150
         LA    R1,8          SET LENGTH                          88255
         LA    R0,=CL8'LOGOFF'                                   88255
         AIF   ('&LOCAL' NE 'CCSI').NOSIGN                       88340
         LA    R0,=CL8'SIGNOFF'  FORCE DISCONNECT                88340
.NOSIGN  SUBCALL CMDSTACK    STACK THE LOGOFF                    88255
         AIF   ('&LOCAL' NE 'CCSI').NOBYE                        88255
         ICM   R4,IIII,@LOUD ANY LOUD ?                          88255
         BZ    SYSBLOUD      SKIP IF NONE                        88255
         USING A$LOUD,R4                                         88255
         MODESET KEY=ZERO                                        88255
         OI    A$LFLG2,A$LFLOG  FORCED LOGOFF IN NETSOL          88255
         MODESET KEY=NZERO                                       88255
SYSBLOUD DS    0H                                                88255
         DROP  R4                                                88255
.NOBYE   SUBEX ,                                                 88255
         SPACE 2                                                 88255
***********************************************************************
*                                                                     *
*        CMDSTACK - PLACE A COMMAND ON THE STACK                      *
*              TEXT ADDRESS IN R0; LENGTH IN R1                  88340
*        CMDSTAK2 - PREFIX BY CMDLOCK2                                *
*                                                                     *
***********************************************************************
CMDSTAK2 OI    PTCHFLG,FGTTEXT  SET FLAG FOR CMDLOCK2 PREFIX     88255
CMDSTACK SUBENT ,            COMMAND STACKING ENTRY              88255
         LR    R6,R0         COPY COMMAND ADDRESS                88255
         LR    R7,R1         COPY COMMAND LENGTH                 88255
         LR    R8,R7         PLAIN COMMAND LENGTH                88255
         TM    PTCHFLG,FGTTEXT  PREFIX ?                         88255
         BZ    *+8           NO                                  88255
         LA    R8,9(,R8)     YES; ADD PREFIX LENGTH              88255
         LA    R0,16(,R8)    REQUIRED GETMAIN LENGTH             88255
         ICM   R0,IOOO,=AL1(78)  SET COMMAND SUBPOOL             88255
         GETMAIN R,LV=(0)    GET TMP STACK SPACE                 88255
         LA    R2,16(,R1)    COMMAND ADDRESS                     88150
         LR    R4,R2         FIRST COMMAND                       88150
         LR    R3,R8         RECLEN / TOTAL LENGTH               88255
         SLR   R5,R5                                             88150
         STM   R2,R5,0(R1)   COMPLETE LSD                        88150
         STH   R8,4(,R1)     COMPLETE RECLEN / TOTAL LENGTH      88255
*        NOTE THAT R2/R3, R6/R7 SET FOR MVCL                     88255
         TM    PTCHFLG,FGTTEXT  COMMAND NAME PREFIX ?            88255
         BZ    CMDSNPFX      NO                                  88255
         MVC   0(9,R2),=CL9'CMDLOCK2'  SET COMMAND PREFIX + BLANK
         LA    R2,9(,R2)     UP DESTINATION                      88255
         SH    R3,=H'9'      DECREASE LENGTH                     88255
CMDSNPFX MVCL  R2,R6         MOVE COMMAND TEXT                   88255
         LR    R2,R1         COPY LSD ADDRESS                    88150
         LA    R0,MYSTACK    POINT TO MY STACK WORK AREA         88150
         ST    R0,IOPLIOPB-IOPL+MYIOPL  PLACE IN LIST            88150
         STACK STORAGE=((R2),SOURCE),MF=(E,MYIOPL)               88150
         BXLE  R15,R15,CMDSEX  EXIT IF ACCEPTED                  88255
         WTERM ' Command stacking failed'                        88255
         ABEND 34,,STEP                                          88255
CMDSEX   NI    PTCHFLG,255-FGTTEXT                               88255
         SUBEX ,             RETURN TO CALLER                    88150
         SPACE 1
RENTWORK DS    0D            SHARED WORK AREA                    88255
SYSPRINT PRTWORK BUGPRINT,TITLE=3
USER     PRTWORK *USER       TPUT OR WTO
DUMP34   FDSNAP (R3),DUAL,ABS,OFFSET,BASE=8(R3),LEN=0(R4)
         FDPRT *END
MSGDONE  FDPRT 'Password updated for user',PAD,NL
         FDPRT UUADUSER,PAD
         FDCLI UUADACCT,0,BE=MSGDONA
         FDPRT 'Account',PAD
         FDPRT UUADACCT
MSGDONA  FDCLI UUADPROC,0,BE=MSGDONP
         FDPRT 'PROC',PAD
         FDPRT UUADPROC
MSGDONP  FDCLI UUADUNIT,0,BE=MSGDONX
         FDPRT 'Unit',PAD
         FDPRT UUADUNIT
MSGDONX  FDPRT *END
PATDSN   DC    CL44'SYS1.UADS '   DATASET NAME
BLANKS   EQU   *-8,8,C'C'    USE TRAILING BLANKS AS CONSTANT
         SPACE 1                                                 85142
*                                                                88255
* TABLE TO TRANSLATE TO UPPER CASE                               88255
*                                                                88255
UPPER    DC    C' ',255AL1(*-UPPER)                              88150
         ORG   UPPER+X'81'                                       88255
         DC    C'ABCDEFGHI'                                      88255
         ORG   UPPER+X'91'                                       88255
         DC    C'JKLMNOPQR'                                      88255
         ORG   UPPER+X'A2'                                       88255
         DC    C'STUVWXYZ'                                       88255
         ORG   ,                                                 88255
         SPACE 1                                                 88150
         IKJIOPL ,           DEFINE LISTS FOR TSO SERVICES       88150
IOPLSIZE EQU   *-IOPL                                            88150
         SPACE 1                                                 88150
         IKJSTPB ,                                               88150
STPBSIZE EQU   *-STPB                                            88150
         SPACE 1                                                 88150
         IKJGTPB ,                                               88150
GTPBSIZE EQU   *-GTPB                                            88150
         SPACE 1                                                 88150
         IKJPTPB ,                                               88150
PTPBSIZE EQU   *-PTPB                                            88150
         SPACE 1                                                 88255
         IKJPGPB ,                                               88255
PGPBSIZE EQU   *-PGPB                                            88255
         SPACE 1
SAVE     DSECT ,
         DS    18F           FIRST LEVEL OF SUBROUTINE CALL      88255
         DS    18F           SECOND LEVEL                        88255
         DS    18F           THIRD LEVEL                         88255
         DS    18F           FOURTH LEVEL (SHOULD NOT NEED?)     88255
DYNWORK  DS    0D            MUTUALLY ADDRESSABLE WORK AREA      88255
DB       DS    D
TEMP     DS    4D            WORK SPACE                          88255
@SERVICE DS    A
@VOLREAD DS    A
@PRINTER DS    A
MEMBER   DS    CL8
UUADACCT DS    CL8
UUADUNIT DS    CL8
UUADPROC DS    CL8
UUADUSER DS    CL8
UUADPSWD DS    CL8           OLD PASSWORD
UUADNEWP DS    CL8           NEW PASSWORD
         SPACE 1
DSNAME   DS    CL44   1/4    UADS DSN
         DS    XL4    2/4    VOLTYPE
VOLSER   DS    CL6    3/4    UADS VOLSER
         DS    XL4,CL6,CL8   LOCAT RETURN/WORK
RETCH    DC    H'0'          RETURN CODE
FLAGS    DS    X             PROCESSING FLAG
FGAUTH   EQU   X'80'           CALLER IS NOT AUTHORIZED (ALREADY)
FGBYE    EQU   X'01'           USER WISHES TO QUIT               88255
PTCHFLG  DC    X'00'         PROCESSING FLAG                     88150
PTCHFIO  EQU   X'80'           RECORD WRITTEN                    88150
PTCHFCR  EQU   X'40'           CARRIAGE RETURN FOUND             88150
PTCHFLF  EQU   X'20'           LINE-FEED FOUND                   88150
FGTTEXT  EQU   X'02'           NEXT TGET/TPUT IS TEXT MODE       88150
FGTCONT  EQU   X'01'           NEXT TPUT IS IN CONTROL MODE      88150
EXTRICT  EXTRACT DB,FIELDS=TIOT,MF=L
MYIOPL   DC    (IOPLSIZE/4)F'0'  IOPL EXPANSION                  88150
MYIOPECB DC    F'0'          IOPL ECB                            88150
MYSTACK  STACK MF=L          STACK SERVICE                       88150
MYGTPB   GETLINE MF=L        GETLINE SERVICE                     88150
MYPTPB   PUTLINE MF=L        PUTLINE SERVICE                     88150
MYPGPB   PUTGET MF=L         PUTGET SERVICE                      88255
@INCOME  DC    A(0)          ADDRESS OF GETLINE'S RETURN BUFFER(+4)
@GETLINE DC    A(0)          ADDRESS OF GETLINE ROUTINE          88150
@PUTLINE DC    A(0)          ADDRESS OF PUTLINE ROUTINE          88150
@PUTGET  DC    A(0)          ADDRESS OF PUTGET ROUTINE           88255
SRFLAGS  DC    A(0)          TSO SERVICE ROUTINE FLAGS           88340
SRCMDLEN DC    A(0)          TSO SERVICE ROUTINE COMMAND LENGTH  88340
SRRETCOD DC    A(0)          TSO SERVICE ROUTINE RETURN CODE     88340
SRREASON DC    A(0)          TSO SERVICE ROUTINE REASON CODE     88340
SRABEND  DC    A(0)          TSO SERVICE ROUTINE ABEND CODE      88340
MYOLD    DC    A(1,PUTRDW,0) SINGLE-LEVEL OLD                    88255
PUTRDW   DC    Y(L'PUTBUFF,0)                                    88255
PUTBUFF  DC    CL132' '      OUTPUT LINE                         88255
INPUTRDW DC    Y(80+4+5,0),C'CMD: '  1/3  INPUT BUFFER PREFIX    88150
INPUT    DC    CL80' '  2/3  COMMAND INPUT BUFFER                88150
         ORG   INPUT                                             88340
TSRPARM  DS    7A            TSO SERVICE ROUTINE PARAMETER LIST  88340
         ORG   ,                                                 88340
ENTNO    DC    X'00'         # OF ENTRY POINT (MAIN = 0)         88255
         AIF   ('&LOCAL' NE 'CCSI').NOSVRB                       88255
A$SVCRB  A$SVCRB DSECT=NO                                        88255
@LOUD    DS    F             ADDRESS OF LOUD                     88255
.NOSVRB  SPACE 1                                                 88255
SAVEND   EQU   *
         SPACE 1
         SPACE 1                                                 88255
         IKJCPPL ,                                               88255
         IKJPSCB ,                                               84246
         IKJUPT ,                                                88255
         IKJTSB ,                                                88255
         IKJTSVT ,           COPIED FROM 1.3.6                   88340
         CVT   DSECT=YES                                         88340
         IHACDE ,
         IHAASCB ,                                               88255
         IHAPSA ,                                                88255
         AIF   ('&LOCAL' NE 'CCSI').NOLOUD                       88255
A$LOUD   A$LOUD ,                                                88255
.NOLOUD  END   ,
