DEDT     TITLE 'D A T E E D I T  ***  EDIT CANNED DATES'
         COPY  OPTIONGB                                          85253
         SPACE 1                                                 85253
         SYSPARM LIST=YES                                        85253
         SPACE 2                                                 85253
***********************************************************************
*                                                                     *
*        COPYRIGHT 1982, 1985  EXPERT SYSTEM PROGRAMMING, INC.        *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR                                           85253
DATEEDIT PGMHEAD ZERO15,BASE=R12,PARM=R9                ADDED ON 80121
         SPACE 1
*        THIS IS A QUICKY ROUTINE TO ADJUST CANNED DATES IN JCL
*        (PARTICULARLY FOR JSI CONTROL CARDS)
*        MASKS LISTED IN 'TABLE' ARE REPLACED.
         SPACE 1
         SERVINIT ,          LOAD THE SERVICE ROUTINE            85253
         SERVCALL LPALD,=CL8'@PRINTER'  LOAD THE PRINT ROUTINE   85253
         ST    R0,@PRINTER                                       85253
         SERVCALL LPALD,=CL8'@INPREAD'  GET THE INPUT READER     85253
         ST    R0,@INPREAD                                       85253
         SPACE 1
         BAL   R9,CURPDATE   GET CURRENT DATE
         BAL   R9,BDATE      CONVERT IT
         MVC   DAYCUR,MMDDYY  SAVE EBCDIC FORM
         BAL   R9,ADJDAY     GET DAY EARLIER
         BAL   R9,BDATE      CONVERT IT
         MVC   DAYPRE,MMDDYY  SAVE EBCDIC EQUIVALENT
         L     R5,DAYWEEK    GET DAY OF WEEK
         CH    R5,=H'2'      MONDAY ?
         BH    DOBUSDAY      NO
         BL    DOBUSTS       NO; TEST MORE
         BAL   R9,ADJDAY     FINAGLE
         B     DOBUSSUN      TOTAL OF THREE BUMPS
DOBUSTS  CH    R5,=H'1'      WEEKEND DAY ?
         BH    DOBUSDAY      NO
         BL    DOBUSSAT      SATURDAY - BUMP BACK ONE
DOBUSSUN BAL   R9,ADJDAY     BUMP ONE DAY FOR SUNDAY
DOBUSSAT BAL   R9,ADJDAY     BUMP ONE FOR SATURDAY
DOBUSDAY BAL   R9,BDATE      GET EBCDIC VALUE
         MVC   DAYBUS,MMDDYY  SAVE LAST BUSINESS DAY
         BAL   R9,CURPDATE   GET CURRENT DATE AGAIN
         LA    R1,1          MAKE FIRST DAY
         L     R5,MONLEN     SAVE LENGTH OF THIS MONTH
         ST    R1,DAY        GET FIRST DAY OF MONTH
         BAL   R9,BDATE       GET EBCDIC
         MVC   MONCUR,MMDDYY  SAVE FIRST DAY OF MONTH
         ST    R5,DAY
         BAL   R9,BDATE
         MVC   MONCURL,MMDDYY  GET LAST DAY OF CURRENT MONTH
         BAL   R9,ADJMON     GET ONE MONTH EARLIER
         LA    R1,1
         ST    R1,DAY
         BAL   R9,BDATE
         MVC   MONPRE,MMDDYY  GET PRIOR MONTH'S FIRST DAY
         MVC   DAY,MONPLEN   SET FOR LAST DAY OF PRIOR MONTH
         BAL   R9,BDATE
         MVC   MONPREL,MMDDYY   SAVE IT
         SPACE 1
         INPOPEN SYSUT1,OPT=ABEND  BOMB IF NO SYSUT1/SYSIN       85253
         PRTOPEN SYSUT2,OPT=ABEND  DITTO IF NO OUTPUT FILE       85253
         SPACE 2
LOOP     INPGET ,            GET A RECORD                        85253
         BXH   R15,R15,IOABEND  FAIL ON ERROR                    85253
         LR    R7,R1         COPY CARD ADDRESS                   85253
         LR    R8,R0         COPY CARD LENGTH                    85253
LOOPCOM  SLR   R9,R9         SET START SCAN OFFSET
         BCTR  R9,0          START WITH COLUMN 0
         LR    R5,R7         COPY START ADDRESS                  85253
NEXTRT   LA    R9,1(,R9)     GO TO NEXT COLUMN
         LA    R14,0(R5,R9)  GET NEXT COMPARE COLUMN
         LR    R15,R8        LOAD LINE LENGTH                    85253
         SH    R15,=H'6'     ALLOW FOR LENGTH OF FIELDS          85253
         SR    R15,R9        NO. OF COLUMNS TO SCAN-1
         BM    PUTPUT        DONE WITH THIS CARD
         LR    R1,R14        JUST IN CASE
         EX    R15,SCANTRT   LOOK FOR SIGNIFICANT CHARACTER
         BZ    PUTPUT        NONE; NEXT CARD
         LR    R9,R1         GET HIT COLUMN
         SR    R9,R5         GET EQUIVALENT OFFSET
         LA    R14,TABLE     GET TABLE START ADDRESS
         USING TABSECT,R14   DECLARE TABLE ENTRY
         XR    R2,R2
NEXTAB   CLI   TABLEN,X'FF'  ALL DONE ?
         BE    NEXTRT        TRY NEXT COLUMN
         IC    R2,TABLEN     GET ENTRY LENGTH - 1
         EX    R2,SCANCLC    DO CLC
         BE    EDITPUT       MATCH - PUT IT OUT
NEXTCOL  LA    R14,TABTEXT-TABSECT+1(R14,R2)  BUMP TO NEXT TABLE ENTRY
         B     NEXTAB
SCANCLC  CLC   0(0,R1),TABTEXT-TABSECT(R14)
SCANTRT  TRT   0(0,R14),TRTTAB  TEST ON FIRST CHARACTER
         SPACE 1                                                 85254
IOABEND  ABEND 001,DUMP      TAKE A HIKE                         85254
         SPACE 1                                                 85254
TRTTAB   DC    256X'0'       TRT TABLE
         ORG   TRTTAB+C'#'
         DC    C'#'
         ORG   ,
         SPACE 1
TABLE    DC    X'07',AL2(CURDAY-DATEEDIT),C'##/##/##'  CURRENT DATE
         DC    X'07',AL2(CURDAY-DATEEDIT),C'##/#D/##'  CURRENT DATE
         DC    X'07',AL2(PREDAY-DATEEDIT),C'##/#P/##'  PREVIOUS DAY
         DC    X'07',AL2(BUSDAY-DATEEDIT),C'##/#B/##'  PRIOR WEEKDAY
         DC    X'07',AL2(CURMON-DATEEDIT),C'#M/##/##'  CURRENT MONTH
         DC    X'07',AL2(CURMONL-DATEEDIT),C'#M/31/##'  LAST DAY, CUR
         DC    X'07',AL2(PREMON-DATEEDIT),C'#P/##/##'  PRIOR MONTH
         DC    X'07',AL2(PREMONL-DATEEDIT),C'#P/31/##'  LAST DAY, PRIOR
         DC    X'FF'         END OF TABLE
         SPACE 1
TABSECT  DSECT ,
TABLEN   DS    X             LENGTH OF TEXT-1
TABADR   DS    XL2           OFFSET FROM BASE TO PROCESSING CODE
TABTEXT  DS    C             VARIABLE LENGTH TEXT
DATEEDIT CSECT ,             RESUME
         SPACE 1
EDITPUT  XR    R15,R15
         ICM   R15,3,TABADR   GET OFFSET TO PROCESSING ROUTINE
         DROP  R14
         B     0(R15,R12)    BRANCH TO IT
         SPACE 1
CURDAY   LA    R15,DAYCUR
         B     EDITCOM
PREDAY   LA    R15,DAYPRE
         B     EDITCOM
BUSDAY   LA    R15,DAYBUS
         B     EDITCOM
CURMONL  LA    R15,MONCURL
         B     EDITCOM
PREMONL  LA    R15,MONPREL
EDITCOM  EX    R2,EDITMVC
         B     NEXTRT
CURMON   LA    R15,MONCUR
         B     EDITTWO
PREMON   LA    R15,MONPRE
EDITTWO  EX    R2,EDITMVC
         LA    R1,1(R1,R2)  SPACE
         LA    R15,1(R15,R2)
         CLC   TABTEXT-TABSECT+TABLE(8),0(R1)  SECOND FIELD ?
         BE    EDITCOM       YES; MOVE SECOND FIELD ALSO
         B     NEXTRT        ELSE TRT AGAIN
EDITMVC  MVC   0(0,R1),0(R15)  MOVE REPLACEMENT DATE
         SPACE 1
PUTPUT   PRTF  (R7),(R8),CC=NO   'PUNCH' THE CARD IMAGE          85253
         B     LOOP          GET ANOTHER CARD
         SPACE 1
EXIT     SERVTERM ,          CLOSE AND FREE EVERYTHING           85253
         PGMEXIT RC=0        EXIT WITH CODE 0                    85253
         SPACE 3
*        DATE CONVERSION ROUTINES
*
* CURPDATE - RETURNS BINARY MONTH/DAY/YEAR/WEEKDAY OF CURRENT DATE
* PDATE - DITTO FOR ANY PACKED DATE SAVED IN DB+5(YYDDDF)
*
* BDATE - CONVERTS BINARY MONTH/DAY/YEAR TO PACKED (IN YYDDD) AND
*          EBCDIC (MMDDYY)
         SPACE 1
CURPDATE TIME  BIN           GET DATE
         ST    R1,DB+4       STASH DATE
         SPACE 1
PDATE    L     R1,DB+4       GET USER'S PACKED DATE
         LR    R3,R1         SAVE DATE FOR LATER
         XR    R0,R0         INSERT CLEAR
         IC    R0,DB+5       INSERT YEAR
         SRDL  R0,4          PUT DECADES IN R0
         SRL   R1,28         AND UNITS IN R1
         SLL   R0,1          DECADES*2
         AR    R1,R0         ADD IN
         SLL   R0,2          DECADES*2*4
         AR    R1,R0         = UNITS + (2+8)*DECADES
         ST    R1,YEAR       SAVE YEAR
         XC    DB(6),DB   CLEAR YEAR
         LA    R1,6(,R1)     ADD FINAGLE FACTOR
         LA    R2,1(,R1)     SAVE YEAR + SECOND FINAGLE
         SRA   R2,2          GET NUMBER OF LEAP YEARS SINCE 1900
         MH    R1,=H'365'    DAYS SINCE 1900
         AR    R1,R2         ADD LEAP DAYS
         CVB   R0,DB   GET DAYS
         AR    R1,R0         ADD DAY IN THIS YEAR
         XR    R0,R0
         D     R0,=F'7'      GET DAY OF WEEK
         ST    R0,DAYWEEK    SAVE BINARY DAY OF WEEK
         MH    R0,=AL2(SUNDAY-SATURDAY)  MAKE OFFSET INTO TABLE
         LA    R1,SATURDAY   GET TABLE OF DAYS
         AR    R1,R0         GET TODAY'S ENTRY
         MVC   WEEKDAY(SUNDAY-SATURDAY),0(R1)  SAVE IT
         ST    R3,DB+4       STASH DATE AGAIN
         TM    DB+5,X'01'    ODD YEAR ?
         BNZ   NOTLEAP       YES; NOT A LEAP YEAR
         TM    DB+5,X'12'    MULTIPLE OF FOUR ?
         BM    NOTLEAP       NO; NOT LEAP YEAR (FOR A WHILE)
         MVI   MONFEB+1,29      MAKE LONG FEBRUARY
NOTLEAP  XC    DB(6),DB      KILL YEAR
         CVB   R0,DB         GET DAY AGAIN
         XR    R1,R1
         LA    R14,MONTAB
         LA    R15,12
MONTHLOP CH    R0,0(,R14)    IN THIS MONTH ?
         BNH   MONTHDON      YES
         SH    R0,0(,R14)
         LA    R14,2(,R14)
         BCT   R15,MONTHLOP
         LA    R14,MONDEC
MONTHDON LH    R15,0(,R14)   GET DAYS IN THIS MONTH
         ST    R0,DAY        SAVE DAY IN THIS MONTH
         ST    R15,MONLEN    SAVE LENGTH OF THIS MONTH
         LA    R15,MONTAB-2
         SR    R14,R15
         SRL   R14,1         GET NUMBER OF MONTH
         ST    R14,MONTH
         BCT   R14,*+8       GET PRIOR MONTH
         LA    R14,12        OR GET PRIOR YEAR
         SLL   R14,1
         LH    R14,MONTAB-2(R14)  GET LENGTH OF PRIOR MONTH
         ST    R14,MONPLEN   SAVE IT
         BR    R9            RETURN TO CALLER
         SPACE 2
BDATE    LM    R14,R0,MONTH  LOAD MONTH/DAY/YEAR
         MH    R0,=H'10'     SCALE
         CVD   R0,DB
         MVC   YYDDD(2),DB+5  MAKE 00YY
         MVI   MONFEB+1,28   RESET FEBRUARY DAYS
         TM    YYDDD+1,1     ODD YEAR ?
         BNZ   BDATENL       YES; NOT LEAP
         TM    YYDDD+1,X'12'  MULTIPLE OF 4 ?                    85253
         BM    BDATENL       NO; NOT LEAP                        85253
         MVI   MONFEB+1,29   SET LEAP
BDATENL  LA    R1,MONTAB     GET FIRST MONTH
         SLL   R14,1         GET CURRENT MONTH'S OFFSET
         LA    R14,MONTAB-2(R14)  STOP MONTH
BDATENA  CR    R14,R1        TIME TO STOP ?
         BNL   BDATENN       YES
         AH    R15,0(,R14)   ADD PRIOR MONTH'S DAYS
         LA    R14,2(,R14)
         B     BDATENA       DO MORE
BDATENN  CVD   R15,DB        CONVERT DAYS
         OI    DB+7,X'0F'    BE POSITIVELY POSITIVE
         MVC   YYDDD+2(2),DB+6  SET DAYS
         LM    R14,R0,MONTH  GET MONTH/DAY/YEAR BACK
         MH    R14,=H'100'   SCALE
         AR    R14,R15       ADD DAYS
         MH    R14,=H'100'
         AR    R14,R0        ADD YEAR
         CVD   R14,DB
         MVC   MMMDDYY,=X'F0212020612020612020'  EDIT MASK
         ED    MMMDDYY,DB+4  MAKE MM/DD/YY
         BR    R9            RETURN
         SPACE 2
*        FINAGLE ROUTINES
*
* ADJDAY - CHANGE MONTH/DAY/YEAR TO PRIOR DAY
* ADJMON - CHANGE TO PRIOR MONTH
         SPACE 1
ADJDAY   LM    R14,R0,MONTH
         BCT   R15,ADJOUT    GO TO STASH IF NON-ZERO
         L     R15,MONPLEN   SET TO LAST DAY OF PRIOR MONTH
         B     ADJMON1       DECREMENT MONTH ALSO
         SPACE 1
ADJMON   LM    R14,R0,MONTH  LOAD VALUES
ADJMON1  BCT   R14,ADJOUT    DECREASE MONTH BY ONE
         LA    R14,12        RESET TO DECEMBER
         BCTR  R0,0          DECREMENT YEAR ALSO
ADJOUT   STM   R14,R0,MONTH  SAVE ADJUSTED VALUES
         BR    R9            RETURN TO CALLER
         SPACE 1                                                 85253
SYSUT2   PUNWORK SYSUT2,SYSOUT                                   85253
SYSUT1   INPWORK SYSUT1,SYSIN,WIDTH=0,EDIT=128,EODAD=EXIT        85253
         SPACE 1
SATURDAY DC    X'07',CL9'SATURDAY'
SUNDAY   DC    X'05',CL9'SUNDAY'
         DC    X'05',CL9'MONDAY'
         DC    X'06',CL9'TUESDAY'
         DC    X'08',CL9'WEDNESDAY'
         DC    X'07',CL9'THURSDAY'
         DC    X'05',CL9'FRIDAY'
WEEKDAY  DC    X'07',CL9'SATURDAY'  SET TO CURRENT DAY
MONTAB   DC    H'31'         DAYS PER MONTH
MONFEB   DC    H'28'         SET TO 28 OR 29
         DC    H'31,30,31,30,31,31,30,31,30'
MONDEC   DC    H'31'         DECEMBER
         SPACE 2                                                 85253
SAVE     DSECT ,             WORK/SAVE AREA                      85253
DB       DS    D             WORK SPACE                          85253
@SERVICE DS    A             SERVICE ROUTINE                     85253
@PRINTER DS    A             PRINTER SERVICES                    85253
@INPREAD DS    A             INPUT SERVICES                      85253
DAYWEEK  DC    F'0'          DAY OF WEEK (0-SAT)
MONTH    DC    F'0'          MONTH (1-12)
DAY      DC    F'0'          DAY OF MONTH
YEAR     DC    F'0'          YEAR (00-99)
MONLEN   DC    F'0'          LENGTH OF CURRENT MONTH
MONPLEN  DC    F'0'          LENGTH OF PREVIOUS MONTH
DAYCUR   DC    CL8' '        CURRENT DATE (EBCDIC MM/DD/YY)
DAYPRE   DC    CL8' '        PREVIOUS DAY
DAYBUS   DC    CL8' '        LAST WEEKDAY
MONCUR   DC    CL8' '        FIRST DAY OF THIS MONTH
MONCURL  DC    CL8' '        LAST DAY OF THIS MONTH
MONPRE   DC    CL8' '        FIRST DAY OF LAST MONTH
MONPREL  DC    CL8' '        LAST DAY OF PRIOR MONTH
YYDDD    DC    PL4'0'        JULIAN YEAR/DAY 00YYDDDF
MMMDDYY  DC    0CL10' ',CL2' '  EDIT PREFIX
MMDDYY   DC    C'MM/DD/YY'   EBCDIC DAY
SAVEEND  EQU   *             END OF SAVE AREA                    85253
         SPACE 2                                                 85253
         PRINT &PRTSYS                                           85253
CVTDSECT DSECT ,                                                 85253
         CVT   DSECT=YES                                         85253
         IHACDE ,                                                85253
         END   ,
