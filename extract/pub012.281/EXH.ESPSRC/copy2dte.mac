CP2DTE   TITLE 'C O P Y 2 D T E  ***  SOURCE (IEBCOPY) TO IEBUPDTE'
         COPY  OPTIONGB                                          92135
         SPACE 1                                                 92135
         SYSPARM LIST=YES    SET OPTIONS                         92135
         EJECT
***********************************************************************
**                                                                   **
**   COPY2DTE READS A SOURCE FORMAT PDS UNLOADED BY IEBCOPY, AND     **
**   PRODUCES A FILE FOR INPUT TO IEBUDPTE OR PDSLOAD (BETTER)       **
**                                                                   **
**   INPUT  DD SYSUT1 OR TAPE                                        **
**   OUTPUT DD SYSUT2 OR SYSPUNCH                                    **
**                                                                   **
**-------------------------------------------------------------------**
**   MAINTENANCE:                                                    **
**  2012-06-12  GYP  UPDATED FOR zOS v1.13 IEBCOPY; OPT. DD NAMES    **
**                   ./ ADD FORMAT COPIED FROM LISTPDS FOR SPF STATS **
**                   (CARDS FLAGGED .SPF.)                           **
**                   PARM=CTL(XX) GENERATES XX ADD CARDS             **
**                       = UPD(YY) REPLACES LEADING ./ IN DATA BY YY **
**                                                                   **
***********************************************************************
         SPACE 1
         PRINT &PRTSOR                                           92135
COPY2DTE PGMHEAD ZERO31,BASE=(R12),BNDRY=PAGE,PARM=R9           GP12164
         LA    R14,PDEONE    FIRST MEMBER ENTRY                 GP12164
         LA    R15,L'PDEONE  ENTRY SIZE                         GP12164
         LR    R0,R14                                           GP12164
         A     R0,=A(PDELAST-PDEONE)   LAST ENTRY               GP12164
         STM   R14,R0,PDEBXLE                                   GP12164
         LA    R0,MYJ        INPUT JFCB                         GP12164
         STCM  R0,7,EXLIST+1  INTO EXIT LIST                    GP12164
         MVI   PRTLEVEL,1    PRINT ADD CARDS, BUT NOT DATA      GP12164
         MVC   USERCC,=C'./'      SET DEFAULT CONTROL BYTES     GP12164
         MVC   SAVECC,=C'./'      SET DEFAULT CONTROL BYTES     GP12164
         SPACE 1
         SERVINIT ,          LOAD @SERVICE ROUTINE               92135
         SERVLOAD @PRINTER,@PARSER,SUBVERB                      GP12164
         PRTOPEN SYSPRINT,OPT=(DUMMY)  OPEN SYSPRINT             92135
         LR    R2,R15        SAVE RETURN CODE                   GP12164
         PRTOPEN SYSPUNCH,OPT=(DUMMY),DEV=2 SYSPUNCH            GP12164
         CH    R15,=H'4'     SYSPRINT AVAILABLE ?                92135
         BNH   PARMLOOK      YES                                GP12164
         CH    R2,=H'4'      OR SYSPUNCH ?                      GP12164
         BNH   PARMLOOK      YES                                GP12164
         ABEND 1403          SIMPLE(MINDED) DIAGNOSTIC
         SPACE 1
PARMLOOK PARMLOAD R8,PTR=R9       GET PARM= FIELD               GP12164
         LTR   R9,R9         ANY PARMS?                         GP12164
         BNP   PARMDONE                                         GP12164
         OI    PAR$RQFG,PAR$PARK  TREAT () AS KEYWORD           GP12164
         PARSE (R8),(R9),OPT=KEYWORD  IGNORE BLANKS AND COMMAS  GP12164
         BXH   R15,R15,DONEPARM   NOTHING FOUND                 GP12164
         LA    R3,PAR@TABL   TOP OF RESULTS                     GP12164
         USING PRSDSECT,R3   DECLARE RESULT MAPPING             GP12164
LOOPPARM ICM   R3,15,PRSLINK      ANY TEXT FOUND ?              GP12164
         BZ    DONEPARM             NO; GET OUT                 GP12164
         TM    PRSTYPE,PRSPFKEY   USER SPECIFIED KEYWORD?       GP12164
         BNZ   KEYWPARM             YES; CONTINUE               GP12164
         OC    PRS$TEXT,BLANKS    MAKE UPPER CASE               GP12164
         PARFGSET FLAGSETP,ERR=FLAGBAD,DONE=LOOPPARM            GP12164
         SPACE 1                                                GP12164
KEYWPARM LA    R0,PRSKEYWD   POINT TO INPUT                     GP12164
         OC    PRSKEYWD,BLANKS    MAKE UPPER CASE               GP12164
         LA    R1,KEYWSETP   POINT TO TABLE                     GP12164
         L     R2,PRS#TEXT   TEXT LENGTH                        GP12164
         SUBCALL SUBVERB     PERFORM LOOKUP                     GP12164
         BZ    KEYWBAD         NO MATCH; FAIL IT                GP12164
         BR    R1            INVOKE SUBROUTINE                  GP12164
         SPACE 1                                                GP12164
SETCTL   CH    R2,=H'2'      REPLACE ./ WITH ???                GP12164
         BNE   KEYWLEN       NOT TOO LARGE ?                    GP12164
         MVC   USERCC,PRS$TEXT   ./ REPLACEMENT TEXT            GP12164
         B     LOOPPARM      DO MORE                            GP12164
         SPACE 1                                                GP12164
SETUPD   CH    R2,=H'2'      REPLACE ./ WITH ???                GP12164
         BNE   KEYWLEN       NOT TOO LARGE ?                    GP12164
         MVC   SAVECC,PRS$TEXT   ./ REPLACEMENT TEXT            GP12164
         B     LOOPPARM      DO MORE                            GP12164
         SPACE 1                                                GP12164
SETDEBUG TM    PRSFLAGS,PRSFGINT  INTEGER VALUE ?               GP12164
         BZ    KEYWINT              NO; FAIL                    GP12164
         LM    R0,R1,PRS#TINT     LOAD VALUE                    GP12164
         LTR   R0,R0         NOT TOO LARGE ?                    GP12164
         BM    KEYWINT                                          GP12164
         CL    R1,=F'9'      NOT TOO LARGE?                     GP12164
         BH    KEYWINT         TOO BAD                          GP12164
         STC   R1,PRTLEVEL   SAVE IT                            GP12164
         B     LOOPPARM      DO MORE                            GP12164
         SPACE 1                                                GP12164
         PARKEYBX FLAGSET    BXLE                               GP12164
FLAGSET  PARKEYFG 'PRINT',PRTLEVEL,0,1           PRINT FINAL MEMBERS
FLAGSET2 PARKEYFG 'NOPRINT',PRTLEVEL,0,0         NO PRINT       GP12164
         PARKEYFG 'ABEND',OPTFLAGS,0,FGDUMP      DUMP ON ERROR  GP12164
         PARKEYFG 'DUMP',OPTFLAGS,0,FGDUMP       DUMP ON ERROR  GP12164
         PARKEYFG 'TEST',OPTFLAGS,0,FGDUMP       DUMP ON ERROR  GP12164
FLAGSETN PARKEYFG 'DEBUG',PRTLEVEL,0,4           MODERATE DEBUG MODE
         SPACE 1                                                GP12164
         PARKEYBX KEYWSET    BXLE                               GP12164
KEYWSET  PARKEYAD 'CONTROL',SETCTL               SET ./ REPL    GP12164
KEYWSET2 PARKEYAD 'CTL',SETCTL                   SET ./ REPL    GP12164
         PARKEYAD 'UPDTE',SETUPD                 REPLACE ./     GP12164
         PARKEYAD 'UPDATE',SETUPD                REPLACE ./     GP12164
         PARKEYAD 'SMP',SETUPD                   REPLACE ./     GP12164
         PARKEYAD 'PRTLEVEL',SETDEBUG            SET DEBUG LEVELGP12164
         PARKEYAD 'DEBLEV',SETDEBUG              SET DEBUG LEVELGP12164
         PARKEYAD 'PRTLVL',SETDEBUG              SET DEBUG LEVELGP12164
         PARKEYAD 'PRTLEV',SETDEBUG              SET DEBUG LEVELGP12164
         PARKEYAD 'PRINTLVL',SETDEBUG            SET DEBUG LEVELGP12164
         PARKEYAD 'PRINTLEV',SETDEBUG            SET DEBUG LEVELGP12164
KEYWSETN PARKEYFG 'DEBUG',SETDEBUG               SET DEBUG LEVELGP12164
         SPACE 1                                                GP12164
FLAGBAD  PRTDATA 'Unknown PARM option',(PRS$TEXT,DEB,PADL)      GP12164
         B     EXIT16                                           GP12164
KEYWBAD  PRTDATA 'Unknown PARM keyword',(PRSKEYWD,DEB,PADL)     GP12164
         B     EXIT16                                           GP12164
KEYWLEN  PRTDATA (PRSKEYWD,DEB,PAD),'has invalid length',(PRS$TEXT,DEB,*
               PADL)                                            GP12164
         B     EXIT16                                           GP12164
KEYWINT  PRTDATA (PRSKEYWD,DEB,PAD),'has invalid value',(PRS$TEXT,DEB,P*
               ADL)                                             GP12164
         B     EXIT16                                           GP12164
         DROP  R3                                               GP12164
         SPACE 1                                                GP12164
PARMFD   FDPRT 'PARM options:',NL                               GP12164
         FDFLAG OPTFLAGS,PAD,DEB,TABLE=OPTTAB,SPACE=2,SEP=C',' LEN=80
         FDCLI PRTLEVEL,1,BL=PARMFDDN,BE=PARMFDD                GP12164
         FDPRT '  Print/Debug level='                           GP12164
         FDPRT PRTLEVEL,I,DEB,PADR                              GP12164
         FDGOTO PARMFDDS                                        GP12164
PARMFDDN FDPRT '  No member listing'                            GP12164
         FDGOTO PARMFDDS                                        GP12164
PARMFDD  FDPRT '  Print member list'                            GP12164
PARMFDDS FDPRT 'Input ./ replaced by',NL                        GP12164
         FDPRT SAVECC,PAD                                       GP12164
         FDPRT 'Control cards generated with',NL                GP12164
         FDPRT USERCC,PAD                                       GP12164
         FD    *END                                             GP12164
         SPACE 1                                                GP12164
OPTTAB   FLGTAB FGDUMP,'Dump on error'                          GP12164
         FLGTAB *END                                            GP12164
         SPACE 1                                                GP12164
DONEPARM PARSE OPT=CLOSE     RELEASE STORAGE                    GP12164
PARMDONE DEVTYPE TAPE+DCBDDNAM-IHADCB,DB    LOOK FOR DD         GP12164
         BXLE  R15,R15,TESTTAPE                                 GP12164
         MVC   DCBDDNAM-IHADCB+TAPE,=CL8'SYSUT1' ALTERNATE INP. GP12164
         DEVTYPE TAPE+DCBDDNAM-IHADCB,DB    LOOK FOR DD         GP12164
         BXLE  R15,R15,TESTTAPE                                 GP12164
         PRTDATA 'Neither TAPE nor SYSUT1 DD supplied'          GP12164
EXIT16   MVICC 16                                               GP12164
         TM    OPTFLAGS,FGDUMP    DUMP ON ERROR ?               GP12164
         BZ    EODAD                NO                          GP12164
         PRTCLOSE ,          CLOSE PRINTER TO FORCE OUT LAST MESSAGE
         ABEND 16,DUMP       DUMP                               GP12164
         SPACE 1
TESTTAPE TM    DB+2,UCB3TAPE+UCB3DACC  TAPE OR DISK INPUT ?     GP12164
         BM    CALLOPEN        YES; USE IT                      GP12164
       PRTDATA (DCBDDNAM-IHADCB+TAPE,8,PAD),'DD neither DASD nor tape'
         B     EXIT16                                           GP12164
         SPACE 1
CALLOPEN OPEN  MF=(E,INPOC)                                     GP12164
         RDJFCB MF=(E,INPOC) GET JFCB                           GP12164
         PRTLIST HEADER,TITLE=1                                  92135
         PRTDATA 'Input',(JFCBDSNM,DEB,PAD),'on',(JFCBVOLS,6,PAD),     *
               TITLE=2                                          GP12164
         PRTLIST PARMFD      DISPLAY OPTIONS                    GP12164
         GET   TAPE
         USING INREC,R1      DECLARE INPUT RECORD
         CLC   =X'CA6D0F',UNLID   IEBCOPY FOOTPRINT ?           GP12164
         BE    REALCOPY
         PRTDATA 'Input is not a recognized IEBCOPY unloaded file.'
         B     EXIT16
         SPACE 1
REALCOPY MVC   TRKPCYL,UTRKPCYL  SAVE TRACKS PER CYLINDER
         SPACE 1
         GET   TAPE
         MVC   NMEXT+1(1),UNMEXT  SAVE NUMBER OF EXTENTS
         MVC   EXTENTS(16*16),UEXTENTS
         LM    R7,R9,PDEBXLE   GET PDS SAVE AREA POINTERS
         SPACE 1
LOOPPDEG GET   TAPE          GET A PDS RECORD
         LH    R0,RDW        GET BLOCK LENGTH
         LA    R1,4(R1,0)    SKIP RDW
         SH    R0,*-2        DECREMENT RESIDUAL LENGTH
         BNP   LOOPPDEG      SKIP NULL RECORD
         USING UWORD,R1
LOOPPDEH STM   R0,R1,CURBLOCK  SAVE CURRENT BLOCK ADDRESS
         CLC   ZEROES(5),UCOUNT  PDS RECORD ?
         BNE   DONEPDER      NO
         CLC   =X'080100',UCKLEN    PDS KEY/LENGTH ?
         BNE   DONEPDER      NO
         TM    PROFLAGS,FGPEOD  LOGICAL END-FILE IN DIRECTORY ?
         BNZ   LOOPPDER      YES; SKIP THIS BLOCK
         LA    R3,UPDSLEN
         LH    R5,UPDSLEN    GET LENGTH OF ENTRY
         LA    R5,UPDSLEN-1(R5)  LAST BYTE
         LA    R4,2          LENGTH OF FIRST SEGMENT
LOOPPDEM BXH   R3,R4,LOOPPDER  DONE; GET NEXT BLOCK
         CLC   =X'FFFFFFFFFFFFFFFF00000000',0(R3)  LAST BLOCK ?
         BNE   LOOPPDEN      NO
         OI    PROFLAGS,FGPEOD  SET LOGICAL END-FILE
LOOPPDER LM    R0,R1,CURBLOCK  GET BLOCK POINTER
         XR    R2,R2
         ICM   R2,3,UDATLEN  GET BLOCK LENGTH
         LA    R2,UFIXLEN+8(,R2)  ADD OVERHEAD
         AR    R1,R2
         SR    R0,R2
         BP    LOOPPDEH      DO NEXT BLOCK
         B     LOOPPDEG      GET ANOTHER BLOCK
         USING PDS2,R3       POINT TO INPUT DIRECTORY ENTRY     GP12164
LOOPPDEN SR    R14,R14                                          GP12164
         IC    R14,PDS2INDC  GET FLAGS AND LENGTH               GP12164
         N     R14,=A(PDS2LUSR)   MASK LENGTH ONLY              GP12164
         LR    R4,R14             SAVE LENGTH                   GP12164
         AR    R14,R14            HALFWORDS TO LENGTH           GP12164
         C     R14,=A(2*SPFUDLEN)   POSSIBLE SPF ENTRY ?        GP12164
         BE    SAVEPDE              YES; KEEP INTACT            GP12164
         MIN   R14,=F'4'          ONLY CARE ABOUT SPF           GP12164
SAVEPDE  MIN   R14,=A(2*SPFUDLEN)   ONLY WHAT'S NEEDED          GP12164
         LA    R14,PDS2USRD-PDS2-1(R14)  LENGTH FOR EXECUTE     GP12164
         EX    R14,EXMVCPDE       SAVE IT                       GP12164
         DROP  R3                                               GP12164
         USING PDS2,R7            POINT TO SAVED ENTRY          GP12164
         TM    PDS2INDC,X'1E'     ANY SSI ?
         BZ    LOOPPDEX             NO
         CLI   PDS2USRD,X'FF'     POSSIBLE IEBUPDTX ENTRY ?     GP12164
         BNE   LOOPPDEX      NO
         OI    PDS2USRD+6,X'0F'   FORCE SIGN                    GP12164
         ZAP   PDS2USRD(4),PDS2USRD+4(3)  SHIFT OVER            GP12164
LOOPPDEX LA    R4,PDS2USRD-PDS2(R4,R4)    ADD OVERHEAD          GP12164
         BXLE  R7,R8,LOOPPDEM  DO ANOTHER MEMBER
         PRTV  MEMOFLOW      MEMBER TABLE OVERFLOW               92135
         B     EXIT16        CLOSE WITH ERROR                   GP12164
EXMVCPDE MVC   0(0,R7),0(R3)      COPY ENTRY                    GP12164
         DROP  R7                                               GP12164
         SPACE 1
DONEPDER SR    R7,R8         GET LAST MEMBER ENTRY
         ST    R7,PDEBXLE+8  UPDATE IT
         OI    PROFLAGS,FGPEOD  SET END-FILE FLAG
         LM    R3,R5,PDEBXLE   GET DIRECTORY POINTERS
         LR    R2,R3         COPY START ADDRESS
         CR    R2,R5         ANY ENTRIES ?
         BH    NOMEMBRS      NONE - EXIT WITH ERROR
         BE    GETDATA       ONLY ONE - SKIP SORT
         ST    R3,SOPLOW     FIRST ENTRY                        GP12164
         ST    R5,SOPHIGH    LAST ENTRY                         GP12164
         OI    SOPHIGH,X'80'   INDICATE ADDRESS, NOT COUNT      GP12164
         MVI   SOPCOO,PDS2TTRP-PDS2    KEY OFFSET               GP12164
         MVI   SOPCOL,L'PDS2TTRP+L'PDS2INDC   KEY LENGTH        GP12164
         STC   R4,SOPLEN     ENTRY LENGTH                       GP12164
         LR    R5,R1         SAVE CURRENT RECORD ADDRESS        GP12164
         SERVCALL SORTH,SOPPARM                                 GP12164
         LR    R1,R5         RESTORE CURRENT RECORD ADDRESS     GP12164
         B     GETDATA2      USE CURRENT BLOCK
         SPACE 1
GETDATA  GET   TAPE          GET NEXT BLOCK
         USING INREC,R1      DECLARE INPUT RECORD
GETDATA2 LH    R0,RDW        GET BLOCK LENGTH
         LA    R1,4(R1,0)    SKIP RDW
         SH    R0,*-2        DECREMENT RESIDUAL LENGTH
         BNP   GETDATA       SKIP NULL RECORD
SETBLOCK STM   R0,R1,CURBLOCK  SAVE CURRENT BLOCK ADDRESS
         USING UWORD,R1
         XR    R2,R2
         ICM   R2,3,UDATLEN    GET BLOCK DATA LENGTH
         BNZ   DATBLOCK      PROCESS DATA
         OI    PROFLAGS,FGPEOD  SET END-FILE ON
         B     NEWBLOCK      DO NEXT BLOCK
DATBLOCK LA    R3,UBLOCK
         LA    R5,UBLOCK-1(R2)  GET LAST BYTE
         LA    R4,80         SET CARD LENGTH
         STM   R3,R5,BLOKBXLE  SET BLOCK POINTERS
         TM    PROFLAGS,FGPEOD  PRIOR END-FILE ?
         BZ    DEBLOCK       NO; RETURN NEXT RECORD
         LM    R7,R9,PDEBXLE      LOOK FOR ALIAS ENTRY          GP12164
         ICM   R0,7,TTR      FIRST TIME ?                       GP12164
         BZ    NEWMEMB         YES; BYPASS ALIAS CHECK          GP12164
         USING PDS2,R7                                          GP12164
LOOPALI  CLC   TTR,PDS2TTRP       SAME TTR AS ADD CARD?         GP12164
         BL    NEWMEMB              NO; PROCESS NEXT            GP12164
         TM    PDS2INDC,PDS2ALIS  ALIAS ENTRY ?                 GP12164
         BZ    BUMPALI              NO; SKIP                    GP12164
         LR    R6,R1         SAVE BLOCK POINTER                 GP12164
         MVC   CONCARD,BLANKS     CLEAR ADD CARD                GP12164
         MVC   CCCON,USERCC       INSERT ./ OR WHAT-NOT         GP12164
         MVC   CCADD(11),=C'ALIAS NAME='   VERB AND NAME        GP12164
         MVC   CCADD+11(8),PDS2NAME     ALIAS NAME              GP12164
         CLI   PDS2INDC,PDS2ALIS+2     SSI PRESENT ?            GP12164
         BL    NOALISSI                  NO; SKIP               GP12164
         CLI   PDS2INDC,PDS2ALIS+PDS2LUSR   FULL DATA ?         GP12164
         BH    NOALISSI                  NO                     GP12164
         LA    R14,PDS2USRD  SSI COMPRISES FIRST FOUR BYTES     GP12164
         CLI   PDS2INDC,PDS2ALIS+SPFUDLEN  SPF DIRECTORY ?      GP12164
         BNE   *+8             NO                               GP12164
         LA    R14,SPFMODDT  IN LIEU OF WHOLE, USE MOD DATE     GP12164
         TRT   CCADD+11(9),TRTBLANK    FIND END OF NAME         GP12164
         MVC   0(5,R1),=C',SSI='                                GP12164
         UNPK  5(9,R1),0(5,R14)   PROCESS SSI OR DATE           GP12164
         TR    5(8,R1),HEXTRTAB                                 GP12164
         MVI   13(R1),C' '                                      GP12164
NOALISSI CLI   PRTLEVEL,1         PRINT CARD?                   GP12164
         BL    PUTALIAS             NO; PUNCH ONLY              GP12164
         PRTF  CONCARD,CC=NO      PRINT CARD                    GP12164
PUTALIAS PRTF  CONCARD,CC=NO,DEV=2     PUNCH IT                 GP12164
         LR    R1,R6         RESTORE BLOCK POINTER              GP12164
BUMPALI  BXLE  R7,R8,LOOPALI      LOOK FOR ANOTHER              GP12164
         SPACE
NEWMEMB  BAL   R9,ABSTTR     CONVERT ABSOLUTE TO TTR
         LM    R7,R9,PDEBXLE  GET MEMBER POINTERS
         USING PDS2,R7                                          GP12164
GETBLMEM CLC   TTR,PDS2TTRP  SAME TTR ?                         GP12164
         BE    MAKEADD       YES; MAKE AN ADD CARD
         BXLE  R7,R8,GETBLMEM  ELSE TRY AGAIN
MISSMEM  PRTDATA 'No member found for TTR',(TTR,HEX,PAD)        GP12164
         B     EXIT16        EXIT WITH ERROR                    GP12164
MAKEADD  ST    R7,PDEBXLE    SAVE ADDRESS OF LAST MEMBER USED
         ZI    PDS2INDC,PDS2ALIS  RESET (WIDOWED) ALIAS         GP12164
         MVC   CONCARD,BLANKS     CLEAR ADD CARD                GP12164
         MVC   CCCON,USERCC       INSERT ./ OR WHAT-NOT         GP12164
         MVC   CCADD,=C'ADD NAME='     VERB AND NAME            GP12164
         MVC   CCNAM,PDS2NAME                                   GP12164
         TRT   CCNAM(L'CCNAM+1),TRTBLANK     FIND FIRST BLANK   GP12164
         LR    R14,R1             SAVE FOR POSSIBLE SSI=        GP12164
         TM    PDS2INDC,PDS2LUSR-1   SSI ?  (LEN>3)             GP12164
         BZ    PUTCARDM
         TM    PDS2INDC,PDS2NTTR  EXTRA TTRS ?                  GP12164
         BNZ   JUSTSSI              YES; CRUD                   GP12164
*---------------------------------------------------------------------*
*    IN ORDER FOR SPF STATISTICS TO BE STORED, THE './ ADD'           *
*    STATEMENT MUST LOOK LIKE THIS:                                   *
*                                                                     *
*       COL     DESCRIPTION                                           *
*       1-20    ./ ADD NAME=XXXXXXXX                                  *
*       21      BLANK                                                 *
*       22-71   VVMM-YYDDD-YYDDD-HHMM-NNNNN-NNNNN-NNNNN-UUUUUUUUUU    *
*               VER CREATE LASTMODIFY  SIZE  INIT   MOD   ID          *
*---------------------------------------------------------------------*
         IC    R15,PDS2INDC                                     GP12164
         N     R15,=A(PDS2LUSR)                                 GP12164
         AR    R15,R15            DATA LENGTH                   GP12164
         C     R15,=A(2*SPFUDLEN)  ARE SPF STATISTICS PRESENT   GP12164
         BNE   JUSTSSI             NO, BRANCH                     .SPF.
         LA    R15,CCSPF          SPF OUTPUT START              GP12164
         TM    SPFCRTDT+L'SPFCRTDT-1,X'0F'   PLUS IN DATE ?     GP09130
         BNO   JUSTSSI             NO; INVALID DATE             GP09130
         TM    SPFMODDT+L'SPFMODDT-1,X'0F'   PLUS IN DATE ?     GP09130
         BNO   JUSTSSI             NO; INVALID DATE             GP09130
         CLI   SPFVERL,X'99'       VALID VERSION ?              GP09130
         BH    JUSTSSI                                          GP09130
         CLI   SPFMODL,X'99'     VALID RELEASE ?                GP09130
         BH    JUSTSSI                                          GP09130
         CLI   SPFMODTM+0,X'23'  VALID TIME ?                   GP09130
         BH    JUSTSSI             NO; INVALID                  GP09130
         CLI   SPFMODTM+1,X'59'  VALID TIME ?                   GP09130
         BH    JUSTSSI             NO; INVALID                  GP09130
         IC    R0,SPFVERL          GET V OF V.M                   .SPF.
         CVD   R0,DB                                              .SPF.
         OI    DB+7,X'0F'                                         .SPF.
         UNPK  CCSPVER,DB+6(2)                                  GP12164
         IC    R0,SPFMODL        GET M OF V.M                     .SPF.
         CVD   R0,DB                                              .SPF.
         OI    DB+7,X'0F'                                         .SPF.
         UNPK  CCSPMOD,DB+6(2)                                  GP12164
         UNPK  CCSPCDT,SPFCRTDT YYDDD CREATED                   GP12164
         UNPK  CCSPMDT,SPFMODDT YYDDD MODIFIED                  GP12164
*OLD*    MVO   DB(3),SPFMODTM(2) HHMM   LAST MODIFIED             .SPF.
*OLD*    OI    DB+2,X'0F'                                         .SPF.
*OLD*    UNPK  CCSPMTM,DB(3)                                    GP12164
         UNPK  DB(2*L'SPFMODTM+1),SPFMODTM(L'SPFMODTM+1)        GP12164
         MVC   CCSPMTM,DB                                       GP12164
         SR    R0,R0                                            GP12164
         ICM   R0,3,SPFCURLN      CURRENT SIZE                  GP12164
         CVD   R0,DB                                              .SPF.
         UNPK  CCSPCUR,DB+5(3)                                  GP12164
         OI    CCSPCUR+L'CCSPCUR-1,C'0'                         GP12164
         SR    R0,R0                                            GP12164
         ICM   R0,3,SPFINILN      INITIAL SIZE                  GP12164
         CVD   R0,DB                                              .SPF.
         UNPK  CCSPINR,DB+5(3)                                  GP12164
         OI    CCSPINR+L'CCSPINR-1,C'0'                         GP12164
         SR    R0,R0                                            GP12164
         ICM   R0,3,SPFMODLN      RECORDS MODIFIED              GP12164
         CVD   R0,DB                                              .SPF.
         UNPK  CCSPMDR,DB+5(3)                                  GP12164
         OI    CCSPMDR+L'CCSPMDR-1,C'0'                         GP12164
         MVC   CCSPUID,SPFUID     USER ID                       GP12164
         MVI   CCSPCDT-1,C'-'     SEPARATOR                     GP12164
         MVI   CCSPMDT-1,C'-'     SEPARATOR                     GP12164
         MVI   CCSPMTM-1,C'-'     SEPARATOR                     GP12164
         MVI   CCSPCUR-1,C'-'     SEPARATOR                     GP12164
         MVI   CCSPINR-1,C'-'     SEPARATOR                     GP12164
         MVI   CCSPMDR-1,C'-'     SEPARATOR                     GP12164
         MVI   CCSPUID-1,C'-'     SEPARATOR                     GP12164
         B     PUTCARDM                                         GP12164
JUSTSSI  MVC   0(5,R14),=C',SSI='
         UNPK  5(9,R14),PDS2USRD(5)                             GP12164
         TR    5(8,R14),HEXTRTAB
         MVI   13(R14),C' '
PUTCARDM ZI    PROFLAGS,FGPEOD     RESET END-FILE               GP12164
         CLI   PRTLEVEL,1    PRINT ADD CARDS?                   GP12164
         BL    PUTCARDU        NO                               GP12164
         PRTF  CONCARD,CC=NO      PRINT THE ADD CARD            GP12164
PUTCARDU PRTF  CONCARD,CC=NO,DEV=2    PUNCH ADD CARD            GP12164
DEBLOCK  LM    R3,R5,BLOKBXLE  GET BLOCK POINTERS
DEBLOCKC CLC   =C'./',0(R3)       IS CARD A CONTROL CARD ?      GP12164
         BNE   *+4+6                NO                          GP12164
         MVC   0(2,R3),SAVECC     REPLACE ./ (SMP, ETC.?)       GP12164
         CLI   PRTLEVEL,1         PRINT CARD?                   GP12164
         BNH   DEBLOCKU             NO; PUNCH ONLY              GP12164
         PRTF  (R3),80,CC=NO      PRINT CARD                    GP12164
DEBLOCKU PRTF  (R3),80,CC=NO,DEV=2     PUNCH IT                 GP12164
         BXLE  R3,R4,DEBLOCKC DO AGAIN
NEWBLOCK LM    R0,R1,CURBLOCK  GET BLOCK POINTER
         USING UWORD,R1
         XR    R2,R2
         ICM   R2,3,UDATLEN  GET BLOCK LENGTH
         LA    R2,UFIXLEN(,R2)    ADD OVERHEAD
         AR    R1,R2
         SR    R0,R2
         BP    SETBLOCK      DO NEXT BLOCK
         B     GETDATA       ELSE READ ANOTHER
         SPACE 1
NOMEMBRS PRTL  'No members found in input',CC=NO                GP12164
         B     EXIT16        QUIT                               GP12164
         SPACE 1
EODAD    CLOSE MF=(E,INPOC)                                     GP12164
PGMEXIT  SERVTERM ,          CLOSE PRINTER                       92135
         PGMEXIT RC=RETCODE  RETURN TO OS                       GP12164
         SPACE 2
         USING UWORD,R1
ABSTTR   MVC   TTR+2(1),UCOUNT+4  COPY RECORD NUMBER
         LH    R14,NMEXT     GET NUMBER OF EXTENTS
         LA    R15,EXTENTS   POINT TO FIRST
         XR    R0,R0         CLEAR TRACK COUNTER
ABSTTRL  CLC   UCOUNT(4),6(R15)  LOWER THAN EXTENT START ?
         BL    ABSTTRN       YES; INCREMENT
         CLC   UCOUNT(4),10(R15)  WITHIN EXTENT ?
         BNH   ABSTTRF       YES
ABSTTRN  AH    R0,14(,R15)   ADD TRACKS IN THIS EXTENT
         LA    R15,16(,R15)  BUMP
         BCT   R14,ABSTTRL   TRY NEXT EXTENT
         MVC   TTR,=X'FFFFFF'  MAKE FF'S                        GP12164
        PRTDATA 'CCHHR',(UCOUNT,5,HEX,PAD),'not in extent list' GP12164
         BR    R9
ABSTTRF  LH    R14,UCOUNT    GET CYLINDER
         SH    R14,6(,R15)   LESS START CYLINDER
         MH    R14,TRKPCYL   CONVERT TO TRACKS
         AH    R14,UCOUNT+2  ADD TRACK NUMBER
         SH    R14,8(,R15)   LESS START TRACK
         AR    R14,R0        PLUS PRIOR EXTENT TRACKS
         STH   R14,TTR       SET TT
         BR    R9            RETURN
         SPACE 2
         USING INEXIT,R15                                        92135
         USING IHADCB,R1                                         92135
INEXIT   LH    R2,DCBBLKSI                                       92135
         LTR   R2,R2
         BP    *+8
         LH    R2,=H'32760'
         LH    R3,DCBLRECL
         LTR   R3,R3
         BMR   R14
         SH    R2,=H'4'
         CR    R3,R2
         BNLR  R14
         STH   R2,DCBLRECL   FIX IEBCOPY LRECL ERROR
         BR    R14
         SPACE 2
         LTORG ,
         SPACE 2
TAPE     DCB   DDNAME=TAPE,DSORG=PS,MACRF=(GL),EODAD=EODAD,            *
               EXLST=EXLIST,RECFM=VBS,BFTEK=A
INPOC    OPEN  (TAPE,(INPUT,REREAD)),MF=L                       GP12164
EXLIST   DC    0A(0),X'07',AL3(MYJ-MYJ),X'85',AL3(INEXIT)       GP12164
BLOKBXLE DC    A(0,80,0)     DEBLOCK POINTERS
TRTAB    DC    C'0123456789ABCDEF'
HEXTRTAB EQU   TRTAB-C'0'
SYSPRINT PRTWORK SYSPRINT,TITLE=3                               GP12164
SYSPUNCH PUNWORK SYSUT2,SYSPUNCH                                GP12164
MEMOFLOW WTU   '0MEMBER TABLE OVERFLOW - REASSEMBLE',MF=L
BLANKS   DC    CL80' '       CONSTANT FOR CLEARING              GP12164
HEADER   FDOPT NL,CC=C'#'                                        92135
         FD    'IEBCOPY UNLOAD REFORMAT',RADJ,LEN=70             92135
         FD    *END                                              92135
         SPACE 2
TRTBLANK DC    XL256'0'      ALLOW ALL CHARACTERS               GP12164
         TRENT TRTBLANK,4,C' '    EXCEPT BLANK                  GP12164
         SPACE 2
         IHAPDS PDSBLDL=NO   MAPPING OF DIRECTORY BLOCK         GP12164
         MAPPDS ,            MAPPING OF SPF DATA                GP12164
         SPACE 2
INREC    DSECT ,             INPUT RECORD MAPPING
RDW      DS    XL2           RECORD LENGTH
RDWFG    DS    XL2           FLAGS
INDC     DS    X             INDICATOR BYTE
INDCERR  EQU   X'80'         ERROR IN COPY
UNLID    DS    XL3           IEBCOPY ID BYTES
UDSORG   DS    XL2           DSORG OF ORIGINAL DATASET
UBLKSI   DS    XL2           BLOCKSIZE
ULRECL   DS    XL2           RECORD LENGTH
URECFM   DS    X             RECFM
UKEYLN   DS    X             KEY LENGTH
UOPTCD   DS    X
         DS    X             RESERVED
TBLKSI   DS    XL2           TAPE BLOCKSIZE
UUCBTYPE DS    XL4           SOURCE UCBTYPE
UBLKMAX  DS    XL4           MAX DEVICE BLOCKSIZE
UCYLMAX  DS    XL2           CYLINDERS PER PACK
UTRKPCYL DS    XL2           TRACKS PER CYLINDER
         DS    XL8           OTHER OVERHEAD CONSTANTS
         SPACE 1
         ORG   INDC
UNMEXT   DS    X             NUMBER OF EXTENTS
         DS    XL15          OTHER DEB DATA
UEXTENTS DS    XL256         DEB EXTENTS
         SPACE 1
         ORG   INDC
UWORD    DS    XL4           CONTROL WORD ?
UCOUNT   DS    XL5           COUNT FIELD
UCKLEN   DS    X             KEY LENGTH
UDATLEN  DS    XL2           DATA LENGTH OF BLOCK
UBLOCK   DS    0X            BLOCK DATA
UFIXLEN  EQU   *-UWORD       FIXED OVERHEAD
UPDSKEY  DS    CL8           DIRECTORY BLOCK KEY
UPDSLEN  DS    XL2           DATA LENGTH
UPDSENT  DS    0X            DIRECTORY ENTRIES
         SPACE 2
COPY2DTE CSECT ,
         SPACE 1                                                 92135
SAVE     DSECT ,             SAVE AND WORK AREA                  92135
DB       DS    2D            WORK AREA                           92135
DB2      DS    2D            WORK AREA                          GP12164
ZEROES   DC    XL8'0'        HEX ZEROES
         SERVDEFS ,          SERVICE ROUTINE DATA               GP12164
PDEBXLE  DS    A,F,A         BXLE POINTERS FOR DIRECTORY SCAN   GP12164
CURBLOCK DC    A(0,0)        LENGTH/ADDRESS OF CURRENT INPUT BLOCK
TRKPCYL  DC    H'19'         TRACKS PER CYLINDER
NMEXT    DC    H'1'          NUMBER OF DEB EXTENTS PRESENT
USERCC   DS    C'./'         DEFAULT CONTROL CHARACTERS         GP12164
SAVECC   DS    C'./'         DEFAULT CARD REPLACEMENT           GP12164
TTR      DC    XL3'0'        TTR OF CURRENT BLOCK
         SPACE 1
PROFLAGS DS    X             PROCESSING OPTIONS
FGPEOD   EQU   X'80'           PHYSICAL END OF DIRECTORY FOUND
*GLEOD   EQU   X'40'           LOGICAL END OF DIRECTORY FOUND
*GDATA   EQU   X'20'           PROCESSING DATA
*GNEEDM  EQU   X'08'           NEED NEW MEMBER
*GTTRN   EQU   X'02'           CHECK ADDITIONAL TTRS
*GTTR1   EQU   X'01'           HAVE FIRST TTR OF MEMBER
*GNUMEM  EQU   (FGNEEDM)       (RE)SET NEED AND TTR FLAGS
         SPACE 1
OPTFLAGS DS    X             RUN OPTIONS
FGDUMP   EQU   X'80'           DUMP ON ERROR
FGREP    EQU   X'40'           REPLACE EXISTING MEMBERS
FGASIS   EQU   X'20'           COPY DATA AS IS
         SPACE 1
CONCARD  DS    CL80          GENERATED CONTROL CARD (./ ADD)    GP12164
CCCON    EQU   CONCARD,2,C'C'   ./ OR SPECIAL                   GP12164
CCADD    EQU   CONCARD+3,9,C'C' ADD NAME=                       GP12164
CCNAM    EQU   CONCARD+12,8,C'C'       SSI ADDED AFTER NAME     GP12164
CCSPF    EQU   CONCARD+21,50,C'C' SPF STATISTICS                GP12164
         ORG   CCSPF              REDEFINE                      GP12164
CCSPVER  DS    CL2                VERSION 00-99                 GP12164
CCSPMOD  DS    CL2                MODIFICATION 00-99            GP12164
         DS    C'-'                                             GP12164
CCSPCDT  DS    CL5                CREATION DATE YYDDD           GP12164
         DS    C'-'                                             GP12164
CCSPMDT  DS    CL5                MODIFICATION DATE             GP12164
         DS    C'-'                                             GP12164
CCSPMTM  DS    CL4                MODIFICATION TIME             GP12164
         DS    C'-'                                             GP12164
CCSPCUR  DS    CL5                CURRENT RECORDS               GP12164
         DS    C'-'                                             GP12164
CCSPINR  DS    CL5                INITIAL RECORDS               GP12164
         DS    C'-'                                             GP12164
CCSPMDR  DS    CL5                MODIFIED RECORDS              GP12164
         DS    C'-'                                             GP12164
CCSPUID  DS    CL10               USER ID +1 +2                 GP12164
         ORG   ,                                                GP12164
         SPACE 1
EXTENTS  DC    16XL16'0'     DEB EXTENTS
         MAPPARSE DSECT=NO   MAP PARSE PARAMETER LIST           GP12164
SOPPARM  SERVSORT DSECT=NO   SORT PARAMETERS                    GP12164
MYJ      DS    0F            INPUT JFCB                         GP12164
         IEFJFCBN ,                                             GP12164
PRTLEVEL DS    X                                                GP12164
         SPACE 1
PDEONE   DS    (2048)XL(12+2*SPFUDLEN)'0'   DIRECTORY SAVE AREA GP12164
PDELAST  DS    XL(12+2*SPFUDLEN)'0'         LAST ENTRY          GP12164
SAVEEND  EQU   *             END ?                              GP12164
         SPACE 2
         PRINT &PRTSYS                                           92135
         MAPPARST ,                                             GP12164
         CVT   DSECT=YES                                         92135
         IHACDE ,                                                92135
         DCBD  DSORG=PS,DEVD=DA                                  92135
MYUCB    DSECT ,                                                GP12164
         IEFUCBOB ,                                             GP12164
         END   ,
