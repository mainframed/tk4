DOSUP    TITLE 'CONVERT DOS LIBRARY TO IEBUPDTE INPUT FORMAT'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES    SET GLOBALS
         SPACE 2
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 1982  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR
DOSUPDTE PGMHEAD ZERO12,BASE=R12,PARM=R9                ADDED ON 82154
*
*        THIS PROGRAM CONVERTS DOS SOURCE/MACRO LIBRARIES DUMPED
*        TO TAPE TO IEBUPDTE INPUT FORMAT.
*
*        ATTRIBUTES: QUICK & DIRTY; RENT, REFR, REUS, PAGE
*
         SERVINIT ,          LOAD THE SERVICE ROUTINE
         SERVCALL LPALD,=CL8'@PRINTER'  LOAD THE PRINTER ROUTINE
         ST    R0,@PRINTER
         SERVCALL LPALD,=CL8'@INPREAD'  LOAD THE INPUT READ ROUTINE
         ST    R0,@INPREAD
         PRTOPEN SYSPRINT,OPT=(DUMMY,NOWTO)  PROCEED IF NO DD
         PRTV  TITLE,TITLE=1   WRITE A TITLE
         INPOPEN SYSIN,OPT=ABEND  ABEND 2540 IF NO INPUT
         PRTOPEN SYSUT2,OPT=ABEND,DEV=2  OPEN THE OUTPUT FILE
         SPACE 1
INITLOOP INPGET ,            GET A CARD
         BXH   R15,R15,INPERR  QUIT ON INPUT ERROR
         LR    R2,R1         SAVE THE CARD ADDRESS
         CLC   =C'CATAL C.',0(R2)  MEMBER CONTROL ?
         BE    HAVEMEM2
         CLC   BOOKEND,0(R2)   SPACER ?
         BE    INITLOOP
         PRTLIST BADCONCD    PRINT BAD CARD
         INC   ERRORS,WORK=R0
         CH    R0,=Y(10)     TEN ERRORS ?
         BL    INITLOOP      NOT YET; CONTINUE
ABEND666 SERVTERM ,          CLOSE AND FREE
         ABEND 666
         SPACE 1
CONLOOP  INPGET ,            GET A (PRESUMED) CONTROL CARD
         BXH   R15,R15,INPERR
         LR    R2,R1         SAVE THE CARD ADDRESS
         CLC   =C'CATAL C.',0(R2)  NEW MEMBER CARD ?
         BE    HAVEMEM       YES
         CLC   BOOKEND,0(R2)  NORMAL CONTROL CARD ?
         BE    CONLOOP
         PRTLIST BADCONCD    PRINT BAD CARD
         INC   ERRORS,WORK=R0
         CH    R0,=Y(10)     TEN ERRORS ?
         BL    CONLOOP       NOT YET; CONTINUE
         B     ABEND666
         SPACE 1
HAVEMEM  PRTLIST NUMLINE     SHOW NUMBER OF CARDS
         XC    NUMCARDS,NUMCARDS  RESET CARD COUNT
HAVEMEM2 INC   NUMMEM        COUNT MEMBERS
         MVC   ADDNAME,8(R2)   COPY MEMBER NAME
         PRTLIST NAMELINE    PRINT THE NAME
         PRTLIST ADDCARD,DEV=2  WRITE THE ./ ADD CARD
         INPGET ,            GET ANOTHER INPUT CARD
         BXH   R15,R15,INPERR
         LR    R2,R1         SAVE THE CARD ADDRESS
         CLC   BOOKEND,0(R2)  START OF MEMBER ?
         BNE   MEMCOPY       NO; HOPE THIS IS DATA
MEMGET   INPGET ,            GET ANOTHER CARD
         BXH   R15,R15,INPERR
         LR    R2,R1         SAVE THE CARD ADDRESS
         CLC   BOOKEND,0(R2) END OF MEMBER ?
         BE    CONLOOP       YES; GET A CONTROL CARD
MEMCOPY  INC   NUMCARDS      UP THE CARD COUNT
         PRTF  (R2),80,CC=NO,DEV=2  COPY THE CARD
         B     MEMGET        GET ANOTHER
         SPACE 2
EOF      PRTLIST NUMLINE     PRINT NUMBER OF CARDS PROCESSED
         PRTLIST FINALS
         SERVTERM ,
         PGMEXIT RC=0        QUIT                               GP03027
         SPACE 1
INPERR   PRTV  INERRMSG
         B     ABEND666
         SPACE 2
SYSUT2   PUNWORK SYSUT2,SYSOUT
SYSPRINT PRTWORK SYSPRINT,SYSTERM,TITLE=3
SYSIN    INPWORK SYSUT1,SYSIN,EDIT=128,FILL=C' ',WIDTH=80,EODAD=EOF
         SPACE 1
TITLE    VCON  '#  DOS TO IEBUPDTE INPUT CONVERSION'
INERRMSG VCON  '0UNRECOVERABLE INPUT ERROR'
BADCONCD FDPRT 'CONTROL CARD EXPECTED; FOUND ''',NL
         FDPRT 0(R2),80
         FDPRT ''''
         FDPRT *END
NAMELINE FDPRT 'MEMBER',PADR,NL
         FDPRT ADDNAME
         FDPRT *END
NUMLINE  FDPRT NUMCARDS,I,RADJ,LEN=15
         FDPRT 'RECORDS COPIED',PADL
         FDPRT *END
FINALS   FDOPT NL,CC=C'0'
         FDREPT 15,C'*'
         FDPRT ' '
         FDPRT NUMMEM,I
         FDPRT 'MEMBERS PROCESSED ',PADL
         FDREPT 15,C'*'
         FDPRT *END
ADDCARD  FDPRT './       ADD   NAME=',NL
         FDPRT ADDNAME
         FDPRT *END
         SPACE 1
BOOKEND  DC    CL80' BKEND'
         SPACE 2
SAVE     DSECT ,             WORK/SAVE AREA
@SERVICE DS    A
@INPREAD DS    A
@PRINTER DS    A
ERRORS   DS    F             ERROR COUNT
NUMCARDS DS    F             CARDS IN CURRENT RECORD
NUMMEM   DS    F             NUMBER OF MEMBERS PROCESSED
ADDNAME  DC    CL8' '        MEMBER NAME
SAVEEND  EQU   *             END OF SAVE AREA                   GP03027
         SPACE 1
         PRINT &PRTSYS
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IHACDE ,
         SPACE 1
         END   ,
