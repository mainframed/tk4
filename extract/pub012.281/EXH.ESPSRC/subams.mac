SUBAMS   TITLE 'S U B A M S  ***  INVOKE IDCAMS WITH EXIT'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
**                                                                   **
**   INVOKE IDCAMS: CALL SUBAMS,(parms),VL                           **
**                                                                   **
**   Format 1:  Single address: list of command addresses; last      **
**                 has x'80' bit on. Each address points to a one    **
**                 byte length, followed by text.                    **
**                 No messages issued; IDCAMS return code in R15/R0  **
**                                                                   **
**   Format 2:  Two addresses; first as above, second address of a   **
**                 return chain of messages (4-byte link, 2-byte     **
**                 length, text). Caller must free all messages      **
**                 (in subpool 0).                                   **
**                                                                   **
***********************************************************************
         PUSH  USING
         DROP  ,
SUBAMS   CSECT ,
         USING SUBAMS,R15
         B     IDCBEG
         DC    AL1(17),CL17'SUBAMS   &SYSDATE'
IDCBEG   STM   R14,R12,12(R13)    SAVE CALLER'S REGISTERS
         LR    R12,R15            SET LOCAL BASE
         DROP  R15
         USING SUBAMS,R12         DECLARE PROGRAM BASE
         LR    R10,R13            SAVE CALLER'S SAVE AREA
         LA    R13,IDCSAVE        LOAD OUR SAVE AREA
         ST    R10,4(,R13)        BACK LINK
         ST    R13,8(,R10)        DOWN LINK
         MVI   EXFLAGS,EXFRET     INITIALIZE FLAGS - NO ABEND
         LA    R15,20             PRESET FOR PARM ERROR
         LA    R1,0(,R1)          ADDRESS OF IDCAMS REQUEST (V-CON)
         L     R2,0(,R1)          GET COMMAND CHAIN
         ST    R2,USERCMD         PASS TO EXIT
         LTR   R2,R2              ONE OR MORE PARMS ?
         BNM   TWOPARM            TWO:  CMDSTACK / MSG QUEUE
         OI    EXFLAGS,EXFSUPP    ONE; SUPPRESS ALL MESSAGES
         B     COMPARM
TWOPARM  L     R1,4(,R1)          GET BLOCK ADDRESS
         SR    R0,R0
         ST    R0,0(,R1)          CLEAR THE QUEUE
         ST    R1,MSGHEAD         SAVE Q HEAD
         TESTAUTH FCTN=0,KEY=YES,RBLEVEL=1  KEY ZERO CALLER ?
         LTR   R6,R15        TEST AND REMEMBER
         BNZ   COMPARM2
COMPARM  MODESET KEY=NZERO   GET UNPRIVIED FOR THE CALL
COMPARM2 LA    R1,AMSPARM         PASS PARAMETER LIST
         LINK  EP=IDCAMS          INVOKE UTILITY
         ST    R0,20(,R10)        ANYTHING IN IT?
         ST    R15,16(,R10)       SAVE RETURN CODE
         LTR   R6,R6         NEED TO RESTORE KEY ?
         BNZ   COMPARM3      NO
         MODESET KEY=ZERO    RESTORE
COMPARM3 LR    R13,R10
         LM    R14,R12,12(R13)    RESTORE CALLER'S REGS
         BR    R14                RETURN
         EJECT ,
***************************************************************
* IDCAMS ASYNCHRONOUS EXIT ROUTINE
***************************************************************
         SPACE 1
         PUSH  USING
         DROP  ,
XIDCAMS  STM   R14,R12,12(R13)
         LR    R12,R15
         USING XIDCAMS,R12
         LA    R9,XIDSAVE         SET MY SAVE AREA
         ST    R13,4(,R9)         MAKE BACK LINK
         ST    R9,8(,R13)         MAKE DOWN LINK
         LR    R13,R9             MAKE ACTIVE SAVE AREA
         SR    R15,R15            PRESET FOR GOOD RETURN
         LM    R3,R5,0(R1)        LOAD PARM LIST ADDRESSES
         SLR   R14,R14
         IC    R14,0(,R4)         LOAD FUNCTION
         B     *+4(R14)
         B     XIDCEXIT   OPEN           CODE IN R14 = X'00'
         B     XIDCEXIT   CLOSE          CODE IN R14 = X'04'
         B     XIDCGET    GET SYSIN      CODE IN R14 = X'08'
         B     XIDCPUT    PUT SYSPRINT   CODE IN R14 = X'0C'
         SPACE 1
XIDCGET  TM    EXFLAGS,EXFGET            X'FF' = PRIOR GET ISSUED ?
         BNZ   XIDCGET4                  YES, SET RET CODE = 04
         L     R2,USERCMD    GET REQUEST ADDRESS
         L     R3,0(,R2)
         SR    R4,R4
         IC    R4,0(,R3)          LOAD LENGTH
         LA    R3,1(,R3)          POINT TO TEXT
         STM   R3,R4,0(R5)        PLACE INTO IDCAMS LIST
         LTR   R4,R4
         BNP   XIDCGETN           NULL LENGTH TERMINATES
         TM    0(R2),X'80'        LAST COMMAND ?
         BNZ   XIDCGETN           YES; QUIT INPUT
         LA    R2,4(,R2)          POINT TO NEXT ONE
         ST    R2,USERCMD         SET FOR NEXT TIME
         B     XIDCEXIT
XIDCGETN OI    EXFLAGS,EXFGET            X'FF' = A GET HAS BEEN ISSUED
         B     XIDCEXIT
         SPACE 1
XIDCGET4 LA    R15,4                     SET REG 15 = X'00000004'
         B     XIDCEXIT
         SPACE 1
XIDCPUT  TM    EXFLAGS,EXFSUPP+EXFSKIP  ANY FORM OF SUPPRESSION?
         BNZ   XIDCPUTZ           YES; DON'T BOTHER WITH REST
         LM    R4,R5,0(R5)
         TM    EXFLAGS,EXFMALL    PRINT ALL MESSAGES?
         BNZ   XIDCSHOW           YES; PUT THEM ALL OUT
         CH    R5,=H'10'          LONG ENOUGH FOR TESTS ?
         BNH   XIDCSHOW           PASS THEM
         CLC   =C'IDCAMS ',1(R4)    IDCAMS TITLE ?
         BE    XIDCEXIT           YES; SKIP
         CLC   =C'IDC0002I',1(R4)   AMS PGM END
         BE    XIDCEXIT           YES; SKIP
         CH    R5,=H'2'           TOO SHORT ?
         BNH   XIDCEXIT           YES; SKIP
XIDCSHOW LA    R2,4+2(,R5)        LINK+LEN+TEXT
         GETMAIN R,LV=(R2)        GET STORAGE
         XC    0(4,R1),0(R1)      CLEAR LINK
         STH   R5,4(,R1)          SET LENGTH
         BCTR  R5,0                 EX LENGTH
         EX    R5,EXMVCTXT        MOVE TEXT
         L     R4,MSGHEAD         GET LAST MESSAGE
         ST    R1,0(,R4)          CHAIN IN
         ST    R1,MSGHEAD
XIDCPUTZ SR    R15,R15
         B     XIDCEXIT
EXMVCTXT MVC   6(0,R1),0(R4)      SAVE TEXT IN GOTTEN STORAGE
XIDCSKIP OI    EXFLAGS,EXFSKIP    SKIP THIS AND REMAINING MESSAGES
         SR    R15,R15
         SPACE 2
***************************************************************
* IDCAMS ASYNC EXIT ROUTINE - EXIT, CONSTANTS & WORKAREAS
***************************************************************
         SPACE 1
XIDCEXIT L     R13,4(,R13)        GET CALLER'S SAVE AREA
         L     R14,12(,R13)
         RETURN (0,12)            RESTORE AND RETURN TO IDCAMS
         POP   USING
         SPACE 1
IDCSAVE  DC    18F'0'             MAIN ROUTINE'S REG SAVEAREA
XIDSAVE  DC    18F'0'             ASYNC ROUTINE'S REG SAVEAREA
USERCMD  DC    A(0),F'0'          COMMAND ADDRESS/LENGTH
MSGHEAD  DC    A(0)               START OF USER MESSAGE CHAIN
         SPACE 1
AMSPARM  DC    A(HALF00,HALF00,HALF00,X'80000000'+ADDRLIST)
         SPACE 1
ADDRLIST DC    F'2'
         DC    A(DDNAME01)
         DC    A(XIDCAMS)
         DC    A(USERCMD)
         DC    A(DDNAME02)
         DC    A(XIDCAMS)
         DC    A(0)
         SPACE 1
HALF00   DC    H'0'
DDNAME01 DC    CL10'DDSYSIN   '
DDNAME02 DC    CL10'DDSYSPRINT'
         SPACE 1
EXFLAGS  DC    X'08'              EXIT PROCESSING FLAGS
EXFGET   EQU   X'01'                PRIOR GET WAS ISSUED
EXFNOM   EQU   X'04'                SUPPRESS ERROR WTOS
EXFRET   EQU   X'08'                NO ABEND; RETURN WITH COND.CODE
EXFMALL  EQU   X'10'                ALWAYS PRINT MESSAGES
EXFSUPP  EQU   X'20'                ALWAYS SUPPRESS MESSAGES
EXFSKIP  EQU   X'40'                SKIP SUBSEQUENT MESSAGES
EXFONCE  EQU   X'80'                QUEUE HEAD SAVED
EXFGLOB  EQU   EXFMALL+EXFSUPP+EXFRET  GLOBAL FLAGS
         POP   USING
         SPACE 1
         YREGS ,
         END  ,
