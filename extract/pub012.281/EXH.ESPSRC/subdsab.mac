SUBDSAB  TITLE 'S U B D S A B   **   SIMULATE GETDSAB SERVICE PRE-ESA'
***********************************************************************
**                                                                   **
**   SUBROUTINE SUBDSAB, WITH MACRO MVSDSAB, SIMULATES THE GETDSAB   **
**   SERVICE AND MACRO OF MORE MODERN SYSTEMS.                       **
**                                                                   **
**   CODE IS COMPATIBLE, BUT MAY DIFFER IN VALIDITY CHECKING AND     **
**   ERROR CODES.                                                    **
**                                   AUTHOR: G. POSTPISCHIL          **
***********************************************************************
**
*   R1 POINTS TO PARAMETER LIST
*   0     REQUEST: 1 FIRST; 2 NEXT; 3 DDNAME ADDR; 4 DCB ADDR
*   4     0 OR DDN/DCB ADDRESS
*   8     ADDRESS OF OLD/NEW DSAB; 0 ON ERROR
*   12    TCB ADDRESS (OPTIONAL); FAIL FOR OPEN DCB PTR
*
*   RETURN CODES AS DOCUMENTED FOR GETDSAB
*
*   R15 - 0    REQUEST COMPLETE
*         4    END OF CHAIN ON FIRST/NEXT REQUEST
*         8    NOT FOUND
*        12    BAD INPUT PARMS      R0 - 1200  BLANK DDNAME
*                                        1210  TCB ADDRESS FOR OPEN DCB
*                                        1220  DSABPTR CONTENTS INVALID
*        16    FUNCTION NOT AVAILABLE
*
*
SUBDSAB  START 0
SUBDSAB  AMODE ANY
SUBDSAB  RMODE ANY
         STM   R14,R12,12(R13)
         LR    R10,R15       AND NEW BASE
         USING SUBDSAB,R10
         LA    R9,7+20+11*4(,R13)  POINT INTO USER'S SAVE AREA
         N     R9,=X'7FFFFFF8'  MAKE DOUBLE-WORD BOUNDARY
         USING USERSAVE,R9   MAKE THIS CODE RE-ENTRANT/REFRESHABLE
         LTR   R8,R1         DID USER SUPPLY A PARM?
         BZ    RSN666        NO; ISSUE ERROR 12/666
         USING PRMDSECT,R8   DECLARE THE PARM LIST
         ICM   R7,15,PRM@DSAB  HAVE A DSAB RETURN ADDRESS?
         BZ    RSN666        NO; BAD PARM LIST
         L     R3,0(,R7)     LOAD PRIOR DSAB ADDRESS (FOR NEXT)
         XC    0(4,R7),0(R7)  CLEAR IT PER DOCS
*
         ICM   R4,15,PRM@TCB   DID USER SPECIFY A TCB ?
         BZ    GET@TCB       NO; USE CURRENT ONE
         ICM   R4,15,0(R4)   LOAD AND TEST
         BNZ   TEST@TCB
GET@TCB  L     R4,PSATOLD-PSA          MY TCB
         USING TCB,R4        DECLARE THE TCB
TEST@TCB CLC   =C'TCB ',TCBTCBID  REALLY A TCB ?
         BNE   RSN666        INVALID TCB (DEFER CHAIN CHASE)
         BIX   VAL=PRMTYPE,BASE=*,ERR=EXIT16,                          *
               LOC=(,        0 - UNDEFINED                             *
               NEWDSAB,      1 - FIRST DSAB                            *
               NXTDSAB,      2 - NEXT  DSAB                            *
               GETDDNM,      3 - FIND DD NAME                          *
               GETDCBP)      4 - FIND DCB BY ADDRESS
         SPACE 2
***********************************************************************
**   BEGIN DSAB SEQUENTIAL LOOKUP                                    **
***********************************************************************
NEWDSAB  ICM   R1,15,TCBJSCB    GET JSCB
         BZ    EXIT4         BAD TCB ?
         ICM   R1,15,JSCDSABQ-IEZJSCB(R1)  DSAB QDB
         BZ    EXIT4         BAD TCB ?
         ICM   R5,15,DSQFRSTP-DSABQDB(R1)  FIRST DSAB ON CHAIN
         BZ    EXIT4         NONE ON CHAIN
         USING DSAB,R5
         ST    R5,0(,R7)     PASS FIRST ADDRESS BACK
         B     GOODEXIT      AND RETURN
         SPACE 2
***********************************************************************
**   CONTINUE SEQUENTIAL DSAB LOOKUP                                 **
***********************************************************************
NXTDSAB  LTR   R3,R3         VALID PREVIOUS DSAB ?
         BZ    RSN1220       NO; FAIL REQUEST
         CLC   =C'DSAB',DSABID-DSAB(R3)  USER'S ADDRESS VALID ?
         BNE   RSN1220       NO; FAIL REQUEST
         ICM   R1,15,TCBJSCB    GET JSCB
         BZ    EXIT4         BAD TCB ?
         ICM   R1,15,JSCDSABQ-IEZJSCB(R1)  DSAB QDB
         BZ    EXIT4         BAD TCB ?
         ICM   R5,15,DSQFRSTP-DSABQDB(R1)  FIRST DSAB ON CHAIN
         BZ    EXIT4         NONE ON CHAIN
NXTLOOP  CR    R3,R5         FOUND RESTART POINT ?
         BE    NXTGET        YES; GET NEXT ADDRESS
         ICM   R5,15,DSABFCHN  GET NEXT DSAB
         BNZ   NXTLOOP       CHECK IT
         B     RSN1220       INVALID DSCB ADDRESS OR CHAIN CHANGED
         SPACE 1
NXTGET   ICM   R5,15,DSABFCHN   GET NEXT ONE
         BZ    EXIT4         END OF CHAIN
         ST    R5,0(,R7)     PASS FIRST ADDRESS BACK
         B     GOODEXIT      AND RETURN
         SPACE 2
***********************************************************************
**   FIND DSAB BY DD NAME (NOT BLANK/ZEROES)                         **
***********************************************************************
GETDDNM  ICM   R3,15,PRM@REQ  VALID POINTER ?
         BZ    RSN1200       NO; FAIL REQUEST
         CLI   0(R3),C' '    INVALID DDNAME ?
         BNH   RSN1200       NO; FAIL REQUEST
         LM    R14,R15,0(R3)  LOAD DD NAME FOR FASTER COMPARE
COMDDNM  ICM   R1,15,TCBJSCB    GET JSCB
         BZ    EXIT8         BAD TCB ?
         ICM   R1,15,JSCDSABQ-IEZJSCB(R1)  DSAB QDB
         BZ    EXIT8         BAD TCB ?
         ICM   R5,15,DSQFRSTP-DSABQDB(R1)  FIRST DSAB ON CHAIN
         BZ    EXIT4         NONE ON CHAIN
GETDDLUP ICM   R3,15,DSABTIOT  GET TIOT ADDRESS
         BZ    GETDDBMP      NONE; IGNORE
         USING TIOENTRY,R3   DECLARE TIOT ENTRY
         CLM   R15,15,TIOEDDNM+4  LAST PART OF DD MATCH ?
         BNE   GETDDBMP
         CLM   R14,15,TIOEDDNM   FIRST PART OF DD MATCH ?
         BE    GETDDGOT      YES; RETURN IT
GETDDBMP ICM   R5,15,DSABFCHN  GET NEXT DSAB
         BNZ   GETDDLUP      CHECK IT
         B     EXIT8         NO MATCH
         SPACE 1
GETDDGOT ST    R5,0(,R7)     PASS FIRST ADDRESS BACK
         B     GOODEXIT      AND RETURN
         SPACE 2
***********************************************************************
**   GET DSAB BY DCB ADDRESS                                         **
**      CLOSED DCB: TREAT AS DDNAME SEARCH REQUEST                   **
**      OPEN DCB: GET BY BLOCK CHASING                               **
***********************************************************************
GETDCBP  ICM   R3,15,PRM@REQ  VALID POINTER ?
         BZ    RSN1200       NO; FAIL REQUEST
         USING IHADCB,R3     SEE IF VALID DCB
         TM    DCBOFLGS,DCBOFOPN  IS IT OPEN ?
         BNZ   GETODCB       YES
         CLI   DCBDDNAM,C' '   VALID DD NAME ?
         BNH   RSN1200       NO; FAIL
         LM    R14,R15,DCBDDNAM
         B     COMDDNM       TREAT AS DD NAME LOOKUP REQUEST
         SPACE 1
GETODCB  B  EXIT16 *****NOT IMPLEMENTED*****YET*****
         SPACE 2
RSN666   LA    R0,666        UNDEFINED ERROR (IN PARM LIST)
         B     EXIT12
RSN1200  LA    R0,1200       INVALID DDNAME
         B     EXIT12
RSN1210  LA    R0,1210       DCBPTR AND TCBPTR FOR OPEN DCB
         B     EXIT12
RSN1220  LA    R0,1220       INVALID DSABPTR
EXIT12   LA    R15,12        SET RETURN CODE 12
         B     PGMEXIT
         SPACE 1
EXIT16   LA    R15,16        SET RETURN CODE 16
         B     PGMRSN0
         SPACE 1
EXIT8    LA    R15,8         SET RETURN CODE 8
         B     PGMRSN0
         SPACE 1
EXIT4    LA    R15,4         SET RETURN CODE 4
         B     PGMRSN0
         SPACE 1
GOODEXIT SR    R15,R15       SET RETURN CODE 0
PGMRSN0  SR    R0,R0         SET REASON CODE TO ZERO
PGMEXIT  LM    R1,R10,24(R13)  RELOAD MANY REGISTERS
         L     R14,12(,R13)  RELOAD RETURN ADDRESS
         BR    R14           RETURN
         SPACE 2
         LTORG ,
         SPACE 1
         PRINT NOGEN
         SPACE 1                                                GP04075
USERSAVE DSECT ,             MY WORK AREA MAPPED ON USER'S SAVE AREA
DB       DS    D                                                GP04075
         SPACE 1                                                GP04075
PRMDSECT DSECT ,             CALLER'S PARMS
PRMTYPE  DS    X,3X          REQUEST TYPE (FIRST/NEXT/DDN/DCB)
PRM@REQ  DS    A             REQUEST DATA ADDRESS (DDN/DCB)
PRM@DSAB DS    A             ADDRESS OF PRIOR/NEXT DSAB
PRM@TCB  DS    A             ADDRESS OF TCB ADDRESS, OR 0
         SPACE 1
DSABQDB  DSECT ,                       DSAB QUEUE DESCRIPTOR BLOCK
DSQDBID  DS    C'DSAB'                 ACRONYM
DSQATTRS DS    XL2                     ATTRIBUTES
DSQQDBLN DS    H'32'                   QDB LENGTH
DSQNELMS DS    F                       NUMBER OF ELEMENTS ON QUEUE
DSQFRSTP DS    A(DSAB)                 FIRST DSAB
DSQLASTP DS    A(DSAB)                 LAST DSAB
         SPACE 1
         IHADSAB ,
         SPACE 1
         YREGS ,
         SPACE 1
         PRINT NOGEN
         IEZJSCB ,
         IKJTCB ,
         IHAPSA ,
         DCBD  DEVD=DA,DSORG=PS
MYTIOT   DSECT ,
         IEFTIOT1 ,
         END   ,
