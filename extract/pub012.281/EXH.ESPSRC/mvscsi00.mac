CSI00    TITLE 'M V S C S I 0 0  *** MVS VERSION OF CATALOG LOOKUP'
***********************************************************************
**                                                                   **
**   MVS REPLACEMENT FOR IGGCSI00. INVOKES SUPER LOCATE TO GET VSAM  **
**     CATALOG INFORMATION. INTERFACE IS THE SAME AS Z/OS, EXCEPT    **
**     FIELD NAMES ARE RESTRICTED TO:                                **
**                                                                   **
**     CATVOL DEVTYP DSCBTTR DSTYPNAM ENTNAME ENTYPE FILESEQ         **
**     RELCRA VOLSER DSETCRDT DSETEXDT OWNERID                       **
**                                                                   **
**   VALID COMPONENT TYPES ARE : A B C G H R U X                     **
**                                                                   **
**   ALSO THE WORK AREA LENGTH IS A HALFWORD, WITH A MAXIMUM OF      **
**     32767 (Z/OS IS FULLWORD)                                      **
**                                                                   **
***********************************************************************
MVSCSI00 START 0
         B     BEGIN-MVSCSI00(,R15)
         DC    AL1(17),C'MVSCSI00 &SYSDATE'
BEGIN    STM   R14,R12,12(R13)
         LR    R12,R15
         USING MVSCSI00,R12
         LA    R15,16                  PRESET ERROR CODE
         LTR   R2,R1                   CHECK FOR PARM AREA
         BZ    EXITCOM
         LA    R15,12                  PRESET ERROR CODE
         ICM   R9,15,0(R2)             CHECK ERROR CODE ADDRESS
         BZ    EXITCOM
         USING RETSECT,R9              MAP IT
         ICM   R10,15,4(R2)            GET SELECTION LIST
         BZ    EXITCOM
         USING CSIFILTK,R10            DECLARE SELECTION MAPPING
         ICM   R11,15,8(R2)
         BZ    EXITCOM
         XC    RETCODE,RETCODE         PROVISIONALLY ZERO RETURN CODE
         SR    R2,R2                   CLEAR STOP VALUE
         LA    R1,CSIDTYPS+L'CSIDTYPS   END IF NO HIT
         TRT   CSIDTYPS,TRTTYPE        CHECK VALID TYPES
         CH    R2,=H'4'                NORMAL STOP ?
         BH    EXITCOM                 NO; INVALID CHARACTER
         LA    R7,CSIDTYPS             GET START
         SR    R7,R1                   -LENGTH
         LCR   R7,R7                   MAKE POSITIVE
         BP    TYPEDONE                LENGTH IN R7
         MVC   CSIDTYPS(L'TYPEDFLT),TYPEDFLT  SPECIFY ALL
         LA    R7,L'TYPEDFLT           SAVE NUMBER FOR CTGPL BUILD
TYPEDONE CLI   CSICLDI,C' '
         BE    GOODLDI
         CLI   CSICLDI,C'Y'
         BE    GOODLDI
         MVC   RETRSN,=X'0308'
EXITFFF  MVC   RETMOD,=X'FFFF'
EXITCOM  L     R14,12(,R13)
         LM    R0,R12,20(R13)
         BR    R14
GOODLDI  CLI   CSIRESUM,C' '
         BE    GOODRESM
         CLI   CSIRESUM,C'Y'
         BE    GOODRESM
         MVC   RETRSN,=X'0408'
         B     EXITFFF
GOODRESM CLI   CSIS1CAT,X'40'
         BE    GOOD1CAT
         CLI   CSIS1CAT,X'E8'
         BE    GOOD1CAT
         MVC   RETRSN,=X'0508'
         B     EXITFFF
GOOD1CAT LH    R0,CSINUMEN             GET FIELD COUNT
         CL    R0,=F'101'              IS IT VALID ?
         BL    GOODCNT
         MVC   RETRSN,=X'0608'
         B     EXITFFF
GOODCNT  LH    R4,0(,R11)              GET WORK AREA SIZE
         C     R4,=A(1024)             AT LEAST MINIMUM ?
         BL    BADSIZE                 NO
         CL    R4,=X'00007FFF'         NOT TOO LARGE?
         BNH   GOODSIZE
BADSIZE  MVC   RETRSN,=X'0708'
         B     EXITFFF
GOODSIZE LA    R14,CTGPL-LOCWORK+CTGPLLEN    FIXED AMOUNT
         LA    R3,1
         AH    R3,CSINUMEN
         MH    R3,=AL2(4+CTGFLLEN)     FIELD SPACE
         ALR   R3,R14
         STORAGE OBTAIN,LENGTH=(R3)    GET STORAGE
         LTR   R15,R15
         BZ    GOTMAIN
         MVC   RETRSN,=X'0708'
         B     EXITFFF
GOTMAIN  LR    R8,R1
         USING LOCWORK,R8              DECLARE IT
         LR    R4,R8                   ADDRESS AND
         LR    R5,R3                     LENGTH TO CLEAR
         MVCL  R4,R14                  CLEAR IT (R15=0 FRM OBTAIN)
         ST    R3,LOCSIZE              PRESERVE SIZE FOR RELEASE
*???*    OI    CTGDSORG+1,X'02'          ???
*MVS*    LA    R1,LOCDATA
*MVS*    ST    R1,SPECFLGS
*MVS*    MVC   LOCDATA(6),=C'CPLEXT'
*MVS*    LA    R14,1
*MVS*    STH   R14,LOCDATA+6
*MVS*    LA    R14,SPECFLGS-LOCDATA    SIZE OF EXTENSION
*MVS*    STH   R14,LOCDATA+8
         SPACE 1
*---------------------------------------------------------------------*
*   BUILD SUPER LOCATE PARAMETER LIST                                 *
*---------------------------------------------------------------------*
         OI    CTGOPTN1,CTGNAME+CTGGENLD   CTGCNAME *MVS*
         OI    CTGOPTN2,CTGRCATN
         CLI   CSICLDI,C'Y'
         BNE   SKIPCLDI
*MVS*    OI    CTGOPTN2,X'02'          CTGCDI
SKIPCLDI CLI   CSIRESUM,C'Y'
         BNE   SKIPRESM
*MVS*    OI    CTGOPTN2,X'08'          CTGRESM
SKIPRESM CLI   CSIS1CAT,C'Y'
         BNE   SKIP1CAT
         OI    CTGOPTN2,CTGGALL
SKIP1CAT CLI   CSIRESRV,C'Y'
         BNE   SKIPSBYP
         OI    CTGOPTN1,CTGBYPSS       UNDOCUMENTED
*MVS*    MVI   CTGOPTN3,CTGCMS+CTGLOC+CTGAM0
*MVS*    OI    CTGOPTN2,X'01'          CTGBOTH (ACCEPT ICF ARCH)
SKIPSBYP MVI   CTGOPTN3,CTGSUPLT+CTGAM0   SUPERLOCATE + VS-CAT
*MVS*    OI    CTGOPTN4,X'01'          CTGIFOR (ICF ONLY)
         LA    R1,CSIRESNM             GET RESUME NAME
         ST    R1,CTGENT               ADDRESS OF NAME/GENERIC
         LA    R1,CSICATNM             GET CATALOG NAME
         CLI   0(R1),C' '              ANY ?
         BE    *+8                     NO; LEAVE ADDRESS = 0
         ST    R1,CTGCAT               ADDRESS OF CATALOG NAME
         ST    R11,CTGWKA              ADDRESS OF CALLER'S WORK AREA
*MVS*    OI    CTGDSORG,X'04'          CTGF2WKA
*MVS*    LA    R1,CSIFILTK             GET FILTER
*MVS*    ST    R1,CTGTYPE              ADDRESS OF FILTER ?
         LH    R1,CSINUMEN
         LA    R1,1(,R1)
         STC   R1,CTGNOFLD             NUMBER OF FIELD NAMES
         CLC   CSIDTYPS,BLANKS
         BE    TYPELEND
         LA    R3,1
         B     TYPELBMP
TYPELLUP LA    R1,CSIDTYPS-1(R3)       POINT TO NEXT TYPE
         CLI   0(R1),C' '              DONE ?
         BE    TYPELEND
         CLI   0(R1),C'L'              TAPE LIBRARY ?
         BNE   TYPENLIB
*MVS*    L     R14,SPECFLGS
*MVS*    OI    44(R14),X'80'
         B     TYPELNXT
TYPENLIB CLI   0(R1),C'W'
         BNE   TYPELNXT
*MVS*    L     R14,SPECFLGS
*MVS*    OI    44(R14),X'40'
TYPELNXT LA    R3,1(,R3)
TYPELBMP CLR   R3,R7
         BNH   TYPELLUP
TYPELEND LA    R3,CTGFIELD
         LR    R5,R3                   SAVE POINTER FIELD
         LA    R1,1
         AH    R1,CSINUMEN
         LR    R0,R1                   SAVE LOOP MAX
         SLA   R1,2
         ALR   R3,R1                   FIELD DATA AFTER A-CONS
         USING CTGFL,R3                DECLARE FIELD AREA
         MVI   CTGDSORG+1,X'FF'        CTGTYPE,CTGMULTI
         ST    R3,0(,R5)               FIELD POINTER
         MVI   CTGFLDNO,1
         LA    R1,=C'MULTITYP'
         ST    R1,CTGFLDNM
         ST    R7,CTGFLNG              NUMBER OF TYPES
         LA    R14,CSIDTYPS
         ST    R14,CTGFLPT
         LA    R3,CTGFLLEN(,R3)        NEXT ENTRY
         LA    R4,2
         LA    R6,CSIFLDNM
         B     FILDBUMP
FILDLOOP LA    R5,4(,R5)               NEXT FIELD DATA
         ST    R3,0(,R5)               A OF FIELD
         MVI   CTGFLDNO,X'01'
         ST    R6,CTGFLDNM
         LA    R3,CTGFLLEN(,R3)        NEXT ENTRY
         LA    R6,L'CSIFLDNM(,R6)
         LA    R4,1(,R4)
FILDBUMP CLR   R4,R0
         BNH   FILDLOOP
         LOCATE CTGPL                  SUPER LOCATE
         LTR   R15,R15
         BZ    GOODSLOC
         MVC   RETMOD,CTGENT
         MVC   RETRSN(1),CTGENT+3
         STC   R15,RETRSN+1
GOODSLOC MVI   CSIRESUM,C' '
         TM    CTGOPTN2,X'08'
         BZ    GOODCMPL
         MVI   CSIRESUM,C'Y'
GOODCMPL SLR   R15,R15
         L     R0,LOCSIZE
         STORAGE RELEASE,ADDR=(R8),LENGTH=(0)   RELEASE WORK AREA
         ICM   R15,15,RETCODE          CHECK RETURN CODE
         BZ    EXITCOM                 OK; RETURN ZERO
         LA    R15,4
         B     EXITCOM                 ELSE SET RC=4
         SPACE 1
TYPEDFLT DC    C'ABCGHRUX'      1/2
BLANKS   DC    CL16' '          2/2
TRTTYPE  DC    X'08'         EVERYTHING INVALID
         TRENT TRTTYPE,4,C' ',    BLANK STOPPER
         TRENT ,0,C'A',C'B',C'C',C'G',C'H',C'R',C'U',C'X'  MVS SUPP'TED
         SPACE 1
RETSECT  DSECT ,
RETCODE  DS    0F'0'         RETURN CODE
RETMOD   DS    CL2' '          MODULE ID
RETRSN   DS    XL2'0'          REASON CODES
         SPACE 1
         PRINT ON,GEN
*^QUITE  MVSCSINA ,          MAP FILTER AREA
*
CSIFILTR DSECT
CSIFILTK DS    CL44          MASK
CSICATNM DS    CL44          CATALOG NAME OR BLANKS
CSIRESNM DS    CL44          RESUME NAME OR BLANKS
CSIDTYPD DS    0CL16         TYPE SELECTOR ENTRIES
CSIDTYPS DS    16CL1           ENTRY TYPES
CSIOPTS  DS    0CL4          CSI OPTIONS  ('Y' OR BLANK)
CSICLDI  DS    CL1             ACCEPT DATA/INDEX IF CLUSTER NAME=KEY
CSIRESUM DS    CL1             RESUME REQUEST
CSIS1CAT DS    CL1             SINGLE CATALOG SEARCH ONLY
CSIRESRV DS    CL1               RESERVED
CSINUMEN DS    H             NUMBER OF ENTRIES FOLLOWING
CSIENTS  DS    0CL8          VARIABLE NUMBER OF ENTRIES FOLLOW
CSIFLDNM DS    CL8           FIELD NAME
*
         SPACE 1
         IEZCTGFL ,
CTGFLLEN EQU   *-CTGFLDNO      LENGTH OF ONE FIELD ENTRY
         SPACE 1
*^QUITE  IEZCTGPL ,          MAP SUPER LOCATE PARM LIST
LOCWORK  DSECT ,             LOCAL WORK AREA
LOCSIZE  DS    F             SIZE OF GOTTEN STORAGE
*MVS  LOCDATA  DS    48X           PREFIX AREA
*MVS  SPECFLGS DS    A             ADDR OF SPECIAL REQUEST FLAGS
*
CTGPL    DS    0D
CTGOPTN1 DS    X              FIRST OPTION BYTE:
CTGBYPSS EQU   X'80' 1... ....  BYPASS CATALOG MANAGMENT SECURITY
CTGMAST  EQU   X'40' .1.. ....  CHECK THE MASTER PASSWORD
CTGCI    EQU   X'20' ..1. ....  CHECK CONTROL INTERVAL PASSWORD
CTGUPD   EQU   X'10' ...1 ....  CHECK UPDATE PASSWORD
CTGREAD  EQU   X'08' .... 1...  CHECK READ PASSWORD
CTGNAME  EQU   X'04' .... .1..  CTGENT CONTAINS DSNAME OR SERIAL ADDR
*                    .... .0..  CTGENT CONTAINS CONTROL INTERVAL NUMBER
CTGCNAME EQU   X'02' .... ..1.  CTGCAT CONTAINS CATALOG DSNAME ADDRESS
*                    .... ..0.  CTGCAT CONTAINS CATALOG ACB ADDRESS
CTGGENLD EQU   X'01' .... ...1  GENERIC LOCATE REQUEST
*
CTGOPTN2 DS    X              SECOND OPTION BYTE:
CTGEXT   EQU   X'80' 1... ....  EXTEND OPTION (WITH UPDATE)
CTGNSVS  EQU   X'80'            NO SCRATCH VSAM SPACE (WITH DELETE)
CTGERASE EQU   X'40' .1.. ....  ERASE OPTION (WITH DELETE)
CTGSMF   EQU   X'40'            WRITE SMF RECORD OPTION (WITH LSPACE)
CTGGALL  EQU   X'40'            SEARCH ALL CATALOGS (WITH LISTCAT)
CTGPURG  EQU   X'20' ..1. ....  PURGE OPTION (WITH DELETE)
CTGVMNT  EQU   X'20'            CALLER IS VSAM OPEN/CLOSE/EOV
CTGRCATN EQU   X'20'            RETURN CATALOG NAME (WITH LISTCAT)
CTGGTNXT EQU   X'10' ...1 ....  GET-NEXT OPTION (WITH LISTCAT)
CTGDISC  EQU   X'08' .... 1...  DISCONNECT OPTION (WITH EXPORT)
CTGOVRID EQU   X'04' .... .1..  ERASE OVERRIDE OPTION (WITH DELETE)
CTGSCR   EQU   X'02' .... ..1.  SCRATCH SPACE (WITH DELETE NON-VSAM)
*                    .... ...X  RESERVED
*
CTGOPTN3 DS    0X             THIRD OPTION BYTE
CTGFUNC  DS    X     XXX. ....  SPECIFIES THE CALLER-REQUESTED FUNCTION
CTGLOC   EQU   X'20' 001. ....    LOCATE
CTGLSP   EQU   X'40' 010. ....    LSPACE
CTGUPDAT EQU   X'60' 011. ....    UPDATE
CTGCMS   EQU   X'80' 100. ....    CATALOG MGMT SERV. FUNCT. SEE CTGOPNS
CTGSUPLT EQU   X'10' ...1 ....  SUPERLOCATE FUNCTION
CTGGDGL  EQU   X'08' .... 1...  GDG LOCATE - CALLER SUPPLIED BASE LEVEL
CTGSRH   EQU   X'04' .... .1..  SEARCH MASTER CATALOG ONLY
*                    .... .0..  SEARCH USER CATALOGS FIRST
*                    .... ..X.  RESERVED
CTGAM0   EQU   X'01' .... ...1  OS/VS2 CATALOG MANAGMENT REQUEST
*                    .... ...0  OS CATALOG REQUEST USER SUPPLIED CAMLST
*
CTGOPTN4 DS    X              FOURTH OPTION BYTE
CTGLBASE EQU   X'80' 1... ....  LOCATE BASE LEVEL (SUPERLOCATE GDG)
CTGDOCAT EQU   X'40' .0.. ....  DYNAMICALLY LOCATE AND OPEN CAT IF REQ.
*                    .1.. ....  DO NOT DYNAMICALLY OPEN NEEDED CATALOG
*                    ..XX XXXX  RESERVED
*
CTGENT   DS    0A             ADDRESS OF CATALOG RECORD IDENTIFIER
CTGFVT   DS    A              ADDRESS OF CALLER'S CTGFV
*
CTGCAT   DS    0A             ADDRESS OF CATALOG DSNAME OR ACB
CTGCVOL  DS    A              ADDRESS OF OS SYSCTLG DSNAME
*
CTGWKA   DS    A              ADDRESS OF CALLER'S WORK AREA
*
CTGDSORG DS    XL2            DATA SET ORGANIZATION IF SUPERLOCATE
*
CTGOPTNS DS    X              CATALOG MANAGMENT SERVICES REQUEST OPTION
CTGDEFIN EQU   X'08' 0000 1...  DEFINE
CTGALTER EQU   X'10' 0001 0...  ALTER
CTGDELET EQU   X'18' 0001 1...  DELETE
CTGLTCAT EQU   X'20' 0010 0...  LISTCAT
*                    .... .XXX    RESERVED
         DS    X              RESERVED
*
CTGTYPE  DS    X              TYPE OF CATALOG RECORD
CTGTALIN EQU   C'A'             NON-VSAM DATA SET
CTGTGBS  EQU   C'B'             GDG BASE RECORD
CTGTCL   EQU   C'C'             CLUSTER
CTGTDATA EQU   C'D'             DATA SET
CTGTINDX EQU   C'I'             INDEX
CTGTMCAT EQU   C'M'             MASTER CATALOG
CTGTPGS  EQU   C'P'             PAGE DATA SET
CTGTUCAT EQU   C'U'             USER CATALOG
CTGTVOL  EQU   C'V'             VOLUME
CTGTANM  EQU   C'X'             ALIAS NAME
*
CTGNOFLD DS    X              NUMBER OF ENTRIES IN CTGFIELD
*
CTGDDNM  DS    0A             ADDRESS OF DD STATEMENT (OPTIONAL)
CTGNEWNM DS    0A             ADDRESS OF NEW DSNAME (ALTER)
CTGFDBK  DS    XL2            FEEDBACK AREA (SUPERLOCATE)
CTGFBFLG DS    X              FLAGS (SUPERLOCATE)
CTGPAR   EQU   X'80' 1... ....  PARALLEL MOUNT
CTGKEEP  EQU   X'40' .1.. ....  FORCED KEEP
CTGGDGB  EQU   X'20' ..1. ....  GDG BASE LOCATED
CTGNGDSN EQU   X'10' ...1 ....  GDG DSNAME GENERATED (DSNAME.GXXXXVYY)
*                    .... XXXX  RESERVED
         DS    X              RESERVED
*
CTGJSCB  DS    0A             ADDRESS OF JSCB
CTGPSWD  DS    A              ADDRESS OF CALLER-SUPPLIED PASSWORD
*
CTGFIELD EQU   *              VARIABLE LENGTH FIELD
*                               4 BYTE ADDRESS OF EACH CTGFL TO SPECIFY
*                               EACH CATALOG FIELD TO BE PROCESSED.
*                               LENGTH OF CTGFIELD IS CTGNOFLD VALUE
*                               TIMES 4.
*
CTGPLLEN EQU   *-CTGPL                 LENGTH OF CTGPL
         SPACE 1
         PRINT ON,NOGEN
         YREGS ,
         END   ,
