CICSCOMP TITLE 'C I C S C O M P  ***  CICS 3270 BUFFER COMPRESSION'
         COPY  OPTIONGB      DEFINE GLOBAL OPTIONS
         SPACE 1
         SYSPARM LIST=YES    SET GLOBAL OPTIONS
         SPACE 2
***********************************************************************
*                                                                     *
*        COPYRIGHT 1990  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 2
         PRINT &PRTSOR
***********************************************************************
*                                                                     *
*        MODULE CICSCOMP, ENTRY XZCOUT1, IS A CICS EXIT ROUTINE THAT  *
*        PERFORMS BUFFER COMPRESSION AND OPTIONAL COLOR SUBSTITUTION. *
*                                                                     *
***********************************************************************
         SPACE 1
*        ENTRY XZCOUT1       CICS INTERNAL NAME
         WXTRN COMP3270      DYNAMIC LOAD OR LKED INCLUDE        90155
CICSCOMP START 0             DERIVATIVE WORK: EXHABASE/COMP3270  90152
         USING CICSCOMP,R12  DECLARE MY (NEW) BASE
         STM   R14,R12,12(R13)  SAVE CICS REGISTERS
         LR    R12,R15       SET MY BASE
         LA    R9,SAVE       GET MY SAVE AREA
         ST    R13,4(,R9)    MAKE BACK POINTER
         ST    R9,8(,R13)    STORE FORWARD POINTER
         SPACE 1
         USING DFHUEPAR,R1   PASSED PARAMETER LIST
         L     R7,UEPHMSA    GET DFHZCB PARAMETER AREA
         LM    R8,R10,20+8*4(R7)  EXTRACT TIOA/RPL/TCTTE ADDRESSES
         LTR   R8,R8         TIOA ADDRESS PASSED ?
         BZ    EXITOK        NO; NOT A 3270
         USING IFGRPL,R9     DECLARE THE RPL
         USING DFHTCTTE,R10  DECLARE THE TCT
TCTTEAR  EQU   10            CICS MACRO ERROR
         LM    R3,R4,UEPGAA  LOAD GLOBAL WORK AREA ADDRESS/SIZE
         LTR   R4,R4         ANY SIZE POINTER
         BZ    *+8           NO
         L     R4,0(,R4)     LOAD WORK AREA SIZE
         LTR   R4,R4         ANY LENGTH ?                        90155
         BP    *+6           YES                                 90155
         SLR   R3,R3         ELSE ZERO BASE                      90155
         SPACE 2
         ICM   R15,15,ADDRCOMP  COMPRESS ROUTINE LOADED ?
         BP    LOADED
         BM    CHEKSIZE      NO; NORMAL CRT SEND
*        LOAD THE BUFFER COMPRESSION ROUTINE
         LOAD  EPLOC=NMCOMPR,ERRET=NOTCMP70
         ST    R0,ADDRCOMP   SET COMPRESSION ADDRESS
         B     LOADED        CONTINUE WITH COMPRESSION CODE
         SPACE 1
NOTCMP70 OI    ADDRCOMP,X'80'  FLAG - ATTEMPTED, BUT NOT LOADED
         WTO   '3270 BUFFER COMPRESSION UNAVAILABLE',DESC=1,ROUTCDE=1
         B     CHEKSIZE      CONTINUE WITH RU SIZE CHECK
         SPACE 2                                                 90140
LOADED   XC    COMPPARM(OPSEQLEN),COMPPARM  CLEAR PARM AREA
         L     R2,RPLAREA    POINT TO ACTUAL MSG.
         L     R1,RPLRLEN    GET INPUT LENGTH
         ST    R1,CPINLEN    SET IT
         LH    R14,TCTEDSCZ  GET SCREEN SIZE
         TM    TCTE32SF,TCTEALW  ALT. SIZE ?
         BZ    *+8           NO
         LH    R14,TCTEASCZ  YES; LOAD ALTERNATE SIZE
         ST    R14,CPBUFLEN  STASH IT                            90140
         MVI   OPFUN,OFYCCW  CCW PRESENT                         90140
         MVI   OPADD,OPA14   14-BIT ADDRESSING (WHO KNOWS ?)
         TM    TCTE32EF,TCTTEEDS+TCTTECOL  COLOR ORDERS ?
         BNO   *+8           NO
         MVI   OPCOL,OPSEVEN 7-COLOR AVAILABLE                   90140
         TM    TCTE32EF,TCTTEEDS+TCTTEHIL  EXTENDED HIGH-LIGHTING ?
         BNO   *+8           NO
         MVI   OPHIGH,OPSEVEN  SET HIGH-LIGHTING SUPPORTED
         LA    R6,IOAREA               POINT TO OUTPUT AREA      90140
         ST    R6,COMPPARM   SET OUTPUT BUFFER ADDRESS           90140
         LH    R14,=Y(IOAREALN) SET MAXIMUM OUTPUT AREA
         ST    R14,CPOUTLEN  STASH IN PARM LIST
         LA    R14,CPOUTLEN  GET POINTER TO OUTPUT LENGTH        90140
         ST    R14,COMPPARM+4  STASH IT                          90140
         ST    R2,COMPPARM+8  SET INPUT ADDRESS
         LA    R14,CPINLEN   POINT TO INPUT LENGTH               90140
         ST    R14,COMPPARM+12  STASH IN LIST                    90140
         LA    R14,CPBUFLEN  GET BUFFER LENGTH                   90140
         ST    R14,COMPPARM+16  STASH                            90140
         LA    R14,OPFUN     POINT TO OPTION LIST                90140
         ST    R14,COMPPARM+20  STASH IN LIST                    90140
         LA    R1,COLORTAB   GET DEFAULT TABLE
         LTR   R3,R3         ANY GLOBAL WORK AREA ?
         BZ    COMPRESS      NO; USE DEFAULT TABLE
         LM    R3,R5,0(R3)   LOAD SCAN POINTERS FOR TABLE
         SR    R3,R4         ADJUST FOR FIRST BXH
         BNP   COMPRESS      GLOBAL AREA NOT INITIALIZED
LOOKTCT  BXH   R3,R4,COMPRESS   NOT FOUND - USE DEFAULT
         C     TCTTEAR,0(R3)  MATCHING TCT ENTRY ?
         BNE   LOOKTCT       NO; TRY AGAIN
         L     R1,4(,R3)     GET DESIRED COLOR TABLE
         SPACE 1
         ST    R1,COMPPARM+24  SET DEFAULT TABLE
COMPRESS LA    R1,COMPPARM   GET PARM LIST
         L     R15,ADDRCOMP  GET COMPRESSION ROUTINE ADDRESS     90140
         BALR  R14,R15       CALL IT                             90140
         CH    R15,=H'8'     DID WE MAKE A BOOBOO ?              90140
         BNL   CHEKSIZE      YES; DO THE SLOW WAY
         L     R1,COMPPARM   GET OUTPUT AREA BACK
         ST    R1,RPLAREA    AND PASS IT
         L     R0,CPOUTLEN   GET NEW OUTPUT LENGTH               90140
         ST    R0,RPLRLEN    SAVE LENGTH OF SEND
         SPACE 1
*        THIS ROUTINE SETS THE LMPEO BIT WHEN THE BUFFER SIZE
*        EXCEEDS THE RU SIZE FOR THE DEVICE. THIS MAKES SEND A LITTLE
*        BIT FASTER (LESS VTAM OVERHEAD).
*
CHEKSIZE DS    0H            NOT DONE YET
         SPACE 2
EXITOK   SLR   R15,R15       SET GOOD RETURN
EXITEXIT L     R13,4(,R13)   GET OLD SAVE AREA
         L     R14,12(,R13)  GET RETURN ADDRESS
         LM    R0,R12,20(R13)  GET OTHER VALUES
         BR    R14           RETURN TO CALLER
         SPACE 2
NMCOMPR  DC    CL8'COMP3270'  COMPRESSION ROUTINE                90140
ADDRCOMP DC    V(COMP3270)   ADDRESS OF COMPRESSION ROUTINE      90155
         SPACE 1
*        COLOR SUBSTITUTION TABLE
*
COLORTAB DC    C'4646464623230000'
         DC    C'4646464623230000'
         DC    C'1111555577770000'
         DC    C'1111555577770000'
         DC    X'00',C'5234567'     REPLACE DARK BLUE BY CYAN
         DC    C'01234567'   RESERVED (PROPOSED EXTENSION)
         DC    CL64'0'       RESERVED (BACKGROUND COLOR BY ATTR)
         SPACE 1                                                 90141
         LTORG ,                                                 90141
         SPACE 2
SAVE     DS    0D,18A(0)     NON-RE-ENTRANT WORK AREA
COMPPARM DS    8A            COMP3270 PARM LIST                  90140
CPOUTLEN DS    F             OUTPUT LENGTH                       90140
CPINLEN  DS    F             INPUT LENGTH                        90140
CPBUFLEN DS    F             BUFFER SIZE                         90140
OPFUN    DS    X             REQUESTED FUNCTIONS                 90140
OFNCMP   EQU   X'40'           BYPASS BUFFER COMPRESSION, ETC.   90140
OFNCOL   EQU   X'20'           BYPASS COLOR SUBSTITUTION         90140
OFYESC   EQU   X'10'           BUFFER MAY CONTAIN ESCAPE (BTAM)  90140
OFYCCW   EQU   X'08'           BUFFER CONTAINS CCW               90140
OFYWCC   EQU   X'04'           BUFFER CONTAINS WCC/PCC           90140
OPADD    DS    X             ADDRESSING MODE                     90140
OPA16    EQU   X'02'           USE 16-BIT ADDRESSING ONLY        90140
OPA14    EQU   X'01'           14-BIT ADDRESSING SUPPORTED       90140
OPCOL    DS    X             COLOR MODE                          90140
OPSEVEN  EQU   X'07'           7-COLOR MODE                      90140
OPBACK   EQU   X'70'           BACKGROUND COLOR AVAILABLE        90140
OPHIGH   DS    X             EXTENDED HIGH-LIGHTING              90140
*PSEVEN  EQU   X'07'           ALL EXTENDED HIGH-LIGHTING        90140
OPMISC   DS    X             MISCELLANEOUS OPTIONS               90140
OPFVAL   EQU   X'80'           FIELD VALIDATION SUPPORTED        90140
OPFOUT   EQU   X'40'           FIELD OUTLINING                   90140
OPSEQLEN EQU   *-COMPPARM    LENGTH TO CLEAR                     90140
         SPACE 1
IOAREA   DS    29CL256       OUTPUT BUFFER AREA
IOAREALN EQU   *-IOAREA
         SPACE 2
         PRINT &PRTMAC
         DFHUEXIT TYPE=EP    MAP EXIT PARAMETER LIST
         SPACE 2
         COPY  DFHTCTTE      EXPAND TCT MAPPING
         SPACE 1
         PRINT &PRTSYS
         REGEQU ,            DEFINE MNEMONIC REGISTERS
         SPACE 1
         IFGRPL ,
         END   ,
