BLKSZ    TITLE 'S U B B L K S Z  ***  GET OPTIMUM BLOCK SIZE'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES    SET A FEW GLOBALS
         SPACE 1
         PRINT &PRTSOR
***********************************************************************
**                                                                   **
**    SUBBLKSZ CALCULATES THE OPTIMUM BLOCK SIZE GIVEN A UCB IN R0   **
**    AND A KEY LENGTH IN R1. RETURN IS IN R0 (BLKSIZE); R1=TRKCAP   **
**                                                                   **
***********************************************************************
SUBBLKSZ PGMHEAD ZERO,BASE=R12,PARM=R1   DECLARE A FEW THINGS
         LR    R3,R0         COPY UCB
         LR    R7,R1         COPY KEY LENGTH
         USING UCBOB,R3      DECLARE IT
         L     R4,CVTPTR
         USING CVTMAP,R4
         L     R4,CVTZDTAB   GET DEVICE CHARACTERISTICS TABLE
         CLI   UCBTBYT3,UCB3DACC       DASD?                    GP09217
         BE    HAVEDASD                                         GP09217
         LM    R1,R3,=A(UNITTABL,UNITTAB2-UNITTABL,UNITTABN)    GP09217
         ICM   R0,3,UCBTBYT3                                    GP09217
LOOPOTH  CLM   R0,3,0(R1)                                       GP09217
         BE    HAVEOTH                                          GP09217
         BXLE  R1,R2,LOOPOTH                                    GP09217
HAVEOTH  LH    R15,2(R1)     LOAD MAX SIZE                      GP09217
         ST    R15,RR1CODE   MAX SIZE SAME                      GP09217
         ST    R15,RSNCODE   AS DEFAULT                         GP09217
         B     PGMEXIT       RETURN IT                          GP09217
         SPACE 1
HAVEDASD SR    R15,R15
         IC    R15,UCBTBYT4  GET DASD TYPE
         N     R15,=A(DVCTYPMK)  MASK FUNNY BITS
         SR    R14,R14
         IC    R14,0(R15,R4) GET OFFSET TO DEVICE ENTRY
         LA    R3,0(R14,R4)  GET DEVICE ENTRY
         DROP  R3,R4
         SR    R6,R6
         ICM   R6,3,DVCTRKLN-DVCT(R3)  GET MAX RAW SIZE
         ST    R6,RR1CODE              RETURN TO USER           GP09217
         LA    R5,1
         CH    R6,=H'32760'
         BNH   *+8
         LA    R5,2
         BAL   R9,NUMMAX
         SPACE 1                                                GP97335
         PRINT &PRTSOR
PGMEXIT  PGMEXIT COPYRET=(RETCODE,12) RETURN R15, R0, AND R1
         SPACE 2                                                 89019
*        SUBROUTINE TO GET MAXIMUM BLOCKSIZE FOR N RECORDS PER TRACK.
*        N IS PASSED IN R5, STARTING BLOCKSIZE IN R4. R15-R2 WORK
*        RETURN ON R9; SIZE 0 (NO FIT) OR MAX IN R0              89019
NUMMAX   XC    RSNCODE,RSNCODE  CLEAR THE RETURN                 89019
         L     R4,=A(32768)  SET MAXIMUM FIRST GUESS             89019
         LR    R2,R4         START WITH MAX OR SO                89019
NUMMAXLP SRL   R2,1          HALVE                               89019
         CR    R4,R6         NOT EXCEEDING RAW LENGTH ?
         BH    NUMMAXLO                                          89019
         TRKCALC FUNCTN=TRKCAP,DEVTAB=(R3),                            *
               R=1,K=(R7),DD=(R4),     RECORD # ONE, ZERO KEY          *
               REGSAVE=YES,MF=(E,MAPCALC)   GET # PER TRACK      89019
         O     R15,RETCODE   MERGE  POSSIBLE WARNING OR ERROR   GP08127
         ST    R15,RETCODE   RETURN POSSIBLE WARNING OR ERROR   GP08127
         CR    R0,R5         DESIRED NUMBER OF RECORDS ?         89019
         BL    NUMMAXLO      NO; FEWER. DECREASE BLOCK SIZE      89019
         BH    NUMMAXUP      NO; HIGHER                          89019
         ST    R4,RSNCODE    EQUAL; SAVE CURRENT COUNT           89019
NUMMAXUP AR    R4,R2         INCREASE BLOCK SIZE                 89019
         B     NUMMAXHA      DECREMENT INCREMENT                 89019
NUMMAXLO SR    R4,R2         DECREMENT BLOCK SIZE                89019
         BP    NUMMAXHA      OK                                  89019
         AR    R4,R2         OOPS                                89019
NUMMAXHA LTR   R2,R2         LAST TIME ?                         89019
         BP    NUMMAXLP      NO; TRY AGAIN                       89019
*inEXIT  L     R0,RSNCODE    RETURN LARGEST SIZE FOUND           89019
         BR    R9                                                89019
         SPACE 1
UNITTABL DC    X'0801',AL2(80)    2540 R                        GP09217
UNITTAB2 DC    X'0802',AL2(80)    2540 P                        GP09217
         DC    X'0802',AL2(80)    2540 P                        GP09217
         DC    X'0803',AL2(80)    1443 R/P                      GP09217
         DC    X'0804',AL2(80)    2501 P                        GP09217
         DC    X'0805',AL2(80)    2520 R/P                      GP09217
         DC    X'0806',AL2(80)    3505 P                        GP09217
         DC    X'0808',AL2(132)   1403 PRT                      GP09217
         DC    X'0809',AL2(132)   3211 PRT                      GP09217
         DC    X'080A',AL2(132)   1443 PRT                      GP09217
         DC    X'080B',AL2(132)   3203 PRT                      GP09217
         DC    X'080C',AL2(132)   3525 PRT                      GP09217
         DC    X'080E',AL2(132)   3800 PRT                      GP09217
         DC    X'080F',AL2(132)   A.F. PRT                      GP09217
         DC    X'0811',AL2(132)   4245 PRT                      GP09217
         DC    X'0813',AL2(132)   4248 PRT                      GP09217
         DC    X'0820',AL2(120)   1052 CON                      GP09217
         DC    X'0821',AL2(120)   2150 CON                      GP09217
         DC    X'0822',AL2(120)   3210 CON                      GP09217
         DC    X'0823',AL2(120)   3215 CON                      GP09217
UNITTABN DC    X'0830',AL2(120)   3213 CON                      GP09217
         DC    X'FFFF',AL2(32760)      ALL ELSE (TAPE, COMM, ETC.)
         SPACE 1
         PRINT &PRTSYS
SAVE     DSECT ,             SAVE AND WORK AREA
         SERVDEFS ,
MAPCALC  SERVCALC MF=D
SAVEEND  EQU   *
         SPACE 1
         CVT   DSECT=YES
         IEFUCBOB ,
         IHADVCT ,
         END   ,
