CLRD     TITLE 'C L E A R D S N  ***  CLEAR/INITIALIZE DASD DSN'
         MACRO ,                                                 87162
&NM      MGT   &DS,&JF                                           87162
         LCLC  &L                                                87162
&L       SETC  'L'''                                             87162
&NM      DC    AL1(&L.DS1&DS-1,DS1&DS-IECSDSL1,JFC&JF-INFMJFCB)  87162
         MEND  ,                                                 87162
         SPACE 1                                                 87162
         PUNCH ' ALIAS CLEAREOF,CLEARDA '                       GP06267
         PUNCH '  ORDER CLEARDSN(P) '  MAKE DUMP READING EASIER GP06267
         SPACE 1                                                 87162
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 1984,1987  EXPERT SYSTEM PROGRAMMING, INC.         *
*        COPYRIGHT 2003       EXPERT SYSTEM PROGRAMMING               *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR
CLEARDSN PGMHEAD ZERO15,PARM=R9,BASE=(R11,R12),                        *
               ENTRY=(CLEAREOF,CLEARDA),ENTNO=FLAG2              87162
*
*        AUTHOR :  G. POSTPISCHIL
*
*        ENTRY CLEARDSN OF THIS PROGRAM ERASES ALL TRACKS OF A
*        DATASET BY WRITING AN END-FILE ON EACH TRACK.
*        ENTRY CLEAREOF LOCATES THE LAST RECORD IN SUCH A
*        PRE-CLEARED DATASET, AND ADDS AN END-FILE (AND UPDATES
*        LSTAR) AS NECESSARY.
*        IN COMBINATION, THE TWO FUNCTIONS MAY BE USED TO MAINTAIN
*        SEQUENTIAL DATASETS OVER SYSTEM/LINE CRASHES.
*
*        1) ALLOCATE DSN; RUN CLEARDSN
*        2) USING DISP=MOD, ADD NEW RECORDS.
*        3) PRIOR TO NEXT USE (BEFORE OR AFTER A CRASH), RUN
*          CLEAREOF TO ENSURE CORRECT LSTAR AND DISP=MOD USAGE.
*                                                                87162
*   ENTRY CLEARDA ADDED TO (RE)INITIALIZE FORTRAN (AND OTHER)    87162
*   DIRECT ACCESS FILES.                                         87162
*   CHECK FOR DSORG=DA RELAXED TO PERMIT DSORG=PS (WYLBUR PAGE) GP11185
         SPACE 1
         SERVINIT ,          LOAD THE SERVICE ROUTINE
         SERVLOAD @PRINTER   LOAD THE PRINT ROUTINE
         SPACE 1
         LA    R9,0(,R9)     CLEAR THE PARM ADDRESS POINTER
         LTR   R9,R9         ANY ?
         BZ    NOPARM        NO
         ICM   R9,7,1(R9)    GET PARM ADDRESS
         SLR   R1,R1
         ICM   R1,3,0(R9)    TEST LENGTH OF PARM STRING
         BNP   NOPARM        NOTHING OR ERRONEOUS LENGTH
PARMAGIN CLI   2(R9),C' '    BLANK ?
         BE    PARMSKP1      YES; SKIP IT
         CLI   2(R9),C','    COMMA ?
         BNE   PARMTEST      NO; TEST LENGTH/PARAMETER
PARMSKP1 LA    R9,1(,R9)     UP
         BCT   R1,PARMAGIN
         B     NOPARM        DONE
PARMTEST LA    R15,3         L'DUMP-1
         CR    R1,R15        LONG ENOUGH FOR DUMP ?
         BNH   PARMBAD       NO; BAD PARM
         CLC   =C'DUMP',2(R9)  DUMP REQUEST ?
         BNE   PARMTST1      NO
         OI    FLAG,FGDUM    SET DUMP MODE
         B     PARMBUMP
PARMTST1 LA    R15,4         L'DEBUG-1
         CR    R1,R15        LONG ENOUGH FOR DEBUG MODE ?
         BNH   PARMBAD       NO; BAD PARM
         CLC   =C'FILL',2(R9)  CLEARDA FILLER ?                  87162
         BE    PARMFILL      YES; EXTRACT VALUE                  87162
         CLC   =C'PRINT',2(R9)  NEGATED 'NOPRINT' REQUEST ?      84115
         BNE   PARMTS1A      NO                                  84115
         NI    FLAG,255-FGNOPRT  RESET IT                        84115
         B     PARMBUMP                                          84115
PARMTS1A CLC   =C'DEBUG',2(R9)  DEBUG MODE ?                     84115
         BNE   PARMTST2      NO
         OI    FLAG,FGDEB    SET DEBUG MODE
         B     PARMBUMP
PARMTST2 LA    R15,5         L'DELETE-1
         CR    R1,R15        LONG ENOUGH ?
         BNH   PARMBAD       NO; TOO BAD
         CLC   =C'DELETE',2(R9)  UNCONDITIONAL DELETION ?
         BNE   PARMTST3      NO
         TM    FLAG2,FGEOF   EOF FUNCTION ?                      84115
         BNZ   PARMBAD       YES; MEANINGLESS REQUEST            84115
         OI    FLAG,FGPRIVY  YES
         B     PARMBUMP                                          84115
PARMTST3 LA    R15,6         L'NOPRINT-1                         84115
         CR    R1,R15        LONG ENOUGH FOR NPRINT ?            84115
         BNH   PARMBAD       NO; BAD PARM                        84115
         CLC   =C'NOPRINT',2(R9)  DEBUG MODE ?                   84115
         BNE   PARMBAD       NO                                  84115
         OI    FLAG,FGNOPRT  SET NO PRINT PROCESSING             84115
PARMBUMP AR    R9,R15        UP SCAN POINTER
         SR    R1,R15        GET RESIDUAL LENGTH
         BP    PARMSKP1      SKIP ONE MORE
         B     NOPARM        ELSE DONE
PARMFILL CLI   6(R9),C','    NULL PARM ?                         87162
         BE    PARMBUMP      YES; IGNORE                         87162
         MVC   FILLCHAR,6(R9)  ELSE USE FILLER                   87162
         LA    R0,5          TRY FOR 6                           87162
         CR    R1,R0         LONGER THAN 5 ?                     87162
         BNH   PARMBUMP      NO; SINGLE BYTE ONLY                87162
         LR    R15,R0        SET FOR 6-BYTE SEQUENCE             87162
         CLI   7(R9),C','    UNLESS TERMINATION                  87162
         BE    PARMBUMP      YES                                 87162
         MVC   DB(2),6(R9)   MOVE TWO BYTES                      87162
         NC    DB(2),=X'1F1F'  KILL HIGH ZONE BITS               87162
   TR    DB(2),=X'000A0B0C0D0E0F00000000000000000000010203040506070809'
         PACK  DB+4(2),DB(3)  PACK                               87162
         MVC   FILLCHAR,DB+4   MOVE DESIRED CHARACTER            87162
         B     PARMBUMP      DO SOME MORE                        87162
PARMBAD  OI    FLAG,FGBADPRM   SET PARM ERROR                    84115
         LA    R9,1(,R9)     SKIP A BIT                          84115
         SH    R1,=H'1'      REDUCE RESIDUAL LENGTH              84115
         BP    PARMAGIN      TRY SOME MORE                       84115
         SPACE 1                                                 84115
NOPARM   TM    FLAG,FGNOPRT  SUPPRESS NORMAL OPEN ?              84115
         BNZ   PRTONCON      YES; PRINT ON CONSOLE               84115
         PRTOPEN SYSPRINT,OPT=(NOWTO,DUMMY)                      84115
         MVI   ERRDEV,1      SET TO SEND ERROR MESSAGES TO //    84115
         CH    R15,=H'4'     ANY ERROR IN OPEN ?                 84115
         BNH   PRTOPNOK      NO                                  84115
PRTONCON MVI   ERRDEV,2      SEND ERROR MESSAGES TO CONSOLE      84115
         PRTOPEN CONSOLE,DEV=2                                   84115
PRTOPNOK PRTLIST HEADER,TITLE=1  MAKE A HEADER                   84115
         TM    FLAG2,FGLOAD  BDAM LOAD MODE ?                    87162
         BZ    TESTPRMR      NO                                  87162
         PRTLIST MGSHOFIL    DISPLAY FILL CHARACTER              87162
TESTPRMR TM    FLAG,FGBADPRM  PARM FIELD ERROR ?                 84115
         BZ    LOOPTIOT      NO; START TIOT SCAN                 84115
         PRTL  'INVALID PARM FIELD',CC=NO                        84115
         B     BOMB
         SPACE 1                                                 84115
LOOPTIOT L     R1,ADTIOT     GET PREVIOUS TIOT ADDRESS           84115
         SERVCALL TIOLK,(R1)  LOOK FOR NON-SPECIAL TIOT ENTRIES  84115
         LTR   R2,R0         ANY FOUND ?                         84115
         BZ    ENDITALL      NO MORE                             84115
         ST    R2,ADTIOT     SAVE FOR NEXT TIME                  84115
         USING TIOELNGH,R2   DECLARE IT                          84115
         CLC   =C'SYSUT',TIOEDDNM  OUR NAME ?                    84115
         BE    HAVEDDNM      YES                                 84115
         CLC   =C'SYSLIB',TIOEDDNM  ALTERNATE ?                  84115
         BNE   LOOPTIOT      NO; TRY ANOTHER                     84115
HAVEDDNM MVI   BEGBLANK,C' '  SET BLANK
         MVC   BEGBLANK+1(LENBLANK-1),BEGBLANK  PROPAGATE
         MVC   DDNAME,TIOEDDNM  SAVE DD NAME FOR MESSAGES        84115
         SERVCALL TIODD,TIOEDDNM  LOOK FOR DUPLICATE DD          84115
         CR    R0,R2         SAME ENTRY ?                        84115
         BNE   DUPEDD        NO; FAIL                            84115
         NI    FLAG,255-FGRECF  RESET RECFM FLAG                 84115
         OI    FLAG,FGFOUND  AT LEAST ONE DD CARD FOUND          84115
         XC    BEGZERO(256),BEGZERO  RE-INITIALIZE               84115
         XC    BEGZERO+256(LENZERO-256),BEGZERO+256   WORK SPACE 84115
         MVC   SYSLIB(SYSLIBL),PATSYS  MOVE DCB
         TM    FLAG2,FGLOAD  LOAD MODE ?                         87162
         BZ    *+10          NO                                  87162
         MVC   SYSLIB(PATSADL),PATSAD  USE BSAM LOAD DCB INSTEAD 87162
         SPACE 1
         LA    R8,SYSLIB
         USING IHADCB,R8     DECLARE DCB
         LA    R1,EXLST      GET EXIT LIST
         STCM  R1,7,DCBEXLSA   SET EXIT LIST ADDRESS
         LA    R0,INFMJFCB   GET JFCB ADDRESS
         ST    R0,EXLST      MAKE EXIT LIST
         MVI   EXLST,X'87'   LAST/JFCB
         LA    R1,SYSLIB
         ST    R1,OPLIST     MAKE OPEN LIST
         MVI   OPLIST,X'80'  SET END LIST BIT
         MVC   DCBDDNAM,TIOEDDNM  SAVE DD NAME                   84115
         MVC   ERASER(PATERASL),PATERASE
         LA    R14,IOBCC-IOBSTDRD+MIOB  GET IOBSEEK ID PORTION
         STCM  R14,7,ERASER+1  MAKE CCW ADDRESS
         LA    R14,ERASER    GET TIC BACK ADDRESS
         STCM  R14,7,ERASER+8+1
         LA    R14,DRCOUNT   GET EOF COUNT FIELD (R1)
         STCM  R14,7,ERASER+16+1
         MVC   READCCWS(16),ERASER  COPY SEEK PORTION
         LA    R14,READCCWS
         STCM  R14,7,READCCWS+8+1   BUT UPDATE TIC
         MVI   DRCOUNT+4,1   SET RECORD # FOR EOF COUNT FIELD
         DEVTYPE DCBDDNAM,DB SYSLIB DD PRESENT ?
         BXLE  R15,R15,HAVELIB
         MVC   DCBDDNAM,=CL8'SYSUT1'  USE OLD NAME
         MVC   DDNAME,DCBDDNAM
         DEVTYPE DCBDDNAM,DB  SYSUT1 PRESENT ?
         BXLE  R15,R15,HAVELIB   YES
DDNOTUSA PRTLIST MGNOTUSA,DEV==ERRDEV  DD NOT USABLE             84115
         OICC  8             BOOBOO                              84115
         B     LOOPTIOT      TRY ANOTHER                         84115
DUPEDD   PRTLIST MGDUPEDD,DEV==ERRDEV  DUPLICATE DD              84115
SETCODE8 OICC  8                                                 84115
         B     LOOPTIOT      TRY MORE                            84115
DDNOTDAS PRTLIST MGNOTDAS,DEV==ERRDEV                            84115
         OICC  4                                                 84115
         B     LOOPTIOT                                          84115
DDBADORG PRTLIST MGBADORG,DEV==ERRDEV                            84115
         OICC  8                                                 84115
         B     LOOPTIOT                                          84115
         SPACE 1                                                 84115
HAVELIB  CLI   DB+2,UCB3DACC DIRECT ACCESS RESIDENCE ?
         BNE   DDNOTDAS      NO; BOOBOO                          84115
         RDJFCB MF=(E,OPLIST)  GET THE SYSLIB JFCB
         BXH   R15,R15,DDNOTUSA  QUIT IF ERROR
         SERVCALL UCBGN,DB-(UCBTBYT1-UCBOB)  PASS DEVTYPE
         MVC   UNITTYPE,0(R1)  SAVE GENERIC
         SERVCALL TIODD,DDNAME  GET TIOT ENTRY
         USING TIOENTRY,R2
         LTR   R2,R0         ANY TIOT ENTRY ADDRESS ?
         BZ    DOPRINT
         ICM   R3,7,TIOEFSRT  GET THE UCB
         BZ    DOPRINT
         USING UCBOB,R3
         ST    R3,UNITADDR
         UCBDEVN UCBPTR=(R3),DEVN=UNITNAME                      GP11185
         TM    UCBTBYT3,UCB3DACC+UCB3TAPE  VOLSER PRESENT ?
         BNM   DOPRINT       NO
DOPRINT  PRTLIST SHOWNAME    SHOW BASIC INFORMATION
         DROP  R2,R3
         CLI   JFCDSRG2,0    CRUD ?
         BNE   DDBADORG      YES
         TM    FLAG2,FGLOAD  LOAD MODE ?                         87162
         BZ    TESTJSEQ      NO                                  87162
         TM    JFCDSRG1,255-JFCORGDA  BDAM ?                     87162
         BZ    TESTJCOM        YES (OR NULL); ACCEPT IT         GP11185
         CLI   JFCDSRG1,JFCORGPS      SEQ?                      GP11185
         BNE   DDBADORG      NO; ERROR                          GP11185
         B     TESTJCOM                                          87162
TESTJSEQ TM    JFCDSRG1,255-JFCORGPS-JFCORGU  NON-SEQUENTIAL ?
         BNZ   DDBADORG      YES; TOO BAD                        84115
TESTJCOM CLI   JFCBELNM,0    ANY MEMBER/GDG NAME ?
         BE    MEMOKJ        NO
         TM    JFCBIND1,JFCPDS  MEMBER NAME ?
         BNZ   DDBADORG      YES; NOT SUPPORTED
*        TM    JFCBIND1,JFCGDG  GDG NAME ?
*        BZ    MEMOKJ        NO; WHAT IS IT ? WHO CARES ?
MEMOKJ   TM    JFCBIND2,JFCDISP   DISP=MOD BIT ON
         BNM   DISPNMOD      NO
         TM    JFCBIND2,JFCMOD   DISP= MOD ?
         BNZ   DISPMOD       YES; OK
DISPNMOD PRTLIST MGDSPMOD    WARNING MESSAGE                     84115
         NI    JFCBIND2,255-JFCDISP
         OI    JFCBIND2,JFCMOD   SET MOD
DISPMOD  NI    JFCBIND1,255-JFCRLSE  DO NOT RELEASE STORAGE
*        OI    JFCBTSDM,JFCNWRIT+JFCNDCB  NO REWRITE
         SERVCALL DSDJ1,JFCBDSNM  GET THE FORMAT 1 DSCB
         BXLE  R15,R15,DDSCBOK
         PRTLIST MGBADOBT,DEV==ERRDEV  MAKE ERROR MESSAGE        84115
         OICC  4             SET WARNING                         84115
         B     LOOPTIOT      TRY ANOTHER                         84115
         SPACE 1                                                 84115
DDSCBOK  MVC   DS1FMTID(DS1END-DS1FMTID),0(R1)  COPY DSCB DATA
         BAS   R9,FORMFMT1   FORMAT DSCB 1 INFORMATION          GP03074
         CLI   DS1DSORG+1,0  BAD DSORG ?
         BNE   DDBADORG      YES; KILL
         TM    FLAG2,FGLOAD  LOAD MODE ?                         87162
         BZ    TESTOSEQ      NO                                  87162
         TM    DS1DSORG,255-JFCORGDA-JFCORGPS  DIRECT OR SEQ?   GP11185
         BNZ   DDBADORG                                          87162
         TM    JFCDSRG1,JFCORGPS+JFCORGDA   SET BY USER ?       GP11185
         BNZ   *+4+4              YES; KEEP IT                  GP11185
         OI    JFCDSRG1,JFCORGDA  MAKE SURE OF DSORG=DA          87162
         ICM   R0,3,DS1BLKL  HAVE A BLOCKSIZE ?                  87162
         BNZ   DOOPEN        YES; PROCESS                        87162
         ICM   R0,3,JFCBLKSI  USER SUPPLIED ?                    87162
         BNZ   DOOPEN        ALSO OK                             87162
         MVC   DS1BLKL,DS1LRECL  PRESUME                         87162
         ICM   R0,3,DS1LRECL   OR RECORD LENGTH ?                87162
         BNZ   DOOPEN                                            87162
         MVC   JFCBLKSI,JFCLRECL  PRESUME                        87162
         ICM   R0,3,JFCLRECL  LAST RESORT ?                      87162
         BNZ   DOOPEN                                            87162
         PRTLIST MGBADDCB,DEV==ERRDEV                            87162
         B     SETCODE8                                          87162
TESTOSEQ TM    DS1DSORG,255-JFCORGPS-JFCORGU  PS ONLY ?
         BNZ   DDBADORG      NO; FAIL
DOOPEN   OPEN  (,OUTPUT),MF=(E,OPLIST),TYPE=J
         NI    DCBOFLGS,255-DCBOFLWR  NO WRITE, YET.
         LA    R0,DS1DSNAM                                       87162
         LA    R15,INFMJFCB                                      87162
         LA    R4,MERGTAB    GET MERGE TABLE                     87162
MERGLOOP SLR   R14,R14                                           87162
         SLR   R1,R1                                             87162
         SLR   R2,R2                                             87162
         ICM   R2,1,0(R4)    GET LENGTH BYTE                     87162
         BM    MERGDONE      NO MORE                             87162
         IC    R14,1(,R4)    GET DSCB FIELD OFFSET               87162
         IC    R1,2(,R4)     GET JFCB FIELD OFFSET               87162
         AR    R14,R0        RELOCATE                            87162
         AR    R1,R15        DITTO                               87162
         EX    R2,MERGCLC    IS THE FIELD ZERO ?                 87162
         BZ    MERGBUMP      YES; DON'T OVERRIDE                 87162
         EX    R2,MERGMVC    ELSE REPLACE DSCB BY JFCB VALUE     87162
MERGBUMP LA    R4,3(,R4)     GET NEXT ENTRY                      87162
         B     MERGLOOP      TRY IT AGAIN                        87162
MERGCLC  OC    0(0,R1),0(R1) IS JFCB FIELD ZERO ?                87162
MERGMVC  MVC   0(0,R14),0(R1) REPLACE DSCB FIELD BY JFCB         87162
MERGTAB  MGT   BLKL,BLKSI                                        87162
         MGT   LRECL,LRECL                                       87162
         MGT   KEYL,KEYLE                                        87162
         MGT   RECFM,RECFM                                       87162
         MGT   OPTCD,OPTCD                                       87162
         MGT   DSORG,DSORG                                       87162
         DC    X'FF'         END OF TABLE                        87162
         SPACE 1
MERGDONE SLR   R14,R14
         ICM   R0,12,DS1BLKL  USER SPECIFIED BLOCKSIZE ?
         BZ    SKIPRCFM      NO
         TM    DS1RECFM,JFCUND  ANY RECORD FORMAT ?
         BNM   SKIPRCFM      NEITHER F NOR V
         TM    DS1RECFM,JFCFIX  RECFM=F ?
         BZ    SKIPRCFM      NO; NO V TESTS
         ICM   R14,3,DS1LRECL  RECORD LENGTH SET ?
         BNP   SKIPRCFM      NO, OR BAD - SKIP IT
         SRDL  R0,16+32      PREPARE FOR DIVIDE
         DR    R0,R14        GET RECORDS/BLOCK
         LTR   R0,R0         TEST REMAINDER
         BNZ   SKIPRCFM      LRECL>BLKSIZE OR OTHER FUNNY
         OI    FLAG,FGRECF   SET FOR RECFM=F CHECKING
         SPACE 1
SKIPRCFM LA    R6,MIOB       DECLARE MY IOB
         STCM  R6,7,DCBIOBAA  SET IOB ADDRESS
         USING IOBSTDRD,R6
         MVI   IOBFLAG1,IOBCMDCH+IOBUNREL  CMD CHAIN/NOT RELATED
         LA    R0,IOECB
         ST    R0,IOBECBPT   STASH ECB ADDRESS
         LA    R0,SYSLIB     GET THE DCB ADDRESS
         ST    R0,IOBDCBPT
         MVC   STARDCTA+L'STARDCTA-L'DCBDVTBL(L'DCBDVTBL),DCBDVTBL
         MVI   STARFLGS,STARFUNC    TRKCAP,DVTAD
         SERVCALL DVCAP,STARKLE  GET EOF RECORDS/TRACK
         STH   R0,MAXRPT     SAVE MAXIMUM RECORDS/TRACK
         STH   R0,MAXURCD    SAVE MAXIMUM USER RECORDS           84115
         MVC   MAXBAL,STARBAL  SAVE TRACK BALANCE FOR EMPTY TRACK
         MVC   STARKL,DS1KEYL     AND KEY LENGTH                 87162
         ICM   R0,3,DS1BLKL  GET THE DSCB SIZE                   87162
         BZ    POSTMAXR      SKIP IF NONE                        84115
         STH   R0,STARDL                                         84115
         SERVCALL DVCAP,STARKLE                                  84115
         STH   R0,MAXURCD    SAVE MAXIMUM USER RECORDS           84115
POSTMAXR PRTLIST SHOWDCBX    ADD EXTENDED INFORMATION            84115
         SPACE 1
         L     R1,CVTPTR     GET THE CVT
         USING CVTMAP,R1     DECLARE IT
         MVC   DRPRLTV,CVTPRLTV  GET THE CONVERSION ROUTINE
         MVC   DRCNVT,CVTPCNVT     ADDRESSES
         SPACE 1
         L     R0,DCBDEBAD   GET THE DEB ADDRESS
         LA    R1,IOBSEEK    GET FULL DASD SEEK ADDRESS
         STM   R0,R1,DRDEB     MAKE PARAMETERS FOR CONVERT
         SPACE 1
         LA    R1,DS1EXT1    POINT TO FIRST EXTENT OF DSN
         USING XTMAP,R1      DECLARE IT
         CLI   XTTYPE,XTTLABEL  LABEL EXTENT ?
         BNE   *+8           NO
         LA    R1,DS1EXT2    POINT TO NEXT EXTENT
         MVC   IOBCC(2+2),XTLOCYL  COPY EXTENT START TO IOB
         BAS   R9,CONTOTTR   MAKE TTR OF FIRST DATA TRACK       GP03074
         MVC   BEGINTTR,CURTTR  SAVE STARTING TTR
         MVC   BEGINBAL,MAXBAL    AND TRACK BALANCE
*                THE ABOVE JUNK USED TO AVOID PROBLEMS WITH FUNNY
*                TTR VALUES FOR USER LABELS (IN CASE OF IBM CHANGES).
         TM    FLAG2,FGLOAD  LOAD MODE ?                         87162
         BZ    READNODA      NO                                  87162
         LH    R4,MAXURCD    GET NUMBER OF CCWS TO BUILD         87162
         SLR   R3,R3                                             87162
         ICM   R3,3,DS1BLKL  GET BLOCK SIZE                      87162
         SLR   R14,R14                                           87162
         IC    R14,DS1KEYL                                       87162
         LA    R3,8+8(R14,R3)  CCW+COUNT+KEY+DATA                87162
         MR    R2,R4         TIMES NUMBER OF RECORDS             87162
         LA    R3,3*8+16(,R3)  PLUS SEARCH + R0 WRITE/DATA       87162
         C     R3,MAINSIZE   ENOUGH GOTTEN PREVIOULY ?           87162
         BNH   HAVEMAIN      YES                                 87162
         LM    R1,R2,MAINAD  GET PRIOR VALUES                    87162
         LTR   R2,R2         ANY GOTTEN ?                        87162
         BZ    NEEDMAIN      NO                                  87162
         FREEMAIN R,A=(R1),LV=(R2)  FREE                         87162
NEEDMAIN XC    MAINAD(8),MAINAD  CLEAR                           87162
         GETMAIN R,LV=(R3)   GET STORAGE                         87162
         ST    R1,MAINAD                                         87162
         ST    R3,MAINSIZE                                       87162
HAVEMAIN L     R5,MAINAD     GET ADDRESS OF GOTTEN STORAGE       87162
         LR    R14,R5        START TO CLEAR                      87162
         LR    R15,R3        SIZE TO CLEAR                       87162
         SLR   R0,R0                                             87162
         SLR   R1,R1                                             87162
         ST    R0,NUMBLOCK   RESET BLOCK COUNT                   87162
         ICM   R1,8,FILLCHAR  GET DESIRED FILLER                 87162
         MVCL  R14,R0        CLEAR NEW TRACK DATA                87162
         LA    R3,8                                              87162
         MR    R2,R4         GET CCW SIZE                        87162
         LA    R3,3*8(R5,R3)   PLUS FIXED SEQUENCE               87162
         ST    R3,MAINDATA   START OF DATA                       87162
         LM    R14,R0,=X'310000004000000508000000'  SCH ID=/TIC  87162
         OR    R14,R3        POINT SEARCH TO R0 COUNT            87162
         OR    R0,R5         SET TIC -8                          87162
         SLR   R1,R1                                             87162
         STM   R14,R1,0(R5)  FIRST TWO CCWS                      87162
         ICM   R14,8,=X'05'  CHANGE TO WRITE R0 DATA             87162
         AL    R14,=F'8'     SKIP OVER R0 COUNT                  87162
         IC    R15,=AL1(8)   COUNT + OS DATA LENGTH              87162
         STM   R14,R15,16(R5)  WRITE R0 CCW                      87162
         ICM   R1,8,MAXURCD+L'MAXURCD-1                          87162
         ST    R1,12(,R3)    R0 DATA (# BLOCKS/ 000000)          87162
         LA    R1,8                                              87162
         ST    R1,4(,R3)     SET R0 COUNT RKDD                   87162
         LA    R3,16(,R3)    FIRST DATA AREA                     87162
         ICM   R1,4,DS1KEYL                                      87162
         ICM   R1,3,DS1BLKL                                      87162
         SLR   R0,R0                                             87162
         SLR   R2,R2                                             87162
         ICM   R2,3,DS1BLKL                                      87162
         IC    R0,DS1KEYL                                        87162
         AR    R2,R0         SIZE OF DATA                        87162
         LA    R2,8(,R2)     PLUS COUNT AREA                     87162
         LM    R14,R15,=X'1D00000040000000'  PATTERN WRITE CKD   87162
         OR    R14,R3        POINT TO DATA AREA                  87162
         OR    R15,R2        LENGTH                              87162
DAINLOOP STM   R14,R15,24(R5)   MAKE NEXT CCW                    87162
         AL    R1,=X'01000000'  MAKE NEXT R/K/DD VALUE           87162
         ST    R1,4(,R3)     MAKE LOW COUNT FIELD                87162
         LA    R5,8(,R5)     NEXT CCW ADDRESS                    87162
         ALR   R3,R2         NEXT DATA AREA                      87162
         ALR   R14,R2        DITTO IN CCW                        87162
         BCT   R4,DAINLOOP                                       87162
         MVI   16+4(R5),0    KILL CHAINING                       87162
         SPACE 1                                                 87162
         MVC   CURTTR(2),BEGINTTR                                87162
DAINMORE BAS   R9,CONTOABS   CONVERT TTR TO ABSOLUTE             87162
         BXH   R15,R15,DAINDONE                                  87162
         ICM   R0,15,IOBCC   GET CCHH                            87162
         L     R2,MAINAD     GET CCW ADDRESS                     87162
         L     R3,MAINDATA   GET DATA ADDRESS                    87162
         LH    R4,MAXURCD    GET RECORDS PER TRACK               87162
         ST    R0,0(,R3)     STASH HOME ADDRESS/R0 COUNT         87162
         ST    R0,8(,R3)     STASH R0 DATA                       87162
         LA    R2,24(,R2)    SKIP TO DATA CCW                    87162
DAINMORL ICM   R3,15,0(R2)   GET THE DATA ADDRESS                87162
         STCM  R0,15,0(R3)   COMPLETE THE COUNT FIELD            87162
         LA    R2,8(,R2)     NEXT CCW                            87162
         BCT   R4,DAINMORL   DO ALL                              87162
         L     R1,MAINAD     POINT TO CCWS                       87162
         BAS   R9,SCHEDIO    WRITE THE TRACK                     87162
         TM    FLAG,FGDEB    DEBUG MODE ?                        87162
         BNZ   DAINPRT       YES; PRINT RESULT                   87162
         CLI   IOECB,X'7F'   ANY ERROR ?                         87162
         BE    DAINBUMP      NO; PROCEED                         87162
DAINPRT  PRTLIST IORESULT    PRINT RESULTS OF I/O                87162
         CLI   IOECB,X'7F'   GOOD COMPLETION ?                   87162
         BNE   BOMB          NO; GO OFF HALF-COCKED              87162
DAINBUMP LH    R2,CURTTR     GET PRIOR TTR                       87162
         LA    R2,1(,R2)     NEXT TRACK                          87162
         SLL   R2,16         CLEAR RZ                            87162
         ST    R2,CURTTR     STASH BACK                          87162
         LH    R1,MAXURCD    GET BLOCKS PER TRACK                87162
         A     R1,NUMBLOCK                                       87162
         ST    R1,NUMBLOCK                                       87162
         B     DAINMORE      DO NEXT TRACK                       87162
DAINDONE L     R3,MAINDATA   GET DATA BACK                       87162
         MVC   DCBFDAD+3(4),8(R3)  MOVE CCHHR OF LAST TRACK      87162
         MVC   DCBFDAD+7(1),MAXURCD+L'MAXURCD-1                  87162
         XC    DCBTRBAL,DCBTRBAL  FAKE NO ROOM FOR EOF           87162
         LH    R1,CURTTR     GET ENDING TRACK (+1)               87162
         SH    R1,BEGINTTR   LESS STARTING TRACK                 87162
         ST    R1,DB         STASH FOR MESSAGE                   87162
         PRTLIST MGNUMBLK    SHOW BLOCKS WRITTEN                 87162
         B     COMMDONE      JOIN EOF WRITE COMMON               87162
         SPACE 2                                                 87162
READNODA TM    FLAG2,FGEOF   EOF OR INIT MODE ?
         BZ    WIPEOUT       INIT - START TRACK ERASURE
         SPACE 1
         MVC   BEGINTTR(3),DS1LSTAR  SAVE OLD OS SETTING
         MVC   BEGINBAL,DS1TRBAL   AND TRACK BALANCE
         SPACE 1
         LH    R4,MAXRPT     NUMBER OF CCWS TO BUILD
         LM    R2,R3,=X'1200000040000008'  READ COUNT CCW
         LA    R0,8          QUICK ADD CONSTANT
         LA    R15,R1COUNT   POINT TO FIRST COUNT AREA
         OR    R2,R15        PLACE IT INTO CCW
         LA    R14,READCCNT  GET FIRST CCW
READINIT STM   R2,R3,0(R14)  STASH A CCW
         AR    R14,R0        NEXT CCW ADDRESS
         ALR   R2,R0         NEXT COUNT FIELD ADDRESS
         BCT   R4,READINIT   REPEAT FOR ALL
         SLR   R14,R0        BACKUP ONE POSITION
         MVI   4(R14),0      KILL COMMAND CHAINING IN LAST ONE
         LH    R2,BEGINTTR   GET USER'S LAST RECORDED TRACK
         STH   R2,CURTTR     SET TO READ LAST USED TRACK
         B     READOUT       START PROCESSING
READLOOP LH    R2,CURTTR     GET PREVIOUS TRACK
         LA    R2,1(,R2)     INCREMENT TO NEXT
         STH   R2,CURTTR     STASH IT BACK
READOUT  BAS   R9,CONTOABS   GET ABSOLUTE ADDRESS               GP03074
         BXH   R15,R15,READEOF   USE PRIOR TRACK
         LA    R1,READCCWS   POINT TO READ CHAIN
         BAS   R9,SCHEDIO    DO THE I/O                         GP03074
         TM    FLAG,FGDEB    DEBUG MODE ?
         BNZ   READPRT       YES; PRINT RESULT
         CLI   IOECB,X'7F'   ANY ERROR ?
         BE    READANAL      NO; PROCEED
         CLI   IOECB,X'41'   ERROR ?
         BNE   READPRT       YES; BUT NOT AN EXPECTED ONE
         CLI   IOBSENS1,X'08'  NO RECORD FOUND ?
         BE    READANAL      YES; EXPECTED RESULT
READPRT  PRTLIST IORESULT    PRINT RESULTS OF I/O
         CLI   IOECB,X'7F'   GOOD COMPLETION ?
         BE    READANAL      YES; CONTINUE PROCESSING
         CLI   IOECB,X'41'   EXPECTED ERROR ?
         BNE   BOMB          NO; QUIT
         CLI   IOBSENS1,X'08'  NO RECORD FOUND ?
         BNE   BOMB          NO; ERROR
READANAL SLR   R9,R9         CLEAR RECORD NUMBER
         SLR   R5,R5
         ICM   R5,7,IOBCMDA  GET LAST COMMAND+8
         LA    R14,READCCNT  GET FIRST COMMAND
         SR    R5,R14        OFFSET TO LAST COMMAND +
         BM    BOMB          MAJOR BOOBOO
         BZ    READEOF       NO R1 ON TRACK
         SRL   R5,3          CONVERT TO NUMBER OF CCWS
         CH    R5,MAXRPT     VALID ?
         BH    BOMB          NO; ERROR
         LA    R7,READCCNT   POINT TO CCW CHAIN
READNEXT ICM   R4,7,1(R7)    GET COUNT FIELD
         LA    R9,1(,R9)     SET EXPECTED RECORD NUMBER
         CLM   R9,1,4(R4)    EXPECTED RECORD NUMBER ?
         BNE   READLAST      NO; DONE WITH TRACK
         ICM   R15,3,6(R4)   DATA LENGTH = 0 ?
         BZ    READEOF       YES; QUIT
         CLM   R9,1,=X'01'   FIRST TIME ON TRACK ?
         BNE   READNOT1      NO
         SLR   R14,R14
         ICM   R14,3,DS1BLKL  DOES THIS DATASET HAVE A BLOCKSIZE ?
         BNP   READSETZ      NO
         ICM   R15,7,READCCNT+1  GET THE DATA AREA
         LR    R0,R8         NUMBER TO TEST
         LH    R1,DS1LRECL   GET LRECL
         SLR   R2,R2
READSET2 ICM   R2,3,6(R15)   GET COUNT FIELD'S BLOCKSIZE
         BZ    READSETZ      EOF - QUIT CHECKING THIS TRACK
         CR    R2,R14        VALID BLOCKSIZE FOR THIS DS ?
         BH    READEOF       NO; ASSUME THIS IS A GARBAGE TRACK
         TM    FLAG,FGRECF   RECFM=F SPECIAL CHECKING ?
         BZ    READSETU      NO
         SRDL  R2,32         PREPARE FOR DIVIDE
         DR    R2,R1         DIVISIBLE ?
         LTR   R2,R2         TEST REMAINDER
         BNZ   READEOF       NO; BAD TRACK
READSETU LA    R15,8(,R15)
         BCT   R0,READSET2   TRY AGAIN
READSETZ MVC   STARBAL,MAXBAL
         MVC   BEGINTTR(2),CURTTR  UPDATE NEW LSTAR
         MVI   BEGINTTR+2,0  BUT KILL RECORD
         MVC   BEGINBAL,STARBAL  SET NEW TRACK BALANCE
         MVI   STARFLGS,STARUBAL  BALANCE PROVIDED
READNOT1 MVC   STARRKDD,4(R4)  MOVE R, KL, DL
         SERVCALL DVCAP,STARKLE  GET NEW BALANCE
         STC   R9,BEGINTTR+2 SET LAST RECORD ADDRESS
         TM    FLAG,FGDEB    DEBUG MODE ?
         BZ    READNDUM
         PRTLIST DEBUGBAL    PRINT DEBUGGING INFO
READNDUM MVC   BEGINBAL,STARBAL
         LA    R7,8(,R7)     POINT TO NEXT COUNT FIELD
         BCT   R5,READNEXT   GET NEXT COUNT FIELD
READLAST B     READLOOP      GET NEXT TRACK
         SPACE 1
READEOF  MVC   CURTTR(3),BEGINTTR  WRITE EOF AFTER LAST GOOD RECORD
         SLR   R15,R15
         IC    R15,CURTTR+2  GET RECORD NUMBER
         LA    R15,1(,R15)   GET EOF'S RECORD NUMBER
         STC   R15,DRCOUNT+4  SET INTO NEW COUNT FIELD
         ICM   R0,3,DS1BLKL  BLOCKSIZE KNOWN ?
         BZ    WIPELOOP      NO; PUT EOF ON NEXT TRACK
         STH   R0,STARDL     SET RECORD LENGTH
         STC   R15,STARR     SET RECORD NUMBER
         MVC   STARKL,DS1KEYL  COPY KEY LENGTH
         MVC   STARBAL,BEGINBAL  SET CURRENT BALANCE
         MVI   STARFLGS,STARUBAL  GET NEW BALANCE
         SERVCALL DVCAP,STARKLE
         BXLE  R15,R15,WIPEOUT  PUT EOF ON SAME TRACK
         SPACE 1
WIPELOOP LH    R2,CURTTR     GET CURRENT TTR
         LA    R2,1(,R2)     BUMP TO NEXT TRACK
         SLL   R2,16         KILL THE RECORD NUMBER
         ST    R2,CURTTR     STASH BACK AS TT00
         MVI   DRCOUNT+4,1   SET EOF RECORD NUMBER TO 1
WIPEOUT  BAS   R9,CONTOABS   CONVERT TO ABSOLUTE                GP03074
         BXH   R15,R15,WIPEDONE   ALL DONE
         MVC   DRCOUNT(4),IOBCC  MAKE NEW R1 COUNT FIELD FOR EOF
         LA    R1,ERASER     GET TRACK ERASE CCW
         BAS   R9,SCHEDIO    DO THE I/O                         GP03074
         TM    FLAG,FGDEB    DEBUG MODE ?
         BNZ   WIPEPRT       YES; PRINT RESULT
         CLI   IOECB,X'7F'   ANY ERROR ?
         BE    WIPELOOP      NO; PROCEED
WIPEPRT  PRTLIST IORESULT    PRINT RESULTS OF I/O
         CLI   IOECB,X'7F'   GOOD COMPLETION ?
         BE    WIPELOOP      YES; GO AGAIN
         B     BOMB
         SPACE 1
WIPEDONE MVC   DCBTRBAL,BEGINBAL
         MVC   CURTTR,BEGINTTR  RESET TO FIRST DATA TRACK
         BAS   R9,CONTOABS   UPDATE ABSOLUTE SEEK FIELD         GP03074
         MVC   DCBFDAD,IOBSEEK  COPY ADDRESS OF LAST RECORD
COMMDONE OI    DCBOFLGS,DCBOFLWR  SHOW WE DID A WRITE
         CLOSE MF=(E,OPLIST) CLOSE THE OUTPUT DATASET           GP08265
         SERVCALL DSDJ1,JFCBDSNM  UPDATE THE DSCB
         BXH   R15,R15,ENDOFDD
         MVC   DS1FMTID(DS1END-DS1FMTID),0(R1)
         PRTLIST PRTLAST     FORMAT THE DSCB 1 INFORMATION
         SERVCALL DSFMT,DS1DSORG  DCB OPTIONS                    84115
         CLC   DSORG(DSORGLEN),0(R1)  ANY CHANGES ?              84115
         BE    ENDOFDD       NO                                  84115
         MVC   DSORG(DSORGLEN),0(R1)                             84115
         PRTLIST SHOWDCBS    SHOW UPDATED INFO                   84115
ENDOFDD  CLOSE MF=(E,OPLIST) CLOSE THE OUTPUT DATASET            84115
         B     LOOPTIOT      TRY FOR ANOTHER                     84115
         SPACE 1
ENDITALL TM    FLAG,FGFOUND  DID WE FIND A DD CARD ?             84115
         BNZ   EXIT          YES; QUIT NORMALLY                  84115
         PRTL  'NO USABLE SYSUT/SYSLIB DD CARD FOUND',CC=NO,DEV==ERRDEV
         OICC  8                      SET NASTIER ERROR          84115
         SPACE 1                                                 84115
EXIT     ICM   R8,15,RETCODE  LOAD AND TEST RETURN CODE          84115
         BZ    EXITCODE                                          84115
         PRTLIST MGRETCOD                                        84115
EXITCODE TM    FLAG,FGDUM    DEBUG DUMP ?
         BZ    EXITCOLD
         PRTL  '0***** DUMP TAKEN FOR DEBUG REQUEST *****'
         SERVTERM ,          CLOSE ALL BUT DATA FILE
         ABEND 88,DUMP       USE NICE ABEND CODE
         SPACE 1
EXITCOLD SERVTERM ,          CLOSE AND FREE EVERYTHING
         PGMEXIT RC=(R8)     RETURN                             GP03074
         SPACE 1
QUIT4    OICC  4                      SET MINOR ERROR            84115
         B     ENDOFDD       AND QUIT THIS DATASET               84115
         SPACE 1
BOMB     SERVTERM ,          CLOSE ALL FILES
         ABEND 666,DUMP      AND QUIT
         SPACE 2
CONTOABS STM   R9,R13,CONTOSAV   SAVE REGISTERS OVER CONVERT CALL
         LR    R7,R13        SAVE SAVE
         L     R1,CURTTR     GET REQUESTED TTR
         ST    R1,DRTTRW     STASH FOR CONVERT
         LM    R15,R2,DRCNVT   FOR TTR CONVERT
         BASR  R14,R15       INVOKE TTR TO MBBCCHHR CONVERSION  GP03074
         LR    R13,R7        RESTORE SAVE
         LM    R9,R13,CONTOSAV   RESTORE REGS
         BR    R9            RETURN TO CALLER
CONTOTTR STM   R9,R13,CONTOSAV   SAVE
         LR    R7,R13        SAVE SAVE
         LM    R1,R2,DRDEB   GET DEB AND MBBCCHHR ADDRESS
         L     R15,DRPRLTV   GET MBBCCHHR TO TTR CONVERSION ROUTINE
         BASR  R14,R15       CONVERT BACK TO TTR FORMAT         GP03074
         LR    R13,R7        RESTORE SAVE
         ST    R0,DRTTRW     STASH ACTUAL TTR
         ST    R0,CURTTR     SET TTR FOR NEXT TIME
         LM    R9,R13,CONTOSAV   GET REGS BACK
         BR    R9            TAKE NORMAL RETURN
         SPACE 1
SCHEDIO  ST    R1,IOBSTART   SET START CCW ADDRESS
         ST    R1,IOBRESTR   FOR SAFETY'S SAKE - INTO RESTART ALSO
SCHEDIOB MVI   IOBFLAG1,IOBCMDCH+IOBUNREL  CMD CHAIN/NOT RELATED
         EXCP  MIOB          SCHEDULE AN I/O
         WAIT  ECB=IOECB     WAIT FOR COMPLETION
         CLI   IOECB,X'7F'   SET CONDITION CODE
         BR    R9            RETURN TO CALLER
         SPACE 1
FORMFMT1 SERVCALL DSFMT,DS1DSORG
         MVC   DSORG(DSORGLEN),0(R1)  COPY
         PRTLIST PRTDSCB
         PRTLIST SHOWDCBS
         BR    R9            RETURN TO CALLER
         SPACE 1
PRTLAST  FDPRT 'FINAL',NL,PADR
         FDGOTO PRTDSCB2
PRTDSCB  FDPRT ' ',PADR,NL,LEN=5                                 84115
PRTDSCB2 FDPRT 'DSCB VALUES : LSTAR='
         FDPRT DS1LSTAR,2,HEX
         FDPRT '.'
         FDPRT DS1LSTAR+2,1,HEX
         FDPRT 'TRACK BALANCE=',PADL
         FDPRT DS1TRBAL,IA,DEB,PADR,LEN=10                      GP03074
         FDPRT *END
         SPACE 1
SHOWDCBS FDPRT 'RECFM=',RADJ,LEN=8
         FDPRT RECFM,DEB,PADR
         FDPRT 'LRECL=',PADL
         FDPRT LRECL,DEB,PADR
         FDPRT 'BLKSIZE=',PADL
         FDPRT BLKSIZE,DEB,PADR
         FDPRT 'DSORG=',PADL
         FDPRT DSORG,DEB
         FDPRT *END                                              84115
SHOWDCBX FDTM  FLAG,FGDEB,BZ=SHOWDCBY                            84115
         FDPRT MAXRPT,I,RADJ,NL,LEN=8                            84115
         FDPRT 'EOF RECORDS PER TRACK,',PAD                      84115
         FDPRT MAXURCD,I,RADJ,LEN=8                              84115
         FDPRT 'USER BLOCKS PER TRACK',PAD                       84115
SHOWDCBY FDPRT *END                                              84115
         SPACE 1                                                 87162
MGSHOFIL FDPRT 'BLOCKS WILL BE INITIALIZED WITH C''',NL          87162
         FDPRT FILLCHAR                                          87162
         FDPRT ''' (HEX'                                         87162
         FDPRT FILLCHAR,HEX,PADL                                 87162
         FDPRT ')'                                               87162
         FDPRT *END                                              87162
         SPACE 1                                                 87162
MGNUMBLK FDPRT NUMBLOCK,ICM,RADJ,NL,LEN=15                       87162
         FDPRT 'BLOCKS INITIALIZED ON',PAD                       87162
         FDPRT DB,4,I,PAD                                        87162
         FDPRT 'TRACK'                                           87162
         FDCLC DB,DCF1,4,BE=MGNUMBLX                             87162
         FDPRT 'S'                                               87162
MGNUMBLX FD    *END                                              87162
         SPACE 1                                                 84115
MGNOTUSA FDOPT NL,CC=C'0'                                        84115
         FDPRT DDNAME,PADR,DEB                                   84115
         FDPRT 'DD CARD NOT USABLE'                              84115
         FDPRT *END                                              84115
MGDUPEDD FDOPT NL,CC=C'0'                                        84115
         FDPRT DDNAME,PADR,DEB                                   84115
         FDPRT 'DD NAME NOT UNIQUE'                              84115
         FDPRT *END                                              84115
MGNOTDAS FDOPT NL,CC=C'0'                                        84115
         FDPRT DDNAME,PADR,DEB                                   84115
         FDPRT 'DD NOT A DASD DEVICE'                            84115
         FDPRT *END                                              84115
MGBADORG FDOPT NL,CC=C'0'                                        84115
         FDPRT DDNAME,DEB,PADR                                   84115
         FDPRT 'DD HAS UNSUPPORTED DATASET ORGANIZATION'         84115
         FDPRT *END                                              84115
MGBADDCB FDOPT NL,CC=C'0'                                        87162
         FDPRT DDNAME,DEB,PADR                                   87162
         FDPRT 'DD HAS OMITTED OR UNSUPPORTED DCB VALUE'         87162
         FDPRT *END                                              87162
MGBADOBT FDOPT NL,CC=C'0'                                        84115
         FDPRT DDNAME,DEB,PADR                                   84115
         FDPRT 'DSN=',PADL                                       84115
         FDPRT JFCBDSNM,DEB                                      84115
         FDPRT 'ON VOLUME',PAD                                   84115
         FDPRT JFCBVOLS,6,DEB,PAD                                84115
         FDPRT 'COULD NOT BE FOUND'                              84115
         FDPRT *END                                              84115
MGDSPMOD FDPRT DDNAME,DEB,PADR,NL                                84115
         FDPRT 'DD CARD DISPOSITION NOT MOD, DISP=MOD FORCED'    84115
         FDPRT *END                                              84115
MGRETCOD FDOPT NL,CC=C'-'                                        84115
         FDREPT 5,C'*'                                           84115
         FDREPT 5,C'>'                                           84115
         FDPRT ' PROGRAM COMPLETE WITH LEVEL'                    84115
         FDPRT RETCODE,I,PAD,DEB                                 84115
         FDPRT 'ERRORS '                                         84115
         FDREPT 5,C'<'                                           84115
         FDREPT 5,C'*'                                           84115
         FDPRT *END                                              84115
         SPACE 1
DEBUGBAL FDPRT 'PRIOR BALANCE=',NL,RADJ,LEN=20
         FDPRT BEGINBAL,IA,DEB,LEN=10                           GP03074
         FDPRT 'R='
         FDPRT STARR,I,DEB,LEN=5
         FDPRT 'KL='
         FDPRT STARKL,I,DEB,LEN=5
         FDPRT 'BLKSIZE='
         FDPRT STARDL,I,DEB,LEN=8
         FDPRT 'NEW BALANCE='
         FDPRT STARBAL,IA,DEB                                   GP03074
         FDPRT *END
         SPACE 1
SHOWNAME FDOPT NL,CC=C'0'                                        84115
         FDPRT 'DDNAME',NL                                       84115
         FDPRT DDNAME,DEB,PAD
         FDPRT 'DSN=',RADJ,LEN=5
         FDPRT JFCBDSNM,DEB
         FDCLI JFCBELNM,C' ',BE=SHOWNELN,BL=SHOWNELN
         FDPRT '('
         FDPRT JFCBELNM,DEB
         FDPRT ')'
SHOWNELN FDCLI JFCBVOLS,C' ',BE=SHOWNVOL,BL=SHOWNVOL
         FDPRT 'ON',PAD
         FDPRT JFCBVOLS,6,DEB,PADR
SHOWNVOL FDPRT 'UNIT',RADJ,LEN=7
         AIF   (NOT &MVSESA).FNAME3
         FDPRT UNITNAME,HEX,DEB,PADL                            GP03074
         AGO   .FNAMEC
.FNAME3  FDPRT UNITNAME,DEB,PADL                                GP03074
.FNAMEC  FDCLI UNITTYPE,C' ',BE=SHOWNTYP,BL=SHOWNTYP
         FDPRT '('
         FDPRT UNITTYPE,DEB
         FDPRT ')',PADR
SHOWNTYP FDPRT *END
         EJECT ,
         PRINT &PRTSYS
SYSPRINT PRTWORK SYSPRINT,SYSTERM,TITLE=2   SYSPRINT FILE BLOCK
CONSOLE  PRTWORK *CONSOLE           JOB LOG
PATSAD   DCB   DDNAME=SYSLIB,DSORG=PS,DEVD=DA,MACRF=(E),EXLST=1  87162
PATSADL  EQU   *-PATSAD                                          87162
PATSYS   DCB   DDNAME=SYSLIB,DSORG=PS,DEVD=DA,MACRF=(E),EXLST=1
SYSLIBL  EQU   *-PATSYS
PATERASE CCW   X'31',0,X'40',5  SEARCH R0
         CCW   8,*-8,0,1     TIC
DCF1     EQU   *-4,4,C'F'                                        87162
PATWREOF CCW   X'1D',0,X'00',8  WRITE EOF
PATERASL EQU   *-PATERASE
         SPACE 1
HEADER   FDOPT NL,CC=C'#'    AUTOMATIC DATE/TIME/PAGE #
         FDSPACE 20
         FDTM  FLAG2,FGLOAD,BZ=HEADSEQ                           87162
         FDPRT 'DIRECT'                                          87162
         FDGOTO HEADCOM                                          87162
HEADSEQ  FDPRT 'SEQUENTIAL'                                      87162
HEADCOM  FDPRT 'DATASET',PADL                                    87162
         FDTM  FLAG2,FGEOF,BZ=HEADERIN
         FDPRT 'CORRECTION',PAD
         FDGOTO HEADERCM
HEADERIN FDPRT 'INITIALIZATION',PAD
HEADERCM FDPRT 'UTILITY'
         FDPRT *END
         SPACE 1
IORESULT FDPRT 'I/O RESULT:',PAD,NL,RADJ,LEN=15
         FDPRT IOECB,HEX,PAD
         FDPRT IOBSENS0,HEX,PADL
         FDPRT IOBSENS1,HEX,PADR
         FDPRT IOBCMDA,HEX,PAD
         FDPRT IOBSTBYT,HEX,PAD
         FDPRT IOBCSW+5,2,HEX,PAD  RESIDUAL LENGTH
         FDPRT IOBSEEK,4,HEX,PADL
         FDPRT IOBSEEK+4,4,HEX,PADR
         FDPRT *END
         SPACE 1
         IEZIOB ,
         SPACE 2
SAVE     DSECT ,             CONTINUE SAVE/WORK AREA DEFINITION
DB       DS    D
         SERVDEFS ,          ADDRESS OF SERVICE ROUTINES, ETC.
ERRDEV   DC    X'01'         DEV INDEX FOR ERROR MESSAGES
FLAG     DC    X'00'
FGDEB    EQU   X'40'           DEBUG MODE
FGDUM    EQU   X'20'           DEBUG MODE/ FINAL DUMP
FGPRIVY  EQU   X'10'           BYPASS VALIDITY CHECKING
FGRECF   EQU   X'08'           DO RECFM=F SPECIAL TESTING
FGFOUND  EQU   X'04'           AT LEAST ONE DD CARD FOUND        84115
FGBADPRM EQU   X'02'           FAIL DUE TO BAD PARM FIELD        84115
FGNOPRT  EQU   X'01'           DO NOT OPEN SYSPRINT FILE         84115
FLAG2    DC    X'00'         SECOND PROCESSING FLAG              87162
FGLOAD   EQU   X'02' *FIXED*   DSORG=DA LOAD MODE               GP03074
FGEOF    EQU   X'01' *FIXED*   WRITE EOF ONLY                   GP03074
FILLCHAR DC    X'00'         CLEARDA BUFFER FILL CHARACTER       87162
ADTIOT   DC    F'0'          TIOT SCAN ADDRESS                   84115
MAINAD   DC    A(0)          GOTTEN ADDRESS                      87162
MAINSIZE DC    A(0)          GOTTEN LENGTH                       87162
MAINDATA DC    A(0)          BEGINNING OF DATA IN GOTTEN BLOCK   87162
         SPACE 1                                                 84115
BEGBLANK DS    0C            START OF AREA TO BLANK OUT
DDNAME   DS    CL8           CURRENT DD NAME
UNITNAME DS    XL(3-&MVSESA) DEVICE NUMBER IN HEX OR EBCDIC     GP03074
UNITTYPE DS    CL8           GENERIC TYPE
LENBLANK EQU   *-BEGBLANK    SIZE TO BLANK
UNITADDR DS    F             UCB ADDRESS
CONTOSAV DS    5F            TTR/CCHHR CONVERT SAVE AREA
DRPRLTV  DC    A(0)          ADDRESS OF MBBCCHHR TO TTR CONV. ROUTINE
BEGINTTR DC    A(0)          FIRST DATA TTR IN DATASET
CURTTR   DC    F'0'          RELATIVE TTR FOR I/O
DRCNVT   DC    A(0) CONVERT PARM:  ADDR OF CONVERT ROUTINE (15)
DRTTRW   DC    F'0'          ACTUAL TTR FOR I/O
DRDEB    DC    A(0)          DEB ADDR. (1)
         DC    A(IOBSEEK)    MBBCCHHR ADDR (2)
OPLIST   DC    0A(0),X'80',AL3(SYSLIB)  OPEN/CLOSE LIST
EXLST    DC    0A(0),X'87',AL3(INFMJFCB)
         SPACE 1                                                 84115
BEGZERO  EQU   *             START TO CLEAR                      84115
DRCOUNT  DC    A(0),AL1(1,0,0,0)  COUNT FIELD 1 FOR EOF RECORD
         SPACE 1
ERASER   CCW   X'31',IOBCC,X'40',5  SEARCH R0
         CCW   8,*-8,0,0     TIC
NUMBLOCK EQU   *-8,4,C'F'    NUMBER OF BLOCKS WRITTEN            87162
         CCW   X'11',0,X'20',65535
MAXRPT   DS    H             MAXIMUM RECORDS PER TRACK
MAXURCD  DS    H             MAXIMUM DATA BLOCKS PER TRACK       84115
MAXBAL   DS    H             TRACK CAPACITY OF EMPTY TRACK (OS R0)
BEGINBAL DS    H             BEGINNING TRACK BALANCE
         SPACE 1
         PRINT &PRTMAC
STARKLE  SERVCALC MF=D
         SPACE 1
DSORG    DS    CL3  1/5      DSFMT RETURN SEQUENCE
RECFM    DS    CL6  2/5
OPTCD    DS    CL8  3/5
BLKSIZE  DS    CL5  4/5
LRECL    DS    CL5  5/5
DSORGLEN EQU   *-DSORG       LENGTH OF RETURN STRING
         PRINT &PRTSYS
         SPACE 1
         DS    0D            JUST IN CASE
         IEFJFCBN  ,
         SPACE 1
SYSLIB   DCB   DDNAME=SYSLIB,DSORG=PS,DEVD=DA,MACRF=(E),EXLST=EXLST
         ORG   SYSLIB                                            87162
SYSLIBDA DCB   DDNAME=SYSLIB,DSORG=PS,MACRF=(WL)                 87162
         ORG   ,                                                 87162
LENZERO  EQU   *-BEGZERO     SIZE TO CLEAR                       84115
         SPACE 1
         IECSDSL1 1          EXPAND FORMAT 1 DSCB
         SPACE 1
IOECB    DS    A             MAIN I/O ECB
         DS    0D
         DS    XL(IOBSTDRD-IOBPREFX)  MAKE A FAKE PREFIX
MIOB     DS    XL256'0'      ENSURE SPACE AT NORMAL IOB OFFSET =0
*                            REQUIRED TO PREVENT ERROR 'RECOVERY'
*                            FOR EXCP I/O USING A BSAM DCB.
         SPACE 1
READCCWS CCW   X'31',0,X'40',5  SEARCH R0
         CCW   8,*-8,0,0        TIC *-8
READCCNT DS    256CL8        READ COUNT CCWS
R1COUNT  DS    256CL8        COUNT FIELDS
         SPACE 1
SAVEEND  EQU   *             END OF GETMAINED WORK AREA         GP03074
         SPACE
         PRINT &PRTSYS
XTMAP    MAPEXTNT ,          EXPAND DS1EXT MAPPING
         SPACE 1
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IHACDE ,
         DCBD  DEVD=DA,DSORG=PS
         IEZDEB ,
MYUCB    DSECT ,                                                GP03074
         IEFUCBOB ,
MYTIOT   DSECT ,                                                GP03074
         IEFTIOT1 ,
         END   ,
