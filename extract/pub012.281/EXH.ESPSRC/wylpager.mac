WPAG     TITLE 'W Y L P A G E R  **  PAGE DATA SET FORMATTER'
         PUNCH '  ORDER WYLPAGER(P) '       EASIER DUMPS
         PRINT NOGEN
***********************************************************************
**                                                                   **
**  TSO WYLBUR WILL ALLOCATE ITS OWN PAGING FILE, UNLESS ONE IS      **
**  SUPPLIED BY THE USER. A USER FILE SHOULD BE PREFORMATTED (ONCE)  **
**  AND ALLOWS RECOVERY IF THE SESSION FAILS.                        **
**  PREFERRED DSN IS userid.WYLPAGE0                                 **
**  THIS PROGRAM WILL FORMAT UP TO TEN FILES AT A TIME, BUT THE      **
**  CURRENT WYLBUR ONLY SUPPORTS ONE. (SEE 'WYL' CLIST FOR USE)      **
**                           G. POSTPISCHIL ADDED 2011-07-29         **
**-------------------------------------------------------------------**
**                                                                   **
**   JCL REQUIRED :                                                  **
**       //FORM  EXEC PGM=WYLPAGER                                   **
**       //PAGE0  DD DISP=(,CATLG),DSN=userid.WYLPAGE0,              **
**       //          UNIT=somedasd,VOL=SER=volser,                   **
**       //          SPACE=(CYL,nnn)                                 **
**       //SYSPRINT DD SYSOUT=*        OUTPUT LOG & MESSAGES         **
**                                                                   **
**   For an existing data set, use :                                 **
**       //PAGE0  DD DISP=OLD,DSN=userid.WYLPAGE0                    **
**                                                                   **
***********************************************************************
WYLPAGER START 0
         STM   R14,R12,12(R13)
         LR    R12,R15       COPY
         LA    R11,2048
         LA    R11,2048(R11,R12)
         USING WYLPAGER,R12,R11
         LA    R8,0(,R1)     SAVE PARM POINTER
         LR    R9,R13        SAVE SAVE AREA
         L     R3,=A(SAVEEND-SAVE)
         GETMAIN RU,LV=(R3),BNDRY=PAGE
         LR    R13,R1        NEW SAVE AND WORK AREA
         USING SAVE,R13
         LR    R0,R1
         LR    R1,R3
         SR    R15,R15
         MVCL  R0,R14        CLEAR DYNAMIC AREA FOR SHORTER DUMPS
         ST    R9,4(,R13)
         ST    R13,8(,R9)    LINK SAVE AREAS
         ST    R3,0(,R13)    SAVE LENGTH GOTTEN
         SPACE 1
         LA    R0,MYJFCB     POINT TO JFCB
         STCM  R0,7,WORKXLST+4+1  JFCB ADDRESS INTO EXIT LIST
         SPACE 1
         LTR   R8,R8         PARM LIST ADDRESS ?
         BZ    NOPARM        NONE, MAJOR ERROR
         L     R8,0(,R8)     GET ADDRESS OF PARM FIELD
         LA    R8,0(,R8)     JUST IN CASE
         LTR   R8,R8         ANY ?
         BNP   NOPARM        NO, TOO BAD
         LH    R9,0(,R8)     GET LENGTH
         LTR   R9,R9
         BNP   NOPARM
         CH    R9,=AL2(L'RAWSIZE)  WILL IT FIT?
         BH    MSG2LONG
         LA    R14,RAWSIZE+L'RAWSIZE
         SR    R14,R9
         BCTR  R9,0          EXECUTE LENGTH
         EX    R9,EXMVCPRM   MOVE PARM FIELD
         TRT   RAWSIZE,TRTDIGIT   VALID INPUT ?
         BNZ   MSGBDPRM
         XC    DB,DB
         MVC   DB+L'DB-L'RAWSIZE(L'RAWSIZE),RAWSIZE
         CVB   R0,DB
         ST    R0,BLKSIZE
         B     NOPARM
EXMVCPRM MVC   0(0,R14),2(R8)   SAVE INPUT FOR LATER
         SPACE 1
MSG2LONG LA    R1,TXT2LONG
         B     BADEXITM
         SPACE 1
MSGBDPRM LA    R1,TXTBDPRM
         B     BADEXITM
         SPACE 1
MSGBDDCB LA    R1,TXTBDDCB
         B     BADEXITM
         SPACE 1
MSGNDASD LA    R1,TXTNDASD
         B     BADEXITM
         SPACE 1
MSGNOOPN LA    R1,TXTNOOPN
         B     BADEXITM
         SPACE 1
MSGNODD  LA    R1,TXTNODD
         B     BADEXITM
         SPACE 1
NOPARM   LA    R5,WORKDCB+40+4    POINT TO FILE NUMBER
         DEVTYPE WORKDCB+40,DB  IS PAGE0 DD PRESENT ?
         BXLE  R15,R15,OPENWORK     YES - OPEN
         MVC   WORKDCB+40(8),=CL8'WYLPAGE0'
         LA    R5,WORKDCB+40+7    POINT TO FILE NUMBER
         DEVTYPE WORKDCB+40,DB  IS ALTERNATE PRESENT ?
         BXLE  R15,R15,OPENWORK     YES; USE IT
         B     MSGNODD       QUIT WITH MESSAGE
         SPACE 1
OPENWORK CLI   DB+2,X'20'    UCB3DACC ?
         BNE   MSGNDASD        NO; NOT USABLE
         RDJFCB MF=(E,WORKOLST)
         TM    JFCRECFM,255-X'98' OTHER THAN FBS ?
         BNZ   MSGBDDCB      YES; FAIL
         L     R0,BLKSIZE    GET BLOCK SIZE
         CLM   R0,12,JFCBLKSI  JFCB SIZE = 0 ?
         BE    CHKREC
         CH    R0,JFCBLKSI   JFCB SIZE MATCH?
         BNE   MSGBDDCB
CHKREC   CLM   R0,12,JFCLRECL  JFCB SIZE = 0 ?
         BE    WORKOPNL
         CH    R0,JFCLRECL   JFCB SIZE MATCH?
         BNE   MSGBDDCB
WORKOPNL OPEN  MF=(E,WORKOLST)
         TM    WORKDCB+48,X'10'    OPEN ?
         BZ    MSGNOOPN
         LH    R0,WORKDCB+DCBBLKSI-IHADCB
         CVD   R0,DB
         MVC   CHRSIZE(6),=X'402020202120'
         ED    CHRSIZE(6),DB+5
         LA    R1,TXTSIZE
         BAL   R9,MSGSUB     WRITE MESSAGE
         SR    R9,R9         BLOCK COUNTER
WORKLOOP WRITE WORKIECB,SF,WORKDCB,(R4),'S'   WRITE DUMMY BLOCK
         WAIT  ECB=WORKIECB
         CLI   WORKIECB,X'7F'
         BNE   MSGCOUNT      QUIT
         CHECK WORKIECB
         LA    R9,1(,R9)     UP BLOCK COUNT
         B     WORKLOOP
         SPACE 1
MSGCOUNT CLOSE MF=(E,WORKOLST)
         CVD   R9,DB         SAVE BLOCK COUNT
         MVC   CHRCOUNT(6),=X'402020202120'
         ED    CHRCOUNT(6),DB+5
         LA    R1,TXTCOUNT
         BAL   R9,MSGSUB
         SR    R4,R4
         IC    R4,0(,R5)     GET FILE NUMBER
         LA    R4,1(,R4)     INCREMENT
         CLM   R4,1,=C'9'    DONE ALL TEN ?
         BH    PGMEXIT         YES; DONE
         STC   R4,0(,R5)     UPDATE IT
         DEVTYPE WORKDCB+40,DB  IS PAGEn DD PRESENT ?
         BXH   R15,R15,PGMEXIT    NO; QUIT NOW
         STC   R4,CHRBDDCB+4
         STC   R4,CHRNOOPN+4
         STC   R4,CHRNDASD+4
         STC   R4,CHRCOUNT+L'CHRCOUNT-1
         B     OPENWORK
         EJECT ,
BADEXITM OI    RETCODE+L'RETCODE-1,16  SET ERROR
MSGEXIT  BAL   R9,MSGSUB     WRITE MESSAGE
*        TERMINATION PROCESSING
*
PGMEXIT  L     R9,RETCODE
         L     R0,0(,R13)    GET GETMAIN SIZE
         LR    R1,R13        GET GETMAIN AREA
         L     R13,4(,R13)   RESTORE CALLER'S SAVE AREA
         FREEMAIN R,LV=(0),A=(1)  FREE DYNAMIC STORAGE
         L     R14,12(,R13)  RESTORE RETURN ADDRESS
         LR    R15,R9        SET RETURN CODE
         LM    R0,R12,20(R13)   RESTORE OTHER REGISTERS
         BR    R14           RETURN
EXMVCMSG MVC   WORKBUF+4(0),1(R1)  MOVE MESSAGE TEXT
         SPACE 1
*---------------------------------------------------------------------*
*   WRITE MESSAGE AS WTO                                              *
*---------------------------------------------------------------------*
MSGSUB   LA    R4,WORKBUF    REUSE BUFFER
         SR    R2,R2
         IC    R2,0(,R1)     MESSAGE LENGTH
         EX    R2,EXMVCMSG   MOVE MESSAGE
         LA    R2,4(,R2)     MESSAGE PLUS RDW
         SLL   R2,16
         ST    R2,WORKBUF    COMPLETE WTO
         WTO   MF=(E,WORKBUF)
         XC    WORKBUF(256),WORKBUF    CLEAR IT AGAIN
         BR    R9            RETURN TO CALLER
         EJECT ,
         DROP  ,
         USING EXITDCB,R15
         USING IHADCB,R1
EXITDCB  N     R1,=X'00FFFFFF'    JUST IN CASE
         L     R0,BLKSIZE    GET DESIRED BLOCK SIZE
         STH   R0,DCBBLKSI
         STH   R0,DCBLRECL
         MVI   DCBRECFM,X'88'  RECFM=FS - NO BLOCKING
         BR    R14
         EJECT ,
*        DATA AND STUFF
WORKDCB  DCB   DDNAME=PAGE0,MACRF=(W),DSORG=PS,EXLST=WORKXLST
WORKOLST OPEN  (WORKDCB,OUTPUT),MF=L
WORKXLST DC    X'05',AL3(EXITDCB),X'87',AL3(0)  OPEN EXIT / JFCB
BLKSIZE  DC    A(CPSIZE)     CURRENT WYLTSO SIZE   (2011/07)
         SPACE 1
TXT2LONG DC    AL1(L'CHR2LONG)
CHR2LONG DC    C'PARM FIELD TOO LARGE'
         SPACE 1
TXTBDPRM DC    AL1(L'CHRBDPRM)
CHRBDPRM DC    C'PARM FIELD NOT NUMERIC'
         SPACE 1
TXTNODD  DC    AL1(L'CHRNODD)
CHRNODD  DC    C'PAGE0 DD MISSING OR UNUSABLE'
         SPACE 1
TXTNDASD DC    AL1(L'CHRNDASD)
CHRNDASD DC    C'PAGE0 DD NOT A DASD FILE'
         SPACE 1
TXTBDDCB DC    AL1(L'CHRBDDCB)
CHRBDDCB DC    C'PAGE0 DCB PARAMETERS UNUSABLE'
         SPACE 1
TXTNOOPN DC    AL1(L'CHRNOOPN)
CHRNOOPN DC    C'PAGE0 DCB OPEN FAILED'
         SPACE 1
TXTCOUNT DC    AL1(L'CHRCOUNT)
CHRCOUNT DC    C' NNNNN BLOCKS WRITTEN TO PAGE0'
         SPACE 1
TXTSIZE  DC    AL1(L'CHRSIZE)
CHRSIZE  DC    C' NNNNN BYTES PER BLOCK'
         SPACE 1
TRTDIGIT DC    256AL1(4)     ALL INVALID
         ORG   TRTDIGIT+0
         DC    X'0'          LEADING ZERO OK
         ORG   TRTDIGIT+C' '
         DC    X'0'          LEADING BLANK OK
         ORG   TRTDIGIT+C'0'
         DC    10X'0'        AND ALL DIGITS OK
         ORG   ,
         SPACE 1
SAVE     DSECT ,
         DS    18A           SYSTEM SAVE AREA
DB       DC    D'0'          DOUBLE WORD WORK AREA
RETCODE  DC    F'0'
RAWSIZE  DC    CL5' '        BLOCK SIZE FROM PARM=
         SPACE 1
MYJFCB   DS    0D
         IEFJFCBN ,
         SPACE 1
WORKBUF  DC    128XL256'0'   REALLY MUCH SMALLER
SAVEEND  EQU   *
         SPACE 2
         PRINT NOGEN
CPAREA   DSECT ,
         COPY  CPTSO         PAGE DEFINITION CPSIZE (IN WYLTSO.MAC)
         DCBD  DSORG=PS,DEVD=DA
         YREGS ,
         END
