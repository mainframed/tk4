CMDTO    TITLE 'TSO / TSSO   T O   (LOCAL SEND) COMMAND'         88343
***********************************************************************
*                                                                     *
*        'TO' TSO COMMAND                                             *
*            COPIED FROM CONNECTICUT BANK & TRUST MODS TAPE - FILE 300*
***********************************************************************
*
*        WRITTEN BY. BILL GODFREY,  PLANNING RESEARCH CORPORATION.
*        INSTALLATION. PRC COMPUTER CENTER INC, MCLEAN VA.
*        DATE WRITTEN. MARCH 5 1976
*        DATE UPDATED. DECEMBER 11 1980.
*        ATTRIBUTES. RE-ENTRANT.
*        DESCRIPTION.
*         THIS TSO COMMAND SENDS A MESSAGE TO A TSO USER.
*         IT DIFFERS FROM THE 'SEND' COMMAND IN THAT THE
*         MESSAGE IS NOT IN QUOTES, SO YOU DON'T HAVE TO
*         THINK ABOUT QUOTES WITHIN YOUR MESSAGE.
*
*         THE SYNTAX IS:  TO  USERID  MESSAGE-TEXT
*
*         EXAMPLES:  TO JONES WHERE'S THAT REPORT
*                    TO (JONES SMITH) OK, WE'LL TEST IT.
*
*         THIS COMMAND WILL NOT PUT A MESSAGE IN A USER'S
*         BROADCAST MAIL.
*
*         11DEC80 - CHANGE SS01 TO TSSO.
*         11DEC80 - 3-BYTE USERID NO LONGER ASSUMED IN MSG05, MSG07.
*                                                                88343
*          8DEC88 - CHANGES TO USE LOCAL MACROS, ETC. SOME TO MAKE IT
*              PRETTIER, OTHERS TO SUPPORT WYLBUR MESSAGES.      88343
*              TO * ...  SENDS A MESSAGE TO ALL REACHABLE USERS  88346
*
***********************************************************************
         SPACE 1                                                 88343
         COPY  OPTIONGB      COPY GLOBAL DEFINITIONS             88343
         SPACE 1                                                 88343
         SYSPARM LIST=YES    SET AND LIST OPTIONS                88343
         EJECT ,                                                 88343
         PRINT &PRTSOR                                           88343
TO       PGMHEAD ZERO12,BASE=R12,PARM=R2,AM=31,RM=24            GP02355
         SPACE 1
         USING CPPL,R2
************************************************************
*                                                          *
*        SET UP IOPL FOR PUTLINE                           *
*                                                          *
************************************************************
         SPACE 1
         LA    R15,MYIOPL
         USING IOPL,R15
         MVC   IOPLUPT(4),CPPLUPT
         MVC   IOPLECT(4),CPPLECT
         LA    R0,MYECB
         ST    R0,IOPLECB
         XC    MYECB,MYECB
         LA    R0,MYPTPB
         ST    R0,IOPLIOPB
         DROP  R15
         SPACE 1
************************************************************
*                                                          *
*        SET UP PPL FOR PARSE                              *
*                                                          *
************************************************************
         SPACE 1
         LA    R15,MYPPL
         USING PPL,R15
         MVC   PPLUPT(4),CPPLUPT
         MVC   PPLECT(4),CPPLECT
         LA    R0,MYECB
         ST    R0,PPLECB
         XC    MYECB,MYECB
         L     R0,=A(TOPCL)
         ST    R0,PPLPCL
         LA    R0,MYANS
         ST    R0,PPLANS
         MVC   PPLCBUF(4),CPPLCBUF
         LA    R0,MYUWA
         ST    R0,PPLUWA
         DROP  R15,R2                                            88343
         SPACE 1
************************************************************
*                                                          *
*        CALL THE PARSE SERVICE ROUTINE                    *
*                                                          *
************************************************************
         SPACE 1
         LR    R1,R15              POINT TO PPL
         CALLTSSR EP=IKJPARS  BAL OR LINK TO PARSE               88343
         SPACE 1
************************************************************
*                                                          *
*        PARSE PDL IS NOW FILLED IN                        *
*                                                          *
************************************************************
         SPACE 1
         LTR   R15,R15             PARSE SUCCESSFUL?
         BNZ   EXIT                NO - BRANCH
         L     R3,MYANS
         USING IKJPARMD,R3
         LA    R4,USERID
         LA    R6,STRING
         TM    STRING+6,X'80'      TEXT SPECIFIED?
         BZ    ERRNOTXT            NO - BRANCH
         LH    R1,STRING+4         GET LENGTH OF TEXT
         MIN   R1,=Y(L'MYTEXT-11),TYPE=H  TRUNCATE IF TOO LONG   88343
         LTR   R2,R1         TEST AND SAVE LENGTH                88343
         BNP   ERRNOTXT      SKIP IF NO TEXT
         LA    R14,MYTEXT    POINT TO DESIRED TEXT LOCATION      88343
         LA    R15,L'MYTEXT  MAXIMUM LENGTH SUPPORTED            88343
         L     R0,STRING     GET INPUT ADDRESS                   88343
         ICM   R1,8,=C' '    SET FOR BLANK FILL                  88343
         MVCL  R14,R0        MOVE AND PAD THE TEXT               88343
         LA    R14,MYTEXT(R2)   POINT PAST LAST BYTE OF TEXT     88343
DEBLANK  BCTR  R14,0         GET PRIOR BYTE                      88346
         CLI   0(R14),C' '   TRAILING BLANK ?                    88346
         BNE   DEBLANKD      NO                                  88346
         BCT   R2,DEBLANK    TRY AGAIN                           88346
         B     ERRNOTXT      INVALID WITHOUT TEXT                88346
DEBLANKD MVI   3(R14),C':'   MAKE SEPARATOR                      88346
         LA    R7,2+9(,R2)   TOTAL LENGTH IN R7                  88343
         LA    R15,4(,R7)    TEXT LENGTH+TRAILER+RDW LENGTH      88343
         STH   R15,MYTEXRDW  STASH IT                            88343
         LA    R6,MYTEXT
         L     R1,PSATOLD-PSA  GET CURRENT TCB                   88343
         L     R1,TCBTIO-TCB(,R1)  TCBTIO                        88343
         MVC   4(8,R14),0(R1)  APPEND JOBNAME/USERID             88346
         CLC   =C'TSSO ',0(R1)  IS THIS &TO FROM CONSOLE?
         BNE   USERLOOP
         MVC   4(4,R14),=C'OPER'  APPEND OPER
USERLOOP TM    6(R4),X'80'         USERID SPECIFIED?
         BZ    ERRNOUSR            NO - BRANCH
         LH    R1,4(,R4)           GET LENGTH OF USERID
         LTR   R1,R1               LENGTH ZERO?
         BNP   ERRNOUSR            YES - BRANCH
         L     R0,0(,R4)     GET USER-ID ADDRESS                 88346
         LA    R14,MYUSER    GET WORKING COPY                    88346
         LA    R15,L'MYUSER  LENGTH WITH BLANK FILL              88346
         ICM   R1,8,=C' '    SET FOR TRAILING BLANK FILL         88346
         MVCL  R14,R0        COPY THE USER ID                    88346
         SPACE 1
         CLC   =C'OPER',MYUSER  SEND TO OPERATOR ?               88343
         BNE   FINDASID      NO; LOOK FOR TSO SESSIONS           88343
         SLR   R0,R0         JUST IN CASE                        88343
         MVC   MYTEXRDW+2(2),PATWTO+2  MOVE FLAG BITS            88346
         WTO   MF=(E,MYTEXRDW),ROUTCDE=1,DESC=2  DON'T DOM       88343
         XC    MYTEXRDW+2(2),MYTEXRDW  CLEAR FLAGS AGAIN         88346
         B     NEXTUSER      SEE IF ANOTHER                      88343
         SPACE 1                                                 88343
FINDASID L     R2,CVTPTR     CVTPTR                              88343
         L     R2,CVTASVT-CVTMAP(,R2)   ASVT                     88343
         USING ASVT,R2                                           88343
         L     R0,ASVTMAXU   MAX ADDRESS SPACES PERMITTED        88343
         LA    R2,ASVTENTY   POINT TO FIRST ASCB POINTER         88343
         DROP  R2                                                88343
ASIDLOOP ST    R0,SAVEASCT   SAVE COUNTER                        94235
         ICM   R15,15,0(R2)  LOAD AND TEST ASCB POINTER          88343
         BNP   ASIDNEXT      NOT ACTIVE OR NEVER USED            88343
         USING ASCB,R15                                          88343
         ICM   R14,15,ASCBTSB  TSO SESSION ?                     88343
         BZ    ASIDNEXT            NO, BRANCH
         ICM   R14,15,ASCBCSCB  GET CSCB POINTER                 88343
         BZ    ASIDNEXT      OR SKIP                             88343
         CLC   MYUSER(7),8(R14) DOES USERID MATCH CSCB NAME ?    88346
         BE    ASIDX               YES, BRANCH
         CLI   MYUSER,C'*'   BROADCAST (ALL)                     88343
         BE    ASIDX         YES                                 88343
         AIF   ('&LOCAL' NE 'PID').NINDEX                        94236
         CLI   MYUSER+7,C' '   ACCOUNT # PARAMETER ?             94236
         BNH   ASIDNEXT      NO                                  94236
         L     R9,CVTPTR     GET THE CVT                         88346
         ICM   R9,15,CVTUSER-CVTMAP(R9)  GET USER EXTENSION      88346
         BZ    ASIDNEXT      ELSE GET NEXT                       88346
         ICM   R9,15,UCLUADS-USERCVT(R9)  POINT TO UADS SUMMARY  88346
         BZ    ASIDNEXT                                          88346
         LH    R1,4(,R4)     GET TEXT LENGTH                     88346
         BCTR  R1,0                                              88346
         LM    R9,R11,0(R9)  POINT TO LIST                       88346
         SR    R9,R10                                            88346
LOOPUADS BXH   R9,R10,ASIDNEXT  GET NEXT OR OUT                  88346
         EX    R1,CLCUADS    COMPARE ACCOUNT NUMBERS             88346
         BNE   LOOPUADS      NO MATCH; TRY AGAIN                 88346
         CLC   0(7,R9),8(R14)  MATCHING USER ID ?                88346
         BNE   LOOPUADS      NO                                  88346
         B     ASIDX         YES                                 88346
CLCUADS  CLC   MYUSER(0),8(R9)  COMPARE ACCOUNT NUMBERS          88346
.NINDEX  SPACE 1                                                 88346
ASIDNEXT L     R0,SAVEASCT   RESTORE LOOP COUNTER                94235
         LA    R2,4(,R2)           POINT TO NEXT ASVTENTY ENTRY
         BCT   R0,ASIDLOOP         BRANCH UNTIL ASVTMAXU EXCEEDED
         ST    R0,SAVEASCT   UPDATE                              94235
         AIF   ('&LOCAL' NE 'PID').NOWYL                         94236
         LA    R0,MYUSER     POINT TO USER-ID                    88343
         LA    R1,MYTEXRDW   POINT TO MESSAGE                    88343
         SVC   247           ISSUE WYLBUR NOTIFY                 88343
         CLI   MYUSER,C'*'   BROADCAST ?                         88346
         BE    WYLCAST       YES                                 88346
         CLI   MYUSER+7,C' '  ACCOUNT # ?                        94236
         BNH   NEXTUSER      NO                                  94236
         SPACE 1                                                 88346
*        LOOP THROUGH LOCAL ON-LINE USER DATA (LOUD) CONTROL BLOCKS.
*                                                                88346
WYLCAST  L     R5,CVTPTR     GET THE CVT                         88346
         ICM   R5,15,CVTUSER-CVTMAP(R5)  CHECK FOR USER CVT      88346
         BZ    NEXTUSER      NONE; DONE                          88346
         LA    R5,UCLOUD-USERCVT-(A$LNEXT-A$LOUD)(,R5)           88346
         USING A$LOUD,R5                                         88346
LOOPLOUD ICM   R5,15,A$LNEXT  TRY ANOTHER                        88346
         BZ    NEXTUSER      NONE                                88346
         TM    A$LFLG1,A$LFGOK  LOGGED-ON USER ?                 88346
         BZ    LOOPLOUD      NO                                  88346
         CLI   MYUSER,C'*'   BROADCAST ?                         88346
         BE    WYLMATCH      YES; FORCED MATCH                   88346
         LH    R14,4(,R4)    GET ACCOUNT LENGTH                  88346
         BCTR  R14,0         SET FOR EXECUTE                     88346
         EX    R14,CLCLOUD   SEE IF ACCOUNT MATCHES              88346
         BNE   LOOPLOUD      NO                                  88346
WYLMATCH LM    R9,R11,A$LSSPTR  POINT TO SUBSYSTEM INFO          88346
         USING A$LSS1,R9     DECLARE IT                          88346
         SR    R9,R10                                            88346
LOOPSSID BXH   R9,R10,LOOPLOUD  TRY ANOTHER                      88346
         CLC   =C'WYL',A$LSS1  WYLBUR ?                          88346
         BNE   LOOPSSID      NO                                  88346
         DROP  R9                                                88346
         LA    R0,A$UUID     POINT TO USER ID                    88346
         CLI   A$UUID+7,C' ' OLD WYLBUR ?                        88346
         BH    LOOPLOUD      YES; ALREADY DONE                   88346
         LA    R1,MYTEXRDW                                       88346
         SVC   247           NOTIFY THIS USER                    88346
         B     LOOPLOUD      TRY AGAIN                           88346
CLCLOUD  CLC   MYUSER(0),A$LCACT  CHECK ACCOUNT NUMBER           88346
         DROP  R5                                                88346
.NOWYL   TM    FLAGS,FGSENT  AT LEAST ONE MESSAGE ATTEMPTED ?    94236
         BNZ   NEXTUSER      YES; DO NEXT USER, IF ANY           94236
         B     TPUT20                                            94235
ASIDX    LH    R8,ASCBASID   GET THE ID                          88343
         DROP  R15                                               88343
         OI    FLAGS,FGSENT  AT LEAST ONE MESSAGE SENT (TRIED)   94236
         SPACE 1
         TPUT  (R6),(R7),ASID=(R8)  HAD ,,,,,LOWP                88343
         B     *+4(R15)
         B     ASIDNEXT      REPEAT FOR BROADCAST/MULTIPLE LOGON 88343
         B     TPUT04
         B     TPUT08
         B     TPUT12
         B     TPUT16
         B     TPUT20
         SPACE 1
TPUT04   MVC   MYMSG(MSG03L),MSG03
         MVC   MYMSG+(MSG03A-MSG03)(7),MYUSER
         LA    R1,MYMSG
         LA    R0,MSG03L
         BAL   R14,PUTLINE
         B     TESTNEXT                                          94235
         SPACE 1
TPUT08   MVC   MYMSG(MSG04L),MSG04
         MVC   MYMSG+(MSG04A-MSG04)(7),MYUSER
         LA    R1,MYMSG
         LA    R0,MSG04L
         BAL   R14,PUTLINE
         B     TESTNEXT                                          94235
         SPACE 1
TPUT12   MVC   MYMSG(MSG05L),MSG05
         MVC   MYMSG+(MSG05A-MSG05)(7),MYUSER
         LA    R1,MYMSG
         LA    R0,MSG05L
         BAL   R14,PUTLINE
         B     TESTNEXT                                          94235
         SPACE 1
TPUT16   MVC   MYMSG(MSG06L),MSG06
         MVC   MYMSG+(MSG06A-MSG06)(7),MYUSER
         LA    R1,MYMSG
         LA    R0,MSG06L
         BAL   R14,PUTLINE
         B     TESTNEXT                                          94235
         SPACE 1
TPUT20   MVC   MYMSG(MSG07L),MSG07
         MVC   MYMSG+(MSG07A-MSG07)(7),MYUSER
         LA    R1,MYMSG
         LA    R0,MSG07L
         BAL   R14,PUTLINE
TESTNEXT ICM   R0,15,SAVEASCT  CALLED FROM END OF ASCB SCAN?     94235
         BNZ   ASIDNEXT      NO; DO NEXT                         94235
         SPACE 1
NEXTUSER CLI   8(R4),X'FF'
         BE    EXIT0
         L     R4,8(,R4)
         NI    FLAGS,255-FGSENT  RESET FOR NEXT ID               94236
         OI    FLAGS,FGFOUND  SET AT LEAST ONE MATCH             94236
         B     USERLOOP
ERRNOTXT LA    R1,MSG01
         LA    R0,L'MSG01
         BAL   R14,PUTLINE
         LA    R15,8         SET ERROR                           94235
         B     EXIT
         SPACE 1
ERRNOUSR LA    R1,MSG02
         LA    R0,L'MSG02
         BAL   R14,PUTLINE
         LA    R15,8         SET ERROR                           94235
         B     EXIT
         SPACE 1
EXIT0    SLR   R15,R15
EXIT     LR    R2,R15
         SPACE 1
         IKJRLSA MYANS
         SPACE 1
         PGMEXIT RC=(R2)     RETURN WITH CONDITION CODE         GP02355
         SPACE 1
************************************************************
*                                                          *
*        PUTLINE ROUTINE                                   *
*                                                          *
************************************************************
         SPACE 1
PUTLINE  STM   R14,R1,PUTLINS
         XC    MYOLD(8),MYOLD
         XC    MYSEG1(4),MYSEG1
         MVC   MYPTPB(12),MODLPTPB
         LA    R14,1               NO. OF MESSAGE SEGMENTS
         ST    R14,MYOLD
         LA    R14,MYSEG1          POINT TO 1ST SEGMENT
         ST    R14,MYOLD+4
         LR    R14,R0              LENGTH IN R0
         LA    R14,4(,R14)         ADD 4
         LA    R15,MYSEG1+4
         CLC   =C'IKJ',0(R1) ARE DATA PRECEDED BY MESSAGE ID?
         BE    *+16                YES - BRANCH
         LA    R14,1(,R14)         ADD 1 TO LENGTH
         MVI   0(R15),C' '         INSERT LEADING BLANK
         LA    R15,1(,R15)         BUMP POINTER
         STH   R14,MYSEG1
         LR    R14,R0
         BCTR  R14,0
         B     *+10
         MVC   0(0,R15),0(R1)      MOVE MESSAGE IN
         EX    R14,*-6
         SPACE 1
         PUTLINE PARM=MYPTPB,OUTPUT=(MYOLD),MF=(E,MYIOPL)
         SPACE 1
         LM    R14,R1,PUTLINS
         BR    R14
         SPACE 1
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE 1
MODLPTPB PUTLINE OUTPUT=(1,TERM,SINGLE,INFOR),                         X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),MF=L
         SPACE 1
MSG01    DC    C'NO TEXT SPECIFIED'
MSG02    DC    C'NO USERID SPECIFIED'
MSG03    DC    C'MESSAGE NOT SENT TO '
MSG03A   DC    C'        - NO BUFFERS AVAILABLE.'
MSG03L   EQU   *-MSG03
MSG04    DC    C'MESSAGE NOT SENT TO '
MSG04A   DC    C'        - ATTN INTERRUPT.'
MSG04L   EQU   *-MSG04
MSG05    DC    C'MESSAGE NOT SENT TO '
MSG05A   DC    C'        - PROFILE NOINTERCOM'
MSG05L   EQU   *-MSG05
MSG06    DC    C'MESSAGE NOT SENT TO '
MSG06A   DC    C'        - LOGIC ERROR IN COMMAND.'
MSG06L   EQU   *-MSG06
MSG07    DC    C'MESSAGE NOT SENT TO '
MSG07A   DC    C'        - NOT LOGGED ON'
MSG07L   EQU   *-MSG07
         SPACE 1                                                 88346
PATWTO   WTO   ' ',ROUTCDE=1,DESC=2,MF=L  NEM-TO-DOM             88346
         SPACE 1                                                 88346
         LTORG
         SPACE 1
************************************************************
*                                                          *
*        PARSE PCL                                         *
*                                                          *
************************************************************
         SPACE 1
         PRINT &PRTSYS                                           88343
TOPCL    IKJPARM
USERID   IKJIDENT 'USERID',LIST,                                       X
               FIRST=ANY,OTHER=ALPHANUM,MAXLNTH=8,                     X
               VALIDCK=USEVC,                                          X
               PROMPT='USERID TO WHOM MESSAGE IS TO BE SENT'
SPACE    IKJPOSIT SPACE
STRING   IKJPOSIT STRING,ASIS,PROMPT='TEXT OF MESSAGE TO BE SENT'
         IKJENDP
         PRINT &PRTSOR                                           88343
*
*        THIS VALIDITY CHECK ROUTINE CHECKS THE FIRST
*        CHARACTER OF EACH USERID SPECIFIED. THE IKJIDENT MACRO
*        SPECIFIES FIRST=ANY SO THAT PARSE IS FORCED TO TREAT
*        THE FIRST OPERAND ENTERED AS THE USERID. THEN THIS
*        ROUTINE REJECTS IT AS IF IT WERE FIRST=ALPHA. SPECIFYING
*        FIRST=ALPHA AND ENTERING SOMETHING LIKE    TO 'XXX'
*        WOULD CAUSE PARSE TO TAKE XXX' AS THE STRING AND PROMPT
*        FOR USERID, WHICH IS NOT DESIRED HERE.
*
         USING USEVC,R10                                         88343
USEVC    STM   R14,R12,12(R13)
         LR    R10,R15
         L     R2,0(,R1)           POINT R2 TO PDE
         SLR   R15,R15
         L     R1,0(,R2)           POINT TO USERID
         CLI   0(R1),C'*'    BROADCAST OPTION ?                  88343
         BE    USEVCR        YES; ALLOW                          88343
         AIF   ('&LOCAL' NE 'CCSI').NOACT#                       94235
         CLI   0(R1),C'0'    ACCOUNT NUMBER VERSION ?            88346
         BNL   USEVCR        YES; SUPPORT IT                     88346
.NOACT#  CLI   0(R1),C'A'          LESS THAN 'A'?
         BL    USEVCR4
         CLI   0(R1),C'Z'          GREATER THAN Z?
         BH    USEVCR4
USEVCR   L     R14,12(,R13)
         LM    R0,R12,20(R13)
         BR    R14
USEVCR4  LA    R15,4
         B     USEVCR
         DROP  R10
         SPACE 1
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
         SPACE 1
SAVE     DSECT ,             SAVE AND WORK AREA                  88343
SAVEASCT DS    F             ASVT LOOP COUNTER                   94235
FLAGS    DC    X'00'         PROCESSING FLAGS                    94236
FGSENT   EQU   X'80'           AT LEAST ONE MESSAGE SENT         94236
FGFOUND  EQU   X'40'           USER ID PROCESSED                 94236
MYPPL    DS    7F
MYANS    DS    F
MYUWA    DS    F
MYECB    DS    F                  USED BY PUTLINE ROUTINE
MYIOPL   DS    4F                 USED BY PUTLINE ROUTINE
MYPTPB   DS    3F                 USED BY PUTLINE ROUTINE
MYOLD    DS    2F                 USED BY PUTLINE ROUTINE
MYSEG1   DS    2H,CL100           USED BY PUTLINE ROUTINE
PUTLINS  DS    4F                 USED BY PUTLINE ROUTINE
MYUSER   DS    D
MYMSG    DS    CL80
MYTEXRDW DC    2H'0'         RDW PREFIX TO TEXT                  88343
MYTEXT   DS    CL256                                             88343
SAVEEND  EQU   *             END OF DYNAMIC AREA                GP02355
         SPACE 1
         PRINT &PRTSYS                                           88343
         IKJCPPL
         SPACE 1
         IKJIOPL
         SPACE 3
         IKJPPL
         SPACE 1
         IKJTCB ,
         IHAPSA ,
         IHAASCB ,                                               88343
         IHAASVT ,                                               88343
         CVT   DSECT=YES
         AIF   ('&LOCAL' NE 'PID').LOCMAC                        94236
USERCVT  USERCVT ,                                               88346
A$LOUD   A$LOUD ,            LOCAL ON-LINE USER DATA             88346
.LOCMAC  END   ,
