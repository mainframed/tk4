@SNAP    TITLE '@ S N A P P E R  ***  STORAGE DUMP; IBM STYLE'
***********************************************************************
**                                                                   **
**   GENERALIZED STORAGE DUMP FACILITY, USING THE INEFFICIENT SNAP   **
**   MACRO AND SERVICES. REQUIRES A SYSTERM  DD CARD.                **
**                                                                   **
**   CALLER DEFINES A SNAP MACRO WITH MF=L, AND PASSES THE ADDRESS   **
**   TO THIS ROUTINE, WHICH WILL DO EVERYTHING ELSE.                 **
**   N.B. R1 HAS SNAP ADDRESS, NOT ADDRESS OF ADDRESS.               **
**                                                                   **
**   YOU MAY, BUT DO NOT NEED TO, CALL @SNAPPER WITH R1=0 TO         **
**   CLOSE THE DCB.                                                  **
**                                                                   **
**   PROGRAM IS REUSABLE, BUT NEITHER REENTRANT NOR REFRESHABLE.     **
**   REQUIRES LOCAL MACROS (SEE CBT FILE 861)                        **
**                                                                   **
**   IBM'S SNAP SUPPORTS 31-BIT ADDRESSES, SO WE DO NOT NEED TO COPY **
**   THE SNAP LIST TO LOW STORAGE. HOWEVER, THE DCB MUST BE LOW.     **
**                                                                   **
**-------------------------------------------------------------------**
**   AUTHOR: G. POSTPISCHIL                          AUGUST 31, 2014 **
**   PLACED IN THE PUBLIC DOMAIN                                     **
**                                                                   **
***********************************************************************
*DEBUG*  PUNCH '  ORDER @SNAPPER(P)'  SNAPPER WITH PEAS?
         SPACE 1
         COPY  ASMOPTS ,     DEFINE GLOBALS
         SPACE 1
         ASMPARM LIST=YES    SET/OVERRIDE GLOBAL ASSEMBLY OPTIONS
         SPACE 1
         PRINT &PRTSRC
@SNAPPER PGMHEAD ZERO,BASE=R12,BNDRY=PAGE,AM=24,RM=24,PARM=R9
         SPACE 1
         LA    R9,0(,R9)     CLEAN IT
         LTR   R9,R9         ANY ADDRESS ?
         BZ    CLOSE           NO; CLOSE REQUEST
         INC   DUMPCTR       INCREMENT DUMP COUNTER
         IC    R7,DUMPCTR+L'DUMPCTR-1
         TM    DCBOFLGS-IHADCB+MYDCB,DCBOFOPN
         BNZ   ISOPEN
         OPEN  MF=(E,OCDCB)
         PRINT &PRTMAC
ISOPEN   SNAP  DCB=MYDCB,ID=(R7),MF=(E,(R9))
         B     EXIT
         SPACE 1
         PRINT &PRTSRC
CLOSE    CLOSE MF=(E,OCDCB)
         SLR   R15,R15       ASSUME IT WORKED
         SPACE 1
         PRINT &PRTSRC
EXIT     PGMEXIT RC=(R15)
         SPACE 1
         LTORG ,
         SPACE 1
OCDCB    OPEN  (MYDCB,OUTPUT),MF=L
MYDCB    DCB   DDNAME=SYSTERM,MACRF=(W),DSORG=PS,                      *
               RECFM=VBA,LRECL=125,BLKSIZE=1632  882
         SPACE 1
DUMPCTR  DC    F'0'          DUMP ID; WRAPS AT 256->0
         SPACE 1
SAVE     DSECT ,
SAVEEND  EQU   *
         SPACE 1
         DCBD  DSORG=PS,DEVD=DA
         END   ,
