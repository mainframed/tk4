RANP     TITLE 'PL/I RANDOM NUMBERS. RETURNS VALUE BETWEEN 0 AND 1'
***********************************************************************
**                                                                   **
**  PL/I: Y=RANDOMP(X) ; WHERE X IS DUMMY PARAMETER              84274*
**  PL/I OPT CALL IS SAME AS CALL RANDOMP(X,Y)                   88055*
**  (FUNCTION INTERFACE MODIFIED TO PASS RETURN VALUE AS         88055*
**    LAST PARAMETER IN CALLING LIST)                            88055*
**                                                                   **
**  IF ENTRY RANDUP IS USED, THE 'X' VALUE PROVIDED IS           84274*
**    THE SEED TO BE USED.                                       81138*
**                                                               81138*
***********************************************************************
         ENTRY RANDUP                                            84274
RANDOMP  START 0                                                 84274
         USING *,R11                                            GP03147
         STM   R14,R12,12(R13)                                   81138
         LR    R11,R15                                          GP03147
         LM    R5,R6,0(R1)   GET ADDRESS OF USER'S PARAMETERS    88055
         BAS   R11,UNCOMMON  GO TO COMMON AND SET BASE           82015
         SPACE 1                                                 81138
         USING *,R11         ALTERNATE ENTRY                     81138
RANDUP   STM   R14,R12,12(R13) SAVE REGISTERS                    81138
         LR    R11,R15       SET BASE                            81138
         LM    R5,R6,0(R1)   GET ADDRESS OF USER'S PARAMETERS    88055
UFIRST   NOP   UNCOMMON      SET TO BRANCH AFTER FIRST CALL      81138
         OI    UFIRST+1,X'F0'                                    81138
         OI    FIRST+1,X'F0'                                     81138
         L     R1,0(,R5)     GET USER'S X VALUE                  84274
         LA    R11,COMMON    MAKE NEW BASE                       81138
         B     ODDITY-COMMON(R11) BRANCH TO COMMON               81138
UNCOMMON BAS   R11,COMMON    USE COMMON CODE                     81138
         SPACE 1                                                 81138
         USING *,R11         NEW BASE                            81138
COMMON   L     R1,U                                              81138
FIRST    NOP   NEXT          CHANGED TO BRANCH AFTER FIRST CALL  81138
         OI    FIRST+1,X'F0'   MAKE NOP INTO BRANCH              81138
         TIME  BIN
         AR    R1,R0                                            GP03147
         A     R1,U                                             GP03147
ODDITY   LA    R0,1                                              81138
         OR    R1,R0         ENSURE ODD SEED                     81138
NEXT     M     R0,Y                                             GP03147
         SRL   R1,8                                             GP03147
         ST    R1,U                                             GP03147
         A     R1,EXP                                           GP03147
         ST    R1,SAVE                                          GP03147
         LE    R0,SAVE                                          GP03147
         LTR   R5,R5         MORE THAN ONE PARAMETER ?           88055
         BNM   TWOPARM       YES                                 88055
         STE   R0,0(,R5)     RETURN TO USER                      84274
         B     DONPARM                                           88055
TWOPARM  STE   R0,0(,R6)     RETURN TO USER                      88055
DONPARM  LM    R14,R12,12(R13)                                  GP03147
         BR    R14                                              GP03147
*
U        DC    F'262145'
Y        DC    F'13187'
EXP      DC    X'40000000'
SAVE     DC    F'0'          TEMP SAVE                           81138
         PRINT NOGEN                                            GP03147
         YREGS ,                                                GP03147
         END
