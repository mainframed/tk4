HELP     TITLE 'P O D H E L P  ***  LIST JCL PDS COMMENTS CARDS' 86300
         COPY  OPTIONGB                                          86300
         SPACE 1                                                 86300
         SYSPARM LIST=YES                                        86300
         EJECT ,                                                 86300
         PRINT &PRTSOR                                           86300
PODHELP  PGMHEAD ZERO12,BASE=R12,PARM=R10,                      GP02265*
               AM=31,RM=24   DCBS AND EXITS - STAY LOW          GP02265
*        (0)   SCRATCH
*        (1)   SCRATCH
*        (3)   DIRECTORY ENTRY
*        (4)   DIRECTORY END
*        (5)   PDS BUFFER A
*        (6)   CONSTANT INCREMENT OF 80
*        (7)   COMPARAND FOR BXLE OF END PDS BUFFER A
*        (8)   PDS BUFFER B
*        (9)  BAL REGISTER
*        (12) BASE
         SERVINIT ,          LOAD THE SERVICE ROUTINE            86300
         SERVCALL LPALD,=CL8'@DCBXLST'  GET THE PATTERN EXIT LIST
         STCM  R0,7,DCBEXLSA-IHADCB+PDS  STASH IT                86300
         PUSH  USING                                            GP02265
         LTCB  R2,USE=YES    GET OUR TCB                         86307
         L     R1,TCBJSCB    GET THE JSCB                        86307
         USING IEZJSCB,R1    DECLARE IT                          86307
         L     R1,JSCBCSCB   GET THE CSCB                        86307
         USING CHAIN,R1                                          86307
         AIF   (NOT &MVSXA).NOCSCBX                             GP05160
         L     R2,CHCSCXP    GET THE EXTENSION                  GP02265
         USING CSCX,R2       DECLARE IT                         GP02265
.NOCSCBX SLR   R0,R0                                             86307
         IC    R0,CHUCMP     GET ORIGINATING CONSOLE             86307
         ICM   R15,15,CHCIBP IS THERE A CIB (START ?)            86307
         BZ    NOCIB                                             86307
         IC    R0,CIBCONID-CIB(,R15)                             86307
NOCIB    L     R15,CVTPTR    GET THE CVT                         86307
         USING CVTMAP,R15                                        86307
         ICM   R1,15,CVTCUCB GET THE UCM                         86307
         BZ    NOTAUTH       NONE ?                              86307
         LM    R3,R5,UCMVEA-UCM(R1)  GET THE CONSOLE POINTERS    94229
         LTR   R15,R0        ANY CONSOLE ?                       86307
         BP    GETUCM        YES; CHECK IT                       86307
         LR    R14,R1        COPY THE UCM ADDRESS                86307
         SH    R14,=H'8'     GET PREFIX                          86307
         ICM   R3,15,UCMPRFXP-UCM2PTR(R14)  GET THE MCS EXTENSION
         BZ    NOTAUTH                                           86307
         ICM   R2,15,UCMMCENT-UCMPRFX(R3)  GET THE MASTER CONSOLE
         BZ    NOTAUTH       TOO BAD                             86307
         LR    R3,R2         COPY OVER                           86307
         B     COMUCM                                            86307
GETUCM   SR    R3,R4         ALLOW FOR RELATIVITY                86307
         MR    R14,R4        GET OFFSET IN R15                   86307
         AR    R3,R15        GET CONSOLE ENTRY                   86307
COMUCM   CR    R3,R5         VALID ?                             86307
         BH    NOTAUTH       NO; ALL HOPES ARE DASHED            86307
         USING UCMLIST,R3                                        86307
         IC    R0,UCMID      (RE)LOAD THE UCM ID                 86307
         ST    R0,MYUCMID                                        86307
         MVI   PATFLAGS,X'40'  SET UCM ID PRESENT                86307
         TM    UCMDISP,UCMDISPC  GRAPHICS DEVICE ?               86307
         BZ    NOTAUTH       NO                                  86307
         POP   USING                                            GP02265
         TESTAUTH FCTN=1     ARE WE AUTHORIZED ?                 86300
         BXH   R15,R15,NOTAUTH  NO                               86300
         OI    FLAGS,AUTH    SET AUTHORIZATION                   86300
         MVI   PATFLAGS+1,X'40'  SET MULTI-LINE WTO REQUEST      86300
         MVI   PATFLAGS,X'80'   ROUT/DESC                        86300
         MVI   ROUTCODE+2,X'40'  MASTER INFO                     86300
         MVI   ROUTCODE,X'01'   DESC=8                           86300
         MVI   ROUTCODE+1,X'80'   DESC=9                         86313
         XC    PREVID,PREVID CLEAR MLWTO ID                      86307
NOTAUTH  SERVCALL RJFCB,DCBDDNAM-IHADCB+POD  GET THE JFCB        86300
         BXH   R15,R15,NOUT1  NO                                 86300
         MVC   INFMJFCB(JFCBLGTH),0(R1)  SAVE IT                 86300
         LA    R1,INFMJFCB                                       86300
         STCM  R1,7,DIREX+1  COMPLETE EXIT LIST                  86300
         OI    JFCBTSDM,JFCNDCB+JFCNDSCB+JFCNWRIT  KEEP IT CLEAN 86300
         OPEN  MF=(E,OP1),TYPE=J
         TM    DCBOFLGS-IHADCB+POD,DCBOFOPN  OPEN ?              86300
         BZ    NOUT1         NO ?                                86300
         OPEN  (PDS,INPUT)
         TM    DCBOFLGS-IHADCB+PDS,DCBOFOPN  OPEN ?              86300
         BZ    NOUT1         NO ?                                86300
         GETBUF PDS,R5
         GETBUF PDS,R8
*              SEPARATE OPENS TAKE LONGER,
*              BUT THEY REDUCE THE SIZE OF THE OPEN WORK AREA.
         BAS   R9,WTOINIT    SIGN ON                             86300
         LA    R6,80
         USING DIRECTRY,R3
READIR   GET   POD                      READ A DIRECTORY BLOCK
         LR    R3,R1
         LH    R4,BLKLEN                FIND END OF DIRECTORY BLOCK
         AR    R4,R3
         LA    R3,MEMNAME
         USING MEMNAME,R3
USEDIR   CLI   MEMNAME,X'FF'            IS IT END OF DIRECTORY
         BE    ENDIR                     YES - TREAT AS EOF
         TM    ENTLEN,X'80'  IS THIS AN ALIAS ?
         BO    ENDMEM
NOSSI    MVC   FINDWORD(3),MEMTTR       SET UP FOR FIND
         FIND  PDS,FINDWORD,C
         NI    FLAGS,255-MEMFG  RESET FLAG FOR NEW MEMBER        86300
         READ  DECB,SF,,(R5),'S',MF=E    PRIME BUFFERS
NEWBLOCK L     R5,DECAREA               SWAP BUFFERS
         CHECK DECB
         LH    R7,PDS+DCBBLKSI-IHADCB   FIND BLOCK END FOR BXLE
         L     R1,DECIOBPT
         USING IOBSTDRD,R1
         SH    R7,IOBCSW+5
         AR    R7,R5
         BCTR  R7,0
         READ  DECB,SF,,(R8),'S',MF=E
         LR    R8,R5                    SAVE BUFFER START LOC FOR SWAP
NEXTCARD CLC   =C'//* ',0(R5) IS THIS A COMMENTS CARD PRECEDING JCL?
         BNE   SKIPMEM       NO, SKIP THIS MEMBER
         LA    R14,4(,R5)    SET START COPY ADDRESS              86300
         LA    R15,72-4      SET LENGTH TO COPY                  86300
         LA    R0,X'20'      SET FOR DATA LINE                   86300
         TM    FLAGS,MEMFG   FIRST ENTRY FOR THIS MEMBER ?       86300
         BO    PUTWTO        NO, WRITE TEXT
         MVC   0(8,R14),MEMNAME  COPY THE MEMBER NAME            86300
         MVI   8(R14),C' '   MAKE SPACER, JUST IN CASE           86300
         OI    FLAGS,MEMFG   SET FIRST TIME FLAG                 86300
PUTWTO   BAS   R9,WTOLINE    PRINT A LINE                        86300
         BXLE  R5,R6,NEXTCARD
         B     NEWBLOCK
         SPACE 1
SKIPMEM  CHECK DECB          DUMMY TO RESET BPAM FOR NEXT MEMBER
         L     R5,DECAREA    GET OTHER BUFFER BACK
ENDMEM   IC    R1,ENTLEN     GET LENGTH OF USER DATA
*                                        IN HALFWORDS
         N     R1,=F'31'                MASK OUT FLAGS
         SLA   R1,1                     CONVERT LENGTH TO BYTES
         LA    R3,USERDATA(R1)           FIND NEXT ENTRY
         CR    R3,R4                    TEST EOB
         BL    USEDIR
         B     READIR
         SPACE 3
ENDIR    CLOSE POD
         CLOSE PDS
         BAS   R9,WTOLAST    SIGN OFF                            86300
EXIT     PGMEXIT RC=0        QUIT                               GP02265
         SPACE 1                                                 86300
NOUT1    WTO   'SYSUT1 DD MISSING OR UNUSABLE',ROUTCDE=1,DESC=2 DOM IT
         LA    R0,NOUT1+4                                        86300
         L     R1,=X'81013000'                                   86300
         SVC   13            TAKE A DIVE                         86300
         SPACE 2                                                 86300
BPAMSIN  SYNADAF ACSMETH=BPAM
         B     SIN
BSAMSIN  SYNADAF ACSMETH=BSAM
         B     SIN
QSAMSIN  SYNADAF ACSMETH=QSAM
SIN      MVC   TSBUF,=CL22' '           ASSUME NO BUFFER ADDRESS
         ST    R0,SYNADCOD
         ST    R1,SYNADBUF
         LTR   R0,R0                    SUCCESSFUL?
         BZ    TSMNOBUF                  YES
         SH    R0,=H'8'
         BM    TSBUFF                    YES
         MVC   TSBUF,=CL22'SYNADF R0=8'
         B     TSMNOBUF
TSBUFF   MVC   TSBUF,=CL22'BUF=XXXXXX, SIZE=XXXX.'
         UNPK  TSBUF+4(7),9(4,R1)       MOVE IN BUFFER LOC
         NC    TSBUF+4(6),=X'0F0F0F0F0F0F'
         TR    TSBUF+4(6),=C'0123456789ABCDEF'
         MVI   TSBUF+10,C','
         UNPK  TSBUF+17(5),12(3,R1)     MOVE IN BUFFER SIZE
         NC    TSBUF+17(4),=X'0F0F0F0F0F0F'
         TR    TSBUF+17(4),=C'0123456789ABCDEF'
         MVI   TSBUF+21,C'.'
TSMNOBUF MVC   TSERRMSG,50(R1)          MOVE SYNADF BUFFER TO MSG
         WTO   MF=(E,TS)
         SPACE 2
*              THIS FEATURE WAS NEVER OFFICIALLY AVAILABLE
*              IT WAS NEVER IN MVT
*              THE ADVENT OF ROUTCDE=11 HAS PROBABLY KILLED IT FOR GOOD
         L     R1,=X'81001000'          DEAR ABBY
         LA    R0,TS                     I HAVE A MESSAGE FOR YOU
         SVC   13                       GOODBY
         SPACE 1                                                 86300
WTOINIT  LA    R0,X'40'      SET FOR LABEL LINE                  86300
         TM    PATFLAGS+1,X'40'  MLWTO MODE ?                    86300
         BZ    WTOLINE1      NO; JUST WRITE A BLANK LINE         86300
         MVC   WTOTEXT(6),JFCBVOLS                               86300
         MVI   WTOTEXT+6,C' '                                    86300
         MVC   WTOTEXT+7,JFCBDSNM                                86300
         LA    R14,WTOTEXT                                       86300
         LA    R15,6+1+L'JFCBDSNM                                86300
         B     WTOLINE       WRITE A LINE                        86300
         SPACE 1                                                 86300
WTOLAST  LA    R0,X'10'      SET END FLAG                        86300
         B     WTOLINE1      WRITE A NULL LINE                   86300
         SPACE 1                                                 86300
WTOLINE  LA    R1,0(R14,R15) POINT PAST LAST BYTE                86300
         LTR   R15,R15       VALID ?                             86300
         BNP   WTOLINE1      NO; FORCE IT                        86300
WTOLINER BCTR  R1,0                                              86300
         CLI   0(R1),C' '    TRAILING BLANK ?                    86300
         BNE   WTOLINEM      NO                                  86300
         BCT   R15,WTOLINER  SHORTEN                             86300
WTOLINE1 LA    R15,1         FORCE AT LEAST ONE                  86300
         LA    R14,=C' '                                         86300
WTOLINEM BCTR  R15,0                                             86300
         EX    R15,WTOMVC    MOVE LINE TO TEXT AREA              86300
         LA    R15,5(,R15)   MAKE NEW LENGTH                     86300
         STH   R15,WTORDW    SET IT                              86300
         LA    R1,WTORDW(R15)  POINT TO NEXT AVAILABLE BYTE      86300
         MVC   WTOFLAGS,PATFLAGS  MOVE FLAGS                     86300
         TM    WTOFLAGS,X'80'  ROUTING CODE                      86300
         BZ    WTONROUT      NO                                  86300
         MVC   0(4,R1),ROUTCODE  MOVE DESCRIPTOR AND ROUTING CODES
         LA    R1,4(,R1)     SKIP OVER THEM                      86300
WTONROUT TM    WTOFLAGS+1,X'40'  MULTI-LINE WTO REQUESTED ?      86300
         BNZ   WTOMLWTO      YES                                 86300
         L     R0,MYUCMID    LOAD UCM-ID (JUST IN CASE)          86300
         WTO   MF=(E,WTORDW)   WRITE IT                          86300
         BR    R9            RETURN                              86300
         SPACE 1                                                 86300
WTOMLWTO STC   R0,0(,R1)     STASH MLWTO FLAGS                   86300
         TM    0(R1),X'F0'   ANY FLAGS SET ?                     86300
         BNZ   *+8           YES                                 86300
         OI    0(R1),X'20'   SET FOR DATA LINE                   86300
         MVC   1(3,R1),=XL3'000001'  SET NUMBER OF LINES         86300
         MODESET KEY=ZERO,MODE=SUP  PRIVILEGED FOR DISCRETE MLWTO
         L     R0,PREVID     GET PRIOR MLWTO ID                  86300
         SLL   R0,8          ALIGN                               86313
         WTO   MF=(E,WTORDW)                                     86300
         LTR   R2,R15        TEST RETURN                         86307
         BZ    WTOMLWOK      OK                                  86307
         NI    PATFLAGS+1,255-X'40'  TURN OFF MLWTO FLAG         86307
         B     WTOMLWON      NO GOOD                             86307
WTOMLWOK ST    R1,PREVID     MAKE CHAIN WORD                     86307
         NI    PATFLAGS,255-X'40'-X'80'  KILL ROUT/DESC/UCMID    86307
WTOMLWON DS    0H            DON'T TRUST LABEL ON MODESET        86307
         MODESET KEY=NZERO,MODE=PROB                             86300
         BR    R9            RETURN                              86300
WTOMVC   MVC   WTOTEXT(0),0(R14)  MOVE TEXT TO WTO AREA          86300
         EJECT
SYNADBUF DS    F
TS       DC    Y(TSEND-TS),X'8000'
         DC    C'TSM I/O ERROR   '
SYNADCOD DS    F
TSERRMSG DC    CL78' '
TSBUF    DC    CL22' '
TSEND    DC    XL2'0200',XL2'0060'
         SPACE 1
         SPACE 1
DECB     DS    0F
DECSDECB DC    F'0'                     ECB
DECTYPE  DC    X'8080'
*              FIRST BYTE MEANS LENGTH 'S'
*              SECOND BYTE MEANS READ SF
DECLNGTH DC    H'0'
DECDCBAD DC    A(PDS)                   DCB ADDRESS
DECAREA  DC    A(0)                     BUFFER
DECIOBPT DC    A(0)                     IOB
         SPACE
         SPACE 1
OP1      OPEN  (POD,INPUT),MF=L
FINDWORD DC    XL3'0',X'0',X'0',X'0'
         SPACE 1                                                 86300
POD      DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GL,                        *
               SYNAD=QSAMSIN,                                          *
               RECFM=F,LRECL=256,BLKSIZE=256,KEYLEN=8,BUFNO=1,         *
               EODAD=ENDIR,EXLST=DIREX
DIREX    DC    0A(0),X'87',AL3(INFMJFCB-INFMJFCB)                86300
         SPACE 1                                                 86300
PDS      DCB   DDNAME=SYSUT1,DSORG=PO,MACRF=R,                         *
               SYNAD=BPAMSIN,                                          *
               RECFM=FB,LRECL=80,BUFNO=2,NCP=1,                        *
               EODAD=ENDMEM,EXLST=1                              86300
         SPACE 2                                                 86300
DIRECTRY DSECT ,
BLKLEN   DS    H                        LENGTH OF DIRECTORY BLOCK
MEMNAME  DS    CL8                      MEMBER NAME
MEMTTR   DS    XL3                      RELATIVE ADDRESS OF MEMBER
ENTLEN   DS    XL1                      LENGTH OF USER DATA IN HALFWORD
USERDATA EQU   *
         SPACE 1
SAVE     DSECT ,             WORK / SAVE AREA                    86300
@SERVICE DS    A             ADDRESS OF @SERVICE ROUTINE         86300
PATFLAGS DC    Y(0)          MCS FLAGS                           86300
ROUTCODE DS    XL4'0'        DESC/ROUT CODE                      86300
PREVID   DS    0F      1/2   PREVIOUS MULTI-LINE WTO ID          86307
MYUCMID  DS    F       2/2   UCM ID IF UCM ROUTING REQUESTED     86307
         SPACE 1                                                 86300
WTORDW   DS    Y             LENGTH                              86300
WTOFLAGS DS    Y             FLAGS                               86300
WTOTEXT  DS    CL70          MAXIMUM TEXT LENGTH                 86300
WTOPOST  DS    XL4           MLWTO FLAGS / DESC/ROUTCDE          86300
         DS    XL4           OTHER                               86300
FLAGS    DC    X'0'          PROCESSING FLAGS                    86300
AUTH     EQU   X'80'           AUTHORIZED TASK FLAG              86300
MEMFG    EQU   X'01'           FIRST TIME IN MEMBER              86300
         IEFJFCBN ,
SAVEEND  EQU   *                                                GP02265
         SPACE 2                                                 86300
         DCBD  DSORG=PS
         CVT   DSECT=YES                                         86300
         IEZIOB ,
         IHACDE ,
         IHAPSA ,                                                86307
         IKJTCB ,                                                86307
         IEZJSCB ,                                               86307
         IEECHAIN ,                                              86307
CIB      IEZCIB ,                                                86307
UCM      DSECT ,             *OLD* FORMAT CONVENTION            GP05160
         IEECUCM ,                                               86307
         SPACE 2                                                 86300
         PRINT GEN                                               86300
SHOWOFF  DSECT ,                                                 86300
HEADLINE WTO   ('CONTROL',C),('LABEL',L),DESC=8,ROUTCDE=2,MF=L   86300
         SPACE 1                                                 86300
OTHER    WTO   ('DATA LINE',D),MF=L                              86300
         SPACE 1                                                 86300
LASTLINE WTO   (,E),MF=L         LAST LINE                       86300
         END
