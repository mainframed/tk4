STAT     TITLE 'S M F S T A T S  ***  DAILY/MONTHLY SMF STATISTICS'
         MACRO ,
&NM      VECTOR &P=DAY                                          GP12069
         AIF   ('&NM' EQ '').NOLAB                              GP12069
&NM      DS    0D                                               GP12069
.NOLAB   ANOP  ,                                                GP12069
&P.DISK  DC    D'0'          DISK I/O                           GP12069
&P.TAPE  DC    D'0'          TAPE I/O                           GP12069
&P.OTH   DC    D'0'          OTHER I/O                          GP12069
&P.SPOOL DC    D'0'          SPOOLED SYSOUT                     GP12069
&P.PAGE  DC    D'0'          PAGING I/O                         GP12069
&P.LEND  EQU   *-&P.DISK      LENGTH OF DOUBLE WORDS            GP12069
&P.WAIT  DC    F'0'          SYSTEM WAIT TIME                   GP12069
&P.OHED  DC    F'0'          SYSTEM OVERHEAD                    GP12069
&P.CPU   DC    F'0'          BILLABLE CPU TIME                  GP12069
&P.IPL   DC    F'0'          NUMBER OF IPLS                     GP12069
&P.CRASH DC    F'0'          SYSTEM CRASHES                     GP12069
&P.JOB   DC    F'0'          NUMBER OF JOBS                     GP12069
&P.STEP  DC    F'0'          NUMBER OF STEPS                    GP12069
&P.ABEND DC    F'0'          NUMBER OF JOB ABENDS               GP12069
&P.JCLER DC    F'0'          NUMBER OF JCL ERRORS               GP12069
&P.CAN   DC    F'0'          OPER. CANCEL                       GP12069
&P.$E    DC    F'0'          RESTARTED JOBS                     GP12069
&P.SESS  DC    F'0'          WYLBUR SESSIONS                    GP12069
&P.LENF  EQU   *-&P.WAIT      LENGTH OF SINGLE WORDS            GP12069
&P.DAY   DC    H'0'          NUMBER OF DAYS WITH DATA           GP12069
&P.FLAG  DC    X'0'          SMF LOST ERROR FLAG                GP12069
&P.WEEK  DC    X'00'         DAY OF WEEK (0-SAT, 1-SUN, ETC)    GP12069
&P.MMDD  DC    CL5'MM/DD'    CALENDAR DATE                      GP12069
&P.MONA  DC    CL5' '        SHORT NAME OF MONTH                GP12069
&P.LEN   EQU   *-&P.DISK      LENGTH OF DAILY RECORD            GP12069
         MEND  ,                                                GP12069
         SPACE 1
         MACRO ,                                                GP12069
&NM      FIXDATE &ITEM                                          GP12069
&NM      MACPARM R15,&ITEM                                      GP12069
         MACPARM R14,FIXDATE,OP=BAL                             GP12069
         MEND  ,                                                GP12069
         SPACE 1
         MACRO                                                   86040
&NM      CNVD  &R,&AD,&LEN                                       86040
         GBLC  &MACPLAB                                          86040
         LCLC  &L                                                86040
&L       SETC  'L'''                                             86040
&MACPLAB SETC  '&NM'                                             86040
         AIF   ('&R'(1,1) EQ '(').RP                             86040
&NM      MACPARM R0,(&R)     SET VALUE                           86040
         AGO   .LA                                               86040
.RP      ANOP  ,                                                 86040
&NM      MACPARM R0,&R       SET VALUE                           86040
.LA      ANOP  ,                                                 86040
&NM      MACPARM R1,&AD      GET OUTPUT ADDRESS                  86040
         AIF   ('&LEN' NE '').LEN                                86040
         AIF   ('&AD'(1,1) EQ '(').TLEN                          86040
&NM      LA    R15,&L&AD .     USE IMPLICIT OUTPUT LENGTH        86040
&MACPLAB SETC  ''                                                86040
         AGO   .BAL                                              86040
.TLEN    AIF   ('&LEN' NE '').LEN                                86040
         MNOTE 4,'LENGTH REQUIRED'                               86040
.LEN     ANOP  ,                                                 86040
         MACPARM R15,&LEN    GET OUTPUT LENGTH                   86040
.BAL     ANOP  ,                                                 86040
         MACPARM R14,DCONVERT,OP=BAL                             86040
         MEND  ,                                                 86040
         SPACE 1                                                 91042
         MACRO ,                                                 91042
&NM      BOFF  &RECNO                                            91042
         AIF   ('&NM' EQ '').NOTAG                               91042
&NM      DS    0H                                                91042
.NOTAG   ANOP  ,                                                 91042
OFF&RECNO EQU  *-OFFBASE                                         91042
         B     TYPE&RECNO                                        91042
         MEND  ,                                                 91042
         SPACE 1                                                 91042
         MACRO ,                                                 91042
&NM      DOREC &RECNO                                            91042
&NM      MVI   SELECTBL+&RECNO,OFF&RECNO  ADD THIS RECORD TYPE   91042
         MEND  ,                                                 91042
         SPACE 1                                                 86040
         COPY  OPTIONGB                                          86040
         SPACE 1                                                 86040
         SYSPARM LIST=YES    SET GLOBAL OPTIONS                  86040
         SPACE 1
         PUNCH ' ORDER SMFSTATS(P) '   DUMPS EASIER TO READ     GP12069
         SPACE 1                                                 91042
***********************************************************************
*                                                                     *
*        THIS PROGRAM PRODUCES LONG-TERM SMF STATISTICS.              *
*           DD CARD SMF POINTS TO SEVERAL MONTHS OF SMF DATA.         *
*        PARM=NOUSE WILL SKIP LOCAL (ESP) RECORDS; PARM=TYPE30 WILL   *
*           IGNORE 4,5,34 AND 35 RECORDS AND USE 30 INSTEAD.          *
*        PARM=TYPE26 WILL USE SPOOL COUNTS FROM TYPE 26 RECORD RATHER *
*           THAN TYPE 6. 26 IS RECORDS SPOOLED, 6 HAS RECORDS ACTUALLY*
*           PRODUCED ON OUTPUT.  ALSO 26 HAS TIME LAG (WRITTEN LATER).*
*                                                                     *
*        CPU=N,  N=1 TO 8, SPECIFIES DEFAULT # OF CPUS IN COMPLEX     *
*                                                                     *
*        RANGE=YYDDD/YYDDD  SPECIFIES RECORD SELECTION BY DATE RANGE  *
*        RANGE=CCYYDDD/CCYYDDD  SPECIFIES RECORD SELECTION BY DATE    *
*                                                                     *
*        ATTRIBUTES: NOT REFRESHABLE, NOT RE-ENTRANT, NOT RE-USABLE   *
*           PAGE ALIGNMENT, AMODE 24, RMODE 24, AC(0)                 *
*                                                                     *
***********************************************************************
         SPACE 1                                                 91042
         PRINT &PRTSOR                                           86040
SMFSTATS PGMHEAD ZERO15,BASE=(R11,R12),PARM=R6,BNDRY=PAGE       GP12069
         ST    R13,@WORK     SAVE WORK AREA ADDRESS              86040
         SERVINIT ,          LOAD AND INITIALIZE THE SERVICE ROUTINE
         SERVLOAD @PRINTER,SUBP2M,SUBP2D  LOAD SERVICE ROUTINES GP12069
         L     R0,=A(24*60*60*100)  CPUS * 1/100 SECS IN ONE DAY 91042
         ST    R0,CPUDAYS    SET FOR CURRENT CPU COUNT           91042
         ST    R0,DEFCPUD                                        91042
         ZAP   LODATE,=P'75001'  LOWEST ACCEPTABLE DATE          91042
         ZAP   HIDATE,=P'2100366'  HIGHEST ACCEPTABLE DATE       91042
         MVI   PRTDEV,1      SET FOR DETAIL LISTING              91042
         PRTOPEN SYSPRINT,OPT=(DUMMY)  OK IF NO DETAIL FILE      86040
         PRTOPEN SUMPRINT,OPT=(DUMMY,NOWTO),DEV=2  SUMMARY FILE  86040
         CH    R15,=H'4'     DID THE SUMMARY FILE OPEN ?         91042
         BH    *+8           NO                                  91042
         MVI   PRTDEV,2      YES; USE IT FOR ERROR MESSAGES      91042
         PRTLIST HEADER0,DEV=(1,2),TITLE=1                       91042
         PARMLOAD R8         LOAD PARM ADDR/LEN INTO R8/R9      GP12069
         LTR   R6,R9         SAVE AND TEST PARM LENGTH          GP12069
         BZ    NOPARMS       NO                                  91042
         LA    R0,SELECTBL   POINT TO TEXT HOLDER                91042
         LR    R5,R0         SAVE DATA ADDRESS FOR SCANNING      91042
         LA    R1,100+1        AND MAXIMUM PARM LENGTH+1         91042
         ICM   R9,8,=C' '    SET FOR BLANK FILL                  91042
         MVCL  R0,R8         SAVE AND PAD PARM DATA              91042
         PRTLIST GUDPRMSG,DEV=2  SHOW PARM                       91042
         AR    R6,R5         SET END (+1)                        91042
PARMLOOP BLOOK T=PARMOPTS,R=(R6)  END PRESET                     91042
         CLI   0(R5),C' '    ALL DONE ?                          91042
         BE    NOPARMS       YES; ALL DONE                       91042
BADPRM   PRTLIST BADPRMSG,DEV==PRTDEV  SHOW BAD PARM             91042
         SERVTERM ,                                              91042
         ABEND 006           QUIT                                91042
         SPACE 1                                                 91042
PARMOPTS BTAB  NOUSE,SETNOUSE,BASE=*  BYPASS USER RECORDS        91042
         BTAB  DEBUG15,SETBUG15        SET INPUT DEBUG MODE     GP12069
         BTAB  TEST15,SETBUG15         SET INPUT DEBUG MODE     GP12069
         BTAB  DUMP15,SETBUG15         DITTO                    GP12069
         BTAB  DEBUG35,SETBUG35        SET OUTPUT DEBUG MODE    GP12069
         BTAB  TEST35,SETBUG35         SET OUTPUT DEBUG MODE    GP12069
         BTAB  DUMP35,SETBUG35         DITTO                    GP12069
         BTAB  DEBUG,SETBUG            SET DEBUG MODE            91042
         BTAB  TEST,SETBUG             SET DEBUG MODE           GP12064
         BTAB  DUMP,SETBUG             DITTO                     91042
         BTAB  RAW,SETRAW              DON'T SCALE BY 1K        GP12069
         BTAB  USER,SETYUSE           ALLOW USER RECORDS         91042
         BTAB  TYPE30,SETTYP30         USE 30; IGNORE 4,5,34,35  91042
         BTAB  TYPE32,SETTYP32         USE TYPE 32 INSTEAD OF 4-30
         BTAB  'CPU=',SETCPU#          NO. OF CPUS IN PROCESSOR  91042
         BTAB  TYPE6,SETTYP6                                     91042
         BTAB  TYPE06,SETTYP6                                    91042
         BTAB  TYPE26,SETTYP26                                   91042
         BTAB  'DATE=',SETDATES  DATE RANGE                      91042
         BTAB  'RANGE=',SETDATES                                 91042
         BTAB  ',',PARMLOOP  SKIP COMMAS                         91042
         BTAB  *END          NO MORE                             91042
         SPACE 1                                                 91042
SETNOUSE OI    OPTS,OFNOUSE   SET NO USER RECORDS                91042
         B     PARMLOOP      GO AGAIN                            91042
SETYUSE  NI    OPTS,255-OFNOUSE  PROCESS USER RECORDS            91042
         B     PARMLOOP      LOOK AGAIN                          91042
SETTYP30 OI    OPTS,OFTYP30  SET TYPE 30 MODE                    91042
         B     PARMLOOP                                          91042
SETTYP32 OI    OPTS,OFTYP32  SET TYPE 32 MODE                    91042
         B     PARMLOOP                                          91042
SETTYP26 OI    OPTS,OFTYP26                                      91042
         B     PARMLOOP                                          91042
SETTYP6  NI    OPTS,255-OFTYP26                                  91042
         B     PARMLOOP                                          91042
SETRAW   OI    OPTS,OFRAW    SET UNSCALED OUTPUT                GP12069
         B     PARMLOOP      LOOK AGAIN                         GP12069
SETBUG   OI    OPTS,OFBUG15+OFBUG35  SET BOTH                   GP12069
         B     PARMLOOP      LOOK AGAIN                          91042
SETBUG15 OI    OPTS,OFBUG15  SET INPUT DEBUG MODE               GP12069
         B     PARMLOOP      LOOK AGAIN                         GP12069
SETBUG35 OI    OPTS,OFBUG35  SET OUTPUT DEBUG MODE              GP12069
         B     PARMLOOP      LOOK AGAIN                         GP12069
         SPACE 1                                                 91042
SETCPU#  SLR   R0,R0                                             91042
         IC    R0,0(,R5)                                         91042
         SH    R0,=H'240'    MAKE INTO BINARY INTEGER            91042
         BNP   BADPRM        TOO BAD                             91042
         L     R1,=A(24*60*60*100)  SECS IN DAY                  91042
         MR    R0,R0         GET TOTAL                           91042
         ST    R1,CPUDAYS                                        91042
         ST    R1,DEFCPUD    STASH IT                            91042
         LA    R5,1(,R5)     SKIP OVER DIGIT                     91042
         B     PARMLOOP      AND CONTINUE                        91042
         SPACE 1                                                 91042
SETDATES CLI   5(R5),C'/'    YYDDD FORMAT ?                     GP12069
         BE    SETDATE5                                         GP12069
         PACK  LODATE,0(7,R5)  PACK LOW CCYYDDD DATE            GP12069
         PACK  HIDATE,8(7,R5)  PACK HIGH DATE                   GP12069
         LA    R5,7+1+7(,R5) SKIP IT                            GP12069
         B     PARMLOOP      TRY FOR MORE                       GP12069
SETDATE5 PACK  LODATE,0(5,R5)  PACK LOW DATE                     91042
         PACK  HIDATE,6(5,R5)  PACK HIGH DATE                    91042
         FIXDATE LODATE                                         GP12069
         FIXDATE HIDATE                                         GP12069
         LA    R5,5+1+5(R5)  RANGE=YYDDD/YYDDD                   91042
         B     PARMLOOP      TRY FOR MORE                        91042
         SPACE 1                                                 91042
GUDPRMSG FD    'PARM=''',NL                                      91042
         FD    SELECTBL,101,DEB                                  91042
         FD    ''''                                              91042
         FD    *END                                              91042
         SPACE 1                                                 91042
BADPRMSG FD    'UNRECOGNIZED PARM ENTRY:',NL                     91042
         FD    0(R5),10,PAD  SHOW BAD TEXT                       91042
         FD    *END                                              91042
         SPACE 1                                                 91042
HEADER0  FDOPT NL,CC=C'#'    REQUEST PAGE/TIME/ETC.              91042
         FD    'SMFSTATS - INPUT INFORMATION',RADJ,LEN=80        91042
         FD    *END                                              91042
         SPACE 1                                                 91042
NOPARMS  XC    SELECTBL(256),SELECTBL  CLEAR RECORD TYPE SELECTION
         DOREC 0             IPL RECORD                          91042
         DOREC 1             WAIT TIME RECORD                    91042
         TM    OPTS,OFTYP26  USE SPOOL COUNTS FROM TYPE 26 ?     91042
         BNZ   SELTYP26      YES                                 91042
SELTYP6  DOREC 6             PRINT/PUNCH OUTPUT                  91042
SELTYP26 DS    0H                                                91042
         DOREC 7             LOST SMF DATA                       91042
         DOREC 12            HALT EOD (NEED NOT BE LAST !!!)     91042
         DOREC 22            CONFIGURATION STATUS (POSS. CHANGE) 91042
         DOREC 26            JES2 JOB RECORD                     91042
         AIF   (NOT &MVSXA).NOD90                               GP12064
         DOREC 90            SYSTEM STATUS RECORD                91042
.NOD90   AIF   ('&LOCAL' NE 'CCSI').NOTWYL                       91042
         DOREC 200           LOCAL WYLBUR RECORD                 91042
.NOTWYL  TM    OPTS,OFNOUSE    SKIP USER RECORDS ?               91042
         BZ    *+10          NO                                  91042
         XC    SELECTBL+128(128),SELECTBL+128  CLEAR USER OFFSETS
         TM    OPTS,OFTYP32  DO TYPE 32 ?                        91042
         BNZ   SELTYP32      YES                                 91042
         AIF   (NOT &MVSXA).NOZ90                               GP12064
         TM    OPTS,OFTYP30  USE TYPE 30 ?                       91042
         BNZ   SELTYP30      YES                                 91042
.NOZ90   DOREC 4             STEP RECORD                         91042
         DOREC 5             JOB RECORD                          91042
         DOREC 34            TSO SESSION                         91042
         DOREC 35            TSO "JOB"                           91042
         B     SELDONE       DONE                                91042
         AIF   (NOT &MVSXA).NON30                               GP12064
SELTYP30 DOREC 30            USE ONLY TYPE 30S                   91042
         B     SELDONE                                           91042
.NON30   ANOP  ,                                                GP12064
SELTYP32 DOREC 32            USE ONLY TYPE 32S                   91042
SELDONE  DS    0H                                                91042
         OPEN  (SMF,(INPUT,REREAD))   OPEN THE SMF INPUT         86040
         TM    SMF+DCBOFLGS-IHADCB,DCBOFOPN  OPEN ?              86040
         BNZ   GOSORT        YES
CRASH    ABEND 666           INVALID JCL SETUP
         SPACE 1
GOSORT   LA    R1,SORTLIST                                       79170
         LINK  EP=SORT       SORT
         LR    R2,R15        SAVE RETURN CODE
         TM    FLAG,LOSSY                                       GP12069
         BZ    ERRTEST                                          GP12069
         PRTF  '0*** A QUESTION MARK INDICATES DATA LOSS ***',CC=ASA,  *
               DEV=(1,2)                                        GP12069
ERRTEST  TM    FLAG,ERRIN                                       GP12069
         BZ    CLOSER                                           GP12069
         PRTF  '0*** ERROR ON SMF INPUT - DATA INCOMPLETE ***',CC=ASA, *
               DEV=(1,2)                                        GP12069
CLOSER   CLOSE (SMF)                                             86040
         SERVTERM ,          CLOSE AND FREE EVERYTHING           86040
         PGMEXIT RC=(R2)     SET RETURN CODE AND EXIT           GP12069
         SPACE 2
SORTLIST DC    0A(0),X'80',AL3(SORTPARM)
         DC    H'0'          ALIGN NEXT TO HALF-WORD
SORTPARM DC    AL2(SORTPARE-*-2)
         DC    A(SORTCD1,SORTCD2,SORTCD2,SORTCD3,E15,E35)
         DC    X'0',C'MAX',X'FF00',C'AP'
SORTPARE EQU   *
**** THIS OLD SORT DOESN'T RECOGNIZE E TYPE FILE SIZE           GP12064
SORTCD1  DC    C' SORT FIELDS=(1,'                              GP12069
         EBCDIG TOSLEN,3     FIELD LENGTH IN EBCDIC             GP12069
         DC    C',CH,A)'   FILSZ=E100000' EST. NOT IN MVS       GP12069
SORTCD2  DC    C' RECORD TYPE=F,LENGTH=('                       GP12069
         EBCDIG TOSLEN,3     DEFINE RECORD LENGTH               GP12069
         DC    C')'                                             GP12069
SORTCD3  DC    C' '
         EJECT ,
         DROP  ,
E15      STM   R14,R12,12(R13)
         LR    R12,R15
         LA    R11,2048(,R12)
         LA    R11,2048(,R11)          MAKE SECOND BASE
         USING E15,R12,R11       MAKE NEW BASE
         L     R9,@WORK      RELOAD MAIN WORK AREA               86040
         LA    R9,SUBSAVE-SAVE(,R9)  GET MAIN WORK AREA BACK     86040
         ST    R9,8(,R13)
         ST    R13,4(,R9)
         LR    R13,R9
         USING SUBSAVE,R13                                       86040
         TM    FLAG,EOFIN    END OF INPUT ?
         BZ    E15GO
         PRTSPACE 1,DEV==PRTDEV  SPACE SUMMARY LISTING           91042
         LA    R2,256                                            91042
         LA    R3,SMFCOUNT   GET COUNTERS                        91042
E15STATL ICM   R1,IIII,0(R3) LOAD COUNTER                        91042
         BZ    E15STATM      NONE                                91042
         LA    R0,256                                            91042
         SR    R0,R2         GET RECORD TYPE                     91042
         STM   R0,R1,DB                                          91042
         PRTLIST REC#MSG,DEV==PRTDEV  FORMAT                     91042
E15STATM LA    R3,4(,R3)     NEXT COUNTER                        91042
         BCT   R2,E15STATL   DO NEXT                             91042
         PRTSPACE 1,DEV==PRTDEV  SPACE SUMMARY LISTING           91042
         LA    R2,SMF30S#                                        91042
         LA    R3,SMF30S     GET COUNTERS                        91042
E15ST30L ICM   R1,IIII,0(R3) LOAD COUNTER                        91042
         BZ    E15ST30M      NONE                                91042
         LA    R0,SMF30S#                                        91042
         SR    R0,R2         GET RECORD TYPE                     91042
         STM   R0,R1,DB                                          91042
         PRTLIST R30#MSG,DEV==PRTDEV  FORMAT                     91042
E15ST30M LA    R3,4(,R3)     NEXT COUNTER                        91042
         BCT   R2,E15ST30L   DO NEXT                             91042
         LA    R15,8         SET NO RETURN RETURN
         B     SMFDONE
REC#MSG  FD    DB+4,4,ICM,RADJ,NL,LEN=13                         91042
         FDPRT ' TYPE',PAD                                       91042
         FDPRT DB,4,I,RADJ,LEN=3                                 91042
         FD    *END                                              91042
R30#MSG  FD    DB+4,4,ICM,RADJ,NL,LEN=13                         91042
         FDPRT ' TYPE 30, SUBTYPE',PAD                           91042
         FDPRT DB,4,I,RADJ,LEN=2                                 91042
         FD    *END                                              91042
         SPACE 1                                                 91042
SMFEOF   OI    FLAG,EOFIN    SET END FLAG
         MVI   TOSORT,X'FF'
         MVC   TOSORT+1(TOSLEN-1),TOSORT  MAKE LAST RECORD      GP12069
         MVC   TOSDATE,=X'2099365F'  MAKE VALID HIGH DATE
SMFPUT   LA    R15,12        SET INSERT CODE
SMFDONE  LA    R1,TOSORT     GET RECORD ADDRESS
         L     R13,4(,R13)
         LM    R2,R12,28(R13)  JUST IN CASE
         L     R14,12(R13)
         BR    R14           RETURN TO SORT
         SPACE 1
E15GO    GET   SMF           GET NEXT RECORD
         LR    R8,R1         PRESERVE RECORD ADDRESS             91042
         USING SMFMAPS,R8    DECLARE COMMON SMF MAPPING
         FIXDATE SMF4DTE                                        GP12069
         CP    SMF4DTE,LODATE  IN DATE RANGE ?                   91042
         BL    E15GO         NO; IGNORE IT                       91042
         CP    SMF4DTE,HIDATE  IN RANGE ?                        91042
         BH    E15GO         NO; IGNORE IT                       91042
         SLR   R3,R3                                             91042
         IC    R3,SMF4RTY    GET SMF TYPE                        91042
         SLL   R3,2          CONVERT TO OFFSET                   91042
         INC   SMFCOUNT(R3)  UP THE COUNT                        91042
         SLR   R2,R2         CLEAR FOR TRT INSERT                91042
E15TYPE  TRT   SMF4RTY,SELECTBL  DESIRED TYPE ?                  91042
         BZ    E15GO         SKIP THIS ONE                       91042
         TM    OPTS,OFBUG15  DEBUG MODE ?                       GP12069
         BZ    E15TYPED      NO                                  86040
         PRTSPACE 1          SPACE A LINE                        86040
         LH    R4,0(,R8)     RDW LENGTH                         GP12069
         PRTLIST RECDUMP     MAKE HEX/EBCDIC DUMP                86040
E15TYPED XC    TOSORT(TOSLEN-4),TOSORT  CLEAR RECORD PASSED TO SORT
         MVC   TOSTIME,SMF4TME  COPY HEADER TIME/DATE
         MVC   TOSDATE,SMF4DTE                                   91042
         MVC   TOSCPID,SMF4SID         COPY SYSTEM ID/MODEL      79170
         MVC   TOSRLEN,SMF4LEN         SAVE ORIG LENGTH          79170
*
         INC   TOSUNIQ       PREVENT FALSE DUPLICATES           GP12069
         STC   R2,TOSTYPE    SAVE BRANCH OFFSET                  91042
OFFBASE  B     *(R2)         BRANCH                              91042
         BOFF  22            CPU/CHAN/STOR STATUS                89248
         BOFF  7             DATA LOST
         BOFF  0             IPL RECORD
         BOFF  12            HALT EOD RECORD
         BOFF  1             WAIT TIME RECORD
         AIF   (NOT &MVSXA).NOB90                               GP12064
         BOFF  90            SYSTEM STATUS RECORD                87254
.NOB90   ANOP  ,                                                GP12064
OFFWYL   BOFF  200           WYLBUR SESSION (LOCAL)
         BOFF  35            TSO SESSION(JOB)                    86040
         BOFF  34            TSO STEP
         BOFF  5             JOB RECORD
         BOFF  26            JES JOB RECORD                      87254
         BOFF  6             OS/HASP SYSOUT
         BOFF  4             STEP RECORD
         AIF   (NOT &MVSXA).NOB30                               GP12064
         BOFF  30            USING 30 INSTEAD OF 4,5,34,35       91042
.NOB30   BOFF  32            USING 32 INSTEAD OF 30, 4,5,34,35   91042
         SPACE 1
TYPE4    DS    0H            BATCH STEP                          91042
TYPE34   SLR   R5,R5         CLEAR DISK I/O
         SLR   R6,R6         CLEAR TAPE I/O
         ICM   R7,IIII,SMF4NCI   CARDS INPUT
         LA    R3,SMF4DEVC   POINT TO FIRST I/O ENTRY           GP12069
         SLR   R2,R2
         ICM   R2,OOII,SMF4LENN  GET LENGTH OF I/O PORTION
         SRA   R2,3          CONVERT TO # OF ENTRIES            GP12069
         BNP   E15SCPU         NONE; SKIP                       GP12069
E15EXLP  ICM   R0,IIII,SMF4EXCP-SMF4DEVC(R3)  GET I/O           GP12069
         CLI   SMF4DEVC-SMF4DEVC(R3),X'80'   TAPE ?
         BE    E15STAPE
         CLI   SMF4DEVC-SMF4DEVC(R3),X'20'   DISK ?
         BE    E15SDISK
         ALR   R7,R0         OTHER
         B     E15IOLP
E15STAPE ALR   R6,R0         TAPE
         B     E15IOLP
E15SDISK ALR   R5,R0         DASD
E15IOLP  LA    R3,8(,R3)     BUMP TO NEXT ENTRY                 GP12069
         BCT   R2,E15EXLP                                       GP12069
         SPACE 1
E15SCPU  SLR   R4,R4
         ICM   R4,OIII,1(R3) GET STEP CPU TIME
         STM   R4,R7,TOSSTEP   WRITE STEP DATA
         CLC   =C'*WYL',SMF4PGMN  FAKE WYLBUR ?                  86040
         BE    FAKEWYL       YES; NO PAGING OR OTHER STATS       86040
         MVC   TOSSYSWT+1(3),SMF4SRBT  OVERHEAD CPU TIME         86040
         LA    R3,SMFRCD4    GET RECORD BASE                     86040
         AH    R3,SMF4RLCT   GET OFFSET TO RELOCATE              86040
         USING SMF4PGIN,R3   DECLARE RELOCATE SECTION            86040
         MVC   WORK1(4*4),SMF4PSI  PAG IN/PAG OUT/VAM IN/VAM OUT 86040
         LM    R14,R1,WORK1  LOAD                                86040
         AR    R14,R15       PLUS PAGES OUT                      86040
         AR    R14,R0        PLUS VAM IN                         86040
         AR    R14,R1        PLUS VAM OUT                        86040
         DROP  R3                                                86040
         ST    R14,TOSPAGE                                       86040
         B     SMFPUT        PUT IT OUT
FAKEWYL  MVI   TOSTYPE,OFFWYL-OFFBASE  SET FOR WYLBUR            87254
         B     SMFPUT        PROCESS                             86040
         SPACE 1
TYPE32   EQU   E15GO         NOT WRITTEN - NOT WANTED ?          91042
TYPE7    EQU   SMFPUT        PASS TO SORT AS IS
TYPE0    EQU   SMFPUT        NO OTHER INFO
TYPE12   MVI   TOSFLAG,1   SHOW SYTEM DOWN                       79170
TYPE1    DS    0H                                                86040
         AIF   (&MVS).NOTYP1                                     86040
         MVC   TOSSYSWT,SMF1SWT  SYSTEM WAIT TIME
         CLI   SMF1FLG,0     MFT/MVT SYSTEM ?                    79170
         BE    SMFPUT        YES - NO PAGING                     79170
         ICM   R2,IIII,SMF1PGIN
         ICM   R3,IIII,SMF1PGOT  GET PAGING TOTAL
         AR    R2,R3
         STCM  R2,IIII,TOSPAGE   SAVE TOTAL PAGING
.NOTYP1  B     SMFPUT        WRITE THE RECORD
         SPACE 1
TYPE35   CLI   TIVRFLG,2     VS SYSTEM ?                         86040
         BNE   TYPE5         NO; DO AS NORMAL JOB                86040
         CLI   TIVRLEN+1,X'83'  OLD MILTEN RECORD ?              86040
         BE    E15GO         YES; SKIP THIS ONE                  86040
TYPE5    MVC   TOSFLAG(1),SMF5JBTI  JOB ENDING STATUS            79170
         CLI   SMF5NST,0     ANY STEPS RUN ?
         BNE   *+8           YES
         MVI   TOSFLAG,X'40'  SET 'IEFUJV' FAILURE               79170
         NI    TOSFLAG,X'7F'         RESERVE HIGH BIT            79170
         TM    TOSFLAG,2   ABEND ?                               79170
         BZ    SMFPUT
         CLC   =X'0122',SMF5JCC
         BE    E15JOBA
         CLC   =X'0222',SMF5JCC
         BNE   E15JOBB
E15JOBA  OI    TOSFLAG,X'80'   SIGNAL CANCEL                     79170
E15JOBB  CLC   =X'8000',SMF5JCC   USER 0 (NORMAL BLOCKPRT COMP)
         BNE   SMFPUT        NON-ZERO; LEAVE
         NI    TOSFLAG,255-2  ABEND 0 IS NORMAL (BLOCKPRT)       79170
         B     SMFPUT        WRITE IT
         SPACE 1
TYPE6    MVC   TOSPRINT,SMF6NLR   GET NUMBER OF LINES
         B     SMFPUT
         SPACE 1
TYPE200  L     R3,ARWYLTIM   GET CPU TIME
         L     R5,ARTPIO     TERMINAL I/O
         LH    R4,ARWYLOPN   GET OPEN COUNT
         MH    R4,=H'5'      ASSUME 5 DISK I/OS FOR OPEN/CLOSE
         AH    R4,ARWYLPGW   ADD PAGE WRITES
         AH    R4,ARWYLPGR   ADDREADS
         AH    R4,ARWYLCAT   AT CATALOG READS
         AH    R4,ARWYLCAT     TWICE
         AH    R4,ARWYLDAW   ADD OTHER WRITES
         AH    R4,ARWYLDAR     AND READS
         STM   R3,R5,TOSSTEP STASH
         B     SMFPUT        WRITE IT
         SPACE 1                                                 87254
         AIF   (NOT &MVSXA).NOP90                               GP12064
         USING SMFRCD90,R8   FUNNY HA-HA ?                       87254
TYPE90   ICM   R7,IIII,SMF90POF  OFFSET TO PRODUCT SECTION       87254
         BZ    E15GO         HUH ?                               87254
         AR    R7,R8         GET PRODUCT SECTION                 87254
         SLR   R5,R5                                             87254
         SLR   R6,R6                                             87254
         ICM   R5,OOII,SMF90PON  GET NUMBER OF PRODUCT SECTIONS  87254
         BZ    E15GO         HUH ?                               87254
         ICM   R6,OOII,SMF90PLN  GET LENGTH OF PRODUCT SECTION   87254
         BZ    E15GO         HUH ?                               87254
         B     E15SYSL+2     SKIP FIRST INCREMENTATION           87254
E15SYSL  AR    R7,R6         NEXT PRODUCT SECTION                87254
         USING SMF90TID,R7                                       87254
         CLI   SMF90TID+1,7  HALT EOD RECORD ?                   87254
         BE    TYPE12        YES; SHOW SYSTEM DOWN               87254
         BCT   R5,E15SYSL    TRY NEXT PRODUCT SECTION            87254
         B     E15GO         ELSE SKIP THE RECORD                87254
         DROP  R7                                                87254
         USING SMFMAPS,R8    RESTORE                             87254
.NOP90   SPACE 1                                                 87254
*        TYPE 26 - JES JOB RECORD                                87254
*              IGNORE JES3 (FLAGS DIFFER)                        87254
*        FOR JES2, IF TYPRUN=SCAN OR =COPY, WRITE ONE            87254
*        IF NEITHER, AND CONVERSION BUT NO EXEC (AND NO CANCEL)  87254
*              REPORT AS JCL ERROR                               87254
TYPE26   CLI   SMF26SBS+1,2  JES2 RECORD ?                       87254
         BNE   E15GO         NO; IGNORE THE RECORD               87254
         MVI   TOSTYPE,0     SET FOR NO RECORD                   91042
         SLR   R6,R6                                             87254
         LA    R7,SMF26LN1   POINT TO OPTIONAL SECTION           87254
         USING SMF26LN1,R7                                       87254
         TM    SMF26IND,SMF26DES/256  DESCRIPTIVE SECTION PRESENT ?
         BZ    E15GO         NO; IGNORE                          87254
         ICM   R6,OOII,SMF26LN1  OFFSET TO NEXT SECTION          87254
         TM    SMF26IN2,4+2  TYPRUN=SCAN OR TYPRUN=COPY ?        87254
         BNZ   TYPE26JB      YES; COUNT AS ONE JOB               87254
         AR    R7,R6                                             87254
         USING SMF26LN2,R7                                       87254
         TM    SMF26IND,SMF26EVT/256  EVENTS PRESENT ?           87254
         BZ    TYPE26SP      NO; GET SPOOL COUNTS                91042
         ICM   R0,IIII,SMF26CPD  WAS THIS RUN THROUGH THE CONVERTER ?
         BZ    TYPE26SP      NO; SKIP IT                         91042
         ICM   R0,IIII,SMF26XSD  DID IT START TO EXECUTE ?       87254
         BNZ   TYPE26SP      YES; USE DATA FROM TYPE 5 RECORD    91042
         MVI   TOSFLAG,X'40' SIGNAL JCL ERROR                    87254
TYPE26JB MVI   TOSTYPE,OFF26 IDENTIFY AS TYPE 26 (JOB)           91042
         DROP  R7                                                91042
TYPE26SP LA    R7,SMF26LN1   POINT TO FIRST SEGMENT              91042
         USING SMF26LN1,R7   DECLARE IT                          91042
         TM    SMF26IND,SMF26DES/256  DESCRIPTIVE PRESENT ?      91042
         BZ    *+8           NO                                  91042
         AH    R7,SMF26LN1   SKIP TO NEXT SECTION                91042
         TM    SMF26IND,SMF26EVT/256  EVENTS ?                   91042
         BZ    *+8           NO                                  91042
         AH    R7,SMF26LN1   SKIP IT, TOO                        91042
         TM    SMF26IND,SMF26ATU/256  ACTUALS PRESENT ?          91042
         BZ    TYPE26XT      NO; SKIP                            91042
         TM    OPTS,OFTYP26  USE TYPE 6 FOR SPOOL DATA ?         91042
         BZ    TYPE26XT      YES; OMIT THIS                      91042
         USING SMF26LN3,R7   DECLARE ACTUALS                     91042
         ICM   R0,IIII,SMF26XLN  GET LINES SPOOLED               91042
         ICM   R1,IIII,SMF26XPU  GET CARDS SPOOLED               91042
         AR    R0,R1         GET TOTAL RECORDS                   91042
         BNP   TYPE26XT      SKIP                                91042
         ST    R0,TOSPRINT   SET SPOOL COUNTS                    91042
         MVI   TOSTYPE,OFF6  SET FOR TYPE 6 OFFSET               91042
TYPE26XT CLI   TOSTYPE,0     NO RECORD ?                         91042
         BNE   SMFPUT        WRONG; WRITE IT                     91042
         B     E15GO         ELSE GET ANOTHER                    91042
         DROP  R7                                                87254
         SPACE 1                                                 89248
*        TYPE 22 - PROPAGATE NUMBER OF ACTIVE CPUS               89248
*                                                                89248
TYPE22   LH    R0,SMF22ECT   NUMBER OF RESOURCE ELEMENTS         89248
         LTR   R0,R0         ANY AT ALL ?                        89248
         BNP   E15GO         NO; IGNORE                          89248
         SLR   R15,R15                                           89248
         LA    R2,SMF22RSC   POINT TO FIRST ENTRY                89248
         USING SMF22RSC,R2   DECLARE IT                          89248
E15T22LP CLI   SMF22PID,1    PROCESSOR ID ENTRY ?                89248
         BNE   E15T22UP      NO; SKIP                            89248
         LA    R15,1(,R15)   INCREASE ACTIVE CPU COUNT           89248
E15T22UP TR    SMF22PID,=AL1(6,6,6,6,42,6,6,6)  L' OF T0-7 ?     89248
         SLR   R14,R14                                           89248
         IC    R14,SMF22PID                                      89248
         AR    R2,R14        BUMP RESOURCE POINTER               89248
         BCT   R0,E15T22LP   GO AGAIN                            89248
         STC   R15,TOSFLAG   UPDATE CPU COUNT                    89248
         BXH   R15,R15,SMFPUT  WRITE RECORD IF CPUS PRESENT      89248
         B     E15GO         HUH ?  IGNORE IF ZERO               89248
         DROP  R2                                                89248
         SPACE 2                                                 91042
         AIF   (NOT &MVSXA).NOP30                               GP12064
*   PROCESS TYPE 30 RECORDS HERE - THIS IS MUTUALLY EXCLUSIVE OF 91042
*     PROCESSING TYPES 4,5,34,35 AND 32                          91042
*        INITIALLY, ONLY THE STEP END AND JOB END RECORDS WILL BE
*        PROCESSED (INPUT NEED NOT BE SORTED).                   91042
*                                                                91042
TYPE30   SLR   R2,R2                                             91042
         USING SMFRCD30,R8   DECLAR ETHIS TURKEY                 91042
         ICM   R2,IIII,SMF30SOF  OFFSET TO SUBSYSTEM SEGMENT     91042
         BZ    E15GO         IGNORE IF SUBTYPE UNAVAILABLE       91042
         ICM   R14,OOII,SMF30SLN  ANY SEGMENT LENGTH ?           91042
         BZ    E15GO         NO; SKIP THIS                       91042
         AR    R2,R8                                             91042
         USING SMF30PSS,R2   SUBSYSTEM SECTION                   91042
         SLR   R3,R3                                             91042
         ICM   R3,OOII,SMF30TYP  GET SUBTYPE                     91042
         LR    R4,R3         COPY SUBTYPE                        91042
         SLL   R4,2          CONVERT TO INDEX                    91042
         INC   SMF30S(R4)    COUNT                               91042
         CH    R3,=H'4'      STEP END ?                          91042
         BL    E15GO         NO; IGNORE                          91042
         BE    STEP30        DO STEP END                         91042
         CH    R3,=H'5'      JOB END ?                           91042
         BNE   E15GO         NO; IGNORE                          91042
         MVI   TOSTYPE,OFF5  FAKE AS JOB                         91042
         ICM   R3,IIII,SMF30IOF  OFFSET TO ID SEGMENT            91042
         BZ    JOB30NID      NO ID                               91042
         ICM   R14,OOII,SMF30ILN  ANY SEGMENT LENGTH ?           91042
         BZ    JOB30NID      NO; SKIP THIS                       91042
         AR    R3,R8         BASE IT                             91042
         USING SMF30ID,R3    DECLARE IT                          91042
         CLI   SMF30JNM,C'T'   TIME SHARING SESSION ?            91042
         BNE   JOB30NUS      NO                                  91042
         MVI   TOSTYPE,OFF35  FKE AS TSO SESSION                 91042
JOB30NUS ICM   R0,OOII,SMF30STN  NUMBER OF STEPS                 91042
         BNZ   JOB30NID      OK                                  91042
         MVI   TOSFLAG,X'40'  SET JCL ERROR                      91042
         B     SMFPUT                                            91042
         DROP  R3                                                91042
JOB30NID ICM   R3,IIII,SMF30TOF  OFFSET TO COMPLETION SEGMENT    91042
         BZ    SMFPUT        COUNT AS JOB/SESSION                91042
         ICM   R14,OOII,SMF30TLN  ANY SEGMENT LENGTH ?           91042
         BZ    SMFPUT        NO; SKIP THIS                       91042
         AR    R3,R8                                             91042
         USING SMF30CMP,R3                                       91042
         MVC   TOSFLAG,SMF30STI  PROPAGATE COMPLETION            91042
         NI    TOSFLAG,X'7F'  KILL CANCEL BIT                    91042
         TM    TOSFLAG,2   ABEND ?                               91042
         BZ    SMFPUT        NO                                  91042
         CLC   =X'0122',SMF30SCC                                 91042
         BE    JOB30CAN                                          91042
         CLC   =X'0222',SMF30SCC                                 91042
         BNE   SMFPUT                                            91042
JOB30CAN OI    TOSFLAG,X'80'   SIGNAL CANCEL                     91042
         B     SMFPUT        WRITE IT                            91042
         DROP  R3                                                91042
         SPACE 1                                                 91042
STEP30   MVI   TOSTYPE,OFF4  SET STEP RECORD                     91042
         MVI   TOSFLAG,X'80'  SET FOR CONTINUATION RECORD        91042
         CLC   SMF30COF,SMF30EOF  IF CPU TIME .LE. EXCP,         91042
         BNL   STEP30IO      THEN THIS IS AN EXCP CONTINUATION   91042
         MVI   TOSFLAG,0     SET FOR NORMAL STEP RECORD          91042
         ICM   R3,IIII,SMF30COF  CPU TIME SECTION                91042
         BZ    STEP30XC      NONE ?                              91042
         ICM   R14,OOII,SMF30CLN  ANY SEGMENT LENGTH ?           91042
         BZ    STEP30XC      NO; SKIP THIS                       91042
         AR    R3,R8                                             91042
         USING SMF30CAS,R3                                       91042
         LM    R4,R7,SMF30CPT  GET CPU TCB/SRB; INIT TCB/SRB     91042
         ST    R4,TOSSTEP    SET STEP CPU TIME                   91042
         AR    R7,R6                                             91042
         AR    R7,R5         ADD OVERHEAD                        91042
         ST    R7,TOSSYSWT   SET OVERHEAD                        91042
STEP30XC ICM   R3,IIII,SMF30UOF  I/O ACTIVITY ?                  91042
         BZ    STEP30NA      NO ?                                91042
         ICM   R14,OOII,SMF30ULN  ANY SEGMENT LENGTH ?           91042
         BZ    STEP30NA      NO; SKIP THIS                       91042
         USING SMF30URA,R3   DECLARE IT                          91042
         AR    R3,R8         RELOCATE                            91042
         L     R0,SMF30INP   GET CARD INPUT                      91042
         A     R0,SMF30TPT   TERM IN                             91042
         A     R0,SMF30TGT   TERM OUT                            91042
         A     R0,TOSPRINT   OTHER I/O                           91042
         ST    R0,TOSPRINT                                       91042
STEP30NA ICM   R3,IIII,SMF30ROF  STORAGE AND PAGING              91042
         BZ    STEP30IO      DONE ?                              91042
         ICM   R14,OOII,SMF30RLN  ANY SEGMENT LENGTH ?           91042
         BZ    STEP30IO      NO; SKIP THIS                       91042
         AR    R3,R8         RELOCATE                            91042
         USING SMF30SAP,R3                                       91042
         L     R0,SMF30PSI                                       91042
         A     R0,SMF30PSO                                       91042
         A     R0,SMF30VPI                                       91042
         A     R0,SMF30VPO                                       91042
         A     R0,SMF30CPI                                       91042
         A     R0,SMF30LPI                                       91042
         ST    R0,TOSPAGE                                        91042
STEP30IO ICM   R3,IIII,SMF30EOF  OFFSET TO EXCP                  91042
         BZ    STEP30ND      NO EXCP ?                           91042
         AR    R3,R8                                             91042
         USING SMF30EXP,R3   DECLARE IT                          91042
         LH    R14,SMF30ELN  ENTRY LENGTH IN EXCP SECTION        91042
         LH    R15,SMF30EON  NUMBER OF SEGMENTS IN THIS RECORD   91042
         SH    R15,=H'1'       LESS ONE FOR BXLE LOGIC           91042
         BM    STEP30ND      NONE                                91042
         MH    R15,SMF30ELN  TOTAL LENGTH                        91042
         AR    R15,R3        END ADDRESS                         91042
         SLR   R4,R4         CLEAR DASD COUNTER                  91042
         SLR   R5,R5         CLEAR TAPE COUNTER                  91042
         SLR   R6,R6         CLEAR MISCELLANEOUS                 91042
         SLR   R7,R7         CLEAR PRINT/PUNCH SYSOUT            91042
STEP30LP TM    SMF30CUA,X'80'  VIRTUAL DEVICE ?                  91042
         BNZ   STEP30BP      YES; DON'T COUNT (USE PAGE CTRS)    91042
         ICM   R0,IIII,SMF30BLK  GET BLOCK COUNT                 91042
         BZ    STEP30BP      DON'T BOTHER                        91042
         BM    SOCK3         ERROR                               91042
         CLI   SMF30DEV,X'80'   TAPE ?                           91042
         BE    STEP30XT                                          91042
         CLI   SMF30DEV,X'20'   DASD ?                           91042
         BE    STEP30XD                                          91042
*WRONG*  ICM   R1,OOII,SMF30DEV  PRESUMED SYSOUT ?               91042
*WRONG*  BZ    STEP30XS      YES                                 91042
         AR    R6,R0                                             91042
         B     STEP30BP                                          91042
STEP30XS AR    R7,R0         TOTAL SYSOUT                        91042
         B     STEP30BP                                          91042
STEP30XT AR    R5,R0                                             91042
         B     STEP30BP                                          91042
STEP30XD AR    R4,R0                                             91042
STEP30BP BXLE  R3,R14,STEP30LP                                   91042
         STM   R4,R7,TOSSTEP+4  SAVE COUNTS                      91042
         DROP  R3                                                91042
STEP30ND B     SMFPUT                                            91042
         DROP  R2                                                91042
.NOP30   SPACE 1                                                 91042
*   FIX UP DATE FIELDS PAST Y2K                                 GP12069
*   FIRST BYTE > 19 - LEAVE AS IS                               GP12069
*   FIRST BYTE >0   - ADD 19                                    GP12069
*   SECOND BYTE <64 - ADD 20                                    GP12069
*                                                               GP12069
FIXDATE  OI    3(R15),X'0F'   MAKE PREFERRED PLUS FOR PRINTING  GP12069
         CLI   0(R15),X'19'   ALREADY SET ?                     GP12069
         BNLR  R14             YES; RETURN                      GP12069
         CLI   0(R15),0       OLD OR NOT SET ?                  GP12069
         BE    FIXWIND         YES; USE WINDOWING               GP12069
FIXD19   AP    0(4,R15),P1900000     SCALE TO PROPER CENTURY    GP12069
         BR    R14                                              GP12069
FIXWIND  CLI   1(R15),X'64'   YOUNGER THAN OS/360 ?             GP12069
         BNL   FIXD19          NO; ASSUME 1964-1999             GP12069
         MVI   0(R15),X'20'   ASSUME 2000-2063                  GP12069
         BR    R14                                              GP12069
P1900000 DC    P'1900000'                                       GP12069
SOCK3    EX    0,*           TAKE A DUMP                         91042
         EJECT ,
         DROP  ,
E35      STM   R14,R12,12(R13)
         LR    R12,R15
         LA    R11,2048(,R12)
         LA    R11,2048(,R11)          MAKE SECOND BASE
         USING E35,R12,R11
         L     R9,@WORK      GET WORK AREA ADDRESS               86040
         LA    R9,SUBSAVE-SAVE(,R9)                              86040
         ST    R9,8(,R13)
         ST    R13,4(,R9)
         LR    R13,R9
         USING SUBSAVE,R13                                       86040
         ICM   R10,OIII,1(R1)  GET THE RECORD ADDRESS
         BNZ   E35BREAK      CONTINUE
         L     R13,4(,R13)
         LM    R14,R12,12(R13)
         LA    R15,8         DON'T COME BACK HERE
         BR    R14           RETURN TO SORT
         SPACE 1
         USING SOTORT,R10    DECLARE SORT RECORD
E35BREAK TM    OPTS,OFBUG35  DEBUG MODE ?                       GP12069
         BZ    E35BREAD      NO                                  86040
         PRTSPACE 1          SPACE A LINE                        86040
         PRTLIST SOTDUMP     MAKE HEX/EBCDIC DUMP                86040
E35BREAD CLC   TOSORT(TOSLEN),SOTORT  DUPLICATE RECORD ?        GP12069
         BE    E35NEXTR      YES; SKIP IT                        79170
E35DAYCH CLC   CURRDATE,SOTDATE  SAME DAY ?                      79170
         BE    SAMEDAY       YES; ADD IN RECORD
         OC    CURRDATE,CURRDATE       FIRST TIME ?
         BNZ   E35DAYCN      NO                                  79170
         MVC   CURRDATE,SOTDATE        FIX UP
         MVC   TOSORT(TOSLEN),SOTORT  INITIALIZE FIRST RECORD   GP12069
         MVC   SYSCPID,TOSCPID  IDENTIFY SYSTEM                  79170
         MVC   SUMCPID,SYSCPID  DITTO                            79170
         PRTF  SUMHEAD,SUMHEADL,DEV=2,TITLE=1                    86040
         MVC   SUBHEAD1+1(5),=C'MONTH'                           79170
         PRTF  SUBHEAD1,L'SUBHEAD1,DEV=2,TITLE=2                 86040
         MVC   SUBHEAD1+1(5),=C' DAY '                           79170
         PRTF  SUBHEAD1,L'SUBHEAD1,DEV=1,TITLE=2                 86040
         PRTLIST SUBHEAD3,TITLE=3,DEV=(1,2)                     GP12069
E35DAYCN ICM   R1,15,SOTDATE  LOAD DATE FIELD                   GP12069
         SUBCALL SUBP2M      GET DATE IN EXTERNAL FORMAT        GP12069
         STCM  R15,2,BINMON+L'BINMON-1                          GP12069
         MVC   NEWDAY(2),4(R1)    MONTH                         GP12069
         MVI   NEWDAY+2,C'/'                                    GP12069
         MVC   NEWDAY+3(2),6(R1)  DAY OF MONTH                  GP12069
         ICM   R1,15,SOTDATE  LOAD DATE FIELD                   GP12069
         SUBCALL SUBP2D      GET DAY OF WEEK                    GP12069
         CLI   0(R1),C'S'    WEEKEND ?                          GP12069
         BNE   *+8             NO
         MVI   NEWDAY+2,C'.'      INDICATE WEEKEND              GP12069
         SRDL  R0,32         SWITCH DAY TO R1                   GP12069
         AL    R1,=F'1'      SUN=1, SAT=7                       GP12069
         D     R0,=F'7'      INEFFICIENT CHEAT                  GP12069
         STC   R0,DAYWEEK    SAVE SAT=0, SUN=1                  GP12069
         STC   R0,NEWWEEK    SAVE SAT=0, SUN=1                  GP12069
         L     R14,BINMON                                       GP12069
         MH    R14,=AL2(FEB-JAN)                                GP12069
         LA    R14,JAN-(FEB-JAN)(R14)                           GP12069
         CLI   SOTTYPE,X'FF'  END-FILE ?
         BE    E35DAYNM      YES                                 79170
         CLC   TOSCPID,SOTCPID  MACHINE BREAK ?                  79170
         BE    E35DAYMN      NO                                  79170
         MVC   CPUDAYS,DEFCPUD  RESET TO ONE CPU                 91042
E35DAYNM MVC   NEWDAY,=C'*EOF*'   FORCE MONTHLY BREAK            79170
E35DAYMN MVC   NEWMON,10(R14)    COPY SHORT NAME                 79170
         TM    FLAG,FGDAY    ANY DATA IN DAY'S RECORD ?
         BZ    E35MONTH      NO; JUST CLEAR AGAIN
         MVC   PRTDAY,DAYMMDD                                   GP12069
         MVC   SYSMONTH,1(R14)   COPY EBCDIC MONTH ALSO
         UNPK  SYSYEAR(5),TOSDATE(3)   UNPACK YEAR              GP12069
         MVI   SYSYEAR+L'SYSYEAR,C' '                           GP12069
         OI    SYSYEAR+1,C'0'  PUT YEAR ON LISTING               79170
         L     R2,DAYCPU
         A     R2,DAYWAIT    CPU+WAIT TIME
         L     R3,CPUDAYS    ONE DAY IN 1/100THS SECONDS         89248
         CR    R2,R3         LARGER THAN ONE DAY ?
         BNH   *+6           NO; OK
         LR    R3,R2         ELSE USE (INVALID) LARGER VALUE
         S     R3,DAYCPU
         S     R3,DAYWAIT
         BNM   *+6
         SLR   R3,R3         SYSTEM OVERHEAD TIME
         ST    R3,DAYOHED    PASS FOR MONTHLY
         LA    R8,DAILY      POINT TO DAILY RECORD
         USING MAPVECT,R8    DECLARE IT                         GP12069
         BAL   R9,FORMLINE   FORMAT THE LINE
         MVI   PRTDAY+L'PRTDAY,C' '
         TM    DAYFLAG,1     SMF LOST ON ?
         BZ    *+8
         MVI   PRTDAY+L'PRTDAY,C'?'  SHOW LOSS
         OI    FLAG,LOSSY    REMEMBER                           GP12069
*   OPTIONAL CODE TO DOUBLE-SPACE SATURDAY AND MONDAY ENTRIES
*OLD*    CLI   DAYWEEK,0     SATURDAY ?
*OLD*    BE    SEPWEND       YES
*OLD*    CLI   DAYWEEK,2     MONDAY ?
*OLD*    BNE   *+8           NO
*EPWEND  MVI   PRINT,C'0'    DOUBLE SPACE THE LINE
         TM    FLAG,FGHEAD   WAS MONTHLY HEADER WRITTEN ?
         BNZ   NOTHEAD       YES
         PRTF  SYSHEAD,SYSHEADL,TITLE=1   WRITE MONTHLY HEADER   86040
         MVI   PRINT,C'-'    TRIPLE-SPACE DATA LINE
         OI    FLAG,FGHEAD   INDICATE WRITTEN
NOTHEAD  PRTF  PRINT,L'PRINT   PRINT THE DATA                    86040
         MVI   PRINT,C' '    RESTORE SINGLE SPACE
         SPACE 1
         LA    R8,MONTHLY    MONTHLY RECORD ACCUMULATION
         CLI   SOTTYPE,X'FF'   END OF SORT ?                     79170
         BE    MERGLAST       YES; CHECK LAST DAY                79170
         CLC   SOTCPID,TOSCPID  MACHINE BREAK ?                  79170
         BE    MERGLESS       NO                                 79170
MERGLAST CLC   TOSTIME,=AL4(20*60*60*100)  ANY RECORD ON LAST SHIFT ?
*OLD*    BNL   MERGLOOP      YES; TREAT AS FULL DAY              79170
MERGLESS LA    R2,DAYDISK
         LA    R3,MAPDISK
         LA    R4,DAYLEND/8  NUMBER OF DOUBLE WORDS
MERGIO   A64D  0(R3),0(R2)   GET PREVIOUS MONTHLY DATA          GP12069
         LA    R2,8(,R2)
         LA    R3,8(,R3)
         BCT   R4,MERGIO     I/O MERGED
         LA    R2,DAYWAIT
         LA    R3,MAPWAIT
         LA    R4,DAYLENF/4  DO FULL-WORDS
MERGF    L     R0,0(,R3)
         A     R0,0(,R2)
         ST    R0,0(,R3)
         LA    R2,4(,R2)
         LA    R3,4(,R3)
         BCT   R4,MERGF
         INCH  MAPDAY                                           GP12069
         OI    FLAG,FGMON    SHOW MONTHLY DATA AVAILABLE
         SPACE 1
E35MONTH CLC   DAYMMDD(2),NEWDAY  SAME MONTH AS BEFORE ?        GP12069
         BE    NEXTDAY       YES; NO MONTHLY BREAK
         TM    FLAG,FGMON    ANY MONTHLY DATA ?
         BZ    NEXTMONT      NO; JUST SKIP
         MVI   PRINT,C'-'    TRIPLE-SPACE
         LA    R8,MONTHLY    POINT TO MONTHLY RECORD
         BAL   R9,FORMLINE   FORMAT COMMON PRINT LINE
         MVC   PRTDAY,MONMONA                                   GP12069
         MVI   PRTDAY+L'PRTDAY,C' '
         NI    FLAG,255-FGHEAD   INDICATE WRITTEN
         PRTF  PRINT,L'PRINT   PRINT THE DATA                    86040
         MVI   PRINT,C'0'   DOUBLE SPACE SUMMARY                 79170
         PRTF  PRINT,L'PRINT,DEV=2    ON MONTHLY SUMMARY         86040
         MVI   PRINT,C' '    RESTORE SINGLE SPACE                79170
         LA    R8,MONTHLY    REUSE FOR AVERAGE DATA             GP12069
         LH    R7,MAPDAY     GET DAYS COLLECTED                 GP12069
         CH    R7,=H'1'      ANY ?
         BNH   NEXTMONT      NO; SKIP                           GP12069
         MVI   PRINT,C'0'    DOUBLE-SPACE
         CNVD  (R7),PRTDAY   SHOW NUMBER OF DAYS
         MVC   PRTDAY(3),=C'AV:'  IDENTIFY
         BAL   R9,FORMAVGE   GO TO OTHER ENTRY
         PRTF  PRINT,L'PRINT           PRINT THE DATA            86040
         MVI   PRINT,C' '    RESTORE SINGLE SPACE
         PRTF  PRINT,L'PRINT,DEV=2    ON MONTHLY SUMMARY         86040
NEXTMONT XC    MONTHLY(MONLEN),MONTHLY  CLEAR
         MVC   MONMONA,NEWMON                                   GP12069
         SPACE 2
NEXTDAY  NI    FLAG,255-FGDAY  RESET DAILY DATA
         XC    DAILY(DAYLEN),DAILY   CLEAR DAILY RECORD
         MVC   CURRDATE,SOTDATE  SAVE NEW DATE
         MVC   DAYMMDD,NEWDAY                                   GP12069
         MVC   DAYWEEK,NEWWEEK
         SPACE 1
SAMEDAY  CLI   SOTTYPE,X'FF'  END OF SORT ?
         BE    E35NEXT       YES; GET NEXT RECORD
         CLC   TOSCPID,SOTCPID  MACHINE BREAK ?                  79170
         BE    SAMEDAYM      NO                                  79170
         XC    CURRDATE,CURRDATE  RESET DATE                     79170
         NI    FLAG,255-FGDAY-FGMON-FGHEAD  RESET                79170
         XC    DAILY,DAILY                                       79170
         MVC   CPUDAYS,DEFCPUD  RESET TO ONE CPU                 91042
         MVC   TOSORT(TOSLEN),SOTORT  MAKE NEW RECORD           GP12069
         B     E35DAYCH      PROCESS AGAIN                       79170
SAMEDAYM OI    FLAG,FGDAY    INDICATE DAILY DATA AVAILABLE       79170
         A64F  DAYPAGE,SOTPAGE    PAGING COUNT                  GP12069
         L     R0,DAYWAIT    GET WAIT TIME                       86040
         A     R0,SOTSYSWT                                       86040
         ST    R0,DAYWAIT    STASH BACK                          86040
         SLR   R3,R3
         ST    R3,SOTPAGE    CLEAR PAGING                        86040
         ST    R3,SOTSYSWT                                       86040
         IC    R3,SOTTYPE    GET BRANCH OFFSET
         B     *(R3)         BRANCH TO PROCESSING ROUTINE
         B     E35TYP22      CPU/CHAN/STOR STATUS                89248
         B     E35LOST       DATA LOST
         B     E35IPL        IPL
         B     E35WAIT       HALT EOD
         B     E35WAIT       WAIT TIME
         AIF   (NOT &MVSXA).NOST90                              GP12069
         B     E35WAIT       SYSSTAT                             87254
.NOST90  AIF   ('&LOCAL' NE 'CCSI').NOSTWY                      GP12069
         B     E35WYLB       WYLBUR SESSION (LOCAL)
.NOSTWY  B     E35JOB        TS SESSION (JOB)
         B     E35STEP       TS RECORD (STEP)
         B     E35JOB        JES JOB ENTRY (FOR TYPRUN=COPY/JCL ERR)
         B     E35JOB        JOB RECORD
         B     E35PRINT      SYSOUT
         B     E35STEP
         AIF   (NOT &MVSXA).NOST30                              GP12069
         B     E35NEXT       TYPE 30                            GP12069
         B     E35NEXT       TYPE 32                            GP12069
.NOST30  SPACE 1                                                GP12069
E35STEP  L     R3,DAYCPU     POINT TO CPU                       GP12069
         A     R3,SOTSTEP    GET STEP CPU
         ST    R3,DAYCPU     STASH BACK (SHOULD NOT OVERFLOW)
         LA    R3,DAYDISK    POINT TO LARGE FIELD
         LA    R4,SOTSTEP+4    AND CORRESPONDING I/O COUNT
         LA    R5,4          DISK/TAPE/OTHER/SPOOL               91042
E35STEPA A64F  0(R3),0(R4)   ADD INCREMENT                      GP12069
         LA    R3,8(,R3)
         LA    R4,4(,R4)
         BCT   R5,E35STEPA
         CLI   SOTFLAG,X'80' DON'T COUNT AS STEP ?               91042
         BE    E35NEXT       YES; BYPASS                         91042
         INC   DAYSTEP       BUMP STEP COUNT                    GP12069
         B     E35NEXT       GET ANOTHER
         SPACE 1
E35JOB   LM    R2,R7,DAYJOB  GET JOB RELATED COUNTERS
         LA    R2,1(,R2)     UP JOB COUNT
         TM    SOTFLAG,X'80'   CANCELLED ?                       79170
         BZ    *+12
         LA    R6,1(,R6)
         B     E35JOBB
         CLI   SOTTYPE,OFF35   TSO TYPE 35 RECORD ?              91042
         BNE   *+12          NO                                  86040
         LA    R7,1(,R7)
         B     E35JOBB
         TM    SOTFLAG,2   ABEND ?                               79170
         BZ    E35JOBA       NO
         LA    R4,1(,R4)     UP ABEND COUNTER
         B     E35JOBB       DO NOT COUNT TWICE
E35JOBA  TM    SOTFLAG,X'78'  ANYTHING ELSE ?                    87254
         BZ    E35JOBB       NO
         LA    R5,1(,R5)
E35JOBB  STM   R2,R7,DAYJOB  STASH COUNTERS BACK
*SMF 26* B     E35NEXT
         SPACE 1
E35PRINT A64F  DAYSPOOL,SOTPRINT   SYSOUT I/O                   GP12069
         SPACE 2                                                 79170
E35NEXT  MVC   TOSORT(TOSLEN),SOTORT  SAVE RECORD               GP12069
E35NEXTR L     R13,4(,R13)                                       79170
         LM    R14,R12,12(R13)
         LA    R15,4         SET FOR MORE
         BR    R14           RETURN TO SORT
         SPACE 1
E35WAIT  A64F  DAYPAGE,SOTPAGE GET PAGING COUNT                 GP12069
         L     R0,DAYWAIT    GET WAIT TIME
         A     R0,SOTSYSWT
         ST    R0,DAYWAIT    STASH BACK
         MVC   UPDOWN,SOTFLAG  PROPAGATE HALT FLAG               79170
         B     E35NEXT       GET ANOTHER
         SPACE 1
E35IPL   INC   DAYIPL                                           GP12069
         TM    UPDOWN,1      WAS SYSTEM DOWN ?
         MVI   UPDOWN,0        IT IS UP NOW
         BNZ   E35NEXT       YES; OK
         INC   DAYCRASH      UP SYSTEM CRASH COUNT              GP12069
         SPACE 1
E35LOST  OI    DAYFLAG,1     SET SMF DATA LOST
         B     E35NEXT
         SPACE 1
E35WYLB  L     R3,SOTSTEP    GET CPU                            GP12069
         A64F  DAYDISK,SOTSTEP+4   GET DISK I/OS                GP12069
         A64F  DAYTAPE,SOTSTEP+8   GET TAPE I/OS                GP12069
         A64F  DAYOTH,SOTSTEP+12   GET OTHER I/O                GP12069
         INC   DAYSESS             SESSION COUNT                GP12069
         B     E35NEXT
         SPACE 1                                                 89248
E35TYP22 SLR   R15,R15                                           89248
         IC    R15,SOTFLAG   GET NUMBER OF CPUS                  89248
         M     R14,=A(24*60*60*100)  CONVERT TO CPU DAYS         89248
         ST    R15,CPUDAYS   STASH FOR OVERHEAD CALCULATION      89248
         B     E35NEXT                                           89248
         EJECT ,
FORMLINE LA    R7,1          'AVERAGE' FOR 1 DAY
FORMAVGE L     R3,MAPCPU     R7 HAS # OF DAYS
         A     R3,MAPWAIT    CPU+WAIT TIME
         A     R3,MAPOHED    +OVERHEAD
         LA    R14,100       MAKE MULTIPLIER
         L     R1,MAPCPU     GET CPU TIME AGAIN
         MR    R0,R14        SCALE FOR PERCENTAGE
         DR    R0,R3         MAKE PERCENT
         ST    R1,WORK3
         L     R1,MAPWAIT    DITTO FOR WAIT TIME
         MR    R0,R14
         DR    R0,R3         PERCENT WAIT
         ST    R1,WORK1
         SR    R14,R1
         S     R14,WORK3
         BNM   *+6
         SLR   R14,R14       CAN'T GO NEGATIVE
         ST    R14,WORK2
         LA    R4,WORK1      GET FIRST VALUE
         LA    R5,PRTWAIT#   POINT TO PERCENTAGE FIELD
         LA    R6,3          NUMBER OF ITEMS
CPULOOP  L     R0,0(,R4)
         CNVD  (R0),0(R5),L'PRTWAIT#  FORMAT
         LA    R4,4(,R4)
         LA    R5,PRTOHED#-PRTWAIT#(,R5)
         BCT   R6,CPULOOP
         LA    R4,MAPWAIT
         LA    R5,PRTWAIT
         LA    R6,3
         LR    R2,R7         COPY DAYS                           91042
         MH    R2,=AL2(60*100)  TIMES CONVERSION TO MINUTES      91042
MINLOOP  L     R1,0(,R4)
         SLR   R0,R0
         AR    R1,R2         PLUS DIVISOR                        91042
         BCTR  R1,0          LESS ONE FOR ROUNDING               91042
         DR    R0,R2         MAKE MINUTES                        91042
         CNVD  (R1),0(R5),L'PRTWAIT   FORMAT
         LA    R4,4(,R4)
         LA    R5,PRTOHED-PRTWAIT(,R5)
         BCT   R6,MINLOOP
         L     R1,MAPIPL
         SLR   R0,R0         CLEAR FOR DIVIDE
         DR    R0,R7         SCALE TO # OF DAYS
         CNVD  (R1),PRTIPL
         L     R1,MAPCRASH
         SLR   R0,R0         CLEAR FOR DIVIDE
         DR    R0,R7         SCALE TO # OF DAYS
         CNVD  (R1),PRTCRASH
         L     R1,MAPJOB
         SLR   R0,R0         CLEAR FOR DIVIDE
         DR    R0,R7         SCALE TO # OF DAYS
         CNVD  (R1),PRTJOB
         L     R1,MAPSTEP
         SLR   R0,R0         CLEAR FOR DIVIDE
         DR    R0,R7         SCALE TO # OF DAYS
         CNVD  (R1),PRTSTEP
         L     R1,MAPABEND
         SLR   R0,R0         CLEAR FOR DIVIDE
         DR    R0,R7         SCALE TO # OF DAYS
         CNVD  (R1),PRTABEND
         L     R1,MAPJCLER
         SLR   R0,R0         CLEAR FOR DIVIDE
         DR    R0,R7         SCALE TO # OF DAYS
         CNVD  (R1),PRTJCLER
         L     R1,MAPCAN
         SLR   R0,R0         CLEAR FOR DIVIDE
         DR    R0,R7         SCALE TO # OF DAYS
         CNVD  (R1),PRTCAN
         L     R1,MAP$E
         SLR   R0,R0         CLEAR FOR DIVIDE
         DR    R0,R7         SCALE TO # OF DAYS
         CNVD  (R1),PRT$E
         L     R1,MAPSESS
         SLR   R0,R0         CLEAR FOR DIVIDE
         DR    R0,R7         SCALE TO # OF DAYS
         CNVD  (R1),PRTSESS
         SPACE 1
         SLR   R0,R0         CLEAR FOR DIVIDE
         DR    R0,R7         SCALE TO # OF DAYS
         LA    R4,MAPDISK
         LA    R5,PRTDISK
         LA    R6,5
IOLOOP   LM    R0,R1,0(R4)   LOAD TOTAL
         LR    R15,R7        COPY DIVISOR
         TM    OPTS,OFRAW    DON'T SCALE ?                      GP12069
         BNZ   IDUNSCAL        RIGHT; NO MULTIPLY               GP12069
         MH    R15,=H'500'   MAKE ROUNDING FACTOR
         A64F  (R0),(R15)    ROUND
IDUNSCAL LA    R14,0(R15,R15)          MAKE DIVISOR             GP12069
         DR    R0,R14        PRINT IN THOUSANDS
         CNVD  (R1),0(R5),L'PRTDISK  FORMAT
         LA    R4,8(,R4)
         LA    R5,PRTTAPE-PRTDISK(,R5)
         BCT   R6,IOLOOP
         BR    R9            RETURN TO CALLER
         SPACE 1                                                 86040
*        CONVERT BINARY TO EBCDIC                                86040
*        (0) - VALUE    (1) O/P ADDRESS     (15) - O/P LENGTH    86040
*                                                                86040
DCONVERT STM   R2,R3,28(R13)                                     86040
         CVD   R0,DB .       MAKE VALUE PACKED                   86040
         MVC   DCONWORK(16),DCONPAT                              86040
         ED    DCONWORK,DB                                       86040
         LA    R2,DCONWORK+16                                    86040
         SR    R2,R15                                            86040
         LR    R3,R15                                            86040
         BCTR  R3,0                                              86040
         EX    R3,DCONMOVE                                       86040
         LM    R2,R3,28(R13)                                     86040
         BR    R14                                               86040
         SPACE                                                   86040
DCONPAT  DC    C' ',13X'20',X'2120' .     EDIT MASK              86040
         DS    C                                                 86040
DCONMOVE MVC   0(0,R1),0(R2)                                     86040
         SPACE 2
         PUSH  USING                                            GP12069
         DROP  ,                                                GP12069
         USING SAVE,R13      DECLARE WORK AREA                  GP12069
         USING ABEXDCB,R15                                      GP12069
ABEXDCB  OI    FLAG,EOFIN+ERRIN  TREAT AS TERMINAL CONDITION    GP12069
         N     R1,=X'00FFFFFF'    MAKE LIST ADDRESSABLE (AM31)  GP12069
         TM    3(R1),X'04'   IGNORANCE IS PERMISSIBLE?          GP12069
         BZR   R14             NO; SO ABEND                     GP12069
         MVI   3(R1),4       IGNORE THE CONDITION               GP12069
         BR    R14           RETURN                             GP12069
         POP   USING                                            GP12069
         EJECT ,
         PRINT &PRTSYS                                           86040
SYSPRINT PRTWORK SYSPRINT,TITLE=5                                86040
SUMPRINT PRTWORK SUMPRINT,SYSTERM,TITLE=3                        86040
SMF      DCB   DDNAME=SMF,DSORG=PS,MACRF=(GL),EODAD=SMFEOF,            *
               RECFM=VBS,BFTEK=A,EXLST=EXLST                    GP12069
EXLST    DC    0A(0),X'91',AL3(ABEXDCB)  DCB ABEND EXIT         GP12069
         SPACE 1
SOTDUMP  FDSNAP SOTORT,SOTLEN,DUAL,NOABS,OFFSET,BASE=SOTORT     GP12069
         FDPRT *END                                              86040
RECDUMP  FDSNAP 0(R8),DUAL,NOABS,OFFSET,BASE=0(R8),LEN=(R4)      91042
         FD    *END                                              91042
SYSHEAD  DC    CL38'#'       AUTO PAGE/DATE REQUEST              86040
SYSMONTH DC    CL9' ',C' '                                      GP12069
SYSYEAR  DC    CL4' ',C' DAILY SMF SUMMARY FOR '                GP12069
SYSCPID  DC    CL4' '                                            79170
SYSHEADL EQU   *-SYSHEAD                                         86040
SUMHEAD  DC    CL45'#'                                           86040
         DC    C'MONTHLY SMF SUMMARY FOR '                       79170
SUMCPID  DC    CL4' '                                            79170
SUMHEADL EQU   *-SUMHEAD                                         86040
SUBHEAD1 DC    CL133'0'
         ORG   SUBHEAD1+1
         DC    C'DAY  ',CL2' '
         DC    C'  SRB TIME',C' '                                86040
         DC    C'  OVERHEAD ',C' '
         DC    C' CPU TIME',C' '
         DC    C'IPL/ERR ',C' '
         DC    C' JOBS/STEPS',C' '
         DC    C'ABENDS',C' '
         DC    C'JCLERR',C' '
         DC    C'CANC',C' '
         DC    C'TSO',C' '                                       86040
         DC    C' WYLB',C' '
         DC    CL6'  DISK',C' '
         DC    CL6'  TAPE',C' '
         DC    CL6'  OTH.',C' '
         DC    CL6' SPOOL',C' '
         DC    C'PAGING'
         DC    CL13' '       TRAILING PAD
         SPACE 1
SUBHEAD3 FDOPT SBA=(*,10)                                       GP12069
         FDPRT 'MIN'         SRB UNITS                          GP12069
         FDOPT SBA=(*,21)                                       GP12069
         FDPRT 'MIN'         CPU UNITS                          GP12069
         FDOPT SBA=(*,32)                                       GP12069
         FDPRT 'MIN'         CPU UNITS                          GP12069
         FDTM  OPTS,OFRAW,BNZ=SUBHED3X    SHOWING RAW DATA ?    GP12069
         FDOPT SBA=(*,90)                                       GP12069
         FDPRT 'K',RADJ,LEN=7                                   GP12069
         FDPRT 'K',RADJ,LEN=7                                   GP12069
         FDPRT 'K',RADJ,LEN=7                                   GP12069
         FDPRT 'K',RADJ,LEN=7                                   GP12069
         FDPRT 'K',RADJ,LEN=7                                   GP12069
SUBHED3X FDPRT *END                                             GP12069
         SPACE 1
PRINT    DC    CL133' '
         ORG   PRINT+1
PRTDAY   DC    CL5' '        MM/DD
         DC    CL2' '
PRTWAIT  DC    CL5' ',C' '
PRTWAIT# DC    CL3' ',C'%'
         DC    C' '
PRTOHED  DC    CL5' ',C' '
PRTOHED# DC    CL3' ',C'%'
         DC    C' '
PRTCPU   DC    CL5' ',C' '
PRTCPU#  DC    CL3' ',C'%'
         DC    C' '
PRTIPL   DC    CL3' ',C'/'
PRTCRASH DC    CL3' '
         DC    C' '
PRTJOB   DC    CL6' ',C'/'
PRTSTEP  DC    CL6' '
         DC    C' '
PRTABEND DC    CL5' '
         DC    C' '
PRTJCLER DC    CL5' '
         DC    C' '
PRTCAN   DC    CL4' '
         DC    C' '
PRT$E    DC    CL4' '
         DC    C' '
PRTSESS  DC    CL5' '
         DC    C' '
PRTDISK  DC    CL6' '
         DC    C' '
PRTTAPE  DC    CL6' '
         DC    C' '
PRTOTH   DC    CL6' '
         DC    C' '
PRTSPOOL DC    CL6' '
         DC    C' '
PRTPAGE  DC    CL6' '
         ORG   ,
         EJECT ,
@WORK    DC    A(0)          ADDRESS OF GOTTEN WORK AREA         86040
         SPACE 1
TOSORT   DS    0F                                               GP12069
TOSCPID  DC    CL4' '        SMCA ID                             79170
TOSDATE  DC    PL4'0'        DATE
TOSTIME  DC    XL4'0'        TIME
TOSTYPE  DC    X'0'          RECORD TYPE
TOSFLAG  DC    X'0'          FLAG DATA                           79170
TOSRLEN  DC    XL2'0'        RECORD LENGTH/SYSTYPE               79170
TOSSTEP  DC    4F'0'         CPU/DISK/TAPE/OTHER                 86040
TOSPRINT DC    F'0'          PRINT LINES                         86040
TOSSYSWT DC    F'0'          SYSTEM WAIT TIME
TOSPAGE  DC    F'0'          PAGING COUNTS
TOSUNIQ  DC    F'0'            FINAGLE FOR UNIQUE RECORDS       GP12069
TOSLEN   EQU   *-TOSORT                                         GP12069
         SPACE 1
DAILY    VECTOR P=DAY        CUMULATIVE RECORD FOR ONE DAY      GP12069
         SPACE 1
MONTHLY  VECTOR P=MON        MONTHLY RECORD                     GP12069
         SPACE 1
JAN      DC    AL1(31),CL9'  JANUARY',CL5' JAN '
FEB      DC    AL1(28),CL9' FEBRUARY',CL5' FEB '
MAR      DC    AL1(31),CL9'    MARCH',CL5'MARCH'
APR      DC    AL1(30),CL9'    APRIL',CL5'APRIL'
MAY      DC    AL1(31),CL9'      MAY',CL5' MAY '
JUN      DC    AL1(30),CL9'     JUNE',CL5' JUNE'
JUL      DC    AL1(31),CL9'     JULY',CL5' JULY'
AUG      DC    AL1(31),CL9'   AUGUST',CL5' AUG '
SEP      DC    AL1(30),CL9'SEPTEMBER',CL5' SEP '
OCT      DC    AL1(31),CL9'  OCTOBER',CL5' OCT '
NOV      DC    AL1(30),CL9' NOVEMBER',CL5' NOV '
DEC      DC    AL1(31),CL9' DECEMBER',CL5' DEC '
         SPACE 2
         LTORG ,
         SPACE 2
MAPVECT  DSECT ,             CUMULATIVE RECORD FOR ONE DAY
         VECTOR P=MAP                                           GP12069
         SPACE 2                                                 86040
SAVE     DSECT ,             SAVE/WORK AREA                      86040
SUBSAVE  DS    18F           EXIT'S SAVE AREA                    86040
DB       DC    D'0'          DOUBLE-WORD WORK AREA
WORK1    DC    F'0'          WORK WORD
WORK2    DC    F'0'          WORK WORD
WORK3    DC    F'0'          WORK WORD
WORK4    DC    F'0'          WORK WORD
         SERVDEFS            SERVICE ROUTINE ADDRESSES          GP12069
CPUDAYS  DC    A(24*60*60*100)  CPUS*1/100 SECS IN ONE DAY       89248
DEFCPUD  DC    A(24*60*60*100)  CPUS*1/100 SECS IN ONE DAY       91042
DCONWORK DS    CL16                                              86040
BINMON   DS    F             BINARY MONTH                       GP12069
CURRDATE DC    XL4'0'        CURRENT DAY
LODATE   DC    PL4'0'                                            91042
HIDATE   DC    PL4'0'                                            91042
NEWMON   DC    CL5' '        SHORT NAME FOR NEXT MONTH
NEWDAY   DC    CL5' '        NEXT DAY
NEWWEEK  DC    X'0'          DAY OF WEEK
         SPACE 1                                                 91042
FLAG     DC    X'0'
EOFIN    EQU   X'80'         END-FILE ON SMF INPUT
ERRIN    EQU   X'40'         ERROR ON SMF DD                    GP12069
LOSSY    EQU   X'20'         SMF DATA LOST                      GP12069
FGDAY    EQU   X'08'         DAILY DATA SAVED
FGMON    EQU   X'04'         MONTHLY DATA AVAILABLE
FGHEAD   EQU   X'02'         MONTHLY HEADER WRITTEN
         SPACE 1                                                 91042
OPTS     DC    X'00'         RUN OPTIONS                         91042
OFNOUSE  EQU   X'80'           DO NOT PROCESS USER RECORDS (128-255)
OFRAW    EQU   X'40'           DON'T SCALE OUTPUT BY 1K         GP12069
OFTYP30  EQU   X'40'           USE 30, NOT 4,5,34,35             91042
OFTYP32  EQU   X'20'           USE 32 INSTEAD OF 30, 4-35        91042
OFTYP26  EQU   X'10'           USE SPOOL COUNTS FROM 26, NOT 6   91042
OFBUG15  EQU   X'02'           DEBUG MODE IN INPUT              GP12069
OFBUG35  EQU   X'01'           DEBUG MODE IN OUTPUT             GP12069
         SPACE 1                                                 91042
PRTDEV   DC    AL1(1)        PRINTER ERROR DEVICE - 1 OR 2       91042
         SPACE 1
UPDOWN   DC    X'00'         0-SYSTEM UP / 1 SYSTEM DOWN
         SPACE 1                                                 91042
SELECTBL DC    256X'00'      TRT TABLE FOR RECORD TYPE SELECTION 91042
SMFCOUNT DC    256F'0'       COUNT OF SMF RECORDS BY TYPE        91042
SMF30S   DC    8F'0'           TYPE 30 SUBTYPES                  91042
SMF30S#  EQU   (*-SMF30S)/4                                      91042
SAVEEND  EQU   *                                                GP12069
         EJECT ,
SOTORT   DSECT ,
SOTCPID  DC    CL4' '        SMCA ID                             79170
SOTDATE  DC    PL4'0'        DATE
SOTTIME  DC    XL4'0'        TIME
SOTTYPE  DC    X'0'          RECORD TYPE
SOTFLAG  DC    X'0'          FLAG DATA                           79170
SOTRLEN  DC    XL2'0'        RECORD LENGTH/SYSTYPE               79170
SOTSTEP  DC    4F'0'         CPU/DISK/TAPE/OTHER                 86040
SOTPRINT DC    F'0'          PRINT LINES                         86040
SOTSYSWT DC    F'0'          SYSTEM WAIT TIME
SOTPAGE  DC    F'0'          PAGING COUNTS
SOTUNIQ  DC    F'0'            UNIQUENESS                       GP12069
SOTLEN   EQU   *-SOTORT      SORT LENGTH                        GP12069
         SPACE 1
         ORG   ,
         SPACE 1
         PRINT &PRTSYS                                           86040
SMFMAPS  DSECT ,
         IFASMFR 0           IPL RECORD
         ORG   SMFMAPS
         AIF   (NOT &MVSXA).NOT90                               GP12064
         AIF   (&MVS).MVS90                                      87254
         IFASMFR 1           WAIT TIME RECORD
         ORG   SMFMAPS
         IFASMFR 12          HALT EOD (ALTERNATE WAIT)
         AGO   .NOT90                                            87254
.MVS90   IFASMFR 90          SYSTEM STATUS                       87254
SMFMAPS  DSECT ,             FIX FOR IBM ERROR                   87254
         PRINT &PRTMAC                                           91042
         ORG   SMFMAPS                                           91042
         IFASMFR 30          ALTERNATE TO 4,5,34,35              91042
         PRINT &PRTSYS                                           91042
SMFMAPS  DSECT ,             FIX FOR IBM ERROR                   91042
.NOT90   ORG   SMFMAPS
         IFASMFR 4
         ORG   SMFMAPS
         IFASMFR 34
         ORG   SMFMAPS
         IFASMFR 5
         ORG   SMFMAPS
         IFASMFR 35
         ORG   SMFMAPS
         IFASMFR 6
         ORG   SMFMAPS
         IFASMFR 7           SMF LOST
         ORG   SMFMAPS                                           87254
         IFASMFR 26          JES JOB RECORD                      87254
         SPACE 1                                                 89248
         ORG   SMFMAPS                                           89248
         IFASMFR 22          CPU/CHANNEL/STORAGE CONFIGURATION   89248
         SPACE 1
         ORG   SMFMAPS
         SPACE 1
ARREC    DS    0F
         SPACE 1
ARLEN    DS    H         INCLUSIVE LENGTH
ARRDWBB  DS    H         X'0000'
ARSIND   DS    XL1       SYSTEM INDICATOR X'02'
ARTYPE   DS    XL1       RECORD TYPE CODE (SMF OR OTHER)
ARTIME   DS    BL4       LOGOFF TIME OF DAY (.01 SECONDS)
ARDATE   DS    PL4       LOGOFF DATE (00YYDDD+)
ARSER    DS    CL2       SYSTEM SERIAL FROM CVT
ARMDL    DS    CL2       SYSTEM MODEL FROM CVT
         SPACE 1
         DS    XL2       .RESERVED
ARTEL    DS    F         SESSION ELAP TIME (.01 SEC) BIN
ARTPIO   DS    F         TERMINAL I/O COUNT
ARUID    DS    CL8       USERID ID
ARACNO   DS    CL12      ACCOUNT NUMBER
ARTERMID DS    CL4       TERMINAL ID ENTERED AT LOGON
ARCOMPNM DS    CL8       MILTEN TERMINAL COMPONET NAME
ARUCBID  DS    CL3       UNIT CONTROL BLOCK NAME
         DS    XL1       .RESERVED
         SPACE 1
*        USER WYLBUR USAGE DATA
ARUSER1  DS    0F        FULLWORD ALIGNMENT
         DS    F         RESERVED
ARWYLTIM DS    F         CPU EDITING TIME (.01) SEC
ARWYLPGW DS    H         NUMBER OF PAGE WRITES
ARWYLPGR DS    H         NUMBER OF PAGES READ
ARWYLJOB DS    H         NUMBER OF JOBS SUBMITTED
ARWYLLST DS    H         NUMBER OF LIST OFFLINES
ARWYLCND DS    H         NUMBER OF CONDENSES
ARWYLOPN DS    H         NUMBER OF OPENS
ARWYLCAT DS    H         NUMBER OF CAT ACCESSES
ARWYLDAW DS    H         NUMBER OF DASD NON-PAGE WRITES
ARWYLDAR DS    H         NUMBER OF DASD NON-PAGE READS
         SPACE 2
         MAPSBP2W  PFX=Y,DSECT=YES  MAP RETURN FROM SUBP2W      GP12069
         PRINT &PRTSYS                                           86040
CVTDSECT DSECT ,                                                 86040
         CVT   DSECT=YES                                         86040
         IHACDE ,                                                86040
         DCBD  DEVD=DA,DSORG=PS                                  92273
         END
